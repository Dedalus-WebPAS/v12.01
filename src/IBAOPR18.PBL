.***************************************************************************
.* System    :   OPR - Theatre                                             *
.* Program   :   IBAOPR18                                                  *
.* Desc      :   OPR Difficult Intubation Flag Report                      *
.***************************************************************************
.* Date      :   08/07/2010                                                *
.* Author    :   Saroeun J. Soeur                                          *
.* Function  :   print out a list of patients with difficult intubation    *
.* Mods      :                                                             *
.***************************************************************************
.
.  V11.03.01   20/03/2023   PJ Le Febour  TSK 0909393
.              amended PACK KEY 19 to KEY22 for theatre index changes
.  V10.04.01   23/06/2014   J.Tan         CAR 261630
.              Removed port number from temp file name
.  V10.02.01   22.07.2011   Saroeun Soeur CAR 246397
.              fix to use correct surg.
.  V10.02.01   28.06.2011   Saroeun Soeur CAR 245267
.              jump to GENR3200
.  V10.00.04   27.08.2010   Saroeun Soeur CAR 218727
.              remove check on surg. and aneas.
.  V10.00.03   27.08.2010   Saroeun Soeur CAR 218727
.              remove check on surg. and aneas.
.  V10.00.02   19.08.2010   Saroeun Soeur  CAR 218727
.              fixed to include unique id
.  V10.00.01   17.08.2010   Saroeun Soeur CAR 218727
.              fixed to match current date and onwards
.  V10.00.00   29.07.2010   Saroeun Soeur CAR 218727
.                           created new report program to print patients
.                           with difficult intubation
.
.===============================================================================
.  FD INCLUDES
.===============================================================================
.
                    INC       STD001FD                * std library
                    INC       IBASEQFD/INC            * Sequential Numbers File
                    INC       TFILEVAR/INC            * Generate Temp File Name
                    INC       WEBERRFD/INC            * Web Server Error Logging
.
                    INC       PATCODFD/INC            * patcodes file
                    INC       OPRSESFD/INC            * theatre session file
                    INC       PATVISFD/INC            * patient admiss. file
                    INC       PMSHCPFD/INC            * hcp file
                    INC       PATMA1FD/INC            * patient master file
                    INC       OPRAAEFD/INC            * recovery adv. evt file
                    INC       OPRCLIFD/INC            * clinic file
+
.------------------------------------------------------------------------------
.         Temp. File - store all the records that match the users
.                      criteria
.------------------------------------------------------------------------------
.
TREPORT1  IFILE     SQL, FIXED=111, KEY="U69-76,77-86,108-110"
+
. Name          Type      Length     Physical     Start   Description
. ----          ----      ------     --------     -----   -----------
TMPSURGN        DIM        20           20          1       Surgeon
TMPANAES        DIM        20           20         21       Anaesthetist
TMPURNUM        DIM         8            8         41       UR number
TMPSNAME        DIM        10           10         49       Patient Surname
TMPFNAME        DIM        10           10         59       Patient First name
TMPADMIS        DIM         8            8         69       Admission number
TMPTHEAD        DIM        10           10         77       Theatre Date
TMPDIFFI        DIM         1            1         87       Difficult Intubation
TMPDTAIL        DIM        20           20         88       Details
TMPCOUNT        DIM         3            3        108       Count
.
.                                       Total:    111     
+ 
.------------------------------------------------------------------------------
.         Local variables
.------------------------------------------------------------------------------
.
DIFFICUC  DIM       1         * difficult intubation
ANAESTHC  DIM       10        * anaesthetist doctor code
SURGEONC  DIM       10        * surgeon doctor code
USERID    DIM       10
URNUMBR   DIM       8
FADMDTE   DIM       8         * from date
TADMDTE   DIM       8         * to date
TTODATE   DIM       8         * date
FROMDATE  DIM       11        * from date
STRTDATE  DIM       11        * start date
YYYY      DIM       4         
SPECIAL   FORM      1         * Special print flag
DMM       DIM       2
DDD       DIM       2
DIM3      DIM       3
COUNTER   FORM      1
COUNT     FORM      3
SURCODE   DIM       10
DOCTYPE   DIM       3
ANACODE   DIM       10 
OPCNCLSU  FORM      1
CWEBDNAM  FORM      1
DISPNAME  DIM       60
CWEBDNAI  DIM       1
NAME35    DIM       35
FIRSTREC  FORM      1
OPCND     FORM      1
CMDLINE   DIM       90
FNAME     DIM       8
STATUS    FORM      1
TEMANA    DIM       10
TEMDOC    DIM       10
DOCFNAME  DIM       50
FMTTITLE  DIM       10
FMTSNAME  DIM       35
FMTGNAME  DIM       35
DIM20     DIM       20
DIM20B    DIM       20
+
.------------------------------------------------------------------------------
.         Constant variables
.------------------------------------------------------------------------------
.
MAXCLNO   FORM      "38"      * Maximum Line Number
FSLASH    INIT      "/"
TILDA8    INIT      "~~~~~~~~"
CATOP     INIT      "OP"
DOWNCASE  INIT      "AaBbCcDdEeFfGgHhIiJjKkLlMmNnOoPpQqRrSsTtUuVvWwXxYyZz"
NA        INIT      "N/A"
ALL       INIT      "ALL"     
+
.------------------------------------------------------------------------------
.         Constant program variables
.------------------------------------------------------------------------------
.
PRGID     INIT      "IBAOPR18"
PRGNAM    INIT      "Difficult Intubation Flag Report"
VERSION   INIT      "V12.01.00"
+
.------------------------------------------------------------------------------
.         MAIN0000 
.------------------------------------------------------------------------------
.
MAIN0000  CALL      INIT0000            * Initialise
.
          CALL      GETC0000            * Get Search Criteria
          BRANCH    EXIT,MAIN9999
.
          CALL      GENR0000            * Generate Report
          CALL      PRNR0000            * Print Report
          CALL      KILL0000            * Kill temp file
.
MAIN9999  CHAIN     PGM
+
.------------------------------------------------------------------------------
.         Initialise 
.------------------------------------------------------------------------------
.
INIT0000  CALL      DISPHEAD             * header
.
          OPEN      OPRAAE11,"opraaeaf"; * open files
          OPEN      OPRSESA1,"oprsesaf";
          OPEN      OPRSESA7,"oprsesaf";
          OPEN      PATVISA1,"patvisaf";
          OPEN      PMSHCPA1,"pmshcpaf";
          OPEN      PATMA1A1,"patma1af";
          OPEN      PATMX1A1,"patmx1af";
          OPEN      PATCODE1,"patcodes";
          OPEN      OPRCLIA1,"oprcliaf";
          OPEN      CONTROLF,"controlf";

          READ      CONTROLF,FORTY;*111,OPCNCLSU  * read control parameter
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,FNAME
.
INIT9999  RETURN
+
.------------------------------------------------------------------------------
.         Remove temp file
.------------------------------------------------------------------------------
.
KILL0000  CLOSE     TREPORT1
          MOVE      ZERO,STATUS
.
          DISPLAY   *P1:24,*EL,*P28:24,*V2LON,"** Deleting Index File **";
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
KILL9999  RETURN
+
.------------------------------------------------------------------------------
.         create temp file
.------------------------------------------------------------------------------
.
CRET0000  MOVE      ZERO,STATUS
.
          CALL      KILL0000
.
          PACK      CMDLINE,SP70,SP70
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME,CMDLINE
          APPEND    " 111 U69-76,77-86,108-110 ",CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          MOVE      ONE,STATUS
.
          MOVE      ZERO,OPCND
          TRAP      CRET8000 IF IO
          OPEN      TREPORT1,FNAME
          TRAPCLR   IO

          CALL      CLET0000              * Clear tempfile
          GOTO      CRET9999
.
CRET8000  MOVE      ONE,OPCND
          DISPLAY   *P1:24,*EL,*B,"** Temporary Index File ",*V2LON,FNAME:
                               *HOFF," not found **   ";
          CALL      HITENTER
          TRAPCLR   IO
.
CRET9999  RETURN
+
.*******************************************************************************
.         Clear tempfile
.*******************************************************************************
CLET0000  MOVE      SP70,KEY21
          CALL      RDSREPRT
          CALL      RDKREPRT
          BRANCH    OVRCD,CLET9999
.
          PACK      KEY21,TMPADMIS,TMPTHEAD,TMPCOUNT,SP70
          CALL      DELREPRT
          GOTO      CLET0000
.
CLET9999  RETURN
.
.------------------------------------------------------------------------------
.         Print report
.------------------------------------------------------------------------------
.
PRNR0000  MOVE      TRUE,FIRSTREC
.
          PACK      KEY21,SP70
          CALL      RDSREPRT                 * position read
PRNR1000  CALL      RDKREPRT                 * read next
          BRANCH    OVRCD,PRNR9000
.
          CALL      CHPG0000                 * Check If Need new Page
.
          IF        FIRSTREC = TRUE
            CALL      PAGE0000               * Setup Patient Heading Stuff
            CALL      FIRP0000
            MOVE      "9",CLNO
            CALL      HEAD0000               * Setup Patient Heading Stuff
            MOVE      FALSE,FIRSTREC
          ENDIF
.
          CALL      PRNA0000                   
.
          GOTO      PRNR1000
.
PRNR9000  IF        FIRSTREC=TRUE
            CALL      PAGE0000               * New Page
          ENDIF
.         
          PRINT     *N,"**** End of Report ****"
          ADD       "2",CLNO

          DISPLAY   *P1:24,*EL,"Print Complete.   ";
          CALL      HITENTER
.
PRNR9999  RETURN
+
.------------------------------------------------------------------------------
.         Print report data
.------------------------------------------------------------------------------
.
PRNA0000  UNPACK    TMPTHEAD,YYYY,DMM,DDD
          PACK      TMPTHEAD,DDD,FSLASH,DMM,FSLASH,YYYY
.
          MATCH     "1",TMPDIFFI
          IF        @EQUAL
            MOVE      "Yes",DIM3
          ELSE
            MOVE      "No",DIM3
          ENDIF
.
          PRINT     *001,"| ",TMPSURGN: 
                    *023,"| ",TMPANAES:
                    *045,"| ",TMPURNUM:
                    *055,"| ",TMPADMIS:
                    *065,"| ",TMPSNAME:
                    *077,"| ",TMPFNAME:
                    *089,"| ",TMPTHEAD:
                    *101,"| ",DIM3:
                    *109,"| ",TMPDTAIL:
                    *132,"|"
.
          CALL       UND132
.
PRNA9999  RETURN
+
.---------------------------------------------------------------------------
.
.---------------------------------------------------------------------------
FIRP0000  UNPACK    FADMDTE,YYYY,DMM,DDD
          PACK      FROMDATE,DDD,FSLASH,DMM,FSLASH,YYYY

          UNPACK    TADMDTE,YYYY,DMM,DDD
          PACK      STRTDATE,DDD,FSLASH,DMM,FSLASH,YYYY
.
          MATCH     SURGEONC,SP70
          IF        @EQUAL
            MOVE      ALL,DIM20
          ELSE
            PACK      KEY10,SURGEONC,SP70
            CALL      RDPMHCP1
            IF        OVRCD=1
              MOVE      NA,DIM20
            ELSE
               MOVE      PMHCTITL,PACTITLE
               MOVE      PMHCGNAM,PACGNAME
               MOVE      PMHCSNAM,PACSNAME
               MOVE      TWO,PACFRMT             * title name surname of doctor
               CALL      PACNAME                 * pack up doctor's name
               MOVE      PACFNAME,DIM20
            ENDIF
          ENDIF

          MATCH     ANAESTHC,SP70
          IF        @EQUAL
            MOVE      ALL,DIM20B
          ELSE
            PACK      KEY10,ANAESTHC,SP70
            CALL      RDPMHCP1
            IF        OVRCD=1
              MOVE      NA,DIM20B
            ELSE
               MOVE      PMHCTITL,PACTITLE
               MOVE      PMHCGNAM,PACGNAME
               MOVE      PMHCSNAM,PACSNAME
               MOVE      TWO,PACFRMT             * title name surname of doctor
               CALL      PACNAME                 * pack up doctor's name
               MOVE      PACFNAME,DIM20B
            ENDIF
          ENDIF

          MATCH     URNUMBR,SP70
          IF        @EQUAL
            MOVE      ALL,URNUMBR
          ENDIF
.
          MATCH     DIFFICUC,SP70
          IF        @EQUAL
            MOVE      ALL,DIM3
          ENDIF
.
          MATCH     "1",DIFFICUC
          IF        @EQUAL
            MOVE      YES,DIM3
          ENDIF
.
          MATCH     "0",DIFFICUC
          IF        @EQUAL
            MOVE      NO,DIM3
          ENDIF
.
          PRINT      *N,*2,"Date Range      :",FROMDATE," - ",STRTDATE:
                     *N,*2,"UR Number       :",URNUMBR:
                     *N,*2,"Surgeon         :",DIM20:
                     *N,*2,"Anaesthetist    :",DIM20B:
                     *N,*2,"Diff. Intubation:",DIM3:
                     *N,*2,"Run by          :",USERID
.

.
FIRP9999  RETURN
.---------------------------------------------------------------------------
.         check if a new page is needed
.---------------------------------------------------------------------------
.
CHPG0000  IF        CLNO >= MAXCLNO
            CALL      PAGE0000               * begin a new page
            CALL      HEAD0000               * print header details   
            MOVE      "45",MAXCLNO           
          ENDIF
.
CHPG9999  RETURN
+
.---------------------------------------------------------------------------
.         Print report header
.---------------------------------------------------------------------------
.
HEAD0000  IF        CLNO >= MAXCLNO
              CALL      PAGE0000
          ENDIF
.
          CALL       UND132
.
          PRINT     *001,"| Surgeon":
                    *023,"| Anaesthetist":
                    *045,"| UR":
                    *055,"| Visit":
                    *065,"| Patient":
                    *077,"| Patient":
                    *089,"| Theatre":
                    *101,"| Diff.":
                    *109,"| Details":
                    *132,"|"
.
          ADD       ONE,CLNO
.
          PRINT     *001,"| Name":
                    *023,"| Name":
                    *045,"| Number":
                    *055,"| Number":
                    *065,"| Name":
                    *077,"| Surname":
                    *089,"| Date":
                    *101,"| Intub.":
                    *109,"| ":
                    *132,"|"
.
          CALL      UND132
.
          ADD       "5",CLNO
          MOVE      FALSE,SPECIAL
.
HEAD9999  RETURN
+
.------------------------------------------------------------------------------
.         Print selection criteria 
.------------------------------------------------------------------------------
.
PAGE0000  CLOCK     TIME,CTIMEIS
          CALL      HEAD132
.
          MOVE       "9",CLNO
.
PAGE9999 RETURN
+
.-----------------------------------------------------
.         Generate the temp file data        
.-----------------------------------------------------
.
GENR0000  CALL      CRET0000             * create temp file
.
          MOVE      ONE,COUNT

          PACK      KEY22,FADMDTE,SP70
          CALL      RSOPSES7
GENR1000  CALL      RKOPSES7
          BRANCH    OVRCD,GENR9999
.
          MOVE      SP70,TMPDIFFI       * clear variables
          MOVE      SP70,TMPDTAIL
          MOVE      SP70,TMPTHEAD
          MOVE      SP70,TMPADMIS
          MOVE      SP70,TMPURNUM
          MOVE      SP70,TMPSNAME
          MOVE      SP70,TMPFNAME
.
          MOVE      SP70,TEMANA
          MOVE      SP70,TEMDOC
          MOVE      SP70,DOCTYPE
          MOVE      SP70,SURCODE
          MOVE      SP70,ANACODE
.         
          MATCH     TADMDTE,OPSEDATE
          GOTO      GENR2000 IF EQUAL
. 
          MATCH     TADMDTE,OPSEDATE              
          GOTO      GENR9999 IF NOT LESS
.
GENR2000  COMPARE   ZERO,OPCNCLSU
          IF        @EQUAL
            PACK    KEY10,OPSECLIN
            CALL      RDPMHCP1
            IF        OVRCD=0
              MOVE      PMHCTITL,PACTITLE         
              MOVE      PMHCGNAM,PACGNAME
              MOVE      PMHCSNAM,PACSNAME
              MOVE      TWO,PACFRMT             * title name surname of doctor
              CALL      PACNAME                 * pack up doctor's name
              MOVE      PACFNAME,TMPSURGN
            ELSE
              PACK      KEY6,OPSECLIN
              CALL      RDOPCLI1
              IF        OVRCD=0
                PACK    KEY10,OPCLDOCT
                CALL      RDPMHCP1
                IF        OVRCD=0
                  MOVE      PMHCTITL,PACTITLE         
                  MOVE      PMHCGNAM,PACGNAME
                  MOVE      PMHCSNAM,PACSNAME
                  MOVE      TWO,PACFRMT             * title name surname of doctor
                  CALL      PACNAME                 * pack up doctor's name
                  MOVE      PACFNAME,TMPSURGN
                ELSE
                  PACK      TMPSURGN,SP70
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
GENR3000  PACK      KEY10,OPSEANAE,SP70
          CALL      RDPMHCP1
          IF        OVRCD=0
            MOVE      PMHCTITL,PACTITLE            * I CAR 13038
            MOVE      PMHCGNAM,PACGNAME
            MOVE      PMHCSNAM,PACSNAME
            MOVE      TWO,PACFRMT             * title name surname of doctor
            CALL      PACNAME                 * pack up doctor's name
            MOVE      PACFNAME,TMPANAES
          ELSE
            PACK    TMPANAES,SP70
          ENDIF
.
GENR3100  PACK      KEY30,OPSEDATE,OPSETIME,OPSECLIN,SP70
          CALL      RSOPAAE1
GENR3200  CALL      RKOPAAE1
          BRANCH    OVRCD,GENR1000
.
          MOVE      SP70,TMPDIFFI       * clear variables
          MOVE      SP70,TMPDTAIL
          MOVE      SP70,TMPTHEAD
          MOVE      SP70,TMPADMIS
          MOVE      SP70,TMPURNUM
          MOVE      SP70,TMPSNAME
          MOVE      SP70,TMPFNAME

          MATCH     OPAADATE,OPSEDATE
          GOTO      GENR1000 IF NOT EQUAL
.
          MATCH     OPAATIME,OPSETIME
          GOTO      GENR1000 IF NOT EQUAL
.
          MATCH     OPAACLIN,OPSECLIN
          GOTO      GENR1000 IF NOT EQUAL
.
          MATCH     DIFFICUC,SP70
          GOTO      GENR4000 IF EQUAL
.
GENR3500  MATCH     DIFFICUC,OPAADIIN
          GOTO      GENR3200 IF NOT EQUAL
.
GENR4000  PACK      KEY8,OPAAADMN,SP70
          CALL      RDPTVIS1
          BRANCH    OVRCD,GENR1000
.
          PACK      KEY8,PVIURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,GENR1000
.
          MATCH     URNUMBR,SP70
          IF        !@EQUAL
            MATCH     PVIURNO,URNUMBR
            IF        @EQUAL
              GOTO      GENR8000
            ELSE
              GOTO      GENR3200
            ENDIF
          ENDIF
.
GENR8000  MOVE      OPAADIIN,TMPDIFFI
          MOVE      OPAADIDT,TMPDTAIL
          MOVE      OPSEDATE,TMPTHEAD
          MOVE      OPAAADMN,TMPADMIS
          MOVE      PVIURNO,TMPURNUM
          MOVE      PSNAME,TMPSNAME
          MOVE      PGNAME,TMPFNAME
          MOVE      COUNT,TMPCOUNT
.
          PACK      KEY21,OPAAADMN,OPSEDATE,COUNT,SP70
          CALL      WRTREPRT
          ADD       ONE,COUNT
.
          GOTO      GENR3200
.
GENR9999  RETURN
+
.-----------------------------------------------------
.         GETC0000
.-----------------------------------------------------
.
GETC0000  CALL      KDAT0000            * keyin date
          BRANCH    EXIT,GETC9999
.
          CALL      KSUR0000            * keyin surgeon
          CALL      KANA0000            * keyin anaesthetist
          CALL      KDIF0000            * keyin difficult intubation
          CALL      URNU0000            * keyin ur number
          CALL      USER0000
          CALL      CONTQST
          MOVE      ZERO,EXIT
          BRANCH    CEXIT,GETC9999,GETC0000,GETC9000

GETC9000  MOVE      ONE,EXIT
.
GETC9999  RETURN
+
.------------------------------------------------------
.
.-----------------------------------------------------
.
KDIF0000  KEYIN     *P1:CVERT,*EF,"Difficult Intubation?  (Y/N): ",*V2LON:
                    DIFFICUC
.
          ENDSET    DIFFICUC
          APPEND    SP70,DIFFICUC
          RESET     DIFFICUC
.
          MATCH     SP70,DIFFICUC            * Test for spaces
          IF        @EQUAL
            PACK      DIFFICUC,SP70
            DISPLAY   *P1:CVERT,*EF,"Difficult Intubation? (Y/N) : ",*V2LON:
                                DIFFICUC
            MOVE      SP70,DIFFICUC
            GOTO      KDIF9999
          ENDIF
.          
          REP       "yYnN",DIFFICUC
.
          MATCH     DIFFICUC,ANSY
          IF        @EQUAL
            MOVE      "1",DIFFICUC
            GOTO      KDIF9999
          ENDIF
.
          MOVE      "0",DIFFICUC
.
KDIF9999  ADD       ONE,CVERT
          RETURN
+
.------------------------------------------------------
.
.-----------------------------------------------------
.
KANA0000  KEYIN     *P1:CVERT,*EF,"Anaesthetist Code  : ",*V2LON:
                    ANAESTHC
.
          ENDSET    ANAESTHC
          APPEND    SP70,ANAESTHC
          RESET     ANAESTHC
.
          MATCH     SP70,ANAESTHC            * Test for spaces
          IF        @EQUAL
            PACK      ANAESTHC,ALL,SP70
            DISPLAY   *P1:CVERT,*EF,"Anaesthetist Code  : ",*V2LON:
                                ANAESTHC
            MOVE      SP70,ANAESTHC
            GOTO      KANA9999
          ENDIF
.
KANA9999  ADD       ONE,CVERT
          RETURN
+
.------------------------------------------------------
.         User name
.-----------------------------------------------------
.
URNU0000  KEYIN     *P1:CVERT,*EF,"UR Number  : ",*V2LON:
                    URNUMBR
.
          ENDSET    URNUMBR
          APPEND    SP70,URNUMBR
          RESET     URNUMBR
.
          MATCH     SP70,URNUMBR            * Test for spaces
          IF        @EQUAL
            PACK      URNUMBR,ALL,SP70
            DISPLAY   *P1:CVERT,*EF,"UR Number  : ",*V2LON:
                                URNUMBR
            MOVE      SP70,URNUMBR
            GOTO      URNU9999

          ENDIF
.
URNU9999  ADD       ONE,CVERT
          RETURN
+
.------------------------------------------------------
.         User name
.-----------------------------------------------------
.
USER0000  KEYIN     *P1:CVERT,*EF,"User ID  : ",*V2LON:
                    USERID
.
          ENDSET    USERID
          APPEND    SP70,USERID
          RESET     USERID
.
          MATCH     SP70,USERID            * Test for spaces
          IF        @EQUAL
            GOTO      USER0000
          ENDIF
.
          DISPLAY   *P1:CVERT,*EF,"User ID  : ",*V2LON:
                              USERID

USER9999  ADD       ONE,CVERT
          RETURN
+
.------------------------------------------------------
.
.-----------------------------------------------------
.
KSUR0000  KEYIN     *P1:4,*EF,"Surgeon Code  : ",*V2LON:
                    SURGEONC
.
          ENDSET    SURGEONC
          APPEND    SP6,SURGEONC
          RESET     SURGEONC
.
          MOVE      FIVE,CVERT
          MATCH     SP6,SURGEONC            * Test for spaces
          IF        @EQUAL
            PACK      SURGEONC,ALL,SP70
            DISPLAY   *P1:4,*EF,"Surgeon Code  : ",*V2LON:
                            SURGEONC
            MOVE      SP70,SURGEONC
            GOTO      KSUR9999
          ENDIF
.
KSUR9999  RETURN
+
.------------------------------------------------------
.
.------------------------------------------------------
KDAT0000  DISPLAY   *P1:4,*EF,"From Date :":
                        *P1:6,"To Date   :";
.
KDAT1000  MOVE      FOUR,CVERT
          MOVE      TWENTY,CCOL
          MOVE      ONE,CCENTRY
          MOVE      CCC,CCENT
          MOVE      ZERO,CDAY
          UNPACK    SP6,CYEAR,CMON,CDAY
          CALL      KEYDATE                     * Keyin todate
          BRANCH    OVRCD,KDAT9000
.
          PACK      FADMDTE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",FADMDTE
.
          MOVE      SIX,CVERT
          CALL      KEYDATE                     * Keyin fromdate
          BRANCH    OVRCD,KDAT1000
.
          PACK      TADMDTE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",TADMDTE
.
          MATCH     FADMDTE,TADMDTE             * match date range
          GOTO      KDAT2000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid date range entered **    ";
          CALL      HITENTER
          GOTO      KDAT0000
.
KDAT2000  CALL      CONTQST
          MOVE      ZERO,EXIT
          BRANCH    CEXIT,KDAT3000,KDAT1000,KDAT9000
.
.------ yes selected ------
.
KDAT3000  UNPACK    TADMDTE,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,CC
          MOVE      CYEAR,YY
          MOVE      CMON,MM
          MOVE      CDAY,DD
          CALL      DMYCON                  * convert to julian format
.
          ADD       ONE,JULDAY              * add one day to todate
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
.
          CALL      JULCON                  * convert back to DDMMYY format
.
          PACK      TTODATE,CC,YY,MM,DD
          REP       " 0",TTODATE
.
          GOTO      KDAT9999
.
KDAT9000  MOVE      ONE,EXIT
KDAT9999  RETURN
+
.------------------------------------------------------------------------------
.
.------------------------------------------------------------------------------
.
WRTREPRT  RESET     KEY21
          WRITE     TREPORT1,KEY21;TMPSURGN,TMPANAES,TMPURNUM,TMPSNAME:
                                   TMPFNAME,TMPADMIS,TMPTHEAD,TMPDIFFI:
                                   TMPDTAIL,TMPCOUNT
          RETURN
.
RDKREPRT  MOVE      ZERO,OVRCD
          READKS    TREPORT1;TMPSURGN,TMPANAES,TMPURNUM,TMPSNAME:
                             TMPFNAME,TMPADMIS,TMPTHEAD,TMPDIFFI:
                             TMPDTAIL,TMPCOUNT
          GOTO      OVERCOND IF OVER
          RETURN
.
RDSREPRT  RESET     KEY21
          READ      TREPORT1,KEY21;;
          RETURN
.
DELREPRT  RESET     KEY21
          DELETE    TREPORT1,KEY21
          RETURN
+
.------------------------------------------------------------------------------
.
.------------------------------------------------------------------------------
          INC       STD001IO
          INC       TFILENAM                * Generate Temp File Name
          INC       IBASEQIO/INC            * Sequential Numbers File
          INC       WEBERRIO/INC            * Web Server Error Logging
.
          INC       OPRSESIO/INC
          INC       PATVISIO/INC
          INC       PATMA1IO/INC
          INC       PMSHCPIO/INC
          INC       OPRAAEIO/INC
          INC       PATCODIO/INC
          INC       OPRCLIIO/INC



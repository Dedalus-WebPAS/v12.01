.***************************************************************************
.* System    :   Patient Management System                                 *
.* Program   :   FXMRMSTU                                                  *
.* Desc      :                                                             *
.***************************************************************************
.* Date      :   16/09/2005                                                *
.* Author    :   Peter Vela                                                *
.* Function  :   This program will loop the(mrtmasaf)MRT master file and   *
.*               delete records that have a temporary URNO && is not merged*
.*               with another URNO && has no visit links (mrtvisaf)        *
.* Mods      :   V9.08.01 31/01/2007  Peter Vela     CAR 127930            *
.*                        Recompiled for MRTMASFD                          *
.*               V9.05.01 30/03/2006 Peter Vela CAR 68230                  *
.*                        Commented out visit link validation              *
.*               V9.04.00 16/09/2005 Peter Vela CAR 68230                  *
.*                        Created Fixit                                    *
.*                                                                         *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATMA1FD/INC
          INC       MRTMASFD/INC
          INC       MRTVISFD/INC
.
. VARIABLES
. -------------------------
COUNTR    FORM      8
CURRDATE  DIM       8
.
PRGID     INIT      "FXMRMSTU"
PRGNAM    INIT      "Fixit for Temporary URNO with MRT Master Records (mrtmasaf)"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with fixit
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"mrtmasaf";
          OPEN      MRTMASA1,"mrtmasaf"
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"mrtvisaf";
          OPEN      MRTVISA2,"mrtvisaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run fixit                              *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit":
                    *P1:11,"Before executing this program, create a backup for mrtmasaf"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.**************************************************************************
.
PROC0000  DISPLAY   *P1:24,*EL,"Processing: ";
          MOVE      ZERO,COUNTR                  * initialise record counter
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          PACK      KEY17,SP70
          CALL      RSMRMAS1
PROC1000  CALL      RKMRMAS1
          BRANCH    OVRCD,PROC9000
.
          UNPACK    MRMAURNO,KEY1
          MATCH     ANST,KEY1                   * Check if URNO is temporary
          GOTO      PROC1000 IF NOT EQUAL       * Skip if not
.
PROC1010  PACK      KEY8,MRMAURNO,SP70          * Read PMI
          CALL      RDMAST1
          BRANCH    OVRCD,PROC2000              * Remove MRTMAS record if no 
.                                               * PMI record
          COMPARE   ONE,PSTAT
          IF        @EQUAL
PROC1100    MOVE      PPSNAME,KEY8     * This is where the new U/R is hidden
            CALL      RDMAST1          * Read in the new PMI record
            BRANCH    OVRCD,PROC2000   * U/R is not merged continue
            BRANCH    PSTAT,PROC1100   * Another associated U/R number
            GOTO      PROC1000         * Skip this merged U/R
          ENDIF
.
.PROC1200  PACK      KEY18,MRMAKEY,SP70 * Check if MR has visit links
.          CALL      RSMRVS2
.          CALL      RKMRVS2
.          IF        OVRCD=0
.            MATCH     MRMAKEY,MRVSMKEY
.            GOTO      PROC1000 IF EQUAL 
.          ENDIF
.
PROC2000  PACK      KEY17,MRMAURNO,MRMAHLOC,MRMADOTY,DMRMAVOL,SP70
          CALL      DETMAS1                           * Delete MRT Master record
          CALL      RSMRMAS1                          * Reposition file pointer
.
          ADD       ONE,COUNTR
          GOTO      PROC1000
.
PROC9000  DISPLAY   *P1:24,*EL,"Fixit completed.  ",COUNTR," records updated.":
                    "  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PATMA1IO/INC
          INC       MRTMASIO/INC
          INC       MRTVISIO/INC

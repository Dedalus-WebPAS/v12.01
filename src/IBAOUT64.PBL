.******************************************************************************
.* System         : Outpatients                                               *
.* Program        : IBAOUT64.PBL                                              *
.* Name           : Waiting Time Analysis                                     *
.******************************************************************************
.* Date           : 13/04/1995                                                *
.* Author         : Max White                  SRF 441249/ Quote No. 440060   *
.* Function       : To report on the number of patients waiting between       *
.*                  0 - 30 mins, 31 - 60 mins & over 61 mins (split between   *
.*                  early & late arrivals.                                    *
.*                  Note: Based on IBAOUT87 - Arrival Time Analysis           *
.* Modifications  :                                                           *
.*        V12.00.01 22/05/2025  Alina Bhari   TSK 0955096                     *
.*                  Added alpanumeric visit number generation -GDAT3000       *
.*        V10.07.01 30/10/2015  Ebon Clements CAR 268970                      *
.*                  Change to use OUTCLIFD index 1 instead of index 2         *
.*        V10.05.01 15/07/2014  Ebon Clements CAR 261630                      *
.*                  Removed port number from temp file name                   *
.*        V9.09.02  04/03/2008  Peter Vela    CAR 162914                      *
.*                  Recompiled for KYOUTCLI - readk of outcliaf               *
.*        V9.09.01  28/07/2007  Jill Habasque CAR 103645                      *
.*                  Recompiled for KYOUTCLI - read of pmshcpaf                *
.*        V5.08.01  19/09/2000  Steve Armstrong   srf 647263                  *
.*                  Recompiled for changes to OUTDSCLN                        *
.*        V5.08.B01 14/03/2000  Steve Armstrong    5.08 Mods                  *
.*                  Mods for changes to OUTCLIFD (effective date)             *
.******************************************************************************
.
          INC       STD001FD
          INC       IBASEQFD/INC
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
          INC       OUTCONFD/INC
          INC       OUTCLIFD/INC
          INC       OUTOSFFD/INC
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PMSHCPFD/INC            * National HCP file
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
TEMPA     IFILE     SQL, FIXED=82, KEY="U1-6,7-12,13-13,14-18,19-21,22-27"
KEYA      INIT      " 82 U1-6,7-12,13-13,14-18,19-21,22-27"
.Name     Type      Length  Phys.  Start  Description
.-------  ----      ------  -----  -----  ---------------------
XSESNID   DIM       6           6      1  Session Id
XCLINID   DIM       6           6      7  Clinic Id
XCLDAY    DIM       1           1     13  Day
XTIME     DIM       5           5     14  Clinic Start Time
XHOSP     DIM       3           3     19  Hospital
XCTYPE    DIM       6           6     22  Clinic Type
DXATTN    DIM       6           6     28  Total Number of Attendances
DXEATTN   DIM       6           6     34  Number of Early Attendances
DXEARLA   DIM       6           6     40  Early arrivals 00-30 mins waiting
DXEARLB   DIM       6           6     46  Early arrivals 31-60 mins waiting
DXEARLC   DIM       6           6     52  Early arrivals 61+ mins waiting
DXLATTN   DIM       6           6     58  Number of Late Attendances
DXLATEA   DIM       6           6     64  Late arrivals 00-30 mins waiting
DXLATEB   DIM       6           6     70  Late arrivals 31-60 mins waiting
DXLATEC   DIM       6           6     76  Late arrivals 61+ mins waiting
.End of record                        82
.
XATTN     FORM      6
XEATTN    FORM      6
XEARLA    FORM      6
XEARLB    FORM      6
XEARLC    FORM      6
XLATTN    FORM      6
XLATEA    FORM      6
XLATEB    FORM      6
XLATEC    FORM      6
.
.         program variables
.
CDFRDATE  DIM       8
CDFRSNID  DIM       6
CDTODATE  DIM       8
CDTOSNID  DIM       6
CFNAMEA   DIM       8
CMDLINE   DIM       80
COUNT     FORM      1
CURRCLIN  DIM       6         * current print clinic id
CURRSNID  DIM       6         * current print session id
DIM3      DIM       3
DIM40     DIM       40
DOCCODE   DIM       6
.
DURATION  FORM      4         * calculated duration of operation
FORM4B    FORM      4         * time work variable
MODFLAG   FORM      1         * modifying a single param or inputting all ?
PRFRDATE  DIM       10
PRFRSNID  DIM       6
PRTODATE  DIM       10
PRTOSNID  DIM       6
PTCNDCQS  FORM      1
TIMESTOP  DIM       5         * ending time
TIMESTRT  DIM       5         * starting time
TOVRCD    FORM      1
.
.         program constants
.
COLUMN    FORM      "23"
FINISH    INIT      "Finish"
ISBUILD   INIT      "isbuild "
ISERASE   INIT      "iserase "
PRFIXA    INIT      "out64A"
START     INIT      "Start "
Z6        INIT      "zzzzzz"
Z20       INIT      "zzzzzzzzzzzzzzzzzzzz"
.
PRGID     INIT      "IBAOUT64"
PRGNAM    INIT      "Waiting Time Analysis"
VERSION   INIT      "V12.01.00"
+
.*********************************************************************
.         mainline code
.*********************************************************************
ML0000    CALL      INIT0000
.
ML1000    CALL      OPTN0000                * run the option ??
          BRANCH    EXIT,ML9999             * exit
.
          CALL      KPAR0000                * keyin parameters
          BRANCH    EXIT,ML1000             * exitchar entered
.
          CALL      GDAT0000                * get the data
          CALL      REPT0000                * produce the report
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.*********************************************************************
.         initilisation
.*********************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P50:24,"Opening"
          PACK      CFNAME,UID,FILBOKA1
          DISPLAY   *P58:24,CFNAME
          OPEN      OUTBOKA1,CFNAME
          PACK      CFNAME,UID,FILBB1A1
          DISPLAY   *P58:24,CFNAME
          CALL      OPOTBB11
          PACK      CFNAME,UID,FILCLIA1
          DISPLAY   *P58:24,CFNAME
          OPEN      OUTCLIA1,CFNAME
          DISPLAY   *P58:24,"outosfaf"
          OPEN      OUTOSFA3,"outosfaf"
          DISPLAY   *P58:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
          DISPLAY   *P64:24,"pmshcpaf";
          OPEN      PMSHCPA1,"pmshcpaf"
.
          DISPLAY   *P58:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TWENTY1;*247,PTCNDCQS
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,CFNAMEA
.
          MOVE      ONE,CNEWDS
.
INIT9999  RETURN
+
.*********************************************************************
.         run the option ??
.*********************************************************************
OPTN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Run report":
                    *P1:7,"Select option : ";
OPTN1000  KEYIN     *P17:7,*V2LON,MOPTION
          MOVE      ONE,EXIT
          COMPARE   ZERO,MOPTION
          GOTO      OPTN9999 IF EQUAL
          MOVE      ZERO,EXIT
          BRANCH    MOPTION,OPTN9999
          BEEP
          GOTO      OPTN1000
OPTN9999  RETURN
+
.*********************************************************************
.         keyin parameters
.*********************************************************************
KPAR0000  CALL      DSCR0000                * display screen
          MOVE      ZERO,MODFLAG            * get all params initially
          MOVE      ZERO,CCITEM
.
KPAR1000  CALL      SNFR0000                * keyin session id from
          BRANCH    EXIT,KPAR9000           * exitchar entered
          BRANCH    MODFLAG,KPAR5000        * modifying single param ?
.
KPAR2000  CALL      SNTO0000                * keyin session id to
          BRANCH    EXIT,KPAR9000           * exitchar entered
          BRANCH    MODFLAG,KPAR5000        * modifying single param ?
.
KPAR3000  CALL      KDFR0000                * keyin booking date from
          BRANCH    MODFLAG,KPAR5000        * modifying single param ?
.
KPAR4000  CALL      KDTO0000                * keyin booking date to
          BRANCH    MODFLAG,KPAR5000        * modifying single param ?
.
. Select Item, (P)ost, (C)ancel ?
.
KPAR5000  MOVE      ONE,MODFLAG              * allow modification of params
          CALL      MAINQST
          COMPARE   ZERO,CCITEM
          GOTO      KPAR8000 IF EQUAL        * (P)ost
          GOTO      KPAR9000 IF LESS         * (C)ancel
.
          BRANCH    CCITEM,KPAR1000:         * session id from
                           KPAR2000:         * session id to
                           KPAR3000:         * booking date from
                           KPAR4000          * booking date to
.
          BEEP       
          GOTO      KPAR5000                 * invalid response entered
.
KPAR8000  MOVE      ZERO,EXIT                * all is ok. continue
          GOTO      KPAR9999
.
KPAR9000  MOVE      ONE,EXIT                 * return to menu
.
KPAR9999  RETURN
+
.*********************************************************************
.         display parameter screen layout
.*********************************************************************
DSCR0000  DISPLAY   *P1:8,*EF:
                    *P2:09,*V2LON,"1. ",*HOFF,"From Session Id : ":
                    *P2:10,*V2LON,"2. ",*HOFF,"To   Session Id : ":
                    *P2:12,*V2LON,"3. ",*HOFF,"From Date       : ":
                    *P2:13,*V2LON,"4. ",*HOFF,"To   Date       : "
DSCR9999  RETURN
+
.*********************************************************************
.         keyin session id range from
.*********************************************************************
SNFR0000  DISPLAY   *PCOLUMN:09,*EL         * clear to end of line
          MOVE      ZERO,CKYIMAND           * mandatory ??
          MOVE      SP6,CKYIDEF6            * default value
          MOVE      "09",EVERT
          MOVE      COLUMN,ECOL
          CALL      KYOUTCLI
          COMPARE   TWO,EXIT
          GOTO      SNFR9100 IF EQUAL       * exitchar entered
.
          PACK      OCACLIN,OCACLIN,SP6
          MOVE      OCACLIN,CDFRSNID        * from code
          MOVE      OCACLIN,PRFRSNID        * from print code
          MATCH     SP6,OCACLIN
          IF        @EQUAL
            DISPLAY   *PCOLUMN:EVERT,*V2LON,START
            MOVE      START,PRFRSNID
          ELSE
            DISPLAY   *PCOLUMN:EVERT,*V2LON,OCACLIN,*HOFF,SP3,OCADESC
          ENDIF
.
          MOVE      ZERO,EXIT
          MATCH     CDFRSNID,CDTOSNID
          GOTO      SNFR9999 IF NOT LESS    * end >= start so OK
.
          DISPLAY   *P1:24,*B,"Invalid Session Id range.  ",*EL;
          CALL      HITENTER
          GOTO      SNFR0000
.
SNFR9100  MOVE      ONE,EXIT
.
SNFR9999  RETURN
+
.*********************************************************************
.         keyin session id range to
.*********************************************************************
SNTO0000  DISPLAY   *PCOLUMN:10,*EL         * clear to end of line
          MOVE      ZERO,CKYIMAND           * mandatory ??
          MOVE      CDFRSNID,CKYIDEF6       * default value
          MOVE      "10",EVERT
          MOVE      COLUMN,ECOL
          CALL      KYOUTCLI
          COMPARE   TWO,EXIT
          GOTO      SNTO9100 IF EQUAL       * exitchar entered
.
          PACK      OCACLIN,OCACLIN,SP6
          MOVE      OCACLIN,CDTOSNID        * set the value
          MOVE      OCACLIN,PRTOSNID        * to print code
          MATCH     SP6,OCACLIN
          IF        @EQUAL
            DISPLAY   *PCOLUMN:EVERT,*V2LON,FINISH
            MOVE      Z6,CDTOSNID
            MOVE      FINISH,PRTOSNID
          ELSE
            DISPLAY   *PCOLUMN:EVERT,*V2LON,OCACLIN,*HOFF,SP3,OCADESC
          ENDIF
.
          MOVE      ZERO,EXIT
          MATCH     CDFRSNID,CDTOSNID
          GOTO      SNTO9999 IF NOT LESS    * end >= start so OK
.
          DISPLAY   *P1:24,*B,"Invalid Session Id range.  ",*EL;
          CALL      HITENTER
          GOTO      SNTO0000
.
SNTO9100  MOVE      ONE,EXIT
.
SNTO9999  RETURN
+
.*********************************************************************
.*                  KDFR0000                    Called by : KPAR3000 *
.*        Enter the date range from                                  *
.*        Returns : EXIT = 1      exit chosen                        * 
.*                  CDFRDATE      starting date  ccyymmdd            *
.*                  PRFRDATE      starting date  dd/mm/ccyy          *
.*********************************************************************
KDFR0000  MOVE      ONE,CCANLDTE            * blank cancel default
          MOVE      ZERO,CDEFDTE
          MOVE      ZERO,CHIGHLT            * use highlight
.
.         enter starting date
.
KDFR1000  MOVE      "12",CVERT              * row
          MOVE      COLUMN,CCOL             * column
          UNPACK    SP6,CYEAR,CMON,CDAY     * no defaults
          MOVE      CCC,CCENT
.
          CALL      KEYDATE
          BRANCH    OVRCD,KDFR1000          * nothing entered
          BRANCH    CQUEST,KDFR1000
.
          PACK      CDFRDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",CDFRDATE           * set up start date
          CALL      PACDATE
          MOVE      CPCDATE,PRFRDATE         * formatted start date
          CALL      PACDATE
          MOVE      CPCDATE,PRTODATE         * formatted end date
.
          MATCH     CDFRDATE,CDTODATE
          GOTO      KDFR9999 IF NOT LESS    * date is <= start date
.
          DISPLAY   *P1:24,*B,"Invalid date range.  ",*EL;
          CALL      HITENTER 
          GOTO      KDFR1000
.
KDFR9999  RETURN
+
.*********************************************************************
.*                  KDTO0000                    Called by : KPAR4000 *
.*        Enter the date range to                                    *
.*        Returns : EXIT = 1      exit chosen                        * 
.*                  CDTODATE      ending date    ccyymmdd            *
.*                  PRTODATE      ending date    dd/mm/ccyy          *
.*********************************************************************
KDTO0000  MOVE      ONE,CCANLDTE            * blank cancel default
          MOVE      ZERO,CDEFDTE
          MOVE      ZERO,CHIGHLT            * use highlight
.
.         enter ending date (default to starting date)
.
KDTO2000  MOVE      "13",CVERT              * row
          MOVE      COLUMN,CCOL             * column
          UNPACK    CDFRDATE,CCENT,CYEAR,CMON,CDAY
.
          CALL      KEYDATE
          BRANCH    OVRCD,KDTO2000          * nothing entered
          BRANCH    CQUEST,KDTO2000
.
          PACK      CDTODATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",CDTODATE           * set up end date
          CALL      PACDATE
          MOVE      CPCDATE,PRTODATE         * formatted end date
.
          MATCH     CDFRDATE,CDTODATE
          GOTO      KDTO9999 IF NOT LESS    * date is <= start date
.
          DISPLAY   *P1:24,*B,"Invalid date range.  ",*EL;
          CALL      HITENTER 
          GOTO      KDTO2000
.
KDTO9999  RETURN
+
.*********************************************************************
.         get the details
.*********************************************************************
GDAT0000  DISPLAY   *P1:24,*EL,"Obtaining the information..."
          CLOCK     TIME,CTIMEIS
          CALL      CTPA0000                * create temp file
          MOVE      ZERO,COUNT
          PACK      KEY34,CDFRDATE,SP30
          CALL      RSOTOSF3
.
GDAT1000  CALL      RKOTOSF3
          BRANCH    OVRCD,GDAT9999          * finished
          MATCH     OTOSDATE,CDTODATE
          GOTO      GDAT9999 IF LESS        * after TO date
.
          MATCH     IDDD,OTOSSITE
          GOTO      GDAT1000 IF NOT EQUAL   * different site
.
          MATCH     CDFRSNID,OTOSSESS
          GOTO      GDAT1000 IF LESS        * before FROM session id
          MATCH     OTOSSESS,CDTOSNID
          GOTO      GDAT1000 IF LESS        * after  TO   session id
.
.         loop over bookings file for session found
.
          PACK      KEY28,OTOSSITE,OTOSCLID,OTOSDATE,OTOSTIME,SP3
          IF        COUNT = ZERO
            DISPLAY   *P41:24,*EL,KEY28
          ENDIF
          ADD       ONE,COUNT
          CALL      RDSBOKA1
.
GDAT3000  CALL      RDKBOKA1
          BRANCH    OVRCD,GDAT1000
          PACK      KEY25,OBASITE,OBACLIN,OBADATE,OBASTRT
          MATCH     KEY25,KEY28
          GOTO      GDAT1000 IF NOT EQUAL   * different session
.
          MATCH     ZEROVISN,OBAOUTNO
          GOTO      GDAT3000 IF EQUAL       * no booking number
.
          COMPARE   FOUR,OBASTAT
          GOTO      GDAT3000 IF NOT EQUAL   * not an attendee
.
          MOVE      OBAOUTNO,KEY8
          CALL      RDBOKB1
          BRANCH    OVRCD,GDAT3000          * invalid booking number
.
          MATCH     SP5,OTBBCITM            * check-in time ?
          GOTO      GDAT3000 IF EQUAL       * no, ignore
.      
          MATCH     SP5,OTBBASTM            * patient outcomed ?
          GOTO      GDAT3000 IF EQUAL       * no, ignore
.
          CALL      UPDT0000                * update temp file
.
          GOTO      GDAT3000
.
GDAT9999  RETURN
+
.*********************************************************************
.         update the temp file
.*********************************************************************
UPDT0000  MOVE      ZERO,XATTN
          MOVE      ZERO,XEATTN
          MOVE      ZERO,XEARLA
          MOVE      ZERO,XEARLB
          MOVE      ZERO,XEARLC
          MOVE      ZERO,XLATTN
          MOVE      ZERO,XLATEA
          MOVE      ZERO,XLATEB
          MOVE      ZERO,XLATEC
.
          MOVE      OTOSSESS,XSESNID         * session id
          MOVE      OTOSCLID,XCLINID         * clinic  id
          MOVE      OBADAY,XCLDAY            * day
          MOVE      OTOSTIME,XTIME           * session time
          MOVE      OTOSHOSP,XHOSP           * hospital
          MOVE      OTOSCTYP,XCTYPE          * clinic type
.
          PACK      KEY27,XSESNID,XCLINID,XCLDAY,XTIME,XHOSP,XCTYPE
          CALL      RDTEMPA
          MOVE      OVRCD,TOVRCD
.
          ADD       ONE,XATTN                * add to number of attendances
          MATCH     SP5,OTBBCITM
          GOTO      UPDT1000 IF EQUAL        * no check in time
.
.         determine if early or late arrival & calculate the waiting time
.
          MATCH     OTBBCITM,OBATIME
          IF        @LESS
            CALL      WTLT0000               * late arrival
          ELSE
            CALL      WTER0000               * early arrival
          ENDIF
.
UPDT1000  IF       TOVRCD = ONE
            CALL     WRTEMPA
          ELSE
            CALL     UPTEMPA
          ENDIF
.
UPDT9999  RETURN
+
.*********************************************************************
.         calculate early arrival waiting times
.*********************************************************************
WTER0000  ADD       ONE,XEATTN
.
          MOVE      OBATIME,TIMESTRT         * appointment time
          MOVE      OTBBASTM,TIMESTOP        * time actually seen
.
          MATCH     OBATIME,OTBBASTM         * seen prior to appointment ?
          IF        @LESS
            ADD       ONE,XEARLA             * yes, 00 mins waiting
            GOTO      WTER9999
          ENDIF
.
          CALL      DURN0000
.
          IF        DURATION > 60
            ADD       ONE,XEARLC              * 61+ minutes waiting
          ELSE
            IF        DURATION > 30
              ADD       ONE,XEARLB            * 31-60 minutes waiting
            ELSE
              ADD       ONE,XEARLA            * 00-30 minutes waiting
            ENDIF
          ENDIF
.
WTER9999  RETURN
+
.*********************************************************************
.         calculate late arrival waiting times
.*********************************************************************
WTLT0000  ADD       ONE,XLATTN
.
          MOVE      OTBBCITM,TIMESTRT        * check-in time
          MOVE      OTBBASTM,TIMESTOP        * time actually seen
          CALL      DURN0000
.
          IF        DURATION > 60
            ADD       ONE,XLATEC              * 61+ minutes waiting
          ELSE
            IF        DURATION > 30
              ADD       ONE,XLATEB            * 31-60 minutes waiting
            ELSE
              ADD       ONE,XLATEA            * 00-30 minutes waiting
            ENDIF
          ENDIF
.
WTLT9999  RETURN
+
.*********************************************************************
.         produce the report
.*********************************************************************
REPT0000  DISPLAY   *P1:24,*EL,"Printing report..."
          MOVE      ZERO,CPAGENO
          MOVE      "******",CURRSNID
          MOVE      "******",CURRCLIN
          MOVE      "70",CLNO
.
          MOVE      SP30,KEY27
          CALL      RSTEMPA
.
REPT1000  CALL      RKTEMPA
          BRANCH    OVRCD,REPT9000
.
          CALL      PLIN0000
          GOTO      REPT1000
.
REPT9000  IF        CPAGENO = ZERO
            CALL      HEAD132
          ELSE
            CALL      UND132
          ENDIF
          PRINT     *N,"*** End of Report ***",*N
          CALL      DTPA0000
.
REPT9999  RETURN
+
.*********************************************************************
.         print a new page heading
.*********************************************************************
HEAD0000  CALL     HEAD132
          PRINT    *40,"Clinic Session : ",PRFRSNID,"     to ",PRTOSNID
          PRINT    *40,"Booking Dates  : ",PRFRDATE," to ",PRTODATE
          ADD      TWO,CLNO
          CALL     FNAM0000                 * format name
          PRINT    *N,"Clinic Session : ",XSESNID,SP2,OCADESC
          CALL     UND132
          PRINT    *1,"|Clinic                        Clinic":
                   *38,"| Total":
                   *49,"|             Early Arrivals":
                   *88,"|             Late Arrivals",*132,"|"
          PRINT    *1,"|ID        Day     Time  Hosp  Type":
                   *38,"| Attendees":
                   *49,"| Attendees   00-30   31-60     61+":
                   *88,"| Attendees   00-30   31-60     61+",*132,"|"
          CALL     UND132
          MOVE     TEN,CLNO
          MOVE     "******",CURRCLIN
HEAD9999  RETURN
+
.*********************************************************************
.*        Format Clinic/Surgeon name                                 *
.*********************************************************************
.
FNAM0000  PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          PACK      KEY20,IDDD,XSESNID,KEY8
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     IDDD,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     XSESNID,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          IF        OVRCD = 1
            MOVE      "Unknown clinic",OCADESC
            PACK      OCADESC,OCADESC,SP30
          ENDIF
.
          MATCH     SP6,OCADOCT
          GOTO      FNAM9999 IF EQUAL       * no docotor code to use
          MOVE      OCADOCT,KEY6            * use the doctor code
.
.         validate using doctors file
.
FNAM5000  CALL      RDDOCT1                 * read docs file
          BRANCH    OVRCD,FNAM9999          * invalid code
.
          MOVE      DSNAME,PACSNAME         * surname
          MOVE      DGNAME,PACGNAME         * given name
          MOVE      DTITL,PACTITLE          * title
          MOVE      TWO,PACFRMT             * SURNAME, TITLE GIVEN
          CALL      PACNAME                 * format the name
          MOVE      PACFNAME,OCADESC        * set name
.
FNAM9999  RETURN
+
.*********************************************************************
.         print a line of the report
.*********************************************************************
PLIN0000  MATCH     CURRSNID,XSESNID
          IF        !@EQUAL
            MOVE      "70",CLNO
          ENDIF
.
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS    * print new page
.
          MATCH     CURRCLIN,XCLINID
          GOTO      PLIN2000 IF EQUAL       * same clinic id
.
          PRINT     *1,"|",XCLINID;
.
PLIN2000  MOVE      XCLDAY,FORM1
          LOAD      KEY9,FORM1,DMON,DTUE,DWED,DTHU,DFRI,DSAT,DSUN
          PRINT     *9,KEY9:
                    *20,XTIME:
                    *26,XHOSP:
                    *32,XCTYPE:
                    *38,"| ",XATTN:
                    *49,"| ",XEATTN:
                    *62,XEARLA:
                    *70,XEARLB:
                    *78,XEARLC:
                    *88,"| ",XLATTN:
                    *101,XLATEA:
                    *109,XLATEB:
                    *117,XLATEC,*132,"|"
          ADD       ONE,CLNO
          MOVE      XSESNID,CURRSNID
          MOVE      XCLINID,CURRCLIN
PLIN9999  RETURN
+
.*********************************************************************
.*                  DURN0000                    Called by : xxxx0000 *
.*        Calculate a duration given two times                       *
.*        Para's  : TIMESTOP      the end time   (HH:MM)             *
.*                  TIMESTRT      the start time (HH:MM)             *
.*        Returns : DURATION      the duration (minutes)             *
.*********************************************************************
DURN0000  MOVE      ZERO,DURATION
          MATCH     TIMESTOP,TIMESTRT
          GOTO      DURN9999 IF EQUAL       * no duration
          GOTO      DURN1000 IF LESS        * times not over midnight
.
.         operation went over midnight so make end time larger
.         eg 22:00 to 01:00 becomes 22:00 to 25:00 which gives 3 hours duration
.
          UNPACK    TIMESTOP,CHOUR,ANS,CMIN
          MOVE      CHOUR,FORM2
          ADD       "24",FORM2
          MOVE      FORM2,CHOUR
          PACK      TIMESTOP,CHOUR,ANS,CMIN
.
.         get end time as minutes
.
DURN1000  UNPACK    TIMESTOP,CHOUR,ANS,CMIN
          MOVE      CHOUR,FORM2             * hour as a form 2
          MOVE      FORM2,FORM4A            * set up work variable
          MULT      "60",FORM4A             * hours turned into minutes
          MOVE      CMIN,FORM2              * minutes as as form 2
          ADD       FORM2,FORM4A            * the total minutes of end time
.
.         get start time as minutes
.
          UNPACK    TIMESTRT,CHOUR,ANS,CMIN
          MOVE      CHOUR,FORM2             * hour as a form 2
          MOVE      FORM2,FORM4B            * set up work variable
          MULT      "60",FORM4B             * hours turned into minutes
          MOVE      CMIN,FORM2              * minutes as as form 2
          ADD       FORM2,FORM4B            * the total minutes of start time
.
.         duration = end minutes - start minutes
.
          SUB       FORM4B,FORM4A           * the duration
          MOVE      FORM4A,DURATION         * the duration
.
DURN9999  RETURN
+
.*********************************************************************
.         Create a temp file
.*********************************************************************
CTPA0000  CALL      DTPA0000                * delete temp file
          CLEAR     CMDLINE                 * clear command line
          PACK      CMDLINE,ISBUILD,CFNAMEA,KEYA,SP30      * set command
          RESET     CMDLINE                 * reset FP
          EXECUTE   CMDLINE,TASKID          * build the temp file
          OPEN      TEMPA,CFNAMEA           * open the temp file
CTPA9999  RETURN
+
.*********************************************************************
.         Delete temp file A
.*********************************************************************
DTPA0000  CLOSE     TEMPA                   * close temp file
          CLEAR     CMDLINE                 * clear command line
          PACK      CMDLINE,ISERASE,CFNAMEA * set command line for erase
          RESET     CMDLINE                 * reset FP
          EXECUTE   CMDLINE,TASKID          * delete the temp file
DTPA9999  RETURN
+
.*********************************************************************
.         temp file I/O
.*********************************************************************
RSTEMPA   READ      TEMPA,KEY27;;
          RETURN
.
RDTEMPA   MOVE      ZERO,OVRCD
          READ      TEMPA,KEY27;XSESNID,XCLINID,XCLDAY,XTIME,XHOSP,XCTYPE:
                                DXATTN,DXEATTN,DXEARLA,DXEARLB,DXEARLC:
                                       DXLATTN,DXLATEA,DXLATEB,DXLATEC
          GOTO      OVERCOND IF OVER
          MOVE      DXATTN,XATTN
          MOVE      DXEATTN,XEATTN
          MOVE      DXEARLA,XEARLA
          MOVE      DXEARLB,XEARLB
          MOVE      DXEARLC,XEARLC
          MOVE      DXLATTN,XLATTN
          MOVE      DXLATEA,XLATEA
          MOVE      DXLATEB,XLATEB
          MOVE      DXLATEC,XLATEC
          RETURN
.
RKTEMPA   MOVE      ZERO,OVRCD
          READKS    TEMPA;XSESNID,XCLINID,XCLDAY,XTIME,XHOSP,XCTYPE:
                          DXATTN,DXEATTN,DXEARLA,DXEARLB,DXEARLC:
                                 DXLATTN,DXLATEA,DXLATEB,DXLATEC
          GOTO      OVERCOND IF OVER
          MOVE      DXATTN,XATTN
          MOVE      DXEATTN,XEATTN
          MOVE      DXEARLA,XEARLA
          MOVE      DXEARLB,XEARLB
          MOVE      DXEARLC,XEARLC
          MOVE      DXLATTN,XLATTN
          MOVE      DXLATEA,XLATEA
          MOVE      DXLATEB,XLATEB
          MOVE      DXLATEC,XLATEC
          RETURN
.
WRTEMPA   MOVE      XATTN,DXATTN
          MOVE      XEATTN,DXEATTN
          MOVE      XEARLA,DXEARLA
          MOVE      XEARLB,DXEARLB
          MOVE      XEARLC,DXEARLC
          MOVE      XLATTN,DXLATTN
          MOVE      XLATEA,DXLATEA
          MOVE      XLATEB,DXLATEB
          MOVE      XLATEC,DXLATEC
          WRITE     TEMPA,KEY27;XSESNID,XCLINID,XCLDAY,XTIME,XHOSP,XCTYPE:
                                DXATTN,DXEATTN,DXEARLA,DXEARLB,DXEARLC:
                                       DXLATTN,DXLATEA,DXLATEB,DXLATEC
          RETURN
.
UPTEMPA   MOVE      XATTN,DXATTN
          MOVE      XEATTN,DXEATTN
          MOVE      XEARLA,DXEARLA
          MOVE      XEARLB,DXEARLB
          MOVE      XEARLC,DXEARLC
          MOVE      XLATTN,DXLATTN
          MOVE      XLATEA,DXLATEA
          MOVE      XLATEB,DXLATEB
          MOVE      XLATEC,DXLATEC
          UPDATE    TEMPA;XSESNID,XCLINID,XCLDAY,XTIME,XHOSP,XCTYPE:
                          DXATTN,DXEATTN,DXEARLA,DXEARLB,DXEARLC:
                                 DXLATTN,DXLATEA,DXLATEB,DXLATEC
          RETURN
+
          INC       STD001IO
          INC       KYOUTCLI
          INC       IBASEQIO/INC
          INC       OUTDSCLN
          INC       OUTBOAIO/INC
          INC       OUTBB1IO/INC
          INC       OUTCLIIO/INC
          INC       OUTOSFIO/INC
          INC       PATDOCKY
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PMSHCPIO/INC            * National HCP file
          INC       TFILENAM
          INC       WEBERRIO/INC
..............................................................................

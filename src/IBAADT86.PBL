.******************************************************************************
.* System    :   Patient Management                                           *
.* Program   :   IBAADT86  -  PATIENT CENSUS REPORT                           *
.* Desc      :                                                                *
.******************************************************************************
.* Date      :   12/04/1999                                                   *
.* Author    :   Jill Habasque                                                *
.* Mods      :                                                                *
.*        V12.00.01 15.05.2025  DA Sarkies     TSK 0955096                    *
.*                  Updated for Alpha-Numeric Visit Number                    *
.*        V10.13.01 05/12/2018  Don Nguyen    TSK 0838558                     *
.*                  Deleted temp files (SECTION1, SECTION2, SECTION3) on exit *
.*        V10.08.01 25/11/2016 Ania P         CAR 261630                      *
.*                  PORT CAR Changes                                          *
.*        V10.05.02 01/12/2014 J.Tan          CAR 309027                      *
.*                  Fixed to recalculate age                                  *
.*        V10.05.01 22/10/2014 Peter Vela     CAR 297004                      *
.*                  Compare tran record ward to SAVWARD in DSIN1000           *
.*        V10.02.04 09/09/2011 Peter Vela     CAR 249071                      *
.*                  Modified further to print PTWDSDES Ward Short Desc        *
.*        V10.02.03 31/08/2011 Jill Parkinson CAR 249071                      *
.*                  Modified to print PTWDSDES                                *
.*        V10.02.02 12/08/2011 Peter Vela     CAR 240705                      *
.*                  Modified Ward column in Boarders section                  *
.*        V10.02.01 21/06/2011 Peter Vela     CAR 240705                      *
.*                  Added Hospital Boarders                                   *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V9.12.01  11/08/2009  Mike Laming   CAR 201247 (203411)             *
.*                  Add test for blank Ward at CADM1000 to fix Standbys count *
.*                  Fix count for previous day for new Admissions at STBY4000 *
.*        V9.08.02  04/06/2007 Ebon Clements       141477                     *
.*                  Fixed incorrect goto at PRAD7000 test for XARRDEP         *
.*                  Removed change - Loop through rest of discharge records   *
.*                   @ PRAD6000                                               *
.*        V9.08.01  27/02/2007 Glenn Mikoska       130276                     *
.*                  Use parameter PTCNDRSM to use Cat D or DD for deceased    *
.*                  Loop through rest of discharge records @ PRAD6000         *
.*        V9.07.01  07/09/2006  Jill Habasque  CAR 109852                     *
.*                  Added output of alternate ID if PTCNDAUR=1                *
.*        V9.05.01  28/02/2006  Peter Vela     CAR 93700                      *
.*                  Added port numbers to the end of temp files to prevent    *
.*                  I*D on simultaneous execution of this program             *
.*        V9.04.06  27/06/2005  Jill Habasque  CAR 64848                      *
.*                  Modified CHNG000 to include all movement types            *
.*        V9.04.05  21/06/2005  Jill Habasque                                 *
.*                  Fixed printing if on leave for census but now discharged  *
.*        V9.04.02  18/06/2005  Jill Habasque                                 *
.*                  Fixed printing of ward for current admissions             *
.*        V9.04.01  07/06/2005  Sandra Barcham    CAR 62410                   *
.*                  Ported from 5.12                                          *
.*        V5.12.03  13/04/2005  Steve Armstrong   CAR 60523                   *
.*                  Mods to change Unique id fields in temp files to be DIM's *
.*                  instead of FORM's (as they are part of the key).          *
.*                  Also fixed CLEV0000 routine so that it checks if still    *
.*                  processing leave records and doesn't just continue on     *
.*                  until the end of the file.                                *
.*        V5.12.02  24/02/2005  Mike Laming  CAR 56133                        *
.*                  Correct Category code at PRAD6500 (should be "D " not "DD"*
.*        V5.12.01  17/02/2005  Mike Laming  CAR 56142                        *
.*                  Add test for e-o-f at label FLEV1500                      *
.*        V5.09.02  07/02/2002  Mike Laming  SRF 15596                        *
.*                  correct KEY6 for reading "patwr1af"                       *
.*                  add test for change of Ward in ARRV0000                   *
.*                  (NOTE - keys for SECTIONn work files is wrong - a FORM 4  *
.*                  CANNOT be put into 3 bytes of a DIM field without it      *
.*                  truncating it. But its doing something so I won't touch it)
.*        V5.09.01  02/07/2001  Caleb Sharman  SRF 10970                      *
.*                  Recompiled for PATWRDDS                                   *
.*        V5.08.01  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.B01 13/01/1999  J Habasque SRF 636109                      *
.*                  Mods to not print inactive wards                       *
.*        V5.07.06  16/11/1999  J Habasque  SRF 635865                     *
.*                  Mods to standby patients                               *
.*        V5.07.05  Jill Habasque                                          *
.*                  Fixed current inpatients                               *
.*        V5.07.04  Jill Habasque                                          *
.*                  Fixed ward changes                                     *
.*        V5.07.03  Jill Habasque   SRF 633213                             *
.*                  Added FLEV0000 to find current admissions that were on *
.*                  leave at the time of census                            *
.*        V5.07.02  Jill Habasque   SRF 632417                             *
.*                  Fixed discharge destination display                    *
.***************************************************************************
. FILE DESCRIPTION INCLUDES
. -------------------------
.
          INC       STD001FD
.
          INC       PATCALAG/INC
          INC       PATDHEAD/INC
          INC       PATBRDFD/INC    * Hospital Boarders
          INC       PATCODFD/INC    * codes file
          INC       PATCONFD/INC    * codes file
          INC       PATDSCFD/INC    * discharge file 
          INC       PATHSPFD/INC    * hospital file
          INC       PATMA1FD/INC    * patient master file
          INC       PATMI1FD/INC    * admission file
          INC       PMSVX1FD/INC    * admission file
          INC       PATTRNFD/INC    * patient tran file
          INC       PATWR1FD/INC    * ward file
          INC       PMSPX2FD/INC
          INC       IBASEQFD/INC 
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
.  Tempfile for Inpatients at midnight on census day
.
SECTION1  IFILE     SQL,FIXED=66, KEY="U50-52,53-55,1-4"
.
.Name     Type      Length    Physical  Position  Description
.----     ----      ------    --------  --------  -----------
DXUNIQ1   DIM       4            4         1      Unique ID
.XURNO    DIM       8            8         5      UR Number
.XNAME    DIM       30          30        13      Patient name
.XSEX     DIM       1            1        43      Sex
.XAGE     DIM       3            3        44      Age
.XCLASS   DIM       3            3        47      Category CL    
.XWARD    DIM       3            3        50      Ward
.XBED     DIM       3            3        53      Bed
XLEAVE    DIM       1            1        56      1=On leave 0=Admitted 
XSTBY     DIM       1            1        57      1=Standby 0=not standby
XADMN     DIM       8            8        58      Adm Number
.
.End of Record                            66
.
. Redefine form fields from key
. -----------------------------
XUNIQ1    FORM      4
.
.
. Tempfile for arrivals/departures on census day
.
SECTION2  IFILE     SQL,FIXED=76, KEY="U62-64,65-67,1-4"
.
.Name     Type      Length    Physical  Position  Description
.----     ----      ------    --------  --------  -----------
DXUNIQ    DIM       4            4         1      Unique ID
XURNO     DIM       8            8         5      UR Number
XNAME     DIM       30          30        13      Patient name
XSEX      DIM       1            1        43      Patient sex
XAGE      DIM       3            3        44      Age
XCLASS    DIM       3            3        47      Category CL    
XUNIT     DIM       3            3        50      Unit
XARRDEP   DIM       1            1        53      1 = Arrival
.                                                 2 = Departure
XTYPE     DIM       8            8        54      Type of Arrival/Departure
XWARD     DIM       3            3        62      Ward
XBED      DIM       3            3        65      Bed
.XADMN    DIM       8            8        68      Adm Number
.
.End of record                            76
.
. Redefine form fields from key
. -----------------------------
XUNIQ     FORM      4
.
. 
. Tempfile for inpatients on the previous day
.
SECTION3  IFILE     SQL,FIXED=25,KEY="U1-4"
.
.Name     Type      Length    Physical  Position  Description
.----     ----      ------    --------  --------  -----------
DCOUNT    DIM       4            4         1      Unique ID
.XURNO    DIM       8            8         5      Urno
.XLEAVE   DIM       1            1        13      1=On leave 0=Admitted
.XWARD    DIM       3            3        14
.XADMN    DIM       8            8        17      Adm Number
.
.End of record                            25
.
. Redefine form fields from key
. -----------------------------
COUNT     FORM      4
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
ADMNO     DIM       8
ALLWRDS   DIM       1               * 1 = all wards 
.
BJDAY     FORM      3
CJDAY     FORM      3
CENDATE   DIM       10              * formatted census date
CHGFLAG   FORM      1
CMDLINE   DIM       50
CURDATE   DIM       8               * current date
CWARD     DIM       3               * ward for census
.
DEATHS    FORM      3               * deaths on census date
DIM8      DIM       8
DIM9      DIM       9
DIM11     DIM       11
.
FADATE    FORM      8               * admission date
FCURDATE  FORM      8               * current date
FDATE     FORM      8          
FDDATE    FORM      8
FILENAM1  DIM       8
FILENAM2  DIM       8
FILENAM3  DIM       8
FSDATE    FORM      8               * census date 
FTDATE    FORM      8
.
IBCNMHOS  FORM      1
.
PREDATE   DIM       8               * date on previous day
PREDAY    DIM       1
PTOTINP   FORM      3               * previous days total inpatients
.
SAVALL    DIM       1
SDATE     DIM       8               * census date
SEQ1      FORM      5
SWARD     DIM       4               * ward
SAVEWARD  DIM       3              
SAVWARD   DIM       3               * save ward if processing single ward only
STANDBY   DIM       1
.
TOTARR    FORM      3               * total arrivals
TOTADM    FORM      3               * total admissions
TOTBRD    FORM      3               * total boarders
TOTDEP    FORM      3               * total departures
TOTDSC    FORM      3               * total discharges
TOTFRLV   FORM      3               * total inpatients returning from leave
TOTINP    FORM      3               * count for total inpatients
TOTLEV    FORM      3               * total inpatients on leave
TOTTOLV   FORM      3               * total inpatients to leave
TOTTRIN   FORM      3               * total patients transferred in to ward
TOTTROT   FORM      3               * total patients transferred out of ward
TYPE      DIM       1               * type of arrival/departure 
FTYPE     FORM      1
.
VACBEDS   FORM      3               * nubmer of vacant beds 
.
CODED     INIT      "D "                                               *I-56133
CODEDD    INIT      "DD"
TYPEA     INIT      "ADMIT"
TYPEC     INIT      "TRANS IN"
TYPER     INIT      "RETURN"
TYPED     INIT      "DISCH"
TYPEL     INIT      "LEAVE"
.
PRGID     INIT      "IBAADT86"
PRGNAM    INIT      "Patient Census Report        "
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
.
ML2000    CALL      SDAT0000               * enter census date  
          CALL      KHOS0000               * enter hospital date
          CALL      SWRD0000               * enter single ward or all wards
          BRANCH    EXIT,ML2000:           * ok to continue = no
                         ML9999            *                = cancel
.
          MATCH     "1",ALLWRDS
          IF        @EQUAL
            CALL      AWRD0000             * process all wards
          ELSE
            CALL      SING0000             * process a single ward
          ENDIF
.
          CALL      KILL0000               * delete the tempfiles
          GOTO      ML0100
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,"Opening patwr1af";  
          OPEN      PATWR1A1,"patwr1af"                  * ward file
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A2,"patwr1af"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A3,"patwr1af"
.
          DISPLAY   *P64:24,"patmi1af";       * admission file
          OPEN      PATMI1A1,"patmi1af" 
          OPEN      PATMI1A2,"patmi1af" 
          OPEN      PATMI1A3,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patdschf";       * discharge file
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"patcodes";       * codes
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"pattranf";       * tran file
          OPEN      PATTRAN2,"pattranf"
          OPEN      PATTRAN4,"pattranf"
.
          DISPLAY   *P64:24,"patma1af";       * master file
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"patbrdaf";
          OPEN      PATBRDA1,"patbrdaf"
          OPEN      PATBRDA2,"patbrdaf"
          OPEN      PATBRDA3,"patbrdaf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TWENTY1;*45,PTCNDRSM
.
          MOVE      ONE,SEQ
          MOVE      ONE,SEQ1
          MOVE      ZERO,XUNIQ
          MOVE      ZERO,XUNIQ1
          MOVE      ZERO,COUNT
          MOVE      ZERO,PREDAY
.
          CALL TFILENAM
          MOVE TFILNAME,FILENAM1
          CALL TFILENAM
          MOVE TFILNAME,FILENAM2
          CALL TFILENAM
          MOVE TFILNAME,FILENAM3
.
          CALL      CREA0000                  * create/open temp files
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Print Census Report"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000    * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**********************************************************************
.*                         CREA0000                                   *
.*                  Create Temporary Files                            *
.**********************************************************************
.
CREA0000  CALL      KILL0000
.
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FILENAM1,CMDLINE
          APPEND    " 66 UC50-52,53-55,1-4",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          PREP      SECTION1,FILENAM1
.
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FILENAM2,CMDLINE
          APPEND    " 76 UC62-64,65-67,1-4",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          PREP      SECTION2,FILENAM2
.
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FILENAM3,CMDLINE
          APPEND    " 25 UC1-4",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          PREP      SECTION3,FILENAM3
          RETURN
.
.**********************************************************************
.*                         KILL0000                                   *
.*                  Remove Temporary Files                            *
.**********************************************************************
.
KILL0000  PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FILENAM1,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FILENAM2,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FILENAM3,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PREP      SECTION1,FILENAM1
          CLOSE     SECTION1,DELETE
          PREP      SECTION2,FILENAM2
          CLOSE     SECTION2,DELETE
          PREP      SECTION3,FILENAM3
          CLOSE     SECTION3,DELETE
.
          RETURN
+
.**************************************************************************
.*                               SDAT0000              Called by: ML0000  *
.*                            Keyin report date                           *
.**************************************************************************
.
SDAT0000  DISPLAY   *P1:11,*EF,"Keyin Date : "
          MOVE      CDD,CDAY                * default to current date
          MOVE      CMM,CMON
          MOVE      CYY,CYEAR
          MOVE      CCC,CCENT
          MOVE      TEN4,CCOL
          MOVE      TEN1,CVERT
          MOVE      ONE,CCANLDTE
          CALL      KEYDATE
          BRANCH    OVRCD,SDAT0000
          PACK      SDATE,CCENT,CYEAR,CMON,CDAY
.
          PACK     CURDATE,CCC,CYY,CMM,CDD  * get current date
          REP      " 0",CURDATE
          MOVE     CURDATE,FCURDATE
          REP      " 0",SDATE
          MOVE     SDATE,FSDATE
.
          COMPARE  FSDATE,FCURDATE           * date cannot be greater than today
          GOTO     SDAT9999 IF NOT LESS    
.
SDAT9000  DISPLAY  *P1:24,*V2LON," Census date cannot be in the future    ";
          CALL     HITENTER
          GOTO     SDAT0000
.
SDAT9999  RETURN
+
.**************************************************************************
.*                               PRED0000              Called by: ML0000  *
.*          Calculate the number of inpatients on the previous day        *
.**************************************************************************
.
PRED0000  UNPACK    SDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,CC
          MOVE      CYEAR,YY             * get the day before to calculate
          MOVE      CMON,MM              * inpatients on previous day
          MOVE      CDAY,DD
          CALL      DMYCON
          MOVE      JULDAY,FORM4
          SUB       ONE,FORM4
          MOVE      FORM4,JULDAY 
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON
          PACK      PREDATE,CC,YY,MM,DD
          REP       " 0",PREDATE
.
          MOVE      ONE,PREDAY
          MOVE      PREDATE,FSDATE
          CALL      CADM0000                 * current admissions
          CALL      STBY0000                 * standby patients
          CALL      CLEV0000                 * current on leave patients
          CALL      DSIN0000                 * discharged since census
.
PRED9999  MOVE      ZERO,PREDAY
          ADD       ONE,COUNT
          MOVE      SDATE,FSDATE             * change back to census day 
          MOVE      SAVALL,ALLWRDS
          RETURN
.
.******************************************************************************
.*                                  KHOS0000              Called by: ML0000   *
.*                            Enter The hospital                              *
.******************************************************************************
KHOS0000  COMPARE   ONE,IBCNMHOS
          GOTO      KHOS9999 IF NOT EQUAL
.
          DISPLAY   *P1:13,*EL,"Hospital Id: ";
          MOVE      TEN4,ECOL
          MOVE      TEN3,EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS2000,KHOS9500
          GOTO      KHOS3000
.
KHOS2000  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          MOVE      ZERO,EXIT
.
KHOS3000  CLOSE     PATHSPA1
.
          DISPLAY   *P14:13,*V2LON,PTHSNAME;
          GOTO      KHOS9999
.
KHOS9500  MOVE      ONE,EXIT
KHOS9999  RETURN
.**************************************************************************
.*                               WARD0000              Called by: ML0000  *
.*                      Keyin the ward (enter for all wards)              *
.**************************************************************************
.
SWRD0000  MOVE      "2",ALLWRDS
          DISPLAY   *P1:15,"Keyin Ward : ","     (Enter for All)"
          MOVE      TEN4,ECOL
          MOVE      TEN5,EVERT
          MOVE      ZERO,CKYIMAND
          CALL      PATWRDKY
          BRANCH    EXIT,SWRD5000:             * enter (all wards)
                         SWRD9999              * exit
.
          MOVE      WWARD,CWARD
          COMPARE   ONE,WACTIVE
          IF        @EQUAL
            DISPLAY   *P1:24,*V2LON,"Inactive Ward        ",*HOFF;
            CALL      HITENTER
            GOTO      SWRD0000
          ENDIF
.
          IF        IBCNMHOS=1
            MATCH     SP10,PTHSHOSP
            IF        !@EQUAL
              MATCH     PTWDHOSN,PTHSHOSP
              IF       !@EQUAL
                DISPLAY   *P1:24,*V2LON,"Ward does not belong to this Hospital ",*HOFF;
                CALL      HITENTER
                GOTO      SWRD0000
              ENDIF
            ENDIF
          ENDIF
.
          MOVE      WWARD,SAVWARD              * save ward for single ward
          GOTO      SWRD6000                   * valid input
.
SWRD5000  DISPLAY   *P14:15,*EL,*V2LON,"All"
          MOVE      "1",ALLWRDS
SWRD6000  MOVE      SP6,CWARD
          PACK      KEY6,SP8
          CALL      RDAWARD1
.
          CALL      CONTQST
          BRANCH    CEXIT,SWRD9000:            * yes
                          SWRD9500:            * no
                          SWRD9700             * cancel
.
SWRD9000  MOVE      ZERO,EXIT
          GOTO      SWRD9999
.
SWRD9500  MOVE      ONE,EXIT
          GOTO      SWRD9999
.
SWRD9700  MOVE      TWO,EXIT
.
SWRD9999  RETURN
+
.**************************************************************************
.*                               SING0000              Called by: ML0000  *
.*                        Processing for single ward                      *
.**************************************************************************
.
SING0000  CALL      CADM0000             * current admissions
          CALL      STBY0000             * standby patients
          CALL      CLEV0000             * patients currently on leave
          CALL      DSIN0000             * discharges since the census date
          CALL      CLEA0000             * clear totals
          MOVE      SP3,CWARD
          PACK      KEY6,CWARD,SP6                                  *I-50902
          CALL      RDSWARD1
SING1000  CALL      ALLW0000             * get arrivals & departures for all wrd
          BRANCH    EXIT,SING5000
          CALL      ARRV0000              
          GOTO      SING1000
.
SING5000  MOVE      SAVWARD,CWARD
          CALL      PRED0000             * calculate inpatients on prev day
          MOVE      ZERO,PREDAY
          PACK      KEY3,SP3
          CALL      RDSSECT3                 * read the 3rd tempfile
SING6000  CALL      RDKSECT3
          BRANCH    OVRCD,SING7000
.
          MATCH     XWARD,CWARD
          GOTO      SING6000 IF NOT EQUAL
.
          MATCH     "1",XLEAVE               * add 1 to the total for each pat
          IF        !@EQUAL
            ADD       ONE,PTOTINP
          ENDIF
          MOVE      ZERO,PREDAY
.
          GOTO      SING6000                 * read next record
.
SING7000  CALL      PRIM0000             * print inpatients
          CALL      PRAD0000             * print arrivals & departures
          CALL      PBRD0000             * print hospital boarders 
          CALL      PRSU0000             * print summary
.
SING9999  RETURN
+
.**************************************************************************
.*                               AWRD0000              Called by: ML0000  *
.*                        Processing for all wards                        *
.**************************************************************************
.
AWRD0000  CALL      CADM0000             * current admissions
          CALL      STBY0000             * standby patients
          CALL      CLEV0000             * patients currently on leave
          CALL      DSIN0000             * discharges since the census date
          CALL      PRED0000             * calculate inpatients on prev day
          CALL      CLEA0000
          MOVE      SP6,KEY6
          CALL      RDSWARD1
AWRD1000  CALL      ALLW0000             * arrivals and departures    
          BRANCH    EXIT,AWRD5000        * final ward
          CALL      ARRV0000
          GOTO      AWRD1000
.
AWRD5000  MOVE      SP6,KEY6             * return to first ward & print report
          CALL      RDAWARD1
AWRD5500  CALL      ALLW0000
          BRANCH    EXIT,AWRD9999
.
          CALL      CLEA0000
          PACK      KEY3,SP3                 * add inpatients on previous day
          CALL      RDSSECT3                 * read the 3rd tempfile
AWRD6000  CALL      RDKSECT3
          BRANCH    OVRCD,AWRD7000
.
          MATCH     XWARD,CWARD
          GOTO      AWRD6000 IF NOT EQUAL
.
          MATCH     "1",XLEAVE               * add 1 to the total for each pat
          IF        !@EQUAL
            ADD       ONE,PTOTINP
          ENDIF
          MOVE      ZERO,PREDAY
.
          GOTO      AWRD6000                 * read next record
.
AWRD7000  CALL      PRIM0000             * print patients in ward
          CALL      PRAD0000             * print arrivals and departures
          CALL      PBRD0000             * print hospital boarders
          CALL      PRSU0000             * print summary for the ward
          GOTO      AWRD5500             * read next ward
.
AWRD9999  RETURN
+
.**************************************************************************
.*                               CADM0000              Called by: ML0000  *
.* Get all current admissions and test that the patients was admitted     *
.* before the census.                                                     *
.**************************************************************************
.
CADM0000  DISPLAY   *P1:24,*EL,"Processing Admissions:";
.
          PACK      KEY9,TWO,SP10        * astat = 2 (current admission)
          CALL      RDSMISS2
CADM1000  CALL      RDKMISS2
          BRANCH    OVRCD,CADM9999
.
          COMPARE   TWO,ASTAT            * still current admission record
          GOTO      CADM9999 IF NOT EQUAL
.
          MATCH     SP3,AWARD            * check if given a ward/bed  *I-201247
          GOTO      CADM1000 IF EQUAL
.
          MOVE      ADATE,FADATE         * change adate to form
          COMPARE   FADATE,FSDATE        * admitted before census, write to file
          GOTO      CADM1000 IF LESS
.
          DISPLAY   *P24:24,*EL,*V2LON,AADMNO;
.
          MOVE      "0",XLEAVE
.
          CALL      CHNG0000
          CALL      FLEV0000
.
CADM2000  MOVE      AURNO,XURNO
          MOVE      ACLAIM,XCLASS
          MOVE      AWARD,XWARD
          MOVE      ABED,XBED
          MOVE      "0",XSTBY
          MOVE      DAADMNO,XADMN
.
          MATCH     "1",PREDAY         
          IF        @EQUAL
            CALL      WRSECT3           * write to prev day tempfile
          ELSE
            CALL      PDET0000          * get patient details for report
            CALL      WRSECT1           * write to census tempfile
          ENDIF
          GOTO        CADM1000
.
CADM9999  RETURN
+
.**************************************************************************
.*                               CHNG0000              Called by: ML0000  *
.*                          Find any change of ward or bed                *
.**************************************************************************
.
.
CHNG0000  MOVE      ZERO,CHGFLAG
          PACK      KEY30,DAADMNO
          CALL      RDSTRAN2
          CALL      RDKTRAN2
          BRANCH    OVRCD,CHNG9999
.
          MATCH     DTADMN,DAADMNO                * same admission?
          GOTO      CHNG9999 IF NOT EQUAL
.
          MATCH     "A",TMOVE
          GOTO      CHNG1000 IF NOT EQUAL
          MOVE      TWARD,AWARD
          MOVE      TBED,ABED
.  
          PACK      KEY30,DAADMNO
          CALL      RDSTRAN2
CHNG1000  CALL      RDKTRAN2
          BRANCH    OVRCD,CHNG9999
.
          MATCH     DTADMN,DAADMNO                * same admission?
          GOTO      CHNG9999 IF NOT EQUAL
.
....      MATCH     "C",TMOVE
....      GOTO      CHNG1000 IF NOT EQUAL
.
          MOVE      TDATE,FTDATE
          COMPARE   FTDATE,FSDATE
          GOTO      CHNG1000 IF LESS
          MOVE      ONE,CHGFLAG
.....     CALL      RDPTRAN2
.
          MOVE      TWARD,AWARD
          MOVE      TBED,ABED
......    CALL      RDKTRAN2
          GOTO      CHNG1000
.
CHNG9999  RETURN
+
.**************************************************************************
.*                               FLEV0000              Called by: ML0000  *
.*                          Find any leave records                        *
.**************************************************************************
.
.
FLEV0000  PACK      KEY30,DAADMNO,SP30
          CALL      RDSTRAN2
FLEV1000  CALL      RDKTRAN2
          BRANCH    OVRCD,FLEV9999
.
          MATCH     DTADMN,DAADMNO                * same admission?
          GOTO      FLEV9999 IF NOT EQUAL
.
          MATCH     "L",TMOVE
          GOTO      FLEV1000 IF NOT EQUAL
.
          MOVE      TDATE,FTDATE
          COMPARE   FTDATE,FSDATE
          GOTO      FLEV9999 IF LESS
.
FLEV1500  CALL      RDKTRAN2
          BRANCH    OVRCD,FLEV9999                                     *I-56142
          MATCH     DTADMN,DAADMNO
          GOTO      FLEV9999 IF NOT EQUAL
          MATCH     "R",TMOVE
          GOTO      FLEV1500 IF NOT EQUAL
.
          MOVE      TDATE,FTDATE
          COMPARE   FTDATE,FSDATE
          IF        @LESS
            MOVE      "1",XLEAVE
          ENDIF
          GOTO      FLEV1000
.
FLEV1800  MATCH     "1",ALLWRDS
          IF        !@EQUAL
            MATCH     AWARD,SAVWARD
            GOTO      FLEV9999 IF NOT EQUAL
          ENDIF
.
FLEV9999  RETURN
+
.**************************************************************************
.*                               STBY0000              Called by: ML0000  *
.*                          Standby patients                              *
.**************************************************************************
.
STBY0000  DISPLAY   *P12:24,*EL,"Standby Patients:";
.
          PACK      KEY6,SP6                     * Position at start of file
          CALL      RDSWARD1
STBY1000  CALL      RDKWARD1
          BRANCH    OVRCD,STBY9999               * End of file
.
          COMPARE   ONE,WACTIVE                  * inactive ward
          GOTO      STBY1000 IF EQUAL
.
          MATCH     ZEROVISN,WSTBY               * no standy patient
          GOTO      STBY1000 IF EQUAL
.
STBY2000  MATCH     "1",ALLWRDS
          IF        !@EQUAL
            MATCH     WWARD,SAVWARD
            GOTO      STBY1000 IF NOT EQUAL
          ENDIF
.
          PACK      KEY8,WSTBY
          CALL      RDMISS1
          BRANCH    OVRCD,STBY1000               * no admission record
.
STBY3000  MATCH     ADATE,SDATE
          GOTO      STBY1000 IF LESS
.
          DISPLAY   *P30:24,*EL,*V2LON,AADMNO;
.
          MOVE      AURNO,XURNO
          MOVE      ONE,XSTBY
          MOVE      WWARD,XWARD
          MOVE      WBED,XBED
          MOVE      DAADMNO,XADMN
.
STBY4000  MATCH     "1",PREDAY         
          IF        @EQUAL
            MATCH     ADATE,SDATE       * admissions not in prev.day  *I-201247
            GOTO      STBY1000 IF EQUAL                               *I-201247
            CALL      WRSECT3           * write to prev day tempfile
          ELSE
            CALL      PDET0000          * get patient details for report
            CALL      WRSECT1           * write to census tempfile
          ENDIF
          MOVE        ZERO,XSTBY
	  GOTO        STBY1000
.
.
STBY9999  RETURN
+
.**************************************************************************
.*                               PDET0000              Called by: ML0000  *
.*                       Get patients details                             *
.**************************************************************************
.
PDET0000    PACK      KEY8,XURNO,SP8           * read master file
            CALL      RDMAST1
            BRANCH    OVRCD,PDET9000
.
            MOVE      PSNAME,PACSNAME            * set up patients name
            MOVE      PGNAME,PACGNAME
            MOVE      PTITL,PACTITLE
            MOVE      TWO, PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,XNAME
.
            MOVE      PSEX,XSEX
            MOVE      PSAGE,XAGE
.
            CALL      IBACLOCK
            PACK      AGEDATE,CCC,CYY,CMM,CDD
            CALL      CALCAGE
            MOVE      PSAGEYRS,XAGE
.
            MOVE      ACLAIM,XCLASS
            GOTO      PDET9999
.
PDET9000    MOVE      "Missing Master File Record",XNAME
            UNPACK    SP30,XSEX,XAGE,XCLASS
.
PDET9999    RETURN

.**************************************************************************
.*                               CLEV0000              Called by: ML0000  *
.* Get all patients currently on leave and test that they were admitted   *
.* before the census then read the tran file to see if they went on leave *
.* before or after the census date.                                       *
.**************************************************************************
.
CLEV0000  DISPLAY   *P12:24,*EL,"Patients on Leave:";
.
          PACK      KEY9,FOUR,SP10      * astat = 4 (on leave)
          CALL      RDSMISS2
CLEV1000  CALL      RDKMISS2
          BRANCH    OVRCD,CLEV9999
.
          COMPARE   FOUR,ASTAT            * still on leave record
          GOTO      CLEV9999 IF NOT EQUAL
.
          MATCH     "1",ALLWRDS
          IF        !@EQUAL
            MATCH     SAVWARD,AWARD
            GOTO      CLEV1000 IF NOT EQUAL
          ENDIF
.
          MOVE      ADATE,FADATE          * admitted before census date?
          COMPARE   FADATE,FSDATE
          GOTO      CLEV1000 IF LESS      * admitted after census    
.
          PACK      KEY30,DAADMNO,SP30  * read the tran file to find
          CALL      RDSTRAN2            * the date the patient went
CLEV5000  CALL      RDKTRAN2            * on leave
          BRANCH    OVRCD,CLEV9000
.
          MATCH     DTADMN,DAADMNO
          GOTO      CLEV8000 IF NOT EQUAL
.
          MATCH     "L",TMOVE           * read next tran record if not leave
          GOTO      CLEV5000 IF NOT EQUAL
. 
          MOVE      TDATE,FDATE         * move to form
          COMPARE   FDATE,FSDATE
          IF        @LESS
            MOVE      "0",XLEAVE        * on leave after census
          ELSE
            MOVE      "1",XLEAVE        * on leave before census
          ENDIF
.
          CALL      CHNG0000 
          CALL      FLEV0000
.
CLEV8000  MOVE      AURNO,XURNO
          MOVE      ACLAIM,XCLASS
CLEV9000  MOVE      AWARD,XWARD
          MOVE      ABED,XBED
          MOVE      DAADMNO,XADMN
.
          DISPLAY   *P31:24,*EL,*V2LON,AADMNO;
.
          MATCH     "1",PREDAY
          IF        @EQUAL
            CALL      WRSECT3
          ELSE
            CALL      PDET0000          * get patient details & write to file
            CALL      WRSECT1
          ENDIF
.
          GOTO      CLEV1000
.
CLEV9999  RETURN
.
.**************************************************************************
.*                               DSIN0000              Called by: ML0000  *
.*                       All patients discharged since census date        *
.**************************************************************************
.
DSIN0000  DISPLAY   *P12:24,*EL,"Discharges:"
.
          PACK      KEY16,FSDATE,SP10
          CALL      RDDSCH2                    * read the discharge file 
DSIN1000  CALL      RDKDSCH2
          BRANCH    OVRCD,DSIN9999
.
          MOVE      ZERO,XLEAVE
          MATCH     "1",ALLWRDS
          IF        !@EQUAL
.           MATCH     SAVWARD,PTDSDWRD
.           GOTO      DSIN1000 IF NOT EQUAL
.
            PACK      KEY30,DDADMNO,FSDATE,Z70
            CALL      RDSTRAN2                   * read the tran file to find
            CALL      RDPTRAN2                   * the ward/bed
            BRANCH    OVRCD,DSIN1000
.
            MATCH     DADMNO,TADMN
            GOTO      DSIN1000 IF NOT EQUAL
.
            MATCH     SAVWARD,TWARD
            GOTO      DSIN1000 IF NOT EQUAL
.
          ENDIF
.
          PACK      KEY8,DDADMNO               * find the admission date
          CALL      RDMISS1                  
          BRANCH    OVRCD,DSIN1000
          MATCH     DAADMNO,DDADMNO            * same admn no.
          GOTO      DSIN1000 IF NOT EQUAL      * no
.
          MOVE      ADATE,FADATE
          COMPARE   FADATE,FSDATE              * is adm date after census?
          GOTO      DSIN1000 IF LESS           * yes, read next discharge
.
          MOVE      DDATE,FDDATE
          COMPARE   FDDATE,FSDATE
          GOTO      DSIN1000 IF EQUAL
.
          PACK      KEY30,DDADMNO,DDATE,SP30
          CALL      RDSTRAN2                   * read the tran file to find
          CALL      RDKTRAN2                   * the ward/bed
          BRANCH    OVRCD,DSIN9000
          MOVE      TWARD,AWARD
          MOVE      TBED,ABED
.
          CALL      CHNG0000
          CALL      FLEV0000
.
DSIN9000  MOVE      AWARD,XWARD
          MOVE      ABED,XBED
          PACK      KEY10,XWARD,XBED,XUNIQ1
          MOVE      DURNO,XURNO
          MOVE      DAADMNO,XADMN
.
          DISPLAY   *P24:24,*EL,*V2LON,AADMNO;
.
          MATCH     "1",PREDAY
          IF        @EQUAL
            CALL      WRSECT3
          ELSE
            CALL      PDET0000
            CALL      WRSECT1                    * patient was in hosp. during 
          ENDIF
          GOTO      DSIN1000
.
DSIN9999  RETURN
+
.**************************************************************************
.*                               ALLW0000              Called by: ML0000  *
.*             Loop through ward file to find all wards                   *
.**************************************************************************
.
ALLW0000  MOVE      ZERO,EXIT
          CALL      RDKWARD1
          BRANCH    OVRCD,ALLW9000
.
          COMPARE   ONE,WACTIVE
          GOTO      ALLW0000 IF EQUAL
.
          MATCH     WWARD,CWARD
          GOTO      ALLW0000 IF EQUAL
.
          IF        IBCNMHOS=1
            MATCH     SP10,PTHSHOSP
            IF        !@EQUAL
              MATCH     PTWDHOSN,PTHSHOSP
              GOTO      ALLW0000 IF NOT EQUAL
            ENDIF
          ENDIF
.
          MOVE      WWARD,CWARD
          GOTO      ALLW9999
.
ALLW9000  MOVE      ONE,EXIT
.
ALLW9999  RETURN
+
.**************************************************************************
.*                               ARRV0000              Called by: ML0000  *
.*             Loop through tran file for all arrivals & departures       *
.**************************************************************************
.
ARRV0000  PACK      KEY30,CWARD,FSDATE,SP30
          CALL      RDSTRAN4
ARRV1000  CALL      RDKTRAN4
          BRANCH    OVRCD,ARRV9999
.
          MOVE      TDATE,FDATE
          COMPARE   FSDATE,FDATE                * transfer on census date?
          GOTO      ARRV9999 IF NOT EQUAL
.
          MATCH     TWARD,CWARD                                        *I-50902
          GOTO      ARRV9999 IF NOT EQUAL                              *I-50902
.
          MOVE      TURNO,XURNO                * read master file for name etc.
          PACK      KEY8,XURNO,SP8
          CALL      RDMAST1
          BRANCH    OVRCD,ARRV9999
.
          MOVE      PSNAME,PACSNAME            * set up patients name
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO, PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,XNAME
.
          MOVE      PSEX,XSEX
          MOVE      PSAGE,XAGE
.
          CALL      IBACLOCK
          PACK      AGEDATE,CCC,CYY,CMM,CDD
          CALL      CALCAGE
          MOVE      PSAGEYRS,XAGE
.
          MOVE      TADMN,KEY8
          CALL      RDMISS1
          MOVE      AUSR6,XUNIT
.
          MOVE      ACLAIM,XCLASS
          MOVE      TMOVE,XARRDEP
          MOVE      TWARD,XWARD
          MOVE      TBED,XBED
          REP       "A1C1R1D2L2",XARRDEP       * 1=arrival 2=departure
.
          MOVE      TMOVE,TYPE
          REP       "A1C2R3D4L5",TYPE
          MOVE      TYPE,FTYPE                 * load type of arrival/departure
          LOAD      XTYPE,FTYPE,TYPEA,TYPEC,TYPER,TYPED,TYPEL
          MOVE      DTADMN,XADMN
.
          MATCH     TYPEC,XTYPE
          GOTO      ARRV9200 IF EQUAL          * change record
.
ARRV9000  PACK      KEY10,XWARD,XBED,XUNIQ
          CALL      WRSECT2
.
. If there is a change record, the previous tran record will show the ward/bed
. that the patients was moved from
.
ARRV9200  MATCH     "C",TMOVE
          IF        @EQUAL
            MOVE      TWARD,SAVEWARD
            PACK      KEY30,DTADMN,TDATE,TTIME,TWARD,TBED
            CALL      RDSTRAN2
            CALL      RDPTRAN2                * read previous record
            MATCH     TWARD,SAVEWARD
            GOTO      ARRV9500 IF EQUAL       * same ward,no transfer
.
            PACK      KEY10,XWARD,XBED,XUNIQ
            CALL      WRSECT2
            MOVE      "2",XARRDEP             * write departure to tempfile
            MOVE      TWARD,XWARD
            MOVE      TBED,XBED
            MOVE      "TRANS. OUT",XTYPE
            CALL      WRSECT2
          ENDIF
.
ARRV9500  MOVE      ZERO,OVRCD
          GOTO      ARRV1000
.
ARRV9999  RETURN
+
.****************************************************************************
.*                                CLEA0000             Called by: ML0000    *
.*                        Clear variables                                   *
.****************************************************************************
.
CLEA0000  MOVE      ZERO,TOTARR
          MOVE      ZERO,TOTADM
          MOVE      ZERO,TOTBRD
          MOVE      ZERO,TOTDEP
          MOVE      ZERO,TOTDSC
          MOVE      ZERO,TOTFRLV
          MOVE      ZERO,TOTINP
          MOVE      ZERO,TOTLEV
          MOVE      ZERO,TOTTOLV
          MOVE      ZERO,TOTTRIN
          MOVE      ZERO,TOTTROT
          MOVE      ZERO,PTOTINP
          MOVE      ZERO,VACBEDS
          MOVE      ZERO,DEATHS
.
CLEA9999  RETURN
+
.**************************************************************************
.*                               PRIM0000              Called by: ML0000  *
.*             Print inpatients at midnight from temp file                *
.**************************************************************************
.
PRIM0000  DISPLAY   *P1:24,*EL,"Printing Report:";
.
          PACK      KEY6,CWARD,SP6           * dont print inactive ward
          CALL      RDWARD1
          COMPARE   ZERO,WACTIVE
          IF        !@EQUAL
            GOTO      PRIM9999
          ENDIF
.
          MOVE      ZERO,TOTINP              * clear total inpatients        
          CLOCK     TIME,CTIMEIS
          MOVE      ZERO,CLNO
          CALL      HEAD80
          UNPACK    SDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                  * display heading
          MOVE      CPCDATE,CENDATE
          PRINT     *N,*10,"Patients in Ward ",PTWDSDES," at Midnight on ":
                    CENDATE,*N:
                    *N,*1,"U/R Number",*12,"Patient Name",*44,"Sex":
                    *49,"Age  Class  Ward/Bed ":
                    *N,*1,"----------",*12,"------------",*44,"---":
                    *49,"---  -----  --------------------"
.
          MOVE      NINE,CLNO
          PACK      KEY10,SP10                 
          CALL      RDSSECT1            
PRIM1000  CALL      RDKSECT1      
          BRANCH    OVRCD,PRIM9000
.
          MATCH     CWARD,XWARD
          GOTO      PRIM1000 IF NOT EQUAL
.
          MATCH      "1",XLEAVE
          GOTO       PRIM1000 IF EQUAL
.
          MATCH      "1",XSTBY
          IF         @EQUAL
            MOVE      "S",STANDBY
          ENDIF
.
          DISPLAY    *P18:24,*V2LON,XURNO;
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,HUND10;*249,PTCNDAUR
          MATCH     "1",PTCNDAUR
          GOTO      PRIM1500 IF NOT EQUAL
          MOVE      XURNO,PURNO
          PACK      KEY20,SP70
          PROC      GETALIDS
          MATCH     SP70,KEY20
          IF        !@EQUAL
            UNPACK    KEY20,DIM11,DIM9
            PRINT     *1,DIM9; 
            GOTO      PRIM1600
          ENDIF
.
PRIM1500  PRINT     *1,XURNO; 
.
PRIM1600  PRINT     *12,XNAME,*44,XSEX,*49,XAGE,*54,XCLASS:
                    *61,PTWDSDES,"/",XBED,STANDBY
          MOVE       SP3,STANDBY
          ADD        ONE,CLNO
          ADD        ONE,TOTINP
          COMPARE   "55",CLNO
          CALL      HEAD80 IF NOT LESS
          GOTO       PRIM1000
.
PRIM9000  PRINT     *N,"Total Patients at Midnight = ",TOTINP
          ADD       ONE,CLNO
.
          COMPARE   "55",CLNO
          CALL      HEAD80 IF NOT LESS
.
          PRINT     *N,*10,"Patients on leave from Ward ",PTWDSDES:
                    " at Midnight on ":
                    CENDATE,*N:
                    *N,*1,"U/R Number",*12,"Patient Name",*44,"Sex":
                    *49,"Age  Class  Ward/Bed ":
                    *N,*1,"----------",*12,"------------",*44,"---":
                    *49,"---  -----  --------------------"
          ADD       FIVE,CLNO
.
          PACK      KEY10,SP10
          CALL      RDSSECT1          * Print on leave patients
PRIM9500  CALL      RDKSECT1
          BRANCH    OVRCD,PRIM9800
.
          MATCH     CWARD,XWARD
          GOTO      PRIM9500 IF NOT EQUAL
.
          MATCH      "1",XLEAVE
          GOTO       PRIM9500 IF NOT EQUAL
.
          COMPARE    "55",CLNO
          CALL       HEAD80 IF NOT LESS
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,HUND10;*249,PTCNDAUR
          MATCH     "1",PTCNDAUR
          GOTO      PRIM9600 IF NOT EQUAL
          MOVE      XURNO,PURNO
          PACK      KEY20,SP70
          PROC      GETALIDS
          MATCH     SP70,KEY20
          IF        !@EQUAL
            UNPACK    KEY20,DIM11,DIM9
            PRINT     *1,DIM9; 
            GOTO      PRIM9700
          ENDIF
.
PRIM9600  PRINT      *1,XURNO;
.
PRIM9700  PRINT     *12,XNAME,*44,XSEX,*49,XAGE,*54,XCLASS:
                    *61,PTWDSDES,"/",XBED,STANDBY
          ADD        ONE,CLNO
          ADD        ONE,TOTLEV
          GOTO       PRIM9500
PRIM9800  PRINT      *N,"Total Patients on leave at Midnight = ",TOTLEV
          GOTO       PRIM9999
.
PRIM9999  RETURN
+
.**************************************************************************
.*                               PRAD0000              Called by: ML0000  *
.*             Print arrivals/departures for single ward                  *
.**************************************************************************
.
PRAD0000  DISPLAY   *P1:24,*EL,"Printing Report:";
.
          MOVE      ZERO,TOTARR              * clear total arrivals & depart.
          MOVE      ZERO,TOTDEP
.
          PACK      KEY6,CWARD,SP6           * dont print inactive ward        
          CALL      RDWARD1                                                    
          COMPARE   ZERO,WACTIVE                                               
          IF        !@EQUAL                                                    
            GOTO      PRIM9999                                                 
          ENDIF                                                                
.                                                                      
          CLOCK     TIME,CTIMEIS
          MOVE      ZERO,CLNO
          CALL      HEAD80
          UNPACK    SDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                  * display heading
          PACK      KEY6,CWARD,SP70
          CALL      RDWARD1
          IF        OVRCD<>1
            PRINT     *N,"Ward : ",PTWDSDES       
          ELSE
            PRINT     *N,"Ward : ",CWARD          
          ENDIF
          PRINT     *N,*10,"Arrivals/Departures from 00:01 to midnight ":
                    "during ",CENDATE,*N,*N,*35,"ARRIVALS":
                    *N,*1,"U/R Number",*12,"Patient Name",*52,"Sex":
                    *56,"Age  Class  Unit  Type of",*N,*74,"Arrival":
                    *N,*1,"----------",*12,"------------",*52,"---":
                    *56,"---  -----  ----  -------"
.
          PACK      KEY10,SP10
          CALL      RDSSECT2
PRAD1000  CALL      RDKSECT2
          BRANCH    OVRCD,PRAD5000
.
          COMPARE   "55",CLNO
          CALL      HEAD80 IF NOT LESS
.
          MATCH     CWARD,XWARD
          GOTO      PRAD1000 IF NOT EQUAL
.
          DISPLAY    *P18:24,*V2LON,XURNO;
.
          MATCH     "1",XARRDEP                * print arrivals
          GOTO      PRAD1000 IF NOT EQUAL
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,HUND10;*249,PTCNDAUR
          MATCH     "1",PTCNDAUR
          GOTO      PRAD1500 IF NOT EQUAL
          MOVE      XURNO,PURNO
          PACK      KEY20,SP70
          PROC      GETALIDS
          MATCH     SP70,KEY20
          IF        !@EQUAL
            UNPACK    KEY20,DIM11,DIM9
            PRINT     *1,DIM9; 
            GOTO      PRAD1600
          ENDIF
.
PRAD1500  PRINT     *1,XURNO;
.
PRAD1600  PRINT     *12,XNAME,*52,XSEX,*56,XAGE,*61,XCLASS:
                    *68,XUNIT,*74,XTYPE
          ADD       ONE,CLNO
          MATCH     TYPEA,XTYPE
          IF        @EQUAL
            ADD       ONE,TOTADM            * add one to admissions
          ENDIF

          MATCH     TYPEC,XTYPE
          IF        @EQUAL
            ADD       ONE,TOTTRIN          * add one to transfer in
          ENDIF
.
          MATCH     TYPER,XTYPE
          IF        @EQUAL
            ADD       ONE,TOTFRLV          * add one to return from leave
          ENDIF
.
          ADD       ONE,TOTARR             * add to total arrivals
          GOTO      PRAD1000
.
PRAD5000  PRINT     *N,*N,"Total Arrivals = ",TOTARR
.
          PRINT     *N,*35,"DEPARTURES":
                    *N,*1,"U/R Number",*12,"Patient Name",*52,"Sex":
                    *56,"Age  Class  Unit  Type of",*N,*74,"Dept.  ":
                    *N,*1,"----------",*12,"------------",*52,"---":
                    *56,"---  -----  ----  -------"
.
          ADD       FIVE,CLNO
.
          PACK      KEY10,SP10
          CALL      RDSSECT2
PRAD6000  CALL      RDKSECT2
          BRANCH    OVRCD,PRAD9000
.
          MATCH     CWARD,XWARD
          GOTO      PRAD6000 IF NOT EQUAL
.
          MATCH     "DISCH",XTYPE              * if patient has been discharged
          GOTO      PRAD7000 IF NOT EQUAL      * find the discharge status
.                                              * and find if the patient is 
          ADD       ONE,TOTDSC                 * deceased
          PACK      KEY16,FSDATE,SP8
          CALL      RDSDSCH2
PRAD6500  CALL      RDKDSCH2
          BRANCH    OVRCD,PRAD7000
.
          MATCH     DURNO,XURNO                * correct patient?
          GOTO      PRAD6500 IF NOT EQUAL
.
          IF        PTCNDRSM=1
            PACK      KEY5,CODED,DSTAT
            CALL      RDCODE1
            MOVE      DSTAT,XTYPE
          ELSE
            PACK      KEY5,CODEDD,DDEST
            CALL      RDCODE1
            MOVE      DDEST,XTYPE
          ENDIF
          MATCH     "D",TCINDC1                * indicator 1 = D
          IF        @EQUAL
            ADD       ONE,DEATHS
            SUB       ONE,TOTDSC
          ENDIF
.
PRAD7000  COMPARE   "55",CLNO
          CALL      HEAD80 IF NOT LESS
.
          DISPLAY    *P18:24,*V2LON,XURNO;
.
          MATCH     "2",XARRDEP              * print departures
          GOTO      PRAD6000 IF NOT EQUAL
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,HUND10;*249,PTCNDAUR
          MATCH     "1",PTCNDAUR
          GOTO      PRAD9500 IF NOT EQUAL
          MOVE      XURNO,PURNO
          PACK      KEY20,SP70
          PROC      GETALIDS
          MATCH     SP70,KEY20
          IF        !@EQUAL
            UNPACK    KEY20,DIM11,DIM9
            PRINT     *1,DIM9; 
            GOTO      PRAD9600
          ENDIF
.
PRAD9500  PRINT     *1,XURNO;
.
PRAD9600  PRINT     *12,XNAME,*52,XSEX,*56,XAGE,*61,XCLASS:
                    *68,XUNIT,*74,XTYPE
          ADD       ONE,CLNO
          ADD       ONE,TOTDEP
.
          MATCH     "TRANS. OUT",XTYPE
          IF        @EQUAL
            ADD       ONE,TOTTROT        * add to transfer out
          ENDIF
.
          MATCH     TYPEL,XTYPE
          IF        @EQUAL
            ADD       ONE,TOTTOLV         * add to total to leave
          ENDIF
.
          GOTO      PRAD6000
PRAD9000  PRINT     *N,*N,"Total Departures = ",TOTDEP
.
PRAD9999  RETURN
+
.**************************************************************************
.*                               PRSU0000              Called by: ML0000  *
.*             Print summary for Ward NNN                                 *
.**************************************************************************
.
PRSU0000  DISPLAY   *P1:24,*EL,"Printing Report:";
.
          PACK      KEY6,CWARD,SP6           * dont print inactive ward        
          CALL      RDWARD1                                                    
          COMPARE   ZERO,WACTIVE                                               
          IF        !@EQUAL                                                    
            GOTO      PRIM9999                                                 
          ENDIF                                                                
.
          UNPACK    PREDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          ASSIGN    (WOCCBED-TOTINP),VACBEDS     
.
          COMPARE   "50",CLNO
          IF        @LESS
            GOTO      PRSU1000
          ELSE
            CLOCK     TIME,CTIMEIS
            CALL      HEAD80
          ENDIF
.
PRSU1000  PRINT     *N,*N,*25,"Summary for ",PTWDSDES," - ",CENDATE
          PRINT     *N,*N
.
          DISPLAY    *P18:24,*V2LON,CWARD;
.
          PRINT     *1,"Total Inpatients at Midnight = ",TOTINP:
                    *37,"Admissions     = ",TOTADM:
                    *59,"Discharges       = ",TOTDSC:
                    *N,*1,"Total Inpatients on Leave    = ",TOTLEV:
                    *37,"Transfer In    = ",TOTTRIN:
                    *59,"Transfer Out     = ",TOTTROT:
                    *N,*1,"Number of Beds in ",CWARD,"        = ",WOCCBED:
                    *37,"From Leave     = ",TOTFRLV:
                    *59,"To Leave         = ",TOTTOLV:
                    *N,*1,"Number of Vacant Beds        = ",VACBEDS:
                    *59,"Deaths           = ",DEATHS:
                    *N,*1,"Total Boarders               = ",TOTBRD:
                    *37,"Total Arrivals = ",TOTARR:
                    *59,"Total Departures = ",TOTDEP:
                    *N,*N,*1,"Total Inpatients at Midnight    ",CPCDATE," = ":
                    PTOTINP:
                    *N,*1,"Plus Total Arrivals             ",CENDATE," = ":
                    TOTARR:
                    *N,*1,"Less Total Departures           ",CENDATE," = ":
                    TOTDEP:
                    *N,*1,"Total Inpatients at Midnight    ",CENDATE," = ":
                    TOTINP
.
          PRINT     *N,*N,*N,*N,*1,"DECLARATION:   I, .......................,":
                    " hereby state that the above Bed Return":
                    *N,*16,"is a true and accurate statement on the above date":
                    " for Ward ",CWARD,".":
                    *N,*N,*N,*16,"Signed:                        . Date:   ":
                    "    Time: ":
                    *N,"(This declaration should be signed by person ":
                    "responsible)."
.
          MOVE      ZERO,PTOTINP
.
PRSU9999  RETURN
+
.
. TEMP FILE I/O
.
RDSSECT1  READ      SECTION1,KEY10;;
          RETURN
.
RDKSECT1  MOVE      ZERO,OVRCD
          READKS    SECTION1;DXUNIQ1,XURNO,XNAME,XSEX,XAGE,XCLASS:
                             XWARD,XBED,XLEAVE,XSTBY,XADMN
          GOTO      OVERCOND IF OVER
          MOVE      DXUNIQ1,XUNIQ1
          RETURN
.
.
WRSECT1   MOVE      XUNIQ1,DXUNIQ1
          WRITE     SECTION1,KEY10;DXUNIQ1,XURNO,XNAME,XSEX,XAGE,XCLASS:
                    XWARD,XBED,XLEAVE,XSTBY,XADMN
          ADD       ONE,XUNIQ1
          ADD       ONE,SEQ1
          RETURN
.
RDSSECT2  READ      SECTION2,KEY10;;
          RETURN
.
RDKSECT2  MOVE      ZERO,OVRCD
          READKS    SECTION2;DXUNIQ,XURNO,XNAME,XSEX,XAGE,XCLASS,XUNIT:
                             XARRDEP,XTYPE,XWARD,XBED,XADMN
          GOTO      OVERCOND IF OVER
          MOVE      DXUNIQ,XUNIQ
          RETURN
.
WRSECT2   MOVE      XUNIQ,DXUNIQ
          WRITE     SECTION2,KEY10;DXUNIQ,XURNO,XNAME,XSEX,XAGE,XCLASS,XUNIT:
                                 XARRDEP,XTYPE,XWARD,XBED,XADMN
          ADD       ONE,XUNIQ
          ADD       ONE,SEQ
          RETURN
.
.
RDSSECT3  READ      SECTION3,KEY3;;
          RETURN
.
RDKSECT3  MOVE      ZERO,OVRCD
          READKS    SECTION3;DCOUNT,XURNO,XLEAVE,XWARD,XADMN
          GOTO      OVERCOND IF OVER
          MOVE      DCOUNT,COUNT
          RETURN
.
WRSECT3   MOVE      COUNT,DCOUNT
          WRITE     SECTION3,KEY3;DCOUNT,XURNO,XLEAVE,XWARD,XADMN
          ADD       ONE,COUNT
          RETURN
.
.**************************************************************************
.*                               PBRD0000              Called by: ML0000  *
.*                       Print hospital boarders                          *
.**************************************************************************
.
PBRD0000  DISPLAY   *P1:24,*EL,"Printing Report:";
.
          PACK      KEY6,CWARD,SP6           * dont print inactive ward
          CALL      RDWARD1
          COMPARE   ZERO,WACTIVE
          IF        !@EQUAL
            GOTO      PBRD9999
          ENDIF
.
          MOVE      ZERO,TOTBRD              * clear total boarders
          CLOCK     TIME,CTIMEIS
          MOVE      ZERO,CLNO
          CALL      HEAD80
          UNPACK    SDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                  * display heading
          MOVE      CPCDATE,CENDATE
          PRINT     *N,*35,"BOARDERS",*N:
                    *N,*1,"U/R Number",*12,"Patient Name",*44,"Sex":
                    *49,"Age  Class  Ward/Bed ":
                    *N,*1,"----------",*12,"------------",*44,"---":
                    *49,"---  -----  --------------------"
.
          MOVE      NINE,CLNO
          PACK      KEY10,SP10
          CALL      RDSSECT1
PBRD1000  CALL      RDKSECT1
          BRANCH    OVRCD,PBRD9000
.
          MATCH     CWARD,XWARD
          GOTO      PBRD1000 IF NOT EQUAL
.
          PACK      KEY26,XADMN,SP70
          CALL      RSPTBRD3
PBRD2000  CALL      RKPTBRD3
          BRANCH    OVRCD,PBRD1000 
.
          MATCH     XADMN,PTBRVISN
          GOTO      PBRD1000 IF NOT EQUAL
.
          MATCH     SP70,PTBRSDAT
          IF        !@EQUAL & !@EOS
            MATCH     PTBRSDAT,SDATE
            GOTO      PBRD2000 IF LESS
          ENDIF
.
          MATCH     SP70,PTBREDAT
          IF        !@EQUAL & !@EOS
            MATCH     SDATE,PTBREDAT
            GOTO      PBRD2000 IF LESS
          ENDIF
.
          MOVE      PTBRSNAM,PACSNAME            * set up patients name
          MOVE      PTBRGNAM,PACGNAME
          MOVE      SP70,PACTITLE
          MOVE      TWO, PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,XNAME
.
          MOVE      PTBRGEND,XSEX
.
          CALL      IBACLOCK
          PACK      AGEDATE,CCC,CYY,CMM,CDD
          MOVE      PTBRBDAT,PBDATE
          CALL      CALCAGE 
          MOVE      PSAGEYRS,XAGE
.
          DISPLAY   *P18:24,*V2LON,PTBRLURN;
.
          PRINT     *1,PTBRLURN;
          PRINT     *12,XNAME,*44,XSEX,*49,XAGE,*54,XCLASS:
                    *61,PTWDSDES,"/",XBED,STANDBY
          MOVE      SP3,STANDBY
          ADD       ONE,CLNO
          ADD       ONE,TOTBRD
          COMPARE   "55",CLNO
          CALL      HEAD80 IF NOT LESS
          GOTO      PBRD2000
.
PBRD9000  PACK      KEY10,SP10
          CALL      RDSSECT2
PBRD9100  CALL      RDKSECT2
          BRANCH    OVRCD,PBRD9990
.
          MATCH     CWARD,XWARD
          GOTO      PBRD9100 IF NOT EQUAL
.
          PACK      KEY26,XADMN,SP70
          CALL      RSPTBRD3
PBRD9200  CALL      RKPTBRD3
          BRANCH    OVRCD,PBRD9100
.
          MATCH     XADMN,PTBRVISN
          GOTO      PBRD9100 IF NOT EQUAL
.
          MATCH     SP70,PTBRSDAT
          IF        !@EQUAL & !@EOS
            MATCH     PTBRSDAT,SDATE
            GOTO      PBRD9200 IF LESS
          ENDIF
.
          MATCH     SP70,PTBREDAT
          IF        !@EQUAL & !@EOS
            MATCH     SDATE,PTBREDAT
            GOTO      PBRD9200 IF LESS
          ENDIF
.
          MOVE      PTBRSNAM,PACSNAME            * set up patients name
          MOVE      PTBRGNAM,PACGNAME
          MOVE      SP70,PACTITLE
          MOVE      TWO, PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,XNAME
.
          MOVE      PTBRGEND,XSEX
.
          CALL      IBACLOCK
          PACK      AGEDATE,CCC,CYY,CMM,CDD
          MOVE      PTBRBDAT,PBDATE
          CALL      CALCAGE
          MOVE      PSAGEYRS,XAGE
.
          DISPLAY   *P18:24,*V2LON,PTBRLURN;
.
          PRINT     *1,PTBRLURN;
          PRINT     *12,XNAME,*44,XSEX,*49,XAGE,*54,XCLASS:
                    *61,PTWDSDES,"/",XBED,STANDBY
          MOVE      SP3,STANDBY
          ADD       ONE,CLNO
          ADD       ONE,TOTBRD
          COMPARE   "55",CLNO
          CALL      HEAD80 IF NOT LESS
          GOTO      PBRD9200 
.
PBRD9990  PRINT      *N,"Total Boarders = ",TOTBRD
          GOTO       PBRD9999
.
PBRD9999  RETURN
+
.
.*****************************************************************************
.*  GETALTID
.*  Procedure to get an alternate ID
.*  Exit=0  No alternate ID
.*       1  Alternate ID's exist
.*****************************************************************************
          DEFPROC   GETALIDS
.
          INC       PMSAIDFD/INC
.
          MOVE      ZERO,EXIT
          PACK      KEY20,SP70
          OPEN      PMSAIDA1,"pmsaidaf"
.
          PACK      KEY31,PURNO,SP70
          CALL      RSPMAID1           * Read alternate id Table
GETALI10  CALL      RKPMAID1
          BRANCH    OVRCD,GETALI99
.
          MATCH     PURNO,PMAIURNO     * Check same UR
          GOTO      GETALI99 IF NOT EQUAL
.
          MATCH     "1",PMAIDISU
          GOTO      GETALI10 IF NOT EQUAL
.
          PACK      KEY20,PMAIALID,SP70
          MOVE      ONE,EXIT
          GOTO      GETALI99
.
          INC       PMSAIDIO/INC
          INC       IBAOVRIO/INC
.
GETALI99  CLOSE     PMSAIDA1
.
          ENDPROC
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PATWRDKY     
          INC       PATHSPKY     
          INC       CALCAGE
          INC       TFILENAM 
.
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC
          INC       PATBRDIO/INC        * Hospital Boarders
          INC       PATCODIO/INC        * codes file
          INC       PATDSCIO/INC        * discharge file
          INC       PATHSPIO/INC        * hospital file
          INC       PATMA1IO/INC        * patient master file
          INC       PATMI1IO/INC        * admission file
          INC       PMSVX1IO/INC        * admission file
          INC       PATTRNIO/INC        * patient tran file
          INC       PATWR1IO/INC        * ward file


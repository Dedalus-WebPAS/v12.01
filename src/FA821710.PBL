.----------------------------------------------------------------------------
. System    :   Patient Management System                                    
. Program   :   FA821710                                                     
. Desc      :   Inpatient/Outpatient Comments Conversion                     
.----------------------------------------------------------------------------
. Date      :   02/10/2017                                                   
. Author    :   Thanh T.                                                     
. Function  :   This program reads all the data in the U/R comment table
.               patcomaf and insert into new U/R comment header and details 
.               pmsuchaf/pmsucdaf tables with the note type of "001".       
.----------------------------------------------------------------------------
. V10.12.03 :   28/06/2018  Thanh T        TSK 0821710
.               Changed to use the current date/time for creation date/time
.               when the visit comment header vismdtaf record is created.
. V10.12.02 :   22/06/2018    Thanh T            TSK 0859209
.               Added line to OPEN the CONTROLF
. V10.12.01 :   12/01/2018    Steve Armstrong    TSK 0821710
.               Fixed display of file names.
.               Removed record display during processing to reduce time.
.               Fixed IF statements to reduce time.
.               Fixed invalid user id message.
.----------------------------------------------------------------------------
. V10.11.00 :   02/06/2017    Thanh T.           TSK 0821710                 
.               Initial Version                                              
.----------------------------------------------------------------------------
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PMSUCHFD/INC
          INC       PMSUCDFD/INC
          INC       PATCOMFD/INC
          INC       WEBSECFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
FORM6     FORM      6
NOTETYPE  INIT      "001"
CUSERID   DIM       10
SAVEURNO  DIM       8
NOTENUMB  FORM      6
PCLNUM    FORM      3
CRTDATE   DIM       8
CRTTIME   DIM       8
NOFPCMR   FORM      6
NOFCMUH   FORM      6
NOFCMUC   FORM      6
.
.---------------------------------------------------------------------
PRGID     INIT      "FA821710"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "U/R Comments Conversion"
.---------------------------------------------------------------------
. Controlling Logic (Mainline code)                 
.---------------------------------------------------------------------
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000         * proceed with clean up
.
MAIN1000  CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN2000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN2000  CALL      INPA0000               * process inpatient
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.------------------------------------------------------------------------
.  The initialisation routine is used to display headings and open files. 
.------------------------------------------------------------------------
.
INIT0000  CALL      DISPHEAD                * display heading
.
          OPEN      CONTROLF,"controlf"
          OPEN      PMSUCHA1,"pmsuchaf"
          OPEN      PMSUCDA1,"pmsucdaf"
          OPEN      PATCOMA1,"patcomaf"
          OPEN      WEBSECA1,"websecaf"
.
          CALL      IBACLOCK
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          MOVE      KEY8,CRTDATE
          MOVE      CTIMEIS,CRTTIME
.
          MOVE      ZERO,NOFPCMR
          MOVE      ZERO,NOFCMUH
          MOVE      ZERO,NOFCMUC
.
INIT9999  RETURN
+
.---------------------------------------------------------------------
. get user options or exit                        
. Returns:  EXIT    = FALSE (0)  Run clean up                           
.                     TRUE  (1)  Exit option selected                  
.---------------------------------------------------------------------
.
OPTN0000  DISPLAY   *P1:3,*EF
.
OPTN0100  MOVE      SP70,CUSERID 
          KEYIN     *P1:4,*EL,*V2LON,"User Id : ",*HOFF:
                    *P11:4,CUSERID
          DISPLAY   *P1:24,*EL
.
          SQUEEZE   CUSERID
          GOTO      OPTN8000 IF EOS
. 
          PACK      KEY10,CUSERID,SP70
          CALL      RDWBSE1
          IF        OVRCD = 1
            DISPLAY   *P1:24,*V2LON,"User Id not on file",*HOFF 
            GOTO      OPTN0100
          ENDIF
. 
OPTN0200  DISPLAY   *P1:6,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:7,*V2LON,ONE,*HOFF,". U/R Comments"
.
OPTN0300  KEYIN     *P1:10,*EL,"Select Option : ":
                    *P17:10,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION            * exit selected ?
          GOTO      OPTN8000 IF EQUAL       * yes
.
          BRANCH    COPTION,OPTN1000
.
          BEEP
          GOTO      OPTN0300
.
OPTN1000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN8000  MOVE      ONE,EXIT
          GOTO      OPTN9999
.
OPTN9999  RETURN
+
.---------------------------------------------------------------------
. Processs copying U/R comments
.---------------------------------------------------------------------
INPA0000  CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:13,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:14,"Processing in progress.  ":
                    "Please wait until complete";
.
          MOVE      SP70,SAVEURNO 
          MOVE      ZERO,PCLNUM
.
          PACK      KEY10,SP70
          CALL      RSPTCOM1                * start Reading patcomaf
INPA1000  CALL      RKPTCOM1                * Next patient Comments
          BRANCH    OVRCD,INPA9200
.
          ADD       ONE,NOFPCMR
.      
          MATCH     SP70,PTCOURNO
          GOTO      INPA1000 IF EQUAL
.      
          MATCH     SP70,PTCOLINE
          GOTO      INPA1000 IF EQUAL
.      
          MATCH     SAVEURNO,PTCOURNO
          GOTO      INPA3000 IF EQUAL
.
          MOVE      PTCOURNO,SAVEURNO
          MOVE      ZERO,PCLNUM
.
. Create UR comment header
.
          CALL      NXTNOT00              * allocate the next note number
          CALL      CLRUCH00
.
INPA2200  MOVE      PTCOURNO,PMUHURNO
          MOVE      NOTETYPE,PMUHCTYP
          MOVE      NOTENUMB,PMUHCNUM
          MOVE      CUSERID,PMUHCUID
.
          MOVE      CRTDATE,PMUHCDAT
          MOVE      CRTTIME,PMUHCTIM     
.
          PACK      KEY17,PMUHURNO,PMUHCTYP,PMUHCNUM,SP70 
          CALL      WRPMUCH1                 * create comment header
          ADD       ONE,NOFCMUH              * Update record counter 
.
.         IF        (NOFCMUH % 1000) = 0
.           DISPLAY   *P1:14,*EL,"Record No. :":
.                     *P15:14,*V2LON,NOFCMUH
.         ENDIF
.
. Create UR Comments Text
.
INPA3000  MOVE      PTCOURNO,PMUCURNO 
          MOVE      NOTETYPE,PMUCCTTY
          MOVE      NOTENUMB,PMUCCNUM
          ADD       ONE,PCLNUM
          MOVE      PCLNUM,PMUCLNUM

          PACK      KEY20,PMUCURNO,PMUCCTTY,PMUCCNUM,PMUCLNUM,SP70
          CALL      RDPMUCD1
          IF        OVRCD = 1
            MOVE      PTCOLINE,PMUCCOMM
            CALL      WRPMUCD1               * create comment text       
            ADD       ONE,NOFCMUC
          ENDIF
.
          GOTO      INPA1000
.
INPA9000  PRINT     *1,"Comments from patcomaf not written ":
                    " for U/R: ",PTCOURNO
          GOTO      INPA1000
.
INPA9200  CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:16,*V2LON,"Number of patcomaf read    : ":
                    NOFPCMR,*HOFF
          DISPLAY   *P1:17,*V2LON,"Number of pmsuchaf created : ":
                    NOFCMUH,*HOFF
          DISPLAY   *P1:18,*V2LON,"Number of pmsucdaf created : ":
                    NOFCMUC,*HOFF
          DISPLAY   *P1:20,"Ended  : ",CDATE,SP1,CTIMEIS:
                    *P1:24,*EL;
          CALL      HITENTER
.
INPA9999  RETURN
.
.---------------------------------------------------------------------
. Clear the file variables - pmsuchaf                                    
.---------------------------------------------------------------------
CLRUCH00  MOVE      SP70,PMUHURNO
          MOVE      SP70,PMUHCTYP
          MOVE      SP70,PMUHCNUM
          MOVE      SP70,PMUHCUID
          MOVE      SP70,PMUHCDAT
          MOVE      SP70,PMUHCTIM
          MOVE      SP70,PMUHOCCG
          MOVE      ZERO,PMUHDELT
          MOVE      SP70,PMUHDUID
          MOVE      SP70,PMUHDDAT
          MOVE      SP70,PMUHDTIM
          MOVE      SP70,PMUHDOCG
          MOVE      SP70,PMUHSPAR
.
CLRUCH99  RETURN

.---------------------------------------------------------------------
. get the next maximum note number for an UR number and type.  
.---------------------------------------------------------------------
NXTNOT00  MOVE      ONE,NOTENUMB
          PACK      KEY17,PTCOURNO,NOTETYPE,Z70
          CALL      RSPMUCH1                * header record exists?
          CALL      RPPMUCH1
          BRANCH    OVRCD,NXTNOT99
.
          MATCH     PTCOURNO,PMUHURNO
          GOTO      NXTNOT99 IF NOT EQUAL
.
          MATCH     NOTETYPE,PMUHCTYP
          GOTO      NXTNOT99 IF NOT EQUAL
.
          MOVE      ZERO,FORM6
          MOVE      PMUHCNUM,FORM6
          ADD       ONE,FORM6
          MOVE      FORM6,NOTENUMB
.
NXTNOT99  RETURN
.
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PMSUCHIO/INC
          INC       PMSUCDIO/INC
          INC       PATCOMIO/INC
          INC       WEBSECIO/INC

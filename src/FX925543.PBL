.***************************************************************************
.* System    :   Inpatients                                                *
.* Program   :   FX925543                                                  *
.* Desc      :   Write operation date to patecoaf                          *
.*                                                                         *
.* V11.02.00 07/10/2022 Jill Parkinson 0925543                             *
.*           Created program                                               *
.***************************************************************************
.
          INC       STD001FD
          INC       PATCONFD/INC
          INC       OPRDEAFD/INC
          INC       PATECOFD/INC
          INC       PATMI1FD/INC
          INC       PATICOFD/INC
          INC       PMSVX1FD/INC
          INC       PATDSCFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
COUNT     FORM      8
PRNTDATE  DIM       10
MRCNOPDT  DIM       1
.
PRGID     INIT      "FX925543"
PRGNAM    INIT      "Write operation date to patecoaf if proc date required"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      OPRDETA2,"oprdetaf"
          OPEN      PATECOA1,"patecoaf"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      PATDSCH2,"patdschf"
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,NINTY8;*149,PTCNGDV2
          READ      CONTROLF,TEN;*105,PTCNU50G                         *I-50344
          READ      CONTROLF,HUND05;*104,PTCNDGET,*147,CMDISM,*158,PTCNDGPV:
                                    *173,PTCNICUT:
                                    *217,PTCNGDV4,*227,PTCNGDV5       *C-105244
          READ      CONTROLF,HUND09;*151,PTCNGDV3

          READ      CONTROLF,NINTY7;*173,MRCNOPDT
          CALL      OPICO1
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Update Operation Date":
                    *P1:6,*V2LON,TWO,*HOFF,". Print Required Only - no update"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000,OPTN9000    * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  MOVE      ZERO,COUNT
          CALL      HEAD0000
          MOVE      "20220701",KEY8
          PACK      KEY16,KEY8,SP70
          CALL      RDSDSCH2
PROC1000  CALL      RDKDSCH2
          BRANCH    OVRCD,PROC9000
.
          PACK      KEY13,DDADMNO,SP70
          CALL      RSPTECO1
PROC2000  CALL      RKPTECO1
          BRANCH    OVRCD,PROC1000
.
          MATCH     DDADMNO,DPTEOADM
          GOTO      PROC1000 IF NOT EQUAL
.
          PACK      KEY9,PTEOCODE,SP10
          CALL      RDPTICO1
          BRANCH    OVRCD,PROC2000
.
          MATCH     SP8,PTEODATE
          GOTO      PROC2000 IF NOT EQUAL
.
          MATCH     "1",PT0ODTMN               * PT0ODTMN 1=not Mandatory
          IF        @EQUAL
            GOTO      PROC2000
          ENDIF
.
          ADD       ONE,COUNT
          IF        (COUNT%1000)=0
            DISPLAY   *P14:15,*V2LON,PTEOADMN:
                      *P14:16,PTEOEPNO:
                      *P14:17,PTEOCNT:
                      *P14:18,COUNT;
          ENDIF
.
          PACK      KEY31,PTEOADMN,SP70
          CALL      RSOPDEA2
PROC3000  CALL      RKOPDEA2
          BRANCH    OVRCD,PROC4000
.
          MATCH     OPDAADMN,DPTEOADM
          GOTO      PROC4000 IF NOT EQUAL
.
          COMPARE   FOUR,OPDASTAT
          GOTO      PROC3000 IF NOT EQUAL
.
          PACK      PTEODATE,OPDADATE,SP70
          PACK      PTEOSTTM,OPDATIME,SP70
          PACK      PTEOEDTM,OPDATIME,SP70
.
          MATCH     SP70,OPDAPOS1
          IF        !@EQUAL
            PACK      PTEOEDTM,OPDAPOS1,SP70
          ENDIF
.
          MATCH     SP70,OPDAPOS2
          IF        !@EQUAL
            PACK      PTEOEDTM,OPDAPOS2,SP70
          ENDIF
.
          MATCH     SP70,OPDAPOS3
          IF        !@EQUAL
            PACK      PTEOEDTM,OPDAPOS3,SP70
          ENDIF
.
          MATCH     SP70,OPDAPOS4
          IF        !@EQUAL
            PACK      PTEOEDTM,OPDAPOS4,SP70
          ENDIF
.
          MATCH     PTEOSTTM,PTEOEDTM
          IF        @LESS
            PACK      PTEOEDTM,PTEOSTTM
          ENDIF
          GOTO      PROC5000
.
PROC4000  MOVE      DDATE,PTEODATE
          MOVE      DTIME,PTEOTIME
          PACK      KEY8,DDADMNO,SP70
          CALL      RDMISS1
          BRANCH    OVRCD,PROC2000
.
          MATCH     "1",MRCNOPDT          * change default to Admiss.Date ?
          IF        @EQUAL
            MOVE      ADATE,PTEODATE
            MOVE      ATIME,PTEOTIME
            GOTO      PROC5000   
          ENDIF
          MATCH     "2",MRCNOPDT          * change default to Admiss.Date ?
          IF        @EQUAL
            MOVE      ADATE,PTEODATE
            MOVE      ATIME,PTEOTIME
          ENDIF

PROC5000  IF        COPTION=1
            CALL      UPPTECO1
          ENDIF
          CALL      PRNT0000
          GOTO      PROC2000
.
PROC9000  PRINT     *N,"End of Report"
          DISPLAY   *P1:24,"Finished";
          CALL      HITENTER
.
PROC9999  RETURN
+
.*********************************************************************
. Print heading
.*********************************************************************
HEAD0000  CALL      IBACLOCK
          CALL      HEAD80
.
          ADD       ONE,CLNO
.
          CALL      UND80
.
          PRINT     *1,"| Disc. Date ",*14,"| Visit ":
                    *25,"| U/R",*35,"| Proc",*49,"| Op date":
                    *61,"| St Time",*70,"| End Time",*80,"|"
.
          ADD       ONE,CLNO
.
          CALL      UND80
.
HEAD9999  RETURN
+
.*********************************************************************
. Print line
.*********************************************************************
PRNT0000  UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,PRNTDATE         * formatted discharge date
.
          UNPACK    PTEODATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,CPCDATE         * formatted op date
.
          PRINT     *1,"| ",PRNTDATE,*14,"| ",DADMNO:
                    *25,"| ",DURNO,*35,"|",PTEOCODE:
                    *49,"| ",CPCDATE,*61,"| ":
                    PTEOSTTM,*70,"| ",PTEOEDTM,*80,"|"
PRNT9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       OPRDEAIO/INC
          INC       PATECOIO/INC
          INC       PATMI1IO/INC
          INC       PATICOIO/INC
          INC       PMSVX1IO/INC
          INC       PATDSCIO/INC

.*****************************************************************************
.* System    :   Allied Health                                               *
.* Program   :   FX305679                                                    *
.* Desc      :   Fixit to update the status of VINAH extract batch records   *
.*****************************************************************************
.* Date      :   10/09/2014                                                  *
.* Author    :   Steve Armstrong                                             *
.* Function  :   This program will prompt the user for a batch number and    *
.*               then loop through the batch records in allhdtaf and reset   *
.*               the status from accepted to invalid.                        *
.*               The program is intended for use where DoH have rejected the *
.*               messages for this batch, however, the records were set to   *
.*               accepted from within webPAS.  This will then get around any *
.*               E052 and E061 validation errors when subsequent messages    *
.*               are sent for the same patient and/or visit.                 *
.*               It works on the assumption that a single rejected record    *
.*               within a batch will result in all records in the batch      *
.*               being rejected by DoH.                                      *
.* Mods      :                                                               *
.*        V10.14.01 17/04/2019  Steve Armstrong  TSK 0868837                 *
.*                  Recompiled for changes to ALLHDTFD.                      *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       ALLHBTFD/INC
          INC       ALLHDTFD/INC
.
.
. LOCAL VARIABLE DEFINITION
. -------------------------
BATCHNUM  DIM       20
RECCOUNT  FORM      3
.
.
PRGID     INIT      "FX305679"
PRGNAM    INIT      "Fixit for Rejected batches with a status of Accepted"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000       * proceed with fixit
.
MAIN1000  CALL      GBAT0000               * get batch number
          BRANCH    EXIT,MAIN0100          * nothing entered
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN2000:        * yes
                          MAIN1000:        * no
                          MAIN0100         * cancel
.
MAIN2000  CALL      PROC0000               * process batch records
          GOTO      MAIN1000
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"allhdtaf";
          OPEN      ALLHDTA2,"allhdtaf"
.
          DISPLAY   *P64:24,"allhbtaf";
          OPEN      ALLHBTA2,"allhbtaf"
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run fixit                               *
.*                        TRUE  (1)  Exit option selected                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               GBAT0000              Called by: MAIN0000   *
.*               Get the batch number to update                              *
.* Returns: BATCHNUM - selected batch number                                 *
.*          EXIT  0 = Ok to continue                                         *
.*                1 = nothing entered                                        *
.*****************************************************************************
.
GBAT0000  KEYIN     *P1:10,"Batch Number:":
                    *P15:10,*V2LON,*JR,BATCHNUM
.
          PACK      BATCHNUM,BATCHNUM,SP70
.
          MATCH     BATCHNUM,SP70                * anything entered ?
          GOTO      GBAT9100 IF EQUAL            * no - finished
.
          PACK      KEY40,BATCHNUM,SP70
          CALL      RSALHBT2                     * position in file
          CALL      RKALHBT2                     * read next record
          BRANCH    OVRCD,GBAT8000               * eof - error
.
          MATCH     BATCHNUM,ALHBBNUM            * same batch still ?
          GOTO      GBAT8000 IF NOT EQUAL        * no - error
.
          GOTO      GBAT9000
.
GBAT8000  DISPLAY   *P1:24,*EL,*B,"Batch not on file.  ";
          CALL      HITENTER
          GOTO      GBAT0000
.
GBAT9000  MOVE      ZERO,EXIT
          GOTO      GBAT9999
.
GBAT9100  MOVE      ONE,EXIT
.
GBAT9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.* Requires:  BATCHNUM - selected batch number                               *
.*      Loop through the records for a given batch and reset the status of   *
.*      any accepted records from "10" (Accepted) to "99" (not valid)        *
.*****************************************************************************
.
PROC0000  DISPLAY   *P1:24,*EL,"Processing:";
          MOVE      ZERO,RECCOUNT                * initialise record counter
.
PROC0500  PACK      KEY62,BATCHNUM,TEN,SP70
          CALL      RSALHDT2                     * position for batch
          CALL      RKALHDT2                     * read next record
          BRANCH    OVRCD,PROC9000               * eof - finished
.
          MATCH     BATCHNUM,ALHDBNUM            * same batch still ?
          GOTO      PROC9000 IF NOT EQUAL        * no - finished
.
          MATCH     "10",ALHDSTAT                * accepted ?
          GOTO      PROC9000 IF NOT EQUAL        * no - finished
.
          DISPLAY   *P13:24,*EL,*V2LON,ALHDMESI;
.
          MOVE      "99",ALHDSTAT                * update status
          CALL      UPALHDT2
          ADD       ONE,RECCOUNT                 * increment record counter
          GOTO      PROC0500                     * get next record
.
PROC9000  DISPLAY   *P1:24,*EL,*V2LON,RECCOUNT,*HOFF," records updated.  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       ALLHBTIO/INC
          INC       ALLHDTIO/INC

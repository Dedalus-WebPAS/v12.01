.******************************************************************************
.* System         : Accident and Emergency                                    *
.* Program        : IBAAAE70.PBL                                              *
.* Name           : Analysis of Time Elapsed prior to Attendance              *
.******************************************************************************
.* Date           : 23/09/1993                                                *
.* Author         : Justin 'The Pom' Coates                                   *
.* Function       : To show the number of attendance and how long after the   *
.*                  accident they attended                                    *
.* Modifications  : 7/10/94  WHirlpool                                        *
.*                  Changed to ignore if things are not set-up !!             *
.*                  Therefore we don't get a total when everything is zero    *
.******************************************************************************
.
          INC       STD001FD
          INC       AAEDE1FD/INC
          INC       PATCODFD/INC
.
.         program variables
.
ATTEND    FORM      6[7]      * number attended
CDFRDATE  DIM       8
CDTODATE  DIM       8
PRFRDATE  DIM       10
PRTODATE  DIM       10
TODAY     DIM       8
.
.         program constants
.
CATAE     INIT      "AE"
.
PRGID     INIT      "IBAAAE70"
PRGNAM    INIT      "Analysis Of Time Elapsed Prior To Attendance"
VERSION   INIT      "V12.01.00"
.
.*********************************************************************
.         mainline code
.*********************************************************************
ML0000    CALL      INIT0000
.
ML1000    CALL      OPTN0000                * run report ??
          BRANCH    EXIT,ML9999             * exit
.
          CALL      KDAT0000                * enter date range
          BRANCH    EXIT,ML1000             * nothing entered
.
          CALL      GDAT0000                * get the data
          CALL      REPT0000                * produce report
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.*********************************************************************
.         initilisation
.*********************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P58:24,"aaede1af"
          OPEN      AAEDE1A3,"aaede1af"
          DISPLAY   *P58:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
          PACK      TODAY,CCC,CYY,CMM,CDD
          REP       " 0",TODAY
.
INIT9999  RETURN
+
.*********************************************************************
.         run the option ??
.*********************************************************************
OPTN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Run report":
                    *P1:7,"Select option : ";
OPTN1000  KEYIN     *P17:7,*V2LON,MOPTION
          MOVE      ONE,EXIT
          COMPARE   ZERO,MOPTION
          GOTO      OPTN9999 IF EQUAL
          MOVE      ZERO,EXIT
          BRANCH    MOPTION,OPTN9999
          BEEP
          GOTO      OPTN1000
OPTN9999  RETURN
+
.*********************************************************************
.*        Enter the date ranges                                      *
.*        Returns : EXIT = 1      exit chosen                        * 
.*                  CDFRDATE      starting date  ccyymmdd            *
.*                  CDTODATE      ending date    ccyymmdd            *
.*                  PRFRDATE      starting date  dd/mm/ccyy          *
.*                  PRTODATE      ending date    dd/mm/ccyy          *
.*********************************************************************
KDAT0000  DISPLAY   *P1:09,"From date : ",*EL:
                    *P1:10,"To   date : ",*EL;
          MOVE      ONE,CCANLDTE            * blank cancel default
          MOVE      ONE,CDEFDTE             * enter once for default
          MOVE      ZERO,CHIGHLT            * use highlight
.
.         enter starting date
.
KDAT1000  MOVE      "09",CVERT              * row
          MOVE      "13",CCOL               * column
          UNPACK    SP6,CYEAR,CMON,CDAY     * no defaults
          MOVE      CCC,CCENT
.
          CALL      KEYDATE
          BRANCH    OVRCD,KDAT9000          * nothing entered
          BRANCH    CQUEST,KDAT1000
.
          PACK      CDFRDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",CDFRDATE           * set up start date
          CALL      PACDATE
          MOVE      CPCDATE,PRFRDATE         * formatted start date
.
          MATCH     CDFRDATE,TODAY
          GOTO      KDAT2000 IF NOT LESS    * date is <= current date
.
          DISPLAY   *P1:24,*B,"Date must not be after current date.  ",*EL;
          CALL      HITENTER 
          GOTO      KDAT1000
.
.         enter ending date (default to starting date)
.
KDAT2000  MOVE      "10",CVERT              * row
          MOVE      "13",CCOL               * column
          UNPACK    CDFRDATE,CCENT,CYEAR,CMON,CDAY
.
          CALL      KEYDATE
          BRANCH    OVRCD,KDAT1000          * nothing entered
          BRANCH    CQUEST,KDAT2000
.
          PACK      CDTODATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",CDTODATE           * set up end date
          CALL      PACDATE
          MOVE      CPCDATE,PRTODATE         * formatted end date
.
          MATCH     CDFRDATE,CDTODATE
          GOTO      KDAT3000 IF NOT LESS    * date is <= start date
.
          DISPLAY   *P1:24,*B,"Ending date must be after Starting date.  ",*EL;
          CALL      HITENTER 
          GOTO      KDAT2000
.
.         check end date is before current date
.
KDAT3000  MATCH     CDTODATE,TODAY
          GOTO      KDAT5000 IF NOT LESS    * date is <= current date
.
          DISPLAY   *P1:24,*B,"Date must not be after current date.  ",*EL;
          CALL      HITENTER 
          GOTO      KDAT2000
.
KDAT5000  CALL      CONTQST
          BRANCH    CEXIT,KDAT8000,KDAT0000,KDAT9000
.                         Yes      No       Cancel
.
KDAT8000  MOVE      ZERO,EXIT               * valid dates
          GOTO      KDAT9999
.
KDAT9000  MOVE      ONE,EXIT                * exit selected
.
KDAT9999  RETURN
+
.*********************************************************************
.         get the data for the report
.*********************************************************************
GDAT0000  DISPLAY   *P1:24,*EL,"Obtaining details..."
          CLOCK     TIME,CTIMEIS
          CALL      CLRV0000
          PACK      KEY21,CDFRDATE,SP20
          CALL      RDSDETA3
.
GDAT1000  CALL      RDKDETA3
          BRANCH    OVRCD,GDAT9999          * end of file
          MATCH     ADADATE,CDTODATE
          GOTO      GDAT9999 IF LESS        * after end date
.
          DISPLAY   *P41:24,ADADATE,ADANUMB
          MOVE      SP1,TCINDC1
          PACK      KEY5,CATAE,AEDATESI
          CALL      RDCODE1
          BRANCH    OVRCD,GDAT1000
          MOVE      ZERO,FORM1
          MOVE      TCINDC1,FORM1           * set the indicator
          
          IF        FORM1 <> ZERO
            ADD       ONE,ATTEND[FORM1]       * add to appropriate column
            ADD       ONE,ATTEND[7]           * add to total
          ENDIF
.
          GOTO      GDAT1000
.
GDAT9999  RETURN
+
.*********************************************************************
.         print the report
.*********************************************************************
REPT0000  DISPLAY   *P1:24,*EL,"Printing report..."
          MOVE      ZERO,CPAGENO
          CALL      HEAD80
          PRINT     *20,"Date range : ",PRFRDATE," to ",PRTODATE,*N
          CALL      UND80
          PRINT     *1," Less Than  Less Than  Less Than  Less Than  ":
                       "Less Than  More Than      Total"
          PRINT     *1,"   3 Hours    6 Hours   12 Hours   24 Hours  ":
                       " 48 Hours   48 Hours        "
          CALL      UND80
          MOVE      FIVE,CCOL
          MOVE      ONE,FORM2
          WHILE     FORM2 <= 7
            PRINT     *CCOL,ATTEND[FORM2];
            ADD       ONE,FORM2
            ADD       "11",CCOL
          DO
          PRINT     *N;
          CALL      UND80
          PRINT     *N,"*** End of Report ***",*N
.
REPT9999  RETURN
+
.*********************************************************************
.         clear the array
.*********************************************************************
CLRV0000  MOVE      ONE,FORM2
          WHILE     FORM2 <= 7
            MOVE      ZERO,ATTEND[FORM2]
            ADD       ONE,FORM2
          DO
CLRV9999  RETURN
+
          INC       STD001IO
          INC       AAEDE1IO/INC
          INC       PATCODIO/INC
.............................................................................

.-------------------------------------------------------
          INC       STDGENDF     * Required Include
.
VERSION   INIT      "V12.01.00"
.-------------------------------------------------------
. V9.04.01  13.04.2005 Peter Vela CAR 59696
.           Recompiled for SINSUAFD
. V9.02.00  03.05.2002  Glenn Saunders
.           Export to new release i.e. v9.02 from vf.09
.-------------------------------------------------------
. VF.09.B01 24.11.2000 Charles Handaya
.           Modification of Warehouse Security
.-------------------------------------------------------
. VF.01.03  18.07.2000 Charles Handaya
.           Recompiled for SINBACIO change
.           Related to Quote 9817
. VF.01.02  10.07.2000 Charles Handaya
.           Fix Total Stock on Order & Exp.Delivery Date
.           for combination of Catalog & Warehouse
. VF.01.01  24.05.2000 Charles Handaya
.           Add Warehouse Range Keyin
. VF.01.00  14.03.2000 Charles Handaya
.           Add in Standard Export File
.           Change to standard Range Keyin
.-------------------------------------------------------
. VF.00.01  22.12.1999 Charles Handaya
.           Recompiled for Fixing Audit Date (CCYYMMDD)
.-------------------------------------------------------
EXPORTFL  FILE      ASCII, FIXED=400     * Export File (Adjust Length to Suit)
EXPORTFN  DIM       8                    * Export File Name
TABDELM   EQU       0x09                 * Tab Delimiter
.
BACKQUAN  FORM      9.2
DELDATE   DIM       10
ENDWAR    DIM       5
LASTWAR   DIM       5
PREVDATE  DIM       8
REQDATE   DIM       10
SKEY30    DIM       30
STRWAR    DIM       5
.-------------------------------------------------------
.@@@@@ Start of Generated Code @@@@@
. NB - Code & Comments Included Before the above line 
.      will not be overwritten by the generation of a 
.      program. The STDGENDF file and VERSION must appear
.      before the line. 
.-----------------------------------------
.   Program       :   IBASIN90
.   Function      :   Backorder by Cost Centre Report         
.   Generated  On :   13/12/2005
.-----------------------------------------
.=========================
.File Description Includes
.-------------------------
          INC       SINBACFD/INC
          INC       SINCCAFD/INC
          INC       SINCIAFD/INC
          INC       SINCIEFD/INC
          INC       SINCONFD/INC
          INC       SINNATFD/INC
          INC       SINPOCFD/INC
          INC       SINSUAFD/INC
          INC       SINTMPDF/INC
          INC       SINWARFD/INC
          INC       SINWSEFD/INC
.=========================
.Local Variable Definition
.-------------------------
FROMCOST  DIM      46 * Scr 00 Fld     4
TOCOST    DIM      46 * Scr 00 Fld     5
EXPORTYN  FORM      1 * Scr 00 Fld     6
FROMWAR   DIM      46 * Scr 00 Fld     7
TOWAR     DIM      46 * Scr 00 Fld     8
.
ACCEPT    FORM      1
SACCEPT   FORM      1
.
PRGID     INIT      "IBASIN90"
PRGNAM    INIT      "Backorder by Cost Centre Report         "
.
.--------------------------------
. MAINLINE - Controlling Logic
.--------------------------------
          TOPIC     PRGID
          MOVE      PORT,PARENTID
          CLOCK     PORT,DIM2
          MOVE      DIM2,PORT
ML0000
          CALL      INIT0000
          CALL      ONAT0000
          CALL      SC00L000
          CHAIN     PGM
.---------------------------------------
. INIT - Open Files & Display Heading
.---------------------------------------
INIT0000  CALL      DISPHEAD
          MOVE      ONE,CCENTRY
          MOVE      ONE,CCANLDTE
          MOVE      ONE,CDEFDTE
          MOVE      ZERO,CHIGHLT
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
          DISPLAY   *P56:24,"Opening scrsbga1";
          OPEN      SCRSBGA1,"scrsbgaf"
          DISPLAY   *P56:24,"Opening scrpsta1";
          OPEN      SCRPSTA1,"scrpstaf"
          DISPLAY   *P56:24,"Opening scrpsta1";
          OPEN      SCRPSTA2,"scrpstaf"
          DISPLAY   *P56:24,"Opening scrslta1";
          OPEN      SCRSLTA1,"scrsltaf"
          MOVE      "   24",AUDTSECT
          MOVE      "219",AUDTPOST
          READ      CONTROLF,AUDTSECT;*AUDTPOST,AUDTFLAG
          IF        AUDTFLAG=1
            DISPLAY   *P56:24,"Opening sinaudsa";
            OPEN      SINAUDSA,"sinaudsa"
          ENDIF
          MOVE      "   24",AUDTSECT
          MOVE      "223",AUDTPOST
          READ      CONTROLF,AUDTSECT;*AUDTPOST,AUDTFLAG
          IF        AUDTFLAG=1
            DISPLAY   *P56:24,"Opening sinaudwa";
            OPEN      SINAUDWA,"sinaudwa"
          ENDIF
          DISPLAY   *P56:24,"Opening sinbacaf";
          OPEN      SINBACA1,"sinbacaf"
          DISPLAY   *P56:24,"Opening sinbacaf";
          OPEN      SINBACA2,"sinbacaf"
          DISPLAY   *P56:24,"Opening sinbacaf";
          OPEN      SINBACA3,"sinbacaf"
          DISPLAY   *P56:24,"Opening sinccaaf";
          OPEN      SINCCAA1,"sinccaaf"
          DISPLAY   *P56:24,"Opening sinccaaf";
          OPEN      SINCCAA2,"sinccaaf"
          DISPLAY   *P56:24,"Opening sinciaaf";
          OPEN      SINCIAA1,"sinciaaf"
          DISPLAY   *P56:24,"Opening sinciaaf";
          OPEN      SINCIAA2,"sinciaaf"
          DISPLAY   *P56:24,"Opening sinciaaf";
          OPEN      SINCIAA3,"sinciaaf"
          DISPLAY   *P56:24,"Opening sincieaf";
          OPEN      SINCIEA1,"sincieaf"
          DISPLAY   *P56:24,"Opening sincieaf";
          OPEN      SINCIEA2,"sincieaf"
          DISPLAY   *P56:24,"Opening sinpocaf";
          OPEN      SINPOCA1,"sinpocaf"
          DISPLAY   *P56:24,"Opening sinpocaf";
          OPEN      SINPOCA2,"sinpocaf"
          DISPLAY   *P56:24,"Opening sinpocaf";
          OPEN      SINPOCA3,"sinpocaf"
          DISPLAY   *P56:24,"Opening sinpocaf";
          OPEN      SINPOCA4,"sinpocaf"
          DISPLAY   *P56:24,"Opening sinpocaf";
          OPEN      SINPOCA5,"sinpocaf"
          DISPLAY   *P56:24,"Opening sinpocaf";
          OPEN      SINPOCA6,"sinpocaf"
          DISPLAY   *P56:24,"Opening sinpocaf";
          OPEN      SINPOCA7,"sinpocaf"
          DISPLAY   *P56:24,"Opening sinpocaf";
          OPEN      SINPOCA8,"sinpocaf"
          DISPLAY   *P56:24,"Opening sinsuaaf";
          OPEN      SINSUAA1,"sinsuaaf"
          DISPLAY   *P56:24,"Opening sinsuaaf";
          OPEN      SINSUAA2,"sinsuaaf"
          DISPLAY   *P56:24,"Opening sinsuaaf";
          OPEN      SINSUAA3,"sinsuaaf"
          DISPLAY   *P56:24,"Opening sinwaraf";
          OPEN      SINWARA1,"sinwaraf"
          DISPLAY   *P56:24,"Opening sinwaraf";
          OPEN      SINWARA2,"sinwaraf"
          DISPLAY   *P56:24,"Opening sinwseaf";
          OPEN      SINWSEA1,"sinwseaf"
          DISPLAY   *P56:24,"Opening sinwseaf";
          OPEN      SINWSEA2,"sinwseaf"
INIT9999  DISPLAY   *P1:24,*EL;
          RETURN
.
.------------------------------------
. Called for Print Screen Routine 
.------------------------------------
PRTSVARS
          MATCH     "00",SCRID
          IF         @EQUAL
            CALL       PS00L000   * Main Screen                        
          ENDIF
          RETURN
.--------------------------------
. Screen - Main Screen                        
.--------------------------------
SC00L000
SC00L100
          MOVE      "00",SCRID
          MOVE      ZERO,PRCOL
          MOVE      ZERO,PRVERT
          MOVE      "    1",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SCPSCOL,PRCOL
            MOVE     SCPSROW,PRVERT
          ENDIF
          CALL      SBACK000 * Display Background
.
SC00L200
          CALL      CF00L000 * Clear Fields
          CALL      SETACCEP
SC00L300
          CALL      KY00L000 * Enter Key Fields
          BRANCH    EXIT,SC00L999,SC00L999
SC00L400
          CALL      WP00L000 * Load any WP Details
SC00L500
          CALL      LM00L000 * Load any Batch Details
SC00L600
          CALL      DF00L000 * Display Fields
SC00L700
          CALL      SF00L000 * Select Fields
          BRANCH    EXIT,SC00L100,SC00L100,SC00L200
          IF        EXIT=4
            MOVE      ONE,EXIT
          ENDIF
SC00L800
SC00L900
SC00L999  RETURN
.--------------------------------------------------------
. Clear Fields for Screen : Main Screen                        
.-------------------------------------------------------
CF00L000
          MOVE      SP70,FROMCOST
          MOVE      SP70,TOCOST  
          MOVE      ZERO,EXPORTYN
          MOVE      SP70,FROMWAR 
          MOVE      SP70,TOWAR   
CF00L999  RETURN
.--------------------------------------------------------
. Enter Key Fields for Main Screen                        
.--------------------------------------------------------
KY00L000
          MOVE      ZERO,EXIT
          GOTO      KY00L999
.
KY00L900  MOVE      ONE,EXIT
KY00L999  RETURN
.---------------------------------------------------------
. Load WP files for : Main Screen                        
.---------------------------------------------------------
WP00L000
          RETURN
.---------------------------------------------------------
. Load Batch Screen for : Main Screen                        
.---------------------------------------------------------
LM00L000
          RETURN
.---------------------------------------------------------
. Display Fields for : Main Screen                        
.---------------------------------------------------------
DF00L000
          BRANCH   ACCEPT,DF00L999
          PACK     KEY15,PRGID,SCRID,SP70
          CALL     RSSCPS1
DF00L010
          CALL     RKSCPS1
          BRANCH   OVRCD,DF00L900
          MATCH    PRGID,SCPSPID
          GOTO     DF00L900 IF NOT EQUAL
          MATCH    SCRID,SCPSSID
          GOTO     DF00L900 IF NOT EQUAL
          MATCH    SP70,SCPSMTY
          GOTO     DF00L010 IF NOT EQUAL
.
          CALL     PS00L000 * Display Field
          GOTO     DF00L010
DF00L900
DF00L999
          RETURN
.---------------------------------------------------------
. Display/Print Fields for : Main Screen                        
.---------------------------------------------------------
PS00L000   PACK      VAR,SP70,SP70
          PACK     PRINTVAR,SP70,SP70
          MOVE     SCPSITM,FIELDNO
          BRANCH    FIELDNO,PS00L999,PS00L999,PS00L999,PS00L101:
                            PS00L102,PS00L103,PS00L104,PS00L105
          GOTO     PS00L999
.
PS00L101
          MOVE      FROMCOST,VAR     
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L102
          MOVE      TOCOST,VAR       
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L103
          MOVE      NO,YESNO
          MOVE      ZERO,FORM2
          MOVE      EXPORTYN,FORM2   
          LOAD      YESNO,FORM2,YES
          MOVE      YESNO,VAR
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L104
          MOVE      FROMWAR,VAR      
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L105
          MOVE      TOWAR,VAR        
          CALL      DISPITEM
          GOTO     PS00L999
PS00L999
          RETURN
.---------------------------------------------------------
. Selections for : Main Screen                        
.---------------------------------------------------------
SF00L000  PACK     KEY13,PRGID,SCRID,SP70
          CALL     RSSCSL1
SF00L001  BRANCH   ACCEPT,SF00L020
.
SF00L005  CALL     MTSTQ000
          MOVE     SCSLITM,FIELDNO
          BRANCH    FIELDNO,SF00L006,SF00L101,SF00L102,SF00L103:
                            SF00L006,SF00L104,SF00L105,SF00L006
SF00L006  BEEP
          GOTO     SF00L005
SF00L020  CALL     RKSCSL1
          BRANCH   OVRCD,SF00L090
          MATCH    PRGID,SCSLPID
          GOTO     SF00L090 IF NOT EQUAL
          MATCH    SCRID,SCSLSID
          GOTO     SF00L090 IF NOT EQUAL
          MOVE     SCSLITM,FIELDNO
          BRANCH    FIELDNO,SF00L020,SF00L020,SF00L020,SF00L103:
                            SF00L020,SF00L104,SF00L105,SF00L020
          GOTO     SF00L020
SF00L090  MOVE     ZERO,ACCEPT
          GOTO     SF00L001
.
SF00L101
          MOVE      ZERO,EXIT
          GOTO      SF00L999
.
SF00L102
            MOVE      ZERO,HLEF
            CALL      GETSCR00
          CALL      PRNT0000
          MOVE      EXIT,SECTOR
          IF        SECTOR=0
            CALL      FRESCR00
            GOTO      SF00L999
          ENDIF
          IF        SECTOR=1
            CALL      FRESCR00
            MOVE      ZERO,EXIT
            GOTO      SF00L001
          ENDIF
          IF        SECTOR=2
            CALL      FRESCR00
            MOVE      ONE,EXIT
            GOTO      SF00L999
          ENDIF
          MOVE      "00",SCRID
          MOVE      ZERO,PRCOL
          MOVE      ZERO,PRVERT
          MOVE      "    1",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SCPSCOL,PRCOL
            MOVE     SCPSROW,PRVERT
          ENDIF
          CALL      PUTSCR00
          IF        SECTOR=4
            CALL      DF00L000
          ENDIF
          GOTO      SF00L001
.
SF00L103
.
.
          MOVE      "    5",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SP70,TOCOST  
            PACK      VAR,SP70,SP70
            CALL      DISPITEM
          ENDIF
          CALL     SELECTED
          MOVE      "0",CKEYTYP
          CALL      ECOS0000
          IF        SCPSMAN=1
            IF        EXIT=1
              MOVE      "Mandatory Field - ",DISPMSG
              BEEP
              CALL      MESSAGE1
              GOTO      SF00L103
            ENDIF
            BRANCH    EXIT,SF00L103,SF00L990
          ELSE
            COMPARE   TWO,EXIT
            GOTO      SF00L990 IF EQUAL
          ENDIF
          MOVE      FROMCOST,VAR     
          CALL      DISPITEM
.
.
          MOVE      "    5",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE      TOCOST,VAR       
            CALL      DISPITEM
          ENDIF
          GOTO     SF00L001
.
SF00L104
          CALL     SELECTED
          CALL      YORN0000
          MOVE       FYESNO,EXPORTYN
          MOVE      NO,YESNO
          MOVE      ZERO,FORM2
          MOVE      EXPORTYN,FORM2   
          LOAD      YESNO,FORM2,YES
          MOVE      YESNO,VAR
          CALL      DISPITEM
          GOTO     SF00L001
.
SF00L105
.
.
          MOVE      "    8",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SP70,TOWAR   
            PACK      VAR,SP70,SP70
            CALL      DISPITEM
          ENDIF
          CALL     SELECTED
          MOVE      "0",CKEYTYP
          CALL      EWAR0000
          IF        SCPSMAN=1
            IF        EXIT=1
              MOVE      "Mandatory Field - ",DISPMSG
              BEEP
              CALL      MESSAGE1
              GOTO      SF00L105
            ENDIF
            BRANCH    EXIT,SF00L105,SF00L990
          ELSE
            COMPARE   TWO,EXIT
            GOTO      SF00L990 IF EQUAL
          ENDIF
          MOVE      FROMWAR,VAR      
          CALL      DISPITEM
.
.
          MOVE      "    8",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE      TOWAR,VAR        
            CALL      DISPITEM
          ENDIF
          GOTO     SF00L001
SF00L990  MOVE      FOUR,EXIT * Exit Char Exit
SF00L999  RETURN
.@@@@@ Function Restart Point @@@@@
.------------------------------------------------------------------------
. Open Nato X-Ref File if required
.------------------------------------------------------------------------
ONAT0000  OPEN      CONTROLF,"controlf"
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF FORMAT
          READ      CONTROLF,SIXTY3;*143,HNONUSAG
          TRAPCLR   FORMAT
          IF        OVRCD=1
            MOVE      ONE,HNONUSAG
            WRITAB    CONTROLF,SIXTY3;*143,HNONUSAG
          ENDIF
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF FORMAT
          READ      CONTROLF,SIXTY3;*141,INCNNATO
          TRAPCLR   FORMAT
          IF        OVRCD=1
            MOVE      ZERO,INCNNATO
            WRITAB    CONTROLF,SIXTY3;*141,INCNNATO
          ENDIF
.
          IF        INCNNATO=1
            MOVE      ZERO,OVRCD
            TRAP      OVERCOND IF IO
            OPEN      SINNATA1,"sinnataf"
            TRAPCLR   IO
            IF        OVRCD=1
              DISPLAY   *P1:24,*EL,*B,"Nato file does not exist. Run fixit.  ";
              CALL      HITENTER
              CHAIN     PGM
            ENDIF
.
            OPEN      SINNATA2,"sinnataf"
          ENDIF
.
ONAT9999  RETURN
.********************************************************************
. Set accept mode
.********************************************************************
SETACCEP  MOVE      ONE,ACCEPT
          RETURN
.********************************************************************
. PRNT - print report for SIBA                      Called by ML
.         Requires : FROMCOST,TOCOST (print range display labels)
.                    FCOST,TCOST     (print range)
.         Returns  : EXIT            (1=illegal range entered)
.********************************************************************
PRNT0000  DISPLAY   *P1:24,*EL,*P50:24,"Printing :   ";
          MOVE      ZERO,CPAGENO                 * reset page no.
          CLOCK     TIME,CTIMEIS                 * get current time
          PACK      LASTCODE,SP70
          PACK      LASTCOD7,SP70
          PACK      SKEY30,SP70
          MOVE      "999",CLNO                   * init last line number
.
          CALL      MAKEXP00                     * Create export file
          CALL      FILE0000                    * go to correct position in file
.
PRNT1000  CALL      NREC0000                     * get next record
          BRANCH    OVRCD,PRNT8000               * end of file ?
.
          MATCH     STRWAR,SIBAWAR
          GOTO      PRNT1000 IF LESS             * warehouse out of range ?
.
          MATCH     SIBAWAR,ENDWAR
          GOTO      PRNT1000 IF LESS             * warehouse out of range ?
.
. check warehouse security
.
          MATCH     SP70,SIBAWAR
          IF        !@EQUAL
            PACK      KEY9,PASSCODE,SIBAWAR,SP70
            CALL      RDSIWS1
            BRANCH    OVRCD,PRNT1000
          ENDIF
.
          DISPLAY   *P61:24,*V2LON,SIBACST,SLASH,SIBACAT,*EL;
.
          CALL      WRIT0000                     * print details from temporary
          GOTO      PRNT1000                     * get next record
.
PRNT8000  COMPARE   ZERO,CPAGENO
          CALL      HEAD0000 IF EQUAL          * print header if nothing printed
          CALL      PLAST000                     * print end of last description
          CALL      UND132                       * print line
          PRINT     "****   End of Report   ****"
.
          CALL      TRAN0000
.
PRNT9000  MOVE      ZERO,EXIT                    * exit screen
          GOTO      PRNT9999
.
PRNT9500  MOVE      ONE,EXIT                     * continue
.
PRNT9999  RETURN
.**********************************************************************
.  FILE - Set file pointer                             Called By PRNT
.**********************************************************************
FILE0000  BRANCH    OPTIONA,FILE0100,FILE9999,FILE9999,FILE9999:
                            FILE9999,FILE9999,FILE9999,FILE9999
.
.---- print by type 1 ----
.
FILE0100  PACK      KEY25,FCOST,SP70
          CALL      RSSIBA1
          GOTO      FILE9999
.
FILE9999  RETURN
.**********************************************************************
.  NREC - Get next record                              Called By PRNT
.**********************************************************************
NREC0000  BRANCH    OPTIONA,NREC0100,NREC9999,NREC9999,NREC9999:
                            NREC9999,NREC9999,NREC9999,NREC9999
.
.---- print by type 1 ----
.
NREC0100  CALL      RKSIBA1
          BRANCH    OVRCD,NREC9500
.
          MATCH     SIBACST,TCOST
          GOTO      NREC9500 IF LESS             * out of range ?
          GOTO      NREC9000
.
NREC9000  MOVE      ZERO,OVRCD         * continue
          GOTO      NREC9999
.
NREC9500  MOVE      ONE,OVRCD          * quit
.
NREC9999  RETURN
.********************************************************************
. HEAD - print report                               Called by PRNT
.         Requires : FROMCOST,TOCOST (print range display labels)
.                    FCOST,TCOST     (print range)
.********************************************************************
HEAD0000  CALL      PLAST000                     * print end of last description
          CALL      HEAD132                      * print heading
.
          ADD       "6",CLNO
          PRINT     *1,"Cost Centre From : ",FROMCOST," To : ",TOCOST:
                    *N,*1,"Warehouse   From : ",FROMWAR," To : ",TOWAR:
                    *N
.
          PACK      SICADESC,SP70
          PACK      KEY5,SIBACST,SP70
          CALL      RDSICA1
          PRINT     "Cost Centre : ",SIBACST,SP2,SICADESC
.
          PACK      SIWADES,SP70
          PACK      KEY5,SIBAWAR,SP70
          CALL      RDSIWA1
.
          CALL      UND132                       * print line
          PRINT     "|                                             |       ":
                    "|                 |      Backorder Details          | ":
                    "Total Stock Earliest":
                    *132,"|",*N:
                    "| Catalog                                     | W/H   ":
                    "| Issue Unit      |   Quantity  Req. No. Date       | ":
                    "   on Order Del. Date":
                    *132,"|"
          CALL      UND132                       * print line
.
HEAD9999  RETURN
.********************************************************************
. WRIT - print data line                            Called by PRNT
.********************************************************************
WRIT0000  MATCH     LASTCODE,SIBACST             * next cost centre
          IF        !@EQUAL
            PACK      LASTCOD7,SP70
            PACK      LASTCODE,SIBACST,SP70
            MOVE      "999",CLNO
          ENDIF
.
          MATCH     LASTWAR,SIBAWAR              * next warehouse
          IF        !@EQUAL
            PACK      LASTCOD7,SP70
            PACK      LASTWAR,SIBAWAR,SP70
            MOVE      "999",CLNO
          ENDIF
.
          COMPARE   "55",CLNO                    * end of page ?
          CALL      HEAD0000 IF NOT LESS         * start new page
.
WRIT0100  PACK      SIIADES,SP70
          PACK      SIIAISS,SP70
          PACK      SIIASUB,SP70
          PACK      KEY7,SIBACAT,SP70
          CALL      RDSIIA1
          UNPACK    SIIADES,KEY60
.
          PACK      SIWADES,SP70
          PACK      KEY5,SIBAWAR,SP70
          CALL      RDSIWA1
.
          CALL      BKLIN000
.
          UNPACK    SIBADAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PACK      REQDATE,CPCDATE,SP70
          MOVE      SIBAQTY,BACKQUAN
.
          MATCH     LASTCOD7,SIBACAT             * next catalog ?
          IF        !@EQUAL
            PACK      SISADESC,SP70
            PACK      KEY5,SIIASUB,SP70
            CALL      RDSISA1
.
            PACK      LASTCOD7,SIBACAT,SP70
            CALL      PLAST000                   * print end of last description
            PRINT     "|",SIBACAT,"-",SIIASUB,SP1,KEY30," | ",SIBAWAR," | ":
                      SIIAISS," |",BACKQUAN," ",SIBAREQ," ",CPCDATE," |";
            CALL      CORDR000                   * calc on order
            PRINT     F9P2," ";
            CALL      PREV0000
            PACK      DELDATE,CPCDATE,SP70
            PRINT     CPCDATE,*132,"|"
            PACK      SKEY30,KEY60,SP70
          ELSE
            PRINT     "|         ",SKEY30,"      |       |":
                      "                 |":
                      BACKQUAN," ",SIBAREQ," ",CPCDATE," |":
                      *132,"|"
            MOVE      SP70,SKEY30
          ENDIF
          CALL        WRTEXP00
.
WRIT9999  RETURN
.********************************************************************
. CORDR - calculate on order
.********************************************************************
CORDR000
          MOVE      ZERO,F9P2
          PACK      KEY12,SIBACAT,SIBAWAR,SP70
          CALL      RDSIIE1
          BRANCH    OVRCD,CORDR999
.
          ADD       SIIESOO,F9P2
.
CORDR999  RETURN
.********************************************************************
. PREV - Get the earlist delivery date coming up
.********************************************************************
PREV0000  MOVE      SP70,PREVDATE
          MOVE      SP70,CPCDATE
          PACK      KEY25,SIBACAT,SP6,ANSZ,SP70
          CALL      RSSIPC6
PREV0100  CALL      RKSIPC6
          BRANCH    OVRCD,PREV9999
.
          MATCH     SIPCCAT,SIBACAT
          GOTO      PREV9999 IF NOT EQUAL
.
          MATCH     SIPCWAR,SIBAWAR
          GOTO      PREV0100 IF NOT EQUAL
.
          MOVE      SIPCEDD,PREVDATE
          UNPACK    PREVDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
PREV9999  RETURN
.********************************************************************
. PLAST - print end of last description
.********************************************************************
PLAST000  MATCH     SP70,SKEY30
          IF        !@EQUAL
            PRINT     "|              ",SKEY30," |       |        ":
                      "         |                                 |":
                      *132,"|"
            ADD       "1",CLNO       * add one to line number
            MOVE      SP70,SKEY30
          ENDIF
.
PLAST999  RETURN
.********************************************************************
. BKLIN - Break up KEY60 into two neat halves        Called by lots
.         Requires - KEY60   (original line)
.         Returns  - KEY30   (line 1)
.                    KEY60   (line 2)
.********************************************************************
BKLIN000  RESET     KEY60,31
          CMATCH    SP70,KEY60
          WHILE     !@EQUAL&!@EOS
            BUMP      KEY60,-1
            IF        !@EOS
              CMATCH    SP70,KEY60
            ENDIF
          DO
.
          IF        @EOS
            SETLPTR   KEY60
            RESET     KEY60
            UNPACK    KEY60,KEY30,KEY60
          ELSE
            LENSET    KEY60
            RESET     KEY60
            MOVE      KEY60,KEY30
            PACK      KEY30,KEY30,SP70
.
            ENDSET    KEY60
            SETLPTR   KEY60
            BUMP      KEY60
            PACK      KEY60,KEY60,SP70
          ENDIF
.
BKLIN999  RETURN
.------------------------------------------------------------
. File       : casetran
.
. Functions  : This file contains standard routines for
.              creating and transfering TAB delimited
.              export files to a PC using the unix script
.              keatran.
.
. Routines   : MAKEXP00  - Create the Export File and Headings
.              WRTEXP00  - Write Export details Record to File
.              TRAN0000  - Transfer file to PC using script keatran
.
. Note       : The Variable EXPORTYN is used to determine if a
.              export file is to be created. 0=No, 1=Yes.
.
. Variables to be Declared
. ------------------------
. EXPORTFL  FILE      ASCII, FIXED=400     * Export File (Adjust Length to Suit)
. EXPORTFN  DIM       8                    * Export File Name
. TABDELM   EQU       0x09                 * Tab Delimiter
.------------------------------------------------------------
. Create Export File
.------------------------------------------------------------
MAKEXP00  COMPARE   ONE,EXPORTYN
          GOTO      MAKEXP99 IF NOT EQUAL
          MOVE      "EXPORT",KEY6
          PACK      EXPORTFN,KEY6,PORT
          REP       " 0",EXPORTFN
          PREP      EXPORTFL,EXPORTFN
.
          WRITE     EXPORTFL,SEQ;"Cost Centre Code",TABDELM:
                                 "Cost Centre Code Description",TABDELM:
                                 "Warehouse Code",TABDELM:
                                 "Warehouse Description",TABDELM:
                                 "Catalog Code",TABDELM:
                                 "Catalog Description",TABDELM:
                                 "Product Group",TABDELM:
                                 "Product Group Description",TABDELM:
                                 "Issue Unit",TABDELM:
                                 "Backorder Quantity",TABDELM:
                                 "Backorder Requisition Number",TABDELM:
                                 "Backorder Date of Requisition",TABDELM:
                                 "Total Stock on Order",TABDELM:
                                 "Earliest Delivery Date"
MAKEXP99  RETURN
.------------------------------------------------------------
. Write to Export File
.------------------------------------------------------------
WRTEXP00  COMPARE   ONE,EXPORTYN
          GOTO      WRTEXP99 IF NOT EQUAL
.
          WRITE     EXPORTFL,SEQ;SIBACST,TABDELM:
                                 SICADESC,TABDELM:
                                 SIBAWAR,TABDELM:
                                 SIWADES,TABDELM:
                                 SIBACAT,TABDELM:
                                 SIIADES,TABDELM:
                                 SIIASUB,TABDELM:
                                 SISADESC,TABDELM:
                                 SIIAISS,TABDELM:
                                 SIBAQTY,TABDELM:
                                 SIBAREQ,TABDELM:
                                 REQDATE,TABDELM:
                                 F9P2,TABDELM:
                                 DELDATE
.
WRTEXP99  RETURN
.------------------------------------------------------------
. Execute Transfer Script eg softerm
.------------------------------------------------------------
TRAN0000  COMPARE   ONE,EXPORTYN
          GOTO      TRAN9999 IF NOT EQUAL
          MOVE      YES,DEFYESNO
          CLOSE     EXPORTFL
          DISPLAY   *P1:24,*EL,"Execute Transfer Now (":
                               *V2LON,"Y",*HOFF,"/":
                               *V2LON,"N",*HOFF,") ? "
          MOVE      "24",CVERT
          MOVE      "30",CCOL
          CALL      YORN0000
          COMPARE   ZERO,FYESNO
          GOTO      TRAN9999 IF EQUAL   *  No Transfer
.
          MOVE      ZERO,HLEF
          CALL      GETSCR00
.
          CLEAR     KEY80
          APPEND    "keatran ",KEY80
          APPEND    EXPORTFN,KEY80
          APPEND    ".txt",KEY80
          RESET     KEY80
          EXECUTE   KEY80,TASKID
.
          CALL      PUTSCR00
.
          DISPLAY   *P1:24,*EL,"Transfer Completed Ok (":
                               *V2LON,"Y",*HOFF,"/":
                               *V2LON,"N",*HOFF,") ? "
          MOVE      "24",CVERT
          MOVE      "31",CCOL
          CALL      YORN0000
          COMPARE   ZERO,FYESNO,
          GOTO      TRAN0000 IF EQUAL   *  Not ok
.
          PREP      EXPORTFL,EXPORTFN
          CLOSE     EXPORTFL
.
TRAN9999  RETURN
.----------------------------------------------------------------------
. Enter Cost Centre Range
.----------------------------------------------------------------------
ECOS0000  MOVE      "    4",FLDID
          CALL      GETPOSIT
          MOVE      SP70,VAR
          CALL      DISPITEM
.
          MOVE      ZERO,CKEYTYP
          CALL      KSICAA00
          BRANCH    EXIT,ECOS1000,ECOS9500
          MOVE      SICACODE,FCOST
          PACK      FROMCOST,SICACODE,SP1,SICADESC,SP70
          GOTO      ECOS2000
.
ECOS1000  MOVE      SP70,FCOST
          MOVE      "Start                                         ",FROMCOST
.
ECOS2000  MOVE      FROMCOST,VAR
          CALL      DISPITEM
.
          MOVE      "    5",FLDID
          CALL      GETPOSIT
          MOVE      SP70,VAR
          CALL      DISPITEM
.
          MOVE      ZERO,CKEYTYP
          CALL      KSICAA00
          BRANCH    EXIT,ECOS3000,ECOS9500
          MOVE      SICACODE,TCOST
          PACK      TOCOST,SICACODE,SP1,SICADESC,SP70
          MATCH     FCOST,TCOST
          GOTO      ECOS9000 IF EQUAL
          GOTO      ECOS9000 IF NOT LESS
          BEEP
          MOVE      "To Cost Centre Must Be > From Cost Centre - ",DISPMSG
          CALL      MESSAGE1
.
          MOVE      "    5",FLDID
          CALL      GETPOSIT
          MOVE      SP70,VAR
          CALL      DISPITEM
.
          GOTO      ECOS0000
.
ECOS3000  PACK      TCOST,Z70
          MOVE      "Finish                                        ",TOCOST
.
ECOS9000  MOVE      "    4",FLDID
          CALL      GETPOSIT
          MOVE      ZERO,EXIT
          GOTO      ECOS9999
.
ECOS9500  MOVE      TWO,EXIT
.
ECOS9999  RETURN
.
.----------------------------------------------------------------------
. Range of Warehouse Code
.----------------------------------------------------------------------
EWAR0000  MOVE      "    7",FLDID
          CALL      GETPOSIT
          MOVE      SP70,VAR
          CALL      DISPITEM
.
          PACK      SIWAWAR,SP70
          PACK      SIWADES,SP70
          CALL      KSIWSA00
          BRANCH    EXIT,EWAR0100,EWAR9500
          MOVE      SIWAWAR,STRWAR
          PACK      FROMWAR,SIWAWAR,SP1,SIWADES
          GOTO      EWAR0150
EWAR0100  MOVE      "Start             ",FROMWAR
          PACK      STRWAR,SP70
.
EWAR0150  MOVE      FROMWAR,VAR
          CALL      DISPITEM
.
          MOVE      "    8",FLDID
          CALL      GETPOSIT
          MOVE      SP70,VAR
          CALL      DISPITEM
.
          PACK      SIWAWAR,SP70
          PACK      SIWADES,SP70
          CALL      KSIWSA00
          BRANCH    EXIT,EWAR0200,EWAR9500
          MOVE      SIWAWAR,ENDWAR
          PACK      TOWAR,SIWAWAR,SP1,SIWADES
          MATCH     STRWAR,ENDWAR
          GOTO      EWAR9000 IF EQUAL
          GOTO      EWAR9000 IF NOT LESS
          BEEP
          MOVE      "To Warehouse Must Be > From Warehouse - ",DISPMSG
          CALL      MESSAGE1
.
          MOVE      "    8",FLDID
          CALL      GETPOSIT
          MOVE      SP70,VAR
          CALL      DISPITEM
.
          GOTO      EWAR0000
.
EWAR0200  MOVE      "Finish             ",TOWAR
          PACK      ENDWAR,Z70
.
EWAR9000  MOVE      "    7",FLDID
          CALL      GETPOSIT
          MOVE      ZERO,EXIT
          GOTO      EWAR9999
.
EWAR9500  MOVE      TWO,EXIT
          GOTO      EWAR9999
.
EWAR9999  RETURN
.
.-------------------------------
. Includes for I/O and Keyins
.-------------------------------
          INC       SINBACIO/INC
          INC       SINCCAIO/INC
          INC       SINCCAKY    
          INC       SINCIAIO/INC
          INC       SINCIEIO/INC
          INC       SINNATIO/INC
          INC       SINPOCIO/INC
          INC       SINSUAIO/INC
          INC       SINWARIO/INC
          INC       SINWSEIO/INC
          INC       SINWSEKY    
.
          INC       STDGENCD
.

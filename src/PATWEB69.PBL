.----------------------------------------------------------------------
. Program       : PATWEB69
.
. Function      : Local Health District (LHD) Patient Search Server
.                 
. Modifications : 
.----------------------------------------------------------------------
. V12.01.01 11/12/2025  Bella Turco    TSK 0949715
.                       Recompiled for UPDUR - added NZPMICHG
. V12.00.01 22/05/2025  J.Tan          TSK 0955096
.                       Added Alphanumeric visit number changes
. V11.02.01 01/04/2022  Thanh T        TSK 0903453   
.                       Added SPPXUCC4, SPPXUCC5, SPPXATF1 and SPPXATF2 for
.                       DEATHAUD changes
. V11.01.02 20/12/2021  Thanh T        TSK 0877055                  
.                       Added DEATHATD for common variables to be used in       
.                       DEATHAUD
. V11.01.01 15/09/2021  Thanh T        TSK 0889595
.                       Recompiled for PMSDAUFD/DEATHAUD changes
. V11.00.02 23/06/2020  Peter Vela        TSK 0878550
.                       Recompiled for DEATHAUD
. V11.00.01 10/06/2020  Peter Vela        TSK 0878550
.                       Recompiled for DEATHAUD
. V10.15.02 03/02/2020  Don Nguyen        TSK 0887880
.                       Removed Surname, Given Name and Date of Birth from
.                       PatientSelect() to fix javascript error when name
.                       contains apostrophe(s).
. V10.15.01 30/10/2019  Tracey Nguyen     TSK 0879283
.                       Added Surname, Given Name and Date of Birth to 
.                       PatientSelect() - TABROW00, LSTROW00
.----------------------------------------------------------------------
. V10.14.02 13/06/2019  Peter Vela        TSK 0871798
.                       Recompiled for DEATHAUD
. V10.14.01 13/03/2019  Steve Armstrong   TSK 0871671
.                       Recompiled for A19HNDLR - check for usual address type
.----------------------------------------------------------------------
. V10.13.06 22/10/2018  Steve Armstrong  TSK 0865126
.                       Recompiled for changes to A19HNDLR
. V10.13.05 03/10/2018  Davin          TSK 0857734
.                       Recompiled for A19HNDLR - remove "" from medi exp date
. V10.13.04 10/09/2018  Davin          TSK 0863288
.                       Recompiled for A19HNDLR - additional array range check
. V10.13.03 07/09/2018  Davin          TSK 0863288
.                       Recompiled for A19HNDLR - Check for max patient limit
. V10.13.02 05/09/2018  Don Nguyen     TSK 0857734
.                       Added JSON output for Extra Contacts (GETEXC00).
.                       Added Resident Status to UPIGET00.
.                       Increased size of MAXPATFL to cater for Resident Status.
.                       Recompiled for A19HNDLR - Resident Status (Cat T; Indc1)
. V10.13.01 13/08/2018  Peter Vela     Task 0845218
.                       Recompiled for DEATHAUD
. V10.12.14 11/07/2018  Peter Vela     TSK 0845218
.                       Recompiled for DEATHAUD
. V10.12.13 17/05/2018  Don Nguyen     TSK 0856298
.                       Recompiled for A19HNDLR (log data in smaller sections).
.                       Read in Maintenance Audit parameter (CAUDM).
.                       Added SRCHADMN tag output.
. V10.12.12 09/05/2018  Don Nguyen     TSK 0844685
.                       Modified GETCCD00, GETCCP00 and GETTIT00 to also check 
.                       for code Active status.
.                       Corrected HCS Equivalent/HDP Default position check 
.                       (CODPOSTN) to be "1" for Category "VA" (Aboriginality).
. V10.12.11 08/05/2018  Don Nguyen     TSK 0844685
.                       Modified PATGET00 to output 'Y/N' value for 'Interpreter
.                       Required'.
. V10.12.10 04/05/2018  Don Nguyen     TSK 0844685
.                       Added validation for Category Code values with 
.                       Patient/UPI patient data field output.
. V10.12.09 01/05/2018  Don Nguyen     TSK 0844685
.                       Added CCDGET00 (Remote Script) & GETCCD00.
.                       Added code to process (update) additional patient data 
.                       fields in MVUPDF00.
.                       Added more fields to PATGET00 and UPIGET00 data array.
. V10.12.08 26/04/2018  Don Nguyen     TSK 0844685
.                       Added remote function UPIEXT00 - Validate UPI exists
.                       Corrected spelling in error messages (NSLHD)
. V10.12.07 20/04/2018  Don Nguyen     TSK 0844685
.                       Added SRCHMEDI (Medicare No.) to A19HNDLR calls.
.                       Added SRCHNPID (NSLHD Person ID) to retrieve calls.
.                       Added "Catch-All" error handling on A19HNDLR calls in 
.                       CHKCON00, CHKEUP00, UPIGET00.
.                       Modified TABOUT00 & TABLST00 to handle more errors.
.                       Modified PatientSelect() output.
. V10.12.06 18/04/2018  Don Nguyen     TSK 0844685
.                       Added Alias functionality (OUTALI00).
.                       Created Aliases temp file in init (CREAT000).
.                       Removed A19 Handler init code and branch after INIT0000.
.                       Modified UPIGET00 to ignore A19HNDLR error codes.
.                       Modified PatientSelect() output.
. V10.12.05 12/04/2018  Don Nguyen     TSK 0844685
.                       Added branch on EXIT after INIT0000.
.                       Modified INIT0000 to handle A19 Handler 'Socket
.                       Connection Failed' error.
. V10.12.04 12/04/2018  Don Nguyen     TSK 0844685
.                       Moved REMOTE00 to Report 4.
.                       Added Patients Missing a UPI (PATMIU00 - Report 3).
. V10.12.03 04/04/2018  Don Nguyen     TSK 0844685
.                       Added Gender & Second Given Name to LHD search 
.                       parameters.
.                       Added PATS1700 (REDIRECT)
. V10.12.02 03/04/2018  Don Nguyen     TSK 0844685
.                       Modified REMOTE00; renamed check existing Alt ID
.                       function and added CHKEUP00 (check existing UPI).
. V10.12.01 29/03/2018  Don Nguyen     TSK 0844685
.                       Added CHKEUR00 to check if UR exists.
.                       Modified TABROW00 & LSTROW00 to output UR Number.
. V10.12.00 09/03/2018  Don Nguyen     TSK 0844685
.                       Created Web Server
.----------------------------------------------------------------------
          INC       IBAWEBDF    
          INC       WEBDATDF
.
          INC       A19UPIVR/INC       * UPI variables for interface to NSLHD
          INC       PATCONFD/INC       * Control File
          INC       PATCALAG/INC       * Data variable definitions for CALCAGE
          INC       PATMASTD/INC       * CGI variables for PSTMASTM
          INC       PATDO1FD/INC       * Doctor File
          INC       PATHSPFD/INC       * Hospital Details
          INC       PATMA1FD/INC       * Patient Master File
          INC       PATMI1FD/INC       * Patient Admissions File
          INC       PATVISFD/INC       * Patient Visit File
          INC       PMSVX1FD/INC       * Patient Visit Extension File
          INC       PATPR1FD/INC       * Pre-Admission Details
          INC       PATWR1FD/INC       * Patient Ward/Bed Details
          INC       PMSPX2FD/INC       * PMI extension table
          INC       PMSPX2TD/INC       * CGI variables for PMSPX2FD
          INC       PMSAIDFD/INC       * Alternate ID
          INC       PATGSRFD/INC       * Given name/Surname Index File
          INC       PATNIDFD/INC       * Patient NMPI File
          INC       DEATHATD/INC
          INC       NHIETHFD/INC
          INC       NHIMASFD/INC
          INC       BOKRC1FD/INC
          INC       PATINVFD/INC
          INC       PATLOCFD/INC
          INC       PMSCURFD/INC
          INC       PATURAFD/INC
          INC       PATAM1FD/INC
          INC       PATPA1FD/INC
          INC       PATRE1FD/INC
          INC       PATRE1TD/INC            * P.R.A. cgi variables
          INC       PATAK1FD/INC            * FD Still needed IO makes no diff
          INC       PMSDAUFD/INC
.
          INC       WEBDINVS/INC
          INC       PATCOMM/INC
.
          INC       PMSMUPFD/INC       * Patients Missing a UPI
          INC       PMSRELFD/INC       * Patient Relationship
          INC       PMSTLEFD/INC       * Patient Titles
.
          INC       TFILEVAR/INC       * Temporary file variables
.
.----------------------------------------------------------------------
CODCATEG  DIM       2        * Category
CODHCSEQ  DIM       4        * HCS Equivalent (THCSCOD)
CODCHARV  DIM       1        * HCS Equivalent Character Value
CODPOSTN  FORM      1        * HCS Equivalent Character Position (to match)
.
DATRETFM  FORM      1        * Patient Data Array Format
.                                0 - UPI array format (pipe-delimited)
.                                1 - eAdmission array format (JSON object)
.
INDPOSTN  FORM      2        * Indicator Position (to match)
INDVALUE  DIM       1        * Indicator Value (to match)
RETCCODE  DIM       3        * Return Category Code
RETTDESC  DIM       20       * Return Description
RETHCSVL  DIM       4        * Return HCS Equivalent
NEXTPAGE  FORM      1        * Nextpage CGI parameter
UPDTTYPE  DIM       1        * Update Type
STARTKEY  DIM       50       * Starting Key
DOCTCODE  DIM       8
PSAGETYP  DIM       3
PSAGECON  DIM       3
REMOTENO  FORM      2
TITLCODE  DIM       4        * Title Code
TITLDESC  DIM       30       * Title Description
FAILFLAG  INIT      "-1"     * Failure flag for invalid Code
.
STRNGINP  DIM       200      * Input string (with occurrences of "'")
STRNGOUT  DIM       600      * Output string (where "'" is replaced with "%60")
.STRNGLEN  FORM      3        * String length
CURRCHAR  DIM       1
POSITION  FORM      3        * Current pos of character pointer
VALSTRNG  DIM       20
.
ALIASSTS  DIM       1
SAVEUSR1  DIM       3
.
SRCHSNAM  DIM       40       * Surname
SRCHGNAM  DIM       40       * First Given Name
SRCHMNAM  DIM       40       * Second Given Name (Middle Name)
SRCHPSEX  DIM       1        * Sex
SRCHPDOB  DIM       8        * Date of Birth
SRCHMEDI  DIM       11       * Medicare No.
SRCHNEXT  DIM       1        * Search Next Batch Flag
SRCHCONT  DIM       180      * Search Continuation Point (HL7)
SRCHTYPE  DIM       1        * Search Type (1=LHD Search)
SRCHUPIN  DIM       10       * Search UPI number
SRCHURNO  DIM       8        * Search UR Number
SRCHNPID  DIM       30       * Search NSLHD Person ID
SRCHCATG  DIM       2        * Search Category
SRCHHCSC  DIM       4        * Search HCS Equivalent Code
SRCHADMN  DIM       8        * Search Admission Number
.
UPINUMBR  DIM       10       * UPI Number
UPINSPID  DIM       30       * NSLHD Person ID
.
DATEDIS1  DIM       12       * Date display 1
DATEDIS2  DIM       12       * Date display 2
LINKNUMB  FORM      1        * Link Number (Patient Folder Type)
MAXRECRD  FORM      3        * Max records for display
QUICKREG  DIM       1        * Quick Registration flag
.
MAXPATFL  EQU       21                 * Num of patient data fields
ARRPATFL  DIM       200[MAXPATFL]      * Patient Data Array (max 200 chars)
.
. Patient Data Array Fields:
. --------------------------
.         1 - Title
.         2 - Surname
.         3 - First Given Name
.         4 - Second Given Name
.         5 - Gender
.         6 - Date of Birth
.         7 - Address Line 1   ( Street Address Line 1)
.         8 - Address Line 2   ( Street Address Line 2)
.         9 - Address Line 3   ( Suburb )
.         10 - Address Line 4  ( State )
.         11 - Address Line 5  ( Postcode )
.         12 - Home Phone
.         13 - Mobile Phone
.         14 - Work Phone
.         15 - Country of Birth
.         16 - Preferred Language
.         17 - Interpreter Required (0, 1 or U)
.         18 - Race (Aboriginality)
.         19 - Medicare Number
.         20 - Medicare Valid To Date
.         21 - Resident Status
.
.
UPDTFLDS  DIM       200[99]   * Update Data Field Values Array (DIM 200)
.
.
. DGCLICUP variables
.
NMPNUMB   DIM       20
.
.
. UPDSUR variables
.
DIM4      DIM       4
FORM8     FORM      8
SAVEALIA  FORM      1
URCHKDGT  FORM      4
URWTARAY  INIT      "234567"
URNLNGTH  FORM      2
URCHKPRD  FORM      2
URNOWGHT  FORM      1
.
.
. UPDUR variables
.
ADD       DIM       2
AMM       DIM       2
AYY       DIM       2
ACC       DIM       2
ACTION    DIM       1
SAVEPHCP  DIM       10               * Used for HCP Audit as SVMAS variable
SAVEPRAC  DIM       10               * are too small
SAVEPCTR  DIM       2
.
SAVESNAM  DIM       40
SAVEXFNM  DIM       40
SAVEXSNM  DIM       40
SAVETITL  DIM       4
SAVEPSEX  DIM       1
SAVEPDOB  DIM       8
SAVEPFGN  DIM       25
SAVEPMST  DIM       3
SAVEPREG  DIM       3
SAVEPCNT  DIM       3
SAVEPTYP  DIM       3
SAVEABRG  DIM       3
SAVEUSR7  DIM       3
SAVEMEDI  DIM       10
SAVEMCCD  DIM       2
SAVEMEDC  DIM       8
SAVECONP  DIM       1
SAVEUPRF  DIM       3
SAVEINTR  DIM       1
SAVELNG1  DIM       3
SAVEPRVI  DIM       3
SAVEFLDR  DIM       3
SAVESN16  DIM       1
SPPXUCC4  DIM       3
SPPXUCC5  DIM       3
SPPXATF1  DIM       80
SPPXATF2  DIM       80
SAVECUID  DIM       8
SAVECDAT  DIM       8
SAVECTIM  DIM       8
NZPMICHG  DIM       1
.
DETNTREC  FORM      2
OPERCODE  DIM       4
SURSYST   FORM      1
USERNAME  DIM       10
VCASE     DIM       1
FIRSTREC  FORM      1   * First Record Output Flag 0=No 1=Yes
.
SP100     INIT      "                                                  ":
                    "                                                  "
CATTC     INIT      "tc"
.
.
.------------------------------------------------------------------------------
PRGID     INIT      "PATWEB69"
VERSION   INIT      "V12.01.01"
PRGNAM    INIT      "LHD Patient Search Web Server"
.------------------------------------------------------------------------------
.
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200,MAIN1300,MAIN1400
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "Invalid Option",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  CALL      SRHPAT00        * Search Patient
          GOTO      MAIN1000
.
MAIN1200  CALL      UPDPAT00        * Update Patient Data
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1300  CALL      PATMIU00        * Patients Missing a UPI
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1400  CALL      REMOTE00        * Remote Scripting Functions
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
+
.------------------------------------------------------------------------------
.         Open Files and Initialize Variables
.------------------------------------------------------------------------------
INIT0000  OPEN      CONTROLF,"controlf"     * Inpatient CONTROL File
.
.         CALL      INTMAS00 
.
.         OPEN      IBASEQA1,"ibaseqaf"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"     
          OPEN      PATCODE5,"patcodes"     
          OPEN      PATHSPA1,"pathspaf"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PMSPX2A1,"pmspx2af"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      PMSAIDA1,"pmsaidaf"
          OPEN      PMSAIDA2,"pmsaidaf"
.
          OPEN      PATGSRN1,"patgsrnf"
          OPEN      PATNIDA1,"patnidaf"          * all other countries
          OPEN      NHIMASA1,"nhimasaf"
          OPEN      NHIMASA2,"nhimasaf"
          OPEN      BOKRC1A1,"bokrc1af"
          OPEN      BOKRC1A6,"bokrc1af"
          OPEN      PATINVR3,"patinvrf"
          OPEN      PATLOCA1,"patlocaf"
          OPEN      PMSCURA1,"pmscuraf"
          OPEN      PATURAD1,"paturadf"
          OPEN      PATURAD2,"paturadf"
          OPEN      PATAM1A1,"patam1af"
.
          OPEN      PATPA1A1,"patpa1af"
          OPEN      PATRE1A1,"patre1af"
.
.         OPEN      IBAPDFA1,"ibapdfaf"
.
          OPEN      PMSMUPA1,"pmsmupaf"
          OPEN      PMSTLEA1,"pmstleaf"
          OPEN      PMSRELA1,"pmsrelaf"
          OPEN      PMSRELA2,"pmsrelaf"
.
          READ      CONTROLF,HUND14;*219,PTCNUSGN     * Using Second Given Name
          READ      CONTROLF,TEN;*213,CAUDM
.
          CALL      CREAT000      * create Aliases & Extra Contacts temp files
.
INIT9999  RETURN
+
.------------------------------------------------------------------------------
.         Clear Parameters
.------------------------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          MOVE      SP70,TEMPLATE
          MOVE      ZERO,STARTNUM
          MOVE      SP70,STARTKEY
          MOVE      ZERO,NORECORD
          MOVE      SP70,NEXTPAGE
          MOVE      SP70,REDIRECT
          MOVE      SP70,UPDTTYPE
          MOVE      SP70,URNUMBER
          MOVE      SP70,ADMISSNO
          MOVE      ZERO,REMOTENO
          MOVE      SP70,UPINUMBR
          MOVE      ZERO,VIEWTYPE
          MOVE      SP70,FRSTDATE
          MOVE      SP70,LASTDATE
          MOVE      SP70,VYEARMTH
          MOVE      SP70,NZPMICHG
.
          MOVE      SP70,SRCHSNAM
          MOVE      SP70,SRCHGNAM
          MOVE      SP70,SRCHMNAM
          MOVE      SP70,SRCHPSEX
          MOVE      SP70,SRCHPDOB
          MOVE      SP70,SRCHMEDI
          MOVE      SP70,SRCHNEXT
          MOVE      SP70,SRCHCONT
          MOVE      SP70,SRCHTYPE
          MOVE      SP70,SRCHUPIN
          MOVE      SP70,SRCHURNO
          MOVE      SP70,SRCHADMN
          MOVE      SP70,SRCHNPID
          MOVE      SP70,HL7CONTP
          MOVE      ZERO,LINKNUMB
          MOVE      Z70,QUICKREG
.
          MOVE      SP70,VALSTRNG
          MOVE      SP70,SRCHCATG
          MOVE      SP70,SRCHHCSC
.
          MOVE      ONE,F3
          WHILE     F3 <= 99
            PACK      UPDTFLDS[F3],Z70,Z70,Z70
            ADD       ONE,F3
          DO
.
CLRPAR99  RETURN
+
.------------------------------------------------------------------------------
.         Set Parameters
.------------------------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updttype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDTTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "urnumber=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,URNUMBER
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "admissno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ADMISSNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "remoteno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REMOTENO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nextpage=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NEXTPAGE
            GOTO      SETPAR99
          ENDIF 
.
          MATCH     "redirect=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REDIRECT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startnum=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTNUM
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startkey=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTKEY
            PACK      STARTKEY,STARTKEY,SP70
            REP       "~'",STARTKEY
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "norecord=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NORECORD
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "srchsnam=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SRCHSNAM
            PACK      SRCHSNAM,SRCHSNAM,SP70
            REP       "~'& % ",SRCHSNAM
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "srchgnam=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SRCHGNAM
            PACK      SRCHGNAM,SRCHGNAM,SP70
            REP       "~'& % ",SRCHGNAM
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "srchmnam=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SRCHMNAM
            PACK      SRCHMNAM,SRCHMNAM,SP70
            REP       "~'& % ",SRCHMNAM
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "srchpsex=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SRCHPSEX
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "srchpdob=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,KEY11   * Change Date as format is (DD MON CCYY)
            PACK      KEY11,KEY11,SP70
            UNPACK    KEY11,KEY8,KEY3
            MATCH     SP70,KEY3
            IF        !@EQUAL
              UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
              CALL      SETMTH00
              PACK      SRCHPDOB,CCENT,CYEAR,CMON,CDAY
              REP       " 0",SRCHPDOB
            ELSE
              PACK      SRCHPDOB,QRYDATA,SP70
            ENDIF
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "srchmedi=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SRCHMEDI
            PACK      SRCHMEDI,SRCHMEDI,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "srchnext=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SRCHNEXT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "srchcont=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SRCHCONT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "srchtype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SRCHTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "srchupin=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SRCHUPIN
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "srchurno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SRCHURNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "srchadmn=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SRCHADMN
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "srchnpid=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SRCHNPID
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "quickreg=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,QUICKREG
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "upinumbr=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPINUMBR
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "upinspid=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPINSPID
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updfld",QRYNAME
          IF        @EQUAL
            UNPACK    QRYNAME,KEY6,KEY2
            MOVE      ZERO,F2
            MOVE      KEY2,F2
.
            COMPARE   F2,NINTY9        * bail if array index already at 99
            GOTO      SETPAR99 IF EQUAL
.
.           Add one since the array index submitted is zero-based
            ADD       ONE,F2
.
            PACK      UPDTFLDS[F2],QRYDATA,SP100,SP100
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "frstdate=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FRSTDATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "lastdate=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,LASTDATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "viewtype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,VIEWTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "vyearmth=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,VYEARMTH
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "srchcatg=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SRCHCATG
            PACK      SRCHCATG,SRCHCATG,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "srchhcsc=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SRCHHCSC
            PACK      SRCHHCSC,SRCHHCSC,SP70
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
+
.------------------------------------------------------------
. Output Next Page Based on CGI Parameter nextpage
.------------------------------------------------------------
NXTPAG00  MOVE      ZERO,F1
          MOVE      NEXTPAGE,F1
          BRANCH    F1,NXTPAG10,NXTPAG20,NXTPAG30,NXTPAG40
.
          CALL      WINCLS00
          GOTO      NXTPAG99
.
NXTPAG10  CALL      RDIREC00
          GOTO      NXTPAG99
.
NXTPAG20  CALL      GOBACK00      * Go Back 1 Html page
          GOTO      NXTPAG99
.
NXTPAG30  CALL      DAH20000      * Go Back 2 html pages
          GOTO      NXTPAG99
.
NXTPAG40  CALL      PREDIR00      * Redirect Parent
          GOTO      NXTPAG99
.
NXTPAG99  RETURN
+
.------------------------------------------------------------------------------
.         Output Patient Enquiry Page
.------------------------------------------------------------------------------
SRHPAT00  MOVE      "LHD Patient Search",WEBTITLE
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,SRHPAT99
.
          CALL      WEBBOD00   * Output Templated Body of HTML
.
          CALL      WEBEND00   * Output End of Web Page
          MOVE      ZERO,EXIT
.
SRHPAT99  RETURN
+
.------------------------------------------------------------------------------
. Readin the surname index record
.------------------------------------------------------------------------------
.
RDSURN00  MOVE      SP1,VCASE
          LOAD      VCASE,PCASE,CCNOTES
.
RDSURN20  CALL      NEWS0000                * using new soundex file
.
RDSURN99  RETURN
+
.------------------------------------------------------------------------------
.                          NEWS0000
.         maintenance of the double soundex surname file
.------------------------------------------------------------------------------
.
NEWS0000  MOVE      PTMASNAM,GSRSNAM
          MOVE      PMPXFNAM,GSRGNAM
          MOVE      PMPXSNAM,PTGSGNM2
          MOVE      PBDATE,GSRDOB            * Date of birth
          MOVE      PSEX,GSRSEX              * Sex
          MOVE      PURNO,GSRURNO
          MOVE      ZEROVISN,GSRBILL
          MOVE      ZERO,GSRSYS
          CALL      SOUNDEX                  * get soundex key for surname
          CALL      SOUNDX2                  * get soundex key for given names
.
          PACK      KEY115,GSRURNO,GSRBILL,GSRSNAM,GSRGNAM,PTGSGNM2,GSRDOB:
                           GSRSEX
          CALL      RDPTGSR1                 * check if its already there
          IF        OVRCD = 1
            IF        SAVEALIA=1
              CALL      WRPTGSR1
            ENDIF
          ENDIF
.
NEWS9999  RETURN
+
.------------------------------------------------------------
. AMSV0000 - Subroutine to save master variables - From PATPRG31
.------------------------------------------------------------
.
AMSV0000  MOVE      PMICRO,SPMICRO
          MOVE      PUSR1,SPUSR1
          MOVE      PUSR2,SPUSR2
          MOVE      PUSR3,SPUSR3
          MOVE      PUSR4,SPUSR4
          MOVE      PUSR5,SPUSR5
          MOVE      PUSR6,SPUSR6
          MOVE      PUYN1,SPUYN1
          MOVE      PUYN2,SPUYN2
          MOVE      PUYN3,SPUYN3
.
          MOVE      PURNO,SPURNO
          MOVE      PTITL,SPTITL
          MOVE      PSNAME,SPSNAME
          MOVE      PTMASNAM,SPTMASNM
          MOVE      PGNAME,SPGNAME
          MOVE      PMPXFNAM,SPMPXFNM
          MOVE      PMPXSNAM,SPMPXSNM
          MOVE      PPSNAME,SPPSNAME
          MOVE      PSMOK,SPSMOK
          MOVE      PSEX,SPSEX
          MOVE      PMSTAT,SPMSTAT
          MOVE      PBDATE,SPBDATE
          MOVE      PSAGE,SPSAGE
          MOVE      PCONT,SPCONT
          MOVE      PREG,SPREG
          MOVE      PTYPE,SPTYPE
          MOVE      POCCUP,SPOCCUP
          MOVE      PMEDI,SPMEDI
          MOVE      PPENNO,SPPENNO
          MOVE      PPNDTE,SPPNDTE
          MOVE      PREPAT,SPREPAT
          MOVE      PCHGDTE,SPCHGDTE
          MOVE      PLOCDOC,SPLOCDOC
          MOVE      PCEASE,SPCEASE
.
          MOVE      PNKNAME,SPNKNAME
          MOVE      PNKADD1,SPNKADD1
          MOVE      PNKADD2,SPNKADD2
          MOVE      PNKSUBR,SPNKSUBR
          MOVE      PNKADD4,SPNKADD4
          MOVE      PNKPOST,SPNKPOST
          MOVE      PNKTELP,SPNKTELP
          MOVE      PNKTELB,SPNKTELB
          MOVE      PNKRELP,SPNKRELP
.
          CALL      PRAV0000                * Person Responsible A/C variables
.
          MOVE      PADD1,SPADD1
          MOVE      PADD2,SPADD2
          MOVE      PSUBRB,SPSUBRB
          MOVE      PADD4,SPADD4
          MOVE      PPOST,SPPOST
          MOVE      PTELEP,SPTELEP
          MOVE      PTELEB,SPTELEB
          MOVE      PTMXCELL,SPCELL
          MOVE      PTMXDOFR,SPDOR
          MOVE      PMPXRHC1,SPREGP
          MOVE      PMPXRH1G,SPGPPC
          MOVE      PMPXR1GC,SPGPPT
          MOVE      PMPXRHC1,SAVEPHCP
          MOVE      PMPXRH1G,SAVEPRAC
          MOVE      PMPXR1GC,SAVEPCTR
          MOVE      PTMXOSMS,SPOSMS
.
          MOVE      PHIGH1,SPHIGH1
          MOVE      PHIGH2,SPHIGH2
          MOVE      PHIGH3,SPHIGH3
          MOVE      PAUTOPY,SPAUTOPY
          MOVE      PDECDTE,SPDECDTE
          MOVE      PYRRES,SPYRRES
.
.-------- Save Community Card Details ---------------------------------
.
          MOVE      PTMXCARD,SPCARD
          MOVE      PTMXCEXP,SPCEXP
          MOVE      PTMXCXMP,SPCXMP
          MOVE      PTMXFMLY,SPFMLY
.
AMSV9999  RETURN
+
.------------------------------------------------------------
. PRAV0000  : save person responsible for account variables
.------------------------------------------------------------
.
PRAV0000  MOVE      PKNAME,SPKNAME
          MOVE      PKADD1,SPKADD1
          MOVE      PKADD2,SPKADD2
          MOVE      PKSUBR,SPKSUBR
          MOVE      PKADD4,SPKADD4
          MOVE      PKPOST,SPKPOST
          MOVE      PKTELEP,SPKTELP
          MOVE      PKTELEB,SPKTELB
          MOVE      PKRELP,SPKRELP
          MOVE      PTREMOBL,SPKMOBL
.
PRAV9999  RETURN
+
.------------------------------------------------------------------------------
.         Report 2 - Update patient (UR) data fields
.------------------------------------------------------------------------------
UPDPAT00  MATCH     SP70,UPDTTYPE           * Display
          GOTO      UPDPAT70 IF EQUAL
.
          MATCH     "A",UPDTTYPE
          GOTO      UPDPAT20 IF EQUAL
          MATCH     "U",UPDTTYPE
          GOTO      UPDPAT30 IF EQUAL
.
          GOTO      UPDPAT91
.
UPDPAT20  
          GOTO      UPDPAT99
.
.
.         Update patient (UR) data fields with those of UPI
.
UPDPAT30  MATCH     SP70,URNUMBER
          GOTO      UPDPAT92 IF EQUAL
.
          PACK      KEY8,URNUMBER,SP10
          CALL      RDMAST1
          BRANCH    OVRCD,UPDPAT92
.
          CALL      RDPMPX21
          BRANCH    OVRCD,UPDPAT92
.
          CALL      MVUPDF00           * Move data from update fields array
.
          CALL      IBACLOCK
          PACK      PCHGDTE,CCC,CYY,CMM,CDD
          REP       " 0",PCHGDTE
.
          CALL      UPMAST1
          CALL      UUPTMAS1
.
          CALL      UPPMPX21
.
          CALL      PMIGTNID                * get national id for dgate write
          MOVE      NMPNUMB,PTNINMPI
          MOVE      ONE,HL7TRGID
          MOVE      SP8,HL7INCLD
          PROC      DGCLICUP                * send details to Datagate *C-90223
.
          MOVE      PTMASNAM,GSRSNAM
          MOVE      PMPXFNAM,GSRGNAM
          MOVE      PMPXSNAM,PTGSGNM2
          MOVE      PURNO,GSRURNO
          MOVE      ZEROVISN,GSRBILL
          MOVE      ZERO,GSRSYS
          CALL      SOUNDEX
.
          CALL      UPDSUR
.
          CALL      RDSURN00           * Readin the surname index record
          CALL      AMSV0000           * Save master file variables
          CALL      UPDUR              * Update U/R number to the appropriate files
.
          GOTO      UPDPAT99
.
UPDPAT70  MOVE      "Update Patient Data",WEBTITLE
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,UPDPAT99
.
          CALL      WEBBOD00   * Output Templated Body of HTML
          CALL      WEBEND00   * Output End of Web Page
          MOVE      ONE,EXIT
          GOTO      UPDPAT99
.
UPDPAT91  MOVE      "Invalid Update Type",WEBTITLE
          CALL      WEBERR00
          GOTO      UPDPAT99
.
UPDPAT92  MOVE      "Invalid U/R Number",WEBTITLE
          CALL      WEBERR00
          GOTO      UPDPAT99
.
UPDPAT99  RETURN
+
.------------------------------------------------------------------------------
.         Report 3 - Patients Missing a UPI
.------------------------------------------------------------------------------
PATMIU00  MOVE      ZERO,EXIT
          RESET     UPDTTYPE
          MATCH     SP70,UPDTTYPE
          GOTO      PATMIU70 IF EQUAL
.
PATMIU70  CALL      IBACLOCK
          CALL      CURPER00
          CALL      CALCDT00   * Calculate Next and Last Period Dates
.
          MOVE      "Patients Without a UPI",WEBTITLE
          CALL      WEBHED00
          BRANCH    EXIT,PATMIU99
.
          CALL      WEBBOD00
          CALL      WEBEND00
          MOVE      ONE,EXIT
          GOTO      PATMIU99
.
PATMIU99  RETURN
+
.------------------------------------------------------------------------------
.         Remote Scripting Functions
.------------------------------------------------------------------------------
REMOTE00  CALL      XMLHED00
          BRANCH    EXIT,REMOTE99
.
          BRANCH    REMOTENO,REMOTE01,REMOTE02,REMOTE03,REMOTE04,REMOTE05:
                             REMOTE06,REMOTE07,REMOTE08,REMOTE09,REMOTE10
.
          WRITE     HTMLFILE;"<RETURN_VALUE TYPE=ERROR>":
                    " INVALID FUNCTION TYPE ":
                    "</RETURN_VALUE>"
          GOTO      REMOTE90
.
REMOTE01  CALL      CHKCON00      * Check LHD connection
          GOTO      REMOTE90
.
REMOTE02  CALL      CHKEAI00      * Check existing Alternate ID (UPI)
          GOTO      REMOTE90
.
REMOTE03  CALL      PATGET00      * Get UR patient data
          GOTO      REMOTE90
.
REMOTE04  MOVE      ZERO,DATRETFM * Set UPI patient data array (return) format
          CALL      UPIGET00      * Get UPI patient data (UPI Retrieve)
          GOTO      REMOTE90
.
REMOTE05  CALL      CHKEUR00      * Check existing UR
          GOTO      REMOTE90
.
REMOTE06  CALL      CHKEUP00      * Check existing UPI
          GOTO      REMOTE90
.
REMOTE07  CALL      UPIEXT00      * Validate if UPI exists for a UR
          GOTO      REMOTE90
.
REMOTE08  CALL      CCDGET00      * Get Category Code using HCS Equivalent value
          GOTO      REMOTE90
.
REMOTE09  MOVE      ONE,DATRETFM  * Set UPI patient data array (return) format
          CALL      UPIGET00      * Get UPI patient data (JSON format)
          GOTO      REMOTE90
.
REMOTE10  CALL      GETEXC00      * Add Extra Contacts for the UPI (JSON format)
          GOTO      REMOTE90
.
REMOTE90  CALL      XMLEND00
REMOTE99  RETURN
+
.------------------------------------------------------------------------------
.         Check LHD connection
.           EXIT = 0   Success
.                  1-5 Errors
.------------------------------------------------------------------------------
CHKCON00  MOVE      THREE,HL7SMODE     * Set mode for Connection Test
          CALL      A19HNDLR           * Format and send message
.
          COMPARE   ZERO,EXIT          * Handler call success?
          GOTO      CHKCON91 IF NOT EQUAL
.
CHKCON90  MOVE      ZERO,EXIT
          GOTO      CHKCON98
.
CHKCON91  MOVE      ONE,EXIT
          GOTO      CHKCON98
.
CHKCON98  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>",EXIT,"</RETURN_VALUE>"
CHKCON99  RETURN
+
.------------------------------------------------------------------------------
.         Check Existing Alternate ID (using UPI)
.           Returns a pipe-delimited string of the associated UR data, if the
.           Alternate ID (UPI) exists.
.
.           PARAMETER:
.             SRCHUPIN - UPI Number
.
.           RETURN:
.             0 = UPI does NOT exist in our Alternate ID table
.
.             OR
.
.             pipe-delimited string (U/R Number|Surname|Given Name)
.------------------------------------------------------------------------------
CHKEAI00  RJUSTIFY  SRCHUPIN
          PACK      KEY31,SP10,SRCHUPIN,SP70
          CALL      RSPMAID2
CHKEAI10  CALL      RKPMAID2
          BRANCH    OVRCD,CHKEAI90
.
          LJUSTIFY  PMAIALID
          MOVE      PMAIALID,DIM10
          RJUSTIFY  DIM10
.
          MATCH     DIM10,SRCHUPIN          * Alternate ID same as UPI ?
          GOTO      CHKEAI10 IF NOT EQUAL
.
          PACK      KEY8,PMAIURNO,SP10
          CALL      RDMAST1
          BRANCH    OVRCD,CHKEAI10
.
          GOTO      CHKEAI91
.
CHKEAI90  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>0</RETURN_VALUE>"
          GOTO      CHKEAI99
.
CHKEAI91  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>":
                             PMAIURNO,"|",PSNAME,"|",PGNAME,"|",PTITL:
                             "</RETURN_VALUE>"
          GOTO      CHKEAI99
.
CHKEAI99  RETURN
+
.------------------------------------------------------------------------------
.         Check Existing UR
.
.           PARAMETER:
.             SRCHURNO - UR Number
.
.           RETURN:
.             0 = UR does NOT exist
.             1 = UR exists
.------------------------------------------------------------------------------
CHKEUR00  RJUSTIFY  SRCHURNO
          PACK      KEY8,SRCHURNO,SP10
          CALL      RDAMAST1
          BRANCH    OVRCD,CHKEUR91
.
CHKEUR90  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>1</RETURN_VALUE>"
          GOTO      CHKEUR99
.
CHKEUR91  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>0</RETURN_VALUE>"
          GOTO      CHKEUR99
.
CHKEUR99  RETURN
+
.------------------------------------------------------------------------------
.         Check Existing UPI (LHD Retrieve)
.
.           PARAMETER:
.             SRCHUPIN - UPI Number
.             SRCHNPID - NSLHD Person ID
.
.           RETURN:
.             0 = UPI does NOT exist
.             1 = UPI exists
.------------------------------------------------------------------------------
CHKEUP00  MOVE      TWO,HL7SMODE       * Set mode for retrieve a selected UPI
          MOVE      ONE,HL7MXREC       * Set max records for retrieval
.
          MOVE      SRCHUPIN,HL7UPIVL  * Set the UPI to retrieve
          MOVE      SRCHNPID,HL7NSPID  * Set the NSLHD Person ID
          MOVE      SP70,HL7SURNM      * Clear Surname
          MOVE      SP70,HL7FGNAM      * Clear First Given Name
          MOVE      SP70,HL7SGNAM      * Clear Second Given Name
          MOVE      SP70,HL7PTDOB      * Clear DOB
          MOVE      SP70,HL7GENDR      * Clear Gender
.
          MOVE      ZERO,DATARCNT
          CALL      A19HNDLR           * Format and send message
.
          COMPARE   ZERO,EXIT          * Handler call success?
          GOTO      CHKEUP91 IF NOT EQUAL
.
          COMPARE   ZERO,DATARCNT      * Patient does not exist?
          GOTO      CHKEUP91 IF EQUAL
.
CHKEUP90  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>1</RETURN_VALUE>"
          GOTO      CHKEUP99
.
CHKEUP91  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>0</RETURN_VALUE>"
          GOTO      CHKEUP99
.
CHKEUP99  RETURN
+
.------------------------------------------------------------------------------
.         Validate a UPI exists (as Alternate ID; Indc4=U) for a UR Number 
.
.           PARAMETER:
.             SRCHURNO - UR Number
.
.           RETURN:
.             0 = UPI does NOT exist
.             1 = UPI exists
.------------------------------------------------------------------------------
UPIEXT00  RJUSTIFY  SRCHURNO
          PACK      KEY31,SRCHURNO,SP70
          CALL      RSPMAID1
UPIEXT10  CALL      RKPMAID1
          BRANCH    OVRCD,UPIEXT91
.
          MATCH     PMAIURNO,SRCHURNO
          GOTO      UPIEXT91 IF NOT EQUAL
.
          PACK      KEY5,ANSA,ANSI,PMAITYPE,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,UPIEXT10
.
          MATCH     "U",TCINDC4
          GOTO      UPIEXT10 IF NOT EQUAL
.
UPIEXT90  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>1</RETURN_VALUE>"
          GOTO      UPIEXT99
.
UPIEXT91  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>0</RETURN_VALUE>"
          GOTO      UPIEXT99
.
UPIEXT99  RETURN
+
.------------------------------------------------------------------------------
.         Get Category Code using HCS Equivalent value - Remote Script
.------------------------------------------------------------------------------
CCDGET00  MOVE      SRCHCATG,CODCATEG
          MOVE      SRCHHCSC,CODHCSEQ
          CALL      GETCCD00
          BRANCH    EXIT,CCDGET91
.
CCDGET90  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>",RETCCODE,"</RETURN_VALUE>"
          GOTO      CCDGET99
.
CCDGET91  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE></RETURN_VALUE>"
          GOTO      CCDGET99
.
CCDGET99  RETURN
+
.------------------------------------------------------------------------------
.         Clear Patient Data Fields Array (ARRPATFL)
.------------------------------------------------------------------------------
CLPATA00  MOVE      ONE,LOOPCNT1
          WHILE     LOOPCNT1 <= MAXPATFL
            PACK      ARRPATFL[LOOPCNT1],SP100
            ADD       ONE,LOOPCNT1
          DO
CLPATA99  RETURN
+
.------------------------------------------------------------------------------
.         Strip Patient Data Fields Array (ARRPATFL) Values
.------------------------------------------------------------------------------
STPATA00  MOVE      ONE,LOOPCNT1
          WHILE     LOOPCNT1 <= MAXPATFL
            STRIP     ARRPATFL[LOOPCNT1]
            ADD       ONE,LOOPCNT1
          DO
STPATA99  RETURN
+
.------------------------------------------------------------------------------
.         Output patient data array (Remote Script return value)
.------------------------------------------------------------------------------
OUTARR00  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>";
          MOVE      ONE,LOOPCNT1
          WHILE     LOOPCNT1 < MAXPATFL
            WRITE     HTMLFILE;*+,ARRPATFL[LOOPCNT1],*-,"|";
            ADD       ONE,LOOPCNT1
          DO
          WRITE     HTMLFILE;*+,ARRPATFL[LOOPCNT1],*-:
                             "</RETURN_VALUE>"
.
OUTARR99  RETURN
+
.------------------------------------------------------------------------------
.         Output patient data array (Remote Script return value) - JSON format
.------------------------------------------------------------------------------
OUTJSN00  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>";
          WRITE     HTMLFILE;"[";
.
          WRITE     HTMLFILE;"{#"name#":#"ptmas001#",":
                             " #"value#":#"",*+,ARRPATFL[ONE],*-,"#"},";
          WRITE     HTMLFILE;"{#"name#":#"ptmas005#",":
                             " #"value#":#"",*+,ARRPATFL[TWO],*-,"#"},";
          WRITE     HTMLFILE;"{#"name#":#"pmpmi084#",":
                             " #"value#":#"",*+,ARRPATFL[THREE],*-,"#"},";
          WRITE     HTMLFILE;"{#"name#":#"pmpmi085#",":
                             " #"value#":#"",*+,ARRPATFL[FOUR],*-,"#"},";
          WRITE     HTMLFILE;"{#"name#":#"ptmas015#",":
                             " #"value#":#"",*+,ARRPATFL[FIVE],*-,"#"},";
          WRITE     HTMLFILE;"{#"name#":#"ptmas017#",":
                             " #"value#":#"",*+,ARRPATFL[SIX],*-,"#"},";
          WRITE     HTMLFILE;"{#"name#":#"ptmas008#",":
                             " #"value#":#"",*+,ARRPATFL[SEVEN],*-,"#"},";
          WRITE     HTMLFILE;"{#"name#":#"ptmas009#",":
                             " #"value#":#"",*+,ARRPATFL[EIGHT],*-,"#"},";
          WRITE     HTMLFILE;"{#"name#":#"ptmas010#",":
                             " #"value#":#"",*+,ARRPATFL[NINE],*-,"#"},";
          WRITE     HTMLFILE;"{#"name#":#"ptmas011#",":
                             " #"value#":#"",*+,ARRPATFL[TEN],*-,"#"},";
          WRITE     HTMLFILE;"{#"name#":#"ptmas012#",":
                             " #"value#":#"",*+,ARRPATFL[TEN1],*-,"#"},";
          WRITE     HTMLFILE;"{#"name#":#"ptmas013#",":
                             " #"value#":#"",*+,ARRPATFL[TEN2],*-,"#"},";
          WRITE     HTMLFILE;"{#"name#":#"ptmsx039#",":
                             " #"value#":#"",*+,ARRPATFL[TEN3],*-,"#"},";
          WRITE     HTMLFILE;"{#"name#":#"ptmas014#",":
                             " #"value#":#"",*+,ARRPATFL[TEN4],*-,"#"},";
          WRITE     HTMLFILE;"{#"name#":#"ptmas019#",":
                             " #"value#":#"",*+,ARRPATFL[TEN5],*-,"#"},";
          WRITE     HTMLFILE;"{#"name#":#"pmpmi002#",":
                             " #"value#":#"",*+,ARRPATFL[TEN6],*-,"#"},";
          WRITE     HTMLFILE;"{#"name#":#"pmpmi001#",":
                             " #"value#":#"",*+,ARRPATFL[TEN7],*-,"#"},";
          WRITE     HTMLFILE;"{#"name#":#"pmpmi065#",":
                             " #"value#":#"",*+,ARRPATFL[TEN8],*-,"#"},";
          WRITE     HTMLFILE;"{#"name#":#"ptmas023#",":
                             " #"value#":#"",*+,ARRPATFL[TEN9],*-,"#"},";
          WRITE     HTMLFILE;"{#"name#":#"pmpmi016#",":
                             " #"value#":#"",*+,ARRPATFL[TWENTY],*-,"#"},";
          WRITE     HTMLFILE;"{#"name#":#"ptmas021#",":
                             " #"value#":#"",*+,ARRPATFL[TWENTY1],*-,"#"}";
.
          WRITE     HTMLFILE;"]</RETURN_VALUE>"
.
OUTJSN99  RETURN
+

.------------------------------------------------------------------------------
.         Get Category Code / Description (using HCS Equivalent value)
.
.         Parameters:
.           CODCATEG - Category
.           CODHCSEQ - HCS Equivalent
.
.         Returns:
.           RETCCODE - Category Code
.           RETTDESC - Description
.           RETHCSVL - HCS Value
.------------------------------------------------------------------------------
GETCCD00  MOVE      SP70,RETCCODE
          MOVE      SP70,RETTDESC
          MOVE      SP70,RETHCSVL
.
          PACK      KEY9,CODCATEG,CODHCSEQ,SP70
          CALL      RDSCODE5
GETCCD10  CALL      RDKCODE5
          BRANCH    OVRCD,GETCCD91
.
          MATCH     TCODE,CODCATEG
          GOTO      GETCCD91 IF NOT EQUAL
.
          MATCH     THCSCOD,CODHCSEQ
          GOTO      GETCCD91 IF NOT EQUAL
.
          MATCH     "A",PTCOACTV                 * Active?
          GOTO      GETCCD10 IF NOT EQUAL
.
          MOVE      ACODE,RETCCODE
          MOVE      THCSCOD,RETHCSVL
          MOVE      TDESC,RETTDESC
          STRIP     RETTDESC
.
          MOVE      ZERO,EXIT
          GOTO      GETCCD99
.
GETCCD91  MOVE      ONE,EXIT
GETCCD99  RETURN
+
.------------------------------------------------------------------------------
.         Get Category Code / Description (using HCS Equiv character & position)
.
.         Parameters:
.           CODCATEG - Category
.           CODCHARV - HCS Equivalent Character Value
.           CODPOSTN - HCS Equivalent Character Position (to match)
.
.         Returns:
.           RETCCODE - Category Code
.           RETTDESC - Description
.           RETHCSVL - HCS Value
.------------------------------------------------------------------------------
GETCCP00  MOVE      SP70,RETCCODE
          MOVE      SP70,RETTDESC
          MOVE      SP70,RETHCSVL
.
          PACK      KEY5,CODCATEG,SP70
          CALL      RDSCODE1
GETCCP10  CALL      RDKCODE1
          BRANCH    OVRCD,GETCCP91
.
          MATCH     TCODE,CODCATEG
          GOTO      GETCCP91 IF NOT EQUAL
.
          MATCH     "A",PTCOACTV                 * Active?
          GOTO      GETCCP10 IF NOT EQUAL
.
          BRANCH    CODPOSTN,GETCCP11,GETCCP12,GETCCP13,GETCCP14
          GOTO      GETCCP91
.
GETCCP11  UNPACK    THCSCOD,DIM1,D3
          GOTO      GETCCP20
.
GETCCP12  UNPACK    THCSCOD,D1,DIM1,D2
          GOTO      GETCCP20
.
GETCCP13  UNPACK    THCSCOD,D2,DIM1,D1
          GOTO      GETCCP20
.
GETCCP14  UNPACK    THCSCOD,D3,DIM1
          GOTO      GETCCP20
.
GETCCP20  MATCH     DIM1,CODCHARV
          GOTO      GETCCP10 IF NOT EQUAL
.
          MOVE      ACODE,RETCCODE
          MOVE      THCSCOD,RETHCSVL
          MOVE      TDESC,RETTDESC
          STRIP     RETTDESC
.
          MOVE      ZERO,EXIT
          GOTO      GETCCP99
.
GETCCP91  MOVE      ONE,EXIT
GETCCP99  RETURN
+
.------------------------------------------------------------------------------
.         Get Category Code for a Matching Indicator Value
.
.         Parameters:
.           CODCATEG - Category
.           INDPOSTN - Indicator Position
.           INDVALUE - Indicator Value
.
.         Returns:
.           RETCCODE - Category Code
.           RETTDESC - Description
.------------------------------------------------------------------------------
GETCCI00  MOVE      ZERO,EXIT
          MOVE      SP70,RETCCODE
          MOVE      SP70,RETTDESC
.
          PACK      KEY5,CODCATEG,SP70
          CALL      RDSCODE1
GETCCI10  CALL      RDKCODE1
          BRANCH    OVRCD,GETCCI91
.
          MATCH     TCODE,CODCATEG
          GOTO      GETCCI91 IF NOT EQUAL
.
          MATCH     "A",PTCOACTV                 * Active?
          GOTO      GETCCI10 IF NOT EQUAL
.
          BRANCH    INDPOSTN,GETCCI01
          GOTO      GETCCI91
.
GETCCI01  MATCH     TCINDC1,INDVALUE
          GOTO      GETCCI91 IF NOT EQUAL
          GOTO      GETCCI90
.
GETCCI90  MOVE      ACODE,RETCCODE
          MOVE      TDESC,RETTDESC
.
          MOVE      ZERO,EXIT
          GOTO      GETCCI99
.
GETCCI91  MOVE      ONE,EXIT
GETCCI99  RETURN
+
.------------------------------------------------------------------------------
.         Get Title Description for a Title Code
.------------------------------------------------------------------------------
GETTIT00  MOVE      SP70,TITLDESC
          PACK      KEY4,TITLCODE,SP70
          CALL      RDPMTLE1
          BRANCH    OVRCD,GETTIT91
.
          MATCH     "1",PMTLACTV            * Inactive?
          GOTO      GETTIT91 IF EQUAL
.
          MOVE      PMTLDESC,TITLDESC
.
          MOVE      ZERO,EXIT
          GOTO      GETTIT99
.
GETTIT91  MOVE      ONE,EXIT
GETTIT99  RETURN
+
.------------------------------------------------------------------------------
.         Get Patient Data Fields (UR) - Remote Script
.------------------------------------------------------------------------------
PATGET00  PACK      KEY8,SRCHURNO,SP10
          CALL      RDMAST1
          BRANCH    OVRCD,PATGET90
.
          CALL      RDPMPX21
          BRANCH    OVRCD,PATGET90
.
          CALL      CLPATA00           * Clear patient data fields array
.
          MOVE      PTITL,TITLCODE
          CALL      GETTIT00
          IF        EXIT=0
            MOVE      TITLDESC,ARRPATFL[1]            * Title
          ELSE
            PACK    ARRPATFL[1],PTITL,TILDA,FAILFLAG  * Invalid Code
          ENDIF
          MOVE      PSNAME,ARRPATFL[2]                * Surname
          MOVE      PMPXFNAM,ARRPATFL[3]              * First Given Name
          MOVE      PMPXSNAM,ARRPATFL[4]              * Second Given Name
          MOVE      PSEX,ARRPATFL[5]                  * Gender
          MOVE      PBDATE,ARRPATFL[6]                * Date of Birth
          MOVE      PADD1,ARRPATFL[7]                 * Address Line 1
          MOVE      PADD2,ARRPATFL[8]                 * Address Line 2
          MOVE      PSUBRB,ARRPATFL[9]                * Suburb
          MOVE      PADD4,ARRPATFL[10]                * State
          MOVE      PPOST,ARRPATFL[11]                * Postcode
          MOVE      PTELEP,ARRPATFL[12]               * Home Phone
          MOVE      PTMXCELL,ARRPATFL[13]             * Mobile Phone
          MOVE      PTELEB,ARRPATFL[14]               * Work Phone
.
.         Country of Birth (Cat C)
          MATCH     SP70,PCONT
          IF        !@EQUAL
            PACK      KEY5,ANSC,SP1,PCONT,SP70
            CALL      RDCODE1
            IF        OVRCD=0
              MOVE      TDESC,ARRPATFL[15]            * Country of Birth
            ELSE
              PACK      ARRPATFL[15],PCONT,TILDA,FAILFLAG  * Invalid Code
            ENDIF
          ENDIF
.
.         Preferred Language (Cat LA)
          MATCH     SP70,PMPXLNG1
          IF        !@EQUAL
            PACK      KEY5,ANSL,ANSA,PMPXLNG1,SP70
            CALL      RDCODE1
            IF        OVRCD=0
              MOVE      TDESC,ARRPATFL[16]            * Preferred Language
            ELSE
              PACK      ARRPATFL[16],PMPXLNG1,TILDA,FAILFLAG  * Invalid Code
            ENDIF
          ENDIF
.
          MOVE      " ",ARRPATFL[17]                  * Interpreter Required
          MATCH     "0",PMPXINTR
          IF        @EQUAL
            MOVE      "N",ARRPATFL[17]
          ENDIF
          MATCH     "1",PMPXINTR
          IF        @EQUAL
            MOVE      "Y",ARRPATFL[17]
          ENDIF
.
.         Preferred Language (Cat VA)
          MATCH     SP70,PMPXABRG
          IF        !@EQUAL
            PACK      KEY5,ANSV,ANSA,PMPXABRG,SP70
            CALL      RDCODE1
            IF        OVRCD=0
              MOVE      TDESC,ARRPATFL[18]            * Preferred Language
            ELSE
              PACK      ARRPATFL[18],PMPXABRG,TILDA,FAILFLAG  * Invalid Code
            ENDIF
          ENDIF
.
          PACK      ARRPATFL[19],PMEDI,PTMXMCCD       * Medicare Number with IRN
          MOVE      PMPXMEDC,ARRPATFL[20]             * Medicare Valid To Date
.
          CALL      STPATA00           * Strip spaces from values
          CALL      OUTARR00           * Output patient data array         
          GOTO      PATGET99
.
PATGET90  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>0</RETURN_VALUE>"
          GOTO      PATGET99
.
PATGET91  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=ERROR>":
                             "UR Number not found.":
                             "</RETURN_VALUE>"
.
PATGET99  RETURN
+
.------------------------------------------------------------------------------
.         Get Patient Data Fields (UPI Retrieve) - Remote Script
.------------------------------------------------------------------------------
UPIGET00  MOVE      TWO,HL7SMODE       * Set mode for retrieve a selected UPI
          MOVE      ONE,HL7MXREC       * Set max records for retrieval
.
          MOVE      SRCHUPIN,HL7UPIVL  * Set the UPI to retrieve
          MOVE      SRCHNPID,HL7NSPID  * Set the NSLHD Person ID
          MOVE      SP70,HL7SURNM      * Clear Surname
          MOVE      SP70,HL7FGNAM      * Clear First Given Name
          MOVE      SP70,HL7SGNAM      * Clear Second Given Name
          MOVE      SP70,HL7PTDOB      * Clear DOB
          MOVE      SP70,HL7GENDR      * Clear Gender
.
          CALL      A19HNDLR           * Format and send message
.
          COMPARE   ZERO,EXIT          * Handler call success?
          GOTO      UPIGET90 IF NOT EQUAL
.
          COMPARE   ZERO,DATARCNT      * Patient does not exist?
          GOTO      UPIGET90 IF EQUAL
.
          CALL      CLPATA00           * Clear patient data fields array
.
          MOVE      DATARRAY[1][1],TITLCODE           * Title
          CALL      GETTIT00
          IF        EXIT=0
            PACK      ARRPATFL[1],TITLDESC,TILDA,TITLCODE
          ELSE
            PACK      ARRPATFL[1],TITLCODE,TILDA,FAILFLAG  * Invalid Code
          ENDIF
.
          MOVE      DATARRAY[1][2],ARRPATFL[2]        * Surname
          MOVE      DATARRAY[1][3],ARRPATFL[3]        * First Given Name
          MOVE      DATARRAY[1][4],ARRPATFL[4]        * Second Given Name
          MOVE      DATARRAY[1][5],ARRPATFL[5]        * Gender
          MOVE      DATARRAY[1][6],ARRPATFL[6]        * Date of Birth
          MOVE      DATARRAY[1][7],ARRPATFL[7]        * Address Line 1
          MOVE      DATARRAY[1][8],ARRPATFL[8]        * Address Line 2
          MOVE      DATARRAY[1][9],ARRPATFL[9]        * Suburb
          MOVE      DATARRAY[1][10],ARRPATFL[10]      * State
          MOVE      DATARRAY[1][11],ARRPATFL[11]      * Postcode
          MOVE      DATARRAY[1][17],ARRPATFL[12]      * Home Phone
          MOVE      DATARRAY[1][18],ARRPATFL[13]      * Mobile Phone
          MOVE      DATARRAY[1][19],ARRPATFL[14]      * Work Phone
.
.         Country of Birth (Cat C - THCSCOD)
          MOVE      "C ",CODCATEG
          PACK      CODHCSEQ,DATARRAY[1][20],SP70
          CALL      GETCCD00      * Get Cat Code Description (using HCS Equiv)
          IF        EXIT=0
            PACK      ARRPATFL[15],RETTDESC,TILDA,RETHCSVL
          ELSE
            PACK      ARRPATFL[15],DATARRAY[1][20],TILDA,FAILFLAG  * Invalid
          ENDIF
.
.         Preferred Language (Cat LA - THCSCOD)
          MOVE      "LA",CODCATEG
          PACK      CODHCSEQ,DATARRAY[1][21],SP70
          CALL      GETCCD00      * Get Cat Code Description (using HCS Equiv)
          IF        EXIT=0
            PACK      ARRPATFL[16],RETTDESC,TILDA,RETHCSVL
          ELSE
            PACK      ARRPATFL[16],DATARRAY[1][21],TILDA,FAILFLAG  * Invalid
          ENDIF
.
          MOVE      DATARRAY[1][22],ARRPATFL[17]      * Interpreter Req'd
.
.         Aboriginality (Cat VA - THCSCOD)
          MOVE      "VA",CODCATEG
          MOVE      DATARRAY[1][23],CODCHARV
          MOVE      "1",CODPOSTN       * match HCS Equiv value at position 1
          CALL      GETCCP00           * Get Cat Code Description
          IF        EXIT=0
            PACK      ARRPATFL[18],RETTDESC,TILDA,RETHCSVL
          ELSE
            PACK      ARRPATFL[18],DATARRAY[1][23],TILDA,FAILFLAG  * Invalid
          ENDIF
.
          MOVE      DATARRAY[1][13],ARRPATFL[19]      * Medicare Number
          MOVE      DATARRAY[1][24],ARRPATFL[20]      * Medicare Valid Date
.
.         Resident Status (Cat T - TCINDC1)
          MOVE      "T ",CODCATEG           * Category T
          MOVE      ONE,INDPOSTN            * Indicator position 1
          MOVE      DATARRAY[1][25],INDVALUE   * Indicator value
          CALL      GETCCI00           * Get Cat Code using indicator value
          IF        EXIT=0
            PACK      ARRPATFL[21],RETTDESC,TILDA,RETCCODE
          ELSE
            PACK      ARRPATFL[21],DATARRAY[1][25],TILDA,FAILFLAG  * Invalid
          ENDIF
.
          CALL      STPATA00           * Strip spaces from values
.
          BRANCH    DATRETFM,UPIGET10
          CALL      OUTARR00           * Output patient data array
          GOTO      UPIGET99
.
UPIGET10  CALL      OUTJSN00           * Output patient data array (JSON format)
          GOTO      UPIGET99
.
UPIGET90  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>0</RETURN_VALUE>"
          GOTO      UPIGET99
.
UPIGET99  RETURN
+
.------------------------------------------------------------------------------
.         Move update data (array) fields to corresponding file variables
.------------------------------------------------------------------------------
MVUPDF00  MOVE      ZERO,EXIT
.
.         1 - Title
.         2 - Surname
.         3 - First Given Name
.         4 - Second Given Name
.         5 - Gender
.         6 - Date of Birth
.         7 - Address Line 1   ( Street Address Line 1)
.         8 - Address Line 2   ( Street Address Line 2)
.         9 - Address Line 3   ( Suburb )
.         10 - Address Line 4  ( State )
.         11 - Address Line 5  ( Postcode )
.         12 - Home Phone
.         13 - Mobile Phone
.         14 - Work Phone
.         15 - Country of Birth
.         16 - Preferred Language
.         17 - Interpreter Required (0, 1 or U)
.         18 - Race (Aboriginality)
.         19 - Medicare Number
.         20 - Medicare Valid To Date
.
          MOVE      ONE,F2
          MATCH     UPDTFLDS[F2],Z70
          IF        !@EQUAL
            MOVE      UPDTFLDS[F2],PTITL         * Title
          ENDIF
.
          MOVE      TWO,F2
          MATCH     UPDTFLDS[F2],Z70
          IF        !@EQUAL
            MOVE      UPDTFLDS[F2],PSNAME        * Surname
            MOVE      UPDTFLDS[F2],PTMASNAM
          ENDIF
.
          MOVE      THREE,F2
          MATCH     UPDTFLDS[F2],Z70
          IF        !@EQUAL
            MOVE      UPDTFLDS[F2],PGNAME        * First Given Name
            MOVE      UPDTFLDS[F2],PMPXFNAM
          ENDIF
.
          MOVE      FOUR,F2
          MATCH     UPDTFLDS[F2],Z70
          IF        !@EQUAL
            MOVE      UPDTFLDS[F2],PMPXSNAM      * Second Given Name
          ENDIF
.
          MOVE      FIVE,F2
          MATCH     UPDTFLDS[F2],Z70
          IF        !@EQUAL
            MOVE      UPDTFLDS[F2],PSEX          * Gender
          ENDIF
.
          MOVE      SIX,F2
          MATCH     UPDTFLDS[F2],Z70
          IF        !@EQUAL
            MOVE      UPDTFLDS[F2],PBDATE        * Date of Birth
          ENDIF
.
          MOVE      SEVEN,F2
          MATCH     UPDTFLDS[F2],Z70
          IF        !@EQUAL
            MOVE      UPDTFLDS[F2],PADD1         * Address Line 1
          ENDIF
.
          MOVE      EIGHT,F2
          MATCH     UPDTFLDS[F2],Z70
          IF        !@EQUAL
            MOVE      UPDTFLDS[F2],PADD2         * Address Line 2
          ENDIF
.
          MOVE      NINE,F2
          MATCH     UPDTFLDS[F2],Z70
          IF        !@EQUAL
            MOVE      UPDTFLDS[F2],PSUBRB        * Suburb
          ENDIF
.
          MOVE      TEN,F2
          MATCH     UPDTFLDS[F2],Z70
          IF        !@EQUAL
            MOVE      UPDTFLDS[F2],PADD4         * State
          ENDIF
.
          MOVE      TEN1,F2
          MATCH     UPDTFLDS[F2],Z70
          IF        !@EQUAL
            MOVE      UPDTFLDS[F2],PPOST         * Postcode
          ENDIF
.
          MOVE      TEN2,F2
          MATCH     UPDTFLDS[F2],Z70
          IF        !@EQUAL
            MOVE      UPDTFLDS[F2],PTELEP        * Home Phone (Private)
          ENDIF
.
          MOVE      TEN3,F2
          MATCH     UPDTFLDS[F2],Z70
          IF        !@EQUAL
            MOVE      UPDTFLDS[F2],PTMXCELL      * Mobile Phone
          ENDIF
.
          MOVE      TEN4,F2
          MATCH     UPDTFLDS[F2],Z70
          IF        !@EQUAL
            MOVE      UPDTFLDS[F2],PTELEB        * Work Phone (Business)
          ENDIF
.
          MOVE      TEN5,F2
          MATCH     UPDTFLDS[F2],Z70
          IF        !@EQUAL
            MATCH     SP70,UPDTFLDS[F2]
            IF        @EQUAL
              MOVE      SP70,PCONT
            ELSE
              MOVE      "C ",CODCATEG
              PACK      CODHCSEQ,UPDTFLDS[F2],SP70
              CALL      GETCCD00
              IF        EXIT=0
                MOVE      RETCCODE,PCONT         * Country of Birth (Cat C)
              ENDIF
            ENDIF
          ENDIF
.
          MOVE      TEN6,F2
          MATCH     UPDTFLDS[F2],Z70
          IF        !@EQUAL
            MATCH     SP70,UPDTFLDS[F2]
            IF        @EQUAL
              MOVE      SP70,PMPXLNG1
            ELSE
              MOVE      "LA",CODCATEG
              PACK      CODHCSEQ,UPDTFLDS[F2],SP70
              CALL      GETCCD00
              IF        EXIT=0
                MOVE      RETCCODE,PMPXLNG1      * Preferred Language (Cat LA)
              ENDIF
            ENDIF
          ENDIF
.
          MOVE      TEN7,F2
          MATCH     UPDTFLDS[F2],Z70
          IF        !@EQUAL
            MOVE      UPDTFLDS[F2],PMPXINTR      * Interpreter Required (Y/N)
          ENDIF
.
          MOVE      TEN8,F2
          MATCH     UPDTFLDS[F2],Z70
          IF        !@EQUAL
            MATCH     SP70,UPDTFLDS[F2]
            IF        @EQUAL
              MOVE      SP70,PMPXABRG
            ELSE
              MOVE      "VA",CODCATEG
              MOVE      UPDTFLDS[F2],CODCHARV
              MOVE      "1",CODPOSTN   * match HCS Equiv value at position 1
              CALL      GETCCP00       * Get Cat Code
              IF        EXIT=0
                MOVE      RETCCODE,PMPXABRG      * Race / Aboriginality (Cat VA)
              ENDIF
            ENDIF
          ENDIF
.
          MOVE      TEN9,F2
          MATCH     UPDTFLDS[F2],Z70
          IF        !@EQUAL
            UNPACK    UPDTFLDS[F2],DIM10,DIM2
            MOVE      DIM10,PMEDI                * Medicare Number
            MOVE      DIM2,PTMXMCCD              * IRN
          ENDIF
.
          MOVE      TWENTY,F2
          MATCH     UPDTFLDS[F2],Z70
          IF        !@EQUAL
            MOVE      UPDTFLDS[F2],PMPXMEDC      * Medicare Valid To Date
          ENDIF
.
MVUPDF99  RETURN
+
.------------------------------------------------------------------------------
.         Redirect Parent URL
.------------------------------------------------------------------------------
PREDIR00  CALL      OPENHTML
          BRANCH    EXIT,PREDIR90
.
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             " parent.location.href=#"",REDIRECT,"#";":
                             "}":
                             "</script>"
          CALL      CLOSHTML
          GOTO      PREDIR99
.
PREDIR90  DISPLAY   "*** Fifo Not Found ***"
.
PREDIR99  RETURN
+
.-------------------------------------------------------------------------------
.         Goes back 1 page
.-------------------------------------------------------------------------------
GOBACK00  CALL      OPENHTML
          BRANCH    EXIT,GOBACK99
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "    alert(#"",WEBTITLE,"#");":
                             "    self.close();":
                             "    opener.history.go(-1);":
                             "}":
                             "</script>"
          CALL      CLOSHTML
GOBACK99  RETURN
+
.-------------------------------------------------------------------------------
.         Process Fields 
.-------------------------------------------------------------------------------
PROFLD00  BRANCH    REPORTNO,PROFLD10,PROFLD20,PROFLD30
          GOTO      PROFLD99 
.
PROFLD10  BRANCH    FLDSETNO,PROFLD11
          GOTO      PROFLD99
.
PROFLD11  CALL      PATS0000           * Patient Search Fields
          GOTO      PROFLD99
.
PROFLD20  BRANCH    FLDSETNO,PROFLD21
          GOTO      PROFLD99
.
PROFLD21  CALL      PATS0000           * Patient Search Fields
          GOTO      PROFLD99
.
PROFLD30  BRANCH    FLDSETNO,PROFLD31
          GOTO      PROFLD99
.
PROFLD31  CALL      PMIU0000           * Patients Missing a UPI
          GOTO      PROFLD99
.
PROFLD99  RETURN
+
.-------------------------------------------------------------------------------
.         Patient Search Fields
.-------------------------------------------------------------------------------
PATS0000  BRANCH    FLDITMNO,PATS0100,PATS0200,PATS0300,PATS0400,PATS0500:
                             PATS0600,PATS0700,PATS0800,PATS0900,PATS1000:
                             PATS1100,PATS1200,PATS1300,PATS1400,PATS1500:
                             PATS1600,PATS1700,PATS1800
          GOTO      PATS9999
.
PATS0100  CALL      NEXT0000           * Next group of results
          GOTO      PATS9999
.
PATS0200  CALL      PREV0000           * Previous group of results
          GOTO      PATS9999
.
PATS0300  MATCH     SP70,SRCHSNAM           * Surname
          GOTO      PATS9999 IF EQUAL
          STRIP     SRCHSNAM
          MOVELPTR  SRCHSNAM,LENGTH
          COMPARE   ZERO,LENGTH
          GOTO      PATS9999 IF EQUAL
          SFORMAT   VAR,LENGTH
          MOVE      SRCHSNAM,VAR
          WRITE     HTMLFILE;VAR;
          SFORMAT   VAR,127
          GOTO      PATS9999
.
PATS0400  MATCH     SP70,SRCHGNAM           * First Given Name
          GOTO      PATS9999 IF EQUAL
          STRIP     SRCHGNAM
          MOVELPTR  SRCHGNAM,LENGTH
          COMPARE   ZERO,LENGTH
          GOTO      PATS9999 IF EQUAL
          SFORMAT   VAR,LENGTH
          MOVE      SRCHGNAM,VAR
          WRITE     HTMLFILE;VAR;
          SFORMAT   VAR,127
          GOTO      PATS9999
.
PATS0500  MATCH     SP70,SRCHMNAM           * Second Given Name
          GOTO      PATS9999 IF EQUAL
          STRIP     SRCHMNAM
          MOVELPTR  SRCHMNAM,LENGTH
          COMPARE   ZERO,LENGTH
          GOTO      PATS9999 IF EQUAL
          SFORMAT   VAR,LENGTH
          MOVE      SRCHMNAM,VAR
          WRITE     HTMLFILE;VAR;
          SFORMAT   VAR,127
          GOTO      PATS9999
.

PATS0600  MATCH     SP70,SRCHPSEX           * Sex
          GOTO      PATS9999 IF EQUAL
          WRITE     HTMLFILE;SRCHPSEX;
          GOTO      PATS9999
.
PATS0700  MATCH     SP70,SRCHPDOB           * Date of Birth
          GOTO      PATS9999 IF EQUAL
          GOTO      PATS9999 IF EOS
          UNPACK    SRCHPDOB,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      PATS9999
.
PATS0800  MATCH     SP70,SRCHMEDI           * Medicare Number
          GOTO      PATS9999 IF EQUAL
          STRIP     SRCHMEDI
          MOVELPTR  SRCHMEDI,LENGTH
          COMPARE   ZERO,LENGTH
          GOTO      PATS9999 IF EQUAL
          SFORMAT   VAR,LENGTH
          MOVE      SRCHMEDI,VAR
          WRITE     HTMLFILE;VAR;
          SFORMAT   VAR,127
          GOTO      PATS9999
.
PATS0900  WRITE     HTMLFILE;*+,HL7CONTP,*-;   * Search (HL7) Continuation Point
          GOTO      PATS9999
.
PATS1000  UNPACK    DIM127,D1,KEY3,DIM127
          MOVE      KEY3,MAXRECRD
          UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      KEY1,LINKNUMB
          UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
          MOVE      ZERO,F1
          MOVE      SRCHTYPE,F1
          PERFORM   F1,TABOUT00             * Search Results Output
          GOTO      PATS9999
.
PATS1100  UNPACK    DIM127,D1,KEY3,DIM127
          MOVE      KEY3,MAXRECRD
          UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      KEY1,LINKNUMB
          UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
          MOVE      ZERO,F1
          MOVE      SRCHTYPE,F1
          PERFORM   F1,TABLST00             * Search Results Output (condensed)
          GOTO      PATS9999
.
PATS1200  WRITE     HTMLFILE;SRCHADMN;      * Search Admission No.
          GOTO      PATS9999
.
PATS1300  WRITE     HTMLFILE;NORECORD;      * Number of Records
          GOTO      PATS9999
.
PATS1400  WRITE     HTMLFILE;QUICKREG;      * Quick Registration
          GOTO      PATS9999
.
PATS1500  WRITE     HTMLFILE;URNUMBER;      * UR Number
          GOTO      PATS9999
.
PATS1600  WRITE     HTMLFILE;UPINUMBR;      * UPI Number
          GOTO      PATS9999
.
PATS1700  WRITE     HTMLFILE;REDIRECT;      * Redirect URL
          GOTO      PATS9999
.
PATS1800  WRITE     HTMLFILE;UPINSPID;      * NSLHD Person ID
          GOTO      PATS9999
.
PATS9999  RETURN
+
.-------------------------------------------------------------------------------
.         Patient Missing a UPI Fields
.-------------------------------------------------------------------------------
PMIU0000  BRANCH    FLDITMNO,PMIU0100,PMIU0200,PMIU0300,PMIU0400,PMIU0500
          GOTO      PMIU9999
.
PMIU0100  CALL      TABMIU00           * Patients Missing a UPI (Table Sort)
          GOTO      PMIU9999
.
PMIU0200  WRITE     HTMLFILE;VIEWTYPE; * View Type (Period Dates)
          GOTO      PMIU9999
.
PMIU0300  CALL      PMUPRV00           * Previous button link
          GOTO      PMIU9999
.
PMIU0400  CALL      SELV0000           * Period Dates Selection List
          GOTO      PMIU9999
.
PMIU0500  CALL      PMUNXT00           * Next button link
          GOTO      PMIU9999
.
PMIU9999  RETURN
+
.-------------------------------------------------------------------------------
.         Patients Missing a UPI (Table Sort Output)
.-------------------------------------------------------------------------------
TABMIU00  MATCH     SP70,FRSTDATE
          IF        @EQUAL
            CALL      IBACLOCK
            PACK      FRSTDATE,CCC,CYY,CMM,ZERO,ONE
            REP       " 0",FRSTDATE
            PACK      LASTDATE,Z70
          ENDIF
.
          PACK      KEY8,SP70
          CALL      RSPMMUP1
TABMIU10  CALL      RKPMMUP1
          BRANCH    OVRCD,TABMIU99
.
.         CLEAR     HTMLLINK
.         APPEND    "javascript:PatientLink('",HTMLLINK
.         APPEND    PMMUURNO,HTMLLINK
.         APPEND    "')",HTMLLINK
.         RESET     HTMLLINK
.
          GOTO      TABMIU10
.
TABMIU99  RETURN
+
.-------------------------------------------------------------------------------
.         Patients Missing a UPI Previous Link
.-------------------------------------------------------------------------------
PMUPRV00  MOVE      TEMPLATE,F3
          MOVE      F3,KEY3
          REP       " 0",KEY3
.
          MOVE      REPORTNO,F2
          MOVE      F2,KEY2
          REP       " 0",KEY2
.
          WRITE     HTMLFILE;"patweb69.pbl":
                             "?reportno=",KEY2:
                             "&template=",KEY3:
                             "&frstdate=",PREFDATE:
                             "&lastdate=",PRELDATE:
                             "&viewtype=",VIEWTYPE;
.
PMUPRV99  RETURN
+
.-------------------------------------------------------------------------------
.         Patients Missing a UPI Next Link
.-------------------------------------------------------------------------------
PMUNXT00  MOVE      TEMPLATE,F3
          MOVE      F3,KEY3
          REP       " 0",KEY3
.
          MOVE      REPORTNO,F2
          MOVE      F2,KEY2
          REP       " 0",KEY2
.
          WRITE     HTMLFILE;"patweb69.pbl":
                             "?reportno=",KEY2:
                             "&template=",KEY3:
                             "&frstdate=",NXTFDATE:
                             "&lastdate=",NXTLDATE:
                             "&viewtype=",VIEWTYPE;
.
PMUNXT99  RETURN
+
.-------------------------------------------------------------------------------
.         Table Output
.-------------------------------------------------------------------------------
TABOUT00  CALL      TABHED00                     * Output table header row
.
          MOVE      ZERO,HL7SMODE                * Set mode for initial search
          MOVE      SP70,HL7UPIVL                * Clear the UPI to retrieve
          MOVE      SRCHSNAM,HL7SURNM            * Surname
          MOVE      SRCHGNAM,HL7FGNAM            * First Given Name
          MOVE      SRCHMNAM,HL7SGNAM            * Second Given Name
          MOVE      SRCHPDOB,HL7PTDOB            * DOB
          MOVE      SRCHPSEX,HL7GENDR            * Gender
          MOVE      SRCHMEDI,HL7PTMED            * Medicare No.
.
          READ      CONTROLF,HUND18;*151,PTCNSRCT  * Num recs for search
          MOVE      ZERO,F2
          STRIP     PTCNSRCT
          MOVE      PTCNSRCT,F2
          IF        F2 = 0
            MOVE      MAXRECRD,HL7MXREC          * Set max records for retrieval
          ELSE
            MOVE      F2,HL7MXREC                * Set max records for retrieval
          ENDIF
.
          MATCH     "1",SRCHNEXT
          IF        @EQUAL
            STRIP     SRCHCONT
            PACK      HL7CONTP,SRCHCONT,SP70     * Set continuation point
            MOVE      ONE,HL7SMODE               * Set mode for next batch
          ENDIF
.
          CALL      A19HNDLR                     * Format and send message
          BRANCH    EXIT,TABOUT91:               * Connection failure
                         TABOUT92:               * Timeout on response
                         TABOUT93:               * Insufficient data provided
                         TABOUT94:               * Blank destination details
                         TABOUT95:               * Socket connection failed
                         TABOUT96                * Response format error
.
          COMPARE   ZERO,EXIT          * Handler call success?
          GOTO      TABOUT98 IF NOT EQUAL
.
          CALL      TABROW00           * Output table data rows
.
          WRITE     HTMLFILE;"</table>"
.
          MATCH     SP70,HL7CONTP
          IF        @EQUAL
            WRITE     HTMLFILE;"<p align=center><b>End of Search</b>"
          ENDIF
          WRITE     HTMLFILE;"</div>";
          GOTO      TABOUT99
.
TABOUT91  WRITE     HTMLFILE;"<table width=100% cellpadding=2>":
                             "<tr><td align=center><b>":
                             "Connection to NSLHD failed.":
                             "</b></td></tr></table>";
          GOTO      TABOUT99
.
TABOUT92  WRITE     HTMLFILE;"<table width=100% cellpadding=2>":
                             "<tr><td align=center><b>":
                             "Timeout on response from NSLHD.":
                             "</b></td></tr></table>";
          GOTO      TABOUT99
.
TABOUT93  WRITE     HTMLFILE;"<table width=100% cellpadding=2>":
                             "<tr><td align=center><b>":
                             "Insufficient data provided for query.":
                             "</b></td></tr></table>";
          GOTO      TABOUT99
.
TABOUT94  WRITE     HTMLFILE;"<table width=100% cellpadding=2>":
                             "<tr><td align=center><b>":
                             "Destination data incomplete.":
                             "</b></td></tr></table>";
          GOTO      TABOUT99
.
TABOUT95  WRITE     HTMLFILE;"<table width=100% cellpadding=2>":
                             "<tr><td align=center><b>":
                             "Connection to socket failed.":
                             "</b></td></tr></table>";
          GOTO      TABOUT99
.
TABOUT96  WRITE     HTMLFILE;"<table width=100% cellpadding=2>":
                             "<tr><td align=center><b>":
                             "Response format error.":
                             "</b></td></tr></table>";
          GOTO      TABOUT99
.
TABOUT98  WRITE     HTMLFILE;"<table width=100% cellpadding=2>":
                             "<tr><td align=center><b>":
                             "Unknown Error.":
                             "</b></td></tr></table>";
          GOTO      TABOUT99
.
TABOUT99  RETURN
+
.-------------------------------------------------------------------------------
.         Output Table Heading Columns
.-------------------------------------------------------------------------------
TABHED00  WRITE      HTMLFILE;"<div id=HeadingDivision>";
          WRITE      HTMLFILE;"<table width=100% border=1 cellspacing=0 ":
                              "cellpadding=1 style=#"table-layout: fixed;#">";
.
          WRITE      HTMLFILE;"<tr>":
                      "<td class=HeadingCell width=200>Patient</td>":
                      "<td class=HeadingCell width=200>Address</td>":
                      "<td class=HeadingCell width=100>U/R Number</td>":
                      "<td class=HeadingCell width=90>Date of Birth</td>":
                      "<td class=HeadingCell width=90>Gender</td>":
                      "<td class=HeadingCell width=100>Medicare No.</td>":
                      "<td class=HeadingCell width=100>UPI</td>":
                      "<td class=HeadingCell>Alias</td>":
                      "<td class=HeadingCell width=125>Date of Last Update</td>";
.
          WRITE      HTMLFILE;"</tr>":
                              "</table>":
                              "</div>";
.
          WRITE     HTMLFILE;"<div id=BodyDivision style=#"height:350;#">"
          WRITE     HTMLFILE;"<table width=100% border=1 cellspacing=0 ":
                              "cellpadding=1 style=#"table-layout: fixed;#">"
.
TABHED99  RETURN
+
.-------------------------------------------------------------------------------
.         Output Table Rows
.-------------------------------------------------------------------------------
TABROW00  MOVE      ONE,LOOPCNT1
          WHILE     LOOPCNT1 <= DATARCNT
.
.           Patient Name (Folder)...
            WRITE     HTMLFILE;"<td style=#"padding:0px 5px#" width=200>":
                               "<table border=0 width=100% cellspacing=0 ":
                               " cellpadding=0>":
                               "<tr>";
.
.           WRITE     HTMLFILE;"<td width=25><a href=",LINKPRG,".pbl":
.                              "?reportno=",LINKREP:
.                              "&template=",LINKTEM:
.                              "&urnumber=",URNUMBER:
.                              "&admissno=",ADMISSNO,">";
.
            MOVE      DATARRAY[LOOPCNT1][14],DIM8
            RJUSTIFY  DIM8
            STRIP     MESSAGID
.
.           Date of Birth
            MOVE      SP70,DATEDIS1
            MATCH     SP70,DATARRAY[LOOPCNT1][6]
            IF        !@EQUAL
              UNPACK    DATARRAY[LOOPCNT1][6],CCENT,CYEAR,CMON,CDAY
              MOVE      CMON,F2
              LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
              PACK      DATEDIS1,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,SP10
            ENDIF

            WRITE     HTMLFILE;"<td width=25>":
                        "<a href='javascript:PatientSelect(#"":
                        *+,DIM8,*-,"#",#"":                     * UR Number
                        *+,DATARRAY[LOOPCNT1][15],*-,"#",#"":   * UPI Number
                        *+,DATARRAY[LOOPCNT1][12],*-,"#")'>";   * Person ID
.                       *+,DATARRAY[LOOPCNT1][2],*-,"#",#"":    * Surname
.                       *+,DATARRAY[LOOPCNT1][3],*-,"#",#"":    * Given Name
.                       *+,DATEDIS1,*-,"#")'>";                 * DOB      
. 
            MOVE      DATARRAY[LOOPCNT1][1],PTITL
            MOVE      DATARRAY[LOOPCNT1][2],PSNAME
            MOVE      DATARRAY[LOOPCNT1][2],PTMASNAM
            MOVE      DATARRAY[LOOPCNT1][3],PGNAME
            MOVE      DATARRAY[LOOPCNT1][3],PMPXFNAM
            MOVE      DATARRAY[LOOPCNT1][4],PMPXSNAM
            CALL      PATNAM00      * returns PATFNAME
.
.           CALL      PATFLD0       * returns KEY3
            MOVE      "010",KEY3    * Patient Folder icon value
.
            WRITE     HTMLFILE;"<img src='../images/PatientFolder",KEY3,".gif'":
                                 " class=Icon></a></td>":
                                 "<td>",PATFNAME,"</td>":           * Full Name
                               "</tr></table></td>";
.
.           Address...
            WRITE     HTMLFILE;"<td style=#"padding:0px 5px#" width=200>":
                               DATARRAY[LOOPCNT1][7],"&nbsp;":      * Addr Lne 1
                               DATARRAY[LOOPCNT1][8],"<br>":        * Addr Lne 2
                               DATARRAY[LOOPCNT1][9],",&nbsp;":     * Addr Lne 3
                               DATARRAY[LOOPCNT1][10],",&nbsp;":    * Addr Lne 4
                               DATARRAY[LOOPCNT1][11]:              * Addr Lne 5
                               "</td>";
.
.           U/R Number
            WRITE     HTMLFILE;"<td style=#"padding:0px 5px#" width=100>":
                               DATARRAY[LOOPCNT1][14]:
                               "</td>";
. 
.           Gender
            MOVE      DATARRAY[LOOPCNT1][5],DSEXCHAR
            CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                             *       returns DSEXDESC & DSEXNO
            MOVE      DSEXDESC,DISPSEX
.
.           Date of Birth - see DATEDIS1 above
.
            WRITE     HTMLFILE;"<td style=#"padding:0px 5px#" width=90>":
                               DATEDIS1,"</td>":
                               "<td style=#"padding:0px 5px#" width=90>":
                               DISPSEX,"</td>":
                               "<td style=#"padding:0px 5px#" width=100>":
                               DATARRAY[LOOPCNT1][13],"</td>":
                               "<td style=#"padding:0px 5px#" width=100>":
                               DATARRAY[LOOPCNT1][15],"</td>";
.                              "<td style=#"padding:0px 5px#" >":
.                              DATARRAY[LOOPCNT1][12],"</td>";
.
.           Aliases
            MOVE      DATARRAY[LOOPCNT1][15],UPINUMBR
            CALL      OUTALI00         * Output aliases
.
            MOVE      SP70,DATEDIS2
            MATCH     SP70,DATARRAY[LOOPCNT1][16]
            IF        !@EQUAL
              UNPACK    DATARRAY[LOOPCNT1][16],CCENT,CYEAR,CMON,CDAY
              MOVE      CMON,F2
              LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
              PACK      DATEDIS2,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,SP10
            ENDIF
.
            WRITE     HTMLFILE;"<td style=#"padding:0px 5px#" width=125>":
                               DATEDIS2,"</td>":
                      "</tr>"
.
            ADD       ONE,LOOPCNT1
          DO
.
TABROW99  RETURN
+
.-------------------------------------------------------------------------------
.         Ouput Aliases table cell (from Alias temporary file)
.-------------------------------------------------------------------------------
OUTALI00  WRITE     HTMLFILE;"<td style=#"padding:0px 5px#">";
.
          RJUSTIFY  MESSAGID
          RJUSTIFY  UPINUMBR
.
          MATCH     SP70,UPINUMBR
          GOTO      OUTALI99 IF EQUAL
.
          MATCH     SP70,MESSAGID
          GOTO      OUTALI99 IF EQUAL
.
          PACK      KEY33,MESSAGID,UPINUMBR,SP70
          CALL      RSTEMP1
OUTALI10  CALL      RKTEMP1
          BRANCH    OVRCD,OUTALI90
.
          MATCH     ALIAMSID,MESSAGID
          GOTO      OUTALI90 IF NOT EQUAL
.
          MATCH     ALIAUPIN,UPINUMBR
          GOTO      OUTALI90 IF NOT EQUAL
.
          STRIP     ALIASURN
          STRIP     ALIAFNME
          STRIP     ALIASNME
.
          WRITE     HTMLFILE;*+,ALIASURN,*-,", ",*+,ALIAFNME,*-," ",ALIASNME:
                             "<br>";
          GOTO      OUTALI10
.          
OUTALI90  WRITE     HTMLFILE;"</td>";
.
OUTALI99  RETURN
+
.-------------------------------------------------------------------------------
.         Table Output (Condensed View)
.-------------------------------------------------------------------------------
TABLST00  CALL      LSTHED00                     * Output table header row
.
          MOVE      ZERO,HL7SMODE                * Set mode for initial search
          MOVE      SP70,HL7UPIVL                * Clear the UPI to retrieve
          MOVE      SRCHSNAM,HL7SURNM            * Surname
          MOVE      SRCHGNAM,HL7FGNAM            * First Given Name
          MOVE      SRCHMNAM,HL7SGNAM            * Second Given Name
          MOVE      SRCHPDOB,HL7PTDOB            * DOB
          MOVE      SRCHPSEX,HL7GENDR            * Gender
          MOVE      SRCHMEDI,HL7PTMED            * Medicare No.
.
          READ      CONTROLF,HUND18;*151,PTCNSRCT  * Num recs for search
          MOVE      ZERO,F2
          STRIP     PTCNSRCT
          MOVE      PTCNSRCT,F2
          IF        F2 = 0
            MOVE      MAXRECRD,HL7MXREC          * Set max records for retrieval
          ELSE
            MOVE      F2,HL7MXREC                * Set max records for retrieval
          ENDIF
.
          MATCH     "1",SRCHNEXT
          IF        @EQUAL
            STRIP     SRCHCONT
            PACK      HL7CONTP,SRCHCONT,SP70     * Set continuation point
            MOVE      ONE,HL7SMODE               * Set mode for next batch
          ENDIF
.
          CALL      A19HNDLR                     * Format and send message
          BRANCH    EXIT,TABLST91:               * Connection failure
                         TABLST92:               * Timeout on response
                         TABLST93:               * Insufficient data provided
                         TABLST94:               * Blank destination details
                         TABLST95:               * Socket connection failed
                         TABLST96                * Response format error
.
          COMPARE   ZERO,EXIT          * Handler call success?
          GOTO      TABLST98 IF NOT EQUAL
.
          CALL      LSTROW00           * Output table data rows
.
          WRITE     HTMLFILE;"</table>"
.
          MATCH     SP70,HL7CONTP
          IF        @EQUAL
            WRITE     HTMLFILE;"<p align=center><b style='font-size:8pt;'>":
                               "End of Search</b>"
          ENDIF
          WRITE     HTMLFILE;"</div>";
          GOTO      TABLST99
.
TABLST91  WRITE     HTMLFILE;"<table width=100% cellpadding=2>":
                             "<tr><td align=center><b>":
                             "Connection to NSLHD failed.":
                             "</b></td></tr></table>";
          GOTO      TABLST99
.
TABLST92  WRITE     HTMLFILE;"<table width=100% cellpadding=2>":
                             "<tr><td align=center><b>":
                             "Timeout on response from NSLHD.":
                             "</b></td></tr></table>";
          GOTO      TABLST99
.
TABLST93  WRITE     HTMLFILE;"<table width=100% cellpadding=2>":
                             "<tr><td align=center><b>":
                             "Insufficient data provided for query.":
                             "</b></td></tr></table>";
          GOTO      TABLST99
.
TABLST94  WRITE     HTMLFILE;"<table width=100% cellpadding=2>":
                             "<tr><td align=center><b>":
                             "Destination data incomplete.":
                             "</b></td></tr></table>";
          GOTO      TABLST99
.
TABLST95  WRITE     HTMLFILE;"<table width=100% cellpadding=2>":
                             "<tr><td align=center><b>":
                             "Connection to socket failed.":
                             "</b></td></tr></table>";
          GOTO      TABLST99
.
TABLST96  WRITE     HTMLFILE;"<table width=100% cellpadding=2>":
                             "<tr><td align=center><b>":
                             "Response format error.":
                             "</b></td></tr></table>";
          GOTO      TABOUT99
.
TABLST98  WRITE     HTMLFILE;"<table width=100% cellpadding=2>":
                             "<tr><td align=center><b>":
                             "Unknown Error.":
                             "</b></td></tr></table>";
          GOTO      TABOUT99
.
TABLST99  RETURN
+
.-------------------------------------------------------------------------------
.         Output Table Heading Columns (Condensed View)
.-------------------------------------------------------------------------------
LSTHED00  WRITE      HTMLFILE;"<div id=HeadingDivision>";
          WRITE      HTMLFILE;"<table width=100% border=1 cellspacing=0 ":
                              "cellpadding=1 style=#"table-layout: fixed;#">";
.
          WRITE      HTMLFILE;"<tr>":
                      "<td class=HeadingCell width=200>Patient</td>":
                      "<td class=HeadingCell width=90>U/R Number</td>":
                      "<td class=HeadingCell width=85>Date of Birth</td>":
                      "<td class=HeadingCell width=90>Medicare No.</td>":
                      "<td class=HeadingCell width=90>UPI</td>":
                      "<td class=HeadingCell >Alias</td>":
                      "<td class=HeadingCell width=85>Update Date</td>";
.
          WRITE      HTMLFILE;"</tr>":
                              "</table>":
                              "</div>";
.
          WRITE     HTMLFILE;"<div id=BodyDivision style=#"height:350;#">"
          WRITE     HTMLFILE;"<table width=100% border=1 cellspacing=0 ":
                              "cellpadding=1 style=#"table-layout: fixed;#">"
.
LSTHED99  RETURN
+
.-------------------------------------------------------------------------------
.         Output Table Rows (Condensed View)
.-------------------------------------------------------------------------------
LSTROW00  MOVE      ONE,LOOPCNT1
          WHILE     LOOPCNT1 <= DATARCNT
.
.           Patient Name (Folder)...
            WRITE     HTMLFILE;"<td style=#"padding:0px 5px#" width=200>":
                               "<table border=0 width=100% cellspacing=0 ":
                               " cellpadding=0>":
                               "<tr>";
.
.           WRITE     HTMLFILE;"<td width=25><a href=",LINKPRG,".pbl":
.                              "?reportno=",LINKREP:
.                              "&template=",LINKTEM:
.                              "&urnumber=",URNUMBER:
.                              "&admissno=",ADMISSNO,">";
.
            MOVE      DATARRAY[LOOPCNT1][14],DIM8
            RJUSTIFY  DIM8
            STRIP     MESSAGID
.
.           Date of Birth
            MOVE      SP70,DATEDIS1
            MATCH     SP70,DATARRAY[LOOPCNT1][6]
            IF        !@EQUAL
              UNPACK    DATARRAY[LOOPCNT1][6],CCENT,CYEAR,CMON,CDAY
              MOVE      CMON,F2
              LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
              PACK      DATEDIS1,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,SP10
            ENDIF

            WRITE     HTMLFILE;"<td width=25>":
                        "<a href='javascript:PatientSelect(#"":
                        *+,DIM8,*-,"#",#"":                     * UR Number
                        *+,DATARRAY[LOOPCNT1][15],*-,"#",#"":   * UPI Number
                        *+,DATARRAY[LOOPCNT1][12],*-,"#")'>";   * Person ID
.                       *+,DATARRAY[LOOPCNT1][2],*-,"#",#"":    * Surname
.                       *+,DATARRAY[LOOPCNT1][3],*-,"#",#"":    * Given Name
.                       *+,DATEDIS1,*-,"#")'>";                 * DOB
.
            MOVE      DATARRAY[LOOPCNT1][1],PTITL
            MOVE      DATARRAY[LOOPCNT1][2],PSNAME
            MOVE      DATARRAY[LOOPCNT1][2],PTMASNAM
            MOVE      DATARRAY[LOOPCNT1][3],PGNAME
            MOVE      DATARRAY[LOOPCNT1][3],PMPXFNAM
            MOVE      DATARRAY[LOOPCNT1][4],PMPXSNAM
            CALL      PATNAM00      * returns PATFNAME
.
.           CALL      PATFLD0       * returns KEY3
            MOVE      "010",KEY3    * Patient Folder icon value
.
.           Patient cell
            WRITE     HTMLFILE;"<img src='../images/PatientFolder",KEY3,".gif'":
                                 " class=Icon></a></td>":
                                 "<td>",PATFNAME,"<br>":            * Full Name
                                 DATARRAY[LOOPCNT1][7],"<br>":      * Addr Lne 1
                                 DATARRAY[LOOPCNT1][9],",&nbsp;":   * Addr Lne 3
                                 DATARRAY[LOOPCNT1][10],",&nbsp;":  * Addr Lne 4
                                 DATARRAY[LOOPCNT1][11]:            * Addr Lne 5
                                 "</td>":
                               "</tr></table></td>";
.
.           U/R Number
            WRITE     HTMLFILE;"<td style=#"padding:0px 5px#" width=90>":
                               DATARRAY[LOOPCNT1][14]:
                               "</td>";
. 
.           Gender
            MOVE      DATARRAY[LOOPCNT1][5],DSEXCHAR
            CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                             *       returns DSEXDESC & DSEXNO
            MOVE      DSEXDESC,DISPSEX
.
.           Date of Birth - see DATEDIS1 above

            WRITE     HTMLFILE;"<td style=#"padding:0px 5px#" width=85>":
                               DATEDIS1,"<br>",DISPSEX,"</td>":
                               "<td style=#"padding:0px 5px#" width=90>":
                               DATARRAY[LOOPCNT1][13],"</td>":
                               "<td style=#"padding:0px 5px#" width=90>":
                               DATARRAY[LOOPCNT1][15],"</td>";
.
.           Aliases
            MOVE      DATARRAY[LOOPCNT1][15],UPINUMBR
            CALL      OUTALI00         * Output aliases
.
            MOVE      SP70,DATEDIS2
            MATCH     SP70,DATARRAY[LOOPCNT1][16]
            IF        !@EQUAL
              UNPACK    DATARRAY[LOOPCNT1][16],CCENT,CYEAR,CMON,CDAY
              MOVE      CMON,F2
              LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
              PACK      DATEDIS2,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,SP10
            ENDIF
.
            WRITE     HTMLFILE;"<td style=#"padding:0px 5px#" width=85>":
                               DATEDIS2,"</td>":
                      "</tr>"
.
            ADD       ONE,LOOPCNT1
          DO
.
LSTROW99  RETURN
+
.------------------------------------------------------------
. Format Patient Name <b>SURNAME</b>Title Given Names
.------------------------------------------------------------
PATNAM00  CLEAR     DISPNAME
          REP       "#" ",PSNAME
          REP       "#" ",PTMASNAM
          REP       "#" ",PGNAME
          MOVE      SP70,PATFNAME
          REP       UPPLOW,PSNAME
          REP       UPPLOW,PTMASNAM
          APPEND    "<b> ",DISPNAME
          MATCH     SP70,PTMASNAM
          IF        !@EQUAL
            STRIP     PTMASNAM
            APPEND    PTMASNAM,DISPNAME
          ELSE
            APPEND    PSNAME,DISPNAME
          ENDIF
          APPEND    " </b>",DISPNAME
          PACK      NAME35,PTITL,SP70
          CALL      NAMSTR00
.
          MATCH     "1",PTCNUSGN                 * Using Second Given Name?
          IF        @EQUAL
            PACK      NAME35,PMPXFNAM,SP1,PMPXSNAM,SP70
          ELSE
            PACK      NAME35,PGNAME,SP70
          ENDIF
.
          CALL      NAMSTR00
          APPEND    SP70,DISPNAME
          RESET     DISPNAME
          MOVE      DISPNAME,PATFNAME
          STRIP     PATFNAME
.
PATNAM99  RETURN
+
.-------------------------------------------------------------------------------
.         Link to Next Group
.-------------------------------------------------------------------------------
NEXT0000  MOVE      TEMPLATE,F3
          MOVE      F3,KEY3
          REP       " 0",KEY3
.
          MOVE      REPORTNO,F2
          MOVE      F2,KEY2
          REP       " 0",KEY2
.
          STRIP     SRCHCONT
          WRITE     HTMLFILE;"patweb69.pbl":
                             "?reportno=",KEY2:
                             "&template=",KEY3:
                             "&srchcont=",*+,SRCHCONT,*-:
                             "&srchtype=",SRCHTYPE;
.
NEXT9999  RETURN
+
.-------------------------------------------------------------------------------
.         Link to Previous Group
.-------------------------------------------------------------------------------
PREV0000  MOVE      TEMPLATE,F3
          MOVE      F3,KEY3
          REP       " 0",KEY3
.
          MOVE      REPORTNO,F2
          MOVE      F2,KEY2
          REP       " 0",KEY2
.
          WRITE     HTMLFILE;"patweb69.pbl":
                             "?reportno=",KEY2:
                             "&template=",KEY3:
                             "&srchtype=",SRCHTYPE;
.
PREV9999  RETURN
+
.-------------------------------------------------------------------------------
.         Get Extra Contacts (UPI Retrieve) - JSON format
.-------------------------------------------------------------------------------
GETEXC00  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>";
          WRITE     HTMLFILE;"[";
.
          MOVE      ZERO,FIRSTREC
          RJUSTIFY  SRCHUPIN
          PACK      KEY13,SRCHUPIN,SP70
          CALL      RSTEMP2
GETEXC10  CALL      RKTEMP2
          BRANCH    OVRCD,GETEXC99
.
          MATCH     NOK1UPIN,SRCHUPIN
          GOTO      GETEXC99 IF NOT EQUAL
.
          PACK      KEY5,CATTC,NOK1TYPE,SP70
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE      SP70,TDESC
            MOVE      SP70,TCINDC1
          ENDIF
.
          MOVE      SP70,PMRLDESC
          MATCH     SP70,NOK1RELP
          IF        !@EQUAL
            PACK      KEY10,NOK1RELP,SP70
            CALL      RDPMREL1
            IF        OVRCD=1
              MOVE      NOK1RELP,PMRLDESC
            ENDIF
          ENDIF
.
          IF        FIRSTREC=0
            MOVE      ONE,FIRSTREC
          ELSE
            WRITE     HTMLFILE;","
          ENDIF
.
          STRIP     NOK1FNME
          STRIP     NOK1SNME
          STRIP     NOK1SURN
          STRIP     NOK1ADD1
          STRIP     NOK1ADD2
          STRIP     NOK1ADD3
          STRIP     NOK1ADD4
          STRIP     NOK1POST
.
          WRITE     HTMLFILE;"{#"urnumber#":#"",URNUMBER,"#",":
                              "#"contactTypeDesc#":#"",TDESC,"#",":
                              "#"contactType#":#"",NOK1TYPE,"#",":
                              "#"contactTypeInd#":#"",TCINDC1,"#",":
                              "#"contactCounter#":#"",NOK1CNTR,"#",":
                              "#"contactRelationship#":#"",NOK1RELP,"#",":
                              "#"contactRelationshipDesc#":#"",PMRLDESC,"#",":
                              "#"contactTitle#":#"",NOK1TITL,"#",":
                              "#"contactName#":#"",*+,NOK1FNME,*-," ":
                                                   *+,NOK1SNME,*-," ":
                                                   *+,NOK1SURN,*-,"#",":
                              "#"contactHomePhone#":#"",NOK1TELP,"#",":
                              "#"contactWorkPhone#":#"",NOK1TELB,"#",":
                              "#"contactMobilePhone#":#"",NOK1TELM,"#",":
.                             "#"contactEmail#":#"",PMCEEMAI,"#",":
                              "#"contactAddress1#":#"",*+,NOK1ADD1,*-,"#",":
                              "#"contactAddress2#":#"",*+,NOK1ADD2,*-,"#",":
                              "#"contactAddress3#":#"",*+,NOK1ADD3,*-,"#",":
                              "#"contactAddress4#":#"",*+,NOK1ADD4,*-,"#",":
                              "#"contactAddress5#":#"",*+,NOK1POST,*-,"#"":
                              "}";

          GOTO      GETEXC10
.
GETEXC99  WRITE     HTMLFILE;"]</RETURN_VALUE>";
          RETURN
+

.------------------------------------------------------------------------------
.
          INC       IBAWEBCD
          INC       WEBDATCD
          INC       CALCAGE
          INC       DAYOFWEK
          INC       NAMSTRCD
.
          INC       CLPMSPX2
          INC       CLPATHSP
          INC       CLNHIMAS           * Clear nhimasaf file variables
          INC       PATNAMCD
          INC       PATDOCTM
          INC       PATDOCCD
          INC       PATMSXTM
          INC       A19HNDLR           * HL7 A19 Handler Interface
          INC       DGCLICUP           * Send PMI details after Update
          INC       PATCOMN2
          INC       UPDUR
          INC       DEATHAUD
          INC       CLPMSDAU
          INC       TFILENAM
.
          INC       PATDO1IO/INC       * Doctor File
          INC       PATHSPIO/INC       * Hospital Details
          INC       PATMA1IO/INC       * Patient Master File
          INC       PATMI1IO/INC       * Patient Admissions File
          INC       PATVISIO/INC       * Patient Visit File
          INC       PMSVX1IO/INC       * Patient Visit Extension File
          INC       PATWR1IO/INC       * Patient Ward/Bed Details
          INC       PMSPX2IO/INC       * PMI extension table
          INC       PMSAIDIO/INC       * Alternate ID
          INC       PATGSRIO/INC       * Given name/Surname Index File
          INC       PATNIDIO/INC       * National ID Number
          INC       NHIETHIO/INC
          INC       NHIMASIO/INC
          INC       BOKRC1IO/INC
          INC       PATINVIO/INC
          INC       PATLOCIO/INC
          INC       PMSCURIO/INC
          INC       PATURAIO/INC
.
          INC       PATAM1IO/INC
          INC       PATPA1IO/INC
          INC       PATRE1IO/INC
          INC       PMSDAUIO/INC
          INC       PATPR1IO/INC
.
          INC       PMSMUPIO/INC       * Patients Missing a UPI
          INC       PMSRELIO/INC       * Patient Relationship
          INC       PMSTLEIO/INC       * Patient Titles


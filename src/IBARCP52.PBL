.******************************************************************************
.*                                                                            *
.*               P R O G R A M  S U M M A R Y                                 *
.*               -------------  -------------                                 *
.*                                                                            *
.*      Program Name  :   IBARCP52                                            *
.*                                                                            *
.*      Function      :   Register File Listing                               *
.*                                                                            *
.*      Modifications :                                                       *
.*                                                                            *
.******************************************************************************
.* V10.02.01 28/07/2010  J.Tan    CAR 244159                                  *
.*           Mods for Credit Card Surcharge                                   *
.* V9.12.01  04.03.2010 J.Tan            CAR 216328                           *
.*           Added parameter for Using one char of Ledger number              *
.* V9.03.02  15.01.2004 Sandra Barcham       44088                            *
.*           Remove S/D No column as not longer used                          *
.* V9.03.01  05.12.2003 Sandra Barcham       44088                            *
.*           Correct system codes                                             *
.******************************************************************************
.* V5.09.B01 13.12.2000 Charles Handaya                                       *
.*           Added New System Code 92 - GST Sundry Cash Sales                 *
.*           RCPREGFD, RCPREGIO changes                                       *
.*           (part of BAS Statement Modification)                             *
.******************************************************************************
.* V5.07.B01 19/11/1998  Davin                                                *
.*           Remove old debtors                                               *
.******************************************************************************
.* V5.03.01  11.07.95 B.G.Ackland                                             *
.*           New A/R System                                                   *
.******************************************************************************
.* V5.01.03  14/04/94 Sandra Barcham                                          *
.*           Standards Upgrade                                                *
.* V5.01.02  22/02/94 I.Rutt                                                  *
.*           Standards Upgrade                                                *
.* V5.01.01  31/12/93 Matt Surridge                                           *
.*           Modified to accept new IBA System Code                           *
.*           97 (Cash Deposits).                                              *
.* V5.01.00  20/01/92  DIG                                                    *
.*           Ported from release 5                                            *
.******************************************************************************
+
          INC       STD001FD
          INC       RCP001
          INC       RCPREGFD/INC
          INC       RCPCONFD/INC
.
          INC       IBAGSTFD/INC
.
ACCNO     DIM       12
.
CHG       FORM      1
.
DIM30     DIM       30
DSH100    INIT      "--------------------------------------------------":
                    "--------------------------------------------------"
DSH30     INIT      "------------------------------"
.
FLAG      DIM       1
FIELD     FORM      1
.
IBADSC    DIM       26
IBACD     FORM      2
.
LEDNO     DIM       2
LEDNUM    DIM       1
.
MODE      DIM       1
MODE1     DIM       1
.
SRDIM20   DIM      20
SRDIM20A  DIM      20
SUNLED    DIM      3
.
PRGID     INIT      "IBARCP52"
PRGNAM    INIT      "Register File Listing"
VERSION   INIT      "V12.01.00"
+
START    CALL       DISPHEAD
         DISPLAY    *P56:24,"Opening rcpregaf";
         OPEN       RCPREGA1,"rcpregaf"
.
         DISPLAY    *P56:24,"Opening ibagstaf";
         OPEN       IBAGSTA1,"ibagstaf"
.
         DISPLAY   *P60:24,"opening controlf"
         OPEN      CONTROLF,"controlf"
         READ      CONTROLF,THIRTY6;*238,RCCNLEDG
.
START1   DISPLAY   *P1:4,*EF,*V2LON,ZERO,*HOFF,". Exit":
                   *P1:5,*V2LON,ONE,*HOFF,". Print Register Listing"
.
         KEYIN     *P1:7,"Select Option : ",*V2LON,COPTION
         COMPARE   ZERO,COPTION
         GOTO      ENDIT IF EQUAL
.
         BRANCH    COPTION,START2
         BEEP
         GOTO      START1
.
START2   MOVE      SP15,KEY15
         MOVE          ZERO,CPAGENO
         MOVE          ONE,CNOUNDLN
         CLOCK         TIME,CTIMEIS
         MOVE          SIXTY,CLNO
         CALL      RDSREGA1
.
         DISPLAY   *P1:24,*EL:
                   *P20:24,"Printing Register  : "
.
MAINLOOP CALL      RDKREGA1
         BRANCH    OVRCD TO END
.
         COMPARE   FIFTY5,CLNO
         CALL      HEAD IF NOT LESS
.
         DISPLAY   *P50:24,*V2LON,REGCODE
.
         MATCH     "1",RCCNLEDG
         IF        @EQUAL
           UNPACK    REGGENLD INTO LEDNUM,ACCNO
         ELSE
           UNPACK    REGGENLD INTO LEDNO,ACCNO
         ENDIF
.
         MOVE      REGIBACD,IBACD
         ADD       ONE,IBACD
.
         MOVE      "Invalid IBA Register Code ",IBADESC
.
         COMPARE   "90",REGIBACD
         GOTO      NEXTYPE0 IF NOT EQUAL
         MOVE      "Emergency Deposits        ",IBADESC
         GOTO      DESC0000
.
NEXTYPE0 COMPARE   "91",REGIBACD
         GOTO      NEXTYPE1 IF NOT EQUAL
         MOVE      "Emergency Deposits        ",IBADESC
         GOTO      DESC0000
.
NEXTYPE1 COMPARE   "92",REGIBACD
         GOTO      NEXTYPE2 IF NOT EQUAL
         MOVE      "GST Sundry Cash Sales     ",IBADESC
         GOTO      DESC0000
.
NEXTYPE2 COMPARE   "94",REGIBACD
         GOTO      NEXTYPE3 IF NOT EQUAL
         MOVE      "Sundry Debtor Rec.        ",IBADESC
         GOTO      DESC0000
.
NEXTYPE3 COMPARE   "95",REGIBACD
         GOTO      NEXTYPE4 IF NOT EQUAL
         MOVE      "Sundry Cash Sales         ",IBADESC
         GOTO      DESC0000
.
NEXTYPE4 COMPARE   "96",REGIBACD
         GOTO      NEXTYPE5 IF NOT EQUAL
         MOVE      "Cash Deposit (Pri.)",IBADESC
         GOTO      DESC0000
.
NEXTYPE5 COMPARE   "97",REGIBACD
         GOTO      NEXTYPE6 IF NOT EQUAL
         MOVE      "Cash Deposit (Pat.)",IBADESC
         GOTO      DESC0000
.
NEXTYPE6 COMPARE   "98",REGIBACD
         GOTO      NEXTYPE7 IF NOT EQUAL
         MOVE      "Miscellaneous",IBADESC
         GOTO      DESC0000
.
NEXTYPE7 COMPARE   "99",REGIBACD
         GOTO      NEXTYPE8 IF NOT EQUAL
         MOVE      "Suspense     ",IBADESC
         GOTO      DESC0000
.
NEXTYPE8 COMPARE   "89",REGIBACD
         GOTO      NEXTLOAD IF NOT EQUAL
         MOVE      "Credit Card Surcharge",IBADESC
         GOTO      DESC0000
.
NEXTLOAD LOAD      IBADESC BY IBACD FROM IBA0,IBA1,IBA2,IBA3,IBA4,IBA5
.
DESC0000 MOVE      IBADESC,IBADSC
.
          MOVE      SP70,DIM30
          MATCH     SP70,REGGST
          GOTO      DESC0100 IF EQUAL
.
          PACK      KEY6,REGGST,SP70
          CALL      RDIBGS1
          IF        OVRCD=0
            MOVE      IBGSDESC,DIM30
          ENDIF
.
DESC0100 MOVE      "N/A",SUNLED
.
PRNTDT   MATCH     "1",RCCNLEDG
         IF        @EQUAL
           PRINT     "| ",REGCODE,*12,"| ",REGDESC,*50,"| ",REGCHQCD:
                   *68,"| ",LEDNUM,"/",ACCNO,*86,"| ",REGGROUP:
                   *94,"| ",REGIBACD,SP1,IBADSC,*132,"|":
                   *N,"|",*12,"|",*50,"|",*68,"|",*86,"|":
                   *94,"| ",REGGST,SP1,DIM30:
                            *132,"|"
         ELSE
           PRINT     "| ",REGCODE,*12,"| ",REGDESC,*50,"| ",REGCHQCD:
                   *68,"| ",LEDNO,"/",ACCNO,*86,"| ",REGGROUP:
                   *94,"| ",REGIBACD,SP1,IBADSC,*132,"|":
                   *N,"|",*12,"|",*50,"|",*68,"|",*86,"|":
                   *94,"| ",REGGST,SP1,DIM30:
                            *132,"|"
         ENDIF
.
         ADD       TWO,CLNO
.
         GOTO      MAINLOOP
.
HEAD     CALL      HEAD132
.
         PRINT     "*",DSH100,DSH30,"*":
                   *N,"| REGISTER |",*50,"| CHEQUE ACCOUNT":
                   *68,"| GENERAL LEDGER",*86,"| GROUP":
                   *94,"| I.B.A. SYSTEM CODE",*132,"|":
                   *N,"| CODE",*12,"| DESCRIPTION",*50,"| CODE":
                   *68,"| INCOME A/C",*86,"| CODE":
                   *94,"| GST CODE",*132,"|":
                   *N,"|",DSH100,DSH30,"|"
.
         MOVE      FOUR,CLNO
         RETURN
.
END      PRINT     "*",DSH100,DSH30,"*":
                   *N,*N,"*** END OF REPORT ***"
.
ENDIT    CHAIN     PGM
         STOP
+
         INC        STD001IO 
+ 
         INC       RCPREGIO/INC
.
         INC       IBAGSTIO/INC
.

.******************************************************************************
.*    Program      : IBAWAT51                                                 *
.*    Description  : INCOMPLETE OUTCOME OF OFFER REPORT                       *
.*                                                                            *
.*    Author       : ROD   17/11/93   (25 two days ago & very pissed in UK)   *
.*                                                                            *
.*    Modifications:                                                          *
.*                                                                            *
.******************************************************************************
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899                         *
.*           Recompiled as WATTR1FD changes                                   *
.******************************************************************************
.*       V10.05.01  01/08/2015  Ania P                      CAR 261630        *
.*                  Removed use of PORT for temp file naming                  *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V5.08.01  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*                   V5.07.B02  19/01/1999  Nicole Harrington 507 rebound 38  *
.*                              Mods to print century                         *
.******************************************************************************
.
          INC       STD001FD
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PATDSCFD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATMA1FD/INC
          INC       WATSEAFD/INC
          INC       WATSEIFD/INC
          INC       WATTR1FD/INC
          INC       IBASEQFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
.
ALLACS    FORM      1
ASATDAT   DIM       8
CMDLINE   DIM       80
CODE      DIM       2
DASATDAT  DIM      10
DIM3      DIM       3
DOCCODE   DIM       6
DXXADATE  DIM       10
DXXDDATE  DIM       10
ENCONS    DIM       6
ENLTYPE   DIM       3
FHDR      INIT      "wat51a"
FINISH    INIT      "Finish"
FNAME     DIM       8
KDATE8    DIM       8
NOPATS    FORM      5
PRECONS   DIM       30
PRELTYPE  DIM       30
PRSCONS   DIM       30
PRSLTYPE  DIM       30
PTCNDCQS  FORM      1
SAVCONS   DIM       6
SPECDESC  DIM       15
START     INIT      "Start"
STCONS    DIM       6
STLTYPE   DIM       3
TOTPATS   FORM      5
.
.Temp file definition
TEMPFILE  IFILE     SQL, FIXED=114, KEY="U1-6,7-26,27-34,35-43,44-45"
XXCONS    DIM       6      1
XXSNAME   DIM       20     7
XXURNO    DIM       8      27
XXPCODE   DIM       9      35
DXXCOUNT  DIM       2      44
XXGNAME   DIM       15     46
XXSPEC    DIM       3      61
XXADATE   DIM       8      64
XXDDATE   DIM       8      72
XXPDESC   DIM       34     80
.    End of record        114
.
XXCOUNT   FORM      2
.
PRGID     INIT      "IBAWAT51"
PRGNAM    INIT      "INCOMPLETE OUTCOME OF OFFER REPORT"
VERSION   INIT      "V12.01.00"
+
.*************************************************************************
.*  ML0000  :  Mainline code                                             *
.*************************************************************************
ML0000    CALL      INIT0000                      * initialisation
.
ML1000    CALL      MENU0000                      * Menu options
          BRANCH    MOPTION,ML3000
          GOTO      ML9999                        * Exit
.
ML3000    CALL      KCON0000                      * keyin consultant range
          BRANCH    EXIT,ML1000                   * EXITCHAR
          CALL      KLTY0000                      * keyin list type range
          BRANCH    EXIT,ML1000                   * EXITCHAR
          CALL      KDAT0000                      * keyin as at date
          BRANCH    EXIT,ML3000                   * nothing input
.
          CALL      CONTQST                       * Ok to Continue?
          BRANCH    CEXIT,ML5000,ML3000,ML1000
.
ML5000    CALL      CRET0000                      * create temp file
          CALL      LOAD0000                      * extract data into temp file
          BRANCH    EXIT,ML3000                   * no data found
.
          CALL      RPRT0000                      * print report
          CALL      KILL0000 
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.*************************************************************************
.*  INIT0000  :  Initialisation                                          *
.*************************************************************************
INIT0000  CALL      DISPHEAD
          CALL      TFILENAM
.
          DISPLAY   *P60:24,"watseaaf"
          OPEN      WATSEAA1,"watseaaf"
          DISPLAY   *P60:24,"watseiaf"
          OPEN      WATSEIA1,"watseiaf"
          OPEN      WATSEIA2,"watseiaf"
          DISPLAY   *P60:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
          DISPLAY   *P60:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
          DISPLAY   *P60:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          DISPLAY   *P60:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
          DISPLAY   *P60:24,"patdschf"
          OPEN      PATDSCH1,"patdschf"
          DISPLAY   *P60:24,"wattr1af"
          OPEN      WATTR1A5,"wattr1af"
.
          DISPLAY   *P60:24,*EL,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TWENTY1;*247,PTCNDCQS
.
          MOVE      ONE,CNEWDS
.
INIT9999  RETURN
+
.*************************************************************************
.*  MENU0000  :  Menu options                                            *
.*************************************************************************
MENU0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Run report":
                    *P1:7,"Select Option : ";
.
MENU1000  KEYIN     *P17:7,*DV,UNDLN1:
                    *P17:7,*V2LON,MOPTION
.
          BRANCH    MOPTION,MENU9999
          COMPARE   ZERO,MOPTION
          GOTO      MENU9999 IF EQUAL
.
          BEEP
          GOTO      MENU1000
.
MENU9999  RETURN
+
.*************************************************************************
.*  KLTY0000  :  keyin List type range                                   *
.*************************************************************************
KLTY0000  MOVE      "19",ECOL
          MOVE      "13",EVERT
          MOVE      "WU",CODE
          MOVE      SP3,CKYIDEF3                  * no default
          MOVE      ZERO,CKYIMAND                 * not mandatory
.
          CALL      PATCODKY
.
          IF        EXIT=2
            MOVE      ONE,EXIT                    * EXITCHAR input
            GOTO      KLTY9999
          ENDIF
.
          IF        EXIT=1
            DISPLAY   *P19:13,*V2LON,"Start"      * nothing input
            MOVE      SP3,STLTYPE
            PACK      PRSLTYPE,START,SP30
            GOTO      KLTY5000
          ENDIF
.
          MOVE      ACODE,STLTYPE
          PACK      PRSLTYPE,STLTYPE,SP2,TDESC
          DISPLAY   *P26:13,TDESC;
.
. keyin Ending List Type
.
KLTY5000  MOVE      "14",EVERT
          CALL      PATCODKY
.
          IF        EXIT=2
            MOVE      ONE,EXIT                    * EXITCHAR input
            GOTO      KLTY9999
          ENDIF
.
          IF        EXIT=1
            DISPLAY   *P19:14,*V2LON,"Finish"     * nothing input
            MOVE      "zzz",ENLTYPE
            PACK      PRELTYPE,FINISH,SP30
            MOVE      ZERO,EXIT
            GOTO      KLTY9999
          ENDIF
.
          MOVE      ACODE,ENLTYPE
          PACK      PRELTYPE,ENLTYPE,SP2,TDESC
          DISPLAY   *P26:14,TDESC;
.
          MATCH     STLTYPE,ENLTYPE
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Invalid range.  ";
            CALL      HITENTER
            GOTO      KLTY0000
          ENDIF
          MOVE      ZERO,EXIT
.
KLTY9999  RETURN
+
.*************************************************************************
.*  KCON0000  :  keyin Consultant                                        *
.*************************************************************************
KCON0000  DISPLAY   *P1:10,*EF:
                    *P1:10,"Consultant from : ":
                    *P1:11,"           to   : ":
                    *P1:13,"List Type from  : ":
                    *P1:14,"          to    : ":
                    *P1:16,"As at Date      : "
.
          MOVE      "19",ECOL
          MOVE      "10",EVERT
          MOVE      SP6,CKYIDEF6                  * no default
          MOVE      ZERO,CKYIMAND                 * not mandatory
.
          CALL      PATDOCKY
.
          IF        EXIT=2
            MOVE      ONE,EXIT                    * EXITCHAR input
            GOTO      KCON9999
          ENDIF
.
          IF        EXIT=1
            DISPLAY   *P19:10,*V2LON,"Start"      * nothing input
            MOVE      SP6,STCONS
            PACK      PRSCONS,START,SP30
            GOTO      KCON5000
          ENDIF
.
          MOVE      DOCCODE,STCONS
.
          MOVE      DGNAME,PACGNAME
          MOVE      DSNAME,PACSNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
.
          PACK      PRSCONS,STCONS,SP3,PACFNAME
          DISPLAY   *P26:10,PACFNAME;
.
. keyin Ending Consultant code
.
KCON5000  MOVE      "11",EVERT
          CALL      PATDOCKY
.
          IF        EXIT=2
            MOVE      ONE,EXIT                    * EXITCHAR input
            GOTO      KCON9999
          ENDIF
.
          IF        EXIT=1
            DISPLAY   *P19:11,*V2LON,"Finish"      * nothing input
            MOVE      "zzzzzz",ENCONS
            PACK      PRECONS,FINISH,SP30
            MOVE      ZERO,EXIT
            GOTO      KCON9999
          ENDIF
.
          MOVE      DOCCODE,ENCONS
.
          MOVE      DGNAME,PACGNAME
          MOVE      DSNAME,PACSNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
.
          PACK      PRECONS,ENCONS,SP3,PACFNAME
          DISPLAY   *P26:11,PACFNAME;
.
          MATCH     STCONS,ENCONS
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Invalid range.  ";
            CALL      HITENTER
            GOTO      KCON0000
          ENDIF
          MOVE      ZERO,EXIT
.
KCON9999  RETURN
+
.*************************************************************************
.*  KDAT0000  :  Keyin as at date                                        *
.*************************************************************************
KDAT0000  CALL      IBACLOCK
.
          UNPACK    SP10,CDAY,CMON,CYEAR
          MOVE      CCC,CCENT
          MOVE      "19",CCOL
          MOVE      "16",CVERT
          MOVE      CCC,CCENT
          MOVE      ZERO,CDEFDTE
.
KDAT1000  CALL      KEYDATE
          BRANCH    CQUEST,KDAT1000
          BRANCH    OVRCD,KDAT9100                * anything input
.
          PACK      ASATDAT,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ASATDAT 
          PACK      KDATE8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",KDATE8
.
          CALL      PACDATE                  * format for printing
          MOVE      CPCDATE,DASATDAT          
.
          MOVE      ZERO,EXIT                * continue
          GOTO      KDAT9999
KDAT9100  MOVE      ONE,EXIT                 * exit
.
KDAT9999  RETURN
+
.*************************************************************************
.*  LOAD0000  :  load data into temp file                                *
.*************************************************************************
LOAD0000  MOVE      ZERO,ALLACS
          PACK      KEY4,PASSCODE
          CALL      RDWTSEA1
          BRANCH    OVRCD,LOAD0500                * user doesnt have ALL access
          MOVE      ONE,ALLACS                    * user has ALL access
.
LOAD0500  DISPLAY   *P1:24,*EL,*P35:24,"Loading... [        ]";
          PACK      KEY32,FOUR,STCONS,SP30
          CALL      RDSTREA5
LOAD1000  CALL      RDKTREA5
          BRANCH    OVRCD,LOAD9000
.
          COMPARE   WMSTAT,FIVE
          GOTO      LOAD9000 IF LESS
.
          MATCH     STCONS,WMDOCTOR
          GOTO      LOAD1000 IF LESS
.
          MATCH     WMDOCTOR,ENCONS
          GOTO      LOAD1000 IF LESS
.
          MATCH     STLTYPE,WMUNIT                * list type in range?
          GOTO      LOAD1000 IF LESS
.
          MATCH     WMUNIT,ENLTYPE                * list type in range?
          GOTO      LOAD1000 IF LESS
.
. check if user has valid security clearance for this cons/list type
.
          BRANCH    ALLACS,LOAD3000               * user has ALL access
.
          PACK      KEY13,WMDOCTOR,WMUNIT,PASSCODE
          CALL      RDWTSEI1
          BRANCH    OVRCD,LOAD1000                * user doesnt have access
.
LOAD3000  PACK      KEY8,WMURNO
          CALL      RDMAST1
          BRANCH    OVRCD,LOAD1000
.
          PACK      KEY8,WMBOOK
          CALL      RDMISS1
          BRANCH    OVRCD,LOAD1000
.
          MATCH     ADATE,KDATE8
          GOTO      LOAD1000 IF LESS
.
          UNPACK    SP10,DDATE
          PACK      KEY8,WMBOOK
          CALL      RDDSCH1
.
. set up vars for temp file
.
          MOVE      WMDOCTOR,XXCONS
          MOVE      PSNAME,XXSNAME
          MOVE      WMURNO,XXURNO                          
          MOVE      WMCODE,XXPCODE
          MOVE      WMCNT,XXCOUNT
.
          MOVE      PGNAME,XXGNAME
          MOVE      WMPCAT,XXSPEC
          PACK      XXADATE,ADATE
          REP       " 0",XXADATE
          PACK      XXDDATE,DDATE
          REP       " 0",XXDDATE
          MOVE      WMDESC,XXPDESC
.
          PACK      KEY45,XXCONS,XXSNAME,XXURNO,XXPCODE,XXCOUNT
          CALL      WRTEMP1
          DISPLAY   *P47:24,XXURNO;
          GOTO      LOAD1000
.
LOAD9000  PACK      KEY45,SP30,SP20
          CALL      RSTEMP1
          CALL      RKTEMP1
          IF        OVRCD=1
            DISPLAY   *P1:24,*B,*EL,"No data found.  ";
            CALL      HITENTER
            MOVE      ONE,EXIT
          ELSE
            MOVE      ZERO,EXIT
          ENDIF
.
LOAD9999  RETURN
+
.****************************************************************************
.*  RPRT0000  :  Print report from temp file                                *
.****************************************************************************
RPRT0000  DISPLAY   *P1:24,*EL,*P35:24,"Printing...";
.
          CALL      IBACLOCK
          MOVE      ZERO,CPAGENO
          MOVE      ONE,CNOUNDLN
          MOVE      "60",CLNO                     * force new page
          MOVE      ZERO,NOPATS
          MOVE      ZERO,TOTPATS
.
          PACK      KEY45,SP30,SP20
          CALL      RSTEMP1 
RPRT0500  CALL      RKTEMP1                       * get next record
          BRANCH    OVRCD,RPRT9000                * no more records
.
. check if we need a new page
.
          COMPARE   "55",CLNO
          GOTO      RPRT4000 IF LESS
.
. we need a new page
.
          CALL      PAGE0000                      * print new page
.
. check if we have a new consultant
.
RPRT4000  MATCH     SP6,SAVCONS
          GOTO      RPRT5000 IF EQUAL
.
          MATCH     SAVCONS,XXCONS
          GOTO      RPRT5000 IF EQUAL
.
          CALL      UND132
          PRINT     *1,"No. patients for consultant = ",NOPATS
          CALL      PAGE0000                      * print a new page
          MOVE      ZERO,NOPATS
.
. print a line of data
.
RPRT5000  CALL      FDAT0000                      * format data for printing
.
          PRINT     *1,"| ",XXURNO,*12,"| ",XXSNAME,SP1,XXGNAME,*51,"| ":
                    XXSPEC,SP1,SPECDESC,*73,"|",DXXADATE,*84,"|",DXXDDATE:
                    *95,"| ",XXPDESC,*132,"|"
.
          ADD       ONE,CLNO                      * increment line counter
          MOVE      XXCONS,SAVCONS                * save consultant printed
          ADD       ONE,NOPATS                    * no records printed for cons
          ADD       ONE,TOTPATS                   * total records printed
          GOTO      RPRT0500
.
RPRT9000  CALL      UND132                        * print underlines
          PRINT     *1,"No. patients for consultant = ",NOPATS
          PRINT     *N,*1,"Total No. patients printed  = ",TOTPATS
          PRINT     *N,*1,"*** End of report ***" 
.
RPRT9999  RETURN
+
.*************************************************************************
.*  FDAT0000  :  Format data for printing                                *
.*************************************************************************
FDAT0000  MOVE      "<<Missing>>",TDESC
          PACK      KEY5,ANSA,SP1,XXSPEC
          CALL      RDCODE1
          MOVE      TDESC,SPECDESC
.
          UNPACK    XXADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DXXADATE
.
          UNPACK    XXDDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DXXDDATE
.
FDAT9999  RETURN
+
.*************************************************************************
.*  PAGE0000  :  Print a New page                                        *
.*************************************************************************
PAGE0000  CALL      HEAD132
          PRINT     *40,"Consultant from : ",PRSCONS:
                    *N,*40,"Consultant to   : ",PRECONS:
                    *N,*40,"List Type from  : ",PRSLTYPE:
                    *N,*40,"List Type to    : ",PRELTYPE:
                    *N,*40,"As at : ",DASATDAT,*N
.
          MOVE      "<<Missing>>",PACFNAME
          PACK      KEY6,XXCONS
          CALL      RDDOCT1
          IF        OVRCD=0
            MOVE      DGNAME,PACGNAME
            MOVE      DSNAME,PACSNAME
            MOVE      DTITL,PACTITLE
            MOVE      ONE,PACFRMT
            CALL      PACNAME
          ENDIF
          PRINT     *1,"Consultant : ",XXCONS,SP3,PACFNAME
.
          CALL      UND132
          PRINT     *1,"|  U/R No. | Patient Name",*51,"| Specialty",*73:
                    "| Adm date | Dis date | Procedure",*132,"|"
          CALL      UND132
.
          MOVE      TEN1,CLNO
          MOVE      SP6,SAVCONS
.
PAGE9999  RETURN
+
.*************************************************************************
.*  Temp file IO's                                                       *
.*************************************************************************
RSTEMP1   RESET     KEY45
          READ      TEMPFILE,KEY45;;
          RETURN
.
RDTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY45
          READ      TEMPFILE,KEY45;XXCONS,XXSNAME,XXURNO,XXPCODE,DXXCOUNT:
                                   XXGNAME,XXSPEC,XXADATE,XXDDATE,XXPDESC
          GOTO      OVERCOND IF OVER
          MOVE      DXXCOUNT,XXCOUNT
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMPFILE;XXCONS,XXSNAME,XXURNO,XXPCODE,DXXCOUNT:
                             XXGNAME,XXSPEC,XXADATE,XXDDATE,XXPDESC
          GOTO      OVERCOND IF OVER
          MOVE      DXXCOUNT,XXCOUNT
          RETURN
.
WRTEMP1   RESET     KEY45
          MOVE      XXCOUNT,DXXCOUNT
          WRITE     TEMPFILE,KEY45;XXCONS,XXSNAME,XXURNO,XXPCODE,DXXCOUNT:
                             XXGNAME,XXSPEC,XXADATE,XXDDATE,XXPDESC
          RETURN
.
UPTEMP1   MOVE      XXCOUNT,DXXCOUNT
          UPDATE    TEMPFILE;XXCONS,XXSNAME,XXURNO,XXPCODE,DXXCOUNT:
                             XXGNAME,XXSPEC,XXADATE,XXDDATE,XXPDESC
          RETURN
+
.*************************************************************************
.*  KILL0000  :  erase temporary file                                    *
.*************************************************************************
KILL0000  CLOSE     TEMPFILE
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    TFILNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
KILL9999  RETURN
+
.*************************************************************************
.*  CRET0000  :  create temporary file                                   *
.*************************************************************************
CRET0000  CALL      KILL0000 
          CLEAR     CMDLINE 
          APPEND    "isbuild ",CMDLINE
          APPEND    TFILNAME,CMDLINE
          APPEND    " 114 U1-6,7-26,27-34,35-43,44-45",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPFILE,TFILNAME
.
CRET9999  RETURN
.
.
          INC       STD001IO
          INC       PATCODKY
          INC       PATDOCKY
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATDSCIO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATMA1IO/INC
          INC       WATTR1IO/INC
          INC       WATSEAIO/INC
          INC       WATSEIIO/INC
          INC       TFILENAM
          INC       IBASEQIO/INC
          INC       WEBERRIO/INC

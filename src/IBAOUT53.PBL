.******************************************************************************
.*                                                                            *
.*              P R O G R A M  S U M M A R Y                                  *
.*              -------------  -------------                                  *
.*                                                                            *
.* PROGRAM NAME   : IBAOUT53                                                  *
.*                                                                            *
.* FUNCTION       : CLINIC MASTER LISTING                                     *
.*                                                                            *
.* DATE WRITTEN   : 01.07.88                                                  *
.*                                                                            *
.* AUTHOR         : BARTHOLOMEUX J. SIMPSON                                   *
.*                                                                            *
.* MODIFICATIONS  :                                                           *
.******************************************************************************
.*        V11.02.01 09/02/2022  Thanh T          TSK 0905641                  *
.*                  Recompiled as OUTMA1FD changes                            *
.*        V11.00.01 18/03/2020  Peter Vela     TSK 0889143                    *
.*                  Recompiled for OUTLETFD                                   *
.*        V10.07.01 29/10/2015  Ebon Clements  CAR 268970                     *
.*                  Change to use OUTCLIFD index 1 instead of index 2         *
.*        V5.08.01  15/08/2000  Tonii                                         *
.*                  Mods to perform positional read if a direct read fails to *
.*                  find the appropriate clinic description. Changes made in  *
.*                  CONT1, CONT3X and PRINTD.                                 *
.******************************************************************************
.*        V5.08.B01 14/03/2000  Steve Armstrong  5.08 Mods3                   *
.*                  Mods for changes to OUTCLIFD (effective date)             *
.******************************************************************************
.*        V5.04.01  07/04/1997  Steve Armstrong  SRF 642553                   *
.*                  Mods to print form feed after between template and next   *
.*                  Clinic Master.                                            *
.******************************************************************************
.*        V5.03.01  24/10/1995    Justin Coates  (SUHT mods)                  *
.*                  changed option 1 to print new details (OUTMPSCR)          *
.******************************************************************************
.
.         FD's needed
.
          INC       STD001FD
          INC       OUTCLIFD/INC
          INC       OUTCONFD/INC            * control file
          INC       OUTCTYFD/INC
          INC       OUTLETFD/INC
          INC       OUTMA1FD/INC 
          INC       OUTMABFD/INC
          INC       PATCODFD/INC
          INC       PATWR1FD/INC
          INC       PATDO1FD/INC
          INC       PATHSPFD/INC
.
.         program variables
.
BCOUNT    FORM      2             /* No. of slots printed down a column  */
.
CODE      DIM       2             /* Used in PATCODDS                    */
CODECG    INIT      "CG"          /* Clinic Group                        */
CODECI    INIT      "CI"          /* Clinic Indicator                    */
CODECV    INIT      "CV"          /* Visit Type                          */
CODECT    INIT      "CT"          /* Clinic Location                     */
COUNT     FORM      1
IBCNMHOS  FORM      1
CLIDESC   DIM       30            /* Clinic Id Description               */
CLINCNT   FORM      1
CMDLINE   DIM       30            /* Rollout command line                */
DESC22    DIM       22
DOCNAME   DIM       30
DESC25    DIM       25
DIM2A     DIM       2
DIM2B     DIM       2
DIM3      DIM       3
DIM5      DIM       5
DIM11     DIM       11
DIM16     DIM       16
DIM20A    DIM       20
DIM20B    DIM       20
DIM20C    DIM       20
DIM20D    DIM       20
DIM20E    DIM       20
DIM20F    DIM       20
DIM24     DIM       24
DIM26A    DIM       26
DIM26B    DIM       26
DSTAT     DIM       8             /* Description for status              */
DDESC     DIM       9             /* Day description for variables below */
DKYDAY    DIM       2             /* Day description for variables below */
DESCDOC   DIM       3             /* Description for Keyin Doctor Code   */
DESCREG   DIM       3             /* Description for Registrar's Clinic  */
DESCFRQ   DIM       5             /* Description for Session Frequency   */
.
FORM2A    FORM      2
FORM2B    FORM      2
FIRST     FORM      1
END6      DIM       6         * end code
LOADED    FORM      1
.
OLDTYPE   DIM       6
RUNTIME   FORM      3.1
POS       FORM      1
.
SAVEREC   FORM      1            /* Following 6 vars used in slot print */
SAVEKEY1  DIM       21
SAVEKEY2  DIM       21
SAVEKEY3  DIM       21
SAVEKEY4  DIM       21
SAVEKEYX  DIM       21
START6    DIM       6
.
XDIM6     DIM       6
YDIM6     DIM       6
.
.
.         program constants
.
DMON2     INIT      "MO"
DTUE2     INIT      "TU"
DWED2     INIT      "WE"
DTHU2     INIT      "TH"
DFRI2     INIT      "FR"
DSAT2     INIT      "SA"
DSUN2     INIT      "SU"
.
.
DMONTH    INIT      "Month"
DYEAR     INIT      "Year "
FSTREC    INIT      "START"
LN55      FORM      "55"
LSTREC    INIT      "FINISH"
SP21      INIT      "                     "
.
PRGID     INIT      "IBAOUT53"
PRGNAM    INIT      "Clinic Master Register"
VERSION   INIT      "V12.01.00"
+
.*********************************************************************
.*                  ML0000                                           *
.*        Semi Main Line Code                                        *
.*********************************************************************
ML0000    CALL      INIT0000                * initilization
.
ML1000    CALL      OPTN0000                * enter option
          BRANCH    MOPTION OF KCLIN,KCLIN,KTYPE
.
ML9999    CHAIN     PGM
+
.------------------------------------------------------------------------------
.        Clinic Id Sequence - Full
.------------------------------------------------------------------------------
.
KCLIN     DISPLAY   *P1:11,"Keyin Clinic Id From :":
                    *P1:12,"             Id To   :"
          MOVE      SP6,START6
          MOVE      SP6,END6
.
KEYST     KEYIN     *P24:12,*EL,*P24:11,*EL,*V2LON,START6,*P24:11,*DV,START6
          RESET     START6
          GOTO      CONT1 IF NOT EOS
.
          MOVE      SP6,START6
          DISPLAY   *P24:11,*V2LON,FSTREC
          GOTO      CONT2
.
CONT1     ENDSET    START6
          APPEND    SP6,START6
          RESET     START6
.
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          PACK      KEY20,OMASITE,START6,KEY8
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     OMASITE,OCASITE
              IF        !@EQUAL
                PACK      KEY20,OMASITE,START6,SP10
                CALL      RDSCLIA1
                CALL      RDKCLIA1
              ELSE
                MATCH     START6,OCACLIN
                IF        !@EQUAL
                  PACK      KEY20,OMASITE,START6,SP10
                  CALL      RDSCLIA1
                  CALL      RDKCLIA1
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          COMPARE   ZERO,OVRCD
          GOTO      CHKDC0 IF EQUAL
.
          DISPLAY   *P43:11,*B,*V2LON,"** Clinic Id does Not Exist **",*W2;
          GOTO      KEYST
.
CHKDC0    MATCH     SP30 TO OCADESC
          GOTO      NOTDOC1 IF NOT EQUAL
.
          PACK      KEY6 WITH OCADOCT
          CALL      RDDOCT1
          MOVE      DGNAME TO ANS
          PACK      OCADESC WITH ANS,DOT,DSNAME
.
NOTDOC1   DISPLAY   *P43:11,*V2LON,OCADESC
.
CONT2     KEYIN     *P24:12,*EL,*V2LON,END6
          RESET     END6
          GOTO      CONT3 IF NOT EOS
.
          MOVE      SP6,END6
          DISPLAY   *P24:12,*V2LON,LSTREC
          GOTO      CONFIRM
.
CONT3     ENDSET    END6
          APPEND    SP6,END6
          RESET     END6
.
          MATCH     START6,END6
          GOTO      CONT3X IF NOT LESS
.
          DISPLAY   *P43:12,*B,*V2LON,"** Must be greater than FROM id **",*W2;
          GOTO      KEYST
.
CONT3X    PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          PACK      KEY20,OMASITE,END6,KEY8
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     OMASITE,OCASITE
              IF        !@EQUAL
                PACK      KEY20,OMASITE,END6,SP10
                CALL      RDSCLIA1
                CALL      RDKCLIA1
              ELSE
                MATCH     END6,OCACLIN
                IF        !@EQUAL
                  PACK      KEY20,OMASITE,END6,SP10
                  CALL      RDSCLIA1
                  CALL      RDKCLIA1
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          COMPARE   ZERO,OVRCD
          GOTO      CHKDC1 IF EQUAL
.
          DISPLAY   *P43:12,*B,*V2LON,"** Clinic ID does Not Exist **",*W2;
          GOTO      CONT2
.
CHKDC1    MATCH     SP30 TO OCADESC
          GOTO      NOTDOC2 IF NOT EQUAL
.
          PACK      KEY6 WITH OCADOCT
          CALL      RDDOCT1
          MOVE      DGNAME TO ANS
          PACK      OCADESC WITH ANS,DOT,DSNAME
NOTDOC2   DISPLAY   *P43:12,*V2LON,OCADESC
          GOTO      CONFIRM
.
.------------------------------------------------------------------------------
.       Clinic Type Sequence
.------------------------------------------------------------------------------
.
KTYPE     DISPLAY   *P1:11,"Keyin Clinic Type From :":
                    *P1:12,"             Type To   :"
          MOVE      SP6,START6
          MOVE      SP6,END6
.
KTYPE0    KEYIN     *P26:12,*EL,*P26:11,*EL,*V2LON,START6,*P26:11,*DV,START6
          RESET     START6
          GOTO      KTYPE1 IF NOT EOS
.
          MOVE      SP6,START6
          DISPLAY   *P26:11,*V2LON,FSTREC
          GOTO      KTYPE2
.
KTYPE1    ENDSET    START6
          APPEND    SP6,START6
          RESET     START6
.
          PACK      KEY12 WITH OMASITE,START6
          CALL      RDCTYA1
          COMPARE   ZERO,OVRCD
          GOTO      KTYPE2 IF EQUAL
.
          DISPLAY   *P43:11,*B,*V2LON,"** Clinic Type does Not Exist **",*W2;
          GOTO      KTYPE0
.
KTYPE2    DISPLAY   *P35:11,*V2LON,OCTDESC
          KEYIN     *P26:12,*EL,*V2LON,END6
          RESET     END6
          GOTO      KTYPE3 IF NOT EOS
.
          MOVE      SP6,END6
          DISPLAY   *P26:12,*V2LON,LSTREC
          GOTO      CONFIRM
.
KTYPE3    ENDSET    END6
          APPEND    SP6,END6
          RESET     END6
.
          MATCH     START6,END6
          GOTO      KTYPE3X IF NOT LESS
.
          DISPLAY   *P43:12,*B,*V2LON,"** Must be greater than FROM Type **",*W2;
          GOTO      KTYPE0
.
KTYPE3X   PACK      KEY12 WITH OMASITE,END6
          CALL      RDCTYA1
          COMPARE   ZERO,OVRCD
          GOTO      KTYPOUT IF EQUAL
.
          DISPLAY   *P43:12,*B,*V2LON,"** Clinic Type does Not Exist **",*W2;
          GOTO      KTYPE2
.
KTYPOUT   DISPLAY   *P35:11,*V2LON,OCTDESC
.
.------------------------------------------------------------------------------
.       Start of print generation
.------------------------------------------------------------------------------
.
CONFIRM   KEYIN     *P1:24,*EL,"Ok to proceed with print (":
                    *V2LON,*DV,ANSY,*HOFF,*DV,SLASH:
                    *V2LON,*DV,ANSN,*HOFF:
                    ") ? ",*V2LON,ANS;
          REP       UPPLOW,ANS
.
          CMATCH    ANSN,ANS
          GOTO      ML1000 IF EQUAL
.
          CMATCH    ANSY,ANS
          GOTO      CONT4 IF EQUAL
.
          BEEP
          GOTO      CONFIRM
.
CONT4     DISPLAY   *P1:24,*EL,*P20:24,*V2LON:
                    "*** Printing Clinic Master File ***"
.
          BRANCH    MOPTION TO DISP1A,DISP1B,DISP1C
.
DISP1A    DISPLAY   *P1:24,*EL,"Processing Clinic Id :",*P35:24,"Slot No. :"
          GOTO      DISP1EX
.
DISP1B    DISPLAY   *P1:24,*EL,"Processing Clinic Id :"
          GOTO      DISP1EX
.
DISP1C    DISPLAY   *P1:24,*EL,"Processing Clinic Type :"
.
DISP1EX   CALL      HEADSET     /* Set up print variables for page header */
.
          MOVE      ZERO,CPAGENO
.
          BRANCH    MOPTION TO READ1,READ1,READ2
.
.------------------------------------------------------------------------------
. Options 1 & 2 - 1st key
.------------------------------------------------------------------------------
.
READ1     PACK      KEY18 WITH OMASITE,START6,SP6
          CALL      RDSMASA1
.
LOOP1     CALL      RDKMASA1
          BRANCH    OVRCD OF EOF
.
          MATCH     IDDD TO OMASITE
          GOTO      EOF IF NOT EQUAL
.
CONT51    MATCH     SP6,END6
          GOTO      CONT61 IF EQUAL
.
          MATCH     OMACLIN,END6
          GOTO      EOF IF LESS
.
CONT61    DISPLAY   *P24:24,*V2LON,OMACLIN;
          GOTO      LOOPOUT
.
.------------------------------------------------------------------------------
. Option 3 - 2nd Key
.------------------------------------------------------------------------------
.
READ2     PACK      KEY24 WITH OMASITE,START6,SP20
          CALL      RDSMASA2
.
LOOP2     CALL      RDKMASA2
          BRANCH    OVRCD OF EOF
.
          MATCH     IDDD TO OMASITE
          GOTO      EOF IF NOT EQUAL
.
CONT52    MATCH     SP6,END6
          GOTO      CONT62 IF EQUAL
.
          MATCH     OMATYP,END6
          GOTO      EOF IF LESS
.
CONT62    DISPLAY   *P26:24,*V2LON,OMATYP;
          GOTO      LOOPOUT
.
.------------------------------------------------------------------------------
. Details read already, print record.
.------------------------------------------------------------------------------
.
LOOPOUT   COMPARE   ZERO,CPAGENO
          CALL      PRNTHEAD IF EQUAL
.
          COMPARE   CLNO,LN55
          CALL      PRNTHEAD IF LESS
.
          CALL      PRINTD
          BRANCH    MOPTION TO LOOP1,LOOP1,LOOP2
          GOTO      LOOP1
.
.------------------------------------------------------------------------------
. End of file - tidy up report
.------------------------------------------------------------------------------
.
EOF       COMPARE   ZERO,CPAGENO
          GOTO      NOREC IF EQUAL
.
          PRINT     *N,*N,*45,"***  END OF REPORT  ***"
.
          DISPLAY   *P1:24,*EL,*P15:24,*V2LON:
                    "*** Generation Completed ***"
          GOTO      ML1000
.
NOREC     DISPLAY   *P1:24,*EL,*P20:24,*V2LON,*B:
                    "*** No Clinic Type selected ***",*W2;
          GOTO      ML1000
.
.------------------------------------------------------------------------------
. Print report header
.------------------------------------------------------------------------------
.
PRNTHEAD  ADD       ONE,CPAGENO
.
          CLOCK     TIME,CTIMEIS
          PRINT     *F,PRGID,*45,CNAME,*116,"DATE : ",CDATE:
                    *N,VERSION,*45,"*** CLINIC MASTER REGISTER ***":
                    *116,"PAGE :",SP6,CPAGENO:
                    *N,*116,"TIME : ",CTIMEIS
.
          BRANCH    MOPTION TO RPTHDR1,RPTHDR2,RPTHDR3
.
RPTHDR1   PRINT     *45,"Clinic Id Sequence":
                    *N,"Clinic Id From : ",XDIM6,*26," to ",YDIM6,*N
          MOVE      EIGHT TO CLNO
          RETURN
.
RPTHDR2   PRINT     *45,"Clinic Id Sequence":
                    *N,"Clinic Id From : ",XDIM6,*26," to ",YDIM6,*N:
                    *N,"Clinic",*9,"Clinic",*27,"Clinic",*40,"Session Time":
                    *54,"Clinic",*66,"Reg's",*74,"No.of Slots",*89,"Slot":
                    *98,"Keyin",*116,"Sess.":
                    *N,*3,"Id",*9,"Description",*28,"Type",*35,"Day":
                    *40,"Start",*48,"End",*54,"Indicator",*66,"Clinic":
                    *74,"New",*78,"Special",*87,"Duration",*97,"Dr Code":
                    *107,"Status",*116,"Freq.":
                    *N,"------",*9,"-----------",*27,"------",*35,"---":
                    *40,"-----",*47,"-----",*54,"---------",*66,"------":
                    *74,"---",*78,"-------",*87,"--------",*97,"-------":
                    *107,"------",*116,"-----"
          MOVE      TEN TO CLNO
          RETURN
.
RPTHDR3   PRINT     *45,"Clinic Type Sequence":
                    *N,"Clinic Type From : ",XDIM6,*28," to ",YDIM6,*N:
                    *N,"Clinic",*9,"Clinic",*40,"Session Time",*54,"Clinic":
                    *66,"Reg's",*74,"No.of Slots",*89,"Slot",*98,"Keyin":
                    *126,"Sess.":
                    *N,*3,"Id",*9,"Description",*35,"Day",*40,"Start":
                    *48,"End",*54,"Indicator",*66,"Clinic",*74,"New":
                    *78,"Special",*87,"Duration",*97,"Dr Code":
                    *107,"Status",*116,"Freq.":
                    *N,"------",*9,"-----------",*35,"---",*40,"-----":
                    *47,"-----",*54,"---------",*66,"------",*74,"---":
                    *78,"-------",*87,"--------",*97,"-------":
                    *107,"------",*116,"-----"
.
          MOVE      TEN TO CLNO
.
          BRANCH    FIRST TO RPTRET
          MOVE      ZERO TO FIRST
          PACK      KEY12 WITH OMASITE,OMATYP
          CALL      RDCTYA1
          PRINT     *N,*N,"*------------------------------------":
                    "------------------*":
                    *N,"| Clinic Type : ",OMATYP,*25,OCTDESC,*56,"|":
                    *N,"*------------------------------------":
                    "------------------*",*N
          ADD       FIVE TO CLNO
.
RPTRET    RETURN
.
.------------------------------------------------------------------------------
.                   PRINTD
.         Print records
.------------------------------------------------------------------------------
.
PRINTD    PACK      KEY5 WITH CODECI,OMACIND
          CALL      RDCODE1
          MOVE      TDESC TO DIM11
.
          PACK      KEY20,OMASITE,OMACLIN,OMADATE
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     OMASITE,OCASITE
              IF        !@EQUAL
                PACK      KEY20,OMASITE,OMACLIN,SP10
                CALL      RDSCLIA1
                CALL      RDKCLIA1
              ELSE
                MATCH     OMACLIN,OCACLIN
                IF        !@EQUAL
                  PACK      KEY20,OMASITE,OMACLIN,SP10
                  CALL      RDSCLIA1
                  CALL      RDKCLIA1
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          IF        OVRCD = 1
            MOVE      "Unknown clinic",OCADESC
            PACK      OCADESC,OCADESC,SP30
          ENDIF
.
          MATCH     SP30 TO OCADESC
          GOTO      NOTDOC3 IF NOT EQUAL
.
          PACK      KEY6 WITH OCADOCT
          CALL      RDDOCT1
          MOVE      DGNAME TO ANS
          PACK      OCADESC WITH ANS,DOT,DSNAME
.
NOTDOC3   MOVE      OCADESC TO DIM16
          MOVE      OCADESC TO DIM24
.
          LOAD      DKYDAY BY OMADAY FROM DMON2,DTUE2,DWED2,DTHU2:
                    DFRI2,DSAT2,DSUN2
.
          LOAD      DDESC BY OMADAY FROM DMON,DTUE,DWED,DTHU:
                    DFRI,DSAT,DSUN
.
          MOVE      DMONTH,DESCFRQ
          LOAD      DESCFRQ BY OMAFREQF FROM DYEAR
.
          CMATCH    ANSY TO OMAREG
          GOTO      NOTYES1 IF NOT EQUAL
          MOVE      "Yes" TO DESCREG
          GOTO      CHKDOC1
.
NOTYES1   MOVE      "No " TO DESCREG
.
CHKDOC1   CMATCH    ANSY TO OMAKDOC
          GOTO      NOTYES2 IF NOT EQUAL
          MOVE      "Yes" TO DESCDOC
          GOTO      CHKSTAT
.
NOTYES2   MOVE      "No " TO DESCDOC
.
CHKSTAT   MOVE      SP8 TO DSTAT
          COMPARE   ZERO TO OMASTAT
          GOTO      NOTACTV IF NOT EQUAL
          MOVE      "Active  " TO DSTAT
          GOTO      PRBRNC
.
NOTACTV   MOVE      "Inactive" TO DSTAT
.
.         print the details for the report
.
PRBRNC    BRANCH    MOPTION,PRINTD1,PRINTD2,PRINTD3
.
.         SRF 642553
.         Mods to print a form feed after second and subsequent Clinic Masters
.
PRINTD1   IF        CLINCNT > 0
            PRINT     *F
          ENDIF
.
          ADD       ONE,CLINCNT             * increment cli. master count
          CALL      PRNA0000                * print screen one
          CALL      PRNB0000                * print screen two
          CALL      PRNC0000                * print screen three
          CALL      PRND0000                * print screen four
          PACK      KEY18,OMASITE,OMACLIN,OMADAY,OMASTART
          IF        OTCNXTSF = ONE
            PROC      OUTPRXSF                * print extra staff
          ENDIF
          PACK      KEY18,OMASITE,OMACLIN,OMADAY,OMASTART
          PROC      OUTPMASB                * print master B
          RETURN
.
PRINTD2   PRINT     OMACLIN,*9,DIM16,*27,OMATYP,*35,DKYDAY,*40,OMASTART:
                    *47,OMAEND,*54,DIM11,*68,DESCREG,*75,OMANEWSL:
                    *81,OMASPCSL,*89,OMADURN,*99,DESCDOC,*107,DSTAT:
                    *116,DESCFRQ
          ADD       ONE TO CLNO
          RETURN
.
PRINTD3   MATCH     OLDTYPE TO OMATYP
          GOTO      NOTNEW IF EQUAL
.
          MOVE      OMATYP TO OLDTYPE
          PACK      KEY12 WITH OMASITE,OMATYP
          CALL      RDCTYA1
          PRINT     *N,"*------------------------------------":
                    "------------------*":
                    *N,"| Clinic Type : ",OMATYP,*25,OCTDESC,*56,"|":
                    *N,"*------------------------------------":
                    "------------------*",*N
          ADD       FIVE TO CLNO
.
NOTNEW    PRINT     OMACLIN,*9,DIM24,*35,DKYDAY,*40,OMASTART:
                    *47,OMAEND,*54,DIM11,*67,DESCREG,*75,OMANEWSL:
                    *81,OMASPCSL,*89,OMADURN,*99,DESCDOC,*107,DSTAT:
                    *116,DESCFRQ
          ADD       ONE TO CLNO
          RETURN
.
.------------------------------------------------------------------------------
. Set up report header variables
.------------------------------------------------------------------------------
.
HEADSET   MOVE      SP6,XDIM6
          MOVE      SP6,YDIM6
          MOVE      START6,XDIM6
          MOVE      END6,YDIM6
          MATCH     SP6,START6
          GOTO      CONT8A IF NOT EQUAL
          MOVE      FSTREC,XDIM6
CONT8A    MATCH     SP6,END6
          RETURN    IF NOT EQUAL
          MOVE      LSTREC,YDIM6
          RETURN
.
.------------------------------------------------------------------------------
. The end ...
.------------------------------------------------------------------------------
.
ENDIT     CHAIN     PGM
          STOP
.
+
.------------------------------------------------------------------------------
.       Subroutine to check that all fixed parameters are ok.
.       Returns 0 in OVRCD if ok, else OVRCD = 1.
.       Used after every readks of 2nd master file & temp file.
.------------------------------------------------------------------------------
.
CHECKMAS  MOVE      ZERO TO OVRCD
          MATCH     OMASTART TO OMBSTRT
          GOTO      OVERCOND IF NOT EQUAL
          COMPARE   OMADAY TO OMBDAY
          GOTO      OVERCOND IF NOT EQUAL
          MATCH     OMACLIN TO OMBCLIN
          GOTO      OVERCOND IF NOT EQUAL
          MATCH     OMASITE TO OMBSITE
          GOTO      OVERCOND IF NOT EQUAL
.
          RETURN
.
.------------------------------------------------------------------------------
.         Routine to print OUTMASB1 without using memory wasting variables.
.------------------------------------------------------------------------------
.
. Needs the following, as well as variables from OUTMASFD :
.
. SAVEREC   FORM      1
. BCOUNT    FORM      2
. SP21      INIT      "                     "
. SAVEKEY1  DIM       21
. SAVEKEY2  DIM       21
. SAVEKEY3  DIM       21
. SAVEKEY4  DIM       21
. SAVEKEYX  DIM       21
. KEY21     DIM       21
. POS       FORM      1
. LOADED    FORM      1
.
.
PRNTMASB  PACK      KEY21 WITH OMASITE,OMACLIN,OMADAY,OMASTART,SP3
          MOVE      SP21 TO SAVEKEYX
          MOVE      ZERO TO LOADED
.
READINIT  MOVE      ZERO TO SAVEREC
          MOVE      TWENTY2 TO BCOUNT
          MOVE      SP21 TO SAVEKEY1
          MOVE      SP21 TO SAVEKEY2
          MOVE      SP21 TO SAVEKEY3
          MOVE      SP21 TO SAVEKEY4
          CALL      RDSMASB1
.
LOOPMASB  CALL      RDKMASB1
          BRANCH    OVRCD TO PRNTLINE
.
          MATCH     OMASTART TO OMBSTRT
          GOTO      PRNTLINE IF NOT EQUAL
          COMPARE   OMADAY TO OMBDAY
          GOTO      PRNTLINE IF NOT EQUAL
          MATCH     OMACLIN TO OMBCLIN
          GOTO      PRNTLINE IF NOT EQUAL
          MATCH     OMASITE TO OMBSITE
          GOTO      PRNTLINE IF NOT EQUAL
.
          ADD       ONE TO BCOUNT
          COMPARE   TWENTY3 TO BCOUNT
          GOTO      LOOPMASB IF NOT EQUAL
.
          MOVE      ZERO TO BCOUNT
          ADD       ONE TO SAVEREC
          PACK      KEY21 WITH OMBSITE,OMBCLIN,OMBDAY,OMBSTRT,OMBSLOT
.
          MATCH     SAVEKEYX TO KEY21
          GOTO      READEXIT IF EQUAL
.
          STORE     KEY21 BY SAVEREC INTO SAVEKEY1,SAVEKEY2,SAVEKEY3,SAVEKEY4
.
          BRANCH    LOADED TO SKIPSAVE
          MATCH     SP21 TO SAVEKEY2
          GOTO      SKIPSAVE IF EQUAL
          MOVE      SAVEKEY2 TO SAVEKEYX
          MOVE      ONE TO LOADED
.
SKIPSAVE  COMPARE   FOUR TO SAVEREC
          GOTO      PRNTLINE IF EQUAL
.
          GOTO      LOOPMASB
.
PRNTLINE  MOVE      ZERO TO POS
          PRINT     "|";
.
PRNTLOOP  ADD       ONE TO POS
          LOAD      KEY21 BY POS FROM SAVEKEY1,SAVEKEY2,SAVEKEY3,SAVEKEY4
.
          MATCH     SP21 TO KEY21
          GOTO      PRNTEXIT IF EQUAL
.
          CALL      RDMASB1
.
          DISPLAY   *P46:24,*V2LON,OMBSLOT;
.
          PRINT     SP2,OMBSLOT,SP4,OMBTIME,SP4,OMBVTYP,SP10,SP1;
.
          COMPARE   POS TO SAVEREC
          GOTO      PRNTLOOP IF NOT EQUAL
.
PRNTEXIT  PRINT     *130,"|"
          MATCH     SP21 TO SAVEKEY1
          GOTO      READEXIT IF EQUAL
.
          MOVE      SAVEKEY1 TO KEY21
          GOTO      READINIT
.
READEXIT  RETURN
..............................................................................
.*********************************************************************
.*                  INIT0000                    Called by : ML0000   *
.*        Initilization and open files                               *
.*********************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening";
.
          PACK      CFNAME WITH UID,FILCTYA1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTCTYA1,CFNAME
.
          PACK      CFNAME WITH UID,FILCLIA1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTCLIA1,CFNAME
.
          PACK      CFNAME WITH UID,FILMASB1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTMASB1,CFNAME
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY   *P64:24,"outletrf";
          OPEN      OUTLETR1,"outletrf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          PACK      CFNAME WITH UID,FILMA1A1
          DISPLAY   *P64:24,CFNAME;
          CALL       OPOTMA11
          CALL       OPOTMA12
          CALL       OPOTMA13
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS     * Multi hospital parameter
          READ      CONTROLF,THIRTY1;*51,HOAUDA,*63,HOAUDM,*95,HORVCODE:
                                    *145,OTCNLGCS,*149,OTCNOPSS,*150,OTCNOPIS:
                                    *153,OTCNAVIS,OTCNSTVT,OTCNSUPX:
                                    *157,OTCNRUSE,*184,OTCNDCMB,*200,OTCNUDDC:
                                    *202,OTCNUARF,*208,OTCNCCIM,*227,OTCNUWDA:
                                    *237,OTCNXTPL,*245,OTCNCSLD,*249,OTCNCLVE
          READ      CONTROLF,THIRTY1;*185,OTCNSQO2
          READ      CONTROLF,THIRTY2;*36,OTCNGSES,*38,OTCNOPCR,*41,OTCNURVS:
                                     OTCNOVBK,OTCNXTSF,OTCNNCOC
.
          MOVE      IDDD TO OMASITE      /* Site code held in common */
          MOVE      IDDD TO OCASITE      /* Used by id "?" enquiry   */
          MOVE      IDDD TO OCTSITE      /* Used by type "?" enquiry */
.
INIT9999  RETURN
+
.*********************************************************************
.*                  OPTN0000                    Called by : ML1000   *
.*        Enter the report option - MOPTION                          *
.*********************************************************************
OPTN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Clinic Id Sequence ":
                    "(with session details)":
                    *P1:6,*V2LON,TWO,*HOFF," = Clinic Id Sequence":
                    *P1:7,*V2LON,THREE,*HOFF," = Clinic Type Sequence"
.
OPTN1000  KEYIN     *P1:9,*EF,"Option : _":
                    *P10:9,*V2LON,MOPTION;
.
          COMPARE   ZERO TO MOPTION
          IF        @EQUAL
            MOVE      ZERO,CLINCNT               * initialise cl. master count
            GOTO      OPTN9999
          ENDIF
.
          MOVE      ONE TO FIRST  /* First page - do not print type desc */
.
          BRANCH    MOPTION,OPTN9999,OPTN9999,OPTN9999
.
          BEEP 
          GOTO      OPTN1000
.
OPTN9999  RETURN
+
.         I/O includes
.
          INC       STD001IO/PBL
          INC       OUTCLIIO/INC
          INC       OUTCTYIO/INC
          INC       OUTMA1IO/INC
          INC       OUTMABIO/INC
          INC       OUTLETIO/INC
          INC       OUTMPSCR
          INC       PATHSPIO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATWR1IO/INC
+

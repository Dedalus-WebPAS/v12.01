.***************************************************************************
.* System    :   Patient Management System                                 *
.* Program   :   FXPRTINV                                                  *
.***************************************************************************
.* Date      :   30/03/2011                                                *
.* Author    :   J.Tan                                                     *
.* Mods      :                                                             *
.*             V10.01.00 30/03/2010  J.Tan         CAR  233202             *
.*                       Created program to set No of lines of invoice body*
.***************************************************************************
.
          INC       STD001FD
.
MAXLINE   FORM      2
.
. FILE DESCRIPTION INCLUDES
. -------------------------
.
          INC       PATCONFD/INC
.
PRGID     INIT      "FXPRTINV"
PRGNAM    INIT      "Fixed No of lines of Invoice body"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select FROM and TO
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
.
          CALL      PROC0000               * process records for update
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                     * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"          * patient master file
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Valid option selected                  *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF:
                    " = Set default to No of lines of Invoice Body"
.
OPTN1000  KEYIN     *P1:7,*EL,"Select option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION
          GOTO      OPTN9500 IF EQUAL                 * exit
.
          BRANCH    COPTION,OPTN2000
.
          BEEP
          GOTO      OPTN1000
.
OPTN2000  CALL      CONTQST                          * Ok to continue ?
          BRANCH    CEXIT,OPTN9000,OPTN1000,OPTN9500 * Yes, no, cancel
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.****************************************************************************
.*                                PROC0000            Called by: ML0000     *
.*              read through master file                                    *
.****************************************************************************
PROC0000  DISPLAY   *P1:24,*EL,"Processing ..";
.
          READ      CONTROLF,TEN;*240,PTCNINVN
          READ      CONTROLF,TWENTY1;*51,PTCNINVL
.
          MOVE      "38",MAXLINE
.
          IF        PTCNINVL=14
            MOVE      "33",MAXLINE     * RCH
          ENDIF
.
          IF        PTCNINVL=15
            MOVE      "23",MAXLINE     * STV
          ENDIF
.
          IF        PTCNINVL=11 | PTCNINVL=12
            MOVE      "26",MAXLINE     * HolySpirit/Greenslop
          ENDIF
.
          IF        PTCNINVL=16
            MOVE      "24",MAXLINE     * Pendlebury
          ENDIF
.
          IF        PTCNINVL=22
            MOVE      "30",MAXLINE     * Johns hopkins
          ENDIF
.
          IF        PTCNINVL=24 | PTCNINVL=25
            MOVE      "30",MAXLINE     * SJOG/Cabrini
          ENDIF
.
          IF        PTCNINVL=18
            MOVE      "24",MAXLINE     * Hec
          ENDIF
.
          MOVE      MAXLINE,PTCNINVN
.
          WRITAB    CONTROLF,TEN;*240,PTCNINVN
.
PROC9000  DISPLAY   *P1:24,*EL,"Processing complete.  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO

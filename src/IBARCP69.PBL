.******************************************************************************
.* System    :   CASH RECEIPTING                                              *
.* Program   :   IBARCP69                                                     *
.* Desc      :   Un-reconciled cash report                                    *
.******************************************************************************
.* Date      :   05/06/1995                                                   *
.* Author    :   Rodney Knowles                                               *
.* Mods      :                                                                *
.*                                                                            *
.* V10.14.01 21/03/2019  J.Tan            TSK 0857392                         *
.*           Changed RCP Transaction count from DIM3 to DIM5                  *
.* V10.07.02 29/01/2016  J.Tan    CAR 303942                                  *
.*           Mods for Payment types                                           *
.* V10.07.01 16/12/2015  J.Tan    CAR 303942                                  *
.*           Mods for new payment types                                       *
.* V10.02.01 28/07/2010  J.Tan    CAR 244159                                  *
.*           Mods for Credit Card Surchagre Register 89                       *
.* V9.11.01  09/01/2009  J.Tan         CAR 184993                             *
.*           Added range of cashier drawers                                   * 
.* V9.09.01  07/09/2007  Ebon Clements CAR 149260                             *
.*           Changed Payee to Payer                                           * 
.* V9.03.03  12/05/2004  Lina Vo      CAR 49813                               *
.*           Changed Money Order to use same field as Cheque Number           * 
.* V9.03.02  03/03/2004  Lina Vo      CAR 47921                               *
.*           Removed use of PRIDBTFD & IO.                                    * 
.* V9.03.01  27/01/2004  Lina Vo      CAR 44074                               *
.*           Changed program to use new receipting tables.                    * 
.*           Changed report layout to be the same as IBARCP62.                * 
.* V9.02.02  14/02/2003  Dean Cramer  CAR 22238                               *
.*           Recompiled for RCPTRNFD                                          * 
.* V9.00.01  18/12/2002  Dean Cramer   srf 22393                              *
.*           Included all detail outside of rcptrnaf for reconcilliation      *
.* V9.00.B01 26/06/2001  Dean Cramer                                          *
.*           Fixed entry option responses      22393                          *
.* V5.10.B01 01/03/2001  Ebon Clements  SRF 8546                              *
.*           Fixed printing of report heading if the total was zero           *
.* V5.07.02  30/03/1999  Jill Habasque                                        *
.*           Mods to line up report                                           *
.* V5.07.B01 02/11/1998  Davin                                                *
.*           Mods for 507                                                     *
.* V5.04.01 19.11.1996 Sandra Barcham                                         *
.*          Ported from release V6.04.01                                      *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
.
          INC       DEBDBTFD/INC
.
          INC       RCPCONFD/INC
          INC       RCPREGFD/INC
          INC       RCPBNKFD/INC
          INC       RCPBDTFD/INC
          INC       RCPDTEFD/INC
          INC       RCPCIDFD/INC   * cash drawer file
.
          INC       PATCODFD/INC
          INC       PATMA1FD/INC
          INC       PATINVFD/INC
          INC       PATVISFD/INC
          INC       PRIINVFD/INC
...       INC       PRIDBTFD/INC
.
CODECR    INIT      "CR"
.
DIM6      DIM       6
DIM10     DIM       10
DISPNAME  DIM       60
DRAWERF   DIM       6
DRAWERT   DIM       6
.
FRSTFLG   FORM      1
FROMDATE  DIM       8
FROMDATD  DIM       10
.
LINCNT    FORM      2
.
NAME35    DIM       35
.
OLDRECNO  DIM       12
.
PAGENO    FORM      3
PAYTYPE   FORM      1
PRNTFLAG  FORM      1
PATFNAME  DIM       60
PDESC     DIM       4
.
RECONCIL  DIM       3
.
SDESC     DIM       9
STATUS0   INIT      "Not Bankd"
STATUS1   INIT      "Cancelled"
STATUS2   INIT      "Banked   "
SYDESC    DIM       14
SYST0     INIT      "N/A   "
SYST1     INIT      "Pat Billing"
SYST2     INIT      "Pri Practice"
SYST5     INIT      "A/C Receivable"
SYST89    INIT      "Surcharge"
SYST90    INIT      "EMR Deposit"
SYST91    INIT      "OUT Deposit"
SYST92    INIT      "GST Cash Sales"
SYST94    INIT      "Sund Debtr Rec"
SYST95    INIT      "Sund Cash Sale"
SYST96    INIT      "Pri Deposit"
SYST97    INIT      "Pat Deposit"
SYST98    INIT      "Internal Recpt"
SYST99    INIT      "Suspense"
.
TODATE    DIM       8
TODATD    DIM       10
TOTAMT   FORM       10.2
.
DOWNCASE  INIT      "AaBbCcDdEeFfGgHhIiJjKkLlMmNnOoPpQqRrSsTtUuVvWwXxYyZz"
.
.-----------------------------------------------------------------------------
PRGID     INIT      "IBARCP69"
PRGNAM    INIT      "Un-Reconciled Cash Report"
VERSION   INIT      "V12.01.00"
.-----------------------------------------------------------------------------
.
MAIN0000  CALL      INIT0000            * display screen header & open file
.
MAIN1000  CALL      OPTN0000            * ask for option
          BRANCH    EXIT,MAIN9999
.
MAIN2000  CALL      PROC0000            * process & print report
          GOTO      MAIN1000
.
MAIN9999  CHAIN     PGM
          STOP
+
.-----------------------------------------------------------------------------
.  INIT0000 - Initialization
.-----------------------------------------------------------------------------
.
INIT0000  CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening rcpregaf";
          OPEN      RCPREGA1,"rcpregaf"
          DISPLAY   *P56:24,"Opening rcpbnkaf";
          OPEN      RCPBNKA2,"rcpbnkaf"
          DISPLAY   *P56:24,"Opening rcpbdtaf";
          OPEN      RCPBDTA1,"rcpbdtaf"
          DISPLAY   *P56:24,"Opening rcpdteaf";
          OPEN      RCPDTEA1,"rcpdteaf"
          OPEN      RCPCIDA1,"rcpcidaf"
          DISPLAY   *P56:24,"Opening patvisaf";
          OPEN      PATVISA1,"patvisaf"
.
          DISPLAY   *P64:24,"debdbtaf";
          OPEN      DEBDBTA1,"debdbtaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,THIRTY6;*87,HSYS2,HSYS3
.
          IF        HSYS2=1
            DISPLAY   *P64:24,"patinvrf";
            OPEN      PATINVR1,"patinvrf"
            DISPLAY   *P64:24,"patma1af";
            OPEN      PATMA1A1,"patma1af"
            OPEN      PATMX1A1,"patmx1af"
          ENDIF
.
          IF        HSYS3=1
...         DISPLAY   *P64:24,"pridebtf";
...         OPEN      PRIDEBT1,"pridebtf"
            DISPLAY   *P64:24,"priinvof";
            OPEN      PRIINVO1,"priinvof"
          ENDIF
.
INIT9999  RETURN
.
.-----------------------------------------------------------------------------
.  OPTN0000 - display user options
.-----------------------------------------------------------------------------
.
OPTN0000  MOVE      ZERO,EXIT
          MOVE      "           0",OLDRECNO
          MOVE      ZERO,FRSTFLG
          MOVE      ZERO,TOTAMT
          MOVE      "60",LINCNT
          DISPLAY   *P1:10,*EF,"From date :":
                    *P1:12,"To   date :";
.
          UNPACK    SP10,CDAY,CMON,CYEAR,CCENT
          MOVE      TEN4,CCOL
          MOVE      TEN,CVERT
          CALL      KEYDATE
          BRANCH    OVRCD,OPTN9000
.
          PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",FROMDATE
          PACK      FROMDATD,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.
          UNPACK    SP10,CDAY,CMON,CYEAR,CCENT
          MOVE      TEN4,CCOL
          MOVE      TEN2,CVERT
          CALL      KEYDATE
          BRANCH    OVRCD,OPTN0000,OPTN0000
.
          PACK      TODATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE
          PACK      TODATD,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.
          MATCH     TODATE,FROMDATE
          GOTO      OPTN1000 IF EQUAL
          GOTO      OPTN1000 IF LESS
.
          DISPLAY   *P1:24,*EL,*B:
                    "To Date must be greater than the From Date - ";
          CALL      HITENTER
          GOTO      OPTN0000
.
OPTN1000  CALL      CDRF0000            * enter from cash drawer
          BRANCH    EXIT,OPTN9000
.
          CALL      CDRT0000            * enter to cash drawer
          BRANCH    EXIT,OPTN1000
.
          CALL      CONTQST
          BRANCH    CEXIT,OPTN9999,OPTN0000
          GOTO      OPTN1000
.
OPTN9000  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.****************************************************************************
.*                                CDRF0000             Called by: RECN00000 *
.*                        Key in cash drawer from                           *
.****************************************************************************
CDRF0000  KEYIN     *P1:3,*EF,*P1:14,"Cash Drawer From: ",*V2LON,DRAWERF
.
          ENDSET    DRAWERF
          APPEND    SP6,DRAWERF
          RESET     DRAWERF
.
          MATCH     SP6,DRAWERF
          GOTO      CDRF9000 IF EQUAL
.
          MOVE      DRAWERF,KEY6
          CALL      RDCIDA1
          BRANCH    OVRCD,CDRF1000
          GOTO      CDRF9000
.
CDRF1000  DISPLAY   *P1:24,"Invalid Cash Drawer";
          CALL      HITENTER
          MOVE      ONE,EXIT
          GOTO      CDRF9999
.
CDRF9000  MOVE      ZERO,EXIT
.
CDRF9999  RETURN
+
.****************************************************************************
.*                                CDRT0000             Called by: RECN00000 *
.*                        Key in cash drawer to                             *
.****************************************************************************
CDRT0000  KEYIN     *P1:15,"Cash Drawer To: ",*V2LON,DRAWERT
.
          ENDSET    DRAWERT
          APPEND    SP6,DRAWERT
          RESET     DRAWERT
.
          MATCH     SP6,DRAWERT
          IF        @EQUAL
            PACK      DRAWERT,Z70
            GOTO      CDRT9000
          ENDIF
.
          MOVE      DRAWERT,KEY6
          CALL      RDCIDA1
          BRANCH    OVRCD,CDRT1000
          MATCH     DRAWERF,DRAWERT
          GOTO      CDRT2000 IF LESS
          GOTO      CDRT9000
.
CDRT1000  DISPLAY   *P1:24,"Invalid Cash Drawer";
          CALL      HITENTER
          MOVE      ONE,EXIT
          GOTO      CDRT9999
.
CDRT2000  DISPLAY   *P1:24,"Cash Drawer from must be less than Cash Drawer to";
          CALL      HITENTER
          MOVE      ONE,EXIT
          GOTO      CDRT9999
.
CDRT9000  MOVE      ZERO,EXIT
.
CDRT9999  RETURN
+
.-----------------------------------------------------------------------------
.  PROC0000 - process and print report
.-----------------------------------------------------------------------------
.
PROC0000  MOVE      ZERO,CPAGENO
          DISPLAY   *P1:24,*EL;
.
          PACK      KEY26,FROMDATE,DRAWERF,SP20
          CALL      RSRCBNK2
PROC1000  CALL      RKRCBNK2
          BRANCH    OVRCD,PROC9000
.
          MATCH     RCBNTDAT,TODATE
          GOTO      PROC9000 IF LESS
.
          MATCH     SP70,DRAWERF
          IF        !@EQUAL
            MATCH     DRAWERF,RCBNCDRW
            GOTO      PROC1000 IF LESS
          ENDIF
.
          MATCH     Z70,DRAWERT
          IF        !@EQUAL
            MATCH     RCBNCDRW,DRAWERT
            GOTO      PROC1000 IF LESS
          ENDIF
.
          UNPACK    RCBNTDAT,XCENT,XYEAR,XMON,XDAY
          PACK      DIM10,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
          DISPLAY   *P20:24,*V2LON,DIM10,"  ",RCBNRECN;
.
          MATCH     "1",RCBNSTAT             * ignore cancelled
          GOTO      PROC1000 IF EQUAL
          MATCH     SP10,RCBNRDAT            * only un-reconciled
          GOTO      PROC1000 IF NOT EQUAL
.
          ADD       RCBNTOTP,TOTAMT
.
          MOVE      STATUS0,SDESC            * get status type
          MOVE      ZERO,F1
          MOVE      RCBNSTAT,F1
          LOAD      SDESC,F1,STATUS1,STATUS2
.
          MATCH     RCBNRDAT,SP10
          IF        @EQUAL
            MOVE      "No",RECONCIL
          ELSE
            MOVE      "Yes",RECONCIL
          ENDIF
.
. ---- read of receipt bank details ----
.
          MOVE      ONE,PRNTFLAG
          PACK      KEY15,RCBNRECN,SP70
          CALL      RSRCBDT1
PROC2100  CALL      RKRCBDT1
          IF        OVRCD=1
            GOTO      PROC3000
          ENDIF
.
          MATCH     RCBNRECN,RCBDRECN
          GOTO      PROC3000 IF NOT EQUAL
.
          GOTO      PROC6000
.
. ---- print receipt bank total ----
.
PROC3000  MOVE      TWO,PRNTFLAG
          PRINT     *73,"-------------":
                    *N,*71,RCBNTOTP:
                    *N,*73,"-------------",*N
          ADD       FOUR,LINCNT               * Increment line counter
.
. ---- read of receipt register details ----
.
          PACK      KEY17,RCBNRECN,SP70
          CALL      RSRCDTE1
PROC3100  CALL      RKRCDTE1
          IF        OVRCD=1
            GOTO      PROC3200
          ENDIF
.
          MATCH     RCBNRECN,RCDTRECN
          GOTO      PROC3200 IF NOT EQUAL
.
          GOTO      PROC6000
.
. ---- print receipt register total ----
.
PROC3200  PRINT     *73,"-------------":
                    *N,*71,RCBNTOTP:
                    *N,*73,"-------------",*N
          ADD       FOUR,LINCNT               * Increment line counter
          MOVE      ZERO,PRNTFLAG
          GOTO      PROC1000
.
. ------- Print -------
.
PROC6000  COMPARE   "53",LINCNT
          CALL      HEAD0000 IF NOT LESS
.
          MATCH     RCBNRECN,OLDRECNO
          GOTO      PROC6200 IF EQUAL
.
          COMPARE   ZERO,FRSTFLG
          GOTO      PROC6100 IF EQUAL
          PRINT     *N;
          ADD       ONE,LINCNT               * Increment line counter
.
.      print receipt header details
.
PROC6100  MOVE      ONE,FRSTFLG
          PRINT     *1,RCBNRECN,*14,XDAY,SLASH,XMON,SLASH,XYEAR,*23,RCBNCDRW:
                    *31,SDESC,*42,RECONCIL;
.
PROC6200  IF        PRNTFLAG=ONE
            CALL      PRTPAY00               * Print receipt payment details
          ELSE
            CALL      PRTREG00               * Print receipt register details
          ENDIF
.
          MOVE      RCBNRECN,OLDRECNO

PROC8000  BRANCH    PRNTFLAG,PROC2100,PROC3100   * read next receipt detail rec
          GOTO      PROC1000
.
.         End of processing of all sites
.
PROC9000  PRINT     *N,*73,"-------------":
                    *N,*63,"Total",*73,TOTAMT:
                    *N,*73,"=============":
                    *N,*N,"*** End of Report ***"
.
PROC9999  RETURN
+
.-----------------------------------------------------------------------------
.  HEAD0000 - print heading
.-----------------------------------------------------------------------------
.
HEAD0000  CLOCK     TIME,CTIMEIS
          ADD       ONE,PAGENO
          MOVE      ONE,CNOUNDLN
          MOVE      "       0",OLDRECNO
          MOVE      ZERO,FRSTFLG
          CALL      HEAD132
.
          MATCH     SP70,DRAWERF
          IF        @EQUAL
            MOVE      "Start ",KEY6
          ELSE
            MOVE      DRAWERF,KEY6
          ENDIF
          MATCH     Z70,DRAWERT
          IF        @EQUAL
            MOVE      "End   ",DIM6
          ELSE
            MOVE      DRAWERT,DIM6
          ENDIF
.
          PRINT     *40,"From Date : ",FROMDATD,"   To Date : ",TODATD:
                    *N,*40,"From Cash Drawer: ",KEY6," To: ",DIM6;
.
          PRINT    *N,*N,*23," Cash ",*47,"Payment/":
                   *N,"     Receipt ",*14,"Date",*23,"Drawer":
                   *31,"Status",*42,"Recn",*47,"Register",*56,"System":
                   *71,"         Amount",*87,"Reference":
                   *N,"------------",*14,"--------",*23,"-------":
                   *31,"----------",*42,"----",*47,"--------":
                   *56,"--------------",*71,"---------------":
                   *87,"----------------------------------------------"
.
          MOVE      TEN,LINCNT
.
HEAD9999  RETURN
.
.-----------------------------------------------------------------------------
.  GETREG00  :  Get system code & desc
.-----------------------------------------------------------------------------
.
GETREG00  PACK      KEY3,RCDTREGI
          CALL      RDREGA1
          IF        OVRCD=1
            MOVE      ZERO,REGIBACD
          ENDIF
.
          MOVE      SYST0,SYDESC
          LOAD      SYDESC,REGIBACD,SYST1,SYST2,SYST0,SYST0,SYST5
.
          IF        REGIBACD>=90
            MOVE      ZERO,F2
            MOVE      REGIBACD,F2
            SUB       "90",F2
            MOVE      SYST90,SYDESC
            LOAD      SYDESC,F2,SYST91,SYST92,SYST0,SYST94,SYST95:
                                SYST96,SYST97,SYST98,SYST99
          ENDIF
          IF        REGIBACD=89
            PACK      SYDESC,SYST89,SP70
          ENDIF     
.
GETREG99  RETURN
+
.-----------------------------------------------------------------------------
.  PRTPAY00  :  Print payment details - rcpbdtaf
.-----------------------------------------------------------------------------
.
PRTPAY00  MOVE      SP70,PDESC               * get Payment type
          MOVE      ZERO,F1
          MOVE      RCBDPTYP,F1
          MOVE      F1,F2
.
          MOVE      "Py",KEY2
          PACK      KEY5,KEY2,RCBDPCOD
          CALL      RDCODE1
          IF        OVRCD=0
            SQUEEZE   PTCDNHCP
            MOVE      PTCDNHCP,F2
            PACK      PDESC,TDESC,SP70
          ENDIF
.
          PRINT     *47,PDESC,*71,RCBDPAMT;
          ADD       ONE,LINCNT               * Increment line counter
.
          BRANCH    F2,PRTPAY10,PRTPAY20,PRTPAY30,PRTPAY40,PRTPAY50:
                       PRTPAY60,PRTPAY40,PRTPAY40,PRTPAY10
          COMPARE   ZERO,F2
          GOTO      PRTPAY99 IF EQUAL
.
          PRINT     *87,"Payer:",RCBDPAYD:
                    *N,*87,"Ref:",RCBDSTRF
          ADD       ONE,LINCNT               * Increment line counter
          GOTO      PRTPAY99
.
PRTPAY10  PRINT     *87,"Payer:",RCBDPAYD
          GOTO      PRTPAY99
.
PRTPAY20  PRINT     *87,"Drawer:",RCBDDRAW,*115,"Cheque No:",RCBDCHQN:
                    *N,*87,"Bank:",RCBDBANK:
                    *N,*87,"Branch:",RCBDBRCH
          ADD       TWO,LINCNT               * Increment line counter
          GOTO      PRTPAY99
.
PRTPAY30  PACK      KEY5,CODECR,RCBDCRDT
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE      RCBDCRDT,TDESC
          ENDIF
          PRINT     *87,"Payer:",RCBDPAYD:
                    *N,*87,"Card Type:",TDESC
          GOTO      PRTPAY99
.
PRTPAY40  PRINT     *87,"Payer:",RCBDPAYD:
                    *N,*87,"Statement Ref:",RCBDSTRF
          ADD       ONE,LINCNT               * Increment line counter
          GOTO      PRTPAY99
.
PRTPAY50  PRINT     *87,"Money Order:",RCBDCHQN
          GOTO      PRTPAY99
.
PRTPAY60  MATCH     "1",RCBDEFTT
          IF        @EQUAL
            PRINT     *87,"Payer:",RCBDPAYD," EFT Type:Cash":
                      *N,*87,"Reference:",RCBDSTRF
            ADD       ONE,LINCNT               * Increment line counter
          ELSE
            PACK      KEY5,CODECR,RCBDCRDT
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      RCBDCRDT,TDESC
            ENDIF
            PRINT     *87,"Payer:",RCBDPAYD," EFT Type:Credit":
                      *N,*87,"Card Type:",TDESC:
                      *N,*87,"Reference:",RCBDSTRF
            ADD       TWO,LINCNT               * Increment line counter
          ENDIF
          GOTO      PRTPAY99
.
PRTPAY99  RETURN
+
.-----------------------------------------------------------------------------
.  PRTREG00  :  Print register details - rcpdteaf
.-----------------------------------------------------------------------------
.
PRTREG00  CALL      GETREG00                 * Get System Code
.
          PRINT     *47,RCDTREGI,*56,SYDESC,*71,RCDTAMNT:
                    *87,"Reference:",RCDTREFR;
          ADD       ONE,LINCNT               * Increment line counter
.
          CALL      GETPNM00                 * Get Patient or Debtor Name
.
          COMPARE   ONE,REGIBACD             * Patient Billing
          GOTO      PRTREG61 IF EQUAL
.
          IF        REGIBACD=2
            MATCH     "1",RCDTSFLG
            GOTO      PRTREG62 IF EQUAL      * Private Practice
            GOTO      PRTREG61
          ENDIF
.
          COMPARE   FIVE,REGIBACD            * Account Receivable
          GOTO      PRTREG61 IF EQUAL
          COMPARE   EIGHTY9,REGIBACD         * Credit Card Surcharge
          GOTO      PRTREG65 IF EQUAL
          COMPARE   NINTY,REGIBACD           * Emergency Deposit
          GOTO      PRTREG63 IF EQUAL
          COMPARE   NINTY1,REGIBACD          * Outpatient Deposit
          GOTO      PRTREG63 IF EQUAL
          COMPARE   NINTY6,REGIBACD          * Private Practice Deposit
          GOTO      PRTREG64 IF EQUAL
          COMPARE   NINTY7,REGIBACD          * Patient Deposit
          GOTO      PRTREG63 IF EQUAL
          COMPARE   NINTY2,REGIBACD          * GST Sundry Cash Sales
          GOTO      PRTREG65 IF EQUAL
          COMPARE   NINTY4,REGIBACD          * Sundry Debtor Rec.
          GOTO      PRTREG65 IF EQUAL
          COMPARE   NINTY5,REGIBACD          * Sundry Cash Sales
          GOTO      PRTREG65 IF EQUAL
          COMPARE   NINTY8,REGIBACD          * Internal Receipts
          GOTO      PRTREG65 IF EQUAL
          COMPARE   NINTY9,REGIBACD          * Suspense
          GOTO      PRTREG65 IF EQUAL
.
PRTREG61  PRINT     *N,*87,"Patient Name:",DISPNAME:
                    *N,*87,"Invoice:",RCDTINVN," Debtor:",RCDTURNO
          ADD       TWO,LINCNT               * Increment line counter
          GOTO    PRTREG99
.
PRTREG62  PRINT     *N,*87,"Patient Name:",DISPNAME:
                    *N,*87,"Invoice:",RCDTINVN," U/R:",RCDTURNO
          ADD       TWO,LINCNT               * Increment line counter
          GOTO    PRTREG99
.
PRTREG63  PRINT     *N,*87,"Patient Name:",DISPNAME:
                    *N,*87,"Visit:",RCDTVIST," U/R:",RCDTURNO
          ADD       TWO,LINCNT               * Increment line counter
          GOTO    PRTREG99
.
PRTREG64  PRINT     *N,*87,"Patient Name:",DISPNAME:
                    *N,*87,"U/R:",RCDTURNO
          ADD       TWO,LINCNT               * Increment line counter
          GOTO    PRTREG99
.
PRTREG65  PRINT     *N,*87,"Name:",RCDTNAME
          ADD       ONE,LINCNT               * Increment line counter
          GOTO    PRTREG99
.
PRTREG99  RETURN
+
.-----------------------------------------------------------------------------
.  GETPNM00  :  Get Patient or Debtor Name
.-----------------------------------------------------------------------------
GETPNM00  MOVE      SP70,DISPNAME
          COMPARE   ONE,REGIBACD
          GOTO      GETPNM10 IF EQUAL
          COMPARE   TWO,REGIBACD
          GOTO      GETPNM20 IF EQUAL
          COMPARE   FIVE,REGIBACD
          GOTO      GETPNM30 IF EQUAL
          COMPARE   NINTY,REGIBACD
          GOTO      GETPNM10 IF EQUAL
          COMPARE   NINTY1,REGIBACD
          GOTO      GETPNM10 IF EQUAL
          COMPARE   NINTY6,REGIBACD
          GOTO      GETPNM20 IF EQUAL
          COMPARE   NINTY7,REGIBACD
          GOTO      GETPNM10 IF EQUAL
          GOTO      GETPNM99
.
. ------- Patient -------
.
GETPNM10  MOVE      RCDTURNO,PURNO
          MOVE      PURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,GETPNM99

          CALL      PATNAA00
          GOTO      GETPNM99
.
. ------- Private practice Debtors -------
.
GETPNM20  MATCH     "1",RCDTSFLG
          GOTO      GETPNM10 IF EQUAL
.
...       MOVE      RCDTURNO,KEY8
...       CALL      RDPRDB1
...       BRANCH    OVRCD,GETPNM99
.
          MOVE      SP70,PTITL
...       MOVE      PRDBGNAM,PGNAME
...       MOVE      PRDBSNAM,PSNAME
          MOVE      "Unknown Debtor",PGNAME
          MOVE      SP70,PSNAME
          CALL      PATNAA00
          GOTO      GETPNM99
.
.
. ------- Acc Rec Debtors -------
.
GETPNM30  MOVE      RCDTURNO,KEY8
          CALL      RDDBDB1
          BRANCH    OVRCD,GETPNM99
.
          MOVE      SP70,PTITL
          MOVE      DBDBNA1,PGNAME
          MOVE      DBDBNA2,PSNAME
          CALL      PATNAA00
          GOTO      GETPNM99
.
GETPNM99  RETURN
+
.------------------------------------------------------------
. Format Patient Name in Title Text
.------------------------------------------------------------
.
PATNAA00  CLEAR     DISPNAME
          MOVE      SP70,PATFNAME
          PACK      NAME35,PTITL,SP70
          CALL      NAMSTR00
          PACK      NAME35,PGNAME,SP70
          CALL      NAMSTR00
          PACK      NAME35,PSNAME,SP70
          REP       UPPLOW,PSNAME
          APPEND    PSNAME,DISPNAME
          APPEND    SP70,DISPNAME
          RESET     DISPNAME
          MOVE      DISPNAME,PATFNAME
          STRIP     PATFNAME
          RETURN
.
.*********************************************************************
.
          INC       STD001IO
.
          INC       DEBDBTIO/INC
.
          INC       RCPREGIO/INC
          INC       RCPBNKIO/INC
          INC       RCPBDTIO/INC
          INC       RCPDTEIO/INC
          INC       RCPCIDIO/INC
.
          INC       PATCODIO/INC
          INC       PATINVIO/INC
          INC       PATMA1IO/INC
          INC       PATVISIO/INC
.
...       INC       PRIDBTIO/INC
          INC       PRIINVIO/INC
.
          INC       NAMSTRCD

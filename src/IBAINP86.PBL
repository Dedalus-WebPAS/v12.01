.******************************************************************************
.*                                                                            *
.*                        P R O G R A M   S U M M A R Y                       *
.*                        -------------   -------------                       *
.*                                                                            *
.*     Program Name   :  IBAINP86                                             *
.*                                                                            *
.*     Function       :  Admissions and discharges for a day report           *
.*                                                                            *
.*     Date Written   :  12/07/93                                             *
.*                                                                            *
.*     Author         :  Gabrielle Cameron                                    *
.*                                                                            *
.*     Comments       :  copied from IBAPAT96, added reffering doctor         *
.*                       Quote  7726     SRF 122728                           *
.*                                                                            *
.*     Modifications  :                                                       *
.*                                                                            *
.*        V12.00.01 30.05.2025  DA Sarkies     TSK 0955096
.*                  Updated for Alpha-Numeric Visit Number
.*        V11.00.01 10/03/2020  Jill Parkinson Task 0879163                   *
.*                  Added SEXDSCIO include                                    *
.*        V10.13.01 05/12/2018  Don Nguyen    TSK 0838558                     *
.*                  Deleted temp file (PSTROL) after processing               *
.*        V10.05.01 01/01/2015  Ania P        CAR 261630                      *
.*                  Removed use of PORT for temp file naming                  *
.*        V10.02.02 27/04/2011  Saroeun Soeur CAR 241405                      *
.*                  fixed non multi hospital                                  *
.*        V10.02.01 15/04/2011 Saroeun Soeur  CAR 241405                      *
.*                  added hospital filter from multi hospital                 *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V9.12.01  20/11/2009  WeeLee Koo    CAR 210219                      *
.*                  Displayed full patient given name instead of 1st letter   *
.*        V9.10.01  05/06/2008  Jill Habasque CAR 169707                      *
.*                  Ported from 6.16                                          *
.*        V6.12.00  17/05/2004  Mike Laming   CAR 49016                       *
.*                  July 2004 PRS/2 - changed to use sex routine SEXDSC00     *
.*        V6.07.02  08/09/2000  Steve Downing                                 *
.*                  Mods to include Indeterminate and Unknown sex             *
.*        V6.07.01  04/07/2000  Tonii  SRF 636992                             *
.*                  Mods to display the Health Fund & Table, unless none is   *
.*                  available then display the Claim Code                     *
.*        V6.07.B01 11/04/2000  Greg Horvat                                   *
.*                  Removed MATADAFD MATADBFD & MATADBIO                      *
.*        V6.06.01  18/10/1999  Steve Downing    SRF 635164                   *
.*                  Fixed page numbering & removed extra ------ line being    *
.*                  printed on a separate page                                *
.******************************************************************************
.*        V6.05.02  08/12/1998  Nicole Harrington                             *
.*                  Removed CCENTRY                                           *
.*        V6.05.01  29/10/1998  Jill Habasque                                 *
.*                  Changed to display centuries                              *
.*        V6.03.01  20/08/1996  Greg Horvat      SRF 616065                   *
.*                  Fixed a bug when validating the referring doctor & the    *
.*                  read is invalid & renamed the program IBAINP86 rather     *
.*                  than IBAIMP86                                             *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
.
          INC       IBACONFD/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATDO1FD/INC
          INC       PATDSCFD/INC
          INC       PATFN1FD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATTRNFD/INC
          INC       PATHSPFD/INC
          INC       PMSVX1FD/INC
          INC       IBASEQFD/INC  
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
+
.         WORK AREA
.
PSTADMP1 IFILE     SQL, FIXED=176, KEY="U1-28"
PSTDSHP1 IFILE     SQL, FIXED=176, KEY="U1-28"
.
.-----------------------------------------------------------------
.Name      Type     Length     Physical     Start
.----      ----     ------     --------     -----
DIM20      DIM         20           20         1
XDAADMNO   DIM          8            8        21
.AURNO     DIM          8            5        29
D5TIME     DIM          5            5        34
.PSNAME    DIM         20           20        39
.PGNAME    DIM         25           25        59
DHOSP      DIM          7            7        84
DDSTAT     DIM         18           18        91
DCLMTAB    DIM         23           23       109
DNAME      DIM         20           20       132
.TWARD     DIM          3            3       152
.TBED      DIM          3            3       155
RDOCNAME   DIM         18           18       158
.
.End of Record                               176
.
XDDADMNO   DIM          8            8        21
.DURNO     DIM          8            5        27
.
.  redefine form fields from key
.  -----------------------------
.Name      Type     Length     Start
.----      ----     ------     -----
.AADMNO    DIM          8        21
.DADMNO    DIM          8        21
+
.-----------------------------------------------------------------
.
MATATMP1 IFILE     SQL, FIXED=99, KEY="U1-29"
MATDTMP1 IFILE     SQL, FIXED=99, KEY="U1-29"
.
.-----------------------------------------------------------------
.Name      Type     Length     Physical     Start
.----      ----     ------     --------     -----
NBNAME     DIM         20           20         1
NBMATNO    DIM          8            8        21
NBPLUR     DIM          1            1        29
NBURNO     DIM          8            8        30
NBSEX      DIM          1            1        38
NBDOB      DIM          8            8        39
NBSTAT     FORM         1            2        47
NBGNAME    DIM         25           25        49
.PGNAME    DIM         25           25        74
.
.End of Record                                99
.-----------------------------------------------------------------
.
CMDLINE   DIM       50
CPNO      FORM      3    *Previously certain occurances of CPAGENO
DIM7      DIM       7
DIM11     DIM       11
.
FNAMEA    DIM       8
FNAMED    DIM       8
FNAMER    DIM       8
FNAMEU    DIM       8
FNAMEDIS  DIM       8
FNAMENEW  DIM       8
.
.
MODEH     FORM      1
OPCND     FORM      1
.
PSTSRT    FILE      ASCII, FIXED=256
PSTROL    FILE      ASCII, FIXED=256
.
SEC       FORM      1
STATUS    FORM      1
.
TOTPAT    FORM      8
.
.      EXTRA VARIABLES
.
ACTDATE   DIM       8        * ACTUAL DATE IN (CCYYMMDD) FORMAT
.
CODED     INIT      "D "
CODECL    INIT      "CL"
CTRF      FORM      4
CTRM      FORM      4
CTRI      FORM      4
CTRR      FORM      4                                                  *I-49016
CTRU      FORM      4
CTRX      FORM      4[9]                                               *I-49016
.
DHEAD     DIM       24
DHEAD1    INIT      "(Admission No. Sequence)"
DHEAD2    INIT      "(Surname Sequence)      "
DOCNAME   DIM       18
DSEX      DIM       8                                         * was 7  *C-49016
.
MABFLAG   FORM      1
MABSTAT1  INIT      "DISCHARGED "
MABSTAT2  INIT      "DECEASED   "
MIN1      FORM      1
.
PATADM    FORM      1
PATNAME   DIM       20
.
.
TODATE    DIM       8        * Date report is being generated for
.
XCENT1    DIM       2
XDAY1     DIM       2
XMON1     DIM       2
XYEAR1    DIM       2
ZERO8     INIT      "0000    "
.
HOSNAME   DIM       50
HOSCODE   DIM       3
.
PRGID     INIT      "IBAINP86"
PRGNAM    INIT      "Admissions & Discharges For A Day Report"
VERSION   INIT      "V12.01.00"
+
.
.         Display Program Header and open files
.
          CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"patfn1af";
          OPEN      PATFN1A1,"patfn1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A3,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.  
          READ      CONTROLF,TEN5;*188,CMATRN,*249,CRDRTYP
          READ      CONTROLF,ZERO;*43,IBCNMHOS

+
.        START OF PROGRAM
.
START     MOVE      ZERO,CPAGENO
          MOVE      ZERO,CPNO
          MOVE      ZERO,TOTPAT
          MOVE      ZERO,CTRM
          MOVE      ZERO,CTRF
          MOVE      ZERO,CTRI
          MOVE      ZERO,CTRU
          MOVE      ZERO,CTRR
          MOVE      ZERO,DSEXNO
          WHILE     DSEXNO < 9
            ADD       ONE,DSEXNO
            MOVE      ZERO,CTRX[DSEXNO]
          DO
.
          DISPLAY   *P1:3,*EF:
                   *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Admission Number Sequence":
                    *P1:6,*V2LON,TWO,*HOFF," = Patient Surname Sequence":
                    *P1:8,"Select Option :";
.
START1    KEYIN     *P17:8,*EF,*DV,UNDLN1,*P17:8,*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
          BRANCH    OPTION OF OPTHDR,OPTHDR
          BEEP
          GOTO      START1
.
OPTHDR    LOAD      DHEAD USING OPTION OF DHEAD1,DHEAD2
.
. ------  Keyin date report is being generated for  ------
.
          DISPLAY   *P1:13,*EF,"Generate report for ";
.
          MOVE      TEN3,CVERT
          MOVE      "21",CCOL
          MOVE      CVERT,EVERT
          MOVE      "50",ECOL
          MOVE      ONE,CDEFDTE
          MOVE      ZERO,CHIGHLT
.
KDATE1    DISPLAY   *P21:13,*EF;
          UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
.
          CALL      KEYDATE
          BRANCH    CQUEST,KDATE1
          BRANCH    OVRCD,START1
.
          PACK      TODATE WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE
          UNPACK    TODATE INTO XCENT1,XYEAR1,XMON1,XDAY1
.
.         Check if Date is ok or not
.
REKEY     CALL      KHOS0000            * keyin hospital (jay)
          BRANCH    EXIT,REKEY
.
          KEYIN     *P1:24,*EF,"Proceed with current options? (",*V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          MATCH     "N" TO ANS
          GOTO      KDATE1 IF EQUAL
.
          MATCH     "Y" TO ANS
          GOTO      SURNAM IF EQUAL
          BEEP
          GOTO      REKEY
.
.         Access via Patient Surname
.
SURNAM    BRANCH    OPTION OF KADMNO
          MOVE      "60",CLNO
          MOVE      ZERO,TOTPAT
          DISPLAY   *P1:24,*EL,*P25:24,*V2LON:
                    "** Initializing Report File **",*W2;
.
          CALL      INDZD
          BRANCH    OPCND OF ENDIT
          GOTO      KADMNO
.
INVMASTA  DISPLAY   *P15:23,*B,*EL,"** Admission ",*V2LON,AADMNO,*HOFF:
                    " has an invalid U/R ",*V2LON,AURNO,*HOFF," **";
.
          PRINT     "** Admission ",AADMNO," has an invalid U/R ":
                    AURNO,".  Please contact I.B.A. **"
          ADD       ONE,CLNO
          GOTO      NEXTA
.
.         use Date on Admission KEY 4  
.
KADMNO    PACK      KEY16,TODATE,SP6
          CALL      RDSMISS3
          MOVE      "60",CLNO
          MOVE      ZERO,TOTPAT
.
          DISPLAY   *P1:24,*EL,*P31:24,"Admission :";
.
NEXTA     CALL      RDKMISS3
          BRANCH    OVRCD OF ENDP1
.
          IF        IBCNMHOS=1
            MATCH     SP70,HOSCODE        * All Hospitals
            IF        !@EQUAL
              PACK      KEY8,AADMNO,SP70
              CALL      RDPMVX11
              IF        OVRCD <> 1
                MATCH     PMVXMHOS,HOSCODE    * Correct hospital
                GOTO      NEXTA IF NOT EQUAL
              ELSE
                GOTO      NEXTA
              ENDIF
            ENDIF
          ENDIF
.
          COMPARE   ONE,ASTAT
          GOTO      NEXTA IF EQUAL
.
          COMPARE   FIVE,ASTAT
          GOTO      NEXTA IF EQUAL
.
          UNPACK    ADATE INTO XCENT,XYEAR,XMON,XDAY
          PACK      ACTDATE WITH XCENT,XYEAR,XMON,XDAY
          REP       " 0",ACTDATE
.
          MATCH     ACTDATE,TODATE
          GOTO      ENDP1 IF NOT EQUAL
.
          PACK      KEY8,AURNO
          CALL      RDMAST1
          BRANCH    OVRCD OF INVMASTA
.
          COMPARE   ONE,CRDRTYP
          IF        !@EQUAL
            MOVE      SP30,RDOCNAME
            PACK      KEY6,ADOCTR
            CALL      RDDOCT1
            BRANCH    OVRCD,ATENDOC1
.
            MOVE      DGNAME,ANS
            PACK      RDOCNAME,ANS,DOT,DSNAME 
          ELSE
            PACK      RDOCNAME,ARDRNAM
          ENDIF
.
ATENDOC1  MOVE      SP30,DNAME
          PACK      KEY6,ADOCTA
          CALL      RDDOCT1
          BRANCH    OVRCD,OKDATA1
.
          MOVE      DGNAME,ANS
          PACK      DNAME WITH ANS,DOT,DSNAME
          MOVE      DNAME,DOCNAME
.
OKDATA1   PACK      KEY30,AADMNO,SP20
          CALL      RDSTRAN2
          CALL      RDKTRAN2
          BRANCH    OVRCD TO NOADREC
.
          IF        IBCNMHOS=1
            MATCH     SP70,HOSCODE        * All Hospitals
            IF        !@EQUAL
              PACK      KEY8,TADMN,SP70
              CALL      RDPMVX11
              IF        OVRCD <> 1
                MATCH     PMVXMHOS,HOSCODE    * Correct hospital
                GOTO      OKDATA1 IF NOT EQUAL
              ELSE
                GOTO      OKDATA1
              ENDIF
            ENDIF
          ENDIF
.
          MATCH     AADMNO,TADMN
          GOTO      NOADREC IF NOT EQUAL
.
          MATCH     "A" TO TMOVE
          GOTO      OKDAT1A IF EQUAL
.
NOADREC   MOVE      SP3 TO TWARD
          MOVE      SP3 TO TBED
.
OKDAT1A   MOVE      ATIME,D5TIME
          MOVE      SP30,DCLMTAB
.
          MATCH     SP6,AFUNDH                * Is there a Health Fund Code?
          GOTO      OKDR1 IF NOT EQUAL        * Yes, print Health Fund Code
.
          PACK      KEY5,CODECL,ACLAIM
          CALL      RDCODE1
          BRANCH    OVRCD,PDF1
          MOVE      TDESC,DCLMTAB             * Claim Code description
          GOTO      PDF1
.
OKDR1     MOVE      ZERO8,HFTABL
          PACK      KEY14,AFUNDH,HFTABL
          CALL      RDFUND1
          BRANCH    OVRCD OF PDF0
          MOVE      HFNAME,DCLMTAB
.
PDF0      PACK      KEY14,AFUNDH,AFNDTB
          CALL      RDFUND1
          BRANCH    OVRCD OF PDF1
          PACK      DCLMTAB,AFUNDH,SLASH,HFNAME     * HF and table Description
.
PDF1      DISPLAY   *P44:24,*V2LON,AADMNO;
.
          BRANCH    OPTION OF NORMPA
.
          MOVE      PSNAME,DIM20
          PACK      KEY28,DIM20,AADMNO
.
          READ      PSTADMP1,KEY28;ANS
          GOTO      NEXTA IF NOT OVER
.
          MOVE      SP3 TO DDSTAT
          WRITE     PSTADMP1,KEY28;KEY28,AURNO,D5TIME,PSNAME,PGNAME:
                                   DHOSP,DDSTAT,DCLMTAB,DNAME,TWARD,TBED:
                                   RDOCNAME
          GOTO      NEXTA
.
NORMPA    CALL      DISPDA
          GOTO      NEXTA
.
INVMASTD  DISPLAY   *P15:23,*B,*EL,"** Discharge ",*V2LON,DADMNO,*HOFF:
                    " has an invalid U/R ",*V2LON,DURNO,*HOFF," **";
.
          PRINT     "** Discharge ",DADMNO," has an invalid U/R ":
                    DURNO,".  Please contact I.B.A. **"
          ADD       ONE,CLNO
          GOTO      NEXTD
.
NOADMN    DISPLAY   *P1:24,*EL,*P22:24,*B,*V2LON:
                    "** No Admissions for date requested **",*W3;
+
DSCHNO1   COMPARE   ZERO,CPNO
          GOTO      DSCHNO2 IF EQUAL
.
          CALL      PAGEND
.
.* ****************************************** Start of Addition        *I-49016
          MOVE      CTRX[1],CTRM
          MOVE      CTRX[2],CTRF
          MOVE      CTRX[3],CTRI
          MOVE      CTRX[4],CTRR
          MOVE      CTRX[9],CTRU
.* ******************************************   End of Addition        *I-49016
.
          COMPARE   ZERO,CTRM
          GOTO      DSXEPP IF NOT EQUAL
.
          COMPARE   ZERO,CTRF
          GOTO      DSXEPP IF NOT EQUAL
.
          COMPARE   ZERO,CTRI
          GOTO      DSXEPP IF NOT EQUAL
.
          COMPARE   ZERO,CTRU
          GOTO      DSCHNO1A IF EQUAL
.
DSXEPP    PRINT     *12,"NUMBER OF NEWBORN MALES          : ",CTRM,*N:
                    *12,"NUMBER OF NEWBORN FEMALES        : ",CTRF,*N:
                    *12,"NUMBER OF NEWBORN INDETERMINATES : ",CTRI,*N:
                    *12,"NUMBER OF NEWBORN UNKNOWNS       : ",CTRU,*N:
                    *N,"  ***  End of Admissions  ***"
.
          MOVE      ZERO,CPNO
          MOVE      ZERO,CTRM
          MOVE      ZERO,CTRF
          MOVE      ZERO,CTRI
          MOVE      ZERO,CTRU
.* ****************************************** Start of Addition        *I-49016
          MOVE      ZERO,CTRR
          MOVE      ZERO,DSEXNO
          WHILE     DSEXNO < 9
            ADD       ONE,DSEXNO
            MOVE      ZERO,CTRX[DSEXNO]
          DO
.* ******************************************   End of Addition        *I-49016
.
.
          GOTO      DSCHNO2
.
DSCHNO1A  PRINT     *N,"  Total patients = ",TOTPAT,*N:
                    *N,"  *** End of Admissions ***"
          MOVE      ZERO,CPNO
.
.         Print Discharges
.
DSCHNO2   PACK      KEY16,TODATE,SP6
          CALL      RDSDSCH2
          MOVE      "60",CLNO
          MOVE      ZERO,TOTPAT
.
          DISPLAY   *P1:24,*EL,*P31:24,"Discharge :";
.
NEXTD     CALL      RDKDSCH2
          BRANCH    OVRCD OF ENDP2
.
          IF        IBCNMHOS=1
            MATCH     SP70,HOSCODE        * All Hospitals
            IF        !@EQUAL
              PACK      KEY8,DADMNO,SP70
              CALL      RDPMVX11
              IF        OVRCD <> 1
                MATCH     PMVXMHOS,HOSCODE    * Correct hospital
                GOTO      NEXTD IF NOT EQUAL
              ELSE
                GOTO      NEXTD
              ENDIF
            ENDIF
          ENDIF
.
          UNPACK    DDATE INTO XCENT,XYEAR,XMON,XDAY
          PACK      ACTDATE WITH XCENT,XYEAR,XMON,XDAY
          REP       " 0",ACTDATE
.
          MATCH     ACTDATE,TODATE
          GOTO      ENDP2 IF NOT EQUAL
.
          PACK      KEY8,DURNO
          CALL      RDMAST1
          BRANCH    OVRCD,INVMASTD
.
          PACK      KEY8,DADMNO
          CALL      RDMISS1
.
          COMPARE   ONE,CRDRTYP
          IF        !@EQUAL
            MOVE      SP30,RDOCNAME
            PACK      KEY6,ADOCTR
            CALL      RDDOCT1
            BRANCH    OVRCD,ATENDOC
.
            MOVE      DGNAME,ANS
            PACK      RDOCNAME,ANS,DOT,DSNAME 
          ELSE
            PACK      RDOCNAME,ARDRNAM
          ENDIF
.
ATENDOC   MOVE      SP30,DNAME
          PACK      KEY6,ADOCTA
          CALL      RDDOCT1
          BRANCH    OVRCD,OKDATA2
.
          MOVE      DGNAME,ANS
          PACK      DNAME WITH ANS,DOT,DSNAME
          MOVE      DNAME,DOCNAME
.
OKDATA2   PACK      KEY30,AADMNO,ACTDATE,SP20
          CALL      RDSTRAN2
.
OKLOOP    CALL      RDKTRAN2
          BRANCH    OVRCD TO NODISRC
.
          IF        IBCNMHOS=1
            MATCH     SP70,HOSCODE        * All Hospitals
            IF        !@EQUAL
              PACK      KEY8,TADMN,SP70
              CALL      RDPMVX11
              IF        OVRCD <> 1
                MATCH     PMVXMHOS,HOSCODE    * Correct hospital
                GOTO      OKLOOP IF NOT EQUAL
              ELSE
                GOTO      OKLOOP
              ENDIF
            ENDIF
          ENDIF
.          
          MATCH     AADMNO,TADMN
          GOTO      NODISRC IF NOT EQUAL
.
          MATCH     "D" TO TMOVE
          GOTO      OKLOOP IF NOT EQUAL
          GOTO      OKDATA3
.
NODISRC   MOVE      SP3 TO TWARD
          MOVE      SP3 TO TBED
.
OKDATA3   MOVE      SP20,DDSTAT
.
          MATCH     SP3,DSTAT
          GOTO      OKDA1 IF EQUAL
.
          PACK      KEY5,CODED,DSTAT
          CALL      RDCODE1
          MOVE      TDESC,DDSTAT
.
OKDA1     MOVE      DTIME,D5TIME
.
          DISPLAY   *P44:24,*V2LON,DADMNO;
.
          BRANCH    OPTION OF NORMPD
.
          MOVE      PSNAME,DIM20
          PACK      KEY28,DIM20,DADMNO
.
          READ      PSTDSHP1,KEY28;ANS
          GOTO      NEXTD IF NOT OVER
.
          MOVE      SP7 TO DHOSP
          MOVE      SP30 TO DCLMTAB
.
          WRITE     PSTDSHP1,KEY28;KEY28,DURNO,D5TIME,PSNAME,PGNAME:
                                   DHOSP,DDSTAT,DCLMTAB,DNAME,TWARD,TBED:
                                   RDOCNAME
          GOTO      NEXTD
.
NORMPD    CALL      DISPDA2
          GOTO      NEXTD
.
NODSCH    DISPLAY   *P1:24,*EL,*P22:24,*B,*V2LON:
                    "** No Discharges for date requested **",*W3;
.
          BRANCH    OPTION OF ENDREP,ENDW
          GOTO      START
.
.         End-of-File or data is beyond date requested - Admissions
.
ENDP1     BRANCH    OPTION OF CNTEP1,DSCHNO1
.
CNTEP1    COMPARE   ZERO,CPNO
          GOTO      NOADMN IF EQUAL
.
          MOVE      ZERO,CPNO
          CALL      PAGEND
.
          PRINT     *N,"  Total patients = ",TOTPAT,*N:
                    *N,"  *** End of Admissions ***"
.
          MOVE      ZERO,CTRM
          MOVE      ZERO,CTRF
          MOVE      ZERO,CTRI
          MOVE      ZERO,CTRU
.* ****************************************** Start of Addition        *I-49016
          MOVE      ZERO,CTRR
          MOVE      ZERO,DSEXNO
          WHILE     DSEXNO < 9
            ADD       ONE,DSEXNO
            MOVE      ZERO,CTRX[DSEXNO]
          DO
.* ******************************************   End of Addition        *I-49016
.
          GOTO      DSCHNO1
.
.         End-of-File or data is beyond date requested - Discharges
.
ENDP2     BRANCH    OPTION OF CNTEP2,ENDW
.
CNTEP2    COMPARE   ZERO,CPNO
          GOTO      NODSCH IF EQUAL
.
          CALL      PAGEND
.
          PRINT     *N,"  Total patients = ",TOTPAT,*N:
                    *N,"  *** End of Discharges ***"
          MOVE      ZERO,CPNO
.
ENDREP    DISPLAY   *P1:24,*EL,*P24:24,*V2LON:
                    "** Report Generation Completed **",*W2;
          GOTO      START
.
.         OVER CONDITION FOUND..
.
ENDW      DISPLAY   *P18:24,*EL,"Generating Admission :";
          MOVE      ZERO,CPNO
          MOVE      ZERO,PATADM         * inpatient 
          MOVE      "60",CLNO
          MOVE      SP30,KEY28
          READ      PSTADMP1,KEY28;;
.
NEXTR     READKS    PSTADMP1;DIM20,XDAADMNO,AURNO,D5TIME,PSNAME,PGNAME:
                             DHOSP,DDSTAT,DCLMTAB,DNAME,TWARD,TBED:
                             RDOCNAME
          GOTO      CNTNR IF OVER
.
          MOVE      XDAADMNO,AADMNO
.
          DISPLAY   *P41:24,*V2LON,PSNAME;
          CALL      DISPDA
          GOTO      NEXTR
.
CNTNR     COMPARE   ZERO,CPNO
          GOTO      CNTNR2 IF EQUAL
.
          MOVE      ZERO,CPNO
          MOVE      ONE,PATADM
          CALL      PAGEND
.
          PRINT     *N,"  Total patients = ",TOTPAT,*N:
                    *N,"  *** End of Admissions ***"
.
CNTNR2    COMPARE   ONE,CMATRN
          GOTO      CNTNXR IF NOT EQUAL
.
          DISPLAY   *P14:24,*EL,"Generating Newborn Admission :";
          MOVE      "60",CLNO
          MOVE      ZERO,TOTPAT
          MOVE      SP30,KEY29
          READ      MATATMP1,KEY29;;
.
NEXTNA    READKS    MATATMP1;NBNAME,NBMATNO,NBPLUR,NBURNO:
                             NBSEX,NBDOB,NBSTAT,NBGNAME,PGNAME
.
          GOTO      CNTNS IF OVER
.
          DISPLAY   *P45:24,*V2LON,NBNAME;
          CALL      DISPDAN
          GOTO      NEXTNA
.
CNTNS     COMPARE   ZERO,CPNO
          GOTO      NONXR IF EQUAL
.
.* ****************************************** Start of Addition        *I-49016
          MOVE      CTRX[1],CTRM
          MOVE      CTRX[2],CTRF
          MOVE      CTRX[3],CTRI
          MOVE      CTRX[4],CTRR
          MOVE      CTRX[9],CTRU
.* ******************************************   End of Addition        *I-49016
.
          CALL      PAGEND
          PRINT     *12,"Number of Newborn Males          : ",CTRM,*N:
                    *12,"                  Females        : ",CTRF,*N:
                    *12,"                  Indeterminates : ",CTRI,*N:
                    *12,"                  Females        : ",CTRU,*N:
                    *N,"  ***  End of Admissions  ***"
.
          MOVE      ZERO,CPNO
          MOVE      ZERO,CTRM
          MOVE      ZERO,CTRF
          MOVE      ZERO,CTRI
          MOVE      ZERO,CTRU
.* ****************************************** Start of Addition        *I-49016
          MOVE      ZERO,CTRR
          MOVE      ZERO,DSEXNO
          WHILE     DSEXNO < 9
            ADD       ONE,DSEXNO
            MOVE      ZERO,CTRX[DSEXNO]
          DO
.* ******************************************   End of Addition        *I-49016
          GOTO      CNTNXR
.
NONXR     BRANCH    PATADM OF CNTNXR
          DISPLAY   *P1:24,*EL,*P22:24,*B,*V2LON:
                    "** No Admissions for date requested **",*W3;
.
CNTNXR    DISPLAY   *P18:24,*EL,"Generating Discharge :";
          MOVE      ZERO,CPNO
          MOVE      "60",CLNO
          MOVE      ZERO,TOTPAT
          MOVE      SP30,KEY28
          READ      PSTDSHP1,KEY28;;
.
NEXTRD    READKS    PSTDSHP1;DIM20,XDDADMNO,DURNO,D5TIME,PSNAME,PGNAME:
                             DHOSP,DDSTAT,DCLMTAB,DNAME,TWARD,TBED:
                             RDOCNAME
          GOTO      ENDP IF OVER
.
          MOVE      XDDADMNO,DADMNO
.
          DISPLAY   *P41:24,*V2LON,PSNAME;
          CALL      DISPDA2
          GOTO      NEXTRD
.
ENDP      COMPARE   ONE,CMATRN
          GOTO      ENDX IF NOT EQUAL
.
          DISPLAY   *P14:24,*EL,"Generating Newborn Discharge :";
          MOVE      "60",CLNO
          MOVE      SP30,KEY29
          READ      MATDTMP1,KEY29;;
.
NEXTDD    READKS    MATDTMP1;NBNAME,NBMATNO,NBPLUR,NBURNO:
                             NBSEX,NBDOB,NBSTAT,NBGNAME,PGNAME
          GOTO      ENDX IF OVER
.
          DISPLAY   *P45:24,*V2LON,NBNAME;
          CALL      DISPDAD
          GOTO      NEXTDD
.
ENDX      COMPARE   ZERO,CPNO
          GOTO      NONXRD IF EQUAL
.
          CALL      PAGEND
.
          PRINT     *N,"  Total patients = ",TOTPAT,*N:
                    *N,"  *** End of Discharges ***"
          MOVE      ZERO,CPNO
          GOTO      CNTEDP
.
NONXRD    DISPLAY   *P1:24,*EL,*P22:24,*B,*V2LON:
                    "** No Discharges for date requested **",*W3;
.
CNTEDP    DISPLAY   *P1:24,*EL,*P24:24,*V2LON:
                   "** Report Generation Completed **",*W2;
          CALL      KILLIDZ
          GOTO      START
.
.         Create an indexed file based on port
.         for accumulating Admission Type totals
.
INDZD     MOVE      ZERO,STATUS
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMED
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMEA
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMER
.
          COMPARE   CMATRN,ONE
          GOTO      ITRAP IF NOT EQUAL
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMEDIS
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMENEW
.
.         Check if previously exists..if so delete it 
.
ITRAP     CALL      KILLIDZ
          PREP      PSTROL,FNAMER
.
          COMPARE   CMATRN,ONE      
          GOTO      NOMATRID IF NOT EQUAL
.
          WRITE     PSTROL,SEQ;"isbuild ",FNAMEA," 176 U1-28"
          WRITE     PSTROL,SEQ;"isbuild ",FNAMED," 176 U1-28"
          WRITE     PSTROL,SEQ;"isbuild ",FNAMEDIS," 99 U1-29"
          WRITE     PSTROL,SEQ;"isbuild ",FNAMENEW," 99 U1-29"
.
          GOTO      WREOFB
.
NOMATRID  WRITE     PSTROL,SEQ;"isbuild ",FNAMEA," 176 U1-28"
          WRITE     PSTROL,SEQ;"isbuild ",FNAMED," 176 U1-28"
WREOFB    WEOF      PSTROL,SEQ
.
          MOVE      ONE,STATUS
.
          CLEAR     CMDLINE
          APPEND    "sh ",CMDLINE
          APPEND    FNAMER,CMDLINE
          APPEND    ".txt",CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
          CLOSE     PSTROL,DELETE
.
          MOVE      ZERO,OPCND
          TRAP      NOINDZ IF IO
          OPEN	    PSTADMP1,FNAMEA
          OPEN	    PSTDSHP1,FNAMED
.
          COMPARE   CMATRN,ONE
          GOTO      TRPIOC IF NOT EQUAL
.
          OPEN	    MATATMP1,FNAMEDIS
          OPEN	    MATDTMP1,FNAMENEW
.
TRPIOC    TRAPCLR   IO
          RETURN
+
.
NOINDZ    MOVE      ONE,OPCND
          DISPLAY   *P1:24,*EL,*B,"** Temporary Index File ",*V2LON:
                            FNAMEA,*HOFF," not found **",SP4;
          CALL      HITENTER
          TRAPCLR   IO
          RETURN
+
.
.         Deleting an indexed file based on port
.
KILLIDZ   DISPLAY   *P1:24,*EL,*P24:24,*V2LON,"** ",*BLINKON,"Deleting Temp ":
                           "Indexed Files",*HOFF,*V2LON," **";
.
          CLOSE     PSTADMP1
          CLOSE     PSTDSHP1
.
          MOVE      ZERO,STATUS
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMEA,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMED,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          COMPARE   ONE,CMATRN
          GOTO      DELEXIT IF NOT EQUAL
          CLOSE     MATATMP1
          CLOSE     MATDTMP1
.
          MOVE      ZERO,STATUS
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMENEW,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMEDIS,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
DELEXIT   RETURN
+
.
.         Print actual Admission data
.
DISPDA    MOVE      ZERO,MABFLAG
          MOVE      ZERO,MODEH
.
          COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
.
          MOVE      PSNAME,PACSNAME          * format patient name
          MOVE      PGNAME,PACGNAME
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          PACK      PATNAME,PACFNAME
.
          MOVE      DNAME TO DOCNAME
          PRINT     *1,"| ",AURNO,*11,"| ",AADMNO,*21,"| ",D5TIME:
                    *29,"| ",PATNAME,*55,"| ",TWARD,"/",TBED:
                    *65,"| ",DCLMTAB,*91,"| ",DOCNAME:
                    *111,"| ",RDOCNAME,*132,"|":
                    *N,"|",*11,"|",*21,"|",*29,"|":
                    *55,"|",*65,"|",*91,"|",*111,"|",*132,"|"
.
          ADD       TWO,CLNO
          ADD       ONE,TOTPAT
          RETURN
+
.
.         Print Newborn Admission Data
.
DISPDAN   MOVE      ONE,MABFLAG
          MOVE      ONE,MODEH
.
          COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
.
          UNPACK    NBDOB INTO XCENT,XYEAR,XMON,XDAY
.
.* ****************************************** Start of Changes         *I-49016
.                                           * original code for sex desc. has
.                                           * been deleted & replaced by :-
          MOVE      NBSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          MOVE      DSEXDESC,DIM7
          ADD       ONE,CTRX[DSEXNO]        * add admission to counters
.* ******************************************   End of Changes         *I-49016
.
DISPP     MOVE      PGNAME,DNAME
          MOVE      DNAME,DOCNAME
          PRINT     *1,"| ",NBMATNO,SLASH,NBPLUR,*12,"| ",NBNAME:
                    *35,"| ",NBURNO,*46,"| ",DIM7:
                    *55,"| ",XDAY,SLASH,XMON,SLASH,XCENT,XYEAR," |":
                    *68,NBGNAME,*93,"|":
                    *N,"|",*12,"|(",DOCNAME,")",*35,"|",*46,"|":
                    *55,"|",*66,"|",*93,"|"
.
          ADD       TWO,CLNO
          ADD       ONE,TOTPAT
          RETURN
+
.
.         Print actual Discharge Data
.
DISPDA2   MOVE      ZERO,MABFLAG
          MOVE      ZERO,MODEH
.
          COMPARE   "55",CLNO
          CALL      HEAD2 IF NOT LESS
.
          MOVE      PSNAME,PACSNAME          * format patient name
          MOVE      PGNAME,PACGNAME
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          PACK      PATNAME,PACFNAME
.
          MOVE      DNAME TO DOCNAME
          PRINT     *1,"| ",DURNO,*11,"| ",DADMNO,*21,"| ",D5TIME:
                    *29,"| ",PATNAME,*55,"| ",TWARD,"/",TBED:
                    *65,"| ",DDSTAT,*91,"| ",DOCNAME:
                    *111,"| ",RDOCNAME,*132,"|":
                    *N,"|",*11,"|",*21,"|",*29,"|",*55,"|":
                    *65,"|",*91,"|",*111,"|",*132,"|"
.
          ADD       TWO,CLNO
          ADD       ONE,TOTPAT
          RETURN
+
.
.         Print Newborn Discharge Details
.
DISPDAD   MOVE      ONE,MABFLAG
          MOVE      TWO,MODEH
.
          COMPARE   "55",CLNO
          CALL      HEAD2 IF NOT LESS
.
          MOVE      MABSTAT1,DIM11
          COMPARE   TWO,NBSTAT
          GOTO      DISPU IF EQUAL
.
          MOVE      MABSTAT2,DIM11
.
DISPU     UNPACK    NBDOB,XCENT,XYEAR,XMON,XDAY
.
.* ****************************************** Start of Changes         *I-49016
.                                           * original code for sex desc. has
.                                           * been deleted & replaced by :-
          MOVE      NBSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          MOVE      DSEXDESC,DIM7
.* ******************************************   End of Changes         *I-49016
.
DISPZP    MOVE      PGNAME,DNAME
          MOVE      DNAME,DOCNAME
          PRINT     *1,"| ",NBMATNO,SLASH,NBPLUR,*12,"| ",NBNAME:
                    *35,"| ",NBURNO,*46,"| ",DIM7:
                    *55,"| ",XDAY,SLASH,XMON,SLASH,XCENT,XYEAR," |":
                    *66,"| ",NBGNAME,*93,"| ",DIM11," |":
                    *N,"|",*12,"|(",DOCNAME,")",*35,"|",*46,"|":
                    *55,"|",*66,"|",*93,"|",*107,"|"
.
          ADD       TWO,CLNO
          ADD       ONE,TOTPAT
          RETURN
+
.
.         Last Line on the Page
.
PAGEND    BRANCH    MODEH OF PAGEND1,PAGEND2
.
          PRINT     "*---------------------------------------------------":
                    "--------------------------------------------------------":
                    "-----------------------*"
          ADD       ONE,CLNO
          RETURN
.
PAGEND1   PRINT     "*-------------------------------------------":
                    "------------------------------------------------*"
          ADD       ONE,CLNO
          RETURN
.
PAGEND2   PRINT     "*---------------------------------------------------":
                    "------------------------------------------------------*"
          ADD       ONE,CLNO
          RETURN
+
.         Headings for Admissions output
.
HEAD      CLOCK     TIME,CTIMEIS
.
          COMPARE   ZERO,CPNO
          CALL      PAGEND IF NOT EQUAL
.
          CALL      HEAD132
          MOVE      CPAGENO,CPNO
.
          BRANCH    MABFLAG OF HED1
.
.
          ADD       ONE,CLNO
.
          IF        IBCNMHOS=1
            PRINT     *40,"Hospital Id :",HOSNAME
            ADD       ONE,CLNO
          ENDIF
.
          PRINT     *40,"Admissions for : ",XDAY1,"/",XMON1,"/",XCENT1,XYEAR1:
                    "     ",DHEAD,*N
          GOTO      HED2
.
HED1      PRINT     *40,"Newborn Admissions for : ",XDAY1,"/",XMON1,"/":
                    XCENT1,XYEAR1,"     ",DHEAD,*N
.
HED2      CALL      PAGEND
.
          BRANCH    MABFLAG OF HEADA
.
          PRINT     *1,"| U/R No.",*11,"| Adm No.":
                    *21,"| Time",*29,"| Patient's Name",*55,"|Ward/Bed":
                    *65,"| Health Fund / Table",*91,"| Attending Doctor":
                    *111,"| Referring Doctor",*132,"|":
                    *N,*1,"|",*11,"|",*21,"|",*29,"|":
                    *55,"|",*65,"| or Claim Type",*91,"|",*111,"|",*132,"|"
          GOTO      HEADP
.
HEADA     PRINT     *1,"| Reg. ",*12,"| Mother's Surname ":
                    *35,"| Mother's",*46,"| Baby's ":
                    *55,"| Baby's ",*66,"| Baby's Given Name ",*93,"|":
                    *N,*1,"| Number ",*12,"|",*35,"| U/R # ":
                    *46,"| Sex ",*55,"|Birth Date|",*93,"|"
.
HEADP     MOVE      "8",CLNO
          CALL      PAGEND
          MOVE      ONE,MABFLAG
          RETURN
+
.         Headings for Discharges output
.
HEAD2     CLOCK     TIME,CTIMEIS
.
          COMPARE   ZERO,CPNO
          CALL      PAGEND IF NOT EQUAL
.
          CALL      HEAD132
          MOVE      CPAGENO,CPNO
.
          BRANCH    MABFLAG OF HDD1
.
.
          ADD       ONE,CLNO
.
          PRINT     *40,"Hospital Id :",HOSNAME
.
          ADD       ONE,CLNO

          PRINT     *40,"Discharges for : ",XDAY1,"/",XMON1,"/",XCENT1,XYEAR1:
                    "     ",DHEAD,*N
          GOTO      HDD2
.
HDD1      PRINT     *40,"Newborn Discharges for : ",XDAY1,"/",XMON1,"/":
                    XCENT1,XYEAR1,"     ",DHEAD,*N
.
HDD2      CALL      PAGEND
.
          BRANCH    MABFLAG OF HEADDX
.
          PRINT     *1,"| U/R No.",*11,"| Adm No.":
                    *21,"| Time",*29,"| Patient's Name":
                    *55,"|Ward/Bed",*65,"| Status":
                    *91,"| Attending Doctor",*111,"| Referring Doctor",*132,"|"
          MOVE      "7",CLNO
          GOTO      HEADPX
.
HEADDX    PRINT     *1,"| Reg. ",*12,"| Mother's Surname ":
                    *35,"| Mother's",*46,"| Baby's ":
                    *55,"| Baby's ",*66,"| Baby's Given Name ",*93,"|":
                    " Status ",*107,"|":
                    *N,*1,"| Number ",*12,"|",*35,"| U/R # ":
                    *46,"| Sex ",*55,"|Birth Date|",*93,"|",*107,"|"
          MOVE      "8",CLNO
.
HEADPX    CALL      PAGEND
          RETURN
+
.
ENDIT     CHAIN     PGM
          STOP
.------------------------------------------------------------------------------
.         KHOS0000
.         Enter The Hospital
.------------------------------------------------------------------------------
KHOS0000  COMPARE   ONE,IBCNMHOS
          GOTO      KHOS9999 IF NOT EQUAL
.
          DISPLAY   *P1:21,*EL,"Hospital Id : ";
          MOVE      TEN5,ECOL
          MOVE      TWENTY1,EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS2000,KHOS9500
          GOTO      KHOS3000
.
KHOS2000  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          MOVE      ZERO,EXIT
.
KHOS3000  CLOSE     PATHSPA1
.
          DISPLAY   *P20:21,*V2LON,PTHSNAME;
          PACK      HOSCODE,PTHSHOSP,SP70
          PACK      HOSNAME,PTHSNAME,SP70
          GOTO      KHOS9999
.
KHOS9500  MOVE      ONE,EXIT
KHOS9999  RETURN
+
.==============================================================================
.
          INC       STD001IO
          INC       SEXDSCIO
.
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATDSCIO/INC
          INC       PATFN1IO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATTRNIO/INC
          INC       PATHSPKY
          INC       PATHSPIO/INC
          INC       PMSVX1IO/INC
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC

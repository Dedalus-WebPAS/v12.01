.----------------------------------------------------------------------
. Program       : PATWEB64
.
. Function      : SMS Notification Web Server
.                 
. Modifications : 
.----------------------------------------------------------------------
. V11.05.02 10/04/2025  Don Nguyen     TSK 0938899
.           Init now opens index for 'mltcodaf' (Multi Hospital Category Codes)
. V11.05.01 31/01/2025  Don Nguyen     TSK 0896463
.           Modified Hospital/Patient list output ('SMS Messages Sent' &
.           'SMS History') to show the 'Resend' button for error status code
.           values: '2' (Error) & '6' (No Mobile Number).
.           Added new processing code for re-sending to the patient/contact
.           mobile number and added validation for a blank number; RESEND20
.           Recompiled for changes in PMSSMHTM - added strings for status '6'
. V11.04.03 19/08/2024  Jacob Jackson  TSK 0911895
.           Ensure that when selecting "THE" code for booking type filter, it 
.           is checked against the actual booking type
. V11.04.02 16/08/2024  Jacob Jackson  TSK 0911895
.           Add filtering logic for SMS Bookings
. V11.04.01 21/12/2023  Don Nguyen     TSK 0893290
.           Removed the need to read the Responses Received table (pmssrraf)
.           for "Error" messages in the SMS List output (SMST0000 & HOSE0000)
.           and only pass the Message Id as reference for the 'Resend' button.
.           Updated the waiting status description to be "Waiting to be sent".
.           Recompiled for PMSSMHTM.
. V11.03.01 22/11/2022  Bella Turco   TSK 0921957
.           Added NOMORE00 - Display "No more records" at end of list
.           Added PTCNNRTD - Displays number of rows set by parameter value
. V11.02.02 27/10/2022  Bella Turco      TSK 0921646
.           Added WBSENAM to Table Sort *21 in HOST0000 and *20 in SMST0000
. V11.02.01 10/02/2022  Thanh T          TSK 0889899
.           Recompiled as WATTR1FD changes
. V11.01.02 26/11/2021  Don Nguyen     TSK 0913536 & 0913053
.           Fixed Response Received date/time value for Sent/History List and
.           removed trailing spaces from response message parts in list outputs.
.           Added 'Ward', 'Health Speciality', OP 'Clinic Type' and
.           'Waiting List Unit' filter values to prev/next button links.
. V11.01.01 16/07/2021  Ebon Clements  TSK 0906892
.           Move PMI detail read to after the record is selected - HOST0000
.           Fixed GMAP0000 loop exit
. V11.00.07 09/12/2020  Thanh T        TSK 0872840
.           Added PMSCCDFD/PMSCCDIO as PMSPX2TM changes
. V11.00.06 07/12/2020  Sunny         TSK 0896467
.           Changed ASEL0000, MISC0270 to sort list through description
. V11.00.05 25/11/2020  Sunny         TSK 0896467
.           Changed HOST1000, HOSE1000, HSMT1030 to append speciality to 
.           message type column
. V11.00.04 13/10/2020  Thanh T       TSK 0893622
.           Changed HOST3000, SMST0000 to append response date/time
. V11.00.03 28/09/2020  Thanh T       TSK 0893622
.           Changed HOST3000 and SMST3000 to append response date/time
.           to response status field when message status is pmsmstat = 1
.           (Response Received) 
. V11.00.02 20/03/2020  Peter Vela     TSK 0889143
.           Fixed OUTLET IO calls
.           Fixed WATLET IO calls
. V11.00.01 18/03/2020  Peter Vela     TSK 0889143
.           Recompiled for OUTLETFD
.           Recompiled for WATLETFD
. V10.14.03 22/08/2019  Don Nguyen     TSK 0875542
.           Modified 'Response Status' (PMRRSTAT) descriptions for 'Status'
.           column output.
. V10.14.02 04/08/2019  Thanh T.       TSK 0867186
.           Changed HOST0000, HOSE0000 and HSMT0000 to process only rows
.           which the user has hospital access in multi hospital mode  
. V10.14.01 04/07/2019  Thanh T.       TSK 0867186
.           Added HSEL0000/WSEL0000 for Hospital and ward selection list 
. V10.11.02 27/11/2017  Thanh T.       TSK 0821710
.           Recompiled as PATMASTM changed
. V10.11.01 30/07/2017  Thanh T.       TSK 0821710
.           Audit admission/outpatient/UR comments. changed PATMASTM/PMSPX2TM.
.----------------------------------------------------------------------
.     V10.08.09  17/11/2016  Ebon Clements  TSK 0294154
.                Changed message text to be on one line - RESEND20
.     V10.08.08  15/11/2016  Ebon Clements  TSK 0294154
.                Removed SP70 test from message text - RESEND20
.     V10.08.07  26/10/2016  Ebon Clements  TSK 0294154
.                Fixed resend of entire message contents - RESEND20
.     V10.08.06  22/09/2016  Ebon Clements  TSK 0294154
.                Removed SP70 test from message text 
.     V10.08.05  22/09/2016  Ebon Clements  TSK 0294154
.                Fixed output of sent message text
.     V10.08.04  20/09/2016  Ebon Clements  TSK 0294154
.                Removed <br> from message display to preserve format
.     V10.08.03  14/09/2016  Ebon Clements  TSK 0294154
.                Added contact type to SMS lists -  DESCCTYP
.     V10.08.02  31/08/2016  Ebon Clements  TSK 0294154
.                Added SMS response already on file edit - RESMAP10
.     V10.08.01  29/08/2016  Ebon Clements  TSK 0294154
.                Increased size of MAPDRESP
.     V10.08.00  23/05/2016  Don Nguyen     TSK 0294154
.                Modified BLKSRL00 & UPBLKR00 to process PMRRMAPD accordingly
.                05/01/2016  Don Nguyen     CAR 294154
.                Created Web Server
.----------------------------------------------------------------------
          INC       IBAWEBDF    
          INC       WEBDATDF
.
          INC       APSMASFD/INC       * Creditor/Supplier Master File
          INC       MRTMASFD/INC       * MRT Master Details
          INC       NHIETHFD/INC       * NHI Ethnicity Codes
          INC       NHIMASFD/INC       * NHI/MWS HCU Details
          INC       OUTCTYFD/INC       * Outpatients Clinic Type
          INC       OUTBOAFD/INC       * Outpatients Bookings  
          INC       OUTSITFD/INC       * Outpatients Site Code
          INC       OUTLETFD/INC       * Outpatient Letters
          INC       PATALRFD/INC       * Patient Alerts
          INC       PATCALAG/INC       * Data variable definitions for CALCAGE
          INC       PMSUCHFD/INC
          INC       PMSUCDFD/INC
          INC       PATMASTD/INC
          INC       PATCONFD/INC       * Control File
          INC       PATDO1FD/INC       * Doctor File
          INC       PATFN1FD/INC       * Patient Health Fund
          INC       PATHSPFD/INC       * Hospital Details
          INC       PATILTFD/INC       * IP Letters
          INC       PATIN1FD/INC       * Insurance Details
          INC       PATMA1FD/INC       * Patient Master File
          INC       PATMI1FD/INC       * Patient Admissions File
          INC       PATVISFD/INC       * Patient Visit File
          INC       PMSVX1FD/INC       * Patient Visit Extension File
          INC       PATPR1FD/INC       * Pre-Admission Details
          INC       PATWR1FD/INC       * Patient Ward/Bed Details
          INC       PATTRNFD/INC       * Patient Ward/Bed Details
          INC       BOKRC1FD/INC
          INC       BOKLETFD/INC
          INC       OPRDEAFD/INC
.
          INC       PMSCCDFD/INC       * Concession Card Details
          INC       PMSHCGFD/INC       * HCP Practice
          INC       PMSHCGTD/INC       * CGI vars for PMSHCGFD
          INC       PMSHCPFD/INC       * Healthcare Profession
          INC       PMSHCPTD/INC       * CGI variables for PMSHCPFD
          INC       PMSPX2FD/INC       * PMI extension table
          INC       PMSPX2TD/INC       * CGI variables for PMSPX2FD
          INC       PMSSMHTD/INC       * SMS history
          INC       PMSSRRTD/INC       * SMS responses
.
          INC       PMSCEXFD/INC       * Extra Contact Details
          INC       PMSCEXTD/INC       * CGI variables for PMSCEXFD
          INC       PMSSMHFD/INC       * SMS History
          INC       PMSSRMFD/INC       * SMS Response Mapping
          INC       PMSSRRFD/INC       * SMS Responses Received
          INC       WATLETFD/INC       * W/L Letters
          INC       WEBDINVS/INC
          INC       MLTSECFD/INC
          INC       WATTR1FD/INC
.
.----------------------------------------------------------------------
ALLVISIT  FORM      1        * All visits filter
RESPONSE  DIM       160      * All reponses
DESCCTYP  DIM       20       * Contact Type Description
DESCMTYP  DIM       20       * Message type description
MTYPSRVC  DIM       50       * Specialty or Clinic Type or WL Unit
DESCSCOD  DIM       70       * SMS (Letter) description
DESCSTAT  DIM       60       * Message status description
NEXTPAGE  DIM       1        * Nextpage parameter CGI parameter
UPDTTYPE  DIM       1        * Update Type
STARTKEY  DIM       50       * Starting Key
CURRDATE  DIM       8        * Current Date
RSDATTIM  DIM       25
FORM8     FORM      8
FILTTYPE  DIM       2    
FILTSTAT  DIM       2
FILTURNO  DIM       8
FILTHOSP  DIM       3
FILTWARD  DIM       3
FILTADTP  DIM       3
FILTCLTP  DIM       6
FILTWLUT  DIM       3
FILTBOKI  DIM       3
FILTSTOU  DIM       3
TEMPFILT  DIM       3
FOLDERSF  DIM       3        * Patient Folder suffix
MAPDRESP  DIM       20
MESSAGID  DIM       16
NEWMESID  FORM      16
ERRORKEY  DIM       18
DOCTCODE  DIM       8
DISPDATE  DIM       12       * Display date
HTMLLNK2  DIM       127
PSAGETYP  DIM       3
PSAGECON  DIM       3
PTCOD001  DIM       10       * Mapped webPAS variable (Yes / No)
PTCOD002  DIM       50       * Inbound SMS value
PTCOD003  DIM       1        * Active status (0=No 1=Yes)
PTCOD004  DIM       1        * Mapped value (0=No 1=Yes)
.
RECRDKEY  DIM       60       * pmssrmaf record key (Index 1)
RECVMESG  DIM       50
.
BSRECKEY  DIM       18[99]   * Array of Bulk SMS Response record keys
BSKEYCNT  FORM      3        * Count of Bulk SMS Response record keys (read)
BSKEYEND  FORM      3        * Count of Bulk SMS Response record keys (submitted)
ERRORCNT  FORM      3        * Error count
ERRORLST  DIM       60[99]   * Array of error record keys
.
RECCNT    FORM      3        * Record count
DRECCNT   DIM       3        * Record count
.
STRNGINP  DIM       200      * Input string (with occurrences of "'")
STRNGOUT  DIM       600      * Output string (where "'" is replaced with "%60")
STRNGLEN  FORM      3        * String length
CURRCHAR  DIM       1
POSITION  FORM      3        * Current pos of character pointer
VALSTRNG  DIM       20
.
CONFMVAL  DIM       1        * Appointment Confirmed 0=No 1=Yes (PMVXCONF)
VISITNUM  DIM       8        * Visit number
HOSPWARD  DIM       16 
SMSHOSPN  DIM       10 
SMSHOSPC  DIM       3 
SMSWARDC  DIM       3 
SHOWFLAG  FORM      1         
ALLHSPAC  FORM      1        * User has all hospital access flag 
DFALLHSP  FORM      1        * Default all hospital flag
.
EXTCCTYP  DIM       3
EXTCMOBN  DIM       20
EXTCURNO  DIM       8
.
CATTC     INIT      "tc"
SP100     INIT      "                                                  ":
                    "                                                  "
ATTEXT    INIT      "at"
NLTEXT    INIT      "<br>"
.
.----------------------------------------------------------------------
PRGID     INIT      "PATWEB64"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "SMS Notification Web Server"
.----------------------------------------------------------------------
.
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200,MAIN1300,MAIN1400,MAIN1500
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "Invalid Option",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  CALL      RESMAP00        * SMS Response Mapping Maintenance
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1200  CALL      BULKRM00        * Bulk SMS Response Mapping
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1300  CALL      PATSMS00        * Patient Level SMS Functionality
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1400  CALL      HOSSMS00        * Hospital Level SMS Functionality
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1500  CALL      RESEND00        * Resend SMS 
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
+
.------------------------------------------------------------
.         Output Next Page Based on CGI Parameter nextpage
.------------------------------------------------------------
NXTPAG00  MOVE      ZERO,F1
          MOVE      NEXTPAGE,F1
          BRANCH    F1,NXTPAG10,NXTPAG20,NXTPAG30,NXTPAG40
.
          CALL      WINCLS00
          GOTO      NXTPAG99
.
NXTPAG10  CALL      RDIREC00
          GOTO      NXTPAG99
.
NXTPAG20  CALL      GOBACK00      * Go Back 1 Html page
          GOTO      NXTPAG99
.
NXTPAG30  CALL      DAH20000      * Go Back 2 html pages
          GOTO      NXTPAG99
.
NXTPAG40  CALL      PREDIR00      * Redirect Parent
          GOTO      NXTPAG99
.
NXTPAG99  RETURN
+
.------------------------------------------------------------------------------
.         Redirect Parent URL
.------------------------------------------------------------------------------
PREDIR00  CALL      OPENHTML
          BRANCH    EXIT,PREDIR90
.
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             " parent.location.href=#"",REDIRECT,"#";":
                             "}":
                             "</script>"
          CALL      CLOSHTML
          GOTO      PREDIR99
.
PREDIR90  DISPLAY   "*** Fifo Not Found ***"
.
PREDIR99  RETURN
+
.-------------------------------------------------------------------------------
.         Goes back 1 page
.-------------------------------------------------------------------------------
GOBACK00  CALL      OPENHTML
          BRANCH    EXIT,GOBACK99
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "    alert(#"",WEBTITLE,"#");":
                             "    self.close();":
                             "    opener.history.go(-1);":
                             "}":
                             "</script>"
          CALL      CLOSHTML
GOBACK99  RETURN
+
.------------------------------------------------------------
.         Open Files and Initialize Variables
.------------------------------------------------------------
INIT0000  OPEN      CONTROLF,"controlf"     * Inpatient CONTROL File
          READ      CONTROLF,HUND24;*105,PTCNSMSP
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,HUND25;*238,PTCNNRTD
.
          IF        IBCNMHOS=1
            OPEN      MLTCODA1,"mltcodaf"
            OPEN      MLTCODA2,"mltcodaf"
          ENDIF

          MOVE      ZERO,F2
          MOVE      PTCNNRTD,F2
          IF        F2 = 0
            MOVE      "10",PTCNNRTD
          ENDIF
.
          CALL      INTMAS00 
.
          OPEN      IBASEQA1,"ibaseqaf"
          OPEN      OUTLETR1,"outletrf"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"     
          OPEN      PATHSPA1,"pathspaf"
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATWR1A7,"patwr1af"
          OPEN      PATTRAN2,"pattranf"
          OPEN      PATLETR1,"patletrf"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      OUTCTYA1,"outctyaf"  
          OPEN      OUTCTYA2,"outctyaf"  
          OPEN      OUTBOKA6,"outbokaf"  
.
          OPEN      PMSCEXA1,"pmscexaf"
          OPEN      PMSCCDA1,"pmsccdaf"
          OPEN      PMSSRMA1,"pmssrmaf"
          OPEN      PMSSRMA2,"pmssrmaf"
          OPEN      PMSSRRA1,"pmssrraf"
          OPEN      PMSSRRA2,"pmssrraf"
          OPEN      PMSSRRA3,"pmssrraf"
          OPEN      PMSSMHA1,"pmssmhaf"
          OPEN      PMSSMHA2,"pmssmhaf"
          OPEN      PMSSMHA3,"pmssmhaf"
          OPEN      WATLETR1,"watletrf"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      IBAPDFA1,"ibapdfaf"
          OPEN      MLTSECA1,"mltsecaf"
          OPEN      WATTR1A1,"wattr1af"
          OPEN      WEBSECA1,"websecaf"
          OPEN      BOKRC1A1,"bokrc1af"
          OPEN      BOKLETR1,"bokletrf"
          OPEN      OPRDETA2,"oprdetaf"
.
INIT9999  RETURN
+
.------------------------------------------------------------
.         Clear Parameters
.------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          MOVE      SP70,TEMPLATE
          MOVE      ZERO,STARTNUM
          MOVE      SP70,STARTKEY
          MOVE      ZERO,NORECORD
          MOVE      SP70,NEXTPAGE
          MOVE      SP70,REDIRECT
          MOVE      SP70,UPDTTYPE
          MOVE      SP70,URNUMBER
          MOVE      SP70,ADMISSNO
          MOVE      SP70,MESSAGID
          MOVE      SP70,ERRORKEY
          MOVE      SP70,SELPRINT
          MOVE      SP70,FILTURNO
.
          MOVE      SP70,VIEWTYPE
          MOVE      SP70,FRSTDATE
          MOVE      SP70,LASTDATE
          MOVE      SP70,FILTSTAT
          MOVE      SP70,FILTTYPE
          MOVE      SP70,FILTHOSP
          MOVE      SP70,FILTWARD
          MOVE      SP70,FILTADTP
          MOVE      SP70,FILTCLTP
          MOVE      SP70,FILTWLUT
          MOVE      SP70,FILTBOKI
          MOVE      SP70,FILTSTOU
          MOVE      ZERO,SHOWFLAG
          MOVE      ZERO,DFALLHSP
.
          MOVE      SP70,VYEARMTH
.
          MOVE      SP70,PTCOD001
          MOVE      SP70,PTCOD002
          MOVE      ZERO,PTCOD003
          MOVE      ZERO,PTCOD004
          MOVE      SP70,RECRDKEY
.
          MOVE      ZERO,BSKEYEND
          MOVE      ZERO,ALLVISIT
          MOVE      SP70,VALSTRNG
          MOVE      SP70,VISITNUM
          MOVE      SP70,CONFMVAL
.
          CALL      CLPMSM00
          CALL      CLPMRR00
.
CLRPAR99  RETURN
+
.------------------------------------------------------------
.         Set Parameters
.------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updttype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDTTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "urnumber=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,URNUMBER
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "admissno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ADMISSNO
            GOTO      SETPAR99
          ENDIF
.  
          MATCH     "nextpage=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NEXTPAGE
            GOTO      SETPAR99
          ENDIF 
.
          MATCH     "redirect=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REDIRECT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startnum=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTNUM
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startkey=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTKEY
            PACK      STARTKEY,STARTKEY,SP70
            REP       "~'",STARTKEY
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "norecord=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NORECORD
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "ptcod",QRYNAME
          IF        @EQUAL
            CALL      STCOD000
            COMPARE   ZERO,EXIT
            GOTO      SETPAR99 IF EQUAL
          ENDIF
.
          MATCH     "pmsmh",QRYNAME
          IF        @EQUAL
            CALL      SPPMS000
            COMPARE   ZERO,EXIT
            GOTO      SETPAR99 IF EQUAL
          ENDIF
.
          MATCH     "pmsrr",QRYNAME
          IF        @EQUAL
            CALL      SPMSR000
            COMPARE   ZERO,EXIT
            GOTO      SETPAR99 IF EQUAL
          ENDIF
.
          MATCH     "viewtype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,VIEWTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "frstdate=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FRSTDATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "allvisit=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ALLVISIT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "lastdate=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,LASTDATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "vyearmth=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,VYEARMTH
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "bsreckey",QRYNAME
          IF        @EQUAL
            COMPARE   "99",BSKEYEND
            GOTO      SETPAR99 IF EQUAL
            ADD       ONE,BSKEYEND
            UNPACK    QRYDATA,D3,QRYDATA
            PACK      BSRECKEY[BSKEYEND],QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filttype=",QRYNAME
          IF        @EQUAL
            PACK      FILTTYPE,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtstat=",QRYNAME
          IF        @EQUAL
            PACK      FILTSTAT,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filthosp=",QRYNAME
          IF        @EQUAL
            PACK      FILTHOSP,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtward=",QRYNAME
          IF        @EQUAL
            PACK      FILTWARD,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtadtp=",QRYNAME
          IF        @EQUAL
            PACK      FILTADTP,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtcltp=",QRYNAME
          IF        @EQUAL
            PACK      FILTCLTP,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtwlut=",QRYNAME
          IF        @EQUAL
            PACK      FILTWLUT,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtboki=",QRYNAME
          IF        @EQUAL
            PACK      FILTBOKI,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtstou=",QRYNAME
          IF        @EQUAL
            PACK      FILTSTOU,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "showflag=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SHOWFLAG
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "dfallhsp=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,DFALLHSP
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "messagid=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,MESSAGID
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "errorkey=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ERRORKEY
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "selprint=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SELPRINT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filturno=",QRYNAME
          IF        @EQUAL
            PACK      FILTURNO,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
+
.-------------------------------------------------------------------------------
.   Set Parameters for SMS Response Mapping variables
.-------------------------------------------------------------------------------
STCOD000  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,STCOD001,STCOD002,STCOD003,STCOD004
.
          MOVE      ONE,EXIT
          GOTO      STCOD999
.
STCOD001  SQUEEZE   QRYDATA
          MOVE      QRYDATA,PTCOD001
          PACK      PTCOD001,PTCOD001,SP70
          GOTO      STCOD999
.
STCOD002  PACK      PTCOD002,QRYDATA,SP70
          GOTO      STCOD999
.
STCOD003  PACK      PTCOD003,QRYDATA,SP70
          GOTO      STCOD999
.
STCOD004  PACK      PTCOD004,QRYDATA,SP70
          GOTO      STCOD999
.
STCOD999  RETURN
+
.------------------------------------------------------------
.         SMS Response Mapping Maintenance
.------------------------------------------------------------
RESMAP00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      RESMAP70 IF EQUAL
.
          MATCH     "A",UPDTTYPE            * Add formaction
          GOTO      RESMAP10 IF EQUAL
.
          MATCH     "U",UPDTTYPE            * Update formaction
          GOTO      RESMAP20 IF EQUAL
.
          MATCH     "D",UPDTTYPE            * Delete formaction
          GOTO      RESMAP30 IF EQUAL
.
          GOTO      RESMAP93
.
.
.         ************ ADD FORMACTION **********
.
RESMAP10  PACK      KEY60,PTCOD002,SP70
          CALL      RSPMSRM2
          CALL      RKPMSRM2
          BRANCH    OVRCD,RESMAP15
.
          MATCH     PTCOD002,PMSRINBS
          GOTO      RESMAP94 IF EQUAL
.
RESMAP15  PACK      KEY60,PTCOD001,PTCOD002,SP70
          CALL      RAPMSRM1
          IF        OVRCD=0
            GOTO      RESMAP91
          ELSE
            MOVE      PTCOD001,PMSRMVAR
            MOVE      PTCOD002,PMSRINBS
            MOVE      PTCOD003,PMSRACTV
            MOVE      PTCOD004,PMSRMVAL
.
            CALL      IBACLOCK
            PACK      PMSRCDTE,CCC,CYY,CMM,CDD   * Current Date
            REP       " 0",PMSRCDTE
            MOVE      CTIMEIS,PMSRCTIM           * Current Time
            MOVE      USERID,PMSRWEBC            * Current Web User ID
            CALL      WRPMSRM1
          ENDIF
          GOTO      RESMAP99
.
.
.         ************ UPDATE FORMACTION **********
.
RESMAP20  PACK      KEY60,PTCOD001,PTCOD002,SP70
          CALL      RDPMSRM1
          BRANCH    OVRCD,RESMAP92
.
          MOVE      PTCOD003,PMSRACTV
          MOVE      PTCOD004,PMSRMVAL
          CALL      UPPMSRM1
.
          MOVE      ZERO,EXIT
          GOTO      RESMAP99
.
.
.         ************ DELETE FORMACTION **********
.
RESMAP30  PACK      KEY60,PTCOD001,PTCOD002,SP70
          CALL      RDPMSRM1
          BRANCH    OVRCD,RESMAP92
          CALL      DEPMSRM1
.
          MOVE      ZERO,EXIT
          GOTO      RESMAP99
.
.
.         ************ DISPLAY FORMACTION **********
.
RESMAP70  PACK      KEY3,WBSEHOSP,SP70
          CALL      RDPTHSP1
          IF        OVRCD=1
            CALL      CLPATHSP
          ENDIF
.
          PACK      KEY60,PTCOD001,PTCOD002,SP70
          CALL      RDPMSRM1
          IF        OVRCD=1
            CALL      CLPMSSRM
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "SMS Response Mapping",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,RESMAP99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      RESMAP99
.
RESMAP91  MOVE      "Mapping already exists!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      RESMAP99
.
RESMAP92  MOVE      "Mapping does not exists!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      RESMAP99
.
RESMAP93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      RESMAP99
.
RESMAP94  MOVE      "SMS Response Mapping already exists!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      RESMAP99
.
RESMAP99  RETURN
+
.------------------------------------------------------------
.         Bulk SMS Response Mapping Maintenance
.------------------------------------------------------------
BULKRM00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      BULKRM70 IF EQUAL
.
          MATCH     "U",UPDTTYPE            * Update formaction
          GOTO      BULKRM10 IF EQUAL
.
          GOTO      BULKRM93
.
.
.         ************ UPDATE FORMACTION **********
.
BULKRM10  CALL      UPBLKR00      * Loop through Bulk Responses & update mapping
          MOVE      ZERO,EXIT
          COMPARE   ZERO,ERRORCNT
          GOTO      BULKRM99 IF EQUAL
.
          CALL      ERRLST00
          MOVE      ONE,EXIT
          GOTO      BULKRM99
.
.
.         ************ DISPLAY FORMACTION **********
.
BULKRM70  CALL      CURPER00   * Determine Current Period
          CALL      CALCDT00   * Calculate Next and Last Period Dates
.         
          CLEAR     WEBTITLE
          MOVE      "Bulk SMS Mapping",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,BULKRM99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      BULKRM99
.
BULKRM93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      BULKRM99
.
BULKRM99  RETURN
+
.------------------------------------------------------------
.         Patient Level SMS Details
.------------------------------------------------------------
PATSMS00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      PATSMS70 IF EQUAL
.
          MATCH     "X",UPDTTYPE            * XXXXX formaction
          GOTO      PATSMS10 IF EQUAL
.
          GOTO      PATSMS90
.
.
.         ************ XXXXX FORMACTION **********
.
PATSMS10 
          MOVE      ZERO,EXIT
          GOTO      PATSMS99
.
.
.         ************ DISPLAY FORMACTION **********
.
PATSMS70  PACK      KEY8,URNUMBER,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,PATSMS91
.
          PACK      KEY8,URNUMBER,SP70
          CALL      RDPMPX21
          BRANCH    OVRCD,PATSMS91
.
          CLEAR     WEBTITLE
          MOVE      "SMS Messages",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,PATSMS99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      PATSMS99
.
PATSMS90  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      PATSMS99
.
PATSMS91  MOVE      "Invalid U/R Number.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      PATSMS99
.
PATSMS99  RETURN
+
.------------------------------------------------------------
.         Process Fields 
.------------------------------------------------------------
PROFLD00  BRANCH    REPORTNO,PROFLD10,PROFLD10,PROFLD30,PROFLD40,PROFLD50
.
          GOTO      PROFLD99 
.
PROFLD10  BRANCH    FLDSETNO,PROFLD11,PROFLD12,PROFLD13
          GOTO      PROFLD99
.
PROFLD11  CALL      MASF0000           * Master File Info
          GOTO      PROFLD99
.
PROFLD12  CALL      MISC0000           * Miscellaneous SMS Notification
          GOTO      PROFLD99
.
PROFLD13  CALL      PBLK0000           * Bulk SMS Responses
          GOTO      PROFLD99
.
PROFLD30  BRANCH    FLDSETNO,PROFLD31,PROFLD32
          GOTO      PROFLD99
.
PROFLD31  CALL      MASF0000           * Master File Info
          GOTO      PROFLD99
.
PROFLD32  CALL      MISC0000           * Miscellaneous SMS Notification
          GOTO      PROFLD99
.
PROFLD40  BRANCH    FLDSETNO,PROFLD41
          GOTO      PROFLD99
.
PROFLD41  CALL      MISC0000           * Miscellaneous SMS Notification
          GOTO      PROFLD99
.
PROFLD50  BRANCH    FLDSETNO,PROFLD51,PROFLD52,PROFLD53
          GOTO      PROFLD99
.
PROFLD51  CALL      MASF0000
          GOTO      PROFLD99
.
PROFLD52  CALL      PMSM0000           * SMS History file
          GOTO      PROFLD99
.
PROFLD53  CALL      PMRR0000           * SMS Error file
          GOTO      PROFLD99
.
PROFLD99  RETURN
+
.------------------------------------------------------------
.         Process Miscellaneous SMS Notification tags
.------------------------------------------------------------
MISC0000  BRANCH    FLDITMNO,MISC0010,MISC0020,MISC0030,MISC0040,MISC0050:
                             MISC0060,MISC0070,MISC0080,MISC0090,MISC0100:
                             MISC0110,MISC0120,MISC0130,MISC0140,MISC0150:
                             MISC0160,MISC0170,MISC0180,MISC0190,MISC0200:
                             MISC0210,MISC0220,MISC0230,MISC0240,MISC0250:
                             MISC0260,MISC0270,MISC0280,MISC0290,MISC0300
.
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      MISC9999
.
MISC0010  CALL      TABMBV00           * Table of Mappings by Value
          GOTO      MISC9999
.
MISC0020  CALL      TABMBR00           * Table of Mappings by SMS Response
          GOTO      MISC9999
.
MISC0030  CALL      PREVGR00           * Prev group of mappings
          GOTO      MISC9999
.
MISC0040  CALL      NEXTGR00           * Next group of mappings
          GOTO      MISC9999
.
MISC0050  MATCH     SP70,STARTKEY
          GOTO      MISC9999 IF EQUAL
          WRITE     HTMLFILE;STARTKEY;
          GOTO      MISC9999
.
MISC0060  WRITE     HTMLFILE;PMSRMVAR;
          GOTO      MISC9999
.
MISC0070  WRITE     HTMLFILE;PMSRINBS;
          GOTO      MISC9999
.
MISC0080  WRITE     HTMLFILE;PMSRACTV;
          GOTO      MISC9999
.
MISC0090  CALL      SMST0000                * Patient level SMS Table
          GOTO      MISC9999
.
MISC0100  WRITE     HTMLFILE;ALLVISIT;
          GOTO      MISC9999
.
MISC0110  CALL      HOST0000                * Hospital level SMS Table
          GOTO      MISC9999
.
MISC0120  WRITE     HTMLFILE;VIEWTYPE;
          GOTO      MISC9999
.
MISC0130  CALL      SELV0000
          GOTO      MISC9999
.
MISC0140  WRITE     HTMLFILE;FILTTYPE;
          GOTO      MISC9999
.
MISC0150  WRITE     HTMLFILE;FILTSTAT;
          GOTO      MISC9999
.
MISC0160  CALL      NEXT0000 
          GOTO      MISC9999
.
MISC0170  CALL      PREV0000
          GOTO      MISC9999
.
MISC0180  CALL      HOSE0000
          GOTO      MISC9999
.
MISC0190  WRITE     HTMLFILE;PMSRMVAL;
          GOTO      MISC9999
.
MISC0200  CALL      HSMT0000                * Hospital level SMS Table
          GOTO      MISC9999
.
MISC0210  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,MISC0215
.
          MATCH     SP70,FILTURNO
          GOTO      MISC9999 IF EQUAL
.
          WRITE     HTMLFILE;FILTURNO;
          GOTO      MISC9999
.
MISC0215  PACK      KEY8,FILTURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,MISC9999
.
          CALL      PATNAA00
          WRITE     HTMLFILE;PATFNAME;
          GOTO      MISC9999
.
MISC0220  CALL      HSEL0000      
          GOTO      MISC9999
.
MISC0230  CALL      WSEL0000      
          GOTO      MISC9999
.
MISC0240  WRITE     HTMLFILE;SHOWFLAG;
          GOTO      MISC9999
.
MISC0250  WRITE     HTMLFILE;DFALLHSP;
          GOTO      MISC9999
.
MISC0260  MOVE      "A ",DIM2
          MOVE      FILTADTP,TEMPFILT
          CALL      ASEL0000      
          GOTO      MISC9999
.
MISC0270  WRITE     HTMLFILE;FILTCLTP;     * clinic type filter   
          GOTO      MISC9999
.
MISC0280  MOVE      "WU",DIM2
          MOVE      FILTWLUT,TEMPFILT
          CALL      ASEL0000      
          GOTO      MISC9999
.
MISC0290  WRITE     HTMLFILE;FILTBOKI;
          GOTO      MISC9999
.
MISC0300  WRITE     HTMLFILE;FILTSTOU;
          GOTO      MISC9999
.
MISC9999  RETURN
+
.------------------------------------------------------------
.         Process Bulk Response Mapping tags
.------------------------------------------------------------
PBLK0000  BRANCH    FLDITMNO,PBLK0010,PBLK0020,PBLK0030,PBLK0040,PBLK0050:
                             PBLK0060,PBLK0070,PBLK0080,PBLK0090
.
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      PBLK9999
.
PBLK0010  CALL      NEXT0000
          GOTO      PBLK9999
.
PBLK0020  CALL      PREV0000
          GOTO      PBLK9999
.
PBLK0030  CALL      CURSTD00
          GOTO      PBLK9999
.
PBLK0040  CALL      CUREND00
          GOTO      PBLK9999
.
PBLK0050  CALL      CURYR000
          GOTO      PBLK9999
.
PBLK0060  CALL      CURMON00
          GOTO      PBLK9999
.
PBLK0070  CALL      SELV0000
          GOTO      PBLK9999
.
PBLK0080  WRITE     HTMLFILE;VIEWTYPE;
          GOTO      PBLK9999
.
PBLK0090  MOVE      ZERO,RECCNT        * Set Record Counter
          CALL      BLKSRL00           * Bulk Response Mapping list
          GOTO      PBLK9999
.
PBLK9999  RETURN
+
.------------------------------------------------------------
. Next Day, Week, Month
.------------------------------------------------------------
NEXT0000  MOVE      TEMPLATE,F3
          MOVE      F3,KEY3
          REP       " 0",KEY3
.
          MOVE      REPORTNO,F2
          MOVE      F2,KEY2
          REP       " 0",KEY2
.
          REP       " +",FILTTYPE
          REP       " +",FILTSTAT
          REP       " +",FILTURNO
          REP       " +",FILTHOSP
.
          WRITE     HTMLFILE;"patweb64.pbl":
                             "?reportno=",KEY2:
                             "&template=",KEY3:
                             "&frstdate=",NXTFDATE:
                             "&lastdate=",NXTLDATE:
                             "&viewtype=",VIEWTYPE:
                             "&filttype=",FILTTYPE:
                             "&filtstat=",FILTSTAT:
                             "&filturno=",FILTURNO:
                             "&showflag=1":
                             "&filthosp=",FILTHOSP:
                             "&dfallhsp=",DFALLHSP:
                             "&filtward=",FILTWARD:   * Ward filter
                             "&filtadtp=",FILTADTP:   * Health Speciality filter
                             "&filtcltp=",FILTCLTP:   * OP Clinic Type filter
                             "&filtwlut=",FILTWLUT:   * Waiting List Unit filter
                             "&filtboki=",FILTBOKI:
                             "&filtstou=",FILTSTOU;
.
          REP       "+ ",FILTTYPE
          REP       "+ ",FILTSTAT
          REP       "+ ",FILTURNO
          REP       "+ ",FILTHOSP
.
NEXT9999  RETURN
+
.------------------------------------------------------------
. Previous Day, Week, Month
.------------------------------------------------------------
PREV0000  MOVE      TEMPLATE,F3
          MOVE      F3,KEY3
          REP       " 0",KEY3
.
          MOVE      REPORTNO,F2
          MOVE      F2,KEY2
          REP       " 0",KEY2
.
          REP       " +",FILTTYPE
          REP       " +",FILTSTAT
          REP       " +",FILTURNO
          REP       " +",FILTHOSP
.
          WRITE     HTMLFILE;"patweb64.pbl":
                             "?reportno=",KEY2:
                             "&template=",KEY3:
                             "&frstdate=",PREFDATE:
                             "&lastdate=",PRELDATE:
                             "&viewtype=",VIEWTYPE:
                             "&filttype=",FILTTYPE:
                             "&filtstat=",FILTSTAT:
                             "&filturno=",FILTURNO:
                             "&showflag=1":
                             "&filthosp=",FILTHOSP:
                             "&dfallhsp=",DFALLHSP:
                             "&filtward=",FILTWARD:   * Ward filter
                             "&filtadtp=",FILTADTP:   * Health Speciality filter
                             "&filtcltp=",FILTCLTP:   * OP Clinic Type filter
                             "&filtwlut=",FILTWLUT:   * Waiting List Unit filter
                             "&filtboki=",FILTBOKI:
                             "&filtstou=",FILTSTOU;
.
          REP       "+ ",FILTTYPE
          REP       "+ ",FILTSTAT
          REP       "+ ",FILTURNO
          REP       "+ ",FILTHOSP
.
PREV9999  RETURN
+
.------------------------------------------------------------
.         Table output for SMS Response Mappings by Value
.------------------------------------------------------------
TABMBV00  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY60,STARTKEY,SP70
          CALL      RSPMSRM1
TABMBV10  CALL      RKPMSRM1
          BRANCH    OVRCD,TABMBV90
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      TABMBV10
          ENDIF
.
          CALL      SMROW000
.
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      TABMBV10 IF NOT EQUAL
          GOTO      TABMBV99
.
TABMBV90  CALL      NOMORE00   * Display No More Records Message
.
TABMBV99  RETURN
+
.------------------------------------------------------------
.         Table output for SMS Response Mappings by Response
.------------------------------------------------------------
TABMBR00  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD     * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY60,STARTKEY,SP70
          CALL      RSPMSRM2
TABMBR10  CALL      RKPMSRM2
          BRANCH    OVRCD,TABMBR90
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      TABMBR10
          ENDIF
.
          CALL      SMROW000
.
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      TABMBR10 IF NOT EQUAL
          GOTO      TABMBR99
.
TABMBR90  CALL      NOMORE00   * Display No More Records Message
.
TABMBR99  RETURN
+
.------------------------------------------------------------
.         SMS Response Mapping details (TABLE ROW)
.------------------------------------------------------------
SMROW000  WRITE     HTMLFILE;"<tr>";
.
          REP       " +",PMSRMVAR
          REP       " +",PMSRINBS
.
.         Replace any instances of ' with %60 for javascript friendliness
.
          MOVE      PMSRINBS,STRNGINP
          CALL      URLENC00
.
          WRITE     HTMLFILE;"<td nowrap><A HREF='javascript:ShowDetail01":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&ptcod001=",PMSRMVAR:
                             "&ptcod002=",STRNGOUT,"#")'>";
.
          REP       "+ ",PMSRMVAR
          REP       "+ ",PMSRINBS
.
          WRITE     HTMLFILE;"<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             PMSRMVAR,"</td>":
                             "<td>&nbsp;",PMSRINBS,"</td>";
.
          MOVE      ZERO,F1
          MOVE      PMSRMVAL,F1
          MOVE      DNO,VALSTRNG
          LOAD      VALSTRNG,F1,DYES,DUNK
          WRITE     HTMLFILE;"<td>&nbsp;",VALSTRNG,"</td>";
.
          MATCH     "1",PMSRACTV
          IF        @EQUAL
            WRITE     HTMLFILE;"<td>&nbsp;",DYES,"</td>";
          ELSE
            WRITE     HTMLFILE;"<td>&nbsp;",DNO,"</td>";
          ENDIF
.
          WRITE     HTMLFILE;"</tr>";
          GOTO      SMROW999
.
SMROW999  RETURN
+
.-------------------------------------------------------------
. Link to Prev Group
.-------------------------------------------------------------
PREVGR00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3
          IF        F3<0
            MOVE      ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
.
          REP       " +",STARTKEY
          REP       " +",PTCOD001
          REP       " +",PTCOD002
.
          REP       "'~",STARTKEY
.
          WRITE     HTMLFILE;"patweb64.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&startkey=",STARTKEY:
                                 "&ptcod001=",PTCOD001:
                                 "&ptcod002=",PTCOD002;
.
          REP       "+ ",STARTKEY
          REP       "+ ",PTCOD001
          REP       "+ ",PTCOD002
.
          REP       "~'",STARTKEY
.
PREVGR99  RETURN
+
.-----------------------------------------------------------
. Link to Next Group
.-----------------------------------------------------------
NEXTGR00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
.
          REP       " +",STARTKEY
          REP       " +",PTCOD001
          REP       " +",PTCOD002
.
          REP       "'~",STARTKEY
.
          WRITE     HTMLFILE;"patweb64.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&startkey=",STARTKEY:
                                 "&ptcod001=",PTCOD001:
                                 "&ptcod002=",PTCOD002;
.
          REP       "+ ",STARTKEY
          REP       "+ ",PTCOD001
          REP       "+ ",PTCOD002
.
          REP       "~'",STARTKEY
.
NEXTGR99  RETURN
+
.------------------------------------------------------------
.         Bulk SMS Response List
.------------------------------------------------------------
BLKSRL00  MOVE      ZERO,EXIT
          PACK      KEY34,FRSTDATE,SP70
          CALL      RSPMSRR3
BLKSRL10  CALL      RKPMSRR3
          BRANCH    OVRCD,BLKSRL99
.
          MATCH     "0",VIEWTYPE       * Day view type
          IF        @EQUAL
            MATCH     FRSTDATE,PMRRRDTE
            GOTO      BLKSRL99 IF NOT EQUAL
          ENDIF
.
          MATCH     FRSTDATE,PMRRRDTE
          GOTO      BLKSRL99 IF LESS
.
          MATCH     PMRRRDTE,LASTDATE
          GOTO      BLKSRL99 IF LESS
.
          MATCH     " 0",PMRRSTAT       * Only process replies not errors
          GOTO      BLKSRL10 IF NOT EQUAL
.
          MATCH     "1",PMRRMAPD       * Already mapped?
          GOTO      BLKSRL10 IF EQUAL
.
          ADD       ONE,RECCNT
          MOVE      RECCNT,DRECCNT
          REP       " 0",DRECCNT
.
          PACK      KEY18,PMRRUMID,PMRRUCNT,SP70
.
          UNPACK    PMRRRDTE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DISPDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          WRITE     HTMLFILE;"<tr>":
                             "<td>",PMRRRESA,"</td>":
                             "<td><input type=checkbox name=bsreckey",DRECCNT:
                             " value='",KEY18,"' ></td>":
                             "<td>",DISPDATE," at ",PMRRRTIM,"</td>":
                             "</tr>";
.
          IF        RECCNT > 99
            GOTO      BLKSRL99
          ENDIF
.
          GOTO      BLKSRL10
.
BLKSRL99  RETURN
+
.------------------------------------------------------------
.         Update Bulk SMS Response mappings
.------------------------------------------------------------
UPBLKR00  MOVE      ZERO,BSKEYCNT
          MOVE      ZERO,ERRORCNT
.
UPBLKR10  ADD       ONE,BSKEYCNT
          IF        BSKEYCNT>BSKEYEND
            GOTO       UPBLKR99
          ENDIF
.
          MATCH     SP70,BSRECKEY[BSKEYCNT] * Bulk Response record key
          GOTO      UPBLKR10 IF EQUAL
          UNPACK    BSRECKEY[BSKEYCNT],KEY18
.
          PACK      KEY18,KEY18,SP70
          CALL      RDPMSRR1                * Get Responses Received record
          BRANCH    OVRCD,UPBLKR91
.
          REP       UPPLOW,PMRRRESA         * Convert to uppercase
          PACK      KEY60,PTCOD001,PMRRRESA,SP70
          CALL      RDPMSRM1
          BRANCH    OVRCD,UPBLKR20          * No corresponding mapping found
.
.
.         Got an existing SMS Response Mapping record...
.
          MATCH     "1",PMRRMAPD
          IF        !@EQUAL
            MOVE      ONE,PMRRMAPD          * Mapped
            CALL      UPPMSRR1              * Update Responses Received record
          ENDIF
.
.         Update SMS Response Mapping record?
.
          MOVE      PTCOD004,PMSRMVAL       * Mapped value
          MOVE      ONE,PMSRACTV            * Active
          CALL      UPPMSRM1                * Update Response Mapping
.
          UNPACK    PMRRRKEY,VISITNUM
          MOVE      PMSRMVAL,CONFMVAL
          CALL      UPPTVX00                * Update visit extn record
.
          GOTO      UPBLKR10
.
.         Add new mapping...
.
UPBLKR20  CALL      CLPMSSRM
          MOVE      PTCOD001,PMSRMVAR       * Mapped variable
          MOVE      PMRRRESA,PMSRINBS       * Inbound SMS value
          MOVE      PTCOD004,PMSRMVAL       * Mapped value
          MOVE      ONE,PMSRACTV            * Initialise mapping to be 'Active'
.
          PACK      KEY60,PTCOD001,PMRRRESA,SP70
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          CALL      WRPMSRM1                * Write new response mapping
          TRAPCLR   IO
.
.         Set mapped status on Responses Received record
.
          MOVE      ONE,PMRRMAPD
          CALL      UPPMSRR1
.
          UNPACK    PMRRRKEY,VISITNUM
          MOVE      PMSRMVAL,CONFMVAL
          CALL      UPPTVX00                * Update visit extn record
.
          GOTO      UPBLKR10
.
UPBLKR91  ADD       ONE,ERRORCNT
          IF        ERRORCNT > 99
            GOTO      UPBLKR10
          ENDIF
.
          CLEAR     ERRORLST[ERRORCNT]
          APPEND    "Invalid Bulk Response Record Key - ",ERRORLST[ERRORCNT]
          APPEND    KEY60,ERRORLST[ERRORCNT]
          RESET     ERRORLST[ERRORCNT]
          GOTO      UPBLKR10
.
UPBLKR99  RETURN
+
.------------------------------------------------------------------------------
.         Update the visit extn record with Appointment Confirmed (PMVXCONF)
.
.         Parameters:
.           VISITNUM - Visit number
.           CONFMVAL - Appointment Confirmed value
.------------------------------------------------------------------------------
UPPTVX00  MATCH     SP70,VISITNUM
          GOTO      UPPTVX90 IF EQUAL
.
          PACK      KEY8,VISITNUM,SP70
          CALL      RDPMVX11
          BRANCH    OVRCD,UPPTVX91
.
          MOVE      CONFMVAL,PMVXCONF
          CALL      UPPMVX11
.
UPPTVX90  MOVE      ZERO,EXIT
          GOTO      UPPTVX99
.
UPPTVX91  MOVE      ONE,EXIT
UPPTVX99  RETURN
+
.------------------------------------------------------------------------------
. Output Javascript List of Errors for Processing Range of U/R or MR Keys
.------------------------------------------------------------------------------
ERRLST00  CALL      OPENHTML
          BRANCH    EXIT,ERRLST95
.
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {"
.
          MOVE      ZERO,F3
ERRLST10  ADD       ONE,F3
          IF        F3>ERRORCNT | F3>99
            GOTO      ERRLST20
          ENDIF
          WRITE     HTMLFILE;"    alert(#"",ERRORLST[F3],"\n#");"
          GOTO      ERRLST10
.
ERRLST20  WRITE     HTMLFILE;"    alert(#"All Other Records Processed/n#");"
          WRITE     HTMLFILE;"    if (window.name.substr(0,4)=='Hide' ) {":
                             "    self.close() } ":
                             "    else {":
                             "    history.go(-1)}":
                             "}":
                             "</script>"
.
          CALL      CLOSHTML
          GOTO      ERRLST99
.
ERRLST95  DISPLAY   "*** Fifo Not Found ***"
.
ERRLST99  RETURN
+
.------------------------------------------------------------------------------
.         Encode a string to be URL friendly (replace ' with %27)
.------------------------------------------------------------------------------
URLENC00  CLEAR     STRNGOUT
          RESET     CURRCHAR
          MOVE      ZERO,STRNGLEN
.
          MOVE      ONE,POSITION
          MOVELPTR  STRNGINP,STRNGLEN
.
URLENC10  CMOVE     STRNGINP,CURRCHAR
          CMATCH    "'",CURRCHAR
          IF        @EQUAL
            APPEND    "%",STRNGOUT
            APPEND    TWENTY7,STRNGOUT
          ELSE
            APPEND    CURRCHAR,STRNGOUT
          ENDIF
.
          BUMP      STRNGINP
          ADD       ONE,POSITION
.
          IF        (POSITION > STRNGLEN)
            GOTO      URLENC90
          ENDIF
.
          GOTO      URLENC10
.
URLENC90  RESET     STRNGOUT
.
URLENC99  RETURN
.
.------------------------------------------------------------
. Hospital Selection
.------------------------------------------------------------
HSEL0000  PACK      KEY3,SP70
          CALL      RSPTHSP1
HSEL0100  CALL      RKPTHSP1
          BRANCH    OVRCD,HSEL9999
.
          PACK      KEY13,USERID,SP70                * All hospital access
          CALL      RDMLSEC1
          IF        OVRCD=1
            PACK      KEY13,USERID,PTHSHOSP,SP70     * This hospital access
            CALL      RDMLSEC1
            BRANCH    OVRCD,HSEL0100
          ENDIF
.
          MOVE      PTHSNAME,KEY50
          PACK      KEY5,QUOTE,PTHSHOSP,QUOTE
          MATCH     FILTHOSP,PTHSHOSP
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=",KEY5," selected>":
                               KEY50,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=",KEY5,">",KEY50,"</option>"
          ENDIF
          GOTO      HSEL0100
.
HSEL9999  RETURN
.
+
.----------------------------------------------------------------
. Selection list for Admission Type / Waiting Unit
.----------------------------------------------------------------
ASEL0000  MOVE      DIM2,KEY2
          PACK      PTCDKEY2,KEY2,SP70
          CALL      RDSCODE2
ASEL1000  CALL      RDKCODE2
          BRANCH    OVRCD,ASEL9999
.
          MATCH     KEY2,TCODE
          GOTO      ASEL9999 IF NOT EQUAL
.
          MATCH     "A",PTCOACTV
          GOTO      ASEL1000 IF NOT EQUAL
.
          MATCH     TEMPFILT,ACODE
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=#"",ACODE,"#" selected>":
                               TDESC,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=#"",ACODE,"#">":
                               TDESC,"</option>"
          ENDIF
.
          GOTO      ASEL1000
.
ASEL9999  RETURN
+
.------------------------------------------------------------
. Ward selection
.------------------------------------------------------------
WSEL0000  PACK      KEY29,SP70
          OPEN      PATWR1A7,"patwr1af"
          IF        IBCNMHOS = 1
            PACK      KEY29,SP3,WBSEHOSP,SP70
          ENDIF
          CALL      RDSWARD7
WSEL0100  CALL      RDKWARD7
          BRANCH    OVRCD,WSEL9999
.
. If we have a bed number we have passed all the ward
. master records, so exit
.
          MATCH     SP70,WBED
          GOTO      WSEL9999 IF NOT EQUAL
.
          COMPARE   WACTIVE,ZERO                 *List only active wards
          GOTO      WSEL0100 IF NOT EQUAL
.
          IF        IBCNMHOS = 1
            MATCH      SP3,WBSEHOSP
            IF        !@EQUAL
              MATCH     WBSEHOSP,PTWDHOSN
              GOTO      WSEL9999 IF NOT EQUAL
            ENDIF
          ENDIF
.
. write the select option
.
          WRITE     HTMLFILE;"<option value=#"",WWARD,"#"";
          MATCH     FILTWARD,WWARD
          IF        @EQUAL
            WRITE     HTMLFILE;" selected";
          ENDIF
          WRITE     HTMLFILE;">",WBDESC;
.
          IF        IBCNMHOS = 1
            MATCH      SP3,WBSEHOSP
            IF        @EQUAL
              WRITE     HTMLFILE;" - ",PTWDHOSN;
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;"</option>"
          GOTO      WSEL0100
.
WSEL9999  RETURN
.
.------------------------------------------------------------------------------
. Patient level SMS message history list
.------------------------------------------------------------------------------
SMST0000  PACK      KEY24,URNUMBER,SP70
          CALL      RSPMSMH2
SMST1000  CALL      RKPMSMH2
          BRANCH    OVRCD,SMST9999
.
          MATCH     URNUMBER,PMSMURNO
          GOTO      SMST9999 IF NOT EQUAL
.
          IF        ALLVISIT<>1
            MATCH     PMSMRKEY,ADMISSNO
            GOTO      SMST1000 IF NOT EQUAL
          ENDIF
.
          CALL      HOSPDT00
          CALL      WARDDT00
.
          IF        IBCNMHOS = 1
            STRIP     SMSHOSPN
            RESET     SMSHOSPN
            PACK      HOSPWARD,SMSHOSPN,SP1,SLASH,SP1,SMSWARDC
          ELSE
            MOVE      SMSWARDC,HOSPWARD
          ENDIF
.
          CLEAR     HTMLLINK
          APPEND    "javascript:linkPatient('",HTMLLINK
          APPEND    PMSMURNO,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    PMSMMTYP,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    PMSMRKEY,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
          STRIP     HTMLLINK
.
          MOVE      SP70,DESCMTYP
          MOVE      SP70,DESCSCOD
.
          MATCH     " 0",PMSMMTYP
          IF        @EQUAL
            MOVE      "Booking",DESCMTYP
            PACK      KEY6,PMSMSCOD,SP1,SP1,ZERO,SP70
            CALL      BRDLET1
            IF        OVRCD<>1
              MOVE      BLTEXT,DESCSCOD
            ENDIF 
          ENDIF
.
          MATCH     " 2",PMSMMTYP
          IF        @EQUAL
            MOVE      "Outpatient",DESCMTYP
            PACK      KEY6,PMSMSCOD,SP1,SP1,ZERO,SP70
            CALL      RDOTLET1
            IF        OVRCD<>1
              MOVE      OTLTTEXT,DESCSCOD
            ENDIF
          ENDIF
.
          MATCH     " 3",PMSMMTYP
          IF        @EQUAL
            MOVE      "Inpatient",DESCMTYP
            PACK      KEY6,PMSMSCOD,SP1,SP1,ZERO,SP70
            CALL      RDPTLET1
            IF        OVRCD<>1
              MOVE      PTLETEXT,DESCSCOD
            ENDIF
          ENDIF
.
          MATCH     " 9",PMSMMTYP
          IF        @EQUAL
            MOVE      "Waiting List",DESCMTYP
            PACK      KEY6,PMSMSCOD,SP1,SP1,ZERO,SP70
            CALL      RDLET1
            IF        OVRCD<>1
              MOVE      WLTEXT,DESCSCOD
            ENDIF
          ENDIF
.
          MOVE      SP70,DESCSTAT
.
          MATCH     " 0",PMSMSTAT
          IF        @EQUAL
            MOVE      "Sent",DESCSTAT
          ENDIF
.
          MATCH     " 1",PMSMSTAT
          IF        @EQUAL
            MOVE      "Response Received",DESCSTAT
          ENDIF
.
          PACK      HTMLLNK2,SP70,SP70
.
          MOVE      ZERO,F1
          MOVE      PMSMSTAT,F1
          IF        F1=2 | F1=6
            IF        F1=2
              MOVE      "Error",DESCSTAT
            ENDIF
.
            IF        F1=6
              MOVE      "No Mobile Number",DESCSTAT
            ENDIF
.
            REP       " +",PMSMUMID
            CLEAR     HTMLLNK2
            APPEND    "<input type=button value='Resend' ",HTMLLNK2
            APPEND    "class=button onclick=reSendSMS('",HTMLLNK2
            APPEND    PMSMUMID,HTMLLNK2
            APPEND    "')>",HTMLLNK2
            RESET     HTMLLNK2
            REP       "+ ",PMSMUMID
          ENDIF
.
          MATCH     " 3",PMSMSTAT
          IF        @EQUAL
            MOVE      "Re-Sent",DESCSTAT
          ENDIF
.
          MATCH     " 4",PMSMSTAT
          IF        @EQUAL
            MOVE      "Waiting to be sent",DESCSTAT
          ENDIF
.
          MATCH     " 5",PMSMSTAT
          IF        @EQUAL
            MOVE      "Error Corrected",DESCSTAT
          ENDIF
.
SMST2000  WRITE     HTMLFILE;"t.addRow(#"",*+,HTMLLINK,*-,"#",":     * 0
                             "#"",PMSMUMID,"#",":                    * 1
                             "#"",PMSMURNO,"#",":                    * 2
                             "#"",PMSMMTYP,"#",":                    * 3
                             "#"",PMSMRKEY,"#",":                    * 4
                             "#"",PMSMSCOD,"#",":                    * 5
                             "#"",PMSMSDAT,PMSMSTIM,"#",":           * 6
                             "#"",PMSMSDAT,"#",":                    * 7
                             "#"",PMSMSTIM,"#",":                    * 8
                             "#"",PMSMMNUM,"#",":                    * 9 
                             "#"",PMSMSUID,"#",":                    * 10
                             "#"",PMSMMESA;                          * 11
.
          WRITE     HTMLFILE;PMSMMESB;        
.
          WRITE     HTMLFILE;PMSMMESC;        
.
          WRITE     HTMLFILE;PMSMMESD;        
.
          WRITE     HTMLFILE;PMSMMESE;        
.
          WRITE     HTMLFILE;PMSMMESF;          
.
          WRITE     HTMLFILE;"#",":
                             "#"",PMSMSTAT,"#",":                    * 12
                             "#"",DESCMTYP,"#",#"";                  * 13
.
          PACK      KEY18,PMSMUMID,Z70
          CALL      RSPMSRR1                * Get responses
SMST3000  CALL      RPPMSRR1
          BRANCH    OVRCD,SMST5000                                   * 14
.
          MATCH     PMSMUMID,PMRRUMID
          GOTO      SMST5000 IF NOT EQUAL
.
          UNPACK    PMRRRDTE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN:
                               JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DISPDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          PACK      RSDATTIM,LBRACK,DISPDATE,SP1,ATTEXT:
                             SP1,PMRRRTIM,RBRACK
.
          MATCH     "1",PMRRMAPD                 * Mapped response
          IF        @EQUAL
            CALL      GMAP0000                   * Get mapped response code
.
            MATCH     SP70,MAPDRESP
            IF        !@EQUAL
              PACK      RESPONSE,PMRRRESA
              REP       "` ",RESPONSE
              REP       "#" ",RESPONSE
              REP       ", ",RESPONSE
              REP       "' ",RESPONSE
              STRIP     RESPONSE
              WRITE     HTMLFILE;"<img src='../images/CommentIcon.gif'":
                        " onmouseover=\#"show_it('",*+,RESPONSE,"');\#"":
                        " onmouseout=\#"hide_it();\#"":
                        " alt='Show Responses'>";
              WRITE     HTMLFILE;MAPDRESP,SP1,RSDATTIM,NLTEXT;
              GOTO      SMST3000
            ENDIF
          ENDIF
.
          STRIP     PMRRRESA
          STRIP     PMRRRESB
          STRIP     PMRRRESC
          STRIP     PMRRRESD
          STRIP     PMRRRESE
          STRIP     PMRRRESF
.
          WRITE     HTMLFILE;*+,PMRRRESA,*-;
.
          WRITE     HTMLFILE;*+,PMRRRESB,*-;
.
          WRITE     HTMLFILE;*+,PMRRRESC,*-;
.
          WRITE     HTMLFILE;*+,PMRRRESD,*-;
.
          WRITE     HTMLFILE;*+,PMRRRESE,*-;
.
          WRITE     HTMLFILE;*+,PMRRRESF,*-;
.
          WRITE     HTMLFILE;" ",RSDATTIM,"<br>";
.
          GOTO      SMST3000
.
SMST5000  MOVE      SP70,DESCCTYP
.
          MATCH     "1",PMSMCONT
          IF        @EQUAL
            MATCH     SP70,PMSMCTYP
            IF        !@EQUAL
              PACK      KEY5,CATTC,PMSMCTYP,SP70
              CALL      RDCODE1
              IF        OVRCD=1
                MOVE      PMSMCTYP,TDESC
              ENDIF
              MOVE      TDESC,DESCCTYP
            ENDIF
          ENDIF
.
          STRIP     DESCSCOD
          STRIP     DESCSTAT
.
          MOVE      SP70,WBSENAM
          MATCH     SP70,PMSMSUID
          IF        !@EQUAL
            PACK      KEY10,PMSMSUID,SP70
            CALL      RDWBSE1
            IF        OVRCD=1
              MOVE      PMSMSUID,WBSENAM
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;"#",":
                             "#"",*+,DESCSCOD,*-,"#",":              * 15
                             "#"",*+,DESCSTAT,*-,"#",":              * 16
                             "#"",DESCSTAT,HTMLLNK2,"#",":           * 17
                             "#"",DESCCTYP,"#",":                    * 18
                             "#"",HOSPWARD,"#",":                    * 19
                             "#"",WBSENAM,"#");"                     * 20
.
          GOTO      SMST1000
.
SMST9999  RETURN
+
.------------------------------------------------------------
.         Get the response error record
.------------------------------------------------------------
GETR0000  PACK      KEY18,PMSMUMID,Z70
          CALL      RSPMSRR1                * Get responses
          CALL      RPPMSRR1
          BRANCH    OVRCD,GETR9000
.
          MATCH     PMSMUMID,PMRRUMID       * Same message id     
          GOTO      GETR9000 IF NOT EQUAL
.
          MATCH     " 3",PMRRSTAT           * Delivery Failure
          GOTO      GETR9000 IF NOT EQUAL
.
          GOTO      GETR9999
.
GETR9000  CALL      CLPMSSRR
.
GETR9999  RETURN
+
.------------------------------------------------------------
.         Get the mapped response code       
.------------------------------------------------------------
GMAP0000  UNPACK    PMRRRESA,RECVMESG     * Only take first 50 chars of msg
          REP       UPPLOW,RECVMESG       * Convert msg to uppercase
          MOVE      SP70,MAPDRESP         * Clear mapped response
.
          PACK      KEY60,RECVMESG,SP70
          CALL      RSPMSRM2
GMAP1000  CALL      RKPMSRM2
          BRANCH    OVRCD,GMAP9999
.
          MATCH     RECVMESG,PMSRINBS
          GOTO      GMAP9999 IF NOT EQUAL
.
          MATCH     "1",PMSRACTV            * Active?
          GOTO      GMAP1000 IF NOT EQUAL
.
          MATCH     "0",PMSRMVAL
          IF        @EQUAL
            MOVE     "<b>No</b>",MAPDRESP
          ENDIF
.
          MATCH     "1",PMSRMVAL
          IF        @EQUAL
            MOVE     "<b>Yes</b>",MAPDRESP
          ENDIF
.
          MATCH     "2",PMSRMVAL
          IF        @EQUAL
            MOVE     "<b>Unknown</b>",MAPDRESP
          ENDIF
.
GMAP9999  RETURN
+
.------------------------------------------------------------
.         Hopsital level SMS functionality
.------------------------------------------------------------
HOSSMS00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      HOSSMS70 IF EQUAL
.
          MATCH     "X",UPDTTYPE            * XXXXX formaction
          GOTO      HOSSMS10 IF EQUAL
.
          GOTO      HOSSMS90
.
.         ************ XXXXXX FORMACTION **********
.
HOSSMS10 
          MOVE      ZERO,EXIT
          GOTO      HOSSMS99
.
.
.         ************ DISPLAY FORMACTION **********
.
HOSSMS70  CALL      CURPER00   * Determine Current Period
          CALL      CALCDT00   * Calculate Next and Last Period Dates
.
          CALL      DEFHSP00   * Default hospital filter
.
          CLEAR     WEBTITLE
          MOVE      "SMS Messages",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,BULKRM99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      HOSSMS99
.
HOSSMS90  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      HOSSMS99
.
HOSSMS99  RETURN
+
.------------------------------------------------------------------------------
. Default hospital filter code 
.------------------------------------------------------------------------------
DEFHSP00  COMPARE  ONE,IBCNMHOS
          GOTO     DEFHSP99 IF NOT EQUAL
.
          COMPARE  ONE,SHOWFLAG
          GOTO     DEFHSP99 IF EQUAL
.
          MOVE     ONE,SHOWFLAG
. 
          IF       DFALLHSP = ONE
            MOVE     SP70,FILTHOSP
          ELSE
            MOVE     WBSEHOSP,FILTHOSP 
          ENDIF  
.
DEFHSP99  RETURN
.
.------------------------------------------------------------------------------
. Get user has access to all hospital flag
.------------------------------------------------------------------------------
ALHSPA00  MOVE     ZERO,ALLHSPAC
.
          PACK      KEY13,USERID,SP70
          CALL      RDMLSEC1
          IF        OVRCD=0
            MOVE     ONE,ALLHSPAC           * user has All hospital access
          ENDIF
.
ALHSPA99  RETURN
.
.------------------------------------------------------------------------------
. Hospital level SMS message send list
.------------------------------------------------------------------------------
HOST0000  CALL      ALHSPA00
          PACK      KEY32,FRSTDATE,SP70
          CALL      RSPMSMH3
HOST1000  CALL      RKPMSMH3
          BRANCH    OVRCD,HOST9999
.
          MATCH     PMSMSDAT,LASTDATE       * Checks lastdate
          GOTO      HOST9999 IF LESS
.
          MATCH     SP70,FILTTYPE           * Message Type Filter
          IF        !@EQUAL
            MATCH     PMSMMTYP,FILTTYPE
            GOTO      HOST1000 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,FILTSTAT           * Message Status Filter
          IF        !@EQUAL
            MATCH     PMSMSTAT,FILTSTAT
            GOTO      HOST1000 IF NOT EQUAL
          ENDIF
.
          CALL      HOSPDT00
          CALL      WARDDT00
.
. If mutl-hospital, user must have all hospital access or have access to
. the sms hospital
. 
          IF        IBCNMHOS = 1
            MATCH     SP70,FILTHOSP           * Hospital Filter
            IF        @EQUAL
              IF        ALLHSPAC = ZERO  
                PACK      KEY13,USERID,SMSHOSPC,SP70  * This hospital access
                CALL      RDMLSEC1
                BRANCH    OVRCD,HOST1000      * no access to hospital 
              ENDIF 
            ELSE 
              MATCH     SMSHOSPC,FILTHOSP
              GOTO      HOST1000 IF NOT EQUAL
            ENDIF
            STRIP     SMSHOSPN
            RESET     SMSHOSPN
            PACK      HOSPWARD,SMSHOSPN,SP1,SLASH,SP1,SMSWARDC
          ELSE
            MOVE      SMSWARDC,HOSPWARD
          ENDIF
.
          MATCH     SP70,FILTWARD           * Ward Filter
          IF        !@EQUAL
            MATCH     SMSWARDC,FILTWARD
            GOTO      HOST1000 IF NOT EQUAL
          ENDIF
.
          PACK      KEY8,PMSMURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,HOST1000
.
          PACK      KEY8,PMSMURNO,SP70
          CALL      RDPMPX21
          BRANCH    OVRCD,HOST1000
.
          PACK      AGEDATE,CCC,CYY,CMM,CDD    * Today's date
          REP       " 0",AGEDATE
          CALL      CALCAGE
          CALL      PATLN000
.
          CALL      PATFLD00                * Set Patient Folder suffix
          MOVE      KEY3,FOLDERSF           * Save returned value
.
          CLEAR     HTMLLINK
          APPEND    "javascript:linkPatient('",HTMLLINK
          APPEND    PMSMURNO,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    PMSMMTYP,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    PMSMRKEY,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
          STRIP     HTMLLINK
.
          MOVE      SP70,DESCMTYP
          MOVE      SP70,DESCSCOD
          MOVE      SP70,MTYPSRVC  
.
HOST1500  MATCH     " 0",PMSMMTYP
          IF        @EQUAL
            MOVE      "Booking",DESCMTYP
            PACK      KEY6,PMSMSCOD,SP1,SP1,ZERO,SP70
            CALL      BRDLET1
            IF        OVRCD<>1
              MOVE      BLTEXT,DESCSCOD
            ENDIF
.
            MOVE      SP8,KEY8
            UNPACK    PMSMRKEY,KEY8
            CALL      RDBKREC1
            BRANCH    OVRCD,HOST1600
.
            PACK      KEY5,ANSB,ANSK,BKRETYPE
            CALL      RDCODE1
            IF        OVRCD=0
              MATCH     SP70,FILTBOKI
              IF        !@EQUAL
                MATCH     "THE",FILTBOKI
                IF        @EQUAL
                  MATCH     SP70,FILTSTOU
                  IF        !@EQUAL
                    PACK      KEY31,DBKREBOO,TWO,BKREADAT,SP70
                    CALL      RSOPDEA2
HOST1505            CALL      RKOPDEA2
                    BRANCH    OVRCD,HOST1000
.
                    MATCH     BKREADOC,OPDACLIN
                    IF        !@EQUAL
                      GOTO      HOST1505
                    ENDIF
.
                    MATCH     FILTSTOU,OPDAUNIT
                    GOTO      HOST1000 IF NOT EQUAL
                  ELSE
                    MATCH     BKRETYPE,FILTBOKI
                    GOTO      HOST1000 IF NOT EQUAL
                  ENDIF
                ELSE
                  MATCH     BKRETYPE,FILTBOKI
                  GOTO      HOST1000 IF NOT EQUAL
                ENDIF
              ENDIF
.
              APPEND    DESCMTYP,MTYPSRVC
              APPEND    " / ",MTYPSRVC
              APPEND    TDESC,MTYPSRVC
              RESET     MTYPSRVC
            ENDIF
          ENDIF
.
          MATCH     " 2",PMSMMTYP
          IF        @EQUAL
            MOVE      "Outpatient",DESCMTYP
            PACK      KEY6,PMSMSCOD,SP1,SP1,ZERO,SP70
            CALL      RDOTLET1
            IF        OVRCD<>1
              MOVE      OTLTTEXT,DESCSCOD
            ENDIF
.                     
HOST1520    MOVE      SP8,DIM8
            UNPACK    PMSMRKEY,DIM8 
            PACK      KEY36,DIM8,SP70     * Visit Number 
            CALL      RDSBOKA6
            CALL      RDKBOKA6
            BRANCH    OVRCD,HOST1600
.    
            MATCH     DIM8,DOBAOUTN
            GOTO      HOST1600 IF NOT EQUAL                
.
            MATCH     PMSMURNO,OBAURNO
            GOTO      HOST1600 IF NOT EQUAL 
. 
            PACK      KEY12,OBASITE,OBACTYP,SP70
            CALL      RDCTYA1                * To get Clinic Type Description
            IF        OVRCD=0
              APPEND    DESCMTYP,MTYPSRVC
              APPEND    " / ",MTYPSRVC
              APPEND    OCTDESC,MTYPSRVC
              RESET     MTYPSRVC 
            ENDIF            
          ENDIF
.
          MATCH     SP70,FILTCLTP           * Clinic Type Filter
          IF        !@EQUAL
            MATCH     OBACTYP,FILTCLTP
            GOTO      HOST1000 IF NOT EQUAL
          ENDIF
.
          MATCH     " 3",PMSMMTYP
          IF        @EQUAL
            MOVE      "Inpatient",DESCMTYP
            PACK      KEY6,PMSMSCOD,SP1,SP1,ZERO,SP70
            CALL      RDPTLET1
            IF        OVRCD<>1
              MOVE      PTLETEXT,DESCSCOD
            ENDIF
.
HOST1530    MOVE      SP8,DIM8
            UNPACK    PMSMRKEY,DIM8
            PACK      KEY8,DIM8,SP70     * Admission Number 
            CALL      RDPTMIS1
            BRANCH    OVRCD,HOST1600 
.
            PACK      KEY5,ANSA,SP1,ATYPE       
            CALL      RDCODE1
            IF        OVRCD=0
              APPEND    DESCMTYP,MTYPSRVC
              APPEND    " / ",MTYPSRVC 
              APPEND    TDESC,MTYPSRVC 
              RESET     MTYPSRVC 
            ENDIF     
          ENDIF
.
          MATCH     SP70,FILTADTP           * Admission Type Filter
          IF        !@EQUAL
            MATCH     ATYPE,FILTADTP
            GOTO      HOST1000 IF NOT EQUAL
          ENDIF
.
          MATCH     " 9",PMSMMTYP
          IF        @EQUAL
            MOVE      "Waiting List",DESCMTYP
            PACK      KEY6,PMSMSCOD,SP1,SP1,ZERO,SP70
            CALL      RDLET1
            IF        OVRCD<>1
              MOVE      WLTEXT,DESCSCOD
            ENDIF
.
HOST1590    PACK      KEY19,PMSMURNO,SP70
            CALL      RDSTREA1
            CALL      RDKTREA1
            BRANCH    OVRCD,HOST1600
.
            MATCH     PMSMURNO,WMURNO
            GOTO      HOST1600 IF NOT EQUAL
.
            PACK      KEY5,ANSW,ANSU,WMUNIT,SP70
            CALL      RDCODE1
            IF        OVRCD=0
              APPEND    DESCMTYP,MTYPSRVC
              APPEND    " / ",MTYPSRVC
              APPEND    TDESC,MTYPSRVC
              RESET     MTYPSRVC
            ENDIF
          ENDIF
.
          MATCH     SP70,FILTWLUT           * Waiting list unit filter
          IF        !@EQUAL
            MATCH     WMUNIT,FILTWLUT
            GOTO      HOST1000 IF NOT EQUAL
          ENDIF
.
HOST1600  MOVE      SP70,DESCSTAT
.
          MATCH     " 0",PMSMSTAT
          IF        @EQUAL
            MOVE      "Sent",DESCSTAT
          ENDIF
.
          MATCH     " 1",PMSMSTAT
          IF        @EQUAL
            MOVE      "Response Received",DESCSTAT
          ENDIF
.
          MATCH     " 2",PMSMSTAT
          IF        @EQUAL
            MOVE      "Error",DESCSTAT
          ENDIF
.
          MATCH     " 3",PMSMSTAT
          IF        @EQUAL
            MOVE      "Re-Sent",DESCSTAT
          ENDIF
.
          MATCH     " 4",PMSMSTAT
          IF        @EQUAL
            MOVE      "Waiting to be sent",DESCSTAT
          ENDIF
.
          MATCH     " 5",PMSMSTAT
          IF        @EQUAL
            MOVE      "Error Corrected",DESCSTAT
          ENDIF
.
.
          PACK      HTMLLNK2,SP70,SP70
.
          MOVE      ZERO,F1
          MOVE      PMSMSTAT,F1
          IF        F1=2 | F1=6
            IF        F1=2
              MOVE      "Error",DESCSTAT
            ENDIF
.
            IF        F1=6
              MOVE      "No Mobile Number",DESCSTAT
            ENDIF
.
            REP       " +",PMSMUMID
            APPEND    "<input type=button value='Resend' ",HTMLLNK2
            APPEND    "class=button onclick=reSendSMS('",HTMLLNK2
            APPEND    PMSMUMID,HTMLLNK2
            APPEND    "')>",HTMLLNK2
            RESET     HTMLLNK2
            REP       "+ ",PMSMUMID
          ENDIF
.
HOST2000  WRITE     HTMLFILE;"t.addRow(#"",*+,HTMLLINK,*-,"#",":     * 0
                             "#"",PMSMUMID,"#",":                    * 1
                             "#"",PMSMURNO,"#",":                    * 2
                             "#"",PMSMMTYP,"#",":                    * 3
                             "#"",PMSMRKEY,"#",":                    * 4
                             "#"",PMSMSCOD,"#",":                    * 5
                             "#"",PMSMSDAT,PMSMSTIM,"#",":           * 6
                             "#"",PMSMSDAT,"#",":                    * 7
                             "#"",PMSMSTIM,"#",":                    * 8
                             "#"",PMSMMNUM,"#",":                    * 9
                             "#"",PMSMSUID,"#",":                    * 10
                             "#"",PMSMMESA;                          * 11
.
          WRITE     HTMLFILE;PMSMMESB;       
.
          WRITE     HTMLFILE;PMSMMESC;       
.
          WRITE     HTMLFILE;PMSMMESD;         
.
          WRITE     HTMLFILE;PMSMMESE;        
.
          WRITE     HTMLFILE;PMSMMESF;         
.
          WRITE     HTMLFILE;"#",":
                             "#"",PMSMSTAT,"#",":                    * 12
                             "#"",MTYPSRVC,"#",#"";                  * 13
.
          PACK      KEY18,PMSMUMID,Z70
          CALL      RSPMSRR1                * Get responses
HOST3000  CALL      RPPMSRR1
          BRANCH    OVRCD,HOST5000                                   * 14
.
          MATCH     PMSMUMID,PMRRUMID
          GOTO      HOST5000 IF NOT EQUAL
.
          UNPACK    PMRRRDTE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN:
                               JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DISPDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          PACK      RSDATTIM,LBRACK,DISPDATE,SP1,ATTEXT:
                             SP1,PMRRRTIM,RBRACK
.
          MATCH     "1",PMRRMAPD                 * Mapped response
          IF        @EQUAL
            CALL      GMAP0000                   * Get mapped response code
.
            MATCH     SP70,MAPDRESP
            IF        !@EQUAL
              PACK      RESPONSE,PMRRRESA
              REP       "` ",RESPONSE
              REP       "#" ",RESPONSE
              REP       ", ",RESPONSE
              REP       "' ",RESPONSE
              STRIP     RESPONSE
              WRITE     HTMLFILE;"<img src='../images/CommentIcon.gif'":
                        " onmouseover=\#"show_it('",*+,RESPONSE,"');\#"":
                        " onmouseout=\#"hide_it();\#"":
                        " alt='Show Responses'>";
              WRITE     HTMLFILE;MAPDRESP,SP1,RSDATTIM,NLTEXT;
              GOTO      HOST3000
            ENDIF
          ENDIF
.
          STRIP     PMRRRESA
          STRIP     PMRRRESB
          STRIP     PMRRRESC
          STRIP     PMRRRESD
          STRIP     PMRRRESE
          STRIP     PMRRRESF
.
          WRITE     HTMLFILE;*+,PMRRRESA,*-;
.
          WRITE     HTMLFILE;*+,PMRRRESB,*-;
.
          WRITE     HTMLFILE;*+,PMRRRESC,*-;
.
          WRITE     HTMLFILE;*+,PMRRRESD,*-;
.
          WRITE     HTMLFILE;*+,PMRRRESE,*-;
.
          WRITE     HTMLFILE;*+,PMRRRESF,*-;
.
          WRITE     HTMLFILE;" ",RSDATTIM,"<br>";
.
          GOTO      HOST3000
.
HOST5000  MOVE      SP70,DESCCTYP
.
          MATCH     "1",PMSMCONT
          IF        @EQUAL
            MATCH     SP70,PMSMCTYP
            IF        !@EQUAL
              PACK      KEY5,CATTC,PMSMCTYP,SP70
              CALL      RDCODE1
              IF        OVRCD=1
                MOVE      PMSMCTYP,TDESC
              ENDIF
              MOVE      TDESC,DESCCTYP
            ENDIF
          ENDIF
.
          STRIP     DESCSCOD
          STRIP     DESCSTAT
.
          MOVE      SP70,WBSENAM
          MATCH     SP70,PMSMSUID
          IF        !@EQUAL
            PACK      KEY10,PMSMSUID,SP70
            CALL      RDWBSE1
            IF        OVRCD=1
              MOVE      PMSMSUID,WBSENAM
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;"#",":
                             "#"",*+,DESCSCOD,*-,"#",":              * 15
                             "#"",*+,DESCSTAT,*-,HTMLLNK2,"#",":     * 16
                             "#"",FORMATNM,"#",":                    * 17
                             "#"",FOLDERSF,"#",":                    * 18
                             "#"",DESCCTYP,"#",":                    * 19
                             "#"",HOSPWARD,"#",":                    * 20
                             "#"",WBSENAM,"#");"                     * 21
.
          GOTO      HOST1000
.
HOST9999  RETURN
.
.--------------------------------------------------------------------------
. Get latest response date and time. ie. (07 Jul 2019 at 15:52:11)
. Input:  PMSMSTAT
.         PMSMUMID
. Output: RSDATTIM
.--------------------------------------------------------------------------
RPDTTM00  MOVE      SP70,RSDATTIM
          MATCH     " 1",PMSMSTAT
          GOTO      RPDTTM99 IF NOT EQUAL
. 
          PACK      KEY18,PMSMUMID,Z70
          CALL      RSPMSRR1                * Get responses
          CALL      RPPMSRR1
          BRANCH    OVRCD,RPDTTM99
.
          MATCH     PMSMUMID,PMRRUMID
          GOTO      RPDTTM99 IF NOT EQUAL
.
          UNPACK    PMRRRDTE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN:
                               JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DISPDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          PACK      RSDATTIM,LBRACK,DISPDATE,SP1,ATTEXT:
                             SP1,PMRRRTIM,RBRACK 
.
RPDTTM99  RETURN
.
.----------------------------------------------------------------------------
. Get hospital details
.----------------------------------------------------------------------------
HOSPDT00  MOVE      SP70,SMSHOSPN
          MOVE      SP70,SMSHOSPC
.
          MATCH     " 0",PMSMMTYP
          GOTO      HOSPDT40 IF EQUAL
.
          MATCH     " 9",PMSMMTYP
          GOTO      HOSPDT50 IF EQUAL
.
. Message Type - Inpatient or Ourpatient
.
          MOVE      PMSMRKEY,KEY8
          CALL      RDPMVX11
          BRANCH    OVRCD,HOSPDT99
.
          MOVE      PMVXMHOS,SMSHOSPC
          GOTO      HOSPDT80 
.
. Message Type - Booking
.
HOSPDT40  MOVE      PMSMRKEY,KEY8
          CALL      RDBKREC1
          BRANCH    OVRCD,WARDDT99
.
          PACK      KEY6,BKREWARD,SP70
          CALL      RDWARD1
          BRANCH    OVRCD,HOSPDT99
.
          MOVE      PTWDHOSN,SMSHOSPC
          GOTO      HOSPDT80
.
. Message Type - Waiting list
.
HOSPDT50  PACK      KEY19,PMSMRKEY,SP70
          CALL      RDTREA1
          BRANCH    OVRCD,HOSPDT99
.
          MOVE      WTWMIHSP,SMSHOSPC
.
HOSPDT80  PACK      KEY3,SMSHOSPC,SP70
          CALL      RDPTHSP1
          IF        OVRCD=1
            CALL      CLPATHSP
          ENDIF
.
          MOVE      PTHSSNAM,SMSHOSPN
.
HOSPDT99  RETURN
.
.---------------------------------------------------------------------------- 
. Get SMS ward code
.---------------------------------------------------------------------------- 
WARDDT00  MOVE      SP70,SMSWARDC
          MATCH     " 0",PMSMMTYP
          GOTO      WARDDT40 IF EQUAL
.
          MATCH     " 2",PMSMMTYP                * Outpatient
          GOTO      WARDDT99 IF EQUAL
.        
          MATCH     " 9",PMSMMTYP                *Waiting List
          GOTO      WARDDT99 IF EQUAL
.
          MOVE      PMSMRKEY,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,WARDDT99
.
          MATCH     "1",DASTAT
          GOTO      WARDDT20 IF NOT EQUAL
.
          MOVE      PTMIXWRD,SMSWARDC
          GOTO      WARDDT99 
.
WARDDT20  MATCH     "2",DASTAT
          GOTO      WARDDT30 IF NOT EQUAL
.
          CALL      TRNWRD00
          GOTO      WARDDT99
.
WARDDT30  MATCH     "3",DASTAT
          GOTO      WARDDT99 IF NOT EQUAL
. 
          CALL      TRNWRD00
          GOTO      WARDDT99
.
WARDDT40  MOVE      PMSMRKEY,KEY8
          CALL      RDBKREC1
          BRANCH    OVRCD,WARDDT99
.
          MOVE      BKREWARD,SMSWARDC
.
WARDDT99  RETURN 
.
.----------------------------------------------------------------------------
. Get admission ward from patient transfer details
.----------------------------------------------------------------------------
TRNWRD00  MOVE      PMSMRKEY,KEY8
          PACK      KEY30,KEY8,Z70
          CALL      RDSTRAN2                * position on admission
TRNWRD20  CALL      RDPTRAN2                * read previous record
          BRANCH    OVRCD,TRNWRD99          * shouldn't happen
.
          MATCH     KEY8,DTADMN             * same admission still ?
          GOTO      TRNWRD99 IF NOT EQUAL   * no - shouldn't happen
.
TRNWRD60  MATCH     "A",TMOVE
          GOTO      TRNWRD20 IF NOT EQUAL
.
          MOVE      TWARD,SMSWARDC
.
TRNWRD99  RETURN  
+
.------------------------------------------------------------------------------
. Patient Level SMS Response Error Messages List
.------------------------------------------------------------------------------
HOSE0000  CALL      ALHSPA00
          PACK      KEY34,FRSTDATE,SP70
          CALL      RSPMSRR3
HOSE1000  CALL      RKPMSRR3
          BRANCH    OVRCD,HOSE9999
.
          MATCH     PMRRRDTE,LASTDATE       * Checks lastdate with received
          GOTO      HOSE9999 IF LESS
.
          MATCH     " 0",PMRRSTAT           * Skip 'A Message' status
          GOTO      HOSE1000 IF EQUAL
.
          MATCH     " 1",PMRRSTAT           * Skip 'Awaiting Delivery' status
          GOTO      HOSE1000 IF EQUAL
.
          MATCH     " 2",PMRRSTAT           * Skip 'Successful Delivery' status
          GOTO      HOSE1000 IF EQUAL
.
          MATCH     SP70,FILTTYPE           * Message Type Filter
          IF        !@EQUAL
            MATCH     PMRRMTYP,FILTTYPE
            GOTO      HOSE1000 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,FILTSTAT           * Filter on 'Response Status'
          IF        !@EQUAL
            MATCH     PMRRSTAT,FILTSTAT
            GOTO      HOSE1000 IF NOT EQUAL
          ENDIF
.
          PACK      KEY16,PMRRUMID,SP70
          CALL      RDPMSMH1
          BRANCH    OVRCD,HOSE1000
.
          PACK      KEY8,PMRRURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,HOSE1000
.
          PACK      KEY8,PMRRURNO,SP70
          CALL      RDPMPX21
          BRANCH    OVRCD,HOSE1000
.
          CALL      HOSPDT00
          CALL      WARDDT00
.
. If mutl-hospital, user must have all hospital access or have access to
. the sms hospital
.
          IF        IBCNMHOS = 1
            MATCH     SP70,FILTHOSP           * Hospital Filter
            IF        @EQUAL
              IF        ALLHSPAC = ZERO
                PACK      KEY13,USERID,SMSHOSPC,SP70  * This hospital access
                CALL      RDMLSEC1
                BRANCH    OVRCD,HOSE1000      * no access to hospital
              ENDIF
            ELSE
              MATCH     SMSHOSPC,FILTHOSP
              GOTO      HOSE1000 IF NOT EQUAL
            ENDIF
            STRIP     SMSHOSPN
            RESET     SMSHOSPN
            PACK      HOSPWARD,SMSHOSPN,SP1,SLASH,SP1,SMSWARDC
          ELSE
            MOVE      SMSWARDC,HOSPWARD
          ENDIF
.
          MATCH     SP70,FILTWARD           * Ward Filter
          IF        !@EQUAL
            MATCH     SMSWARDC,FILTWARD
            GOTO      HOSE1000 IF NOT EQUAL
          ENDIF
.
          PACK      AGEDATE,CCC,CYY,CMM,CDD    * Today's date
          REP       " 0",AGEDATE
          CALL      CALCAGE
          CALL      PATLN000
.
          CALL      PATFLD00                * Set Patient Folder suffix
          MOVE      KEY3,FOLDERSF           * Save returned value
.
          CLEAR     HTMLLINK
          APPEND    "javascript:linkPatient('",HTMLLINK
          APPEND    PMSMURNO,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    PMSMMTYP,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    PMSMRKEY,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
          STRIP     HTMLLINK
.
          MOVE      SP70,DESCMTYP
          MOVE      SP70,DESCSCOD
.
          MOVE      SP70,DESCCTYP
          MOVE      SP70,MTYPSRVC
.
          MATCH     "1",PMSMCONT
          IF        @EQUAL
            MATCH     SP70,PMSMCTYP
            IF        !@EQUAL
              PACK      KEY5,CATTC,PMSMCTYP,SP70
              CALL      RDCODE1
              IF        OVRCD=1
                MOVE      PMSMCTYP,TDESC
              ENDIF
              MOVE      TDESC,DESCCTYP
            ENDIF
          ENDIF
.
          MATCH     " 0",PMSMMTYP
          IF        @EQUAL
            MOVE      "Booking",DESCMTYP
            PACK      KEY6,PMSMSCOD,SP1,SP1,ZERO,SP70
            CALL      BRDLET1
            IF        OVRCD<>1
              MOVE      BLTEXT,DESCSCOD
            ENDIF
.
HOSE1010    MOVE      SP8,KEY8
            UNPACK    PMSMRKEY,KEY8
            CALL      RDBKREC1
            BRANCH    OVRCD,HOSE1100
.
            PACK      KEY5,ANSB,ANSK,BKRETYPE
            CALL      RDCODE1
            IF        OVRCD=0
              MATCH     SP70,FILTBOKI
              IF        !@EQUAL
                MATCH     "THE",FILTBOKI
                IF        @EQUAL
                  MATCH     SP70,FILTSTOU
                  IF        !@EQUAL
                    PACK      KEY31,DBKREBOO,TWO,BKREADAT,SP70
                    CALL      RSOPDEA2
HOSE1015            CALL      RKOPDEA2
                    BRANCH    OVRCD,HOSE1000
.
                    MATCH     BKREADOC,OPDACLIN
                    IF        !@EQUAL
                      GOTO      HOSE1015
                    ENDIF
.
                    MATCH     FILTSTOU,OPDAUNIT
                    GOTO      HOSE1000 IF NOT EQUAL
                  ELSE
                    MATCH     BKRETYPE,FILTBOKI
                    GOTO      HOSE1000 IF NOT EQUAL
                  ENDIF
                ELSE
                  MATCH     BKRETYPE,FILTBOKI
                  GOTO      HOSE1000 IF NOT EQUAL
                ENDIF
              ENDIF
.
              APPEND    DESCMTYP,MTYPSRVC
              APPEND    " / ",MTYPSRVC
              APPEND    TDESC,MTYPSRVC
              RESET     MTYPSRVC
            ENDIF
          ENDIF
.
          MATCH     " 2",PMSMMTYP
          IF        @EQUAL
            MOVE      "Outpatient",DESCMTYP
            PACK      KEY6,PMSMSCOD,SP1,SP1,ZERO,SP70
            CALL      RDOTLET1
            IF        OVRCD<>1
              MOVE      OTLTTEXT,DESCSCOD
            ENDIF
.
HOSE1020    MOVE      SP8,DIM8
            UNPACK    PMSMRKEY,DIM8
            PACK      KEY36,DIM8,SP70     * Outpatient Visit Number
            CALL      RDSBOKA6
            CALL      RDKBOKA6
            BRANCH    OVRCD,HOSE1100
.
            MATCH     DIM8,DOBAOUTN
            GOTO      HOSE1100 IF NOT EQUAL
.
            MATCH     PMSMURNO,OBAURNO
            GOTO      HOSE1100 IF NOT EQUAL
.
            PACK      KEY12,OBASITE,OBACTYP,SP70
            CALL      RDCTYA1             * To get Clinic Type Description
            IF        OVRCD=0
              APPEND    DESCMTYP,MTYPSRVC
              APPEND    " / ",MTYPSRVC
              APPEND    OCTDESC,MTYPSRVC
              RESET     MTYPSRVC
            ENDIF
          ENDIF
.
          MATCH     SP70,FILTCLTP           * Clinic Type Filter
          IF        !@EQUAL
            MATCH     OBACTYP,FILTCLTP
            GOTO      HOSE1000 IF NOT EQUAL
          ENDIF   
.
          MATCH     " 3",PMSMMTYP
          IF        @EQUAL
            MOVE      "Inpatient",DESCMTYP
            PACK      KEY6,PMSMSCOD,SP1,SP1,ZERO,SP70
            CALL      RDPTLET1
            IF        OVRCD<>1
              MOVE      PTLETEXT,DESCSCOD
            ENDIF
.
HOSE1030    MOVE      SP8,DIM8
            UNPACK    PMSMRKEY,DIM8
            PACK      KEY8,DIM8,SP70     * Inpatient Admission Number
            CALL      RDPTMIS1
            BRANCH    OVRCD,HOSE1100
.
            PACK      KEY5,ANSA,SP1,ATYPE
            CALL      RDCODE1
            IF        OVRCD=0
              APPEND    DESCMTYP,MTYPSRVC
              APPEND    " / ",MTYPSRVC
              APPEND    TDESC,MTYPSRVC
              RESET     MTYPSRVC
            ENDIF
          ENDIF
.
          MATCH     SP70,FILTADTP           * Admission Type Filter
          IF        !@EQUAL
            MATCH     ATYPE,FILTADTP
            GOTO      HOSE1000 IF NOT EQUAL
          ENDIF
.
          MATCH     " 9",PMSMMTYP
          IF        @EQUAL
            MOVE      "Waiting List",DESCMTYP
            PACK      KEY6,PMSMSCOD,SP1,SP1,ZERO,SP70
            CALL      RDLET1
            IF        OVRCD<>1
              MOVE      WLTEXT,DESCSCOD
            ENDIF
.
HOSE1090    PACK      KEY19,PMSMURNO,SP70
            CALL      RDSTREA1
            CALL      RDKTREA1
            BRANCH    OVRCD,HOSE1100
.
            MATCH     PMSMURNO,WMURNO
            GOTO      HOSE1100 IF NOT EQUAL
.
            PACK      KEY5,ANSW,ANSU,WMUNIT,SP70
            CALL      RDCODE1
            IF        OVRCD=0
              APPEND    DESCMTYP,MTYPSRVC
              APPEND    " / ",MTYPSRVC
              APPEND    TDESC,MTYPSRVC
              RESET     MTYPSRVC
            ENDIF
          ENDIF
.
          MATCH     SP70,FILTWLUT           * Waiting list unit filter
          IF        !@EQUAL
            MATCH     WMUNIT,FILTWLUT
            GOTO      HOSE1000 IF NOT EQUAL
          ENDIF  
.
HOSE1100  MOVE      SP70,DESCSTAT
.
          MATCH     " 0",PMRRSTAT
          IF        @EQUAL
            MOVE      "Response Message",DESCSTAT
          ENDIF
.
          MATCH     " 1",PMRRSTAT
          IF        @EQUAL
            MOVE      "Awaiting Delivery",DESCSTAT
          ENDIF
.
          MATCH     " 2",PMRRSTAT
          IF        @EQUAL
            MOVE      "Successful Delivery",DESCSTAT
          ENDIF
.
          PACK      HTMLLNK2,SP70,SP70
          MATCH     " 3",PMRRSTAT
          IF        @EQUAL
            MOVE      "Delivery Failure",DESCSTAT
.
            REP       " +",PMRRUMID
            REP       " +",PMRRUCNT
            CLEAR     HTMLLNK2
            APPEND    "<input type=button value='Resend' ",HTMLLNK2
            APPEND    "class=button onclick=reSendSMS('",HTMLLNK2
            APPEND    PMRRUMID,HTMLLNK2
            APPEND    "','",HTMLLNK2
            APPEND    PMRRUCNT,HTMLLNK2
            APPEND    "')>",HTMLLNK2
            RESET     HTMLLNK2
            REP       "+ ",PMRRUMID
            REP       "+ ",PMRRUCNT
          ENDIF
.
          MATCH     " 4",PMRRSTAT
          IF        @EQUAL
            MOVE      "Error Processed",DESCSTAT
          ENDIF
.
HOSE2000  WRITE     HTMLFILE;"t.addRow(#"",*+,HTMLLINK,*-,"#",":     * 0
                             "#"",PMSMUMID,"#",":                    * 1
                             "#"",PMSMURNO,"#",":                    * 2
                             "#"",PMSMMTYP,"#",":                    * 3
                             "#"",PMSMRKEY,"#",":                    * 4
                             "#"",PMSMSCOD,"#",":                    * 5
                             "#"",PMSMSDAT,PMSMSTIM,"#",":           * 6
                             "#"",PMSMSDAT,"#",":                    * 7
                             "#"",PMSMSTIM,"#",":                    * 8
                             "#"",PMSMMNUM,"#",":                    * 9
                             "#"",PMSMSUID,"#",":                    * 10
                             "#"",PMSMMESA;                          * 11
.
          WRITE     HTMLFILE;PMSMMESB;           
.
          WRITE     HTMLFILE;PMSMMESC;          
.
          WRITE     HTMLFILE;PMSMMESD;         
.
          WRITE     HTMLFILE;PMSMMESE;          
.
          WRITE     HTMLFILE;PMSMMESF;          
.
          WRITE     HTMLFILE;"#",":
                             "#"",PMSMSTAT,"#",":                    * 12
                             "#"",MTYPSRVC,"#",#"";                  * 13
.
          STRIP     PMRRRESA
          STRIP     PMRRRESB
          STRIP     PMRRRESC
          STRIP     PMRRRESD
          STRIP     PMRRRESE
          STRIP     PMRRRESF
.
          WRITE     HTMLFILE;*+,PMRRRESA,*-;                         * 14
.
          WRITE     HTMLFILE;*+,PMRRRESB,*-;
.
          WRITE     HTMLFILE;*+,PMRRRESC,*-;
.
          WRITE     HTMLFILE;*+,PMRRRESD,*-;
.
          WRITE     HTMLFILE;*+,PMRRRESE,*-;
.
          WRITE     HTMLFILE;*+,PMRRRESF,*-;
.
          WRITE     HTMLFILE;"#",":
                             "#"",DESCSCOD,"#",":                    * 15
                             "#"",DESCSTAT,"#",":                    * 16
                             "#"",FORMATNM,"#",":                    * 17
                             "#"",FOLDERSF,"#",":                    * 18
                             "#"",PMRRRDTE,PMRRRTIM,"#",":           * 19
                             "#"",DESCSTAT,HTMLLNK2,"#",":           * 20
                             "#"",DESCCTYP,"#",":                    * 21
                             "#"",HOSPWARD,"#");"                    * 22
.
          GOTO      HOSE1000
.
HOSE9999  RETURN
+
.------------------------------------------------------------
.         Hopsital level SMS functionality
.------------------------------------------------------------
RESEND00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      RESEND70 IF EQUAL
.
          MATCH     "U",UPDTTYPE            * Update SMS formaction
          GOTO      RESEND10 IF EQUAL
.
          MATCH     "R",UPDTTYPE            * Resend SMS formaction
          GOTO      RESEND20 IF EQUAL
.
          MATCH     "B",UPDTTYPE            * To be Bulk Resent formaction
          GOTO      RESEND20 IF EQUAL
.
          GOTO      RESEND90
.
.         ************ UPDATE FORMACTION **********
.
RESEND10  PACK      KEY16,MESSAGID,SP70
          CALL      RDPMSMH1
          BRANCH    OVRCD,RESEND91
.
          CALL      MVPMRM00                * Move cgi to file vars
.
          CALL      UPPMSMH1
.
          MOVE      ZERO,EXIT
          GOTO      RESEND99
.
.         ************ RESEND / BULK RESENT FORMACTION **********
.
RESEND20  PACK      KEY16,MESSAGID,SP70
          CALL      RDPMSMH1                 * Read original message
          BRANCH    OVRCD,RESEND91
.
          MOVE      ZERO,F1
          MOVE      PMSMCONT,F1
          BRANCH    F1,RESEND21             * Process Contact Type number
.
.         Process Patient number...
          PACK      KEY8,PMSMURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,RESEND92
.
          MATCH     "1",PTMXOSMS            * patient Opted Out of SMS?
          GOTO      RESEND60 IF EQUAL       * YES - finish
.
          MATCH     SP20,PTMXCELL           * Blank mobile number?
          GOTO      RESEND96 IF EQUAL       * Error
          GOTO      RESEND30
.
.         Process Contact Type number...
RESEND21  MOVE      PMSMURNO,EXTCURNO
          MOVE      PMSMCTYP,EXTCCTYP
          CALL      GETEC000                * Get the Contact Type mobile number
          BRANCH    EXIT,RESEN991,RESEN992
.
          MATCH     SP20,EXTCMOBN           * Blank mobile number?
          GOTO      RESEN993 IF EQUAL       * Error
.
RESEND30  CALL      GSMSID00                * Get new SMS message id
          MOVE      IBSQNEXT,NEWMESID
          MOVE      NEWMESID,PMSMUMID
.
          PACK      KEY16,NEWMESID,SP70
          CALL      RAPMSMH1
          IF        OVRCD=1
            MOVE      USERID,PMSMSUID        
            MOVE      SP70,PMSMSDAT
            MOVE      SP70,PMSMSTIM
            MOVE      SP70,PMSMRMID
.
            MATCH     "B",UPDTTYPE
            IF        @EQUAL
              MOVE      " 5",PMSMSTAT       * Error corrected (Bulk resend)
            ELSE
              MOVE      " 4",PMSMSTAT       * Waiting status             
            ENDIF
.
            MOVE      ZERO,F1
            MOVE      PMSMCONT,F1           * Contact Type
            IF        F1=1
              MOVE      EXTCMOBN,PMSMMNUM   * use Contact Type mobile number
            ELSE
              MOVE      PTMXCELL,PMSMMNUM   * use updated Patient number
            ENDIF
.
            CALL      WRPMSMH1              * Write new message id
          ELSE
            GOTO      RESEND94
          ENDIF
.
          PACK      KEY16,MESSAGID,SP70
          CALL      RDPMSMH1
          IF        OVRCD=0
            MOVE      NEWMESID,PMSMRMID      * Resent message id
            MOVE      " 3",PMSMSTAT          * Update orginal message to re-sent
            CALL      UPPMSMH1
          ENDIF
.
          MATCH     "B",UPDTTYPE             * Error Corrected to be Bulk Resend
          GOTO      RESEND60 IF EQUAL
.
          PACK      KEY16,NEWMESID,SP70
          CALL      RDPMSMH1                 * Re-read new message to send
          BRANCH    OVRCD,RESEND91
.
          MOVE      SP70,WBSEHOSP
          MOVE      PTCNSMSP,SELPRINT
          MOVE      ONE,NOCOPIES
          CALL      OPNPRINT
          PRINT     *1,"<messages>"
          PRINT     *1,"<sms_message>"                        * SMS message
          PRINT     *1,"<resend>1</resend>"                   * Resend sms
          PRINT     *1,"<msg_id>",*+,PMSMUMID,*-,"</msg_id>"  * Message Id
          PRINT     *1,"<msg_code>",PMSMSCOD,"</msg_code>"    * SMS code
          PRINT     *1,"<key>",PMSMRKEY,"</key>"              * SMS key
          PRINT     *1,"<phone_no>",*+,PMSMMNUM,*-,"</phone_no>"  * Phone Number
          PRINT     *1,"<urno>",PMSMURNO,"</urno>"            * UR No
          PRINT     *1,"<user_id>",PMSMSUID,"</user_id>"      * User ID
          PRINT     *1,"<other_contacts>0</other_contacts>"  * No other contacts
          PRINT     *1,"<text><![CDATA["
          PRINT     *1,PMSMMESA,PMSMMESB,PMSMMESC,PMSMMESD:   * Message text
                       PMSMMESE,PMSMMESF                  
          PRINT     *1,"]]></text>" 
          PRINT     *1,"</sms_message>"                
          PRINT     *1,"</messages>"
.
          CALL      CLSPRINT
.
RESEND60  MOVE      ZERO,EXIT
          GOTO      RESEND99
.
.         ************ DISPLAY FORMACTION **********
.
RESEND70  CALL      CURPER00   * Determine Current Period
          CALL      CALCDT00   * Calculate Next and Last Period Dates
.
.         PACK      KEY18,ERRORKEY,SP70
.         CALL      RDPMSRR1
.         BRANCH    OVRCD,RESEND93
.
          PACK      KEY16,MESSAGID,SP70
          CALL      RDPMSMH1
          BRANCH    OVRCD,RESEND91
.
          PACK      KEY8,PMSMURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,RESEND92
.
          PACK      KEY8,PMSMURNO,SP70
          CALL      RDPMPX21
          BRANCH    OVRCD,RESEND92
.
          CLEAR     WEBTITLE
          MOVE      "SMS Messages",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,BULKRM99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      RESEND99
.
RESEND90  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      RESEND99
.
RESEND91  MOVE      "Invalid SMS Message ID.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      RESEND99
.
RESEND92  CLEAR     WEBTITLE
          APPEND    "Invalid U/R Number - ",WEBTITLE
          APPEND    URNUMBER,WEBTITLE
          RESET     WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      RESEND99
.
RESEND93  MOVE      "Invalid SMS Response ID.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      RESEND99
.
RESEND94  MOVE      "Error generating message ID - Please try again.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      RESEND99
.
RESEND95  MOVE      "Invalid SMS Error Record.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      RESEND99
.
RESEND96  MOVE      "Cannot re-send message - there is no mobile number recorded for this patient.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      RESEND99
.
RESEN991  MOVE      "Cannot re-send message - the Contact does not exist.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      RESEND99
.
RESEN992  MOVE      "Cannot re-send message - the Contact has not Opted-In to 'Send SMS'.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      RESEND99
.
RESEN993  MOVE      "Cannot re-send message - the Contact does not have a mobile number recorded.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      RESEND99
.
RESEND99  RETURN
+
.------------------------------------------------------------------------------
. Hospital level SMS message history list
.------------------------------------------------------------------------------
HSMT0000  CALL      ALHSPA00 
          PACK      KEY32,FRSTDATE,SP70
          CALL      RSPMSMH3
HSMT1000  CALL      RKPMSMH3
          BRANCH    OVRCD,HSMT9999
.
          MATCH     PMSMSDAT,LASTDATE       * Checks lastdate 
          GOTO      HSMT9999 IF LESS
.
          MATCH     SP70,FILTURNO
          IF        !@EQUAL
            MATCH     FILTURNO,PMSMURNO
            GOTO      HSMT1000 IF NOT EQUAL
          ENDIF
.
          PACK      KEY8,PMSMURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,HSMT1000
.
          PACK      KEY8,PMSMURNO,SP70
          CALL      RDPMPX21
          BRANCH    OVRCD,HSMT1000
.
          CALL      HOSPDT00
          CALL      WARDDT00
.
. If mutl-hospital, user must have all hospital access or have access to
. the sms hospital
.
          IF        IBCNMHOS = 1
            MATCH     SP70,FILTHOSP           * Hospital Filter
            IF        @EQUAL
              IF        ALLHSPAC = ZERO
                PACK      KEY13,USERID,SMSHOSPC,SP70  * This hospital access
                CALL      RDMLSEC1
                BRANCH    OVRCD,HSMT1000      * no access to hospital
              ENDIF
            ELSE
              MATCH     SMSHOSPC,FILTHOSP
              GOTO      HSMT1000 IF NOT EQUAL
            ENDIF
            STRIP     SMSHOSPN
            RESET     SMSHOSPN
            PACK      HOSPWARD,SMSHOSPN,SP1,SLASH,SP1,SMSWARDC
          ELSE
            MOVE      SMSWARDC,HOSPWARD
          ENDIF
.
          MATCH     SP70,FILTWARD           * Ward Filter
          IF        !@EQUAL
            MATCH     SMSWARDC,FILTWARD
            GOTO      HSMT1000 IF NOT EQUAL
          ENDIF
.
          PACK      AGEDATE,CCC,CYY,CMM,CDD    * Today's date
          REP       " 0",AGEDATE
          CALL      CALCAGE
          CALL      PATLN000
.
          CALL      PATFLD00                * Set Patient Folder suffix
          MOVE      KEY3,FOLDERSF           * Save returned value
.
          CLEAR     HTMLLINK
          APPEND    "javascript:linkPatient('",HTMLLINK
          APPEND    PMSMURNO,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    PMSMMTYP,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    PMSMRKEY,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
          STRIP     HTMLLINK
.
          MOVE      SP70,DESCMTYP
          MOVE      SP70,DESCSCOD
          MOVE      SP70,MTYPSRVC
.
          MATCH     " 0",PMSMMTYP
          IF        @EQUAL
            MOVE      "Booking",DESCMTYP
            PACK      KEY6,PMSMSCOD,SP1,SP1,ZERO,SP70
            CALL      BRDLET1
            IF        OVRCD<>1
              MOVE      BLTEXT,DESCSCOD
            ENDIF
.
HSMT1010    MOVE      SP8,KEY8
            UNPACK    PMSMRKEY,KEY8
            CALL      RDBKREC1
            BRANCH    OVRCD,HSMT1600
.
            PACK      KEY5,ANSB,ANSK,BKRETYPE
            CALL      RDCODE1
            IF        OVRCD=0
              APPEND    DESCMTYP,MTYPSRVC
              APPEND    " / ",MTYPSRVC
              APPEND    TDESC,MTYPSRVC
              RESET     MTYPSRVC
            ENDIF
          ENDIF
.
          MATCH     " 2",PMSMMTYP
          IF        @EQUAL
            MOVE      "Outpatient",DESCMTYP
            PACK      KEY6,PMSMSCOD,SP1,SP1,ZERO,SP70
            CALL      RDOTLET1
            IF        OVRCD<>1
              MOVE      OTLTTEXT,DESCSCOD
            ENDIF
.
HSMT1020    MOVE      SP8,DIM8
            UNPACK    PMSMRKEY,DIM8
            PACK      KEY36,DIM8,SP70     * Outpatient Visit Number
            CALL      RDSBOKA6
            CALL      RDKBOKA6
            BRANCH    OVRCD,HSMT1600
.
            MATCH     DIM8,DOBAOUTN
            GOTO      HSMT1600 IF NOT EQUAL
.
            MATCH     PMSMURNO,OBAURNO
            GOTO      HSMT1600 IF NOT EQUAL
.
            PACK      KEY12,OBASITE,OBACTYP,SP70
            CALL      RDCTYA1             * To get Clinic Type Description
            IF        OVRCD=0
              APPEND    DESCMTYP,MTYPSRVC
              APPEND    " / ",MTYPSRVC
              APPEND    OCTDESC,MTYPSRVC
              RESET     MTYPSRVC
            ENDIF
          ENDIF
.
          MATCH     " 3",PMSMMTYP
          IF        @EQUAL
            MOVE      "Inpatient",DESCMTYP
            PACK      KEY6,PMSMSCOD,SP1,SP1,ZERO,SP70
            CALL      RDPTLET1
            IF        OVRCD<>1
              MOVE      PTLETEXT,DESCSCOD
            ENDIF
.
HSMT1030    MOVE      SP8,DIM8
            UNPACK    PMSMRKEY,DIM8
            PACK      KEY8,DIM8,SP70     * Inpatient Admission Number
            CALL      RDPTMIS1
            BRANCH    OVRCD,HSMT1600
.
            PACK      KEY5,ANSA,SP1,ATYPE
            CALL      RDCODE1
            IF        OVRCD=0
              APPEND    DESCMTYP,MTYPSRVC
              APPEND    " / ",MTYPSRVC
              APPEND    TDESC,MTYPSRVC
              RESET     MTYPSRVC
            ENDIF
          ENDIF
.
          MATCH     " 9",PMSMMTYP
          IF        @EQUAL
            MOVE      "Waiting List",DESCMTYP
            PACK      KEY6,PMSMSCOD,SP1,SP1,ZERO,SP70
            CALL      RDLET1
            IF        OVRCD<>1
              MOVE      WLTEXT,DESCSCOD
            ENDIF
.
HSMT1090    PACK      KEY19,PMSMURNO,SP70
            CALL      RDSTREA1
            CALL      RDKTREA1
            BRANCH    OVRCD,HSMT1600
.
            MATCH     PMSMURNO,WMURNO
            GOTO      HSMT1600 IF NOT EQUAL
.
            PACK      KEY5,ANSW,ANSU,WMUNIT,SP70
            CALL      RDCODE1
            IF        OVRCD=0
              APPEND    DESCMTYP,MTYPSRVC
              APPEND    " / ",MTYPSRVC
              APPEND    TDESC,MTYPSRVC
              RESET     MTYPSRVC
            ENDIF
          ENDIF
.
HSMT1600  MOVE      SP70,DESCSTAT
.
          MATCH     " 0",PMSMSTAT
          IF        @EQUAL
            MOVE      "Sent",DESCSTAT
          ENDIF
.
          MATCH     " 1",PMSMSTAT
          IF        @EQUAL
            MOVE      "Response Received",DESCSTAT
            CALL      RPDTTM00
            PACK      DESCSTAT,DESCSTAT,NLTEXT,RSDATTIM
          ENDIF
.
          PACK      HTMLLNK2,SP70,SP70
.
          MOVE      ZERO,F1
          MOVE      PMSMSTAT,F1
          IF        F1=2 | F1=6
            IF        F1=2
              MOVE      "Error",DESCSTAT
            ENDIF
.
            IF        F1=6
              MOVE      "No Mobile Number",DESCSTAT
            ENDIF
.
            REP       " +",PMSMUMID
            CLEAR     HTMLLNK2
            APPEND    "<input type=button value='Resend' ",HTMLLNK2
            APPEND    "class=button onclick=reSendSMS('",HTMLLNK2
            APPEND    PMSMUMID,HTMLLNK2
            APPEND    "')>",HTMLLNK2
            RESET     HTMLLNK2
            REP       "+ ",PMSMUMID
          ENDIF
.
          MATCH     " 3",PMSMSTAT
          IF        @EQUAL
            MOVE      "Re-Sent",DESCSTAT
          ENDIF
.
          MATCH     " 4",PMSMSTAT
          IF        @EQUAL
            MOVE      "Waiting to be sent",DESCSTAT
          ENDIF
.
          MATCH     " 5",PMSMSTAT
          IF        @EQUAL
            MOVE      "Error Corrected",DESCSTAT
          ENDIF
.
HSMT2000  WRITE     HTMLFILE;"t.addRow(#"",*+,HTMLLINK,*-,"#",":     * 0
                             "#"",PMSMUMID,"#",":                    * 1
                             "#"",PMSMURNO,"#",":                    * 2
                             "#"",PMSMMTYP,"#",":                    * 3
                             "#"",PMSMRKEY,"#",":                    * 4
                             "#"",PMSMSCOD,"#",":                    * 5
                             "#"",PMSMSDAT,PMSMSTIM,"#",":           * 6
                             "#"",PMSMSDAT,"#",":                    * 7
                             "#"",PMSMSTIM,"#",":                    * 8
                             "#"",PMSMMNUM,"#",":                    * 9
                             "#"",PMSMSUID,"#",":                    * 10
                             "#"",PMSMMESA;                          * 11
.
          WRITE     HTMLFILE;PMSMMESB;
.
          WRITE     HTMLFILE;PMSMMESC;
.
          WRITE     HTMLFILE;PMSMMESD;
.
          WRITE     HTMLFILE;PMSMMESE;
.
          WRITE     HTMLFILE;PMSMMESF;
.
          WRITE     HTMLFILE;"#",":
                             "#"",PMSMSTAT,"#",":                    * 12
                             "#"",MTYPSRVC,"#",#"";                  * 13
.
          PACK      KEY18,PMSMUMID,Z70
          CALL      RSPMSRR1                * Get responses
HSMT3000  CALL      RPPMSRR1
          BRANCH    OVRCD,HSMT5000                                   * 14
.
          MATCH     PMSMUMID,PMRRUMID
          GOTO      HSMT5000 IF NOT EQUAL
.
          MATCH     "1",PMRRMAPD                 * Mapped response
          IF        @EQUAL
            WRITE     HTMLFILE;"<b>";
          ENDIF
.
          STRIP     PMRRRESA
          STRIP     PMRRRESB
          STRIP     PMRRRESC
          STRIP     PMRRRESD
          STRIP     PMRRRESE
          STRIP     PMRRRESF
.
          WRITE     HTMLFILE;*+,PMRRRESA,*-;
.
          WRITE     HTMLFILE;*+,PMRRRESB,*-;
.
          WRITE     HTMLFILE;*+,PMRRRESC,*-;
.
          WRITE     HTMLFILE;*+,PMRRRESD,*-;
.
          WRITE     HTMLFILE;*+,PMRRRESE,*-;
.
          WRITE     HTMLFILE;*+,PMRRRESF,*-;
.
          MATCH     "1",PMRRMAPD                 * Mapped response
          IF        @EQUAL
            WRITE     HTMLFILE;"</b>";
          ENDIF
.
          WRITE     HTMLFILE;"<br>";
.
          GOTO      HSMT3000
.
HSMT5000  MOVE      SP70,DESCCTYP
.
          MATCH     "1",PMSMCONT
          IF        @EQUAL
            MATCH     SP70,PMSMCTYP
            IF        !@EQUAL
              PACK      KEY5,CATTC,PMSMCTYP,SP70
              CALL      RDCODE1
              IF        OVRCD=1
                MOVE      PMSMCTYP,TDESC
              ENDIF
              MOVE      TDESC,DESCCTYP
            ENDIF
          ENDIF
.
          STRIP     DESCSCOD
          STRIP     DESCSTAT
.
          WRITE     HTMLFILE;"#",":
                             "#"",*+,DESCSCOD,*-,"#",":              * 15
                             "#"",*+,DESCSTAT,*-,"#",":              * 16
                             "#"",DESCSTAT,HTMLLNK2,"#",":           * 17
                             "#"",FORMATNM,"#",":                    * 18
                             "#"",FOLDERSF,"#",":                    * 19
                             "#"",DESCCTYP,"#",":                    * 20 
                             "#"",HOSPWARD,"#");"                    * 21
.
          GOTO      HSMT1000
.
HSMT9999  RETURN
.
.------------------------------------------------------------
. NOMORE00 - Display "No more records" at end of list
.------------------------------------------------------------
NOMORE00  WRITE     HTMLFILE;"<tr>":
                             "<td class=RedCell colspan=20 align=center>":
                             "No more records</td>"
.
NOMORE99  RETURN
+
.------------------------------------------------------------
. BOKLETIO table reads (that don't interfere)
.------------------------------------------------------------
BRDLET1  MOVE     ZERO,OVRCD
         RESET    KEY6
         READ     BOKLETR1,KEY6;DBLETNO,DBLINNO,BLTEXT,BLVAR,BLETPBOT,BLETPTOP:
                           BLETPPAG,BLETPTAB,BLETACTV,BLETCOMM,BLETSPAR
         GOTO     OVERCOND IF OVER
         MOVE     DBLETNO,BLETNO
         MOVE     DBLINNO,BLINNO
         RETURN
+
.------------------------------------------------------------------------------
. GETEC000 - Get Extra Contact mobile number for the matching Contact Type
.
.   PARAMS:
.           EXTCURNO - UR Number
.           EXTCCTYP - Contact Type (Cat tc)
.           
.   RETURN:
.           EXTCMOBN - Mobile Number
.
.   EXIT:
.           0 - Matching Contact Type
.           1 - No matching Contact Type
.           2 - 'Send SMS' not set to 1 (either 0 or blank)
.------------------------------------------------------------------------------
GETEC000  MOVE      ZERO,EXIT
          MOVE      SP70,EXTCMOBN
.
          PACK      KEY14,EXTCURNO,SP20
          CALL      RSPMCEX1
GETEC010  CALL      RKPMCEX1
          BRANCH    OVRCD,GETEC910
.
          MATCH     PMCEURNO,EXTCURNO
          GOTO      GETEC910 IF NOT EQUAL
.
          MATCH     PMCETYPE,EXTCCTYP
          IF        @EQUAL
            MATCH     "1",PMCESSMS          * Send SMS?
            GOTO      GETEC920 IF NOT EQUAL
            GOTO      GETEC900
          ENDIF
.
          GOTO      GETEC010
.
GETEC900  MOVE      PMCETELM,EXTCMOBN
          GOTO      GETEC999
.
GETEC910  MOVE      ONE,EXIT
          GOTO      GETEC999
.
GETEC920  MOVE      TWO,EXIT
          GOTO      GETEC999
.
GETEC999  RETURN
+
.------------------------------------------------------------
          INC       IBAWEBCD
          INC       WEBDATCD
          INC       CALCAGE
          INC       DAYOFWEK
          INC       NAMSTRCD
          INC       CLPMSPX2
          INC       CLPMSSRR
          INC       CLNHIMAS
          INC       CLPATHSP
          INC       PATNAMCD
          INC       PATDOCTM
          INC       PATDOCCD
          INC       PATMASTM
          INC       PATMSXTM
          INC       PMSHCGTM
          INC       PMSHCPTM
          INC       PMSSRRTM
          INC       PMSSMHTM
          INC       PMSPX2TM
          INC       SMSMESID 
.
          INC       CLPMSSRM
.
          INC       APSMASIO/INC       * Creditor/Supplier Master File
          INC       MRTMASIO/INC       * MRT Master Details
          INC       NHIETHIO/INC       * NHI Ethnicity Codes
          INC       NHIMASIO/INC       * NHI/MWS HCU Details
          INC       OUTCTYIO/INC       * Outpatients Clinic Type
          INC       OUTBOAIO/INC       * Outpatients Booking  
          INC       OUTSITIO/INC       * Outpatients Site Code
          INC       OUTLETIO/INC       * O/P letters
          INC       PATALRIO/INC       * Patient Alerts
          INC       PMSUCHIO/INC
          INC       PMSUCDIO/INC
          INC       PATDO1IO/INC       * Doctor File
          INC       PATFN1IO/INC       * Patient Health Fund
          INC       PATHSPIO/INC       * Hospital Details
          INC       PATIN1IO/INC       * Insurance Details
          INC       PATILTIO/INC       * I/P Letters
          INC       PATMA1IO/INC       * Patient Master File
          INC       PATMI1IO/INC       * Patient Admissions File
          INC       PATVISIO/INC       * Patient Visit File
          INC       PMSVX1IO/INC       * Patient Visit Extension File
          INC       PATWR1IO/INC       * Patient Ward/Bed Details
          INC       PATTRNIO/INC       
.
          INC       PMSCCDIO/INC       * Concession Card Details
          INC       PMSHCGIO/INC       * HCP Practice
          INC       PMSHCPIO/INC       * Healthcare Profession
          INC       PMSPX2IO/INC       * PMI extension table
          INC       BOKRC1IO/INC
          INC       OPRDEAIO/INC
.
          INC       PMSCEXIO/INC       * Extra Contact Details
          INC       PMSSMHIO/INC       * SMS History
          INC       PMSSRMIO/INC       * SMS Response Mapping
          INC       PMSSRRIO/INC       * SMS Responses Received
          INC       WATLETIO/INC       * W/L Letters
          INC       MLTSECIO/INC
          INC       WATTR1IO/INC
.

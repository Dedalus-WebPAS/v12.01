.******************************************************************************
.* System         : Operating Rooms         Release 5.01                      *
.* Program        : IBAOPR60.PBL                                              *
.* Description    : Session Usage Report                                      *
.******************************************************************************
.* Date           : 03/01/1991                                                *
.* Author         : Justin Coates                                             *
.* Function       : To print the session usage statistics                     *
.* Modifications  :                                                           *
.*        V10.04.01 15/04/2014  J.Tan          CAR 261630                     *
.*                  Removed port number from temp file name                   *
.*        V10.03.01 19/06/2012  Mike Laming    CAR 257005                     *
.*                  Changed all "Time" variables to "FORM 7", Mod print layout*
.*        V9.09.01  15/04/2008  Peter Vela       166259                       *
.*                  Added number of cases to report layout                    *
.*        V9.07.01  20/09/2006  Peter Vela       118228                       *
.*                  Added Reallocated Sessions to report                      *
.*        V9.04.02  12/09/2005  Peter Vela       71724                        *
.*                  Fixed layout 3 - Total Session Time Used should not       *
.*                  include overtime
.*        V9.04.01  11/08/2005  Sandra Barcham   71724                        *
.*                  Fixed layout 3 - Total Session Time Used should not       *
.*                  include cancellations                                     *
.*        V5.07.01  23/09/1999  Andrew Peel SRF  634279                       *
.*                  Fixed layout 3 - no. sessions & total session time        *
.*        V5.04.01  26/06/1997  Howellsy         505                          *
.*                  Use PATDOCKY instead of PATDOCDS                          *
.*        V5.01.01  06/02/91  ROD                                             *
.*                  Changed No. of sessions & Total session time to include   *
.*                  Extra sessions.                                           *
.*        V5.01.002 30/12/1991    Justin Coates  (Modbury changes)            *
.*                  Added report layout 3 - uses temp file D                  *
.*        V5.01.003 Mods report layout 3 - other patients to include private  *
.*                  & public whose att.doctor not equal to theatre session doc*
.*        V5.01.004 18/06/92   ROD                                            *
.*                  display /print date for period                            *
.*        V5.01.005 29/09/92 J.Tan    SRF 116885                              *
.*                  Mods not to include cancelled session (oprcsc) to number  *
.*                  of sessions avail. and session time avail.                *
.*        V5.01.006 27/10/92 J.Tan  SRF 118267                                *
.*                  Fixed private patients columns                            *
.*        V5.01.007 17/12/92 J.Tan                                            *
.*                  Fixed I*A on the tempfile                                 *
.*        V5.01.008 04/06/1993  Glenn Berry      SRF 120247                   *
.*                  Changed page breaks to line 60                            *
.*        V5.01.09  12/10/93    Whirlpool        SRF 600476                   *
.*                  Fixed printing of clinic on page throw rep layout 1       *
.*        V5.01.10  24/05/1995  Matt Surridge                                 *
.*                  Fixed bugs for global recompile.                          *
.******************************************************************************
.
.$$$$$
.         IBAOPR60.PBL        Session Usage Report
.         ============        ====================
.         INIT0000 - Initialization
.         OPTN0000 - see if to produce report
.         KPER0000 - enter the date range
.         KCOS0000 - Enter the clinic or surgeon range
.         REPT0000 - produce the report
.             PAGE0000 - prints page heading and table heading
.             LCOU0000 - loop thru OPRCOUFD and write to temp files
.             LCSU0000 - loop thru OPRCSUFD and write to temp files
.             LCSC0000 - loop thru OPRCSCFD and write to temp files
.             print the main report details
.             SUMM0000 - print the summary details
.$$$$$
.
. ------- FD includes needed -------
.
          INC       STD001FD                * all fd's
          INC       IBASEQFD/INC            * Sequential Numbers File
          INC       TFILEVAR/INC            * Generate Temp File Name
          INC       WEBERRFD/INC            * Web Server Error Logging
.
          INC       OPRCLIFD/INC            * clinic file
          INC       OPRCONFD/INC            * control file
          INC       OPRCOUFD/INC            * clinic operation usage file
          INC       OPRCSCFD/INC            * clinic session cancellation file
          INC       OPRCSUFD/INC            * clinic session usage file
.
          INC       PATCODFD/INC            * codes file
          INC       PATCONFD/INC            * control file
          INC       PATDO1FD/INC            * doctor file
          INC       PATDRGFD/INC            * date range file
.
. ------- program variables -------
.
CFNAMEA   DIM       8         * file name for temp file A
CFNAMEB   DIM       8         * file name for temp file B
CFNAMEC   DIM       8         * file name for temp file C
CFNAMED   DIM       8         * file name for temp file D
CFNAMEZ   DIM       8         * file name for temp file Z
CMDLINE   DIM       50        * command line for 'execute' command
CPTDATE   DIM       8         * for PATCOMN3
CURRCLSU  DIM       6         * current clinic/surgeon
CURRDATE  DIM       8         * current date
.
DIM3      DIM       3         * patdsdoc
DIM7A     DIM       7         * from period   mm/ccyy
DIM7B     DIM       7         * to   period   mm/ccyy
DIM20A    DIM       20        * from period description
DIM20B    DIM       20        * to   period description
DOCCODE   DIM       6
CLSUCODE  DIM       6         * common clin/surg code used in keyin
CLSUNAME  DIM       40        * common clin/surg name used in keyin
..FORM6P5   FORM      6.5       * used for percenatage calculation
FORM7P5   FORM      7.5       * used for percenatage calculation
FORM5     FORM      5  
FORM6     FORM      6         * total variable for counts
FORM6B    FORM      6         * total variable
FORM7     FORM      7         * total variable for Times
FORM8     FORM      8         * total variable (bigger)
.
WNSES5    FORM      5         * work fields to change FORM6 to FORM5
WNOPR5    FORM      5
WOVER5    FORM      5
WDLAY5    FORM      5
WCANC5    FORM      5
WPCAS5    FORM      5
WOCAS5    FORM      5
WRPCAL    FORM      5
WOVNU5    FORM      5
WPRNU5    FORM      5
WOTNU5    FORM      5
.
PCENTA    FORM      4.1       * percentage value 
PCENTB    FORM      4.1       * percentage value 
PCENTC    FORM      4.1       * percentage value 
PCENTD    FORM      3.2       * percentage value 
PCENTE    FORM      3.2       * percentage value 
PCENTF    FORM      3.2       * percentage value 
PERDSTRT  DIM       6         * starting period/month
PERDSTOP  DIM       6         * ending period/month
PRFDATE   DIM       10
PRTDATE   DIM       10
SOVRCD    FORM      1
SAVEEND   DIM       6         * financial end period 
SAVEMON1  DIM       2         * start financial month
SAVEMON2  DIM       2         * end financial month
SAVEYR1   DIM       4         * start financial year
SAVEYR2   DIM       4         * end financial year
SCRNFLAG  FORM      1         * ? performed flag
STRTCOSC  DIM       6         * start clinic/surgeon code
STRTCOSD  DIM       40        * start clinic/surgeon description
STRTLDAT  DIM       8         * to   date of starting period
STRTHDAT  DIM       8         * from date of starting period
.
STOPCOSC  DIM       6         * end clinic/surgeon code
STOPCOSD  DIM       40        * end clinic/surgeon description
STOPLDAT  DIM       8         * to   date of ending period
STOPHDAT  DIM       8         * from date of ending period
.
.
TOTAL1    FORM      6         * total variable - by column
TOTAL2    FORM      6         * total variable
TOTAL3    FORM      6         * total variable
TOTAL4    FORM      6         * total variable
TOTAL5    FORM      6         * total variable
TOTAL6    FORM      6         * total variable
TOTAL7    FORM      6         * total variable
TOTAL8    FORM      6         * total variable
TOTAL9    FORM      6         * total variable
TOTAL10   FORM      6         * total variable
TOTAL11   FORM      6         * total variable
TOTAL12   FORM      6         * total variable
TOTAL13   FORM      6         * total variable
TOTAL14   FORM      6         * total variable
TOTAL15   FORM      6         * total variable
TOTAL16   FORM      6         * total variable
TOTAL17   FORM      6         * total variable
.
TOTTM1    FORM      7         * Total Time in minutes var - by column
TOTTM2    FORM      7         * Total Time in minutes var
TOTTM3    FORM      7         * Total Time in minutes var
TOTTM4    FORM      7         * Total Time in minutes var
TOTTM5    FORM      7         * Total Time in minutes var
TOTTM6    FORM      7         * Total Time in minutes var
TOTTM7    FORM      7         * Total Time in minutes var
TOTTM8    FORM      7         * Total Time in minutes var
TOTTM9    FORM      7         * Total Time in minutes var
TOTTM10   FORM      7         * Total Time in minutes var
TOTTM11   FORM      7         * Total Time in minutes var
TOTTM12   FORM      7         * Total Time in minutes var
TOTTM13   FORM      7         * Total Time in minutes var
TOTTM14   FORM      7         * Total Time in minutes var
TOTTM15   FORM      7         * Total Time in minutes var
TOTTM16   FORM      7         * Total Time in minutes var
TOTTM17   FORM      7         * Total Time in minutes var
.
.         temp file A : stores information for a particular clinic/surgeon
.
TEMPA     IFILE     SQL, FIXED=50, KEY="U1-6"
.
.Name     Type      Length Phys Start  Description
.-------  ----      ------ ---- -----  ---------------------------------------
ACLIN     DIM       6        6     1   Clinic/Surgeon code
ANSES     FORM      6        4     7   Number of sessions used
ASEST     FORM      7        5    11   Total session time booked in minutes
ANOPR     FORM      6        4    16   Number of operations performed
ATIMU     FORM      7        5    20   Total session time used in minutes
AOVER     FORM      6        4    25   Number of sessions that went overtime
ADLAY     FORM      6        4    29   Number of sessions that were delayed
ACANC     FORM      6        4    33   Number of sessions that were cancelled
ATSHR     FORM      7        5    37   Shared Session Time
ANCAS     FORM      5        4    42   Number of Cases
ASPAR     DIM       4        4    46
.                                 50   End of Record
.
.         temp file B : stores information for a particular clinic/surgeon and
.                       an operation type
.
TEMPB     IFILE     SQL, FIXED=30, KEY="U1-6,7-9"
.
.Name     Type      Length Phys Start  Description
.-------  ----      ------ ---- -----  ---------------------------------------
BCLIN     DIM       6        6     1   Clinic/Surgeon code
BTYPE     DIM       3        3     7   Operation type
BNOPR     FORM      6        4    10   Number of operations performed
BTIMU     FORM      7        5    14   Total time used in minutes
BNCAS     FORM      5        4    19   Number of Cases
BSPAR     DIM       7        7    23
.                                 30   End of Record
.
.         temp file C : stores information for a particular clinic/surgeon 
.                       considers private patients and when not own clinic
.
TEMPC     IFILE     SQL, FIXED=60, KEY="U1-6"
.
.Name     Type      Length Phys Start  Description
.-------  ----      ------ ---- -----  ---------------------------------------
CCLIN     DIM       6        6     1   Clinic/Surgeon code
CNSES     FORM      6        4     7   Number of sessions used
CSEST     FORM      7        5    11   Total session time booked in minutes
CNOPR     FORM      6        4    16   Number of operations performed
CTIMU     FORM      7        5    20   Total session time used in minutes
COVER     FORM      6        4    25   Number of sessions that went overtime
CDLAY     FORM      6        4    29   Number of sessions that were delayed
CCANC     FORM      6        4    33   Number of sessions that were cancelled
CPCAS     FORM      6        4    37   Number of cases for private patients
CPTIM     FORM      7        5    41   Total time used for private patients
COCAS     FORM      6        4    46   Number of cases when not own clinic
COTIM     FORM      7        5    50   Total time used when not own clinic
CSHAR     FORM      6        4    55   Total time shared minutes
CSPAR     DIM       1        1    59
.                                 60   End of Record
.
.         temp file D : stores information for a particular clinic/surgeon 
.                       considers private patients and when not own clinic
.
TEMPD     IFILE     SQL, FIXED=75, KEY="U1-6"
.
.Name     Type      Length Phys Start  Description
.-------  ----      ------ ---- -----  ---------------------------------------
DCLIN     DIM       6        6     1   Clinic/Surgeon code
DAVLN     FORM      6        4     7   Total number of sessions available
DAVLT     FORM      7        5    11   Total time   of sessions available
DUSEN     FORM      6        4    16   Total number of sessions used
DUSET     FORM      7        5    20   Total time   of sessions used
DNPAT     FORM      6        4    25   Number of patients
DOVTI     FORM      7        5    29   Total time of overtime sessions
DPRNU     FORM      6        4    34   Private patients - number
DPRTI     FORM      6        4    38   Private patients - time
DOTNU     FORM      6        4    42   Other   patients - number
DOTTI     FORM      6        4    46   Other   patients - time
DNOPR     FORM      6        4    50   Number of operations
DOVNU     FORM      6        4    54   Number of overtime sessions
DDLAY     FORM      6        4    58   Number of delayed sessions
DCANC     FORM      6        4    62   Number of cancelled sessions
DSHAR     FORM      7        5    66   Total session time shared
DSPAR     DIM       4        4    71
.                                 75   End of Record
.
.         temp file Z : stores the totals for each operation type
.
TEMPZ     IFILE     SQL, FIXED=20, KEY="U1-3"
.
.Name     Type      Length Phys Start  Description
.-------  ----      ------ ---- -----  ---------------------------------------
ZTYPE     DIM       3        3     1   Operation type
ZNOPR     FORM      6        4     4   Number of operations performed
ZTIMU     FORM      7        5     8   Total time used in minutes
ZNCAS     FORM      6        4    13   Number of cases.
ZSPAR     DIM       3        3    17
.                                 20   End of Record
.
. ------- program constants -------
.
INDEXA    INIT      " 50 U1-6"                             * was 43
INDEXB    INIT      " 30 U1-6,7-9"                         * was 22
INDEXC    INIT      " 60 U1-6"                             * was 55
INDEXD    INIT      " 75 U1-6"                             * was 67
INDEXZ    INIT      " 20 U1-3"                             * was 16
INVCODE   INIT      "Invalid Code "
ISBUILD   INIT      "isbuild "
ISERASE   INIT      "iserase "
UNKNOWN   INIT      "Unknown "
.
.  Report 1          ....*....1....*....2....*....3....*....4....
R1L1PT1   INIT      "|                |      | Total |      |    "
R1L2PT1   INIT      "|                |      | Sessn |      |    "
R1L3PT1   INIT      "| Surgeon        |No.of | Time  | No.of| No."
R1L4PT1   INIT      "| Description    |Sessns| (Mins)| Cases| Ope"
.
.                    *....5....*....6....*....7....*....8....*...
R1L1PT2   INIT      "  | Total | Total |      |      |      |    "
R1L2PT2   INIT      "  | Time  | Shared|      |      |      |    "
R1L3PT2   INIT      "of| Util  | Time  | Util | Over | Delay|Canc"
R1L4PT2   INIT      "rs| (Mins)| (Mins)|  %   | Time | Time | Tim"
.
.                    .9....*....0....*....1....*....2....*....3..
R1L1PT3   INIT      "  |                  |      |      | Total |"
R1L2PT3   INIT      "  |                  |      |      | Used  |"
R1L3PT3   INIT      "el|                  |No.of |No.of | Time  |"
R1L4PT3   INIT      "e | Operation Type   |Cases |Opers |(Mins) |"
.
.  Report 2          ....*....1....*....2....*....3....*....4....
R2L1PT1   INIT      "|                            |     | Total |"
R2L2PT1   INIT      "|                            | No. | Sessn |"
R2L3PT1   INIT      "| Surgeon                    | of  | Time  |"
R2L4PT1   INIT      "| Description                |Sessn|(Mins) |"
.
.                    *....5....*....6....*....7....*....8....*...
R2L1PT2   INIT      "     | Total | Total|      |     |     |    "
R2L2PT2   INIT      " No. | Util  | Share|      |     |     |    "
R2L3PT2   INIT      " of  | Time  | Time | Util | Over|     |    "
R2L4PT2   INIT      "Opers|(Mins) |(Mins)|  %   | Time|Delay|Canc"
.
.                    .9....*....0....*....1....*....2....*....3..
R2L1PT3   INIT      " |--Private Patients--|----Other Users-----|"
R2L2PT3   INIT      " | No. | Used  |      | No. | Used  |      |"
R2L3PT3   INIT      " | of  | Time  | Util | of  | Time  | Util |"
R2L4PT3   INIT      "l|Pat's|(Mins) |  %   |Pat's|(Mins) |  %   |"
.
.  Report 3          ....*....1....*....2....*....3....*....4....
R3L1PT1   INIT      "|      |Total | Total |Total | Total | Total"
R3L2PT1   INIT      "|      |No.of | Sessn |No.of | Sessn | Share"
R3L3PT1   INIT      "|Surgeo|Sessn | Avail |Sessn | Used  | Time "
R3L4PT1   INIT      "|      |Avail |(Mins) |Used  |(Mins) |(Mins)"
.
.                    *....5....*....6....*....7....*....8....*...
R3L1PT2   INIT      " |      |      |      | No. |Total|     |   "
R3L2PT2   INIT      " |      | No.  | No.  |Ses'n|Ses's|No.of|No."
R3L3PT2   INIT      " | Util | of   | of   | Over|O'Tim|Ses'n|Ses"
R3L4PT2   INIT      " |  %   | Pats | Oper | Time|(Min)|Delay|Can"
.
.                    .9....*....0....*....1....*....2....*....3..
R3L1PT3   INIT      "  |--Private Patients--|---Other Patients--|"
R3L2PT3   INIT      "of| No. | Time  |      | No. | Time |      |"
R3L3PT3   INIT      "'n| of  | Used  | Util | of  | Used | Util |"
R3L4PT3   INIT      "cl| Pats|(Mins) |  %   | Pats|(Mins)|  %   |"
.
.
PRGID     INIT      "IBAOPR60"
PRGNAM    INIT      "Session Usage Report"
VERSION   INIT      "V12.01.00"
+
.*********************************************************************
.*                  ML0000                                           *
.*        Mainline Code / Controlling Logic                          *
.*********************************************************************
ML0000    CALL      INIT0000
.
ML1000    CALL      OPTN0000                * get which option
          BRANCH    EXIT,ML9999             * exit
.
ML1500    CALL      KPER0000                * enter periods
          BRANCH    EXIT,ML1000             * nothing entered
.
ML2000    CALL      KCOS0000                * enter clinic/surgeon
.         
.         OK to continue
.
ML3000    CALL      CONTQST
          BRANCH    CEXIT,ML4000,ML1500,ML1000
.                         Yes    No     Cancel
.
.         produce the report
.
ML4000    CALL      REPT0000
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.*********************************************************************
.*                  INIT0000                    Called by : ML0000   *
.*        Initialization and open files                              *
.*********************************************************************
INIT0000  CALL      DISPHEAD                * display program header
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE           * current date
          DISPLAY   *P1:3,*EF               * clear the screen
          MOVE      ONE,CNEWDS              * new ? mark option
          MOVE      ONE,CDEFDTE             * once to accept default
          MOVE      ONE,CNOUNDLN            * no underlines
          MOVE      ZERO,CHIGHLT            * use highlighting
.
          DISPLAY   *P50:24,"Opening controlf"
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,FORTY;*96,OPCNCDSC,*111,OPCNCLSU
          READ      CONTROLF,FORTY2;*52,OPCNR60A
          READ      CONTROLF,FIFTY9;*27,CPERMTH
.
          DISPLAY   *P58:24,"patcodes"
          OPEN      PATCODE1,"patcodes"     * codes file
          DISPLAY   *P58:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"     * doctor file
          OPEN      PATDO1A2,"patdo1af"     * doctor file
          DISPLAY   *P58:24,"patdrgaf"
          OPEN      PATDRGA3,"patdrgaf"     * date range file
          DISPLAY   *P58:24,"oprcliaf"
          OPEN      OPRCLIA1,"oprcliaf"     * clinic file
          OPEN      OPRCLIA2,"oprcliaf"     * clinic file
          DISPLAY   *P58:24,"oprcouaf"
          OPEN      OPRCOUA1,"oprcouaf"     * clinic operation usage file
          DISPLAY   *P58:24,"oprcsuaf"
          OPEN      OPRCSUA1,"oprcsuaf"     * clinic session usage file
          DISPLAY   *P58:24,"oprcscaf"
          OPEN      OPRCSCA1,"oprcscaf"     * clinic session cancellation file
.
.         remove trailing blanks 
.
          ENDSET    CPERMTH
.
INIT9000  CMATCH    SP1,CPERMTH
          GOTO      INIT9010 IF NOT EQUAL   * found a letter
.
          BUMP      CPERMTH,-1
          GOTO      INIT9000 IF NOT EOS     * more letters to go
.
INIT9010  LENSET    CPERMTH                 * set logical length
          RESET     CPERMTH
.
          ENDSET    OPCNCDSC
.
INIT9020  CMATCH    SP1,OPCNCDSC
          GOTO      INIT9030 IF NOT EQUAL   * found a letter
.
          BUMP      OPCNCDSC,-1
          GOTO      INIT9020 IF NOT EOS     * more letters to go
.
INIT9030  LENSET    OPCNCDSC                * set logical length
          RESET     OPCNCDSC
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,CFNAMEA
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,CFNAMEB
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,CFNAMEC
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,CFNAMED
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,CFNAMEZ
.
INIT9999  RETURN
+
.*********************************************************************
.*                  OPTN0000                    Called by : ML1000   *
.*        See if to produce report                                   *
.*        Returns : EXIT = 1      exit chosen                        *
.*                  MOPTION       option type                        *
.*********************************************************************
OPTN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*P1:5,ONE,*HOFF:
                    *P3:4,"= Exit":
                    *P3:5,"= Produce report ":
                    *P1:7,"Select Option : "
.
OPTN1000  KEYIN     *P17:7,*DV,UNDLN1:
                    *P17:7,*V2LON,MOPTION:
                    *P17:7,*DV,MOPTION
.
          BRANCH    MOPTION,OPTN8000
.
          COMPARE   ZERO,MOPTION
          GOTO      OPTN9000 IF EQUAL       * exit selected
.
          BEEP
          GOTO      OPTN1000
.
OPTN8000  MOVE      FALSE,EXIT              * valid answer
          GOTO      OPTN9999
.
OPTN9000  MOVE      TRUE,EXIT               * exit selected
.
OPTN9999  RETURN
+
.********************************************************************* 
.*                  SCRN0000                    Called by : KDOC0000 *
.*        Redisplay Screen after a ? mark                   KNUR0000 *
.*********************************************************************
SCRN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,"Starting ",CPERMTH,*P26:4,": ",*V2LON,DIM7A,*HOFF:
                    *P1:6,"Ending ",CPERMTH,*P26:6,": ",*V2LON,DIM7B,*HOFF:
                    *P1:8,"Starting ",OPCNCDSC,*P26:8,": ",*V2LON,STRTCOSC:
                    *HOFF,*P36:8,STRTCOSD;
.
          MATCH     SP6,STRTCOSC
          GOTO      SCRN1000 IF NOT EQUAL   * have a code
.
          DISPLAY   *P28:8,*V2LON,"Start"
.
SCRN1000  DISPLAY   *P1:10,"Ending ",OPCNCDSC,*P26:10,": ",*V2LON,STOPCOSC:
                    *HOFF,*P36:10,STOPCOSD
.
SCRN9999  RETURN
+
.*********************************************************************
.*                  KPER0000                    Called by : ML1500   *
.*        Enter the period ranges                                    *
.*        Returns : EXIT = 1      exit chosen                        * 
.*                  PERDSTRT      starting period                    *
.*                  PERDSTOP      ending period                      *
.*********************************************************************
KPER0000  DISPLAY   *P1:3,*EF:
                    *P1:4,"Starting ",CPERMTH,*P26:4,":":
                    *P1:6,"Ending ",CPERMTH,*P26:6,":"
.
.         enter starting period
.
KPER1000  MOVE      "4",CVERT               * row
          MOVE      "28",CCOL               * column
          UNPACK    SP4,CYEAR,CMON          * no defaults
          MOVE      CCC,CCENT
.
          CALL      KEYPER  
          BRANCH    OVRCD,KPER9000          * nothing entered
          BRANCH    CQUEST,KPER1000         * ? entered
.
          PACK      PERDSTRT,ICENT,IYEAR,IMON
          REP       " 0",PERDSTRT
.
          MOVE      PERDSTRT,KEY6           * period is the key
          CALL      RDDRGA3                 * read date range file
          BRANCH    OVRCD,KPER1500          * invalid period
.
          PACK      DIM7A,IMON,SLASH,ICENT,IYEAR
          REP       " 0",DIM7A
          MOVE      DRGLDSC,DIM20A          * long description
          MOVE      DRGFDTE,STRTLDAT        * from date of period
          MOVE      DRGTDTE,STRTHDAT        * to date of period
          MOVE      DRGYR,SAVEYR1           * financial year
          MOVE      DRGNUM,SAVEMON1         * financial month
.
          MATCH     STRTHDAT,CURRDATE
          GOTO      KPER2000 IF NOT LESS    * to date is <= current date
.
          DISPLAY   *P1:24,*B,"Date must not be after current date.  ",*EL;
          CALL      HITENTER 
          GOTO      KPER1000
.
KPER1500  DISPLAY   *P1:24,*B,*+,CPERMTH," not on file.  ",*EL;
          CALL      HITENTER 
          GOTO      KPER1000
.
.         enter ending period (default to starting period)
.
KPER2000  UNPACK    STRTLDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P37:4,*V2LON,CPCDATE
          MOVE      CPCDATE,PRFDATE
          MOVE      "6",CVERT               * row
          MOVE      "28",CCOL               * column
          UNPACK    PERDSTRT,CCENT,CYEAR,CMON
.
          CALL      KEYPER 
          BRANCH    OVRCD,KPER2000          * nothing entered
          BRANCH    CQUEST,KPER2000         * ? entered
.
          PACK      PERDSTOP,ICENT,IYEAR,IMON
          REP       " 0",PERDSTOP           * set up end period
.
          MOVE      PERDSTOP,KEY6           * ending period as key
          CALL      RDDRGA3                 * read date range file
          BRANCH    OVRCD,KPER2500          * invalid period
.
          PACK      DIM7B,IMON,SLASH,ICENT,IYEAR
          REP       " 0",DIM7B
          MOVE      DRGLDSC,DIM20B          * long description
          MOVE      DRGFDTE,STOPLDAT        * from date of period
          MOVE      DRGTDTE,STOPHDAT        * to date of period
          MOVE      DRGYR,SAVEYR2           * financial year
          MOVE      DRGNUM,SAVEMON2         * financial month
          PACK      SAVEEND,DRGYR,DRGNUM
.
          MATCH     PERDSTRT,PERDSTOP
          GOTO      KPER3000 IF NOT LESS    * ending period is >= start period
.
          DISPLAY   *P1:24,*B,"Ending ",*+,CPERMTH," must be after Starting ":
                    *+,CPERMTH,".  ",*EL;
          CALL      HITENTER 
          GOTO      KPER2000
.
KPER2500  DISPLAY   *P1:24,*B,*+,CPERMTH," not on file.  ",*EL;
          CALL      HITENTER 
          GOTO      KPER2000
.
.         check end date of end period is before current date
.
KPER3000  MATCH     STOPHDAT,CURRDATE
          GOTO      KPER8000 IF NOT LESS    * date is <= current date
.
          DISPLAY   *P1:24,*B,"Date must not be after current date.  ",*EL;
          CALL      HITENTER 
          GOTO      KPER2000
.
KPER8000  UNPACK    STOPHDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P37:6,*V2LON,CPCDATE
          MOVE      CPCDATE,PRTDATE
.
          MOVE      FALSE,EXIT              * valid dates
          GOTO      KPER9999
.
KPER9000  MOVE      TRUE,EXIT               * exit selected
.
KPER9999  RETURN
+
.*********************************************************************
.*                  KCOS0000                    Called by : ML2000   *
.*        Enter either the clinic/surgeon ranges                     *
.*        Returns : STRTCOSC      starting code                      * 
.*                  STOPCOSC      ending code                        * 
.********************************************************************* 
KCOS0000  DISPLAY   *P1:7,*EF:
                    *P1:8,"Starting ",OPCNCDSC,*P26:8,":":
                    *P1:10,"Ending ",OPCNCDSC,*P26:10,":"
.
          MOVE      SP6,STRTCOSC            * clear the field
          PACK      STRTCOSD,SP20,SP20      * clear the field
          MOVE      SP6,STOPCOSC            * clear the field
          PACK      STOPCOSD,SP20,SP20      * clear the field
.
          BRANCH    OPCNCLSU,KCOS5000       * using surgeons
.
.         enter clinics
.
KCOS1000  CALL      FCLI0000                * enter starting clinic
KCOS1100  CALL      TCLI0000                * enter ending clinic
          GOTO      KCOS7000                * validate codes entered
.
.         enter surgeons
.
KCOS5000  CALL      FDOC0000                * enter starting doctor
KCOS5100  CALL      TDOC0000                * enter ending doctor
.
.         validate order of codes entered
.
KCOS7000  MATCH     "Finish",STOPCOSC
          GOTO      KCOS9999 IF EQUAL       * going to end
.
          MATCH     "Start",STRTCOSC
          GOTO      KCOS9999 IF EQUAL       * going from start
.
.         check that end code >= start code
.
          MATCH     STRTCOSC,STOPCOSC
          GOTO      KCOS9999 IF NOT LESS    * end is >= start, OK
.
          DISPLAY   *P1:24,*B,"End code must be after Start code.  ",*EL;
          CALL      HITENTER 
          GOTO      KCOS0000
.
KCOS9999  RETURN
+
.********************************************************************* 
.*                  REPT0000                    Called by : ML4000   *
.*        Produce the report                                         *
.*********************************************************************
REPT0000  DISPLAY   *P1:24,*EL,"Producing the report now - ",*V2LON:
                    *BLINKON,"Please wait"
.
          MOVE      ZERO,CPAGENO            * clear page number
          MOVE      SP6,CURRCLSU            * no current clinic/surgeon
          CALL      CLTOT000
          CALL      CTMP0000                * create required temp files
          CALL      PAGE0000                * print page header
.
.         loop through required files and write to the temp files
.
REPT0500  CALL      LCOU0000                * loop through OPRCOUFD
          CALL      LCSU0000                * loop through OPRCSUFD
          CALL      LCSC0000                * loop through OPRCSCFD
.
.         produce the report
.
          BRANCH    OPCNR60A,REPT1000,REPT2000,REPT3000
          GOTO      REPT8000
.
. -------------------------------------
. ------- produce report type 1 -------
. -------------------------------------
.
REPT1000  MOVE      SP6,KEY6                * start at beginning
          RESET     KEY6
          READ      TEMPA,KEY6;ACLIN,ANSES,ASEST,ANOPR,ATIMU,AOVER,ADLAY,ACANC:
                               ATSHR,ANCAS
.
REPT1100  READKS    TEMPA;ACLIN,ANSES,ASEST,ANOPR,ATIMU,AOVER,ADLAY,ACANC:
                               ATSHR,ANCAS
          GOTO      REPT8000 IF OVER
.
          COMPARE   "60",CLNO
          CALL      PAGE0000 IF NOT LESS    * need a new page
.
          MOVE      ACLIN,CURRCLSU          * current clinic surgeon
          CALL      GDES0000                * get clinic/surgeon desc
          MOVE      OPCLDESC,KEY16
.
          MOVE      ATIMU,FORM7P5           * get time used
          DIV       ASEST,FORM7P5           * divide by time booked
          MULT      "100",FORM7P5           * get as a percentage
          MOVE      FORM7P5,PCENTA          * set up percentage
.
          PRINT     *N,"|",KEY16,"|",ANSES,"|",ASEST,"| ",ANCAS,"|",ANOPR,"|":
                    ATIMU,"|",ATSHR,"|",PCENTA,"|",AOVER,"|",ADLAY,"|",ACANC;
          ADD       ONE,CLNO                * add to line counter
.
.         update the total's
.
          ADD       ANSES,TOTAL1            * number of sessions
          ADD       ASEST,TOTTM2            * session time booked
          ADD       ANCAS,TOTAL3            * number of cases
          ADD       ANOPR,TOTAL4            * number of operations
          ADD       ATIMU,TOTTM5            * time used
          ADD       ATSHR,TOTTM6            * shared time
          ADD       AOVER,TOTAL8            * overtime sessions
          ADD       ADLAY,TOTAL9            * delayed sessions
          ADD       ACANC,TOTAL10           * cancelled sessions
.
          CALL      PTPB0000                * print temp file b details
          PRINT     *N,"*-------------------------------------------------":
                       "--------------------------------":
                       "-------------------------------------------------*";
          ADD       ONE,CLNO                * add to line counter
          GOTO      REPT1100
.
. -------------------------------------
. ------- produce report type 2 -------
. -------------------------------------
.
REPT2000  MOVE      SP6,KEY6                * start at beginning
          RESET     KEY6
          READ      TEMPC,KEY6;CCLIN,CNSES,CSEST,CNOPR,CTIMU,COVER,CDLAY,CCANC:
                               CPCAS,CPTIM,COCAS,COTIM,CSHAR
.
REPT2100  READKS    TEMPC;CCLIN,CNSES,CSEST,CNOPR,CTIMU,COVER,CDLAY,CCANC:
                          CPCAS,CPTIM,COCAS,COTIM,CSHAR
          GOTO      REPT8000 IF OVER
.
          COMPARE   "60",CLNO
          CALL      PAGE0000 IF NOT LESS    * need a new page
.
          MOVE      CCLIN,CURRCLSU          * current clinic surgeon
          CALL      GDES0000                * get clinic/surgeon desc
.
          MOVE      OPCLDESC,KEY27
.
          MOVE      CTIMU,FORM7P5           * get time used
          DIV       CSEST,FORM7P5           * divide by time booked
          MULT      "100",FORM7P5           * get as a percentage
          MOVE      FORM7P5,PCENTA          * session util %
.
          MOVE      CPTIM,FORM7P5           * get private time used
          DIV       CTIMU,FORM7P5           * divide by time used
          MULT      "100",FORM7P5           * get as a percentage
          MOVE      FORM7P5,PCENTB          * private patients util %
.
          MOVE      COTIM,FORM7P5           * get other users time used
          DIV       CTIMU,FORM7P5           * divide by time used
          MULT      "100",FORM7P5           * get as a percentage
          MOVE      FORM7P5,PCENTC          * other users util %
.
          MOVE      CNSES,WNSES5   
          MOVE      CNOPR,WNOPR5   
          MOVE      COVER,WOVER5
          MOVE      CDLAY,WDLAY5
          MOVE      CCANC,WCANC5   
          MOVE      CPCAS,WPCAS5   
          MOVE      CCANC,WOCAS5   
.
          PRINT     *N,"|",KEY27," |",WNSES5,"|",CSEST,"|",WNOPR5:
                       "|",CTIMU,"|",CSHAR,"|",PCENTA,"|",WOVER5,"|",WDLAY5:
                       "|",WCANC5,"|",WPCAS5,"|",CPTIM,"|",PCENTB,"|",WOCAS5:
                       "|",COTIM,"|",PCENTC,"|":
                    *N,"*-------------------------------------------------":
                       "--------------------------------":
                       "-------------------------------------------------*";
          ADD       TWO,CLNO                * add to line counter
.
.         update the total's
.
          ADD       CNSES,TOTAL1            * number of sessions
          ADD       CSEST,TOTTM2            * session time booked
          ADD       CNOPR,TOTAL3            * number of operations
          ADD       CTIMU,TOTTM4            * time used
          ADD       CSHAR,TOTTM5            * time shared
.                         PCENT6
          ADD       COVER,TOTAL7            * overtime sessions
          ADD       CDLAY,TOTAL8            * delayed sessions
          ADD       CCANC,TOTAL9            * cancelled sessions
          ADD       CPCAS,TOTAL10           * private patients
          ADD       CPTIM,TOTTM11           * private patients time used
.                         PCENT12
          ADD       COCAS,TOTAL13           * other users
          ADD       COTIM,TOTTM14           * other users time used
          GOTO      REPT2100
.
. -------------------------------------
. ------- produce report type 3 -------
. -------------------------------------
.
REPT3000  MOVE      SP6,KEY6                * start at beginning
          READ      TEMPD,KEY6;;
.
REPT3100  READKS    TEMPD;DCLIN,DAVLN,DAVLT,DUSEN,DUSET,DNPAT,DOVTI:
                          DPRNU,DPRTI,DOTNU,DOTTI,DNOPR,DOVNU,DDLAY,DCANC:
                          DSHAR
          GOTO      REPT8000 IF OVER
.
          COMPARE   "60",CLNO
          CALL      PAGE0000 IF NOT LESS    * need a new page
.
          MOVE      DUSET,FORM7P5           * get time used
          DIV       DAVLT,FORM7P5           * divide by available
          MULT      "100",FORM7P5           * get as a percentage
          MOVE      FORM7P5,PCENTD          * utilization %
.
          MOVE      DPRTI,FORM7P5           * private time used
          MOVE      DUSET,FORM7             * total sessions used time
          ADD       DOVTI,FORM7             * + overtime time
          DIV       FORM7,FORM7P5           * divide by sum
          MULT      "100",FORM7P5           * get as percentage
          MOVE      FORM7P5,PCENTE          * private util %
.
          MOVE      DOTTI,FORM7P5           * other time used
          MOVE      DUSET,FORM7             * used time
          ADD       DOVTI,FORM7             * + overtime time
          DIV       FORM7,FORM7P5           * divide by sum
          MULT      "100",FORM7P5           * get as percentage
          MOVE      FORM7P5,PCENTF          * other util %
.
          MOVE      DOVNU,WOVNU5
          MOVE      DDLAY,WDLAY5
          MOVE      DCANC,WCANC5
          MOVE      DPRNU,WPRNU5
          MOVE      DOTNU,WOTNU5
          MOVE      DOVTI,FORM5
.
          PRINT     *1,"|",DCLIN,"|",DAVLN,"|",DAVLT,"|",DUSEN,"|",DUSET:
                       "|",DSHAR,"|",PCENTD,"|",DNPAT,"|",DNOPR,"|",WOVNU5:
                       "|",FORM5,"|",WDLAY5,"|",WCANC5,"|",WPRNU5,"| ",DPRTI:
                       "|",PCENTE,"|",WOTNU5,"|",DOTTI,"|",PCENTF,"|"
          ADD       ONE,CLNO
.
          ADD       DAVLN,TOTAL1
          ADD       DAVLT,TOTTM2
          ADD       DUSEN,TOTAL3
          ADD       DUSET,TOTTM4
          ADD       DSHAR,TOTTM5
.                         Util
          ADD       DNPAT,TOTAL7
          ADD       DNOPR,TOTAL8
          ADD       DOVNU,TOTAL9
          ADD       DOVTI,TOTTM10
          ADD       DDLAY,TOTAL11
          ADD       DCANC,TOTAL12
          ADD       DPRNU,TOTAL13
          ADD       DPRTI,TOTTM14
.                         Util
          ADD       DOTNU,TOTAL16
          ADD       DOTTI,TOTTM17
          GOTO      REPT3100
.
REPT8000  CALL      SUMM0000
.
REPT9999  RETURN
+
.********************************************************************* 
.*                  CTMP0000                    Called by : REPT0000 *
.*        Create the required temp files for the report              *
.*********************************************************************
CTMP0000  CALL      DTMP0000                * delete temp file
          BRANCH    OPCNR60A,CTMP1000,CTMP2000,CTMP3000
.
CTMP1000  CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,CFNAMEA,INDEXA
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID          * create temp file A
          OPEN      TEMPA,CFNAMEA           * open temp file 
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,CFNAMEB,INDEXB
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID          * create temp file B
          OPEN      TEMPB,CFNAMEB           * open temp file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,CFNAMEZ,INDEXZ
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID          * create temp file Z
          OPEN      TEMPZ,CFNAMEZ           * open temp file
          GOTO      CTMP9999
.
CTMP2000  CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,CFNAMEC,INDEXC
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID          * create temp file C
          OPEN      TEMPC,CFNAMEC           * open temp file
          GOTO      CTMP9999
.
CTMP3000  CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,CFNAMED,INDEXD
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID          * create temp file D
          OPEN      TEMPD,CFNAMED           * open temp file
.
CTMP9999  RETURN
+
.*********************************************************************
.*                  DTMP0000                    Called by : CTMP0000 *
.*        Delete the temp files used for the report         REPT0000 *
.********************************************************************* 
DTMP0000  CLOSE     TEMPA                   * close temp file
          CLEAR     CMDLINE
          PACK      CMDLINE,ISERASE,CFNAMEA
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CLOSE     TEMPB                   * close temp file 
          CLEAR     CMDLINE
          PACK      CMDLINE,ISERASE,CFNAMEB
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CLOSE     TEMPC                   * close temp file
          CLEAR     CMDLINE
          PACK      CMDLINE,ISERASE,CFNAMEC
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CLOSE     TEMPD                   * close temp file 
          CLEAR     CMDLINE
          PACK      CMDLINE,ISERASE,CFNAMED
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CLOSE     TEMPZ                   * close temp file
          CLEAR     CMDLINE
          PACK      CMDLINE,ISERASE,CFNAMEZ
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          GOTO      DTMP9999
.
DTMP9999  RETURN
+
.*********************************************************************
.*                  FCLI0000                    Called by : KCOS5000 *
.*        Enter the from clinic code                                 *
.*        Returns : STRTCOSC      starting code                      * 
.*                  STRTCOSD      starting description               * 
.*********************************************************************
FCLI0000  MOVE      "28",CCOL               * Column for keyin
          MOVE      "8",CVERT               * Line for keyin
          MOVE      SP6,STRTCOSC            * clear the field
          PACK      STRTCOSD,SP20,SP20      * clear the field
.
          CALL      KCLI0000                * Keyin the code
.
          MOVE      CLSUCODE,STRTCOSC       * the code
          MOVE      CLSUNAME,STRTCOSD       * the description
.
          MATCH     SP6,STRTCOSC
          GOTO      FCLI8000 IF NOT EQUAL   * code entered
.
          DISPLAY   *P28:8,*V2LON,"Start",*EL;
          GOTO      FCLI9999
.
FCLI8000  DISPLAY   *P28:8,*V2LON,STRTCOSC,*HOFF,*P36:8,STRTCOSD
.
FCLI9999  RETURN
+
.*********************************************************************
.*                  TCLI0000                    Called by : KCOS5100 *
.*        Enter the to clinic code                                   *
.*        Returns : STOPCOSC      starting code                      * 
.*                  STOPCOSD      starting description               * 
.*********************************************************************
.
TCLI0000  MOVE      "28",CCOL               * Column for keyin
          MOVE      "10",CVERT              * Line for keyin
          MOVE      SP6,STOPCOSC            * clear the field
          PACK      STOPCOSD,SP20,SP20      * clear the field
.
          CALL      KCLI0000                * Keyin the code
.
          MOVE      CLSUCODE,STOPCOSC       * the code
          MOVE      CLSUNAME,STOPCOSD       * the description
.
          MATCH     SP6,STOPCOSC
          GOTO      TCLI8000 IF NOT EQUAL   * code entered
.
          MOVE      "zzzzzz",STOPCOSC
          DISPLAY   *P28:10,*V2LON,"Finish",*EL;
          GOTO      TCLI9999
.
TCLI8000  DISPLAY   *P28:10,*V2LON,STOPCOSC,*HOFF,*P36:10,STOPCOSD
.
TCLI9999  RETURN
+
.********************************************************************
.*                  KCLI0000                   Called by : FCLI0000 *
.*        Keyin a clinic code                              TCLI0000 *
.*        Para's  : CCOL          column for input                  *
.*                  CVERT         row for input                     *
.*        Returns : EXIT  = 1     exitchar entered                  *
.*                  CLSUNAME      the description                   *
.*                  CLSUCODE      the entered code                  *
.********************************************************************
.
KCLI0000  MOVE      ONE,SCRNFLAG            * set ? as not performed
          MOVE      SP6,CLSUCODE            * clear code
          PACK      CLSUNAME,SP20,SP20      * clear the description
.
KCLI1000  KEYIN     *PCCOL:CVERT,*DV,UNDLN6,*EL:
                    *PCCOL:CVERT,*V2LON,CLSUCODE:
                    *PCCOL:CVERT,*DV,CLSUCODE;
.
          ENDSET    CLSUCODE
          APPEND    SP6,CLSUCODE
          RESET     CLSUCODE
.
          MATCH     SP6,CLSUCODE 
          GOTO      KCLI7900 IF EQUAL       * nothing entered
.
          MATCH     QUEST,CLSUCODE
          GOTO      KCLI2000 IF NOT EQUAL   * ? not entered
.
.         ? entered
.
          CALL      OPRCLIDS                * ? routine
          MOVE      TWENTY4,CVERT           * row for input
          MOVE      TEN8,CCOL               * column for input
          MOVE      ZERO,SCRNFLAG           * set ? as performed
          DISPLAY   *P1:24,"Input the code : ",*EL
          GOTO      KCLI1000
.
.         validate what was entered
.
KCLI2000  MOVE      CLSUCODE,KEY6           * key of entered value
          CALL      RDOPCLI1                * read clinic file
          BRANCH    OVRCD,KCLI7000          * invalid input
.
          MOVE      OPCLDESC,CLSUNAME       * set up the name of clinic
.
          MATCH     SP6,OPCLDOCT
          GOTO      KCLI8000 IF EQUAL       * no doctor code to use
.
          MOVE      OPCLDOCT,KEY6           * doctor code is the key
          CALL      RDDOCT1                 * read doctor file
.
          MOVE      DSNAME,PACSNAME
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,CLSUNAME       * set up the name
          GOTO      KCLI8000                * valid input
.
.         ERROR: invalid code entered
.
KCLI7000  DISPLAY   *P1:24,*EL,*B,"Invalid code.  ",*EL;
          CALL      HITENTER
          BRANCH    SCRNFLAG,KCLI1000       * ? not entered
.
          DISPLAY   *P1:24,"Input the code : ",*EL
          GOTO      KCLI1000    
.
.         nothing was entered
.
KCLI7900  PACK      CLSUNAME,SP20,SP20      * clear the description
          MOVE      SP6,CLSUCODE            * clear the code
.
.         valid code entered
.
KCLI8000  BRANCH    SCRNFLAG,KCLI9999       * ? not entered
.
          CALL      SCRN0000                * redisplay screen
.
KCLI9999  RETURN
+
.*********************************************************************
.*                  FDOC0000                    Called by : KCOS5000 *
.*        enter the from doctor code                                 *
.*        Returns : STRTCOSC      starting code                      * 
.*                  STRTCOSD      starting description               * 
.*********************************************************************
.
FDOC0000  MOVE      "28",CCOL               * Column for keyin
          MOVE      "8",CVERT               * Line for keyin
          MOVE      SP6,STRTCOSC            * clear the field
          PACK      STRTCOSD,SP20,SP20      * clear the field
.
          CALL      KDOC0000                * Keyin the code
.
          MOVE      CLSUCODE,STRTCOSC       * the code
          MOVE      CLSUNAME,STRTCOSD       * the description
.
          MATCH     SP6,STRTCOSC
          GOTO      FDOC8000 IF NOT EQUAL   * code entered
.
          DISPLAY   *P28:8,*V2LON,"Start",*EL;
          GOTO      FDOC9999
.
FDOC8000  DISPLAY   *P28:8,*V2LON,STRTCOSC,*HOFF,*P36:8,STRTCOSD
.
FDOC9999  RETURN
+
.*********************************************************************
.*                  TDOC0000                    Called by : KCOS5100 *
.*        Enter the to doctor code                                   *
.*        Returns : STOPCOSC      starting code                      * 
.*                  STOPCOSD      starting description               * 
.*********************************************************************
.
TDOC0000  MOVE      "28",CCOL               * Column for keyin
          MOVE      "10",CVERT              * Line for keyin
          MOVE      SP6,STOPCOSC            * clear the field
          PACK      STOPCOSD,SP20,SP20      * clear the field
.
          CALL      KDOC0000                * Keyin the code
.
          MOVE      CLSUCODE,STOPCOSC       * the code
          MOVE      CLSUNAME,STOPCOSD       * the description
.
          MATCH     SP6,STOPCOSC
          GOTO      TDOC8000 IF NOT EQUAL   * code entered
.
          MOVE      "zzzzzz",STOPCOSC
          DISPLAY   *P28:10,*V2LON,"Finish",*EL;
          GOTO      TDOC9999
.
TDOC8000  DISPLAY   *P28:10,*V2LON,STOPCOSC,*HOFF,*P36:10,STOPCOSD
.
TDOC9999  RETURN
+
.********************************************************************
.*                  KDOC0000                   Called by : FDOC0000 *
.*        Keyin a doctors code                             TDOC0000 *
.*        Para's  : CCOL          column for input                  *
.*                  CVERT         row for input                     *
.*        Returns : EXIT  = 1     exitchar entered                  *
.*                  CLSUNAME      the description                   *
.*                  CLSUCODE      the entered code                  *
.********************************************************************
.
KDOC0000  MOVE      SP6,CLSUCODE            * clear code
          PACK      CLSUNAME,SP20,SP20      * clear the description
          MOVE      SP10,CLSUCODE
          MOVE      CCOL,ECOL
          MOVE      CVERT,EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          CALL      PATDOCKY
          BRANCH    EXIT,KDOC7900,KDOC0000
.
          MOVE      DCODE,CLSUCODE
.
KDOC2000  MOVE      CLSUCODE,KEY6           * key of entered value
          MOVE      DSNAME,PACSNAME
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,CLSUNAME       * set up the name
          GOTO      KDOC9999                * valid input
.
.         nothing was entered
.
KDOC7900  PACK      CLSUNAME,SP20,SP20      * clear the description
          MOVE      SP6,CLSUCODE            * clear the code
.
.
KDOC9999  RETURN
+
.*********************************************************************
.*                  LCOU0000                    Called by : REPT0500 *
.*        Loop through OPRCOUFD and write to the temp files          *
.********************************************************************* 
LCOU0000  PACK      KEY21,SAVEYR1,SAVEMON1,STRTCOSC,SP9
          CALL      RDOPCOU1                * direct read on file
          BRANCH    OVRCD,LCOU0200          * no doctor code entered
          GOTO      LCOU0400                * validate code
.
.         next read on clinic operation usage file
.
LCOU0200  CALL      RKOPCOU1                * next read
          BRANCH    OVRCD,LCOU9999          * end of file
.
LCOU0400  MATCH     OPCODATE,SAVEEND
          GOTO      LCOU9999 IF LESS        * out of range
.
          MATCH     STRTCOSC,OPCODOCT
          GOTO      LCOU0200 IF LESS        * read in doctor code is too low
.
          MATCH     OPCODOCT,STOPCOSC
          GOTO      LCOU0200 IF LESS        * read in doctor code is too high
.
          MATCH     SP6,OPCODOCT
          GOTO      LCOU0200 IF EQUAL       * dont allow blank doctors
.
.         have a valid period and clinic/surgeon
.
          MATCH     SP3,OPCOOTYP
          GOTO      LCOU1000 IF NOT EQUAL   * have an operation type
.
          MOVE      "***",OPCOOTYP          * change from blanks
.
LCOU1000  BRANCH    OPCNR60A,LCOU1100,LCOU2000,LCOU3000
.
. ------------------------------------
. ------- maintain temp file A -------
. ------------------------------------
.
LCOU1100  MOVE      OPCODOCT,KEY6           * set up key
          MOVE      OPCODOCT,ACLIN          * clinic
          CALL      CLRA0000                * clear variables
          MOVE      ZERO,OVRCD
          RESET     KEY6
          READ      TEMPA,KEY6;ACLIN,ANSES,ASEST,ANOPR,ATIMU,AOVER,ADLAY,ACANC:
                               ATSHR,ANCAS
          CALL      OVERCOND IF OVER        * no record
.
          ADD       OPCONOPR,ANOPR          * add to # of operations
          ADD       OPCONCAS,ANCAS
.
          BRANCH    OVRCD,LCOU1200          * do a write
.
.         update temp file A
.
          UPDATE    TEMPA;ACLIN,ANSES,ASEST,ANOPR,ATIMU,AOVER,ADLAY,ACANC:
                          ATSHR,ANCAS
          GOTO      LCOU1500                * check temp file B
.
.         write to temp file A
.
LCOU1200  RESET     KEY6   
          WRITE     TEMPA,KEY6;ACLIN,ANSES,ASEST,ANOPR,ATIMU,AOVER,ADLAY,ACANC:
                               ATSHR,ANCAS
.
. ------------------------------------
. ------- maintain temp file B -------
. ------------------------------------
.
LCOU1500  PACK      KEY9,OPCODOCT,OPCOOTYP
          UNPACK    KEY9,BCLIN,BTYPE        * clinic and type
          MOVE      ZERO,BNOPR              * clear # of operations
          MOVE      ZERO,BTIMU              * clear time used
          MOVE      ZERO,BNCAS
.
          MOVE      ZERO,OVRCD
          RESET     KEY9
          READ      TEMPB,KEY9;BCLIN,BTYPE,BNOPR,BTIMU,BNCAS
          CALL      OVERCOND IF OVER        * no record
.
          ADD       OPCONOPR,BNOPR          * add to # of operations
          ADD       OPCOTIME,BTIMU          * add to time used
          ADD       OPCONCAS,BNCAS
.
          BRANCH    OVRCD,LCOU1700          * write to temp file
.
.         update temp file B
.
          UPDATE    TEMPB;BCLIN,BTYPE,BNOPR,BTIMU,BNCAS
          GOTO      LCOU0200                * get next record
.
.         write to temp file B
.
LCOU1700  RESET     KEY9
          WRITE     TEMPB,KEY9;BCLIN,BTYPE,BNOPR,BTIMU,BNCAS
          GOTO      LCOU0200
.
. ------------------------------------
. ------- maintain temp file C -------
. ------------------------------------
.
LCOU2000  MOVE      OPCODOCT,KEY6           * key to temp file
          MOVE      OPCODOCT,CCLIN          * clinic/surgeon
          CALL      CLRC0000                * clear variables
          MOVE      ZERO,OVRCD
          RESET     KEY6
          READ      TEMPC,KEY6;CCLIN,CNSES,CSEST,CNOPR,CTIMU,COVER,CDLAY,CCANC:
                               CPCAS,CPTIM,COCAS,COTIM,CSHAR
          CALL      OVERCOND IF OVER        * no record
          MOVE      OVRCD,SOVRCD
.
          ADD       OPCONOPR,CNOPR          * add number of operations
.
          COMPARE   "2",OPCOATYP
          GOTO      LCOU2050 IF NOT EQUAL   * not a private patient
.
          ADD       OPCONCAS,CPCAS          * add number of private cases
          ADD       OPCOTIME,CPTIM          * add time for private patients
.
LCOU2050  MOVE      SOVRCD,OVRCD
          COMPARE   "2",OPCOOWN
          GOTO      LCOU2100 IF NOT EQUAL   * attend doct = oper doct
.
          ADD       OPCONCAS,COCAS          * add to not own clinic cases
          ADD       OPCOTIME,COTIM          * add to not own clinic time
.
LCOU2100  BRANCH    OVRCD,LCOU2500          * write to temp file
.
.         update temp file C
.
          UPDATE    TEMPC;CCLIN,CNSES,CSEST,CNOPR,CTIMU,COVER,CDLAY,CCANC:
                          CPCAS,CPTIM,COCAS,COTIM,CSHAR
          GOTO      LCOU0200
.
.         write to temp file C
.
LCOU2500  RESET     KEY6 
          WRITE     TEMPC,KEY6;CCLIN,CNSES,CSEST,CNOPR,CTIMU,COVER,CDLAY,CCANC:
                               CPCAS,CPTIM,COCAS,COTIM,CSHAR
          GOTO      LCOU0200
.
. ------------------------------------
. ------- maintain temp file D -------
. ------------------------------------
.
LCOU3000  MOVE      OPCODOCT,KEY6           * set up key
          MOVE      OPCODOCT,DCLIN          * clinic
          CALL      CLRD0000                * clear variables
          MOVE      ZERO,OVRCD
          RESET     KEY6
          READ      TEMPD,KEY6;DCLIN,DAVLN,DAVLT,DUSEN,DUSET,DNPAT,DOVTI:
                          DPRNU,DPRTI,DOTNU,DOTTI,DNOPR,DOVNU,DDLAY,DCANC:
                          DSHAR
          CALL      OVERCOND IF OVER        * no record
          MOVE      OVRCD,SOVRCD
.
          ADD       OPCONCAS,DNPAT          * add to # of patients
          ADD       OPCONOPR,DNOPR          * add to # of operations
.
.         Check if att.doctor is the theatre session doctor
.
          COMPARE   ONE,OPCOOWN
          GOTO      LCOU3100 IF NOT EQUAL
.
.         check for private/other
.
          COMPARE   "2",OPCOATYP
          GOTO      LCOU3200 IF NOT EQUAL
.
          ADD       OPCONCAS,DPRNU          * add to private number
          ADD       OPCOTIME,DPRTI          * add to private time
          GOTO      LCOU3200
.
.         All patients whose att.doctor was not the theatre session doctor
.
LCOU3100  ADD       OPCONCAS,DOTNU          * add to other number
          ADD       OPCOTIME,DOTTI          * add to other time
.
LCOU3200  MOVE      SOVRCD,OVRCD
          BRANCH    OVRCD,LCOU3500          * do a write
.
.         update temp file D
.
          UPDATE    TEMPD;DCLIN,DAVLN,DAVLT,DUSEN,DUSET,DNPAT,DOVTI:
                          DPRNU,DPRTI,DOTNU,DOTTI,DNOPR,DOVNU,DDLAY,DCANC:
                          DSHAR
          GOTO      LCOU0200
.
.         write to temp file D
.
LCOU3500  RESET     KEY6   
          WRITE     TEMPD,KEY6;DCLIN,DAVLN,DAVLT,DUSEN,DUSET,DNPAT,DOVTI:
                          DPRNU,DPRTI,DOTNU,DOTTI,DNOPR,DOVNU,DDLAY,DCANC:
                          DSHAR
          GOTO      LCOU0200
.
LCOU9999  RETURN
+
.*********************************************************************
.*                  LCSU0000                    Called by : REPT0500 *
.*        Loop through OPRCSUFD and write to the temp files          *
.********************************************************************* 
LCSU0000  PACK      KEY16,SAVEYR1,SAVEMON1,STRTCOSC,SP4
          CALL      RDOPCSU1                * direct read on file
          BRANCH    OVRCD,LCSU0200          * no doctor code entered
          GOTO      LCSU0400                * validate code
.
.         next read on clinic session usage file
.
LCSU0200  CALL      RKOPCSU1                * next read
          BRANCH    OVRCD,LCSU9999          * end of file
.
LCSU0400  MATCH     OPCSDATE,SAVEEND
          GOTO      LCSU9999 IF LESS        * out of range
.
          MATCH     STRTCOSC,OPCSDOCT
          GOTO      LCSU0200 IF LESS        * read in doctor code is too low
.
          MATCH     OPCSDOCT,STOPCOSC
          GOTO      LCSU0200 IF LESS        * read in doctor code is too high
.
          BRANCH    OPCNR60A,LCSU1000,LCSU2000,LCSU3000
.
. ------------------------------------
. ------- maintain temp file A -------
. ------------------------------------
.
LCSU1000  MOVE      OPCSDOCT,KEY6           * set up key
          MOVE      OPCSDOCT,ACLIN          * clinic
          CALL      CLRA0000                * clear variables
          MOVE      ZERO,OVRCD
          RESET     KEY6
          READ      TEMPA,KEY6;ACLIN,ANSES,ASEST,ANOPR,ATIMU,AOVER,ADLAY,ACANC:
                               ATSHR,ANCAS
          CALL      OVERCOND IF OVER        * no record
.
          ADD       OPCSUTIM,ATIMU          * add to total time used
          ADD       OPCSBSES,ANSES          * add to # of sessions used
          ADD       OPCSBTIM,ASEST          * add to total time booked
          ADD       OPCSSTIM,ATSHR          * add to total time shared
.
          BRANCH    OVRCD,LCSU1500          * write to temp file
.
          UPDATE    TEMPA;ACLIN,ANSES,ASEST,ANOPR,ATIMU,AOVER,ADLAY,ACANC:
                               ATSHR,ANCAS
          GOTO      LCSU0200                * get next record
.
.         write to temp file A
.
LCSU1500  MOVE      ACLIN,KEY6
          RESET     KEY6   
          WRITE     TEMPA,KEY6;ACLIN,ANSES,ASEST,ANOPR,ATIMU,AOVER,ADLAY,ACANC:
                               ATSHR,ANCAS
          GOTO      LCSU0200                * get next record
.
. -----------------------------------
. ------- maintain temp file C ------
. -----------------------------------
.
LCSU2000  MOVE      OPCSDOCT,KEY6           * key to temp file
          MOVE      OPCSDOCT,CCLIN          * clinic/surgeon
          CALL      CLRC0000                * clear variables
          MOVE      ZERO,OVRCD
          RESET     KEY6
          READ      TEMPC,KEY6;CCLIN,CNSES,CSEST,CNOPR,CTIMU,COVER,CDLAY,CCANC:
                               CPCAS,CPTIM,COCAS,COTIM,CSHAR
          CALL      OVERCOND IF OVER        * no record
.
          ADD       OPCSUTIM,CTIMU          * add to total time used
          ADD       OPCSBSES,CNSES          * add to # of sessions used
          ADD       OPCSBTIM,CSEST          * add to total time booked
          ADD       OPCSSTIM,CSHAR          * add to total time shared
.
          BRANCH    OVRCD,LCSU2500          * write to temp file
.
.         update temp file C
.
          UPDATE    TEMPC;CCLIN,CNSES,CSEST,CNOPR,CTIMU,COVER,CDLAY,CCANC:
                          CPCAS,CPTIM,COCAS,COTIM,CSHAR
          GOTO      LCSU0200                * get next record
.
.         write to temp file C
.
LCSU2500  RESET     KEY6
          WRITE     TEMPC,KEY6;CCLIN,CNSES,CSEST,CNOPR,CTIMU,COVER,CDLAY,CCANC:
                               CPCAS,CPTIM,COCAS,COTIM,CSHAR
          GOTO      LCSU0200                * get next record
.
. ------------------------------------
. ------- maintain temp file D -------
. ------------------------------------
.
LCSU3000  MOVE      OPCSDOCT,KEY6           * key to temp file
          MOVE      OPCSDOCT,DCLIN          * clinic/surgeon
          CALL      CLRD0000                * clear variables
          MOVE      ZERO,OVRCD
          RESET     KEY6
          READ      TEMPD,KEY6;DCLIN,DAVLN,DAVLT,DUSEN,DUSET,DNPAT,DOVTI:
                          DPRNU,DPRTI,DOTNU,DOTTI,DNOPR,DOVNU,DDLAY,DCANC:
                          DSHAR
          CALL      OVERCOND IF OVER        * no record
.
          ADD       OPCSBSES,DAVLN          * add to # of available sessions
          ADD       OPCSBTIM,DAVLT          * add to time available
          ADD       OPCSBRKS,DAVLT          * add to time available
          ADD       OPCSUSES,DUSEN          * add to number used sessions
          ADD       OPCSUTIM,DUSET          * add to number time used
          ADD       OPCSSTIM,DSHAR          * add to shared time
.
          BRANCH    OVRCD,LCSU3500          * write to temp file
.
.         update temp file D
.
          UPDATE    TEMPD;DCLIN,DAVLN,DAVLT,DUSEN,DUSET,DNPAT,DOVTI:
                          DPRNU,DPRTI,DOTNU,DOTTI,DNOPR,DOVNU,DDLAY,DCANC:
                          DSHAR
          GOTO      LCSU0200                * get next record
.
.         write to temp file D
.
LCSU3500  RESET     KEY6
          WRITE     TEMPD,KEY6;DCLIN,DAVLN,DAVLT,DUSEN,DUSET,DNPAT,DOVTI:
                          DPRNU,DPRTI,DOTNU,DOTTI,DNOPR,DOVNU,DDLAY,DCANC:
                          DSHAR
          GOTO      LCSU0200                * get next record
.
LCSU9999  RETURN
+
.*********************************************************************
.*                  LCSC0000                    Called by : REPT0500 *
.*        Loop through OPRCSDFD and write to the required temp file  *
.********************************************************************* 
LCSC0000  PACK      KEY20,SAVEYR1,SAVEMON1,STRTCOSC,SP8
          CALL      RDOPCSC1                * direct read on file
          BRANCH    OVRCD,LCSC0200          * no doctor code entered
          GOTO      LCSC0400                * validate code
.
.         next read on clinic session cancellation file
.
LCSC0200  CALL      RKOPCSC1                * next read
          BRANCH    OVRCD,LCSC9999          * end of file
.
LCSC0400  MATCH     OPCCDATE,SAVEEND
          GOTO      LCSC9999 IF LESS        * out of range
.
          MATCH     STRTCOSC,OPCCDOCT
          GOTO      LCSC0200 IF LESS        * read in doctor code is too low
.
          MATCH     OPCCDOCT,STOPCOSC
          GOTO      LCSC0200 IF LESS        * read in doctor code is too high
.
          BRANCH    OPCNR60A,LCSC1000,LCSC2000,LCSC3000
.
. ------------------------------------
. ------- maintain temp file A -------
. ------------------------------------
.
LCSC1000  MOVE      OPCCDOCT,KEY6           * set up key
          MOVE      OPCCDOCT,ACLIN          * clinic
          CALL      CLRA0000                * clear variables
          MOVE      ZERO,OVRCD
          RESET     KEY6
          READ      TEMPA,KEY6;ACLIN,ANSES,ASEST,ANOPR,ATIMU,AOVER,ADLAY,ACANC:
                               ATSHR,ANCAS
          CALL      OVERCOND IF OVER        * no record
.
          BRANCH    OPCCCTYP,LCSC1100,LCSC1200,LCSC1300
.
LCSC1100  ADD       OPCCNUMB,ACANC          * add to # cancelled
          GOTO      LCSC1400
.
LCSC1200  ADD       OPCCNUMB,ADLAY          * add to # delayed
          GOTO      LCSC1400
.
LCSC1300  ADD       OPCCNUMB,AOVER          * add to # overtime
.
LCSC1400  BRANCH    OVRCD,LCSC1500          * write to temp file
.
.         update temp file A
.
          UPDATE    TEMPA;ACLIN,ANSES,ASEST,ANOPR,ATIMU,AOVER,ADLAY,ACANC:
                               ATSHR,ANCAS
          GOTO      LCSC0200                * get next record
.
.         write to temp file A
.
LCSC1500  RESET     KEY6   
          WRITE     TEMPA,KEY6;ACLIN,ANSES,ASEST,ANOPR,ATIMU,AOVER,ADLAY,ACANC:
                               ATSHR,ANCAS
          GOTO      LCSC0200                * get next record
.
. ------------------------------------
. ------- maintain temp file C -------
. ------------------------------------
.
LCSC2000  MOVE      OPCCDOCT,KEY6           * key to temp file
          MOVE      OPCCDOCT,CCLIN          * clinic/surgeon
          CALL      CLRC0000                * clear variables
          MOVE      ZERO,OVRCD
          RESET     KEY6
          READ      TEMPC,KEY6;CCLIN,CNSES,CSEST,CNOPR,CTIMU,COVER,CDLAY,CCANC:
                               CPCAS,CPTIM,COCAS,COTIM,CSHAR
          CALL      OVERCOND IF OVER        * no record
.
          BRANCH    OPCCCTYP,LCSC2100,LCSC2200,LCSC2300
.
LCSC2100  ADD       OPCCNUMB,CCANC          * add to # cancelled
          GOTO      LCSC2400
.
LCSC2200  ADD       OPCCNUMB,CDLAY          * add to # delayed
          GOTO      LCSC2400
.
LCSC2300  ADD       OPCCNUMB,COVER          * add to # overtime
.
LCSC2400  BRANCH    OVRCD,LCSC2500          * write to temp file
.
.         update temp file C
.
          UPDATE    TEMPC;CCLIN,CNSES,CSEST,CNOPR,CTIMU,COVER,CDLAY,CCANC:
                          CPCAS,CPTIM,COCAS,COTIM,CSHAR
          GOTO      LCSC0200                * get next record
.
.         write to temp file C
.
LCSC2500  RESET     KEY6
          WRITE     TEMPC,KEY6;CCLIN,CNSES,CSEST,CNOPR,CTIMU,COVER,CDLAY,CCANC:
                               CPCAS,CPTIM,COCAS,COTIM,CSHAR
          GOTO      LCSC0200                * get next record
.
. ------------------------------------
. ------- maintain temp file D -------
. ------------------------------------
.
LCSC3000  MOVE      OPCCDOCT,KEY6           * key to temp file
          MOVE      OPCCDOCT,DCLIN          * clinic/surgeon
          CALL      CLRD0000                * clear variables
          MOVE      ZERO,OVRCD
          RESET     KEY6
          READ      TEMPD,KEY6;DCLIN,DAVLN,DAVLT,DUSEN,DUSET,DNPAT,DOVTI:
                          DPRNU,DPRTI,DOTNU,DOTTI,DNOPR,DOVNU,DDLAY,DCANC:
                          DSHAR
          CALL      OVERCOND IF OVER        * no record
.
          BRANCH    OPCCCTYP,LCSC3100,LCSC3200,LCSC3300
.                            canc     delayed  overtime
.
LCSC3100  ADD       OPCCNUMB,DAVLN          * add to number of available
          ADD       OPCCDURA,DAVLT          * add to total time available
          ADD       OPCCBRKT,DAVLT          * add to total time available
.
.         ADD       OPCCDURA,DUSET          * add total time canc to time used
          ADD       OPCCNUMB,DCANC          * add to number cancelled
          GOTO      LCSC3400
.
LCSC3200  ADD       OPCCNUMB,DDLAY          * add to number of delayed
          GOTO      LCSC3400
.
LCSC3300  ADD       OPCCDURA,DOVTI          * add to time overtime
          SUB       OPCCDURA,DUSET          * subtract overtime from to time use
          ADD       OPCCNUMB,DOVNU          * add to total overtime
.
LCSC3400  BRANCH    OVRCD,LCSC3500          * write to temp file
.
.         update temp file D
.
          UPDATE    TEMPD;DCLIN,DAVLN,DAVLT,DUSEN,DUSET,DNPAT,DOVTI:
                          DPRNU,DPRTI,DOTNU,DOTTI,DNOPR,DOVNU,DDLAY,DCANC:
                          DSHAR
          GOTO      LCSC0200                * get next record
.
.         write to temp file D
.
LCSC3500  RESET     KEY6
          WRITE     TEMPD,KEY6;DCLIN,DAVLN,DAVLT,DUSEN,DUSET,DNPAT,DOVTI:
                          DPRNU,DPRTI,DOTNU,DOTTI,DNOPR,DOVNU,DDLAY,DCANC:
                          DSHAR
          GOTO      LCSC0200                * get next record
.
LCSC9999  RETURN
+
.*********************************************************************
.*                  PAGE0000                    Called by : REPT0000 *
.*        Print the heading for a new page for the report            *
.********************************************************************* 
PAGE0000  CALL      HEAD132

          PRINT     *40,"Start Date : ",DIM7A,*65,DIM20A,SP2,PRFDATE:
                    *N,*40,"End   Date : ",DIM7B,*65,DIM20B,SP2,PRTDATE:
                    *N,*N,*40,*+,OPCNCDSC," Codes from : ";
.
          MATCH     SP6,STRTCOSC
          GOTO      PAGE0100 IF NOT EQUAL   * have a start code
.
          PRINT     "Start to ";
          GOTO      PAGE0200
.
PAGE0100  PRINT     STRTCOSC," to ";
.
PAGE0200  MATCH     "zzzzzz",STOPCOSC
          GOTO      PAGE0300 IF NOT EQUAL   * have an end code
.
          PRINT     "Finish"
          GOTO      PAGE0400
.
PAGE0300  PRINT     STOPCOSC
.
PAGE0400  ADD       "3",CLNO                * set the line counter
.
          BRANCH    OPCNR60A,PAGE1000,PAGE2000,PAGE3000
.
.         print the table heading for report type 1
.
PAGE1000  CALL      UND132
          PRINT     *1,R1L1PT1,R1L1PT2,R1L1PT3:
                    *N,R1L2PT1,R1L2PT2,R1L2PT3:
                    *N,R1L3PT1,R1L3PT2,R1L3PT3:
                    *N,R1L4PT1,R1L4PT2,R1L4PT3:
                    *N,"*-------------------------------------------------":
                       "--------------------------------":
                       "-------------------------------------------------*";
          ADD       "4",CLNO                * add to line counter
          GOTO      PAGE9999
.
.         print table heading for report type 2
.
PAGE2000  CALL      UND132
          PRINT     *1,R2L1PT1,R2L1PT2,R2L1PT3:
                    *N,R2L2PT1,R2L2PT2,R2L2PT3:
                    *N,R2L3PT1,R2L3PT2,R2L3PT3:
                    *N,R2L4PT1,R2L4PT2,R2L4PT3:
.
                    *N,"*-------------------------------------------------":
                       "--------------------------------":
                       "-------------------------------------------------*";
          ADD       "4",CLNO                * add to line counter
          GOTO      PAGE9999
.
.         print report heading 3
.
PAGE3000  CALL      UND132
          PRINT     *1,R3L1PT1,R3L1PT2,R3L1PT3:
                    *N,R3L2PT1,R3L2PT2,R3L2PT3:
                    *N,R3L3PT1,R3L3PT2,R3L3PT3:
                    *N,R3L4PT1,R3L4PT2,R3L4PT3
.
          CALL      UND132
          ADD       "5",CLNO                * add to line counter
.
PAGE9999  RETURN
+
.********************************************************************* 
.*                  GDES0000                    Called by : REPT0000 *
.*        Get the clinic/surgeon description                         *
.*        Para's  : CURRCLSU      current clinic/surgeon             * 
.*        Returns : OPCLDESC      the name/description               * 
.********************************************************************* 
GDES0000  MOVE      CURRCLSU,KEY6           * set up key
          PACK      OPCLDESC,INVCODE,CURRCLSU * incase of errors
          BRANCH    OPCNCLSU,GDES5000       * using doctors
.
.         using clinics
.
          CALL      RDOPCLI1
          BRANCH    OVRCD,GDES9999          * invalid clinic code
.
          MATCH     SP6,OPCLDOCT
          GOTO      GDES9999 IF EQUAL       * no doctor code
.
          MOVE      OPCLDOCT,KEY6           * use doctor code as key
.
.         using doctors
.
GDES5000  CALL      RDDOCT1
          BRANCH    OVRCD,GDES9999          * invalid code
.
          MOVE      DSNAME,PACSNAME
          MOVE      DGNAME,PACGNAME
          MOVE      SP70,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,OPCLDESC       * set up the name
.
GDES9999  RETURN
+
.*********************************************************************
.*                  PTPB0000                    Called by : REPT0000 *
.*        Print the details from temp file B                         *
.*        and update values on temp file Z                           *
.*        Para's  : CURRCLSU      current clinic/surgeon             * 
.*********************************************************************
PTPB0000  MOVE      ONE,FORM1               * indicate first op. type
.
          PACK      KEY9,CURRCLSU,SP3       * get at start of clinic
          READ      TEMPB,KEY9;BCLIN,BTYPE,BNOPR,BTIMU,BNCAS
.
.         read next item on temp file B
.
PTPB1000  READKS    TEMPB;BCLIN,BTYPE,BNOPR,BTIMU,BNCAS
          GOTO      PTPB9000 IF OVER        * end of file
.
          MATCH     BCLIN,CURRCLSU
          GOTO      PTPB9000 IF NOT EQUAL   * different clinic/surgeon
.
.         get operation type description
.
          PACK      TDESC,UNKNOWN,BTYPE,SP20
          PACK      KEY5,ANSO,ANSY,BTYPE
          CALL      RDCODE1
          MOVE      TDESC,KEY15
.
.         print the details
.
          IF        CLNO > 59
            CALL      PAGE0000
            PRINT     *N,"|",KEY16,"|",*18,"|",*25,"|",*33,"|",*40,"|":
                      *47,"|",*55,"|",*63,"|",*70,"|",*77,"|",*84,"|":
                      *91,"| ",KEY15,*110,"| ",BNCAS,*117,"|",BNOPR,*124,"|": 
                      BTIMU,*132,"|";
            GOTO      PTPB3000
          ENDIF
.
          BRANCH    FORM1,PTPB2000          * in first item
.
.         print when not first op. type -> print from start of line
.
          PRINT     *N,"|",*18,"|",*25,"|",*33,"|",*40,"|":
                    *47,"|",*55,"|",*63,"|",*70,"|",*77,"|",*84,"|":
                    *91,"| ",KEY15,*110,"| ",BNCAS,*117,"|",BNOPR,*124,"|":
                    BTIMU,*132,"|";
          GOTO      PTPB3000
.
.         print for the first operation type -> print from column 90
.
PTPB2000  MOVE      ZERO,FORM1              * signal done different print
          PRINT     *91,"| ",KEY15,*110,"| ",BNCAS,*117,"|",BNOPR,*124,"|":
                    BTIMU,*132,"|";
.
.         add to the totals
.
PTPB3000  ADD       ONE,CLNO                * add to line counter
          MOVE      BTYPE,KEY3
          MOVE      BTYPE,ZTYPE             * type
          MOVE      ZERO,ZNOPR              * number of operations
          MOVE      ZERO,ZTIMU              * time used
          MOVE      ZERO,ZNCAS
.
          RESET     KEY3
          READ      TEMPZ,KEY3;ZTYPE,ZNOPR,ZTIMU,ZNCAS
          GOTO      PTPB3100 IF OVER
.
.         update temp file Z
.
          ADD       BNOPR,ZNOPR             * number of perations
          ADD       BTIMU,ZTIMU             * time used
          ADD       BNCAS,ZNCAS
          UPDATE    TEMPZ;ZTYPE,ZNOPR,ZTIMU,ZNCAS
          GOTO      PTPB1000
.
.         write to temp file Z
.
PTPB3100  ADD       BNOPR,ZNOPR             * number of perations
          ADD       BTIMU,ZTIMU             * time used
          ADD       BNCAS,ZNCAS
          RESET     KEY3
          WRITE     TEMPZ,KEY3;ZTYPE,ZNOPR,ZTIMU,ZNCAS
          GOTO      PTPB1000
.
.         end of file
.
PTPB9000  COMPARE   ONE,FORM1
          GOTO      PTPB9999 IF NOT EQUAL   * have printed something
.
          PRINT     *91,"|",*110,"|",*117,"|",*124,"|",*132,"|";
.
PTPB9999  RETURN
+
.*********************************************************************
.*                  CLRA0000                    Called by : xxxx0000 *
.*        Clear stats variables for temp file A                      *
.*********************************************************************
CLRA0000  MOVE      ZERO,ANSES              * clear # of sessions used
          MOVE      ZERO,ASEST              * clear tot. session time booked
          MOVE      ZERO,ANOPR              * clear # of operations performed
          MOVE      ZERO,ATIMU              * clear tot. session time used
          MOVE      ZERO,AOVER              * clear # of sessions overtime
          MOVE      ZERO,ADLAY              * clear # of sessions delayed
          MOVE      ZERO,ACANC              * clear # of sessions cancelled
          MOVE      ZERO,ATSHR              * clear time shared
          MOVE      ZERO,ANCAS
CLRA9999  RETURN
+
.*********************************************************************
.*                  CLRC0000                    Called by : xxxx0000 *
.*        Clear stats variables for temp file C                      *
.*********************************************************************
CLRC0000  MOVE      ZERO,CNSES              * clear # of sessions used
          MOVE      ZERO,CSEST              * clear tot. session time booked
          MOVE      ZERO,CNOPR              * clear # of operations performed
          MOVE      ZERO,CTIMU              * clear tot. session time used
          MOVE      ZERO,COVER              * clear # of sessions overtime
          MOVE      ZERO,CDLAY              * clear # of sessions delayed
          MOVE      ZERO,CCANC              * clear # of sessions cancelled
          MOVE      ZERO,CPCAS              * clear # of private cases
          MOVE      ZERO,CPTIM              * clear time for private cases
          MOVE      ZERO,COCAS              * clear # when not own clinic
          MOVE      ZERO,COTIM              * clear time when not own clinic
          MOVE      ZERO,CSHAR              * clear total time shared minutes
CLRC9999  RETURN
+
.*********************************************************************
.*                  SUMM0000                    Called by : REPT8000 *
.*        Print the end of report summary                            *
.********************************************************************* 
SUMM0000  COMPARE   "60",CLNO
          CALL      PAGE0000 IF NOT LESS    * need a new page
.
          BRANCH    OPCNR60A,SUMM1000,SUMM2000,SUMM3000
          GOTO      SUMM9999
.
. -----------------------------------------
. ------- summary for report type 1 -------
. -----------------------------------------
.
SUMM1000  MOVE      TOTTM5,FORM7P5          * total time utilized
          DIV       TOTTM2,FORM7P5          * divide by total session time
          MULT      "100",FORM7P5           * get as a percentage
          MOVE      FORM7P5,PCENTA          * set up value
.
          PRINT     *N,"| Grand Total    |",TOTAL1,"|",TOTTM2,"|",TOTAL3:
                    "|",TOTAL4,"|",TOTTM5,"|",TOTTM6,"|",PCENTA,"|",TOTAL8:
                    "|",TOTAL9,"|",TOTAL10;
          ADD       "1",CLNO
.
.         print the totals of temp file Z
.
          CALL      PTPZ0000                * print temp file Z totals
          GOTO      SUMM9000
.
. -----------------------------------------
. ------- summary for report type 2 -------
. -----------------------------------------
.
SUMM2000  MOVE      TOTTM4,FORM7P5          * total time utilized
          DIV       TOTTM2,FORM7P5          * divide by total session time
          MULT      "100",FORM7P5           * get as a percentage
          MOVE      FORM7P5,PCENTA          * set up value
.
          MOVE      TOTTM11,FORM7P5         * total private patient time 
          DIV       TOTTM4,FORM7P5          * divide by total utilized time
          MULT      "100",FORM7P5           * get as a percentage
          MOVE      FORM7P5,PCENTB          * set up value
.
          MOVE      TOTTM14,FORM7P5         * total other users time 
          DIV       TOTTM4,FORM7P5          * divide by total utilized time
          MULT      "100",FORM7P5           * get as a percentage
          MOVE      FORM7P5,PCENTC          * set up value
.
          MOVE      TOTAL1,WNSES5 
          MOVE      TOTAL3,WNOPR5  
          MOVE      TOTAL7,WOVER5  
          MOVE      TOTAL8,WDLAY5
          MOVE      TOTAL9,WCANC5
          MOVE      TOTAL10,WPCAS5
          MOVE      TOTAL13,WOCAS5
.
          MOVE      TOTTM5,FORM6
.
          PRINT     *N,"|",*18,"Grand Total |",WNSES5,"|",TOTTM2:
                    "|",WNOPR5,"|",TOTTM4,"|",FORM6,"|",PCENTA,"|",WOVER5:
                    "|",WDLAY5,"|",WCANC5,"|",WPCAS5,"|",TOTTM11,"|",PCENTB:
                    "|",WOCAS5,"|",TOTTM14,"|",PCENTC,"|";
          PRINT     *N;
          ADD       "1",CLNO
          GOTO      SUMM9000
.
. -----------------------------------------
. ------- summary for report type 3 -------
. -----------------------------------------
.
SUMM3000  CALL      UND132
          MOVE      TOTTM4,FORM7P5          * get time used
          DIV       TOTTM2,FORM7P5          * divide by time avail.
          MULT      "100",FORM7P5           * get as a percentage
          MOVE      FORM7P5,PCENTD          * utilization %
.
          MOVE      TOTTM14,FORM7P5         * private time used
          MOVE      TOTTM4,FORM7            * used time
          ADD       TOTTM10,FORM7           * + overtime time
          DIV       FORM7,FORM7P5           * divide by sum
          MULT      "100",FORM7P5           * get as percentage
          MOVE      FORM7P5,PCENTE          * private util %
.
          MOVE      TOTTM17,FORM7P5         * other time used
          MOVE      TOTTM4,FORM7            * used time
          ADD       TOTTM10,FORM7           * + overtime time
          DIV       FORM7,FORM7P5           * divide by sum
          MULT      "100",FORM7P5           * get as percentage
          MOVE      FORM7P5,PCENTF          * other util %
.
          MOVE      TOTAL9,WOVNU5  
          MOVE      TOTTM10,FORM5
          MOVE      TOTAL11,WDLAY5
          MOVE      TOTAL12,WCANC5
          MOVE      TOTAL13,WPRNU5
          MOVE      TOTAL16,WOTNU5
          MOVE      TOTTM17,FORM6

.
          PRINT     "|Total |",TOTAL1,"|",TOTTM2,"|",TOTAL3,"|",TOTTM4:
                    "|",TOTTM5,"|",PCENTD,"|",TOTAL7,"|",TOTAL8,"|",WOVNU5:
                    "|",FORM5,"|",WDLAY5,"|",WCANC5,"|",WPRNU5,"|",TOTTM14:
                    "|",PCENTE,"|",WOTNU5,"|",FORM6,"|",PCENTF,"|"
          CALL      UND132
          CALL      SMMC0000                * summary for report C
          GOTO      SUMM9990
.
.         common summary details
.
SUMM9000   CALL     UND132
.
           COMPARE  "45",CLNO               * (Summary notes need 15 lines)
           CALL     PAGE0000 IF NOT LESS    * new page
.
           PRINT    *N,*N,"Number of Sess'n",*29,"= Number of Sessions booked":
                    " (booked and extra)":
                    *N,"Total Sess'n Time (Mins)",*29,"= Total booked time for":
                    " sessions - time for breaks":
                    " (booked and extra)":
                    *N,"Number of Oper's",*29,"= Number of operations ":
                    "performed in all sessions (booked and extra)":
                    *N,"Total Time Util (Mins)",*29,"= Total Utilisation time ":
                    "of sessions (booked and extra)":
                    *N,"Total time Shared",*29,"= Total shared/reallocation ":
                    "time (booked and extra)":
                    *N,"Util %",*29,"= Total Time Util / Total Sess'n Time":
                    " * 100":
                    *N,"Overtime",*29,"= Number of sessions that went overtime":
                    " (finished after scheduled end time)":
                    *N,"Delay",*29,"= Number of sessions that were delayed ":
                    "(started after the scheduled start time)":
                    *N,"Cancel",*29,"= Number of sessions that were cancelled":
                    *N,"Other",*29,"= Operating surgeon who is not the ":
                    "attending doctor":
                    *N,"Total Time Used (Mins)",*29,"= Operation duration ":
                    " for an operation type";
          ADD       "12",CLNO
.
          BRANCH    OPCNR60A,SUMM9990,SUMM9200
.
.         only for report type 2
.
SUMM9200  COMPARE   "50",CLNO               * (Summary notes need 10 lines)
          CALL      PAGE0000 IF NOT LESS    * need a new page
.
          PRINT     *N,"Number of Private pat's",*29,"= Number of Private ":
                    "patients operated on":
                    *N,"Time Used (Private)",*29,"= Theatre time used by ":
                    "private patients":
                    *N,"Private Patients Util %",*29,"= Private Patient time ":
                    "used / Total time util * 100":
                    *N,"Total time Shared",*29,"= Total shared/reallocation ":
                    "time (booked and extra)":
                    *N,"Number of Other pat's",*29,"= Number of Other ":
                    "patients operated on":
                    *N,"Time Used (Other)",*29,"= Theatre time used by ":
                    "Other patients":
                    *N,"Other Users Util %",*29,"= Other users time used / ":
                    "Total time util * 100":
                    *N,"Other Users",*29,"= Patients whose operating surgeon ":
                    "was not the attending doctor ";
          ADD       "8",CLNO
.
SUMM9990  PRINT     *N,*N,"*** End of Report ***",*N
          ADD       "2",CLNO
          CALL      DTMP0000
.
SUMM9999  RETURN
+
.*********************************************************************
.*                  SMMC0000                    Called by : SUMM3000 *
.*        Print the report summary for Layout 3                      *
.*********************************************************************
SMMC0000  COMPARE   "35",CLNO               * (Summary notes need 25 lines) 
          CALL      PAGE0000 IF NOT LESS    * need a new page
.
          PRINT     *N,"Total Number of Sess'n Avail   = ":
                    "Number of sessions booked + Number of sessions cancelled":
                    *N,"Total Sess'n Time Avail (Mins) = ":
                    "Total time available (including time for breaks) + ":
                    "Total time cancelled":
                    *N,"Total Number of Sess'n Used    = ":
                    "Number of sessions used":
                    *N,"Total Sess'n Time Used (Mins)  = ":
                    "Total time used - Total time overtime":
                    *N,"Util %                         = ":
                    *N,"Total time Shared",*29,"= Total shared/reallocation ":
                    "time (booked and extra)":
                    "[(Total time used - Total time overtime) / Total time ":
                    "available] * 100":
                    *N,"Number of Pat's                = ":
                    "Number of patients serviced by the ",OPCNCDSC:
                    *N,"Number of Oper's               = ":
                    "Number of operations performed by the ",OPCNCDSC:
                    *N,"Number of Sess'n Overtime      = ":
                    "Number of sessions where the session ended after the ":
                    "expected session end time":
                    *N,"Total Sess'n Overtime (Mins)   = ":
                    "Total time after the expected session times":
                    *N,"Number of Sess'n Delay         = ":
                    "Number of sessions where the sessions starts after the ":
                    "expected session start time":
                    *N,"Number of Sess'n Canc          = ":
                    "Number of times when sessions were cancelled":
                    *N,*N,"PRIVATE PATIENTS (Private patients whose attending ":
                    "doctor is the doctor associated with the clinic":
                    *N,"Number of Pat's                = ":
                    "Number of patients serviced by the clinic":
                    " who were classified as private (as defined above)":
                    *N,"Time Used                      = ":
                    "Time used by private patients":
                    *N,"Util %                         = ":
                    "[Time used by private patients / Total time used] * 100":
                    *N,*N,"OTHER PATIENTS":
                    *N,"Number of Pat's                = ":
                    "Number of patients whose attending doctor was not the":
                    " doctor associated with the clinic":
                    *N,"Time Used                      = ":
                    "Time used by patients as defined above":
                    *N,"Util %                         = ":
                    "[Time used by patients as defined above / ":
                    "Total time used] ":
                    "* 100"
          ADD       "23",CLNO
.
SMMC9999  RETURN
+
.*********************************************************************
.*                  PTPZ0000                    Called by : SUMM1000 *
.*        Print all the totals for each Operation Type               *
.*        by looping through temp file Z                             *
.*********************************************************************
PTPZ0000  MOVE      ONE,FORM1               * indicate first op. type
.
          MOVE      SP3,KEY3                * get at start of file
          READ      TEMPZ,KEY3;ZTYPE,ZNOPR,ZTIMU,ZNCAS
.
.         read next item on temp file B
.
PTPZ1000  READKS    TEMPZ;ZTYPE,ZNOPR,ZTIMU,ZNCAS
          GOTO      PTPZ9000 IF OVER        * end of file
.
.         get operation type description
.
          PACK      TDESC,UNKNOWN,ZTYPE,SP20
          PACK      KEY5,ANSO,ANSY,ZTYPE
          CALL      RDCODE1
          MOVE      TDESC,KEY15
.
.         print the details
.
          BRANCH    FORM1,PTPZ2000          * in first item
.
          COMPARE   "60",CLNO
          GOTO      PTPZ1500 IF LESS  
.
          CALL      PAGE0000
          PRINT     *N;
.
PTPZ1500
.         print when not first op. type -> print from start of line
.
          PRINT     "|",*18,"|",*25,"|",*33,"|",*40,"|":
                    *47,"|",*55,"|",*63,"|",*70,"|",*77,"|",*84,"|":
                    *91,"| ",KEY15,*110,"|",ZNCAS,*117,"|",ZNOPR,*124,"|": 
                    ZTIMU,*132,"|"
          ADD       "1",CLNO
          GOTO      PTPZ1000
.
.         print for the first operation type -> print from column 90
.
PTPZ2000  MOVE      ZERO,FORM1              * signal done different print
          PRINT     *91,"| ",KEY15,*110,"|",ZNCAS,*117,"|",ZNOPR,*124,"|": 
                    ZTIMU,*132,"|"
          GOTO      PTPZ1000
.
.         end of file
.
PTPZ9000  COMPARE   ONE,FORM1
          GOTO      PTPZ9999 IF NOT EQUAL   * have printed something
.
          PRINT     *91,"| ",*110,"|",*117,"|",*124,"|",*132,"|"
.
PTPZ9999  RETURN
+
.*********************************************************************
.*                  CLRD0000                    Called by : xxxx0000 *
.*        Clear stats variables for temp file D                      *
.*********************************************************************
CLRD0000  MOVE      ZERO,DAVLN              * total number of avail sessions
          MOVE      ZERO,DAVLT              * total time used for avail sessions
          MOVE      ZERO,DUSEN              * total number of used sessions
          MOVE      ZERO,DUSET              * total time used for used sessions
          MOVE      ZERO,DNPAT              * number of patients
          MOVE      ZERO,DOVNU              * number of overtime sessions
          MOVE      ZERO,DOVTI              * total time for overtime
          MOVE      ZERO,DDLAY              * number of delayed sessions
          MOVE      ZERO,DCANC              * number of delayed sessions
          MOVE      ZERO,DPRNU              * Private patients - number
          MOVE      ZERO,DPRTI              * Private patients - time
          MOVE      ZERO,DOTNU              * Other   patients - number
          MOVE      ZERO,DOTTI              * Other   patients - time
          MOVE      ZERO,DNOPR              * Number of operations
          MOVE      ZERO,DSHAR              * Total session time shared
CLRD9999  RETURN
+
.*********************************************************************
.*                  CLTOT000                    Called by : xxxx0000 *
.*        Clear all total variables                                  *
.*********************************************************************
CLTOT000  MOVE      ZERO,TOTAL1
          MOVE      ZERO,TOTAL2
          MOVE      ZERO,TOTAL3
          MOVE      ZERO,TOTAL4
          MOVE      ZERO,TOTAL5
          MOVE      ZERO,TOTAL6
          MOVE      ZERO,TOTAL7
          MOVE      ZERO,TOTAL8
          MOVE      ZERO,TOTAL9
          MOVE      ZERO,TOTAL10
          MOVE      ZERO,TOTAL11
          MOVE      ZERO,TOTAL12
          MOVE      ZERO,TOTAL13
          MOVE      ZERO,TOTAL14
          MOVE      ZERO,TOTAL15
          MOVE      ZERO,TOTAL16
          MOVE      ZERO,TOTAL17
          MOVE      ZERO,TOTTM1
          MOVE      ZERO,TOTTM2
          MOVE      ZERO,TOTTM3
          MOVE      ZERO,TOTTM4
          MOVE      ZERO,TOTTM5
          MOVE      ZERO,TOTTM6
          MOVE      ZERO,TOTTM7
          MOVE      ZERO,TOTTM8
          MOVE      ZERO,TOTTM9
          MOVE      ZERO,TOTTM10
          MOVE      ZERO,TOTTM11
          MOVE      ZERO,TOTTM12
          MOVE      ZERO,TOTTM13
          MOVE      ZERO,TOTTM14
          MOVE      ZERO,TOTTM15
          MOVE      ZERO,TOTTM16
          MOVE      ZERO,TOTTM17
CLRT9999  RETURN
+
.
. ------- I/O includes needed -------
.
          INC       STD001IO                * all standard routines
          INC       OPRCLIDS                * ? for clinic file
          INC       TFILENAM                * Generate Temp File Name
          INC       IBASEQIO/INC            * Sequential Numbers File
          INC       WEBERRIO/INC            * Web Server Error Logging
          INC       OPRCLIIO/INC            * clinic file
          INC       OPRCOUIO/INC            * clinic operation usage file
          INC       OPRCSCIO/INC            * clinic session cancellation file
          INC       OPRCSUIO/INC            * clinic session usage file
. 
          INC       PATCOMN3                * holds KEYPER
          INC       PATDOCKY                * ? for doctor file
          INC       PATCODIO/INC            * codes file
          INC       PATDO1IO/INC            * doctor file
          INC       PATDRGIO/INC            * date range file
................................................................................

.*****************************************************************************
.*    Program      : IBAMEH59                                                *
.*    Description  : MH Statistic by Ward                                    *
.*                                                                           *
.*    Author       : ROD    (10/01/92)                                       *
.*    Function     : List stats generated by stat update program by Ward     *
.*                                                                           *
.*    Modifications:                                                         * 
.*       V10.05.01  01/01/2015  Ania P                      CAR 261630       *
.*                  Removed use of PORT for temp file naming                 *
.*        V9.02.00  26/10/2002  Lina Vo     SRF 22733                        *
.*                  Ported to V9                                             *
.*****************************************************************************
.*       V5.01.002  31/07/92  ROD   (still haven't had reviews)              * 
.*                  If ward has ALL zero figures dont print it               *
.*       V5.01.003  03/06/94  Ian Rutt                                       *
.*                  Fixed Global Recompile                                   *
.*                                                                           *
.*****************************************************************************
.
          INC       STD001FD
          INC       PATCODFD/INC
          INC       PATDRGFD/INC
          INC       PATWR1FD/INC
          INC       MEHWSSFD/INC
          INC       IBASEQFD/INC  
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
.
AVELOS    FORM      4.1
CDYSDAYS  FORM      6
CDYSFDTE  DIM       8
CDYSTDTE  DIM       8
CDYSYEAR  FORM      2
CMDLINE   DIM       80
CPTDATE   DIM       8
CURRDATE  DIM       8
DAYSINP   FORM      2
ERRMESS   INIT      ":Missing code"
GRNABEDS  FORM      4
GRNAVLOS  FORM      4.1
GRNBDSL   FORM      7
GRNBDXSL  FORM      7
GRNCADM   FORM      6
GRNDEAD   FORM      6
GRNDSCH   FORM      6
GRNIADM   FORM      6
GRNLVE    FORM      6
GRNOISL   FORM      3.2
GRNOXSL   FORM      3.2
GRNRETS   FORM      6
GRNSADM   FORM      6
GRNTOOUT  FORM      6
GRNTOTIN  FORM      6
GRNTRANO  FORM      6
NEWWARDC  DIM       3
OCCINSL   FORM      3.2
OCCEXSL   FORM      3.2
PRTFDTE   DIM       10
PRTTDTE   DIM       10
REQPER    DIM       6
SEPBDAYS  FORM      8
SUBABEDS  FORM      4
SUBBDSL   FORM      7
SUBBDXSL  FORM      7
SUBCADM   FORM      6
SUBDEAD   FORM      6
SUBDSCH   FORM      6
SUBIADM   FORM      6
SUBLVE    FORM      6
SUBRETS   FORM      6
SUBSADM   FORM      6
SUBTOOUT  FORM      6
SUBTOTIN  FORM      6
TEMPTOT   FORM      7
TMPAVLOS  FORM      4.1
TMPISL    FORM      3.2
TMPXSL    FORM      3.2
TOTALIN   FORM      6
TOTALOUT  FORM      6
TOTBEXSL  FORM      7
TOTBINSL  FORM      7
TOTLVE    FORM      6
TOTRETS   FORM      6
TOTSEPBD  FORM      8
TRANSOUT  FORM      6
.
. Temp file vars
MEHTMPA1  IFILE     SQL, FIXED=7, KEY="U1-3,4-6"
XXWCLSS   DIM       3         1       Ward Classification
XXWARD    DIM       3         4       Ward
.              Rec length     7
.
PRGID     INIT      "IBAMEH59"
PRGNAM    INIT      "Mh Statistics By Ward"
VERSION   INIT      "V12.01.00"
.
.****************************************************************************
.* ML0000  :  Mainline code                                                 *
.****************************************************************************
ML0000    CALL      INIT0000                      * initialisation
          CALL      CRET0000                      * create temp file
.
ML1000    CALL      MENU0000                      * get menu options
          BRANCH    EXIT,ML9999                   * Exit selected
.
ML2000    CALL      GPER0000                      * Get Period
          BRANCH    EXIT,ML1000                   * nothing input
.
          CALL      CONTQST                       * Ask Continue?
          BRANCH    CEXIT,ML5000,ML2000,ML1000    * Yes, No, Cancel
.
ML5000    CALL      LOAD0000                      * load data into temp file
.
          CALL      RPRT0000                      * Print Report
          CALL      CRET0000                      * create/erase temp file
          GOTO      ML2000
.
ML9999    CHAIN     PGM
+
.****************************************************************************
.* INIT0000  :  Initialisation                                              *
.****************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P60:24,"controlf"
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P60:24,"patdrgaf"
          OPEN      PATDRGA3,"patdrgaf"
.
          DISPLAY   *P60:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P60:24,"mehwssaf"
          OPEN      MEHWSSA1,"mehwssaf"
.
          DISPLAY   *P60:24,"patwr1af"
          OPEN      PATWR1A1,"patwr1af"
.
          CALL      TFILENAM
.
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
INIT9999  RETURN
+
.****************************************************************************
.* MENU0000  :  Main Menu                                                   *
.****************************************************************************
MENU0000  DISPLAY   *P1:4,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = By Ward"
.
MENU1000  KEYIN     *P1:7,"Select Option ? ":
                    *P17:7,*DV,UNDLN1:
                    *P17:7,*V2LON,OPTION
.
          IF        OPTION = 0
                      MOVE   ONE,EXIT
                      GOTO   MENU9999
          ENDIF
.
          MOVE      ZERO,EXIT
          BRANCH    OPTION,MENU9999
          BEEP
          GOTO      MENU1000
.
MENU9999  RETURN
+
.****************************************************************************
.*  GPER0000  :  Get Period for report                                      *
.****************************************************************************
GPER0000  DISPLAY   *P1:9,"Enter the report period :"
.
. Set up KEYPER parametres and default period
.
GPER0050  DISPLAY   *P27:9,*EF
          MOVE      NINE,CVERT
          MOVE      TWENTY7,CCOL
          MOVE      TWENTY4,EVERT
          MOVE      ONE,ECOL 
          MOVE      ZERO,CHIGHLT   
          
.         PACK      CURRDATE,CCC,CYY,CMM,CDD
.         REP       " 0",CURRDATE
.         CALL      GPERD                         * get default period
.         BRANCH    EXIT,GPER0100
.
.         PACK      KEY6,DRGCYR,DRGCNUM
.         CALL      RDSDRGA3
.         CALL      RDPDRGA3
.         BRANCH    OVRCD,GPER0100
.
.         PACK      REQPER,DRGCYR,DRGCNUM
          UNPACK    SP6,CCENT,CYEAR,CMON
          MOVE      CCC,CCENT
          CALL      KEYPER                        * keyin the period
          BRANCH    CQUEST,GPER0050
          BRANCH    OVRCD,GPER9000
          GOTO      GPER1000
.
. period not on file error
.
GPER0100  DISPLAY   *P1:24,*V2LON,"Period not on file.  ";
          CALL      HITENTER
          MOVE      TRUE,EXIT
          GOTO      GPER9999
.
GPER1000  PACK      KEY6,CCENT,CYEAR,CMON
          CALL      RDDRGA3                      * valid REQPER?
          BRANCH    OVRCD,GPER3000
          PACK      REQPER,DRGYR,DRGNUM
.
. check if period keyed in is less than current period
.
          REP       " 0",DRGTDTE
          MATCH     CURRDATE,DRGTDTE
          GOTO      GPER1500 IF LESS              * Ok.
          GOTO      GPER8000                      * error
.
. set up to and from dates to print
.
GPER1500  UNPACK    DRGFDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,PRTFDTE
.
          UNPACK    DRGTDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,PRTTDTE
          CALL      DINP0000                      * get no. days in period
.
. display long desciption and from and to dates
.
          DISPLAY   *P38:9,*V2LON,DRGLDSC:
                    *P38:10,PRTFDTE," to ",PRTTDTE
          MOVE      ZERO,EXIT
          GOTO      GPER9999
.
GPER3000  DISPLAY   *P1:24,*B,*V2LON,"Invalid Period.  ";
          CALL      HITENTER
          GOTO      GPER0050
.
. date keyed in was >= current period
.
GPER8000  DISPLAY   *P1:24,*EL,*B,"Must be less than current period.  ";
          CALL      HITENTER
          GOTO      GPER0050
.
. no REQPER entered or cancel so exit = true
.
GPER9000  MOVE      TRUE,EXIT
.
GPER9999  RETURN          
+
.***************************************************************************
.*  DINP0000  :  Calculate no. days in period                              *
.***************************************************************************
DINP0000  MOVE      DRGFDTE,CDYSFDTE
          MOVE      DRGTDTE,CDYSTDTE
.
          CALL      CALCDAYS
.
          MOVE      CDYSDAYS,DAYSINP
.
DINP9999  RETURN
+
.***************************************************************************
.*  LOAD0000  :  Load data into temp file                                  *
.***************************************************************************
LOAD0000  DISPLAY   *P1:24,*EL,*P30:24,"Loading data... [        ]"
.
          PACK      KEY9,REQPER,SP10
          CALL      RSMHWSS1
LOAD1000  CALL      RKMHWSS1                      * get next record
          BRANCH    OVRCD,LOAD9000                * no more records
.
          MATCH     REQPER,MHWSDATE               * still same period?
          GOTO      LOAD9000 IF NOT EQUAL
.
. check if this ward has zero figures (if so ignore this ward)
.
          ASSIGN    (MHWSADMI+MHWSADMS+MHWSADMC+MHWSRETH+MHWSRETT),TEMPTOT
          ASSIGN    (TEMPTOT+MHWSRETA+MHWSLVEH+MHWSLVET+MHWSLVEA),TEMPTOT
          ASSIGN    (TEMPTOT+MHWSDSCH+MHWSDEAD+MHWSINBD),TEMPTOT
          COMPARE   ZERO,TEMPTOT                  * if zero, ignore
          GOTO      LOAD1000 IF EQUAL
.
          CALL      IDAT0000                      * init temp vars
.
. get Ward Classification
.
          PACK      KEY6,MHWSWARD,SP3
          CALL      RDWARD1
          BRANCH    OVRCD,LOAD1000
.
          MOVE      WCLASS,XXWCLSS                * ward classification
          MOVE      MHWSWARD,XXWARD               * ward
.
. now post details to temp file
.
          DISPLAY   *P47:24,MHWSWARD;
.
          PACK      KEY6,XXWCLSS,XXWARD
          CALL      WRTEMP1
          GOTO      LOAD1000
.
LOAD9000  DISPLAY   *P1:24,*EL
.
LOAD9999  RETURN
+
.****************************************************************************
.* RPRT0000  :  Print Report                                                *
.****************************************************************************
RPRT0000  DISPLAY   *P30:24,"Printing...";
.
          CALL      IBACLOCK                      * initialise a few things
          MOVE      ZERO,CPAGENO
          MOVE      ONE,CNOUNDLN
          MOVE      TEN,CLNO                      * line counter
          MOVE      "~~~",NEWWARDC                * init to non blank
          CALL      IDAT0000                      * init data
.
          CALL      HEAD132                       * Print 3 line header
          PRINT     *40,"Period : ",DRGNUM,"/",DRGYR,SP2,"(",PRTFDTE," to ":
                    PRTTDTE,")",*N
          CALL      UND132
          CALL      PTHD0000                      * print table headings
          CALL      UND132
.
. Now loop thru temp file and print data
.
          PACK      KEY6,SP6
          CALL      RSTEMP1
RPRT1000  CALL      RKTEMP1
          BRANCH    OVRCD,RPRT8000                * no more records
.
          COMPARE   "55",CLNO                     * need a new page?
          GOTO      RPRT2000 IF LESS
.
. set up for new page
.
          CALL      HEAD132
          PRINT     *40,"Period : ",DRGNUM,"/",DRGYR,SP2,"(",PRTFDTE," to ":
                    PRTTDTE,")",*N
          CALL      UND132
          CALL      PTHD0000                      * print table headings
          CALL      UND132
          MOVE      TEN,CLNO
.
. get details for printing
.
RPRT2000  PACK      WBDESC,XXWARD,ERRMESS         * default error desc
          PACK      KEY6,XXWARD,SP3               * get ward desc.
          CALL      RDWARD1
.
          PACK      KEY9,REQPER,XXWARD            * get stats
          CALL      RDMHWSS1
          BRANCH    OVRCD,RPRT1000                * should never occur
.
          ASSIGN    (MHWSRETH + MHWSRETT + MHWSRETA),TOTRETS
          ASSIGN    (MHWSADMI + MHWSADMS + MHWSADMC + TOTRETS),TOTALIN
          ASSIGN    (MHWSLVEH + MHWSLVET + MHWSLVEA),TOTLVE
          ASSIGN    (TOTLVE + MHWSDSCH + MHWSDEAD),TOTALOUT
          ASSIGN    (MHWSINBD+MHWSHLBD+MHWSTLBD+MHWSAWBD+MHWSSLBD),TOTBINSL
          ASSIGN    (MHWSINBD+MHWSHLBD+MHWSTLBD+MHWSAWBD),TOTBEXSL
          MOVE      ZERO,OCCINSL
          MOVE      ZERO,OCCEXSL
          IF        !((MHWSBEDS*DAYSINP) = 0)
            ASSIGN    (TOTBINSL/(MHWSBEDS*DAYSINP))*100,OCCINSL
            ASSIGN    (TOTBEXSL/(MHWSBEDS*DAYSINP))*100,OCCEXSL
          ENDIF
          MOVE      ZERO,AVELOS
          IF        !((TOTALOUT+MHWSTRNO) = 0)
            ASSIGN    (MHWSSEPB+MHWSSSLB)/(TOTALOUT+MHWSTRNO),AVELOS
          ENDIF
.
. add up data for GRAND totals
.
          ADD       MHWSBEDS,GRNABEDS             * avail beds grand total
          ADD       MHWSADMI,GRNIADM              * inf admiss. grand total
          ADD       MHWSADMS,GRNSADM              * special admiss. grand total
          ADD       MHWSADMC,GRNCADM              * committed admiss. grand tot
          ADD       TOTRETS,GRNRETS               * returns grand total
          ADD       TOTALIN,GRNTOTIN              * total in grand total
          ADD       TOTLVE,GRNLVE                 * leave grand total
          ADD       MHWSDSCH,GRNDSCH              * discharges grand total
          ADD       MHWSDEAD,GRNDEAD              * deaths grand total
          ADD       TOTALOUT,GRNTOOUT             * total out grand total
          ADD       TOTBINSL,GRNBDSL              * bed days inc S/L grand total
          ADD       TOTBEXSL,GRNBDXSL             * bed days exc S/L grand total
          ASSIGN    (TOTSEPBD+MHWSSEPB+MHWSSSLB),TOTSEPBD
          ADD       MHWSTRNO,GRNTRANO             * grand total transfers out
.
. check if we need to print sub totals
.
          MATCH     "~~~",NEWWARDC                * initially?
          GOTO      RPRT4500 IF EQUAL
.
          MATCH     XXWCLSS,NEWWARDC              * still same ward classif.?
          GOTO      RPRT5000 IF EQUAL
.
          CALL      UND132
          CALL      PSUB0000                      * print sub-totals
          CALL      PEQL0000                      * print =========='s
          CALL      ISUB0000                      * initialise sub totals
RPRT4500  MOVE      XXWCLSS,NEWWARDC              * save ward class
.
. print details
.
RPRT5000  PRINT     *1,"| ",WBDESC,*24,"|",MHWSBEDS,*30,"|",MHWSADMI,"|":
                    MHWSADMS,"|",MHWSADMC,"|",TOTRETS,"|",TOTALIN,"|",TOTLVE:
                    "|",MHWSDSCH,"|",MHWSDEAD,"|",TOTALOUT,"|",TOTBINSL:
                    "|",TOTBEXSL,"|",OCCINSL," |",OCCEXSL," |",AVELOS,"|"
.
          CALL      CSUB0000                      * increment sub-totals
          ADD       ONE,CLNO
          GOTO      RPRT1000
.
. we have finished printing data so print end of report details
.
RPRT8000  CALL      UND132
          CALL      PSUB0000                      * print sub-totals
          CALL      PEQL0000                      * print =========='s
          CALL      PGRN0000                      * print grand totals
          CALL      PEQL0000                      * print =========='s
          CALL      PSUM0000                      * print summary details
.
RPRT9999  RETURN
+
.**************************************************************************
.*  CSUB0000  :  Calculate Sub totals                                     *
.**************************************************************************
CSUB0000  ADD       MHWSBEDS,SUBABEDS             * avail beds sub total
          ADD       MHWSADMI,SUBIADM              * inf admiss. sub total
          ADD       MHWSADMS,SUBSADM              * special admiss. sub total
          ADD       MHWSADMC,SUBCADM              * committed admiss. sub total
          ADD       TOTRETS,SUBRETS               * returns sub total
          ADD       TOTALIN,SUBTOTIN              * total in sub total
          ADD       TOTLVE,SUBLVE                 * leave sub total
          ADD       MHWSDSCH,SUBDSCH              * discharges sub total
          ADD       MHWSDEAD,SUBDEAD              * deaths sub total
          ADD       TOTALOUT,SUBTOOUT             * total out sub total
          ADD       TOTBINSL,SUBBDSL              * bed days inc S/L sub total
          ADD       TOTBEXSL,SUBBDXSL             * bed days exc S/L sub total
          ASSIGN    (SEPBDAYS+(MHWSSEPB+MHWSSSLB)),SEPBDAYS
          ADD       MHWSTRNO,TRANSOUT             * transfers out
.
CSUB9999  RETURN
+
.**************************************************************************
.*  PEQL0000  :  Print a 132 line of equal signs                          *
.**************************************************************************
PEQL0000  PRINT     *1,"*================================":
                       "=================================":
                       "=================================":
                       "================================*"
.
PEQL9999  RETURN
+
.**************************************************************************
.*  PSUB0000  :  Print Sub-Totals for a Ward classification               *
.**************************************************************************
PSUB0000  PACK      TDESC,NEWWARDC,ERRMESS        * default error desc
          PACK      KEY5,ANSC,ANSG,NEWWARDC       * get ward classif. desc.
          CALL      RDCODE1
.
. calc % fields
. do the following check because if div by 0 '999.99' appears in field
.
          MOVE      ZERO,TMPISL
          MOVE      ZERO,TMPXSL
          IF        !((SUBABEDS*DAYSINP) = 0)
            ASSIGN    (SUBBDSL/(SUBABEDS*DAYSINP)*100),TMPISL  * Occup % Inc SL
            ASSIGN    (SUBBDXSL/(SUBABEDS*DAYSINP)*100),TMPXSL * Occup % Exc SL
          ENDIF
.
. calc Ave LOS (sub total)
.
          MOVE      ZERO,TMPAVLOS
          IF        !((SUBTOOUT+TRANSOUT) = 0)
            ASSIGN    (SEPBDAYS/(SUBTOOUT+TRANSOUT)),TMPAVLOS    * Ave LOS
          ENDIF
.
. print sub totals
.
          PRINT     *1,"| ",TDESC,*24,"|",SUBABEDS,*30,"|",SUBIADM,"|",SUBSADM:
                    "|",SUBCADM,"|",SUBRETS,"|",SUBTOTIN,"|",SUBLVE:
                    "|",SUBDSCH,"|",SUBDEAD,"|",SUBTOOUT,"|",SUBBDSL:
                    "|",SUBBDXSL,"|",TMPISL," |",TMPXSL," |",TMPAVLOS,"|"
.
PSUB9999  RETURN
+
.**************************************************************************
.*  PGRN0000  :  Print GRAND totals                                       *
.**************************************************************************
PGRN0000
.
. calc % fields
. do the following check because if div by 0 '999.99' appears in field
.
          MOVE      ZERO,GRNOISL
          MOVE      ZERO,GRNOXSL
          IF        !((GRNABEDS*DAYSINP) = 0)
            ASSIGN    (GRNBDSL/(GRNABEDS*DAYSINP)*100),GRNOISL  * Occu % Inc SL
            ASSIGN    (GRNBDXSL/(GRNABEDS*DAYSINP)*100),GRNOXSL * Occu % Exc SL
          ENDIF
.
. calc Ave LOS (grand total)
.
          MOVE      ZERO,GRNAVLOS
          IF        !((GRNTOOUT+GRNTRANO) = 0)
            ASSIGN    (TOTSEPBD/(GRNTOOUT+GRNTRANO)),GRNAVLOS    * Ave LOS
          ENDIF
.
. print Grand totals
.
          PRINT     *1,"| GRAND TOTALS",*24,"|",GRNABEDS,*30,"|",GRNIADM,"|":
                    GRNSADM,"|",GRNCADM,"|",GRNRETS,"|",GRNTOTIN,"|",GRNLVE:
                    "|",GRNDSCH,"|",GRNDEAD,"|",GRNTOOUT,"|",GRNBDSL:
                    "|",GRNBDXSL,"|",GRNOISL," |",GRNOXSL," |",GRNAVLOS,"|"
.
PGRN9999  RETURN
+
.**************************************************************************
.*  PSUM0000  :  Print Summary details                                    *
.**************************************************************************
PSUM0000  PRINT     *N,*N,*17,"Bed Days   *   100.00",*74,"Separation Days ":
                    "(including Short Leave)":
                    *N,*1,"Occupancy %  =  ---------------------------------":
                    *57,"Average Stay  =  ---------------------------------":
                    "------":
                    *N,*17,"Available Beds * # Days in Period",*82,"Number ":
                    "of Separations":
                    *N,*N,*1,"Separations = Discharges + Deaths + Holiday ":
                    "Leave + Trial Leave + AWOL + Transfers Out"
.
          PRINT     *N,"***  End of Report ***"
.
PSUM9999  RETURN
+
.**************************************************************************
.*  ISUB0000  :  Initialise Sub totals                                    *
.**************************************************************************
ISUB0000  MOVE      ZERO,SUBABEDS                 * avail beds sub total
          MOVE      ZERO,SUBIADM                  * inf admiss. sub total
          MOVE      ZERO,SUBSADM                  * special admiss. sub total
          MOVE      ZERO,SUBCADM                  * committed admiss. sub total
          MOVE      ZERO,SUBRETS                  * returns sub total
          MOVE      ZERO,SUBTOTIN                 * total in sub total
          MOVE      ZERO,SUBLVE                   * leave sub total
          MOVE      ZERO,SUBDSCH                  * discharges sub total
          MOVE      ZERO,SUBDEAD                  * deaths sub total
          MOVE      ZERO,SUBTOOUT                 * total out sub total
          MOVE      ZERO,SUBBDSL                  * bed days inc S/L sub total
          MOVE      ZERO,SUBBDXSL                 * bed days exc S/L sub total
.
          MOVE      ZERO,TMPXSL                   * sub tot bed days x SL
          MOVE      ZERO,TMPISL                   * sub tot bed days inc SL
          MOVE      ZERO,SEPBDAYS                 * separation bed days
          MOVE      ZERO,SUBTOOUT
          MOVE      ZERO,TRANSOUT                 * total trans out for Wrd cls
          MOVE      ZERO,TMPAVLOS                 * ave LOS sub tot
.
ISUB9999  RETURN
+
.**************************************************************************
.*  PTHD0000  :  Print Table Heading                                      *
.**************************************************************************
PTHD0000  PRINT     *1,"|",*24,"|Avail|---- Admissions ----|Replce| Total|":
                    "    To| Disch|      | Total|Pat Day|Pat Day|Occup %":
                    "|Occup %| Aver |":
                    *N,*1,"| Ward",*24,"| Beds|   Inf|  Spec|Commit| Leave|":
                    "    In| Leave|x Dths|Deaths|   Out|Inc S/L|Exc S/L|":
                    "Inc S/L|Exc S/L| Stay |"
.
PTHD9999  RETURN
+
.*************************************************************************
.*  IDAT0000  :  initialise vars                                         *
.*************************************************************************
IDAT0000  CALL      ISUB0000                      * init sub totals
.
          MOVE      ZERO,GRNABEDS                 * avail beds grand total
          MOVE      ZERO,GRNIADM                  * inf admiss. grand total
          MOVE      ZERO,GRNSADM                  * special admiss. grand total
          MOVE      ZERO,GRNCADM                  * committed admiss. grand tot
          MOVE      ZERO,GRNRETS                  * returns grand total
          MOVE      ZERO,GRNTOTIN                 * total in grand total
          MOVE      ZERO,GRNLVE                   * leave grand total
          MOVE      ZERO,GRNDSCH                  * discharges grand total
          MOVE      ZERO,GRNDEAD                  * deaths grand total
          MOVE      ZERO,GRNTOOUT                 * total out grand total
          MOVE      ZERO,GRNBDSL                  * bed days inc S/L grand total
          MOVE      ZERO,GRNBDXSL                 * bed days exc S/L grand total
          MOVE      ZERO,TOTSEPBD                 * total separation bed days
          MOVE      ZERO,GRNTRANO                 * total transfers out
.
IDAT9999  RETURN
+
.*************************************************************************
.*  KILL0000  :  erase temp file                                         *
.*************************************************************************
KILL0000  CLOSE     MEHTMPA1
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    TFILNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
KILL9999  RETURN
+
.*************************************************************************
.*                             CRET0000            Called by:            *
.*                      create temporary file                            *
.*************************************************************************
CRET0000  CALL      KILL0000 
          CLEAR     CMDLINE 
          APPEND    "isbuild ",CMDLINE
          APPEND    TFILNAME,CMDLINE
          APPEND    " 7 U1-3,4-6",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      MEHTMPA1,TFILNAME
.
CRET9999  RETURN
+
.***************************************************************************
.*  Temp file I/O's                                                        *
.***************************************************************************
RSTEMP1   RESET     KEY6
          READ      MEHTMPA1,KEY6;;
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    MEHTMPA1;XXWCLSS,XXWARD
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   RESET     KEY6
          WRITE     MEHTMPA1,KEY6;XXWCLSS,XXWARD
          RETURN
.
.
          INC       STD001IO
          INC       CALCDAYS
          INC       PATCOMN3
          INC       PATCODIO/INC
          INC       PATDRGIO/INC
          INC       PATWR1IO/INC
          INC       MEHWSSIO/INC
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC

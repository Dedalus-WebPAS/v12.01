.******************************************************************************
.* System   : Accident & Emergency                                            *
.* Program  : IBAAAE28                                                        *
.* Desc     : A&E MBS Charges Master Listing                                  *
.******************************************************************************
.* Date     : 09/05/90                                                        *
.* Author   : Eric Phan                                                       *
.* Function : This program prints a master listing of A&E MBS Charges details.*
.* Mods     :                                                                 *
.*        V11.00.01 04/03/2020  J.Tan          TSK 0838262                    *
.*                  Added Effective from and to date to MBS Item file         *
.*        V5.08.02  25/10/2000  Dean Cramer   SRF 6710                        *
.*                  Recomplied for PATFNDDS                                   *
.*                  Changed for health fund table code size increase to       *
.*                  8 characters                                              *
.*        V5.08.01  15/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund table variables to 8 chars            *
.*        V5.07.B01 12/01/1998  Nicole Harrington 507 rebounds - Error 29     *
.*                  Fixed header date display                                 *
.*        V5.03.01  16/04/1996    Howellsy   5.04                             *
.*                  Changed Accident & Emergency to Emergency.                *
.******************************************************************************
.*            V5.01.01  09/05/90  E.Phan                                      *
.*                      Created program.                                      *
.*            V5.01.02  19/09/90  i chung          SRF 106248                 *
.*                      Changed program name (old name - Misc Charges Master  *
.*                      Listing)                                              *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
          INC       AAEMCHFD/INC
          INC       PATCODFD/INC
          INC       PATFN1FD/INC
          INC       PATITMFD/INC
.
.*******************************************************************************
.*        Constants                                                            *
.*******************************************************************************
.
CODEA     INIT      "A "     * Patient Classification
CODECL    INIT      "CL"     * Claim Code
.
.
Z3        INIT      "ZZZ"    * For ENDING MBS code
ZERO4     INIT      "0000"   * For Health Funds (not tables)
.
.*******************************************************************************
.*        Variables                                                            *
.*******************************************************************************
.
AFUNDH    DIM       6        * For PATFNDDS
.
CODE      DIM       2        * For PATCODDS
.
DIM1A     DIM       1        * For PATITMDS
DIM20     DIM       20       * For PATITMDS
DIM26     DIM       26
.
.
VENDCLMT  DIM       3
VENDCLSS  DIM       3
VSTRCLMT  DIM       3
VSTRCLSS  DIM       3
.
PRGID     INIT      "IBAAAE28"
PRGNAM    INIT      "Emergency MBS Charges Master Listing"
VERSION   INIT      "V12.01.00"
.
.*******************************************************************************
.*        Mainline - Program Control Block                                     *
.*******************************************************************************
.
          CALL      INIT0000            * Open files & initialise parameters
.
ML10000   CALL      OPTN0000            * Select option required
          BRANCH    EXIT,ML99999        * Finished - exit
          BRANCH    OPTION,ML20000,ML30000
.
.         Open appropriate charge file
.
ML20000   OPEN      AAEMCHG1,"aaemchgf" * Open aaemchgf
          GOTO      ML40000             * Get other parameters
.
ML30000   CALL      ONAE0000            * Open naemchgf (includes error trap)
          BRANCH    EXIT,ML90000        * File does not exist - error
.
.         Get other parameters
.
ML40000   CALL      KCLM0000            * Keyin Claim Type range
.
          CALL      KCLS0000            * Keyin Patient Classification range
.
          CALL      CONF0000            * Confirm parameters
          BRANCH    EXIT,ML40000,ML90000 * 0=Ok  1=No  2=Cancel
.
          CALL      REPT0000            * Print report for parameters chosen
.
.         Finished printing
.
ML90000   CLOSE     AAEMCHG1            * Close aaemchgf/naemchgf
          GOTO      ML10000             * Select another option
.
ML99999   CHAIN     PGM                 * Go back to menu
.
.*******************************************************************************
.*        Open Files & Initialise Parameters                                   *
.*******************************************************************************
.
INIT0000  CALL      DISPHEAD                 * Display screen heading
.
          DISPLAY   *P56:24,"Opening aaemchgf"
          OPEN      AAEMCHG1,"aaemchgf"
.
          DISPLAY   *P64:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patfn1af"
          OPEN      PATFN1A1,"patfn1af"
.
          DISPLAY   *P64:24,"patitemn"
          OPEN      PATITEM1,"patitemn"
.
INIT9999  DISPLAY   *P1:24,*EL               * Clear bottom line on screen
          RETURN
.
.*******************************************************************************
.*        Select Options                                                       *
.*******************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF:
                    *P2:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P2:5,*V2LON,ONE,*HOFF," = Current file":
                    *P2:6,*V2LON,TWO,*HOFF," = New file":
                    *P1:8,"Select Option :"
.
OPTN1000  KEYIN     *P17:8,*EL,*DV,UNDLN1,*P17:8,*V2LON,OPTION
          COMPARE   ZERO,OPTION
          GOTO      OPTN9998 IF EQUAL        * Exit
          DISPLAY   *P17:8,*V2LON,OPTION
.
          BRANCH    OPTION,OPTN9997,OPTN9997
          BEEP
          GOTO      OPTN1000
.
OPTN9997  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9998  MOVE      ONE,EXIT
.
OPTN9999  RETURN
.
.*******************************************************************************
.*        Confirm Global Copy                                                  *
.*        Requires : Nothing                                                   *
.*        Returns  : EXIT  (0=Yes  1=No  2=Cancel)                             *
.*******************************************************************************
.
CONF0000  CALL      CONTQST
          BRANCH    CEXIT,CONF1000,CONF2000,CONF3000
          GOTO      CONF0000
.
CONF1000  MOVE      ZERO,EXIT                * set EXIT = 0
          GOTO      CONF9999
.
CONF2000  MOVE      ONE,EXIT                 * set EXIT = 1
          DISPLAY   *P23:10,*EL,*P23:11,*EL,*P23:13,*EL,*P23:14,*EL
          GOTO      CONF9999
.
CONF3000  MOVE      TWO,EXIT                 * set EXIT = 2
.
CONF9999  RETURN
.
.*******************************************************************************
.*        Keyin Claim Type Range                                               *
.*******************************************************************************
.
KCLM0000  KEYIN     *P1:10,*EL,"Starting Claim Type : ___":
                    *P23:10,*V2LON,VSTRCLMT:
                    *P23:10,*DV,VSTRCLMT
          ENDSET    VSTRCLMT
          APPEND    SP3,VSTRCLMT
          RESET     VSTRCLMT
.
          MATCH     SP3,VSTRCLMT
          GOTO      KCLM2000 IF EQUAL        * "Start"
.
          CMATCH    EXITCHAR,VSTRCLMT
          GOTO      KCLM9998 IF EQUAL        * Exit character typed - exit
.
          CMATCH    QUEST,VSTRCLMT
          GOTO      KCLM1000 IF NOT EQUAL    * Not "?" - validate entry
.
          MOVE      CODECL,CODE
          CALL      PATCODDS
          DISPLAY   *P1:3,*EF:
                    *P2:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P2:5,*V2LON,ONE,*HOFF," = Current file":
                    *P2:6,*V2LON,TWO,*HOFF," = New file":
                    *P1:8,"Select Option :",*V2LON,OPTION
          GOTO      KCLM0000                 * Reenter code
.
KCLM1000  PACK      KEY5,CODECL,VSTRCLMT
          CALL      RDCODE1
          BRANCH    OVRCD,KCLM9900
          DISPLAY   *P30:10,TDESC
          GOTO      KCLM3000
.
KCLM2000  DISPLAY   *P23:10,*EL,*V2LON,"Start"
.
. ------- Keyin Ending Claim Type -------
.
KCLM3000  KEYIN     *P1:11,*EL,"Ending   Claim Type : ___":
                    *P23:11,*V2LON,VENDCLMT:
                    *P23:11,*DV,VENDCLMT
          ENDSET    VENDCLMT
          APPEND    SP3,VENDCLMT
          RESET     VENDCLMT
.
          MATCH     SP3,VENDCLMT
          GOTO      KCLM5000 IF EQUAL        * "Finish"
.
          CMATCH    EXITCHAR,VENDCLMT
          GOTO      KCLM9998 IF EQUAL        * Exit character typed - exit
.
          CMATCH    QUEST,VENDCLMT
          GOTO      KCLM4000 IF NOT EQUAL    * Not "?" - validate entry
.
          MOVE      CODECL,CODE
          CALL      PATCODDS
          DISPLAY   *P1:3,*EF:
                    *P2:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P2:5,*V2LON,ONE,*HOFF," = Current file":
                    *P2:6,*V2LON,TWO,*HOFF," = New file":
                    *P1:8,"Select Option :",*V2LON,OPTION
.
          MATCH     SP3,VSTRCLMT
          GOTO      KCLM3500 IF EQUAL
.
          PACK      KEY5,CODECL,VSTRCLMT
          CALL      RDCODE1
          DISPLAY   *P1:10,"Starting Claim Type : ",*V2LON,VSTRCLMT,*HOFF:
                    *P30:10,TDESC
          GOTO      KCLM3000                 * Reenter code
.
KCLM3500  DISPLAY   *P1:10,"Starting Claim Type : ",*V2LON,"Start"
          GOTO      KCLM3000                 * Reenter code
.
KCLM4000  PACK      KEY5,CODECL,VENDCLMT
          CALL      RDCODE1
          BRANCH    OVRCD,KCLM9910
          DISPLAY   *P30:11,TDESC
          GOTO      KCLM6000
.
KCLM5000  PACK      VENDCLMT,Z3
          DISPLAY   *P23:11,*EL,*V2LON,"Finish"
.
.         check for valid range
.
KCLM6000  MATCH     VSTRCLMT,VENDCLMT
          GOTO      KCLM9997 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Ending Claim Type must be > or = ":
                                  "Starting Claim Type.  ";
          CALL      HITENTER
          GOTO      KCLM3000
.
. ------- Error Messages -------
.
KCLM9900  KEYIN     *P1:24,*EL,*B,*V2LON,"* Invalid Code *",*HOFF:
                    " Hit any key to continue ... ",*EOFF,*+,ANS,*P1:24,*EL;
          GOTO      KCLM0000                 * Reenter claim code
.
KCLM9910  KEYIN     *P1:24,*EL,*B,*V2LON,"* Invalid Code *",*HOFF:
                    " Hit any key to continue ... ",*EOFF,*+,ANS,*P1:24,*EL;
          GOTO      KCLM3000                 * Reenter claim code
.
. ------- Finished -------
.
KCLM9997  MOVE      ZERO,EXIT
          GOTO      KCLM9999
.
KCLM9998  MOVE      ONE,EXIT
.
KCLM9999  RETURN
.
.*******************************************************************************
.*        Keyin Patient Classification Range                                   *
.*******************************************************************************
.
KCLS0000  KEYIN     *P1:13,*EL,"Starting Pat. Class : ___":
                    *P23:13,*V2LON,VSTRCLSS:
                    *P23:13,*DV,VSTRCLSS
          ENDSET    VSTRCLSS
          APPEND    SP3,VSTRCLSS
          RESET     VSTRCLSS
.
          MATCH     SP3,VSTRCLSS
          GOTO      KCLS2000 IF EQUAL        * "Start"
.
          CMATCH    EXITCHAR,VSTRCLSS
          GOTO      KCLS9998 IF EQUAL        * Exit character typed - exit
.
          CMATCH    QUEST,VSTRCLSS
          GOTO      KCLS1000 IF NOT EQUAL    * Not "?" - validate entry
.
          MOVE      CODEA,CODE
          CALL      PATCODDS
          DISPLAY   *P1:3,*EF:
                    *P2:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P2:5,*V2LON,ONE,*HOFF," = Current file":
                    *P2:6,*V2LON,TWO,*HOFF," = New file":
                    *P1:8,"Select Option :",*V2LON,OPTION
.
          MATCH     SP3,VSTRCLMT
          GOTO      KCLS0100 IF EQUAL
.
          PACK      KEY5,CODECL,VSTRCLMT
          CALL      RDCODE1
          DISPLAY   *P1:10,"Starting Claim Type : ",*V2LON,VSTRCLMT,*HOFF:
                    *P30:10,TDESC
          GOTO      KCLS0200
.
KCLS0100  DISPLAY   *P1:10,"Starting Claim Type : ",*V2LON,"Start"
.
KCLS0200  MATCH     Z3,VENDCLMT
          GOTO      KCLS0300 IF EQUAL
.
          PACK      KEY5,CODECL,VENDCLMT
          CALL      RDCODE1
          DISPLAY   *P1:11,"Ending   Claim Type : ",*V2LON,VENDCLMT,*HOFF:
                    *P30:11,TDESC
          GOTO      KCLS0000                 * Reenter code
.
KCLS0300  DISPLAY   *P1:11,"Ending   Claim Type : ",*V2LON,"Finish"
          GOTO      KCLS0000                 * Reenter code
.
KCLS1000  PACK      KEY5,CODEA,VSTRCLSS
          CALL      RDCODE1
          BRANCH    OVRCD,KCLS9900
          DISPLAY   *P30:13,TDESC
          GOTO      KCLS3000
.
KCLS2000  DISPLAY   *P23:13,*EL,*V2LON,"Start"
.
. ------- Keyin Ending Patient Classification -------
.
KCLS3000  KEYIN     *P1:14,*EL,"Ending   Pat. Class : ___":
                    *P23:14,*V2LON,VENDCLSS:
                    *P23:14,*DV,VENDCLSS
          ENDSET    VENDCLSS
          APPEND    SP3,VENDCLSS
          RESET     VENDCLSS
.
          MATCH     SP3,VENDCLSS
          GOTO      KCLS5000 IF EQUAL        * "Finish"
.
          CMATCH    EXITCHAR,VENDCLSS
          GOTO      KCLS9998 IF EQUAL        * Exit character typed - exit
.
          CMATCH    QUEST,VENDCLSS
          GOTO      KCLS4000 IF NOT EQUAL    * Not "?" - validate entry
.
          MOVE      CODEA,CODE
          CALL      PATCODDS
          DISPLAY   *P1:3,*EF:
                    *P2:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P2:5,*V2LON,ONE,*HOFF," = Current file":
                    *P2:6,*V2LON,TWO,*HOFF," = New file":
                    *P1:8,"Select Option :",*V2LON,OPTION
.
          MATCH     SP3,VSTRCLMT
          GOTO      KCLS3100 IF EQUAL
.
          PACK      KEY5,CODECL,VSTRCLMT
          CALL      RDCODE1
          DISPLAY   *P1:10,"Starting Claim Type : ",*V2LON,VSTRCLMT,*HOFF:
                    *P30:10,TDESC
          GOTO      KCLS3200
.
KCLS3100  DISPLAY   *P1:10,"Starting Claim Type : ",*V2LON,"Start"
.
KCLS3200  MATCH     Z3,VENDCLMT
          GOTO      KCLS3300 IF EQUAL
.
          PACK      KEY5,CODECL,VENDCLMT
          CALL      RDCODE1
          DISPLAY   *P1:11,"Ending   Claim Type : ",*V2LON,VENDCLMT,*HOFF:
                    *P30:11,TDESC
          GOTO      KCLS3400
.
KCLS3300  DISPLAY   *P1:11,"Ending   Claim Type : ",*V2LON,"Finish"
.
KCLS3400  MATCH     SP3,VSTRCLSS
          GOTO      KCLS3500 IF EQUAL
.
          PACK      KEY5,CODEA,VSTRCLSS
          CALL      RDCODE1
          DISPLAY   *P1:13,"Starting Pat. Class : ",*V2LON,VSTRCLSS,*HOFF:
                    *P30:13,TDESC
          GOTO      KCLS3000                 * Reenter code
.
KCLS3500  DISPLAY   *P1:13,"Starting Pat. Class : ",*V2LON,"Start"
          GOTO      KCLS3000                 * Reenter code
.
KCLS4000  PACK      KEY5,CODEA,VENDCLSS
          CALL      RDCODE1
          BRANCH    OVRCD,KCLS9910
          DISPLAY   *P30:14,TDESC
          GOTO      KCLS6000
.
KCLS5000  PACK      VENDCLSS,Z3
          DISPLAY   *P23:14,*EL,*V2LON,"Finish"
.
.         check for valid range
.
KCLS6000  MATCH     VSTRCLSS,VENDCLSS
          GOTO      KCLS9997 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Ending Pat. Class must be > or = ":
                                  "Starting Pat. Class.  ";
          CALL      HITENTER
          GOTO      KCLS3000
.
. ------- Error Messages -------
.
KCLS9900  KEYIN     *P1:24,*EL,*B,*V2LON,"* Invalid Code *",*HOFF:
                    " Hit any key to continue ... ",*EOFF,*+,ANS,*P1:24,*EL;
          GOTO      KCLS0000                 * Reenter claim code
.
KCLS9910  KEYIN     *P1:24,*EL,*B,*V2LON,"* Invalid Code *",*HOFF:
                    " Hit any key to continue ... ",*EOFF,*+,ANS,*P1:24,*EL;
          GOTO      KCLS3000                 * Reenter claim code
.
. ------- Finished -------
.
KCLS9997  MOVE      ZERO,EXIT
          GOTO      KCLS9999
.
KCLS9998  MOVE      ONE,EXIT
.
KCLS9999  RETURN
.
.*******************************************************************************
.*        Open naemchgf                                                        *
.*******************************************************************************
.
ONAE0000  TRAP      ONAE1000 IF IO           * Error if open fails
          OPEN      AAEMCHG1,"naemchgf"      * Try to open naemchgf
          TRAPCLR   IO                       * Open succeeded
          GOTO      ONAE9997
.
ONAE1000  NORETURN                           * Pop address off stack
          TRAPCLR   IO                       * Clear trap
          KEYIN     *P1:24,*EL,*V2LON,*B,"* New file does not exist *",*HOFF:
                    " Hit any key to continue ... ",*EOFF,*+,ANS,*P1:24,*EL;
          GOTO      ONAE9998
.
ONAE9997  MOVE      ZERO,EXIT
          GOTO      ONAE9999
.
ONAE9998  MOVE      ONE,EXIT
.
ONAE9999  RETURN
.
.*******************************************************************************
.*        Print Routines                                                       *
.*******************************************************************************
.         Print page header
.-------------------------------------------------------------------------------
.
PRHD0000  CALL      IBACLOCK     
          COMPARE   ZERO,CPAGENO
          GOTO      PRHD0100 IF EQUAL
          CALL      PRLN0000
PRHD0100  MOVE      ONE,CNOUNDLN
          BRANCH    OPTION,PRHD1000          * Old file used - ignore print
          MOVE      " - New File",CPHDROPT
.
.         Print Claim Type Range
.
PRHD1000  CALL      HEAD132
          PRINT     *N,*40,"Claim Type from ";
          MATCH     SP3,VSTRCLMT             * Spaces for start claim type ?
          GOTO      PRHD2000 IF EQUAL        * Yes - print "start"
          PRINT     SP1,VSTRCLMT,SP1;        * Else print starting code
          GOTO      PRHD3000                 * Print "to" code
PRHD2000  PRINT     "Start";                 * Start of file
PRHD3000  PRINT     " to ";
          MATCH     Z3,VENDCLMT              * "ZZZ" for end claim type ?
          GOTO      PRHD4000 IF EQUAL        * Yes - print "finish"
          PRINT     SP1,VENDCLMT;            * Else print end code
          GOTO      PRHD5000                 * Print page number
PRHD4000  PRINT     "Finish";
.
.         Print Patient Classification Range
.
PRHD5000  PRINT     *N,*40,"Pat. Class from ";
          MATCH     SP3,VSTRCLSS             * Spaces for start pat.class. ?
          GOTO      PRHD6000 IF EQUAL        * Yes - print "start"
          PRINT     SP1,VSTRCLSS,SP1;        * Else print starting code
          GOTO      PRHD7000                 * Print "to" code
PRHD6000  PRINT     "Start";                 * Start of file
PRHD7000  PRINT     " to ";
          MATCH     Z3,VENDCLSS              * "ZZZ" for end pat.class. ?
          GOTO      PRHD8000 IF EQUAL        * Yes - print "finish"
          PRINT     SP1,VENDCLSS,*N          * Else print end code
          GOTO      PRHD9000                 * Print page number
PRHD8000  PRINT     "Finish",*N
.
PRHD9000  CALL      PRLN0000                 * Print page separator
          PRINT     *1,"| Claim Type",*28,"| Health Fund":
                    *42,"| Patient Classification":
                    *69,"| MBS Code  Description":
                    *108,"|  Patient",*120,"|  Rebate",*132,"|":
                    *N,"|-------------------------------------------":
                       "--------------------------------------------":
                       "-------------------------------------------|"
          MOVE      NINE,CLNO                * 9 lines printed so far
          RETURN
.
.-------------------------------------------------------------------------------
.         Print page separator line
.-------------------------------------------------------------------------------
.
PRLN0000  PRINT     *1,"*-------------------------------------------":
                       "--------------------------------------------":
                       "-------------------------------------------*"
          ADD       ONE,CLNO                 * 1 more line printed
          RETURN
.
.-------------------------------------------------------------------------------
.         Print record
.-------------------------------------------------------------------------------
.
PRRC0000  COMPARE   "60",CLNO
          GOTO      PRRC1000 IF LESS
          CALL      PRHD0000
.
.         Claim Type
.
PRRC1000  MOVE      "*** Invalid Code ***",TDESC
          PACK      KEY5,CODECL,AEMCCLM
          CALL      RDCODE1
          PRINT     *1,"| ",AEMCCLM,SP1,TDESC;
.
.         Health Fund
.
          PRINT     *28,"| ";
          MATCH     SP6,AEMCFUND
          GOTO      PRRC2000 IF EQUAL
          PRINT     AEMCFUND,SLASH,AEMCTAB;
.
.         Patient Classification
.
PRRC2000  MOVE      "*** Invalid Code ***",TDESC
          PACK      KEY5,CODEA,AEMCCLSS
          CALL      RDCODE1
          PRINT     *42,"| ",AEMCCLSS,SP1,TDESC;
.
.         MBS Code & Patient & Rebate amounts
.
          MOVE      "*** Invalid Code ***",IDESC
.
          PACK      KEY9,AEMCMBS
          PACK      KEY17,KEY9,SP70
          CALL      PATITMRD
          MOVE      IDESC,DIM26
          PRINT     *69,"| ",AEMCMBS,SP1,DIM26,*108,"| ",AEMCPAT:
                    *120,"| ",AEMCREB,*132,"|"
.
          ADD       ONE,CLNO
.
PRRC9999  RETURN
.
.*******************************************************************************
.*        Print Report                                                         *
.*******************************************************************************
.
REPT0000  DISPLAY   *P1:18,*EF,*V2LON,"*** ",*BLINKON,"Printing Report",*HOFF:
                               *V2LON," ***",*HOFF:
                    *P1:20,"Claim Type :":
                    *P1:21,"Pat. Class :":
                    *P1:22,"MBS Code   :"
.
          MOVE      ZERO,CPAGENO
          MOVE      "80",CLNO
          PACK      KEY29,VSTRCLMT,SP30
          CALL      RSAEMCH1                 * Positioning read
.
REPT1000  CALL      RKAEMCH1
          BRANCH    OVRCD,REPT9900           * No more records - finished
.
          MATCH     AEMCCLM,VENDCLMT
          GOTO      REPT9900 IF LESS         * Past end of range - finished
          MATCH     VSTRCLSS,AEMCCLSS
          GOTO      REPT1000 IF LESS         * Before range - ignore
          MATCH     AEMCCLSS,VENDCLSS
          GOTO      REPT1000 IF LESS         * Past end of range - ignore
.
          DISPLAY   *P14:20,*V2LON,AEMCCLM,*P14:21,AEMCCLSS,*P14:22,AEMCMBS
          CALL      PRRC0000                 * Print record
.
          GOTO      REPT1000                 * Read next record
.
.         No more records
.
REPT9900  CALL      PRLN0000
          PRINT     *N,"*** End of Report ***",*N
          KEYIN     *P1:24,*V2LON,"*** ",*BLINKON,"Print Completed",*HOFF:
                           *V2LON," ***",*HOFF:
                    "     Hit any key to continue ... ",*EOFF,*+,ANS;
REPT9999  RETURN
.
.*******************************************************************************
.*        File I/O Subroutines                                                 *
.*******************************************************************************
.
          INC       STD001IO
          INC       AAEMCHIO/INC
          INC       PATCODKY
          INC       PATITMRD
          INC       PATCODIO/INC
          INC       PATFN1IO/INC
          INC       PATITMIO/INC
.

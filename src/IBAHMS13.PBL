.******************************************************************************
.* System   :  HOSPITAL MAINTENANCE SYSTEM                                    *
.* Program  :  IBAHMS13                                                       *
.* Desc     :  PRS2 Account Status Maintenance                                *
.******************************************************************************
.* Date     :  29/11/1991                                                     *
.* Author   :  DIG                                                            *
.* Mods.    :  V5.01.02  02/12/91 DIG                                         *
.*                       Changed so that in Insert mode, control returns to   *
.*                       Highest Day in Period after posting the record.      *
.*             V5.01.03  25/03/92 i chung          SRF 112867                 *
.*                       Added "?" facility for Highest Day in Period in      *
.*                       "Change" mode                                        *
.*             V5.01.04  5/04/94  I.Rutt                                      *
.*                       Added Single user program locking and standards      * 
.*             V5.01.05  06/06/94  ROD                                        *
.*                       Added Previous account class                         * 
.*         V11.0.1.01    25/11/2021 Jill Parkinson Task 0908535               *
.*                       Removed locking                                      *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
.
          INC       PATACCFD/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
+
.**************************************************************************
.*                            GLOBAL VARIABLES                            *
.**************************************************************************
CODE      DIM       2
CPTOATYP  DIM       3
CPTOCLAM  DIM       3
DESCCLAM  DIM       20
SAVATYP   DIM       3
SAVCLAM   DIM       3
SAVCLM    DIM       3
SAVTYP    DIM       3
TEMPCODE  DIM       3
+
.**************************************************************************
.*                                CONSTANTS                               *
.**************************************************************************
CATA      INIT      "A "
CATCL     INIT      "CL"
.
PRGID     INIT      "IBAHMS13"
PRGNAM    INIT      "Prs2 Account Status Maintenance"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              MAINLINE                                  *
.*                            Controlling Logic                           *
.**************************************************************************
ML0000    CALL      INIT0000               * initialization and open files
          BRANCH    EXIT,ML9999
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9500            * EXIT = 1 if 0 chosen in menu
          BRANCH    MOPTION,ML1000,ML2000,ML3000,ML4000,ML5000
.
. ---- add an account status ----
.
ML1000    CALL      INPK0000               * input key details
          BRANCH    EXIT,ML0100
.
.------ keyin the account status ------
.
ML1100    CALL      PACS0000               * keyin previous account
          BRANCH    EXIT,ML1000
          CALL      ACCS0000               * keyin current account status
          BRANCH    EXIT,ML1100
.
          CALL      SEL0000                * select field to modify
          BRANCH    EXIT,ML1000              or ok to post
.
          CALL      WRT0000                * write to file
          CALL      INPK5000               * go back to the keyin of the
          BRANCH    EXIT,ML0100              highest day the period
          GOTO      ML1100
.
. ---- modify an account status ----
.
ML2000    CALL      INPK0000               * input key details
          BRANCH    EXIT,ML0100
.
          CALL      DISP0000               * display details
.
          CALL      SEL0000                * select field to modify
          BRANCH    EXIT,ML2000
.
          CALL      UPPTACC1               * update the account status file
          GOTO      ML2000
.
. ---- delete an account status ----
.
ML3000    CALL      INPK0000               * input key details
          BRANCH    EXIT,ML0100
.
          CALL      DISP0000               * display details
.
          CALL      CONT0000               * ok to delete ?
          BRANCH    EXIT,ML3000
.
          CALL      DEPTACC1               * delete a record
          GOTO      ML3000
.
. ---- global copy ----
.
ML4000    CALL      GCPY0000
          GOTO      ML0100
.
. ---- global delete ----
.
ML5000    CALL      GDEL0000
          GOTO      ML0100
.
ML9500    PACK      PTCNACCM,ZERO,ZERO
          WRITAB    CONTROLF,SEVENTY9;*7,PTCNACCM
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000                                  *
.*                             initialization                               *
.*  The initialization routine is used to display headings and open files.  *
.****************************************************************************
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"pataccaf";
          OPEN      PATACCA1,"pataccaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"controlf"
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P1:24,*EL
.
          MOVE      ONE,CNEWDS
.
.         READ      CONTROLF,SEVENTY9;*7,PTCNACCM
.         MATCH     "00",PTCNACCM
.         GOTO      INIT5000 IF EQUAL
.
.         MATCH     PORT,PTCNACCM
.         GOTO      INIT5000 IF EQUAL
.
.         CALL      PRGLOCK
.         MOVE      ONE,EXIT
          GOTO      INIT9999
.
INIT5000  MOVE      PORT,PTCNACCM
          WRITAB    CONTROLF,SEVENTY9;*7,PTCNACCM
.  
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000                                  *
.*                        get user options or exit                          *
.*                                                                          *
.*    valid options     --->  (0) Exit                                      *
.*                            (1) Insert                                    *
.*                            (2) Change                                    *
.*                            (3) Delete                                    *
.*                            (4) Global Copy   
.*                            (5) Global Delete                             *
.****************************************************************************
OPTN0000  MOVE      FALSE,EXIT
          DISPLAY   *P1:4,*EF,*V2LON," 0",*HOFF," = Exit":
                    *P1:5,*V2LON," 1",*HOFF," = Insert":
                    *P1:6,*V2LON," 2",*HOFF," = Change":
                    *P1:7,*V2LON," 3",*HOFF," = Delete":
                    *P1:8,*V2LON," 4",*HOFF," = Global Copy":
                    *P1:9,*V2LON," 5",*HOFF," = Global Delete":
                    *P1:11,"Select Option : ";
.
          KEYIN     *P17:11,*V2LON,MOPTION;
.
          COMPARE   ZERO,MOPTION
          GOTO      OPTN9000 IF EQUAL             * exit
.
          COMPARE   SIX,MOPTION                   * see if a valid option 
          GOTO      OPTN9999 IF LESS                selected
          BEEP                                    * invalid option, re-enter
          GOTO      OPTN0000
.
.------ exit option chosen ------
.
OPTN9000  MOVE      TRUE,EXIT    
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               INPK0000                                 *
.*                       input the key details                            *
.**************************************************************************
INPK0000  CALL      SCR0000                 * display screen format
.
          MOVE      "4",EVERT 
          CALL      KEYC0000                * keyin the claim code
          BRANCH    EXIT,INPK9999
.
.------ keyin the admission type ------
.
INPK1000  MOVE      "5",EVERT
          CALL      KEYA0000                * keyin the admission type
          BRANCH    EXIT,INPK0000
.
.------ input the highest day in the period ------
.
INPK5000  CALL      KEYH0000                * keyin the highest day in period
          BRANCH    EXIT,INPK1000
.
          PACK      KEY9,PTASCLAM,PTASATYP,PTASHDAY
          CALL      RDPTACC1                * read the account status file
          BRANCH    OVRCD,INPK9000
.
          COMPARE   ONE,MOPTION             * see if we are in insert mode
          GOTO      INPK8000 IF EQUAL
          GOTO      INPK9999
.
.------ record already on file ------
.
INPK8000  DISPLAY   *P1:24,*EL,*B:
                    "Record for these details already on file. ";
          CALL      HITENTER
          GOTO      INPK0000
.
.------ see if we are in insert mode ------
.
INPK9000  BRANCH    MOPTION,INPK9999
.
          DISPLAY   *P1:24,*EL,*B:
                    "Record for these details not on file. ";
          CALL      HITENTER
          GOTO      INPK1000
.
INPK9999  RETURN
+
.**************************************************************************
.*                               KEYC0000                                 * 
.*                       Keyin the Claim Code                             *
.**************************************************************************
KEYC0000  MOVE      SP3,PTASCLAM
          MOVE      SP20,DESCCLAM
          MOVE      "28",ECOL
.....     MOVE      "4",EVERT 
          DISPLAY   *P28:EVERT,*EL;
          MOVE      CATCL,CODE
          CALL      PATCODKY
          BRANCH    EXIT,KEYC9999,KEYC9999    
          MOVE      ACODE,PTASCLAM 
          DISPLAY   *P35:EVERT,TDESC;
          MOVE      TDESC,DESCCLAM
          MOVE      ZERO,EXIT
          GOTO      KEYC9999
.
KEYC9999  RETURN
+
.**************************************************************************
.*                               KEYA0000                                 * 
.*                       Keyin the Admission Type                         *
.**************************************************************************
KEYA0000  MOVE      SP3,PTASATYP
          MOVE      SP20,TDESC
          DISPLAY   *P28:EVERT,*EL;
          MOVE      "28",ECOL
...       MOVE      "5",EVERT
          MOVE      CATA,CODE
          CALL      PATCODKY
          BRANCH    EXIT,KEYA9999,KEYA9999  
          MOVE      ACODE,PTASATYP
          MOVE      PTASATYP,TEMPCODE
.
          ASSIGN    (EVERT-1),FORM2
          DISPLAY   *P28:FORM2,*V2LON,PTASCLAM,*P35:FORM2,*HOFF,DESCCLAM:
                    *P28:EVERT,*V2LON,PTASATYP,*P35:EVERT,*HOFF,TDESC;
          MOVE      ZERO,EXIT
          GOTO      KEYA9999
.
KEYA9999  RETURN
+
.**************************************************************************
.*                                  CHK0000                               *
.*               Check what was entered for a codes file entry            *
.* RETURNS - EXIT  = 0  Code was entered                                  *
.*                 = 1  Nothing or Exitchar entered                       *
.*                 = 2  ? entered                                         *
.**************************************************************************
CHK0000   ENDSET    TEMPCODE
          APPEND    SP3,TEMPCODE
          RESET     TEMPCODE
.
          MATCH     SP3,TEMPCODE            * see if anything was entered 
          GOTO      CHK9000 IF EQUAL        * no
.
          MATCH     EXITCHAR,TEMPCODE       * see if exitchar was entered 
          GOTO      CHK9000 IF EQUAL        * yes
.
          MATCH     QUEST,TEMPCODE          * ? entered 
          GOTO      CHK8000 IF EQUAL        * yes
          MOVE      ZERO,EXIT               * code entered
          GOTO      CHK9999
.
.------ ? entered ------
.
CHK8000   MOVE      TWO,EXIT
          GOTO      CHK9999
.
.------ Nothing or Exitchar entered ------
.
CHK9000   MOVE      ONE,EXIT
.
CHK9999   RETURN
+
.**************************************************************************
.*                               KEYH0000                                 *
.*                Keyin the highest day in the period                     *
.**************************************************************************
KEYH0000  MOVE      FALSE,EXIT
          MOVE      SIX,CCOL1
          MOVE      ONE,FORM1
.
KEYH1000  MOVE      SP3,DPTASDAY
          DISPLAY   *P28:CCOL1,*EF,UNDLN3;
          KEYIN     *P28:CCOL1,*V2LON,DPTASDAY;
          DISPLAY   *P28:CCOL1,*V2LON,DPTASDAY;
.
          TYPE      DPTASDAY
          GOTO      KEYH2000 IF EQUAL
.
          ENDSET    DPTASDAY
          APPEND    SP3,DPTASDAY
          RESET     DPTASDAY
.
          CMATCH    QUEST,DPTASDAY
          GOTO      KEYH1100 IF NOT EQUAL
          MOVE      PTASCLAM,SAVCLM
          MOVE      PTASATYP,SAVTYP
          GOTO      KEYH4000
.
KEYH1100  MATCH     SP3,DPTASDAY
          GOTO      KEYH3000 IF NOT EQUAL
          BRANCH    FORM1,KEYH1300
.
KEYH1200  MOVE      SAVCLM,PTASCLAM
          CALL      SCR0000
          DISPLAY   *P28:4,*V2LON,PTASCLAM,*P35:4,*HOFF,DESCCLAM;
.
KEYH1300  MOVE      TRUE,EXIT
          GOTO      KEYH9999
.
.------ we have a numeric entered ------
.
KEYH2000  MOVE      DPTASDAY,PTASHDAY
.
          COMPARE   ZERO,PTASHDAY           * make sure a positive number
          GOTO      KEYH3000 IF LESS          is entered
.
          BRANCH    FORM1,KEYH9999
          CALL      SCR0000
          DISPLAY   *P28:4,*V2LON,PTASCLAM,*P35:4,*HOFF,DESCCLAM:
                    *P28:5,*V2LON,PTASATYP,*P35:5,*HOFF,TDESC:
                    *P28:6,*V2LON,PTASHDAY;
          GOTO      KEYH9999
.
.------ invalid highest day in period entered ------
.
KEYH3000  DISPLAY   *P1:24,*EL,*B:
                    "Invalid Highest Day in Period. ";
          CALL      HITENTER
          BRANCH    FORM1,KEYH1000
          GOTO      KEYH4900
.
.------ "?" option requested ------
.
KEYH4000  PACK      KEY9,SAVCLM,SAVTYP,SP3
          CALL      RSPTACC1
          CALL      RKPTACC1
          BRANCH    OVRCD,KEYH4700
          MATCH     PTASCLAM,SAVCLM
          GOTO      KEYH4700 IF NOT EQUAL
          MATCH     PTASATYP,SAVTYP
          GOTO      KEYH4700 IF NOT EQUAL
.
          DISPLAY   *P1:3,*EF,*P28:4,*V2LON,*ULON,"Highest Day in Period Scan":
                    *HOFF,*P19:5,"for Claim Code ",*V2LON,PTASCLAM,*HOFF,"/":
                    "Adm. Type ",*V2LON,PTASATYP,*HOFF," combination";
.
          MOVE      SEVEN,CVERT
          MOVE      ONE,CCOL
          GOTO      KEYH4200
.
KEYH4100  CALL      RKPTACC1
          BRANCH    OVRCD,KEYH4500
          MATCH     PTASCLAM,SAVCLM
          GOTO      KEYH4500 IF NOT EQUAL
          MATCH     PTASATYP,SAVTYP
          GOTO      KEYH4500 IF NOT EQUAL
.
KEYH4200  DISPLAY   *PCCOL:CVERT,*V2LON,PTASHDAY;
          ADD       ONE,CVERT
          COMPARE   TWENTY3,CVERT
          GOTO      KEYH4100 IF LESS
.
          MOVE      SEVEN,CVERT
          ADD       "40",CCOL
          COMPARE   "70",CCOL
          GOTO      KEYH4100 IF LESS
.
          DISPLAY   *P1:24,*EL,"(",*V2LON,ANSC,*HOFF,")ontinue, (",*V2LON:
                               "F",*HOFF,")irst screen or (",*V2LON,ANSN,*HOFF:
                               ")ext screen :";
.
KEYH4300  KEYIN     *P47:24,*DV,UNDLN1,*P47:24,*V2LON,ANS;
          RESET     ANS
          GOTO      KEYH4400 IF EOS
          REP       UPPLOW,ANS
          DISPLAY   *P47:24,*V2LON,ANS;
.
          MATCH     ANSC,ANS                     * Continue ?
          GOTO      KEYH4800 IF EQUAL            * yes
          MATCH     ANSF,ANS                     * First screen ?
          GOTO      KEYH4000 IF EQUAL            * yes
          MATCH     ANSN,ANS                     * Next screen ?
          GOTO      KEYH4400 IF EQUAL            * yes
          BEEP
          GOTO      KEYH4300
.
KEYH4400  MOVE      SEVEN,CVERT
          MOVE      ONE,CCOL
          DISPLAY   *P1:6,*EF;
          GOTO      KEYH4100
.
KEYH4500  DISPLAY   *P1:24,*EL,"(",*V2LON,ANSC,*HOFF,")ontinue, (",*V2LON:
                               "F",*HOFF,")irst screen :";
.
KEYH4600  KEYIN     *P30:24,*DV,UNDLN1,*P30:24,*V2LON,ANS;
          RESET     ANS
          GOTO      KEYH4800 IF EOS
          REP       UPPLOW,ANS
          DISPLAY   *P30:24,*V2LON,ANS;
.
          MATCH     ANSC,ANS                     * Continue ?
          GOTO      KEYH4800 IF EQUAL            * yes
          MATCH     ANSF,ANS                     * First screen ?
          GOTO      KEYH4000 IF EQUAL            * yes
          BEEP
          GOTO      KEYH4600
.
KEYH4700  DISPLAY   *P1:23,*EF,*B,*V2LON,"No Highest Day in Period exist ":
                    "for the Claim Code/Admission Type combination":
                    *P1:24,"specified. ";
          CALL      HITENTER
          GOTO      KEYH1200
.
KEYH4800  MOVE      "24",CCOL1
          MOVE      ZERO,FORM1
          MOVE      SAVCLM,PTASCLAM
          MOVE      SAVTYP,PTASATYP
.
KEYH4900  DISPLAY   *P1:24,*EL,*P4:24,"Highest Day in Period : ";
          GOTO      KEYH1000
.
KEYH9999  RETURN
+
.**************************************************************************
.*                                  SCR0000                               *
.*                         display screen format                          *
.**************************************************************************
SCR0000   DISPLAY   *P1:4,*EF:
                    *P4:4,"Claim Code            : ":
                    *P4:5,"Admission Type        : ":
                    *P4:6,"Highest Day in Period : ";
.
SCR9999   RETURN
+
.*************************************************************************
.*                                  DISP0000                             *
.*                       display the screen details                      *
.*************************************************************************
DISP0000  DISPLAY   *P1:8,*V2LON,ONE,*HOFF,". Previous Year Account ":
                    "Status : ",*V2LON,PTASPRAC:
                    *P1:9,*V2LON,TWO,*HOFF,". Current Year Account ":
                    "Status  : ",*V2LON,PTASACCS;
.
DISP9999  RETURN
+
.*************************************************************************
.*                                  PACS0000                             *
.*                         Keyin the account status (Previous)           *
.*************************************************************************
PACS0000  MOVE      FALSE,EXIT
          MOVE      SP3,PTASPRAC
          DISPLAY   *P1:8,*V2LON,ONE,*HOFF,". Previous Year Account ":
                    "Status : ":
                    *P1:9,*V2LON,TWO,*HOFF,". Current Year Account ":
                    "Status  : "
          KEYIN     *P35:8,*V2LON,PTASPRAC;
.
          TYPE      PTASPRAC                * see if a number was entered
          GOTO      PACS9000 IF EQUAL
.
          ENDSET    PTASPRAC
          APPEND    SP3,PTASPRAC
          RESET     PTASPRAC
.
          MATCH     SP3,PTASPRAC            * see if nothing was entered
          GOTO      PACS9000 IF EQUAL
.
          CMATCH    QUEST,PTASPRAC          * see if a ? was entered
          GOTO      PACS9000 IF EQUAL
.
          CMATCH    EXITCHAR,PTASPRAC       * see if Exitchar was entered
          GOTO      PACS9900 IF EQUAL
.
          DISPLAY   *P35:8,*V2LON,PTASPRAC;
          GOTO      PACS9999
.
.------ account status entered is invalid ------
.
PACS9000  DISPLAY   *P1:24,*EL,*B,"Invalid Account Status. ";
          CALL      HITENTER
          GOTO      PACS0000
.
.------ Exitchar was entered ------
.
PACS9900  MOVE      TRUE,EXIT
.
PACS9999  RETURN
+
.*************************************************************************
.*                                  ACCS0000                             *
.*                         Keyin the account status  (Current)           *
.*************************************************************************
ACCS0000  MOVE      FALSE,EXIT
          MOVE      SP3,PTASACCS
          DISPLAY   *P35:9,*EL
          KEYIN     *P35:9,*V2LON,PTASACCS;
.
          TYPE      PTASACCS                * see if a number was entered
          GOTO      ACCS9000 IF EQUAL
.
          ENDSET    PTASACCS
          APPEND    SP3,PTASACCS
          RESET     PTASACCS
.
          MATCH     SP3,PTASACCS            * see if nothing was entered
          GOTO      ACCS9000 IF EQUAL
.
          CMATCH    QUEST,PTASACCS          * see if a ? was entered
          GOTO      ACCS9000 IF EQUAL
.
          CMATCH    EXITCHAR,PTASACCS       * see if Exitchar was entered
          GOTO      ACCS9900 IF EQUAL
.
          DISPLAY   *P35:9,*V2LON,PTASACCS;
          GOTO      ACCS9999
.
.------ account status entered is invalid ------
.
ACCS9000  DISPLAY   *P1:24,*EL,*B,"Invalid Account Status. ";
          CALL      HITENTER
          GOTO      ACCS0000
.
.------ Exitchar was entered ------
.
ACCS9900  MOVE      TRUE,EXIT
.
ACCS9999  RETURN
+
.**************************************************************************
.*                                   SEL0000                              *
.*                         select field to modify or post                 *
.**************************************************************************
SEL0000   MOVE      FALSE,EXIT
.
          CALL      MAINQST                    * select an option
.
          COMPARE   ZERO,CCITEM                * ok to post
          GOTO      SEL9999 IF EQUAL
.
          COMPARE   "-1",CCITEM                * cancel option
          GOTO      SEL9000 IF EQUAL
.
          BRANCH    CCITEM,SEL1000,SEL2000
.
          BEEP                                 * invalid selection
          GOTO      SEL0000
.
.------ enter the Previous account status ------
.
SEL1000   CALL      PACS0000                   * enter the account status
          BRANCH    EXIT,SEL9000
          GOTO      SEL0000
.
.------ enter the Current account status ------
.
SEL2000   CALL      ACCS0000                   * enter the account status
          BRANCH    EXIT,SEL9000
          GOTO      SEL0000
.
.------ Cancel option chosen ------
.
SEL9000   MOVE      TRUE,EXIT
.
SEL9999   RETURN
+
.**************************************************************************
.*                              WRT0000                                   *
.*                          write a new record                            *
.**************************************************************************
WRT0000   PACK      KEY9,PTASCLAM,PTASATYP,PTASHDAY
          CALL      RAPTACC1                * see if the record exists on the
          COMPARE   ZERO,OVRCD                account status file
          GOTO      WRT9999 IF EQUAL
.
          MOVE      SP30,PTASSPAR
          CALL      WRPTACC1                * write to the account status file
.
WRT9999   RETURN
+
.**************************************************************************
.*                              CONT0000                                  *
.*                         check if OK to Delete                          *
.**************************************************************************
CONT0000  MOVE      FALSE,EXIT
          CALL      DELQST
.
          COMPARE   ONE,CEXIT 
          GOTO      CONT9000 IF NOT EQUAL
          GOTO      CONT9999 
.
.------ Not Ok to Delete ------
.
CONT9000  MOVE      TRUE,EXIT
.
CONT9999  RETURN
+
.**************************************************************************
.*  GCPY0000  :  Global Copy                                              *
.**************************************************************************
GCPY0000  DISPLAY   *P1:3,*EF:
                    *P4:4,*V2LON,*ULON,"Copy From",*HOFF:
                    *P4:5,"Claim Code            : ":
                    *P4:6,"Admission Type        : "
.
          MOVE      "5",EVERT 
          CALL      KEYC0000                * keyin the claim code
          BRANCH    EXIT,GCPY9999
.
.------ keyin the admission type ------
.
GCPY1000  MOVE      "6",EVERT
          CALL      KEYA0000                * keyin the admission type
          BRANCH    EXIT,GCPY0000
.
          MOVE      PTASCLAM,SAVCLAM
          MOVE      PTASATYP,SAVATYP
.
          PACK      KEY9,PTASCLAM,PTASATYP,SP10
          CALL      RSPTACC1
          CALL      RKPTACC1
          BRANCH    OVRCD,GCPY9000
.
          MATCH     PTASCLAM,SAVCLAM
          GOTO      GCPY9000 IF NOT EQUAL
.
          MATCH     PTASATYP,SAVATYP
          GOTO      GCPY9000 IF NOT EQUAL
.
. get copy to fields
.
          DISPLAY   *P4:8,*V2LON,*ULON,"Copy To",*HOFF:
                    *P4:9,"Claim Code            : ":
                    *P4:10,"Admission Type        : "
.
GCPY1500  MOVE      "9",EVERT 
          CALL      KEYC0000                * keyin the claim code
          BRANCH    EXIT,GCPY0000
.
.------ keyin the admission type ------
.
GCPY2000  MOVE      "10",EVERT
          CALL      KEYA0000                * keyin the admission type
          BRANCH    EXIT,GCPY1500
.
          MOVE      PTASCLAM,CPTOCLAM
          MOVE      PTASATYP,CPTOATYP
.
          CALL      POSTQST
          BRANCH    CEXIT,GCPY5000,GCPY0000,GCPY9999
.
. delete appropriate records
.
GCPY5000  DISPLAY   *P1:24,*EL,*P35:24,"Copying..."
          PACK      KEY9,SAVCLAM,SAVATYP,SP10
          CALL      RSPTACC1
GCPY5100  CALL      RKPTACC1
          BRANCH    OVRCD,GCPY0000
.
          MATCH     PTASCLAM,SAVCLAM
          GOTO      GCPY0000 IF NOT EQUAL
.
          MATCH     PTASATYP,SAVATYP
          GOTO      GCPY0000 IF NOT EQUAL
.
          MOVE      CPTOCLAM,PTASCLAM
          MOVE      CPTOATYP,PTASATYP
          PACK      KEY9,PTASCLAM,PTASATYP,PTASHDAY
          CALL      RAPTACC1
          PERFORM   OVRCD,WRPTACC1
          PACK      KEY9,SAVCLAM,SAVATYP,PTASHDAY
          CALL      RSPTACC1
          GOTO      GCPY5100
.
GCPY9000  DISPLAY   *P1:24,*EL,*B,"Invalid Combination.  ";
          CALL      HITENTER
          GOTO      GCPY1000
.
GCPY9999  RETURN
+
.**************************************************************************
.*  GDEL0000  :  Global Delete                                            *
.**************************************************************************
GDEL0000  DISPLAY   *P1:4,*EF:
                    *P4:4,"Claim Code            : ":
                    *P4:5,"Admission Type        : "
.
          MOVE      "4",EVERT 
          CALL      KEYC0000                * keyin the claim code
          BRANCH    EXIT,GDEL9999
.
.------ keyin the admission type ------
.
GDEL1000  MOVE      "5",EVERT
          CALL      KEYA0000                * keyin the admission type
          BRANCH    EXIT,GDEL0000
.
          MOVE      PTASCLAM,SAVCLAM
          MOVE      PTASATYP,SAVATYP
.
          PACK      KEY9,PTASCLAM,PTASATYP,SP10
          CALL      RSPTACC1
          CALL      RKPTACC1
          BRANCH    OVRCD,GDEL9000
.
          MATCH     PTASCLAM,SAVCLAM
          GOTO      GDEL9000 IF NOT EQUAL
.
          MATCH     PTASATYP,SAVATYP
          GOTO      GDEL9000 IF NOT EQUAL
.
          DISPLAY   *P1:23,*EL,*B,*V2LON,"Warning: ",*HOFF,"This will ":
                    "delete all combinations."
.
          CALL      DELQST
          BRANCH    CEXIT,GDEL5000,GDEL0000,GDEL9999
.
. delete appropriate records
.
GDEL5000  DISPLAY   *P1:24,*EL,*P35:24,"Deleting..."
          PACK      KEY9,SAVCLAM,SAVATYP,SP10
          CALL      RSPTACC1
          CALL      RKPTACC1
          BRANCH    OVRCD,GDEL0000
.
          MATCH     PTASCLAM,SAVCLAM
          GOTO      GDEL0000 IF NOT EQUAL
.
          MATCH     PTASATYP,SAVATYP
          GOTO      GDEL0000 IF NOT EQUAL
.
          PACK      KEY9,PTASCLAM,PTASATYP,PTASHDAY
          CALL      DEPTACC1
          GOTO      GDEL5000
.
GDEL9000  DISPLAY   *P1:24,*EL,*B,"Invalid Combination.  ";
          CALL      HITENTER
          GOTO      GDEL1000
.
GDEL9999  RETURN
+
.**************************************************************************
.
          INC       STD001IO
          INC       PATCODKY
.
          INC       PATACCIO/INC
          INC       PATCODIO/INC
+

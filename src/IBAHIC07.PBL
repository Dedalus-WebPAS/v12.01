.***************************************************************************
.* System    :   HIC Claims                                                *
.* Program   :   IBAHIC07                                                  *
.* Desc      :   Patients On Hold Report                                   *
.***************************************************************************
.* Date      :   01/06/2005                                                *
.* Author    :   J.Tan                                                     *
.* Function  :   This program will list all batches that may be hold       *
.*                                                                         *
.* V10.11.01 25/07/2017  Ebon Clements  TSK 0268970                        *
.*                       Change to use OUTCLIFD index 1 instead of index 2 *
.* V10.04.01 17/04/2014  J.Tan          CAR 261630                         *
.*                       Removed port number from temp file name           *
.* V9.05.01  10/03/2006  J.Tan         CAR 96067                           *
.*                       Mods maximum number of lines to print             *
.* V9.05.B02 22/02/2005  J.Tan          CAR 96067                          *
.*                       Fixed Clinic ID description                       *
.* V9.05.B01 20/02/2006  Ebon Clements     CAR 76341                       *
.*                       Added referral and encounter file audit           *
.* V9.04.01  20/06/2005  J.Tan          CAR 56701                          *
.*                       Mods checking for status of claim                 *
.* V9.04.00  01/06/2005  J.Tan          CAR 56701                          *
.*                       Created program                                   *
.***************************************************************************
.
          INC       STD001FD
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       ALLCONFD/INC 
          INC       HICCLMFD/INC
          INC       HICCITFD/INC
          INC       PMSHCPFD/INC
          INC       PRIPRAFD/INC
          INC       PRIDOCFD/INC
          INC       OUTSITFD/INC
          INC       OUTCLIFD/INC
          INC       PATCODFD/INC
          INC       WEBSECFD/INC
          INC       PRIALSFD/INC
          INC       PRISECFD/INC
          INC       PATMA1FD/INC
          INC       PMSHCLFD/INC
          INC       ALLREFFD/INC
          INC       ALLLNKFD/INC
          INC       VISCMTFD/INC
.
.         Temporary File Definition
.         -------------------------
.
.         Filename : hic07axx.dat          (xx = port id)
.
HICTEMP1  IFILE SQL, FIXED=54, KEY="UC23-27,46-53"
HICTEMP2  IFILE SQL, FIXED=54, KEY="UC1-6,23-27,46-53"
HICTEMP3  IFILE SQL, FIXED=54, KEY="UC7-12,23-27,46-53"
HICTEMP4  IFILE SQL, FIXED=54, KEY="UC13-22,23-27,46-53"
.
.NAME     TYPE    LENGTH     PHYSICAL     START     DESCRIPTION
.----     ----    ------     --------     -----     -----------
TEMPCLID  DIM       6      6        1     Clinic Id
TEMPMPRA  DIM       6      6        7     Medical Practice
TEMPPSRV  DIM       10     10       13    Service Provider Number
TEMPBTCH  DIM       5      5        23    Batch Number (1 Alpha 4 Numeric)
TEMPPMCI  DIM       8      8        28    MCI (3 Alpha 5 Numeric)
TEMPPYEE  DIM       10     10       36    Payee Provider Number
TEMPCLMN  DIM       8      8        46    Claim Number
.End of Record                      54
.
.         Temporary File Variables
.
ERASE     INIT      "iserase "
ISBUILD   INIT      "isbuild "
TEMPFILE  DIM       8
TOTCLMN   FORM      10.2
UKEY      INIT      " 54 UC23-27,46-53 UC1-6,23-27,46-53 UC7-12,23-27,46-53 UC13-22,23-27,46-53"
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
ALACCESS  FORM      1
CLINFROM  DIM       6
CLINICTO  DIM       6
CMDLINE   DIM       127
DISPNAME  DIM       60
DOCFNAME  DIM       50
FORM102   FORM      10.2
FROMDATE  DIM       8
FILTPROV  DIM       10
FILBTCTO  DIM       5
FILBTCFR  DIM       5
FILCLMTO  DIM       8
FILCLMFR  DIM       8
FMTSNAME  DIM       35
FMTGNAME  DIM       35
FMTTITLE  DIM       10
HEADFLAG  FORM      1
HEADING1  DIM       30
HEADING2  DIM       30
HEADING3  DIM       40
HEADING4  DIM       10
HEADING5  DIM       30
HEADING6  DIM       30    
HEADING7  DIM       30    
HEADING8  DIM       30    
HEADING9  DIM       30    
HEADING0  DIM       30    
LOOPFLAG  FORM      1
NAME35    DIM       35
PRACTIFR  DIM       6
PRACTITO  DIM       6
REFPROV   DIM       10
SITE      DIM       6
SAVEVAR   DIM       10
SAVEBTCH  DIM       5
SORTFLAG  FORM      1
TODATE    DIM       8
WEBUSRID  DIM       10
.
PRGID     INIT      "IBAHIC07"
PRGNAM    INIT      "Patients On Hold "
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
          BRANCH    EXIT,ML9999
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
.
          CALL      PRCF0000               * keyin practice from
          BRANCH    EXIT,ML0100
.
          CALL      PRCT0000               * keyin practice to
          BRANCH    EXIT,ML0100
.
          CALL      FCLN0000               * keyin clinic from
          BRANCH    EXIT,ML0100
.
          CALL      TCLN0000               * keyin clinic to
          BRANCH    EXIT,ML0100
.
          CALL      KPRV0000               * keyin provider
.
          CALL      DRNG0000               * keyin date range
.
ML4000    CALL      WEBU0000               * keyin user
          BRANCH    EXIT,ML0100
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML6000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML6000    CALL      CREA0000               * Create tempfile
.
          CALL      PROC0000               * process clinic id/medical prac
.
          CALL      PRIN0000               * Print data
          CALL      ENDP0000               * End of report
.
          CALL      KILL0000               * Delete tempfile
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          CALL      IBACLOCK
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,TEMPFILE
.
          OPEN      HICCITA1,"hiccitaf"       * item claim file
          OPEN      HICCLMA1,"hicclmaf"       * claim file
          OPEN      HICCLMA4,"hicclmaf"       * claim file
          OPEN      HICCLMA6,"hicclmaf"       * claim file
          OPEN      HICCLMA8,"hicclmaf"       * claim file
          OPEN      PMSHCPA1,"pmshcpaf"       * HCP file
          OPEN      PRIPRAC1,"pripracf"       * private practice
          OPEN      PRIDOCT1,"pridoctf"       * practice doctors
          OPEN      OUTSITA1,"outsitaf"       * outpatient site file
          OPEN      PATCODE1,"patcodes"       * codes file
          OPEN      WEBSECA1,"websecaf"       * web user id
          OPEN      PRIALSA1,"prialsaf"       * all practice security
          OPEN      PRISECA1,"prisecaf"       * practice security
          OPEN      PATMA1A1,"patma1af"       * master file
          OPEN      PATMX1A1,"patmx1af"
          OPEN      ALLREFA1,"allrefaf"
          OPEN      ALLLNKA2,"alllnkaf"
          OPEN      VISCMTA1,"viscmtaf"
          OPEN      PMSHCLA1,"pmshclaf"
.
          OPEN      OUTSITA1,"outsitaf"
          MOVE      IDDD,SITE
          MOVE      IDDD,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD,INIT9000
.
          PACK      CFNAME WITH UID,FILCLIA1        * OUTCLIFD
          OPEN      OUTCLIA1,CFNAME
.
          MOVE      ZERO,EXIT
          GOTO      INIT9999
.
INIT9000  DISPLAY   *P1:24,*EL,*B,"Site Code not on file. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
          GOTO      INIT9999
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          MOVE      SP70,CLINFROM
          MOVE      SP70,CLINICTO
          MOVE      SP70,PRACTIFR
          MOVE      SP70,PRACTITO
          MOVE      SP70,FILTPROV
.
          BRANCH    COPTION,OPTN9000          
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
.
.**************************************************************************
.*                              CREA0000               Called by:         *
.*             create a new temporary file                                *
.**************************************************************************
.
CREA0000  CALL      KILL0000                     * remove existing file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,TEMPFILE,UKEY
          EXECUTE   CMDLINE,TASKID               * create temporary index file
.
          OPEN      HICTEMP1,TEMPFILE            * open temp index file
          OPEN      HICTEMP2,TEMPFILE            * open temp index file
          OPEN      HICTEMP3,TEMPFILE            * open temp index file
          OPEN      HICTEMP4,TEMPFILE            * open temp index file
.
          MOVE      ZERO,ALACCESS           * Set all access to no
          PACK      KEY10,WEBUSRID,SP70
          CALL      RDPRALS1
          IF        OVRCD=0
            MOVE      ONE,ALACCESS
          ENDIF
.
CREA9999  RETURN
+
.****************************************************************************
.*                              KILL0000               Called by:           *
.*               close and erase the temporary file                         *
.****************************************************************************
.
KILL0000  CLOSE     HICTEMP1                     * close file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ERASE,TEMPFILE       * set file erase command
          EXECUTE   CMDLINE,TASKID               * erase temp file
.
KILL9999 RETURN
+
.**********************************************************************
.*                         PROC0000                                   *
.*                    By clinic id and medical practice               *
.**********************************************************************
PROC0000  MOVE      ZERO,LOOPFLAG
          MOVE      ONE,SORTFLAG          * sort by clinic id
          MATCH     SP70,CLINFROM
          GOTO      PROC2000 IF NOT EQUAL
.
          MOVE      THREE,SORTFLAG       * sort by provider
          MATCH     SP70,FILTPROV
          GOTO      PROC1000 IF NOT EQUAL * sort by clinic id if all blank
.
          MOVE      ONE,SORTFLAG          * default sort by clinic id
          MATCH     SP70,PRACTIFR
          GOTO      PROC0100 IF EQUAL
          MOVE      TWO,SORTFLAG          * sort by medical practice
.
.******************************************************************************
.         Position read from 'On Hold' status
.
PROC0100  MOVE      " 2",HCCLSTAT
          PACK      KEY18,HCCLSTAT,FROMDATE,SP70
          CALL      RSHCCLM8
PROC0200  CALL      RKHCCLM8
          BRANCH    OVRCD,PROC9999
.
          MATCH     " 2",HCCLSTAT         * Status on hold?
          GOTO      PROC9999 IF NOT EQUAL
.
          MATCH     SP70,TODATE
          IF        !@EQUAL
            MATCH     HCCLDATE,TODATE 
            GOTO      PROC9999 IF LESS
          ENDIF
.
          MATCH     SP70,FILTPROV
          IF        !@EQUAL
            MATCH     HCCLPSRV,FILTPROV
            GOTO      PROC0200 IF NOT EQUAL
          ENDIF
.
          MATCH     IDDD,HCCLSITE         * Same site?
          GOTO      PROC0200 IF NOT EQUAL
.
          GOTO      PROC1800              * go to check clinic id and med.prac.
.
.******************************************************************************
.         By Provider number
.
PROC1000  MOVE      ONE,LOOPFLAG
          PACK      KEY26,FILTPROV,FROMDATE,SP70
          CALL      RSHCCLM4
PROC1200  CALL      RKHCCLM4
          BRANCH    OVRCD,PROC9999
.
          MATCH     FILTPROV,HCCLPSRV      * Same provider
          GOTO      PROC9999 IF NOT EQUAL
.
          MATCH     SP70,TODATE
          IF        !@EQUAL
            MATCH     HCCLDATE,TODATE 
            GOTO      PROC9999 IF LESS
          ENDIF
.
          MATCH     " 2",HCCLSTAT           * check status
          GOTO      PROC1200 IF NOT EQUAL   * not on hold
.
          MATCH     IDDD,HCCLSITE           * Same site?
          GOTO      PROC1200 IF NOT EQUAL
.
.         Check range of clinic id
.
PROC1800  MATCH     SP70,CLINFROM
          IF        !@EQUAL
            MATCH     CLINFROM,HCCLCLID    * clinic id within range?
            GOTO      PROC9000 IF LESS
          ENDIF
.
          MATCH     SP70,CLINICTO
          IF        !@EQUAL
            MATCH     HCCLCLID,CLINICTO    * clinic id within range?
            GOTO      PROC9000 IF LESS
          ENDIF
          GOTO      PROC4000
.
.******************************************************************************
.         By clinic id
.
PROC2000  MOVE      TWO,LOOPFLAG
          PACK      KEY28,IDDD,CLINFROM,FROMDATE,SP70
          CALL      RSHCCLM6
PROC2200  CALL      RKHCCLM6
          BRANCH    OVRCD,PROC9999
.
          MATCH     IDDD,HCCLSITE       * Same site?
          GOTO      PROC9999 IF NOT EQUAL
.
          MATCH     SP70,CLINICTO
          IF        !@EQUAL
            MATCH     HCCLCLID,CLINICTO    * clinic id within range?
            GOTO      PROC9999 IF LESS
          ENDIF
.
          MATCH     SP70,FROMDATE
          IF        !@EQUAL
            MATCH     FROMDATE,HCCLDATE 
            GOTO      PROC2200 IF LESS
          ENDIF
.
          MATCH     SP70,TODATE
          IF        !@EQUAL
            MATCH     HCCLDATE,TODATE 
            GOTO      PROC2200 IF LESS
          ENDIF
.
          MATCH     " 2",HCCLSTAT           * check status
          GOTO      PROC2200 IF NOT EQUAL   * not on hold
.
.         Check for provider number
.
          MATCH     SP70,FILTPROV
          IF        !@EQUAL
            MATCH     HCCLPSRV,FILTPROV
            GOTO      PROC2200 IF NOT EQUAL
          ENDIF
.
.         Check for range of medical practice
.
PROC4000  MATCH     SP70,PRACTIFR
          IF        !@EQUAL
            MATCH     PRACTIFR,HCCLMPRA
            GOTO      PROC9000 IF LESS
          ENDIF
.
          MATCH     SP70,PRACTITO
          IF        !@EQUAL
            MATCH     HCCLMPRA,PRACTITO
            GOTO      PROC9000 IF LESS
          ENDIF
.
          BRANCH    ALACCESS,PROC6000       * Access to all practices 
.
          PACK      KEY16,HCCLMPRA,WEBUSRID,SP70
          CALL      RDPRSEC1
          BRANCH    OVRCD,PROC9000          * No access to this practice
.
PROC6000  CALL      WTMP0000                * write to tempfile
.
PROC9000  BRANCH    LOOPFLAG,PROC1200,PROC2200
          GOTO      PROC0200
.
PROC9999  RETURN
+
.**********************************************************************
.*                         WTMP0000                                   *
.*                        Write to tempfile                           *
.**********************************************************************
WTMP0000  MOVE      HCCLCLID,TEMPCLID
          MOVE      HCCLMPRA,TEMPMPRA
          MOVE      HCCLPSRV,TEMPPSRV
          MOVE      HCCLBTCH,TEMPBTCH
          MOVE      HCCLPMCI,TEMPPMCI
          MOVE      HCCLPYEE,TEMPPYEE
          MOVE      HCCLCLMN,TEMPCLMN
.
          PACK      KEY13,TEMPBTCH,TEMPCLMN,SP70
          CALL      RATEMPA
          IF        OVRCD=1
            CALL      WRTEMPA
          ENDIF
.
WTMP9999  RETURN
.
.**********************************************************************
.*                         PRIN0000                                   *
.*                        Print Data                                  *
.**********************************************************************
PRIN0000  MOVE      ZERO,CPAGENO
          MOVE      ZERO,TOTCLMN
          MOVE      SP70,SAVEBTCH
          MOVE      SP70,SAVEVAR
          CALL      RSTEMPA
PRIN1000  CALL      RKTEMPA
          BRANCH    OVRCD,PRIN9000
.
          MATCH     SP70,SAVEVAR
          GOTO      PRIN1200 IF EQUAL            * first record
          CALL      CVAR0000                     * check if sort field changed
          BRANCH    EXIT,PRIN1100                * changed
.
          GOTO      PRIN1300
.
PRIN1100  CALL      UND132
          PRINT     *69,"| TOTAL",*80,"|",TOTCLMN,*94,"|":
                    *N,*69,"*----------",*80,"*-------------*"
          ADD       TWO,CLNO
          MOVE      ZERO,TOTCLMN
          MOVE      ONE,CNOUNDLN                 * no underline
.
PRIN1200  CALL      HEAD0000
          MOVE      ZERO,CNOUNDLN
          CALL      SVAR0000                     * save sort variable
.
PRIN1300  MOVE      TEMPCLMN,KEY8
          CALL      RDHCCLM1
          BRANCH    OVRCD,PRIN1000               * Invalid claim number
.
          MOVE      SP70,REFPROV
          MOVE      SP70,ALRERDAT
          PACK      KEY16,HCCLVISN,SP70
          CALL      RSALLNK2
          CALL      RKALLNK2                * get referral link
          BRANCH    OVRCD,PRIN1600
.
          MATCH     HCCLVISN,ALLNLNKV
          GOTO      PRIN1600 IF NOT EQUAL   * same visit?
.
          PACK      KEY8,ALLNVISN,SP70
          CALL      RDALREF1                * get referral record
          BRANCH    OVRCD,PRIN1600
          CALL      PROV0000              * Get referring provider
.
PRIN1600  MOVE      HCCLURNO,KEY8
          CALL      RDMAST1
          IF        OVRCD=0
            MOVE      PSNAME,PACSNAME
            MOVE      PGNAME,PACGNAME
            MOVE      PTITL,PACTITLE
            MOVE      ONE,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,KEY23
          ENDIF
.
          PACK      KEY18,HCCLCLMN,SP70
          CALL      RSHCCIT1
PRIN2000  CALL      RKHCCIT1
          BRANCH    OVRCD,PRIN1000
.
          MATCH     HCCLCLMN,HCCICLMN
          GOTO      PRIN1000 IF NOT EQUAL        * Different claim number
.
          COMPARE   "48",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          MATCH     SP70,HCCIIDAT               * service date
          IF        !@EQUAL
            UNPACK    HCCIIDAT,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
          ENDIF
.
          MOVE      HCCICAMT,FORM102
          PRINT     *1,"|",HCCLCLMN,*10,"|",HCCLURNO:
                    *21,"|",KEY23,*46,"|",PMEDI:
                    *58,"|",CPCDATE:
                    *69,"|",HCCIITMN,*80,"|",FORM102;
.
          UNPACK    ALRERDAT,CCENT,CYEAR,CMON,CDAY * referral date
          CALL      PACDATE
          REP       " 0",CPDATE
.
          CALL      GETCMN00              * Get claim comment
          PRINT     *94,"|",REFPROV,*106,"|",CPCDATE:
                    *117,"|",KEY13,*132,"|"
          ADD       HCCICAMT,TOTCLMN              * total claim amount
.
          ADD       ONE,CLNO
          MOVE      ZERO,HEADFLAG
          GOTO      PRIN2000              * read next record
.
PRIN9000  COMPARE   ZERO,CPAGENO
          GOTO      PRIN9200 IF NOT EQUAL
          CALL      HEAD0000
          GOTO      PRIN9999
.
PRIN9200  CALL      UND132                   * finish off report
          PRINT     *69,"| TOTAL",*80,"|",TOTCLMN,*94,"|":
                    *N,*69,"*----------",*80,"*-------------*"
.
PRIN9999  RETURN
+
.*********************************************************************
.         Get claim comments
.*********************************************************************
GETCMN00  MOVE      SP70,KEY13
          PACK      KEY14,HCCIVISN,SP70
          CALL      RSVSCMT1
GETCMN10  CALL      RKVSCMT1
          BRANCH    OVRCD,GETCMN99
          MATCH     HCCIVISN,VSCTVIST     * Same visit number
          GOTO      GETCMN99 IF NOT EQUAL
          PACK      KEY13,VSCTDATA
GETCMN99  RETURN
.
.*********************************************************************
.         Get the referring provider from either the referring HCP/Practice
.         (if not blank), or the free format referring HCP field.
.*********************************************************************
PROV0000  MOVE      ZERO,EXIT
          MATCH     SP10,ALRERHCP                * referring HCP blank ?
          IF        !@EQUAL
            MATCH     SP10,ALRERHCR              * no - ref. HCP Practice blank?
            IF        !@EQUAL
              PACK      KEY20,ALRERHCP,ALRERHCR  * no
              CALL      RDPMHCL1                 * HCP link record found ?
              BRANCH    OVRCD,PROV5000           * no - get free format HCP
.
              MATCH     SP10,PMHLPRV1
              IF        !@EQUAL
                MOVE      PMHLPRV1,REFPROV 
                GOTO      PROV9999
              ENDIF
.
              MATCH     SP10,PMHLPRV2
              IF        !@EQUAL
                MOVE      PMHLPRV2,REFPROV
                GOTO      PROV9999
              ENDIF
.
              MATCH     SP10,PMHLPRV3
              IF        !@EQUAL
                MOVE      PMHLPRV3,REFPROV
                GOTO      PROV9999
              ENDIF
.
              MATCH     SP10,PMHLPRV4
              IF        !@EQUAL
                MOVE      PMHLPRV4,REFPROV
                GOTO      PROV9999
              ENDIF
.
              MATCH     SP10,PMHLPRV5
              IF        !@EQUAL
                MOVE      PMHLPRV5,REFPROV
                GOTO      PROV9999
              ENDIF
              GOTO      PROV8000                 * provider not found
            ENDIF
          ENDIF
.
PROV5000  MATCH     SP70,ALREHCPP
          IF        !@EQUAL
            MOVE      ALREHCPP,REFPROV
            GOTO      PROV9999
          ENDIF
PROV8000  MOVE      ONE,EXIT
PROV9999  RETURN
.
.*********************************************************************
.         Save sort variable
.*********************************************************************
SVAR0000  MOVE      SP70,SAVEVAR
          COMPARE   ONE,SORTFLAG
          GOTO      SVAR4000 IF EQUAL    * sort by clinic id
          COMPARE   TWO,SORTFLAG
          GOTO      SVAR6000 IF EQUAL    * sort by medical practice
.
.         Sort by service provider
.
          PACK      SAVEVAR,TEMPPSRV,SP70
          GOTO      SVAR9999
.
.         Sort by clinic id
.
SVAR4000  PACK      SAVEVAR,TEMPCLID,SP70
          GOTO      SVAR9999
.
.         Sort by medical practice
.
SVAR6000  PACK      SAVEVAR,TEMPMPRA,SP70
.
SVAR9999  RETURN
+
.*********************************************************************
.         check if sort variable change
.*********************************************************************
CVAR0000  MOVE      ZERO,EXIT
          COMPARE   ONE,SORTFLAG
          GOTO      CVAR4000 IF EQUAL    * sort by clinic id
          COMPARE   TWO,SORTFLAG
          GOTO      CVAR6000 IF EQUAL    * sort by medical practice
.
          MATCH     SAVEVAR,TEMPPSRV     * check service provider
          GOTO      CVAR9999 IF EQUAL
          GOTO      CVAR9000
.
CVAR4000  MATCH     SAVEVAR,TEMPCLID     * check clinic id
          GOTO      CVAR9999 IF EQUAL
          GOTO      CVAR9000
.
CVAR6000  MATCH     SAVEVAR,TEMPMPRA     * check medical practice
          GOTO      CVAR9999 IF EQUAL
.
CVAR9000  MOVE      ONE,EXIT
CVAR9999  RETURN

.*********************************************************************
.         print heading
.*********************************************************************
PRHD0000  COMPARE   ONE,SORTFLAG
          GOTO      PRHD4000 IF EQUAL    * sort by clinic id
          COMPARE   TWO,SORTFLAG
          GOTO      PRHD6000 IF EQUAL    * sort by medical practice
          GOTO      PRHD8000             * by provider no
.
PRHD4000  MOVE      SP70,OCADESC
          PACK      KEY20,IDDD,TEMPCLID,Z70
          CALL      RDSCLIA1
          CALL      RDPCLIA1                *To get Clinic ID Description
          BRANCH    OVRCD,PRHD4100
.
          MATCH     IDDD,OCASITE
          GOTO      PRHD4100 IF NOT EQUAL
.
          MATCH     TEMPCLID,OCACLIN
          GOTO      PRHD4100 IF NOT EQUAL
.
          MATCH     SP70,OCADESC            * Read HCP if no doctor
          GOTO      PRHD4200 IF NOT EQUAL
.
          PACK      KEY10,OCADOCT,SP70
          CALL      RDPMHCP1
          BRANCH    OVRCD,PRHD4200
          MOVE      PMHCSNAM,PACSNAME
          MOVE      PMHCGNAM,PACGNAME
          MOVE      PMHCTITL,PACTITLE
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,OCADESC
          GOTO      PRHD4200
.
PRHD4100  MOVE      SP70,OCADESC
.
PRHD4200  PRINT     *N,*1,"Clinic Id : ",OCADESC,"(",TEMPPSRV,")"
          GOTO      PRHD9000
.
PRHD6000  PACK      PRPRDESC,SP70
          PACK      KEY6,TEMPMPRA
          CALL      RDPRPR1
          PRINT     *N,*1,"Medical Practice : ",PRPRDESC
          GOTO      PRHD9000
.
PRHD8000  MATCH     "All",HEADING0
          GOTO      PRHD9999 IF NOT EQUAL
          PRINT     *N,*1,"Provider No.: ",TEMPPSRV
.
PRHD9000  ADD       TWO,CLNO
PRHD9999  RETURN
+
.*********************************************************************
.         print a new page
.*********************************************************************
HEAD0000  CALL      HEAD132
.
          PRINT     *1,"Medical Practice From : ",HEADING7," To : ",HEADING8
          PRINT     *1,"Clinic From           : ",HEADING5," To : ",HEADING6
          PRINT     *1,"Provider Number       : ",HEADING0
.
          MATCH     SP70,FROMDATE
          IF        @EQUAL
            PRINT     *1,"Claim Date From       : Start   to";
          ELSE
            UNPACK    FROMDATE,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            PRINT     *1,"Claim Date From       : ",CPCDATE," to";
          ENDIF
.
          MATCH     SP70,TODATE
          IF        @EQUAL
            PRINT     *40,"Finish"
          ELSE
            UNPACK    TODATE,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            PRINT     *40,CPCDATE
          ENDIF
.
          ADD       FOUR,CLNO
.
          CALL      PRHD0000                     * print sort heading
          CALL      UND132
          PRINT     *1,"|Claim No",*10,"| U/R No":
                    *21,"|Patient Name",*46,"|Medicare ##",*58,"|Serv.Date":
                    *69,"|Item No",*80,"| Item Amount":
                    *94,"|Ref.Prov":
                    *106,"|Ref.Date":
                    *117,"|Claim Comnt.",*132,"|"
          CALL      UND132
          ADD       THREE,CLNO
          MOVE      ONE,HEADFLAG
.
HEAD9999  RETURN
+
.**********************************************************************
.*                           ENDP0000                                 *
.*                      Print the end of the report                   *
.**********************************************************************
ENDP0000  PRINT     *N,*50,"*** End of Report ***"
ENDP9999  RETURN
+
.******************************************************************************
.*                                   FBTCH000             Called by: ML0000   *
.*                             Keyin the Batch number                         *
.******************************************************************************
FBTCH000  MOVE      SP70,FILBTCFR
          KEYIN     *P1:9,*EL,"Batch Number : ",*V2LON,FILBTCFR
.
          PACK      FILBTCFR,FILBTCFR,SP70
          MATCH     SP70,FILBTCFR
          GOTO      FBTCH200 IF EQUAL
.
          MOVE      FILBTCFR,HEADING1
          DISPLAY   *P29:9,*EL,*V2LON,FILBTCFR;
          MOVE      ZERO,EXIT
          GOTO      FBTCH999
.
FBTCH200  MOVE      "All",HEADING1
          DISPLAY   *P29:9,*EL,*V2LON,"All";
          MOVE      ZERO,EXIT
          GOTO      FBTCH999
.
FBTCH999  RETURN
+
.******************************************************************************
.*                                   TBTCH000             Called by: ML0000   *
.*                             Keyin the Batch number                         *
.******************************************************************************
TBTCH000  MOVE      SP70,FILBTCTO
          KEYIN     *P1:10,*EL,"Batch Number : ",*V2LON,FILBTCTO
.
          PACK      FILBTCTO,FILBTCTO,SP70
          MATCH     SP70,FILBTCTO
          GOTO      TBTCH200 IF EQUAL
.
          MOVE      FILBTCTO,HEADING2
          DISPLAY   *P29:10,*EL,*V2LON,FILBTCTO;
          MOVE      ZERO,EXIT
          GOTO      TBTCH999
.
TBTCH200  MOVE      "All",HEADING2
          DISPLAY   *P29:10,*EL,*V2LON,"All";
          MOVE      ZERO,EXIT
          GOTO      TBTCH999
.
TBTCH999  RETURN
.
.******************************************************************************
.*                                   FCLMN000             Called by: ML0000   *
.*                             Keyin the Claim number                         *
.******************************************************************************
FCLMN000  MOVE      SP70,FILCLMFR
          KEYIN     *P1:9,*EL,"Claim Number : ",*JR,*V2LON,FILCLMFR
.
          PACK      FILCLMFR,FILCLMFR,SP70
          MATCH     SP70,FILCLMFR
          GOTO      FCLMN200 IF EQUAL
.
          MOVE      FILCLMFR,HEADING3
          DISPLAY   *P29:9,*EL,*V2LON,FILCLMFR;
          MOVE      ZERO,EXIT
          GOTO      FCLMN999
.
FCLMN200  MOVE      "All",HEADING3
          DISPLAY   *P29:9,*EL,*V2LON,"All";
          MOVE      ZERO,EXIT
          GOTO      FCLMN999
.
FCLMN999  RETURN
+
.******************************************************************************
.*                                   TCLMN000             Called by: ML0000   *
.*                             Keyin the Claim number                         *
.******************************************************************************
TCLMN000  MOVE      SP70,FILCLMTO
          KEYIN     *P1:10,*EL,"Claim Number : ",*JR,*V2LON,FILCLMTO
.
          PACK      FILCLMTO,FILCLMTO,SP70
          MATCH     SP70,FILCLMTO
          GOTO      TCLMN200 IF EQUAL
.
          MOVE      FILCLMTO,HEADING4
          DISPLAY   *P29:10,*EL,*V2LON,FILCLMTO;
          MOVE      ZERO,EXIT
          GOTO      TCLMN999
.
TCLMN200  MOVE      "All",HEADING4
          DISPLAY   *P29:10,*EL,*V2LON,"All";
          MOVE      ZERO,EXIT
          GOTO      TCLMN999
.
TCLMN999  RETURN
.
.******************************************************************************
.*                                   FCLN0000             Called by: ML0000   *
.*                             Keyin The Clinic Code From                     *
.******************************************************************************
FCLN0000  KEYIN     *P1:11,*EL,"Clinic Id From : ",*V2LON,CLINFROM
.
          PACK      CLINFROM,CLINFROM,SP70
          MATCH     SP70,CLINFROM
          GOTO      FCLN6000 IF EQUAL
.
          PACK      KEY20,IDDD,CLINFROM,Z70
          CALL      RDSCLIA1
FCLN1000  CALL      RDPCLIA1
          BRANCH    OVRCD,FCLN9500
.
          MATCH     OCASITE,IDDD         * Must be in the Same Site
          GOTO      FCLN9500 IF NOT EQUAL
.
          MATCH     OCACLIN,CLINFROM     * Must be in the Same clinic
          GOTO      FCLN9500 IF NOT EQUAL
.
          MATCH     "1",OTCLIACT
          GOTO      FCLN1000 IF EQUAL
.
          MATCH     SP70,OCADESC            * Read HCP if no doctor
          GOTO      FCLN5000 IF NOT EQUAL
.
          PACK      KEY10,OCADOCT,SP70
          CALL      RDPMHCP1
          IF        OVRCD=0
            MOVE      PMHCSNAM,PACSNAME
            MOVE      PMHCGNAM,PACGNAME
            MOVE      PMHCTITL,PACTITLE
            MOVE      ONE,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,OCADESC
          ELSE
            MOVE      "Invalid clinic HCP Code",OCADESC
          ENDIF
.
FCLN5000  MOVE      OCADESC,HEADING5
          DISPLAY   *P25:11,*EL,*V2LON,OCADESC;
          MOVE      ZERO,EXIT
          GOTO      FCLN9999
.
FCLN6000  MOVE      "Start",HEADING5
          DISPLAY   *P25:11,*EL,*V2LON,"Start";
          MOVE      ZERO,EXIT
          GOTO      FCLN9999
.
FCLN9500  DISPLAY   *P1:24,*EL,*B,"Invalid Clinic id. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
FCLN9999  RETURN
+
.******************************************************************************
.*                                   TCLN0000             Called by: ML0000   *
.*                             Keyin The Clinic Code To                       *
.******************************************************************************
TCLN0000  KEYIN     *P1:12,*EL,"Clinic Id To   : ",*V2LON,CLINICTO
.
          PACK      CLINICTO,CLINICTO,SP70
          MATCH     SP70,CLINICTO
          GOTO      TCLN6000 IF EQUAL
.
          PACK      KEY20,IDDD,CLINICTO,Z70
          CALL      RDSCLIA1
TCLN1000  CALL      RDPCLIA1
          BRANCH    OVRCD,TCLN9500
.
          MATCH     OCASITE,IDDD         * Must be in the Same Site
          GOTO      TCLN9500 IF NOT EQUAL
.
          MATCH     OCACLIN,CLINICTO     * Must be in the Same clinic
          GOTO      TCLN9500 IF NOT EQUAL
.
          MATCH     "1",OTCLIACT
          GOTO      TCLN1000 IF EQUAL
.
          MATCH     SP70,OCADESC            * Read HCP if no doctor
          GOTO      TCLN5000 IF NOT EQUAL
.
          PACK      KEY10,OCADOCT,SP70
          CALL      RDPMHCP1
          IF        OVRCD=0
            MOVE      PMHCSNAM,PACSNAME
            MOVE      PMHCGNAM,PACGNAME
            MOVE      PMHCTITL,PACTITLE
            MOVE      ONE,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,OCADESC
          ELSE
            MOVE      "Invalid clinic HCP Code",OCADESC
          ENDIF
.
TCLN5000  MOVE      OCADESC,HEADING6
          DISPLAY   *P25:12,*EL,*V2LON,OCADESC;
          MOVE      ZERO,EXIT
          GOTO      TCLN9999
.
TCLN6000  MOVE      "Finish",HEADING6
          DISPLAY   *P25:12,*EL,*V2LON,"Finish";
          MOVE      ZERO,EXIT
          GOTO      TCLN9999
.
TCLN9500  DISPLAY   *P1:24,*EL,*B,"Invalid Clinic id. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
TCLN9999  RETURN
.
+
.******************************************************************************
.*                                   PRCF0000             Called by: ML0000   *
.*                             Keyin The medical practice from                *
.******************************************************************************
PRCF0000  KEYIN     *P1:13,*EL,"Medical Practice : ",*V2LON,PRACTIFR
.
          PACK      PRACTIFR,PRACTIFR,SP70
          MATCH     SP70,PRACTIFR
          GOTO      PRCF6000 IF EQUAL
.
          PACK      KEY6,PRACTIFR,SP70
          CALL      RDPRPR1
          BRANCH    OVRCD,PRCF9500
.
PRCF5000  MOVE      PRPRDESC,HEADING7
          DISPLAY   *P27:13,*EL,*V2LON,PRPRDESC;
          MOVE      ZERO,EXIT
          GOTO      PRCF9999
.
PRCF6000  MOVE      "Start",HEADING7
          DISPLAY   *P27:13,*EL,*V2LON,"Start";
          MOVE      ZERO,EXIT
          GOTO      PRCF9999
.
PRCF9500  DISPLAY   *P1:24,*EL,*B,"Invalid Medical Practice. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
PRCF9999  RETURN
+
.******************************************************************************
.*                                   PRCT0000             Called by: ML0000   *
.*                             Keyin The medical practice from                *
.******************************************************************************
PRCT0000  KEYIN     *P1:14,*EL,"Medical Practice : ",*V2LON,PRACTITO
.
          PACK      PRACTITO,PRACTITO,SP70
          MATCH     SP70,PRACTITO
          GOTO      PRCT6000 IF EQUAL
.
          PACK      KEY6,PRACTITO,SP70
          CALL      RDPRPR1
          BRANCH    OVRCD,PRCT9500
.
PRCT5000  MOVE      PRPRDESC,HEADING8
          DISPLAY   *P27:14,*EL,*V2LON,PRPRDESC;
          MOVE      ZERO,EXIT
          GOTO      PRCT9999
.
PRCT6000  MOVE      "Finish",HEADING8
          DISPLAY   *P27:14,*EL,*V2LON,"Finish";
          MOVE      ZERO,EXIT
          GOTO      PRCT9999
.
PRCT9500  DISPLAY   *P1:24,*EL,*B,"Invalid Medical Practice. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
PRCT9999  RETURN
.
+
.******************************************************************************
.*                                   KPRV0000             Called by: ML0000   *
.*                             Keyin the provider number                      *
.******************************************************************************
KPRV0000  KEYIN     *P1:16,*EL,"Provider Number : ",*V2LON,FILTPROV
.
          PACK      FILTPROV,FILTPROV,SP70
          MATCH     SP70,FILTPROV
          GOTO      KPRV6000 IF EQUAL
.
KPRV5000  MOVE      FILTPROV,HEADING0
          DISPLAY   *P29:16,*EL,*V2LON,FILTPROV;
          MOVE      ZERO,EXIT
          GOTO      KPRV9999
.
KPRV6000  MOVE      "All",HEADING0
          DISPLAY   *P29:16,*EL,*V2LON,"All";
          MOVE      ZERO,EXIT
          GOTO      KPRV9999
.
KPRV9999  RETURN
+
.******************************************************************************
.*                                   DRNG0000             Called by: ML0000   *
.*                             Keyin the date range                           *
.******************************************************************************
DRNG0000  DISPLAY   *P1:17,*EL,"From Date : ":
                    *P1:18,*EL,"  To Date : ";
          UNPACK    SP70,FROMDATE,TODATE
          MOVE      ZERO,CHIGHLT
          MOVE      ZERO,CDEFDTE
          MOVE      TEN7,CVERT
          MOVE      TEN3,CCOL
          UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          CALL      KEYDATE
          BRANCH    OVRCD,DRNG2000
          PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",FROMDATE
.
DRNG2000  MOVE      TEN8,CVERT
          UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          CALL      KEYDATE
          BRANCH    OVRCD,DRNG9999
          PACK      TODATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE
.
DRNG9999  RETURN
+
.******************************************************************************
.*                                   WEBU0000             Called by: ML0000   *
.*                             Keyin The web user id                          *
.******************************************************************************
WEBU0000  MOVE      SP70,WEBUSRID
          KEYIN     *P1:19,*EL,"User Id : ",*V2LON,WEBUSRID
.
          PACK      WEBUSRID,WEBUSRID,SP70
.
          PACK      KEY10,WEBUSRID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,WEBU9500
.
          MOVE      ZERO,EXIT
          GOTO      WEBU9999
.
WEBU9500  DISPLAY   *P1:24,*EL,*B,"Invalid User Id. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
WEBU9999  RETURN
+
.****************************************************************************
.*        IO ROUTINES FOR TEMPORARY FILE                                    *
.****************************************************************************
RATEMPA   RESET     KEY13
          MOVE      ZERO,OVRCD
          READ      HICTEMP1,KEY13;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMPA   WRITE     HICTEMP1,KEY13;TEMPCLID,TEMPMPRA,TEMPPSRV,TEMPBTCH:
                                   TEMPPMCI,TEMPPYEE,TEMPCLMN
          RETURN
.
RSTEMPA   MOVE      SP70,KEY19
          MOVE      SP70,KEY23
          IF        SORTFLAG=1
            READ      HICTEMP2,KEY19;;
          ELSE
            IF        SORTFLAG=2
              READ      HICTEMP3,KEY19;;
            ELSE
              READ      HICTEMP4,KEY23;;
            ENDIF
          ENDIF
          RETURN
.
RKTEMPA   MOVE      ZERO,OVRCD
          IF        SORTFLAG=1
            READKS    HICTEMP2;TEMPCLID,TEMPMPRA,TEMPPSRV,TEMPBTCH:
                               TEMPPMCI,TEMPPYEE,TEMPCLMN
          ELSE
            IF        SORTFLAG=2
              READKS    HICTEMP3;TEMPCLID,TEMPMPRA,TEMPPSRV,TEMPBTCH:
                                 TEMPPMCI,TEMPPYEE,TEMPCLMN
            ELSE
              READKS    HICTEMP4;TEMPCLID,TEMPMPRA,TEMPPSRV,TEMPBTCH:
                                 TEMPPMCI,TEMPPYEE,TEMPCLMN
            ENDIF
          ENDIF
          GOTO      OVERCOND IF OVER
          RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       TFILENAM                * Generate Temp File Name
.
          INC       IBASEQIO/INC            * Sequential Numbers File
          INC       WEBERRIO/INC            * Web Server Error Logging
          INC       HICCLMIO/INC
          INC       HICCITIO/INC
          INC       PMSHCPIO/INC
          INC       PRIPRAIO/INC
          INC       PRIDOCIO/INC
          INC       OUTSITIO/INC
          INC       OUTCLIIO/INC
          INC       PATCODIO/INC
          INC       WEBSECIO/INC
          INC       PRIALSIO/INC
          INC       PRISECIO/INC
          INC       PATMA1IO/INC
          INC       PMSHCLIO/INC
          INC       ALLREFIO/INC
          INC       ALLLNKIO/INC
          INC       VISCMTIO/INC
.

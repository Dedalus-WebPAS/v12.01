.***************************************************************************
.* System    :   EDWARD Emergency Extract                                  *
.* Program   :   FX900517                                                  *
.* Desc      :   Write ED visit update trigger to report missing Obj 63    *
.*           :   records                                                   *
.***************************************************************************
.* Date      :   03/12/2020                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   This program will check ED visits from July 2018 to Sep 14*
.*           :   2020 for missing object 63 records. If none are found     *
.*           :   the it will write and update ED visit record to pmsadwaf. *
.*           :   Then they will be reported in the next extract            *
.* Modifications  :                                                        *
.*        V11.00.01 08/12/2020  Ebon Clements TSK 0900517                  *
.*                  Added display and print of visit date. Added print of  *
.*                  total records checked.                                 *
.*        V11.00.00 03/12/2020  Ebon Clements TSK 0900517                  *
.*                  Created program                                        *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
.
          INC       COMEWDFD/INC
          INC       EMRVISFD/INC
          INC       PMSADWFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
RECCOUNT  FORM      8
CURRDATE  DIM       8
DELETSTM  DIM       3
DELETOBJ  DIM       3
DISPFDAT  DIM       11
DISPTDAT  DIM       11
STRTDATE  DIM       8
ENDDDATE  DIM       8
VISTDATE  DIM       11
.
.
PRGID     INIT      "FX900517"
PRGNAM    INIT      "Trigger missing EDWARD ED obj 63 records"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      DATE0000               * keyin date range
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      COMEWDA2,"comewdaf"
          OPEN      EMRVISA4,"emrvisaf"
          OPEN      PMSADWA1,"pmsadwaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Report Only":
                    *P1:6,*V2LON,TWO,*HOFF,". Run Report and Trigger Records"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000,OPTN9000    * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
+
.***************************************************************************
.*                               DATE0000                                  *
.*                          accept date range                              *
.***************************************************************************
.
.---------- keyin first date range -------
.
DATE0000    MOVE       FALSE,EXIT
            DISPLAY    *P1:10,*EF,"ED Visit Date From :":
                          *P1:11,"ED Visit Date To   :";
.
            MOVE      "10",CVERT
            MOVE      "22",CCOL
            CALL      IBACLOCK
            PACK      CURRDATE,CCC,CYY,CMM,CDD
            REP       " 0",CURRDATE
            MOVE      CCC,CCENT
            MOVE      CYY,CYEAR
            MOVE      CMM,CMON
            MOVE      CDD,CDAY

            CALL      KEYDATE                          * enter the from date
            PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY   * move to form field
            REP       " 0",STRTDATE                    * from date
.
            MATCH     CURRDATE,STRTDATE
            IF        @EQUAL
              DISPLAY    *P1:24,*EL,*B,"Fix can't be run for the current date":
                         " Hit <",*V2LON,"ENTER",*HOFF,"> To Continue ";
              KEYIN      ANS;
              DISPLAY    *P1:24,*EL;
              GOTO       DATE0000
            ENDIF
.
            MATCH     "20180701",STRTDATE
            IF        @LESS
              DISPLAY    *P1:24,*EL,*B,"Fix can't be run before July 2018":
                         " Hit <",*V2LON,"ENTER",*HOFF,"> To Continue ";
              KEYIN      ANS;
              DISPLAY    *P1:24,*EL;
              GOTO       DATE0000
            ENDIF
.
            MOVE       "11",CVERT
            MOVE       "22",CCOL
            CALL      KEYDATE                          * enter the from date
            PACK      ENDDDATE,CCENT,CYEAR,CMON,CDAY   * move to form field
            REP       " 0",ENDDDATE                    * from date
.
            MATCH     CURRDATE,ENDDDATE
            IF        @EQUAL
              DISPLAY    *P1:24,*EL,*B,"Fix can't be run for the current date":
                         " Hit <",*V2LON,"ENTER",*HOFF,"> To Continue ";
              KEYIN      ANS;
              DISPLAY    *P1:24,*EL;
              GOTO       DATE0000
            ENDIF
.
            MATCH     "20200915",ENDDDATE
            IF        !@LESS
              DISPLAY    *P1:24,*EL,*B,"Fix can't be run after Sept 14 2020":
                         " Hit <",*V2LON,"ENTER",*HOFF,"> To Continue ";
              KEYIN      ANS;
              DISPLAY    *P1:24,*EL;
              GOTO       DATE0000
            ENDIF
.
            MATCH      STRTDATE,ENDDDATE
            GOTO       DATE9999 IF NOT LESS
.
            DISPLAY    *P1:24,*EL,*B,"2nd Range Must Be Greater Than 1st ":
                       "Range - Hit <",*V2LON,"ENTER",*HOFF,"> To Continue ";
            KEYIN      ANS;
            DISPLAY    *P1:24,*EL;
            GOTO       DATE0000
.
DATE9999    RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  CALL      IBACLOCK
          CALL      HEAD80
.
          UNPACK    STRTDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DISPFDAT
.
          UNPACK    ENDDDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DISPTDAT
.
          PRINT     *20,"Visit Dates :",DISPFDAT," to ",DISPTDAT
.
          IF        COPTION=1
            PRINT     *20,"Report Only"
          ELSE
            PRINT     *20,"Report and Trigger Records"
          ENDIF
.
          CALL      UND80
.
          PRINT     *1,"| ED Visit ",*12,"| Visit Date",*80,"|"
.
          CALL      UND80
          MOVE      ZERO,RECCOUNT           * Records Checked
.
          MOVE      "18 ",DELETSTM          * 18=Emergency Department
          MOVE      "63 ",DELETOBJ          * SERVICE_EVENT_PROVIDER
.
          PACK      KEY24,STRTDATE,SP70
          CALL      RSEMVIS4
PROC1000  CALL      RKEMVIS4
          BRANCH    OVRCD,PROC9000
.
          MATCH     EMVIDATE,ENDDDATE        * End Date
          GOTO      PROC9000 IF LESS

          ADD       ONE,RECCOUNT           * Records Checked
          UNPACK    EMVIDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,VISTDATE
.
          DISPLAY   *P25:15,*EL,*V2LON,"ED Visit: ",VISTDATE,SP4,DEMVIADM
.
          COMPARE   FOUR,EMVISTAT           * Skip cancelled ED visit
          GOTO      PROC1000 IF EQUAL
.
          MATCH     "       0",EMVIURNO     * Skip Zero U/R
          GOTO      PROC1000 IF EQUAL
.
          PACK      KEY112,DELETSTM,DELETOBJ,DEMVIADM,SP70
          CALL      RSCMEWD2
          CALL      RKCMEWD2                * Check for an ED Obj 63
          BRANCH    OVRCD,PROC5000
.
          MATCH     DELETSTM,CMEWDSTR       * Stream 18
          GOTO      PROC5000 IF NOT EQUAL
.
          MATCH     DELETOBJ,CMEWOBJT       * Correct Object 63
          GOTO      PROC5000 IF NOT EQUAL
.
          MATCH     DEMVIADM,CMEWOKEY       * Only match visit number
          GOTO      PROC1000 IF EQUAL             
.
PROC5000  CALL      WEDV0000                * Write ED update trigger
          PRINT     *1,"| ",DEMVIADM,*12,"| ",VISTDATE,*80,"|"
.
          GOTO      PROC1000
.
PROC9000  CALL      UND80
          PRINT     *1," Number of emrvisaf records checked :",RECCOUNT
          DISPLAY   *P1:20,*EF,"Fix Complete.";
          CALL      HITENTER
.
PROC9999  RETURN
+
.-----------------------------------------------------------
. Write EDWARD emergency visit alteration record
.------------------------------------------------------------
WEDV0000  BRANCH    COPTION,WEDV9999        * Report Only
.
          MOVE      EMVIADMN,PMAWVISN
          MOVE      FOUR,PMAWTYPE
          PACK      PMAWOLDV,SP70,SP70,SP70,SP70
          PACK      PMAWNEWV,SP70,SP70,SP70,SP70
.
          CALL      IBACLOCK
          PACK      PMAWCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PMAWCDAT
          MOVE      "00:00:00",PMAWCTIM
          MOVE      "iba",PMAWCUID
          PACK      PMAWSPAR,SP70,SP70
.
          PACK      KEY27,PMAWCDAT,PMAWCTIM,PMAWVISN,PMAWTYPE,SP70
          CALL      RAPMADW1
          IF        OVRCD=1
            CALL      WRPMADW1
          ELSE
            CALL      UPPMADW1
          ENDIF
.
WEDV9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       COMEWDIO/INC
          INC       EMRVISIO/INC
          INC       PMSADWIO/INC

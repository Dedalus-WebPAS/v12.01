.*******************************************************************
.*  System    :     Outpatient System                              *
.*  Program   :     IBAOUT94                                       *
.*  Desc      :     Outpatient Units Maintenance                   *
.*******************************************************************
.*  Date      :     12/04/91                                       *
.*  Author    :     Peter Eddey                                    *
.*  Function  :     To maintain the outpatients units file         *
.*  Mods      :                                                    *
.*******************************************************************
.*    V10.09.01  28/11/2016  Ania P         CAR 261630             *
.*               PORT CAR changes                                  *
.*    V10.07.01  04/11/2015  Ebon Clements  CAR 268970             *          
.*               Change to use OUTCLIFD index 1 instead of index 2 *
.*     V5.08.01  19/09/2000  Steve Armstrong   srf 647263          *
.*               Recompiled for changes to OUTDSCLN                *
.*    V5.08.B01  16/03/2000  Steve Armstrong     5.08 Mods         *
.*               Mods for changes to OUTCLIFD (effective date)     *
.*******************************************************************
.*       V5.07.B01  10/12/1998  Jim Stathopoulos                   *
.*                  507 Changes                                    *
.*                                                                 *
.*******************************************************************
.
          INC       STD001FD
          INC       OUTUNIFD/INC
          INC       OUTSITFD/INC
          INC       OUTCLIFD/INC
          INC       OUTCTYFD/INC 
          INC       PATTMAFD/INC
          INC       PATDO1FD/INC
          INC       PATCODFD/INC
          INC       IBASEQFD/INC 
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
Z20       INIT      "zzzzzzzzzzzzzzzzzzzz"
.
PRGID     INIT      "IBAOUT94"
PRGNAM    INIT      "Outpatient Units Maintenance"
VERSION   INIT      "V12.01.00"
.
.********************************************************************
.*                   VARIABLES REQUIRED                             *
.********************************************************************
.
CODE      DIM       2
CODEAC    INIT      "AC"
CLIAF     INIT      "cliaf"
CLINFILE  DIM        8
CTYAF     INIT      "ctyaf"
DIM14     DIM       14
DIM30     DIM       30
DIM40     DIM       40
DIM3      DIM       3
DOCTDESC  DIM       30
ENDDATE   DIM       10
SPECIALT  DIM       20
STRTDATE  DIM       10
TYPEFILE  DIM       8
.
.********************************************************************
.*                        ML0000                                    *
.*                                                                  *
.*                     Mainline Code                                *
.********************************************************************
.
ML0000
          CALL      INIT0000             * initialisation routine
ML0500    
          CALL      SELOPTN              * get menu selection
          BRANCH    OPTION,ML1000,ML2000,ML3000
          GOTO      ML9999
          
.
.----- Insert Inpatient unit details ----
.
ML1000 
          CALL      CVAR0000             * clear the variables
          CALL      SCRN0000             * display the screen
.
ML1100    CALL      KSIT0000             * keyin the site       
          BRANCH    EXIT,ML0500
.
ML1200    CALL      KCTY0000             * keyin the Clinic type
          BRANCH    EXIT,ML1100
.
ML1300    CALL      KCLI0000             * keyin the clinic
          BRANCH    EXIT,ML1200
.
          CALL      FILE0000             * test if onfile
          BRANCH    EXIT,ML1300
.
          CALL      KTMC0000             * keyin the team code[
          BRANCH    EXIT,ML1300
.
          CALL      KSDT0000             * keyin start date
          CALL      KEDT0000             * keyin end date
.
          CALL      SIPC0000
          BRANCH    EXIT,ML1300
.
          CALL      POST0000
          CALL      CLSC0000             * clear the screen
          MOVE      ONE,AUDTFLAG
          CALL      AUDT0000             * write to audit file
          GOTO      ML1300
.
.
.----- Modify Inpatient unit details ----
.         
ML2000
          CALL      CVAR0000             * Clear the required variables
          CALL      SCRN0000             * display the screen
.
ML2100    CALL      KSIT0000             * keyin the site       
          BRANCH    EXIT,ML0500
.
ML2200    CALL      KCTY0000             * keyin the Clinic type
          BRANCH    EXIT,ML2100
.
ML2300    CALL      KCLI0000             * keyin the clinic
          BRANCH    EXIT,ML2200
.
          CALL      FILE0000             * test if onfile
          BRANCH    EXIT,ML2300
.
          CALL      SVAR0000             * set up and display variable
.                                          descriptions
          MOVE      TWO,AUDTFLAG
          CALL      AUDT0000             * write to audit file
.
          CALL      SIPC0000             * select item post cancel
          BRANCH    EXIT,ML2500
.
          CALL      UPOTUNI1
          CALL      CLSC0000
          MOVE      THREE,AUDTFLAG
          CALL      AUDT0000             * write to audit file
.
          GOTO      ML2300
ML2500
          CALL      DAUD0000             * Exit selected so delete the
.                                          "B" audit record that was
.                                           written             
          GOTO      ML2300
.
.----- Delete Inpatient unit details ----
.         
ML3000
          CALL      CVAR0000             * Clear the required variables
          CALL      SCRN0000             * display the screen
.
ML3100    CALL      KSIT0000             * keyin the site       
          BRANCH    EXIT,ML0500
.
ML3200    CALL      KCTY0000             * keyin the Clinic type
          BRANCH    EXIT,ML3100
.
ML3300    CALL      KCLI0000             * keyin the clinic
          BRANCH    EXIT,ML3200
.
          CALL      FILE0000             * test if onfile
          BRANCH    EXIT,ML3300
.
          CALL      SVAR0000             * set up and display variable
.                                          descriptions
          CALL      DELQST               * ok to delete?
          BRANCH    CEXIT,ML3500,ML3600,ML0500
.
ML3500    
          CALL      DEOTUNI1
          MOVE      FOUR,AUDTFLAG
          CALL      AUDT0000             * write to audit file
.
ML3600    CALL      CLSC0000
          DISPLAY   *P1:24,*EL
          GOTO      ML3300
.
ML9999
          CHAIN     PGM

.
.
.********************************************************************
.*                        INIT0000                                  *
.*                                                                  *
.*                    Initialisation Code                           *
.********************************************************************
.
INIT0000  MOVE      ONE,CNEWDS
          CALL      DISPHEAD
.
          DISPLAY   *P50:24,*EL,"Opening : ",*P61:24,"pattmaaf"
          OPEN      PATTMAA1,"pattmaaf"
          DISPLAY   *P61:24,*EL,"outuniaf"
          OPEN      OUTUNIA1,"outuniaf"
          DISPLAY   *P61:24,*EL,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
          DISPLAY   *P61:24,*EL,"patcodes"
          OPEN      PATCODE1,"patcodes"
          DISPLAY   *P61:24,*EL,"outaudui"
          OPEN      OUTAUDUI,"outaudui"
          DISPLAY   *P61:24,*EL,"outsitaf"
          OPEN      OUTSITA1,"outsitaf"
.
          MOVE      PASSCODE,OTUIAUDO
          CALL      TFILENAM
          MOVE      TFILNAME,OTUIAUDP
.
INIT9999  RETURN
.
.*********************************************************************
.*                           CVAR0000                                *
.*                  Clear the required variables                     *
.*********************************************************************
.
CVAR0000
          CLEAR     OTUISITE
          CLEAR     OTUITYPE
          CLEAR     OTUICLIN
          CLEAR     OTUITEAM
          CLEAR     OTUISDAT
          CLEAR     OTUIEDAT
          CLEAR     PTTADESC
          CLEAR     OSTDESC
          CLEAR     OCADESC
          CLEAR     OCTDESC
          CLEAR     STRTDATE
          CLEAR     ENDDATE
.
CVAR9999  RETURN
.
.********************************************************************
.*                         SCRN0000                                 *
.*                    Display the screen                            *
.********************************************************************
.
SCRN0000
          DISPLAY   *P1:3,*EF:
                    *P4:4,"Site             :":      
                    *P4:5,"Clinic type      :":               
                    *P4:6,"Clinic ID        :":               
                    *P1:8,*V2LON,"1. ",*HOFF:
                          "Team Code        :":
                    *P1:9,*V2LON,"2. ",*HOFF:
                          "Starting Date    :":
                    *P1:10,*V2LON,"3. ",*HOFF:
                          "Ending Date      :":
                    *P23:4,*V2LON,OTUISITE,*HOFF,*P30:4,OSTDESC: 
                    *P23:5,*V2LON,OTUITYPE,*HOFF,*P30:5,OCTDESC: 
                    *P23:6,*V2LON,OTUICLIN,*HOFF,*P30:6,OCADESC:
                    *P23:8,*V2LON,OTUITEAM,*HOFF,*P30:8,PTTADESC:
                    *P23:9,*V2LON,STRTDATE:
                    *P23:10,*V2LON,ENDDATE
                    
SCRN9999  RETURN
.
.*********************************************************************
.*                         KSIT0000                                  *
.*                     Keyin the Site                                *
.*********************************************************************
.
KSIT0000
          MOVE      ZERO,EXIT            * set exit flag
.
          KEYIN     *P23:4,*EL,*DV,UNDLN6:
                    *P23:4,*V2LON,OTUISITE:
                    *P23:4,*DV,OTUISITE
.
          PACK      OTUISITE,OTUISITE,SP6
          MATCH     SP6,OTUISITE          * test if noting entered
          GOTO      KSIT9000 IF EQUAL
.
          MATCH     QUEST,OTUISITE        * test for a ?
          GOTO      KSIT5000 IF EQUAL
.
          PACK      KEY6,OTUISITE         
          CALL      RDSITA1
          BRANCH    OVRCD,KSIT4000
          MOVE      OTUISITE,IDDD
          DISPLAY   *P30:4,*EL,OSTDESC
          CALL      OFIL0000             * open outpatient files
          GOTO      KSIT9999
.
KSIT4000  BEEP
          DISPLAY   *P1:24,*EL,"Code is not on file, ";
          CALL      HITENTER
          GOTO      KSIT0000
.
.***** Handle the question mark option *****
.
KSIT5000
          
          CALL      OUTDSSIT
.
KSIT6000
          DISPLAY   *P1:24,"Site : "        
.
          KEYIN     *P8:24,*DV,UNDLN3:
                    *P8:24,*V2LON,OTUISITE:
                    *P8:24,*DV,OTUISITE
.
          PACK      OTUISITE,OTUISITE,SP6
          MATCH     SP6,OTUISITE          * test if noting entered
          GOTO      KSIT8500 IF EQUAL
.
          MATCH     QUEST,OTUISITE       * test for a ?
          GOTO      KSIT5000 IF EQUAL
          MATCH     EXITCHAR,OTUISITE
          GOTO      KSIT9000 IF EQUAL
.
          PACK      KEY6,OTUISITE        
          CALL      RDSITA1
          BRANCH    OVRCD,KSIT7000
          MOVE      OTUISITE,IDDD
          CALL      SCRN0000             * redisplay the screen
          CALL      OFIL0000             * open outpatient files
          GOTO      KSIT9999
.
KSIT7000  BEEP
          DISPLAY   *P1:24,*EL,"Team code is not on file, ";
          CALL      HITENTER
          GOTO      KSIT6000
.
KSIT8500  
          CALL      SCRN0000             * redisplay the screen
KSIT9000
          MOVE      ONE,EXIT
.
KSIT9999  RETURN
.
.**********************************************************************
.*                         OFIL0000                                   *
.*              Open the outpatient files                             *
.**********************************************************************
.
OFIL0000
.         CLOSE     CLINFILE
.         CLOSE     TYPEFILE
          CLEAR     CLINFILE
          CLEAR     TYPEFILE
.
          PACK      CLINFILE,OSTFILE,CLIAF
          PACK      TYPEFILE,OSTFILE,CTYAF
.
          OPEN      OUTCLIA1,CLINFILE
          OPEN      OUTCTYA1,TYPEFILE
.
OFIL9999 RETURN
.*********************************************************************
.*                         KTMC0000                                  *
.*                    Keyin the team code                            *
.*********************************************************************
.
KTMC0000  MOVE      ZERO,EXIT
          CLEAR     PTTADESC
.
          KEYIN     *P23:8,*EL,*DV,UNDLN6:
                    *P23:8,*V2LON,OTUITEAM:
                    *P23:8,*DV,OTUITEAM
.
          PACK      OTUITEAM,OTUITEAM,SP6
          MATCH     SP6,OTUITEAM          * test if noting entered
          GOTO      KTMC3000 IF EQUAL
.
          MATCH     QUEST,OTUITEAM       * test for a ?
          GOTO      KTMC5000 IF EQUAL
          MATCH     EXITCHAR,OTUITEAM    * test for exit ?
          GOTO      KTMC8000 IF EQUAL
.
          PACK      KEY6,OTUITEAM
          CALL      RDPTTMA1
          BRANCH    OVRCD,KTMC4000
          DISPLAY   *P30:8,PTTADESC
          GOTO      KTMC9999
KTMC3000
          BEEP
          GOTO      KTMC0000
.
KTMC4000  BEEP
          DISPLAY   *P1:24,*EL,"Team code is not on file. ";
          CALL      HITENTER
          GOTO      KTMC0000
.
.***** Handle the question mark option *****
.
KTMC5000
          CALL      PATTMADS
.
KTMC6000
          DISPLAY   *P1:24,"Team Code : "
.
          KEYIN     *P13:24,*DV,UNDLN6:
                    *P13:24,*V2LON,OTUITEAM:
                    *P13:24,*DV,OTUITEAM
.
          PACK      OTUITEAM,OTUITEAM,SP6
          MATCH     SP6,OTUITEAM          * test if noting entered
          GOTO      KTMC6500 IF EQUAL
.
          MATCH     QUEST,OTUITEAM       * test for a ?
          GOTO      KTMC5000 IF EQUAL
          MATCH     EXITCHAR,OTUITEAM    * test for exit ?
          GOTO      KTMC8000 IF EQUAL
.
          PACK      KEY6,OTUITEAM
          CALL      RDPTTMA1
          BRANCH    OVRCD,KTMC7000
          CALL      SCRN0000             * redisplay the screen
          GOTO      KTMC9999
KTMC6500  BEEP
          GOTO      KTMC6000
.
KTMC7000  BEEP
          DISPLAY   *P1:24,*EL,"Team code is not on file. ";
          CALL      HITENTER
          GOTO      KTMC6000
.
KTMC8000  MOVE      ONE,EXIT
.
KTMC8500  CLEAR     PTTADESC
          CALL      SCRN0000
.
KTMC9999  RETURN
.
.*********************************************************************
.*                         KCTY0000                                  *
.*             Keyin the consulting / attending doctor               *
.*********************************************************************
.
KCTY0000
          MOVE      ZERO,EXIT            * set exit flag
.
          KEYIN     *P23:5,*EL,*DV,UNDLN6:
                    *P23:5,*V2LON,OTUITYPE:
                    *P23:5,*DV,OTUITYPE
.
          PACK      OTUITYPE,OTUITYPE,SP6
          MATCH     SP6,OTUITYPE          * test if noting entered
          GOTO      KCTY9000 IF EQUAL
.
          MATCH     QUEST,OTUITYPE       * test for a ?
          GOTO      KCTY5000 IF EQUAL
.
          PACK      KEY12,OTUISITE,OTUITYPE
          CALL      RDCTYA1 
          BRANCH    OVRCD,KCTY4000
          DISPLAY   *P30:5,*EL,OCTDESC 
          GOTO      KCTY9999
.
KCTY4000  BEEP
          DISPLAY   *P1:24,*EL,"Clinic type is not on file. ";
          CALL      HITENTER
          GOTO      KCTY0000
.
.***** Handle the question mark option *****
.
KCTY5000
          CALL      OUTDSCTY
.
KCTY6000
          DISPLAY   *P1:24,"Clinic Type : "       
.
          KEYIN     *P15:24,*DV,UNDLN6:
                    *P15:24,*V2LON,OTUITYPE:
                    *P15:24,*DV,OTUITYPE
.
          PACK      OTUITYPE,OTUITYPE,SP6
          MATCH     SP6,OTUITYPE          * test if noting entered
          GOTO      KCTY8500 IF EQUAL
.
          MATCH     QUEST,OTUITYPE       * test for a ?
          GOTO      KCTY5000 IF EQUAL
.
          PACK      KEY12,OTUISITE,OTUITYPE
          CALL      RDCTYA1 
          BRANCH    OVRCD,KCTY7000
          CALL      SCRN0000             * redisplay the screen
          GOTO      KCTY9999
.
KCTY7000  BEEP
          DISPLAY   *P1:24,*EL,"Clinic type is not on file. ";
          CALL      HITENTER
          GOTO      KCTY6000
.
KCTY8500  
          CALL      SCRN0000             * redisplay the screen
KCTY9000
          CLEAR     OCTDESC
          MOVE      ONE,EXIT
.
KCTY9999  RETURN
.
.*********************************************************************
.*                         KCLI0000                                  *
.*                     Keyin the clinic                              *
.*********************************************************************
.
KCLI0000  MOVE      ZERO,EXIT            * set exit flag
.
          KEYIN     *P23:6,*EL,*DV,UNDLN6:
                    *P23:6,*V2LON,OTUICLIN:
                    *P23:6,*DV,OTUICLIN
.
          PACK      OTUICLIN,OTUICLIN,SP6
          MATCH     SP6,OTUICLIN          * test if nothing entered
          GOTO      KCLI9000 IF EQUAL
.
          MATCH     QUEST,OTUICLIN       * test for a ?
          GOTO      KCLI5000 IF EQUAL
.
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          PACK      KEY20,OTUISITE,OTUICLIN,KEY8
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     OTUISITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     OTUICLIN,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          BRANCH    OVRCD,KCLI4000
          CALL      GCLD0000             * get the clinic description
          DISPLAY   *P30:6,*EL,OCADESC 
          GOTO      KCLI9999
.
KCLI4000  BEEP
          DISPLAY   *P1:24,*EL,"Clinic id is not on file. ";
          CALL      HITENTER
          GOTO      KCLI0000
.
.***** Handle the question mark option *****
.
KCLI5000  CALL      OUTDSCLN
.
KCLI6000  DISPLAY   *P1:24,"Clinic ID : "       
.
          KEYIN     *P13:24,*DV,UNDLN6:
                    *P13:24,*V2LON,OTUICLIN:
                    *P13:24,*DV,OTUICLIN
.
          PACK      OTUICLIN,OTUICLIN,SP6
          MATCH     SP6,OTUICLIN          * test if noting entered
          GOTO      KCLI8500 IF EQUAL
.
          MATCH     QUEST,OTUICLIN       * test for a ?
          GOTO      KCLI5000 IF EQUAL
.
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          PACK      KEY20,OTUISITE,OTUICLIN,KEY8
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     OTUISITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     OTUICLIN,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          BRANCH    OVRCD,KCLI7000
          CALL      GCLD0000             * get the clinic description
          CALL      SCRN0000             * redisplay the screen
          GOTO      KCLI9999
.
KCLI7000  BEEP
          DISPLAY   *P1:24,*EL,"Clinic id is not on file. ";
          CALL      HITENTER
          GOTO      KCLI6000
.
KCLI8500  
          CALL      SCRN0000             * redisplay the screen
KCLI9000
          CLEAR     OCADESC
          MOVE      ONE,EXIT
.
KCLI9999  RETURN
.
.********************************************************************
.*                         KSDT0000                                 *
.*                  Keyin the start date                            *
.********************************************************************
.
KSDT0000
          MOVE      SP2,CDAY
          MOVE      SP2,CMON
          MOVE      SP2,CYEAR
          MOVE      CCC,CCENT
.
          MOVE      ZERO,CCANLDTE
          MOVE      ZERO,CHIGHLT
          MOVE      "23",CCOL
          MOVE      NINE,CVERT
          
          CALL      KEYDATE
.
          BRANCH    OVRCD,KSDT9000
          BRANCH    CQUEST,KSDT9000
.
          MATCH     SP2,CDAY
          GOTO      KSDT9000 IF EQUAL
.
          PACK      OTUISDAT,CCENT,CYEAR,CMON,CDAY
          REP       " 0",OTUISDAT
          PACK      STRTDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          REP       " 0",STRTDATE
          GOTO      KSDT9999
.
KSDT9000
          BEEP
          GOTO      KSDT0000
KSDT9999
          RETURN
.
.********************************************************************
.*                         KEDT0000                                 *
.*                  Keyin the start date                            *
.********************************************************************
.
KEDT0000
          MOVE      SP2,CDAY
          MOVE      SP2,CMON
          MOVE      SP2,CYEAR
          MOVE      CCC,CCENT
.
          MOVE      ONE,CCANLDTE
          MOVE      ZERO,CHIGHLT
          MOVE      "23",CCOL
          MOVE      TEN,CVERT
          
          CALL      KEYDATE
.
          BRANCH    OVRCD,KEDT9000
          BRANCH    CQUEST,KEDT9000
.
          MATCH     SP2,CDAY
          GOTO      KEDT9000 IF EQUAL
.
          PACK      OTUIEDAT,CCENT,CYEAR,CMON,CDAY
          REP       " 0",OTUIEDAT
          PACK      ENDDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          REP       " 0",STRTDATE
.
          MATCH     OTUISDAT,OTUIEDAT
          GOTO      KEDT5000 IF LESS
          GOTO      KEDT9999
KEDT5000
          BEEP
          DISPLAY   *P1:24,"Ending date must be > or = starting date. ";
          CALL      HITENTER
          GOTO      KEDT0000
.
KEDT9000
          MOVE      SP2,CCENT
          MOVE      SP8,OTUIEDAT
          CLEAR     ENDDATE
KEDT9999
          RETURN
.********************************************************************
.*                         SIPC0000                                 *
.*                Select item, post Cancel                          *
.********************************************************************
.
SIPC0000
          MOVE      ZERO,EXIT
          CALL      MAINQST
.
          COMPARE   ZERO,CCITEM
          GOTO      SIPC9999 IF EQUAL
.
          COMPARE   "-1",CCITEM
          GOTO      SIPC9000 IF EQUAL
.
          BRANCH    CCITEM,SIPC1000,SIPC2000,SIPC3000
          BEEP
          GOTO      SIPC0000
.
SIPC1000  
          CALL      KTMC0000
          GOTO      SIPC0000
SIPC2000
          CALL      KSDT0000
          CALL      KEDT0000
          GOTO      SIPC0000
SIPC3000
          CALL      KEDT0000
          GOTO      SIPC0000
.
SIPC9000
          MOVE      ONE,EXIT
          CLEAR     DOCTDESC
          CALL      CLSC0000             * clear the screen

SIPC9999  RETURN
.
.********************************************************************
.*                         CLSC0000                                 *
.*                  Clear the screen                                *
.********************************************************************
.
CLSC0000
          DISPLAY   *P23:8,*EL,*P23:9,*EL,*P23:10,*EL
          CLEAR     OTUITEAM
          CLEAR     PTTADESC
          CLEAR     STRTDATE
          CLEAR     ENDDATE
          CLEAR     OTUISDAT
          CLEAR     OTUIEDAT
CLSC9999  RETURN
.
.********************************************************************
.*                         FILE0000                                 *
.*          Tests wether the record is on/not on the file           *
.*          depending on program mode (insert,modify,delete)        *
.********************************************************************
.
FILE0000
          MOVE      ZERO,EXIT
          PACK      KEY18,OTUISITE,OTUITYPE,OTUICLIN
          CALL      RDOTUNI1
.
          BRANCH    OPTION,FILE1000,FILE2000,FILE2000
.
FILE1000
          BRANCH    OVRCD,FILE9999
          MOVE      ONE,EXIT
          DISPLAY   *P1:24,*B,"Record is already on file. ";
          CALL      HITENTER
          CLEAR     OTUITEAM
          CLEAR     OTUISDAT
          CLEAR     OTUIEDAT
          GOTO      FILE9999
.
FILE2000
          BRANCH    OVRCD,FILE3000
          GOTO      FILE9999
.
FILE3000
          MOVE      ONE,EXIT
          DISPLAY   *P1:24,*B,"Record is not on file. ";
          CALL      HITENTER
.
FILE9999  RETURN
.
.********************************************************************
.*                         POST0000                                 *
.*                  Write the record to the file                    *
.********************************************************************
.
POST0000
          PACK      KEY18,OTUISITE,OTUITYPE,OTUICLIN 
          CALL      WROTUNI1
POST9999  RETURN
.
.*********************************************************************
.*                         FDOC0000                                  *
.*                 Format the doctors name                           *
.*********************************************************************
.
FDOC0000
          MOVE      DSNAME,PACSNAME
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          MOVE      ONE,PACFRMT
.
          CALL      PACNAME
FDOC9999
          RETURN
.
.*********************************************************************
.*                         SVAR0000                                  *
.*                  Set up and display variables                     *
.*********************************************************************
.
SVAR0000
          PACK      KEY6,OTUITEAM
          CALL      RDPTTMA1
.
          PACK      OTUISDAT,OTUISDAT,SP8
          MATCH     SP8,OTUISDAT
          GOTO      SVAR1000 IF EQUAL
.
          UNPACK    OTUISDAT,CCENT,CYEAR,CMON,CDAY
          PACK      STRTDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.
SVAR1000
          PACK      OTUIEDAT,OTUIEDAT,SP8
          MATCH     SP8,OTUIEDAT
          GOTO      SVAR2000 IF EQUAL
.
          UNPACK    OTUIEDAT,CCENT,CYEAR,CMON,CDAY
          PACK      ENDDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.
SVAR2000
          DISPLAY   *P23:8,*V2LON,OTUITEAM,*HOFF,*P30:8,PTTADESC:
                    *P23:9,*V2LON,STRTDATE:
                    *P23:10,*V2LON,ENDDATE
SVAR9999
          RETURN
.
.********************************************************************
.*                         AUDT0000                                 *
.*                   Write to the audit file                        *
.********************************************************************
.
AUDT0000
          CALL      IBACLOCK
          MOVE      CTIMEIS,OTUIAUDT
          PACK      OTUIAUDD,CCC,CYY,CMM,CDD
          MOVE      ONE,OTUIAUDS
.
          BRANCH    AUDTFLAG,AUDT1000,AUDT2000,AUDT3000,AUDT4000
          GOTO      AUDT9999
.
AUDT1000
          MOVE      ANSA,OTUIAUDR
          GOTO      AUDT5000
AUDT2000
          MOVE      ANSB,OTUIAUDR
          GOTO      AUDT5000
AUDT3000
          MOVE      ANSC,OTUIAUDR
          GOTO      AUDT5000
AUDT4000
          MOVE      ANSD,OTUIAUDR
.
AUDT5000  PACK      KEY19,OTUIAUDD,OTUIAUDT,OTUIAUDP,OTUIAUDR
          CALL      AWOTUNI
.
AUDT9999  RETURN
.
.********************************************************************
.*                        DAUD0000                                  *
.*                  Delete the "B" audit record                     *
.********************************************************************
.
DAUD0000
          PACK      KEY19,OTUIAUDD,OTUIAUDT,OTUIAUDP,OTUIAUDR
          CALL      ADOTUNI
DAUD9999
          RETURN
.
.********************************************************************
.*                         GCLD0000                                 *
.*                    Get clinic description                        *
.********************************************************************
.
GCLD0000
          PACK      OCADOCT,OCADOCT,SP6
          MATCH     SP6,OCADOCT
          GOTO      GCLD9999 IF EQUAL
.
          PACK      KEY6,OCADOCT
          CALL      RDDOCT1
          BRANCH    OVRCD,GCLD9999
.
          CALL      FDOC0000
          MOVE      PACFNAME,OCADESC
GCLD9999
          RETURN
.
.-----  other includes required  ------
. 
          INC       PATTMADS
          INC       OUTDSCLN
          INC       OUTDSCTY
          INC       OUTDSSIT
          INC       STD001IO
          INC       TFILENAM
          INC       OUTUNIIO/INC
          INC       OUTSITIO/INC
          INC       OUTCLIIO/INC
          INC       OUTCTYIO/INC
          INC       PATTMAIO/INC
          INC       PATDO1IO/INC
          INC       PATCODIO/INC
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC


.*****************************************************************************
.*    Program      : FXPATELG                                                *
.*    Modification :                                                         *
.*    ---------------------------------------------------------------------  *
.*         V9.08.00 13/12/2006 Peter Vela    CAR 128097                      *
.*                  Created fix program                                      *
.*****************************************************************************
.
          INC       STD001FD
          INC       PATELGFD/INC
.
FINDFILE  FILE      ASCII, FIXED=284
.
OLDFILE   IFILE     SQL, FIXED=284, KEY="U1-8"       
.
.Old File Layout
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
OLDVISN   DIM       8      8        1     Visit Number
OLDFINC   DIM       1      1        9     Financial Y/N (1-Yes)
OLDXCSS   FORM      12.2   8        10    Excess for this Admission
OLDPPRI   FORM      12.2   8        18    Co-payments Private room
OLDPSHA   FORM      12.2   8        26    Co-payments Shared room
OLDPTHE   FORM      12.2   8        34    Theatre Co-payments
OLDXCLS   DIM       1      1        42    Exclusions exist Y/N (1-Yes)
OLDCMPL   DIM       1      1        43    Check Complete Y/N (1-Yes)
OLDCOVR   DIM       1      1        44    Covered for this Procedure Y/N
OLDQEXS   DIM       1      1        45    Quote Exists Y/N (1-Yes)
OLDQACC   DIM       1      1        46    Quote Accepted Y/N (1-Yes)
OLDCDON   DIM       3      3        47    Check done via (Catg EG)
OLDRDAT   DIM       8      8        50    Request date (CCYYMMDD)
OLDSPEA   DIM       1      1        58    Subject to Pre-Existing Ailment (Y/N)
OLDHFRN   DIM       20     20       59    Health Fund Reference Number
OLDPTHF   DIM       25     25       79    Person at Health Fund provided Info
OLDXCMN   DIM       100    100      104   Exclusion Comments
OLDSPAR   DIM       80     80       204   Spare variable
.
.End of Record                      284
.
.
CMDLINE   DIM       100
CMDLINE1  DIM       100
DIM4      DIM       4
DIM4A     DIM       4
DIM6      DIM       6
DIM6A     DIM       6
DIM60     DIM       60
DIM60A    DIM       60
DDCENT    DIM       2
.
RECNUM    FORM      8
NEWFILE   DIM       60
NEWFILE1  DIM       60
NEWPATH   DIM       60
OLDPATH   DIM       60
DEFPATH   DIM       60
SAVEFLG   FORM      1
SP50      INIT    "                                                  "
SP60      INIT    "                                                            "
.
PRGNAM    INIT      "Conversion PATELGFD"
PRGID     INIT      "FXPATELG"
VERSION   INIT      "V12.01.00"
.
. Start of Program
.
MAIN0000  CALL      INIT0000                     * init and open files
          BRANCH    EXIT,MAIN9999                * init failure
.
          CALL      KDIR0000                     * Keyin directory
          BRANCH    EXIT,MAIN9999
.
          CALL      CONTQST                      * Ok to continue ?
          BRANCH    CEXIT,MAIN1000:              * Yes
                          MAIN0000:              * No
                          MAIN9999               * Cancel
.
MAIN1000  CALL      PREP0000                     * preparing files
          BRANCH    EXIT,MAIN9999
.
. Loop thru old file and WRPTELG1 records to new file
.
          CALL      READ0000                     * Read old recs and WRPTELG1
          CALL      ENDP0000                     * Save/compress saved file
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
.
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
.
INIT9999  RETURN
+
.********************************************************************
.*                          KDIR0000                                *
.*  Keyin directories                                               *
.********************************************************************
.
KDIR0000  MOVE      ONE,SAVEFLG                  * Default to keep file
          KEYIN     *P1:10,*EL,"Do you want to save the original file (Y/N) ? ":
                    ANS;
          REP       "yYnN",ANS
          CMATCH    "Y",ANS
          GOTO      KDIR1000 IF EQUAL
          MOVE      ZERO,SAVEFLG                 * Not to keep file
          CMATCH    "N",ANS
          GOTO      KDIR1000 IF EQUAL
          BEEP
          GOTO      KDIR0000
.
KDIR1000  CALL      DEFT0000                     * Get the default path
          BRANCH    EXIT,KDIR9999
          MOVE      SP60,OLDPATH
.
          DISPLAY   *P1:24,*EL,"The directory path must end with a slash (/) ";
.
          MOVE      DEFPATH,OLDPATH
          BRANCH    SAVEFLG,KDIR2000
.
.         Keyin the path for the original file
.
          KEYIN     *P1:12,*EL,"Keyin path for temporarily saving original ":
                    "file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
          GOTO      KDIR3000
.
KDIR2000  KEYIN     *P1:12,*EL,"Keyin path for saving original file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
.
KDIR3000  ENDSET    OLDPATH
          APPEND    SP60,OLDPATH
          RESET     OLDPATH
.
          MATCH     SP60,OLDPATH
          IF        @EQUAL
            PACK      OLDPATH,DEFPATH            * Use the default
            DISPLAY   *P10:13,*EL,*V2LON,OLDPATH;
          ENDIF
.
          SQUEEZE   OLDPATH
          CMATCH    EXITCHAR,OLDPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,OLDPATH,SP60
          CALL      VALD0000                     * Check valid directory
          BRANCH    EXIT,KDIR1000
.
. Keyin the path for the new file
.
KDIR5000  MOVE      SP60,NEWPATH
          MOVE      DEFPATH,NEWPATH
          KEYIN     *P1:15,*EL,"Keyin path for the new file: ":
                    *P10:16,*EL,*DV,*RV,NEWPATH:
                    *P10:16,NEWPATH;
          ENDSET    NEWPATH
          APPEND    SP60,NEWPATH
          RESET     NEWPATH
.
          MATCH     SP60,NEWPATH
          IF        @EQUAL
            PACK      NEWPATH,DEFPATH            * Use the default
            DISPLAY   *P10:16,*V2LON,NEWPATH;
          ENDIF
.
          SQUEEZE   NEWPATH
          CMATCH    EXITCHAR,NEWPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,NEWPATH,SP60
          CALL      VALD0000                     * Check valid directory
          BRANCH    EXIT,KDIR5000
          GOTO      KDIR9999
.
KDIR9000  MOVE      ONE,EXIT
.
KDIR9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.********************************************************************
.*                          PREP0000                                *
.*  Preparing files                                                 *
.********************************************************************
PREP0000  MOVE      ZERO,OVRCD
.
. Open old patelgaf file
.
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE,"patelgaf"          
          TRAPCLR   IO
          BRANCH    OVRCD,PREP5000               * patelgaf file not found 
          CLOSE     OLDFILE
.
          DISPLAY   *P1:24,*EL,"Copying files ... Please wait !! ";
.
. Copy old patelgaf.dat file to the keyin directory
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat patelgaf.dat` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "oldelgaf.dat",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          GOTO      PREP8000 IF NOT EQUAL
.
. Copy old patelgaf.idx file to the keyin directory
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat patelgaf.idx` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "oldelgaf.idx",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          GOTO      PREP8000 IF NOT EQUAL
.
. Remove the original patelgaf files
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    "patelgaf",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Unable to remove the original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
. Opening old patelgaf file after being copied
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60
          APPEND    "oldelgaf",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE,DIM60
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Unable to open saved original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
. Create the new patelgaf file
.
          DISPLAY   *P1:24,*EL,"Creating new files ... Please wait !! ";
          PACK      NEWFILE,SP60
          CLEAR     NEWFILE
          APPEND    NEWPATH,NEWFILE
          APPEND    "patelgaf",NEWFILE
          APPEND    SP60,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
. Build files and indexes
.
PREP1000  PACK      CMDLINE,SP60,SP60
.
. Build primary index for patelgaf - isbuild patelgaf 499 UC1-8
. 
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
.
          APPEND    " 499 UC1-8",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
. Open new files
.
          OPEN      PATELGA1,NEWFILE
.
. Set the flag to say we want to continue
.
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
          GOTO      PREP9999
.
PREP5000  DISPLAY   *P1:24,*EL,*B,"Old file not found.  ";
          CALL      HITENTER
          GOTO      PREP9000
.
.PREP5100  DISPLAY   *P1:24,*EL,*B,"Old audit file not found.  ";
.          CALL      HITENTER
.          GOTO      PREP9000
.
PREP8000  DISPLAY   *P1:24,*EL,*B,"Unable to copy original file.  ":
                    "(Error: ",TASKID,")  ";
          CALL      HITENTER
          GOTO      PREP9000
.
.PREP8100  DISPLAY   *P1:24,*EL,*B,"Unable to copy original audit file.  ":
.                    "(Error: ",TASKID,")  ";
.          CALL      HITENTER
.
PREP9000  MOVE      ONE,EXIT
.
PREP9999  RETURN
+
.**********************************************************************
.*                               READ0000                             *
.*             loop thru old file and WRSIGB1 each record             *
.**********************************************************************
.
READ0000  DISPLAY   *P1:20,*EL,"Record No. : ";
          PACK      KEY8,SP60
          CALL      READSOLD                      * position at start
READ1000  CALL      READKOLD                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
          ADD       ONE,RECNUM                    * update record counter
.
          MOVE      OLDVISN,PTELVISN
          MOVE      OLDFINC,PTELFINC
          MOVE      OLDXCSS,PTELXCSS
          MOVE      OLDPPRI,PTELPPRI
          MOVE      OLDPSHA,PTELPSHA
          MOVE      OLDPTHE,PTELPTHE
          MOVE      OLDXCLS,PTELXCLS
          MOVE      OLDCMPL,PTELCMPL
          MOVE      OLDCOVR,PTELCOVR
          MOVE      OLDQEXS,PTELQEXS
          MOVE      OLDQACC,PTELQACC
          MOVE      OLDCDON,PTELCDON
          MOVE      OLDRDAT,PTELRDAT
          MOVE      OLDSPEA,PTELSPEA
          MOVE      OLDHFRN,PTELHFRN
          MOVE      OLDPTHF,PTELPTHF
          MOVE      OLDXCMN,PTELXCMN
          MOVE      ZERO,PTELPPUT
          MOVE      ZERO,PTELPSUT
          MOVE      ZERO,PTELPTUT
          PACK      PTELECMN,SP70,SP70,SP70,SP70
          PACK      PTELSPAR,SP70,SP70
.   
          PACK      KEY8,PTELVISN,SP70
          CALL      WRPTELG1                      * WRPTELG1 to new file
.
          IF        (RECNUM%1000) = 0
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
          GOTO      READ1000                      * get next record
.
READ6000  CLOSE     PATELGA1                      * close new file
          CLOSE     OLDFILE                       * close old file
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS
.
READ9999  RETURN
+
.**********************************************************************
.*                               VALD0000                             *
.*        Check if it a valid directory                               *
.**********************************************************************
.
VALD0000  MOVE      ZERO,EXIT
          SQUEEZE   DIM60
          ENDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory path must end with a slash (/) ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory does not exist ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT            * Not valid
.
VALD9999  RETURN
+
.**********************************************************************
.*                               ENDP0000                             *
.*        Remove or compress save file                                *
.**********************************************************************
.
ENDP0000  MOVE      SP60,DIM60
          MOVE      SP60,DIM60A
.
. Set up the saved oldelgaf file with path
.
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60        
          APPEND    "oldelgaf*",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
          SQUEEZE   DIM60
.
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
.
. The saved files are compressed to keep or they are removed
. 
          IF        SAVEFLG = 1
            APPEND    "compress -f ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Compressing original file.. ":
                      "Please wait !!  ";
.
          ELSE
            APPEND    "rm ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Removing original file.. Please wait !!  ";
.
          ENDIF
.
          EXECUTE   CMDLINE,TASKID               * Check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Saved file not compressed or removed.  ";
            CALL      HITENTER
            GOTO      ENDP9999
          ENDIF
.
          CALL      IBACLOCK
          DISPLAY   *P1:24,*EL,*B,"Finish processing.  ";
          CALL      HITENTER
.
ENDP9999  RETURN
+
.**********************************************************************
.         Get the default directory
.**********************************************************************
.
DEFT0000  EXECUTE   "ldat patelgaf.dat > fxpatelg.txt",TASKID
.
          PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"fxpatelg"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  DISPLAY   *P1:24,*EL,*B,"Error finding patelgaf.  ";
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
DEFT9999  RETURN
+
.**********************************************************************
. ---- OLD IO ROUTINES ----
.**********************************************************************
.  Copy and paste the Positional Read routine from the original
.  IO file, rename the routine READSOLD and change the index name
.  to OLDFILE
.
READSOLD  MOVE      ZERO,OVRCD
          RESET     KEY8
          READ      OLDFILE,KEY8;;
          RETURN
.
.  Copy and paste the Read Next routine from the original
.  IO file, rename the routine READKOLD and change the index name 
.  to OLDFILE. Careful of variables that have changed length
.
READKOLD  MOVE      ZERO,OVRCD
          READKS    OLDFILE;OLDVISN,OLDFINC,OLDXCSS,OLDPPRI,OLDPSHA:
                            OLDPTHE,OLDXCLS,OLDCMPL,OLDCOVR,OLDQEXS:
                            OLDQACC,OLDCDON,OLDRDAT,OLDSPEA,OLDHFRN:
                            OLDPTHF,OLDXCMN,OLDSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
. I/O Includes
.
          INC       PATELGIO/INC
          INC       STD001IO
+

.***************************************************************************
.* System    :   Inpatients                                                *
.* Program   :   IBAPMS91                                                  *
.* Desc      :   MOH Statistical Report for Johns Hopkins                  *
.***************************************************************************
.* Date      :   13/07/2006                                                *
.* Author    :   Jill Habasque                                             *
.* Function  :                                                             *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.***************************************************************************
.
          INC       STD001FD
          INC       PATCODFD/INC
          INC       PATDSCFD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATTRNFD/INC
          INC       PATWR1FD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
CALDAYS   FORM      4
CMDLINE   DIM       100
CURDTE8   DIM       8
ENDDTE8   DIM       8
FORM5     FORM      5
FROMDATE  DIM       8
MTHNAM3   DIM       3
NBRDAYS   FORM      5
STRTDTE8  DIM       8
TODATE    DIM       8
TOTDAYC   FORM      5
.
JAN       INIT      "Jan"
FEB       INIT      "Feb"
MAR       INIT      "Mar"
APR       INIT      "Apr"
MAY       INIT      "May"
JUN       INIT      "Jun"
JUL       INIT      "Jul"
AUG       INIT      "Aug"
SEP       INIT      "Sep"
OCT       INIT      "Oct"
NOV       INIT      "Nov"
DEC       INIT      "Dec"
ERASE     INIT      "iserase "
FNAME     INIT      "TMPPMS91"          * set up for temp file name
ISBUILD   INIT      "isbuild "
UKEY      INIT      " 28 U1-1,2-4,5-7" * set up for record length & relevant key
ETD       INIT      "</td>"
ETR       INIT      "</tr>"
TD        INIT      "<td>"
TR        INIT      "<tr>"
.
TEMP1     IFILE     SQL, FIXED=28, KEY="U1-1,2-4,5-7"
.Name     Type      Length   Physical   Start   Description
.----     ----      ------   --------   -----   -----------
XXBEDTYP  DIM       1        1          1       Bed type - ind 7 category BT
XXWARD    DIM       3        3          2       Ward
XXBED     DIM       3        3          5       Bed
XXDIS     FORM      5        4          8       Discharges
XXDEATH   FORM      5        4          12      Deaths
XXSTATS   FORM      5        4          16      No of beds for stats
XXTOTBD   FORM      5        4          20      Total Bed Days
XXTOTLOS  FORM      5        4          24      Total LOS for discharges
.End of Record 
.
TOTBEDA    FORM      5
TOTBD      FORM      5
TOTLS      FORM      5
TOTDIS     FORM      5
TOTDEATH   FORM      5
.
DELBEDA    FORM      5
DELTOTBD   FORM      5
DELTOTLS   FORM      5
DELDIS     FORM      5
DELDEATH   FORM      5
.
BD1BEDA    FORM      5
BD1TOTBD   FORM      5
BD1TOTLS   FORM      5
BD1DIS     FORM      5
BD1DEATH   FORM      5
.
BD2BEDA    FORM      5
BD2TOTBD   FORM      5
BD2TOTLS   FORM      5
BD2DIS     FORM      5
BD2DEATH   FORM      5
.
BD3BEDA    FORM      5
BD3TOTBD   FORM      5
BD3TOTLS   FORM      5
BD3DIS     FORM      5
BD3DEATH   FORM      5
.
BD4BEDA    FORM      5
BD4TOTBD   FORM      5
BD4TOTLS   FORM      5
BD4DIS     FORM      5
BD4DEATH   FORM      5
.
BD5BEDA    FORM      5
BD5TOTBD   FORM      5
BD5TOTLS   FORM      5
BD5DIS     FORM      5
BD5DEATH   FORM      5
.
NURBEDA    FORM      5
NURTOTBD   FORM      5
NURTOTLS   FORM      5
NURDIS     FORM      5
NURDEATH   FORM      5
.
BABBEDA    FORM      5
BABTOTBD   FORM      5
BABTOTLS   FORM      5
BABDIS     FORM      5
BABDEATH   FORM      5
.
ICUBEDA    FORM      5
ICUTOTBD   FORM      5
ICUTOTLS   FORM      5
ICUDIS     FORM      5
ICUDEATH   FORM      5
.
HDUBEDA    FORM      5
HDUTOTBD   FORM      5
HDUTOTLS   FORM      5
HDUDIS     FORM      5
HDUDEATH   FORM      5
.
OTHBEDA    FORM      5
OTHTOTBD   FORM      5
OTHTOTLS   FORM      5
OTHDIS     FORM      5
OTHDEATH   FORM      5
.
PRGID     INIT      "IBAPMS91"
PRGNAM    INIT      "Johns Hopkins MOH Statistical Report  "
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      DATE0000               * Enter the date range
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          CALL      CREA0000
.
          OPEN      PATCODE1,"patcodes"
          OPEN      PATDSCH2,"patdschf"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PATTRAN2,"pattranf"
          OPEN      PATWR1A1,"patwr1af"
.
INIT9999  RETURN
+
.******************************************************************************
.*                                  DATE0000              Called by: ML0000   *
.*                            Keyin The Date Range                            *
.******************************************************************************
DATE0000  MOVE      "21",CCOL
          MOVE      "9",CVERT
          DISPLAY   *P1:CVERT,"Starting Date     : ",*EF;
          ADD       ONE,CVERT
          DISPLAY   *P1:CVERT,"Ending Date       : ";
.
          MOVE      ONE,CCANLDTE            * Blank cancel default
          MOVE      ZERO,CHIGHLT            * Use highlight
.
. ----- Enter starting date -----
.
DATE1000  SUB       ONE,CVERT
DATE2000  MOVE      CCC,CCENT
          UNPACK    SP6,CYEAR,CMON,CDAY     * No defaults
          CALL      KEYDATE                 * Keyin date
          BRANCH    OVRCD,DATE9500          * Nothing entered
          BRANCH    CQUEST,DATE2000
.
          PACK      STRTDTE8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDTE8
.
          MATCH     STRTDTE8,CURDTE8
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"Date must not be in the future.  ";
            CALL      HITENTER
            GOTO      DATE2000
          ENDIF
.
. ----- Enter ending date -----
.
          ADD       ONE,CVERT
DATE3000  UNPACK    STRTDTE8,CCENT,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin date
          BRANCH    OVRCD,DATE1000          * Nothing entered
          BRANCH    CQUEST,DATE3000
.
          PACK      ENDDTE8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDTE8
.
          MATCH     ENDDTE8,CURDTE8
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"Date must not be in the future.  ";
            CALL      HITENTER
            GOTO      DATE3000
          ENDIF
.
          MATCH     STRTDTE8,ENDDTE8
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"End date must be after start date.  ";
            CALL      HITENTER
            GOTO      DATE3000
          ENDIF
.
DATE9000  MOVE      ZERO,EXIT
          GOTO      DATE9999
.
DATE9500  MOVE      ONE,EXIT
DATE9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  CALL      CLRVAR00
          DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  CALL      WRWD0000       * Write wards to temp file
.
          CALL      WRDS0000       * Write discharges to temp file
          CALL      CADS0000       * Write current adm and on leave to tempfile
.
          CALL      PRNT0000       * Print report
.
PROC9999  RETURN
+
.**************************************************************************
.*                               WRWD0000              Called by: PROC0000*
.*                                                                        *
.**************************************************************************
WRWD0000  PACK      KEY6,SP70     * write all wards/beds to the tempfile
          CALL      RDSWARD1
WRWD1000  CALL      RDKWARD1
          BRANCH    OVRCD,WRWD9999
.
          COMPARE   ONE,WACTIVE                  * ignore inactive beds
          GOTO      WRWD1000 IF EQUAL
.
          PACK      KEY5,ANSB,ANST,WEFRBT,SP70   * get the type of bed
          CALL      RDCODE1
          BRANCH    OVRCD,WRWD1000
.
          MATCH     SP70,TCINDC7
          GOTO      WRWD1000 IF EQUAL
.
          PACK      XXBEDTYP,TCINDC7,SP70
          PACK      XXWARD,WWARD,SP70
          PACK      XXBED,WBED,SP70
          MOVE      ZERO,XXDIS
          MOVE      ZERO,XXDEATH
          MOVE      ZERO,XXSTATS
          MOVE      ZERO,XXTOTBD
.
          MATCH     SP70,WBED
          IF        @EQUAL
            MOVE      WOCCBED,XXSTATS
          ENDIF
.
          PACK      KEY7,XXBEDTYP,XXWARD,XXBED,SP70
          CALL      RATEMP1
          IF        OVRCD=1
            CALL      WRTEMP1
          ENDIF
          GOTO      WRWD1000
.
WRWD9999  RETURN
+
.**************************************************************************
.*                               WRDS0000              Called by: PROC0000*
.*                                                                        *
.**************************************************************************
WRDS0000  PACK      KEY16,STRTDTE8,SP70 * write the number of disch to tempfile
          CALL      RDSDSCH2
WRDS1000  CALL      RDKDSCH2
          BRANCH    OVRCD,WRDS9999
.
          PACK      KEY8,DDADMNO,SP70
          CALL      RDMISS1
          BRANCH    OVRCD,WRDS1000
.
          MATCH     ADATE,ENDDTE8
          GOTO      WRDS1000 IF LESS
.
          PACK      KEY30,DDADMNO,Z70  * get discharge ward from pattranf
          CALL      RDSTRAN2
          CALL      RDPTRAN2
          BRANCH    OVRCD,WRDS1000
.
          MATCH     DTADMN,DDADMNO
          GOTO      WRDS1000 IF NOT EQUAL
.
          MATCH     ANSD,TMOVE
          GOTO      WRDS1000 IF NOT EQUAL
.
          PACK      KEY6,TWARD,TBED,SP70
          CALL      RDWARD1
          BRANCH    OVRCD,WRDS1000
.         
          COMPARE   ONE,WACTIVE                  * ignore inactive beds
          GOTO      WRDS1000 IF EQUAL
.         
          PACK      KEY5,ANSB,ANST,WEFRBT,SP70   * get the type of bed
          CALL      RDCODE1
          BRANCH    OVRCD,WRDS1000
.         
          MATCH     SP70,TCINDC7
          GOTO      WRDS1000 IF EQUAL
.
          PACK      KEY7,TCINDC7,TWARD,TBED,SP70 
          CALL      RDTEMP1                      * update tempfile disch count
          BRANCH    OVRCD,WRDS1000
.
          MATCH     ADATE,DDATE
          IF        !@EQUAL
            ADD       ONE,XXDIS             * exclude day cases from discharges
          ELSE
            ADD       ONE,TOTDAYC           * add to day case count
          ENDIF
.
          CALL      DEADST00
          IF        EXIT=1
            ADD       ONE,XXDEATH   * number of deaths
          ENDIF
.
          MOVE      ADATE,FROMDATE
          MOVE      DDATE,TODATE
          MATCH     DDATE,ENDDTE8
          IF        @LESS
            MOVE      ENDDTE8,TODATE
          ENDIF
          CALL      NHOSPDAY
          ADD       NBRDAYS,XXTOTLOS    * LOS for discharged patients
.
          MOVE      STRTDTE8,FROMDATE
          MOVE      ENDDTE8,TODATE
          CALL      NHOSPDAY
          ADD       NBRDAYS,XXTOTBD   * total bed days
          
          CALL      UPTEMP1
.
          GOTO      WRDS1000
.
WRDS9999  RETURN
+
.**************************************************************************
.*                               CADS0000              Called by: PROC0000*
.*                  Add on current admission bed days                     *
.**************************************************************************
CADS0000  PACK      KEY9,TWO,SP70 
          CALL      RDSMISS2
CADS1000  CALL      RDKMISS2
          BRANCH    OVRCD,CADS2000
.         
          COMPARE   TWO,ASTAT        * only current admissions
          GOTO      CADS2000 IF NOT EQUAL
.
          MATCH     ADATE,ENDDTE8
          GOTO      CADS1000 IF LESS
.         
          CALL      GETC0000
          GOTO      CADS1000
.
CADS2000  PACK      KEY9,FOUR,SP70
          CALL      RDSMISS2
CADS3000  CALL      RDKMISS2
          BRANCH    OVRCD,CADS9999
.
          COMPARE   FOUR,ASTAT        * get on leave admissions
          GOTO      CADS3000 IF NOT EQUAL
.
          MATCH     ADATE,ENDDTE8
          GOTO      CADS3000 IF LESS
.
          CALL      GETC0000
          GOTO      CADS3000
.
CADS9999  RETURN
+
.**************************************************************************
.*                               GETC0000              Called by: CADS0000*
.*                  Calculate current admission bed days                  *
.**************************************************************************
GETC0000  PACK      KEY30,DAADMNO,Z70  * get discharge ward from pattranf
          CALL      RDSTRAN2
          CALL      RDPTRAN2
          BRANCH    OVRCD,GETC9999
.
          MATCH     DTADMN,DAADMNO
          GOTO      GETC9999 IF NOT EQUAL
.
          PACK      KEY6,TWARD,TBED,SP70
          CALL      RDWARD1
          BRANCH    OVRCD,GETC9999
.
          COMPARE   ONE,WACTIVE                  * ignore inactive beds
          GOTO      GETC9999 IF EQUAL
.
          PACK      KEY5,ANSB,ANST,WEFRBT,SP70   * get the type of bed
          CALL      RDCODE1
          BRANCH    OVRCD,GETC9999
.
          MATCH     SP70,TCINDC7
          GOTO      GETC9999 IF EQUAL
.
          PACK      KEY7,TCINDC7,TWARD,TBED,SP70
          CALL      RDTEMP1                      * update tempfile disch count
          BRANCH    OVRCD,GETC9999
.
          MOVE      STRTDTE8,FROMDATE
          MOVE      ENDDTE8,TODATE
          CALL      NHOSPDAY
          ADD       NBRDAYS,XXTOTBD   * total bed days

          CALL      UPTEMP1
.
GETC9999  RETURN
+
.------------------------------------------------------------
. DEADST00  
. Check if either discharge status or discharge destination
. codes have a deceased indicator against them.
. Returns EXIT, 1=Discharged deceased
.------------------------------------------------------------
.
DEADST00  MOVE      ZERO,EXIT
          MATCH     SP3,DSTAT                    * discharge status blank ?
          GOTO      DEADST50 IF EQUAL            * yes - ignore
.
          PACK      KEY5,ANSD,SP1,DSTAT
          CALL      RDCODE1                      * dis. status code on file ?
          BRANCH    OVRCD,DEADST50               * no
.
          MATCH     ANSD,TCINDC1                 * deceased indicator set ?
          GOTO      DEADST95 IF EQUAL            * yes
.
DEADST50  MATCH     SP3,DDEST                    * discharge dest. blank ?
          GOTO      DEADST99 IF EQUAL            * yes - ignore
.
          PACK      KEY5,ANSD,ANSD,DDEST
          CALL      RDCODE1                      * dis. dest. code on file ?
          BRANCH    OVRCD,DEADST99               * no
.
          MATCH     ANSD,TCINDC1                 * deceased indicator set ?
          GOTO      DEADST95 IF EQUAL            * yes
.
          GOTO      DEADST99
.
DEADST95  MOVE      ONE,EXIT                     * set as deceased
.
DEADST99  RETURN
+
.****************************************************************************
.*                              PRNT0000               Called by:PROC0000   *
.*               Print the report                                           *
.****************************************************************************
PRNT0000  UNPACK    STRTDTE8,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          REP       " 0",CYEAR
.
          PRINT     "<table border=1><tr><td colspan=13 align=center>":
                    "PRIVATE HOSPITAL STATISTICAL RETURN":
                    " TO MINISTRY OF HEALTH",ETD,ETR,*N:
                    TR,"<td colspan=7 align=left>","Name of Hospital: ",CNAME:
                    ETD,"<td colspan=6 align=right>","Return for the month of ":
                    MTHNAM3," (month)":
                    CYEAR," (year)",ETD,ETR,*N;
.
          PACK      KEY7,SP70
          CALL      RSTEMP1
PRNT1000  CALL      RKTEMP1
          BRANCH    OVRCD,PRNT5000
.
          ADD       XXSTATS,TOTBEDA
          ADD       XXTOTBD,TOTBD
          ADD       XXTOTLOS,TOTLS
          ADD       XXDIS,TOTDIS
          ADD       XXDEATH,TOTDEATH
.
          MATCH     ANSD,XXBEDTYP
          IF        @EQUAL
            ADD       XXSTATS,DELBEDA
            ADD       XXTOTBD,DELTOTBD
            ADD       XXTOTLOS,DELTOTLS
            ADD       XXDIS,DELDIS
            ADD       XXDEATH,DELDEATH
            GOTO      PRNT1000
          ENDIF
.
          MATCH     "1",XXBEDTYP
          IF        @EQUAL
            ADD       XXSTATS,BD1BEDA
            ADD       XXTOTBD,BD1TOTBD
            ADD       XXTOTLOS,BD1TOTLS
            ADD       XXDIS,BD1DIS
            ADD       XXDEATH,BD1DEATH
            GOTO      PRNT1000
          ENDIF       
.
          MATCH     "2",XXBEDTYP
          IF        @EQUAL
            ADD       XXSTATS,BD2BEDA
            ADD       XXTOTBD,BD2TOTBD
            ADD       XXTOTLOS,BD2TOTLS
            ADD       XXDIS,BD2DIS
            ADD       XXDEATH,BD2DEATH
            GOTO      PRNT1000
          ENDIF
.
          MATCH     "3",XXBEDTYP
          IF        @EQUAL
            ADD       XXSTATS,BD3BEDA
            ADD       XXTOTBD,BD3TOTBD
            ADD       XXTOTLOS,BD3TOTLS
            ADD       XXDIS,BD3DIS
            ADD       XXDEATH,BD3DEATH
            GOTO      PRNT1000
          ENDIF
.
          MATCH     "4",XXBEDTYP
          IF        @EQUAL
            ADD       XXSTATS,BD4BEDA
            ADD       XXTOTBD,BD4TOTBD
            ADD       XXTOTLOS,BD4TOTLS
            ADD       XXDIS,BD4DIS
            ADD       XXDEATH,BD4DEATH
            GOTO      PRNT1000
          ENDIF
.
          MATCH     "5",XXBEDTYP
          IF        @EQUAL
            ADD       XXSTATS,BD5BEDA
            ADD       XXTOTBD,BD5TOTBD
            ADD       XXTOTLOS,BD5TOTLS
            ADD       XXDIS,BD5DIS
            ADD       XXDEATH,BD5DEATH
            GOTO      PRNT1000
          ENDIF
.
          MATCH     ANSN,XXBEDTYP
          IF        @EQUAL
            ADD       XXSTATS,NURBEDA
            ADD       XXTOTBD,NURTOTBD
            ADD       XXTOTLOS,NURTOTLS
            ADD       XXDIS,NURDIS
            ADD       XXDEATH,NURDEATH
            GOTO      PRNT1000
          ENDIF
.
          MATCH     ANSB,XXBEDTYP
          IF        @EQUAL
            ADD       XXSTATS,BABBEDA
            ADD       XXTOTBD,BABTOTBD
            ADD       XXTOTLOS,BABTOTLS
            ADD       XXDIS,BABDIS
            ADD       XXDEATH,BABDEATH
            GOTO      PRNT1000
          ENDIF
.
          MATCH     ANSI,XXBEDTYP
          IF        @EQUAL
            ADD       XXSTATS,ICUBEDA
            ADD       XXTOTBD,ICUTOTBD
            ADD       XXTOTLOS,ICUTOTLS
            ADD       XXDIS,ICUDIS
            ADD       XXDEATH,ICUDEATH
            GOTO      PRNT1000
          ENDIF
.
          MATCH     ANSH,XXBEDTYP
          IF        @EQUAL
            ADD       XXSTATS,HDUBEDA
            ADD       XXTOTBD,HDUTOTBD
            ADD       XXTOTLOS,HDUTOTLS
            ADD       XXDIS,HDUDIS
            ADD       XXDEATH,HDUDEATH
            GOTO      PRNT1000
          ENDIF
.
          ADD       XXSTATS,OTHBEDA
          ADD       XXTOTBD,OTHTOTBD
          ADD       XXTOTLOS,OTHTOTLS
          ADD       XXDIS,OTHDIS
          ADD       XXDEATH,OTHDEATH
          GOTO      PRNT1000
.
PRNT5000  PRINT       TR,TD,"&nbsp;":
                      ETD,TD,"Total",ETD,TD,"Deluxe Beds/VIP suite":
                      ETD,TD,"1-Bedded",ETD,TD,"2-Bedded":
                      ETD,TD,"3-Bedded",ETD,TD,"4-Bedded":
                      ETD,TD,"5-10 Bedded",*N,ETD,TD,"Nursery Ward":
                      ETD,TD,"Sick Baby Unit",ETD,TD,"ICU Ward":
                      ETD,TD,"High Dependency Ward",ETD,TD,"Others":
                      ETD,ETR,*N
.
          PRINT       TR,TD,"Bed Complement ",ETD,TD:
                      TOTBEDA,ETD,TD,DELBEDA,ETD,TD,BD1BEDA,ETD,TD:
                      BD2BEDA,ETD,TD,BD3BEDA,ETD,TD,BD4BEDA,ETD,TD:
                      BD5BEDA,ETD,TD,NURBEDA,ETD,TD,BABBEDA,ETD,TD:
                      ICUBEDA,ETD,TD,HDUBEDA,ETD,TD,OTHBEDA,ETD,ETR,*N
.
          PRINT       TR,TD,"Number of Beds Available ",ETD,TD:
                      TOTBEDA,ETD,TD,DELBEDA,ETD,TD,BD1BEDA,ETD,TD:
                      BD2BEDA,ETD,TD,BD3BEDA,ETD,TD,BD4BEDA,ETD,TD:
                      BD5BEDA,ETD,TD,NURBEDA,ETD,TD,BABBEDA,ETD,TD:
                      ICUBEDA,ETD,TD,HDUBEDA,ETD,TD,OTHBEDA,ETD,ETR,*N
.
          PRINT       TR,TD,"Total Bed Days ",ETD,TD:
                      TOTBD,ETD,TD,DELTOTBD,ETD,TD,BD1TOTBD,ETD,TD:
                      BD2TOTBD,ETD,TD,BD3TOTBD,ETD,TD,BD4TOTBD,ETD,TD:
                      BD5TOTBD,ETD,TD,NURTOTBD,ETD,TD,BABTOTBD,ETD,TD:
                      ICUTOTBD,ETD,TD,HDUTOTBD,ETD,TD,OTHTOTBD,ETD,ETR,*N
.
          PRINT       TR,TD,"Actual Total Length of Stay ",ETD,TD:
                      TOTLS,ETD,TD,DELTOTLS,ETD,TD,BD1TOTLS,ETD,TD:
                      BD2TOTLS,ETD,TD,BD3TOTLS,ETD,TD,BD4TOTLS,ETD,TD:
                      BD5TOTLS,ETD,TD,NURTOTLS,ETD,TD,BABTOTLS,ETD,TD:
                      ICUTOTLS,ETD,TD,HDUTOTLS,ETD,TD,OTHTOTLS,ETD,ETR,*N
.
          PRINT       TR,TD,"Number of Discharges ",ETD,TD:
                      TOTDIS,ETD,TD,DELDIS,ETD,TD,BD1DIS,ETD,TD:
                      BD2DIS,ETD,TD,BD3DIS,ETD,TD,BD4DIS,ETD,TD:
                      BD5DIS,ETD,TD,NURDIS,ETD,TD,BABDIS,ETD,TD:
                      ICUDIS,ETD,TD,HDUDIS,ETD,TD,OTHDIS,ETD,ETR,*N
.
          PRINT       TR,TD,"Number of Deaths ",ETD,TD:
                      TOTDEATH,ETD,TD,DELDEATH,ETD,TD,BD1DEATH,ETD,TD:
                      BD2DEATH,ETD,TD,BD3DEATH,ETD,TD,BD4DEATH,ETD,TD:
                      BD5DEATH,ETD,TD,NURDEATH,ETD,TD,BABDEATH,ETD,TD:
                      ICUDEATH,ETD,TD,HDUDEATH,ETD,TD,OTHDEATH,ETD,ETR,*N
.
          PRINT       TR,TD,"Number of Day Surgery Cases:",ETD,TD,TOTDAYC:
                      ETD,ETR
.
          PRINT       TR,TD,"Number of Deliveries:",ETD,TD,"NA":
                      ETD,ETR
          PRINT       "</table>"
.
PRNT9000 
.
PRNT9999 RETURN
+
CLRVAR00  MOVE      ZERO,TOTBEDA   
          MOVE      ZERO,TOTBD    
          MOVE      ZERO,TOTLS   
          MOVE      ZERO,TOTDIS 
          MOVE      ZERO,TOTDEATH   
.
          MOVE      ZERO,DELBEDA   
          MOVE      ZERO,DELTOTBD 
          MOVE      ZERO,DELTOTLS
          MOVE      ZERO,DELDIS 
          MOVE      ZERO,DELDEATH  
.
          MOVE      ZERO,BD1BEDA  
          MOVE      ZERO,BD1TOTBD
          MOVE      ZERO,BD1TOTLS
          MOVE      ZERO,BD1DIS 
          MOVE      ZERO,BD1DEATH
.
          MOVE      ZERO,BD2BEDA
          MOVE      ZERO,BD2TOTBD  
          MOVE      ZERO,BD2TOTLS 
          MOVE      ZERO,BD2DIS  
          MOVE      ZERO,BD2DEATH   
.
          MOVE      ZERO,BD3BEDA   
          MOVE      ZERO,BD3TOTBD 
          MOVE      ZERO,BD3TOTLS
          MOVE      ZERO,BD3DIS 
          MOVE      ZERO,BD3DEATH 
.
          MOVE      ZERO,BD4BEDA    
          MOVE      ZERO,BD4TOTBD  
          MOVE      ZERO,BD4TOTLS 
          MOVE      ZERO,BD4DIS  
          MOVE      ZERO,BD4DEATH
.
          MOVE      ZERO,BD5BEDA
          MOVE      ZERO,BD5TOTBD   
          MOVE      ZERO,BD5TOTLS  
          MOVE      ZERO,BD5DIS   
          MOVE      ZERO,BD5DEATH
.
          MOVE      ZERO,NURBEDA
          MOVE      ZERO,NURTOTBD   
          MOVE      ZERO,NURTOTLS  
          MOVE      ZERO,NURDIS   
          MOVE      ZERO,NURDEATH
.
          MOVE      ZERO,BABBEDA
          MOVE      ZERO,BABTOTBD 
          MOVE      ZERO,BABTOTLS
          MOVE      ZERO,BABDIS 
          MOVE      ZERO,BABDEATH 
.
          MOVE      ZERO,ICUBEDA 
          MOVE      ZERO,ICUTOTBD 
          MOVE      ZERO,ICUTOTLS   
          MOVE      ZERO,ICUDIS    
          MOVE      ZERO,ICUDEATH 
.
          MOVE      ZERO,HDUBEDA    
          MOVE      ZERO,HDUTOTBD  
          MOVE      ZERO,HDUTOTLS 
          MOVE      ZERO,HDUDIS  
          MOVE      ZERO,HDUDEATH   
.
          MOVE      ZERO,OTHBEDA   
          MOVE      ZERO,OTHTOTBD 
          MOVE      ZERO,OTHTOTLS
          MOVE      ZERO,OTHDIS 
          MOVE      ZERO,OTHDEATH 
          MOVE      ZERO,TOTDAYC  
.
CLRVAR99  RETURN
+
.**************************************************************************
.*                              CREA0000               Called by:         *
.*             create a new temporary file                                *
.**************************************************************************
.
CREA0000  CALL      KILL0000                     * remove existing file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,FNAME,UKEY
          EXECUTE   CMDLINE,TASKID               * create temporary index file
.
          OPEN      TEMP1,FNAME                  * open temp index file
.
CREA9999  RETURN
+
.****************************************************************************
.*                              KILL0000               Called by:           *
.*               close and erase the temporary file                         *
.****************************************************************************
.
KILL0000  CLOSE     TEMP1                        * close file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ERASE,FNAME          * set file erase command
          EXECUTE   CMDLINE,TASKID               * erase temp file
.
KILL9999 RETURN
+
.****************************************************************************
.*        IO ROUTINES FOR TEMPORARY FILE                                    *
.****************************************************************************
.
RATEMP1   MOVE      ZERO,OVRCD
          READ      TEMP1,KEY7;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTEMP1   READ      TEMP1,KEY7;;
          RETURN
.
RDTEMP1   MOVE      ZERO,OVRCD
          READ      TEMP1,KEY7;XXBEDTYP,XXWARD,XXBED,XXDIS,XXDEATH:
                          XXSTATS,XXTOTBD,XXTOTLOS
          GOTO      OVERCOND IF OVER
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMP1;XXBEDTYP,XXWARD,XXBED,XXDIS,XXDEATH:
                          XXSTATS,XXTOTBD,XXTOTLOS
          GOTO      OVERCOND IF OVER
          RETURN
.
RPTEMP1   MOVE      ZERO,OVRCD
          READKP    TEMP1;XXBEDTYP,XXWARD,XXBED,XXDIS,XXDEATH:
                          XXSTATS,XXTOTBD,XXTOTLOS
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   WRITE     TEMP1,KEY7;XXBEDTYP,XXWARD,XXBED,XXDIS,XXDEATH:
                          XXSTATS,XXTOTBD,XXTOTLOS
          RETURN
.
UPTEMP1   UPDATE    TEMP1;XXBEDTYP,XXWARD,XXBED,XXDIS,XXDEATH:
                          XXSTATS,XXTOTBD,XXTOTLOS
          RETURN
.
DETEMP1   DELETE    TEMP1,KEY7
          RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       NHOSPDAY
          INC       PATCODIO/INC
          INC       PATDSCIO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATTRNIO/INC
          INC       PATWR1IO/INC

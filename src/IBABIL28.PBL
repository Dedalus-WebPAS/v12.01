.******************************************************************************
.*                                                                            *
.*                        P R O G R A M   S U M M A R Y                       *
.*                        -------------   -------------                       *
.*                                                                            *
.*     PROGRAM NAME:     IBABIL28                                             *
.*                                                                            *
.******************************************************************************
.*     NOTE AS OF 1/7/87 ANY MODS TO THIS PROGRAM MUST BE DONE TO MEDICAL     *
.*     SERVICES CORRESPONDING PROGRAM                                         *
.******************************************************************************
.*                                                                            *
.*     FUNCTION:         ACCOUNTS IN CREDIT REPORT                            *
.*                                                                            *
.*     DATE WRITTEN:     19/08/1986                                           *
.*                                                                            *
.*     AUTHOR:           MALCOLM HAYSE                                        *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.*       V12.00.01 14/05/2025  J.Tan          TSK 0955096                     *
.*                 Added alpanumeric visit number generation                  *
.*       V11.00.01 01/04/2020  J.Tan             TSK 0868813                  *
.*                 Changed PKNAME to DIM80                                    *
.*       V10.13.01  05/12/2018  Don Nguyen       TSK 0838558                  *
.*                  Deleted temp file (PSTROL) after processing               *
.*       V10.05.01  02/02/2015  Ania P        CAR 261630                      *
.*                  Removed use of PORT for temp file naming                  *
.*        V9.12.01  22/06/09 J.Tan    CAR 196141/198497                       *
.*                  Mods to include credit notes and GST Journal              *
.*        V9.04.02  03/08/05 J.Tan    CAR 62750                               *
.*                  Removed redefined amount variables                        *
.*        V9.04.01  26/08/2003  J.Tan           CAR 52835                     *
.*                  Mods for multi hospital                                   *
.*        V9.03.01  13/08/2003  Lina Vo         CAR 41196                     *
.*                  Recompiled for big changes in PATSELFN.                   *
.*        V9.02.01  28/08/2000  Dean Cramer     CAR 38200                     *
.*                  Fixed print overlaps for Web                              *
.*                  Included Sequence Option heading                          *
.*        V5.08.02  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.01  03/05/2000  Jill Habasque                                 *
.*                  Added Start & Finish display                              *
.*        V5.07.B01 11/11/1998  Jim Stathopoulos                              *
.*                  507 Changes                                               *
.******************************************************************************
.*        V5.04.01  02/05/1997  Steve Armstrong   SRF 642857                  *
.*                  Changed temp file name "pstidx" to standard format        *
.******************************************************************************
.*                       V5.01  05/05/89 Graeme Williams  SRF 4219            *
.*                              Added PINVCADM to Invoice file                *
.*                       V5.02  15/05/89 D Di.Paola                           *
.*                              Alter reports to allow 8 chars. U/R & Adm Nos.*
.*                              Put in STD001FD                               *
.*                       V5.03  23/05/89 Graeme Williams                      *
.*                              Fixed reset of line count in heading (was     *
.*                              missed) (Modbury Call)                        *
.*                       V5.04  27/05/89 J.Tan            SRF 100750          *
.*                              Fixed Mother Adm. to Form 8 (PATMI1FD)        *
.*                       V5.05  06/06/89 J.Tan            SRF 100809          *
.*                              Mods Locking master file (PATIOMAS)           *
.*                       V5.06  30/06/89 DIG                                  *
.*                              Recompiled for changes to OUTSITFD            *
.*                    V5.01.01  05/07/89 S. O'Gorman                          *
.*                              Converted for N.Z.                            *
.*                    V5.01.02  21/09/89 K.Peak           SRF 102367          *
.*                              Fixed Y/N question                            *
.*                    V5.01.03  20/11/89 J.Tan            SRF 102252          *
.*                              Added other financial option in PATSELFN      *
.*                    V5.01.121 27/11/90 Glen Hobby                           *
.*                              Recompiled for changes to PATMX1FD            *
.*                    V5.01.122 20/05/91 Steve O'Gorman                       *
.*                              Fixed errors from new compiler                *
.*                    V5.01.123 01/06/92 i chung          SRF 115319          *
.*                              Fixed incorrect P.R.A. details for Option 3   *
.*        V5.03.B1  06/06/95  Paul Howells                  SRF 136735        *
.*                  Recompiled for PATSELFN                                   *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
          INC       IBAPASFD/INC
          INC       IBASECFD/INC
          INC       PATSELDT/INC
          INC       IBACONFD/INC
.
          INC       OUTSITFD/INC
          INC       PATCODFD/INC
          INC       PATDSCFD/INC
          INC       PATINVFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATRE1FD/INC
          INC       PATVISFD/INC
          INC       PATHSPFD/INC
          INC       PMSVX1FD/INC
          INC       IBASEQFD/INC  
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
+
.
.           Temporary index file for report
.
TEMPFILE  IFILE     SQL, FIXED=269, KEY="u17-22,1-8,23-30"
.
.Name      Type     Size    Physical     Start
.----      ----     ----    --------     -----
.PINVNO    DIM       8          8           1
.PINVDTE   DIM       8          8           9
.PINALPHA  DIM       6          6          17
.UNIQUEX   DIM       8          8          23
.PINVADM   DIM       8          8          31
.PINVUR    FORM      8          8          39
.PINVAMT   FORM     12.2        8          47
.PINVPATA  FORM     12.2        8          55
.PINVHFDA  FORM     12.2        8          63
.PINVOTHA  FORM     12.2        8          71
.PINVJOUR  FORM     12.2        8          79
.PTINCRTT  FORM     12.2        8          87
.PTINGSTJ  FORM     12.2        8          95
.PINVCLM   DIM       3          3          98
.ACLSS     DIM       3          3         101
.ADATE     DIM       8          8         104
.DDATE     DIM       8          8         112
.PKNAME    DIM       80         80        120
.PKADD1    DIM       25         25        200
.PKADD2    DIM       25         25        225
.PKSUBR    DIM       15         15        250
.PKPOST    DIM       4          4         265
.End of Record                            269
+
.         WORK AREA
.
AFIRST    DIM       6
ALAST     DIM       6
.
CDESC     DIM       20
CMDLINE   DIM       50
CODE      DIM       2
COTHSFN   FORM      1
.
DIM2A     DIM       2
DIM6      DIM       6
DIM8      DIM       8
DIM17A    DIM       17
DIM23A    DIM       23
DIM23B    DIM       23
DIM30     DIM       30
DFNAME    DIM       6
DLNAME    DIM       6
DPSTAT    DIM       30
.
EOYDAY    DIM       2
EOYMON    DIM       2
EOYYEAR   DIM       2
EOYCENT   DIM       2
.
FIRST     DIM       8
FNAME     DIM       6
FNAMER    DIM       8
FNAMET    DIM       8
FORM92    FORM      9.2
FORM82A   FORM      8.2
FORM82    FORM      8.2
.
LAST      DIM       8
LNAME     DIM       6
.
PINVCRED  FORM      10.2
PINVPAID  FORM      8.2
PNAME     DIM       29
PSTROL    FILE      ASCII, FIXED=256
PSUBR     DIM       15
.
SFIRST    FORM      8
STMAST    DIM       2
.
TINVAMT   FORM      8.2
TINVCRED  FORM      8.2
TINVJOUR  FORM      8.2
TINVPAID  FORM      8.2
.
XMON1     DIM       2
XMON2     DIM       2
XDAY1     DIM       2
XDAY2     DIM       2
XYEAR1    DIM       2
XYEAR2    DIM       2
XCENT1    DIM       2
XCENT2    DIM       2
.
UNIQUE    FORM      8
UNIQUEX   DIM       8
.
.         CONSTANTS
.
FIVE9     FORM      "59"
STAT0     INIT      "U/R IS VALID                   "
STAT1     INIT      "U/R IS CANCELLED               "
STAT2     INIT      "REFER TO U/R NUMBER : "
STAT3     INIT      "U/R IS NOT YET ALLOCATED       "
.
PRGID     INIT      "IBABIL28"
PRGNAM    INIT      "Accounts In Credit Report"
VERSION   INIT      "V12.01.00"
+
.
.         DISPLAY SCREEN HEADER AND OPEN FILES
.
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening outsitaf";
          OPEN      OUTSITA1,"outsitaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"patinvrf";
          OPEN      PATINVR1,"patinvrf"
          OPEN      PATINVR4,"patinvrf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"patre1af";
          OPEN      PATRE1A1,"patre1af"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA2,"patvisaf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS:
                                  *85,IBCNUGST
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMER
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMET
+
.         START OF PROGRAM
.
.          Main  Menu - SELECT OPTION
.
START     MOVE      ZERO,CPAGENO
.
          DISPLAY   *P1:4,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Invoice Number Sequence":
                    *P1:6,*V2LON,TWO,*HOFF," = Alphabetic Sequence":
                  *P1:7,*V2LON,THREE,*HOFF," = Alphabetic Sequence (Detailed)":
                    *P1:9,"Select Option : ";
.
REKSTRT   KEYIN     *P17:9,*EL,*DV,UNDLN1,*P17:9,*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
BRNCH     BRANCH    OPTION OF INVNOSEQ,ALPHASEQ,ALPHADET
          BEEP
          GOTO      REKSTRT
.
NOMAST    DISPLAY   *P1:24,*EL,*P28:24,*V2LON,"** No Patients on file **",*W3;
.
          BRANCH    OPTION OF START
          CALL      KILLIDX
          GOTO      START
.
.         PRINT VIA INVOICE NUMBER SEQUENCE
.
INVNOSEQ  KEYIN     *P1:4,*EF,*P1:6,"Starting Invoice Number : ":
                    *V2LON,*DE,*JR,FIRST;
          RESET     FIRST
          GOTO      START IF EOS
          DISPLAY   *P27:6,*V2LON,FIRST;
.
MASTN1A   KEYIN     *P1:8,*EL,"  Ending Invoice Number : ":
                    *V2LON,*DE,*JR,LAST;
          RESET     LAST
          GOTO      MASTN1B IF NOT EOS
          MOVE      "99999999" TO LAST
.
MASTN1B   DISPLAY   *P27:8,*V2LON,LAST;
.
          RESET     FIRST
          GOTO      MASTN2 IF EOS  
.
          MATCH     FIRST,LAST
          GOTO      MASTN1C IF LESS         
          MOVE      FIRST,SFIRST
          SUB       ONE,SFIRST
          GOTO      MASTN2
.
MASTN1C   DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Ending Inv. No. must be >= Starting Inv. No. **    ";
          CALL      HITENTER
          GOTO      MASTN1A
.
.         KEYIN STARTING AND ENDING NAME
.
ALPHASEQ  MOVE      SP6,FNAME
          MOVE      SP6,LNAME
.
          KEYIN     *P1:4,*EF,*P1:6,"Starting Name : ",*V2LON,FNAME;
          RESET     FNAME
          IF        @EOS
            MOVE      "Start",DFNAME
          ELSE
            MOVE      FNAME,DFNAME
          ENDIF
          DISPLAY   *P17:6,*V2LON,DFNAME;
.
          KEYIN     *P1:8,"  Ending Name : ",*V2LON,LNAME;
          RESET     LNAME
          IF        @EOS
            MOVE      "ZZZZZZ" TO LNAME
            MOVE      "Finish",DLNAME
          ELSE
            MOVE      LNAME,DLNAME
          ENDIF
.
ALPHA1    DISPLAY   *P17:8,*V2LON,DLNAME;
          GOTO      MASTN2
.
.         KEYIN STARTING AND ENDING NAME (DETAILED)
.
ALPHADET  MOVE      SP6,FNAME
          MOVE      SP6,LNAME
.
          KEYIN     *P1:4,*EF,*P1:6,"Starting Name : ",*V2LON,FNAME;
          RESET     FNAME
          IF        @EOS
            MOVE      "Start",DFNAME
          ELSE
            MOVE      FNAME,DFNAME
          ENDIF
          DISPLAY   *P17:6,*V2LON,DFNAME;
.
ALPHADT1  KEYIN     *P1:8,"  Ending Name : ",*V2LON,LNAME;
          RESET     LNAME
          IF        @EOS
            MOVE      "ZZZZZZ" TO LNAME
            MOVE      "Finish",DLNAME
          ELSE
            MOVE      LNAME,DLNAME
          ENDIF
.
ALPHADT2  DISPLAY   *P17:8,*V2LON,DLNAME;
.
          MATCH     FNAME,LNAME
          GOTO      MASTN2 IF NOT LESS
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Ending Name must be >= Starting Name **    ";
          CALL      HITENTER
          GOTO      ALPHADT1
.
MASTN2    KEYIN     *P1:10,*EL,"Ok to Continue (",*V2LON,"Y",*HOFF,"/":
                        *V2LON,"N",*HOFF,") ? ",*V2LON,ANS; 
          MATCH     "Y" TO ANS
          GOTO      MASTN2A IF EQUAL
          MATCH     "N" TO ANS
          GOTO      MATNN IF NOT EQUAL
          BRANCH    OPTION,INVNOSEQ,ALPHASEQ,ALPHADET
.
MATNN     BEEP
          GOTO      MASTN2
.
MASTN2A   CALL      PATSELFN
          BRANCH    EXIT,START
.
MASTN2B   IF        IBCNMHOS=1
            CALL      KHOSID             * Keyin hospital id
            BRANCH    EXIT,BRNCH
          ENDIF
.
          KEYIN     *P1:24,*EL,"Ok to Continue (",*V2LON,"Y",*HOFF,"/":
		        *V2LON,"N",*HOFF,") ? ",*V2LON,ANS; 
          MATCH     "Y" TO ANS
          GOTO      MASTN3 IF EQUAL
          MATCH     "N" TO ANS
          GOTO      BRNCH IF EQUAL
          BEEP
          GOTO      MASTN2B
.
MASTN3    MOVE      ZERO,TINVAMT
          MOVE      ZERO,TINVPAID
          MOVE      ZERO,TINVJOUR
          MOVE      ZERO,TINVCRED
.
          PACK      PKNAME,SP70,SP20    * initialize Name for Options 1 & 2
          MOVE      SP30,PKADD1         * initialize Address 1 for Options 1 & 2
          MOVE      SP30,PKADD2         * initialize Address 2 for Options 1 & 2
          MOVE      SP20,PKSUBR         * initialize Suburb for Options 1 & 2
          MOVE      SP4,PKPOST          * initialize Postcode for Options 1 & 2
          MOVE      "60",CLNO
.
          BRANCH    OPTION OF INVOICE,ALPHA2,ALPHA3
.
INVOICE   MOVE      SFIRST,KEY8
          CALL      RDSINV1
          DISPLAY   *P1:24,*EL,*P10:24,"Invoice ## :",*P60:24,"Scanning :";
.
NEXTN     CALL      RDKINV1
          BRANCH    OVRCD OF ENDP
.
          MATCH     ZEROVISN,PINVADM
          GOTO      NEXTN IF EQUAL
          DISPLAY   *P70:24,PINVNO;
.
          COMPARE   FSTSYS,PINVSYS
          GOTO      NEXTN IF LESS
.
          COMPARE   PINVSYS,FEDSYS
          GOTO      NEXTN IF LESS
.
          MATCH     FSTSITE,PINVSITE
          GOTO      NEXTN IF LESS
.
          MATCH     PINVSITE,FEDSITE
          GOTO      NEXTN IF LESS
.
          MATCH     SP3,FSTDEPT
          GOTO      CNT1 IF EQUAL
.
          MOVE      PINVSITE,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD,NEXTN
.
          MATCH     FSTDEPT,OSTSYST
          GOTO      NEXTN IF LESS
.
          MATCH     OSTSYST,FEDDEPT
          GOTO      NEXTN IF LESS
.
CNT1      MATCH     LAST TO PINVNO
          GOTO      NEXTN2 IF EQUAL
          GOTO      ENDP IF NOT LESS
.
NEXTN2    MOVE      PINVUR,KEY8
          CALL      RDMAST1
.
          CALL      DISPDA
          GOTO      NEXTN
.
ALPHA2    DISPLAY   *P1:24,*EL,*V2LON,*P29:24,"** Generating Report **",*W2;
          CALL      CREAIDX                   * creating temp. index file
.
          DISPLAY   *P1:24,*EL,*P10:24,"Name ## :",*P60:24,"Scanning :";
          MOVE      SP8,KEY8
          CALL      RDSINV1
.
NXTALP    CALL      RDKINV1
          BRANCH    OVRCD OF NXTAL1
.
          DISPLAY   *P70:24,PINALPHA;
.
          COMPARE   FSTSYS,PINVSYS
          GOTO      NXTALP IF LESS
.
          COMPARE   PINVSYS,FEDSYS
          GOTO      NXTALP IF LESS
.
          MATCH     FSTSITE,PINVSITE
          GOTO      NXTALP IF LESS
.
          MATCH     PINVSITE,FEDSITE
          GOTO      NXTALP IF LESS
.
          MATCH     SP3,FSTDEPT
          GOTO      CNTI IF EQUAL
          MOVE      PINVSITE,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD,NXTALP
.
          MATCH     FSTDEPT,OSTSYST
          GOTO      NXTALP IF LESS
.
          MATCH     OSTSYST,FEDDEPT
          GOTO      NXTALP IF LESS
.
CNTI      MATCH     ZEROVISN,PINVADM
          GOTO      NXTALP IF EQUAL
.
          MATCH     FNAME,PINALPHA
          GOTO      WREC IF EQUAL
          GOTO      NXTALP IF LESS
.
          MATCH     LNAME,PINALPHA
          GOTO      WREC IF EQUAL
          GOTO      NXTALP IF NOT LESS
.
WREC      DISPLAY   *P25:24,*V2LON,PINALPHA;
.
          MOVE      UNIQUE,UNIQUEX
          PACK      KEY22 WITH PINALPHA,PINVNO,UNIQUEX
          WRITE     TEMPFILE,KEY22;PINVNO,PINVDTE,PINALPHA,UNIQUEX,PINVADM:
                                   PINVUR,PINVAMT,PINVPATA,PINVHFDA,PINVOTHA:
                                   PINVJOUR,PTINCRTT,PTINGSTJ:
                                   PINVCLM,ACLSS,ADATE,DDATE,PKNAME:
                                   PKADD1,PKADD2,PKSUBR,PKPOST
          ADD       ONE,UNIQUE
          GOTO      NXTALP
.
NXTAL1    DISPLAY   *P1:24,*EL,*P10:24,*V2LON,"** Printing **"; 
.
          MOVE      SP30,KEY22
          READ      TEMPFILE,KEY22;;
READLOP   READKS    TEMPFILE;PINVNO,PINVDTE,PINALPHA,UNIQUEX,PINVADM,PINVUR:
                             PINVAMT,PINVPATA,PINVHFDA,PINVOTHA,PINVJOUR:
                             PTINCRTT,PTINGSTJ:
                             PINVCLM,ACLSS,ADATE,DDATE,PKNAME,PKADD1:
                             PKADD2,PKSUBR,PKPOST
          GOTO      ENDP IF OVER
          DISPLAY   *P60:24,PINALPHA;
.
          MOVE      PINVUR,KEY8
          CALL      RDMAST1
.
          CALL      DISPDA
          GOTO      READLOP
.
ALPHA3    DISPLAY   *P1:24,*EL,*V2LON,*P29:24,"** Generating Report **",*W2;
          CALL      CREAIDX                  * creating temp. index file
.   
          DISPLAY   *P1:24,*EL,*P10:24,"Name ## :",*P55:24,"Scanning :";
          MOVE      SP8,KEY8
          CALL      RDSINV1
.
NXTDLP    CALL      RDKINV1
          BRANCH    OVRCD OF NXTALD
.
          DISPLAY   *P66:24,PINALPHA;
.
          COMPARE   FSTSYS,PINVSYS
          GOTO      NXTDLP IF LESS
.
          COMPARE   PINVSYS,FEDSYS
          GOTO      NXTDLP IF LESS
.
          MATCH     FSTSITE,PINVSITE
          GOTO      NXTDLP IF LESS
.
          MATCH     PINVSITE,FEDSITE
          GOTO      NXTDLP IF LESS
.
          MATCH     SP3,FSTDEPT
          GOTO      CNT2 IF EQUAL
          MOVE      PINVSITE,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD,NXTDLP
.
          MATCH     FSTDEPT,OSTSYST
          GOTO      NXTDLP IF LESS
.
          MATCH     OSTSYST,FEDDEPT
          GOTO      NXTDLP IF LESS
.
CNT2      MATCH     ZEROVISN,PINVADM
          GOTO      NXTDLP IF EQUAL
.
          MATCH     FNAME,PINALPHA
          GOTO      DREC IF EQUAL
          GOTO      NXTDLP IF LESS
.
          MATCH     LNAME,PINALPHA
          GOTO      DREC IF EQUAL
          GOTO      NXTDLP IF NOT LESS
.
DREC      DISPLAY   *P20:24,*V2LON,PINALPHA;
.
          MOVE      SP3,ACLSS
          MOVE      SP8,ADATE
          MOVE      SP8,DDATE
.
          BRANCH    PINVSYS,VISITDET,VISITDET,INPATDET,VISITDET
.
INPATDET  MOVE      PINVADM,KEY8
          CALL      RDMISS1
.
          MOVE      PINVADM,KEY8
          CALL      RDDSCH1
          GOTO      NAMEDET
.
VISITDET  PACK      KEY24,PINVUR,SP20
          CALL      RDSVISA2
.
VISITDT1  CALL      RDKVISA2
          BRANCH    OVRCD,NAMEDET
.
          MATCH     PVIURNO,PINVUR
          GOTO      NAMEDET IF NOT EQUAL
          MATCH     PVIBILL,PINVADM
          GOTO      VISITDT1 IF NOT EQUAL
          COMPARE   PVITYPE,PINVSYS
          GOTO      VISITDT1 IF NOT EQUAL
.
          UNPACK    PVIDATE,CCENT,CYEAR,CMON,CDAY
          PACK      ADATE,CCENT,CYEAR,CMON,CDAY
.
NAMEDET   MOVE      PINVUR,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,PRADET
.
          CLEAR     PKNAME
          MATCH     SP4,PTITL
          GOTO      ENDTTL2 IF EQUAL
          APPEND    PTITL,PKNAME
.
TPL1      CMATCH    SP1,PKNAME
          GOTO      ENDTTL1 IF NOT EQUAL
          BUMP      PKNAME,-1
          GOTO      TPL1 IF NOT EOS
.
ENDTTL1   APPEND    SP1,PKNAME
.
ENDTTL2   MOVE      PGNAME,ANS
          APPEND    ANS,PKNAME
          APPEND    DOT,PKNAME
          APPEND    PTMASNAM,PKNAME
          APPEND    SP70,PKNAME
          APPEND    SP20,PKNAME
          RESET     PKNAME
.
          MOVE      PADD1,PKADD1
          MOVE      PADD2,PKADD2
          MOVE      PSUBR,PKSUBR
          MOVE      PPOST,PKPOST
.
PRADET    MOVE      PINVADM,KEY8
          CALL      RDRESP1
.
          MOVE      UNIQUE,UNIQUEX
          PACK      KEY22 WITH PINALPHA,PINVNO,UNIQUEX
          WRITE     TEMPFILE,KEY22;PINVNO,PINVDTE,PINALPHA,UNIQUEX,PINVADM:
                                   PINVUR,PINVAMT,PINVPATA,PINVHFDA,PINVOTHA:
                                   PINVJOUR,PTINCRTT,PTINGSTJ:
                                   PINVCLM,ACLSS,ADATE,DDATE,PKNAME:
                                   PKADD1,PKADD2,PKSUBR,PKPOST
          ADD       ONE,UNIQUE
          GOTO      NXTDLP
.
NXTALD    DISPLAY   *P1:24,*EL,*P10:24,*V2LON,"** Printing **"; 
.
          MOVE      SP30,KEY22
          READ      TEMPFILE,KEY22;;
RDDETAL   READKS    TEMPFILE;PINVNO,PINVDTE,PINALPHA,UNIQUEX,PINVADM,PINVUR:
                             PINVAMT,PINVPATA,PINVHFDA,PINVOTHA,PINVJOUR:
                             PTINCRTT,PTINGSTJ:
                             PINVCLM,ACLSS,ADATE,DDATE,PKNAME,PKADD1:
                             PKADD2,PKSUBR,PKPOST
          GOTO      ENDP IF OVER
          DISPLAY   *P60:24,PINALPHA;
.
          MOVE      PINVUR,KEY8
          CALL      RDMAST1
.
          CALL      DISPDA
          GOTO      RDDETAL
.
.         OVER Condition found..print last line of -- and display message
.
ENDP      COMPARE   ZERO,CPAGENO
          GOTO      NOMAST IF EQUAL
.
          BRANCH    OPTION OF ENINV,ENINV,ENDET
.
ENINV     CALL      PAGEN1
          PRINT     *43,"TOTALS":
                    *52,TINVAMT,*64,TINVPAID,*77,TINVJOUR,*90,TINVCRED
          GOTO      CONDP
.
ENDET     PRINT     *76,"TOTALS":
                    *83,TINVAMT,*95,TINVPAID,*107,TINVJOUR,*121,TINVCRED
.
CONDP     PRINT     *N,"  ***  End of Report  ***"
          DISPLAY   *P1:24,*EL,*P24:24,*V2LON:
                    "** Report Generation Completed **",*W2,*P1:12,*EF;
.
          BRANCH    OPTION OF START
          CALL      KILLIDX
          GOTO      START
.
.         PRINT ACTUAL DATA 
.
DISPDA    PACK        KEY8,PINVADM,SP8
          CALL        RDPMVX11
          BRANCH      OVRCD,ENDDSP
          IF          IBCNMHOS=1 
            MATCH       SP10,PTHSHOSP
            IF          !@EQUAL
              MATCH       PMVXMHOS,PTHSHOSP
              GOTO        ENDDSP IF NOT EQUAL
            ENDIF
          ENDIF
.
          MOVE      PGNAME,ANS
          PACK      PNAME WITH PTITL,SP1,ANS,DOT,PSNAME
.
          ADD       PTINCRTT,PINVAMT
.
          MOVE      PINVPATA,PINVPAID
          ADD       PINVHFDA,PINVPAID
          ADD       PINVOTHA,PINVPAID
.
          MOVE      PINVPAID,PINVCRED
          ADD       PINVJOUR,PINVCRED
          IF        IBCNUGST=2 | IBCNUGST=3
            ADD       PTINGSTJ,PINVCRED
          ENDIF
          MULT      SEQ,PINVCRED            *** MAKE IT POSTIVE TO PRINT
          SUB       PINVAMT,PINVCRED
.
          COMPARE   ZERO,PINVCRED
          GOTO      ENDDSP IF LESS
          GOTO      ENDDSP IF EQUAL
.
          COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
.
          BRANCH    OPTION OF DISINV,DISALP,DISDET
.
DISINV    MOVE      PINVAMT,FORM82
          MOVE      PINVJOUR,FORM92
          IF        IBCNMHOS=1
            PRINT     *1,PINVNO,*11,PNAME,*43,PINVADM,*52,FORM82:
                      *64,PINVPAID,*76,FORM92,*88,PINVCRED,*104,PMVXMHOS
          ELSE
            PRINT     *1,PINVNO,*11,PNAME,*43,PINVADM,*52,FORM82:
                      *64,PINVPAID,*76,FORM92,*88,PINVCRED
          ENDIF
          ADD       ONE,CLNO
.
          DISPLAY   *P25:24,*V2LON,PINVNO;
          GOTO      CONTR1
.
DISALP    MOVE      PINVAMT,FORM82
          MOVE      PINVJOUR,FORM92
          IF        IBCNMHOS=1
            PRINT     *1,PNAME,*33,PINVNO,*43,PINVADM,*52,FORM82:
                      *64,PINVPAID,*76,FORM92,*88,PINVCRED,*106,PMVXMHOS
          ELSE
            PRINT     *1,PNAME,*33,PINVNO,*43,PINVADM,*52,FORM82:
                      *64,PINVPAID,*76,FORM92,*88,PINVCRED
          ENDIF
          ADD       ONE,CLNO
.
          DISPLAY   *P27:24,*V2LON,PINALPHA;
          GOTO      CONTR1
.
DISDET    UNPACK    ADATE INTO XCENT1,XYEAR1,XMON1,XDAY1
.   
          PRINT     *1,"|",*2,PNAME,*31,"|",PINVNO,*40,"|",PINVADM:
                    *49,"|",PINVUR,*59,"|",XDAY1,"/",XMON1,"/",XYEAR1,*68,"|";
.
          MATCH     SP6,DDATE
          GOTO      DISD10 IF EQUAL
.
          UNPACK    DDATE INTO XCENT2,XYEAR2,XMON2,XDAY2
          PRINT     *69,XDAY2,"/",XMON2,"/",XYEAR2;
.
DISD10    MOVE      PINVJOUR,FORM82A
          MOVE      PKADD1,DIM23A
          MOVE      PKADD2,DIM23B
          MOVE      PKSUBR,DIM17A
          MOVE      PKPOST,DIM6   
          MOVE      PINVAMT,FORM82
          UNPACK    PKNAME,KEY21
          PRINT     *77,"| ",ACLSS,*82,"|",FORM82,*94,"|",PINVPAID:
                    *106,"|",*107,FORM82A,*118,"|",PINVCRED,*132,"|",*N:
                    *1,"|",*8,KEY21,*31,"|",*40,"|",*49,"|",*59,"|",*68,"|";
.
          IF        IBCNMHOS=1
            PRINT     *77,"|",PMVXMHOS,*82,"|",*94,"|":
                      *106,"|",*118,"|",*132,"|",*N;
          ELSE
            PRINT     *77,"|",*82,"|",*94,"|",*106,"|",*118,"|",*132,"|",*N;
          ENDIF
.
          PRINT     *1,"|",*8,DIM23A,*31,"|",*40,"|",*49,"|",*59,"|",*68,"|":
                    *77,"|",*82,"|",*94,"|",*106,"|",*118,"|",*132,"|",*N:
                    *1,"|",*8,DIM23B,*31,"|",*40,"|",*49,"|",*59,"|",*68,"|":
                    *77,"|",*82,"|",*94,"|",*106,"|",*118,"|",*132,"|",*N:
                    *1,"|",*8,DIM17A,*25,DIM6,*31,"|",*40,"|",*49,"|",*59,"|":
                    *68,"|",*77,"|",*82,"|",*94,"|",*106,"|",*118,"|",*132,"|"
.
          CLEAR     PNAME
          CALL      PAGEND
          ADD       FIVE,CLNO
.
          DISPLAY   *P27:24,*V2LON,PINALPHA;
.
CONTR1    ADD       PINVAMT,TINVAMT
          ADD       PINVPAID,TINVPAID
          ADD       PINVJOUR,TINVJOUR
          ADD       PINVCRED,TINVCRED
ENDDSP    RETURN
+
.******************************************************************************
.         Keyin hospital ID
.******************************************************************************
KHOSID  DISPLAY    *P1:15,*EL,"Hospital Id : ";
        MOVE       TEN5,ECOL
        MOVE       "15",EVERT
        MOVE       SP3,CKYIDEF3
        MOVE       ZERO,CKYIMAND           * Not Mandatory
        OPEN       PATHSPA1,"pathspaf"
.
        CALL       PATHSPKY
        BRANCH     EXIT,KHOS10,KHOS20
        MOVE       ZERO,EXIT
        GOTO       KHOS99  
.
KHOS10  MOVE       "All Hospitals",PTHSNAME
        MOVE       ZERO,EXIT
        GOTO       KHOS99
.
KHOS20  MOVE       ONE,EXIT
KHOS99  RETURN
.
.******************************************************************************
.         Deleting  AN INDEXED FILE BASED ON PORT
.
KILLIDX   CLOSE     TEMPFILE
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMET,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          RETURN
.
CREAIDX   CALL      KILLIDX
          PREP      PSTROL,FNAMER
          WRITE     PSTROL,SEQ;"isbuild ",FNAMET," 269 u17-22,1-8,23-30"
          WEOF      PSTROL,SEQ
          CLOSE     PSTROL
          CALL      ROLLF
.
          OPEN      TEMPFILE,FNAMET
          MOVE      ONE,UNIQUE
          RETURN  
.
ROLLF     CLEAR     CMDLINE
          APPEND    "sh ",CMDLINE
          APPEND    FNAMER,CMDLINE
          APPEND    ".txt",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          PREP      PSTROL,FNAMER
          CLOSE     PSTROL,DELETE
          DISPLAY   *P1:12,*EF;
          RETURN
+
.         LAST LINE ON THE PAGE
.
PAGEND    IF        IBCNMHOS=1
            PRINT     "*----------------------------------------------":
                      "---------------------------------------------":
                      "---------------------------------------*"
          ELSE
            PRINT     "*----------------------------------------------":
                      "---------------------------------------------":
                      "----------------------------------------*"
          ENDIF
          ADD       ONE,CLNO
          RETURN
+
.
PAGEN1    IF        IBCNMHOS=1
            PRINT     "*------------------------------------------------------":
                      "------------------------------------------------------*"
          ELSE
            PRINT     "*----------------------------------------------":
                      "----------------------------------------------------*"
          ENDIF
          ADD       ONE,CLNO
          RETURN
+
.         HEADINGS FOR OUTPUT
.
HEAD      ADD       ONE,CPAGENO
          CLOCK     TIME,CTIMEIS
.
          BRANCH    OPTION OF PINV1,PINV2,PDET
.
PINV1     COMPARE   ONE,OPTION
          GOTO      PINV2 IF NOT EQUAL
.
          COMPARE   ONE,CPAGENO
          CALL      PAGEN1 IF NOT EQUAL
          GOTO      HEAD1
.
PINV2     COMPARE   TWO,OPTION
          GOTO      PDET IF NOT EQUAL
.
          COMPARE   ONE,CPAGENO
          CALL      PAGEN1 IF NOT EQUAL
.
HEAD1     PRINT     *F,*1,PRGID,*25,CNAME,*78,"DATE : ",CDATE:
                    *N,*1,VERSION,*25,"***  ",PRGNAM,"  ***":
                       *78,"TIME : ",CTIMEIS:
                    *N,*78,"PAGE : ",*90,CPAGENO,*N
.
         IF        IBCNMHOS =1
           PRINT     *25,"Hospital : ",PTHSHOSP,"    ",PTHSNAME
         ENDIF
.
          COMPARE   ONE,OPTION
          GOTO      PRTINV IF EQUAL
.
          COMPARE   TWO,OPTION
          GOTO      PRTALP IF EQUAL
.
PRTINV    PRINT     *25,"Starting Invoice : ",FIRST:
                    *N,*25,"  Ending Invoice : ",LAST,*N
.
          PRINT     *19,"INVOICE NUMBER SEQUENCE - ";
          CALL      PRTSEL
.
          IF        IBCNMHOS =1
            PRINT      *1," Invoice",*11,"Patient Name",*43,"Admission":
                      *55," Invoice",*67,"  Amount":
                      *79,"  Journal",*89,"   Amount in",*104,"Hospital":
                       *N,"    ##",*43,"    ##":
                      *55,"   Total",*67,"    Paid",*89,"      Credit":
                      *104,"   ID"
            MOVE      TEN2,CLNO
          ELSE
            PRINT      *1," Invoice",*11,"Patient Name",*43,"Admission":
                      *55," Invoice",*67,"  Amount":
                      *79,"  Journal",*89,"   Amount in":
                       *N,"    ##",*43,"    ##":
                      *55,"   Total",*67,"    Paid",*89,"      Credit"
            MOVE      TEN1,CLNO
          ENDIF
.
          GOTO      HEAD10
.
PRTALP    PRINT     *25,"Starting Name : ",DFNAME:
                    *N,*25,"  Ending Name : ",DLNAME,*N
.
          PRINT     *23,"ALPHABETIC SEQUENCE - ";
          CALL      PRTSEL
.
          IF        IBCNMHOS =1
            PRINT         *1," Patient Name",*34," Invoice",*43,"Admission":
                         *55," Invoice",*67,"  Amount":
                         *79,"  Journal",*91," Amount in",*106,"Hospital":
                      *N,*34,"    ##",*43,"    ##":
                         *52,"   Total",*67,"    Paid",*91,"    Credit":
                         *106,"   ID"
            MOVE      TEN2,CLNO
          ELSE
            PRINT         *1," Patient Name",*34," Invoice",*43,"Admission":
                         *55," Invoice",*67,"  Amount":
                         *79,"  Journal",*91," Amount in":
                      *N,*34,"    ##",*43,"    ##":
                         *52,"   Total",*67,"    Paid",*91,"    Credit"
            MOVE      TEN1,CLNO
          ENDIF
.
          GOTO      HEAD10
.
PDET      PRINT     *F,*1,PRGID,*45,CNAME,*118,"DATE : ",CDATE:
                    *N,*1,VERSION,*45,"***  ",PRGNAM,"  ***":
                       *118,"TIME : ",CTIMEIS:
                    *N,*118,"PAGE : ",*130,CPAGENO,*N
.
          IF        IBCNMHOS =1
            PRINT     *25,"Hospital : ",PTHSHOSP,"    ",PTHSNAME
          ENDIF
.
          PRINT     *45,"Starting Name : ",DFNAME:
                    *N,*45,"  Ending Name : ",DLNAME,*N
. 
          PRINT     *12,"DETAILED - ALPHABETIC SEQUENCE - ";
          CALL      PRTSEL
          CALL      PAGEND
.
          IF        IBCNMHOS =1
            PRINT      *1,"| Patient Name",*31,"|Invoice",*40,"|   Adm":
                      *49,"|   U/R",*59,"|  Adm.",*68,"|  Dsc.",*77,"|Clss":
                      *82,"|  Invoice",*94,"|  Amount",*106,"|  Journal":
                     *118,"|  Amount in  |",*N:
                       *1,"| Person Responsible for Acct",*31,"|   ##":
                      *40,"|    ##",*49,"|    ##",*59,"|  Date":
                      *68,"|  Date",*77,"|Hosp",*82,"|   Total":
                      *94,"|   Paid",*106,"|",*118,"|    Credit   |"
            MOVE      TEN3,CLNO
          ELSE
            PRINT      *1,"| Patient Name",*31,"|Invoice",*40,"|   Adm":
                      *49,"|   U/R",*59,"|  Adm.",*68,"|  Dsc.",*77,"|Pat.":
                      *82,"|  Invoice",*94,"|  Amount",*106,"|  Journal":
                     *118,"|  Amount in  |",*N:
                       *1,"| Person Responsible for Acct",*31,"|   ##":
                      *40,"|    ##",*49,"|    ##",*59,"|  Date":
                      *68,"|  Date",*77,"|Clss",*82,"|   Total":
                      *94,"|   Paid",*106,"|",*118,"|    Credit   |"
            MOVE      TEN2,CLNO
          ENDIF
.
.
HEAD10    BRANCH    OPTION OF PAGEN1,PAGEN1,PAGEND
          RETURN
+
.
PRTSEL    COMPARE   ZERO,FSTSYS
          GOTO      AEHEAD IF NOT EQUAL
.
          PRINT     *45,"CONSOLIDATED REPORT",*N,*N;
          COMPARE   ZERO,FSTSYS
          RETURN    IF EQUAL
.
AEHEAD    COMPARE   ONE,FSTSYS
          GOTO      OPHEAD IF NOT EQUAL
.
          PRINT     *45,"ACCIDENT & EMERGENCY REPORT",*N,*N;
          COMPARE   ONE,FSTSYS
          RETURN    IF EQUAL
.
OPHEAD    COMPARE   TWO,FSTSYS
          GOTO      IPHEAD IF NOT EQUAL
.
          PRINT     *45,"OUTPATIENT REPORT",*N;
          MATCH     SP6,FSTSITE
          GOTO      ALSITE IF NOT EQUAL
.
          PRINT     *45,"ALL SITES";
          GOTO      OPDEPT
.
ALSITE    PRINT     *45,"SITE",FSTSITE;
.
OPDEPT    MATCH     SP3,FSTDEPT
          GOTO      ALDEPT IF NOT EQUAL
.
          PRINT     *59,"ALL DEPARTMENTS",*N,*N;
          GOTO      PRTSEL9     
.
ALDEPT    PRINT     *59,"DEPT",FSTDEPT,*N,*N;
          GOTO      PRTSEL9     
.
IPHEAD    COMPARE   THREE,FSTSYS
          GOTO      OTHEAD IF NOT EQUAL
.
          PRINT     *45,"INPATIENT REPORT",*N,*N;
          GOTO      PRTSEL9
.
OTHEAD    PRINT     *45,"OTHER FINANCIAL REPORT",*N,*N;
.
PRTSEL9   RETURN 
+
.
ENDIT     CHAIN      PGM
          STOP
+
.==============================================================================
.
          INC       STD001IO
          INC       PATSELFN
          INC       PATHSPKY
          INC       PATCODKY
          INC       IBAPASIO/INC
          INC       IBASECIO/INC
          INC       OUTSITIO/INC
          INC       PATCODIO/INC
          INC       PATDSCIO/INC
          INC       PATINVIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATRE1IO/INC
          INC       PATVISIO/INC
          INC       PATHSPIO/INC
          INC       PMSVX1IO/INC
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC
+

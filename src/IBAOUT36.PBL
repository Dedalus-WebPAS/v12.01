.******************************************************************************
.* System         : Outpatients                                               *
.* Program        : IBAOUT36.PBL                                              *
.* Name           : Print Tracer Labels                                       *
.******************************************************************************
.* Date           : 25/06/1993                                                *
.* Author         : Justin 'The Pom' Coates                                   *
.* Function       : To print tracer labels - along the lines of Pulling Lists *
.* Modifications  :                                                           *
.******************************************************************************
.*        V11.02.01 09/02/2022  Thanh T       TSK 0905641                     *
.*                  Recompiled as OUTMA1FD changes                            *
.*        V10.07.01 30/10/2015  Ebon Clements CAR 268970                      *
.*                  Change to use OUTCLIFD index 1 instead of index 2         *
.*        V10.05.01 15/07/2014  Ebon Clements CAR 261630                      *
.*                  Removed port number from temp file name                   *
.*        V9.09.02  04/03/2008  Peter Vela    CAR 162914                      *
.*                  Recompiled for KYOUTCLI - readk of outcliaf               *
.*        V9.09.01  28/07/2007  Jill Habasque CAR 103645                      *
.*                  Recompiled for KYOUTCLI - read of pmshcpaf                *
.*        V5.08.02  19/09/2000  Steve Armstrong   srf 647263                  *
.*                  Recompiled for changes to OUTDSCLN                        *
.*        V5.08.01  29/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.B01 13/03/2000  Steve Armstrong    5.08 Mods                  *
.*                  Mods for changes to OUTCLIFD (effective date)             *
.******************************************************************************
.*        V5.01.02  12/07/1993    Justin 'The Pom' Coates                     *
.*                  change so labels line up                                  *
.*                  and just for Pat, print 3 across the page                 *
.*        V5.01.03  27/05/1994 Ian Rutt                                       *
.*                  Fixed Global Recompile Bugs                               * 
.*        V5.01.04  20/07/1994  Rob Leonard                                   *
.*                  Fixed hospital keyin                                      * 
.******************************************************************************
.
          INC       STD001FD
          INC       IBASEQFD/INC
          INC       OUTBOAFD/INC                 * Outpatients Booking file
          INC       OUTBB1FD/INC                 * Outpatients Booking file
          INC       OUTCLIFD/INC                 * Outpatients Clinics file
          INC       OUTCONFD/INC
          INC       OUTMA1FD/INC                 * Clinic Master file
          INC       OUTSITFD/INC                 * Outpatients Sites file
          INC       PATBAYFD/INC
          INC       PATDO1FD/INC
          INC       PMSHCPFD/INC            * National HCP file
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC                 * Patients Masterfile details
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
.        File layout for temporary index file
.
TEMPA     IFILE     SQL, FIXED=43, KEY="U1-6,7-11,12-14,15-22,23-24"
KEYA      INIT      " 43 U1-6,7-11,12-14,15-22,23-24"
.
.VAR     TYPE       SIZE  PHYS. START  DESCRIPTION
.----    ----       ----  ----- -----  -----------
XCLIN     DIM       6       6       1  Clinic Id 
XSTIM     DIM       5       5       7  Clinic Start time 
XBAYC     DIM       3       3      12  Bay Code
XURNO     DIM       8       8      15  U/R Number
XCNTR     DIM       2       2      23  Unique counter
XCTYP     DIM       6       6      25  Clinic Type 
XDOFW     DIM       1       1      31  Clinic Day of Week 
XVISTYPE  DIM       3       3      32  Visit type code (Cat CV)
XVSDATE   DIM       8       8      35  Appointment date 
.         End of record            43
.
.         program variables
.
CFNAMEA   DIM       8
CLINIC    DIM       6
CMDLINE   DIM       50
DATE      DIM       8
DADDOPT   FORM      1
DDAY      DIM       9
DIM40     DIM       40
DIM6      DIM       6
HOSPITAL  DIM       3
IBCNMHOS  FORM      1
LABELCNT  FORM      1         * number of labels read
LST2UR    DIM       2
LINE1     DIM       30
LINE2     DIM       30
LINE3     DIM       30
LINE4     DIM       30
LINE1A    DIM       30
LINE1B    DIM       30
LINE1C    DIM       30
LINE2A    DIM       30
LINE2B    DIM       30
LINE2C    DIM       30
LINE3A    DIM       30
LINE3B    DIM       30
LINE3C    DIM       30
LINE4A    DIM       30
LINE4B    DIM       30
LINE4C    DIM       30
NUMURS    FORM      8
SITE      DIM       6
.
.         program constants
.
PRFIXA    INIT      "out36a"
Z20       INIT      "zzzzzzzzzzzzzzzzzzzz"
.
PRGID     INIT      "IBAOUT36"
PRGNAM    INIT      "Print Tracer Labels"
VERSION   INIT      "V12.01.00"
.
.*********************************************************************
.         mainline code
.*********************************************************************
ML0000    CALL      INIT0000
ML1000    CALL      OPTN0000                * run program ??
          BRANCH    EXIT,ML9999             * exit
          BRANCH    OPTION,ML2000,ML3000
          GOTO      ML1000
.
ML2000    CALL      OUTS0000                * outpatient single clinic
          GOTO      ML1000
.
ML3000    CALL      OUTA0000                * outpatient all clinics
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.*********************************************************************
.         initilisation
.*********************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P64:24,"patbaysf";
          OPEN      PATBAYS2,"patbaysf"
          DISPLAY   *P56:24,"Opening outsitaf";
          OPEN      OUTSITA1,"outsitaf"
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
          DISPLAY   *P64:24,"pmshcpaf";
          OPEN      PMSHCPA1,"pmshcpaf"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          IF        IBCNMHOS = 1
            DISPLAY   *P64:24,"pathospf";
            OPEN      PATHSPA1,"pathspaf"
          ENDIF
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,CFNAMEA
.
          MOVE      ONE,CNEWDS
.
INIT9999  RETURN
+
.*********************************************************************
.         run the report ??
.*********************************************************************
OPTN0000  MOVE      ZERO,NUMURS
          HLINE     *G37,2,50,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF:
                    " = Tracer Labels - Single Clinic":
                    *P1:6,*V2LON,TWO,*HOFF:
                    " = Tracer Labels - All Clinics"
.
OPTN1000  KEYIN     *P1:8,"Select option : ",*V2LON,OPTION
.
          MOVE      ONE,EXIT
          COMPARE   ZERO,OPTION
          GOTO      OPTN9999 IF EQUAL       * exit
          MOVE      ZERO,EXIT
          BRANCH    OPTION,OPTN9999,OPTN9999
          BEEP
          GOTO      OPTN1000
.
OPTN9999  RETURN
+
.*********************************************************************
.         Tracer Labels - Single Clinic
.*********************************************************************
OUTS0000  DISPLAY   *P50:2,*V2LON,"- Single Clinic "
          CALL      KCLN0000                * Keyin Clinic details
          BRANCH    EXIT,OUTS9999           * exit
.
          CLOCK     TIME,CTIMEIS
          CALL      CTPI0000
          DISPLAY   *P1:24,*EL,*P10:24,"U/R No:",*P60:24,"Scanning:";
          MOVE      ZERO,NUMURS
          CALL      POPS0000                * process the clinic
          CALL      REPT0000                * print the report
          GOTO      OUTS0000
.
OUTS9999  RETURN
+
.*********************************************************************
.         Tracer Labels - All Clinics
.*********************************************************************
OUTA0000  DISPLAY   *P50:2,*V2LON,"- All Clinics "
          CALL      KSIT0000                * Keyin Site/Date details
          BRANCH    EXIT,OUTA9999
.
          CLOCK     TIME,CTIMEIS
          CALL      CTPI0000
          DISPLAY   *P1:24,*EL,*P10:24,"U/R No:",*P60:24,"Scanning:";
.
.         Loop over all clinics 
.
          PACK      KEY20,SITE,SP20
OUTA1000  CALL      RDSCLIA1
          CALL      RDKCLIA1
          BRANCH    OVRCD,OUTA5000
.
          MATCH     SITE,OCASITE
          GOTO      OUTA5000 IF NOT EQUAL   * different site
.
          DISPLAY   *P1:24,*V2LON,OCACLIN;
.
.         Process the data for this clinic
.
          MOVE      OCACLIN,CLINIC
          CALL      POPS0000
          PACK      KEY20,SITE,CLINIC,Z20
          GOTO      OUTA1000
.
OUTA5000  CALL      REPT0000                * print the report
          GOTO      OUTA0000
.
OUTA9999  RETURN
+
.*********************************************************************
.         do the report
.*********************************************************************
REPT0000  COMPARE   ZERO,NUMURS
          GOTO      REPT9000 IF EQUAL       * nothing found
.
.         Generate the report from the temporary file
.
          CALL      GRPT0000
          GOTO      REPT9100 
.
.         No data found
.
REPT9000  DISPLAY   *P1:24,*EL,*B,"No Data found.  ";
          CALL      HITENTER
.
REPT9100  CALL      DTPI0000
.
REPT9999  RETURN
+
.*********************************************************************
.         Subroutine to get the date 
.*********************************************************************
GDOL0000  DISPLAY   *P1:CVERT,*EL,"Enter Report Date    : "
          CALL      TMRW0000
          MOVE      TWENTY4,CCOL
          MOVE      ONE,CDEFDTE
          CALL      KEYDATE
.
          PACK      DATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",DATE
.
GDOL9999  RETURN
.
.*********************************************************************
.         Subroutine to get tomorrow's date
.*********************************************************************
TMRW0000  MOVE     CDD,DD
          MOVE     CMM,MM
          MOVE     CYY,YY
          MOVE     CCC,CC
.
          CALL     DMYCON                  * Convert to Julian date
.
          ADD      ONE,JULDAY
          MOVE     JULDAY,JWKDAY
          MOVE     JULYR,JWKYER
          MOVE     JULCC,JWKCC
.
          CALL     JULCON    
.
          MOVE     DD,CDAY
          MOVE     MM,CMON
          MOVE     YY,CYEAR
          MOVE     CC,CCENT
          REP      " 0",CYEAR              * incase the year was 2000-2009
.
TMRW9999  RETURN
.
.*********************************************************************
.         Subroutine to open site dependant files
.*********************************************************************
OSDF0000  CLOSE     OUTCLIA1
          PACK      CFNAME,OSTFILE,FILCLIA1
          OPEN      OUTCLIA1,CFNAME
.
          CLOSE     OUTBOKA1
          PACK      CFNAME,OSTFILE,FILBOKA1
          OPEN      OUTBOKA1,CFNAME
.
          CLOSE     OUTBOKA3
          PACK      CFNAME,OSTFILE,FILBOKA3
          OPEN      OUTBOKA3,CFNAME
.
          CLOSE     OUTBOKA4
          PACK      CFNAME,OSTFILE,FILBOKA4
          OPEN      OUTBOKA4,CFNAME
.
          CLOSE     OUTBB1A1
          PACK      CFNAME,OSTFILE,FILBB1A1
          CALL      OPOTBB11
.
          IF         IBCNMHOS = 1
            CLOSE     OUTMA1A1
            PACK      CFNAME,OSTFILE,FILMA1A1
            CALL       OPOTMA11
          ENDIF
.
OSDF9999  RETURN
.
.*********************************************************************
.         Keyin the hospital code
.*********************************************************************
KHOS0000  DISPLAY   *P1:CVERT,"Enter Hospital ID    : "
          MOVE      CVERT,EVERT
          MOVE      "24",ECOL
          MOVE      ZERO,CKYIMAND
          MOVE      SP3,CKYIDEF3
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS9999,KHOS0000
.
          DISPLAY   *P40:EVERT,PTHSNAME
.
KHOS9999  RETURN
.
.*********************************************************************
.         Subroutine to get and validate a single clinic
.*********************************************************************
KCLN0000  KEYIN     *P1:4,*EF:
                    *P1:4,"Enter Site Code      : ",*V2LON,SITE
          PACK      SITE,SITE,SP6
          MATCH     SP6,SITE
          GOTO      KCLN9100 IF EQUAL
.
.         Validate the site code
.
          MOVE      SITE,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD,KCLN7100
.
          DISPLAY   *P40:4,OSTDESC
          CALL      OSDF0000                * open site dependant files
.
KCLN1000  DISPLAY   *P1:6,*EF,"Enter Clinic Code    : "
          MOVE      "24",ECOL
          MOVE      SIX,EVERT
          MOVE      SP6,CKYIDEF6
          MOVE      ZERO,CKYIMAND
          MOVE      SITE,IDDD
.
          CALL      KYOUTCLI
          BRANCH    EXIT,KCLN0000,KCLN9100,KCLN0000
.
          MOVE      OCACLIN,CLINIC
          CALL      GCLD0000
          DISPLAY   *P1:8,"Clinic Description   : ",*V2LON,OCADESC
          MOVE      TEN,CVERT
          CALL      GDOL0000
.
          MOVE      TEN2,CVERT
          CALL      ADDN0000                * additions Y/N
.
          IF        IBCNMHOS = 1
            MOVE      TEN4,CVERT
            CALL      KHOS0000
            BRANCH    EXIT,KCLN0000
            MOVE      KEY3,HOSPITAL
          ENDIF
.
          CALL      CONTQST
          BRANCH    CEXIT,KCLN5000,KCLN0000,KCLN9100
.
KCLN5000  MOVE      ZERO,EXIT
          GOTO      KCLN9999
.
KCLN7100  DISPLAY   *P1:24,*B,"Invalid Site Code.  ",*EL;
          CALL      HITENTER
          GOTO      KCLN0000
.
KCLN7200  DISPLAY   *P1:24,*B,"Invalid Clinic Code.  ",*EL;
          CALL      HITENTER
          GOTO      KCLN1000
.
KCLN9100  MOVE      ONE,EXIT
.
KCLN9999  RETURN
+
.*********************************************************************
.         Subroutine to get and validate a single site
.*********************************************************************
KSIT0000  KEYIN     *P1:4,*EF:
                    *P1:4,"Enter Site Code      : ",*V2LON,SITE
.
          PACK      SITE,SITE,SP6
          MATCH     SP6,SITE
          GOTO      KSIT9100 IF EQUAL
.
.         Validate the clinic code
.
          MOVE      SITE,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD,KSIT7100
.
          CALL      OSDF0000                * open site dep. files
          DISPLAY   *P1:6,"Site Description     : ",*V2LON,OSTDESC
.
          MOVE      EIGHT,CVERT
          CALL      GDOL0000
.
          MOVE      TEN,CVERT
          CALL      ADDN0000                * additions Y/N
.
          IF        IBCNMHOS = 1
            MOVE      TEN2,CVERT
            CALL      KHOS0000
            BRANCH    EXIT,KSIT0000
            MOVE      KEY3,HOSPITAL
          ENDIF
.
          CALL      CONTQST
          BRANCH    CEXIT,KSIT9000,KSIT0000,KSIT9100
.
KSIT7100  DISPLAY   *P1:24,*B,"Invalid Site Code.  ",*EL;
          CALL      HITENTER
          GOTO      KSIT0000
.
KSIT9000  MOVE      ZERO,EXIT 
          GOTO      KSIT9999
.
KSIT9100  MOVE      ONE,EXIT 
KSIT9999  RETURN
+
.*********************************************************************
.         Subroutine to get the description for a clinic
.*********************************************************************
GCLD0000  MATCH     SP6,OCADOCT
          GOTO      GCLD9999 IF EQUAL
.
.         Use the doctor's code from the clinic file to get the doctor's name
.
          MOVE      OCADOCT,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD,GCLD2000
.
.         Pack up the doctors name
.
          CLEAR     OCADESC
          MOVE      DTITL,PACTITLE
          MOVE      DGNAME,PACGNAME
          MOVE      DSNAME,PACSNAME
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,OCADESC
          GOTO      GCLD9999
.
.         Doctor code not found
.
GCLD2000  MOVE      "UNKNOWN DOCT. ",OCADESC
.
GCLD9999  RETURN
+
.*********************************************************************
.         Subroutine to ask if Additions only are wanted
.*********************************************************************
ADDN0000  KEYIN     *P1:CVERT,*EL,"Additions Only (Y/N) : ",*V2LON,ANS
          REP       UPPLOW,ANS
          CMATCH    ANSY,ANS
          GOTO      ADDN1000 IF EQUAL
          CMATCH    ANSN,ANS
          GOTO      ADDN2000 IF EQUAL
          BEEP
          GOTO      ADDN0000
.
ADDN1000  MOVE      ONE,DADDOPT             * additions only
          DISPLAY   *P24:CVERT,*V2LON,"Yes";
          GOTO      ADDN9999
.
ADDN2000  MOVE      ZERO,DADDOPT            * any one
          DISPLAY   *P24:CVERT,*V2LON,"No ";
.
ADDN9999  RETURN
+
.*********************************************************************
.         Subroutine to get the bay code for a given U/R number
.*********************************************************************
GBAY0000  MOVE      SP3,BAYCODE
          MOVE      SP3,XBAYC               * clear bay code
          UNPACK    XURNO,DIM6,LST2UR       * get last 2 U/R digits
          REP       " 0",LST2UR
.
.         Check for a record in the bay code file
.
          PACK      KEY5,LST2UR,SP3
          CALL      RDSBAYS2
          CALL      RDKBAYS2
          BRANCH    OVRCD,GBAY9999
          MATCH     LST2UR,BAYUR
          GOTO      GBAY9999 IF NOT EQUAL
.
          MOVE      BAYCODE,XBAYC
.
GBAY9999  RETURN
.
.*********************************************************************
.         Subroutine to process the temporary file
.*********************************************************************
GRPT0000  DISPLAY   *P1:24,*EL,"Printing Labels..."
          MOVE      "60",CLNO
          MOVE      ZERO,CPAGENO
          CALL      CTLL0000                * clear the label lines
          MOVE      SP30,KEY24
          CALL      RDSTEMP1
.
GRPT1000  CALL      RDKTEMP1
          BRANCH    OVRCD,GRPT5000
.
          DISPLAY   *P19:24,*V2LON,XCLIN,SP1,XURNO;
          MOVE      XURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,GRPT1000
.
.         get the details and store them
.
GRPT2000  ADD       ONE,LABELCNT            * add to number of labels
          COMPARE   LABELCNT,THREE
          GOTO      GRPT5000 IF LESS        * on fourth label so print
.
.         set up and store the label details
.
          MOVE      XDOFW,FORM1
          LOAD      DDAY,FORM1,DMON,DTUE,DWED,DTHU,DFRI,DSAT,DMON
          UNPACK    XVSDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PACK      LINE1,PSNAME,SP1,PURNO,SP30
          PACK      LINE2,PGNAME,SP30
          PACK      LINE3,XCTYP,SP2,XCLIN,SP30
          PACK      LINE4,CPCDATE,SP1,DDAY,SP1,HOSPITAL,SP1,XVISTYPE,SP30
.
          STORE     LINE1,LABELCNT,LINE1A,LINE1B,LINE1C
          STORE     LINE2,LABELCNT,LINE2A,LINE2B,LINE2C
          STORE     LINE3,LABELCNT,LINE3A,LINE3B,LINE3C
          STORE     LINE4,LABELCNT,LINE4A,LINE4B,LINE4C
          GOTO      GRPT1000
.
.         print the labels - 9 lines per label
.
GRPT5000  PRINT     *1,LINE1A,SP1,LINE1B,SP1,LINE1C
          PRINT     *1,LINE2A,SP1,LINE2B,SP1,LINE2C
          PRINT     *1,LINE3A,SP1,LINE3B,SP1,LINE3C
          PRINT     *1,LINE4A,SP1,LINE4B,SP1,LINE4C
          PRINT     *1,SP30
          PRINT     *1,SP30
          PRINT     *1,SP30
          PRINT     *1,SP30
          PRINT     *1,SP30
          BRANCH    OVRCD,GRPT9999          * end if file
.
          CALL      CTLL0000                * clear label details
          GOTO      GRPT2000
.
GRPT9999  RETURN
+
.*********************************************************************
.         Subroutine to process the booking file
.*********************************************************************
POPS0000  PACK      KEY28,SITE,CLINIC,DATE,SP30
          CALL      RDSBOKA1
POPS1000  CALL      RDKBOKA1
          BRANCH    OVRCD,POPS9999
.
          MATCH     SITE,OBASITE
          GOTO      POPS9999 IF NOT EQUAL
.
          MATCH     CLINIC,OBACLIN
          GOTO      POPS9999 IF NOT EQUAL
.
          MATCH     DATE,OBADATE
          GOTO      POPS9999 IF NOT EQUAL
.
          DISPLAY   *P70:24,OBAOUTNO;
          COMPARE   ONE,OBASTAT
          GOTO      POPS1000 IF NOT EQUAL   * not a booking
.
.         Check if we only want additions ( Reject if DADDOPT=1 & OBAPULL=1 )
.
          MOVE      ZERO,FORM1
          LOAD      FORM1,DADDOPT,OBAPULL
          BRANCH    FORM1,POPS1000
.
          MOVE      OBAOUTNO,KEY8
          CALL      RDBOKB1
          BRANCH    OVRCD,POPS1000 
.
.-------   If we are using multi hospitals we need to check the hospital ----
.-------     stored on the clinic master                                ----
.
          IF         IBCNMHOS = 1
            PACK       KEY18,OBASITE,OBACLIN,OBADAY,OBASTRT
            CALL       RDMASA1
            BRANCH     OVRCD,POPS1000
.
            MATCH      HOSPITAL,OTMAHOSP
            GOTO       POPS1000 IF NOT EQUAL
          ENDIF
.
.         Write a record to the temporary file
.
          MOVE      OBAURNO,XURNO
          MOVE      OBACLIN,XCLIN
          MOVE      OBACTYP,XCTYP
          MOVE      OBASTRT,XSTIM
          MOVE      OBADAY,XDOFW
          MOVE      OBAVISIT,XVISTYPE
          MOVE      OBADATE,XVSDATE
          CALL      GBAY0000
.
          MOVE      ZERO,FORM2
.
POPS2000  ADD       ONE,FORM2
          MOVE      FORM2,XCNTR
          PACK      KEY24,XCLIN,XSTIM,XBAYC,XURNO,XCNTR
          CALL      RATEMP1
          COMPARE   ZERO,OVRCD
          GOTO      POPS2000 IF EQUAL       * already exists
.
          CALL      WRTEMP1
          DISPLAY   *P18:24,*V2LON,XURNO;
          ADD       ONE,NUMURS
          GOTO      POPS1000
.
POPS9999  RETURN
+
.*********************************************************************
.         clear the label lines
.*********************************************************************
CTLL0000  MOVE      ONE,FORM2
          WHILE     FORM2 <= 12
            STORE     SP30,FORM2,LINE1A,LINE1B,LINE1C:
                                 LINE2A,LINE2B,LINE2C:
                                 LINE3A,LINE3B,LINE3C:
                                 LINE4A,LINE4B,LINE4C
            ADD       ONE,FORM2
          DO
          MOVE      ZERO,LABELCNT           * clear label count
CTLL9999  RETURN
+
.****************************************************************************
.         Delete the temporary index file
.****************************************************************************
DTPI0000  CLOSE     TEMPA
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    CFNAMEA,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
DTPI9999  RETURN
.
.*********************************************************************
.         Create the temporary index file
.*********************************************************************
CTPI0000  CALL      DTPI0000
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    CFNAMEA,CMDLINE
          APPEND    KEYA,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPA,CFNAMEA
CTPI9999  RETURN
+
.*********************************************************************
.        I/O for the temporary index file
.*********************************************************************
RDSTEMP1  READ      TEMPA,KEY24;;
          RETURN
.
RATEMP1   MOVE      ZERO,OVRCD
          READ      TEMPA,KEY24;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RDKTEMP1  MOVE      ZERO,OVRCD
          READKS    TEMPA;XCLIN,XSTIM,XBAYC,XURNO,XCNTR:
                          XCTYP,XDOFW,XVISTYPE,XVSDATE
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   WRITE     TEMPA,KEY24;XCLIN,XSTIM,XBAYC,XURNO,XCNTR:
                            XCTYP,XDOFW,XVISTYPE,XVSDATE
          RETURN
+
          INC       STD001IO
          INC       KYOUTCLI
          INC       PATHSPKY
          INC       OUTDSCLN
          INC       TFILENAM
.
          INC       IBASEQIO/INC
          INC       OUTBOAIO/INC
          INC       OUTBB1IO/INC
          INC       OUTCLIIO/INC
          INC       OUTMA1IO/INC                 * Clinic Master file
          INC       OUTSITIO/INC
          INC       PATHSPIO/INC
          INC       PATBAYIO/INC
          INC       PATMA1IO/INC
          INC       PATDO1IO/INC
          INC       PMSHCPIO/INC            * National HCP file
          INC       WEBERRIO/INC
.............................................................................

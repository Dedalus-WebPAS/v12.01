.***************************************************************************
.* System    :    Inpatients                                               *
.* Program   :    IBAPMS65                                                 *
.* Desc      :    Patient Days Stay by Ward/Department                     *
.***************************************************************************
.* Date      :    09/05/90                                                 *
.* Author    :    Graeme Williams                                          *
.* Function  :                                                             *
.*                                                                         *
.* Mods      :                                                             *
.*        V12.00.01 21/05/2025 Nikitha B       TSK 0955096                 *
.*                  Alphanumeric changes                                   *
.*        V10.10.01 18/04/2017  J.Tan          TSK 0816285                 *
.*                  Fixed Separation bed days for patient went on leave    *
.*                  and return all on same ward for whole visit            *
.*        V10.05.01 22/09/2014  J.Tan          CAR 294962                  *
.*                  Added input Hospital id for multi hospital             *
.*        V10.03.01 02/01/2013 Patrick Adair  CAR 261630                   *
.*                  Removed port number from temp file name                *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                   *
.*                  Added pmsvx1af                                         *
.*        V9.12.01  02/03/2010  Mike Laming   CAR 216878                   *
.*                  Mod to also bypass "Pre-Admissions" at PROC1500        *
.*        V9.04.00  22/10/2004  Lina Vo  CAR 54397                         *
.*                  Ported from V5.12                                      *
.*        V5.08.01  28/08/2000  Caleb Sharman                              *
.*                  Changed Health Fund variables to be 8 chars            *
.*                V5.07.01  22/06/1999 J.Tan                               *
.*                Fixed printing the grand total                           *
.*                V5.07.B01 20/05/1999 Steve Armstrong    Y2K 5.07 mods    *
.***************************************************************************
.*                V5.01.02 30/05/90 J.Tan       SRF 105058                 *
.*                Mods to keyin dates instead of using period file         *
.*           :    V5.01.03 06/09/91 J.Tan       SRF 109916                 *
.*                Mods to use day file, added separation bed days etc.     *
.*           :    V5.01.04 07/04/92 J.Tan                                  *
.*                Fixed separation bed days                                *
.*           :    V5.04.01 26/02/97 J.Tan       SRF 618876                 *
.*                Fixed printing the heading of the last page              *
.***************************************************************************
.
          INC       STD001FD
          INC       PATHSPFD/INC
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       PATCOMM/INC
          INC       PATCODFD/INC
          INC       PATDSCFD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATTRNFD/INC
          INC       PATWR1FD/INC
          INC       PATDAYFD/INC
          INC       PATMA1FD/INC
          INC       PMSPX2FD/INC
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
.------------------------------------------------------------------------------
.         Temporary index file 1   - Totals for report
.
WORK1     IFILE     SQL, FIXED=23, KEY="U1-3,4-6"
.
.Name     Type     Size    Physical   Start       Description
.----     ----     ----    --------   -----       -----------
XWARD     DIM       3         3         1         Ward Code
.                                                    zzz = Grand Totals
XDEPT     DIM       3         3         4         Dept Code
XNPAT     FORM      6         4         7         Number of patients
XDCASE    FORM      6         4         11        Number of day cases
XSBDAYS   FORM      6         4         15        Separation bed days
XPBDAYS   FORM      6         4         19        Patient bed days
.End of record                          23
.------------------------------------------------------------------------------
.         Temporary index file 2   - Used to check number of patients
.
WORK2     IFILE     SQL, FIXED=15, KEY="U1-3,4-6,7-14"
.
.Name     Type     Size    Physical   Start       Description
.----     ----     ----    --------   -----       -----------
.XWARD    DIM       3         3         1         Ward Code
.                                                    zzz = Grand Totals
.XDEPT    DIM       3         3         4         Dept Code
XADMN     DIM       8         8         7         Admission number
.End of record                          15
.------------------------------------------------------------------------------
.         Temporary index file 3   - Used to get the patients on report
.
WORK3     IFILE     SQL, FIXED=9, KEY="U1-8"
.
.Name     Type     Size    Physical   Start       Description
.----     ----     ----    --------   -----       -----------
.XADMN    DIM       8         8         1         Admission number
.End of record                          9
.------------------------------------------------------------------------------
.
BEDDAYS   FORM      6
.
CDYSDAYS  FORM      6
CDYSFDTE  DIM       8
CDYSTDTE  DIM       8
CDYSYEAR  FORM      2
CMDLINE   DIM       50
CODEU5    INIT      "U5"      * Department category
CPERMTH   DIM       6
.
DEFDSC    INIT      " : Unk. Code      "
DAYCASE   FORM      1
DCLOS     FORM      6.2
.
ENDDATE   DIM       8
.
FORM6     FORM      6
FNAME1    DIM       8
FNAME2    DIM       8
FNAME3    DIM       8
FROMDATE  DIM       8
.
GNDFLAG   FORM      1
JYEAR     FORM      2
HOSPID    DIM       3
HOSPNAME  DIM       50
IBCNMHOS  FORM      1
.
NDCLOS    FORM      6.2
NBRDAYS   FORM      6
.
OTDATE    DIM       8
PATNUM    FORM      6
PRNTDESC  DIM       15
.
SAVDATE   DIM       8
SEPDAYS   FORM      6
SUBTOT    INIT      "Total for "
SYEAR     DIM       4           CCYY of the fromdate of the period
SDAYMON   DIM       4           MMDD of the fromdate of the period
SKEY30    DIM       30
.
TNPAT     FORM      6
TDCASE    FORM      6
TSBDAYS   FORM      6
TPBDAYS   FORM      6
TODATE    DIM       8
TODAY     DIM       8
TYEAR     DIM       4           CCYY of the todate of the period
TDAYMON   DIM       4           MMDD of the todate of the period
.
WDATE1    DIM       8
WDATE2    DIM       8
Z30       INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
.
PRGID     INIT      "IBAPMS65"
PRGNAM    INIT      "Patient Days Stay By Ward/department"
VERSION   INIT      "V12.01.00"
.
. *****************************************************************************
. * ML0000 : Mainline code                                                    *
. *****************************************************************************
.
ML0000    CALL      INIT0000                 * Initialisation
.
ML0100    CALL      KSTR0000                 * Keyin the start date
          BRANCH    EXIT,ML9999              * Nothing input
.
          CALL      KEND0000                 * Keyin the ending date
          BRANCH    EXIT,ML0100              * Nothing input
.
          CALL      KHOS0000                 * Keyin Hospital id
          BRANCH    EXIT,ML0100              * Nothing input
.
          CALL      CONT0000                 * OK to continue
          BRANCH    EXIT,ML0100              * no
.
          CALL      CRET0000                 * Create temporary files
          CALL      GINP0000                 * Get the list of inpatients
          CALL      PROC0000                 * Process the inpatients
          CALL      PRNT0000                 * Print the report
          CALL      KILL0000                 * Delete temporary files
          GOTO      ML0100
.
ML9999    CHAIN     PGM                      * Return from whence thou comest
.
. *****************************************************************************
. * INIT0000 : Initialisation routine                  Called by : ML0000     *
. *****************************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,FIFTY9;*27,CPERMTH
          CALL      IBACLOCK
          PACK      TODAY,CCC,CYY,CMM,CDD
          REP       " 0",TODAY
.
.         Set up the temporary index file names
.
          DISPLAY   *P1:6,"From Date : ":
                    *P1:7,"To   Date : ";
.
INIT9999  RETURN
.
. *****************************************************************************
. * KSTR0000 : Keyin the start date                    Called by : ML0000     *
. * Returns  : EXIT = 0   if date was input                                   *
. *                 = 1   if a nothing was input                              *
. *            FROMDATE   starting date (CCYYMMDD)                            *
. *****************************************************************************
.
KSTR0000  MOVE      ZERO,EXIT
          DISPLAY   *P13:6,*EL:
                    *P13:7,*EF;
.
          MOVE      SIX,CVERT                * Line for keyin of date
          MOVE      TEN3,CCOL                * Column for keyin of date
          MOVE      CVERT,EVERT              * Column for error message
          MOVE      "40",ECOL                * Column for error message
          MOVE      ZERO,CHIGHLT             * No highlight
          MOVE      ONE,CDEFDTE
.
          UNPACK    SP6,CDAY,CMON,CYEAR      * Initialise date
          MOVE      SP8,FROMDATE
          MOVE      CCC,CCENT                * Initialise century
.
          CALL      KEYDATE                  * Key in the date
          BRANCH    OVRCD,KSTR9000
.
          PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",FROMDATE
          MATCH     FROMDATE,TODAY
          GOTO      KSTR9999 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Date May Not be Greater than Today's ":
                    "Date.  ";
          CALL      HITENTER
          GOTO      KSTR0000
.
KSTR9000  MOVE      ONE,EXIT
.
KSTR9999  RETURN
.
. *****************************************************************************
. * KEND0000 : Keyin the end date                      Called by : ML0000     *
. * Returns  : EXIT = 0   if date was input                                   *
. *                 = 1   if a nothing was input                              *
. *            ENDDATE    Endding date (CCYYMMDD)                             *
. *****************************************************************************
.
KEND0000  MOVE      ZERO,EXIT
.
          MOVE      SEVEN,CVERT               * Line for keyin of date
          MOVE      TEN3,CCOL                * Column for keyin of date
          MOVE      CVERT,EVERT              * Column for error message
          MOVE      "40",ECOL                * Column for error message
          MOVE      ZERO,CHIGHLT             * No highlight
          MOVE      ONE,CDEFDTE
.
          MOVE      SP8,ENDDATE
.
          CALL      KEYDATE                  * Key in the date
          BRANCH    OVRCD,KEND9000
.
          PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDATE
          MATCH     ENDDATE,TODAY
          GOTO      KEND2000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Date May Not be Greater than Today's ":
                    "Date.  ";
          CALL      HITENTER
          GOTO      KEND0000
.
KEND2000  MATCH     FROMDATE,ENDDATE
          GOTO      KEND9999 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"To Date May Not be Less than From ":
                    "Date.  ";
          CALL      HITENTER
          GOTO      KEND0000
.
KEND9000  MOVE      ONE,EXIT
.
KEND9999  RETURN
.
. *****************************************************************************
.        KHOS0000: Keyin hospital ID
. *****************************************************************************
KHOS0000 MOVE       SP10,HOSPID
         MOVE       ZERO,EXIT
         COMPARE    ONE,IBCNMHOS
         GOTO       KHOS9999 IF NOT EQUAL
.
         DISPLAY    *P1:16,*EL,"Hospital Id : ";
         MOVE       TEN5,ECOL
         MOVE       "16",EVERT
         MOVE       SP3,CKYIDEF3
         MOVE       ZERO,CKYIMAND           * Not Mandatory
         OPEN       PATHSPA1,"pathspaf"
.
         CALL       PATHSPKY
         BRANCH     EXIT,KHOS1000,KHOS2000
         MOVE       PTHSHOSP,HOSPID
         MOVE       PTHSNAME,HOSPNAME
         MOVE       ZERO,EXIT
         GOTO       KHOS9999
.
KHOS1000 MOVE       "All Hospitals",HOSPNAME
         MOVE       SP10,PTHSHOSP
         MOVE       ZERO,EXIT
         GOTO       KHOS9999
.
KHOS2000 MOVE       ONE,EXIT
KHOS9999 RETURN
+
. *****************************************************************************
. * CONT0000 : Ok. to continue (Y/N)  and Add one day to the ending date      *
. *****************************************************************************
.
CONT0000  MOVE      ZERO,EXIT
          DISPLAY   *P1:24,*EL,"Ok to Continue (":
                    *V2LON,ANSY,*HOFF,SLASH,*V2LON,ANSN,*HOFF,") ? ";
.
CONT1000  KEYIN     *P24:24,"_",*P24:24,*V2LON,ANS;
.
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
.
          MATCH     ANS,ANSY
          GOTO      CONT2000 IF EQUAL
.
          MATCH     ANS,ANSN
          GOTO      CONT9000 IF EQUAL
.
          BEEP
          GOTO      CONT1000
.
.         We want TODATE to be the day after the ending date in the range.
.
CONT2000  UNPACK    ENDDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,CC
          MOVE      CYEAR,YY
          MOVE      CMON,MM
          MOVE      CDAY,DD
.
          CALL      DMYCON                   * Convert to date to julian
.
          ADD       ONE,JULDAY               * Add one to the to date
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
.
          CALL      JULCON                   * Convert back to DD,MM,YY
.
          PACK      TODATE,CC,YY,MM,DD       * Set up the to date
          REP       " 0",TODATE
.
.         Set up report printing parameters
.
          MOVE      ZERO,CPAGENO             * Start from page one
          MOVE      ZERO,GNDFLAG
          CLOCK     TIME,CTIMEIS             * Get the starting time
          CALL      HEAD0000                 * Print the 1st page heading
.
          MOVE      ZERO,EXIT                * Valid date input
          GOTO      CONT9999
.
.         Nothing input
.
CONT9000  MOVE      ONE,EXIT
.
CONT9999  RETURN
.
. *****************************************************************************
. * CRET0000 : Create the temporary index files        Called by : ML0000     *
. * Parameter: FNAME1     file name of index file 1                           *
. *            FNAME2     file name of index file 2                           *
. *            FNAME3     file name of index file 3                           *
. * Returns  : WORK1      open 1st index file                                 *
. *            WORK2      open 2nd index file                                 *
. *            WORK3      open 3rd index file                                 *
. *****************************************************************************
.
CRET0000  CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAME1
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAME2
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAME3
.
          CALL      KILL0000                 * Make sure the files are gone
.
          CLEAR     CMDLINE                  * Clear the command line
          APPEND    "isbuild ",CMDLINE       * Set up the command to
          APPEND    FNAME1,CMDLINE           * create the 1st temporary
          APPEND    " 23 U1-3,4-6",CMDLINE   * index file.
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID           * Create the 1st file
.
          CLEAR     CMDLINE                  * Clear the command line
          APPEND    "isbuild ",CMDLINE       * Set up the command to
          APPEND    FNAME2,CMDLINE           * create the 2nd temporary file
          APPEND    " 15 U1-3,4-6,7-14",CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID           * Create the 2nd file
.
          CLEAR     CMDLINE                  * Clear the command line
          APPEND    "isbuild ",CMDLINE       * Set up the command to
          APPEND    FNAME3,CMDLINE           * create the 3rd temporary file
          APPEND    " 9 U1-8",CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID           * Create the 3rd file
.
          OPEN      WORK1,FNAME1             * Open the 1st file
          OPEN      WORK2,FNAME2             * Open the 2nd file
          OPEN      WORK3,FNAME3             * Open the 3rd file
.
CRET9999  RETURN
.
. *****************************************************************************
. * KILL0000 : Delete the temporary index files        Called by : ML0000     *
. * Parameter: FNAME1     file name of index file 1                CRET0000   *
. *            FNAME2     file name of index file 2                           *
. *            FNAME3     file name of index file 3                           *
. *****************************************************************************
.
KILL0000  CLOSE     WORK1                    * Make sure the 1st file is closed
          CLOSE     WORK2                    * Make sure the 2nd file is closed
          CLOSE     WORK3                    * Make sure the 3rd file is closed
.
          CLEAR     CMDLINE                  * Clear the command line
          APPEND    "iserase ",CMDLINE       * Set up the command to
          APPEND    FNAME1,CMDLINE           * delete the 1st temporary file
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID           * Delete the 1st file
.
          CLEAR     CMDLINE                  * Clear the command line
          APPEND    "iserase ",CMDLINE       * Set up the command to
          APPEND    FNAME2,CMDLINE           * delete the 2nd temporary file
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID           * Delete the 2nd file
.
          CLEAR     CMDLINE                  * Clear the command line
          APPEND    "iserase ",CMDLINE       * Set up the command to
          APPEND    FNAME3,CMDLINE           * delete the 3rd temporary file
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID           * Delete the 3rd file
.
KILL9999  RETURN
.
. *****************************************************************************
. * GINP0000 : Put the inpatients required for the     Called by : ML0000     *
. *            report into WORK3                                              *
. *****************************************************************************
.
GINP0000  DISPLAY   *P1:24,*EL,*P10:24,"Adm. No :":
                    *P58:24,"Scanning:";
.
          UNPACK    ENDDATE,TYEAR,TDAYMON
          REP       " 0",TYEAR
.
          UNPACK    FROMDATE,SYEAR,SDAYMON
          REP       " 0",SYEAR
.
GINP1000  CLOSE     PATDAYA1
          PACK      CFNAME,FILDAYA1,SYEAR
.
          TRAP      CCRT0000 IF IO
          OPEN      PATDAYA1,CFNAME
          TRAPCLR   IO
.
          PACK      KEY12,SDAYMON,SP10
          CALL      RSPTDAY1
GINP1100  CALL      RKPTDAY1
          BRANCH    OVRCD,GINP3000
.
.         Display day and month that is being processd
.
          UNPACK    PTDYDATE,XMON,XDAY
          DISPLAY   *P1:24,XDAY,SLASH,XMON:
                    *P70:24,*EL,PTDYADMN;
.
          MATCH     TYEAR,SYEAR
          GOTO      GINP2000 IF NOT EQUAL
.
          MATCH     PTDYDATE,TDAYMON
          GOTO      GINP9000 IF LESS
.
.         Patient was in during the period
.
GINP2000  DISPLAY   *P28:24,*V2LON,PTDYADMN;
.
.         Only need to write to the temporary file if the admission
.         number is not already there
.
          MOVE      PTDYADMN,KEY8
          CALL      RDWORK3                  * Check if already on file
          COMPARE   ZERO,OVRCD
          GOTO      GINP1100 IF EQUAL        * Already on file
.
          MOVE      PTDYADMN,KEY8
          CALL      WRWORK3                  * Post to the 3rd index file.
          GOTO      GINP1100
.
GINP3000  MATCH     TYEAR,SYEAR
          GOTO      GINP9000 IF NOT LESS
.
          MOVE      SYEAR,FORM4
          ADD       ONE,FORM4
          MOVE      FORM4,SYEAR
          REP       " 0",SYEAR
          MOVE      "0101",SDAYMON
          GOTO      GINP1000
.
.         Finished getting the list of inpatients in the period.
.
GINP9000  DISPLAY   *P1:24,*EL;
.
GINP9999  RETURN
.
.**********************************************************************
.*                         CCRT0000                                   *
.*        Create the file that is needed to write to                  *
.**********************************************************************
.
CCRT0000  CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    CFNAME,CMDLINE
          APPEND    " 13 U1-4,5-12 ",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      PATDAYA1,CFNAME          
.
CCRT9999  RETURN
.
. *****************************************************************************
. * PROC0000 : Process the inpatients                  Called by : ML0000     *
. *****************************************************************************
.
PROC0000  DISPLAY   *P10:24,"Processing : ";
          MOVE      SP8,KEY8
          CALL      RDSWORK3                 * Position at the start of file
PROC1000  CALL      RDKWORK3                 * Get the next record
          BRANCH    OVRCD,PROC9999           * Finished processing
.
          DISPLAY   *P24:24,*V2LON,XADMN;
.
.         Get the inpatient details
.
          MOVE      XADMN,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,PROC1000
.
          IF         IBCNMHOS=1
            MATCH     SP70,HOSPID
            IF        !@EQUAL
              MATCH     PMVXMHOS,HOSPID  * Check hospital id
              GOTO      PROC1000 IF NOT EQUAL
            ENDIF
          ENDIF
.
.         Check if patient is cancelled or not
.                                                    * start changes  *C-216878
.                                           * bypass Pre-Admis or Cancelled
PROC1500  IF        ASTAT = 1 | ASTAT = 5
            GOTO      PROC1000
          ENDIF
.                                                    * end            *C-216878
.
.         Check if the patient on the master file
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,PROC1000
.
          MATCH     FROMDATE,ADATE           * Admission date within period ?
          GOTO      PROC4000 IF LESS
.
          MATCH     ADATE,TODATE
          GOTO      PROC1000 IF LESS
.
PROC4000  MOVE      SP6,DDATE                * Default discharge date
          COMPARE   THREE,ASTAT              * Patient discharged ?
          GOTO      PROC5000 IF NOT EQUAL    * No.
.
.         Get the discharged date.
.
          MOVE      XADMN,KEY8
          CALL      RDDSCH1
          BRANCH    OVRCD,PROC1000
.
          MATCH     FROMDATE,DDATE
          GOTO      PROC1000 IF LESS
.
.         Process this patient
.
PROC5000  CALL      PPAT0000                 * Process a single patient
          GOTO      PROC1000                 * Get the next patient
.
PROC9999  RETURN
.
. *****************************************************************************
. * PPAT0000 : Process a single patient                Called by : PROC0000   *
. *****************************************************************************
.
PPAT0000  MOVE      ZERO,PATNUM              * Number of patients
          MOVE      ZERO,DAYCASE             * Initialise no of daycases
          MOVE      ZERO,SEPDAYS             * Separation bed days
          MOVE      ZERO,BEDDAYS             * Patient bed days
          MOVE      SP10,SAVDATE
          MOVE      SP10,OTDATE
.
          MATCH     ADATE,DDATE              * Day case patient ?
          GOTO      DPAT0000 IF EQUAL        * Yes. Treat specially
.

.         Loop through the transfer file, and accumulate details for
.         each ward this patient has been in.
.
          MOVE      SP3,STWARD               * Saved ward
          MOVE      SP6,STDATE               * Saved date
          MOVE      SP1,STMOVE               * Saved movement type
          PACK      KEY30,AADMNO,SP30        * Position at start of patient
          CALL      RDSTRAN2
PPAT1000  CALL      RDKTRAN2                 * Get the next record
          BRANCH    OVRCD,PPAT8000           * Oops. Read too far
.
          MATCH     TADMN,AADMNO             * Oops. Read too far
          GOTO      PPAT8000 IF NOT EQUAL
.
.         Check for a "R" without an "L"
.
          MATCH     ANSR,TMOVE
          GOTO      PPAT1100 IF NOT EQUAL
.
          MATCH     ANSL,STMOVE
          GOTO      PPAT1100 IF EQUAL
.
          PRINT     *1,"Error: Patient ",AADMNO:
                    " has a corrupted transfer file"
          ADD       ONE,CLNO
.
.         Process the type of movement
.
PPAT1100  MOVE      TMOVE,STMOVE             * Save the movement type
.
          MATCH     ANSA,TMOVE               * Admission record.
          GOTO      PPAT1500 IF EQUAL        * Save details
.
          MATCH     ANSR,TMOVE               * Return from leave record.
          GOTO      PPAT2200 IF EQUAL        * Save details
.
          MATCH     ANSL,TMOVE               * Leave record.
          GOTO      PPAT5000 IF EQUAL        * Process details
.
          MATCH     ANSD,TMOVE               * Discharge record.
          GOTO      PPAT6000 IF NOT EQUAL    * Process details
.
.         Discharge record
.
          MOVE      TDATE,OTDATE             * Save date to workout sep.beddays
          MATCH     FROMDATE,TDATE
          GOTO      PPAT9999 IF LESS
          MATCH     TODATE,TDATE
          GOTO      PPAT8500 IF LESS
          MOVE      TODATE,TDATE
          GOTO      PPAT8500
.
.         Admission record
.
PPAT1500  MOVE      TDATE,SAVDATE
          MOVE      TDATE,OTDATE             * Save date to workout sep.beddays
.
.         Return record
.
PPAT2000  MATCH     TWARD,STWARD
          GOTO      PPAT2100 IF EQUAL
.
PPAT2200  MOVE      TDATE,OTDATE
.
PPAT2100  MATCH     TODATE,TDATE
          GOTO      PPAT9999 IF NOT LESS
.
          MATCH     FROMDATE,TDATE
          GOTO      PPAT3000 IF NOT LESS
.
          MOVE      FROMDATE,STDATE
          GOTO      PPAT7500
.
PPAT3000  MOVE      TDATE,STDATE
          GOTO      PPAT7500
.
.         On leave record
.
PPAT5000  MATCH     FROMDATE,TDATE           * Within period
          GOTO      PPAT7500 IF LESS
.
          MATCH     TODATE,TDATE
          GOTO      PPAT5500 IF LESS
.
          MOVE      TODATE,WDATE1
          MOVE      STDATE,WDATE2
          CALL      BDAY0000
          GOTO      PPAT9999
.
PPAT5500  MOVE      TDATE,WDATE1
          MOVE      STDATE,WDATE2
          CALL      BDAY0000
          MOVE      TDATE,STDATE
          GOTO      PPAT7500
.
.         Change record
.
PPAT6000  MATCH     TWARD,STWARD
          GOTO      PPAT1000 IF EQUAL
          MOVE      TDATE,OTDATE
.
          MATCH     FROMDATE,TDATE
          GOTO      PPAT7500 IF LESS
.
          MATCH     TODATE,TDATE
          GOTO      PPAT7000 IF LESS
.
          MOVE      TODATE,WDATE1             * todate
          MOVE      STDATE,WDATE2             * fromdate
          CALL      BDAY0000
          GOTO      PPAT9999
.
PPAT7000  MOVE      TDATE,WDATE1              * todate
          MOVE      STDATE,WDATE2             * fromdate
          CALL      BDAY0000
.
          MOVE      TDATE,STDATE
.
.         Save the ward
.
PPAT7500  MOVE      TDATE,SAVDATE
          MOVE      TWARD,STWARD
          GOTO      PPAT1000                  * Read next trans record
.
.         Check the last record
.
PPAT8000  MOVE      STWARD,TWARD
.
PPAT8500  MATCH     ANSL,STMOVE               * On-leave ?
          GOTO      PPAT9999 IF EQUAL
.
          MATCH     SP1,STMOVE
          GOTO      PPAT9999 IF EQUAL
.
.         Last record
.
          MATCH     ANSD,STMOVE               * Discharge record ?
          GOTO      PPAT9500 IF NOT EQUAL
.
          MOVE      TDATE,WDATE1
          GOTO      PPAT9700
.
PPAT9500  MOVE      TODATE,WDATE1
.
PPAT9700  MOVE      STDATE,WDATE2
          CALL      BDAY0000
.
PPAT9999  RETURN
.
. *****************************************************************************
. * BPAT0000 : Processing day cases                    Called by : PPAT0000   *
. *****************************************************************************
.
.         The patient was a day case patient. Treat as one bed day.
.
DPAT0000  PACK      KEY30,AADMNO,Z30         * Position at end of patient
          CALL      RDSTRAN2
          CALL      RDPTRAN2                 * Get the last patient
          BRANCH    OVRCD,DPAT9999           * No record -> ignore
.
          MATCH     TADMN,AADMNO             * Make sure this is right patient.
          GOTO      DPAT9999 IF NOT EQUAL    * No -> ignore patient
.
.         Process this ward with one bed day
.
          MOVE      ONE,DAYCASE
          MOVE      TWARD,STWARD
          MOVE      ONE,BEDDAYS              * Patient beddays
          CALL      BDAY9000                 * Process the last record
.
DPAT9999  RETURN
.
. *****************************************************************************
. * BDAY0000 : Calculate patient/separation bed days.  Called by : PPAT0000   *
. *****************************************************************************
.
BDAY0000  CALL      CALC0000                 * Calculating patient bed days
          MOVE      NBRDAYS,BEDDAYS          * Patient beddays
.
.         Calculate the separation bed days
.         Check if the original transfer date is within the period
.
          MATCH     FROMDATE,OTDATE
          GOTO      BDAY9000 IF LESS
.
          MATCH     TODATE,OTDATE
          GOTO      BDAY9000 IF NOT LESS
.
          CMATCH    ANSR,STMOVE              * Ignore on-leave bed days
          GOTO      BDAY9000 IF EQUAL
.
          PACK      SKEY30,TADMN,TDATE,TTIME,TWARD,TBED    * Save the key
.
          CMATCH    ANSD,STMOVE              * patient discharged in periods?
          GOTO      BDAY1500 IF NOT EQUAL    * No.
.
.         BRANCH    DAYCASE,BDAY9000         * Day case patient ?
.
.         Check if the next transaction move is a discharged record
.
BDAY1500  CALL      RDKTRAN2
          BRANCH    OVRCD,BDAY2000
.
          MATCH     TADMN,AADMNO             * Make sure this is right patient.
          GOTO      BDAY2000 IF NOT EQUAL
.
          MATCH     TODATE,TDATE
          GOTO      BDAY2000 IF LESS
.
          CMATCH    ANSD,TMOVE               * Don't include bed days if patient
          GOTO      BDAY3000 IF EQUAL        * discharged in the next trans.
.
.         Calculate the separation bed days
.
BDAY2000  MOVE      SAVDATE,WDATE2           * Calculate the full period
          MOVE      OTDATE,WDATE1
          CALL      CALC0000
          MOVE      NBRDAYS,SEPDAYS
.
BDAY3000  MOVE      SKEY30,KEY30             * Reposition the trans record
          CALL      RDTRAN2
.
.         Add these bed days to the temporary index file
.
BDAY9000  CALL      ADD0000                  * Add this patient
          MOVE      STWARD,D3
          MOVE      Z30,STWARD               * Set up for grand totals
          CALL      ADD0000                  * Add this patient
          MOVE      D3,STWARD
.
          RETURN
.
. *****************************************************************************
. * CALC0000 : Process a pair of transfer records.     Called by : BDAY0000   *
. *****************************************************************************
.
.         Calculate the number of days between the two dates. Do this
.         by calculating the julian dates, and subtracting
.
CALC0000  MOVE      ZERO,NBRDAYS
          MOVE      WDATE1,CDYSTDTE
          MOVE      WDATE2,CDYSFDTE
          CALL      CALCDAYS
          ASSIGN    (CDYSDAYS-1),NBRDAYS
.
CALC9999  RETURN
.
. *****************************************************************************
. * ADD0000  : Add a patient to temp index files.      Called by : BDAY0000   *
. *                                                                PPAT0000   *
. *****************************************************************************
.
ADD0000   PACK      KEY6,STWARD,AUSR5        * Pack up ward/department
          CALL      RDWORK1                  * Check if a record already exists
          BRANCH    OVRCD,ADD5000            * New record required
.
          ADD       DAYCASE,XDCASE           * Accumulate daycases
          ADD       BEDDAYS,XPBDAYS          * Accumulate Patient bed days
          ADD       SEPDAYS,XSBDAYS          * Accumulate Separation bed days
.
          MATCH     "zzz",STWARD
          GOTO      ADD1000 IF EQUAL         * Grand totals
.
          MOVE      ZERO,PATNUM
          BRANCH    DAYCASE,ADD1000          * Daycase patient
.
.         Check if this patient has already been counted
.
          PACK      KEY14,STWARD,AUSR5,AADMNO
          CALL      RDWORK2                  * Already counted ?
          COMPARE   ZERO,OVRCD               * Counted already if on file
          GOTO      ADD1000 IF EQUAL         * Already counted
.
          ADD       ONE,PATNUM               * Add to number of patients
.
          PACK      KEY14,STWARD,AUSR5,AADMNO
          CALL      WRWORK2                  * Add the 2nd temp file

.         Update the temp. index file with this data
.
ADD1000   ADD       PATNUM,XNPAT
          CALL      UPWORK1                  * Update the work file
          GOTO      ADD9999
.
.         New record required.
.
ADD5000   MOVE      STWARD,XWARD             * Set up ward
          MOVE      AUSR5,XDEPT              * Set up department
.
          MOVE      DAYCASE,XDCASE           * Daycases
          MOVE      BEDDAYS,XPBDAYS          * Patient bed days
          MOVE      SEPDAYS,XSBDAYS          * Separation bed days
.
          MATCH     "zzz",STWARD
          GOTO      ADD9000 IF EQUAL         * Grand totals
.
          MOVE      ZERO,PATNUM
.
.         If this is the first record to the 1st work file, then it will
.         automatically be the first record to the 2nd work file as well.
.
          BRANCH    DAYCASE,ADD9000
          PACK      KEY14,STWARD,AUSR5,AADMNO
          ADD       ONE,PATNUM
          CALL      WRWORK2                  * Add to the 2nd temp file
.
ADD9000   MOVE      PATNUM,XNPAT
          PACK      KEY6,XWARD,XDEPT
          CALL      WRWORK1                  * Add to the temp file.
.
ADD9999   RETURN
.
. *****************************************************************************
. * PRNT0000 : Print the contents of the 1st index file  Called by: ML0000    *
. *****************************************************************************
.
PRNT0000  MOVE      SP3,STWARD               * Initialise starting ward
          DISPLAY   *P1:24,*EL,*P10:24,"Printing: ";
.
          MOVE      SP6,KEY6                 * Position at start of temp file
          CALL      RDSWORK1
PRNT1000  CALL      RDKWORK1                 * Get the next record
          BRANCH    OVRCD,PRNT9000           * Finished temp file
.
          DISPLAY   *P20:24,*V2LON,XWARD,SLASH,XDEPT;
.
.         Check for a change of ward
.
          MATCH     STWARD,XWARD             * Still the same ward ?
          GOTO      PRNT5000 IF EQUAL        * Yes.
.
.         New ward. Check for 1st ward (old ward = SP3). If so, simply
.         initialise totals
.
          MATCH     SP3,STWARD               * 1st time through ?
          GOTO      PRNT4000 IF EQUAL        * Yes.
.
          CALL      PSUB0000                 * Print the subtotal
.
.         Initialise totals, and print the heading for the new ward
.
PRNT4000  MOVE      ZERO,TNPAT               * Zero totals
          MOVE      ZERO,TDCASE              * Zero totals
          MOVE      ZERO,TSBDAYS             * Zero totals
          MOVE      ZERO,TPBDAYS             * Zero totals
          MOVE      XWARD,STWARD             * Save ward code
.
          CALL      PWRD0000                 * Print the ward heading
.
.         Get the details for this record
.
PRNT5000  COMPARE   "60",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          MOVE      "UNKNOWN             ",TDESC
          MATCH     SP3,XDEPT                * Check for a blank dept code
          GOTO      PRNT5200 IF EQUAL        * Is blank. Use a blank desc.
.
          PACK      TDESC,XDEPT,DEFDSC       * Set up default description.
.
          PACK      KEY5,CODEU5,XDEPT
          CALL      RDCODE1                  * Get the description
.
.         Print this record.
.
PRNT5200  MOVE      TDESC,PRNTDESC           * Shorten description
.
          MOVE      ZERO,DCLOS
          MOVE      XNPAT,FORM6              * Total no of patients
          ADD       XDCASE,FORM6
.
          COMPARE   ZERO,FORM6
          GOTO      PRNT5300 IF EQUAL
.
          MOVE      XSBDAYS,DCLOS
          ADD       XDCASE,DCLOS
          DIV       FORM6,DCLOS              * LOS include day cases
.
PRNT5300  MOVE      ZERO,NDCLOS 
          COMPARE   ZERO,XNPAT
          GOTO      PRNT5400 IF EQUAL
.
          MOVE      XSBDAYS,NDCLOS
          DIV       XNPAT,NDCLOS             * LOS exclude day cases
.
PRNT5400  PRINT     *1,"|",*7,XDEPT,*12,PRNTDESC:
                    *27,"|",*33,XNPAT:
                    *44,"|",*50,XDCASE:
                    *61,"|",*68,XSBDAYS:
                    *78,"|",*83,DCLOS:
                    *96,"|",*101,NDCLOS:
                    *114,"|",*121,XPBDAYS:
                    *132,"|"
          ADD       ONE,CLNO
.
          ADD       XNPAT,TNPAT              * Add to totals
          ADD       XDCASE,TDCASE            * Add to totals
          ADD       XSBDAYS,TSBDAYS          * Add to totals
          ADD       XPBDAYS,TPBDAYS          * Add to totals
.
          GOTO      PRNT1000                 * Get the next record
.
.         Finished processing file. Print final totals
.
PRNT9000  MATCH     SP3,STWARD               * Anything printed ?
          GOTO      PRNT9500 IF EQUAL        * No.
.
          CALL      PSUB0000
.
PRNT9500  MOVE      ONE,GNDFLAG              * To avoid printing grand total
          COMPARE   "45",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PRINT     *N,"No of Inpatients",*18,"= (For each ward) The number":
                    " of patients who were in each ":
                    "department for this ward.":
                    *N,*18,"= (For Grand Total) The number of patients in":
                    " each department.":
                    *N,*N,"No of Bed Days":
                    *18,"= The number of bed days ":
                    "accumulated for each Inpatient in each ":
                    "ward for each department (Excluding Day Cases).":
                    *N,*18,"= Separation Bed days.":
                    *N,*N,"Patient Bed Days":
                    *18,"= Bed Days for the period (Including Day Cases).":
                    *N,*N:
                    *1,"ALOS inc.Day Cases =":
                    *20,"  No of Bed Days  + No Of Day Cases":
                    *62,"ALOS exc.Day Cases =":
                    *82,"  No of Bed Days ":
                 *N,*20,"  ----------------------------------":
                    *82,"  ----------------":
                 *N,*20,"  No of Inpatients + No of Day Cases":
                    *82,"  No of Inpatients":
                    *N,*N,"*** End of Report ***"
.
PRNT9999  RETURN
.
. *****************************************************************************
. * HEAD0000 : Print the Heading                         Called by: CONT0000  *
. *                                                                 PRNT0000  *
. *****************************************************************************
.
HEAD0000  CALL      HEAD132                  * Print the standard heading
.
          IF        IBCNMHOS =1
            PRINT     *20,"Hospital : ",HOSPID,"    ",HOSPNAME
            ADD       ONE,CLNO
          ENDIF
.
          UNPACK    FROMDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PRINT     *N,*20,"Date Range : ",CPCDATE," to ";
.
          UNPACK    ENDDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PRINT     CPCDATE,*N
.
          CALL      UND132
          PRINT     *1,"|",*27,"|",*44,"|",*61,"|":
                    *78,"|     A.L.O.S.":
                    *96,"|    A.L.O.S.":
                    *114,"|     Patient":
                    *132,"|":
                    *N,*1,"| Ward/Department":
                    *27,"|No of Inpatients":
                    *44,"| No of Day Case":
                    *61,"| No of Bed Days":
                    *78,"|  Inc.Day Case":
                    *96,"|  Exc.Day Case":
                    *114,"|     Bed Days ":
                    *132,"|"
          CALL      UND132
          ADD       FIVE,CLNO
.
.         On every page except the first, also re-print the ward name
.
          BRANCH    CPAGENO,HEAD9999
          BRANCH    GNDFLAG,HEAD9999
.
          CALL      PWRD0000
.
HEAD9999  RETURN
.
. *****************************************************************************
. * PWRD0000 : Print the Ward sub-heading                Called by: HEAD0000  *
. *                                                                 PRNT0000  *
. *****************************************************************************
.
PWRD0000  MATCH     "zzz",STWARD             * Grand Totals or ward code
          GOTO      PWRD1000 IF NOT EQUAL
.
.         Grand totals
.
          COMPARE   "57",CLNO                * Room for new ward heading ?
          GOTO      PWRD9000 IF NOT LESS     * No. Start new page instead
.
          PRINT     *1,"|",*3,"GRAND TOTAL":
                    *44,"|",*61,"|":
                    *78,"|",*96,"|":
                    *114,"|",*132,"|"
          ADD       ONE,CLNO
          GOTO      PWRD9999
.
.         Ward heading
.
PWRD1000  PACK      WBDESC,STWARD,DEFDSC     * Set up default description
          PACK      KEY6,STWARD,SP3
          CALL      RDWARD1                  * Get the ward description
.
          COMPARE   "57",CLNO                * Room for new ward heading ?
          GOTO      PWRD9000 IF NOT LESS     * No. Start new page instead
.
          PRINT     *1,"|",*3,STWARD,*7,WBDESC:
                    *44,"|",*61,"|":
                    *78,"|",*96,"|":
                    *114,"|",*132,"|"
          ADD       ONE,CLNO
          GOTO      PWRD9999
.
.         No room on current page. Start a new page instead
.
PWRD9000  CALL      HEAD0000
.
PWRD9999  RETURN
.
. *****************************************************************************
. * PSUB0000: Print the sub-total                        Called by: PRNT0000  *
. *****************************************************************************
.
PSUB0000  MATCH     "zzz",STWARD             * Grand totals or ward totals
          GOTO      PSUB1000 IF NOT EQUAL
.
.         Grand totals
.
          MOVE      ZERO,DCLOS
          MOVE      TNPAT,FORM6
          ADD       TDCASE,FORM6
          COMPARE   ZERO,FORM6
          GOTO      PSUB0200 IF EQUAL
.
          MOVE      TSBDAYS,DCLOS
          ADD       TDCASE,DCLOS
          DIV       FORM6,DCLOS
.
PSUB0200  MOVE      ZERO,NDCLOS
          COMPARE   ZERO,TNPAT
          GOTO      PSUB0500 IF EQUAL
.
          MOVE      TSBDAYS,NDCLOS
          DIV       TNPAT,NDCLOS         
PSUB0500  MOVE      "Grand Totals   ",PRNTDESC
          PRINT     *1,"|":
                    *27,"|----------------":
                    *44,"|----------------":
                    *61,"|----------------":
                    *78,"|-----------------":
                    *96,"|-----------------":
                    *114,"|-----------------",*132,"|":
                    *N,*1,"|",*12,PRNTDESC:
                    *27,"|",*33,TNPAT:
                    *44,"|",*50,TDCASE:
                    *61,"|",*68,TSBDAYS:
                    *78,"|",*83,DCLOS:
                    *96,"|",*101,NDCLOS:
                    *114,"|",*121,TPBDAYS:
                    *132,"|"
.
          GOTO      PSUB5100
.
.         Ward totals
.
PSUB1000  PACK      PRNTDESC,SUBTOT,STWARD,SP20
          MOVE      ZERO,DCLOS
          MOVE      TNPAT,FORM6              * Total no of patients
          ADD       TDCASE,FORM6
.
          COMPARE   ZERO,FORM6
          GOTO      PSUB2000 IF EQUAL
.
          MOVE      TSBDAYS,DCLOS
          ADD       TDCASE,DCLOS
          DIV       FORM6,DCLOS              * LOS include day cases
.
PSUB2000  MOVE      ZERO,NDCLOS
          COMPARE   ZERO,TNPAT
          GOTO      PSUB5000 IF EQUAL
.
          MOVE      TSBDAYS,NDCLOS
          DIV       TNPAT,NDCLOS             * LOS exclude day cases
.
.         Print the totals
.
PSUB5000  PRINT     *1,"|":
                    *27,"|----------------":
                    *44,"|----------------":
                    *61,"|----------------":
                    *78,"|-----------------":
                    *96,"|-----------------":
                    *114,"|-----------------",*132,"|":
                    *N,*1,"|",*12,PRNTDESC:
                    *27,"|",*33,TNPAT:
                    *44,"|",*50,TDCASE:
                    *61,"|",*68,TSBDAYS:
                    *78,"|",*83,DCLOS:
                    *96,"|",*101,NDCLOS:
                    *114,"|",*121,TPBDAYS:
                    *132,"|"
PSUB5100  CALL      UND132
.
          ADD       THREE,CLNO
.
PSUB9999  RETURN
.
. *****************************************************************************
. * I/O routines for the three index files                                    *
. *****************************************************************************
.
RDSWORK1  READ      WORK1,KEY6;;
          RETURN
.
RDKWORK1  MOVE      ZERO,OVRCD
          READKS    WORK1;XWARD,XDEPT,XNPAT,XDCASE,XSBDAYS,XPBDAYS
          GOTO      OVERCOND IF OVER
          RETURN
.
RDWORK1   MOVE      ZERO,OVRCD
          READ      WORK1,KEY6;XWARD,XDEPT,XNPAT,XDCASE,XSBDAYS,XPBDAYS
          GOTO      OVERCOND IF OVER
          RETURN
.
WRWORK1   WRITE     WORK1,KEY6;KEY6,XNPAT,XDCASE,XSBDAYS,XPBDAYS
          RETURN
.
UPWORK1   UPDATE    WORK1;XWARD,XDEPT,XNPAT,XDCASE,XSBDAYS,XPBDAYS
          RETURN
.
.
RDWORK2   MOVE      ZERO,OVRCD
          READ      WORK2,KEY14;XWARD,XDEPT,XADMN
          GOTO      OVERCOND IF OVER
          RETURN
.
WRWORK2   WRITE     WORK2,KEY14;KEY14
          RETURN
.
.
RDSWORK3  READ      WORK3,KEY8;;
          RETURN
.
RDKWORK3  MOVE      ZERO,OVRCD
          READKS    WORK3;XADMN
          GOTO      OVERCOND IF OVER
          RETURN
.
RDWORK3   MOVE      ZERO,OVRCD
          READ      WORK3,KEY8;XADMN
          GOTO      OVERCOND IF OVER
          RETURN
.
WRWORK3   WRITE     WORK3,KEY8;KEY8
          RETURN
.
          INC       STD001IO
          INC       PATHSPKY
          INC       CALCDAYS
          INC       TFILENAM                     * Generate Temp File Name
.
          INC       PATHSPIO/INC
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       PATCODIO/INC
          INC       PATDSCIO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATTRNIO/INC
          INC       PATWR1IO/INC
          INC       PATDAYIO/INC
          INC       WEBERRIO/INC                 * Web Server Error Logging
          INC       PATMA1IO/INC

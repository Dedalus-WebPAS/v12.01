.*******************************************************************************
.* System         : Medicare WebServices
.* Program        : IBAECL05.PBL                                               *
.* Name           : Medicare WebServices IHCW Date Range Status Request        *
.*******************************************************************************
.* Date           : 09/11/2021                                                 *
.* Author         : Peter Vela                                                 *
.* Function       : Medicare WebServices IHCW Date Range Status Request        *
.* Modifications  :                                                            *
.*---------------------------------------------------------------------------- *
.* V11.04.01  04/03/2024 PJ Le Febour   0929044dc                              *
.*                       DRRQ1000 MOVE  PTCNSUID,WBETCUID
.* V11.01.01  03/11/2021 Peter Vela TSK 0902857                                *
.*                       Created Report                                        *
.*******************************************************************************
.
          INC       STD001FD
.
          INC       IBACONFD/INC
          INC       PATCONFD/INC
          INC       WEBETRFD/INC               
          INC       PATCRTFD/INC
          INC       PATLCNFD/INC
.
.-------------------------------------------------------------------------------
.                           Variables
.-------------------------------------------------------------------------------
DATEFROM  DIM       8                            * Input From Date
DATETO    DIM       8                            * Input To Date
TIMEFROM  DIM       8                            * Input From Time
TIMETO    DIM       8                            * Input To Time
TIMFRDI6  DIM       6                            * From Time DIM 6
TIMTODI6  DIM       6                            * To TIme DIM 6
DIM2A     DIM       2
DIM2B     DIM       2
.-------------------------------------------------------------------------------
.                            Constants
.-------------------------------------------------------------------------------
IBAUSER   INIT      "iba"
.-------------------------------------------------------------------------------
PRGID     INIT      "IBAECL05"
PRGNAM    INIT      "Medicare WebServices IHCW Date Range Status Request"
VERSION   INIT      "V12.01.00"
.
. ******************************************************************************
. *                                  MAIN0000                                  *
. ******************************************************************************
.
MAIN0000  CALL      INIT0000
.
          MATCH     "1",PTCNIHSD
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Date Range IHCW STSW not switched on.";
            CALL      HITENTER
            GOTO      MAIN9999
          ENDIF
.
MAIN1000  CALL      GSDT0000                     * Keyin the start date
          BRANCH    EXIT,MAIN9999                * Exit if nothing input
.
          CALL      GSTM0000                     * Keyin the start time
          BRANCH    EXIT,MAIN9999                * Exit if nothing input
.
          CALL      GEDT0000                     * Keyin the ending date
          BRANCH    EXIT,MAIN1000                * Exit if nothing input
.
          CALL      GETM0000                     * Keyin the ending time
          BRANCH    EXIT,MAIN1000                * Exit if nothing input
.
          CALL      GCON0000                     * OK to continue
          BRANCH    EXIT,MAIN1000                * restart if no selected
.
          CALL      DRRQ0000                     * Write Date Range Reqeust
.
MAIN9999  CHAIN     PGM                          
.
. ******************************************************************************
. *                                 INIT0000              Called by : MAIN0000 *
. *                                                                            *
. * Initialisation procedure                                                   *
. ******************************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening webetraf";
          OPEN      WEBETRA1,"webetraf"
.
          DISPLAY   *P64:24,"patcrtaf";
          OPEN      PATCRTA1,"patcrtaf"
.
          DISPLAY   *P64:24,"patlcnaf";
          OPEN      PATLCNA1,"patlcnaf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.                                                * 1 = Multi Hospital
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,HUND14;*145,PTCNCLID
          READ      CONTROLF,HUND28;*223,PTCNIHSD
.
INIT9999  RETURN
.
. ******************************************************************************
. *                                  GSDT0000             Called by : MAIN0000 *
. *                                                                            *
. * Get the start  date                                                        *
. * EXIT = 1   if a nothing was input                                          *
. ******************************************************************************
.
GSDT0000  MOVE      ZERO,EXIT
.
          MOVE      SEVEN,CVERT                  * Line for keyin of from date
          MOVE      TWENTY5,CCOL                 * Col for keyin of from date
          MOVE      CVERT,EVERT                  * Col for error message
          MOVE      "40",ECOL                    * Col for error message
          MOVE      ZERO,CHIGHLT                 * No highlight
          MOVE      ONE,CDEFDTE
.
          UNPACK    SP6,CDAY,CMON,CYEAR          * Initialise date
          MOVE      SP8,DATEFROM
          MOVE      CCC,CCENT                    * Initialise century
.
          CALL      KEYDATE                      * Key in the date
          BRANCH    OVRCD,GSDT9000
.
          PACK      DATEFROM,CCENT,CYEAR,CMON,CDAY
          REP       " 0",DATEFROM
          GOTO      GSDT9999
.
GSDT9000  MOVE      ONE,EXIT
.
GSDT9999  RETURN
.
. ******************************************************************************
. *                                  GSTM0000             Called by : MAIN0000 *
. *                                                                            *
. * Get the start  time                                                        *
. * EXIT = 1   if a nothing was input                                          *
. ******************************************************************************
GSTM0000  MOVE      ZERO,EXIT
.
          KEYIN     *P25:8,TIMEFROM;
.
          MATCH     SP70,TIMEFROM
          GOTO      GSTM9000 IF EQUAL
          GOTO      GSTM9000 IF EOS
.
          GOTO      GSTM9999
.
GSTM9000  MOVE      ONE,EXIT          
.
GSTM9999  RETURN
.
. ******************************************************************************
. *                                  GEDT0000             Called by : MAIN0000 *
. *                                                                            *
. * Get the end date of the report                                             *
. *            EXIT = 1   if a nothing was input                               *
. ******************************************************************************
.
GEDT0000  MOVE      ZERO,EXIT
.
          MOVE      NINE,CVERT                  * Line for keyin of to date
          MOVE      TWENTY5,CCOL                 * Col for keyin of to date
          MOVE      CVERT,EVERT                  * Col for error message
          MOVE      "40",ECOL                    * Col for error message
          MOVE      ZERO,CHIGHLT                 * No highlight
          MOVE      ONE,CDEFDTE
.
          MOVE      SP8,DATETO
.
          CALL      KEYDATE                      * Key in the date
          BRANCH    OVRCD,GEDT9000
.
          PACK      DATETO,CCENT,CYEAR,CMON,CDAY
          REP       " 0",DATETO
          GOTO      GEDT9999
.
GEDT9000  MOVE      ONE,EXIT
.
GEDT9999  RETURN
.
. ******************************************************************************
. *                                  GETM0000             Called by : MAIN0000 *
. *                                                                            *
. * Get the end time                                                           *
. * EXIT = 1   if a nothing was input                                          *
. ******************************************************************************
GETM0000  MOVE      ZERO,EXIT
.
          KEYIN     *P25:10,TIMETO;
.
          MATCH     SP70,TIMETO
          GOTO      GETM9000 IF EQUAL
          GOTO      GETM9000 IF EOS
.
          GOTO      GETM9999
.
GETM9000  MOVE      ONE,EXIT
.
GETM9999  RETURN
.
. ******************************************************************************
. *                                  GCON0000             Called by : MAIN0000 *
. *                                                                            *
. * Ok. to continue (Y/N)                                                      *
. ******************************************************************************
.
GCON0000  MOVE      ZERO,EXIT
          DISPLAY   *P1:24,*EL,"Ok to Continue (":
                    *V2LON,ANSY,*HOFF,SLASH,*V2LON,ANSN,*HOFF,") ? ";
.
GCON1000  KEYIN     *P24:24,"_",*P24:24,*V2LON,ANS;
.
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
.
          MATCH     ANS,ANSY
          GOTO      GCON9999 IF EQUAL
.
          MATCH     ANS,ANSN
          GOTO      GCON9000 IF EQUAL
.
          BEEP
          GOTO      GCON1000
.
GCON9000  MOVE      ONE,EXIT
.
GCON9999  RETURN
.
. ******************************************************************************
. *                                  DRRQ0000             Called by : MAIN0000 *
. *                                                                            *
. * Get date range status requests                                             *
. * EXIT = 1   if a nothing was input                                          *
. ******************************************************************************
DRRQ0000  UNPACK    TIMEFROM,DIM2,DIM1,DIM2A,DIM1,DIM2B
          PACK      TIMFRDI6,DIM2,DIM2A,DIM2B
.
          UNPACK    TIMETO,DIM2,DIM1,DIM2A,DIM1,DIM2B
          PACK      TIMTODI6,DIM2,DIM2A,DIM2B
.
          MOVE      SP1,WBETSTAT
          MOVE      "19",WBETTYPE                * IMCW Date Range Status
          MOVE      SP70,WBETTRID
          MOVE      SP70,WBETBATN
          MOVE      SP70,WBETINVN
          MOVE      SP30,WBETUDTE
          MOVE      SP30,WBETUTIM
          MOVE      SP30,WBETEROR
          MOVE      SP30,WBETSPAR
.
          CALL      IBACLOCK                     * get current date/time
          PACK      WBETCDTE,CCC,CYY,CMM,CDD
          REP       " 0",WBETCDTE
.
          IF        IBCNMHOS<>1
            MOVE      PTCNCLID,WBETLOCN          * get location from controlf
            GOTO      DRRQ0700
          ENDIF
.
          PACK      KEY8,SP70
          CALL      RSPTLCN1
DRRQ0500  CALL      RKPTLCN1                     * get (multiple) location ids
          BRANCH    OVRCD,DRRQ9999
.
          MATCH     ANSA,PTLNACTV
          GOTO      DRRQ0500 IF NOT EQUAL        * location not active
.
          MOVE      PTLNLOCN,WBETLOCN            * location from patlcn
.
DRRQ0700  
.         CALL      GSID0000                     * get sender id
.
DRRQ1000  CLOCK     TIME,WBETCTIM
          REP       " 0",WBETCTIM

          MOVE      PTCNSUID,WBETCUID
.UPTOHERE
          PACK      KEY51,WBETSTAT,WBETTYPE,WBETLOCN,WBETCDTE,WBETCTIM,WBETTRID:
                          SP70
          CALL      RDWBETR1
          IF        OVRCD = 0
            GOTO      DRRQ1000
          ENDIF
.
          PACK      WBETFDTM,DATEFROM,TIMFRDI6,SP70
          PACK      WBETTDTM,DATETO,TIMTODI6,SP70
.
.
          CALL      IBACLOCK
          PACK      WBETCDTE,CCC,CYY,CMM,CDD
          REP       " 0",WBETCDTE
          MOVE      CTIMEIS,WBETCTIM
          PACK      WBETCUID,IBAUSER,SP7
          CALL      WRWBETR1                     * write transaction request
.
          COMPARE   ONE,IBCNMHOS
          IF        @EQUAL
            GOTO      DRRQ0500                   * get next location if multhosp
          ENDIF
.
DRRQ9999  RETURN
.
.*****************************************************************************
.*                                GSID0000                                   *
.*                   Get sender id from certificate table                    *
.*****************************************************************************
.
.         Get the certificate that covers today
.
.GSID0000  PACK      KEY16,WBETLOCN,WBETCDTE,SP70
.          CALL      RDPTCTI1                     * record found for today ?
.          COMPARE   ZERO,OVRCD
.          GOTO      GSID9000 IF EQUAL           * yes - use this certificate id
.
.          CALL      RPPTCTI1                     * no - read previous record
.          IF        OVRCD = 0
.            MATCH     WBETCDTE,PTCITDTE          * within date range ?
.            GOTO      GSID9000 IF NOT LESS      * yes - use this certificate id
.          ENDIF
.
.          MOVE      SP70,WBETSNID                * no current certificate
.          GOTO      GSID9999
.
.GSID9000  MOVE      PTCISNID,WBETSNID            * use certificate sender id
.GSID9999  RETURN
+
.*******************************************************************************
.
          INC       STD001IO                     * System Standard Routines
.
          INC       WEBETRIO/INC
          INC       PATCRTIO/INC
          INC       PATLCNIO/INC

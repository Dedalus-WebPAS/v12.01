.----------------------------------------------------------------------
. Program       : COMWEB09
.
. Function      : Common Maintenance programs
.
. Modifications : 
.----------------------------------------------------------------------
. V11.02.01 23.09.2022  Bella Turco              TSK 0921957 
.           Added NOMORE00 - Display "No more records" at end of list
.           Added PTCNNRTD - Displays number of rows set by parameter value
. V11.01.10 20.09.2021  DA Sarkies               TSK 0905305 
.           Changed column titles to match spec for report 3 template 1 
. V11.01.09 18.09.2021  DA Sarkies               TSK 0905305 
.           Changed 1 to 0 for active flags in two displays       
.           Moved columns around on some displays
. V11.01.08 08.09.2021  Ebon Clements            TSK 90910802
.           Fixed leave notification list active filter - LNTAB000
. V11.01.07 01.09.2021  DA Sarkies               TSK 0905305
.           Changed 'inactive' to 'active' title for list
. V11.01.06 01.09.2021  DA Sarkies               TSK 0905305
.           Changed 'inactive' to 'active' title for list
. V11.01.05 20/08/2021  Thanh T                  TSK 0905305
.           Renamed EMRECHVL to EMRACTVL and EMRACPVL to be
.           consistent with table name
. V11.01.04 31/05/2021  Tony Hui                 TSK 0905305
.           Added EMRACPAF to the Web Server as ReportNo=3.
. V11.01.03 20/05/2021  Tony Hui                 TSK 0905305
.           Added EMRACTAF to the Web Server as ReportNo=2.
. V11.01.02 07/06/2021  Ebon Clements            TSK 0900650
.           Added All to ward description list - LNROW000
. V11.01.01 19/05/2021  Ebon Clements            TSK 0900650
.           Recompiled for MLTOLNFD layout change
. V11.01.00 13/05/2021  Ebon Clements            TSK 0900650
.           Created Web Server
.----------------------------------------------------------------------
          INC       IBAWEBDF    
          INC       WEBDATDF
.
          INC       MLTOLNFD/INC
          INC       MLTOLNTD/INC
          INC       MLTSECFD/INC
          INC       PATCONFD/INC
          INC       PATHSPFD/INC
          INC       PATILTFD/INC
          INC       PATWR1FD/INC
.
	  INC       EMRACTFD/INC
	  INC       EMRACTTD/INC
.
          INC       EMRACPFD/INC
          INC       EMRACPTD/INC

.----------------------------------------------------------------------
ACTIDESC  DIM       8       * Active or Inactive
DISPGOVM  DIM       3       * Display greater than overdue minues
FILTHOSP  DIM       3       * Hospital filter
FILTWARD  DIM       3       * Ward filter
FILTLTYP  DIM       3       * Leave type filer
FILTSTAT  DIM       1       * Active status filter
HOSPCODE  DIM       3       * Hospital Code
LASTFLAG  FORM      1       * Last record flag
NEXTPAGE  DIM       1       * Nextpage parameter CGI parameter
REMINDER  FORM      2       * Reminder Number
SHOWFLAG  DIM       1       * Page displayed flag
UPDTTYPE  DIM       1       * Update Type
WARDCODE  DIM       3       * Ward Code
.
.------------------------------------------------------------------------
. EMRACT / EMRACP - Additional Global Variables
.------------------------------------------------------------------------
STARTKEY  DIM        14     * Goto Search Start Key
F3P6      FORM       3.6    * Storing exact precision
NUMBRONE  FORM       1      * For Comparison
DISPFDAT  DIM        11     * Display Format - From Date
DISPTDAT  DIM        11     * Display Format - To Date
DISPAFLG  DIM        3      * Display Format - Inactive Flag
ENDKEY38  DIM        38     * Variable to hold key for validation
VALFDATE  DIM        8      * From Date for Validation
VALTDATE  DIM        8      * To Date for Validation
AUTOEDAT  DATE       8      * Date for Validation
SAVKEY38  DIM        38     * Variable to storing validation keys
SAVKEY30  DIM        30     * Variable for storing validation keys
.
.----------------------------------------------------------------------
PRGID     INIT      "COMWEB09"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "Common Maintenance"
.----------------------------------------------------------------------
.
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200,MAIN1300
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "Invalid Option",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  CALL      OLNM0000        * Overdue Leave Notification Maintenance
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1200  CALL      AITM0000        * AECC Codes Inbound Transport          
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1300  CALL      AAPM0000        * AECC Codes Admitted Predictors        
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
.
.------------------------------------------------------------
. Output Next Page Based on CGI Parameter nextpage
.------------------------------------------------------------
NXTPAG00  MOVE      ZERO,F1
          MOVE      NEXTPAGE,F1
          BRANCH    F1,NXTPAG10
.
          CALL      WINCLS00
          GOTO      NXTPAG99
.
NXTPAG10  CALL      RDIREC00
          GOTO      NXTPAG99
.
NXTPAG99  RETURN
.
.------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------
INIT0000  READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,HUND25;*238,PTCNNRTD
.
          MOVE      ZERO,F2
          MOVE      PTCNNRTD,F2
          IF        F2 = 0
            MOVE      "10",PTCNNRTD
          ENDIF
.
          OPEN      MLTOLNA1,"mltolnaf"
          OPEN      MLTSECA1,"mltsecaf"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
          OPEN      PATHSPA1,"pathspaf"
          OPEN      PATLETR1,"patletrf"
          OPEN      PATLETR2,"patletrf"
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATWR1A7,"patwr1af"
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      EMRACTA1,"emractaf"
          TRAPCLR   IO
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      EMRACPA1,"emracpaf"
          TRAPCLR   IO
.
INIT9999  RETURN
.
.------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          MOVE      SP70,TEMPLATE
          MOVE      SP70,NEXTPAGE
          MOVE      SP70,REDIRECT
          MOVE      SP70,UPDTTYPE
          MOVE      ZERO,NORECORD
          MOVE      ZERO,STARTNUM
          MOVE      SP70,STARTKEY
.
          MOVE      SP70,FILTHOSP
          MOVE      SP70,FILTWARD
          MOVE      SP70,FILTLTYP
          MOVE      SP70,FILTSTAT
          MOVE      ZERO,SHOWFLAG
.
          CALL      CLMLOL00                * Clear mlolnXXX
	  CALL      CLEMAT00                * Clear CGI EMAT Fields
          CALL      CLEMAP00                * Clear CGI EMAP Fields
.
CLRPAR99  RETURN
.
.------------------------------------------------------------
. Set Parameters
.------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updttype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDTTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nextpage=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NEXTPAGE
            GOTO      SETPAR99
          ENDIF 
.
          MATCH     "redirect=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REDIRECT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "mloln",QRYNAME
          IF        @EQUAL
            CALL      SPOLN000
            GOTO      SETPAR99
          ENDIF
.
	  MATCH     "emact",QRYNAME
          IF        @EQUAL
            CALL      SPEMAT00
            GOTO      SETPAR99
          ENDIF
.
	  MATCH     "emacp",QRYNAME
          IF        @EQUAL
            CALL      SPEMAP00
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filthosp=",QRYNAME
          IF        @EQUAL
            PACK      FILTHOSP,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtward=",QRYNAME
          IF        @EQUAL
            PACK      FILTWARD,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtltyp=",QRYNAME
          IF        @EQUAL
            PACK      FILTLTYP,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtstat=",QRYNAME
          IF        @EQUAL
            PACK      FILTSTAT,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "showflag=",QRYNAME
          IF        @EQUAL
            PACK      SHOWFLAG,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "norecord=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NORECORD
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startnum=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTNUM
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startkey=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTKEY
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
.
.------------------------------------------------------------
. Overdue Leave Notification Maintenance
.------------------------------------------------------------
OLNM0000  MATCH     SP70,UPDTTYPE
          GOTO      OLNM7000 IF EQUAL
.
          MATCH     "A",UPDTTYPE                 * Add
          GOTO      OLNM1000 IF EQUAL
.
          MATCH     "U",UPDTTYPE                 * Update
          GOTO      OLNM2000 IF EQUAL
.
          MATCH     "D",UPDTTYPE                 * Delete
          GOTO      OLNM3000 IF EQUAL
.
          GOTO      OLNM9000
.
OLNM1000  CALL      CLMLTOLN                     * Clear file vars
.
          PACK      KEY13,MLOLN001,MLOLN002,MLOLN003,MLOLN004,SP70
          CALL      RAMLOLN1                     * Check if already exisits
          COMPARE   ONE,OVRCD
          GOTO      OLNM9100 IF NOT EQUAL
.
          CALL      CALR0000                     * Calculate reminder number
.
          CALL      MVMLOL00                     * Move cgi to file vars
.
          PACK      MLOLCDAT,CCC,CYY,CMM,CDD
          REP       " 0",MLOLCDAT                * Date Created     
          CLOCK     TIME,MLOLCTIM                * Time Created
          MOVE      USERID,MLOLCUID              * Created By
.
          CALL      WRMLOLN1                     * Write record
.
          CALL      REOR0000                     * Re order reminder numbers
          CALL      SETG0000                     * Set greater than reminder
.
          PACK      KEY13,MLOLN001,MLOLN002,MLOLN003,MLOLN004,SP70
          CALL      RDMLOLN1                     * Check if already exisits
          BRANCH    OVRCD,OLNM9200

          MOVE      ZERO,EXIT
          GOTO      OLNM9999
.
OLNM2000  PACK      KEY13,MLOLN001,MLOLN002,MLOLN003,MLOLN004,SP70
          CALL      RDMLOLN1                     * Check if already exisits
          BRANCH    OVRCD,OLNM9200
.
          CALL      MVMLOL00                     * Move cgi to file vars
.
          PACK      MLOLUDAT,CCC,CYY,CMM,CDD
          REP       " 0",MLOLUDAT                * Date Updated     
          CLOCK     TIME,MLOLUTIM                * Time Updated
          MOVE      USERID,MLOLUUID              * Updated By
.
          CALL      UPMLOLN1                     * Update record
.
          PACK      KEY13,MLOLN001,MLOLN002,MLOLN003,MLOLN004,SP70
          CALL      RDMLOLN1                     * Check if already exisits
          BRANCH    OVRCD,OLNM9200
.
          MOVE      ZERO,EXIT
          GOTO      OLNM9999
.
OLNM3000  PACK      KEY13,MLOLN001,MLOLN002,MLOLN003,MLOLN004,SP70
          CALL      RDMLOLN1                     * Check if already exisits
          BRANCH    OVRCD,OLNM9200
.
          CALL      DEMLOLN1                     * Delete record
.
          CALL      REOR0000                     * Re order reminder numbers
          CALL      SETG0000                     * Set greater than reminder
.
          MOVE      ZERO,EXIT
          GOTO      OLNM9999
.
OLNM7000  PACK      KEY13,MLOLN001,MLOLN002,MLOLN003,MLOLN004,SP70
          CALL      RDMLOLN1                     * Read for update display
          IF        OVRCD=1
            CALL      CLMLTOLN                   * Clear file vars
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Overdue Leave Notification Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,OLNM9999
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      OLNM9999
.
OLNM9000  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      OLNM9999
.
OLNM9100  MOVE      "Overdue leave notification record exists.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      OLNM9999
.
OLNM9200  MOVE      "Overdue leave notification record does not exist.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      OLNM9999
.
OLNM9999  RETURN
.
.------------------------------------------------------------------------
. AAEC Codes Inbound Transport - Processing Logic for List, Add,
. Update and Delete.
.------------------------------------------------------------------------
AITM0000  MATCH     SP70,UPDTTYPE
          GOTO      AITM7000 IF EQUAL
.
          MATCH     "A",UPDTTYPE                      * Add
          GOTO      AITM1000 IF EQUAL
.
          MATCH     "U",UPDTTYPE                      * Update
          GOTO      AITM2000 IF EQUAL
.
          MATCH     "D",UPDTTYPE                      * Delete
          GOTO      AITM3000 IF EQUAL
.
          GOTO      AITM9000
.
.       ---------------------------------
.         Create New Record Sub-Routine
.       ---------------------------------
.
AITM1000  PACK      KEY38,EMACT001,EMACT002,SP70
          MOVE      EMACT002,VALFDATE       * Validate From date
          MOVE      EMACT003,VALTDATE       * Validate To date
          MOVE      SP70,ENDKEY38           * Auto end date key
          CALL      REVL0000                * Check for date overlap
          BRANCH    EXIT,AITM9300
.
          CALL      CLEMRACT                          * Clear File Variables
.
          PACK      KEY38,EMACT001,EMACT002,SP70
          CALL      RAEMACT1
          COMPARE   ONE,OVRCD
          GOTO      AITM9100 IF NOT EQUAL
.
          CALL      MVEMAT00
.
          PACK      EMATCDAT,CCC,CYY,CMM,CDD
          REP       " 0",EMATCDAT
          CLOCK     TIME,EMATCTIM
          MOVE      USERID,EMATCUID
.
          CALL      WREMACT1
.
          MATCH     SP70,ENDKEY38                * Auto end prev record
          IF        !@EQUAL
            MOVE      EMATFDAT,AUTOEDAT          * Set end date to 1 day
            SUB       ONE,AUTOEDAT               * prior to new records start
            PACK      KEY38,ENDKEY38,SP70
            CALL      RDEMACT1
            IF        OVRCD=0
              MATCH     SP70,EMATTDAT            * blank end date
              IF        @EQUAL
                MOVE      AUTOEDAT,EMATTDAT      * set end date
                CALL      UPEMACT1
              ENDIF
            ENDIF
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      AITM9999
.
.       -----------------------------
.         Update Record Sub-Routine
.       -----------------------------
.
AITM2000  PACK      KEY38,EMACT001,EMACT002,SP70 * Update record key
          MOVE      EMACT002,VALFDATE            * Validate from date
          MOVE      EMACT003,VALTDATE            * Validate to date
          CALL      REVL0000                     * Check for date overlap
          BRANCH    EXIT,AITM9300
.
          PACK      KEY38,EMACT001,EMACT002,SP70
          CALL      RDEMACT1
          BRANCH    OVRCD,AITM9200
.
          CALL      MVEMAT00
.
          PACK      EMATUDAT,CCC,CYY,CMM,CDD
          REP       " 0",EMATUDAT
          CLOCK     TIME,EMATUTIM
          MOVE      USERID,EMATUUID
.
          CALL      UPEMACT1
.
          MOVE      ZERO,EXIT
          GOTO      AITM9999
.
.       -----------------------------
.         Delete Record Sub-Routine
.       -----------------------------
.
AITM3000  PACK      KEY38,EMACT001,EMACT002,SP70
          CALL      RDEMACT1
          BRANCH    OVRCD,AITM9200
.
          CALL      DEEMACT1
.
          MOVE      ZERO,EXIT
          GOTO      AITM9999
.
.       -------------------------
.         List View Sub-Routine
.       -------------------------
.
AITM7000  PACK      KEY38,EMACT001,EMACT002,SP70
          CALL      RDEMACT1
          IF        OVRCD=1
            CALL      CLEMRACT
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "AECC Co-Efficient Codes: Inbound Transport Mode",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00
          BRANCH    EXIT,AITM9999
          CALL      WEBBOD00
          CALL      WEBEND00
.
          MOVE      ONE,EXIT
          GOTO      AITM9999
.
.       ------------------------------
.         Error Messages Sub-Routine
.       ------------------------------
.
AITM9000  MOVE      "Invalid Form Action.",WEBTITLE 
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      AITM9999
.
AITM9100  MOVE      "AECC Co-Efficient Code already exists.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      AITM9999
.
AITM9200  MOVE      "AECC Co-Efficient Code does not exist.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      AITM9999
.
AITM9300  MOVE      "Invalid Effective From/To Date (Overlap).",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      AITM9999
.
AITM9999  RETURN
.
.------------------------------------------------------------------------
. AAEC Codes Admitted Predictors - Processing Logic for List, Add,
. Update and Delete.
.------------------------------------------------------------------------
AAPM0000  MATCH     SP70,UPDTTYPE
          GOTO      AAPM7000 IF EQUAL
.
          MATCH     "A",UPDTTYPE                      * Add
          GOTO      AAPM1000 IF EQUAL
.
          MATCH     "U",UPDTTYPE                      * Update
          GOTO      AAPM2000 IF EQUAL
.
          MATCH     "D",UPDTTYPE                      * Delete
          GOTO      AAPM3000 IF EQUAL
.
          GOTO      AAPM9000
.
.       ---------------------------------
.         Create New Record Sub-Routine
.       ---------------------------------
.
AAPM1000  PACK      KEY38,EMACP001,EMACP002,SP70
          MOVE      EMACP002,VALFDATE       * Validate From date
          MOVE      EMACP003,VALTDATE       * Validate To date
          MOVE      SP70,ENDKEY38           * Auto end date key
          CALL      RPVL0000                * Check for date overlap
          BRANCH    EXIT,AAPM9300
.
          CALL      CLEMRACP                          * Clear File Variables.
.
          PACK      KEY38,EMACP001,EMACP002,SP70
          CALL      RAEMACP1
          COMPARE   ONE,OVRCD
          GOTO      AAPM9100 IF NOT EQUAL
.
          CALL      MVEMAP00
.
          PACK      EMAPCDAT,CCC,CYY,CMM,CDD
          REP       " 0",EMAPCDAT
          CLOCK     TIME,EMAPCTIM
          MOVE      USERID,EMAPCUID
.
          CALL      WREMACP1
.
          MATCH     SP70,ENDKEY38                * Auto end prev record
          IF        !@EQUAL
            MOVE      EMAPFDAT,AUTOEDAT          * Set end date to 1 day
            SUB       ONE,AUTOEDAT               * prior to new records start
            PACK      KEY38,ENDKEY38,SP70
            CALL      RDEMACP1
            IF        OVRCD=0
              MATCH     SP70,EMAPTDAT            * blank end date
              IF        @EQUAL
                MOVE      AUTOEDAT,EMAPTDAT      * set end date
                CALL      UPEMACP1
              ENDIF
            ENDIF
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      AAPM9999
.
.       -----------------------------
.         Update Record Sub-Routine
.       -----------------------------
.
AAPM2000  PACK      KEY38,EMACP001,EMACP002,SP70 * Update record key
          MOVE      EMACP002,VALFDATE            * Validate from date
          MOVE      EMACP003,VALTDATE            * Validate to date
          CALL      RPVL0000                     * Check for date overlap
          BRANCH    EXIT,AAPM9300
.
          PACK      KEY38,EMACP001,EMACP002,SP70
          CALL      RDEMACP1
          BRANCH    OVRCD,AAPM9200
.
          CALL      MVEMAP00
.
          PACK      EMAPUDAT,CCC,CYY,CMM,CDD
          REP       " 0",EMAPUDAT
          CLOCK     TIME,EMAPUTIM
          MOVE      USERID,EMAPUUID
.
          CALL      UPEMACP1
.
          MOVE      ZERO,EXIT
          GOTO      AAPM9999
.
.       -----------------------------
.         Delete Record Sub-Routine
.       -----------------------------
.
AAPM3000  PACK      KEY38,EMACP001,EMACP002,SP70
          CALL      RDEMACP1
          BRANCH    OVRCD,AAPM9200
.
          CALL      DEEMACP1
.
          MOVE      ZERO,EXIT
          GOTO      AAPM9999
.
.       -------------------------
.         List View Sub-Routine
.       -------------------------
.
AAPM7000  PACK      KEY38,EMACP001,EMACP002,SP70
          CALL      RDEMACP1
          IF        OVRCD=1
            CALL      CLEMRACP
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "AECC Co-Efficient Codes: Admitted Predictors",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00
          BRANCH    EXIT,AAPM9999
          CALL      WEBBOD00
          CALL      WEBEND00
.
          MOVE      ONE,EXIT
          GOTO      AAPM9999
.
.       ------------------------------
.         Error Messages Sub-Routine
.       ------------------------------
.
AAPM9000  MOVE      "Invalid Form Action.",WEBTITLE 
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      AAPM9999
.
AAPM9100  MOVE      "AECC Co-Efficient Code already exists.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      AAPM9999
.
AAPM9200  MOVE      "AECC Co-Efficient Code does not exist.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      AAPM9999
.
AAPM9300  MOVE      "Invalid Effective From/To Date (Overlap).",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      AAPM9999
.
AAPM9999  RETURN
.
.------------------------------------------------------------
. Process Fields 
.------------------------------------------------------------
PROFLD00  BRANCH    REPORTNO,PROFLD10,PROFLD20,PROFLD30
          GOTO      PROFLD99 
.
PROFLD10  BRANCH    FLDSETNO,PROFLD11,PROFLD12
          GOTO      PROFLD99
.
PROFLD11  CALL      MLOL0000                * MLTOLNFD tags
          GOTO      PROFLD99
.
PROFLD12  CALL      GTAG0000                * General tags
          GOTO      PROFLD99
.
PROFLD20  BRANCH    FLDSETNO,PROFLD21,PROFLD22  * reportno=2
          GOTO      PROFLD99
.
PROFLD21  CALL      EMAT0000                * EMRACTFD tags
          GOTO      PROFLD99
.
PROFLD22  CALL      GTAG0000                * General tags 
          GOTO      PROFLD99
.
PROFLD30  BRANCH    FLDSETNO,PROFLD31,PROFLD32  * reportno=3
          GOTO      PROFLD99
.
PROFLD31  CALL      EMAP0000                * EMRACPFD tags
          GOTO      PROFLD99
.
PROFLD32  CALL      GTAG0000                * General tags
          GOTO      PROFLD99
.
PROFLD99  RETURN
.
.------------------------------------------------------------------------
. General Tags 
. ------------------------------------------------------------------------
GTAG0000  BRANCH    FLDITMNO,GTAG0010,GTAG0020,GTAG0030,GTAG0040,GTAG0050:
                             GTAG0060,GTAG0070,GTAG0080,GTAG0090,GTAG0100:
                             GTAG0110

          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      GTAG9999
.
GTAG0010  CALL      HOSSEL00                * Hospital filter selection list
          GOTO      GTAG9999
.
GTAG0020  MOVE      FILTHOSP,HOSPCODE       * Hospital filter
          MOVE      FILTWARD,WARDCODE       * Ward filter
          CALL      WSEL0000                * Ward Select Options
          GOTO      GTAG9999
.
GTAG0030  MOVE      "TL",CATEGORY           * Leave type filter
          MOVE      FILTLTYP,CODE
          CALL      CODITM00
          GOTO      GTAG9999
.
GTAG0040  WRITE     HTMLFILE;FILTSTAT;      * Active status filter
          GOTO      GTAG9999
.
GTAG0050  WRITE     HTMLFILE;SHOWFLAG;      * Page displayed flag
          GOTO      GTAG9999
.
GTAG0060  CALL      LNTAB000                * Overdue leave notification list
          GOTO      GTAG9999
.
GTAG0070  CALL      NEXT0000                * Next Page
          GOTO      GTAG9999
.
GTAG0080  CALL      PREV0000                * Previous Page
          GOTO      GTAG9999
.
GTAG0090  CALL      ISMS0000                * SMS Selection list
          GOTO      GTAG9999
.
GTAG0100  CALL      EMATTB00                * AECC Codes for Inbound Transport
          GOTO      GTAG9999
.
GTAG0110  CALL      EMAPTB00                * AECC Codes for Admitted Predictors
          GOTO      GTAG9999
.
GTAG9999  RETURN
.
.------------------------------------------------------------
. Hospital Selection
.------------------------------------------------------------
HOSSEL00  PACK      KEY3,SP70
          CALL      RSPTHSP1
HOSSEL10  CALL      RKPTHSP1
          BRANCH    OVRCD,HOSSEL99
.
          PACK      KEY13,WBSEUID,SP70                * All hospital access
          CALL      RDMLSEC1
          IF        OVRCD=1
            PACK      KEY13,WBSEUID,PTHSHOSP,SP70     * This hospital access
            CALL      RDMLSEC1
            BRANCH    OVRCD,HOSSEL10
          ENDIF
.
          MOVE      PTHSNAME,KEY50
          PACK      KEY5,QUOTE,PTHSHOSP,QUOTE
          MATCH     FILTHOSP,PTHSHOSP
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=",KEY5," selected>":
                               KEY50,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=",KEY5,">",KEY50,"</option>"
          ENDIF
          GOTO      HOSSEL10
.
HOSSEL99  RETURN
.
.-----------------------------------------------------------
. Link to next page              
.-----------------------------------------------------------
NEXT0000  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F2
.
          REP       " +",FILTHOSP
          REP       " +",FILTWARD
          REP       " +",FILTLTYP
          REP       " +",FILTSTAT
.
          WRITE     HTMLFILE;"comweb09.pbl":
                                 "?reportno=",F2:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&filthosp=",FILTHOSP:
                                 "&filtward=",FILTWARD:
                                 "&filtltyp=",FILTLTYP:
                                 "&filtstat=",FILTSTAT;
.
          REP       "+ ",FILTHOSP
          REP       "+ ",FILTWARD
          REP       "+ ",FILTLTYP
          REP       "+ ",FILTSTAT
.
NEXT9999  RETURN
.
.-------------------------------------------------------------
. Link to previous page
.-------------------------------------------------------------
PREV0000  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3
          IF        F3<0
            MOVE      ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F2
.
          REP       " +",FILTHOSP
          REP       " +",FILTWARD
          REP       " +",FILTLTYP
          REP       " +",FILTSTAT
.
          WRITE     HTMLFILE;"comweb09.pbl":
                                 "?reportno=",F2:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&filthosp=",FILTHOSP:
                                 "&filtward=",FILTWARD:
                                 "&filtltyp=",FILTLTYP:
                                 "&filtstat=",FILTSTAT;
.
          REP       "+ ",FILTHOSP
          REP       "+ ",FILTWARD
          REP       "+ ",FILTLTYP
          REP       "+ ",FILTSTAT
.
PREV9999  RETURN
.
.------------------------------------------------------------
. Table of overdue leave notification records
.------------------------------------------------------------
LNTAB000  IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
. 
          CALL      LNHED000                 * Heading
.
          IF        IBCNMHOS=1
            PACK      KEY13,FILTHOSP,SP70
          ELSE
            PACK      KEY13,SP70
          ENDIF
.
          CALL      RSMLOLN1                 * Position on start
LNTAB100  CALL      RKMLOLN1                 * Read next
          BRANCH    OVRCD,LNTAB900
.
          IF        IBCNMHOS=1
            MATCH     SP70,FILTHOSP          * Hospital filter
            IF        !@EQUAL
              MATCH     FILTHOSP,MLOLHOSP
              GOTO      LNTAB900 IF NOT EQUAL
            ENDIF
          ENDIF
.
          MATCH     SP70,FILTWARD            * Ward filter
          IF        !@EQUAL
            MATCH     FILTWARD,MLOLWARD
            GOTO      LNTAB100 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,FILTLTYP            * Leave type filter
          IF        !@EQUAL
            MATCH     FILTLTYP,MLOLLTYP
            GOTO      LNTAB100 IF NOT EQUAL
          ENDIF
.
          MATCH     "0",FILTSTAT            * Active filter
          IF        @EQUAL
            MATCH     "1",MLOLIACT
            GOTO      LNTAB100 IF EQUAL
          ENDIF
.
          MATCH     "1",FILTSTAT            * Inactive filter
          IF        @EQUAL
            MATCH     "1",MLOLIACT
            GOTO      LNTAB100 IF NOT EQUAL
          ENDIF
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      LNTAB100
          ENDIF
.
          CALL      LNROW000                * Display Dep.code Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      LNTAB100 IF NOT EQUAL
          GOTO      LNTAB999
.
LNTAB900  CALL      NOMORE00                * Display No More Records Message
.
LNTAB999  RETURN
.
.------------------------------------------------------------------------
. Drawing Table List of Codes for Inbound Transport
.------------------------------------------------------------------------
EMATTB00  IF        NORECORD=0
            MOVE      "20",NORECORD
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          CALL      EMATLH00               * Generate Table Header
.
          MATCH     SP70,STARTKEY
          IF        @EQUAL
            PACK      KEY38,SP70
          ELSE
            PACK      KEY38,STARTKEY,SP70
          ENDIF
.
          CALL      RSEMACT1
EMATTB01  CALL      RKEMACT1
          BRANCH    OVRCD,EMATTB99
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      EMATTB01
          ENDIF
.
	  CALL      EMATLD00               * Generate Table Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      EMATTB01 IF NOT EQUAL
.
EMATTB99  RETURN
.
.------------------------------------------------------------------------
. Drawing Table List of Codes for Inbound Transport
.------------------------------------------------------------------------
EMAPTB00  IF        NORECORD=0
            MOVE      "20",NORECORD
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          CALL      EMAPLH00               * Generate Table Header
.
          MATCH     SP70,STARTKEY
          IF        @EQUAL
            PACK      KEY38,SP70
          ELSE
            PACK      KEY38,STARTKEY,SP70
          ENDIF
.
          CALL      RSEMACP1
EMAPTB01  CALL      RKEMACP1
          BRANCH    OVRCD,EMAPTB99
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      EMAPTB01
          ENDIF
.
	  CALL      EMAPLD00               * Generate Table Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      EMAPTB01 IF NOT EQUAL
.
EMAPTB99  RETURN
.
.------------------------------------------------------------
. Overdue leave notification table heading
.------------------------------------------------------------
LNHED000  PACK      KEY5,ANST,ANSL,SP70 
          CALL      RDCODE1    
          IF        OVRCD=1
            MOVE      "Type of Leave",PTCDLDES
          ENDIF
.
          WRITE     HTMLFILE;"<tr>";
.
          IF        IBCNMHOS=1
            WRITE     HTMLFILE;"<td class=HeadingCell>Hospital</td>";
          ENDIF
.
          WRITE     HTMLFILE;"<td class=HeadingCell>Ward</td>":
                             "<td class=HeadingCell>",PTCDLDES,"</td>":
                             "<td class=HeadingCell width=10%>":
                             "Overdue Minutes</td>":
                             "<td class=HeadingCell width=10%>":
                             "Greater Than Overdue Minutes</td>":
                             "<td class=HeadingCell>SMS</td>":
                             "<td class=HeadingCell width=5%>":
                             "Reminder Number</td>":
                             "<td class=HeadingCell width=5%>Active</td>":
                             "</tr>"
.
LNHED999  RETURN
.
.------------------------------------------------------------------------
. Table Header - Co-Efficient Codes For Inbound Transport Header
.------------------------------------------------------------------------
EMATLH00  WRITE     HTMLFILE;"<tr>";
          WRITE     HTMLFILE;"<td class=HeadingCell>ECDG Code</td>":
                             "<td class=HeadingCell>Admitted - Yes</td>":
                             "<td class=HeadingCell>Discharge Deceased</td>":
                             "<td class=HeadingCell>Left at Own Risk</td>":
                             "<td class=HeadingCell>Referred Other Hospital":
                             "</td>":
                             "<td class=HeadingCell>Arrival By Ambulance</td>":
			     "<td class=HeadingCell>Triage Category 1</td>":
                             "<td class=HeadingCell>Triage Category 2</td>":
                             "<td class=HeadingCell>Triage Category 3</td>":
                             "<td class=HeadingCell>Triage Category 4</td>":
                             "<td class=HeadingCell>Triage Category 5</td>":
                             "<td class=HeadingCell>Date Effective From</td>":
                             "<td class=HeadingCell>Date Effective To</td>":
                             "<td class=HeadingCell>Active</td>"
          WRITE     HTMLFILE;"</tr>";

EMATLH99  RETURN
.
.------------------------------------------------------------------------
. Table Header - Co-Efficient Codes Header
.------------------------------------------------------------------------
EMAPLH00  WRITE     HTMLFILE;"<tr>";
          WRITE     HTMLFILE;"<td class=HeadingCell>ECDG Code</td>":
                             "<td class=HeadingCell>Age 0 to 14 Years</td>":
                             "<td class=HeadingCell>Age 80+ Years</td>":
                             "<td class=HeadingCell>Triage 1</td>":
                             "<td class=HeadingCell>Triage 2</td>":
                             "<td class=HeadingCell>Triage 3</td>":
                             "<td class=HeadingCell>Triage 4</td>":
                             "<td class=HeadingCell>Triage 5</td>":
                             "<td class=HeadingCell>Date Effective From</td>":
                             "<td class=HeadingCell>Date Effective To</td>":
                             "<td class=HeadingCell>Active</td>"
          WRITE     HTMLFILE;"</tr>";

EMAPLH99  RETURN
.
.------------------------------------------------------------
. Overdue leave notification table rows
.------------------------------------------------------------
LNROW000  PACK      KEY3,MLOLHOSP,SP70
          CALL      RDPTHSP1
          IF        OVRCD=1
            MOVE      MLOLHOSP,PTHSNAME
          ENDIF
.
          MATCH     SP70,MLOLWARD
          IF        @EQUAL
              MOVE      "All",WBDESC
          ELSE
            PACK      KEY6,MLOLWARD,SP70
            CALL      RDWARD1
            IF        OVRCD=1
              MOVE      MLOLWARD,WBDESC
            ENDIF
          ENDIF
.
          MOVE      "TL",CATEGORY
          MOVE      MLOLLTYP,CODE
          CALL      GETCOD00
.
          PACK      KEY6,MLOLSMSM,SP2,ZERO,SP70
          CALL      RDPTLET1
          IF        OVRCD=1
            MOVE      KEY6,PTLETEXT
          ENDIF
.
          MATCH     "1",MLOLGOVM            * Display greater thn overdue min
          IF        @EQUAL
            MOVE      "Yes",DISPGOVM
          ELSE
            MOVE      "No",DISPGOVM
          ENDIF
.
          MATCH     "1",MLOLIACT            * Active
          IF        @EQUAL
            MOVE      "Inactive",ACTIDESC
          ELSE
            MOVE      "Active",ACTIDESC
          ENDIF
.
          IF        IBCNMHOS=1
            WRITE     HTMLFILE;"<tr><td nowrap><a href='javascript:UpdLeave":
                               "(#"comweb09.pbl":
                               "?reportno=01":
                               "&template=003":
                               "&mloln001=",MLOLHOSP:
                               "&mloln002=",MLOLWARD:
                               "&mloln003=",MLOLLTYP:
                               "&mloln004=",MLOLOMIN,"#")'>":
                               "<img src=../images/MaintenanceIcon.gif ":
                               "class=Icon hspace=4></a>":
                               PTHSNAME,"</td>":
                               "<td>",WBDESC,"</td>";
          ELSE
            WRITE     HTMLFILE;"<tr><td nowrap><a href='javascript:UpdLeave":
                               "(#"comweb09.pbl":
                               "?reportno=01":
                               "&template=003":
                               "&mloln001=",MLOLHOSP:
                               "&mloln002=",MLOLWARD:
                               "&mloln003=",MLOLLTYP:
                               "&mloln004=",MLOLOMIN,"#")'>":
                               "<img src=../images/MaintenanceIcon.gif ":
                               "class=Icon hspace=4></a>":
                               WBDESC,"</td>";
          ENDIF
.
          WRITE     HTMLFILE;"<td>",TDESC,"</td>":
                             "<td align=center>",MLOLOMIN,"</td>":
                             "<td align=center>",DISPGOVM,"</td>":
                             "<td>",PTLETEXT,"</td>":
                             "<td align=center>",MLOLREMN,"</td>":
                             "<td align=center>",ACTIDESC,"</td>":
                             "</tr>"
.
LNROW999 RETURN
.
.------------------------------------------------------------------------
. Table Data Rows - Inbound Transport Codes Data
.------------------------------------------------------------------------
.
.         -------------------------------
.           Display Effective From Date
.         -------------------------------

EMATLD00  UNPACK    EMATFDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DISPFDAT,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
.         -----------------------------
.           Display Effective To Date
.         -----------------------------
.
          MATCH     SP70,EMATTDAT
          IF        !@EQUAL
.
            UNPACK    EMATTDAT,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      DISPTDAT,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
            ELSE     
            MOVE      SP70,DISPTDAT
.
          ENDIF
.
.         ----------------------------------
.           Display Active / Inactive Flag
.         ----------------------------------
.
          MATCH     "0",EMATAFLG
          IF        @EQUAL
            MOVE    "Yes",DISPAFLG
          ELSE
            MOVE    "No",DISPAFLG
          ENDIF
.
.         --------------------------------------------
.           Write the HTML File to Output Table Data
.         --------------------------------------------
.
          WRITE     HTMLFILE;"<tr>"
.
          WRITE     HTMLFILE;"<td nowrap><a href='javascript:UpdCode":
                               "(#"comweb09.pbl":
                               "?reportno=02":
                               "&template=003":
                               "&emact001=",EMATCODE:
                               "&emact002=",DISPFDAT:
                               "#")'>":
                               "<img src=../images/MaintenanceIcon.gif ":
                               "class=Icon hspace=4></a>&nbsp;":
                                EMATCODE,"</td>":
                             "<td nowrap>",EMATADMF,"</td>":
                             "<td nowrap>",EMATDSDC,"</td>":
                             "<td nowrap>",EMATRABA,"</td>":
                             "<td nowrap>",EMATRTAH,"</td>":
                             "<td nowrap>",EMATABAM,"</td>":
                             "<td nowrap>",EMATTCT1,"</td>":
                             "<td nowrap>",EMATTCT2,"</td>":
                             "<td nowrap>",EMATTCT3,"</td>":
                             "<td nowrap>",EMATTCT4,"</td>":
                             "<td nowrap>",EMATTCT5,"</td>":
                             "<td nowrap>",DISPFDAT,"</td>":
                             "<td nowrap>",DISPTDAT,"</td>":
                             "<td nowrap>",DISPAFLG,"</td>":
                             "</tr>"
.
EMATLD99  RETURN
.
.------------------------------------------------------------------------
. Table Data Rows - Admitted Predictor Codes Data
.------------------------------------------------------------------------
.
.         -------------------------------
.           Display Effective From Date
.         -------------------------------

EMAPLD00  UNPACK    EMAPFDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DISPFDAT,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
.         -----------------------------
.           Display Effective To Date
.         -----------------------------
.
          MATCH     SP70,EMAPTDAT
          IF        !@EQUAL
.
            UNPACK    EMAPTDAT,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      DISPTDAT,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          ELSE
            MOVE    SP70,DISPTDAT
          ENDIF
.
.         ----------------------------------
.           Display Active / Inactive Flag
.         ----------------------------------
.
          MATCH     "0",EMAPAFLG
          IF        @EQUAL
            MOVE    "Yes",DISPAFLG
          ELSE
            MOVE    "No",DISPAFLG
          ENDIF
.
.         --------------------------------------------
.           Write the HTML File to Output Table Data
.         --------------------------------------------
.
          WRITE     HTMLFILE;"<tr>"
.
          WRITE     HTMLFILE;"<td nowrap><a href='javascript:UpdCode":
                               "(#"comweb09.pbl":
                               "?reportno=03":
                               "&template=003":
                               "&emacp001=",EMAPCODE:
                               "&emacp002=",DISPFDAT:
                               "#")'>":
                               "<img src=../images/MaintenanceIcon.gif ":
                               "class=Icon hspace=4></a>&nbsp;":
                                EMAPCODE,"</td>":
                             "<td nowrap>",EMAPAGE1,"</td>":
                             "<td nowrap>",EMAPAGE2,"</td>":
                             "<td nowrap>",EMAPTCT1,"</td>":
                             "<td nowrap>",EMAPTCT2,"</td>":
                             "<td nowrap>",EMAPTCT3,"</td>":
                             "<td nowrap>",EMAPTCT4,"</td>":
                             "<td nowrap>",EMAPTCT5,"</td>":
                             "<td nowrap>",DISPFDAT,"</td>":
                             "<td nowrap>",DISPTDAT,"</td>":
                             "<td nowrap>",DISPAFLG,"</td>":
                             "</tr>"
.
EMAPLD99  RETURN
.
.------------------------------------------------------------
. Ward selection
.------------------------------------------------------------
WSEL0000  PACK      KEY29,SP3,FILTHOSP,SP70
          CALL      RDSWARD7
WSEL0100  CALL      RDKWARD7
          BRANCH    OVRCD,WSEL9999
.
          MATCH     SP70,WBED                    * End of ward master records
          GOTO      WSEL9999 IF NOT EQUAL
.
          COMPARE   WACTIVE,ZERO                 * List only active wards
          GOTO      WSEL0100 IF NOT EQUAL
.
          MATCH     SP70,FILTHOSP
          IF        !@EQUAL
            MATCH     FILTHOSP,PTWDHOSN          * Filter by hospital
            GOTO      WSEL9999 IF NOT EQUAL
          ENDIF
.
          WRITE     HTMLFILE;"<option value=#"",WWARD,"#"";
.
          MATCH     WARDCODE,WWARD
          IF        @EQUAL
            WRITE     HTMLFILE;" selected";
          ENDIF
          WRITE     HTMLFILE;">",WBDESC,"</option>"
.
          GOTO      WSEL0100
.
WSEL9999  RETURN
.
.------------------------------------------------------------
. IP SMS Selection
.------------------------------------------------------------
ISMS0000  MOVE      ZERO,PTLELINO
          PACK      KEY6,PTLELINO,SP70
          CALL      RSPTLET2
ISMS1000  CALL      RKPTLET2
          BRANCH    OVRCD,ISMS9999
.
          COMPARE   ZERO,PTLELINO
          GOTO      ISMS9999 IF NOT EQUAL
.
          MATCH     "1",PTLEACTV                 * Skip Inactive
          GOTO      ISMS1000 IF EQUAL
.
          MATCH     "1",PTLECOMM                 * Skip if not SMS
          GOTO      ISMS1000 IF NOT EQUAL
.
          WRITE     HTMLFILE;"<option value=#"",DPTLELNO,"#">":
                             PTLETEXT,"</option>"
.
          GOTO      ISMS1000
.
ISMS9999  RETURN
.
.------------------------------------------------------------
. Calculate the reminder number based on the hospial,
. ward and leave type
.------------------------------------------------------------
CALR0000  MOVE      ONE,REMINDER                 * Set reminder number
.
          PACK      KEY13,MLOLN001,MLOLN002,MLOLN003,Z70
          CALL      RSMLOLN1                     * Check if already exisits
          CALL      RPMLOLN1                    
          BRANCH    OVRCD,CALR9000
.
          MATCH     MLOLN001,MLOLHOSP            * Same hospital
          GOTO      CALR9000 IF NOT EQUAL
.
          MATCH     MLOLN002,MLOLWARD            * Same ward
          GOTO      CALR9000 IF NOT EQUAL
.
          MATCH     MLOLN003,MLOLLTYP            * Same leave type
          GOTO      CALR9000 IF NOT EQUAL
.
          SQUEEZE   MLOLREMN
          MOVE      MLOLREMN,REMINDER
          ADD       ONE,REMINDER                 * Add one to reminder number
          RESET     MLOLREMN
.
CALR9000  MOVE      REMINDER,MLOLN007            * Set reminder number cgi
.
CALR9999  RETURN
.
.------------------------------------------------------------
. Calculate the reminder number based on the hospial,
. ward and leave type
.------------------------------------------------------------
REOR0000  MOVE      ONE,REMINDER                 * Set reminder number
.
          PACK      KEY13,MLOLN001,MLOLN002,MLOLN003,SP70
          CALL      RSMLOLN1                     * Check if already exisits
REOR1000  CALL      RKMLOLN1
          BRANCH    OVRCD,REOR9999
.
          MATCH     MLOLN001,MLOLHOSP            * Same hospital
          GOTO      REOR9999 IF NOT EQUAL
.
          MATCH     MLOLN002,MLOLWARD            * Same ward
          GOTO      REOR9999 IF NOT EQUAL
.
          MATCH     MLOLN003,MLOLLTYP            * Same leave type
          GOTO      REOR9999 IF NOT EQUAL
.
          MOVE      REMINDER,MLOLREMN            * Update reminder number
.
          CALL      UPMLOLN1                     * Update record
.
          ADD       ONE,REMINDER                 * Increase reminder number
          GOTO      REOR1000
.
REOR9999  RETURN
.
.------------------------------------------------------------
. Set the last record number greater than minutes based in the
. hospital ward and leave type
.------------------------------------------------------------
SETG0000  MOVE      ONE,LASTFLAG                 * Set last record to yes
.
          PACK      KEY13,MLOLN001,MLOLN002,MLOLN003,Z70
          CALL      RSMLOLN1                     * Check if already exisits
SETG1000  CALL      RPMLOLN1
          BRANCH    OVRCD,SETG9999
.
          MATCH     MLOLN001,MLOLHOSP            * Same hospital
          GOTO      SETG9999 IF NOT EQUAL
.
          MATCH     MLOLN002,MLOLWARD            * Same ward
          GOTO      SETG9999 IF NOT EQUAL
.
          MATCH     MLOLN003,MLOLLTYP            * Same leave type
          GOTO      SETG9999 IF NOT EQUAL
.
          IF        LASTFLAG=1
            MOVE      ONE,MLOLGOVM               * Yes highest Overdue Minutes
            MOVE      ZERO,LASTFLAG
          ELSE
            MOVE      ZERO,MLOLGOVM
          ENDIF
.
          CALL      UPMLOLN1                     * Update record
          GOTO      SETG1000
.
SETG9999  RETURN
.------------------------------------------------------------
. NOMORE00 - Display "No more records" at end of list
.------------------------------------------------------------
NOMORE00  WRITE     HTMLFILE;"<tr>":
                             "<td class=RedCell colspan=20 align=center>":
                             "No more records</td>"
.
NOMORE99  RETURN
.
.------------------------------------------------------------
.
          INC       IBAWEBCD
          INC       WEBDATCD
          INC       DAYOFWEK
.
          INC       CLMLTOLN
          INC       MLTOLNTM
.
          INC       CLEMRACT
          INC       EMRACTTM
.
          INC       CLEMRACP
          INC       EMRACPTM
          INC       EMRACTVL 
          INC       EMRACPVL
.
          INC       MLTOLNIO/INC
          INC       MLTSECIO/INC
          INC       PATHSPIO/INC
          INC       PATILTIO/INC
          INC       PATWR1IO/INC
          INC       EMRACTIO/INC
          INC       EMRACPIO/INC

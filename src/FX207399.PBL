.***************************************************************************
.* System    :   Theatre System                                            *
.* Program   :   FX207399                                                  *
.***************************************************************************
.* Date      :   28/10/2009                                                *
.* Author    :   Davin Sloan                                               *
.* Mods      :                                                             *
.*              V9.12.00  28/10/2009  Davin         CAR 207399             *
.*                        Created program - Write comments in bkrxuff4 to  *
.*                                          oprdedaf comment type 011.     *
.***************************************************************************
.
          INC       STD001FD
.
.
. FILE DESCRIPTION INCLUDES
. -------------------------
.
          INC       BOKRX1FD/INC
          INC       OPRDEAFD/INC
          INC       OPRDEDFD/INC
.
COUNTER   FORM      5
.
PRGID     INIT      "FX207399"
PRGNAM    INIT      "Write bkrxuff4 to oprdedaf "
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select FROM and TO
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
.
          CALL      PROC0000               * process records for update
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                     * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"bokrx1af";
          OPEN      BOKRX1A1,"bokrx1af"          * booking extension file
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"oprdetaf";
          OPEN      OPRDETA2,"oprdetaf"          * theatre details file
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"oprdedaf";
          OPEN      OPRDEDA1,"oprdedaf"          * theatre comments file
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Valid option selected                  *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Update oprdedaf with bkrxuff4"
.
          DISPLAY   *P1:11,*EF,*V2LON,"WARNING: PLEASE BACKUP oprdedaf BEFORE RUNNING THIS PROGRAM."
.
OPTN1000  KEYIN     *P1:7,*EL,"Select option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION
          GOTO      OPTN9500 IF EQUAL                 * exit
.
          BRANCH    COPTION,OPTN2000
.
          BEEP
          GOTO      OPTN1000
.
OPTN2000  CALL      CONTQST                          * Ok to continue ?
          BRANCH    CEXIT,OPTN9000,OPTN1000,OPTN9500 * Yes, no, cancel
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.****************************************************************************
.*                                PROC0000            Called by: ML0000     *
.*              read through bokrx1af                                       *
.****************************************************************************
PROC0000  MOVE      ZERO,COUNTER
          DISPLAY   *P1:24,*EL,"Processing BOK Number: ";
          MOVE      SP10,KEY8
          CALL      RSBKRX11                     * position at start of file
PROC1000  CALL      RKBKRX11                     * read next record
          BRANCH    OVRCD,PROC9000               * end of file
.
          DISPLAY   *P30:24,*EL,*V2LON,BKRXBNUM;
.
          MATCH     SP70,BKRXUFF4
          GOTO      PROC1000 IF EQUAL
.
.         loop through oprdet to get unique number for oprdedaf
.
          PACK      KEY31,BKRXBNUM,SP70
          CALL      RSOPDEA2                     * position at start of file
PROC5000  CALL      RKOPDEA2                     * read next record
          BRANCH    OVRCD,PROC1000               * end of file
.
          MATCH     BKRXBNUM,OPDAADMN
          GOTO      PROC1000 IF NOT EQUAL        * same admn?
.
          CALL      UDED0000                     * write comments to oprdedaf
.
          GOTO      PROC1000                     * get next booking number
.
PROC9000  DISPLAY   *P1:20,*EL,COUNTER," oprdedaf records written.  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
.****************************************************************************
.*                                UDED0000                                  *
.*              update oprdedaf with bkrxuff4                               *
.****************************************************************************
UDED0000  MOVE        "011",KEY3
          PACK        KEY16,OPDAUNIQ,KEY3,SP2,ONE,SP70
          CALL        RDOPDED1
          IF          OVRCD=1
            MOVE        OPDAUNIQ,OPDDUNIQ
            MOVE        "011",OPDDTYPE
            PACK        OPDDLINE,SP2,ONE
            MOVE        BKRXUFF4,OPDDDATA
            CALL        WROPDED1
            ADD         ONE,COUNTER
          ENDIF
.
UDED9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       BOKRX1IO/INC
          INC       OPRDEAIO/INC
          INC       OPRDEDIO/INC

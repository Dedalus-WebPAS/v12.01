.***************************************************************************
.* System    :   Inpatients                                                *
.* Program   :   FX244504                                                  *
.* Desc      :   Update nzpspraf theatre unique value                      *
.***************************************************************************
.* Date      :   28/06/2011                                                *
.* Author    :   Jill Habasque                                             *
.* Function  :   This program will fix corrupt nzspunth records            *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       NZPSPRFD/INC
          INC       OPRDEAFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
FOUNDAD   DIM       10
FOUNDFLG  FORM      1
COUNTER   FORM      8
.
PRGID     INIT      "FX222655"
PRGNAM    INIT      "Update nzspuniq              "
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      NZPSPRA1,"nzpspraf"
.
          OPEN      OPRDETA2,"oprdetaf"
          OPEN      OPRDETA3,"oprdetaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run":
                    *P1:6,*V2LON,TWO,*HOFF,". Print Details Only"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000,OPTN9000    * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  DISPLAY   *P1:20,*EL,"Record No. : ";
          MOVE      ZERO,COUNTER
          PACK      KEY11,SP70
          CALL      RSNZSPR1
PROC0100  CALL      RKNZSPR1
          BRANCH    OVRCD,PROC9000
.
          ADD       ONE,COUNTER
          IF        (COUNTER%100) = 0
            DISPLAY   *P15:20,*V2LON,COUNTER
          ENDIF
.
          MATCH     "0",NZSPINVD
          GOTO      PROC0100 IF NOT EQUAL
.
          MATCH     SP70,NZSPUNTH
          GOTO      PROC0150 IF EQUAL
.
          PACK      KEY10,NZSPUNTH,SP70
          CALL      RDOPDEA3
          IF        OVRCD=1
            GOTO      PROC0150
          ENDIF
.
          MATCH     NZSPADMN,OPDAADMN
          GOTO      PROC0100 IF EQUAL
.
PROC0150  MOVE      ZERO,FOUNDFLG
          MOVE      SP70,FOUNDAD
.
          PACK      KEY31,NZSPADMN,SP70
          CALL      RSOPDEA2
PROC0200  CALL      RKOPDEA2
          BRANCH    OVRCD,PROC0300
.
          MATCH     OPDAADMN,NZSPADMN
          GOTO      PROC0300 IF NOT EQUAL
.
          COMPARE   THREE,OPDASTAT
          GOTO      PROC0200 IF EQUAL
.
          PACK      FOUNDAD,OPDAUNIQ
          ADD       ONE,FOUNDFLG
          GOTO      PROC0200
.
PROC0300  COMPARE   ZERO,FOUNDFLG
          GOTO      PROC0100 IF EQUAL
.
          COMPARE   ONE,FOUNDFLG
          IF        @EQUAL
            PACK      NZSPUNTH,FOUNDAD,SP70
            IF        COPTION=2
              PRINT     "Updating Admission ",NZSPADMN," with ",NZSPUNTH
            ELSE
              PRINT     "Updating Admission ",NZSPADMN," with ",NZSPUNTH
              CALL      UPNZSPR1
            ENDIF
            GOTO      PROC0100
          ENDIF
.
          PRINT     "Multiple bookings for Admission ",NZSPADMN
          GOTO      PROC0100
.
PROC9000  DISPLAY   *P1:24,*EL,*B,"Finished processing.  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       NZPSPRIO/INC
          INC       OPRDEAIO/INC
          INC       STD001IO

.*****************************************************************************
.*    Program      : IBAMEH54                                                *
.*    Description  : Change of Legal Status/Diagnosis report                 *
.*                                                                           *
.*    Author       : ROD    (11/12/91)                                       *
.*    Function     : List all MH patients who have had a change of legal     *
.*                   status or diagnosis on the specified date               *
.*                                                                           *
.*    Modifications:                                                         *
.*****************************************************************************
.* V12.00.01 28/05/2025 Thanh T         TSK 0955096                          *
.*           Changed for alphanumeric visit number                           *
.*****************************************************************************
.*        V10.05.01 01/01/2015  Ania P                      CAR 261630       *
.*                  Removed use of PORT for temp file naming                 *
.*        V10.01.02 10/02/2011  Jill Parkinson CAR 233088                    *
.*                  Added pmsvx1af                                           *
.*        V10.00.01 31/03/2010  Mike Laming  CAR 217575                      *
.*                  Recompiled for changes to MEHDIAFD                       *
.*****************************************************************************
.*        V9.02.01  15/01/2003  Jill Habasque SRF 35341                      *
.*                  Fixed branch to load1000                                 *
.*        V9.02.00  26/10/2002  Lina Vo     SRF 22733                        *
.*                  Ported to V9                                             *
.*****************************************************************************
.*        V5.08.01  28/08/2000  Caleb Sharman                                *
.*                  Changed Health Fund variables to be 8 chars              *
.*        V5.08.B01 30/12/1999  Steve Armstrong                              *
.*                  Fixed global recompile bugs                              *
.*****************************************************************************
.*       V5.07.B02 14/01/1999  Jim Stathopoulos                              *
.*                 Date fixed for Report                                     *
.*        V5.07.00 27/10/1998  Davin                                         *
.*                 Mods for 507                                              *
.*        V5.05.01 22/09/1997  Steve Armstrong                               *
.*                 Mods for AXIS V to be a free text description.            *
.*                 (MEHRVDIA)                                                *
.*****************************************************************************
.*        V5.04.02  10/01/1997  Steve Armstrong   MEH Changes                *
.*                  Mods to cater for new CMH MHVISTAT statuses              *
.*        V5.04.01  07/11/1996  Howellsy                                     *
.*                  Added Review Time & Legal Status Time.                   *
.*****************************************************************************
.*                   V5.00.002  29/07/92  ROD                                *
.*                              Use "R" on tran for leave type. ZZZ now zzz  *
.*                                                                           *
.*****************************************************************************
.
          INC       STD001FD
          INC       MEHVI1FD/INC
          INC       MEHDIAFD/INC
          INC       MEHLEGFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATDSCFD/INC
          INC       PATCODFD/INC
          INC       PATTRNFD/INC
          INC       IBASEQFD/INC  
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
.
CMDLINE   DIM       80
CODE6     DIM       6
CURRDATE  DIM       8
DRDATE    DIM       10
DSADMD    DIM       10
DSDIAD    DIM       10
DSLSD     DIM       10
DSRECD    DIM       10
FNDTL     FORM      1
ICFLAG    FORM      1
LSHDP     DIM       4
LSFLAG    FORM      1
RDATE     DIM       8
SAVLTYP   DIM       3
SAVTDATE  DIM       8
ZED20     INIT      "zzzzzzzzzzzzzzzzzzzz"
.
. Temp file vars
MEHTMPA1  IFILE     SQL, FIXED=193, KEY="U1-8,9-10"
XXURNO    DIM       8         1       U/R Number
DXXUNIQ   DIM       2         9       Unique counter
XXNAME    DIM       28       11       Patients Name
XXADMD    DIM       8        39       Admission date
XXOLSTC   DIM       2        47       Original Legal Status (HDP equiv)
XXOLSTD   DIM       20       49       Original Legal Status Description
XXNLSTC   DIM       2        69       New Legal Status (HDP equiv)
XXNLSTD   DIM       20       71       New Legal Status Description
XXDATLS   DIM       8        91       Date of legal status change
XXRECD    DIM       8        99       Reception order date
XXODIAC   DIM       9       107       Original Diagnosis code
XXODIAD   DIM       30      116       Original Diagnosis Description
XXNDIAC   DIM       9       146       New Diagnosis code
XXNDIAD   DIM       30      155       New Diagnosis Description
XXDNDIA   DIM       8       185       Date of new diagnosis
.              Rec length   193
XXUNIQ    FORM      2
.
PRGID     INIT      "IBAMEH54"
PRGNAM    INIT      "Hss Prs986 - Change Of Status Or Diagnosis Report"
VERSION   INIT      "V12.01.00"
.
.****************************************************************************
.* ML0000  :  Mainline code                                                 *
.****************************************************************************
ML0000    CALL      INIT0000                      * initialisation
ML0500    CALL      CRET0000                      * create temp file
.
ML1000    CALL      MENU0000                      * get menu options
          BRANCH    EXIT,ML9999                   * Exit selected
.
ML2000    CALL      DATE0000                      * Get Date
          BRANCH    EXIT,ML1000                   * nothing input
.
          CALL      CONTQST                       * Ask Continue?
          BRANCH    CEXIT,ML5000,ML2000,ML1000    * Yes, No, Cancel
.
ML5000    CALL      LOAD0000                      * load data into temp file
.
          CALL      RPRT0000                      * Print Report
          CALL      CRET0000                      * erase temp file
          GOTO      ML2000
.
ML9999    CHAIN     PGM
+
.****************************************************************************
.* INIT0000  :  Initialisation                                              *
.****************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P60:24,"controlf"
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P60:24,"pattranf"
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P60:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P60:24,"mehlegaf"
          OPEN      MEHLEGA1,"mehlegaf"
.
          DISPLAY   *P60:24,"mehdiaaf"
          OPEN      MEHDIAA1,"mehdiaaf"
.
          DISPLAY   *P60:24,"mehvi1af"
          OPEN      MEHVI1A1,"mehvi1af"
.
          DISPLAY   *P60:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P60:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P60:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          CALL      TFILENAM 
.
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
INIT9999  RETURN
+
.****************************************************************************
.* MENU0000  :  Main Menu                                                   *
.****************************************************************************
MENU0000  DISPLAY   *P1:4,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = By U/R Number"
.
MENU1000  KEYIN     *P1:7,"Select Option ? ":
                    *P17:7,*DV,UNDLN1:
                    *P17:7,*V2LON,OPTION
.
          IF        OPTION = 0
                      MOVE   ONE,EXIT
                      GOTO   MENU9999
          ENDIF
.
          MOVE      ZERO,EXIT
          BRANCH    OPTION,MENU9999
          BEEP
          GOTO      MENU1000
.
MENU9999  RETURN
+
.****************************************************************************
.* DATE0000  :  Get the Report date                                         *
.****************************************************************************
DATE0000  DISPLAY   *P1:24,*EL,*P1:9,"Enter the report date : "
.
DATE1000  MOVE      NINE,CVERT
          MOVE      "25",CCOL
          MOVE      CCC,CCENT
          UNPACK    SP10,CYEAR,CMON,CDAY
          MOVE      ONE,CDEFDTE
.
          CALL      KEYDATE
          BRANCH    CQUEST,DATE1000
          BRANCH    OVRCD,DATE9000
.
          PACK      RDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",RDATE
.
          MATCH     CURRDATE,RDATE
          GOTO      DATE8000 IF LESS
          GOTO      DATE8000 IF EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"Date must not be into future.  ";
          CALL      HITENTER
          GOTO      DATE1000
.
DATE8000  CALL      PACDATE
          MOVE      CPCDATE,DRDATE                 * display var
          MOVE      ZERO,EXIT
          GOTO      DATE9999
.
DATE9000  MOVE      ONE,EXIT
.
DATE9999  RETURN
+
.****************************************************************************
.* RPRT0000  :  Print Report                                                *
.****************************************************************************
RPRT0000  DISPLAY   *P30:24,"Printing...";
.
          CALL      IBACLOCK                      * initialise a few things
          MOVE      ZERO,CPAGENO
          MOVE      ONE,CNOUNDLN
          MOVE      TEN,CLNO                      * line counter
.
          CALL      HEAD132                       * Print 3 line header
          PRINT     *40,"Report Date : ",DRDATE,*N
          CALL      PTHD0000                      * print table headings
.
. Now loop thru temp file and print data
.
          PACK      KEY10,SP10
          CALL      RSTEMP1
RPRT1000  CALL      RKTEMP1
          BRANCH    OVRCD,RPRT8000                * no more records
.
          COMPARE   "55",CLNO                     * need a new page?
          GOTO      RPRT2000 IF LESS
.
. set up for new page
.
          CALL      HEAD132
          PRINT     *40,"Report Date : ",DRDATE,*N
          CALL      PTHD0000                      * print table headings
          MOVE      TEN,CLNO
.
RPRT2000  CALL      PSPC0000                      * set flag for printing
.
          UNPACK    XXADMD,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DSADMD                 * display var (Adm Date)
.
          UNPACK    XXRECD,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DSRECD               * display var (Reception Date)
.
          UNPACK    XXDATLS,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DSLSD                * display var (LS change Date)
.
          UNPACK    XXDNDIA,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DSDIAD                 * display (ICD change Date)
.
. print 1st line of details
.
          PRINT     *1,XXURNO,*11,XXNAME,*40,DSADMD;
.
          BRANCH    LSFLAG,RPRT3000               * dont print LS details
          PRINT     *51,"From : ",XXOLSTC,SP2,XXOLSTD;
RPRT3000  BRANCH    ICFLAG,RPRT3100
          MOVE      XXODIAC,CODE6                 * move to shorter var for dsp
          PRINT     *88,"From : ",CODE6,SP2,XXODIAD;
RPRT3100  PRINT     *N;
.
. print 2nd line of details
.
          BRANCH    LSFLAG,RPRT3200               * dont print LS details
          PRINT     *51,"To   : ",XXNLSTC,SP2,XXNLSTD;
RPRT3200  BRANCH    ICFLAG,RPRT3300
          MOVE      XXNDIAC,CODE6                 * move to shorter var for dsp
          PRINT     *88,"To   : ",CODE6,SP2,XXNDIAD;
RPRT3300  PRINT     *N;
.
. print 3rd line of details
.
          BRANCH    LSFLAG,RPRT3400               * dont print LS details
          PRINT     *51,"Date : ",DSLSD,SP1,"Reception : ",DSRECD;
RPRT3400  BRANCH    ICFLAG,RPRT3500
          PRINT     *88,"Date : ",DSDIAD;
RPRT3500  PRINT     *N
.
          ADD       FOUR,CLNO
          GOTO      RPRT1000
.
. we have finished printing patient data so print end of report details
.
RPRT8000  PRINT     "*** End of Report ***"
.
RPRT9999  RETURN
+
.***************************************************************************
.*  PSPC0000  :  Set flag for printing spaces for Legal Status or ICD9     *
.***************************************************************************
PSPC0000  MOVE      ONE,LSFLAG                    * dont print LS details
          MATCH     SP8,XXDATLS                   * no change in Legal Status?
          GOTO      PSPC5000 IF EQUAL
          MOVE      ZERO,LSFLAG                   * print LS details
.
PSPC5000  MOVE      ONE,ICFLAG                    * dont print ICD9 details
          MATCH     SP8,XXDNDIA                   * no change in ICD9 Codes?
          GOTO      PSPC9999 IF EQUAL
          MOVE      ZERO,ICFLAG                   * print ICD9 details
.
PSPC9999  RETURN
+
.***************************************************************************
.*  LOAD0000  :  Load data into temp file                                  *
.***************************************************************************
LOAD0000  DISPLAY   *P1:24,*EL,*P30:24,"Loading data... [        ]"
          MOVE      ONE,XXUNIQ                    * init unique counter
.
          PACK      KEY8,SP8
          CALL      RSMHVIS1
LOAD1000  CALL      RKMHVIS1                      * get next record
          BRANCH    OVRCD,LOAD9000                * no more records
.
          COMPARE   MHVISTAT,SIX                  * ignore cmh records
          GOTO      LOAD1000 IF LESS
.
          CALL      IDAT0000                      * init temp vars
.
          PACK      KEY8,MHVIADMN                 * get U/R Number
          CALL      RDMISS1
          BRANCH    OVRCD,LOAD1000
.
          PACK      KEY8,AURNO
          CALL      RDMAST1
          BRANCH    OVRCD,LOAD1000
.
          MOVE      AURNO,XXURNO                  * U/R Number
          STRIP     PSNAME
          PACK      XXNAME,PSNAME,COMMA,SP1,PGNAME    * patient name
          MOVE      MHVIRODT,XXRECD               * Reception Order date
.
. now look for a change in diagnosis (1)
.
LOAD1500  PACK      KEY16,MHVIADMN,RDATE
          CALL      RDMHDIA1
          BRANCH    OVRCD,LOAD2500
.
LOAD1600  MOVE      MHDIICD1,XXNDIAC              * New ICD9 code
          MOVE      MHDIDES1,XXNDIAD              * New ICD9 description
          MOVE      MHDIDATE,XXDNDIA              * Date of Change (Why?????)
.
. check for a change in Diagnosis (1)
.
          CALL      RPMHDIA1                      * read back 1 record
          BRANCH    OVRCD,LOAD2500
.
          MATCH     MHVIADMN,MHDIADMN             * same admission number?
          GOTO      LOAD2500 IF NOT EQUAL
.
          MOVE      MHDIICD1,XXODIAC              * Old ICD9 code
          MOVE      MHDIDES1,XXODIAD              * Old ICD9 description
.
          MATCH     XXODIAC,XXNDIAC               * same ICD9 code (Old vs New)?
          GOTO      LOAD3000 IF NOT EQUAL
.
LOAD2500  UNPACK    SP70,XXODIAC,XXODIAD
          UNPACK    SP70,XXNDIAC,XXNDIAD,XXDNDIA
.
. Get New Legal status (If one exists)
.
LOAD3000  PACK      KEY21,MHVIADMN,RDATE,ZED20
          CALL      RSMHLEG1
LOAD3100  CALL      RPMHLEG1
          BRANCH    OVRCD,LOAD4000
.
          MATCH     MHVIADMN,MHLEADMN
          GOTO      LOAD4000 IF NOT EQUAL
.
          MATCH     RDATE,MHLEDATE
          GOTO      LOAD4000 IF NOT EQUAL
.
          MATCH     ANSR,MHLEFLAG           * review status change only ?
          GOTO      LOAD3100 IF EQUAL       * yes - ignore
.
          MATCH     ANSN,MHLEFLAG           * no status change ?
          GOTO      LOAD3100 IF EQUAL       * yes - ignore
.
.      get HDP equivalent of New legal status
.
          MOVE      SP20,TDESC
          MOVE      SP4,THCSCOD
          PACK      KEY5,ANSL,ANSS,MHLESTAT
          CALL      RDCODE1
          BRANCH    OVRCD,LOAD4500
          MOVE      THCSCOD,XXNLSTC               * New Legal Status HDP equiv
          MOVE      TDESC,XXNLSTD                 * New Legal Status Description
          MOVE      MHLEDATE,XXDATLS              * Date of Legal Status change
.
. check for a change in legal status (HDP equiv)
.
LOAD3500  CALL      RPMHLEG1                      * read back 1 record
          BRANCH    OVRCD,LOAD4000
.
          MATCH     MHVIADMN,MHLEADMN             * same admission number?
          GOTO      LOAD4000 IF NOT EQUAL
.
          MATCH     ANSR,MHLEFLAG           * review status change only ?
          GOTO      LOAD3500 IF EQUAL       * yes - ignore
.
          MATCH     ANSN,MHLEFLAG           * no status change ?
          GOTO      LOAD3500 IF EQUAL       * yes - ignore
.
          MOVE      SP20,TDESC
          MOVE      SP4,THCSCOD
          PACK      KEY5,ANSL,ANSS,MHLESTAT
          CALL      RDCODE1
          BRANCH    OVRCD,LOAD4500
          MOVE      THCSCOD,XXOLSTC               * New Legal Status HDP equiv
          MOVE      TDESC,XXOLSTD                 * New Legal Status Description
.
          MATCH     XXOLSTC,XXNLSTC               * same HDP equiv (Old vs New)?
          GOTO      LOAD4500 IF NOT EQUAL
.
LOAD4000  UNPACK    SP70,XXOLSTC,XXOLSTD,XXNLSTC,XXNLSTD,XXDATLS,XXRECD
.
. there was no change in Legal Status check diagnosis 
.
          MATCH     SP9,XXODIAC
          GOTO      LOAD1000 IF EQUAL             * ignore record
.
LOAD4500  CALL      LADM0000                      * get last admission date
.
. now post details to temp file
.
          DISPLAY   *P47:24,MHVIADMN;
.
          PACK      KEY10,XXURNO,XXUNIQ
          CALL      WRTEMP1
          ADD       ONE,XXUNIQ
.
          MATCH     RDATE,MHLEDATE
          GOTO      LOAD1000 IF NOT EQUAL
.
          MOVE      XXOLSTC,XXNLSTC
          MOVE      XXOLSTD,XXNLSTD
          GOTO      LOAD1000
.
LOAD9000  DISPLAY   *P1:24,*EL
.
LOAD9999  RETURN
+
.**************************************************************************
.*  PTHD0000  :  Print Table Heading                                      *
.**************************************************************************
PTHD0000  PRINT     *1," U/R No.  Patient Name",*41,"Adm Date":
                    *51,"Status Change",*88,"Diagnosis Change",*N
.
PTHD9999  RETURN
+
.*************************************************************************
.*  LADM0000  :  Get last admission (Return from leave or Admission)     *
.*************************************************************************
LADM0000  MOVE      ADATE,XXADMD                  * admission date
          PACK      KEY30,TADMN,ZED20,ZED20
          CALL      RDSTRAN2
LADM1000  CALL      RDPTRAN2
          BRANCH    OVRCD,LADM9999
.
          MATCH     MHVIADMN,TADMN                * same admission number?
          GOTO      LADM9999 IF NOT EQUAL
.
          MATCH     ANSR,TMOVE                    * Return from Leave?
          GOTO      LADM1000 IF NOT EQUAL
.
          PACK      KEY5,ANST,ANSL,PTTRLTYP
          CALL      RDCODE1
          BRANCH    OVRCD,LADM1000
.
          MATCH     ANST,TCINDC1                  * Trial Leave?
          GOTO      LADM1000 IF NOT EQUAL
.
          MOVE      TDATE,XXADMD                  * new admission date
.
LADM9999  RETURN
+
.*************************************************************************
.*  IDAT0000  :  initialise vars                                         *
.*************************************************************************
IDAT0000  UNPACK    SP70,XXURNO,XXNAME,XXADMD,XXOLSTC
          UNPACK    SP70,XXOLSTD,XXNLSTC,XXNLSTD,XXDATLS,XXRECD,XXODIAC
          UNPACK    SP70,XXODIAD,XXNDIAC
          UNPACK    SP70,XXNDIAD,XXDNDIA
.
IDAT9999  RETURN
+
.*************************************************************************
.*  KILL0000  :  erase temp file                                         *
.*************************************************************************
KILL0000  CLOSE     MEHTMPA1
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    TFILNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
KILL9999  RETURN
+
.*************************************************************************
.*                             CRET0000            Called by:            *
.*                      create temporary file                            *
.*************************************************************************
CRET0000  CALL      KILL0000 
          CLEAR     CMDLINE 
          APPEND    "isbuild ",CMDLINE
          APPEND    TFILNAME,CMDLINE
          APPEND    " 193 U1-8,9-10",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      MEHTMPA1,TFILNAME
.
CRET9999  RETURN
+
.***************************************************************************
.*  Temp file I/O's                                                        *
.***************************************************************************
RSTEMP1   RESET     KEY10
          READ      MEHTMPA1,KEY10;;
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    MEHTMPA1;XXURNO,DXXUNIQ,XXNAME,XXADMD,XXOLSTC,XXOLSTD:
                         XXNLSTC,XXNLSTD,XXDATLS,XXRECD,XXODIAC,XXODIAD,XXNDIAC:
                         XXNDIAD,XXDNDIA
          GOTO      OVERCOND IF OVER
          MOVE      DXXUNIQ,XXUNIQ
          RETURN
.
WRTEMP1   RESET     KEY10
          MOVE      XXUNIQ,DXXUNIQ
          WRITE     MEHTMPA1,KEY10;XXURNO,DXXUNIQ,XXNAME,XXADMD,XXOLSTC:
                         XXOLSTD,XXNLSTC,XXNLSTD,XXDATLS,XXRECD,XXODIAC,XXODIAD:
                         XXNDIAC,XXNDIAD,XXDNDIA
          RETURN
.
.
          INC       STD001IO
          INC       MEHVI1IO/INC
          INC       MEHDIAIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATCODIO/INC
          INC       MEHLEGIO/INC
          INC       PATTRNIO/INC
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC

.*****************************************************************************
.*    Program      : IBAOUT74                                                *
.*    Description  : Clinic Appointments Summary                             *
.*                                                                           *
.*    Author       : ROD             (03/03/93)                              *
.*    Modifications:                                                         *
.*****************************************************************************
.*       V12.00.01  16/05/2025  J.Tan          TSK 0955096                   *
.*                  Added alpanumeric visit number generation                *
.*       V11.03.01  17/08/2023  DA Sarkies       TSK 0934650                 *
.*                  Fixed issues where Hospital and Clinic name/dr not       *
.*                  displaying on extract                                    *
.*       V11.02.01  09/02/2022  Thanh T          TSK 0905641                 *
.*                  Recompiled as OUTMA1FD changes                           *
.*       V10.07.01  30/10/2015  Ebon Clements  CAR 268970                    *
.*                  Change to use OUTCLIFD index 1 instead of index 2        *
.*       V10.05.01  17/12/2014  Ebon Clements  CAR 261630                    *
.*                  Removed port number from temp file name                  *
.*       V10.03.02  17/10/2012  Jill Parkinson CAR 273375                    *
.*                  Recompiled for DATECONV - first day of year calculation  *
.*       V10.03.01  19/03/2012  Peter McMullen CAR 259587                    *
.*                  Remove all reference to redundant file patyears          *
.*        V9.09.02  04/03/2008  Peter Vela    CAR 162914                     *
.*                  Recompiled for KYOUTCLI - readk of outcliaf              *
.*        V9.09.01  28/07/2007  Jill Habasque CAR 103645                     *
.*                  Recompiled for KYOUTCLI - read of pmshcpaf               *
.*        V5.08.02  19/09/2000  Steve Armstrong   srf 647263                 *
.*                  Recompiled for changes to OUTDSCLN                       *
.*        V5.08.01  29/08/2000  Caleb Sharman                                *
.*                  Changed Health Fund variables to be 8 chars              *
.*        V5.08.B01  15/03/2000  Steve Armstrong      5.08 Mods              *
.*                   Mods for changes to OUTCLIFD (effective date)           *
.*****************************************************************************
.*        V5.01.006  06/05/93 ROD                                            *
.*                   Rewritten because Dudley didn't know what they wanted   *
.*        V5.01.007  13/05/93 ROD                                            *
.*                   Fixed bug when printing end of report                   *
.*        V5.01.008  28/05/93 ROD                                            *
.*                   Fixed bug when printing end of report                   *
.*        V5.01.009  26/10/93 ROD      SRF 440321                            *
.*                   Fixed bug when printing end of report                   *
.*        V5.01.010  27/05/1994 Ian Rutt                                     *
.*                   Fixed Global Recompile Bugs                             * 
.*        V5.01.011  20/07/1994  Rob Leonard                                 *
.*                   Fixed hospital keyin                                    * 
.*---------------------------------------------------------------------------*
.*        V5.03.01   05/06/1995  Max White SRF 441300 / Quote 440066         *
.*                   Modify waiting times for DNA's and patient              *
.*                   cancellations.                                          *
.*                                                                           *
.*****************************************************************************
.
          INC       STD001FD
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
          INC       OUTCONFD/INC            * System Parameters
          INC       OUTMA1FD/INC
          INC       OUTOSFFD/INC
          INC       OUTCLIFD/INC
          INC       OUTCTYFD/INC
          INC       OUTRAPFD/INC            * Outpatients Reappointments
          INC       OUTRSHFD/INC
          INC       PATMA1FD/INC
          INC       PATPR1FD/INC
          INC       PATHSPFD/INC
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PMSHCPFD/INC            * National HCP file
          INC       IBACONFD/INC
.
BOOKPATS  FORM      4
CANDATE   DIM       8
CDYSDAYS  FORM      6
CDYSFDTE  DIM       8
CDYSTDTE  DIM       8
CDYSYEAR  FORM      2
CLINDESE  DIM       25
CLINDESS  DIM       25
CMDLINE   DIM       80
DAYOWEEK  DIM       3
DESC28    DIM       28
DIM4      DIM       4
DIM25     DIM       25
DIM40     DIM       40
DIM6      DIM       6
DNADATE   DIM       8
EMPSLOTS  FORM      4
FDDATE    DIM       10
PRDATA    FORM      1
FRI       INIT      "FRI"
FROMDATE  DIM       8
HOSPNAME  DIM       30
HOSPNAMS  DIM       30
KYCLINE   DIM       6
KYCLINS   DIM       6
KYHOSPE   DIM       3
KYHOSPS   DIM       3
MISSING   INIT      "Missing code: "
MON       INIT      "MON"
NOBOOKS   FORM      1
PATNAME   DIM       23
PKYCLINE  DIM       6
PKYCLINS  DIM       6
RESCH     DIM       1
SAT       INIT      "SAT"
SAVCDATE  DIM       8
SAVCLID   DIM       6
SAVCLSE   DIM       6
SAVHOSP   DIM       3
SAVOUTNO  DIM       8
SAVRBKN   DIM       8
SAVTIME   DIM       5
SUN       INIT      "SUN"
TDDATE    DIM       10
THU       INIT      "THU"
TODATE    DIM       8
TODAY     DIM       8
TUE       INIT      "TUE"
WED       INIT      "WED"
WKSWAIT   FORM      3
Z20       INIT      "zzzzzzzzzzzzzzzzzzzz"
.
PRGID     INIT      "IBAOUT74"
PRGNAM    INIT      "Clinic Appointments Summary"
VERSION   INIT      "V12.01.00"
.
.****************************************************************************
.* ML0000  :  Mainline code                                                 *
.****************************************************************************
ML0000    CALL      INIT0000                      * initialisation
.
ML1000    CALL      MENU0000                      * get menu options
          BRANCH    EXIT,ML9999                   * Exit selected
.
          CALL      PROC0000                      * keyin search fields
          BRANCH    EXIT,ML1000                   * Cancel or EXITCHAR
.
          CALL      RPRT0000                      * Print Report
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.****************************************************************************
.* INIT0000  :  Initialisation                                              *
.****************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P60:24,"controlf"
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P60:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
          PACK      CFNAME,UID,FILBOKA1
          DISPLAY   *P60:24,CFNAME
          OPEN      OUTBOKA1,CFNAME
.
          PACK      CFNAME,UID,FILCTYA1
          DISPLAY   *P60:24,CFNAME
          OPEN      OUTCTYA1,CFNAME
.
          PACK      CFNAME,UID,FILRAPA2
          DISPLAY   *P60:24,CFNAME;
          OPEN      OUTRAPA2,CFNAME  
.
          PACK      CFNAME,UID,FILRSHA1
          DISPLAY   *P60:24,CFNAME
          OPEN      OUTRSHA1,CFNAME
.
          PACK      CFNAME,UID,FILBB1A1
          DISPLAY   *P60:24,CFNAME
          CALL      OPOTBB11
.
          PACK      CFNAME,UID,FILCLIA1
          DISPLAY   *P60:24,CFNAME
          OPEN      OUTCLIA1,CFNAME
.
          DISPLAY   *P60:24,"outosfaf"
          OPEN      OUTOSFA2,"outosfaf"
.
          DISPLAY   *P60:24,"pathspaf"
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P60:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P60:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P60:24,"patpramf"
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
.
          DISPLAY   *P60:24,"patpraxf"
.
          DISPLAY   *P60:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
          DISPLAY   *P64:24,"pmshcpaf";
          OPEN      PMSHCPA1,"pmshcpaf"
.
          DISPLAY   *P1:24,*EL;
.
          MOVE      ONE,CNEWDS
.         
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
INIT9999  RETURN
+
.****************************************************************************
.* MENU0000  :  Main Menu                                                   *
.****************************************************************************
MENU0000  DISPLAY   *P1:4,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Print Report"
.
MENU1000  KEYIN     *P1:7,"Select Option ? ":
                    *P17:7,*DV,UNDLN1:
                    *P17:7,*V2LON,MOPTION
.
          IF        MOPTION = 0
            MOVE      ONE,EXIT
            GOTO      MENU9999
          ENDIF
.
          MOVE      ZERO,EXIT
          BRANCH    MOPTION,MENU9999
          BEEP
          GOTO      MENU1000
.
MENU9999  RETURN
+
.****************************************************************************
.* PROC0000  :  Keyin Hospital and Clinic Session                           *
.****************************************************************************
PROC0000  DISPLAY   *P1:10,*EF
.
. --- keyin date range ---
.
          CALL      KDAT0000
          BRANCH    EXIT,PROC9999                 * nothing input for start date
.
. --- keyin Hospital Id range ---
.
          CALL      KHSP0000
          BRANCH    EXIT,PROC9999                 * EXITCHAR
.
. --- keyin Clinic Session ---
.
          CALL      KCLI0000
          BRANCH    EXIT,PROC9999                 * EXITCHAR
.
. ask Continue?
.
PROC7000  CALL      CONTQST
          BRANCH    CEXIT,PROC8000,PROC0000,PROC9000
.
PROC8000  MOVE      ZERO,EXIT                     * everything cool!
          GOTO      PROC9999
.
PROC9000  MOVE      ONE,EXIT                      * EXITCHAR or Cancel
.
PROC9999  RETURN
+
.**************************************************************************
.*  KCLI0000  :  Keyin a Clinic Id range                                  *
.**************************************************************************
KCLI0000  DISPLAY   *P1:15,"Clinic Id  From : ":
                    *P1:16,"           To   : "
.
          MOVE      "19",ECOL
          MOVE      "15",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
.
          CALL      KYOUTCLI
          BRANCH    EXIT,KCLI2000,KCLI9000,KCLI2000      * nothing or EXITCHAR
.
          MOVE      OCACLIN,KYCLINS               * valid input
          MOVE      OCACLIN,PKYCLINS              * print variable
          DISPLAY   *P27:15,OCADESC
          MOVE      OCADESC,CLINDESS
          GOTO      KCLI5000
.
KCLI2000  MOVE      SP6,KYCLINS                   * nothing input
          DISPLAY   *P19:15,*V2LON,"Start"
          MOVE      "Start",PKYCLINS              * print var
          MOVE      SP30,CLINDESS
.
. keyin End clinic
.
KCLI5000  MOVE      "19",ECOL
          MOVE      "16",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
.
          CALL      KYOUTCLI
          BRANCH    EXIT,KCLI6000,KCLI9000,KCLI6000      * nothing or EXITCHAR
.
          MOVE      OCACLIN,KYCLINE               * valid input
          MOVE      OCACLIN,PKYCLINE              * print variable
          DISPLAY   *P27:16,OCADESC
          MOVE      OCADESC,CLINDESE
.
. check if valid range
.
          MATCH     KYCLINS,KYCLINE
          GOTO      KCLI8000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Invalid range.  ";
          CALL      HITENTER
          GOTO      KCLI0000
.
KCLI6000  MOVE      "zzzzzz",KYCLINE              * nothing input
          DISPLAY   *P19:16,*V2LON,"Finish"
          MOVE      "Finish",PKYCLINE
          MOVE      SP30,CLINDESE
.
KCLI8000  MOVE      ZERO,EXIT                     * All is howdy dudey
          GOTO      KCLI9999
.
KCLI9000  MOVE      ONE,EXIT                      * EXITCHAR
.
KCLI9999  RETURN
+
.**************************************************************************
.*  KHSP0000  :  Keyin a Hospital code range                              *
.**************************************************************************
KHSP0000  DISPLAY   *P1:12,"Hospital  From  : ":
                    *P1:13,"          To    : "
.
          MOVE      "19",ECOL
          MOVE      "12",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP3,CKYIDEF3
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHSP2000,KHSP9000        * nothing or EXITCHAR
.
          MOVE      KEY3,KYHOSPS                  * valid input
          DISPLAY   *P27:12,PTHSNAME
          MOVE      PTHSNAME,HOSPNAMS
          GOTO      KHSP4000
.
KHSP2000  MOVE      SP3,KYHOSPS                   * nothing input
          DISPLAY   *P19:12,*V2LON,"Start",*EL
          MOVE      "Start",PTHSNAME
          MOVE      PTHSNAME,HOSPNAMS
.
. keyin end hospital code
.
KHSP4000  MOVE      "19",ECOL
          MOVE      "13",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP3,CKYIDEF3
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHSP5000,KHSP9000        * nothing or EXITCHAR
.
          MOVE      KEY3,KYHOSPE                  * valid input
          DISPLAY   *P27:13,PTHSNAME
          MOVE      PTHSNAME,HOSPNAME
.
. check if valid range
.
          MATCH     KYHOSPS,KYHOSPE
          GOTO      KHSP8000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Invalid range.  ";
          CALL      HITENTER
          GOTO      KHSP0000
.
KHSP5000  MOVE      "zzz",KYHOSPE                 * nothing input
          DISPLAY   *P19:13,*V2LON,"Finish",*EL
          MOVE      "Finish",PTHSNAME
          MOVE      PTHSNAME,HOSPNAME
.
KHSP8000  MOVE      ZERO,EXIT                     * Ok.
          GOTO      KHSP9999
.
KHSP9000  MOVE      ONE,EXIT                      * EXITCHAR
.
KHSP9999  RETURN
+
.**************************************************************************
.*  KDAT0000      Keyin a date range                                      *
.**************************************************************************
KDAT0000  DISPLAY   *P1:10,*EF,"Date From       : ",*P31:10,"To: "
.
. keyin from date
.
          CALL      IBACLOCK
          PACK      TODAY,CCC,CYY,CMM,CDD
          REP       " 0",TODAY
          UNPACK    TODAY,CCENT,CYEAR,CMON,CDAY
          MOVE      "19",CCOL
          MOVE      TEN,CVERT
          MOVE      ZERO,CDEFDTE
.
KDAT1000  CALL      KEYDATE
          BRANCH    CQUEST,KDAT1000
          BRANCH    OVRCD,KDAT9500                * anything input
.
          PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY
          MATCH     TODAY,FROMDATE
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Date cannot be into the Past. ";
            CALL      HITENTER
            GOTO      KDAT0000
          ENDIF
          PACK      FDDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR    * display var
          REP       " 0",FDDATE 
          GOTO      KDAT5000
.
. keyin to date
.
KDAT5000  MOVE      "35",CCOL
          MOVE      TEN,CVERT
          MOVE      ZERO,CDEFDTE
.
KDAT6000  CALL      KEYDATE
          BRANCH    CQUEST,KDAT6000
          BRANCH    OVRCD,KDAT0000                * anything input?
.
          PACK      TODATE,CCENT,CYEAR,CMON,CDAY
          PACK      TDDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR    * display var
          REP       " 0",TDDATE 
.
. check if dates entered are valid (range)
.
KDAT7000  REP       " 0",FROMDATE
          REP       " 0",TODATE
.
          MATCH     FROMDATE,TODATE
          GOTO      KDAT9000 IF LESS
          MOVE      ZERO,EXIT                     * Continue
          GOTO      KDAT9999
. 
KDAT9000  DISPLAY   *P1:24,*EL,*B,"Invalid date range.  ";
          CALL      HITENTER
          GOTO      KDAT0000
.
KDAT9500  MOVE      ONE,EXIT                      * No (don't continue)
.
KDAT9999  RETURN
+
.***************************************************************************
.*  PAGE0000  :  Print stuff for new page                                  *
.***************************************************************************
PAGE0000  IF        IBCNMHOS = 1
            PACK      PTHSNAME,MISSING,OTOSHOSP     * in case of error
            MOVE      OTOSHOSP,KEY3
            CALL      RDPTHSP1
            MOVE      PTHSNAME,CNAME                * get hospital name
          ENDIF
.
          CALL      HEAD80                        * Print 3 line header
          PRINT     *20,"Date From      : ",FDDATE,"  to  ",TDDATE,*N,*N:
                    *20,"Clinic Id from : ",PKYCLINS,SP1,CLINDESS,*N:
                    *20,"          to   : ",PKYCLINE,SP1,CLINDESE
.
. if no data was found dont print the rest of the header details
.
          IF        PRDATA=0
            GOTO      PAGE9999
          ENDIF
.
          MOVE      OTOSCTYP,DIM6
          CALL      CLIT0000                      * get clinic desc
          PRINT     *N,*1,"Clinic Type  : ",OTOSCTYP,SP2,OCTDESC
.
          MOVE      OTOSSESS,DIM6
          CALL      CLID0000                      * get clinic desc
          PRINT     *1,"Session      : ",OTOSSESS,SP2,OCADESC,*N
          ADD       "8",CLNO
.
          MOVE      OTOSCLID,DIM6
          CALL      CLID0000                      * get clinic description
          MOVE      OCADESC,DESC28
.
          PRINT     *1,"Clinic Id    : ",OTOSCLID,SP2,DESC28
.
          CALL      PTHD0000                      * print table headings
.
PAGE9999  RETURN
+
.***************************************************************************
.*  FDAT0000  :  Format data for printing                                  *
.***************************************************************************
FDAT0000  MOVE      "<Missing Rec>",PATNAME
          MATCH     ZEROUR,OBAURNO
          IF        @EQUAL
            PACK      KEY8,OBAOUTNO
            CALL      RDPRAM1
            BRANCH    OVRCD,FDAT5000
          ELSE
            MOVE      OBAURNO,KEY8
            CALL      RDMAST1
            BRANCH    OVRCD,FDAT5000
          ENDIF
.
          MOVE      PGNAME,PATNAME
          APPEND    DOT,PATNAME
          APPEND    PSNAME,PATNAME
          RESET     PATNAME
.
FDAT5000  CALL      WAIT0000                      * calculate weeks waiting
.
          UNPACK    OBADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
. check if this is a rescheduled appointment
.
          MOVE      SP1,RESCH
          PACK      KEY11,OBAOUTNO,SP10
          CALL      RDSRSHA1
          CALL      RDKRSHA1
          BRANCH    OVRCD,FDAT9999
.
          MATCH     OBAOUTNO,OPRSOUTN
          GOTO      FDAT9999 IF NOT EQUAL
.
          MOVE      "R",RESCH                     * rescheduled appointment
.
FDAT9999  RETURN
+
.**************************************************************************
.*  WAIT0000  :  Calculate # weeks between appointment and attendance     *
.**************************************************************************
WAIT0000  CALL      GDNA0000                      * check if DNA  
          CALL      GCAN0000                      * check if Patient Cancell'n
.
. The idea is to get the hospitals waiting times down, so use the latest date
.         
          MATCH     DNADATE,CANDATE
          IF        @LESS
            MATCH     DNADATE,OBABKDTE
            IF        @LESS
              MOVE      DNADATE,CDYSFDTE          * DNA date = from date
            ELSE
              MOVE      OBABKDTE,CDYSFDTE         * booking date = from date
            ENDIF
          ELSE
            MATCH     CANDATE,OBABKDTE
            IF        @LESS
              MOVE      CANDATE,CDYSFDTE          * cancell'n date = from date
            ELSE
              MOVE      OBABKDTE,CDYSFDTE         * booking date = from date
            ENDIF
          ENDIF
.
          MOVE      OBADATE,CDYSTDTE              * to date
.
          MATCH     CDYSFDTE,CDYSTDTE             * to date less than from date?
          GOTO      WAIT9000 IF LESS              * yes, ignore
.
          CALL      CALCDAYS
          SUBTRACT  ONE,CDYSDAYS                  * not no. of days inclusive
.
          MOVE      CDYSDAYS,WKSWAIT              * no. days
          DIV       SEVEN,WKSWAIT                 * no. weeks wait
          GOTO      WAIT9999
.
WAIT9000  MOVE      ZERO,WKSWAIT
.
WAIT9999  RETURN
+
.***********************************************************************
.*  GCAN0000  :  Check if Patient Cancelled the Booking                *
.***********************************************************************
GCAN0000  MOVE      SP8,CANDATE
          PACK      KEY11,OBAOUTNO,SP20
          CALL      RDSRSHA1
GCAN1000  CALL      RDKRSHA1
          BRANCH    OVRCD,GCAN9999
.
          MATCH     OBAOUTNO,OPRSOUTN
          GOTO      GCAN9999 IF NOT EQUAL
.
          MATCH     OBACLIN,OPRSNCLI
          GOTO      GCAN1000 IF NOT EQUAL
.
          MATCH     OBADATE,OPRSNDAT
          GOTO      GCAN1000 IF NOT EQUAL
.
          MATCH     OBASTRT,OPRSNSTR 
          GOTO      GCAN1000 IF NOT EQUAL
. 
          COMPARE   OBASLOT,OPRSSLOT 
          GOTO      GCAN1000 IF NOT EQUAL
.
          MATCH     OBATIME,OPRSNTIM
          GOTO      GCAN1000 IF NOT EQUAL
.
          PACK      KEY5,ANSR,ANSR,OPRSREAS       * Reschedule Reason
          CALL      RDCODE1
          BRANCH    OVRCD,GCAN1000
.
          MATCH     "P",TCINDC1                   * Patient Cancellation?
          GOTO      GCAN1000 IF NOT EQUAL
.
. We have a patient cancellation
.
          MATCH     OPRSDATE,FROMDATE             * before period
          GOTO      GCAN9999 IF LESS
.
          MOVE      OPRSDATE,CANDATE
.
.
GCAN9999  RETURN
+
.***********************************************************************
.*  GDNA0000  :  Get the End Date for DNA's                            *
.***********************************************************************
.
. Check if the attendance was originally DNA'd. 
.
GDNA0000
          MOVE      OBAOUTNO,SAVRBKN
          MOVE      OBAOUTNO,SAVOUTNO
          MOVE      SP8,DNADATE
          PACK      KEY16,OBAOUTNO,SP20
GDNA1000  CALL      RSOTRAP2
          CALL      RKOTRAP2
          BRANCH    OVRCD,GDNA9000
.
          MATCH     SAVRBKN,OTRPRBKN              * same reappointment
          GOTO      GDNA9000 IF NOT EQUAL
.
. patient attendance is a reappointment
.
. check to see if original booking was DNA'd
.
          PACK      KEY8,OTRPOBKN,SP10
          CALL      RDBOKB1
          BRANCH    OVRCD,GDNA5000
.
          PACK      KEY5,ANSO,ANSZ,OTBBOUTC
          CALL      RDCODE1
          BRANCH    OVRCD,GDNA5000
.
          MATCH     "1",TCINDC1
          GOTO      GDNA5000 IF NOT EQUAL
.
          MATCH     FROMDATE,OBADATE               * before period
          GOTO      GDNA5000 IF LESS
.
          MOVE      OBADATE,DNADATE
.
          MOVE      ZERO,EXIT
          GOTO      GDNA9000
.
GDNA5000  PACK      KEY16,OTRPOBKN,SP20
          MOVE      OTRPOBKN,SAVRBKN
          GOTO      GDNA1000
.
GDNA9000  PACK      KEY8,SAVOUTNO,SP20
          CALL      RDBOKB1                       * restore file
.
GDNA9999  RETURN
+
.**************************************************************************
.*  CLID0000  :  get clinic description                                   *
.*               Requires: DIM6 - Clinic code   Returns: OCADESC - desc   *
.**************************************************************************
.
CLID0000  PACK      KEY20,OTOSSITE,DIM6,OTOSDATE
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     OTOSSITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     DIM6,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          IF        OVRCD = 1
            MOVE      "Unknown clinic",OCADESC
            PACK      OCADESC,OCADESC,SP30
          ELSE
            MATCH     SP70,OCADOCT
            IF        !@EQUAL
              MOVE      OCADOCT,KEY6
              CALL      RDDOCT1
              IF        OVRCD=0
                MATCH     DTITL,SP70
                MOVE      DTITL,PACTITLE
                CMOVE     DGNAME,PACGNAME
                MOVE      DSNAME,PACSNAME
                MOVE      ONE,PACFRMT
                CALL      PACNAME
                MOVE      PACFNAME,OCADESC
              ENDIF
            ENDIF
          ENDIF
.
CLID9999  RETURN
+
.**************************************************************************
.*  CLIT0000  :  get clinic type description                              *
.*               Requires: DIM6 - Clinic code   Returns: OCTDESC - desc   *
.**************************************************************************
CLIT0000  PACK      OCTDESC,MISSING,DIM6
          PACK      KEY12,OTOSSITE,DIM6
          CALL      RDCTYA1
CLIT9999  RETURN
+
.**************************************************************************
.*  PTHD0000  :  Print Table Heading                                      *
.**************************************************************************
PTHD0000  PACK      OTOSDATE,OTOSDATE,SP10
          UNPACK    OTOSDATE,CCENT,CYEAR,CMON,CDAY
.
          MOVE      "Unk",DAYOWEEK
          MATCH     SP10,OTOSDATE
          IF        !@EQUAL
            CALL      DATECONV
            LOAD      DAYOWEEK,WEEKDAY,MON,TUE,WED,THU,FRI,SAT,SUN
          ENDIF
.
          CALL      PACDATE
          PRINT     *1,CPCDATE,*54,"Day: ",DAYOWEEK,SP2,"Start Time: ",OTOSTIME
          ADD       TWO,CLNO
.
          CALL      HDRL0000                    * Call header line
.
PTHD9999  RETURN
+
.*****************************************************************************
.*                              HDRL0000                                     *
.*                         Print the header lines                            *
.*****************************************************************************
HDRL0000  CALL      UND80
.
          PRINT     *1,"|",*12,"|",*38,"| Slot | Slot  |Visit | Wks. | Prty |":
                    *80,"|",*N:
                    *1,"| U/R No.  | Patient Name",*38,"|  No. | Time  | Type":
                    " | Wait | code |Rsch.|"
          CALL      UND80
          ADD       "4",CLNO
HDRL9999  RETURN
+
.****************************************************************************
.* RPRT0000  :  Print Report                                                *
.****************************************************************************
RPRT0000  DISPLAY   *P1:24,*EL:
                    *P1:24,"Processing Clinic : ":
                    *P28:24,"Time : ",*P41:24,"Slot :":
                    *P54:24,"U/R :"
.
          CALL      IBACLOCK                      * initialise a few things
          MOVE      ZERO,CPAGENO
          MOVE      ONE,CNOUNDLN
          MOVE      "60",CLNO                     * force new page
          MOVE      SP6,SAVCLID
          MOVE      SP8,SAVCDATE
          MOVE      SP6,SAVCLSE
          MOVE      SP3,SAVHOSP
          MOVE      SP5,SAVTIME
          MOVE      ZERO,EMPSLOTS                 * init # empty slots
          MOVE      ZERO,BOOKPATS                 * init # booked patients
          MOVE      ZERO,PRDATA                   * assume no data printed
          MOVE      ZERO,NOBOOKS                  * no bookings but empty slots
.
          PACK      KEY40,KYHOSPS,SP4
          CALL      RSOTOSF2
RPRT1000  CALL      RKOTOSF2
          BRANCH    OVRCD,RPRT8000                * no more records
.
. check if we have a valid record
.
          MATCH     OTOSHOSP,KYHOSPE              * still in range?
          GOTO      RPRT8000 IF LESS              * no
.
          MATCH     KYCLINS,OTOSCLID              * Start clinic Id Ok.?
          GOTO      RPRT1000 IF LESS              * no
          MATCH     OTOSCLID,KYCLINE              * End clinic Id Ok.?
          GOTO      RPRT1000 IF LESS              * no
.
          MATCH     FROMDATE,OTOSDATE             * Start date Ok.?
          GOTO      RPRT1000 IF LESS              * no
          MATCH     OTOSDATE,TODATE               * End date Ok.?
          GOTO      RPRT1000 IF LESS              * no
.
          MOVE      ONE,PRDATA                    * something will be printed
.
. no more room on page?
.
          COMPARE   "55",CLNO
          GOTO      RPRT1200 IF LESS
.
          CALL      PAGE0000
          GOTO      RPRT1500
.
. new hospital?
.
RPRT1200  MATCH     SAVHOSP,OTOSHOSP
          IF        !@EQUAL
            CALL      PAGE0000
            GOTO      RPRT1500
          ENDIF
.
. new clinic session?
.
          MATCH     SAVCLSE,OTOSSESS
          IF        !@EQUAL
            CALL      PAGE0000
            GOTO      RPRT1500
          ENDIF
.
. new clinic Id?
.
          MATCH     SAVCLID,OTOSCLID
          IF        !@EQUAL
            CALL      PAGE0000
            GOTO      RPRT1500
          ENDIF
.
          CALL      PTHD0000                      * print table headings
.
. loop thru bonkings file for patients in the above session
.
RPRT1500  DISPLAY   *P21:24,*V2LON,OTOSCLID,*P35:24,OTOSTIME
          PACK      KEY28,OTOSSITE,OTOSCLID,OTOSDATE,OTOSTIME,SP10
          CALL      RDSBOKA1
RPRT2000  CALL      RDKBOKA1
          BRANCH    OVRCD,RPRT7500
.
          PACK      DIM25,OBASITE,OBACLIN,OBADATE,OBASTRT
          MATCH     DIM25,KEY28                   * still same key?
          GOTO      RPRT7500 IF NOT EQUAL
.
          IF        (OBASTAT=2) | (OBASTAT=3)
            GOTO      RPRT2000                    * ignore (not used, extended)
          ENDIF
.
          IF        OBASTAT=0
            ADD       ONE,EMPSLOTS                * increment # empty slots
            GOTO      RPRT2000
          ENDIF
.
. read bokb for priority
.
          PACK      KEY8,OBAOUTNO
          CALL      RDBOKB1
          BRANCH    OVRCD,RPRT2000                
.
          ADD       ONE,BOOKPATS                * increment # booked pats
.
. print a line
.
          CALL      FDAT0000                      * format data for printing
.
          DISPLAY   *P48:24,*V2LON,OBASLOT,*P60:24,OBAURNO
          PRINT     *1,"| ",OBAURNO,*12,"| ",PATNAME,*38,"|  ",OBASLOT,*45:
                    "| ",OBATIME,*53,"| ",OBAVISIT,*60,"| ",WKSWAIT,*67,"| ":
                    OBAPRTY,*74,"|  ",RESCH,*80,"|"
          ADD       ONE,CLNO                      * increment line counter
.
          MOVE      OTOSCLID,SAVCLID
          MOVE      OTOSSESS,SAVCLSE
          MOVE      OTOSHOSP,SAVHOSP
          MOVE      OBADATE,SAVCDATE
          MOVE      OBASTRT,SAVTIME
          GOTO      RPRT2000                      * get next booking
.
. print sub totals for the just printed session
.
RPRT7500  CALL      UND80
          PRINT     *1,"No. Booked Patients: ",BOOKPATS,*N:
                    *1,"No. Empty Slots    : ",EMPSLOTS,*N,*N
          ADD       FOUR,CLNO
          MOVE      ZERO,EMPSLOTS
          MOVE      ZERO,BOOKPATS
          MOVE      OTOSCLID,SAVCLID
          MOVE      OTOSSESS,SAVCLSE
          MOVE      OTOSHOSP,SAVHOSP
          MOVE      OBADATE,SAVCDATE
          MOVE      OBASTRT,SAVTIME
          GOTO      RPRT1000                      * get next clinic
.
. we have finished printing patient data so print end of report details
.
RPRT8000  IF        PRDATA=0
            DISPLAY   *P1:24,*EL,*B,"No data found.  ";
            CALL      HITENTER
.......     CALL      PAGE0000                    * print new page stuff
            GOTO      RPRT9999
          ENDIF
          PRINT     *N,"*** End of Report ***"
.
RPRT9999  RETURN
+
.
          INC       STD001IO
          INC       OUTBOAIO/INC
          INC       OUTBB1IO/INC
          INC       OUTCLIIO/INC
          INC       OUTCTYIO/INC
          INC       OUTRAPIO/INC             * Outpatients Reappointments
          INC       OUTRSHIO/INC
          INC       OUTOSFIO/INC
          INC       OUTMA1IO/INC
          INC       PATMA1IO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PMSHCPIO/INC            * National HCP file
          INC       PATPR1IO/INC
          INC       PATHSPIO/INC
          INC       PATHSPKY
          INC       KYOUTCLI
          INC       CALCDAYS
          INC       OUTDSCLN
          INC       DATECONV

.******************************************************************************
.* System   : WAITING LIST                                                    *
.* Program  : IBAWAT37                                                        *
.* Desc     : Category Assessment Report                                      *
.* Function : Print all procedures with a staged or deferred admission date   *
.******************************************************************************
.* Author   : Justin Coates                                                   *
.* Date     : 26/07/1990                                                      *
.* Comments :                                                                 *
.* Mods     :                                                                 *
.*                                                                            *
.******************************************************************************
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899                         *
.*           Recompiled as WATTR1FD changes                                   *
.******************************************************************************
.*        V11.00.01 10/03/2020  Jill Parkinson Task 0879163                   *
.*                  Added SEXDSCIO include                                    *
.*       V10.05.01  01/08/2015  Ania P                      CAR 261630        *
.*                  Removed use of PORT for temp file naming                  *
.*        V9.03.01  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2      *
.*                  - Add Sex Description routine SEXDSC00 with Code "R" -    *
.*                  "Intersex" added                                          *
.*        V5.08.03  28/09/2000  Tonii                                         *
.*                  Fixed Date keyin, if nothing is entered to date will      *
.*                  default to 'Finish'                                       *
.*                  Fixed keyin line for Doctor code, was showing up in the   *
.*                  wrong place                                               *
.*        V5.08.02  01/09/2000  Tonii                                         *
.*                  Mods to accept Unknown and Indeterminate as valid patient *
.*                  sex description                                           *
.*        V5.08.01  29/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.B01 30/12/1999  Steve Armstrong                               *
.*                  Fixed global recompile bugs                               *
.******************************************************************************
.*        V5.07.B02 12/01/1999  Nicole Harrington  5.07 rebounds Error 36     *
.*                  Fixed printing of keyed in date range                     *
.******************************************************************************
.*        V5.04.03  26/06/1997  Howellsy         505                          *
.*                  Use PATDOCKY instead of PATDOCDS                          *
. *       V5.04.02  15/04/1997  howellsy   RHA                                *
. *                 Recompiled for WATDSPRO                                   *
.*        V5.04.01  18/12/1996  Howellsy         SRF 642436                   *
.*                  Use WATCNTCN to derive the number of cancellations        *
.******************************************************************************
.*        V5.01.01  12/10/90 Bill Dew                                         *
.*                  Fix report heading.  And report in general                *
.*        V5.01.02  19/10/90 DIG / i chung (14/11/90)                         *
.*                  Recompile for WATCATIO.INC - incorrect Keys size          *
.*        V5.01.03  27/11/90 Glen Hobby                                       *
.*                  Recompiled for changes to                                 *
.*                  PATMX1FD                                                  *
.*        V5.01.04  26/05/1994 Ian Rutt                                       *
.*                  Fixed Global Recompile Bugs                               * 
.*                                                                            *
.******************************************************************************
.
.         IBAWAT37.PBL        Category Assessment Report
.         ============        ==========================
.         KSEQ0000 - determine which report to do
.
.         UNCL0000 - produces a report for Unit/Clinic order
.             KDAT0000 - enter the from and to dates
.             KUCC0000 - enter the Unit/Clinic code
.             CLRP0000 - produce the report
.
.         DOCT0000 - produces a report for Doctor code order
.             KDAT0000 - enter the from and to dates
.             KDOC0000 - enter the Doctor code
.             DORP0000 - produce the report
.         
.
          INC       STD001FD                * common variables
          INC       PATCOMM/INC             * needed for 'gperd'/'patcomn3'
          INC       PATCODFD/INC            * codes file
          INC       PATDO1FD/INC            * doctors file
          INC       PATDRGFD/INC            * date ranges for gperd
          INC       PATMA1FD/INC            * master file
          INC       WATCATFD/INC            * WL category change file
          INC       WATCONFD/INC            * WL control file
          INC       WATMESFD/INC            * WL comments file
          INC       WATPROFD/INC            * WL procedures file
          INC       WATTR1FD/INC            * WL treatment file
          INC       WATWTAFD/INC            * WL movement analysis file
          INC       IBASEQFD/INC 
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
. ------ program variables ------
.
PTCNURHA  FORM      1
CODE      DIM       2                       * PATCODDS
COL       FORM      3                       * for WATDSPRO
CMDLINE   DIM       78                      * line for 'execute' command
CNTPATCL  FORM      5                       * no. patients in a unit/clinic
CNTPATTL  FORM      5                       * total no. of patients in report
CURRDATE  DIM       8                       * current date ccyymmdd
.
CODEDOCT  DIM       6                       * doctor code entered
CODEUNCL  DIM       3                       * unit/clinic code entered
CODEPROC  DIM       9                       * procedure code entered
DESCDOCT  DIM       40                      * doctor description
DESCUNCL  DIM       20                      * unit/clinic description
DESCPROC  DIM       50                      * procedure code description
.
DIM3      DIM       3                       * PATDOCDS
DIM14     DIM       14                      * dummy variable
DIM26     DIM       26                      * holds unit : code - description
DIM31     DIM       31                      * holds doct : code - description
DISPACT   FORM      1
DOCCODE   DIM       6
.
FROMDATE  DIM       8                       * from date for report ccyymmdd
.
KEY21OR   DIM       22                      * original values for cat. change
KEY21NE   DIM       22                      * read in values for cat. change
.
PREVDT    DIM       6                       * previous doct
PREVUN    DIM       3                       * previous unit
PTCNDCQS  FORM      1
QUPER     FORM      1                       * ? performed flag
.
TODATE    DIM       8                       * to date for report ccyymmdd
.
VERT      FORM      2                       * for WATDSPRO
.
. ------ variables for the report ------
.
PRADDR    DIM       45                      * address
PRCANC    DIM       17                      * booking cancellation reason
PRCLIN    DIM       24                      * clinic to print
PRCOMM    DIM       106                     * comments
PRDATE1   DIM       10                      * date added
PRDATE2   DIM       10                      * deferred/staged admission date
PRDATE3   DIM       10                      * latest booking canc'n date
PRDATE4   DIM       10                      * birth date
PRDATE5   DIM       10                      * re-assement date
PRDOCT    DIM       31                      * doctors name
PRFROM    DIM       40                      * from date and description
PRNAME    DIM       45                      * patients name
PRPRTY    DIM       1                       * priority
PRSEX     DIM       8                       * male or female           *C-49016
PRTO      DIM       40                      * to date and description
PRUNDO1   DIM       45                      * current unit/doct code
PRUNDO2   DIM       45                      * unit/doct code entered
.
. ------ variables needed for the temporary index file ------
.
CLINFILE  IFILE     SQL, FIXED=116, KEY="U60-62,1-8,9-17,18-19"
DOCTFILE  IFILE     SQL, FIXED=116, KEY="U66-71,1-8,9-17,18-19"
.
.Name     Type      Length  Phys.  Sta-Fin
.----     ----      ------  -----  -------
XWMURNO   DIM       8         8      1-8    * patients UR number
XWMCODE   DIM       9         9      9-17   * procedure code
DXWMCNT   DIM       2         2     18-19   * procedure code count/dim
XWMDESC   DIM       32        32    20-51   * procedure description
XWMDATE1  DIM       8         8     52-59   * date on list ccyymmdd
XWMUNIT   DIM       3         3     60-62   * unit/clinic code
XWMPTY    DIM       3         3     63-65   * priority code
XWMDOCTO  DIM       6         6     66-71   * doctors description
XWMSTAY   FORM      5         4     72-75   * estimated length of stay
XWMTIME   FORM      5         4     76-79   * estimated time for procedure
XWMREADA  DIM       8         8     80-87   * re-assessment date
XWMDEFDA  DIM       8         8     88-95   * deferred/staged admission date
XWTWMHOS  FORM      3         3     96-98   * number of times hosp. canc'd
XWTWMPAT  FORM      3         3     99-101  * number of times patient canc'd
XWTWMOTH  FORM      3         3    102-104  * number of times other canc'd
XWMBOOKR  DIM       3         3    105-107  * latest booking removal reason
XWMBOKDA  DIM       8         8    108-115  * latest booking removal date
.
.End of Record                     116-116 
.
XWMCNT    FORM      2                       * procdure code count/form
.
. ------ program constants ------
.
ALL       INIT      "All"                   * default U/C search code
CATA      INIT      "A "                    * Pat. Class category
CATBC     INIT      "BC"                    * booking cancellation reason
CATTP     INIT      "TP"                    * Priority category
CATWU     INIT      "WU"                    * U/C category
DASH      INIT      " - "                   * a dash
FILPREC   INIT      "wat37c"                * prefix for temp file clinic
FILPRED   INIT      "wat37d"                * prefix for temp file doctors
FINISH    INIT      "Finish"                * default to description
NEEDNP    FORM      "48"                    * limit when new tble headg printed
PREFDOCT  INIT      "Doctor Code : "        * Doctor Code prefix for report
PREFUNCL  INIT      "Unit/Clinic Code : "   * Unit/Clinic prefix for report
PREFFROM  INIT      "From Re-assessment Date : "  * From date prefix for report
PREFTO    INIT      "To   Re-assessment Date : "  * To date prefix for report
PRINTLIM  FORM      "55"                    * no. lines to print per page
SP50      INIT      "                                                   "
START     INIT      "Start"                 * default from value
Z8        INIT      "zzzzzzzz"              * used in previous read
.
PRGID     INIT      "IBAWAT37"
PRGNAM    INIT      "Category Assessment Report"
VERSION   INIT      "V12.01.00"
.
.*********************************************************************
.*                  ML0000                                           *
.*        Mainline Code / Controlling Logic                          *
.*********************************************************************
ML0000    CALL      INIT0000                * initialization and open files
.
ML0500    CALL      KSEQ0000                * enter the report type
          BRANCH    MOPTION,ML1000:         * unit/clinic surname sequence
                            ML2000          * doctor code surname sequence
.
          GOTO      ML9999
.
. ------ produce a report in Unit/Clinic sequence ------
.
ML1000    CALL      UNCL0000                * produce Unit/Clinic report
          GOTO      ML0500
.
. ------ produce a report in Doctor Code sequence ------
.
ML2000    CALL      DOCT0000                * produce Doctor Code report
          GOTO      ML0500
.
ML9999    CHAIN     PGM
+
.*********************************************************************
.*                  INIT0000                    Called by : ML0000   *
.*        Initialization and open files                              *
.*********************************************************************
INIT0000  DISPLAY   *P1:1,*EF               * clear screen
          CALL      DISPHEAD                * display program heading
          CALL      IBACLOCK                * get current date/time
          CALL      TFILENAM
.
          OPEN      PATCODE1,"patcodes"     * open codes file
          OPEN      PATDO1A1,"patdo1af"     * open doctors file
          OPEN      PATDO1A2,"patdo1af"     * open doctors file
          OPEN      PATMA1A1,"patma1af"     * open master file
          OPEN      PATMX1A1,"patmx1af"     * open master extension file
          OPEN      WATCATA2,"watcataf"     * WL category change file
          OPEN      WATMESA1,"watmesaf"     * WL comments file
          OPEN      WATTR1A2,"wattr1af"     * WL treatment file
          OPEN      WATWTAA1,"watwtaaf"     * WL movement analysis file
          OPEN      CONTROLF,"controlf"     
.
          MOVE      ONE,CNEWDS              * new ? option
          READ      CONTROLF,TWENTY1;*247,PTCNDCQS
          READ      CONTROLF,THIRTY7;*140,WTCNTRG1,WTCNTRG2,WTCNTRG3,WTCNTRG4:
                                          WTCNTRG5,WTCNTRG6
          READ      CONTROLF,THIRTY7;*173,WTCNURDY,WTCNSUDY,WTCNRODY
.
INIT9999  RETURN
+
.*********************************************************************
.*                  KSEQ0000                    Called by : ML0500   *
.*        Enter the sequence to report by                            *
.*        Returns : MOPTION = 0   exit selected                      *
.*                  MOPTION = 1   unit/clinic surname selected       *
.*                  MOPTION = 2   doctor code selected               *
.*********************************************************************
KSEQ0000  DISPLAY   *P1:3,*EF:
                    *V2LON,*P1:4,"0",*P1:5,"1",*P1:6,"2",*HOFF:
                    *P3:4,"= Exit":
                    *P3:5,"= Unit/Clinic Code Sequence":
                    *P3:6,"= Doctor Code Sequence":
                    *P1:8,"Select Option : "
.
KSEQ1000  KEYIN     *P17:8,*DV,UNDLN1:
                    *P17:8,*V2LON,MOPTION:
                    *P17:8,*DV,MOPTION 
.
          BRANCH    MOPTION,KSEQ9999,KSEQ9999
.
          COMPARE   ZERO,MOPTION            * was 0 entered
          GOTO      KSEQ9999 IF EQUAL       * yes
.
          BEEP
          GOTO      KSEQ1000                * invalid selection
.
KSEQ9999  RETURN
+
.*********************************************************************
.*                  UNCL0000                    Called by : ML1000   *
.*        Produce a report in Unit/Clinic sequence                   *
.*********************************************************************
UNCL0000  DISPLAY   *P48:2,*V2LON,"- Unit/Clinic Code Sequence "
.
UNCL0500  CALL      KDAT0000                * enter the dates
          BRANCH    EXIT,UNCL9000           * no to date entered
.
          CALL      KUCC0000                * enter the Unit/Clinic Code
.
          CALL      CONTQST                 * continue question
          BRANCH    CEXIT,UNCL1000:         * Yes entered
                          UNCL0500:         * No entered
                          UNCL9000          * Cancel entered
.
UNCL1000  CALL      CLRP0000                * generate the report
.
UNCL9000  HLINE     *G37,2,48,80            * clear the sub-heading
.
UNCL9999  RETURN
+
.*********************************************************************
.*                  DOCT0000                    Called by : ML2000   *
.*        Produce a report in Doctor Code sequence                   *
.*********************************************************************
DOCT0000  DISPLAY   *P48:2,*V2LON,"- Doctor Code Sequence "
.
DOCT0500  CALL      KDAT0000                * enter the dates
          BRANCH    EXIT,DOCT9000           * no to date entered
.
          CALL      KDOC0000                * enter the doctors code
.
          CALL      CONTQST                 * continue question
          BRANCH    CEXIT,DOCT1000:         * Yes entered
                          DOCT0500:         * No entered
                          DOCT9000          * Cancel entered
.
DOCT1000  CALL      DORP0000                * generate the report
.
DOCT9000  HLINE     *G37,2,48,80            * clear the sub-heading
.
DOCT9999  RETURN
+
.*********************************************************************
.*                  KDAT0000                    Called by : UNCL0000 *
.*        Enter the from date and to date for the report    DOCT0000 *
.*        Returns   FROMDATE      the from date                      *
.*                  TODATE        the to date                        *
.*                  EXIT = 1      nothing entered for 'dateto'       *
.*********************************************************************
KDAT0000  DISPLAY   *P1:9,*EF:
                    *P1:10,"From Re-Assessment Date : ":
                    *P1:11,"To   Re-Assessment Date : "
.
. ------ set up the common parameters for keydate ------
.
          MOVE      ONE,CCANLDTE            * can cancel default
          MOVE      ONE,CDEFDTE             * hit enter once to accept default
          MOVE      ZERO,CHIGHLT            * use highlighting
          CALL      IBACLOCK                * get current date
.
KDAT0500  MOVE      CCC,CCENT               * default to current century
          UNPACK    SP6,CYEAR,CMON,CDAY     * no default date
.
. ------ enter the from date ------
.
          MOVE      TEN,CVERT               * row for input
          MOVE      "27",CCOL               * column for input
          CALL      KEYDATE                 * enter the date
.
          MATCH     SP2,CYEAR               * was a date entered ?
          GOTO      KDAT1000 IF EQUAL       * no, use default
.
          PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",FROMDATE           * zero fill
          RESET     FROMDATE
.
KDAT0700  CALL      PACDATE                 * format date
          PACK      PRFROM,PREFFROM,CPCDATE * set up the date for printing
          RESET     PRFROM   
          GOTO      KDAT1500                * enter to date
.
. ------ use default from date ------
.
KDAT1000  DISPLAY   *PCCOL:CVERT,*V2LON,START      * display start
          PACK      FROMDATE,START,SP8      * set up the date
          RESET     FROMDATE
          PACK      PRFROM,PREFFROM,FROMDATE * set up the date for printing
          RESET     PRFROM   
.
. ------ enter the to date ------
.
KDAT1500  MOVE      TEN1,CVERT              * row for input
          MOVE      "27",CCOL               * column for input
          UNPACK    SP6,CYEAR,CMON,CDAY     * no default date
          CALL      KEYDATE                 * enter the date
.
          MATCH     SP2,CYEAR               * was a date entered ?
          GOTO      KDAT2000 IF EQUAL       * no, use default
.
          PACK      TODATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE             * zero fill
          RESET     TODATE   
.
. ------ use default to date ------
.
KDAT2000  DISPLAY   *PCCOL:CVERT,*V2LON,FINISH      * display Finish
          PACK      TODATE,FINISH,SP8              * set up the date
          RESET     TODATE
          PACK      PRTO,PREFTO,TODATE        * set up the date for printing
          RESET     PRTO   
.
KDAT2050  CALL      PACDATE                 * format date
.
. ------ check that to date is after from date ------
.
          MATCH     START,FROMDATE          * was default entered
          GOTO      KDAT2100 IF EQUAL       * yes, cont do check
.
          MATCH     FROMDATE,TODATE         * is to < from date ?
          GOTO      KDAT5200 IF LESS        * yes, invalid to date
.
KDAT2100  MOVE      FALSE,EXIT              * set exit status
          PACK      PRTO,PREFTO,CPCDATE     * set up print date line
          RESET     PRTO
          GOTO      KDAT9999
.
. ------ ERROR: to date is less than from date ------
.
KDAT5200  DISPLAY   *P1:24,*B,"The TO date mustn't be before the FROM date.  ";
          CALL      HITENTER
          GOTO      KDAT1500
.
. ------ no to date entered ------
.
KDAT9000  MOVE      ONE,EXIT                * no date entered
          UNPACK    SP8,FROMDATE            * clear from date
          UNPACK    SP8,TODATE              * clear to date
.
KDAT9999  RETURN
+
.*********************************************************************
.*                  KUCC0000                    Called by : UNCL0000 *
.*        Keyin the Unit/Clinic Code                                 *
.*        Returns : CODEUNCL      the unit/clinic code               *
.*                  DESCUNCL      the unit/clinic description        *
.*********************************************************************
KUCC0000  MOVE      ONE,QUPER               * ? not performed
          MOVE      TEN3,CVERT              * row for input
          MOVE      "24",CCOL               * column for input
.
          DISPLAY   *P1:13,"For Unit/Clinic Code : "
.
KUCC1000  KEYIN     *PCCOL:CVERT,*DV,UNDLN3:
                    *PCCOL:CVERT,*V2LON,CODEUNCL:
                    *PCCOL:CVERT,*DV,CODEUNCL
.
          ENDSET    CODEUNCL
          APPEND    SP3,CODEUNCL
          RESET     CODEUNCL
.
          MATCH     SP3,CODEUNCL            * was anything entered ?
          GOTO      KUCC4000 IF EQUAL       * no
.
          MATCH     QUEST,CODEUNCL          * was ? entered ?
          GOTO      KUCC2000 IF NOT EQUAL   * no
.
. ------ ? entered ------
.
          PACK      CODE,CATWU              * set up category for codes file
          CALL      PATCODDS                * ? routine
          MOVE      ZERO,QUPER              * set ? as performed
          MOVE      "24",CVERT              * input on line 24
          MOVE      "24",CCOL               * input in col 24
          PACK      CODEUNCL,SP3            * clear the code
          DISPLAY   *P1:24,"For Unit/Clinic Code : ",*EL
          GOTO      KUCC1000

. ------ validate what was entered ------
.
KUCC2000  PACK      KEY5,CATWU,CODEUNCL     * key = cat and value entered
          CALL      RDCODE1                 * read codes file
          BRANCH    OVRCD,KUCC3000          * invalid entry
.
          MOVE      TDESC,DESCUNCL          * set up description
          PACK      PRUNDO2,PREFUNCL,CODEUNCL,DASH,TDESC * set up heading
          RESET     PRUNDO2
          GOTO      KUCC5000
.
. ------ ERROR: invalid code entered ------
.
KUCC3000  DISPLAY   *P1:24,*B,"Invalid Unit/Clinic Code entered.  ",*EL;
          CALL      HITENTER
          PACK      CODEUNCL,SP3            * clear the code
.
          BRANCH    QUPER,KUCC1000          * ? not entered
.
          DISPLAY   *P1:24,"For Unit/Clinic Code : ",*EL
          GOTO      KUCC1000
.
. ------ only enter hit ------
.
KUCC4000  PACK      CODEUNCL,ALL,SP1        * set up default value
          PACK      DESCUNCL,SP20           * clear description
          PACK      PRUNDO2,PREFUNCL,ALL,SP30 * set up 'all' description
          RESET     PRUNDO2
.
. ------ valid entry ------
.
KUCC5000  BRANCH    QUPER,KUCC8000          * ? not entered
.
          CALL      RDIS0000                * redisplay the screen
.
          MATCH     ALL,CODEUNCL            * was anything entered ??
          GOTO      KUCC0000 IF EQUAL       * no, so re-enter code
.
          DISPLAY   *P1:13,"For Unit/Clinic Code : "
.
KUCC8000  DISPLAY   *P24:13,*V2LON,CODEUNCL,*HOFF,*P40:13,DESCUNCL
.
KUCC9999  RETURN
+
.*********************************************************************
.*                  KDOC0000                    Called by : DOCT0000 *
.*        Keyin the Doctors Code                                     *
.*        Returns : CODEDOCT      the doctor code                    *
.*                  DESCDOCT      the doctor description             *
.*********************************************************************
KDOC0000  MOVE      TEN3,CVERT              * row for input
          MOVE      "24",CCOL               * column for input
.
          DISPLAY   *P1:13,"For Doctor Code      : "
.
          MOVE      SP10,CODEDOCT
          MOVE      "24",ECOL
          MOVE      "13",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          CALL      PATDOCKY
          BRANCH    EXIT,KDOC4000,KDOC0000
.
          MOVE      DCODE,CODEDOCT
.
KDOC2000  PACK      KEY6,CODEDOCT           * key = cat and value entered
          CALL      FDOC0000                * format the name
          PACK      DESCDOCT,PACFNAME       * store the name
          PACK      PRUNDO2,PREFDOCT,CODEDOCT,DASH,DESCDOCT * set up print head.
          RESET     PRUNDO2
          GOTO      KDOC5000
.
. ------ only enter hit ------
.
KDOC4000  PACK      CODEDOCT,ALL,SP3        * set up default value
          PACK      DESCDOCT,SP20           * clear description
          PACK      PRUNDO2,PREFDOCT,ALL,SP30 * set up 'all' description
          RESET     PRUNDO2
.
KDOC5000  DISPLAY   *P24:13,*V2LON,CODEDOCT,*P40:13,*HOFF,DESCDOCT
.
KDOC9999  RETURN
+
.*********************************************************************
.*                  RDIS0000                    Called by : KUCC0000 *
.*        Redisplay the screen after ?                      KDOC0000 *
.*********************************************************************
RDIS0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*P1:5,"1",*P1:6,"2",*HOFF:
                    *P03:4,"= Exit":
                    *P03:5,"= Unit/Clinic Code Sequence":
                    *P03:6,"= Doctor Code Sequence":
                    *P01:8,"Select Option : ",*V2LON,MOPTION
.
. ------ display the From date ------
.
RDIS6000  MATCH     START,FROMDATE          * is there a date ?
          GOTO      RDIS6500 IF NOT EQUAL   * yes
.
          DISPLAY   *P1:10,"From Re-assessment Date : ",*V2LON,START,*HOFF
          GOTO      RDIS7000

RDIS6500  UNPACK    FROMDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          DISPLAY   *P1:10,"From Re-assessment Date : ",*V2LON,CPCDATE,*HOFF
.
. ------ display the To date ------
.
RDIS7000  MATCH     FINISH,TODATE           * is there a date ?
          GOTO      RDIS8000 IF NOT EQUAL   * yes
.
          DISPLAY   *P1:11,"From Re-assessment Date : ",*V2LON,FINISH,*HOFF
          GOTO      RDIS9999

RDIS8000  UNPACK    TODATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          DISPLAY   *P1:11,"To   Re-assessment Date : ",*V2LON,CPCDATE,*HOFF
.
. ------ note: 'kucc' or 'kdoc' will redisplay their own prompt ------
.
RDIS9999  RETURN
+
.*********************************************************************
.*                  CLRP0000                    Called by : UNCL0000 *
.*        Generates the report for Unit/Clinic sequence              *
.*********************************************************************
CLRP0000  CALL      CRTC0000                * create temp file
          DISPLAY   *P1:24,"Obtaining the valid entries now.",*EL
.
. ------ start looping through 2nd index of treatment file ------
.
CLRP1000  PACK      KEY20,ONE,SP20          * start at status one
          CALL      RDSTREA2                * positional read
.
. -----------------------------------------
. ------ next read on treatment file ------
. -----------------------------------------
.
CLRP2000  CALL      RDKTREA2                * next read
          BRANCH    OVRCD,CLRP5000          * finished report
.
          DISPLAY   *P70:24,WMURNO          * display UR number so shows working
.
          CALL      DAVA0000                * validate the data
          BRANCH    EXIT,CLRP2000:          * invalid
                         CLRP5000           * at end of date range 
.
. --------------------------------------------------------------------
. ------ have a correct patient so write a temp file record ------
. --------------------------------------------------------------------
.
CLRP4000  CALL      WATCNTCN                * obtain booking canc. stats
.
. ------ write to the temp file ------
.
CLRP4950  PACK      KEY22,WMUNIT,WMDATE1,WMURNO,WMCODE,DWMCNT
          MOVE      WMDESC,XWMDESC          * shorten the description
          MOVE      WMCNT,DWMCNT            * set up as a dim
          WRITE     CLINFILE,KEY22;WMURNO,WMCODE,DWMCNT,XWMDESC,WMDATE1,WMUNIT:
                                   WMPTY,WMDOCTOR,WMSTAY,WMTIME,WTWMREAD:
                                   WTWMDEDA,XWTWMHOS,XWTWMPAT,XWTWMOTH:
                                   XWMBOOKR,XWMBOKDA
.
          DISPLAY   *P70:24,*V2LON,WMURNO   * highlight UR to show valid person
.
          GOTO      CLRP2000                * read next record
.
. -----------------------------------------------------------
. ------ have finished looping through treatment file, ------
. ------ start doing the report by reading temp file   ------
. -----------------------------------------------------------
.
CLRP5000  CALL      IBACLOCK                * get current date and time
          MOVE      ZERO,CPAGENO            * initialize page number
          MOVE      ZERO,CNTPATTL           * clear total count
          MOVE      ZERO,CNTPATCL           * clear patient count
          MOVE      "Unit/Clinc Code Seq.",CPHDROPT
          DISPLAY   *P1:24,"Producing the report now.....",*EL
.
. ------ do an initial positional read then next read to obtain ------
. ------ the first clinics name before the heading is printed   ------
.
          PACK      KEY22,SP20,SP2          * key to position at start
          READ      CLINFILE,KEY22;;        * positional read
.
          READKS    CLINFILE;XWMURNO,XWMCODE,DXWMCNT,XWMDESC,XWMDATE1,XWMUNIT:
                             XWMPTY,XWMDOCTO,XWMSTAY,XWMTIME,XWMREADA,XWMDEFDA:
                             XWTWMHOS,XWTWMPAT,XWTWMOTH,XWMBOOKR,XWMBOKDA
          GOTO      CLRP7000 IF OVER
          MOVE      DXWMCNT,XWMCNT 
.
. ------ stores the first unit's name ------
.
          PACK      KEY5,CATWU,XWMUNIT      * unit as the key
          CALL      RDCODE1                 * read codes file
          PACK      PRUNDO1,PREFUNCL,XWMUNIT,DASH,TDESC * unit name
          RESET     PRUNDO1
          MOVE      XWMUNIT,PREVUN          * update previous unit
.
          CALL      NEWP0000                * print new page and table heading
          GOTO      CLRP5200                * continue
.
. -------------------------------------------
. ------ read next record of temp file ------
. -------------------------------------------
.
CLRP5100  READKS    CLINFILE;XWMURNO,XWMCODE,DXWMCNT,XWMDESC,XWMDATE1,XWMUNIT:
                             XWMPTY,XWMDOCTO,XWMSTAY,XWMTIME,XWMREADA,XWMDEFDA:
                             XWTWMHOS,XWTWMPAT,XWTWMOTH,XWMBOOKR,XWMBOKDA
          GOTO      CLRP9000 IF OVER
          MOVE      DXWMCNT,XWMCNT 
.
. ------ see if it is a different clinic ------
.
          MATCH     PREVUN,XWMUNIT          * is previous same as current ??
          GOTO      CLRP5200 IF EQUAL       * yes, dont need a new heading
.
. ---------------------------------------------------------------
. ------ have a new clinic type (only applies when ALL)    ------
. ------ do underlines for last clinic, give patient count ------
. ------ set up new clinic name, do new table heading      ------
. ------ update the previous clinic with the new one       ------
. ---------------------------------------------------------------
.
          PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*"
          UNPACK    PRUNDO1,DIM19,DIM26     * get the two parts
          PRINT     *N,"Total Patients Printed for ",DIM26," is ",CNTPATCL
          ADD       THREE,CVERT             * increment line counter
          MOVE      ZERO,CNTPATCL           * clear patient count
.
. ------ get the new units details ------
.
          PACK      KEY5,CATWU,XWMUNIT      * unit as the key
          CALL      RDCODE1                 * read codes file
          PACK      PRUNDO1,PREFUNCL,XWMUNIT,DASH,TDESC * store new unit
          RESET     PRUNDO1
          MOVE      XWMUNIT,PREVUN          * update previous unit
.
. ------ see if there is enough room on the page for new table heading ------
.
          COMPARE   CVERT,NEEDNP            * will table fit on page ??
          GOTO      CLRP5150 IF NOT LESS    * yes, print new table heading only
.
          CALL      NEWP0000                * print new page and table heading
          GOTO      CLRP5200
.
CLRP5150  CALL      PTBH0000                * print new table
.
. -------------------------------------------------------
. ------ obtain the current information to display ------
. -------------------------------------------------------
.
CLRP5200  CALL      FINF0000                * format all the information
.
. ------ print the line but first see if it will fit on page ------
.
CLRP5500  COMPARE   CVERT,PRINTLIM          * is new page needed ??
          GOTO      CLRP5700 IF NOT LESS    * no
.
CLRP5600  CALL      NEWP0000                * print new page heading
.
CLRP5700  CALL      PPAT0000                * print the line
          GOTO      CLRP5100                * read next record
.
. ------ there is no data obtained for this report ------
.
CLRP7000  CALL      NEWP0000                * print a page heading
          PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*":
                    *N,*N,"There are no patients for the entered values."
          GOTO      CLRP9050
.
. --------------------------------
. ------ report is finished ------
. --------------------------------
.
CLRP9000  UNPACK    PRUNDO1,DIM19,DIM26     * get the two parts
          PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*":
                    *N,*N,"Total Patients Printed for ",DIM26," : ",CNTPATCL:
                    *N,*N,"Grand Total Patients Printed ",SP20,SP4," : ":
                    CNTPATTL
.
CLRP9050  PRINT     *N,*N,*N,"***  END OF REPORT ****"
.
          CALL      DETP0000                * delete the temp file
          MOVE      "CATEGORY ASSESSMENT REPORT   ",PRGNAM  * get back name
.
CLRP9999  RETURN
+
.*********************************************************************
.*                  DORP0000                    Called by : DOCT0000 *
.*        Generates the report for Doctor Code sequence              *
.*********************************************************************
DORP0000  CALL      CRTD0000                * create temp file
          DISPLAY   *P1:24,"Obtaining the valid entries now.",*EL
.
. ------ start looping through 2nd index of treatment file ------
.
DORP1000  PACK      KEY20,ONE,SP20          * start at status one
          CALL      RDSTREA2                * positional read
.
. -----------------------------------------
. ------ next read on treatment file ------
. -----------------------------------------
.
DORP2000  CALL      RDKTREA2                * next read
          BRANCH    OVRCD,DORP5000          * finished report
.
          DISPLAY   *P70:24,WMURNO          * display UR number so shows working
.
. ------ validate the record just read in ------
.
          CALL      DAVA0000                * validate the data
          BRANCH    EXIT,DORP2000:          * invalid
                         DORP5000           * at end of date range 
.
. --------------------------------------------------------------------
. ------ have a correct patient so write a temp file record ------
. --------------------------------------------------------------------
.
DORP4000  CALL      WATCNTCN                * obtain booking canc. stats
.
. ------ write to the temp file ------
.
DORP4950  PACK      KEY25,WMDOCTOR,WMDATE1,WMURNO,WMCODE,DWMCNT
          MOVE      WMDESC,XWMDESC          * shorten the description
          MOVE      WMCNT,DWMCNT            * set up as a dim
          WRITE     DOCTFILE,KEY25;WMURNO,WMCODE,DWMCNT,XWMDESC,WMDATE1,WMUNIT:
                                   WMPTY,WMDOCTOR,WMSTAY,WMTIME,WTWMREAD:
                                   WTWMDEDA,XWTWMHOS,XWTWMPAT,XWTWMOTH:
                                   XWMBOOKR,XWMBOKDA
.
          DISPLAY   *P70:24,*V2LON,WMURNO   * highlight UR to show valid person
.
          GOTO      DORP2000                * read next record
.
. -----------------------------------------------------------
. ------ have finished looping through treatment file, ------
. ------ start doing the report by reading temp file   ------
. -----------------------------------------------------------
.
DORP5000  CALL      IBACLOCK                * get current date and time
          MOVE      ZERO,CPAGENO            * initialize page number
          MOVE      ZERO,CNTPATTL           * clear total count
          MOVE      ZERO,CNTPATCL           * clear patient count
          MOVE      "Doctor Code Sequence",CPHDROPT
          DISPLAY   *P1:24,"Producing the report now.....",*EL
.
. ------ do an initial positional read then next read to obtain ------
. ------ the first clinics name before the heading is printed   ------
.
          PACK      KEY25,SP20,SP5          * key to position at start
          READ      DOCTFILE,KEY25;;        * positional read
.
          READKS    DOCTFILE;XWMURNO,XWMCODE,DXWMCNT,XWMDESC,XWMDATE1,XWMUNIT:
                             XWMPTY,XWMDOCTO,XWMSTAY,XWMTIME,XWMREADA,XWMDEFDA:
                             XWTWMHOS,XWTWMPAT,XWTWMOTH,XWMBOOKR,XWMBOKDA
          GOTO      DORP7000 IF OVER
          MOVE      DXWMCNT,XWMCNT 
.
. ------ stores the first doct's name ------
.
          PACK      KEY6,XWMDOCTO           * doct as the key
          CALL      FDOC0000                * format docts name
          PACK      PRUNDO1,PREFDOCT,XWMDOCTO,DASH,PACFNAME
          RESET     PRUNDO1
          MOVE      XWMDOCTO,PREVDT         * update previous doct
.
          CALL      NEWP0000                * print new page and table heading
          GOTO      DORP5200                * continue
.
. -------------------------------------------
. ------ read next record of temp file ------
. -------------------------------------------
.
DORP5100  READKS    DOCTFILE;XWMURNO,XWMCODE,DXWMCNT,XWMDESC,XWMDATE1,XWMUNIT:
                             XWMPTY,XWMDOCTO,XWMSTAY,XWMTIME,XWMREADA,XWMDEFDA:
                             XWTWMHOS,XWTWMPAT,XWTWMOTH,XWMBOOKR,XWMBOKDA
          GOTO      DORP9000 IF OVER
          MOVE      DXWMCNT,XWMCNT 
.
. ------ see if it is a different doctor ------
.
          MATCH     PREVDT,XWMDOCTO         * is previous same as current ??
          GOTO      DORP5200 IF EQUAL       * yes, dont need a new heading
.
. ---------------------------------------------------------------
. ------ have a new doctor code (only applies when ALL)    ------
. ------ do underlines for last doctor, give patient count ------
. ------ set up new doctor name, do new table heading      ------
. ------ update the previous doctor with the new one       ------
. ---------------------------------------------------------------
.
          PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*"
          UNPACK    PRUNDO1,DIM14,DIM31     * get the two parts
          PRINT     *N,"Total Patients Printed for ",DIM31," is ",CNTPATCL
          ADD       THREE,CVERT             * increment line counter
          MOVE      ZERO,CNTPATCL           * clear patient count
.
. ------ get the new docts details ------
.
          PACK      KEY6,XWMDOCTO           * doct as the key
          CALL      FDOC0000                * get docs name
          PACK      PRUNDO1,PREFDOCT,XWMDOCTO,DASH,PACFNAME
          RESET     PRUNDO1
          MOVE      XWMDOCTO,PREVDT         * update previous doct
.
. ------ see if there is enough room on the page for new table heading ------
.
          COMPARE   CVERT,NEEDNP            * will table fit on page ??
          GOTO      DORP5150 IF NOT LESS    * yes, print new table heading only
.
          CALL      NEWP0000                * print new page and table heading
          GOTO      DORP5200
.
DORP5150  CALL      PTBH0000                * print new table
.
. -------------------------------------------------------
. ------ obtain the current information to display ------
. -------------------------------------------------------
.
DORP5200  CALL      FINF0000                * format all the information
.
. ------ print the line but first see if it will fit on page ------
.
DORP5500  COMPARE   CVERT,PRINTLIM          * is new page needed ??
          GOTO      DORP5700 IF NOT LESS    * no
.
DORP5600  CALL      NEWP0000                * print new page heading
.
DORP5700  CALL      PPAT0000                * print the line
          GOTO      DORP5100                * read next record
.
. ------ there is no data obtained for this report ------
.
DORP7000  CALL      NEWP0000                * print a page heading
          PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*":
                    *N,*N,"There are no patients for the entered values."
          GOTO      DORP9050
.
. --------------------------------
. ------ report is finished ------
. --------------------------------
.
DORP9000  UNPACK    PRUNDO1,DIM14,DIM31     * get the two parts
          PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*":
                    *N,*N,"Total Patients Printed for ",DIM31," : ",CNTPATCL:
                    *N,*N,"Grand Total Patients Printed ",SP20,SP9," : ":
                    CNTPATTL
.
DORP9050  PRINT     *N,*N,*N,"***  END OF REPORT ****"
.
          CALL      DETP0000                * delete the temp file
          MOVE      "CATEGORY ASSESSMENT REPORT   ",PRGNAM  * get back name
.
DORP9999  RETURN
+
.*********************************************************************
.*                  DAVA0000                    Called by : CLRP0000 *
.*        Checks each record of the treatment file to see   DORP0000 *
.*        it satisfies all the criteria                              *
.*        Returns : EXIT = 0      valid record                       *
.*                  EXIT = 1      invalid record                     *
.*                  EXIT = 2      at end of allowable data           *
.    Modified 13/10/1990 Bill Dew
.    Routine was setting Exit=2 when the re-assessment date was outside
.    the date range which is wrong.  The CLRP routine loops on 2nd
.    index by wmstat thus when wmstat=3 then exit=2.
.    Note only wmstat 1 and 2 are accepted.
.*********************************************************************
DAVA0000  MATCH     SP8,WTWMREAD            * is there a re-ass. date ??
          GOTO      DAVA9000 IF EQUAL       * no, so ignore patient
.
DAVA1000  PACK      KEY8,WMURNO             * UR number as key
          CALL      RDMAST1                 * read master file
          BRANCH    OVRCD,DAVA9000          * yes, so ignore
.
. ------ check that still in the date range ------
.
DAVA2000  MATCH     START,FROMDATE          * was start entered ??
          GOTO      DAVA2100 IF EQUAL       * yes, dont do initial date check
.
          MATCH     FROMDATE,WTWMREAD 
          GOTO      DAVA9000 IF LESS
.
DAVA2100  MATCH     WTWMREAD,TODATE 
          GOTO      DAVA9000 IF LESS 
.
. ------ check that have the correct status of 1 or 2 ------
.
          BRANCH    WMSTAT,DAVA2200,DAVA2200 * status of one or two
          GOTO      DAVA9500           * if (wmstat==3) return(exit=2)
.
. ------ check on unit/clinic OR doctors code ------
.
DAVA2200  BRANCH    MOPTION,DAVA3000:       * check unit/clinic
                            DAVA3100        * check doctor code
.
. ------ check that have the correct unit/clinic ------
.
DAVA3000  MATCH     ALL,CODEUNCL            * are we using all codes ??
          GOTO      DAVA3200 IF EQUAL       * yes, use this value
.
          MATCH     CODEUNCL,WMUNIT         * is it the correct unit ???
          GOTO      DAVA9000 IF NOT EQUAL   * no, read next record
.
          GOTO      DAVA3200                * valid record so continue
.
. ------ check that have the correct doctor code ------
.
DAVA3100  MATCH     ALL,CODEDOCT            * are we using all docts ??
          GOTO      DAVA3200 IF EQUAL       * yes, use this value
.
          MATCH     CODEDOCT,WMDOCTOR       * is it the correct doct ???
          GOTO      DAVA9000 IF NOT EQUAL   * no, read next record
.
. ------ check that a deferred or staged admission ------
.
DAVA3200  PACK      KEY5,CATTP,WMPTY        * priority as key
          CALL      RDCODE1                 * read codes file
          BRANCH    OVRCD,DAVA9000          * invalid priority
.
          MATCH     ANST,TCINDC2            * is it a staged patient ??
          GOTO      DAVA8000 IF EQUAL       * yes
.
          MATCH     ANSD,TCINDC2            * is it a deferred patient ??
          GOTO      DAVA9000 IF NOT EQUAL   * no, so ignore
.
. ------ have a valid patient ------
.
DAVA8000  MOVE      FALSE,EXIT              * valid data
          GOTO      DAVA9999
.
. ------ have an invalid patient ------
.
DAVA9000  MOVE      TRUE,EXIT               * invalid data
          GOTO      DAVA9999
.
. ------ at the end of the date range ------
.
DAVA9500  MOVE      TWO,EXIT                * end of data range
.
DAVA9999  RETURN
+
.*********************************************************************
.*                  FDOC0000                    Called by : lots     *
.*        Obtain the doctors name and format it                      *
.*        Paras   : KEY6          contains the doctors code          *
.*        Returns : PACFNAME      doctors name (65 chars)            *
.*********************************************************************
FDOC0000  PACK      PACFNAME,SP30,SP30,SP5  * clear the variable
          CALL      RDDOCT1                 * read doct file
          BRANCH    OVRCD,FDOC9999          * invalid code
          MOVE      DTITL,PACTITLE          * title
          MOVE      DGNAME,PACGNAME         * given name
          MOVE      DSNAME,PACSNAME         * surname
          MOVE      TWO,PACFRMT             * format as Surname, Title Given
          CALL      PACNAME                 * format the name
.
FDOC9999  RETURN
+
.*********************************************************************
.*                  FINF0000                    Called by : CLRP0000 *
.*        Obtain and format all info on patient             DORP0000 *
.*********************************************************************
FINF0000  PACK      KEY8,XWMURNO            * UR number as key
          CALL      RDMAST1                 * read master file
          MOVE      PTITL,PACTITLE          * title
          MOVE      PGNAME,PACGNAME         * given name
          MOVE      PSNAME,PACSNAME         * surname
          MOVE      TWO,PACFRMT             * format as Surname, Title Given
          CALL      PACNAME                 * format the name
          PACK      PRNAME,PACFNAME         * store the name
          RESET     PRNAME
          CALL      FADD0000                * format the address
.
. ------ format the date added to list ------
.
          UNPACK    XWMDATE1,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,PRDATE1
.
. ------ format the staged/deferred admission date ------
.
          UNPACK    XWMDEFDA,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,PRDATE2
.
. ------ format the date of birth ------
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,PRDATE4
.
. ------ format the re-assessment date ------
.
          UNPACK    XWMREADA,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,PRDATE5
.
. ------ format the doctors name ------
.
          PACK      KEY6,XWMDOCTO           * docs name as key
          CALL      FDOC0000                * get docs name
          MOVE      PACFNAME,PRDOCT         * set up docs name
.
. ------ format the unit/clinic ------
.
          PACK      KEY5,CATWU,XWMUNIT      * key of unit
          CALL      RDCODE1                 * read codes file
          PACK      PRCLIN,XWMUNIT,SP1,TDESC
.
          CALL      FCOM0000                * format the comments
.
FINF9999  RETURN
+
.*********************************************************************
.*                  FADD0000                    Called by : CLRP0000 *
.*        Format the address of the patient                 DORP0000 *
.*        Returns : PRADDR        the address variable      SURP0000 *
.*********************************************************************
FADD0000  CLEAR     PRADDR                  * clear the variable
. 
. ------ add address line 1 ------
.
          MATCH     SP30,PADD1              * is there an addres line 1 ??
          GOTO      FADD1010 IF EQUAL       * no there isnt
.
          APPEND    PADD1,PRADDR            * put in first address line
.
FADD0500  MATCH     SP1,PRADDR              * is last char a blank ??
          GOTO      FADD1000 IF NOT EQUAL   * no, contine
.
          BUMP      PRADDR,-1               * go back one character
          GOTO      FADD1010 IF EOS         * at start of string
          GOTO      FADD0500
.
. ------ add address line 2 ------
.
FADD1000  APPEND    ",",PRADDR              * add a comma
.
FADD1010  MATCH     SP30,PADD2              * is there an addres line 2 ??
          GOTO      FADD2010 IF EQUAL       * no there isnt
.
          APPEND    PADD2,PRADDR            * append address line 2
.
FADD1500  MATCH     SP1,PRADDR              * is last char a blank ??
          GOTO      FADD2000 IF NOT EQUAL   * no, contine
.
          BUMP      PRADDR,-1               * go back one character
          GOTO      FADD2010 IF EOS         * at start of string
          GOTO      FADD1500
.
. ------ add suburb ------
.
FADD2000  APPEND    ",",PRADDR              * add a comma
.
FADD2010  MATCH     SP20,PSUBRB             * is there a suburb ??
          GOTO      FADD3010 IF EQUAL       * no there isnt
.
          APPEND    PSUBRB,PRADDR           * append suburb
.
FADD2500  MATCH     SP1,PRADDR              * is last char a blank ??
          GOTO      FADD3000 IF NOT EQUAL   * no, contine
.
          BUMP      PRADDR,-1               * go back one character
          GOTO      FADD3010 IF EOS         * at start of string
          GOTO      FADD2500
.
. ------ add post code ------
.
FADD3000  APPEND    ",",PRADDR              * add a comma
.
FADD3010  MATCH     SP4,PPOST               * is there a post code ??
          GOTO      FADD4000 IF EQUAL       * no there isnt
.
          APPEND    PPOST,PRADDR            * append post code
.
FADD3500  MATCH     SP1,PRADDR              * is last char a blank ??
          GOTO      FADD4000 IF NOT EQUAL   * no, contine
.
          BUMP      PRADDR,-1               * go back one character
          GOTO      FADD4000 IF EOS         * at start of string
          GOTO      FADD3500
.
FADD4000  RESET     PRADDR                  * reset var
.
FADD9999  RETURN
+
.*********************************************************************
.*                  FCOM0000                    Called by : CLRP0000 *
.*        Format the comments about patient                          *
.*        Returns : PRCOMM        the comments                       *
.*********************************************************************
FCOM0000  PACK      KEY21,XWMURNO,XWMCODE,XWMCNT,SP1,ONE * get first comments 
          CALL      RDMESA1                 * read comments file
          BRANCH    OVRCD,FCOM9000          * no comments on file
.
          CLEAR     PRCOMM
          APPEND    WATEXT,PRCOMM           * add line one of comments
.
FCOM1000  MATCH     SP1,PRCOMM              * is last char a blank ??
          GOTO      FCOM2000 IF NOT EQUAL   * no
.
          BUMP      PRCOMM,-1               * go back one char
          GOTO      FCOM1000
.
. ------ get the second line of comments ------
.
FCOM2000  PACK      KEY21,XWMURNO,XWMCODE,XWMCNT,SP1,TWO * get second comments 
          CALL      RDMESA1                 * read comments file
          BRANCH    OVRCD,FCOM9500          * no more comments on file
.
          APPEND    SP1,PRCOMM              * put space b/w two comments
          APPEND    WATEXT,PRCOMM           * add line two of comments
          GOTO      FCOM9500                * reset for display
.
FCOM9000  PACK      PRCOMM,SP30,SP30,SP30,SP30 * no comments on file
.
FCOM9500  RESET     PRCOMM
.
FCOM9999  RETURN
+
.*********************************************************************
.*                  NEWP0000                    Called by : CLRP0000 *
.*        Prints the new page heading for a report          DORP0000 *
.*        Para's  : CPHRDOPT      sub heading                        *
.*********************************************************************
NEWP0000  MOVE      ZERO,CNOUNDLN           * underlines at end of page
          MOVE      "CATEGORY ASSESSMENT REPORT -",PRGNAM
          RESET     PRGNAM
.
NEWP0500  CALL      HEAD132                 * do the new heading
.
          PRINT     *1,PRFROM,*45,PRUNDO2   * from date, search type
          PRINT     *1,PRTO                 * to date
.
. ------ print the table heading ------
.
          MOVE      SEVEN,CVERT             * set line counter
          CALL      PTBH0000                * print a table heading
.
NEWP9999  RETURN
+
.*********************************************************************
.*                  PPAT0000                    Called by : CLRP0000 *
.*        Prints a single data line of the report           DORP0000 *
.*        ie. the information about a patients procedure             *
.*        Updates : CNTPATCL      count of pats                      *
.*                  CNTPATTL      count of total patients            *
.*                  CVERT         current row number                 *
.*********************************************************************
PPAT0000  PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*":
                    *N,"|",XWMURNO,*13,PRNAME,*58,"|",*60,XWMDESC,*92,"|":
                    *94,"Added: ",PRDATE1,*113,"Assmnt: ",PRDATE5,*132,"|"
.
. -------------------------------------------
. ------ print line two about patient ------
. -------------------------------------------
.
PPAT2500  PRINT     *1,"|",*13,PRADDR,*58,"|",*60,PRDOCT,*92,"|",*94:
                    "Priority : ",XWMPTY;
.
. ------ determine if a staged or deferred admission ------
.
          PACK      KEY5,CATTP,XWMPTY       * key of priority
          CALL      RDCODE1                 * read codes file
.
          MATCH     ANST,TCINDC2            * is it a staged admission ??
          GOTO      PPAT2700 IF EQUAL       * yes
.
. ------ deferred admission ------
.
          PRINT     *109,"Deferred ";
          GOTO      PPAT2900
.
. ------ staged admission ------
.
PPAT2700  PRINT     *109,"Staged   ";
.
PPAT2900  PRINT     *119,PRDATE2,*132,"|"
.
. -------------------------------------------
. ------ print line three about patient ------
. -------------------------------------------
.
PPAT5000  PRINT     *1,"|",*13,"Home : ",PTELEP,*34,"Business : ",PTELEB:
                    *58,"|",*60,"Unit : ",PRCLIN,*92,"|";
.
. ------ see if there is a booking cancellation reason ------
.
          MATCH     SP3,XWMBOOKR            * is there a booking canc. reason ??
          GOTO      PPAT5500 IF EQUAL       * no, dont display anything
.
. ------ display the stats on booking cancellations ------
.
          PRINT     *94,"Cancel  : Hos=",XWTWMHOS," Pat=",XWTWMPAT:
                    " Oth=",XWTWMOTH;
          GOTO      PPAT5500
.
PPAT5500  PRINT     *132,"|"
.
. -------------------------------------------
. ------ print line four about patient ------
. -------------------------------------------
.
.* ****************************************** Start of Changes         *C-49016
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          MOVE      DSEXDESC,PRSEX
.* ******************************************   End of Changes         *C-49016
.
PPAT7900  PRINT     *1,"|",*13,"Sex: ",PRSEX,*34,"D.O.B.   : ",PRDATE4:
                    *58,"|",*60,"Stay : ",XWMSTAY,*74,"Proc. Time :":
                    *86,XWMTIME,*92,"|";
.
. ------ determine if a there is a latest booking cancellation ------
.
          MATCH     SP3,XWMBOOKR            * is there a booking canc ??
          GOTO      PPAT8300 IF EQUAL       * no.
.
          UNPACK    XWMBOKDA,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * format the cancellation date
          MOVE      CPCDATE,PRDATE3         * set up the date
          MOVE      SP20,TDESC              * default if read fails
          PACK      KEY5,CATBC,XWMBOOKR     * key of booking canc reason
          CALL      RDCODE1                 * read codes file
          MOVE      TDESC,PRCANC            * set up reason
          PRINT     *94,"Latest  : ",PRDATE3," ",PRCANC;
.
PPAT8300  PRINT     *132,"|"
.
. -------------------------------------------
. ------ print line five about patient ------
. -------------------------------------------
.
          PRINT     *1,"|",*13,"Comments : ",PRCOMM,*132,"|"
.
          DISPLAY   *P70:24,XWMURNO         * display UR number to show working
.
. ------ increment the counters ------
.
PPAT9000  ADD       SIX,CVERT               * add to line counter
          ADD       ONE,CNTPATCL            * add to patient count for clinic
          ADD       ONE,CNTPATTL            * add to total pat. count
.
PPAT9999  RETURN
+
.*********************************************************************
.*                  PTBH0000                    Called by : NEWP0000 *
.*        Prints a new table heading                        CLRP0000 *
.*        Needed at a new page or for a new unit/clinic     DORP0000 *
.*        Paras/Returns : CVERT      current row number              *
.*********************************************************************
PTBH0000  PRINT     *N,PRUNDO1:
                    *N,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*":
                    *N,*1,"|",*3,"U/R No ",*13,"Personal Details ",*58,"|":
                    *60,"Medical Details ",*92,"|",*94,"Waiting List Details":
                    *132,"|"
.
          ADD       FOUR,CVERT              * add to line count
.
PTBH9999  RETURN
+
.*********************************************************************
.*                  CRTC0000                    Called by : CLRP0000 *
.*        Create temporary file for Unit/Clinic sequence             *
.*********************************************************************
CRTC0000  CALL      DETP0000                  * delete the temp file
          DISPLAY   *V2LON,*BLINKON,*P1:24,"Please wait.",*EL
          CLEAR     CMDLINE                   * clear the line
          APPEND    "isbuild ",CMDLINE        * create command
          APPEND    TFILNAME,CMDLINE          * add filename
          APPEND    " 116 U60-62,1-8,9-17,18-19",CMDLINE  * add index 1
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID            * create temp file
          OPEN      CLINFILE,TFILNAME         * open temp. file index 1
.
CRTC9999  RETURN
+
.*********************************************************************
.*                  CRTD0000                    Called by : DORP0000 *
.*        Create temporary file for Doctor sequence                  *
.*********************************************************************
CRTD0000  CALL      DETP0000                  * delete the temp file
          DISPLAY   *V2LON,*BLINKON,*P1:24,"Please wait.",*EL
          CLEAR     CMDLINE                   * clear the line
          APPEND    "isbuild ",CMDLINE        * create command
          APPEND    TFILNAME,CMDLINE          * add filename
          APPEND    " 116 U66-71,1-8,9-17,18-19",CMDLINE  * add index 1
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID            * create temp file
          OPEN      DOCTFILE,TFILNAME         * open temp. file index 1
.
CRTD9999  RETURN
+
.*********************************************************************
.*                  DETP0000                    Called by : CLRP0000 *
.*        Delete temporary file                             DORP0000 *
.*********************************************************************
DETP0000  CLEAR     CMDLINE                   * clear the command line
          APPEND    "iserase ",CMDLINE        * create command
          APPEND    TFILNAME,CMDLINE          * add file name
          RESET     CMDLINE
          CLOSE     CLINFILE                  * close temp file so that the
          CLOSE     DOCTFILE                  * iserase works properly
          EXECUTE   CMDLINE,TASKID            * execute the delete command
          DISPLAY   *P1:24,"Temporary file erased.",*EL
.
DETP9999  RETURN
.
. ------ I/O routines needed ------
.
          INC       STD001IO                * standard IO routines
          INC       PATCOMN3                * used for 'gperd'
          INC       PATCODKY                * ? routine on codes file
          INC       PATDOCKY                * ? routine on doctors file
          INC       SEXDSCIO
          INC       WATDSPRO                * ? on procedures
          INC       PATDRGIO/INC            * date ranges for gperd
          INC       PATCODIO/INC            * codes file
          INC       PATDO1IO/INC            * doctors file
          INC       PATMA1IO/INC            * master file
          INC       WATCATIO/INC            * WL category change file
          INC       WATMESIO/INC            * WL comments file
          INC       WATPROIO/INC            * WL procedures file
          INC       WATTR1IO/INC            * WL treatment file
          INC       WATWTAIO/INC            * WL movement analysis file
          INC       WATCNTCN                * WL Cancellations            
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC
 

. ********************************************************** 
. *                                                        *
. *              P R O G R A M  S U M M A R Y              *
. *              -------------  -------------              *
. *                                                        *
. *     Program Name  :   IBARCP01                         *
. *                                                        *
. *     Function      :   Cashier Id Maintenance           *
. *                                                        *
. *     Modifications :   V5.01.00  20/01/92  DIG          *
. *                       Ported from release 5            *
. *                       V5.01.02  22/02/94  I.Rutt       *
. *                       Standards Upgrade                *
. *                       V5.01.03  08/03/94  I.Rutt       *
. *                       Added new keyin (RCPCIDKY) for   *
. *                       Cashier Code.  Added record      *
. *                       Locking                          *    
. *                                                        *
. **********************************************************
+
          INC       STD001FD
          INC       RCPCIDFD/INC
.
CHG       FORM      1
.
FLAG      DIM       1
FIELD     FORM      1
MODE      DIM       1
MODE1     DIM       1
.
SRDIM20   DIM      20
SRDIM20A  DIM      20
.
.
PRGID    INIT    "IBARCP01"
PRGNAM    INIT      "Cashier Id Maintenance"
VERSION   INIT      "V12.01.00"
+
START    CALL          DISPHEAD
.
         DISPLAY       *P56:24,"Opening rcpcidaf";
         OPEN          RCPCIDA1,"rcpcidaf"
.
         DISPLAY       *P64:24,"controlf";
         OPEN          CONTROLF,"controlf"
.
         MOVE          ONE,CNEWDS
         MOVE          ZERO,CKYIMAND
.
MENU     CALL      ULCIDA1
         CALL      SELOPTN
.
         COMPARE   ZERO TO OPTION
         GOTO      ENDIT IF EQUAL
.
REPEAT   BRANCH    OPTION OF INSERT,CHANGE,DELETE
.
.        Add a Cashier
.
INSERT   CALL      ULCIDA1
         CALL      DISP1
         CALL      CLEAR
.
         DISPLAY   *P21:4,*EL;
         MOVE      "21",ECOL
         MOVE      "4",EVERT
         MOVE      ONE,CKYIMODE
         CALL      RCPCIDKY
         BRANCH    EXIT,MENU,MENU   
         CALL      RLCIDA1
         COMPARE   TWO,OVRCD
         GOTO      RECL0000 IF EQUAL
.
INSERT1  MOVE      UNDLN,RCPPASS
         DISPLAY   *P21:6,*EL,RCPPASS
         MOVE      SP6,RCPPASS
.
         KEYIN     *P21:6,*V2LON,RCPPASS,*P21:6,*DV,RCPPASS
.
INSERT2  MOVE      UNDLN,RCPSUR
         DISPLAY   *P21:7,*EL,RCPSUR
         MOVE      SP20,RCPSUR
.
         KEYIN     *P21:7,*V2LON,RCPSUR,*P21:7,*DV,RCPSUR
.
         ENDSET    RCPSUR
         APPEND    SP20,RCPSUR
         RESET     RCPSUR
.
         MATCH     SP20,RCPSUR
         GOTO      INSERT3 IF NOT EQUAL
.
         DISPLAY   *P1:24,*EL,*B,"Field is Mandatory. ";
         CALL      HITENTER
         GOTO      INSERT2
.
INSERT3  MOVE      UNDLN,RCPGIVEN
         DISPLAY   *P21:8,*EL,RCPGIVEN
         MOVE      SP20,RCPGIVEN
.
         KEYIN     *P21:8,*V2LON,RCPGIVEN,*P21:8,*DV,RCPGIVEN
.
INSERT4  MOVE      UNDLN,RCPSECUR
         DISPLAY   *P21:9,*EL,RCPSECUR
         MOVE      ZERO,RCPSECUR
.
         KEYIN     *P21:9,*V2LON,RCPSECUR,*P21:9,*DV,RCPSECUR
         GOTO      INSMOD1
.
INSPOST  KEYIN     *P1:24,*EL,"Ok To Post (",*V2LON,*DV,ANSY,*HOFF,*DV,SLASH:
                   *V2LON,*DV,ANSN,*HOFF,*DV,SLASH,*V2LON,*DV,ANSC,*HOFF:
                   ") ? ",*V2LON,ANS;
.
         REP       UPPLOW,ANS
         CMATCH    ANSN,ANS
         GOTO      INSMOD1 IF EQUAL
.
         CMATCH    ANSY,ANS
         GOTO      INSPOSTY IF EQUAL
.
         CMATCH    ANSC,ANS
         GOTO      INSPOSTC IF EQUAL
.
         GOTO      INSPOST
.
INSPOSTY MOVE      RCPCASH,KEY6
         CALL      WRCIDA1
         COMPARE   TWO,OVRCD
         GOTO      RECL0000 IF EQUAL
         GOTO      INSERT
.
INSMOD1  DISPLAY   *P21:4,*V2LON,RCPCASH:
                   *P21:6,*V2LON,RCPPASS:
                   *P21:7,*V2LON,RCPSUR:
                   *P21:8,*V2LON,RCPGIVEN:
                   *P21:9,*V2LON,RCPSECUR
.
INSMOD2  CALL      MAINQST 
         MOVE      CCITEM,FIELD
.
         COMPARE   ZERO,FIELD
         GOTO      INSPOSTY IF EQUAL
.
         COMPARE   ZERO,CCITEM
         GOTO      INSPOSTC IF LESS
.
INSMOD3  BRANCH    FIELD TO INSPASS,INSSUR,INSGIVEN,INSSECUR
.
         BEEP
         GOTO      INSMOD2
.
INSPASS  MOVE      UNDLN,RCPPASS
         DISPLAY   *P21:6,*EL,RCPPASS
         MOVE      SP6,RCPPASS
.
         KEYIN     *P21:6,*V2LON,RCPPASS,*P21:6,*DV,RCPPASS
         GOTO      INSMOD2
.
INSSUR   MOVE      UNDLN,RCPSUR
         DISPLAY   *P21:7,*EL,RCPSUR
         MOVE      SP20,RCPSUR
.
         KEYIN     *P21:7,*V2LON,RCPSUR,*P21:7,*DV,RCPSUR
.
         ENDSET    RCPSUR
         APPEND    SP20,RCPSUR
         RESET     RCPSUR
.
         MATCH     SP20,RCPSUR
         GOTO      INSMOD2 IF NOT EQUAL
.
         DISPLAY   *P1:24,*EL,*B,"Field is Mandatory. ";
         CALL      HITENTER
         GOTO      INSSUR
.
INSGIVEN MOVE      UNDLN,RCPGIVEN
         DISPLAY   *P21:8,*EL,RCPGIVEN
         MOVE      SP20,RCPGIVEN
.
         KEYIN     *P21:8,*V2LON,RCPGIVEN,*P21:8,*DV,RCPGIVEN
         GOTO      INSMOD2
.
INSSECUR MOVE      UNDLN,RCPSECUR
         DISPLAY   *P21:9,*EL,RCPSECUR
         MOVE      ZERO,RCPSECUR
.
         KEYIN     *P21:9,*V2LON,RCPSECUR,*P21:9,*DV,RCPSECUR
         GOTO      INSMOD2
.
INSPOSTC GOTO      INSERT
.
.        Change a Cashier
.
CHANGE   CALL      ULCIDA1
         CALL      DISP1
         CALL      CLEAR
.
         DISPLAY   *P21:4,*EL;
         MOVE      "21",ECOL
         MOVE      "4",EVERT
         MOVE      ZERO,CKYIMODE
         CALL      RCPCIDKY
         BRANCH    EXIT,MENU,MENU   
         CALL      RLCIDA1  
         COMPARE   TWO,OVRCD
         GOTO      RECL0000 IF EQUAL 
.
CHANGE1  DISPLAY   *P21:4,*V2LON,RCPCASH:
                   *P21:6,*V2LON,RCPPASS:
                   *P21:7,*V2LON,RCPSUR:
                   *P21:8,*V2LON,RCPGIVEN:
                   *P21:9,*V2LON,RCPSECUR
.
CHANGE2  CALL      MAINQST
.
         COMPARE   ZERO,CCITEM
         GOTO      POSTDET IF EQUAL
         GOTO      CHANGE IF LESS
.
CHGBRCH  BRANCH    CCITEM TO CHGPASS,CHGSUR,CHGGIVEN,CHGSECUR
.
         BEEP
         GOTO      CHANGE1
.
CHGPASS  MOVE      UNDLN,RCPPASS
         DISPLAY   *P21:6,*EL,RCPPASS
         MOVE      SP6,RCPPASS
.
         KEYIN     *P21:6,*V2LON,RCPPASS,*P21:6,*DV,RCPPASS
         GOTO      CHANGE2
.
CHGSUR   MOVE      UNDLN,RCPSUR
         DISPLAY   *P21:7,*EL,RCPSUR
         MOVE      SP20,RCPSUR
.
         KEYIN     *P21:7,*V2LON,RCPSUR,*P21:7,*DV,RCPSUR
.
         ENDSET    RCPSUR
         APPEND    SP20,RCPSUR
         RESET     RCPSUR
.
         MATCH     SP20,RCPSUR
         GOTO      CHANGE2 IF NOT EQUAL
.
         DISPLAY   *P1:24,*B,*EL,"Field is Mandatory. ";
         CALL      HITENTER
         GOTO      CHGSUR
.
CHGGIVEN MOVE      UNDLN,RCPGIVEN
         DISPLAY   *P21:8,*EL,RCPGIVEN
         MOVE      SP20,RCPGIVEN
.
         KEYIN     *P21:8,*V2LON,RCPGIVEN,*P21:8,*DV,RCPGIVEN
         GOTO      CHANGE2
.
CHGSECUR MOVE      UNDLN,RCPSECUR
         DISPLAY   *P21:9,*EL,RCPSECUR
         MOVE      ZERO,RCPSECUR
.
         KEYIN     *P21:9,*V2LON,RCPSECUR,*P21:9,*DV,RCPSECUR
         GOTO      CHANGE2
.
POSTDET  CALL      UPCIDA1
         GOTO      CHANGE
.
.        Delete a Cashier
.
DELETE   CALL      ULCIDA1
         CALL      DISP1
.
         DISPLAY   *P21:4,*EL;
         MOVE      "21",ECOL
         MOVE      "4",EVERT
         MOVE      ZERO,CKYIMODE
         CALL      RCPCIDKY
         BRANCH    EXIT,MENU,MENU   
         CALL      RLCIDA1 
         COMPARE   TWO,OVRCD
         GOTO      RECL0000 IF EQUAL 
.
DELETE1  DISPLAY   *P21:4,*V2LON,RCPCASH:
                   *P21:6,*V2LON,RCPPASS:
                   *P21:7,*V2LON,RCPSUR:
                   *P21:8,*V2LON,RCPGIVEN:
                   *P21:9,*V2LON,RCPSECUR
.
REKEY1  CALL       DELQST
.
        BRANCH     CEXIT,DELCSH,DELETE,MENU
.
DELCSH  MOVE       RCPCASH,KEY6
        CALL       DECIDA1
        GOTO       DELETE
.
RECL0000  COMPARE   ONE,OPTION
          GOTO      RECL5000 IF EQUAL
.
          CALL      RECLOCK
          GOTO      RECL9999
.
RECL5000  CALL      REXISTS
RECL9999  GOTO      REPEAT  
.
.      MAIN MENU
.
DISP1    DISPLAY     *P1:4,*EF:
                     *P1:4,"   Cashier Code   :":
                     *P1:6,*V2LON,ONE,*HOFF:
                     *P1:7,*V2LON,TWO,*HOFF:
                     *P1:8,*V2LON,THREE,*HOFF:
                     *P1:9,*V2LON,FOUR,*HOFF:
                     *P2:6,". Password       :":
                     *P2:7,". Surname        :":
                     *P2:8,". Given Name     :":
                     *P2:9,". Security Code  :"
         RETURN
.
CLEAR    MOVE        SP6,RCPCASH
         MOVE        SP6,RCPPASS
         MOVE        SP20,RCPSUR
         MOVE        SP20,RCPGIVEN
         MOVE        ZERO,RCPSECUR
.
         RETURN
.
ENDIT    CHAIN     PGM
         STOP
+
         INC        STD001IO 
         INC       RCPCIDIO/INC
         INC        RCPCIDKY
+

.******************************************************************************
.* System    :  OUTPATIENTS                                                   *
.* Program   :  IBAOUT45                                                      *
.* Desc      :  Attendance By Referral Type                                   *
.******************************************************************************
.* Date      :  27/04/1989                                                    *
.* Author    :  Eric Phan                                                     *
.* Function  :  This program prints attendance statistics from the attendance *
.*              file.                                                         *
.* Mods      :                                                                *
.*       V10.05.01  15/07/2014  Ebon Clements    CAR 261630                   *
.*                  Removed port number from temp file name                   *
.*       V5.07.01   15/10/1999  Steve Armstrong                               *
.*                  Removed reference to CSTRTYR (not used)                   *
.*          V5.07.B02  18/01/1999  Nicole Harrington 507 rebound 095          *
.*                     Mods to use HEAD132                                    *
.*          V5.07.B01  30/10/1998  Jim Stathopoulos                           *
.*                     507 Changes                                            *
.******************************************************************************
.*              V5.02  01/06/89 ETP              SRF 100871                   *
.*                     Fixed loop bug                                         *
.*              V5.03  03/06/89 S.Barcham        SRF 100751                   *
.*                     Split OUTBOK & OUTMAS includes                         *
.*              V5.04  07/06/89 E.Phan                                        *
.*                     Added non-booked attendees                             *
.*                     22/06/89 K.Peak           SRF 101063                   *
.*                     Fixed doubling of totals                               *
.*           V5.01.01  16/07/89 K.Peak                                        *
.*                     Changed Adm & U/R to 8 characters                      *
.*           V5.01.02  03/10/89 K.Peak           SRF 102549                   *
.*                     Changed Month to Period                                *
.*           V5.01.03  04/10/89 K.Peak           SRF 102549                   *
.*                     Fixed display of date                                  *
.*           V5.01.04  05/10/89 E.Phan           SRF 102422                   *
.*                     Fixed multiple printing of totals & heading            *
.*           V5.01.05  14/12/89 E.Phan                                        *
.*                     Previous vers didn't cater for 13 week periods. Fixed. *
.*           V5.01.06  26/05/91 J.Tan            SRF 108541                   *
.*                     Fixed the report heading                               *
.*           V5.01.07  13/07/92 i chung          SRF 115638                   *
.*                     Initialized all total variables for Non-Booked Attend. *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
          INC       IBASEQFD/INC
          INC       PATCOMM/INC
.
          INC       OUTATTFD/INC
          INC       OUTCTYFD/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATDRGFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
+
.
CMDLINE   DIM       50
CODECG    INIT      "CG"         * Group Code
CODES     INIT      "S "         * Referral Source
COUNT     FORM      2
COUNTY    FORM      2
CURPER    DIM       6
CSTPER    DIM       6
.
DIM3      DIM       3
DIM4      DIM       4
DIM14     DIM       14
.
FIRSTREC  FORM      1
FORM7     FORM      7
FORM7Y    FORM      7
.
.
MPERREQD  FORM      2            * Required period (calculated for every record)
MPERUSED  FORM      2            * Number of months used (calculated initially)
MPRTPOS   FORM      3            * Variable print position
.
NPYEAR    FORM      2
.
OUTTMP    DIM       8
OUTTMPXX  IFILE     SQL, FIXED=208, KEY="U1-12"
.
PDDESC    DIM       6            * Period date description (mmm yy)
PENDDATE  DIM       7
PSTRDATE  DIM       7
.
SAVCTYPE  DIM       6
SAVGROUP  DIM       3
.
TGROUP    DIM       3            * (3) OUTTMPXX - Group Code
TCTYPE    DIM       6            * (6)          - Clinic Type
TREFER    DIM       3            * (3)          - Referral Source
TRPERA1   FORM      7            * Attendee totals
TRPERA2   FORM      7
TRPERA3   FORM      7
TRPERA4   FORM      7
TRPERA5   FORM      7
TRPERA6   FORM      7
TRPERA7   FORM      7
TRPERA8   FORM      7
TRPERA9   FORM      7
TRPERA10  FORM      7
TRPERA11  FORM      7
TRPERA12  FORM      7
TRPERA13  FORM      7
TRPERN1   FORM      7            * Non-attendee totals
TRPERN2   FORM      7
TRPERN3   FORM      7
TRPERN4   FORM      7
TRPERN5   FORM      7
TRPERN6   FORM      7
TRPERN7   FORM      7
TRPERN8   FORM      7
TRPERN9   FORM      7
TRPERN10  FORM      7
TRPERN11  FORM      7
TRPERN12  FORM      7
TRPERN13  FORM      7
TRPERX1   FORM      7            * Non-booked attendee totals
TRPERX2   FORM      7
TRPERX3   FORM      7
TRPERX4   FORM      7
TRPERX5   FORM      7
TRPERX6   FORM      7
TRPERX7   FORM      7
TRPERX8   FORM      7
TRPERX9   FORM      7
TRPERX10  FORM      7
TRPERX11  FORM      7
TRPERX12  FORM      7
TRPERX13  FORM      7
TRPERATO  FORM      7            * period totals for Clinic Type
TRPERNTO  FORM      7
TRPERXTO  FORM      7
.
TCPERA1   FORM      7            * CLINIC TYPE TOTALS
TCPERA2   FORM      7
TCPERA3   FORM      7
TCPERA4   FORM      7
TCPERA5   FORM      7
TCPERA6   FORM      7
TCPERA7   FORM      7
TCPERA8   FORM      7
TCPERA9   FORM      7
TCPERA10  FORM      7
TCPERA11  FORM      7
TCPERA12  FORM      7
TCPERA13  FORM      7
TCPERN1   FORM      7
TCPERN2   FORM      7
TCPERN3   FORM      7
TCPERN4   FORM      7
TCPERN5   FORM      7
TCPERN6   FORM      7
TCPERN7   FORM      7
TCPERN8   FORM      7
TCPERN9   FORM      7
TCPERN10  FORM      7
TCPERN11  FORM      7
TCPERN12  FORM      7
TCPERN13  FORM      7
TCPERX1   FORM      7
TCPERX2   FORM      7
TCPERX3   FORM      7
TCPERX4   FORM      7
TCPERX5   FORM      7
TCPERX6   FORM      7
TCPERX7   FORM      7
TCPERX8   FORM      7
TCPERX9   FORM      7
TCPERX10  FORM      7
TCPERX11  FORM      7
TCPERX12  FORM      7
TCPERX13  FORM      7
TCPERATO  FORM      7
TCPERNTO  FORM      7
TCPERXTO  FORM      7
.
TGPERA1   FORM      7            * GROUP TOTALS *
TGPERA2   FORM      7
TGPERA3   FORM      7
TGPERA4   FORM      7
TGPERA5   FORM      7
TGPERA6   FORM      7
TGPERA7   FORM      7
TGPERA8   FORM      7
TGPERA9   FORM      7
TGPERA10  FORM      7
TGPERA11  FORM      7
TGPERA12  FORM      7
TGPERA13  FORM      7
TGPERN1   FORM      7
TGPERN2   FORM      7
TGPERN3   FORM      7
TGPERN4   FORM      7
TGPERN5   FORM      7
TGPERN6   FORM      7
TGPERN7   FORM      7
TGPERN8   FORM      7
TGPERN9   FORM      7
TGPERN10  FORM      7
TGPERN11  FORM      7
TGPERN12  FORM      7
TGPERN13  FORM      7
TGPERX1   FORM      7
TGPERX2   FORM      7
TGPERX3   FORM      7
TGPERX4   FORM      7
TGPERX5   FORM      7
TGPERX6   FORM      7
TGPERX7   FORM      7
TGPERX8   FORM      7
TGPERX9   FORM      7
TGPERX10  FORM      7
TGPERX11  FORM      7
TGPERX12  FORM      7
TGPERX13  FORM      7
TGPERATO  FORM      7
TGPERNTO  FORM      7
TGPERXTO  FORM      7
.
TZPERA1   FORM      7            * GRAND TOTALS *
TZPERA2   FORM      7
TZPERA3   FORM      7
TZPERA4   FORM      7
TZPERA5   FORM      7
TZPERA6   FORM      7
TZPERA7   FORM      7
TZPERA8   FORM      7
TZPERA9   FORM      7
TZPERA10  FORM      7
TZPERA11  FORM      7
TZPERA12  FORM      7
TZPERA13  FORM      7
TZPERN1   FORM      7
TZPERN2   FORM      7
TZPERN3   FORM      7
TZPERN4   FORM      7
TZPERN5   FORM      7
TZPERN6   FORM      7
TZPERN7   FORM      7
TZPERN8   FORM      7
TZPERN9   FORM      7
TZPERN10  FORM      7
TZPERN11  FORM      7
TZPERN12  FORM      7
TZPERN13  FORM      7
TZPERX1   FORM      7
TZPERX2   FORM      7
TZPERX3   FORM      7
TZPERX4   FORM      7
TZPERX5   FORM      7
TZPERX6   FORM      7
TZPERX7   FORM      7
TZPERX8   FORM      7
TZPERX9   FORM      7
TZPERX10  FORM      7
TZPERX11  FORM      7
TZPERX12  FORM      7
TZPERX13  FORM      7
TZPERATO  FORM      7
TZPERNTO  FORM      7
TZPERXTO  FORM      7
.
VENDDATE  DIM       6            * End month - CCYYMM
VSTRDATE  DIM       6            * Start month - CCYYMM
.
PRGID     INIT      "IBAOUT45"
PRGNAM    INIT      "Attendance By Referral Type"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              MAINLINE                                  *
.*                            Controlling Logic                           *
.**************************************************************************
          CALL      INIT0000          * initialization and open files
          BRANCH    EXIT,ML9900
.
ML1000    CALL      OPTN0000          * select options
          BRANCH    EXIT,ML9900       * EXIT=1 if "0" chosen in menu
.
          CALL      PROC0000          * Process data
          BRANCH    FIRSTREC,ML9500   * If set, no records found
.
          CALL      PRNT0000          * Print data
          GOTO      ML1000
.
. ------  No records found in processing pass  ------
.
ML9500    DISPLAY   *P30:24,*EL,*B,*V2LON,"*** No records found ***",*W;
          GOTO      ML1000
.
. ------  The end  ------
.
ML9900    CALL      KILL0000
ML9999    CHAIN     PGM               * chain back to appropriate menu
+
.****************************************************************************
.*                                INIT0000                                  *
.*                             initialization                               *
.****************************************************************************
INIT0000  CALL       DISPHEAD
.
          DISPLAY    *P56:24,"Opening controlf";
          OPEN       CONTROLF,"controlf"
          READ       CONTROLF,FIFTY9;*27,CPERMTH
          CLOSE      CONTROLF
.
          DISPLAY    *P64:24,"patcodes";
          OPEN       PATCODE1,"patcodes"
.
          DISPLAY    *P64:24,"patdrgaf";
          OPEN       PATDRGA1,"patdrgaf"
          OPEN       PATDRGA3,"patdrgaf"
.
          PACK       CFNAME,UID,FILCTYA1
          DISPLAY    *P64:24,CFNAME;
          OPEN       OUTCTYA1,CFNAME
.
          PACK       CFNAME,UID,FILATTA1
          DISPLAY    *P64:24,CFNAME;
          OPEN       OUTATTA1,CFNAME
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,OUTTMP
.
.         Work out what the current period is, and the first period in the
.         current financial year
.
          PACK       CPTDATE,CCC,CYY,CMM,CDD
          REP        " 0",CPTDATE
          CALL       GPERD                  * Find the current period
          BRANCH     EXIT,INIT9000          * No current period found
.
          PACK       CURPER,DRGCYR,DRGCNUM
.
          PACK       KEY6,DRGYR,SP2
          CALL       RDSDRGA1
          CALL       RDKDRGA1
.
          PACK       CSTPER,DRGCYR,DRGCNUM
.
.         Now count the number of periods in a financial year
.
          MOVE       ZERO,NPYEAR
          MOVE       DRGYR,DIM4
          PACK       KEY6,DRGYR,SP2
          CALL       RDSDRGA1
INIT8000  CALL       RDKDRGA1
          BRANCH     OVRCD,INIT8100
.
          MATCH      DRGYR,DIM4
          GOTO       INIT8100 IF NOT EQUAL
          ADD        ONE,NPYEAR
          GOTO       INIT8000
.
.         Finished initialization
.
INIT8100  MOVE       ZERO,EXIT
          GOTO       INIT9999
.
.         No current period on file
.
INIT9000  KEYIN      *P1:24,*EL,*B,*V2LON:
                     "** Error: Current date not found in Period file **",*HOFF:
                     "    Hit <",*V2LON,"ENTER",*HOFF,"> to exit ",*EOFF,ANS;
          MOVE       ONE,EXIT
.
INIT9999  RETURN 
+
.****************************************************************************
.*                                OPTN0000                                  *
.*                        get user options or exit                          *
.****************************************************************************
OPTN0000   DISPLAY    *P1:3,*EF:
                      *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                      *P1:5,*V2LON,ONE,*HOFF," = Group Code sequence":
                      *P1:7,"Select Option : ";
.
OPTN0500   MOVE       ONE,EXIT
           KEYIN      *P17:7,*EL,*DV,UNDLN1,*P17:7,*V2LON,OPTION;
           COMPARE    ZERO,OPTION
           GOTO       OPTN9999 IF EQUAL
           MOVE       ZERO,EXIT
           BRANCH     OPTION,OPTN3000
           BEEP
           GOTO       OPTN0500
.
.----------------------------------------------------------------------------
.          Keyin dates
.----------------------------------------------------------------------------
OPTN3000  DISPLAY   *P1:10,*EF,"Generate Report from ";
          MOVE      ZERO,CHIGHLT
          MOVE      TWENTY2,CCOL
          MOVE      TEN,CVERT
          UNPACK    CSTPER,CCENT,CYEAR,CMON
          CALL      KEYPER
          BRANCH    OVRCD,OPTN0000
.
.         Validate the date entered
.
          PACK      KEY6,CCENT,CYEAR,CMON
          REP       " 0",KEY6
          CALL      RDDRGA3
          BRANCH    OVRCD,OPTN3200
.
          PACK      VSTRDATE,DRGYR,DRGNUM
          PACK      PSTRDATE,DRGCNUM,SLASH,DRGCYR
.
.         Get the to date
.
OPTN3100  DISPLAY   *P30:10,"to ";
          UNPACK    CURPER,CCENT,CYEAR,CMON
          MOVE      "33",CCOL
          CALL      KEYPER
          BRANCH    OVRCD,OPTN3000
.
.         Validate the date entered
.
          PACK      KEY6,CCENT,CYEAR,CMON
          REP       " 0",KEY6
          CALL      RDDRGA3
          BRANCH    OVRCD,OPTN3300
.
          PACK      VENDDATE,DRGYR,DRGNUM
          PACK      PENDDATE,DRGCNUM,SLASH,DRGCYR
.
          MATCH     VSTRDATE,VENDDATE
          GOTO      OPTN3400 IF NOT LESS
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** To Date must be greater than or equal From Date **  ";
          CALL      HITENTER
          GOTO      OPTN3000
.
.         From date not on file
.
OPTN3200  DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid Date **    ";
          CALL      HITENTER
          GOTO      OPTN3000
.
.         To date not on file
.
OPTN3300  DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid Date **    ";
          CALL      HITENTER
          GOTO      OPTN3100
.
.         Calculate MPERUSED - # of periods used
.
OPTN3400  MOVE      ZERO,MPERUSED
          PACK      KEY6,VSTRDATE
          CALL      RDDRGA1
          BRANCH    OVRCD,OPTN3500
          GOTO      OPTN3600
.
OPTN3500  CALL      RDKDRGA1
          BRANCH    OVRCD,OPTN3700
.
OPTN3600  PACK      XDATE,DRGYR,DRGNUM
          MATCH     XDATE,VENDDATE
          GOTO      OPTN3700 IF LESS
          ADD       ONE,MPERUSED
          GOTO      OPTN3500
.
.         Validate the number of periods
.
OPTN3700  COMPARE   TEN4,MPERUSED
          GOTO      OPTN9000 IF LESS
.
          DISPLAY   *P1:24,*EL,*B,*V2LON,"** Maximum 13 periods allowed **    ";
          CALL      HITENTER
          GOTO      OPTN3000
.
.----------------------------------------------------------------------------
.         Ok to proceed ?
.----------------------------------------------------------------------------
OPTN9000  MOVE      SP1,ANS
          KEYIN     *P1:24,*EL,"Ok to proceed (",*V2LON,"Y",*HOFF,"/",*V2LON:
                    "N",*HOFF,"/",*V2LON,"C",*HOFF,") ? ",*V2LON,ANS;
          MATCH     ANSY,ANS
          GOTO      OPTN9999 IF EQUAL
          MATCH     ANSN,ANS
          GOTO      OPTN3000 IF EQUAL
          MATCH     ANSC,ANS
          GOTO      OPTN0000 IF EQUAL
          BEEP
          GOTO      OPTN9000
.
OPTN9999  RETURN
+
.**************************************************************************
.*                              PROC0000                                  *
.*                            Process data                                *
.**************************************************************************
PROC0000  CALL      CREA0000               * Create temp file
          MOVE      ONE,FIRSTREC           * 1st record not read yet
.
          DISPLAY   *P1:14,*EF,*V2LON:
                    "*** Processing Records ***",*HOFF:
                    *P1:16,"Group Code  :":
                    *P1:17,"Clinic Type :":
                    *P1:18,"Referral    :":
                    *P1:19,"Date        :";
.
          PACK      KEY24,IDDD,SP30
          CALL      RDSATTA1
PROC1000  CALL      RDKATTA1
          BRANCH    OVRCD,PROC9999         * No more records
.
          MATCH     OATSITE,IDDD
          GOTO      PROC9999 IF NOT EQUAL  * Different site - exit
          MATCH     VSTRDATE,OATDATE
          GOTO      PROC1000 IF LESS       * Before start of date range
          MATCH     OATDATE,VENDDATE
          GOTO      PROC1000 IF LESS       * Past end of date range
.
          UNPACK    OATDATE,CCENT,CYEAR,CMON
          DISPLAY   *P15:16,*V2LON,OATGRUP,*P15:17,OATCTYP:
                    *P15:18,OATREFR,*P15:19,CMON,SLASH,CCENT,CYEAR;
          MOVE      ZERO,FIRSTREC          * Got a record
.
          CALL      CALM0000               * Calculate period # for this month
.
          PACK      KEY12,OATGRUP,OATCTYP,OATREFR
          CALL      RDTMP1
          BRANCH    OVRCD,PROC4000
.
.         Update attendees
.
          LOAD      FORM7,MPERREQD,TRPERA1,TRPERA2,TRPERA3,TRPERA4,TRPERA5:
                                   TRPERA6,TRPERA7,TRPERA8,TRPERA9,TRPERA10:
                                   TRPERA11,TRPERA12,TRPERA13
                                   
          ADD       OATNREF,FORM7
          STORE     FORM7,MPERREQD,TRPERA1,TRPERA2,TRPERA3,TRPERA4,TRPERA5:
                                   TRPERA6,TRPERA7,TRPERA8,TRPERA9,TRPERA10:
                                   TRPERA11,TRPERA12,TRPERA13
.
.         Update non-attendees
.
          LOAD      FORM7,MPERREQD,TRPERN1,TRPERN2,TRPERN3,TRPERN4,TRPERN5:
                                   TRPERN6,TRPERN7,TRPERN8,TRPERN9,TRPERN10:
                                   TRPERN11,TRPERN12,TRPERN13
          ADD       OATNNON,FORM7
          STORE     FORM7,MPERREQD,TRPERN1,TRPERN2,TRPERN3,TRPERN4,TRPERN5:
                                   TRPERN6,TRPERN7,TRPERN8,TRPERN9,TRPERN10:
                                   TRPERN11,TRPERN12,TRPERN13
.
.         Update non-booked attendees
.
          LOAD      FORM7,MPERREQD,TRPERX1,TRPERX2,TRPERX3,TRPERX4,TRPERX5:
                                   TRPERX6,TRPERX7,TRPERX8,TRPERX9,TRPERX10:
                                   TRPERX11,TRPERX12,TRPERX13
          ADD       OATNBKD,FORM7
          STORE     FORM7,MPERREQD,TRPERX1,TRPERX2,TRPERX3,TRPERX4,TRPERX5:
                                   TRPERX6,TRPERX7,TRPERX8,TRPERX9,TRPERX10:
                                   TRPERX11,TRPERX12,TRPERX13
          CALL      UPTMP1
          GOTO      PROC1000
.
.         Write new record
.
PROC4000  MOVE      OATGRUP,TGROUP
          MOVE      OATCTYP,TCTYPE
          MOVE      OATREFR,TREFER
          CALL      CLRR0000                 * Clear TR variables
          STORE     OATNREF,MPERREQD,TRPERA1,TRPERA2,TRPERA3,TRPERA4,TRPERA5:
                                     TRPERA6,TRPERA7,TRPERA8,TRPERA9,TRPERA10:
                                     TRPERA11,TRPERA12,TRPERA13
          STORE     OATNNON,MPERREQD,TRPERN1,TRPERN2,TRPERN3,TRPERN4,TRPERN5:
                                     TRPERN6,TRPERN7,TRPERN8,TRPERN9,TRPERN10:
                                     TRPERN11,TRPERN12,TRPERN13
          STORE     OATNBKD,MPERREQD,TRPERX1,TRPERX2,TRPERX3,TRPERX4,TRPERX5:
                                     TRPERX6,TRPERX7,TRPERX8,TRPERX9,TRPERX10:
                                     TRPERX11,TRPERX12,TRPERX13
          CALL      WRTMP1
          GOTO      PROC1000
.
PROC9999  RETURN
+
.----------------------------------------------------------------------------
.         Calculate the period for a particular month - value in MPERREQD
.----------------------------------------------------------------------------
CALM0000  UNPACK    VSTRDATE,XCENT,XYEAR,XMON
          MOVE      XMON,IMON
          MOVE      XYEAR,IYEAR
          MOVE      XCENT,ICENT
.
          UNPACK    OATDATE,CCENT,CYEAR,CMON
          MOVE      CMON,MPERREQD
.
          MATCH     XYEAR,CYEAR
          GOTO      CALM1000 IF EQUAL
          ADD       NPYEAR,MPERREQD         * Different year
.
CALM1000  SUB       IMON,MPERREQD           * Same year
          ADD       ONE,MPERREQD
.
CALM9999  RETURN
+
.----------------------------------------------------------------------------
.         Clear TR variables
.----------------------------------------------------------------------------
CLRR0000  MOVE      ZERO,FORM2
CLRR1000  ADD       ONE,FORM2
          COMPARE   "40",FORM2
          RETURN    IF EQUAL
          STORE     ZERO,FORM2,TRPERA1,TRPERA2,TRPERA3,TRPERA4,TRPERA5,TRPERA6:
                               TRPERA7,TRPERA8,TRPERA9,TRPERA10,TRPERA11:
                               TRPERA12,TRPERN1,TRPERN2,TRPERN3,TRPERN4,TRPERN5:
                               TRPERN6,TRPERN7,TRPERN8,TRPERN9,TRPERN10:
                               TRPERN11,TRPERN12,TRPERX1,TRPERX2,TRPERX3:
                               TRPERX4,TRPERX5,TRPERX6,TRPERX7,TRPERX8,TRPERX9:
                               TRPERX10,TRPERX11,TRPERX12,TRPERA13,TRPERN13:
                               TRPERX13
          GOTO      CLRR1000
.
.----------------------------------------------------------------------------
.         Clear TC variables
.----------------------------------------------------------------------------
CLRC0000  MOVE      ZERO,FORM2
CLRC1000  ADD       ONE,FORM2
          COMPARE   "40",FORM2
          RETURN    IF EQUAL
          STORE     ZERO,FORM2,TCPERA1,TCPERA2,TCPERA3,TCPERA4,TCPERA5,TCPERA6:
                               TCPERA7,TCPERA8,TCPERA9,TCPERA10,TCPERA11:
                               TCPERA12,TCPERN1,TCPERN2,TCPERN3,TCPERN4,TCPERN5:
                               TCPERN6,TCPERN7,TCPERN8,TCPERN9,TCPERN10:
                               TCPERN11,TCPERN12,TCPERX1,TCPERX2,TCPERX3:
                               TCPERX4,TCPERX5,TCPERX6,TCPERX7,TCPERX8,TCPERX9:
                               TCPERX10,TCPERX11,TCPERX12,TCPERA13,TCPERN13:
                               TCPERX13
          GOTO      CLRC1000
.
.----------------------------------------------------------------------------
.         Clear TG variables
.----------------------------------------------------------------------------
CLRG0000  MOVE      ZERO,FORM2
CLRG1000  ADD       ONE,FORM2
          COMPARE   "40",FORM2
          RETURN    IF EQUAL
          STORE     ZERO,FORM2,TGPERA1,TGPERA2,TGPERA3,TGPERA4,TGPERA5,TGPERA6:
                               TGPERA7,TGPERA8,TGPERA9,TGPERA10,TGPERA11:
                               TGPERA12,TGPERN1,TGPERN2,TGPERN3,TGPERN4,TGPERN5:
                               TGPERN6,TGPERN7,TGPERN8,TGPERN9,TGPERN10:
                               TGPERN11,TGPERN12,TGPERX1,TGPERX2,TGPERX3:
                               TGPERX4,TGPERX5,TGPERX6,TGPERX7,TGPERX8,TGPERX9:
                               TGPERX10,TGPERX11,TGPERX12,TGPERA13,TGPERN13:
                               TGPERX13
          GOTO      CLRG1000
.
.----------------------------------------------------------------------------
.         Clear TZ variables
.----------------------------------------------------------------------------
CLRZ0000  MOVE      ZERO,FORM2
CLRZ1000  ADD       ONE,FORM2
          COMPARE   "40",FORM2
          RETURN    IF EQUAL
          STORE     ZERO,FORM2,TZPERA1,TZPERA2,TZPERA3,TZPERA4,TZPERA5,TZPERA6:
                               TZPERA7,TZPERA8,TZPERA9,TZPERA10,TZPERA11:
                               TZPERA12,TZPERN1,TZPERN2,TZPERN3,TZPERN4,TZPERN5:
                               TZPERN6,TZPERN7,TZPERN8,TZPERN9,TZPERN10:
                               TZPERN11,TZPERN12,TZPERX1,TZPERX2,TZPERX3:
                               TZPERX4,TZPERX5,TZPERX6,TZPERX7,TZPERX8,TZPERX9:
                               TZPERX10,TZPERX11,TZPERX12,TZPERA13,TZPERN13:
                               TZPERX13
          GOTO      CLRZ1000
+
.**************************************************************************
.*                              PRNT0000                                  *
.*                             Print data                                 *
.**************************************************************************
PRNT0000  CALL      CLRR0000                 * Initialize all variables
          CALL      CLRC0000
          CALL      CLRG0000
          CALL      CLRZ0000
.
          MOVE      "80",CLNO
          UNPACK    SP30,KEY12,SAVGROUP,SAVCTYPE
          DISPLAY   *P1:14,*EF,*V2LON,"*** Printing Records ***",*HOFF:
                    *P1:15,"Group Code  :":
                    *P1:16,"Clinic Type :":
                    *P1:17,"Referral    :";
.
          CALL      RDSTMP1                  * Positioning read at top of file
PRNT1000  CALL      RDKTMP1                  * Read next record
          BRANCH    OVRCD,PRNT4000           * No more records - print totals
.
          DISPLAY   *P15:15,*V2LON,TGROUP,*P15:16,TCTYPE,*P15:17,TREFER;
.
          MATCH     SP3,SAVGROUP             * 1st time round ?
          GOTO      PRNT2500 IF EQUAL        * Yes - init save vars
.
          MATCH     SAVGROUP,TGROUP          * Same group ?
          GOTO      PRNT2000 IF EQUAL        * Yes - check clinic
          CALL      PRNX2000                 * Print clinic type totals
          CALL      PRNX3000                 * Print group totals
          GOTO      PRNT2500                 * Print record
.
PRNT2000  MATCH     SAVCTYPE,TCTYPE          * Same clinic type ?
          GOTO      PRNT3000 IF EQUAL        * Yes - print record
          CALL      PRNX2000                 * Print clinic type totals
.
PRNT2500  CALL      PRNT5000                 * Initialize save variables
          CALL      PRNX7000                 * Print clinic heading
.
PRNT3000  CALL      PRNX1000                 * Print record
          GOTO      PRNT1000                 * Read next record
.
PRNT4000  CALL      PRNX2000                 * Print clinic type totals
          CALL      PRNX3000                 * Print group totals
          CALL      PRNX4000                 * Print grand totals
          CALL      PRNX6000                 * Print end of report
          RETURN
.
.         Initialize save variables
.
PRNT5000  MOVE      TGROUP,SAVGROUP
          MOVE      TCTYPE,SAVCTYPE
          RETURN
+
.****************************************************************************
.*                                                                          *
.*                        Printing subroutines                              *
.*                                                                          *
.****************************************************************************
.         Print Record
.----------------------------------------------------------------------------
.
PRNX1000  COMPARE   "55",CLNO
          GOTO      PRNX1100 IF LESS
          CALL      PRNX8000           * Print new page head
.
PRNX1100  MOVE      "**Invalid Referral**",TDESC
          MATCH     SP3,TREFER
          GOTO      PRNX1110 IF EQUAL
          PACK      KEY5,CODES,TREFER
          CALL      RDCODE1
.
PRNX1110  MOVE      TDESC,DIM14
          PRINT     *N,TREFER,*5,DIM14,*20,"Attendees     :";
.
          MOVE      ZERO,TRPERATO            * Initialize totals
          MOVE      ZERO,TRPERNTO
          MOVE      ZERO,TRPERXTO
          MOVE      "28",MPRTPOS             * Set print position
          MOVE      ZERO,COUNT
.
PRNX1200  ADD       SEVEN,MPRTPOS
          ADD       ONE,COUNT
          COMPARE   COUNT,MPERUSED
          GOTO      PRNX1500 IF LESS
          LOAD      FORM7,COUNT,TRPERA1,TRPERA2,TRPERA3,TRPERA4,TRPERA5:
                                TRPERA6,TRPERA7,TRPERA8,TRPERA9,TRPERA10:
                                TRPERA11,TRPERA12,TRPERA13
          PRINT     *MPRTPOS,FORM7;
.
.         Add to clinic type monthly figures
.
          LOAD      FORM7Y,COUNT,TCPERA1,TCPERA2,TCPERA3,TCPERA4,TCPERA5:
                                 TCPERA6,TCPERA7,TCPERA8,TCPERA9,TCPERA10:
                                 TCPERA11,TCPERA12,TCPERA13
          ADD       FORM7,FORM7Y
          STORE     FORM7Y,COUNT,TCPERA1,TCPERA2,TCPERA3,TCPERA4,TCPERA5:
                                 TCPERA6,TCPERA7,TCPERA8,TCPERA9,TCPERA10:
                                 TCPERA11,TCPERA12,TCPERA13
          ADD       FORM7,TRPERATO           * Add to total
          GOTO      PRNX1200
.
.         Finished attendee figures, now print non-attendee data
.
PRNX1500  PRINT     *MPRTPOS,TRPERATO,*N,*20,"Non-Attendees :";
          MOVE      "28",MPRTPOS
          MOVE      ZERO,COUNT
.
PRNX1600  ADD       SEVEN,MPRTPOS
          ADD       ONE,COUNT
          COMPARE   COUNT,MPERUSED
          GOTO      PRNX1700 IF LESS
          LOAD      FORM7,COUNT,TRPERN1,TRPERN2,TRPERN3,TRPERN4,TRPERN5:
                                TRPERN6,TRPERN7,TRPERN8,TRPERN9,TRPERN10:
                                TRPERN11,TRPERN12,TRPERN13
          PRINT     *MPRTPOS,FORM7;
.
.         Add to clinic type monthly figures
.
          LOAD      FORM7Y,COUNT,TCPERN1,TCPERN2,TCPERN3,TCPERN4,TCPERN5:
                                 TCPERN6,TCPERN7,TCPERN8,TCPERN9,TCPERN10:
                                 TCPERN11,TCPERN12,TCPERN13
          ADD       FORM7,FORM7Y
          STORE     FORM7Y,COUNT,TCPERN1,TCPERN2,TCPERN3,TCPERN4,TCPERN5:
                                 TCPERN6,TCPERN7,TCPERN8,TCPERN9,TCPERN10:
                                 TCPERN11,TCPERN12,TCPERN13
          ADD       FORM7,TRPERNTO           * Add to total
          GOTO      PRNX1600
.
.         Finished non-attendee figures, now print non-booked attendee data
.
PRNX1700  PRINT     *MPRTPOS,TRPERNTO,*N,*20,"Non-Booked Att:";
          MOVE      "28",MPRTPOS
          MOVE      ZERO,COUNT
.
PRNX1800  ADD       SEVEN,MPRTPOS
          ADD       ONE,COUNT
          COMPARE   COUNT,MPERUSED
          GOTO      PRNX1900 IF LESS
          LOAD      FORM7,COUNT,TRPERX1,TRPERX2,TRPERX3,TRPERX4,TRPERX5:
                                TRPERX6,TRPERX7,TRPERX8,TRPERX9,TRPERX10:
                                TRPERX11,TRPERX12,TRPERX13
          PRINT     *MPRTPOS,FORM7;
.
.         Add to clinic type monthly figures
.
          LOAD      FORM7Y,COUNT,TCPERX1,TCPERX2,TCPERX3,TCPERX4,TCPERX5:
                                 TCPERX6,TCPERX7,TCPERX8,TCPERX9,TCPERX10:
                                 TCPERX11,TCPERX12,TCPERX13
          ADD       FORM7,FORM7Y
          STORE     FORM7Y,COUNT,TCPERX1,TCPERX2,TCPERX3,TCPERX4,TCPERX5:
                                 TCPERX6,TCPERX7,TCPERX8,TCPERX9,TCPERX10:
                                 TCPERX11,TCPERX12,TCPERX13
          ADD       FORM7,TRPERXTO           * Add to total
          GOTO      PRNX1800
.
PRNX1900  PRINT     *MPRTPOS,TRPERXTO
          ADD       FOUR,CLNO
          RETURN
.
.----------------------------------------------------------------------------
.         Print Clinic Type Totals
.----------------------------------------------------------------------------
PRNX2000  COMPARE   "55",CLNO
          GOTO      PRNX2100 IF LESS
          CALL      PRNX8000                 * Print new page head
.
PRNX2100  CALL      PRNX5000                 * Print line separator
          PRINT     *1,"* Clinic Type Tot.",*20,"Attendees     :";
.
          MOVE      ZERO,TCPERATO            * Initialize totals
          MOVE      ZERO,TCPERNTO
          MOVE      ZERO,TCPERXTO
          MOVE      "28",MPRTPOS             * Set print position
          MOVE      ZERO,COUNT
.
PRNX2200  ADD       SEVEN,MPRTPOS
          ADD       ONE,COUNT
          COMPARE   COUNT,MPERUSED
          GOTO      PRNX2500 IF LESS
          LOAD      FORM7,COUNT,TCPERA1,TCPERA2,TCPERA3,TCPERA4,TCPERA5:
                                TCPERA6,TCPERA7,TCPERA8,TCPERA9,TCPERA10:
                                TCPERA11,TCPERA12,TCPERA13
          PRINT     *MPRTPOS,FORM7;
.
.         Add to group monthly figures
.
          LOAD      FORM7Y,COUNT,TGPERA1,TGPERA2,TGPERA3,TGPERA4,TGPERA5:
                                 TGPERA6,TGPERA7,TGPERA8,TGPERA9,TGPERA10:
                                 TGPERA11,TGPERA12,TGPERA13
          ADD       FORM7,FORM7Y
          STORE     FORM7Y,COUNT,TGPERA1,TGPERA2,TGPERA3,TGPERA4,TGPERA5:
                                 TGPERA6,TGPERA7,TGPERA8,TGPERA9,TGPERA10:
                                 TGPERA11,TGPERA12,TGPERA13
          ADD       FORM7,TCPERATO           * Add to total
          GOTO      PRNX2200
.
.         Finished attendee figures, now print non-attendee data
.
PRNX2500  PRINT     *MPRTPOS,TCPERATO,*N,*20,"Non-Attendees :";
          MOVE      "28",MPRTPOS
          MOVE      ZERO,COUNT
.
PRNX2600  ADD       SEVEN,MPRTPOS
          ADD       ONE,COUNT
          COMPARE   COUNT,MPERUSED
          GOTO      PRNX2700 IF LESS
          LOAD      FORM7,COUNT,TCPERN1,TCPERN2,TCPERN3,TCPERN4,TCPERN5:
                                TCPERN6,TCPERN7,TCPERN8,TCPERN9,TCPERN10:
                                TCPERN11,TCPERN12,TCPERN13
          PRINT     *MPRTPOS,FORM7;
.
.         Add to clinic type monthly figures
.
          LOAD      FORM7Y,COUNT,TGPERN1,TGPERN2,TGPERN3,TGPERN4,TGPERN5:
                                 TGPERN6,TGPERN7,TGPERN8,TGPERN9,TGPERN10:
                                 TGPERN11,TGPERN12,TGPERN13
          ADD       FORM7,FORM7Y
          STORE     FORM7Y,COUNT,TGPERN1,TGPERN2,TGPERN3,TGPERN4,TGPERN5:
                                 TGPERN6,TGPERN7,TGPERN8,TGPERN9,TGPERN10:
                                 TGPERN11,TGPERN12,TGPERN13
          ADD       FORM7,TCPERNTO           * Add to total
          GOTO      PRNX2600
.
.         Finished non-attendee figures, now print non-booked attendee data
.
PRNX2700  PRINT     *MPRTPOS,TCPERNTO,*N,*20,"Non-Booked Att:";
          MOVE      "28",MPRTPOS
          MOVE      ZERO,COUNT
.
PRNX2800  ADD       SEVEN,MPRTPOS
          ADD       ONE,COUNT
          COMPARE   COUNT,MPERUSED
          GOTO      PRNX2900 IF LESS
          LOAD      FORM7,COUNT,TCPERX1,TCPERX2,TCPERX3,TCPERX4,TCPERX5:
                                TCPERX6,TCPERX7,TCPERX8,TCPERX9,TCPERX10:
                                TCPERX11,TCPERX12,TCPERX13
          PRINT     *MPRTPOS,FORM7;
.
.         Add to clinic type monthly figures
.
          LOAD      FORM7Y,COUNT,TGPERX1,TGPERX2,TGPERX3,TGPERX4,TGPERX5:
                                 TGPERX6,TGPERX7,TGPERX8,TGPERX9,TGPERX10:
                                 TGPERX11,TGPERX12,TGPERX13
          ADD       FORM7,FORM7Y
          STORE     FORM7Y,COUNT,TGPERX1,TGPERX2,TGPERX3,TGPERX4,TGPERX5:
                                 TGPERX6,TGPERX7,TGPERX8,TGPERX9,TGPERX10:
                                 TGPERX11,TGPERX12,TGPERX13
          ADD       FORM7,TCPERXTO           * Add to total
          GOTO      PRNX2800
.
PRNX2900  PRINT     *MPRTPOS,TCPERXTO
          ADD       FOUR,CLNO
          CALL      PRNX5000                 * Print line separator
          CALL      CLRC0000
          RETURN
+
.----------------------------------------------------------------------------
.         Print Group Totals
.----------------------------------------------------------------------------
PRNX3000  COMPARE   "55",CLNO
          GOTO      PRNX3100 IF LESS
          CALL      PRNX8000                 * Print new page head
.
PRNX3100  PRINT     *1,"* Group Total",*20,"Attendees     :";
.
          MOVE      ZERO,TGPERATO            * Initialize totals
          MOVE      ZERO,TGPERNTO
          MOVE      ZERO,TGPERXTO
          MOVE      "28",MPRTPOS             * Set print position
          MOVE      ZERO,COUNT
.
PRNX3200  ADD       SEVEN,MPRTPOS
          ADD       ONE,COUNT
          COMPARE   COUNT,MPERUSED
          GOTO      PRNX3500 IF LESS
          LOAD      FORM7,COUNT,TGPERA1,TGPERA2,TGPERA3,TGPERA4,TGPERA5:
                                TGPERA6,TGPERA7,TGPERA8,TGPERA9,TGPERA10:
                                TGPERA11,TGPERA12,TGPERA13
          PRINT     *MPRTPOS,FORM7;
.
.         Add to grand total monthly figures
.
          LOAD      FORM7Y,COUNT,TZPERA1,TZPERA2,TZPERA3,TZPERA4,TZPERA5:
                                 TZPERA6,TZPERA7,TZPERA8,TZPERA9,TZPERA10:
                                 TZPERA11,TZPERA12,TZPERA13
          ADD       FORM7,FORM7Y
          STORE     FORM7Y,COUNT,TZPERA1,TZPERA2,TZPERA3,TZPERA4,TZPERA5:
                                 TZPERA6,TZPERA7,TZPERA8,TZPERA9,TZPERA10:
                                 TZPERA11,TZPERA12,TZPERA13
          ADD       FORM7,TGPERATO           * Add to total
          GOTO      PRNX3200
.
.         Finished attendee figures, now print non-attendee data
.
PRNX3500  PRINT     *MPRTPOS,TGPERATO,*N,*20,"Non-Attendees :";
          MOVE      "28",MPRTPOS
          MOVE      ZERO,COUNT
.
PRNX3600  ADD       SEVEN,MPRTPOS
          ADD       ONE,COUNT
          COMPARE   COUNT,MPERUSED
          GOTO      PRNX3700 IF LESS
          LOAD      FORM7,COUNT,TGPERN1,TGPERN2,TGPERN3,TGPERN4,TGPERN5:
                                TGPERN6,TGPERN7,TGPERN8,TGPERN9,TGPERN10:
                                TGPERN11,TGPERN12,TGPERN13
          PRINT     *MPRTPOS,FORM7;
.
.         Add to clinic type monthly figures
.
          LOAD      FORM7Y,COUNT,TZPERN1,TZPERN2,TZPERN3,TZPERN4,TZPERN5:
                                 TZPERN6,TZPERN7,TZPERN8,TZPERN9,TZPERN10:
                                 TZPERN11,TZPERN12,TZPERN13
          ADD       FORM7,FORM7Y
          STORE     FORM7Y,COUNT,TZPERN1,TZPERN2,TZPERN3,TZPERN4,TZPERN5:
                                 TZPERN6,TZPERN7,TZPERN8,TZPERN9,TZPERN10:
                                 TZPERN11,TZPERN12,TZPERN13
          ADD       FORM7,TGPERNTO           * Add to total
          GOTO      PRNX3600
.
.         Finished non-attendee figures, now print non-booked attendee data
.
PRNX3700  PRINT     *MPRTPOS,TGPERNTO,*N,*20,"Non-Booked Att:";
          MOVE      "28",MPRTPOS
          MOVE      ZERO,COUNT
.
PRNX3800  ADD       SEVEN,MPRTPOS
          ADD       ONE,COUNT
          COMPARE   COUNT,MPERUSED
          GOTO      PRNX3900 IF LESS
          LOAD      FORM7,COUNT,TGPERX1,TGPERX2,TGPERX3,TGPERX4,TGPERX5:
                                TGPERX6,TGPERX7,TGPERX8,TGPERX9,TGPERX10:
                                TGPERX11,TGPERX12,TGPERX13
          PRINT     *MPRTPOS,FORM7;
.
.         Add to clinic type monthly figures
.
          LOAD      FORM7Y,COUNT,TZPERX1,TZPERX2,TZPERX3,TZPERX4,TZPERX5:
                                 TZPERX6,TZPERX7,TZPERX8,TZPERX9,TZPERX10:
                                 TZPERX11,TZPERX12,TZPERX13
          ADD       FORM7,FORM7Y
          STORE     FORM7Y,COUNT,TZPERX1,TZPERX2,TZPERX3,TZPERX4,TZPERX5:
                                 TZPERX6,TZPERX7,TZPERX8,TZPERX9,TZPERX10:
                                 TZPERX11,TZPERX12,TZPERX13
          ADD       FORM7,TGPERXTO           * Add to total
          GOTO      PRNX3800
.
PRNX3900  PRINT     *MPRTPOS,TGPERXTO
          ADD       THREE,CLNO
          CALL      PRNX5000                 * Print line separator
          CALL      CLRG0000
          RETURN
+
.----------------------------------------------------------------------------
.         Print Grand Totals
.----------------------------------------------------------------------------
PRNX4000  COMPARE   "55",CLNO
          GOTO      PRNX4100 IF LESS
          CALL      PRNX8000                 * Print new page head
.
PRNX4100  PRINT     *1,"* Grand Total",*20,"Attendees     :";
.
          MOVE      ZERO,TZPERATO            * Initialize totals
          MOVE      ZERO,TZPERNTO
          MOVE      "28",MPRTPOS             * Set print position
          MOVE      ZERO,COUNT
.
PRNX4200  ADD       SEVEN,MPRTPOS
          ADD       ONE,COUNT
          COMPARE   COUNT,MPERUSED
          GOTO      PRNX4500 IF LESS
          LOAD      FORM7,COUNT,TZPERA1,TZPERA2,TZPERA3,TZPERA4,TZPERA5:
                                TZPERA6,TZPERA7,TZPERA8,TZPERA9,TZPERA10:
                                TZPERA11,TZPERA12,TZPERA13
          PRINT     *MPRTPOS,FORM7;
          ADD       FORM7,TZPERATO           * Add to total
          GOTO      PRNX4200
.
.         Finished attendee figures, now print non-attendee data
.
PRNX4500  PRINT     *MPRTPOS,TZPERATO,*N,*20,"Non-Attendees :";
          MOVE      "28",MPRTPOS
          MOVE      ZERO,COUNT
.
PRNX4600  ADD       SEVEN,MPRTPOS
          ADD       ONE,COUNT
          COMPARE   COUNT,MPERUSED
          GOTO      PRNX4700 IF LESS
          LOAD      FORM7,COUNT,TZPERN1,TZPERN2,TZPERN3,TZPERN4,TZPERN5:
                                TZPERN6,TZPERN7,TZPERN8,TZPERN9,TZPERN10:
                                TZPERN11,TZPERN12,TZPERN13
          PRINT     *MPRTPOS,FORM7;
          ADD       FORM7,TZPERNTO           * Add to total
          GOTO      PRNX4600
.
.         Finished non-attendee figures, now print non-booked attendee data
.
PRNX4700  PRINT     *MPRTPOS,TZPERNTO,*N,*20,"Non-Booked Att:";
          MOVE      "28",MPRTPOS
          MOVE      ZERO,COUNT
.
PRNX4800  ADD       SEVEN,MPRTPOS
          ADD       ONE,COUNT
          COMPARE   COUNT,MPERUSED
          GOTO      PRNX4900 IF LESS
          LOAD      FORM7,COUNT,TZPERX1,TZPERX2,TZPERX3,TZPERX4,TZPERX5:
                                TZPERX6,TZPERX7,TZPERX8,TZPERX9,TZPERX10:
                                TZPERX11,TZPERX12,TZPERX13
          PRINT     *MPRTPOS,FORM7;
          ADD       FORM7,TZPERXTO           * Add to total
          GOTO      PRNX4800
.
PRNX4900  PRINT     *MPRTPOS,TZPERXTO
          CALL      PRNX5000                 * Print line separator
          RETURN
+
.----------------------------------------------------------------------------
.         Print page separator
.----------------------------------------------------------------------------
PRNX5000  MOVE      "28",MPRTPOS
          MOVE      ZERO,COUNT
.
PRNX5100  ADD       SEVEN,MPRTPOS
          ADD       ONE,COUNT
          COMPARE   COUNT,MPERUSED
          GOTO      PRNX5200 IF LESS
          PRINT     *MPRTPOS," ------";
          GOTO      PRNX5100
.
PRNX5200  PRINT     *MPRTPOS," ------"
          RETURN
+
.----------------------------------------------------------------------------
.         Print tail of report
.----------------------------------------------------------------------------
PRNX6000  PRINT     *N,*N,"***  End of Report  ***"
          RETURN
+
.----------------------------------------------------------------------------
.         Print Group Code & Clinic Type heading
.----------------------------------------------------------------------------
PRNX7000  COMPARE   "55",CLNO
          GOTO      PRNX7100 IF LESS
          CALL      PRNX8000           * Print new page head
.
PRNX7100  MOVE      "*ERROR*Unknown Group",TDESC
          PACK      KEY5,CODECG,TGROUP
          CALL      RDCODE1
.
          MOVE      "* ERROR * Invalid Clinic Type ",OCTDESC
          PACK      KEY12,IDDD,TCTYPE
          CALL      RDCTYA1
.
          PRINT     *N,"Group Code : ",TGROUP,SP1,TDESC:
                    *42,"Clinic Type : ",TCTYPE,SP1,OCTDESC
          ADD       TWO,CLNO
          RETURN
+
.----------------------------------------------------------------------------
.         Print New Page
.----------------------------------------------------------------------------
PRNX8000  MOVE      ONE,CNOUNDLN
          CALL      IBACLOCK    
.
          CALL      HEAD132
          PRINT     *N,*40,"PERIOD From ",PSTRDATE," To ":
                       PENDDATE:
                    *N,*N,"Referral Source/Description";
.
.         Loop over the period file and get the description for each column
.
          MOVE      "35",MPRTPOS            * Location to start actual print
          MOVE      VSTRDATE,KEY6
          CALL      RDDRGA1
PRNX8100  PACK      XDATE,DRGYR,DRGNUM
.
          MATCH     XDATE,VENDDATE
          GOTO      PRNX8200 IF LESS
          UNPACK    DRGCYR,CCENT,CYEAR
.
          PRINT     *MPRTPOS,SP1,DRGSDSC,SP1,CYEAR;
          ADD       SEVEN,MPRTPOS
.
          CALL      RDKDRGA1
          BRANCH    OVRCD,PRNX8200
          GOTO      PRNX8100
.
PRNX8200  PRINT     *MPRTPOS,"  Total":
                    *N,"----------------------------------";
.
          CALL      PRNX5000                * Print rest of separator lines
          MOVE      SIX,CLNO
          RETURN
+
.**************************************************************************
.*                              CREA0000                                  *
.*                   Create temp file                                     *
.**************************************************************************
CREA0000  CALL      KILL0000
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    OUTTMP,CMDLINE
          APPEND    " 208 u1-12",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      OUTTMPXX,OUTTMP
          RETURN
+
.**************************************************************************
.*                              KILL0000                                  *
.*                   Remove temp file                                     *
.**************************************************************************
KILL0000  PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    OUTTMP,CMDLINE
          RESET     CMDLINE
          CLOSE     OUTTMPXX
          EXECUTE   CMDLINE,TASKID
          RETURN
+
.------------------------------------------------------------------------------
.         Temp file I/O's
.------------------------------------------------------------------------------
RDTMP1    MOVE      ZERO,OVRCD
          RESET     KEY12
          READ      OUTTMPXX,KEY12;TGROUP,TCTYPE,TREFER:
                             TRPERA1,TRPERA2,TRPERA3,TRPERA4,TRPERA5,TRPERA6:
                             TRPERA7,TRPERA8,TRPERA9,TRPERA10,TRPERA11,TRPERA12:
                             TRPERN1,TRPERN2,TRPERN3,TRPERN4,TRPERN5,TRPERN6:
                             TRPERN7,TRPERN8,TRPERN9,TRPERN10,TRPERN11,TRPERN12:
                             TRPERX1,TRPERX2,TRPERX3,TRPERX4,TRPERX5,TRPERX6:
                             TRPERX7,TRPERX8,TRPERX9,TRPERX10,TRPERX11,TRPERX12:
                             TRPERA13,TRPERN13,TRPERX13
          GOTO      OVERCOND IF OVER
          RETURN
.
RDSTMP1   MOVE      ZERO,OVRCD
          RESET     KEY12
          READ      OUTTMPXX,KEY12;;
          RETURN
.
RDKTMP1   MOVE      ZERO,OVRCD
          READKS    OUTTMPXX;TGROUP,TCTYPE,TREFER:
                             TRPERA1,TRPERA2,TRPERA3,TRPERA4,TRPERA5,TRPERA6:
                             TRPERA7,TRPERA8,TRPERA9,TRPERA10,TRPERA11,TRPERA12:
                             TRPERN1,TRPERN2,TRPERN3,TRPERN4,TRPERN5,TRPERN6:
                             TRPERN7,TRPERN8,TRPERN9,TRPERN10,TRPERN11,TRPERN12:
                             TRPERX1,TRPERX2,TRPERX3,TRPERX4,TRPERX5,TRPERX6:
                             TRPERX7,TRPERX8,TRPERX9,TRPERX10,TRPERX11,TRPERX12:
                             TRPERA13,TRPERN13,TRPERX13
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTMP1    MOVE      ZERO,OVRCD
          UPDATE    OUTTMPXX;TGROUP,TCTYPE,TREFER:
                             TRPERA1,TRPERA2,TRPERA3,TRPERA4,TRPERA5,TRPERA6:
                             TRPERA7,TRPERA8,TRPERA9,TRPERA10,TRPERA11,TRPERA12:
                             TRPERN1,TRPERN2,TRPERN3,TRPERN4,TRPERN5,TRPERN6:
                             TRPERN7,TRPERN8,TRPERN9,TRPERN10,TRPERN11,TRPERN12:
                             TRPERX1,TRPERX2,TRPERX3,TRPERX4,TRPERX5,TRPERX6:
                             TRPERX7,TRPERX8,TRPERX9,TRPERX10,TRPERX11,TRPERX12:
                             TRPERA13,TRPERN13,TRPERX13
          RETURN
.
WRTMP1    MOVE      ZERO,OVRCD
          RESET     KEY12
          WRITE     OUTTMPXX,KEY12;KEY12:
                             TRPERA1,TRPERA2,TRPERA3,TRPERA4,TRPERA5,TRPERA6:
                             TRPERA7,TRPERA8,TRPERA9,TRPERA10,TRPERA11,TRPERA12:
                             TRPERN1,TRPERN2,TRPERN3,TRPERN4,TRPERN5,TRPERN6:
                             TRPERN7,TRPERN8,TRPERN9,TRPERN10,TRPERN11,TRPERN12:
                             TRPERX1,TRPERX2,TRPERX3,TRPERX4,TRPERX5,TRPERX6:
                             TRPERX7,TRPERX8,TRPERX9,TRPERX10,TRPERX11,TRPERX12:
                             TRPERA13,TRPERN13,TRPERX13
          GOTO      OVERCOND IF OVER
          RETURN
+
.==============================================================================
.
           INC        STD001IO
           INC        PATCOMN3
           INC       TFILENAM
.
           INC       IBASEQIO/INC
           INC       OUTATTIO/INC
           INC       OUTCTYIO/INC
           INC       PATCODIO/INC
           INC        PATDRGIO/INC
           INC       WEBERRIO/INC
+

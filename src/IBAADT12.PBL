******************************************************************************
.* System         :  Patient Billing                                          *
.* Program        :  IBAADT12                                                 *
.* Description    :  Upload Billing Defaults                                  *
.******************************************************************************
.* Date           :  02/11/2020                                               *
.* Author         :  Tracey Nguyen                                            *
.* Function       :  Upload Billing Defaults from ASCII file                  *
.* Modifications  :                                                           *
.* V11.05.01 27/03/2022 Alina Bhari    Task 0957676                           *
.*           Updated the prior record with End-dated 1 day before the new     *
.*           Start date, if there is a previous record with a prior Effective *
.*           Start Date - FLAGLEFD,EDAT8000,EDAT1000                          *
.*           Recompiled for PATBDEVL                                          *
.* V11.01.04 17/01/2022 Alina Bhari    Task 0907384                           *
.*           Updated Casepayment to accept Blank , Y & N                      *
.*           Skipped and corrected the version no to match version variable   *
.* V11.01.02 23/07/2021 Jill Parkinson Task 0906724                           *
.*           Update invalid DRG error message                                 *
.* V11.01.01 13/04/2021 Tracey Nguyen   TSK 0888079                           *
.*           1)Corrected validation of single/multi hospital                  *
.*           2)Corrected validation of Health Fund Table as patfn1af.hftabl)  *
.* V11.00.02 30/11/2020 Tracey Nguyen   TSK 0888079                           *
.*           Added TRAP IO against patbdeaf file                              *
.* V11.00.01 02/11/2020 Tracey Nguyen   TSK 0888079                           *
.*           Created upload Billing Defaults patbdeaf file                    *
.******************************************************************************
.
          INC       STD001FD
.
          INC       IBACONFD/INC
          INC       PATBDEFD/INC      * Billing Default file
          INC       PATCMCFD/INC      * Casemix Codes File
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATDGWFD/INC      * Patient DRG Desc & Weighting files
          INC       PATFN1FD/INC      * Health Fund File
          INC       PATHSPFD/INC      * Hospital file
          INC       PATITMFD/INC      * Item file
          INC       PMSPRGFD/INC      * Program Codes Table
          INC       PMSVSCFD/INC      * Clinical Coding Value Set Codes
.
.         Temporary file for Billing Default records which
.         are loaded from the upload file provided by the customer (PROC0000)
.
.         Billing Defaults Upload file
.
UITEMTXT  FILE       HL7, FIXED=1000        * Upload file for Billing Default
.
.Name     Type     Size     Start
.----     ----     ----     -----
XHOSPID   DIM       3           1       Hospital ID
XCLMTYPE  DIM       3           4       Claim Type (Cat CL)
XHFUNDCD  DIM       6           7       Health Fund Code
XHFUNDTB  DIM       8          13       Health Fund Table  
XVSIDCOD  DIM      30          21       Value Set ID - Code
XEFFSDTE  DIM       8          51       Effective Start Date (CCYYMMDD)
XEFFEDTE  DIM       8          59       Effective End Date (CCYYMMDD)
XTLOSTAY  DIM       3          67       Target Length of Stay
XILOSTAY  DIM       3          70       Intended Length of Stay (Cat VI)
XCMXCODE  DIM       9          73       Casemix Code
XCASEPAY  DIM       1          74       Casepayment (Y/N)
XDRGCODE  DIM       4          75       Provisional DRG Code
XDRGVERS  DIM       3          79       Provisional DRG Version
XADMTYPE  DIM       3          82       Admission Type (Cat A )
XCARETYP  DIM       3          85       Care Type (Cat CC)
XPGMCODE  DIM       8          88       Program Code
XPRESILL  DIM       3          96       OEC Presenting Illness (Cat il)
XMBSITEM  DIM       9          99       OEC MBS Item
XBDALERT  DIM       3         108       Billing Default Alerts (Cat az)
.
.End of Record
.
. ----- Program Variables -----
.
ADDCOUNT  FORM      8
CMDLINE   DIM       127
DIM3      DIM       3
ERRFLAG   FORM      1
ERRDATE   FORM      1
ERRDATE2  FORM      1
ERRCOUNT  FORM      8
ERRORMSG  DIM       40
FORM3     FORM      3
FNAMEI    DIM       150
FLAGLEFD  FORM      1       * Previous record with a prior Effective Start Date
LINECNTR  FORM      8
ITEMDESC  DIM       20
PATHNAME  DIM       150
PRIVIND   DIM       1
SAVKEY50  DIM       50
USERID    DIM       10
UPDCOUNT  FORM      8
VALFDATE  DIM       8
VALTDATE  DIM       8
WORKDATE  DATE      8
.
XFLD0001  DIM       30
XFLD0002  DIM       30
XFLD0003  DIM       30
XFLD0004  DIM       30
XFLD0005  DIM       30
XFLD0006  DIM       30
XFLD0007  DIM       30
XFLD0008  DIM       30
XFLD0009  DIM       30
XFLD0010  DIM       30
.
XFLD0011  DIM       30
XFLD0012  DIM       30
XFLD0013  DIM       30
XFLD0014  DIM       30
XFLD0015  DIM       30
XFLD0016  DIM       30
XFLD0017  DIM       30
XFLD0018  DIM       30
XFLD0019  DIM       30
.
.
CATil     INIT      "il"
CATaz     INIT      "az"
.
PRGID     INIT      "IBAADT12"
PRGNAM    INIT      "Upload & Update Billing Defaults"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                  ML0000                                    *
.*                                Main Module                                 *
.******************************************************************************
ML0000    CALL      INIT0000                * Initailize variables & open files
          BRANCH    EXIT,ML9800             * Exit if file not found
          CALL      KOPT0000                * Keyin Upload or Update option
          BRANCH    EXIT,ML9999
.
          BRANCH    COPTION,ML1000
          GOTO      ML9999
.
ML1000    CALL      KASC0000                * Keyin ascii file
          BRANCH    EXIT,ML9900
.
ML1100    CALL      SCRD0000                * Display screen
.
          CALL      KUSR0000                * Keyin user id
          BRANCH    EXIT,ML0000
.
          CALL      KPTH0000                * Keyin pathname
          BRANCH    EXIT,ML0000
.
          CALL      SFLD0000
          BRANCH    EXIT,ML1100
.
          CALL      PRHD0000                * Print header
.
          CALL      CTOT0000                * Clear total vars
.
          CALL      PROC0000                * Updating file
.
          CALL      PTOT0000                * Print report totals
.
          GOTO      ML9999
.
ML9800    CALL      HEAD132
          PRINT     *N,"Billing Defaults file not found." 
          GOTO      ML9999
.
ML9900    CALL      HEAD132
          PRINT     *N,"Failed to open upload file. "
          GOTO      ML9999
.
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                 INIT0000                                   *
.*                     Initialize Variables & Open Files                      *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen heading
.
          CALL      IBACLOCK
.
          MOVE      ZERO,EXIT
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patbdeaf"
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATBDEA1,"patbdeaf"
          TRAPCLR   IO
          IF        OVRCD=1
            MOVE      ONE,EXIT
            GOTO      INIT9999
          ENDIF
.
          DISPLAY   *P64:24,"patcmcaf"
          OPEN      PATCMCA1,"patcmcaf"
.
          DISPLAY   *P64:24,"patdgwaf"
          OPEN      PATDGWA1,"patdgwaf"
.
          DISPLAY   *P64:24,"patfn1af"
          OPEN      PATFN1A1,"patfn1af"
.
          DISPLAY   *P64:24,"pathspaf"
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P64:24,"patitemn"
          OPEN      PATITEM1,"patitemn"
.
          DISPLAY   *P64:24,"pmsprgaf"
          OPEN      PMSPRGA1,"pmsprgaf"
.
          DISPLAY   *P64:24,"pmsvscaf"
          OPEN      PMSVSCA1,"pmsvscaf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
INIT9999  RETURN
+
.******************************************************************************
.*                                 KOPT0000                                   *
.*                            Choose Upload or Update files                   *
.******************************************************************************
KOPT0000  MOVE      ZERO,EXIT
          MOVE      ZERO,COPTION
          DISPLAY   *P1:6,*EF:
                    *P1:6,*V2LON,"0",*HOFF," = Exit":
                    *P1:7,*V2LON,"1",*HOFF," = Upload Billing Defaults";
          DISPLAY   *P1:9,"Select Option : ";
.
KOPT1000  KEYIN     *P17:9,*V2LON,COPTION;
          BRANCH    COPTION,KOPT9999
.
          COMPARE   ZERO,COPTION
          GOTO      KOPT9000 IF EQUAL
          BEEP
          GOTO      KOPT1000
.
KOPT9000  MOVE      ONE,EXIT
KOPT9999  RETURN
+
.******************************************************************************
.*                                 KASC0000                                   *
.*                            Keyin ASCI file                                 *
.******************************************************************************
KASC0000  MOVE      ZERO,EXIT
          PACK      FNAMEI WITH SP70,SP70,SP70
.
          DISPLAY   *P19:13,*EF,"Billing Defaults Load File";
.
          KEYIN     *P1:14,*EL,"Keyin Path & File Name : ",*V2LON,FNAMEI
.
          PACK      FNAMEI,FNAMEI,SP70,SP70,SP70
.
          MATCH     SP70,FNAMEI
          GOTO      KASC9000 IF EQUAL
.
          SCAN      ".txt",FNAMEI
          IF        !@EQUAL
            SQUEEZE   FNAMEI
            ENDSET    FNAMEI
            APPEND    ".txt",FNAMEI      * add ".txt" if there is a path
            APPEND    SP70,FNAMEI
            RESET     FNAMEI
          ENDIF
          RESET     FNAMEI
.
          DISPLAY   *P26:14,*V2LON,*EL,FNAMEI;
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      UITEMTXT,FNAMEI
          TRAPCLR   IO
          BRANCH    OVRCD,KASC8000
.
          CALL      EXCUS000
          MOVE      ZERO,EXIT
          GOTO      KASC9999
.
KASC8000  DISPLAY   *P1:24,*EL,*B,"File not found.  ";
          CALL      HITENTER
          GOTO      KASC0000
.
KASC9000  MOVE      ONE,EXIT
.
KASC9999  RETURN
+
.******************************************************************************
.*                                 SCRD0000                                   *
.*                            Screen display                                  *
.******************************************************************************
SCRD0000  DISPLAY   *P1:11,*EF:
                    *P1:15," 3. Keyin User id      :":
                    *P1:16," 4. Keyin Path name    :";
SCRD9999  RETURN
+
.******************************************************************************
.*                                  KUSR0000                                  *
.*                     Keyin user id                                          *
.******************************************************************************
KUSR0000  KEYIN     *P26:15,*EL,*V2LON,USERID;
          PACK      USERID,USERID,SP70
          MATCH     SP70,USERID
          GOTO      KUSR9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KUSR9999
.
KUSR9500  MOVE      ONE,EXIT
KUSR9999  RETURN
+
.******************************************************************************
.*                                  KPTH0000                                  *
.*                     Keyin path name                                        *
.******************************************************************************
KPTH0000  PACK      PATHNAME,SP70,SP70,SP70
          KEYIN     *P26:16,*EL,*V2LON,PATHNAME;
          ENDSET    PATHNAME
          APPEND    SP70,PATHNAME
          APPEND    SP70,PATHNAME
          APPEND    SP70,PATHNAME
          RESET     PATHNAME
.
.         PACK      PATHNAME,PATHNAME,SP70,SP70,SP70
          MATCH     SP70,PATHNAME
          GOTO      KPTH9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KPTH9999
.
KPTH9500  MOVE      ONE,EXIT
KPTH9999  RETURN
+
.******************************************************************************
.*                                  SFLD0000                                  *
.*                        Select Field                                        *
.******************************************************************************
SFLD0000  CALL      MAINQST                 * Select Item, (P)ost, (C)ancel ?
.
          COMPARE   ZERO,CCITEM
          GOTO      SFLD9999 IF EQUAL
          GOTO      SFLD1000 IF LESS
.
          MOVE      ZERO,EXIT
.
          PERFORM   CCITEM,KUSR0000:             * Keyin user id
                           KPTH0000              * Keyin pathname
          BRANCH    EXIT,SFLD9999
          GOTO      SFLD0000
.
SFLD1000  MOVE      ONE,EXIT
SFLD9999  RETURN
+
.******************************************************************************
.*                                  PRHD0000                                  *
.*                        Print the selection details                         *
.******************************************************************************
PRHD0000  CALL      HEAD132
.
          PRINT     *N,"Upload Billing Defaults"
.
          PRINT     *N,"Date     : ",CDATE:
                    *N,"Time     : ",CTIMEIS:
                    *N,"User ID  : ",USERID
.
          CALL      UND132
.
          PRINT     *1,"| Line",*12,"| Error ",*54,"| Record Detail":
                    *132,"|"
          CALL      UND132
.
PRHD9999  RETURN
+
.******************************************************************************
.*                                  PROC0000                                  *
.*                        Post record                                         *
.******************************************************************************
PROC0000  MOVE      ZERO,LINECNTR
.
          OPEN      UITEMTXT,FNAMEI
.
PROC1000  READ      UITEMTXT,SEQ;XFLD0001,XFLD0002,XFLD0003,XFLD0004,XFLD0005:
                                 XFLD0006,XFLD0007,XFLD0008,XFLD0009,XFLD0010:
                                 XFLD0011,XFLD0012,XFLD0013,XFLD0014,XFLD0015:
                                 XFLD0016,XFLD0017,XFLD0018,XFLD0019
          GOTO      PROC9999 IF OVER
.
          PACK      XHOSPID,XFLD0001,SP70    * Hospital ID
          PACK      XCLMTYPE,XFLD0002,SP70   * Claim Code (Cat CL)
          PACK      XHFUNDCD,XFLD0003,SP70   * Health Fund Code   
          PACK      XHFUNDTB,XFLD0004,SP70   * Health Fund Table
          PACK      XVSIDCOD,XFLD0005,SP70   * Reason for adm - value set ID
          PACK      XEFFSDTE,XFLD0006,SP70   * Start Date(CCYYMMDD)
.
          PACK      KEY58,XHOSPID,XCLMTYPE,XHFUNDCD,XHFUNDTB,XVSIDCOD,XEFFSDTE
          MATCH     SP70,KEY58
          GOTO      PROC1000 IF EQUAL
. 
          ADD       ONE,LINECNTR             * Text file line counter for error
          PACK      XEFFEDTE,XFLD0007,SP70   *End Date(CCYYMMDD)
.
          SQUEEZE   XFLD0008
          MOVE      XFLD0008,XTLOSTAY        * Target Length of Stay
.
          PACK      XILOSTAY,XFLD0009,SP70   * Intended Length of Stay (Cat VI)
          PACK      XCMXCODE,XFLD0010,SP70   * Casemix Code           
          PACK      XCASEPAY,XFLD0011,SP70   * Casepayment (Blank/Y/N)
          PACK      XDRGCODE,XFLD0012,SP70   * DRG Code
          PACK      XDRGVERS,XFLD0013,SP70   * DRG Version
          PACK      XADMTYPE,XFLD0014,SP70   * Admission Type (Cat A )
          PACK      XCARETYP,XFLD0015,SP70   * Care Type (Cat CC)
          PACK      XPGMCODE,XFLD0016,SP70   * Program Code
          PACK      XPRESILL,XFLD0017,SP70   * Presenting Illness (Cat il)
          PACK      XMBSITEM,XFLD0018,SP70   * OEC MBS Item
          PACK      XBDALERT,XFLD0019,SP70   * Billing Default Alerts (Cat az)
.
          PACK      KEY75,XHOSPID,SP1,XCLMTYPE,SP1,XHFUNDCD,SP1,XHFUNDTB:
                          SP1,XVSIDCOD,SP1,XEFFSDTE,SP70
.
          CALL      VFLD0000                * Validation pass
          BRANCH    ERRFLAG,PROC1000        * validate fields
.
          REP       " 0",XEFFSDTE
          MATCH     SP70,XEFFEDTE
          IF        !@EQUAL
            REP       " 0",XEFFEDTE
          ENDIF
          PACK      KEY58,XHOSPID,XCLMTYPE,XHFUNDCD,XHFUNDTB,XVSIDCOD:
                          XEFFSDTE,SP70
          CALL      RDPTBDE1
          IF        OVRCD=1
            CALL      SETF0000
            ADD       ONE,ADDCOUNT        * Total records added
            MOVE      USERID,PTBECUID
            CALL      IBACLOCK
            PACK      PTBECDAT,CCC,CYY,CMM,CDD
            REP       " 0",PTBECDAT
            MOVE      CTIMEIS,PTBECTIM
            CALL      WRPTBDE1            * Write patbdeaf              
          ELSE
            CALL      SETF0000
            ADD       ONE,UPDCOUNT        * Total records updated
            MOVE      USERID,PTBEUUID
            CALL      IBACLOCK
            PACK      PTBEUDAT,CCC,CYY,CMM,CDD
            REP       " 0",PTBEUDAT
            MOVE      CTIMEIS,PTBEUTIM
            CALL      UPPTBDE1            * Update patbdeaf
          ENDIF
.
          CALL      EDAT0000              * Check Eff.todate of previous record
          GOTO      PROC1000
.
PROC9999  RETURN
+
.******************************************************************************
.*                             EDAT0000                                       *
.*    Auto populate the previous misc items effective to date if blank        *
.******************************************************************************
EDAT0000  PACK      KEY58,XHOSPID,XCLMTYPE,XHFUNDCD,XHFUNDTB,XVSIDCOD:
                          XEFFSDTE,SP70
          MOVE      KEY58,SAVKEY50
          CALL      RSPTBDE1
          CALL      RPPTBDE1
          BRANCH    OVRCD,EDAT8000
.
          PACK      KEY50,PTBEHOSP,PTBECLMC,PTBEHFND,PTBEHFTB,PTBEVSID,SP20
          MATCH     SAVKEY50,KEY50
          GOTO      EDAT9999 IF NOT EQUAL
.
.         Previous record with a prior Effective Start Date 
          COMPARE   ONE,FLAGLEFD
          IF        @EQUAL
            GOTO      EDAT1000
          ENDIF
.
          MATCH     SP70,PTBEEDTE                * Exit if effective to date is
          GOTO      EDAT9999 IF NOT EQUAL        * already populated
.
.--------- If previous record exists then that record should be automatically
.--------- end-dated with the date begin set to 1 day prior to the new Start
.--------- Date. 
EDAT1000  MOVE      XEFFSDTE,WORKDATE
          SUB       ONE,WORKDATE            * Calculate prev record End date
          MOVE      WORKDATE,PTBEEDTE
.
          CALL      IBACLOCK
          PACK      PTBEUDAT,CCC,CYY,CMM,CDD
          REP       " 0",PTBEUDAT           * date record updated
          MOVE      CTIMEIS,PTBEUTIM        * time record updated
          MOVE      USERID,PTBEUUID         * web user updated
          CALL      UPPTBDE1                * Update the prev record
          GOTO      EDAT9999
.
EDAT8000  COMPARE   ONE,FLAGLEFD
          IF        @EQUAL
            GOTO      EDAT1000
          ENDIF
.
EDAT9999  RETURN
+
.******************************************************************************
.*                             VFLD0000                                       *
.*                           Validate fields                                  *
.******************************************************************************
VFLD0000  MOVE      ZERO,ERRFLAG
          MOVE      ZERO,ERRDATE
          MOVE      ZERO,ERRDATE2
.
.-------- Hospital ID is mandatory for Multi hospital (IBCNMHOS=1)
          COMPARE   ONE,IBCNMHOS
          GOTO      VFLD0500 IF NOT EQUAL
.
          MATCH     SP70,XHOSPID
          IF        @EQUAL
            MOVE      "Hospital ID is mandatory for multi hospital",ERRORMSG
            CALL      PERR0000
            GOTO      VFLD1000
          ENDIF
.
          PACK      KEY3,XHOSPID,SP70
          CALL      RDPTHSP1
          IF        OVRCD=1
            MOVE      "Invalid Hospital ID",ERRORMSG
            CALL      PERR0000
            GOTO      VFLD1000
          ENDIF
.
          GOTO      VFLD1000
.
.-------- Hospital ID must be blank for Single Hospital
VFLD0500  MATCH     SP70,XHOSPID
          IF        !@EQUAL
            MOVE      "Hospital ID must be blank for single hospital",ERRORMSG
            CALL      PERR0000
            GOTO      VFLD1000
          ENDIF
.
.-------- Mandatory Reason for Admission
VFLD1000  MATCH     SP70,XVSIDCOD
          IF        @EQUAL
            MOVE      "Blank Reason for Admission",ERRORMSG
            CALL      PERR0000
          ELSE 
            MOVE      "BRA",DIM3
            PACK      KEY33,DIM3,XVSIDCOD,SP70
            CALL      RDPMVSC1
            IF        OVRCD=1
              MOVE      "Invalid Reason for Admission",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF

.
.-------- Mandatory Claim Type - Cat CL
          MOVE      SP70,PRIVIND
          MATCH     SP70,XCLMTYPE
          IF        @EQUAL
            MOVE      "Blank Claim Type",ERRORMSG
            CALL      PERR0000
          ELSE
            PACK      KEY5,ANSC,ANSL,XCLMTYPE,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      "Invalid Claim Type",ERRORMSG
              CALL      PERR0000
            ELSE
              MOVE      TCINDC5,PRIVIND 
            ENDIF
          ENDIF
.
.-------- Health Fund Code - Conditional on Claim Type
.-------- Mandatory if Claim Type Cat CL Indicator5 = 1 (Private)
          MATCH     SP70,XHFUNDCD
          IF        @EQUAL
            MATCH     "1",PRIVIND                 * Private Claim type?
            IF        @EQUAL      
              MOVE      "Health Fund Code is mandatory for Claim Type",ERRORMSG
              CALL      PERR0000
            ENDIF
          ELSE
            PACK      KEY14,XHFUNDCD,ZERO,ZERO,ZERO,ZERO,SP4,SP70
            CALL      RDFUND1
            IF        OVRCD=1
              MOVE      "Invalid Health Fund Code",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
          MATCH     SP70,XHFUNDTB
          IF        @EQUAL
            MATCH     "1",PRIVIND                * Private Claim Type?
            IF        @EQUAL
              MOVE      "Health Fund Table is mandatory for Claim Type",ERRORMSG
              CALL      PERR0000
            ENDIF
          ELSE
            PACK      KEY14,XHFUNDCD,XHFUNDTB,SP70
            CALL      RDFUND1
            IF        OVRCD=1
              MOVE      "Invalid Health Fund Code/Table Combination",ERRORMSG
             CALL      PERR0000
            ENDIF
          ENDIF
.
.-------- Mandatory Effective Start Date
          MATCH     SP70,XEFFSDTE
          IF        @EQUAL
            MOVE      "Blank Effective Start Date",ERRORMSG
            CALL      PERR0000
            MOVE      ONE,ERRDATE
          ELSE
            UNPACK    XEFFSDTE,CCENT,CYEAR,CMON,CDAY
            MOVE      ONE,F1
            CALL      VLDT0000             * validate date
          ENDIF
.
.-------- Optional - Effective End Date
          MATCH     SP70,XEFFEDTE
          IF        !@EQUAL
            UNPACK    XEFFEDTE,CCENT,CYEAR,CMON,CDAY
            MOVE      ZERO,F1
            CALL      VLDT0000             * validate date
          ENDIF
.
.-------- Check Effective Start/End date for overlaps
          COMPARE   ZERO,ERRDATE
          GOTO      VFLD2000 IF NOT EQUAL
.
          MATCH     SP70,XEFFEDTE
          IF        !@EQUAL
            MATCH     XEFFSDTE,XEFFEDTE
            IF        @LESS
              MOVE      "Effective To Date is Less than Eff.From Date",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
          PACK      VALFDATE,XEFFSDTE,SP70
          PACK      VALTDATE,XEFFEDTE,SP70
          PACK      KEY58,XHOSPID,XCLMTYPE,XHFUNDCD,XHFUNDTB,XVSIDCOD:
                          XEFFSDTE,SP70
          CALL      VBDDT000               * Validate effective dates
          IF        EXIT=1
            MOVE      "Invalid Effective From/To Date(Overlap)",ERRORMSG
            CALL      PERR0000
          ENDIF
.
.-------- Mandatory Target Length of Stay
VFLD2000  MATCH     SP70,XTLOSTAY
          IF        @EQUAL|@EOS
            MOVE      "Blank Target Length of Stay",ERRORMSG
            CALL      PERR0000
            GOTO      VFLD3000
          ENDIF
.
          MOVE      ZERO,FORM3
          TYPE      XTLOSTAY                         * If not numeric
          IF        !@EQUAL
            MOVE      "Invalid TLOS value must be numeric",ERRORMSG
            CALL      PERR0000
            GOTO      VFLD3000
          ENDIF
.
          MOVE      XTLOSTAY,FORM3                    * If numeric & negative
          IF        FORM3 < 0
            MOVE      "Invalid TLOS value cannot be negative",ERRORMSG
            CALL      PERR0000 
          ENDIF
.
.-------- Mandatory Intended Length of Stay - Cat VI
VFLD3000  MATCH     SP70,XILOSTAY
          IF        @EQUAL
            MOVE      "Blank Intended Length of Stay",ERRORMSG
            CALL      PERR0000
          ELSE
            PACK      KEY5,ANSV,ANSI,XILOSTAY,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      "Invalid Intended Length of Stay ",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
.-------- Optional Casemix Code
          MATCH     SP70,XCMXCODE
          IF        !@EQUAL
            PACK      KEY9,XCMXCODE,SP70
            CALL      RDPTCMC1
            IF        OVRCD=1
              MOVE      "Invalid Casemix Code",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
.-------- Casepayment (Y/N) - Conditional on Casemix Code
.
. Valid values: Casemix Code blank - Casepayment must be No or Blank
.               Casemix Code populated - Casepayment must be Yes or No
.
          MATCH     SP70,XCASEPAY
          IF        @EQUAL
            MATCH     SP70,XCMXCODE
            IF        !@EQUAL
              MOVE      "Casepayment is mandatory for Casemix Code",ERRORMSG
              CALL       PERR0000
            ENDIF
          ELSE
            REP       "nNyY",XCASEPAY
            MATCH     ANSN,XCASEPAY
            GOTO      VFLD4000 IF EQUAL
.
            MATCH     SP70,XCMXCODE
            IF        !@EQUAL
              MATCH     ANSY,XCASEPAY
              GOTO      VFLD4000 IF EQUAL
            ENDIF
.
            MOVE      "Invalid Casepayment Yes/No/Blank",ERRORMSG
            CALL      PERR0000
          ENDIF
.         
.-------- Optional Provisional DRG Code/Version
VFLD4000  MATCH     SP70,XDRGCODE
          IF        !@EQUAL
            PACK      KEY7,XDRGCODE,XDRGVERS,SP70
            CALL      RDPTDGW1
            IF        OVRCD=1
              MOVE      "Invalid DRG Code ",ERRORMSG
              ENDSET    ERRORMSG
              APPEND    XDRGCODE,ERRORMSG
              APPEND    XDRGVERS,ERRORMSG
              RESET     ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
.-------- Mandatory Admission Type - Cat A
          MATCH     SP70,XADMTYPE
          IF        @EQUAL
            MOVE      "Blank Admission Type",ERRORMSG
            CALL      PERR0000
          ELSE
            PACK      KEY5,ANSA,SP1,XADMTYPE,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      "Invalid Admission Type ",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
.-------- Mandatory Care Type - Cat CC
          MATCH     SP70,XCARETYP
          IF        @EQUAL
            MOVE      "Blank Care Type",ERRORMSG
            CALL      PERR0000
          ELSE
            PACK      KEY5,ANSC,ANSC,XCARETYP,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      "Invalid Care Type ",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
.-------- Optional Program Code
          MATCH     SP70,XPGMCODE
          IF        !@EQUAL
            PACK      KEY8,XPGMCODE,SP70
            CALL      RDPMPRG1
            IF        OVRCD=1
              MOVE      "Invalid Program Code",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
.-------- OEC Presenting Illness Code - Cat il
          MATCH     SP70,XPRESILL
          IF        !@EQUAL
            PACK      KEY5,CATil,XPRESILL,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      "Invalid OEC Presenting Illness Code",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
.-------- OEC MBS Item
          MATCH     SP70,XMBSITEM
          IF        !@EQUAL
            IF        ERRDATE=0
              PACK      KEY17,XMBSITEM,XEFFSDTE,SP70
              CALL      PATITMRD
              IF        OVRCD=1
                MOVE      "Invalid MBS Item",ERRORMSG
                CALL      PERR0000
              ENDIF
            ELSE
              MOVE      "Invalid MBS Item needs Effective start date",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
.-------- Billing Default Alerts - Cat az
          MATCH     SP70,XBDALERT
          IF        !@EQUAL
            PACK      KEY5,CATaz,XBDALERT,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      "Invalid Billing Default Alert Code",ERRORMSG
              CALL      PERR0000
            ENDIF
          ENDIF
.
VFLD9999  RETURN
+
.******************************************************************************
.*                                  VLDT0000                                  *
.*                             Validate date                                  *
.******************************************************************************
VLDT0000  MOVE        ZERO,FORM2
          MOVE        CDAY,FORM2
          IF          FORM2 < 1 | FORM2 > 31
            GOTO        VLDT8000
          ENDIF
.
          MOVE        ZERO,FORM2
          MOVE        CMON,FORM2
          IF          FORM2 < 1 | FORM2 >12
            GOTO        VLDT8000
          ENDIF
.
          MOVE        ZERO,FORM2
          MOVE        SP70,KEY4
          PACK        KEY4,CCENT,CYEAR,SP70
          MOVE        KEY4,FORM4
          IF          FORM4 < 1
            GOTO        VLDT8000
          ENDIF
          GOTO        VLDT9999
.
VLDT8000  IF          F1=1
            MOVE        "Invalid format of From Date",ERRORMSG
            MOVE        ONE,ERRDATE
          ELSE
            MOVE        "Invalid format of To Date",ERRORMSG
            MOVE        ONE,ERRDATE2
          ENDIF
          CALL        PERR0000
.
VLDT9999  RETURN
+
.******************************************************************************
.*                                  SETF0000                                  *
.*                             Set fields                                     *
.******************************************************************************
SETF0000  PACK      PTBEHOSP,XHOSPID,SP70    * Billing Default Hospital ID
          PACK      PTBECLMC,XCLMTYPE,SP70   * Claim Code (Cat CL)
          PACK      PTBEHFND,XHFUNDCD,SP70   * Health Fund Code
          PACK      PTBEHFTB,XHFUNDTB,SP70   * Health Fund Table
          PACK      PTBEVSID,XVSIDCOD,SP70   * Value Set Id Code      
          PACK      PTBESDTE,XEFFSDTE,SP70   * Effective Start Date (CCYYMMDD)
          PACK      PTBEEDTE,XEFFEDTE,SP70   * Effective End Date (CCYYMMDD)
          MOVE      XTLOSTAY,PTBETLOS        * Target Length of Stay
          PACK      PTBEILOS,XILOSTAY,SP70   * Admission Type (Cat VI )
          PACK      PTBECSMX,XCMXCODE,SP70   * Casemix Code
.
          PACK      PTBECPAY,XCASEPAY,SP70   * Casepayment (Y/N)
          MATCH     ANSN,XCASEPAY
          IF        @EQUAL
            MOVE      "0",PTBECPAY
          ELSE
            MATCH     ANSY,XCASEPAY
            IF        @EQUAL
              MOVE      "1",PTBECPAY
            ENDIF
          ENDIF
.         
          PACK      PTBEDRGC,XDRGCODE,SP70   * Provisional DRG Code
          PACK      PTBEDRGV,XDRGVERS,SP70   * Provisional DRG Version
          PACK      PTBEADMN,XADMTYPE,SP70   * Admission Type (Cat A )
          PACK      PTBECARE,XCARETYP,SP70   * Care Type (Cat CC )
          PACK      PTBEPRGM,XPGMCODE,SP70   * Program Code
          PACK      PTBEPILL,XPRESILL,SP70   * OEC Presenting Illness code 
.                                            *  (Cat il)
          PACK      PTBEMBSI,XMBSITEM,SP70   * OEC MBS Item
          PACK      PTBEALER,XBDALERT,SP70   * Billing Defaults Alerts (Cat az)
.
          MOVE      SP70,PTBESPAR
SETF9999  RETURN
+
.******************************************************************************

.         Execute us1 command before processing
.******************************************************************************
EXCUS000  CLEAR     CMDLINE
          APPEND    "ibaadt12.us1 ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
EXCUS999  RETURN
+
.******************************************************************************
. Print error line
.******************************************************************************
PERR0000  ADD       ONE,ERRCOUNT            * Total error count
.
          PRINT     *1,"|",LINECNTR,*12,"|",ERRORMSG,*54,"|":
                    KEY75,*132,"|"
          PACK      KEY75,SP70,SP20
          MOVE      ONE,ERRFLAG
.
PERR9999  RETURN
+
.******************************************************************************
. Clear total vars
.******************************************************************************
CTOT0000  MOVE      ZERO,ERRCOUNT
          MOVE      ZERO,ADDCOUNT
          MOVE      ZERO,UPDCOUNT
.
CTOT999   RETURN
+
.******************************************************************************
. Print total line
.******************************************************************************
PTOT0000  CALL      UND132
.
          PRINT     *1,"| Total Errors  : ",ERRCOUNT,*132,"|"
          PRINT     *1,"| Total Added   : ",ADDCOUNT,*132,"|"
          PRINT     *1,"| Total Updated : ",UPDCOUNT,*132,"|"
.
          CALL      UND132
.
PTOT9999  RETURN
.
          INC       STD001IO
          INC       PATITMRD
          INC       PATBDEVL   * Validate patbdeaf for effective date overlaps
.
          INC       PATCODIO/INC
          INC       PATBDEIO/INC      * Billing Default file
          INC       PATCMCIO/INC      * Casemix Codes File
          INC       PATDGWIO/INC      * Patient DRG Desc & Weighting files
          INC       PATFN1IO/INC      * Health Fund File
          INC       PATHSPIO/INC      * Hospital file
          INC       PATITMIO/INC      * Item file
          INC       PMSPRGIO/INC      * Program Codes Table
          INC       PMSVSCIO/INC      * Clinical Coding Value Set Codes

.

.******************************************************************************
.* System         : Outpatients                                               *
.* Program        : IBAOUT34.PBL                                              *
.* Name           : Input Referral Letter Priority                            *
.******************************************************************************
.* Date           : 03/03/1992                                                *
.* Author         : Justin Coates                                             *
.* Function       : To allow the entry of a priority of a referral letter.    *
.* Modifications  :                                                           *
.*        V10.07.01 30/10/2015  Ebon Clements CAR 268970                      *
.*                  Change to use OUTCLIFD index 1 instead of index 2         *
.*        V9.09.02  04/03/2008  Peter Vela    CAR 162914                      *
.*                  Recompiled for KYOUTCLI - readk of outcliaf               *
.*        V9.09.01  28/07/2007  Jill Habasque CAR 103645                      *
.*                  Recompiled for KYOUTCLI - read of pmshcpaf                *
.*        V5.09.01  27/05/2002  Mike Laming  SRF 15999                        *
.*                  Recompiled for OUTRF1IO.INC                               *
.*        V5.08.02  19/09/2000  Steve Armstrong   srf 647263                  *
.*                  Recompiled for changes to OUTDSCLN                        *
.*        V5.08.01  29/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.B01 13/03/2000  Steve Armstrong  508 Mods                     *
.*                  Recompiled for changes to OUTCLIFD (effective date)       *
.******************************************************************************
.*        V5.05.02  30/04/1998  Steve Armstrong  506 Mods                     *
.*                  Mods to highlight selected record                         *
.*        V5.05.01  28/03/1998  Steve Armstrong  506 Mods                     *
.*                  Mods to add Clinic Type option                            *
.******************************************************************************
.*        V5.04.04  23/04/1997  howellsy   RHA                                *
.*                  Write to History                                          *
.*        V5.04.03  30/01/1997  Howellsy          EOC & ACC                   *
.*                  Outpatient Referrals Only - OTRLTREF = 0                  *
.*        V5.04.02  20/12/1996  Howellsy          EOC & ACC                   *
.*                  Recompiled for OUTRF1FD                                   *
.*                  V5.04.001 02/09/96  Howellst      SRF 642010              *
.*                :           Changed Consultant to Clinic Id                 *
.*                  V5.01.002 26/04/93  Steve O'Gorman                        *
.*                :           Fixed Paging and line 24 prompt                 *
.******************************************************************************
. 
.         FD's required
.
          INC       STD001FD/PBL
          INC       OUTCONFD/INC            * system parameters
          INC       OUTCLIFD/INC            * clinic file
          INC       OUTCTYFD/INC            * clinic type file
          INC       OUTRF1FD/INC            * OP referral letters file
          INC       PATCODFD/INC            * codes file
          INC       PATDO1FD/INC            * doctors file
          INC       PMSHCPFD/INC            * National HCP file
          INC       PATMA1FD/INC            * master file
          INC       PATPR1FD/INC            * pre-admission file
          INC       OUTHISFD/INC 
.
.         program variables
.
PTCNURHA  FORM      1
CALLPOSN  FORM      1         * which line 24 prompt
CNTITEM   FORM      2         * item counter
CODE      DIM       2         * patdscod
CODECONS  DIM       6         * consultant code
CODEPRTY  DIM       3         * priority code
CLINTYPE  DIM       6         * clinic type
DESCCONS  DIM       30        * consultant name
DESCPRTY  DIM       20        * priority desc
DIAGNOS   DIM       30        * diagnosis
DIM14     DIM       14        * for OUTDSCTY
DIM40     DIM       40        * outdscln
LETTER    DIM       22[17]    * letters displayed
MODE      FORM      1         * input mode flag
.                                  1 = by Clinic Id
.                                  2 = by Clinic Type
PATNAME   DIM       25        * patient name
ROWNUMB   FORM      2
.
.         program constants
.
CATPR     INIT      "PR"
Z20       INIT      "zzzzzzzzzzzzzzzzzzzz"
.
PRGID     INIT      "IBAOUT34"
PRGNAM    INIT      "Input Referral Letter Priority"
VERSION   INIT      "V12.01.00"
.
.*********************************************************************
.*                  ML0000                                           *
.*        MAINLINE CODE / CONTROLLING LOGIC                          *
.*********************************************************************
ML0000    CALL      INIT0000
.
ML1000    CALL      OPTN0000                * run program ??
          BRANCH    EXIT,ML9999             * exit
.
          CALL      GMOD0000                * get mode
          BRANCH    EXIT,ML1000             * nothing selected
          BRANCH    MODE,ML2000:            * by Clinic Id
                         ML2500             * by Clinic Type
.
ML2000    CALL      KCON0000                * enter consultant
          BRANCH    EXIT,ML1000             * exitchar entered
          GOTO      ML3000
.
ML2500    CALL      KTYP0000                * input clinic type
          BRANCH    EXIT,ML1000             * exitchar entered
.
ML3000    CALL      DRFL0000                * display letters
          BRANCH    MODE,ML2000:            * by clinic id
                         ML2500             * by clinic type
.
ML9999    CHAIN     PGM
+
.*********************************************************************
.*                  INIT0000                    Called by : ML0000   *
.*********************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P50:24,"Opening "
          DISPLAY   *P58:24,"outrf1af"
          OPEN      OUTRF1A5,"outrf1af"
          DISPLAY   *P58:24,"outrf1af"
          OPEN      OUTRF1A1,"outrf1af"
          OPEN      OUTRF1A7,"outrf1af"
          DISPLAY   *P58:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P58:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
          DISPLAY   *P58:24,"patpramf"
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
          DISPLAY   *P58:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
          DISPLAY   *P64:24,"pmshcpaf";
          OPEN      PMSHCPA1,"pmshcpaf"
.
          DISPLAY   *P58:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
          PACK      CFNAME,UID,FILCLIA1
          DISPLAY   *P58:24,CFNAME
          OPEN      OUTCLIA1,CFNAME
.
          PACK      CFNAME,UID,FILCTYA1
          DISPLAY   *P58:24,CFNAME
          OPEN      OUTCTYA1,CFNAME
.
          MOVE      ONE,CNEWDS
.
INIT9999  RETURN
+
.*********************************************************************
.*                  OPTN0000                    Called by : ML1000   *
.*        See if to run the program                                  *
.*********************************************************************
.
OPTN0000  HLINE     *G37,2,56,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*P1:5,"1",*HOFF:
                    *P2:4,". Exit":
                    *P2:5,". Input Priorities":
                    *P1:7,"Select option : "
.
OPTN1000  KEYIN     *P17:7,*DV,UNDLN1:
                    *P17:7,*V2LON,MOPTION
.
          BRANCH    MOPTION,OPTN9000
          COMPARE   ZERO,MOPTION
          GOTO      OPTN9100 IF EQUAL       * exit
          BEEP
          GOTO      OPTN1000
.
.         set exit flags
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9100  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.****************************************************************************
.*                              GMOD0000               Called by: ML0000    *
.*                 Get the mode of input                                    *
.* Returns:  EXIT   0 = valid mode selected                                 *
.*                  1 = exit selected                                       *
.*           MODE   1 = by Clinic Id                                        *
.*                  2 = by Clinic Type                                      *
.****************************************************************************
.
GMOD0000  KEYIN     *P1:24,*EL,"By Clinic (",*V2LON,*DV,ANSI,*HOFF,")d or ":
                    "Clinic (",*V2LON,*DV,ANST,*HOFF,")ype ?":
                    *P35:24,*V2LON,ANS:
                    *P35:24,*DV,ANS
.
          PACK      ANS,ANS,SP1
.
          REP       UPPLOW,ANS
          MATCH     ANSI,ANS                     * "I" input ?
          IF        @EQUAL
            MOVE      ONE,MODE                   * yes - set mode for clinic id
            DISPLAY   *P56:2,*V2LON," by Clinic Id "
            GOTO      GMOD9000
          ENDIF
.
          MATCH     ANST,ANS                     * "T" input ?
          IF        @EQUAL
            MOVE      TWO,MODE                   * yes - set mode for cl. type
            DISPLAY   *P56:2,*V2LON," by Clinic Type "
            GOTO      GMOD9000
          ENDIF
.
          MATCH     SP1,ANS                      * nothing input ?
          GOTO      GMOD9500 IF EQUAL            * yes - exit
.
          MATCH     EXITCHAR,ANS                 * exitchar input ?
          GOTO      GMOD9500 IF EQUAL            * yes - exit
.
          BEEP                                   * invalid
          GOTO      GMOD0000
.
GMOD9000  MOVE      ZERO,EXIT
          GOTO      GMOD9999
.
GMOD9500  MOVE      ONE,EXIT
.
GMOD9999  RETURN
+
.****************************************************************************
.*                                KTYP0000             Called by: ML0000    *
.*                 Keyin the clinic type                                    *
.*         Returns: EXIT  0 = OK                                            *
.*                        1 = nothing or exitchar entered                   *
.****************************************************************************
.
KTYP0000  KEYIN     *P1:3,*EF,*P1:4,"Clinic Type  :          (`\' to exit)":
                    *P16:4,*V2LON,CLINTYPE:
                    *P16:4,*EL,*DV,CLINTYPE
.
          CALL      CHKC0000                * see what was entered
          BRANCH    EXIT,KTYP1000:          * ? entered
                         KTYP7500:          * valid code
                         KTYP6600:          * nothing entered
                         KTYP9000           * exitchar
.
          GOTO      KTYP0000                * invalid code
.
KTYP1000  MOVE      ZERO,HLEF
          CALL      GETSCR00
KTYP1200  CALL      OUTDSCTY                * display codes on file
.
KTYP1500  KEYIN     *P1:24,*EL,"Clinic Type: ":
                    *P14:24,*V2LON,CLINTYPE:
                    *P14:24,*DV,CLINTYPE
.
          CALL      CHKC0000                * see what was entered
          BRANCH    EXIT,KTYP1200:          * ? entered
                         KTYP7000:          * valid code
                         KTYP6500:          * nothing entered
                         KTYP8000           * exitchar
.
          GOTO      KTYP1500
.
KTYP6500  CALL      PUTSCR00
KTYP6600  DISPLAY   *P16:4,*EL,*P34:4,*V2LON,"All Clinic Types"
          GOTO      KTYP7700
.
KTYP7000  CALL      PUTSCR00                * restore screen
          DISPLAY   *P16:4,*V2LON,CLINTYPE
KTYP7500  DISPLAY   *P30:4,OCTDESC
KTYP7700  MOVE      ZERO,EXIT
          GOTO      KTYP9999
.
KTYP8000  CALL      FRESCR00
KTYP9000  MOVE      ONE,EXIT
.
KTYP9999  RETURN
+
.***************************************************************************
.*                                  CHKC0000           Called by: KTYP0000 *
.*                      See what was input                                 *
.*      Returns: EXIT  0 = invalid                                         *
.*                     1 = "?"                                             *
.*                     2 = valid                                           *
.*                     3 = nothing                                         *
.*                     4 = exitchar                                        * 
.***************************************************************************
.
CHKC0000  PACK      CLINTYPE,CLINTYPE,SP6
.
          MATCH     SP6,CLINTYPE                 * anything entered ?
          GOTO      CHKC9000 IF EQUAL            * no
.
          MATCH     EXITCHAR,CLINTYPE            * exitchar entered ?
          GOTO      CHKC7000 IF EQUAL            * yes
.
          MATCH     QUEST,CLINTYPE               * ? entered ?
          GOTO      CHKC8000 IF EQUAL            * yes
.
.         Code entered
.
          PACK      KEY12,IDDD,CLINTYPE
          CALL      RDCTYA1                      * code on file ?
          BRANCH    OVRCD,CHKC6000               * no
.
CHKC5000  MOVE      TWO,EXIT                     * code entered
          GOTO      CHKC9999
.
CHKC6000  DISPLAY   *P1:24,*EL,*B,"Clinic Type not on file. ";
          CALL      HITENTER
          MOVE      ZERO,EXIT
          GOTO      CHKC9999
.
CHKC7000  MOVE      FOUR,EXIT
          GOTO      CHKC9999
.
CHKC8000  MOVE      ONE,EXIT
          GOTO      CHKC9999
.
CHKC9000  MOVE      THREE,EXIT
.
CHKC9999  RETURN
+
.*********************************************************************
.*                  KCON0000                    Called by : ML2000   *
.*        Keyin the consultant (outpatient clinic)                   *
.*        Returns : EXIT = 1      nothing entered                    *
.*                  EXIT = 0      valid code or spaces entered       *
.*********************************************************************
.
KCON0000  DISPLAY   *P1:3,*EF:
                    *P1:4,"Clinic Id    :           (`\' to exit)"
          MOVE      "4",EVERT
          MOVE      "16",ECOL
          MOVE      SP6,CKYIDEF6            * no default
          MOVE      ZERO,CKYIMAND           * not mand
          CALL      KYOUTCLI
          BRANCH    EXIT,KCON7900,KCON9100,KCON7900
.                        spaces   exitchar nothing
.
          MOVE      OCACLIN,CODECONS
          MOVE      OCADESC,DESCCONS
          GOTO      KCON8000
.
.         nothing was entered, see if field is mandatory
.
KCON7900  MOVE      SP6,CODECONS            * clear code
          DISPLAY   *P16:4,*EL,*P25:4,*V2LON,"All Clinics" 
          GOTO      KCON8100
.
.         set exit flags
.
KCON8000  DISPLAY   *P16:4,*V2LON,CODECONS,*HOFF,*P25:4,DESCCONS
KCON8100  MOVE      ZERO,EXIT               * valid input
          GOTO      KCON9999
.
KCON9100  MOVE      ONE,EXIT                * exitchar
.
KCON9999  RETURN
+
.*********************************************************************
.*                  DRFL0000                    Called by : ML3000   *
.*        Display all the letters without a priority for the cons.   *
.*********************************************************************
.
DRFL0000  DISPLAY   *P1:5,*EF,*V2LON,*ULON:
                    *P4:6,"Date of Letter":
                    *P19:6,"U/R No. ":
                    *P28:6,"Patients Name            ":
                    *P56:6,"Diagnosis                     "
.
          CALL      CLRA0000                * clear storage variables
          MOVE      ONE,CPAGENO             * set page
          MOVE      "6",CVERT               * starting line
          MOVE      ZERO,CNTITEM            * clear count
.
          IF        MODE = 1
            PACK      KEY22,CODECONS,SP30
            CALL      RSOTRFL5              * position for clinic id
          ELSE
            PACK      KEY22,CLINTYPE,SP30
            CALL      RSOTRFL7              * position for clinic type
          ENDIF
.
DRFL1000  IF        MODE = 1
            CALL      RKOTRFL5              * read next record
          ELSE
            CALL      RKOTRFL7              * read next record
          ENDIF
          BRANCH    OVRCD,DRFL5000          * end of file
.
          IF        MODE = 1
            MATCH     SP6,CODECONS          * clinic id blank ?
          ELSE
            MATCH     SP6,CLINTYPE          * clinic type blank ?
          ENDIF
          GOTO      DRFL1100 IF EQUAL       * yes - check all records
.
          IF        MODE = 1
            MATCH     CODECONS,OTRLCONS     * same clinic id ?
          ELSE
            MATCH     CLINTYPE,OTRLCTYP     * same clinic type ?
          ENDIF
          GOTO      DRFL5000 IF NOT EQUAL   * no - finished
.
DRFL1100  MATCH     SP3,OTRLPRIO
          GOTO      DRFL1000 IF NOT EQUAL   * already have a priority
.
          COMPARE   ZERO,OTRLTREF
          GOTO      DRFL1000 IF NOT EQUAL   * Not Outpatient Referral
.
. ******* increment counters *******
.
DRFL2000  ADD       ONE,CVERT
          COMPARE   "23",CVERT
          GOTO      DRFL4000 IF NOT LESS    * need a new page
          ADD       ONE,CNTITEM             * add to item counter
.
. ******* display data and store key *******
.
DRFL3000  CALL      GPAT0000                * get patient details
          DISPLAY   *P1:CVERT,*V2LON,CNTITEM,*HOFF,".  ",CPCDATE:
                    *P19:CVERT,OTRLURNO,SP1,PATNAME:
                    SP3,*+,DIAGNOS
          IF        MODE = 1
            PACK      KEY22,OTRLCONS,OTRLDATE,OTRLOUTN
          ELSE
            PACK      KEY22,OTRLCTYP,OTRLDATE,OTRLOUTN
          ENDIF
          MOVE      KEY22,LETTER[CNTITEM]
          GOTO      DRFL1000
.
. ******* new screen needed *******
.
DRFL4000  BRANCH    CPAGENO,DRFL4500
.
.         Middle pages : (N)ext & (F)irst
.
DRFL4100  MOVE      ONE,CALLPOSN            * set position
          CALL      KEYA0000
          BRANCH    EXIT,DRFL7000,DRFL0000,DRFL9999,DRFL8500
.                        More     First    Nothing  Print
          GOTO      DRFL6000
.
.         first page : (N)ext
.
DRFL4500  MOVE      TWO,CALLPOSN
          CALL      KEYA0000
          BRANCH    EXIT,DRFL7000,DRFL5900,DRFL9999,DRFL8500
.                        More     First    Nothing  Print
          GOTO      DRFL6000
.
. ******* no more data to display *******
.
DRFL5000  COMPARE   ZERO,CNTITEM
          GOTO      DRFL8900 IF EQUAL       * no data displayed
.
          BRANCH    CPAGENO,DRFL5500
.
.         last page : (F)irst
.
DRFL5100  MOVE      THREE,CALLPOSN
          CALL      KEYA0000
          BRANCH    EXIT,DRFL5900,DRFL0000,DRFL9999,DRFL8500
.                        More     First    Nothing  Print
          GOTO      DRFL6000
.
.         first page :
.
DRFL5500  MOVE      FOUR,CALLPOSN
          CALL      KEYA0000
          BRANCH    EXIT,DRFL5900,DRFL5900,DRFL9999,DRFL8500
.                        More     First    Nothing  Print
          GOTO      DRFL6000
.
DRFL5900  BEEP   
DRFL5910  BRANCH    CALLPOSN,DRFL4100,DRFL4500,DRFL5100,DRFL5500
.
. ******* item on screen selected *******
.
DRFL6000  MOVE      LETTER[FORM2],KEY22
          IF        MODE = 1
            CALL      RDOTRFL5
          ELSE
            CALL      RDOTRFL7
          ENDIF
          BRANCH    OVRCD,DRFL5900          * invalid letter
.
          MOVE      OTRLPRIO,SAOTPRIO
          CALL      KPTY0000                * enter the priority
          IF        EXIT = 1
            DISPLAY   *P1:ROWNUMB,*V2LON,FORM2,DOT,SP1
            GOTO      DRFL5910
          ENDIF
          BRANCH    EXIT,DRFL5910           * no change
          GOTO      DRFL0000                * redisplay all letters
.
. ******* set up for a new page *******
.
DRFL7000  ADD       ONE,CPAGENO             * add to page counter
          MOVE      ZERO,CNTITEM            * reset item counter
          MOVE      "6",CVERT               * set row
          DISPLAY   *P1:7,*EF               * clear screen
          GOTO      DRFL2000
.
. ******* Print chosen *******
.
DRFL8500  CALL      PRFL0000                * print the details
          GOTO      DRFL5910
.
. ******* no letters to display *******
.
DRFL8900  DISPLAY   *P1:24,*B,"There are no letters without a priority.  ",*EL;
          CALL      HITENTER 
.
DRFL9999  RETURN
+
.*********************************************************************
.*                  KEYA0000                    Called by :          *
.*        Keyin response for line 24 question                        *
.*        Para's  : CALLPOSN      which line 24 prompt               *
.*        Returns : EXIT = 0      item selected (FORM2)              *
.*                  EXIT = 1      Next screen selected               *
.*                  EXIT = 2      First screen selected              *
.*                  EXIT = 3      Nothing entered                    *
.*                  EXIT = 4      Print entered                      *
.*********************************************************************
.
KEYA0000  DISPLAY   *P1:24,*EL,"Select Letter, (",*V2LON,ANSE,*HOFF,")xit";
          MOVE      "21",CCOL
          IF        ((CALLPOSN = ONE) | (CALLPOSN = TWO))
            DISPLAY   ", (",*V2LON,"N",*HOFF,")ext";
            ADD       "8",CCOL
          ENDIF
          IF        ((CALLPOSN = ONE) | (CALLPOSN = THREE))
            DISPLAY   ", (",*V2LON,"F",*HOFF,")irst";
            ADD       "9",CCOL
          ENDIF
          DISPLAY   ", (",*V2LON,"P",*HOFF,")rint ? ",*EL;
          ADD       "13",CCOL
.
KEYA1000  KEYIN     *PCCOL:24,*DV,UNDLN2:
                    *PCCOL:24,*V2LON,*JR,DIM2:
                    *PCCOL:24,*DV,DIM2
.
          PACK      DIM2,DIM2,SP2
.
          TYPE      DIM2
          GOTO      KEYA5000 IF EQUAL       * number entered
.
.         letter entered
.
          MATCH     SP1,DIM2
          GOTO      KEYA1500 IF NOT EQUAL   * invalid entry
.
          UNPACK    DIM2,ANS,ANS            * get required letter in ANS
          REP       UPPLOW,ANS              * get as uppercase
          REP       "N1F2E3P4",ANS          * turn valid letters into number
          MOVE      ZERO,EXIT               * clear exit flag
          MOVE      ANS,EXIT                * set exit flag
          BRANCH    EXIT,KEYA9999,KEYA9999,KEYA4300,KEYA9999
.
KEYA1500  BEEP
          GOTO      KEYA1000
.
KEYA4300  MOVE      THREE,EXIT
          GOTO      KEYA9999
.
.         number entered
.
KEYA5000  MOVE      DIM2,FORM2              * get as a number
.
          COMPARE   FORM2,ZERO
          GOTO      KEYA1500 IF NOT LESS    * negative entered
.
          COMPARE   FORM2,CNTITEM
          GOTO      KEYA1500 IF LESS        * number too large
.
.         Highlight the selected record
.
          ASSIGN    (SIX+FORM2),ROWNUMB
          DISPLAY   *P1:ROWNUMB,*HON,FORM2,DOT,SP1
          MOVE      ZERO,EXIT               * set exit flag
.
KEYA9999  RETURN
+
.*********************************************************************
.*                  CLRA0000                    Called by :          *
.*        Clear the storage variables                                *
.*********************************************************************
CLRA0000  MOVE      TEN7,FORM2
          WHILE     FORM2
            MOVE      SP20,LETTER[FORM2]
            SUB       ONE,FORM2
          DO
.
CLRA9999  RETURN
+
.*********************************************************************
.*                  GPAT0000                    Called by : xxxx0000 *
.*        Get the patient name                                       *
.*********************************************************************
GPAT0000  MATCH     ZEROUR,OTRLURNO
          IF        @EQUAL
            MOVE      OTRLOUTN,KEY8
            CALL      RDPRAM1
          ELSE
            MOVE      OTRLURNO,KEY8
            CALL      RDMAST1
          ENDIF
.
          IF        OVRCD = ZERO
            MOVE      PSNAME,PACSNAME
            MOVE      PGNAME,PACGNAME
            MOVE      PTITL,PACTITLE
            MOVE      TWO,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,PATNAME
          ELSE
            MOVE      "No PMI details",PATNAME
          ENDIF
.
          UNPACK    OTRLDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      OTRLDIAG,DIAGNOS        * shorten diagnosis
          SETLPTR   DIAGNOS,21
.
GPAT9999  RETURN
+
.*********************************************************************
.*                  KPTY0000                    Called by : DRFL6000 *
.*        Enter the priority for the selected Letter                 *
.*        Returns : EXIT = 1      no priority entered                *
.*********************************************************************
KPTY0000  DISPLAY   *P1:24,"Enter Priority : ",*EL;
          MOVE      "24",EVERT
          MOVE      "18",ECOL
          MOVE      CATPR,CODE              * category
          MOVE      ZERO,CKYIMAND           * isn't mandatory
          MOVE      SP3,CKYIDEF3            * no default
.
          CALL      PATCODKY
          BRANCH    EXIT,KPTY9999:          * nothing
                         KPTY9999           * exitchar
.
          MOVE      ACODE,CODEPRTY          * code
          MOVE      TDESC,DESCPRTY          * desc
          DISPLAY   *P18:24,*V2LON,CODEPRTY,*HOFF,SP2,DESCPRTY,*EL;
.
KPTY1000  DISPLAY   *P45:24,"Ok (",*V2LON,"Y",*HOFF,"/",*V2LON,"N":
                    *HOFF,") ? ",*EL;
          KEYIN     *P56:24,*DV,UNDLN1:
                    *P56:24,*V2LON,ANS
          REP       UPPLOW,ANS
          MATCH     ANSC,ANS
          GOTO      KPTY9999 IF EQUAL       * cancel
          MATCH     ANSN,ANS
          GOTO      KPTY0000 IF EQUAL       * no
          MATCH     ANSY,ANS
          GOTO      KPTY2000 IF EQUAL       * yes
          BEEP
          GOTO      KPTY1000
.
.         update the new priority
.
KPTY2000  MOVE      OTRLOUTN,KEY8
          CALL      RDOTRFL1
          MOVE      CODEPRTY,OTRLPRIO       * set the priority
          CALL      UPOTRFL1
.
          CALL      WRRHIS00                * write to history
.
KPTY9999  RETURN
+
.*********************************************************************
.*                  PRFL0000                    Called by : DRFL8500 *
.*        Print all the letters without a priority for the cons.     *
.*********************************************************************
.
PRFL0000  MOVE      ZERO,CPAGENO             * set page
          CLOCK     TIME,CTIMEIS             * get current time
          CALL      HEAD0000
          MOVE      ZERO,CNTITEM             * clear count
.
          IF        MODE = 1
            PACK      KEY22,CODECONS,SP30    * position on clinic id
            CALL      RSOTRFL5
          ELSE
            PACK      KEY22,CLINTYPE,SP30    * position on clinic type
            CALL      RSOTRFL7
          ENDIF
.
PRFL1000  IF        MODE = 1
            CALL      RKOTRFL5              * read next record
          ELSE
            CALL      RKOTRFL7              * read next record
          ENDIF
          BRANCH    OVRCD,PRFL5000          * end of file
.
          IF        MODE = 1
            MATCH     SP6,CODECONS          * blank clinic id ?
          ELSE
            MATCH     SP6,CLINTYPE          * blank clinic type ?
          ENDIF
          GOTO      PRFL1100 IF EQUAL       * yes - print all
.
          IF        MODE = 1
            MATCH     CODECONS,OTRLCONS     * same clinic id ?
          ELSE
            MATCH     CLINTYPE,OTRLCTYP     * same clinic type ?
          ENDIF
          GOTO      PRFL5000 IF NOT EQUAL   * no - finished
.
PRFL1100  MATCH     SP3,OTRLPRIO
          GOTO      PRFL1000 IF NOT EQUAL   * already have a priority
.
          COMPARE   ZERO,OTRLTREF
          GOTO      PRFL1000 IF NOT EQUAL   * Not Outpatient Referral
.
. ******* print the data *******
.
PRFL2000  COMPARE   "55",CLNO 
          CALL      HEAD0000 IF NOT LESS    * need a new page
.
          CALL      GPAT0000                * get patient details
          ADD       ONE,CNTITEM             * add to item counter
          PRINT     *1,CNTITEM,".  ",CPCDATE,SP3,PATNAME,SP3,DIAGNOS
          DISPLAY   *P50:24,*V2LON,OTRLOUTN
          ADD       ONE,CLNO
          GOTO      PRFL1000
.
. ******* no more data to display *******
.
PRFL5000  COMPARE   ZERO,CNTITEM
          GOTO      PRFL5500 IF NOT EQUAL   * no data displayed
.
          PRINT     *N,"There are no letters without a priority."
          GOTO      PRFL6000
.
PRFL5500  PRINT     *N,"Number of letters without a Priority : ",CNTITEM
.
PRFL6000  PRINT     *N,"*** End of Report ***",*N
.
PRFL9999  RETURN
+
.*********************************************************************
.*                  HEAD0000                    Called by : PRFL0000 *
.*        Print a new page heading                                   *
.*********************************************************************
HEAD0000  CALL      HEAD80
          PRINT     *4,"Clinic Id  : ",CODECONS,SP2,DESCCONS
          PRINT     *N,*4,"Date of Letter":
                    *19,"Patients Name            ":
                    *47,"Diagnosis                     ":
                    *N,*4,"--------------":
                    *19,"-------------------------":
                    *47,"------------------------------"
          MOVE      "8",CLNO
.
HEAD9999  RETURN
+
.
.         I/O includes
.
          INC       STD001IO/PBL
          INC       KYOUTCLI/PBL            * keyin OP clinic
          INC       OUTDSCLN/PBL            * ? for clinic file
          INC       OUTDSCTY
          INC       OUTCLIIO/INC            * clinic file
          INC       OUTCTYIO/INC            * clinic type file
          INC       OUTRF1IO/INC            * OP referral letters file
          INC       PATCODIO/INC            * codes file
          INC       PATDO1IO/INC            * doctors file
          INC       PMSHCPIO/INC            * National HCP file
          INC       PATCODKY/PBL            * ? for codes file
          INC       PATMA1IO/INC            * master file
          INC       PATPR1IO/INC            * pre-admission file
          INC       OUTHISIO/INC 
          INC       WRTHISRF     
...............................................................................

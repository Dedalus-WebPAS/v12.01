.******************************************************************************
.* System    :  WAITING LIST                                                  *
.* Program   :  IBAWAT28                                                      *
.* Desc      :  Patient Removal Report                                        *
.******************************************************************************
.* Author    :  N.S.                                                          *
.* Date      :  18/10/1988                                                    *
.* Mods      :                                                                *
.******************************************************************************
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899                         *
.*           Recompiled as WATTR1FD changes                                   *
.******************************************************************************
.*        V11.00.01 10/03/2020  Jill Parkinson Task 0879163                   *
.*                  Added SEXDSCIO include                                    *
.*        V10.05.01 05/01/2015  Ebon Clements CAR 261630                      *
.*                  Removed port number from temp file name                   *
.*        V9.04.01  01/03/2005  Peter Vela    CAR 57547                       *
.*                  Recompiled for PATVADFD and PATVADIO                      *
.*        V9.03.01  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2      *
.*                  - Add Sex Description routine SEXDSC00 with Code "R" -    *
.*                  "Intersex" added                                          *
.*        V5.08.02  01/09/2000  Tonii                                         *
.*                  Mods to accept Unknown and Indeterminate as valid patient *
.*                  sex description                                           *
.*        V5.08.01  29/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.07.02  27/10/1999 J Habasque                                     *
.*                  Recompiled for changes to WATPROFD - Active/Inactive codes*
.*        V5.07.01  16/08/1999 J.Tan                                          *
.*                  Added transfer destination description                    *
.*        V5.00     23/05/1989  MO                                            *
.*                  Changes in report format                                  *
.*        V5.01     03/06/1989  S.Barcham         SRF 100809                  *
.*                  Recompiled for locking in PATIOMAS                        *
.*        V5.02     15/06/1989  M.Ohleiter        SRF 100926                  *
.*                  Included '?' option for unit and doctor                   *
.*        V5.01.01  19/06/1989  M.Ohleiter                                    *
.*                  Changes for release 5.01                                  *
.*                  09/07/1989  J.Clark                                       *
.*                  Added Key26 variable added to PATIODOC by SOG             *
.*        V5.01.02  08/09/1989  K.Peak                                        *
.*                  Added priority code                                       *
.*        V5.01.03  11/09/1989  M.Ohleiter                                    *
.*                  Changed removal code to WR, fixed summary page            *
.*        V5.01.04  23/02/1990  E.Phan                                        *
.*                  Recompiled for WATPROFD                                   *
.*        V5.01.10  01/08/1990  Bill Dew                                      *
.*                  Update IBAWAT28 to Standards and upgrade to new waiting   *
.*                  list standards, In otherwords slash and hack code into    *
.*                  well structured modular code.  Delete and rewrite, show   *
.*                  no mercy to poor code.                                    *
.*                  03/08/1990  Justin Coates                                 *
.*                  Added use of WTCNTPUC                                     *
.*                  13/08/1990  Justin Coates                                 *
.*                  Fixed the change of unit-doctor in the report as it was   *
.*                  stuffed                                                   *
.*                  17/09/1990  Bill Dew                                      *
.*                  Fixed EXTR0000 to check date with WMDATE4 not with WMDATE1*
.*        V5.01.121 13/11/1990  i chung           SRF 106963                  *
.*                  Changed category for removal reasons from "BC" to "WR"    *
.*        V5.01.122 27/11/1990  Glen Hobby                                    *
.*                  Recompiled for changes to PATMX1FD                        *
.*        V5.01.123 22/02/1993  Justin Coates                                 *
.*                  Allow for status 8 - not performed and removed            *
.*        V5.01.124 02/03/93  Neeriem "I Hate Support" Dye                    *
.*                  Reformat print for new address lengths                    *
.*        V5.01.125 27/03/1993  i chung           (Dudley change)             *
.*                  Added user security check                                 *
.*        V5.01.126 26/05/1994 Ian Rutt                                       *
.*                  Fixed Global Recompile Bugs                               * 
.******************************************************************************
.*        V5.04.01  26/06/1997  Howellsy         505                          *
.*                  Use PATDOCKY instead of PATDOCDS                          *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
.
          INC       IBASEQFD/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATDO1FD/INC
          INC       PATMA1FD/INC
          INC       TFILEVAR/INC
          INC       WATCONFD/INC
          INC       WATRTDFD/INC            * Transfer destination file
          INC       WATPROFD/INC
          INC       WATSEAFD/INC
          INC       WATSEIFD/INC
          INC       WATTR1FD/INC
          INC       WEBERRFD/INC
+
.******************************************************************************
.                   TEMPORARY FILES DECLARATION
.******************************************************************************
.
.   Tempfile format for storing patient removal details
.
INDXINFO  DIM       30
WATTMP1   IFILE     SQL, FIXED=220, KEY=INDXINFO
FILEKEY1  INIT      "U1-3,10-17,18-26,27-28"
FILEKEY2  INIT      "U4-9,10-17,18-26,27-28"
.
.WMUNIT   DIM       3         1
.WMDOCTOR DIM       6         4
.WMURNO   DIM       8         10
.WMCODE   DIM       9         18
.DWMCNT   DIM       2         27
.WMDESC   DIM       50        29
.DATE10   DIM       8         79
.DATE20   DIM       8         87
.DATE40   DIM       8         95
.WMPTY    DIM       3         103
.WMSTAY   FORM      5    4    106
.WMESTL   FORM      5    4    111
.WMREMOVE DIM       3         116
.End of Record marker 1       119 
.Total                        220
.
.
.    Tempfile details for cancellation summary report
.
.    1-3    Unit / Clinic code
.    4-9    Doctor Code
.    10-12  Cancellation code
.
WATTMP2   IFILE     SQL, FIXED=18, KEY=INDXINFO
FILEKEY3  INIT      "u1-3,10-12"
FILEKEY4  INIT      "u4-9,10-12"
+
.******************************************************************************
.                              WORK VARIABLES
.******************************************************************************
ADDRESS   DIM       50
CANCCODE  DIM       3          * cancellation code for tempfile  
CANCNUMB  FORM      8          * number of cancellation for canccode
CODE      DIM       2
COL       FORM      2
.
DATE1     DIM       10         * DD/MM/YYYY From
DATE2     DIM       10         * DD/MM/YYYY To
DELCOMM   DIM       30         * Dos command to delete tempfile
DESCUNIT  DIM       20         * description unit (default = ALL)
DIM3      DIM       3
DIM31     DIM       31         * store wmdesc for print
DOCT30    DIM       30
DOCCODE   DIM       6 
DOCTNAME  DIM       30
DOCTOR    DIM       6          * doctor code keyed in
DOSCMND   DIM       60         * Dos command to isbuild tempfile
.
FROMDATE  DIM       8          * FROMdate ccyymmdd
GRANDTOT  FORM      8
.
LASTCODE  DIM       6          * last code printed
LINE80    DIM       80
.
NORMDATE  DIM       10
.
PNAME     DIM       50         * patient name to print (surname title givname)
PATDOB    DIM       10         * patient DOB to print
PRNTCODE  DIM       6          * printing code 
PRNTDESC  DIM       30         * printing code description
.
REASON    DIM       14         * store tdesc (reason for removal)
.
SCRNFLAG  FORM      1
SEXDESC   DIM       10         * sex description (M/F)
SUMMDOCT  DIM       6          * summary doctor code
SUMMFLAG  FORM      1          * removal details or summary heading in HEAD0000
SUMMUNIT  DIM       3          * summary unit 
.
TEMPNAM1  DIM       8          * Physical name for tempfile
TEMPNAM2  DIM       8          * Physical name for tempfile
TODATE    DIM       8          * To Date ccyymmdd
TODAY     DIM       8          * current date
TOTAL     FORM      8
.
UNITCLIN  DIM       3          * unit keyed in
VERT      FORM      2
+
.******************************************************************************
.                              CONSTANTS
.******************************************************************************
CODETP    INIT      "TP"       * category TP (priority)
CODEWR    INIT      "WR"       * catagory WR (reason for removal)
CODEWU    INIT      "WU"       * catagory WU (unit)
.
MENU1     INIT      "Unit/Clinic Code Sequence"
MENU2     INIT      "Doctor Code Sequence"
.
TEMPSIZ1  INIT      " 220 "    * Please leave a space infront of number
TEMPSIZ2  INIT      " 18 "     * Please leave a space infront of number
.
Z3        INIT      "ZZZ"
Z6        INIT      "ZZZZZZ"
Z9        INIT      "ZZZZZZZZZ"
.
PRGID     INIT      "IBAWAT28"
PRGNAM    INIT      "Patient Removal Report"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.                           MAINLINE Rock & Roll
.**************************************************************************
ML0000    CALL      INIT0000           * open files     
ML1000    CALL      MENU0000           * select options
.
ML2000    CALL      CLRV0000
          BRANCH    MOPTION,ML3000,ML4000
          GOTO      ML9000             * exit chosen
.
ML3000    CALL      REDP1000
          CALL      DRNG0000           * keyin from/to date
          CALL      GUNT0000           * get unit/clinic code
          BRANCH    EXIT,ML1000        * Exit character entered
          GOTO      ML5000
.
ML4000    CALL      REDP1000
          CALL      DRNG0000           * keyin from/to date
          CALL      GDOC0000           * get doctor code
          BRANCH    EXIT,ML1000        * Exit character entered
.
ML5000    CALL      CONTQST            * ok to continue ?
          BRANCH    CEXIT,ML6000,ML2000,ML1000
.
ML6000    CALL      TEMP0000           * create tempfile
          CALL      EXTR0000           * read wattrefd write watmp1
          BRANCH    EXIT,ML1000
          CALL      PPRR0000           * print patient removal report
          GOTO      ML1000
.
ML9000    CALL      DELTEMP1           * delete the temp file
          CALL      DELTEMP2           * delete the temp file
.
ML9999    CHAIN     PGM
+
.***************************************************************************
.*        Initialisation                                                   *
.***************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,FIFTY9;*220,PTCNUADT
          READ      CONTROLF,THIRTY7;*122,WTCNTPUC,*211,WTCNWLSC
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"watproaf";
          OPEN      WATPROA1,"watproaf"
.
          IF        WTCNWLSC = 1
            DISPLAY   *P64:24,"watseaaf";
            OPEN      WATSEAA1,"watseaaf"
            DISPLAY   *P64:24,"watseiaf";
            OPEN      WATSEIA2,"watseiaf"
          ENDIF
.
          DISPLAY   *P64:24,"wattr1af";
          OPEN      WATTR1A1,"wattr1af"
          OPEN      WATTR1A2,"wattr1af"
          OPEN      WATTR1A3,"wattr1af"
.
          MOVE      ONE,CNEWDS         * set new ? routine display format
.
          CALL      TFILENAM * Get temp file name in create
          MOVE      TFILNAME,TEMPNAM1  * setup removal details
.
          CALL      TFILENAM * Get temp file name in create
          MOVE      TFILNAME,TEMPNAM2   * setup removal summary
.
INIT9999  RETURN
+
.**************************************************************************
.                                 MENU0000
.    Menu driver routine
.*****************************************************************************
MENU0000  HLINE     *G37,2,51,80
          DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = ",MENU1:
                    *P1:6,*V2LON,"2",*HOFF," = ",MENU2:
                    *P1:8,"Select Option : ";
.
MENU1111  KEYIN     *P17:8,*EL,*V2LON,MOPTION;
          COMPARE   ZERO,MOPTION
          GOTO      MENU9999 IF EQUAL
          BRANCH    MOPTION,MENU9000,MENU9000
          BEEP
          GOTO      MENU1111
.
MENU9000  LOAD      LINE80,MOPTION,MENU1,MENU2   * display sub-heading
          LOAD      CPHDROPT,MOPTION,MENU1,MENU2 * print sub-heading
          DISPLAY   *P51:2,*V2LON,"- ",*+,LINE80,SP1;
.
MENU9999  RETURN
+
.******************************************************************************
.                                 CLRV0000
.******************************************************************************
CLRV0000  UNPACK    SP30,DATE1,DATE2
          UNPACK    SP30,UNITCLIN,DESCUNIT
          MOVE      SP6,DOCTOR
          MOVE      SP30,DOCTNAME
          RETURN
+
.******************************************************************************
.                                 DRNG0000
.    Get date range of date on list
.******************************************************************************
DRNG0000  MOVE      ZERO,CHIGHLT
          UNPACK    SP20,FROMDATE,TODATE
          MOVE      "21",CCOL               * set up position for keydate
          MOVE      "12",CVERT
.
DRNG1000  UNPACK    SP8,CDAY,CMON,CCENT,CYEAR
          MOVE      CCC,CCENT
          CALL      KEYDATE                 * get start date
          BRANCH    CQUEST,DRNG1000
.
          COMPARE   ZERO,OVRCD              * test for return hit
          GOTO      DRNG1100 IF EQUAL
          MOVE      "Start    ",DATE1       * set start if return hit
          DISPLAY   *PCCOL:CVERT,*V2LON,DATE1;
          GOTO      DRNG2000
.
DRNG1100  PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY
          REPLACE   " 0",FROMDATE
          CALL      PACDATE
          PACK      DATE1,CPCDATE,SP10
          REPLACE   " 0",DATE1
.
DRNG2000  MOVE      "13",CVERT         * line for keydate
.
DRNG2100  UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          CALL      KEYDATE            * get second date range
          BRANCH    CQUEST,DRNG2100
.
          COMPARE   ZERO,OVRCD         * test for return hit
          GOTO      DRNG2200 IF EQUAL
          MOVE      "99999999",TODATE  * set filedate to highest value
          MOVE      "Finish    ",DATE2
          DISPLAY   *PCCOL:CVERT,*V2LON,DATE2;
          GOTO      DRNG3000
.
DRNG2200  PACK      TODATE,CCENT,CYEAR,CMON,CDAY
          REPLACE   " 0",TODATE
          CALL      PACDATE
          PACK      DATE2,CPCDATE,SP10
          REPLACE   " 0",DATE2
.
DRNG3000  MATCH     FROMDATE,TODATE      * test dates are correct sequence
          GOTO      DRNG9999 IF NOT LESS
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** To Date must be greater than or equal From Date **   ";
          CALL      HITENTER
          GOTO      DRNG2000
.
DRNG9999  RETURN
+
.*****************************************************************************
.                                 GUNT0000
.    get unit / clinic code
.*****************************************************************************
GUNT0000  MOVE      ONE,SCRNFLAG            * set the redisplay flag
.
GUNT1000  DISPLAY   *P21:15,"___",*P21:15;
.
GUNT2000  KEYIN     *V2LON,UNITCLIN         * get unit clinic code  
          ENDSET    UNITCLIN
          APPEND    SP3,UNITCLIN
          RESET     UNITCLIN
.
          MATCH     SP3,UNITCLIN            * test for return hit
          GOTO      GUNT7000 IF EQUAL
          CMATCH    EXITCHAR,UNITCLIN       * test for return hit
          GOTO      GUNT8000 IF EQUAL
.
          CMATCH    "?",UNITCLIN            * test for ? routine
          GOTO      GUNT4000 IF NOT EQUAL
          PACK      CODE,ANSW,ANSU
          CALL      PATCODDS
          MOVE      ZERO,SCRNFLAG           * signal requiring redisplay
.
GUNT3000  DISPLAY   *P1:24,*EL,"Unit/Clinic : ___",*P15:24;
          GOTO      GUNT2000
.
GUNT4000  PACK      KEY5,ANSW,ANSU,UNITCLIN,SP5
          CALL      RDCODE1
          BRANCH    OVRCD,GUNT9000
          CALL      REDP0000                * do redisplay if needed
          DISPLAY   *P21:15,*V2LON,UNITCLIN,*HOFF,SP4,TDESC;
          MOVE      TDESC,DESCUNIT
          MOVE      ZERO,EXIT               * if alls well EXIT with 0
          GOTO      GUNT9999
.
GUNT7000  BRANCH    SCRNFLAG,GUNT7100
          CALL      REDP0000  
.
GUNT7100  DISPLAY   *P21:15,*V2LON,*EL,"ALL Unit/Clinic codes";
          MOVE      ZERO,EXIT               * if alls well EXIT with 0
          GOTO      GUNT9999
.
GUNT8000  MOVE      ONE,EXIT                * exit=1 if return hit
          GOTO      GUNT9999
.
GUNT9000  DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Unit/Clinic code not on file **    ";
          CALL      HITENTER
          BRANCH    SCRNFLAG,GUNT1000
          GOTO      GUNT3000
.
GUNT9999  RETURN
+
.******************************************************************************
.                                 GDOC0000
.                             get doctor code
.*****************************************************************************
GDOC0000  DISPLAY   *P21:15,"______"
.
          MOVE      SP10,DOCTOR
          MOVE      "21",ECOL
          MOVE      "15",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          CALL      PATDOCKY
          BRANCH    EXIT,GDOC7000,GDOC8000
.
          MOVE      DCODE,DOCTOR
.
          PACK      KEY6,DOCTOR,SP6
          CALL      DOCN0000
          MOVE      PACFNAME,DOCTNAME
          DISPLAY   *P21:15,*V2LON,DOCTOR,*HOFF,SP4,DOCTNAME;
          MOVE      ZERO,EXIT          * if alls well EXIT with 0
          GOTO      GDOC9999
.
GDOC7000  DISPLAY   *P21:15,*V2LON,*EL,"ALL Doctors";
          MOVE      ZERO,EXIT          * if alls well EXIT with 0
          GOTO      GDOC9999
.
GDOC8000  MOVE      ONE,EXIT           * exit=1 if return hit
          GOTO      GDOC9999
.
GDOC9999  RETURN
+
.**************************************************************************
.                                 DOCN0000
.    Setup doctors name in PACFNAME
.*****************************************************************************
DOCN0000  CALL      RDDOCT1
          BRANCH    OVRCD,DOCN9990     * tell user his code not on file
.
DOCN1111  MOVE      TWO,PACFRMT        * title name surname
          MOVE      DSNAME,PACSNAME    * use pacname to fix doctors name
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          CALL      PACNAME
          MOVE      ZERO,EXIT
          GOTO      DOCN9999
.
DOCN9990  MOVE      ONE,EXIT
          PACK      PACFNAME,SP30,SP30
.
DOCN9999  RETURN
+
.**************************************************************************
.                                 REDP0000
.**************************************************************************
REDP0000  BRANCH    SCRNFLAG,REDP9999
.
          DISPLAY   *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = ",MENU1:
                    *P1:6,*V2LON,"2",*HOFF," = ",MENU2:
                    *P1:8,"Select Option : ",*V2LON,MOPTION;
.
REDP1000  DISPLAY   *P1:12,"From Date on List : ",*V2LON,DATE1,*HOFF:
                    *P1:13,"To   Date on List : ",*V2LON,DATE2;
.
          BRANCH    MOPTION,REDP2000,REDP3000
.
REDP2000  DISPLAY   *P1:15,*EL,"Unit/Clinic Code  : ";
          GOTO      REDP9999
.
REDP3000  DISPLAY   *P1:15,*EL,"Doctor Code       : ";
.
REDP9999  RETURN
+
.*****************************************************************************
.                                 TEMP0000
.    setup TEMP file for stats and storage
.*****************************************************************************
TEMP0000  DISPLAY   *P1:24,*EL,*V2LON,"Creating temporary work file.  ":
                           *BLINKON,"Please wait...";
.
          CALL      DELTEMP1           * delete the temp file
          CALL      DELTEMP2           * delete the temp file
.
.    Setup tempfile for removal details
.
          LOAD      INDXINFO,MOPTION,FILEKEY1,FILEKEY2
.
          CLEAR     DOSCMND
          APPEND    "isbuild ",DOSCMND
          APPEND    TEMPNAM1,DOSCMND   * set the name of tempfile
          APPEND    TEMPSIZ1,DOSCMND   * set the tempfile size
          APPEND    INDXINFO,DOSCMND 
          RESET     DOSCMND 
.
          EXECUTE   DOSCMND,TASKID     * temp index file for item code order
          OPEN      WATTMP1,TEMPNAM1   * open the tempfile file pointer
.
.         setup tempfile for removal summary
.
          LOAD      INDXINFO,MOPTION,FILEKEY3,FILEKEY4
.
          CLEAR     DOSCMND
          APPEND    "isbuild ",DOSCMND
          APPEND    TEMPNAM2,DOSCMND   * set the name of tempfile
          APPEND    TEMPSIZ2,DOSCMND   * set the tempfile size
          APPEND    INDXINFO,DOSCMND 
          RESET     DOSCMND 
.
          EXECUTE   DOSCMND,TASKID     * temp index file for item code order
          OPEN      WATTMP2,TEMPNAM2   * open the tempfile file pointer
.
TEMP9999  RETURN
+
.*****************************************************************************
.
DELTEMP1  CLOSE     WATTMP1
          CLEAR     DELCOMM
          APPEND    "iserase ",DELCOMM 
          APPEND    TEMPNAM1,DELCOMM 
          RESET     DELCOMM 
          EXECUTE   DELCOMM,TASKID
          RETURN
+
.
DELTEMP2  CLOSE     WATTMP2
          CLEAR     DELCOMM
          APPEND    "iserase ",DELCOMM 
          APPEND    TEMPNAM2,DELCOMM 
          RESET     DELCOMM 
          EXECUTE   DELCOMM,TASKID
          RETURN
+
.****************************************************************************
.                                EXTR0000
.    This extraction routine created 01/08/1990 by Bill Dew.  The previous
.    extraction routine was so rooted it didnt even work.  How can anyone
.    program such shit, again its my job to make it right.
.
.    First read wattr1af for records with WMSTAT=6 removed and read patient
.    details patmasaf and transfer data to tempfile for later printing.
.    I've used KEY30 as the tempfile key regardless of the sequence.  Trust
.    me I'm one of the very few who know what their doing.
.   
.    Meanwhile : tally the number of patients removed in respect to removal
.    code and store in summary file.  
.****************************************************************************
EXTR0000  DISPLAY   *P1:24,*EL,*V2LON,*P28:24,"***  Extracting Data  ***",*W:
                    *HOFF,*P16:24,*EL,"U/R Number : ",*P47:24,"Scanning : ";
          MOVE      ZERO,TOTAL              * zero the record counter
          PACK      KEY20,SIX,SP20
          CALL      RDSTREA2                * indexed on WMSTAT
.
EXTR1000  CALL      RDKTREA2
          BRANCH    OVRCD,EXTR8000
          DISPLAY   *P58:24,*EL,WMURNO;
.
          COMPARE   SIX,WMSTAT
          GOTO      EXTR1100 IF EQUAL       * removed
          COMPARE   EIGHT,WMSTAT
          GOTO      EXTR8000 IF NOT EQUAL   * not performed and removed
.
EXTR1100  MATCH     FROMDATE,WMDATE1
          GOTO      EXTR1000 IF LESS        * DATE1 < From Date
.
          MATCH     WMDATE1,TODATE
          GOTO      EXTR1000 IF LESS        * DATE1 > To Date
.
          PACK      KEY8,WMURNO             * U/R Number as key
          CALL      RDMAST1                 * read master file
          BRANCH    OVRCD,EXTR1000          * invalid U/R Number
.
.         check if user has security access to this record
.
          COMPARE   ONE,WTCNWLSC
          GOTO      EXTR2000 IF NOT EQUAL
.
          PACK      KEY4,PASSCODE
          CALL      RDWTSEA1
          COMPARE   ZERO,OVRCD
          GOTO      EXTR2000 IF EQUAL       * ALL access
          PACK      KEY13,PASSCODE,WMDOCTOR,WMUNIT
          CALL      RDWTSEI2
          BRANCH    OVRCD,EXTR1000          * no access
.
EXTR2000  BRANCH    MOPTION,EXTR3000,EXTR4000
.
. ------ check on clinics ------
.
EXTR3000  MATCH     SP3,UNITCLIN
          GOTO      EXTR3100 IF EQUAL       * test print all units
          MATCH     UNITCLIN,WMUNIT
          GOTO      EXTR1000 IF NOT EQUAL
.
EXTR3100  PACK      KEY12,WMUNIT,WMREMOVE,WMDOCTOR
          CALL      WSUM0000                * write summary file
          PACK      KEY12,WMUNIT,Z9
          CALL      WSUM0000                * write unit total
          PACK      KEY12,Z3,WMREMOVE,Z6
          CALL      WSUM0000                * write remove code total 
.
          PACK      KEY30,WMUNIT,WMURNO,WMCODE,WMCNT
          GOTO      EXTR5000
.
. ------ check on doctors ------
.
EXTR4000  MATCH     SP6,DOCTOR              * test print all doctors
          GOTO      EXTR4100 IF EQUAL
          MATCH     DOCTOR,WMDOCTOR         * else test if user required doctor 
          GOTO      EXTR1000 IF NOT EQUAL
.
EXTR4100  PACK      KEY12,WMDOCTOR,WMREMOVE,WMUNIT
          CALL      WSUM0000                * write summary file
          PACK      KEY12,WMDOCTOR,Z9
          CALL      WSUM0000
          PACK      KEY12,Z6,WMREMOVE,Z3
          CALL      WSUM0000
.
          PACK      KEY30,WMDOCTOR,WMURNO,WMCODE,WMCNT
.
EXTR5000  RESET     KEY30
          MOVE      WMCNT,KEY2
          WRITE     WATTMP1,KEY30;WMUNIT,WMDOCTOR,WMURNO,WMCODE,KEY2,WMDESC:
                                  WMDATE1,WMDATE2,WMDATE4,WMPTY,WMSTAY,WMTIME:
                                  WMREMOVE
          ADD       ONE,TOTAL               * increment the record counter
          DISPLAY   *P29:24,*V2LON,WMURNO;
          GOTO      EXTR1000
.
EXTR8000  MOVE      ZERO,EXIT
          COMPARE   ZERO,TOTAL              * test any records saved to tempfile
          GOTO      EXTR9999 IF NOT EQUAL
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "***  No patients removed in selected range  ***   ";
          CALL      HITENTER
          MOVE      TRUE,EXIT
.
EXTR9999  RETURN
+
.****************************************************************************
.                                WSUM0000
.                         Write summary record
.****************************************************************************
WSUM0000  BRANCH    MOPTION,WSUM1111,WSUM2222
.
WSUM1111  UNPACK    KEY12,SUMMUNIT,CANCCODE,SUMMDOCT
          PACK      KEY9,SUMMUNIT,CANCCODE
          GOTO      WSUM3333
.
WSUM2222  UNPACK    KEY12,SUMMDOCT,CANCCODE,SUMMUNIT
          PACK      KEY9,SUMMDOCT,CANCCODE
.
WSUM3333  RESET     KEY9
          READ      WATTMP2,KEY9;SUMMUNIT,SUMMDOCT,CANCCODE,CANCNUMB
          GOTO      WSUM5555 IF OVER
          ADD       ONE,CANCNUMB
          UPDATE    WATTMP2;SUMMUNIT,SUMMDOCT,CANCCODE,CANCNUMB
          GOTO      WSUM9999
.
WSUM5555  MOVE      ONE,CANCNUMB
          WRITE     WATTMP2,KEY9;SUMMUNIT,SUMMDOCT,CANCCODE,CANCNUMB
.
WSUM9999  RETURN
+
.****************************************************************************
.                                 PPRR0000
.                       Print patient removal report
.****************************************************************************
PPRR0000  MOVE      ZERO,SUMMFLAG      * set removal detail heading for HEAD000
          MOVE      ZERO,TOTAL         * zero patient counter
          MOVE      ZERO,GRANDTOT      * zero grand total patient counter
          MOVE      SP6,LASTCODE       * blank the previous unit variable
          MOVE      ZERO,CPAGENO       * initialize the page counter
          MOVE      ONE,CNOUNDLN
.
          PACK      KEY30,SP30
          READ      WATTMP1,KEY30;;
.
. ------ next read on temp file ------
.
PPRR2222  
          READKS    WATTMP1;WMUNIT,WMDOCTOR,WMURNO,WMCODE,KEY2,WMDESC,WMDATE1:
                            WMDATE2,WMDATE4,WMPTY,WMSTAY,WMTIME,WMREMOVE
          GOTO      PPRR9000 IF OVER
          MOVE      KEY2,WMCNT
          CALL      TPPH0000           * test print page header
.
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PACK      KEY5,CODEWR,WMREMOVE
          CALL      CODE0000           * get removal description
          MOVE      TDESC,REASON
.
          PACK      KEY19,WMURNO,WMCODE,KEY2
          PROC      DTRNDEST               * to get the transfer destination
          MOVE      TDESC,REASON
.
          CALL      RDPD0000           * setup patient details
.
. ------ print the first line about patient ------
.
          MOVE      WMDESC,DIM31       * DIM31 is truncated procedure desc
          UNPACK    WMDATE1,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PACK      NORMDATE,CPCDATE,SP10
          REP       " 0",NORMDATE
          PRINT      *1,"|",WMURNO,*11,PNAME,*61,"| ",DIM31:
                    *94,"| Added    : ",NORMDATE,*132,"|"
.
. ------ print the second line about patient ------
.
          PACK      KEY6,WMDOCTOR,SP6
          CALL      DOCN0000           * get doctor's name
          MOVE      PACFNAME,KEY20
          UNPACK    WMDATE2,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PACK      NORMDATE,CPCDATE,SP10
          REP       " 0",NORMDATE
          PRINT      *1,"|",*11,ADDRESS,*61,"| ",KEY20:
                    *94,"| Review   : ",NORMDATE,*132,"|"
.
. ------ print the third line about patient ------
.
          PACK      KEY5,ANST,ANSP,WMPTY
          CALL      CODE0000
          MOVE      TDESC,KEY20        * save priority description in KEY20
          PACK      KEY5,ANSW,ANSU,WMUNIT
          CALL      CODE0000
          PRINT     *1,"|",*11,"Home ",PTELEP,SP1,"Bus ",PTELEB,*61,"| Unit : ":
                    TDESC,*94,"| Priority : ",WMPTY,SP2,KEY20,*132,"|"
.
. ------ print the fourth line about patient ------
.
          UNPACK    WMDATE4,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PACK      NORMDATE,CPCDATE,SP10
          REP       " 0",NORMDATE
          PRINT      *1,"|",*11,"Sex  : ",SEXDESC,*34,"D.O.B.   : ",PATDOB:
                    *61,"| Stay : ",WMSTAY,SP1,"Proc. Time :",WMTIME:
                    *94,"| Cancelled: ",NORMDATE,SP1,REASON,*132,"|"
.
          CALL      PRNTLINE                * print the underline
          ADD       FOUR,CLNO
          ADD       ONE,TOTAL               * add one to patient counter
          ADD       ONE,GRANDTOT
          GOTO      PPRR2222
.
. ------ end of temp file reached ------
.
PPRR9000  CALL      PEND0000                * Print ENDing
          DISPLAY   *P1:24,*EL,*V2LON,*P22:24:
                    "***  Report Generation Completed  ***";
          MOVE      "PATIENT REMOVAL REPORT  ",PRGNAM * name for display
.
PPRR9999  RETURN
+
.*****************************************************************************
.                                 PEND0000
.                             Print End report
.*****************************************************************************
PEND0000  BRANCH    MOPTION,PEND1000,PEND2000
.
. ------ obtain the previous units description ------
.
PEND1000  MOVE      SP20,TDESC              * default if read fails
          PACK      KEY5,CODEWU,LASTCODE    * prev. unit as key
          CALL      RDCODE1                 * read codes file
          PRINT     *N,"Total Patients for Unit/Clinic ",LASTCODE,SP4,TDESC:
                    ": ",TOTAL
          MOVE      ZERO,TOTAL
          GOTO      PEND2100
.
. ------ obtain the previous doctors description ------
.
PEND2000  PACK      KEY6,LASTCODE           * prev. doct as key
          CALL      DOCN0000                * format docs name
          MOVE      PACFNAME,DOCT30         * set up description
          PRINT     *N,"Total Patients for Doctor ",LASTCODE,SP4,DOCT30:
                    ": ",TOTAL
          MOVE      ZERO,TOTAL
.
PEND2100  CALL      PSUM0000           * print summary report
.
          LOAD      KEY6,MOPTION,UNITCLIN,DOCTOR
          MATCH     SP3,KEY6
          GOTO      PEND8000 IF NOT EQUAL
.
          MOVE      ONE,SUMMFLAG
          CALL      HEAD0000
.
          PACK      KEY9,Z3,SP9
          READ      WATTMP2,KEY9;;
PEND2222
          READKS    WATTMP2;SUMMUNIT,SUMMDOCT,CANCCODE,CANCNUMB
          GOTO      PEND8888 IF OVER
.
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PACK      KEY5,CODEWR,CANCCODE    * get unit description
          CALL      CODE0000
          PRINT     "| ",TDESC,*49,CANCCODE,*54,"|",*60,CANCNUMB,*80,"|"
          ADD       ONE,CLNO                * increment the printed line counter
          GOTO      PEND2222
PEND8000
          PRINT     *N,"Grand Total of Patients printed : ",GRANDTOT:
                    *N,*N,*N,"***  End of Report  ***"
          GOTO      PEND9999
PEND8888
          CALL      UND80
          PRINT     "| Grand Total",*54,"|",*60,GRANDTOT,*80,"|"
          CALL      UND80
          PRINT     *N,*N,"***  End of Report  ***"
.
PEND9999  RETURN
+
.*****************************************************************************
.         Get code description from codes file
.*****************************************************************************
CODE0000  MOVE      SP20,TDESC
          UNPACK    KEY5,TCODE,ACODE
          MATCH     SP3,ACODE
          GOTO      CODE9000 IF EQUAL
          MOVE      "Unknown",TDESC
          CALL      RDCODE1
          GOTO      CODE9999
.
CODE9000  MOVE      SP20,TDESC
.
CODE9999  RETURN
+
.*****************************************************************************
.                                 TPPH0000
.    Test Print Page Header
.    This routine checks to print a new page for each new unit/clinic or
.    doctor code and for a summary cancellation page
.*****************************************************************************
TPPH0000  MATCH     SP6,LASTCODE            * is it the first code ??
          GOTO      TPPH7777 IF EQUAL       * yes, so dont print prev. details
.
          BRANCH    MOPTION,TPPH2222,TPPH4444
.
. ------ check on previous unit/clinic ------
.
TPPH2222  MATCH     WMUNIT,LASTCODE
          GOTO      TPPH9999 IF EQUAL
.
. ------ obtain the previous units description ------
.
          MOVE      SP20,TDESC              * default if read fails
          PACK      KEY5,CODEWU,LASTCODE    * prev. unit as key
          CALL      RDCODE1                 * read codes file
          PRINT     *N,"Total Patients for Unit/Clinic ",LASTCODE,SP4,TDESC:
                    ": ",TOTAL
          MOVE      ZERO,TOTAL
          GOTO      TPPH5555
.
. ------ check on previous doctor ------
.
TPPH4444  MATCH     WMDOCTOR,LASTCODE
          GOTO      TPPH9999 IF EQUAL
.
. ------ obtain the previous doctors description ------
.
          PACK      KEY6,LASTCODE           * prev. doct as key
          CALL      DOCN0000                * format docs name
          MOVE      PACFNAME,DOCT30         * set up description
          PRINT     *N,"Total Patients for Doctor ",LASTCODE,SP4,DOCT30:
                    ": ",TOTAL
          MOVE      ZERO,TOTAL
.
. ------ print the summary details and update lastcode ------
.
TPPH5555  CALL      PSUM0000                * print summary report
          CALL      HEAD0000
          LOAD      LASTCODE,MOPTION,WMUNIT,WMDOCTOR * set up new lastcode value
          ENDSET    LASTCODE
          APPEND    SP6,LASTCODE
          RESET     LASTCODE
          GOTO      TPPH9999
.
. ------ on the first page so set up previous unit or doctor ------
.
TPPH7777  LOAD      LASTCODE,MOPTION,WMUNIT,WMDOCTOR * set up new lastcode value
          ENDSET    LASTCODE
          APPEND    SP6,LASTCODE
          RESET     LASTCODE
          CALL      HEAD0000
.
TPPH9999  RETURN
+
.*****************************************************************************
.                                 PSUM0000
.    Print cancellation summary report from tempfile 2
.         Modified 13/08/90 Justin "I Pooch Male Dogs" Coates
.                  removed the calls to HEAD0000 as this updated the values of
.                  prntcode and prntdesc which was not desired
.*****************************************************************************
PSUM0000  MOVE      ONE,SUMMFLAG            * set summary heading flag
.
. ------ new page details ------
.
          CALL      HEAD132
          PRINT     *40,"Date on List Range : ",DATE1," to ",DATE2,*N,*N
          CALL      UND80
          PRINT     "| Cancellation Reason",*49,"Code":
                    *54,"| Number of Cancellations |"
          CALL      UND80
          MOVE      FOUR,CLNO
.
          PACK      KEY9,LASTCODE,SP20
          READ      WATTMP2,KEY9;;          * positional read
.
PSUM2222  READKS    WATTMP2;SUMMUNIT,SUMMDOCT,CANCCODE,CANCNUMB
          GOTO      PSUM3333 IF OVER
.
          MATCH     CANCCODE,Z9             * test for total
          GOTO      PSUM4444 IF NOT EQUAL
.
. ------ print grand total for this unit or doctor ------
.
PSUM3333  CALL      UND80
          PRINT     "| Total ",PRNTCODE," - ",PRNTDESC,*54,"|":
                    *60,CANCNUMB,*80,"|"
          CALL      UND80
          GOTO      PSUM8888
.
PSUM4444  COMPARE   "55",CLNO
          GOTO      PSUM5555 IF LESS        * will fit on this page
.
. ------ new page details ------
.
          CALL      HEAD132
          PRINT     *40,"Date on List Range : ",DATE1," to ",DATE2,*N,*N
          CALL      UND80
          PRINT     "| Cancellation Reason",*49,"Code":
                    *54,"| Number of Cancellations |"
          CALL      UND80
          MOVE      FOUR,CLNO
.
. ------ print the total for single cancellation reason ------
.
PSUM5555  PACK      KEY5,CODEWR,CANCCODE
          CALL      CODE0000
          PRINT     "| ",TDESC,*49,CANCCODE,*54,"|",*60,CANCNUMB,*80,"|"
          ADD       ONE,CLNO                * increment the printed line counter
          GOTO      PSUM2222
.
PSUM8888  MOVE      ZERO,SUMMFLAG           * reset the removal detail header
.
PSUM9999  RETURN
+
.*****************************************************************************
.                                 HEAD000   
.    Do main page heading for both summary and details
.         Sets up the values of 'prntcode' and 'prntdesc' which is the current
.         unit/doctor code and description
.*****************************************************************************
HEAD0000  CLOCK     TIME,CTIMEIS
          MOVE      "PATIENT REMOVAL REPORT -",PRGNAM * name for printing
          CALL      HEAD132
          PRINT     *40,"Date on List Range : ",DATE1," to ",DATE2,*N
.
          MOVE      FIVE,CLNO
          BRANCH    SUMMFLAG,HEAD6666       * last page
.
          ADD       ONE,CLNO
          BRANCH    MOPTION,HEAD3333,HEAD4444
.
. ------ check on unit/clinic ------
.
HEAD3333  MATCH     SP3,UNITCLIN            * are we using all units ??
          GOTO      HEAD3666 IF NOT EQUAL   * no, using a single unit
.
          PACK      PRNTCODE,WMUNIT,SP6
          PACK      KEY5,CODEWU,PRNTCODE
          CALL      CODE0000
          MOVE      TDESC,PRNTDESC
          PRINT     "Unit/Clinic Code : ",PRNTCODE,SP6,PRNTDESC
          GOTO      HEAD5555
.
HEAD3666  PRINT     "Unit/Clinic Code : ",UNITCLIN,SP6,DESCUNIT
          MOVE      UNITCLIN,PRNTCODE
          MOVE      DESCUNIT,PRNTDESC
          GOTO      HEAD5555
.
. ------ check on doctor ------
.
HEAD4444  MATCH     SP6,DOCTOR              * are we using all doctors ??
          GOTO      HEAD4666 IF NOT EQUAL   * no, using a single doctor
.
          PACK      PRNTCODE,WMDOCTOR,SP6
          MOVE      PRNTCODE,KEY6
          CALL      DOCN0000
          MOVE      PACFNAME,PRNTDESC
          PRINT     "Doctor Code : ",PRNTCODE,SP6,PRNTDESC
          GOTO      HEAD5555
.
HEAD4666  PRINT     "Doctor Code : ",DOCTOR,SP6,DOCTNAME
          MOVE      DOCTOR,PRNTCODE
          MOVE      DOCTNAME,PRNTDESC
.
HEAD5555  CALL      PRNTLINE
          PRINT     "| U/R No",*11,"Personal Details",*61,"| Medical Details":
                    *94,"| Waiting List Details",*132,"|"
          ADD       ONE,CLNO
          CALL      PRNTLINE
          GOTO      HEAD9999
.
HEAD6666  PRINT     *N;
          CALL      UND80
          PRINT      *1,"| Cancellation Reason",*49,"Code":
                    *54,"| Number of Cancellations |"
          ADD       TWO,CLNO
          CALL      UND80
.
HEAD9999  RETURN
+
.****************************************************************************
PRNTLINE  PRINT       *1,"*---------------------------------------------":
                         "----------------------------------------------":
                         "---------------------------------------*"
          ADD         ONE,CLNO
          RETURN
+
.*****************************************************************************
.*                                RDPD0000                                   *
.*                          ReaD Patient Details                             *
.*****************************************************************************
RDPD0000  PACK      ADDRESS,SP30,SP30
          MOVE      SP10,PATDOB
          PACK      PTELEP,SP10
          PACK      PTELEB,SP10
          MOVE      SP10,SEXDESC
.
          MOVE      WMURNO,KEY8
          CALL      RDMAST1                 * read pat. details from master file
          BRANCH    OVRCD OF RDPD4000
          CALL      PATN0000                * set patient's name
.
RDPD1000  UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PACK      PATDOB,CPCDATE,SP10
          REP       " 0",PATDOB
.
.* ****************************************** Start of Changes         *C-49016
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          MOVE      DSEXDESC,SEXDESC
.* ******************************************   End of Changes         *C-49016
.
RDPD3000  CALL      ADDR0000  
          GOTO      RDPD9999
.
RDPD4000  DISPLAY   *P20:24,"OVERCODE ",WMURNO;
.
RDPD9999  RETURN
+
.**********************************************************************
.*                             ADDR0000                               *
.*                     pack patient's ADDRess                         *
.**********************************************************************
ADDR0000  PACK      ADDRESS,SP70
          CLEAR     ADDRESS
.
          MATCH     SP70,PADD1
          IF        !@EQUAL
            STRIP     PADD1
            APPEND    PADD1,ADDRESS
            APPEND    ",",ADDRESS
          ENDIF
.
          MATCH     SP70,PADD2
          IF        !@EQUAL
            STRIP     PADD2
            APPEND    PADD2,ADDRESS
            APPEND    ",",ADDRESS
          ENDIF
.
          MATCH     SP70,PSUBRB
          IF        !@EQUAL
            STRIP     PSUBRB
            APPEND    PSUBRB,ADDRESS
            APPEND    ",",ADDRESS
          ENDIF
.
          MATCH     SP70,PADD4
          IF        !@EQUAL
            STRIP     PADD4
            APPEND    PADD4,ADDRESS
            APPEND    " ",ADDRESS
          ENDIF
.
          MATCH     SP70,PPOST
          IF        !@EQUAL
            SQUEEZE   PPOST
            APPEND    PPOST,ADDRESS
          ENDIF
.
          RESET     ADDRESS
          RETURN
+
.*****************************************************************************
.                                 PATN0000
.                        set PATient's Name in PNAME
.*****************************************************************************
PATN0000  MOVE      TWO,PACFRMT             * title name surname
          MOVE      PSNAME,PACSNAME         * use pacdate to fix doctor's name
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          CALL      PACNAME
          MOVE      PACFNAME,PNAME
          MOVE      ZERO,EXIT
          GOTO      PATN9999
.
PATN9990  MOVE      ONE,EXIT
          PACK      PNAME,SP30,SP30
.
PATN9999  RETURN
+
.*****************************************************************************
.*        Heading for summary page                                           *
.*****************************************************************************
HEAL0000  CALL        HEAD0000
.
          PRINT        *1,"|",*3,WTCNTPUC,*55,"|",*57,"REASON",*97,"|":
                      *99,"NO. OF PATIENTS",*120,"|",*122,"TOTAL",*130,"|"
          RETURN
+
.*****************************************************************************
.
          INC       STD001IO       
.
          INC       PATCODKY
          INC       PATDOCKY
          INC       TRANDEST         * Transfer destination
          INC       TFILENAM
          INC       SEXDSCIO
.
          INC       IBASEQIO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATMA1IO/INC
          INC       WATPROIO/INC
          INC       WATTR1IO/INC
          INC       WATRTDIO/INC     * Transfer destination file
          INC       WATSEAIO/INC
          INC       WATSEIIO/INC
          INC       WEBERRIO/INC
+

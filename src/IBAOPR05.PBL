.******************************************************************************
.* System    :   OPERATION ROOMS                                              *
.* Program   :   IBAOPR05                                                     *
.* Desc      :   Operation Averages Maintenance                               *
.******************************************************************************
.* Date      :   18/12/89                                                     *
.* Author    :   MMO                                                          *
.* Mods.     :                                                                *
.*       V11.05.01  20/03/2025  Davin          TSK 0956210                    *
.*                  Add ICD10 Ed.13 - Go-live Date PTCNGDX3 (Sect.130 Pos.86) *
.*       V11.02.01  31/03/2022  Davin          TSK 0917793                    *
.*                  Add ICD10 Ed.12 - Go-live Date PTCNGDX2 (Sect.128 Pos.231)*
.*       V11.00.01  24/02/2020  J.Tan          TSK 0838262                    *
.*                  Added Effective from and to date to MBS Item file         *
.*       V10.10.01  17/03/2017  Davin          TSK 0833093     HDP 2017/2018  *
.*                  Add ICD10 Ed.10 - Go-live Date PTCNGDVX (Sect.110 Pos.85) *
.*       V10.06.01  17/03/2015  Davin         CAR 311669      HDP 2015/16     *
.*                  Add PTCNGDV9 for ICD10 Ed.9 Go-live Date (Sect.110 Pos.77)*
.*       V10.03.01  23/04/2013  Davin         CAR 284420      HDP 2013/14     *
.*                  Add PTCNGDV8 for ICD10 Ed.8 Go-live Date (Sect.110 Pos.69)*
.*       V10.00.01  07/04/2010  Mike Laming   CAR 219246      HDP 2010        *
.*                  Add PTCNGDV7 for ICD10 Ed.7 Go-live Date (Sect.110 Pos.61)*
.*        V9.09.01  20/03/2008  Mike Laming   CAR 163918    HDP 2008          *
.*                  Add PTCNGDV6 for ICD10 Ed.6 Go-live                       *
.*        V9.06.01  15/05/2006  Mike Laming   CAR 105244      2006 HDP        *
.*                  ADD PTCNGDV5 for ICD10 Ed.5 Go-live                       *
.*        V9.03.01  23/06/2004  Henry Son        CAR 49037                    *
.*                  ICD10 Ed.4 changes - add PTCNGDV4.                        *
.******************************************************************************
.*       V5.07.01   31/03/1999  Jim Stathopoulos  SRF 645437                  *
.*                  Opened the patcodes file                                  *
.*       V5.07.B01  18/01/1999  Jim Stathopoulos                              *
.*                  507 Mods                                                  *
.******************************************************************************
.*               21/09/90 Steve Armstrong                                     *
.*                  Updated to allow for operation/doctor index               *
.*        V5.01.02  24/05/1995  Matt Surridge                                 *
.*                  Fixed bugs for global recompile.                          *
.*        V5.04.01  26/06/1997  Howellsy         505                          *
.*                  Use PATDOCKY instead of PATDOCDS                          *
.*                                                                            *
.***************************************************************************
.
.$$$$$
.    Operating Room System Operating Averages Maintenance ( IBAOPR05 )
.    -----------------------------------------------------------------
.
.    ML0000
.    Initialise and open files
.
.    ML0100
.    See which option is selected - insert, change, delete.
.    If 0 is selected, chain back to calling program.
.
.    ML1000
.    Add option selected.
.
.    Prompt user to enter an operation code.
.    If nothing is entered, chain back to the calling program.
.    If a code is entered, validate it against the operation codes
.    (mbs or icd9 depending on flag).
.    If a "?" is entered, list the operation codes on file, then allow
.    the user to enter a code.
.    Once an operation code has been entered, prompt the user to enter
.    a doctor's code.
.    If nothing is entered, then a hospital average is assumed instead
.    of an individual doctor
.    If exitchar is entered, the program returns to ask for an operation 
.    code again.
.    If a code is entered, it is validated against the doctors code file.
.    If a "?" is entered, list the doctor codes on file, then allow the user
.    to enter a code.
.    The program then checks to see if the operation/doctor code is already
.    on file.  If not, the program calls the next routine to input the 
.    average time.
.
.    The user is prompted to enter the average time (greater than or equal 
.    to zero).
.    Once a valid time is entered, the user is prompted to change the time
.    entry, to cancel the entry, or to post the entry.  If posted or 
.    cancelled, the program returns to get the next operation code.
.
.    ML2000
.    This routine allows a record to be changed.
.    The user inputs an operation and doctor code in the same way as for
.    addition of a record.  
.    If the code is on file, the record is then displayed, and the user
.    has the option to change the average time, to cancel the alteration,
.    or to post the changes.
.
.    ML3000
.    This routine allows a record to be deleted.
.    The user inputs an operation and doctor code in the same way as for
.    addition of a record.  
.    If the code is on file, the record is then displayed, and the user
.    has the option to delete the record.  If "N" is selected, the 
.    program returns to ask for the operation code.  If "C" is entered
.    the program returns to ask for the next option.
.
.$$$$$
.
          INC       STD001FD
          INC       OPRCONFD/INC
          INC       OPRAVEFD/INC
          INC       PATICOFD/INC
          INC       PATITMFD/INC
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
+
.**************************************************************************
.*                            GLOBAL VARIABLES                            *
.**************************************************************************
.
AVTIME    FORM      4                       * average time
DESCR     DIM       40                      * desc of item
DIM1A     DIM       1                       * used in patdsitm
DIM20     DIM       20                      * used in patdsitm
DIM3      DIM       3
DOCCODE   DIM       6                       * doctor code     
OPRCODE   DIM       9                       * operation code
RANGE     FORM      1
PTCNDCQS  FORM      1
PTCNGDV3  DIM       8
PTCNGDV4  DIM       8                       * Ed4 golive date          *I-49037
PTCNGDV5  DIM       8                       * Ed5 golive date         *I-105244
PTCNGDV6  DIM       8                       * Ed.6 Go-live date       *I-163918
PTCNGDV7  DIM       8                       * Ed.7 Go-live date       *I-219246
PTCNGDV8  DIM       8                       * Ed.8 Go-live date       *I-284420
PTCNGDV9  DIM       8                       * Ed.9 Go-live date       *I-311669
PTCNGDVX  DIM       8                       * Ed.10 Go-live date      *I-0833093
PTCNGDX1  DIM       8                       * Go live date for ICD10 Ed.11
PTCNGDX2  DIM       8                       * Go live date for ICD10 Ed.12
PTCNGDX3  DIM       8                       * Go live date for ICD10 Ed.13
PTCNI10D  DIM       8
.
PRGID     INIT      "IBAOPR05"
PRGNAM    INIT      "Operation Average Maintenance"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              MAINLINE                                  *
.*                            Controlling Logic                           *
.**************************************************************************
ML0000    CALL      INIT0000               * initialization and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000:        * insert 
                            ML2000:        * change
                            ML3000         * delete
.
. ---- add a operation code ----
.
ML1000    CALL      SCR0000                * display screen format
          CALL      PROC0000               * input operation and doctor codes
          BRANCH    EXIT,ML0100
          CALL      ACPT0000               * accept initial values
          CALL      SEL0000                * select field to modify
.                                           * or ok to post
          BRANCH    EXIT,ML1000
          CALL      WRT0000                * write to file
          GOTO      ML1000
.
. ---- modify a operation code ----
.
ML2000    CALL      SCR0000                * display screen format
          CALL      PROC0000               * input operation and doctor codes
          BRANCH    EXIT,ML0100
          CALL      DISP0000               * display details
          CALL      SEL0000                * select field to modify
.                                          * or ok to post
          BRANCH    EXIT,ML2000
          CALL      UPD0000                * update record
          GOTO      ML2000
.
. ---- delete a operation code ----
.
ML3000    CALL      SCR0000                * display screen format
          CALL      PROC0000               * input operation and doctor codes
          BRANCH    EXIT,ML0100
          CALL      DISP0000               * display detail
          CALL      DELQST                 * ok to delete ?
          BRANCH    CEXIT,ML4000:          * yes
                          ML3000:          * no
                          ML0100           * cancel
.
ML4000    CALL      DEL0000                * delete this record
          GOTO      ML3000
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                  INIT0000                     Called by: ML0000          *
.*  The initialization routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,TWENTY1;*247,PTCNDCQS
          READ      CONTROLF,FORTY;*43,OPCNAUDA,*112,OPCNCODE
.
          BRANCH    OPCNCODE,INIT1000       * using mbs codes
.
          DISPLAY   *P64:24,"paticdof";     * using icd9 codes
          OPEN      PATICDO1,"paticdof"
          DISPLAY   *P64:24,"pati10of";     * using icd10 codes
          OPEN      PATI10O1,"pati10of"
          GOTO      INIT2000
.
INIT1000  DISPLAY   *P64:24,"patitemn";
          OPEN      PATITEM1,"patitemn"
.
INIT2000  DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
          DISPLAY   *P64:24,"opraveaf";
          OPEN      OPRAVEA1,"opraveaf"
          OPEN      OPRAVEA2,"opraveaf"
.
          BRANCH    OPCNAUDA,INIT3000       * no audit required
.
          DISPLAY   *P64:24,"opraudav";
          OPEN      OPRAUDAV,"opraudav"
.  
INIT3000  DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
.
          MOVE      ONE,PACFRMT
.
INIT9999  RETURN
+
.****************************************************************************
.*                  OPTN0000                     Called by: ML0100          *
.*        get user options or exit                                          *
.*                                                                          *
.*    valid options     --->  (0) Exit                                      *
.*                            (1) Insert                                    *
.*                            (2) Change                                    *
.*                            (3) Delete                                    *
.****************************************************************************
.
. ---- display option screen ----
.
OPTN0000  HLINE     *G37,2,68,80
          CALL      SELOPTN                       * select option
          COMPARE   ZERO,COPTION
          GOTO      OPTN9000 IF EQUAL             * exit
.
          MOVE      FALSE,EXIT
          GOTO      OPTN9999 
.
OPTN9000  MOVE      TRUE,EXIT                     * set EXIT = 1
.
OPTN9999  RETURN
+
.**************************************************************************
.*                  CLR0000                      Called by: PROC0000      *
.*        clear global variables                                          *
.**************************************************************************
.
CLR0000   MOVE      SP9,OPRCODE
          MOVE      SP6,DOCCODE
          MOVE      ZERO,AVTIME 
          PACK      DESCR,SP30,SP30
.
CLR9999   RETURN
+
.**************************************************************************
.*                  PROC0000                     Called by: ML1000        *
.*        input operation code                              ML2000        *
.*                                                          ML3000        *
.**************************************************************************
.
PROC0000  CALL      CLR0000                * clear input variables
.
          KEYIN     *P22:4,*EL,*DV,UNDLN9:
                    *P22:4,*V2LON,OPRCODE:
                    *P22:4,*DV,OPRCODE
.
          CALL      CHKO0000
          BRANCH    EXIT,PROC9700:          * exitchar entered
                         PROC1000:          * ? entered
                         PROC0000:          * invalid code entered
                         PROC9700           * nothing entered
.
          GOTO      PROC5000               * valid code entered
.
PROC1000  MOVE      ONE,CNEWDS             * new "?" option
          BUMP      OPRCODE
          PACK      DIM1A,OPRCODE,SP1
          CALL      PATITMDS               * list items
.
PROC2000  KEYIN     *P1:24,*EL,"Operation Code : ",*DV,UNDLN9:
                    *P18:24,*V2LON,OPRCODE:
                    *P18:24,*DV,OPRCODE
.
PROC3000  CALL      CHKO0000
          BRANCH    EXIT,PROC9700:          * exitchar entered
                         PROC1000:          * ? entered
                         PROC2000:          * invalid code entered
                         PROC9700           * nothing entered
.
          CALL      SCR0000
.
PROC5000  DISPLAY   *P22:4,*EL,*V2LON,OPRCODE,*HOFF,*P35:4,DESCR
.
.         Get doctors code
.
PROC6000  MOVE      SP6,DOCCODE
          MOVE      SP9,DTITL
          PACK      DGNAME,SP20,SP5
          MOVE      SP20,DSNAME
          MOVE      SP6,DCODE
.
          DISPLAY   *P5:5,"Doctor Code    : "
          MOVE      SP10,DOCCODE
          MOVE      "22",ECOL
          MOVE      "05",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          CALL      PATDOCKY
          BRANCH    EXIT,PROC9400,PROC0000
.
          MOVE      DCODE,DOCCODE
.
.         Format the doctors name
.
PROC9400  MOVE      DTITL,PACTITLE
          MOVE      DGNAME,PACGNAME
          MOVE      DSNAME,PACSNAME
          CALL      PACNAME
          DISPLAY   *P22:5,*V2LON,DCODE,*HOFF,*P35:5,PACFNAME
.
          PACK      KEY15,OPRCODE,DOCCODE
          CALL      RDOPAVE1
.
          BRANCH    COPTION,PROC9600        * insert
.
          BRANCH    OVRCD,PROC9500          * change/delete- code not on file
          GOTO      PROC9800                *                code on file
.
PROC9500  DISPLAY   *P1:24,*B,"Operation/Doctor not on file.  ",*EL;
          CALL      HITENTER
          GOTO      PROC0000
.
PROC9600  BRANCH    OVRCD,PROC9800          * code not on file
.
          DISPLAY   *P1:24,*B,"Operation/Doctor already on file.  ",*EL;
          CALL      HITENTER
          GOTO      PROC0000
.
PROC9700  MOVE      TRUE,EXIT
          GOTO      PROC9999
.
PROC9800  MOVE      FALSE,EXIT
.
PROC9999  RETURN
+
.**************************************************************************
.*                  CHKO0000                     Called by: PROC0000      *
.*        See what was input                                              *
.*        0 = valid operation code entered                                *
.*        1 = exitchar entered                                            *
.*        2 = ? entered                                                   *
.*        3 = invalid code                                                *
.*        4 = nothing entered                                             *
.**************************************************************************
.
CHKO0000  ENDSET    OPRCODE
          APPEND    SP9,OPRCODE
          RESET     OPRCODE
.
          MATCH     SP9,OPRCODE             * anything entered ?
          GOTO      CHKO9000 IF EQUAL       * no
.
          MATCH     EXITCHAR,OPRCODE        * exitchar entered ?
          GOTO      CHKO7000 IF EQUAL       * yes
.
          BRANCH    OPCNCODE,CHKO3000       * mbs code
.
          MOVE      OPRCODE,KEY9            * icd9 code 
          CALL      RD10T9O1
          IF        OVRCD=1
              CALL      RDPT10O1
          ENDIF
          BRANCH    OVRCD,CHKO9500          * code not on file
.
          MOVE      ODESC,DESCR             * valid code
          MOVE      ZERO,EXIT
          GOTO      CHKO9999
.
CHKO3000  MATCH     QUEST,OPRCODE           * ? entered ?
          GOTO      CHKO8000 IF EQUAL       * yes
.
          PACK      KEY17,OPRCODE,SP70      * code entered, so validate it
          CALL      PATITMRD
          BRANCH    OVRCD,CHKO9500          * code not on file
          MOVE      IDESC,DESCR             * code on file
.
          MOVE      ZERO,EXIT
          GOTO      CHKO9999 
.
CHKO7000  MOVE      ONE,EXIT
          GOTO      CHKO9999
.
CHKO8000  MOVE      TWO,EXIT
          GOTO      CHKO9999
.
CHKO9000  MOVE      FOUR,EXIT
          GOTO      CHKO9999
.
CHKO9500  DISPLAY   *P1:24,*B,"Operation code not on file.  ",*EL;
          CALL      HITENTER
          MOVE      THREE,EXIT
.
CHKO9999  RETURN
+
.**************************************************************************
.*                  CHKD0000                     Called by: PROC0000      *
.*        See what was input                                              *
.*        0 = valid doctors code entered                                  *
.*        1 = exitchar entered                                            *
.*        2 = ? entered                                                   *
.*        3 = invalid code                                                *
.*        4 = nothing entered                                             *
.**************************************************************************
.
CHKD0000  ENDSET    DOCCODE
          APPEND    SP6,DOCCODE
          RESET     DOCCODE
.
          MATCH     SP6,DOCCODE             * anything entered ?
          GOTO      CHKD9000 IF EQUAL       * no
.
          MATCH     QUEST,DOCCODE           * ? entered ?
          GOTO      CHKD8000 IF EQUAL       * yes
.
          MATCH     EXITCHAR,DOCCODE        * exitchar entered ?
          GOTO      CHKD7000 IF EQUAL       * yes
.
          MOVE      DOCCODE,KEY6            * code entered, so validate it
          CALL      RDDOCT1
          BRANCH    OVRCD,CHKD9500          * code not on file
.
          MOVE      ZERO,EXIT
          GOTO      CHKD9999 
.
CHKD7000  MOVE      ONE,EXIT
          DISPLAY   *P22:5,*EL     
          GOTO      CHKD9999
.
CHKD8000  MOVE      TWO,EXIT
          GOTO      CHKD9999
.
CHKD9000  MOVE      FOUR,EXIT
          GOTO      CHKD9999
.
CHKD9500  DISPLAY   *P1:24,*B,"Doctor code not on file.  ",*EL;
          CALL      HITENTER
          MOVE      THREE,EXIT
.
CHKD9999  RETURN
+
.**************************************************************************
.*                  SCR0000                      Called by: ML1000        *
.*        display screen format                             ML2000        *
.*                                                          ML3000        *
.*                                                          PROC0000      *
.**************************************************************************
.
SCR0000   DISPLAY   *P1:3,*EF,*P5:4,"Operation Code : ":
                    *P5:5,"Doctor Code    :":
                    *P2:7,*V2LON,ONE,*HOFF,DOT,*P5:7,"Average (Mins) : "
.
SCR9999   RETURN
+
.*************************************************************************
.*                  ACPT0000                     Called by: ML1000       *
.*   INPUT mode only :      accept initial values                        *
.*************************************************************************
.
ACPT0000  CALL      AVTM0000            * keyin average time
.
ACPT9999  RETURN
+
.**************************************************************************
.*                  SEL0000                      Called by: ML1000        *
.*        select field to modify or post                    ML2000        *
.**************************************************************************
.
SEL0000   CALL      MAINQST
.
          COMPARE   ZERO,CCITEM                * 0 selected ?
          GOTO      SEL9500 IF EQUAL           * yes
          GOTO      SEL9000 IF LESS            * cancel
.
          BRANCH    CCITEM,SEL1000
.
          BEEP
          GOTO      SEL0000
.
SEL1000   CALL      AVTM0000                   * input time
          GOTO      SEL0000
.
.------ Cancel option chosen ------
.
SEL9000   MOVE      TRUE,EXIT
          GOTO      SEL9999
.
SEL9500   MOVE      FALSE,EXIT
.
SEL9999   RETURN
+
.**************************************************************************
.*                  AVTM0000                     Called by: SEL0000       *
.*        input average time                                ACPT0000      *
.**************************************************************************
.
AVTM0000  MOVE      ZERO,AVTIME
.
          KEYIN     *P22:7,*DV,UNDLN4:
                    *P22:7,*V2LON,AVTIME:
                    *P22:7,*DV,AVTIME
.
          COMPARE   ZERO,AVTIME             * time >= zero ?
          GOTO      AVTM8000 IF LESS        * no
.
          MOVE      AVTIME,OPAVAVER         
          GOTO      AVTM9999
.
AVTM8000  DISPLAY   *P1:24,*B,"Time input is invalid.  ",*EL;
          CALL      HITENTER
          GOTO      AVTM0000
.
AVTM9999  RETURN
+
.**************************************************************************
.*                  WRT0000                      Called by: ML1000        *
.*        write a new record                                              *
.**************************************************************************
.
WRT0000   DISPLAY   *P1:24,*EL,"** Record inserted **";
.
          MOVE      OPRCODE,OPAVCODE
          MOVE      DOCCODE,OPAVDOCT
          MOVE      AVTIME,OPAVAVER
          PACK      KEY15,OPAVCODE,DCODE
.
          CALL      WROPAVE1
.
          BRANCH    OPCNAUDA,WRT9999
          MOVE      ANSA,OPAVAUDR
          CALL      AUDF0000                     * audit file
          PACK      KEY19,OPAVAUDD,OPAVAUDT,OPAVAUDP,OPAVAUDR
          CALL      AWOPAVE                      * write to audit file
          DISPLAY   *P1:24,*EL
.
WRT9999   RETURN
+
.*************************************************************************
.*                  AUDF0000                     Called by: UPD0000      *
.*        prepare audit file variables                      DEL0000      *
.*                                                          WRT0000      *
.*************************************************************************
.
AUDF0000  CALL      IBACLOCK
          PACK      OPAVAUDD,CCC,CYY,CMM,CDD
          REP       " 0",OPAVAUDD
          MOVE      CTIMEIS,OPAVAUDT
          MOVE      ONE,OPAVAUDS
          MOVE      PASSCODE,OPAVAUDO
          MOVE      PORT,OPAVAUDP
.
AUDF9999  RETURN
+
.*************************************************************************
.*                  DISP0000                     Called by: ML2000       *
.*        display all data variable details                 ML3000       *
.*************************************************************************
.
DISP0000  DISPLAY   *P22:4,*EL,*V2LON,OPAVCODE,*HOFF,*P35:4,DESCR:
                    *P22:5,*V2LON,DCODE,*HOFF,*P35:5,PACFNAME:
                    *P22:7,*V2LON,OPAVAVER
          MOVE      OPAVAVER,AVTIME
.
DISP9999  RETURN
+
.***************************************************************************
.*                  UPD0000                      Called by: ML2000         *
.*        update record and insert key if part of                          *
.*        key has been modified.                                           *
.***************************************************************************
.
UPD0000   DISPLAY   *P1:24,*EL,"** Record Updated **";
.
          BRANCH    OPCNAUDA,UPD5000
          MOVE      ANSB,OPAVAUDR
          CALL      AUDF0000                     * audit file
          PACK      KEY19,OPAVAUDD,OPAVAUDT,OPAVAUDP,OPAVAUDR
          CALL      AWOPAVE                      * write to audit file
.
UPD5000   MOVE      OPRCODE,OPAVCODE
          MOVE      DOCCODE,OPAVDOCT
          MOVE      AVTIME,OPAVAVER
.
          BRANCH    OPCNAUDA,UPD8000
          MOVE      ANSC,OPAVAUDR
          PACK      KEY19,OPAVAUDD,OPAVAUDT,OPAVAUDP,OPAVAUDR
          CALL      AWOPAVE                      * write to audit file
.
UPD8000   CALL      UPOPAVE1
          DISPLAY   *P1:24,*EL
.
UPD9999   RETURN
+
.****************************************************************************
.*                  DEL0000                      Called by: ML3000          *
.*        delete an existing record                                         *
.****************************************************************************
.
DEL0000   DISPLAY   *P1:24,*EL,"** Record Deleted **";
.
          PACK      KEY15,OPAVCODE,OPAVDOCT
          CALL      DEOPAVE1
.
          BRANCH    OPCNAUDA,DEL9999
          MOVE      ANSD,OPAVAUDR
          CALL      AUDF0000                     * audit file
          PACK      KEY19,OPAVAUDD,OPAVAUDT,OPAVAUDP,OPAVAUDR
          CALL      AWOPAVE                      * write to audit file
          DISPLAY   *P1:24,*EL
.
DEL9999   RETURN
+
.****************************************************************************
.
          INC       STD001IO
          INC       PATITMDS
          INC       PATITMRD
          INC       PATDOCKY
          INC       OPRAVEIO/INC
          INC       PATICOIO/INC
          INC       PATITMIO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
.

.*******************************************************************************
.* System         :  PMI                                                       *
.* Program        :  WIESLOAD                                                  *
.* Description    :  Upload WIES 27 details into webPAS                        *
.*******************************************************************************
.* Date           :  04/08/2017                                                *
.* Author         :  Davin Sloan                                               *
.* Function       :  Upload WIES 27 Details using pipe delimited 'wiesload.txt'*
.*                   To create wiesload.txt:                                   *
.*                   1) Save VIC WIES Cost Weights as tab delimited text file. *
.*                   2) Remove any trailing columns (ensure 17 fields/16 tabs).*
.*                   3) Convert all tabs to pipes.                             *
.*                   4) Remove any start " and end " from description field.   *
.*                   5) Execute 'dos2unix' on wiesload.txt to remove any ^Ms.  *
.*******************************************************************************
.* Modifications  :                                                            *
.*                                                                             *
.*   V11.01.01  10/01/2021  Ebon Clements    TSK 0912886                       *
.*                          Allow user to keyin WIES/NWAU version being loaded *
.*   V11.00.01  23/09/2020  Ebon Clements    TSK 0893634                       *
.*                          Changed to upload WIES27 (UPLDVRSN=027)            *
.*   V10.14.01  22/08/2019  Davin            TSK 0880255                       *
.*                          Changed to upload WIES26 (UPLDVRSN=026)            *
.*   V10.13.01  17/08/2018  Davin            TSK 0860935                       *
.*                          Added new WIES25 field for non-invasive ventilation*
.*   V10.11.00  04/08/2017  Davin Sloan     TSK 0842626                        *
.*                          New program                                        *
.*******************************************************************************
.
          INC       STD001FD           * Standard Include
.
          INC       PATWIEFD/INC       * Cost Weights for WIES
.
.*******************************************************************************
.*                    WIES Upload file (17 fields)                             *
.*******************************************************************************
.
UPLDDTXT  FILE       HL7, FIXED=4096
.
.Name     Type      Size  Description
.----     ----      ----  -----------
UPLDDRGC  DIM          4  DRG Code
.PIPE     DIM          1  Pipe Delimiter
UPLDDRGD  DIM         60  DRG Description
.PIPE     DIM          1  Pipe Delimiter
UPLDMVEL  DIM          1  Mech. Ventilation Eligibility
.PIPE     DIM          1  Pipe Delimiter
UPLDNINV  DIM          1  Non-Invasive Ventilation Eligibility
.PIPE     DIM          1  Pipe Delimiter
UPLDCPAY  DIM          3  Other Co-payments
.PIPE     DIM          1  Pipe Delimiter
UPLDLIBP  DIM          4  Low Inlier Boundary Point (days)
.PIPE     DIM          1  Pipe Delimiter
UPLDHIBP  DIM          4  High Inlier Boundary Point (days)
.PIPE     DIM          1  Pipe Delimiter
UPLDILOS  DIM          7  Average Inlier Lngth of Stay (days)
.PIPE     DIM          1  Pipe Delimiter
UPLDSSDR  DIM          1  Short Stay DRG (S)
.PIPE     DIM          1  Pipe Delimiter
UPLDSSWT  DIM         11  Short Stay Weight
.PIPE     DIM          1  Pipe Delimiter
UPLDSDOD  DIM          1  (S)ame Day or (O)ne Day DRG
.PIPE     DIM          1  Pipe Delimiter
UPLDTDSW  DIM         11  Total Same Day Weight
.PIPE     DIM          1  Pipe Delimiter
UPLDTODW  DIM         11  Total One Day Weight
.PIPE     DIM          1  Pipe Delimiter
UPLDTLOM  DIM         11  Total Low Outlier Multiday Weight
.PIPE     DIM          1  Pipe Delimiter
UPLDTIMW  DIM         11  Total Inlier Multiday Weight
.PIPE     DIM          1  Pipe Delimiter
UPLDTHOM  DIM         11  Total High Outlier Multiday Weight
.PIPE     DIM          1  Pipe Delimiter
UPLDTHIH  DIM         11  Total Hospital In The Home Weight
.
.
. LOCAL VARIABLE DEFINITION
. -------------------------
ADDCOUNT  FORM      8             * added record count
.
CURRDATE  DIM       8             * current date
.
DUPCOUNT  FORM      8             * duplicate record count  >>>(may not be used)
.
ERORDESC  DIM       70
ERORFLAG  FORM      1             * Field Validation Error Flag
.                                     0 = No Errors on Field Validations
.                                     1 = Errors on Field Validations
.
FORM6P4   FORM      6.4
FORM10    FORM      10
.
MANCOUNT  FORM      8             * mandatory error record count
.
NUMCOUNT  FORM      8             * numeric error record count
.
RECCOUNT  FORM      8             * record read counter
.
TMPFIELD  DIM       8             * Field name
TMPSTRNG  DIM       8             * Temporary string (max field length)
.
.
. PROGRAM CONSTANTS
. -----------------
UPLDVRSN  DIM       3             * Upload WIES/NWAU version eg "027"
PIPE      INIT      "|"
.
.
.*******************************************************************************
.*                        Program Version Information                          *
.*******************************************************************************
PRGID     INIT      "WIESLOAD"
PRGNAM    INIT      "Upload WIES Details"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000       * proceed with upload
.
MAIN1000  CALL      OPEN0000               * open upload file
          BRANCH    EXIT,MAIN0100          * file not found
.
          CALL      KEYV0000               * Keying WIES/NWAU version to load
          BRANCH    EXIT,MAIN0100          * file not found
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN2000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN2000  CALL      UPLD0000               * process upload
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patwieaf";
          OPEN      PATWIEA1,"patwieaf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          MOVE      ONE,CNOUNDLN
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run data upload                         *
.*                        TRUE  (1)  Exit selected                           *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Data Upload"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run data upload
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               OPEN0000              Called by: MAIN0000   *
.*                  Open upload file (wiesload.txt)                          *
.*****************************************************************************
.
OPEN0000  MOVE      ZERO,OVRCD
.
          TRAP      OVERCOND IF IO
          OPEN      UPLDDTXT,"wiesload"
          TRAPCLR   IO
          BRANCH    OVRCD,OPEN9100
.
          MOVE      ZERO,EXIT
          GOTO      OPEN9999
.
OPEN9100  DISPLAY   *P1:24,*EL,*B,"Upload file - wiesload.txt - not found.  ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
OPEN9999  RETURN
+
.*****************************************************************************
.*                               KEYV0000              Called by: MAIN1000   *
.*                   Keying WIES/NWAU version to load                        *
.*****************************************************************************
.
KEYV0000  KEYIN     *P1:9,*EL,"WIES/NWAU Version : ":
                    *P21:9,*V2LON,UPLDVRSN
.
          PACK      UPLDVRSN,UPLDVRSN,SP70
.
KEYV9000  MOVE      ZERO,EXIT
          GOTO      KEYV9999
.
KEYV9999  RETURN
+
.******************************************************************************
.*                                  UPLD0000              Called by: MAIN0000 *
.*                  Process the WIES upload file records                      *
.******************************************************************************
.
UPLD0000  CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          DISPLAY   *P1:13,"Processing ",*V2LON,"wiesload.txt",*HOFF:
                    *P1:15,"Started    : ",*V2LON,CTIMEIS,*HOFF:
                    *P1:17,"DRG Code   : ":
                    *P1:18,"Records    : "
.
          MOVE      ZERO,RECCOUNT                * initialise records read
          MOVE      ZERO,ADDCOUNT                * initialise added record count
          MOVE      ZERO,DUPCOUNT                * initialise dup. record count
          MOVE      ZERO,MANCOUNT                * init. mand error record count
          MOVE      ZERO,NUMCOUNT                * init. num error record count
.
          CALL      HEAD0000                     * Print the report header
.
.         Loop through upload file records
.
UPLD1000  READ      UPLDDTXT,SEQ;UPLDDRGC,UPLDDRGD,UPLDMVEL,UPLDNINV:
                                 UPLDCPAY,UPLDLIBP,UPLDHIBP,UPLDILOS:
                                 UPLDSSDR,UPLDSSWT,UPLDSDOD,UPLDTDSW:
                                 UPLDTODW,UPLDTLOM,UPLDTIMW,UPLDTHOM:
                                 UPLDTHIH
.
          GOTO      UPLD9000 IF OVER
.
          ADD       ONE,RECCOUNT                 * increment records read
          IF        (RECCOUNT%1000) = 0 | RECCOUNT = 1
            DISPLAY   *P14:17,*V2LON,UPLDDRGC:
                      *P14:18,RECCOUNT;
          ENDIF
.
          CALL      POUT0000                     * pad out fields with spaces
.
          CALL      CLER0000                     * clear database variables
.
          CALL      MAND0000                     * check for Mandatory fields
          IF        EXIT = 1
            GOTO      UPLD1000                   * errors on validation
          ENDIF
.
          CALL      VNUM0000                     * validate numeric fields
          IF        EXIT = 1
            GOTO      UPLD1000                   * errors on validation
          ENDIF
.
          CALL      SETV0000                     * set up file variables
.
          PACK      KEY7,UPLDDRGC,UPLDVRSN
          CALL      RAPTWIE1                     * record on file ?
          IF        OVRCD = 1
            CALL      WRPTWIE1                   * no - write new record
            ADD       ONE,ADDCOUNT               * increment added record count
          ELSE
            MOVE      "WIES DRG record already exists ",ERORDESC
            CALL      PERR0000                   * print error line
            ADD       ONE,DUPCOUNT               * increment dupl. record count
          ENDIF
.
          GOTO      UPLD1000                     * get next record
.
.         Upload completed
.
UPLD9000  ASSIGN    (DUPCOUNT+MANCOUNT+NUMCOUNT),FORM10
          IF        FORM10 > 0
            CALL      LINE0000                   * print bottom line if errors
          ENDIF
.
          COMPARE   CLNO,FIFTY8                  * room for stats ?
          CALL      HEAD0000 IF LESS             * no - new page req'd.
.
          CLOCK     TIME,CTIMEIS
          DISPLAY   *P14:18,*V2LON,RECCOUNT,*HOFF:
                    *P1:21,"Finished   : ",*V2LON,CTIMEIS
.
.         Warn the user if there were any records with errors
.
          IF        FORM10 > 0
            DISPLAY   *P1:22,*V2LON,*BLINKON,"Warning:  Records found with ":
                      "errors.  Refer to error report."
          ENDIF
.
          DISPLAY   *P1:24,*EL,*B,"Upload complete.  ";
          CALL      HITENTER
.
          PRINT     *N:
                    *1,"Total records read                    - ",RECCOUNT,*N:
                    *1,"Records added                         - ",ADDCOUNT,*N:
                    *1,"Duplicate Records detected            - ",DUPCOUNT,*N:
                    *1,"Records with invalid numbers detected - ",NUMCOUNT,*N:
                    *1,"Records with blank mandatory fields   - ",MANCOUNT:
                    *N,*N,*1,"*** End of Report ***"
.
UPLD9999  RETURN
+
.******************************************************************************
.*                                  CLER0000              Called by: UPLD0000 *
.*                    Clear all the database fields                           *
.******************************************************************************
.
CLER0000  MOVE      SP70,PTWIDRGC
          MOVE      SP70,PTWIDRGD
          MOVE      SP70,PTWIVRSN
          MOVE      SP70,PTWISDMT
          MOVE      SP70,PTWIMVEL
          MOVE      SP70,PTWICPAY
          MOVE      ZERO,PTWILIBP
          MOVE      ZERO,PTWIHIBP
          MOVE      ZERO,PTWIILOS
          MOVE      SP70,PTWISDOD
          MOVE      ZERO,PTWITDSW
          MOVE      ZERO,PTWITODW
          MOVE      ZERO,PTWITLOM
          MOVE      ZERO,PTWITIMW
          MOVE      ZERO,PTWITHOM
          MOVE      ZERO,PTWITHIH
          MOVE      SP70,PTWISSDR
          MOVE      ZERO,PTWISSWT
          MOVE      SP70,PTWISPAR
CLER9999  RETURN
+
.****************************************************************************
.*                             MAND0000                Called by: UPLD0000  *
.*                 Check for standard Mandatory fields                      *
.* Returns: EXIT   0 = all mandatory fields are populated                   *
.*                 1 = one or more mandatory fields are blank               *
.*          MANCOUNT - updated count of records with mandatory fields       *
.*                     missing                                              *
.****************************************************************************
.
MAND0000  MOVE      ZERO,ERORFLAG                * init. validation error flag
.
          MOVE      UPLDDRGC,TMPSTRNG
          MOVE      "UPLDDRGC",TMPFIELD          * code
          CALL      CHKM0000
.
          MOVE      UPLDDRGD,TMPSTRNG
          MOVE      "UPLDDRGD",TMPFIELD          * description
          CALL      CHKM0000
.
          MOVE      UPLDVRSN,TMPSTRNG
          MOVE      "UPLDVRSN",TMPFIELD          * version
          CALL      CHKM0000
.
          BRANCH    ERORFLAG,MAND9100            * errors on mand. fields
.
          MOVE      ZERO,EXIT                    * no errors on mand. fields
          GOTO      MAND9999
.
MAND9100  ADD       ONE,MANCOUNT                 * incr. mand error record count
          MOVE      ONE,EXIT
.
MAND9999  RETURN
+
.*****************************************************************************
.*                            CHKM0000             Called by: MAND0000       *
.*                    Check if a mandatory field is blank                    *
.* Requires: TMPSTRNG - field to be checked                                  *
.*           TMPFIELD - 8 character field name                               *
.* Returns:  ERORFLAG = 1   field is blank (error)                           *
.*****************************************************************************
.
CHKM0000  MATCH     TMPSTRNG,SP8                 * blank field ?
          GOTO      CHKM9999 IF NOT EQUAL        * no - finished
.
          MOVE      "Mandatory Field blank ",ERORDESC
          ENDSET    ERORDESC
          APPEND    LBRACK,ERORDESC
          APPEND    TMPFIELD,ERORDESC
          APPEND    RBRACK,ERORDESC
          APPEND    SP70,ERORDESC
          RESET     ERORDESC
          CALL      PERR0000                     * print error line
.
          MOVE      ONE,ERORFLAG                 * set error flag
.
CHKM9999  RETURN
+
.******************************************************************************
.*                                  HEAD0000              Called by: UPLD0000 *
.*                           Print The Report Header                 PERR0000 *
.******************************************************************************
.
HEAD0000  MOVE      "- Error Report",CPHDROPT
          CALL      HEAD132                 * Print the report header
.
          PRINT     *1,"< wiesload.txt >",SP5,"Version : ",UPLDVRSN
.
          CALL      LINE0000                     * draw line across page
.
          PRINT     *1,PIPE,*3,"Code",*13,PIPE,*15,"Error Description",*132,PIPE
.
          CALL      LINE0000                     * draw line across page
.
          MOVE      SEVEN,CLNO                   * initialise line count
.
HEAD9999  RETURN
+
.****************************************************************************
.*                            LINE0000                 Called by: HEAD0000  *
.*                      Draw line across page                               *
.****************************************************************************
.
LINE0000  PRINT     "*-----------------------------------------------":
                    "------------------------------------------------":
                    "-----------------------------------*"
.
LINE9999  RETURN
+
.******************************************************************************
.*                                  SETV0000              Called by: UPLD0000 *
.*                       Load the record variables                            *
.******************************************************************************
.
SETV0000  MOVE      UPLDDRGC,PTWIDRGC
          MOVE      UPLDDRGD,PTWIDRGD
          MOVE      UPLDVRSN,PTWIVRSN
          MOVE      UPLDNINV,PTWISDMT
          MOVE      UPLDMVEL,PTWIMVEL
          MOVE      UPLDCPAY,PTWICPAY
          MOVE      UPLDLIBP,PTWILIBP
          MOVE      UPLDHIBP,PTWIHIBP
          MOVE      UPLDILOS,PTWIILOS
          MOVE      UPLDSDOD,PTWISDOD
          MOVE      UPLDTDSW,PTWITDSW
          MOVE      UPLDTODW,PTWITODW
          MOVE      UPLDTLOM,PTWITLOM
          MOVE      UPLDTIMW,PTWITIMW
          MOVE      UPLDTHOM,PTWITHOM
          MOVE      UPLDTHIH,PTWITHIH
          MOVE      UPLDSSDR,PTWISSDR
          MOVE      UPLDSSWT,PTWISSWT
.
SETV9999  RETURN
+
.*****************************************************************************
.*                            POUT0000             Called by: UPLD0000       *
.*            Pad out all the record fields with spaces given that we        *
.*            are dealing with pipe delimited records                        *
.*****************************************************************************
.
POUT0000  PACK      UPLDDRGC,UPLDDRGC,SP70
          PACK      UPLDDRGD,UPLDDRGD,SP70
          PACK      UPLDVRSN,UPLDVRSN,SP70
          PACK      UPLDNINV,UPLDNINV,SP70
          PACK      UPLDMVEL,UPLDMVEL,SP70
          PACK      UPLDCPAY,UPLDCPAY,SP70
.
          PACK      UPLDLIBP,UPLDLIBP,SP70
          RJUSTIFY  UPLDLIBP
          REP       " 0",UPLDLIBP
.
          PACK      UPLDHIBP,UPLDHIBP,SP70
          RJUSTIFY  UPLDHIBP
          REP       " 0",UPLDHIBP
.
          PACK      UPLDILOS,UPLDILOS,SP70
          RJUSTIFY  UPLDILOS
          REP       " 0",UPLDILOS
.
          PACK      UPLDSDOD,UPLDSDOD,SP70
.
          PACK      UPLDTDSW,UPLDTDSW,SP70
          RJUSTIFY  UPLDTDSW
          REP       " 0",UPLDTDSW

          PACK      UPLDTODW,UPLDTODW,SP70
          RJUSTIFY  UPLDTODW
          REP       " 0",UPLDTODW
.
          PACK      UPLDTLOM,UPLDTLOM,SP70
          RJUSTIFY  UPLDTLOM
          REP       " 0",UPLDTLOM
.
          PACK      UPLDTIMW,UPLDTIMW,SP70
          RJUSTIFY  UPLDTIMW
          REP       " 0",UPLDTIMW
.
          PACK      UPLDTHOM,UPLDTHOM,SP70
          RJUSTIFY  UPLDTHOM
          REP       " 0",UPLDTHOM
.
          PACK      UPLDTHIH,UPLDTHIH,SP70
          RJUSTIFY  UPLDTHIH
          REP       " 0",UPLDTHIH
.
          PACK      UPLDSSDR,UPLDSSDR,SP70
.
          PACK      UPLDSSWT,UPLDSSWT,SP70
          RJUSTIFY  UPLDSSWT
          REP       " 0",UPLDSSWT
.
POUT9999  RETURN
+
.*****************************************************************************
.*                            VNUM0000             Called by: UPLD0000       *
.*                    Validate numeric fields                                *
.* Returns:  EXIT - 0 = No numeric validation errors                         *
.*                  1 = Numeric validation errors                            *
.*          NUMCOUNT - updated count of records with numeric field errors    *
.*****************************************************************************
.
VNUM0000  MOVE      ZERO,ERORFLAG                * init. validation error flag
.
.         Low Inlier Boundary Point (days)
.
          TYPE      UPLDLIBP                     * numeric ?
          GOTO      VNUM0050 IF NOT EQUAL        * no - error
          GOTO      VNUM0100                     * yes - valid value
.
VNUM0050  MOVE      "UPLDLIBP",TMPFIELD
          CALL      NERR0000
.
.         High Inlier Boundary Point (days)
.
VNUM0100  TYPE      UPLDHIBP                     * numeric ?
          GOTO      VNUM0150 IF NOT EQUAL        * no - error
          GOTO      VNUM0200                     * yes - valid value
.
VNUM0150  MOVE      "UPLDHIBP",TMPFIELD
          CALL      NERR0000
.
.         Average Inlier Lngth of Stay (days)
.
VNUM0200  TYPE      UPLDILOS                     * numeric ?
          GOTO      VNUM0250 IF NOT EQUAL        * no - error
          GOTO      VNUM0300                     * yes - valid value
.
VNUM0250  MOVE      "UPLDILOS",TMPFIELD
          CALL      NERR0000
.
.         Total Same Day Weight
.
VNUM0300  TYPE      UPLDTDSW                     * numeric ?
          GOTO      VNUM0350 IF NOT EQUAL        * no - error
          GOTO      VNUM0400                     * yes - valid value
.
VNUM0350  MOVE      "UPLDTDSW",TMPFIELD
          CALL      NERR0000
.
.         Total One Day Weight
.
VNUM0400  TYPE      UPLDTODW                     * numeric ?
          GOTO      VNUM0450 IF NOT EQUAL        * no - error
          GOTO      VNUM0500                     * yes - valid value
.
VNUM0450  MOVE      "UPLDTODW",TMPFIELD
          CALL      NERR0000
.
.         Total Low Outlier Multiday Weight
.
VNUM0500  TYPE      UPLDTLOM                     * numeric ?
          GOTO      VNUM0550 IF NOT EQUAL        * no - error
          GOTO      VNUM0600                     * yes - valid value
.
VNUM0550  MOVE      "UPLDTLOM",TMPFIELD
          CALL      NERR0000
.
.         Total Inlier Multiday Weight
.
VNUM0600  TYPE      UPLDTIMW                     * numeric ?
          GOTO      VNUM0650 IF NOT EQUAL        * no - error
          GOTO      VNUM0700                     * yes - valid value
.
VNUM0650  MOVE      "UPLDTIMW",TMPFIELD
          CALL      NERR0000
.
.         Total High Outlier Multiday Weight
.
VNUM0700  TYPE      UPLDTHOM                     * numeric ?
          GOTO      VNUM0750 IF NOT EQUAL        * no - error
          GOTO      VNUM0800                     * yes - valid value
.
VNUM0750  MOVE      "UPLDTHOM",TMPFIELD
          CALL      NERR0000
.
.         Total Hospital In The Home Weight
.
VNUM0800  TYPE      UPLDTHIH                     * numeric ?
          GOTO      VNUM0850 IF NOT EQUAL        * no - error
          GOTO      VNUM0900                     * yes - valid value
.
VNUM0850  MOVE      "UPLDTHIH",TMPFIELD
          CALL      NERR0000
.
.         Short Stay Weight
.
VNUM0900  TYPE      UPLDSSWT                     * numeric ?
          GOTO      VNUM0950 IF NOT EQUAL        * no - error
          GOTO      VNUM9000                     * yes - valid value
.
VNUM0950  MOVE      "UPLDSSWT",TMPFIELD
          CALL      NERR0000
.
VNUM9000  BRANCH    ERORFLAG,VNUM9100            * errors on number validation
.
          MOVE      ZERO,EXIT                    * no errors on no. validation
          GOTO      VNUM9999
.
VNUM9100  ADD       ONE,NUMCOUNT                 * incr. num. error record count
          MOVE      ONE,EXIT
.
VNUM9999  RETURN
+
.*****************************************************************************
.*                            NERR0000             Called by: VNUM0000       *
.*                    Process number error                                   *
.* Requires: TMPFIELD - field name                                           *
.*****************************************************************************
.
NERR0000  MOVE      "Invalid numeric field ",ERORDESC
          ENDSET    ERORDESC
          APPEND    LBRACK,ERORDESC
          APPEND    TMPFIELD,ERORDESC
          APPEND    RBRACK,ERORDESC
          APPEND    SP70,ERORDESC
          RESET     ERORDESC
          CALL      PERR0000                     * print error line
.
          MOVE      ONE,ERORFLAG                 * set number error flag
.
NERR9999  RETURN
+
.*****************************************************************************
.*                              PERR0000           Called by: UPLD0000       *
.*                   Print an error detail line               CHKM0000       *
.* Requires: Valid read on PMI records (for name fields)      DERR0000       *
.*           ERORDESC - error description                     TERR0000       *
.*           CLNO - current page line count                   VURN0000       *
.* Returns:  CLNO - updated page line count                   NERR0000       *
.*                                                            VCOD0000       *
.*****************************************************************************
.
PERR0000  COMPARE   CLNO,SIXTY3                  * page full ?
          IF        @LESS
            CALL      LINE0000                   * yes - print bottom line
            CALL      HEAD0000                   * print header
          ENDIF
.
          PRINT     *1,PIPE,*3,UPLDDRGC,*13,PIPE,*15,ERORDESC,*132,PIPE
          ADD       ONE,CLNO                     * increment page line count
.
PERR9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PATWIEIO/INC       * Cost Weights for WIES

.******************************************************************************
.* System         : Screen Painter                                            *
.* Program        : FC1SCDAT                                                  *
.* Name           : Fix Screen Painter Date Fields                            *
.******************************************************************************
.* Date           : 22/01/1999                                                *
.* Author         : Davin                                                     *
.* Function       : Checks that all screen painter date (selection) fields    *
.*                  use & display centuries. The items to be checked are read *
.*                  in from a text file.                                      *
.* Modifications  :                                                           *
.*                                                                            *
.******************************************************************************
+
          INC       STD001FD
.
          INC       SCRMASFD/INC            * Screen master file
          INC       SCRFLDFD/INC            * Screen fields file
          INC       SCRPSTFD/INC            * Screen field positions file
.
. ----- Input text file definition -----
.
SCRPNTIN  FILE      ASCII, FIXED=256
.
. ----- Program Variables -----
.
FLDCOUNT  FORM      3                            * fields file counter
FNUM      DIM       5                            * field number from input file
FORM5     FORM      5                            * field number from input file
PROG      DIM       8                            * FD from input file
PSTCOUNT  FORM      3                            * fields posns file counter
SCRIDENT  DIM       2                            * screen i.d.
SCRITEM   FORM      5                            * item from scrpstaf
UPFLAG    FORM      1                            * 0 = don't update / 1 = upd.
.
. ----- Program Constants -----
.
PRGID     INIT      "FC1SCDAT"
PRGNAM    INIT      "Fix Screen Painter Date Fields"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                   ML0000                                   *
.*                                 Main Module                                *
.******************************************************************************
ML0000    CALL      INIT0000                * Initialise variables & open files
ML1000    CALL      OPTN0000                * Choose main option
          BRANCH    EXIT,ML9999
.
ML2000    CALL      READ0000                * Read in FDs & field numbers
.
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                   INIT0000             Called by: ML0000   *
.*                        Initialise Variable & Open Files                    *
.******************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P64:24,"scrmasaf";
          OPEN      SCRMASA1,"scrmasaf"
.
          DISPLAY   *P64:24,"scrfldaf";
          OPEN      SCRFLDA1,"scrfldaf"
.
          DISPLAY   *P64:24,"scrpstaf";
          OPEN      SCRPSTA2,"scrpstaf"
.
INIT9000  MOVE      ZERO,EXIT
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000              Called by: ML0000   *
.*                                Choose Option                               *
.******************************************************************************
OPTN0000  MOVE      ZERO,OPTION
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Run Fixit":
                    *P1:7,"Select Option : ";
OPTN1000  KEYIN     *P17:7,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN9000
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                  READ0000              Called by: ML0000   *
.*                  Read in FDs & field numbers to be checked                 *
.******************************************************************************
READ0000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      SCRPNTIN,"scrpaint"
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Screen painter input file does not ":
                      "exist. ";
            CALL      HITENTER
            GOTO      READ9999
          ENDIF
.
          MOVE      ZERO,FLDCOUNT                * field file update counter
          MOVE      ZERO,PSTCOUNT                * posns file update counter
READ1000  READ      SCRPNTIN,SEQ;PROG,FNUM       * read in FD & field number
          GOTO      READ9000 IF OVER
.
          MOVE      FNUM,FORM5                   * remove leading zeros
          CALL      UFLD0000                     * update screen fields file
          CALL      GPRG0000                     * get programs to check
          GOTO      READ1000
.
READ9000  CLOSE     SCRPNTIN
          DISPLAY   *P1:22,*EF,"scrfldaf records updated: ",*V2LON,FLDCOUNT:
                    *P1:23,*HOFF,"scrpstaf records updated: ",*V2LON,PSTCOUNT
          CALL      HITENTER
READ9999  RETURN
+
.******************************************************************************
.*                                  UFLD0000              Called by: READ0000 *
.*                          Update Screen Fields File                         *
.******************************************************************************
UFLD0000  MOVE      ZERO,UPFLAG
          DISPLAY   *P1:24,*EL,"Processing scrfldaf : ",*V2LON,FNUM:
                    *P40:24,*HOFF,"Updating : ";
.
          MOVE      "01",SCRIDENT
          PACK      KEY15,PROG,SCRIDENT,FORM5
          CALL      RDSCFL1                      * read screen fields file
          BRANCH    OVRCD,UFLD9999
.
          COMPARE   EIGHT,SCFLFLN                * check field length is 8
          IF        !@EQUAL
            MOVE      EIGHT,SCFLFLN
            MOVE      ONE,UPFLAG
          ENDIF
.
          COMPARE   TEN,SCFLMIN                  * check min length is 10
          IF        !@EQUAL
            MOVE      TEN,SCFLMIN
            MOVE      ONE,UPFLAG
          ENDIF
.
          COMPARE   TEN,SCFLMAX                  * check max length is 10
          IF        !@EQUAL
            MOVE      TEN,SCFLMAX
            MOVE      ONE,UPFLAG
          ENDIF
.
          IF        UPFLAG = 1
            CALL      UPSCFL1                    * update screen fields file
            DISPLAY   *P51:24,*V2LON,SCFLFNO;
            ADD       ONE,FLDCOUNT
          ENDIF
.
UFLD9999  RETURN
+
.******************************************************************************
.*                                  GPRG0000              Called by: READ0000 *
.*                      Check Programs That Use FD Items                      *
.******************************************************************************
GPRG0000  PACK      KEY10,SP10
          CALL      RSSCMA1                 * position on screen master file
.
GPRG1000  CALL      RKSCMA1                 * read screen master file record
          BRANCH    OVRCD,GPRG9999
.
          MATCH     PROG,SCMAFPR            * is FD used by this screen ?
          IF        @EQUAL
            CALL      UPST0000              * update scr fields posn file
          ENDIF
          GOTO      GPRG1000
.
GPRG9999  RETURN
+
.******************************************************************************
.*                                  UPST0000              Called by: GPRG0000 *
.*                     Update Screen Fields Positions File                    *
.******************************************************************************
UPST0000  DISPLAY   *P1:24,*EL,"Processing scrpstaf : ",*V2LON,FNUM:
                    *P40:24,*HOFF,"Updating : ";
.
          PACK      KEY20,SCMAPID,SCMASID,FORM5,SP20
          CALL      RSSCPS2                 * position on scr fields posn file
UPST1000  CALL      RKSCPS2                 * read scr fields posn file record
          BRANCH    OVRCD,UPST9999
.
          MATCH     SCMAPID,SCPSPID
          GOTO      UPST9999 IF NOT EQUAL
.
          MATCH     SCMASID,SCPSSID
          GOTO      UPST9999 IF NOT EQUAL
.
          MOVE      SCPSITM,SCRITEM         * move to form5
          COMPARE   FORM5,SCRITEM           * still correct item ?
          GOTO      UPST9999 IF NOT EQUAL
.
          COMPARE   TEN,SCPSLEN             * check length is 10
          IF        !@EQUAL
            MOVE      TEN,SCPSLEN
            CALL      UPSCPS2                   * update scr fields posn file
            DISPLAY   *P51:24,*V2LON,SCPSITM;
            ADD       ONE,PSTCOUNT
          ENDIF
          GOTO      UPST1000                * next record
.
UPST9999  RETURN
+
.==============================================================================
.
          INC       STD001IO
.
          INC       SCRMASIO/INC            * Screen master file
          INC       SCRFLDIO/INC            * Screen fields file
          INC       SCRPSTIO/INC            * Screen field positions file
+
.


.*******************************************************************************
.* System    :   Referrals Management                                          *
.* Program   :   FXALLINK                                                      *
.* Desc      :   Loop through ALLENCAF and right justify all alenlink fields   *
.*               that are left justified.                                      *
.*******************************************************************************
.* Date      :   18/09/2013                                                    *
.* Author    :   Ania P                                                        *
.* Function  :   This program will loop the allencaf table and right justify   *
.*               any alenlink field that is left justified.                    *
.* Mods      :                                                                 * 
.*               V10.03.00 18/09/2013 Ania P               CAR 236915          *
.*                         Created Fixit                                       *
.*                                                                             *
.*******************************************************************************
.
          INC       STD001FD
          INC       ALLENCFD/INC
.
.
FILENAME  DIM       26
LOGFILE   FILE      ASCII, FIXED=1200
.
.
. VARIABLES
. -------------------------
.
ALCNEAUD  DIM       1                  * Not used, required for ALLENCIO compile
COUNT     FORM      8                  * Row count
DIM8      DIM       8                  * Formatted row count
FORM8     FORM      8                  * Temp var for right aligning visit no.
HYP       INIT      "-"
STARTKEY  INIT      " ~~~~~~~"         
TXT       INIT      ".txt"
UPDFLAG   FORM      1                  * Display bad rows only or update also?
.
PRGID     INIT      "FXALLINK"
PRGNAM    INIT      "Linked Visit Right-Justifier"
VERSION   INIT      "V12.01.00"
+
.*******************************************************************************
.*                              ML0000                                         *
.*                      Controlling Logic (Mainline code)                      *
.*******************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with fixit
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.*******************************************************************************
.*                                  INIT0000              Called by: ML0000    *
.*                               initialisation                                *
.*******************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
          CALL      IBACLOCK
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"allencaf";
          OPEN      ALLENCA7,"allencaf"
.
          REP       ": ",CTIMEIS
          SQUEEZE   CTIMEIS
          PACK      FILENAME,PRGID,HYP,CYY,CMM,CDD,HYP,CTIMEIS,TXT
          REP       " 0",FILENAME
          PREP      LOGFILE,FILENAME
.
INIT9999  RETURN
+
.*******************************************************************************
.*                                 OPTN0000               Called by: ML0000    *
.*                         get user options or exit                            *
.*                                                                             *
.*    Returns:  EXIT  = FALSE (0)  Run fixit                                   *
.*                      TRUE  (1)  Exit option selected                        *
.*******************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit - Output only":
                    *P1:6,*V2LON,TWO,*HOFF,". Run Fixit - Output & Update"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ":
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000,OPTN9000    * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          ASSIGN    (COPTION-1),UPDFLAG
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*******************************************************************************
.*                                PROC0000                Called by: ML0000    *
.*******************************************************************************
.
PROC0000  DISPLAY   *P1:24,*EL,"Processing: ";
          MOVE      ZERO,COUNT
          MOVE      ZERO,FORM8
.
PROC1000  PACK      KEY24,STARTKEY,SP70
          CALL      RSALENC7
PROC1100  CALL      RKALENC7
          BRANCH    OVRCD,PROC9000
.
          SCAN      " ",ALENLINK
          GOTO      PROC1100 IF NOT EQUAL
.
PROC2000  DISPLAY   *P1:24,*EL,"Updating Visit: ",ALENVISN
          CALL      LOGROW00
          RESET     ALENLINK
          SQUEEZE   ALENLINK
          CLEAR     FORM8
          MOVE      ALENLINK,FORM8
.
          COMPARE   FORM8,ZERO
          IF !@EQUAL 
            MOVE      FORM8,ALENLINK
            CALL      LOGROW00
            ADD       ONE,COUNT
            IF        UPDFLAG=1                            
              CALL      UPALENC7                           * Update Mode
              GOTO      PROC1000
            ENDIF
          ELSE
            WRITE     LOGFILE,SEQ;"*** ROW IGNORED ***"    * Non-numeric visit
          ENDIF           
.
          GOTO      PROC1100
.
PROC9000  MOVE      COUNT,DIM8
          SQUEEZE   DIM8
          DISPLAY   *P1:24,*EL,"Fixit completed. ":
                    *P18:24,"No. of left aligned visits found: ",DIM8
          CALL      HITENTER
.
PROC9999  RETURN
+
.*******************************************************************************
.*                                LOGROW00             Called by: PROC2000     *
.*                        Logs current values of row                           *
.*******************************************************************************
LOGROW00  WRITE     LOGFILE,SEQ;ALENVISN,ALENENCT,ALENCLIN,ALENHCP,ALENSDAT:
                    ALENSTIM,ALENLOCA,ALENLINK,ALENTYPE,ALENSERV,ALENDIUN:
                    ALENDBUN,ALENINUN,ALENIBUN,ALENBULK,ALENRSTA,ALENUDT1:
                    ALENUTM1,ALENUDT2,ALENUTM2,ALENUDT3,ALENUTM3,ALENUDT4:
                    ALENUTM4,ALENUHC1,ALENUHC2,ALENUHC3,ALENUHC4,ALENUDI1:
                    ALENUDI2,ALENUDI3,ALENUDI4,ALENUDI5,ALENUDI6,ALENUYN1:
                    ALENUYN2,ALENUYN3,ALENUYN4,ALENUYN5,ALENUYN6,ALENUYN7:
                    ALENUYN8,ALENUYN9,ALENUFF1,ALENUFF2,ALENUFF3,ALENUC01:
                    ALENUC02,ALENUC03,ALENUC04,ALENUC05,ALENUC06,ALENUC07:
                    ALENUC08,ALENUC09,ALENUC10,ALENUC11,ALENUC12,ALENUC13:
                    ALENUC14,ALENUC15,ALENUC16,ALENUC17,ALENUC18,ALENUC19:
                    ALENUC20,ALENUC21,ALENUC22,ALENUC23,ALENUC24,ALENUC25:
                    ALENUC26,ALENUC27,ALENUC28,ALENUC29,ALENUC30,ALENUC31:
                    ALENUC32,ALENUC33,ALENUC34,ALENUC35,ALENUC36,ALENUC37:
                    ALENUC38,ALENUC39,ALENUC40,ALENUF01,ALENUF02,ALENUF03:
                    ALENUF04,ALENUF05,ALENUF06,ALENUF07,ALENUF08,ALENUF09:
                    ALENUF10,ALENUF11,ALENUF12,ALENUF13,ALENUF14,ALENUF15:
                    ALENUF16,ALENUF17,ALENUF18,ALENUF19,ALENUF20,ALENCMT1:
                    ALENCMT2,ALENCDAT,ALENCTIM,ALENCUID,ALENUDAT,ALENUTIM:
                    ALENUUID,ALENCOMP,ALENUNIQ,ALENSSAC,ALENREVD,ALENCREF:
                    ALENPURC,ALENCONT,ALENMOHR,ALENRHOS,ALENCART,ALENSTRA:
                    ALENOUTC,ALENSRVD,ALENSPAR
.
LOGROW99  RETURN
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       ALLENCIO/INC
.

.*******************************************************************************
.* System         : Waiting List                                               *
.* Program        : IBAHMS71.PBL                                               *
.* Name           : AN-SNAP 10 Most Common SNAP Classes Report                 *
.*******************************************************************************
.* Date           : 30/05/2012                                                 *
.* Author         : Patrick Adair                                              *
.* Function       : AN-SNAP 10 Most Common SNAP Classes Report                 *
.* Modifications  :                                                            *
.*                                                                             *
.* V10.06.01 20/04/2015 Steve Armstrong                             CAR 290369 *
.*                      Recompiled for changes to PMSSNPFD                     *
.*******************************************************************************
.* V10.04.02 13/06/2014 Steve Armstrong                             CAR 301639 *
.*                      Moved call to TFILENAM into INIT0000                   *
.* V10.04.01 20/05/2014 Patrick Adair                               CAR 299626 *
.*                      Reversed print order of temp file                      *
.*******************************************************************************
.* V10.03.02 19/12/2012 Patrick Adair                               CAR 261630 *
.*                      Removed port number from temp file name                *
.* V10.03.01 23/11/2012 Patrick Adair                               CAR 275451 *
.*                      Changed to include/exclude day cases                   *
.* V10.03.00 11/07/2012 Patrick Adair                               CAR 257776 *
.*                      Created Program                                        *
.*******************************************************************************
.
          INC       STD001FD                     * Standard Include
.
          INC       IBACONFD/INC                 * Control File
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       PATCODFD/INC                 * Category Codes
          INC       PATCONFD/INC                 * Control File
          INC       PATDSCFD/INC                 * Patient Discharge File
          INC       PATHSPFD/INC                 * Hospital File
          INC       PATMA1FD/INC                 * Patient Master File
          INC       PATMI1FD/INC                 * Patient Admission File
          INC       PATTRNFD/INC                 * Patient Transfer File
          INC       PATVISFD/INC                 * Patient Visit File
          INC       PMSAPGFD/INC                 * Program Details Ext File
          INC       PMSSNPFD/INC                 * Classification Code File
          INC       PMSUPGFD/INC                 * Program Details File
          INC       PMSVX1FD/INC                 * Patient Visit Ext File
          INC       TFILEVAR/INC                 * Generate Temp File Name 
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
.-------------------------------------------------------------------------------
.         Temporary file 1  - AN-SNAP Classification extract
.
WORK1     IFILE     SQL, FIXED=11, KEY="D1-3,4-5"
.
.Name     Type     Size    Physical   Start       Description
.----     ----     ----    --------   -----       -----------
XXX1SNAP  DIM       3         3         1         AN-SNAP Class Code
XXX1VERS  DIM       2         2         4         AN-SNAP Class Code Version
XXPBDAYS  FORM      6         4         6         Patient bed days
.End of record                         11
.
.-------------------------------------------------------------------------------
.         Temporary file 2  - AN-SNAP Classification Consolidation
.
WORK2     IFILE     SQL, FIXED=24, KEY="U1-6,7-9,10-11"
.
.Name     Type     Size    Physical   Start       Description
.----     ----     ----    --------   -----       -----------
XXNOEPSD  DIM       6         6         1         Number of Epsiodes
XXX2SNAP  DIM       3         3         7         AN-SNAP Class Code
XXX2VERS  DIM       2         2        10         AN-SNAP Class Code Version
XXPCTPTS  FORM      4.2       4        12         % Patients
XXTOTDYS  FORM      6         4        16         Total No of Days
XXAVGLOS  FORM      4.2       4        20         Average length of Stay
.End of record                         24
.
.-------------------------------------------------------------------------------
.                           Variables
.-------------------------------------------------------------------------------
CARECATG  DIM       2                            * category for care type
CMDLINE   DIM       50                           * Command Line
DATEFROM  DIM       8                            * Input From Date
DATETO    DIM       8                            * Input To Date
FNAME1    DIM       8                            * Temporary File 1 name
FNAME2    DIM       8                            * Temporary File 2 name
HOSPAPPR  DIM       10                           * Hospital Approval Number
HOSPCODE  DIM       3                            * Hospital Code
HOSPNAME  DIM       40                           * Hospital Name
INCLDAYC  DIM       1                            * Include Day Cases
.                                                  0 = No
.                                                  1 = Yes
NUMBEPSD  FORM      6                            * No of Episodes/Class
PRINTCNT  FORM      2                            * Count of Records Printed
PRNTDESC  DIM       25                           * Classification Descrpt
RECDKEY5  DIM       5                            * Current record key
SAVEKEY5  DIM       5                            * Processing Record key
SAVEVERS  DIM       2                            * Class Version
SAVESNAP  DIM       3                            * Class Code
TODAY     DIM       8                            * Today's Date
TOTDAYS   FORM      6                            * No of Bed Days/Class
TOTLRECS  FORM      6                            * Total number of episodes
.-------------------------------------------------------------------------------
.                            Variables for NHOSPAY
.-------------------------------------------------------------------------------
CALDAYS   FORM      4
FROMDATE  DIM       8                            * From date (admission)
NBRDAYS   FORM      6                            * No of days in hospital
TODATE    DIM       8                            * To date (disch/date to)
.-------------------------------------------------------------------------------
.                            Constants
.-------------------------------------------------------------------------------
CATA      INIT      "A "
CATCC     INIT      "CC"
UNKNOWNC  INIT      "Unknown SNAP Class Code"
.
PRGID     INIT      "IBAHMS71"
PRGNAM    INIT      "AN-SNAP 10 Most Common SNAP Classes Report"
VERSION   INIT      "V12.01.00"
.
. ******************************************************************************
. *                                  MAIN0000                                  *
. *                                                                            *
. * Process Flow Control                                                       *
. ******************************************************************************
.
MAIN0000  CALL      INIT0000                     * Initialise - begin descent
.
MAIN1000  IF        IBCNMHOS=1
            DISPLAY   *P25:6,*EL:
                      *P25:7,*EL:
                      *P25:8,*EL:
                      *P25:9,*EF;
            CALL      GHOS0000                   * Get Multi Hospital Id
            BRANCH    EXIT,MAIN9999
          ELSE
            DISPLAY   *P1:6,*EL:
                      *P25:7,*EL:
                      *P25:8,*EL:
                      *P25:9,*EF;
            PACK      HOSPAPPR,CAPPRVNO,SP70     * Hospital Approval Number
            PACK      HOSPNAME,CONAME,SP70       * Hospital Name
          ENDIF
.
          CALL      GSTD0000                     * Keyin the start date
          BRANCH    EXIT,MAIN9999                * Nothing input
.
          CALL      GEND0000                     * Keyin the ending date
          BRANCH    EXIT,MAIN1000                * Nothing input
.
          CALL      GDAY0000                     * Keyin Include day cases
          BRANCH    EXIT,MAIN1000                * Nothing input
.
          CALL      GCON0000                     * OK to continue
          BRANCH    EXIT,MAIN1000                * exit if no
.
          CALL      CRET0000                     * Create temporary files
.
          CALL      EXTR0000                     * Extract AN-SNAP Classifictns
          BRANCH    EXIT,MAIN8000                * exit if no records extracted
.
          CALL      CONS0000                     * Consolidate AN-SNAP Class
.
          CALL      PRNT0000                     * Print the report
.
MAIN8000  CALL      KILL0000                     * Delete temporary files
          GOTO      MAIN1000                     * Next run
.
MAIN9999  CHAIN     PGM                          * Return from the nine circles
.
. ******************************************************************************
. *                                 INIT0000              Called by : MAIN0000 *
. *                                                                            *
. * Initialisation procedure                                                   *
. ******************************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA1,"patvisaf"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA2,"patvisaf"
.
          DISPLAY   *P64:24,"pmsapgaf";
          OPEN      PMSAPGA1,"pmsapgaf"
.
          DISPLAY   *P64:24,"pmssnpaf";
          OPEN      PMSSNPA1,"pmssnpaf"
.
          DISPLAY   *P64:24,"pmsupgaf";
          OPEN      PMSUPGA2,"pmsupgaf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.                                                * 1 = Multi Hospital
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.                                                * Hospital Approval Number
          READ      CONTROLF,TEN;*79,CAPPRVNO
.                                                * Hospital Name
          READ      CONTROLF,TWO;*4,CONAME
.
          CALL      IBACLOCK
          PACK      TODAY,CCC,CYY,CMM,CDD
          REP       " 0",TODAY
.
          DISPLAY   *P1:6,*EL,"Hospital Id           : ";
          DISPLAY   *P1:7,"From Date             : ":
                    *P1:8,"To   Date             : ":
                    *P1:9,"Include Day Cases Y/N : ";
.
          MOVE      ZERO,TOTLRECS                * Init no of Episodes
          MOVE      ZERO,PRINTCNT                * Init no of Class's Printed
          MOVE      ZERO,INCLDAYC                * Include Day Cases Flag
          MOVE      SEVENTY,CLNO                 * Init Line Count
          MOVE      ZERO,CPAGENO                 * Init Page Count
          CLOCK     TIME,CTIMEIS                 * Get the starting time
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAME1
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAME2
.
INIT9999  RETURN
.
.*******************************************************************************
.*                                  GHOS0000               Called by: MAIN0000 *
.*                                                                             *
.* Get Hospital Id                                                             *
.*******************************************************************************
GHOS0000  MOVE      ZERO,EXIT
          DISPLAY   *P25:6,*EL:
                    *P25:7,*EL:
                    *P25:8,*EL:
                    *P25:9,*EF;
.
          MOVE      SP70,HOSPCODE
          MOVE      SP70,PTHSHOSP
          MOVE      TWENTY5,ECOL                 * Column of key in
          MOVE      SIX,EVERT                    * Row of key in
          MOVE      SP3,CKYIDEF3                 * Defualt value
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,GHOS98000,GHOS98000
.
          PACK      HOSPCODE,KEY3,SP70           * Hospital Code
          PACK      HOSPAPPR,PTHSAPPR,SP70       * Hospital Approval Number
          PACK      HOSPNAME,PTHSNAME,SP70       * Hospital Name
.
          DISPLAY   *P25:6,*V2LON,PTHSNAME;
          MOVE      ZERO,EXIT
          GOTO      GHOS9999
.
GHOS98000  MOVE     ONE,EXIT
.
GHOS9999  RETURN
.
. ******************************************************************************
. *                                  GSTD0000             Called by : MAIN0000 *
. *                                                                            *
. * Get the start  date of the report                                          *
. *            EXIT = 1   if a nothing was input                               *
. ******************************************************************************
.
GSTD0000  MOVE      ZERO,EXIT
.
          MOVE      SEVEN,CVERT                  * Line for keyin of from date
          MOVE      TWENTY5,CCOL                 * Col for keyin of from date
          MOVE      CVERT,EVERT                  * Col for error message
          MOVE      "40",ECOL                    * Col for error message
          MOVE      ZERO,CHIGHLT                 * No highlight
          MOVE      ONE,CDEFDTE
.
          UNPACK    SP6,CDAY,CMON,CYEAR          * Initialise date
          MOVE      SP8,DATEFROM
          MOVE      CCC,CCENT                    * Initialise century
.
          CALL      KEYDATE                      * Key in the date
          BRANCH    OVRCD,GSTD9000
.
          PACK      DATEFROM,CCENT,CYEAR,CMON,CDAY
          REP       " 0",DATEFROM
          MATCH     DATEFROM,TODAY
          GOTO      GSTD9999 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Date May Not be Greater than Today's ":
                    "Date.  ";
          CALL      HITENTER
          GOTO      GSTD0000
.
GSTD9000  MOVE      ONE,EXIT
.
GSTD9999  RETURN
.
. ******************************************************************************
. *                                  GEND0000             Called by : MAIN0000 *
. *                                                                            *
. * Get the end date of the report                                             *
. *            EXIT = 1   if a nothing was input                               *
. ******************************************************************************
.
GEND0000  MOVE      ZERO,EXIT
.
          MOVE      EIGHT,CVERT                  * Line for keyin of to date
          MOVE      TWENTY5,CCOL                 * Col for keyin of to date
          MOVE      CVERT,EVERT                  * Col for error message
          MOVE      "40",ECOL                    * Col for error message
          MOVE      ZERO,CHIGHLT                 * No highlight
          MOVE      ONE,CDEFDTE
.
          MOVE      SP8,DATETO
.
          CALL      KEYDATE                      * Key in the date
          BRANCH    OVRCD,GEND9000
.
          PACK      DATETO,CCENT,CYEAR,CMON,CDAY
          REP       " 0",DATETO
          MATCH     DATETO,TODAY
          GOTO      GEND2000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Date May Not be Greater than Today's ":
                    "Date.  ";
          CALL      HITENTER
          GOTO      GEND0000
.
GEND2000  MATCH     DATEFROM,DATETO
          GOTO      GEND9999 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"To Date May Not be Less than From ":
                    "Date.  ";
          CALL      HITENTER
          GOTO      GEND0000
.
GEND9000  MOVE      ONE,EXIT
.
GEND9999  RETURN
.
. ******************************************************************************
. *                                  GDAY0000             Called by : MAIN0000 *
. *                                                                            *
. * Include/Exclude Day cases from report?                                     *
. ******************************************************************************
.
GDAY0000  MOVE      ZERO,EXIT
.
GDAY1000  KEYIN     *P25:9,"_",*P25:9,*V2LON,ANS;
.
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
.
          MATCH     ANS,ANSY
          IF        @EQUAL
            MOVE      ONE,INCLDAYC
            GOTO      GDAY9999
          ENDIF
.
          MATCH     ANS,ANSN
          IF        @EQUAL
            MOVE      ZERO,INCLDAYC
            GOTO      GDAY9999
          ENDIF
.
          BEEP
          GOTO      GDAY1000
.
GDAY9999  RETURN
.
. ******************************************************************************
. *                                  GCON0000             Called by : MAIN0000 *
. *                                                                            *
. * Ok. to continue (Y/N)                                                      *
. ******************************************************************************
.
GCON0000  MOVE      ZERO,EXIT
          DISPLAY   *P1:24,*EL,"Ok to Continue (":
                    *V2LON,ANSY,*HOFF,SLASH,*V2LON,ANSN,*HOFF,") ? ";
.
GCON1000  KEYIN     *P24:24,"_",*P24:24,*V2LON,ANS;
.
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
.
          MATCH     ANS,ANSY
          GOTO      GCON9999 IF EQUAL
.
          MATCH     ANS,ANSN
          GOTO      GCON9000 IF EQUAL
.
          BEEP
          GOTO      GCON1000
.
GCON9000  MOVE      ONE,EXIT
.
GCON9999  RETURN
.
. ******************************************************************************
. *                                  CRET0000             Called by : MAIN0000 *
. *                                                                            *
. * Create the temporary files                                                 *
. ******************************************************************************
.
CRET0000  CALL      KILL0000                     * Remove any existing files
.
          CLEAR     CMDLINE                      * Create temporary file 1
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME1,CMDLINE
          APPEND    " 11 D1-3,4-5",CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
          CLEAR     CMDLINE                      * Create temporary file 2
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME2,CMDLINE
          APPEND    " 24 U1-6,7-9,10-11",CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
          OPEN      WORK1,FNAME1                 * Open temporary file 1
          OPEN      WORK2,FNAME2                 * Open temporary file 2
.
CRET9999  RETURN
.
. ******************************************************************************
. *                                  KILL0000             Called by : MAIN0000 *
. *                                                                   CRET0000 *
. * Delete the temporary index files                                           *
. ******************************************************************************
.
KILL0000  CLOSE     WORK1                        * ensure temporary files are
          CLOSE     WORK2                        * closed
.
          CLEAR     CMDLINE                      * remove temporary file 1
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME1,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
          CLEAR     CMDLINE                      * remove temporary file 2
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME2,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
KILL9999  RETURN
.
. ******************************************************************************
. *                                  EXTR0000             Called by : MAIN0000 *
. *                                                                            *
. * Extract all An-SNAP Classifications for the selected period and calculate  *
. * the total number of bed days for each visit                                *
. ******************************************************************************
.
EXTR0000  MOVE      ZERO,EXIT
          MOVE      ZERO,XXPBDAYS
          DISPLAY   *P1:24,*EL,*P10:24,"Adm. No :":
                    *P58:24,"Scanning:";
.
          PACK      KEY31,SP70
          CALL      RSPMUPG2
EXTR1000  CALL      RKPMUPG2
          BRANCH    OVRCD,EXTR9999
.
          MATCH     DATEFROM,PMUGSDAT            * Check program start date
          GOTO      EXTR1000 IF LESS             * is in range
.
          MATCH     PMUGENDD,DATETO              * Check program end date
          GOTO      EXTR1000 IF LESS             * is in date range
.
          PACK      KEY31,PMUGURNO,PMUGATYP,PMUGCLAM,PMUGFUND,PMUGTABT,PMUGEDAT
          CALL      RDPMAPG1
          BRANCH    OVRCD,EXTR1000
.
          CALL      BDYS0000                     * Calculate Bed & Leave Days
.
EXTR2000  MOVE      PMAGVERS,XXX1VERS
          MOVE      PMAGSNAP,XXX1SNAP
          PACK      KEY5,PMAGSNAP,PMAGVERS
          CALL      WRWORK1                      * Write to temporary file 1
.
          ADD       ONE,TOTLRECS
          MOVE      ZERO,XXPBDAYS
          GOTO      EXTR1000
.
EXTR9000  IF        TOTLRECS < 1
            MOVE      ONE,EXIT
          ENDIF
.
EXTR9999  RETURN
.
. ******************************************************************************
. *                                  BDYS0000             Called by : MAIN0000 *
. *                                                                            *
. * Process all visits to identify number of bed days for this program         *
. ******************************************************************************
.
BDYS0000  DISPLAY   *P10:24,"Processing : ";
.
          MOVE      ZERO,XXPBDAYS
.
          READ      CONTROLF,SEVENTY9;*80,PTCNQLDB
          READ      CONTROLF,EIGHTY;*250,PTCNHDPS
          IF        (PTCNHDPS=4 & PTCNQLDB=6)
            MOVE      CATA,CARECATG
          ELSE
            MOVE      CATCC,CARECATG
          ENDIF
.
          PACK      KEY24,PMUGURNO,DATEFROM,SP70
          CALL      RDSVISA2                     * Process patient visits
BDYS1000  CALL      RDKVISA2                     * within the date range
          BRANCH    OVRCD,BDYS9999
.
          MATCH     PVIURNO,PMUGURNO             * end if change of Patient
          GOTO      BDYS9999 IF NOT EQUAL
.
          MATCH     DATEFROM,PVIDATE             * visit before report date
          GOTO      BDYS9999 IF LESS
.
          COMPARE   THREE,PVITYPE                * only process Inpatient
          GOTO      BDYS1000 IF NOT EQUAL
.
          MATCH     PMUGSDAT,PVIDATE             * visit before program start
          GOTO      BDYS1000 IF LESS
.
          MATCH     PMUGENDD,SP70
          IF        !@EQUAL
            MATCH     PVIDATE,PMUGENDD           * visit after program end
            GOTO      BDYS1000 IF LESS
          ENDIF
.
          PACK      KEY8,DPVIBILL,SP70           * check if visit for hospital
          CALL      RDPMVX11
          BRANCH    OVRCD,BDYS1000
.
          IF        IBCNMHOS=1
            MATCH     PMVXMHOS,HOSPCODE
            GOTO      BDYS1000 IF NOT EQUAL
          ENDIF
.
          PACK      KEY8,DPVIBILL,SP70           * Read Admission File
          CALL      RDMISS1
          BRANCH    OVRCD,BDYS9999
.
          MATCH     "A ",CARECATG                * Read relevant category type
          IF        @EQUAL
            PACK      KEY5,CARECATG,ATYPE,SP70
          ELSE
            PACK      KEY5,CARECATG,ACARE,SP70
          ENDIF
.
          CALL      RDCODE1
          BRANCH    OVRCD,BDYS9999
.
          MATCH     "A ",CARECATG                * Check if Rehab Visit
          IF        @EQUAL
            MATCH   "9",TCINDC8
            GOTO    BDYS2000 IF EQUAL
          ELSE
            MATCH   "2",TCINDC6
            GOTO    BDYS2000 IF EQUAL
          ENDIF
.
          GOTO      BDYS1000                     * Get the next visit
.
BDYS2000  PACK      KEY8,DPVIBILL,SP70           * Read Discharge File
          CALL      RDDSCH1
          IF        OVRCD=1
            MOVE      DATETO,TODATE
          ELSE
            MOVE      DDATE,TODATE
          ENDIF
.
          MOVE      ADATE,FROMDATE
          MATCH     TODATE,FROMDATE
          IF        @EQUAL
            MATCH     "0",INCLDAYC               * Exclude Day Visits
            GOTO      BDYS1000 IF EQUAL
            MOVE      1,NBRDAYS                  * Include Day Visits
            GOTO      BDYS3000
          ENDIF
.
          CALL      NHOSPDAY                     * Calcuate no of days in hosp
.
BDYS3000  ASSIGN    NBRDAYS+XXPBDAYS,XXPBDAYS
.
          GOTO      BDYS1000                     * Get the next visit
.
BDYS9999  RETURN
.
. ******************************************************************************
. *                                  CONS0000              Called by: MAIN0000 *
. *                                                                            *
. * Consolidate the contents of the temporary file 1 into temporary file 2     *
. ******************************************************************************
.
CONS0000  MOVE      ZERO,NUMBEPSD
          MOVE      ZERO,TOTDAYS
          PACK      SAVEKEY5,SP70
.
          PACK      KEY5,SP70                    * Position temporary file 1
          CALL      RDSWORK1
CONS1000  CALL      RDKWORK1                     * Get the next record
          BRANCH    OVRCD,CONS9999
.
          PACK      SAVEKEY5,XXX1SNAP,XXX1VERS,SP70
.
CONS2000  ADD       ONE,NUMBEPSD
          ADD       XXPBDAYS,TOTDAYS
.
          CALL      RDKWORK1                     * Get the next record
          IF        OVRCD=1
            PACK      SAVEKEY5,Z70
            GOTO      CONS8000
          ENDIF
.
          PACK      RECDKEY5,XXX1SNAP,XXX1VERS,SP70
          MATCH     RECDKEY5,SAVEKEY5
          GOTO      CONS8000 IF NOT EQUAL
.
          PACK      SAVEVERS,XXX1VERS,SP70
          PACK      SAVESNAP,XXX1SNAP,SP70
          GOTO      CONS2000
.
CONS8000  ASSIGN    NUMBEPSD/TOTLRECS*100,XXPCTPTS
          ASSIGN    TOTDAYS/NUMBEPSD,XXAVGLOS
.
          MOVE      SAVEVERS,XXX2VERS
          MOVE      SAVESNAP,XXX2SNAP
          MOVE      NUMBEPSD,XXNOEPSD
          IF        TOTDAYS > 0
            MOVE      TOTDAYS,XXTOTDYS
          ELSE
            MOVE      ZERO,XXPBDAYS
          ENDIF
.
          PACK      KEY11,XXNOEPSD,XXX2SNAP,XXX2VERS
          CALL      WRWORK2                      * Write to temporary file 2
.
          MATCH     Z70,SAVEKEY5
          GOTO      CONS9999 IF EQUAL
.
          MOVE      RECDKEY5,SAVEKEY5
          MOVE      XXX1VERS,SAVEVERS
          MOVE      XXX1SNAP,SAVESNAP
          MOVE      ZERO,NUMBEPSD
          MOVE      ZERO,TOTDAYS
.
          GOTO      CONS2000
.
CONS9999  RETURN
.
. ******************************************************************************
. *                                  PRNT0000              Called by: MAIN0000 *
. *                                                                            *
. * Print the 10 Most Common SNAP classes from temporary file 2                *
. ******************************************************************************
.
PRNT0000  DISPLAY   *P1:24,*EL,*P10:24,"Printing: ";
.
          PACK      KEY11,Z70                     * Position temporary file 2
          CALL      RDSWORK2
PRNT1000  CALL      RDPWORK2                     * Get the next record
          BRANCH    OVRCD,PRNT9000               * End of file reached
.
          PACK      KEY5,XXX2SNAP,XXX2VERS,SP70
          CALL      RDPMSNP1
          IF        OVRCD=1
            MOVE      UNKNOWNC,PRNTDESC
          ELSE
            MOVE      PMSNDESC,PRNTDESC
          ENDIF
.
          DISPLAY   *P20:24,*V2LON,XXX2SNAP," ",XXX2VERS;
.
PRNT5000  COMPARE   "60",CLNO
          CALL      HEAD0000 IF NOT LESS
.
PRNT5400  PRINT     *1,"|",*4,XXX2SNAP,*8,XXX2VERS,*11,PRNTDESC:
                    *38,"|",*41,XXNOEPSD:
                    *61,"|",*64,XXPCTPTS:
                    *79,"|",*82,XXTOTDYS:
                    *101,"|",*103,XXAVGLOS:
                    *132,"|"
          ADD       ONE,CLNO
.
          ADD       ONE,PRINTCNT
.                                                * Check if 10 have been printed
          IF        PRINTCNT = 10
            GOTO      PRNT9000
          ENDIF
.
          GOTO      PRNT1000                     * Get next record
.
PRNT9000  IF        CPAGENO > 0
            CALL      UND132
            PRINT     *N,*N,"*** End of Report ***"
          ENDIF
.
PRNT9999  RETURN
.
. ******************************************************************************
. *                                  HEAD0000              Called by: PRNT0000 *
. *                                                                            *
. * Print Report Heading                                                       *
. ******************************************************************************
.
HEAD0000  CALL      HEAD132                      * Standard heading
.
          UNPACK    DATEFROM,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PRINT     *40,"Hospital : ",HOSPNAME
.
          PRINT     *N,*38,"Date Range : ",CPCDATE," to ";
.
          UNPACK    DATETO,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PRINT     CPCDATE,*N
.
          CALL      UND132
          PRINT     *1,"|  AN-SNAP Classification Code":
                    *38,"|  Number of Episodes":
                    *61,"|  % of Patients":
                    *79,"|  Number of Bed Days":
                    *101,"|  Average Length of Stay":
                    *132,"|"
          CALL      UND132
          ADD       FIVE,CLNO
.
HEAD9999  RETURN
.
. ******************************************************************************
. * I/O routines for temporary files                                           *
. ******************************************************************************
.
RDSWORK1  READ      WORK1,KEY5;;
          RETURN
.
RDKWORK1  MOVE      ZERO,OVRCD
          READKS    WORK1;XXX1SNAP,XXX1VERS,XXPBDAYS
          GOTO      OVERCOND IF OVER
          RETURN
.
RDWORK1   MOVE      ZERO,OVRCD
          READ      WORK1,KEY5;XXX1SNAP,XXX1VERS,XXPBDAYS
          GOTO      OVERCOND IF OVER
          RETURN
.
WRWORK1   WRITE     WORK1,KEY5;KEY5,XXPBDAYS
          RETURN
.
RDSWORK2  READ      WORK2,KEY11;;
          RETURN
.
RDPWORK2  MOVE      ZERO,OVRCD
          READKP    WORK2;XXNOEPSD,XXX2SNAP,XXX2VERS,XXPCTPTS,XXTOTDYS:
                          XXAVGLOS
          GOTO      OVERCOND IF OVER
          RETURN
.
RDWORK2   MOVE      ZERO,OVRCD
          READ      WORK2,KEY11;XXNOEPSD,XXX2SNAP,XXX2VERS,XXPCTPTS,XXTOTDYS:
                               XXAVGLOS
          GOTO      OVERCOND IF OVER
          RETURN
.
WRWORK2   WRITE     WORK2,KEY11;KEY11,XXPCTPTS,XXTOTDYS,XXAVGLOS
          RETURN
.
.*******************************************************************************
.
          INC       STD001IO                     * System Standard Routines
.
          INC       CLPATHSP                     * Clear Hospital Table variables
          INC       TFILENAM                     * Generate Temp File Name
          INC       NHOSPDAY                     * Calc no days in Hospital
          INC       PATHSPKY                     * Keyin of a Hospital Id/code
.
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       PATCODIO/INC                 * Category Codes
          INC       PATDSCIO/INC                 * Patient Discharge File
          INC       PATHSPIO/INC                 * Hospital File
          INC       PATMA1IO/INC                 * Patient Master File
          INC       PATMI1IO/INC                 * Patient Admission File
          INC       PATTRNIO/INC                 * Patient Transfer File
          INC       PATVISIO/INC                 * Patient Visit File
          INC       PMSAPGIO/INC                 * Program Details Ext File
          INC       PMSSNPIO/INC                 * Classification Code File
          INC       PMSUPGIO/INC                 * Program Details File
          INC       PMSVX1IO/INC                 * Patient Visit Ext File
          INC       WEBERRIO/INC                 * Web Server Error Logging

. **********************************************************
. *              P R O G R A M  S U M M A R Y              *
. *              -------------  -------------              *
. *                                                        *
. *     PROGRAM NAME:     IBAHOS85                         *
. *                                                        *
. *     FUNCTION:         IN-PATIENT STATISTICS REPORT     *
. *                                                        *
. *     DATE WRITTEN:     20/10/88                         *
. *                                                        *
. *     AUTHOR:           JENI TAN      (I.B.A)            *
. *                                                        *
. *     MODIFICATIONS:                                     ********************
.* V10.03.02  02/01/2013 Patrick Adair                              CAR 261630 *
.*                       Removed port number from temp file name               *
.*        V10.03.01 18/10/2012  Jill Parkinson CAR 273375                     *
.*                  Recompiled for DATECONV - first day of year calculation   *
.*        V9.02.01  24/10/2001  B.G.Ackland                                   *
.*                  Remove pi on Temp File                                    *
.*        V5.07.01  03/02/1999  Nicole Harrington                             *
.*                  507 rebounds                                              *
.*        V5.07.B02 28/01/1999  Nicole Harrington                             *
.*                  507 rebounds                                              *
.*        V5.07.B01 14/11/1998  Davin                                         *
.*                  Mods for 507                                              *
.*        V5.04.01  02/05/1997  Steve Armstrong   SRF 642857                  *
.*                  Changed temp file name "pstidx" to standard format        *
.******************************************************************************
.*                        V5.01 11.05.89 S.Barcham         *
. *                             Fix 1630 I * L             *
. *                             SRF100516                  *
. *                       V5.02 17.06.89 S.Barcham         *
. *                             Compiled for new standbys  *
. *                             SRF 101101                 *
. *                    V5.01.01 03.07.89 K.Peak            *
. *                             Changed Adm & U/R to 8 chrs*
. *                             19/07/89 Graeme Williams   *
. *                             Allow for 13x4 week periods*
. *                    V5.01.02 05.01.90 K.Peak            *
. *                             Fixed the bed total column *
. *                             to add up to the sub-total *
. *                             SRF 103506                 *
. *                    V5.01.03 25/06/90 K.Peak            *
. *                             Changed the sub & grand    *
. *                             total to a form4 from a    *
. *                             form3, & removed the print *
. *                             of lines if all totals     *
. *                             equal zero                 *
. *                    V5.01.04 28/06/90 J.Tan  SRF 104921 *
. *                             Added separation bed days  *
. *                             field in patmastsf file    *
. *                    V5.01.05 23/05/91 J.Tan  SRF 108541 *
. *                             Fixed getting data for the *
. *                             year                       *
. *                    V5.01.06 31/08/92 J.Tan  SRF 116789 *
. *                             Fixed to use financial     *
. *                             period month for the totals*
. *                    V5.01.07 02/06/93 J.Tan  SRF 121660 *
. *                             Added ward/bed history file*
. *                    V5.01.08 30/12/93 I.Rutt MTA        *
. *                             Added new include PATACDAY ********************
. *        V5.02.001 02/03/95 Paul Howells     SRF 135026                     *
. *                  Fixed the use of PATWBHFD file.                          *
. *        V5.03.01  15/04/1996  Steve Armstrong                              *
. *                  Mods to use WDACTVDY to calculate active bed days.       *
. *                  Mods to use PTCNH82W.                                    *
. *****************************************************************************
.
          INC       STD001FD
          INC       PATCOMM/INC
.
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       PATCODFD/INC    * Codes file
          INC       PATDRGFD/INC    * Period Range file
          INC       PATMSTFD/INC    * Monthly inpatient statistics file
          INC       PATWR1FD/INC    * Ward file
          INC       PATWBHFD/INC    * Ward/bed history file
          INC       WDACTVVR/INC
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
.----------------------------------------------------------------
.Name     Type     Length     Physical      Start
.----     ----     ------     --------      -----
.MSDCLS   DIM          3            3          1
.MSWCLS   DIM          3            3          4
.MSWARD   DIM          3            3          7
.XCENT    DIM          2            2         10
.XYEAR    DIM          2            2         12
INBNUM    FORM         3            3         14
INP01     FORM         8            5         17
INP02     FORM         8            5         22
INP03     FORM         8            5         27
INP04     FORM         8            5         32
INP05     FORM         8            5         37
INP06     FORM         8            5         42
INP07     FORM         8            5         47
INP08     FORM         8            5         52
INP09     FORM         8            5         57
INP10     FORM         8            5         62
INP11     FORM         8            5         67
INP12     FORM         8            5         72
INP13     FORM         8            5         77
CHNGFLG   FORM         1            2         82
.
.  END OF RECORD                              84
.----------------------------------------------------------------
TEMPFILE  IFILE     SQL, FIXED=84, KEY="U1-9"
.
ASKFLAG   FORM      1             * asterisk printed flag
.                                   0 = asterisk printed
.                                   1 = asterisk not printed
CODECG    INIT      "CG"
CMDLINE   DIM       50
COUNT     FORM      2
CHKZER    FORM      8
.
DIM4      DIM       4
DIM8      DIM       8
ENDFLG    FORM      1
.
FNAMET    DIM       8
FIRST     FORM      1
FMON      FORM      2
FORM8     FORM      8
FROMMTH   DIM       6
FROMDATE  DIM       8
FINYEAR   DIM       4
.
SAVDESC   DIM       20
SHRT01    FORM      7
SHRT02    FORM      7
SHRT03    FORM      7
SHRT04    FORM      7
SHRT05    FORM      7
SHRT06    FORM      7
SHRT07    FORM      7
SHRT08    FORM      7
SHRT09    FORM      7
SHRT10    FORM      7
SHRT11    FORM      7
SHRT12    FORM      7
SHRT13    FORM      7
.
GRTP01    FORM      8
GRTP02    FORM      8
GRTP03    FORM      8
GRTP04    FORM      8
GRTP05    FORM      8
GRTP06    FORM      8
GRTP07    FORM      8
GRTP08    FORM      8
GRTP09    FORM      8
GRTP10    FORM      8
GRTP11    FORM      8
GRTP12    FORM      8
GRTP13    FORM      8
GRTWRD    FORM      8
GRTBED    FORM      3
GTOTBED   FORM      4
.
INWRD     FORM      8
INPAT     FORM      8
.
.
NUMPERS   FORM     2
NOWYEAR   DIM      4
ENDYEAR   DIM      4
STRTYEAR  DIM      4
OPCND     FORM     1
MONTH     DIM      104
MONTH12   DIM      108
PTCNH82W  FORM     1
.
SAVWARD   DIM      3
SAVCLS    DIM      3
STATUS    FORM     1
SUBFLG    FORM     1
STOTBED   FORM     4
SVHNOCC   FORM     3
.
TIMEIS    DIM      8
TMPCENT   DIM      2
TMPYEAR   DIM      2
TODATE    DIM      8
TOMTH     DIM      6
TOTLBED   FORM     3
TOTP01    FORM     8
TOTP02    FORM     8
TOTP03    FORM     8
TOTP04    FORM     8
TOTP05    FORM     8
TOTP06    FORM     8
TOTP07    FORM     8
TOTP08    FORM     8
TOTP09    FORM     8
TOTP10    FORM     8
TOTP11    FORM     8
TOTP12    FORM     8
TOTP13    FORM     8
TOTWRD    FORM     8
TOJULDD   FORM     3
TOJULYY   FORM     2
TOJULCC   FORM     2
WARBEDAC  FORM     8.2
WARTOTBD  FORM     6
WKDATE    DIM      8
.
ZFOUR     INIT     "ZZZZ"
.
VERSION   INIT      "V12.01.00"
PRGID     INIT     "IBAHOS85"
PRGNAM    INIT      "In-patient Statistics Report"
+
.         START OF PROGRAM
.
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdrgaf";
          OPEN      PATDRGA1,"patdrgaf"
.
          DISPLAY   *P64:24,"patmstsf";
          OPEN      PATMSTS1,"patmstsf"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY   *P64:24,"patwbhaf";
          OPEN      PATWBHA1,"patwbhaf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TWENTY;*250,PTCNH82W
.
.         Get the current financial period
.
          PACK      CPTDATE,CCC,CYY,CMM,CDD
          REP       " 0",CPTDATE
          CALL      GPERD
          BRANCH    EXIT,NOCPER
.
          MOVE      DRGYR,FINYEAR
.
          GOTO      STUPFL
.
NOCPER    KEYIN     *P1:24,*EL,*V2LON,*B:
                    "Current date not found in Period file.  ";
          CALL      HITENTER
          GOTO      ENDIT
.
.         Set up temporary file names
.
STUPFL    CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAMET
.
.         SELECT OPTION
.
START     DISPLAY   *P1:4,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = In-Patient Admission":
                    *P1:6,*V2LON,TWO,*HOFF," = In-Patient Bed Days"
.
KEYOPT    KEYIN     *P1:8,"Option : ",*EF,*V2LON,OPTION
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
BRNCH     BRANCH    OPTION OF INPSTAT,INPSTAT
          BEEP
          GOTO      KEYOPT
.
.         Input the financial year
.
INPSTAT   MOVE      FINYEAR,NOWYEAR 
.
          DISPLAY   *P1:10,*EF,"Year : ",*V2LON,NOWYEAR
.
          KEYIN     *P8:10,*V2LON,*RV,*JR,*DE,NOWYEAR
.
          RESET     NOWYEAR
          GOTO      START IF EOS
.
          UNPACK    NOWYEAR,TMPCENT,TMPYEAR
          MATCH     SP2,TMPCENT
          IF        @EQUAL
            PACK      NOWYEAR,CCC,TMPYEAR
            DISPLAY   *P8:10,*V2LON,NOWYEAR
          ENDIF
.
.         Check if dates are OK or not
.
REKEY     KEYIN     *P1:24,*EF,"Ok to continue (":
                    *V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
.
          MATCH     "N" TO ANS
          GOTO      KEYOPT IF EQUAL
.
          MATCH     "Y" TO ANS
          GOTO      OKPROC IF EQUAL
          BEEP
          GOTO      REKEY
.
.         Check the tempfile, remove if it exists
.
OKPROC    DISPLAY   *P1:24,*EL,*P10:24,"** Processing Report **",*W;
.
          CALL      KILLIDX
          CALL      CREAIDX                     * Creating tempfile
.
          OPEN      TEMPFILE,FNAMET
          MOVE      ZERO,TOTLBED
          MOVE      ZERO,GTOTBED
.
.         Workout the starting month and ending month for the financial year
.
          PACK      KEY6 WITH NOWYEAR,SP2
          CALL      RDSDRGA1
          CALL      RDKDRGA1
.
          MOVE      DRGFDTE,ACTVFDTE
          PACK      FROMMTH,DRGYR,DRGNUM
          UNPACK    DRGCYR,STRTYEAR
.
.         Loop over the file and get the descriptions, number of periods
.         and find the last period of the year
.
          PACK      TOMTH,DRGYR,DRGNUM
          UNPACK    DRGCYR,ENDYEAR
          CLEAR     MONTH
          CLEAR     MONTH12
          APPEND    "|     ",MONTH12
          APPEND    DRGSDSC,MONTH12
          APPEND    "|    ",MONTH
          APPEND    DRGSDSC,MONTH
          MOVE      ONE,NUMPERS
.
NEXTM     CALL      RDKDRGA1
          BRANCH    OVRCD,STRTLOOP
.
          MATCH     DRGYR,NOWYEAR
          GOTO      STRTLOOP IF NOT EQUAL
.
          APPEND    "|     ",MONTH12
          APPEND    DRGSDSC,MONTH12
          APPEND    "|    ",MONTH
          APPEND    DRGSDSC,MONTH
          MOVE      DRGTDTE,ACTVTDTE
          PACK      TOMTH,DRGYR,DRGNUM
          UNPACK    DRGCYR,ENDYEAR
          ADD       ONE,NUMPERS
          GOTO      NEXTM
.
STRTLOOP  RESET     MONTH
          RESET     MONTH12
.
.         Start loop over the statistics file
.
          DISPLAY   *P1:24,*EL,*P25:24,"Ward:",*W;
          MOVE      SP20,KEY15
          CALL      RDSMSTS1
NXTMST    CALL      RDKMSTS1
          BRANCH    OVRCD OF GENREP
.
.         Check if the month is in the financial year
.
          MATCH     FROMMTH,MSDATE
          GOTO      NXTMST IF LESS
.
          MATCH     MSDATE,TOMTH
          GOTO      NXTMST IF LESS
.
          MOVE      MSDATE,KEY6
          CALL      RDDRGA1
          BRANCH    OVRCD,NXTMST
.
          DISPLAY   *P35:24,*V2LON,DRGCNUM,*HOFF,"/",*V2LON,DRGCYR:
                    *P45:24,MSWARD;
.
          BRANCH    OPTION OF ADOPT,BDOPT
.
.        Add up number of patients for each month
.
ADOPT     MOVE      MSNPRAD,INPAT
          ADD       MSNPBAD,INPAT
          ADD       MSBPRAD,INPAT
          ADD       MSBPBAD,INPAT
          GOTO      WRTMP
.
.         Add up number of bed days for each month
.
BDOPT     MOVE      MSNBDAY,INPAT
          ADD       MSBBDAY,INPAT
.
WRTMP     MOVE      DRGNUM,FMON
.
          PACK      KEY9 WITH MSDCLS,MSWCLS,MSWARD
          CALL      RDTEMP1
          BRANCH    OVRCD OF NEWTMP
.
          LOAD      FORM8 USING FMON OF INP01,INP02,INP03,INP04,INP05:
                    INP06,INP07,INP08,INP09,INP10,INP11,INP12:
                    INP13
.
          ADD       INPAT,FORM8
          STORE     FORM8 USING FMON OF INP01,INP02,INP03,INP04,INP05:
                    INP06,INP07,INP08,INP09,INP10,INP11,INP12:
                    INP13
          CALL      UPTEMP1
          GOTO      NXTMST
.
.         New record
.
NEWTMP    CALL      INTTMP
.
.         Get number of beds in the ward
.
          PACK      KEY6 WITH MSWARD,SP3
          CALL      RDWARD1
          BRANCH    OVRCD,NXTMST
.
          MOVE      ONE,ACTVWFLG                 * don't need weekends
          PROC      WDACTVDY                     * get days ward was active
          COMPARE   ZERO,ACTVDAYS                * ward active during period ?
          IF        !@EQUAL
            MOVE      TOTBED,INBNUM              * yes - update bed count
            MOVE      ACTVBCHG,CHNGFLG           * set change flag
          ENDIF
.
          STORE     INPAT,FMON,INP01,INP02,INP03,INP04,INP05:
                    INP06,INP07,INP08,INP09,INP10,INP11,INP12:
                    INP13
          CALL      WRTEMP1
          GOTO      NXTMST
.
.         Initialize the temp variables
.
INTTMP    MOVE      ZERO,INP01
          MOVE      ZERO,INP02
          MOVE      ZERO,INP03
          MOVE      ZERO,INP04
          MOVE      ZERO,INP05
          MOVE      ZERO,INP06
          MOVE      ZERO,INP07
          MOVE      ZERO,INP08
          MOVE      ZERO,INP09
          MOVE      ZERO,INP10
          MOVE      ZERO,INP11
          MOVE      ZERO,INP12
          MOVE      ZERO,INP13
          MOVE      ZERO,CHNGFLG
          MOVE      ZERO,INBNUM
          RETURN
+
GENREP    DISPLAY   *P1:24,*EL,*P10:24,"** Generating Report **",*W;
          MOVE      ONE,ASKFLAG
.
          CLOSE     TEMPFILE
          TRAP      NOOPEN IF IO
          OPEN      TEMPFILE,FNAMET
          TRAPCLR   IO
.
          DISPLAY   *P1:12,*EF
          BRANCH    OPCND OF ENDP
.
          MOVE      ZERO,CPAGENO
          CLOCK     TIME,TIMEIS
          CALL      HEAD
          MOVE      SP3,SAVCLS
          MOVE      ZERO,FIRST
          MOVE      ZERO,SUBFLG
          MOVE      ZERO,ENDFLG
          CALL      INTVAR
.
          DISPLAY   *P1:24,*EL,*P25:24,"Ward :";
          MOVE      SP10,KEY9
          CALL      RDSTEMP1
READLOP   CALL      RDKTEMP1
          COMPARE   ZERO,OVRCD
          GOTO      READL10 IF EQUAL
.
.         End of record, check if it is neccesary to print sub total
.
          COMPARE   ZERO,SUBFLG
          GOTO      ENDP IF EQUAL
.
          MOVE      ONE,ENDFLG
          GOTO      PRTSUB
.
READL10   COMPARE   ZERO,FIRST
          GOTO      NOSUB IF EQUAL
.
.         Check if it is a new ward class
.
          MATCH     MSWCLS,SAVCLS
          GOTO      SAMECLS IF EQUAL
.
          DISPLAY   *P35:24,*EL,*V2LON,XMON,*HOFF,"/",*V2LON,XCENT,XYEAR:
                    *P45:24,MSWARD;
.
.         Printing the sub-total of each ward class
.
PRTSUB    MOVE      TOTP01,TOTWRD
          ADD       TOTP02,TOTWRD
          ADD       TOTP03,TOTWRD
          ADD       TOTP04,TOTWRD
          ADD       TOTP05,TOTWRD
          ADD       TOTP06,TOTWRD
          ADD       TOTP07,TOTWRD
          ADD       TOTP08,TOTWRD
          ADD       TOTP09,TOTWRD
          ADD       TOTP10,TOTWRD
          ADD       TOTP11,TOTWRD
          ADD       TOTP12,TOTWRD
          ADD       TOTP13,TOTWRD
.
          CALL      PRTLIN1
.
          COMPARE   TEN2,NUMPERS
          GOTO      PRTS10 IF EQUAL
.
.         Print 13 periods
.
          MOVE      TOTP01,SHRT01
          MOVE      TOTP02,SHRT02
          MOVE      TOTP03,SHRT03
          MOVE      TOTP04,SHRT04
          MOVE      TOTP05,SHRT05
          MOVE      TOTP06,SHRT06
          MOVE      TOTP07,SHRT07
          MOVE      TOTP08,SHRT08
          MOVE      TOTP09,SHRT09
          MOVE      TOTP10,SHRT10
          MOVE      TOTP11,SHRT11
          MOVE      TOTP12,SHRT12
          MOVE      TOTP13,SHRT13
          ADD       STOTBED,GTOTBED
.
          PRINT     *1,"| Sub-Tot",*11,"| ",*12,STOTBED:
                    *18,"|",SHRT01,*26,"|",SHRT02:
                    *34,"|",SHRT03,*42,"|",SHRT04:
                    *50,"|",SHRT05,*58,"|",SHRT06:
                    *66,"|",SHRT07,*74,"|",SHRT08:
                    *82,"|",SHRT09,*90,"|",SHRT10:
                    *98,"|",SHRT11,*106,"|",SHRT12:
                    *114,"|",SHRT13,*122,"|",TOTWRD,*132,"|"
          GOTO      PRTS20
.
.         Print twelve months
.
PRTS10    PRINT     *1,"|Sub-Tot",*9,"|",*10,TOTLBED:
                    *14,"|",TOTP01,*23,"|",TOTP02:
                    *32,"|",TOTP03,*41,"|",TOTP04:
                    *50,"|",TOTP05,*59,"|",TOTP06:
                    *68,"|",TOTP07,*77,"|",TOTP08:
                    *86,"|",TOTP09,*95,"|",TOTP10:
                    *104,"|",TOTP11,*113,"|",TOTP12,*122,"|",TOTWRD,*132,"|"
.
PRTS20    COMPARE   FIFTY3,CLNO
          IF        @LESS
            CALL      PRTLINE
          ELSE
            CALL      HEAD
          ENDIF
          CALL      ACCGRT
          CALL      INTTOT
          MOVE      ZERO,SUBFLG
          BRANCH    ENDFLG,ENDP
.
.         Get the ward class description
.
NOSUB     MOVE      "Unknown Ward Class  ",TDESC
.
          MATCH     SP3,MSWCLS
          GOTO      NOCLS IF EQUAL
.
          PACK      KEY5 WITH CODECG,MSWCLS
          CALL      RDCODE1
.
NOCLS     MOVE      TDESC,SAVDESC
          PRINT     *1,"|",TDESC,*132,"|"
          MOVE      MSWCLS,SAVCLS
          CALL      PRTLIN1
          ADD       ONE,CLNO
.
.         Print each ward details
.
SAMECLS   MOVE      INP01,CHKZER
          ADD       INP02,CHKZER
          ADD       INP03,CHKZER
          ADD       INP04,CHKZER
          ADD       INP05,CHKZER
          ADD       INP06,CHKZER
          ADD       INP07,CHKZER
          ADD       INP08,CHKZER
          ADD       INP09,CHKZER
          ADD       INP10,CHKZER
          ADD       INP11,CHKZER
          ADD       INP12,CHKZER
          ADD       INP13,CHKZER
.
          COMPARE   ZERO,CHKZER
          IF        @EQUAL
            COMPARE   ZERO,PTCNH82W
            GOTO      SAMEC30 IF EQUAL
          ENDIF
.
          MOVE      INP01,INWRD
          ADD       INP02,INWRD
          ADD       INP03,INWRD
          ADD       INP04,INWRD
          ADD       INP05,INWRD
          ADD       INP06,INWRD
          ADD       INP07,INWRD
          ADD       INP08,INWRD
          ADD       INP09,INWRD
          ADD       INP10,INWRD
          ADD       INP11,INWRD
          ADD       INP12,INWRD
          ADD       INP13,INWRD
.
          COMPARE   "55",CLNO
          IF        !@LESS
            CALL      HEAD
            PRINT     *1,"|",SAVDESC,"(contd.)",*132,"|"
            CALL      PRTLIN1
          ENDIF
.
          COMPARE   TEN2,NUMPERS
          GOTO      SAMEC10 IF EQUAL
.
.         Print 13 periods
.
          MOVE      INP01,SHRT01
          MOVE      INP02,SHRT02
          MOVE      INP03,SHRT03
          MOVE      INP04,SHRT04
          MOVE      INP05,SHRT05
          MOVE      INP06,SHRT06
          MOVE      INP07,SHRT07
          MOVE      INP08,SHRT08
          MOVE      INP09,SHRT09
          MOVE      INP10,SHRT10
          MOVE      INP11,SHRT11
          MOVE      INP12,SHRT12
          MOVE      INP13,SHRT13
          ADD       INBNUM,STOTBED
. 
          PRINT     *1,"| ",MSWARD,*11,"| ",INBNUM;
          IF        CHNGFLG = 1
            MOVE      ZERO,ASKFLAG
            PRINT     *15,"*";
          ENDIF
          PRINT     *18,"|",SHRT01,*26,"|",SHRT02:
                    *34,"|",SHRT03,*42,"|",SHRT04:
                    *50,"|",SHRT05,*58,"|",SHRT06:
                    *66,"|",SHRT07,*74,"|",SHRT08:
                    *82,"|",SHRT09,*90,"|",SHRT10:
                    *98,"|",SHRT11,*106,"|",SHRT12:
                    *114,"|",SHRT13,*122,"|",INWRD,*132,"|"
          GOTO      SAMEC20
.
.         Print 12 months
.
SAMEC10   ADD       INBNUM,TOTLBED
.
          PRINT     *1,"| ",MSWARD,*9,"|",INBNUM;
          IF        CHNGFLG = 1
            MOVE      ZERO,ASKFLAG
            PRINT     *13,"*";
          ENDIF
          PRINT     *14,"|",INP01,*23,"|",INP02:
                    *32,"|",INP03,*41,"|",INP04:
                    *50,"|",INP05,*59,"|",INP06:
                    *68,"|",INP07,*77,"|",INP08:
                    *86,"|",INP09,*95,"|",INP10:
                    *104,"|",INP11,*113,"|",INP12,*122,"|",INWRD,*132,"|"
.
SAMEC20   ADD       ONE,CLNO
          CALL      ACCTOT
SAMEC30   MOVE      ONE,FIRST
          MOVE      ONE,SUBFLG
          GOTO      READLOP
.
.         OVER CONDITION FOUND..PRINT LAST LINE OF -- AND DISPLAY MESSAGE
.
ENDP      COMPARE   ZERO,CPAGENO
          GOTO      NODATA IF EQUAL
.
.         Print the grand-total
.
          COMPARE   TEN2,NUMPERS
          GOTO      ENDP0 IF EQUAL
.
.         Print 13 periods
.
          MOVE      GRTP01,SHRT01
          MOVE      GRTP02,SHRT02
          MOVE      GRTP03,SHRT03
          MOVE      GRTP04,SHRT04
          MOVE      GRTP05,SHRT05
          MOVE      GRTP06,SHRT06
          MOVE      GRTP07,SHRT07
          MOVE      GRTP08,SHRT08
          MOVE      GRTP09,SHRT09
          MOVE      GRTP10,SHRT10
          MOVE      GRTP11,SHRT11
          MOVE      GRTP12,SHRT12
          MOVE      GRTP13,SHRT13
.
          PRINT     *1,"| Total",*11,"| ",*12,GTOTBED:
                    *18,"|",SHRT01,*26,"|",SHRT02:
                    *34,"|",SHRT03,*42,"|",SHRT04:
                    *50,"|",SHRT05,*58,"|",SHRT06:
                    *66,"|",SHRT07,*74,"|",SHRT08:
                    *82,"|",SHRT09,*90,"|",SHRT10:
                    *98,"|",SHRT11,*106,"|",SHRT12:
                    *114,"|",SHRT13,*122,"|",GRTWRD,*132,"|"
          GOTO      ENDP1
.
.         Print 12 months
.
ENDP0     PRINT     *1,"!Total",*9,"!",*10,GRTBED:
                    *14,"!",GRTP01,*23,"!",GRTP02:
                    *32,"!",GRTP03,*41,"!",GRTP04:
                    *50,"!",GRTP05,*59,"!",GRTP06:
                    *68,"!",GRTP07,*77,"!",GRTP08:
                    *86,"!",GRTP09,*95,"!",GRTP10:
                    *104,"!",GRTP11,*113,"!",GRTP12,*122,"!",GRTWRD,*132,"!"
.
ENDP1     CALL      PRTLINE
.
          IF        ASKFLAG = 0
            PRINT     *N,"* - Some wards changed number of `Beds' during ":
                      "period."
          ENDIF
          PRINT     *N,"*** End Of Report ***"
          DISPLAY   *P1:24,*EL,*P40:24,*V2LON:
                    "** Report Generation Completed **",*W2;
.
           CALL     KILLIDX
           GOTO     START
.
NODATA    DISPLAY   *P20:24,*EL,*B,*V2LON:
                    "** No Available Data on File **",*W2;
          CALL      KILLIDX
          GOTO      START
.
NOOPEN    MOVE      ONE,OPCND
          DISPLAY   *P1:24,*B,*EL,"** TEMPORARY FILE ":
                    FNAMET," NOT FOUND **",*W2;
          RETURN
.
.         Accumulate for the sub-total
.
ACCTOT    ADD       INP01,TOTP01
          ADD       INP02,TOTP02
          ADD       INP03,TOTP03
          ADD       INP04,TOTP04
          ADD       INP05,TOTP05
          ADD       INP06,TOTP06
          ADD       INP07,TOTP07
          ADD       INP08,TOTP08
          ADD       INP09,TOTP09
          ADD       INP10,TOTP10
          ADD       INP11,TOTP11
          ADD       INP12,TOTP12
          ADD       INP13,TOTP13
          ADD       INWRD,TOTWRD
          RETURN
.
.         Accumulate for the grand-total
.
ACCGRT    ADD       TOTP01,GRTP01
          ADD       TOTP02,GRTP02
          ADD       TOTP03,GRTP03
          ADD       TOTP04,GRTP04
          ADD       TOTP05,GRTP05
          ADD       TOTP06,GRTP06
          ADD       TOTP07,GRTP07
          ADD       TOTP08,GRTP08
          ADD       TOTP09,GRTP09
          ADD       TOTP10,GRTP10
          ADD       TOTP11,GRTP11
          ADD       TOTP12,GRTP12
          ADD       TOTP13,GRTP13
          ADD       TOTWRD,GRTWRD
          RETURN
.
.         Initialize the grand-total variables
.
INTVAR    MOVE      ZERO,GRTBED
          MOVE      ZERO,GRTWRD
          MOVE      ZERO,COUNT
.
NXTVAR    ADD       ONE,COUNT
          STORE     ZERO USING COUNT OF GRTP07,GRTP08,GRTP09,GRTP10,GRTP11:
                           GRTP12,GRTP01,GRTP02,GRTP03,GRTP04,GRTP05,GRTP06:
                           GRTP13
          COMPARE   TEN3,COUNT
          GOTO      NXTVAR IF NOT EQUAL
.
.         Initialize the sub-total variables
.
INTTOT    MOVE      ZERO,STOTBED
          MOVE      ZERO,TOTLBED
          MOVE      ZERO,TOTWRD
          MOVE      ZERO,COUNT
.
NXTTOT    ADD       ONE,COUNT
          STORE     ZERO USING COUNT OF TOTP07,TOTP08,TOTP09,TOTP10,TOTP11:
                           TOTP12,TOTP01,TOTP02,TOTP03,TOTP04,TOTP05,TOTP06:
                           TOTP13
.
          COMPARE   TEN3,COUNT
          GOTO      NXTTOT IF NOT EQUAL
          RETURN
.
.         LAST LINE ON THE CODE
.
PRTLINE   PRINT     *1,"*-----------------------------------------":
                       "-------------------------------------------":
                       "----------------------------------------------*"
          ADD       ONE,CLNO
          RETURN
.
PRTLIN1   PRINT     *1,"|-----------------------------------------":
                       "-------------------------------------------":
                       "----------------------------------------------|"
          ADD       ONE,CLNO
          RETURN
.
.         HEADINGS FOR OUTPUT
.
HEAD      COMPARE   ZERO,CPAGENO
          CALL      PRTLINE IF NOT EQUAL
.
          BRANCH    OPTION OF HEAD02
.
          MOVE      "Inpatient Bed Days",CPHDROPT
          GOTO      HEAD04
.
HEAD02    PRINT     "Inpatient Admissions",CPHDROPT
.
HEAD04    MOVE      ONE,CNOUNDLN
          CALL      IBACLOCK
          CALL      HEAD132
.
          MATCH     STRTYEAR,ENDYEAR
          GOTO      HEAD05 IF NOT EQUAL
.
          PRINT     *40,"Year : ",NOWYEAR,*N
          GOTO      HEAD08
.
HEAD05    PRINT     *40,"Financial Year : ",STRTYEAR,"/",ENDYEAR,*N
.
HEAD08    CALL      PRTLINE
.
          COMPARE   TEN2,NUMPERS
          GOTO      HEAD10 IF EQUAL
.
          PRINT     *1,"| Ward",*11,"| Beds",*18,MONTH,*122,"|  Total",*132,"|"
          GOTO      HEAD20
.
HEAD10    PRINT     *1,"| Ward",*9,"|Beds",*14,MONTH12,*122,"|  Total",*132,"|"
.
HEAD20    CALL      PRTLINE
          MOVE      SEVEN,CLNO
          RETURN
.
.         Deleting  AN INDEXED FILE BASED ON PORT
.
KILLIDX   CLOSE     TEMPFILE
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMET,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          DISPLAY   *P1:14,*EF,*P1:14
          RETURN
.
CREAIDX   CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMET,CMDLINE
          APPEND    " 84 u1-9",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          DISPLAY   *P1:14,*EF,*P1:14
          RETURN
.
.         I/O Routine for tempfile (used for MMHS outpatient)
.
RDTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY9
          READ      TEMPFILE,KEY9;MSDCLS,MSWCLS,MSWARD,XCENT,XYEAR,INBNUM:
                                  INP01,INP02,INP03,INP04,INP05,INP06:
                                  INP07,INP08,INP09,INP10,INP11,INP12:
                                  INP13,CHNGFLG
          GOTO      OVERCOND IF OVER
          RETURN
.
RDSTEMP1  RESET     KEY9
          READ      TEMPFILE,KEY9;;
          RETURN
.
RDKTEMP1  MOVE      ZERO,OVRCD
          READKS    TEMPFILE;MSDCLS,MSWCLS,MSWARD,XCENT,XYEAR,INBNUM:
                                    INP01,INP02,INP03,INP04,INP05,INP06:
                                    INP07,INP08,INP09,INP10,INP11,INP12:
                                    INP13,CHNGFLG
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTEMP1   UPDATE    TEMPFILE;MSDCLS,MSWCLS,MSWARD,XCENT,XYEAR,INBNUM:
                                    INP01,INP02,INP03,INP04,INP05,INP06:
                                    INP07,INP08,INP09,INP10,INP11,INP12:
                                    INP13,CHNGFLG
          RETURN
.
WRTEMP1   RESET     KEY9
          MOVE      ZERO,OVRCD
          READ      TEMPFILE,KEY9;ANS
          GOTO      OVERCOND IF NOT OVER
.
          WRITE     TEMPFILE,KEY9;KEY9,XCENT,XYEAR,INBNUM:
                                  INP01,INP02,INP03,INP04,INP05,INP06:
                                  INP07,INP08,INP09,INP10,INP11,INP12:
                                  INP13,CHNGFLG
          RETURN
.
          INCLUDE   STD001IO
          INCLUDE   CALCDAYS    
          INC       DATECONV
          INC       TFILENAM                     * Generate Temp File Name
          INC       WDACTVDY
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INCLUDE   PATCOMN3
          INC       PATCODIO/INC
          INCLUDE   PATDRGIO/INC
          INC       PATMSTIO/INC
          INC       PATWR1IO/INC
          INCLUDE   PATWBHIO/INC
          INC       WEBERRIO/INC                 * Web Server Error Logging
.
ENDIT     CHAIN     PGM
          STOP

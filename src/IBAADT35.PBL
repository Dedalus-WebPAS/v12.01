.******************************************************************************
.* System    :   Patient Management                                           *
.* Program   :   IBAADT35                                                     *
.* Desc      :   Patient Satisfaction Monitor                                 *
.******************************************************************************
.* Date      :   01/06/2005                                                   *
.* Author    :   Peter Vela                                                   *
.* Function  :   This program will collect patient details for DHS            *
.*               Patient Satisfaction Monitor and produce an extract for      *
.*               these details, along with an report for exceptions/errors    *
.* Mods      :                                                                *
.*        V12.00.01 21/05/2025 Bella Turco    TSK 0955096                     *
.*                  Alphanumeric changes                                      *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V9.08.01  01/06/2007  Mike Laming  CAR 140164                       *
.*                  Correct DDEST/DSTAT Indicator selection at GDAT2000       *
.*     V9.05.B02 16/01/2006  Peter Vela CAR 90713                             *
.*               Added multi hospital functionality                           *
.*     V9.04.01  01/06/2005  Peter Vela CAR 62569                             *
.*               Created program                                              *
.******************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
.
          INC       PATCALAG/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATDHEAD/INC
          INC       PATDSCFD/INC
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC
          INC       PMSPX2FD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATTRNFD/INC
          INC       PATVISFD/INC
          INC       PATWR1FD/INC

.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
PSSFILE1  FILE      ASCII,FIXED=155
.
.Extract File Layout
.-------------------
.Name     Type    Length    Start  Description
.-------  ----    ------    -----  -----------
PSSURNO    DIM      8          1      UR Number
.TABCHAR   INIT     2          9      "^I"
PSSTITL    DIM      4         11      Title
.TABCHAR   INIT     2         15      "^I"
PSSSNAME   DIM     20         17      Surname
.TABCHAR   INIT     2         37      "^I"
PSSGNAME   DIM     25         39      First Given Name
.TABCHAR   INIT     2         64      "^I"
PSSSTRT    DIM     35         66      Street
.TABCHAR   INIT     2        101      "^I"
PSSSUBRB   DIM     35        103      Town
.TABCHAR   INIT     2        138      "^I"
PSSPCODE   DIM      8        140      Postcode
.TABCHAR   INIT     2        148      "^I"
PSSDDATE   DIM      8        150      Discharge Date 
.TABCHAR   INIT     2        158      "^I"
PSSATYPE   DIM      3        160      Admission Type (Category A )
.TABCHAR   INIT     2        163      "^I"
PSSCTYPE   DIM      3        165      Care Type      (Category CC)
.TABCHAR   INIT     2        168      "^I" 
PSSLANG    DIM      3        170      Language
.TABCHAR   INIT     2        173      "^I"
.
.End of Record               175
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
ADMNFLG   FORM      1
BJDAY     FORM      3                       * For CALCAGE
CJDAY     FORM      3                       * For CALCAGE
CMDLINE   DIM       100
CURDATE   DIM       8
DIM8      DIM       8
DIM10     DIM       10
DIM37     DIM       37
DMINAGE   DIM       3
ENDDATE   DIM       8
ERRFLG    FORM      1
EXCPTMSG  DIM       60                      * Error message line
EXCPTCNT  FORM      5                       * Error counter
EXTRCNT   FORM      5                       * Extracted record counter
HOSPCODE  DIM       3                       * Hospital Code work variable
IBCNMHOS  FORM      1                       * Multi Hospital
MINAGE    FORM      3
PREFLANG  DIM       3
PSSFNAME  DIM       8
PSSFNME2  DIM       14
STRTDATE  DIM       8
TOTRDCNT  FORM      5                       * Total record counter
.
DASH      INIT      "-"
TABCHAR   INIT      "	"
YEARSOLD  INIT      " years old"
.
P1LIT     INIT      "P1"
P2LIT     INIT      "P2"
P3LIT     INIT      "P3"
P4LIT     INIT      "P4"
P5LIT     INIT      "P5"
P6LIT     INIT      "P6"
U1LIT     INIT      "U1"
U2LIT     INIT      "U2"
.
PRGID     INIT      "IBAADT35"
PRGNAM    INIT      "Patient Satisfaction Monitor"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
          CALL      CHKP0000
          BRANCH    EXIT,ML9999
.
ML0100    CALL      CHKMH000
          BRANCH    EXIT,ML9999,ML9999     * check multi hospital
          CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      KDAT0000               * keyin date range
          BRANCH    EXIT,ML0100
.
ML2000    CALL      KAGE0000               * keyin minimum age
          BRANCH    EXIT,ML1000
.
ML3000    CALL      FILE0000               * create extract file
          BRANCH    EXIT,ML2000
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML4000:          * yes
                          ML3000:          * no
                          ML0100           * cancel
.
ML4000    CALL      HEAD0000               * print header for exception report
          BRANCH    EXIT,ML0100
.
          CALL      GDAT0000               * load data into extract file
          BRANCH    EXIT,ML0100
.
          CALL      TAIL0000               * print tail for exception report
          CALL      MVEXT000               * load extract into web directory
          GOTO      ML0100
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,"Opening ":
                    *P64:24,"patdschf";
          OPEN      PATCODE1,"patcodes"
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH2,"patdschf"
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A3,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PMSPX2A1,"pmspx2af"
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATTRAN2,"pattranf"
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATVISA1,"patvisaf"
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          DISPLAY  *P1:24,*EL
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TWENTY1;*45,PTCNDRSM                     *I-140164
          READ      CONTROLF,TWENTY1;*222,CNSWLNG
          READ      CONTROLF,HUND05;*70,PTCNSRVY
.
          IF        IBCNMHOS = 1
            DISPLAY   *P64:24,"pathspaf";
            OPEN      PATHSPA1,"pathspaf"
          ENDIF
.
          CLOCK     TIME,CTIMEIS
          MOVE      "- Exceptions Report",CPHDROPT
.
INIT9000  MOVE      ZERO,EXIT
INIT9999  RETURN
+
.****************************************************************************
.*                                CHKP0000             Called by: ML0000    *
.*                        check that PTCNSRVY is set                        *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Parameter set to 1-3, ok               *
.*                        TRUE  (1)  Parameter <1 or >3, exit               *
.****************************************************************************
CHKP0000  IF       PTCNSRVY<1 | PTCNSRVY>2
            DISPLAY  *P1:24,*EL,"Not Using Patient Satisfaction Monitor. ";
            CALL     HITENTER
            MOVE     ONE,EXIT
          ELSE
            MOVE     ZERO,EXIT
          ENDIF
CHKP9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Create Extract"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run create extract
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                  KDAT0000              Called by: ML0000   *
.*                            Enter The Date Ranges                           *
.******************************************************************************
KDAT0000  MOVE      "21",CCOL
          MOVE      "9",CVERT
          DISPLAY   *P1:CVERT,"Starting Date     : ",*EF;
          ADD       ONE,CVERT
          DISPLAY   *P1:CVERT,"Ending Date       : ";
.
          MOVE      ONE,CCANLDTE            * Blank cancel default
          MOVE      ZERO,CHIGHLT            * Use highlight
.
. ----- Enter Starting Date -----
.
KDAT1000  SUB       ONE,CVERT
KDAT2000  MOVE      CCC,CCENT
          UNPACK    SP6,CYEAR,CMON,CDAY     * No defaults
          CALL      KEYDATE                 * Keyin date
          BRANCH    OVRCD,KDAT9500          * Nothing entered
          BRANCH    CQUEST,KDAT2000
.
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDATE
.
          MATCH     STRTDATE,CURDATE
          GOTO      KDAT3000 IF NOT LESS    * Date is <= current date
.
          DISPLAY   *P1:24,*B,"Date must not be after current date.  ",*EL;
          CALL      HITENTER
          GOTO      KDAT2000
.
. ----- Enter Ending Date -----
.
KDAT3000  ADD       ONE,CVERT
KDAT4000  UNPACK    STRTDATE,CCENT,CYEAR,CMON,CDAY * Get defaults
          CALL      KEYDATE                 * Keyin date
          BRANCH    OVRCD,KDAT1000          * Nothing entered
          BRANCH    CQUEST,KDAT4000
.
          PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDATE
.
          MATCH     STRTDATE,ENDDATE
          GOTO      KDAT5000 IF NOT LESS    * Date is <= start date
.
          DISPLAY   *P1:24,*B,"Ending date must be after Starting date.  ",*EL;
          CALL      HITENTER
          GOTO      KDAT4000
.
. ----- Check End Date >= Current Date -----
.
KDAT5000  MATCH     ENDDATE,CURDATE
          GOTO      KDAT9000 IF NOT LESS    * Date is <= current date
.
          DISPLAY   *P1:24,*B,"Date must not be after current date.  ",*EL;
          CALL      HITENTER
          GOTO      KDAT4000
.
KDAT9000  MOVE      ZERO,EXIT
          GOTO      KDAT9999
.
KDAT9500  MOVE      ONE,EXIT
KDAT9999  RETURN
+
.******************************************************************************
.*                                  FILE0000              Called by: ML0000   *
.*                            Get Extract Filename                            *
.******************************************************************************
FILE0000  MOVE       TEN4,CVERT
          DISPLAY   *P1:CVERT,*EL,"Extract File Name : ";
FILE1000  MOVE      "PSM",KEY3
          UNPACK    ENDDATE,CCENT,CYEAR,CMON,CDAY
.
          IF        IBCNMHOS = 1
            PACK      PSSFNAME,ANSP,CDAY,CMON,HOSPCODE
          ELSE
            PACK      PSSFNAME,ANSP,CDAY,CMON
          ENDIF
.
          KEYIN     *P21:CVERT,*EL,*V2LON,*RV,PSSFNAME;
.
          ENDSET    PSSFNAME
          APPEND    SP8,PSSFNAME
          RESET     PSSFNAME
.
          CMATCH    EXITCHAR,PSSFNAME
          GOTO      FILE9500 IF EQUAL       * Exit
.
          MATCH     SP8,PSSFNAME
          GOTO      FILE9500 IF EQUAL       * Exit
.
. ----- Set Up Extract Filename -----
.
          STRIP     PSSFNAME
          SQUEEZE   PSSFNAME
          MOVE      SP20,PSSFNME2
          CLEAR     PSSFNME2
          APPEND    PSSFNAME,PSSFNME2
          APPEND    "/txt",PSSFNME2
          RESET     PSSFNME2
.
          DISPLAY   *P21:CVERT,*EL,*V2LON,*+,PSSFNAME,".txt";
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
.
          OPEN      PSSFILE1,PSSFNME2
          TRAPCLR   IO
          BRANCH    OVRCD,FILE3000          * File doesnt exist
.
          CLOSE     PSSFILE1
.
FILE2000  KEYIN     *P1:24,*B,*EL,"An extraction file with this name":
                    " already exists. Overwrite it (",*V2LON,*DV,ANSY:
                    *HOFF,"/",*V2LON,*DV,ANSN,*HOFF,") ? ",*V2LON,ANS;
          REP       UPPLOW,ANS
          MATCH     ANSY,ANS
          GOTO      FILE9000 IF EQUAL       * Overwrite file
.
          MATCH     ANSN,ANS
          GOTO      FILE1000 IF EQUAL       * Keyin filename again
.
          BEEP
          GOTO      FILE2000
.
FILE3000  PREP      PSSFILE1,PSSFNME2
.
FILE9000  MOVE      ZERO,EXIT
          GOTO      FILE9999
.
FILE9500  MOVE      ONE,EXIT
FILE9999  RETURN
+
.**************************************************************************
.*                               KAGE0000              Called by: ML0000  *
.*                      Key in minimum age for survey                     *
.**************************************************************************
.
KAGE0000  MOVE      TEN8,DMINAGE             * set default to 18 years old 
          MOVE      TEN2,CVERT
          KEYIN     *P1:CVERT,"Minimum age       : ",*V2LON,*RV,DMINAGE,*HOFF
.
          TYPE      DMINAGE
          GOTO      KAGE1000 IF EQUAL
.
          PACK      DMINAGE,DMINAGE,SP3
.
          MATCH     SP3,DMINAGE
          GOTO      KAGE9500 IF EQUAL
.
          BEEP
          GOTO      KAGE0000
.
KAGE1000  MOVE      DMINAGE,MINAGE
.
          COMPARE   ZERO,MINAGE
          GOTO      KAGE9500 IF LESS
.
KAGE9000  MOVE      ZERO,EXIT
          GOTO      KAGE9999
.
KAGE9500  MOVE      ONE,EXIT
KAGE9999  RETURN
+
.**************************************************************************
.*                               GDAT0000              Called by: ML0000  *
.*                         Get data for extract                           *
.**************************************************************************
.
GDAT0000  MOVE      ZERO,EXCPTCNT
          MOVE      ZERO,EXTRCNT 
          MOVE      ZERO,TOTRDCNT 
          DISPLAY   *P1:24,*EL,"Processing : ",*P40:24,"Loading : ";
.
          OPEN      PSSFILE1,PSSFNAME       * Open extract file
.
          PACK      KEY16,STRTDATE,SP20
          CALL      RDSDSCH2
GDAT1000  CALL      RDKDSCH2                * Search disch. file by disch. date
          BRANCH    OVRCD,GDAT9000
.
          MATCH     DDATE,ENDDATE           * Check dis. date is in date range
          GOTO      GDAT9000 IF LESS
.
          PACK      KEY8,DADMNO,SP8
          CALL      RDPTVIS1                * Check visit file for patient type
          BRANCH    OVRCD,GDAT1000          
.
          COMPARE   THREE,PVITYPE           * Survey inpatients only
          GOTO      GDAT1000 IF NOT EQUAL
.
          PACK      KEY8,DURNO,SP8
          CALL      RDMAST1                 * Get PMI details for patient
          BRANCH    OVRCD,GDAT1000
.
          COMPARE   ZERO,PCEASE             * Is patient deceased?
          GOTO      GDAT1000 IF NOT EQUAL   * yes, go to next admission
.
          PACK      KEY8,DURNO,SP8
          CALL      RDPMPX21
          BRANCH    OVRCD,GDAT1000          * Get PMI extension details
.
          PACK      KEY8,DADMNO,SP8
          CALL      RDMISS1                 * Get PMI details for patient
          BRANCH    OVRCD,GDAT1000
.
          ADD       ONE,TOTRDCNT            * Total no. of records checked
          MOVE      ZERO,ADMNFLG            * New admission record
          MOVE      ZERO,ERRFLG             * Error flag
          UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P14:24,*V2LON,DADMNO,SP2,CPCDATE; * Display something
.
.         Check for multi-hospital flag. If true then only extract patients
.         in chosen hospital
.
          IF        IBCNMHOS = 1
            PACK      KEY30,DADMNO,SP20
            CALL      RDSTRAN2
            CALL      RDKTRAN2
            BRANCH    OVRCD,GDAT1000
.
            MATCH     DADMNO,TADMN
            GOTO      GDAT1000 IF NOT EQUAL
.
            PACK      KEY6,TWARD,SP3
            CALL      RDWARD1
            BRANCH    OVRCD,GDAT1000
            MATCH     PTWDHOSN,HOSPCODE
            GOTO      GDAT1000 IF NOT EQUAL
          ENDIF
.
.         Check if patient meets minimum age for report
.
          PACK      AGEDATE,CCC,CYY,CMM,CDD
          REP       UPPLOW,AGEDATE
          CALL      CALCAGE                 * Determine age of patient
          COMPARE   MINAGE,PSAGEYRS         * Is patient >= minimum age
          GOTO      GDAT2000 IF NOT LESS    * If no, add to exception report
.
          MOVE      "Under minimum age of ",EXCPTMSG   
          PACK      EXCPTMSG,EXCPTMSG,MINAGE,SP1,DASH,PSAGEYRS,YEARSOLD
          CALL      EXRP0000                * Print exception details
.
.         Check for Psychiatric patient
.
GDAT2000  MOVE      SP1,ANS
.                                                                     *C-140164
          IF        PTCNDRSM=1
            PACK      KEY3,DSTAT
            PACK      KEY5,ANSD,SP1,DSTAT,SP5
          ELSE
            PACK      KEY3,DDEST
            PACK      KEY5,ANSD,ANSD,DDEST,SP5
          ENDIF
.
          MATCH     SP3,KEY3
          GOTO      GDAT3000 IF EQUAL
          CALL      RDCODE1
.
          UNPACK    THCSCOD,ANS,ANS,KEY2     * Check HCS equiv. field for Psych
          MATCH     ANSA,ANS                 * patients
          GOTO      GDAT2500 IF EQUAL
.
          CMATCH    ANSA,THCSCOD
          GOTO      GDAT3000 IF NOT EQUAL
.
GDAT2500  MOVE      "Psychiatric Care - ",EXCPTMSG
          PACK      EXCPTMSG,EXCPTMSG,TDESC
          CALL      EXRP0000                 * Print exception details
.
.         Check for Hospital in the Home (HITH) patients
.
GDAT3000  PACK      KEY30,DADMNO,SP30
          CALL      RDSTRAN2                 * Check transfer file for
GDAT3500  CALL      RDKTRAN2                 * admission within date range
          BRANCH    OVRCD,GDAT4000
.
          MATCH     TADMN,DADMNO             * Still same admission?
          GOTO      GDAT4000 IF NOT EQUAL    * No, go to next record
.
          PACK      KEY5,ANSB,ANST,TRBTYP    * Yes, get bed type
          CALL      RDCODE1
          BRANCH    OVRCD,GDAT3500
.
          MATCH     ANSH,TCINDC1             * Is bed used for HITH
          GOTO      GDAT3500 IF NOT EQUAL    * No, don't print exception
.
          MOVE      "HITH Patient - ",EXCPTMSG
          PACK      EXCPTMSG,EXCPTMSG,TDESC
          CALL      EXRP0000                * Print exception details
.
GDAT4000  MOVE      SP3,PREFLANG
          MOVE      PMPXLNG1,PREFLANG
          MATCH     PREFLANG,SP3
          GOTO      GDAT5000 IF NOT EQUAL
.
          MOVE      "Language field blank",EXCPTMSG
          CALL      EXRP0000
.
GDAT5000  LOAD      FORM1,PTCNSRVY,PTMIUYN1,PTMIUYN2
          COMPARE   ONE,FORM1               * Has patient agreed to participate
          GOTO      GDAT6000 IF EQUAL
.   
          MOVE      "Did not consent to release of details",EXCPTMSG  * No
          CALL      EXRP0000                * Print exception details
          GOTO      GDAT1000
.
GDAT6000  COMPARE   ONE,ERRFLG              * Has an error been found
          GOTO      GDAT1000 IF EQUAL       * Yes, go to next record
.
          CALL      EXTR0000                * No, load patient data to extract
          GOTO      GDAT1000
.
GDAT9000  MOVE      ZERO,EXIT
GDAT9999  RETURN
+
.**************************************************************************
.*                               GDLG0000            Called by: GDAT0000  *
.*                         Get default Language                           *
.**************************************************************************
.
GDLG0000  LOAD      DIM2,CNSWLNG,P1LIT,P2LIT,P3LIT,U1LIT,U2LIT:
                                 P4LIT,P5LIT,P6LIT
          PACK      KEY5,DIM2,SP3
          CALL      RDCODE1
          MATCH     TCODE,DIM2
          GOTO      GDLG9999 IF NOT EQUAL
.
GDLG1000  CALL      RDKCODE1
          MATCH     TCODE,DIM2
          GOTO      GDLG9999 IF NOT EQUAL
.
          MATCH     ANSD,TCINDC10
          GOTO      GDLG1000 IF NOT EQUAL
.
          MOVE      ACODE,PREFLANG
.
GDLG9999  RETURN
+
.**************************************************************************
.*                               EXTR0000            Called by: GDAT0000  *
.*                        Load data into extract                          *
.**************************************************************************
.
EXTR0000  CALL      CVAR0000                      * Clear extract variables
          PACK      PSSURNO,PURNO,SP70            * Load data into variables
          PACK      PSSTITL,PTITL,SP4
          PACK      PSSSNAME,PSNAME,SP20
          PACK      PSSGNAME,PGNAME,SP30
          PACK      PSSSTRT,PADD1,SP20,SP20
          PACK      PSSSUBRB,PSUBRB,SP20,SP20
          PACK      PSSPCODE,PPOST,SP8
.
          UNPACK    DDATE,XCENT,XYEAR,XMON,XDAY
          PACK      PSSDDATE,XDAY,XMON,XCENT,XYEAR  * Format date for DHS
.
          PACK      PSSATYPE,ATYPE,SP70           * Admission Type
          PACK      PSSCTYPE,ACARE,SP70           * Care Type
.
          PACK      PSSLANG,PREFLANG,SP3          * Preferred language
.
          WRITE     PSSFILE1,SEQ;PSSURNO,TABCHAR,PSSTITL,TABCHAR,PSSSNAME:
                                 TABCHAR,PSSGNAME,TABCHAR,PSSSTRT,TABCHAR:
                                 PSSSUBRB,TABCHAR,PSSPCODE,TABCHAR,PSSDDATE:
                                 TABCHAR,PSSATYPE,TABCHAR,PSSCTYPE,TABCHAR:
                                 PSSLANG,TABCHAR
.                                                 * Write to extract file
          ADD       ONE,EXTRCNT                   * # of records extracted
          DISPLAY   *P50:24,*EL,*V2LON,DADMNO;
.
EXTR9000  MOVE      ZERO,EXIT
EXTR9999  RETURN
+
.**************************************************************************
.*                               CVAR0000            Called by: EXTR0000  *
.*                           Clear variables                              *
.**************************************************************************
.
CVAR0000  MOVE      SP8,PSSURNO             * UR Number
          MOVE      SP4,PSSTITL             * Title
          MOVE      SP20,PSSSNAME           * Surname
          MOVE      SP30,PSSGNAME           * Given Name
          PACK      PSSSTRT,SP20,SP20       * Street
          PACK      PSSSUBRB,SP20,SP20      * Suburb
          MOVE      SP8,PSSPCODE            * Postcode
          MOVE      SP8,PSSDDATE            * Discharge Date
          MOVE      SP3,PSSATYPE            * Admission type
          MOVE      SP3,PSSCTYPE            * Care Type
          MOVE      SP3,PSSLANG             * Preferred Language
.
CVAR9000  MOVE      ZERO,EXIT
CVAR9999  RETURN
+
.******************************************************************************
.*                                  HEAD0000                Called by: ML0000 *
.*                          Print The Report Heading                          *
.******************************************************************************
HEAD0000  CALL      HEAD132
          UNPACK    STRTDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *40,"From Date         : ",CPCDATE
          UNPACK    ENDDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          IF        IBCNMHOS = 1
            PRINT     *40,"To Date           : ",CPCDATE
            PRINT     *40,"Hospital Name     : ",PTHSNAME
          ELSE
            PRINT     *40,"To Date           : ",CPCDATE,*N
          ENDIF
.
          ADD       "2",CLNO
.
          CALL      UND132
          PRINT     *1,"| Name",*40,"| U/R No.",*51,"| Adm. No.":
                    *62,"| Dis. Date",*75,"| Exception Reason",*132,"|"
          ADD       "1",CLNO
          CALL      UND132
.
HEAD9000  MOVE      ZERO,EXIT
HEAD9999  RETURN
+
.******************************************************************************
.*                                  TAIL0000                Called by: ML0000 *
.*                          Print The Report Tail                             *
.******************************************************************************
TAIL0000  CALL      UND132
          PRINT     *N,"Number of Records Read      : ",TOTRDCNT:
                    *N,"Number of Records Extracted : ",EXTRCNT:
                    *N,"Number of Exceptions Found  : ",EXCPTCNT:
                    *N,*N,"*** End of Report ***"
          DISPLAY   *P1:24,*EL,*V2LON,EXTRCNT,*HOFF," records extracted. ":
                    *V2LON,EXCPTCNT,*HOFF," exceptions found.  ";
          CALL      HITENTER
.
TAIL9000  MOVE      ZERO,EXIT
TAIL9999  RETURN
+
.******************************************************************************
.*                                  EXRP0000              Called by: GDAT0000 *
.*                          Print An Exception Message                        *
.******************************************************************************
EXRP0000  COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          IF        ADMNFLG=ZERO
            MOVE      PSNAME,PACSNAME
            MOVE      PGNAME,PACGNAME
            MOVE      PTITL,PACTITLE
            MOVE      TWO,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,DIM37
            MOVE      DADMNO,DIM8
            ADD       ONE,EXCPTCNT
          ELSE
            PACK      DIM37,SP20,SP20
            MOVE      SP8,PURNO
            MOVE      SP8,DIM8
            MOVE      SP10,CPCDATE
          ENDIF
.
          PRINT     *1,"| ",DIM37,*40,"| ",PURNO,*51,"| ",DIM8,*62,"| ":
                    CPCDATE,*75,"| ",EXCPTMSG,*132,"|"
          ADD       ONE,CLNO
          MOVE      ONE,ADMNFLG
          MOVE      ONE,ERRFLG
.
EXRP9000  MOVE      ZERO,EXIT
EXRP9999  RETURN
+
.******************************************************************************
.*                                  MVEXT000                Called by: ML0000 *
.*                          Move Extract to Web Directory                     *
.*****************************************************************************
MVEXT000  CLEAR     CMDLINE
          APPEND    "ibaadt35.us1 ",CMDLINE
          APPEND    PSSFNAME,CMDLINE
          APPEND    ".txt",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
MVEXT999  RETURN
.****************************************************************************
.*                                CHKMH000             Called by: ML0000    *
.*                        check and input multi hospital                    *
.****************************************************************************
CHKMH000  IF        IBCNMHOS = 1
            DISPLAY   *P1:3,*EF,"Hospital ID: ";
            MOVE      SP70,HOSPCODE
            MOVE      TEN5,ECOL
            MOVE      THREE,EVERT
            MOVE      SP3,CKYIDEF3
            MOVE      ZERO,CKYIMAND
            CALL      PATHSPKY
            BRANCH    EXIT,CHKMH950,CHKMH950
            MOVE      KEY3,HOSPCODE
            DISPLAY   *P22:3,*EL,PTHSNAME,*W1;
          ENDIF
.
CHKMH900  MOVE      ZERO,EXIT
          GOTO      CHKMH999
.
CHKMH950  MOVE      ONE,EXIT
CHKMH999  RETURN
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       CALCAGE
          INC       PATHSPKY
.
          INC       PATCODIO/INC
          INC       PATDSCIO/INC
          INC       PATHSPIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PMSPX2IO/INC
          INC       PATTRNIO/INC
          INC       PATVISIO/INC
          INC       PATWR1IO/INC

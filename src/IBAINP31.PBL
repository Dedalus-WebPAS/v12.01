.******************************************************************************
.* System    : PRS/2                                                          * 
.* Program   : IBAINP31                                                       *
.* Desc      : Account Status Validation Maintenance                          *
.******************************************************************************
.* Date      : 14/08/95                                                       *
.* Author    : Paul Howells                                                   *
.* Modifications :                                                            *
.*        V9.08.01  15/05/2007  Jill Habasque CAR 137885                      *
.*                  Added medicare suffix Name,C-U,N-E,P-N                    *
.*        V9.03.02  21/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2      *
.*                  Change PATACTFD to increase Key to 6 & PTACCARE to 2 bytes*
.*        V9.03.01  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2      *
.*                  Change PATACTFD to increase Key to 6 & PTACCARE to 2 bytes*
.*        V9.02.01  11/09/2002  Jill Habasque 02/03 Mods                      *
.*                  Added medicare suffix '9' to patactaf                     *
.*        V9.00.B01 04/06/2001  Steve Downing                                 *
.*                  Mods to accept characters for Accommodation Type for PRS2 *
.******************************************************************************
.*        V5.08.01  16/05/2000  Greg Horvat      SRF 1887                     *
.*                  Added 8 as a valid patactaf medicare suffix               *
.*        V5.07.01  22/01/2000  Steve Armstrong                               *
.*                  Fixed recompile bugs                                      *
.*                 V5.03.01  01/02/1996  Whirlpool                            *
.*                           Modified for PTACCARE becoming a DIM for "U"     *
.******************************************************************************
.
         INC       STD001FD
.
. =========================================================================
. FILE DESCRIPTION INCLUDES
. -------------------------
         INC       PATACTFD/INC
. =========================================================================
. LOCAL VARIABLE DEFINITION
. -------------------------
.
ACCOMTYP  DIM       1
CARETYPE  DIM       2                                         * was 1  *C-49016
MEDSUFFI  DIM       2
OPT       DIM       1
PREVACC   DIM       6
KFLAG     FORM      1
SAVACCLS  DIM       3
SAVACCM   DIM       1
SAVCARE   DIM       2                                         * was 1  *C-49016
.
SUFDESC   DIM       24 
SUF1      INIT      "N-E                     "
SUF2      INIT      "Name, C-U               "
SUF3      INIT      "Name, C-U, BAB          "
SUF4      INIT      "Name, C-U, BAB, P-N     "
SUF5      INIT      "Name, C-U, BAB, P-N, N-E"
SUF6      INIT      "Name, C-U, N-E          "
SUF7      INIT      "Name, C-U, BAB, N-E     "
SUF8      INIT      "C-U                     "
SUF9      INIT      "Name, P-N               "
SUF10     INIT      "Name, C-U,N-E,P-N       "
.
PRGID     INIT      "IBAINP31"
PRGNAM    INIT      "Account Status Validation Maintenance"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              MAINLINE                                  *
.*                            Controlling Logic                           *
.**************************************************************************
         CALL      INIT0000               * initialization and open files
.
ML0100   CALL      OPTN0000               * select options
         BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
         BRANCH    OPTION,ML2000,ML3000,ML4000
.
. ------ Insert     
.
ML2000   CALL      DSCR0000               * display screen 
         CALL      IKEY0000               * input key fields
         BRANCH    EXIT,ML0100
.
         CALL      INST0000               * insert mode
         BRANCH    EXIT,ML0100            * exit
.
         CALL      CHGE0000               * change mode
         BRANCH    EXIT,ML0100            * exit
.         
         CALL      POST0000               * post
         GOTO      ML2000
.
. ------ Change     
.
ML3000   CALL      DSCR0000               * display screen 
         CALL      IKEY0000               * input key fields
         BRANCH    EXIT,ML0100
.
         CALL      DISP0000               * display record
.
         CALL      CHGE0000               * change mode
         BRANCH    EXIT,ML0100            * exit
.         
         CALL      POST0000               * post
         GOTO      ML3000
.
. ------ Delete     
.
ML4000   CALL      DSCR0000               * display screen 
         CALL      IKEY0000               * input key fields
         BRANCH    EXIT,ML0100
.
         CALL      DISP0000               * display record
.
         CALL      POST0000               * post
         COMPARE   THREE,CEXIT
         GOTO      ML0100 IF EQUAL
.
         GOTO      ML4000
.
ML9999   CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000                                  *
.*                             initialization                               *
.*  The initialization routine is used to display headings and open files.  *
.*  Any other general initialization can also be performed here.            *
.****************************************************************************
INIT0000  CALL      DISPHEAD                * display heading
.
          DISPLAY   *P56:24,"Opening patactaf";
          OPEN      PATACTA1,"patactaf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Valid option selected                  *
.*                        TRUE  (1)  Exit option selected                   *
.*              COPTION = 1  Insert option selected                         *
.*                        2  Change option selected                         *
.*                        3  Delete option selected                         *
.****************************************************************************
.
. ---- display option screen ----
.
OPTN0000  CALL      SELOPTN                       * select option
          COMPARE   ZERO,COPTION
          GOTO      OPTN9000 IF EQUAL                 * exit
          MOVE      FALSE,EXIT
          GOTO      OPTN9999
.
OPTN9000  MOVE      TRUE,EXIT                   * set EXIT = 1
OPTN9999  RETURN
+
.***************************************************************************
.*                                DSCR0000                                 *
.*                        display screen format                            *
.***************************************************************************
DSCR0000  DISPLAY   *P1:4,*EF:
                    *P5:04,"Account Class     : ": 
                    *P5:05,"Accomodation Type : ": 
                    *P5:06,"Care Type         : ": 
                    *P1:08,*V2LON," 1",*HOFF,". Medicare Suffix   : "
.
          CALL      CLRF0000                * clear file variables
.
DSCR9999  RETURN     
+
.***************************************************************************
.*                               CLRF0000                                  *
.*                         Clear file variables                            *
.***************************************************************************
CLRF0000
.
          MOVE      SP70,PTACCLSS
          MOVE      ZERO,PTACACCM
          MOVE      "0 ",PTACCARE                                      *C-49016
          MOVE      ZERO,PTACMEDI
          MOVE      SP70,PTACSPAR
.
CLRF9999  RETURN
.***************************************************************************
.*                               DISP0000                                  *
.*                    display details for chosen code                      *
.***************************************************************************
DISP0000  DISPLAY   *P25:4,*V2LON,PTACCLSS:
                    *P25:5,PTACACCM:
                    *P25:6,PTACCARE:
                    *P25:8,PTACMEDI
.
          MOVE      "Unknown Medicare Suffix ",SUFDESC
          LOAD      SUFDESC,PTACMEDI,SUF1,SUF2,SUF3,SUF4,SUF5:
                                     SUF6,SUF7,SUF8,SUF9,SUF10
          DISPLAY   *P30:8,SUFDESC
.
DISP9999  RETURN
+
.***************************************************************************
.* KACS0000 - Keyin Account Classification                                 * 
.***************************************************************************
KACS0000  MOVE      ONE,KFLAG
          CALL      CLRF0000                * Clear file variables
          KEYIN     *P25:4,*EL,*V2LON,PTACCLSS
.
          MATCH     QUEST,PTACCLSS
          IF        @EQUAL
            GOTO      QACCLSS IF EQUAL
            GOTO      KACS0000
          ENDIF
.
          ENDSET    PTACCLSS
          APPEND    SP3,PTACCLSS
          RESET     PTACCLSS
.
          MATCH     SP3,PTACCLSS
          GOTO      KACS9500 IF EQUAL
.
          MATCH     EXITCHAR,PTACCLSS
          GOTO      KACS9700 IF EQUAL
.
KACS9000  MOVE      ZERO,EXIT
          GOTO      KACS9999
.
KACS9500  MOVE      ONE,EXIT
          GOTO      KACS9999
.
KACS9700  MOVE      TWO,EXIT
          GOTO      KACS9999
.
KACS9999  RETURN     
+
.***************************************************************************
.* QACCLSS - ? Search for Account Class                                    *
.***************************************************************************
.
QACCLSS   CALL      GETSCR00
          MOVE      PTACCLSS,SAVACCLS
          MOVE      PTACCARE,SAVCARE
          MOVE      PTACACCM,SAVACCM 
          MOVE      SP10,KEY6                              * was KEY5  *C-49016
          MOVE      SP6,PREVACC
          MOVE      SP6,PTACCLSS
          MOVE      FIVE,CVERT
          MOVE      THREE,CCOL
          DISPLAY   *P1:4,*EF
          DISPLAY   *P2:4,*V2LON,*ULON,"ACCOUNT",*HOFF,"   ",*V2LON,*ULON:
                    "ACCOM.",*HOFF,"   ",*V2LON,*ULON,"CARE",*HOFF:
                    "                  ",*V2LON,*ULON,"ACCOUNT",*HOFF,"   ":
                    *V2LON,*ULON,"ACCOM.",*HOFF,"   ",*V2LON,*ULON,"CARE"
.
          CALL      RSPTACT1
QACCLSS2  MOVE      PTACCLSS,PREVACC
          CALL      RKPTACT1
          BRANCH    OVRCD,QACCLSS8
          MATCH     PTACCLSS,PREVACC
          GOTO      QACCLSS2 IF EQUAL
.         
          COMPARE   TWENTY3,CVERT
          IF        @EQUAL
            MOVE      FIVE,CVERT
            ADD       FORTY1,CCOL
            COMPARE   SIXTY,CCOL
            GOTO      QACCLSS4 IF LESS
            KEYIN     *P1:24,"(",*V2LON,"N",*HOFF,")ext,(":  
                      *V2LON,"E",*HOFF,")xit ? ",ANS
            REP       UPPLOW,ANS
            MATCH     ANSE,ANS
            GOTO      QACCLSS9 IF EQUAL
            MOVE      THREE,CCOL
            MOVE      FIVE,CVERT
            DISPLAY   *PCCOL:CVERT,*EF          
          ENDIF
.
QACCLSS4  DISPLAY   *PCCOL:CVERT,PTACCLSS,"        ",PTACACCM,"       ",PTACCARE
          ADD       ONE,CVERT
          GOTO      QACCLSS2
.
QACCLSS8  DISPLAY   *P1:24,*EL
          CALL      HITENTER
QACCLSS9  MOVE      SAVACCLS,PTACCLSS 
          MOVE      SAVCARE,PTACCARE
          MOVE      SAVACCM,PTACACCM
          CALL      PUTSCR00
          BRANCH    KFLAG,KACS0000,KACT0000,KCAT0000

.***************************************************************************
.* IKEY0000 - Input Key Fields                                             * 
.***************************************************************************
IKEY0000  DISPLAY   *P25:4,*EL,*P25:5,*EL,*P25:6,*EL 
          CALL      KACS0000                * account status
          BRANCH    EXIT,IKEY9500,IKEY9500
.
IKEY1000  DISPLAY   *P25:5,*EL,*P25:6,*EL 
          CALL      KACT0000                * accomodation type
          BRANCH    EXIT,IKEY0000,IKEY9500
.
IKEY2000  DISPLAY   *P25:8,*EL 
          CALL      KCAT0000                * care type
          BRANCH    EXIT,IKEY1000,IKEY9500
.
          BRANCH    OPTION,IKEY3000,IKEY4000,IKEY4000
.
IKEY3000  PACK      KEY6,PTACCLSS,PTACACCM,PTACCARE,SP6                *C-49016
          CALL      RAPTACT1
          BRANCH    OVRCD,IKEY9000
.
          DISPLAY   *P1:24,*B,*EL,"Record Already Exists. ";
          CALL      HITENTER
          GOTO      IKEY2000
.
IKEY4000  PACK      KEY6,PTACCLSS,PTACACCM,PTACCARE,SP6                *C-49016
          CALL      RDPTACT1
          IF        OVRCD=1
            DISPLAY   *P1:24,*B,*EL,"Record Does Not Exist. ";
            CALL      HITENTER
            GOTO      IKEY2000
          ENDIF
.
IKEY9000  MOVE      ZERO,EXIT
          GOTO      IKEY9999
.
IKEY9500  MOVE      ONE,EXIT
          GOTO      IKEY9999
.
IKEY9999  RETURN     
.
+
.***************************************************************************
.* KACT0000 - Keyin Accomodation Type                                      * 
.***************************************************************************
KACT0000  MOVE      TWO,KFLAG 
          KEYIN     *P25:5,*EL,*V2LON,ACCOMTYP
.
          MATCH     QUEST,ACCOMTYP
          GOTO      QACCLSS IF EQUAL
          ENDSET    ACCOMTYP
          APPEND    SP1,ACCOMTYP
          RESET     ACCOMTYP
.
          MATCH     SP1,ACCOMTYP
          GOTO      KACT9500 IF EQUAL
.
          MATCH     EXITCHAR,ACCOMTYP
          GOTO      KACT9700 IF EQUAL
.
          REP       UPPLOW,ACCOMTYP
.
          MOVE      ACCOMTYP,PTACACCM
.
KACT9000  MOVE      ZERO,EXIT
          GOTO      KACT9999
.
KACT9500  MOVE      ONE,EXIT
          GOTO      KACT9999
.
KACT9700  MOVE      TWO,EXIT
          GOTO      KACT9999
.
.
KACT9999  RETURN     
+
.***************************************************************************
.* KCAT0000 - Keyin Care Type                                              * 
.***************************************************************************
KCAT0000  MOVE      THREE,KFLAG
          KEYIN     *P25:6,*EL,*V2LON,CARETYPE
.
          CMATCH    QUEST,CARETYPE                                     *C-49016
          GOTO      QACCLSS IF EQUAL
.
          ENDSET    CARETYPE
          APPEND    SP2,CARETYPE                                       *C-49016
          RESET     CARETYPE
.
          MATCH     SP2,CARETYPE                                       *C-49016
          GOTO      KCAT9500 IF EQUAL
.
          CMATCH    EXITCHAR,CARETYPE                                  *C-49016
          GOTO      KCAT9700 IF EQUAL
.
.******************************************** Start of Deletion         D-49016
.                                           * Trudy said so
.....     TYPE      CARETYPE
.....     IF        !@EQUAL
.....       REP       UPPLOW,CARETYPE
.....       MATCH     ANSU,CARETYPE
.....       IF        !@EQUAL
.....         DISPLAY   *P1:24,*B,"Invalid Care Type. ";
.....         CALL      HITENTER
.....         GOTO      KCAT0000
.....       ENDIF
.....     ENDIF
.********************************************   End of Deletion         D-49016
.
          MOVE      CARETYPE,PTACCARE
.
KCAT9000  MOVE      ZERO,EXIT
          GOTO      KCAT9999
.
KCAT9500  MOVE      ONE,EXIT
          GOTO      KCAT9999
.
KCAT9700  MOVE      TWO,EXIT
          GOTO      KCAT9999
.
KCAT9999  RETURN     
.***************************************************************************
.* KMED0000 - Keyin Medicare Suffix                                        *
.***************************************************************************
KMED0000  DISPLAY   *P25:8,*EL,*V2LON,MEDSUFFI
          KEYIN     *P25:8,*V2LON,*RV,MEDSUFFI:
                    *P25:8,*V2LON,*DV,MEDSUFFI
.
          MATCH     QUEST,MEDSUFFI
          GOTO      KMED1000 IF NOT EQUAL
.
          CALL      GETSCR00
          DISPLAY   *P1:4,*EF:
                    *P1:5,*ULON,*V2LON,"Medicare Suffixes",*HOFF:
                    *P1:6,*V2LON,"1 - ",*HOFF,SUF1:
                    *P1:7,*V2LON,"2 - ",*HOFF,SUF2:
                    *P1:8,*V2LON,"3 - ",*HOFF,SUF3:
                    *P1:9,*V2LON,"4 - ",*HOFF,SUF4:
                    *P1:10,*V2LON,"5 - ",*HOFF,SUF5:
                    *P1:11,*V2LON,"6 - ",*HOFF,SUF6:
                    *P1:12,*V2LON,"7 - ",*HOFF,SUF7:
                    *P1:13,*V2LON,"8 - ",*HOFF,SUF8:
                    *P1:14,*V2LON,"9 - ",*HOFF,SUF9:
                    *P1:15,*V2LON,"10- ",*HOFF,SUF10:
                    *P1:24
          CALL      HITENTER
          CALL      PUTSCR00
          GOTO      KMED0000
.
KMED1000  MATCH     EXITCHAR,MEDSUFFI
          GOTO      KMED9500 IF EQUAL
.
          MATCH     SP3,MEDSUFFI
          GOTO      KMED0000 IF EQUAL
.
. ------- validate medicare suffix -----------------------------------------
.
          TYPE      MEDSUFFI
          GOTO      KMED8000 IF NOT EQUAL
.
          MOVE      MEDSUFFI,PTACMEDI
.
          COMPARE   ONE,PTACMEDI
          GOTO      KMED8000 IF LESS
.
          COMPARE   TEN1,PTACMEDI
          GOTO      KMED8000 IF NOT LESS
.
          MOVE      "Unknown Medicare Suffix ",SUFDESC
          LOAD      SUFDESC,PTACMEDI,SUF1,SUF2,SUF3,SUF4,SUF5:
                                     SUF6,SUF7,SUF8,SUF9,SUF10
          DISPLAY   *P30:8,SUFDESC
          GOTO      KMED9000
.
KMED8000  DISPLAY   *P1:24,*EL,*B,"Invalid Medicare Suffix. ";
          CALL      HITENTER
          GOTO      KMED0000
.
KMED9000  MOVE      ZERO,EXIT
          GOTO      KMED9999
.
KMED9500  MOVE      ONE,EXIT
          GOTO      KMED9999
.
KMED9999  RETURN     
.
+
.***************************************************************************
.*                                INST0000                                 *
.*                        Input the Details                                *
.***************************************************************************
INST0000  CALL      KMED0000                * keyin Medicare Suffix
          BRANCH    EXIT,INST9999
.
INST9999  RETURN
+
.***************************************************************************
.*                                CHGE0000                                 *
.*                        Change the Details                               *
.***************************************************************************
CHGE0000  CALL      MAINQST
          BRANCH    CCITEM,CHGE1000
.
          COMPARE   ZERO,CCITEM
          GOTO      CHGE9000 IF EQUAL       * post
          GOTO      CHGE9100 IF LESS        * cancel
.
          BEEP
          GOTO      CHGE0000
.
. ------- change the details
.
CHGE1000  CALL      KMED0000                * keyin Medicare Suffix
          BRANCH    EXIT,CHGE9999
          GOTO      CHGE0000
.
CHGE9000  MOVE      ZERO,EXIT
          GOTO      CHGE9999
.
CHGE9100  MOVE      ONE,EXIT
.
CHGE9999  RETURN
.
.**************************************************************************
.*  POST0000  : Post DRG records
.**************************************************************************
POST0000  BRANCH    OPTION,POST1000,POST2000,POST3000
          GOTO      POST9999
.
POST1000
.
. ------- write a record
.
          PACK      KEY6,PTACCLSS,PTACACCM,PTACCARE,SP6                *C-49016
          CALL      RAPTACT1
          IF        OVRCD=0
            DISPLAY   *P1:24,*B,*EL,"Record Already Exists. ";
            CALL      HITENTER
          ELSE
            CALL      WRPTACT1
          ENDIF
.
          GOTO      POST9999
.
POST2000
.
. ------- update a record
.
          PACK      KEY6,PTACCLSS,PTACACCM,PTACCARE,SP6                *C-49016
          CALL      RAPTACT1
          IF        OVRCD=1
            DISPLAY   *P1:24,*B,*EL,"Record Does Not Exist. ";
            CALL      HITENTER
          ELSE
            CALL      UPPTACT1
          ENDIF
.
          GOTO      POST9999
.
POST3000
.
. ------- delete a record
.
          CALL      DELQST
          IF        CEXIT = 1
            DISPLAY   *P1:24,*EL,*V2LON,"Deleting..."
            PACK      KEY6,PTACCLSS,PTACACCM,PTACCARE,SP6              *C-49016
            CALL      RAPTACT1
            IF        OVRCD=1
              DISPLAY   *P1:24,*B,*EL,"Record Does Not Exist. ";
              CALL      HITENTER
            ELSE
              CALL      DEPTACT1
            ENDIF
          ENDIF
.
POST9999  RETURN
+
. -------------------------------------------------------------------------
.
          INC       PATACTIO/INC
.
          INC       STD001IO

.******************************************************************************
.* System         :  Patient Billing                                          *
.* Program        :  NZPADT36                                                 *
.* Description    :  Financial Summary Listing program                        *
.******************************************************************************
.* Date           :  31/01/1997                                               *
.* Author         :  J.Tan                                                    *
.* Function       :  Full listing of nzpfinaf file                            *
.* Modifications  :                                                           *
.*        V12.00.01 26/05/2025  Bella Turco    TSK 0955096                    *
.*                  Alphanumeric changes                                      *
.*        V11.00.01 10/03/2020  J.Tan          TSK 0838262                    *
.*                  Added Effective from and to date to MBS Item file         *
.*        V10.13.01 07/12/2013 J.Tan         TSK 0859743                      *
.*                  Mod for OTRECTYP=6 for Theatre item                       *
.*        V10.12.01 15.01.2018  J.Tan   TSK 0848146                           *
.*                  Added CACCFEE=5 - Accumulation FI by Claim code           *
.*        V10.04.01 08/04/2013 J.Tan          CAR 261630                      *
.*                  Removed port number from temp file name                   *
.*        V10.01.02 20/12/2010  Mike Laming   CAR 233046                      *
.*                  Mods to Misc.Charges PATMCHFD - Key change, logic changes *
.*        V9.12.02  13/10/2009  J.Tan   CAR 207195                            *
.*                  Fixed printing Cash from health fund                      *
.*        V9.12.01  18/05/2009  J.Tan   CAR 194470                            *
.*                  Mods printing Cash Sub Total                              *
.*        V9.10.00  16/09/2008  J.Tan   CAR 170967                            *
.*                  Fixed Fees For Service (CACCFEE) breakup                  *
.*        V9.08.06  21/08/2008  J.Tan   CAR 170967                            *
.*                  Fixed Credit notes                                        *
.*        V9.08.05  23/07/2008  J.Tan   CAR 170967                            *
.*                  Fixed Updating for previous year                          *
.*        V9.08.04  23/07/2008  J.Tan   CAR 170967                            *
.*                  Mods for SX Billing                                       *
.*        V9.08.03  23/04/2008  J.Tan   CAR 167215                            *
.*                  Fixed generating credit notes                             *
.*        V9.08.01  21/02/2008  J.Tan   CAR 160240                            *
.*                  Fixed for Emergency procedures dtr record                 *
.*        V9.04.05  08/10/2003  J.Tan   CAR 54081                             *
.*                  Fixed lumpsum and credit notes                            *
.*        V9.04.04  21/09/2003  J.Tan   CAR 52835                             *
.*                  Mods for Multi hospitals                                  *
.*                  Mods for credit notes                                     *
.*        V9.04.03  03/09/2003  J.Tan   CAR 52687                             *
.*                  Fixed problem with caccfee=3 - looping                    *
.*        V9.04.02  03/09/2004  Ebon Clements  CAR 53142                      *
.*                  Joined fileds PINVNO and DUNIQKEY so key has 8 parts      *
.*        V9.04.01  19/08/2004  J.Tan  CAR 52835                              *
.*                  Mods for Multi hospitals                                  *
.*        V9.03.01  16/09/2003  Lina Vo   CAR 40497                           *
.*                  Changed index and read of patmchgf to include effective   *
.*                  date.                                                     *
.*        V9.02.02  04/10/2002  J.Tan                                         *
.*                  Fixed generating GST                                      *
.*        V9.02.01  26/02/2002  J.Tan                                         *
.*                  Mods to use invoice date if effective date is blank       *
.*        V9.02.00  24/02/2002  J.Tan                                         *
.*                  Ported from r5.09                                         *
.*        V6.09.04  12/04/2002  Davin                                         *
.*                  Changed to keyin starting invoice date                    *
.*        V5.09.03  04/01/2002  Davin                                         *
.*                  Changed patfinsf update option to only write 'A' records  *
.*        V5.09.02  18/12/2001  Davin                                         *
.*                  Added option to update fees invoiced file                 *
.*        V5.09.01  01/08/2001  J.Tan                                         *
.*                  Mods FINDATE for accommodation to use Invoice Date        *
.*        V5.09.B01 29/03/2001  J Habasque                                    *
.*                  Casemix mods - removed episode type                       *
.*        V5.08.03  18/08/2000  Jill Habasque                                 *
.*                  Added patdetnt variables                                  *
.*       V5.08.02   17/05/2000  J Habasque                                    *
.*                  Fixed printing of report when no data                     *
.*       V5.07.B03  14/03/2000  J.Tan                                         *
.*                  Mods for GST                                              *
.*        V5.07.01  14/07/1999  J.Tan                                         *
.*                  Fixed the breakup of accommodation                        *
.*        V6.06.01  10/02/1999  Jill Habasque                                 *
.*                  Fixed DISPHEAD display                                    *
.*                   V6.05.02  08/12/1998  Nicole Harrington                  *
.*                             Removed CCENTRY                                *
.*                   V6.04.03  08/04/97 J.Tan                                 *
.*                             Added total only option and ignore merchantcard*
.*                   V6.05.01  27/11/98 J.Tan                                 *
.*                             Mods to display adm.number for invalid period  *
.******************************************************************************
.
          INC       STD001FD
.
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
          INC       AAEDTRFD/INC
          INC       AAEDE1FD/INC
          INC       OUTDTRFD/INC
          INC       OUTSITFD/INC
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
          INC       PATDRGFD/INC
          INC       PATDTRFD/INC
          INC       PATDSCFD/INC
          INC       PATMCHFD/INC
          INC       PATMI1FD/INC
          INC       PATINVFD/INC
          INC       PATITMFD/INC
          INC       PMSFCIFD/INC
          INC       PMSCNOFD/INC
          INC       NZPFINFD/INC
          INC       NZPFIGFD/INC
          INC       PATFINFD/INC
          INC       PATHSPFD/INC
          INC       PATCONFD/INC
          INC       PMSVX1FD/INC
          INC       NZPSPRFD/INC
          INC       NZPRDTFD/INC
          INC       PATFN1FD/INC
+
.
FINDFILE FILE      ASCII, FIXED=256
PATTEMP  IFILE     SQL, FIXED=63, KEY="U1-3,4-4,5-10,11-14,15-15,16-28,29-36,37-44+45-52"
.
. ----- Tempfile consists of following vars -----
.
. Name          Type      Length     Physical     Start   Description
. ----          ----      ------     --------     -----   -----------
.FINHOSP     DIM       3          3          1      Hospital ID
.DFINSYS     DIM       1          1          4      SYSTEM INDICATOR
.                                                   1= AAE, 2=OUT, 3=INP, 4=GST
.FINSITE     DIM       6          6          5      OUTPATIENT SITE
.FINYEAR     DIM       4          4         11      FINANCIAL YEAR
.FINTYPE     DIM       1          1         15      FINANCIAL TYPE
.FINCODE     DIM       13         13        16      FINANCE CODE
.PINVDTE     DIM       8          8         29      INVOICE DATE
.PINVNO      DIM       8          8         37      INVOICE NUMBER
DUNIQKEY     DIM       8          8         45      UNIQUE KEY
.FINAMT      FORM      9.2        7         53      AMOUNT
FPERIOD      FORM      3          3         60      PERIOD
.                                           63      EOR
. ----- EOR 63 -----
.
.
PGSTTOT   FORM      1
CSTRTYR   DIM       8
CURFYEAR  DIM       4
CURRDATE  DIM       8
PATSTCUR  DIM       4
PROCCODE  DIM       13

JRNLCOD  DIM    2
CASHTYP  DIM    1
CASHDESC DIM    12
CREDFLAG  FORM   1
CRDFLAG  FORM   1
SCRDFLAG FORM   1
SCASHDES DIM    12
SFINCODE DIM    13
SUBFIN   DIM    1
NEGONE   FORM    "-1"
NOCREDNT  FORM   1
NOPERIOD FORM   1
DAYFLG   FORM   1
DIM3     DIM    3
DIM4     DIM    4
DIM6     DIM    6
DIM8     DIM    8
DIM9     DIM    9
DIM13    DIM    13
DIM13A   DIM    13
DIM10    DIM    10
DIM16    DIM    16
SDIM10   DIM    10
DIM10A   DIM    10
DIM22    DIM    22
FORM3    FORM   3
FROMPERD FORM   2
TOPERD   FORM   2
FORM12P2 FORM   12.2
FORM10P2 FORM   10.2
FORM82   FORM   8.2
IBCNUGST FORM   1
IBCNMHOS FORM   1
HOSPFLAG FORM   1
AAE      INIT   "A&E"
OUT      INIT   "OUT"
INP      INIT   "INP"
INPAT    INIT   "Inpatient "
AAEPAT   INIT   "A&E       "
OUTPAT   INIT   "Outpatient"
ALLPAT   INIT   "All       "
INVOICE  INIT   "- Invoice "
CASH     INIT   "- Cash    "
JOURNAL  INIT   "- Journals"
ALL      INIT   "- All     "
.
DEFPATH  DIM    60
LUMPFLAG FORM   1
SJRNLCOD DIM    2
SJRNGCOD DIM    2
SCASHTYP DIM    1
SFINTYPE DIM    1
SFINHOSP DIM    3
SFINSYS  FORM   1
SFINSITE DIM    5
STINDATE DIM     8
TODATE   DIM     8
TOTCRNT  FORM    12.2
TOTINVC  FORM    12.2
TOTINVS  FORM    12.2
TOTONLY  FORM    1
FROMDATE DIM     8
FRYEAR   DIM     2
MSCFLAG  DIM     1
MISGRP   DIM     3
.
RECFLAG  FORM    1
RECCNT   FORM    8
UNIQKEY  FORM    8
UPDFLAG  FORM    1
FNAMEI   DIM     8
FNAMES   DIM     8
GNDTOT   FORM    9.2
.
SKEY22   DIM     22
SKEY28   DIM     28
KEY21A   DIM     21
KEY26A   DIM     26
HOSPID   DIM     3
HOSPNAME DIM       50
SAVKEY31 DIM     31
SP13     INIT    "             "
SUBOPT   FORM    1
SUBTOT   FORM    9.2
STATUS   FORM    1
SAVAMT   FORM    12.2
TOTAMT   FORM    12.2
OPCND    FORM    1
CMDLINE  DIM     80
.
PRGID     INIT      "NZPADT36"
PRGNAM    INIT      "Financial Summary Listing"
VERSION   INIT      "V12.01.00"
+
.
ML0000    CALL      INIT0000                     * program initialisation
          BRANCH    EXIT,ML9999
.
ML0100    CALL      OPTN0000
          COMPARE   ZERO,OPTION
          GOTO      ML9999 IF EQUAL
.
          CALL      SOPT0000
          COMPARE   ZERO,SUBOPT
          GOTO      ML0100 IF EQUAL
.
          CALL      KDAT0000                     * keyin date range
          CALL      KIND0000                     * keyin starting invoice date
          CALL      TOTL0000                     * Total only ?
.
          MOVE      ZERO,HOSPFLAG 
          MOVE      SP10,HOSPID
          IF        IBCNMHOS=1
            CALL      KHOS0000                   * Hospital id
            BRANCH    EXIT,ML0100
          ENDIF
.
ML0180    CALL      CONTQST                      * OK to continue ?
          BRANCH    CEXIT,ML0200,ML0100,ML9999   * yes,no,cancel
.
ML0200    BRANCH    OPTION,ML1000,ML1000,ML1000,ML1000
          GOTO      ML9999
.
ML1000    CALL      CRTT0000
          BRANCH    EXIT,ML9999
          CALL      INVN0000
          BRANCH    NOPERIOD,ML9999
          CALL      WFIN0000         * update nzpfinaf
          CALL      PRNT0000         * Print
          CALL      KILL0000
          GOTO      ML0100
.
ML9999    CHAIN     PGM
.
.         Program initialisation
.
INIT0000  CALL          DISPHEAD
          CALL          IBACLOCK
          PACK          CURRDATE,CCC,CYY,CMM,CDD
          REP           " 0",CURRDATE
.
          DISPLAY       *P64:24,"nzpfinaf";
          OPEN          NZPFINA1,"nzpfinaf"
          OPEN          NZPFINA2,"nzpfinaf"
          OPEN          NZPFIGA2,"nzpfigaf"
.
          DISPLAY       *P64:24,"patdschf";
          OPEN          PATDSCH1,"patdschf"
.
          DISPLAY       *P64:24,"patinvrf";
          OPEN          PATINVR1,"patinvrf"
          OPEN          PATINVR2,"patinvrf"
.
          DISPLAY       *P64:24,"aaedtref";
          OPEN          AAEDTRE1,"aaedtref"
          OPEN          AAEDTRE2,"aaedtref"
.
          DISPLAY       *P64:24,"aaede1af";
          OPEN          AAEDE1A1,"aaede1af"
.
          DISPLAY       *P64:24,"outsitaf";
          OPEN          OUTSITA1,"outsitaf"
.
          DISPLAY       *P64:24,"patdtraf";
          OPEN          PATDTRA1,"patdtraf"
          OPEN          PATDTRA2,"patdtraf"
.
          DISPLAY       *P64:24,"patdschf";
          OPEN          PATDSCH1,"patdschf"
.
          DISPLAY       *P64:24,"patdrgaf";
          OPEN          PATDRGA1,"patdrgaf"
          OPEN          PATDRGA2,"patdrgaf"
.
          DISPLAY       *P64:24,"patitemn";
          OPEN          PATITEM1,"patitemn"
.
          DISPLAY       *P64:24,"patmi1af";
          OPEN          PATMI1A1,"patmi1af"
.
          DISPLAY       *P64:24,"patmchgf";
          OPEN          PATMCHG1,"patmchgf"
.
          DISPLAY       *P64:24,"pmsfciaf";
          OPEN          PMSFCIA1,"pmsfciaf"
.
          DISPLAY       *P64:24,"pmscnoaf";
          OPEN          PMSCNOA3,"pmscnoaf"
.
          DISPLAY       *P64:24,"pathspaf";
          OPEN          PATHSPA1,"pathspaf"
.
          DISPLAY       *P64:24,"pmsvx1af";
          OPEN          PMSVX1A1,"pmsvx1af"
.
          OPEN          PATFN1A1,"patfn1af"
.
          DISPLAY       *P64:24,"controlf";
          OPEN          CONTROLF,"controlf"
.
          READ          CONTROLF,ZERO;*43,IBCNMHOS
          READ          CONTROLF,TEN;*192,CACCFEE
          READ          CONTROLF,TEN9;*212,CFEETYP
          READ          CONTROLF,SEVENTY9;*68,PTCNMFEE
          READ          CONTROLF,ZERO;*85,IBCNUGST
          READ          CONTROLF,HUND09;*247,PTCNROUN
.
          OPEN          NZPSPRA1,"nzpspraf"
          OPEN          NZPRDTA1,"nzprdtaf"
INIT9999  RETURN
+
.
.         Keyin date range
.
KDAT0000  DISPLAY    *P1:13,*EF,"Starting Date : "
          MOVE       "13",CVERT
          MOVE       "17",CCOL
          MOVE       SP70,CURFYEAR
.
          OPEN       PATDRGA2,"patdrgaf"
          CALL       PATSTRYR
          UNPACK     CSTRTYR,XCENT,XYEAR,XMON,XDAY
          MOVE       XDAY,IDAY
          MOVE       XMON,IMON
          MOVE       XCENT,ICENT
          MOVE       XYEAR,IYEAR
          CALL       KEYDTE
          PACK       FROMDATE,ICENT,IYEAR,IMON,IDAY
          REP        " 0",FROMDATE
.
          DISPLAY    *P1:15,"  Ending Date :"
          MOVE       "15",CVERT
          MOVE       "17",CCOL
KDAT2000  MOVE       CDD,IDAY
          MOVE       CMM,IMON
          MOVE       CCC,ICENT
          MOVE       CYY,IYEAR
          CALL       KEYDTE
          PACK       TODATE WITH ICENT,IYEAR,IMON,IDAY
          REP        " 0",TODATE
.
          PACK       KEY14,TODATE,SP6    * Position to this date
          CALL       RDSDRGA2            * Go to this date in the file
          CALL       RDKDRGA2            * Get the first record on or after date
          BRANCH     OVRCD,KDAT9000      * No records in file.
.
          MATCH      DRGFDTE,TODATE      * Is the date before the from date ?
          GOTO       KDAT9000 IF LESS    * Yes. Date not found in file.
.
.         Period record found
.
          MOVE       DRGYR,CURFYEAR      * Save financial year
.
          MATCH      FROMDATE,TODATE
          GOTO       KDAT9999 IF NOT LESS
.
          DISPLAY    *P40:24,*V2LON,"** End Date must be >= Start Date **",*W2;
          GOTO       KDAT2000
.
KDAT9000  UNPACK    FINDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          KEYIN     *P1:24,*EL,*V2LON,*B:
                    *DV,PINVADM," ** Date ",*DV,CPCDATE," not in Period Range":
                    " file. Hit <ENTER> to retry ",*EOFF,ANS;
.
.         CMATCH    EXITCHAR,ANS
.
KDAT9999  RETURN
+
.         Keyin starting invoice date
.
KIND0000  DISPLAY    *P1:17,*EF,"Starting Invoice Date : "
          MOVE       "25",CCOL
          MOVE       "17",CVERT
.
          UNPACK     FROMDATE,XCENT,XYEAR,XMON,XDAY
          MOVE       XDAY,IDAY
          MOVE       XMON,IMON
          MOVE       XCENT,ICENT
          MOVE       XYEAR,IYEAR
          CALL       KEYDTE
          PACK       STINDATE,ICENT,IYEAR,IMON,IDAY
          REP        " 0",STINDATE
.
KIND9999  RETURN
+
.
TOTL0000  MOVE      ZERO,TOTONLY
          DISPLAY   *P1:20,*EL,*V2LON,"Total Only ? ",*HOFF:
                    "(",*V2LON,ANSY,*HOFF,"/",*V2LON,ANSN,*HOFF,")  ";
          KEYIN     ANS;
          REP       "yYnN",ANS
          CMATCH    "N",ANS
          GOTO      TOTL9999 IF EQUAL
          CMATCH    "Y",ANS
          GOTO      TOTL9000 IF EQUAL
          BEEP
          GOTO      TOTL0000
.
TOTL9000  MOVE      ONE,TOTONLY             * total only
TOTL9999  RETURN
+
.         Select sub-options
.
OPTN0000  CALL      DISPHEAD
          DISPLAY   *P1:4,*EF;
.
          DISPLAY   *P1:6,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:7,*V2LON,ONE,*HOFF," = Invoices":
                    *P1:8,*V2LON,TWO,*HOFF," = Cash":
                    *P1:9,*V2LON,THREE,*HOFF," = Journals":
.                   *P1:10,*V2LON,FOUR,*HOFF," = All above":
                    *P1:12,"Select Option ? ";
.
OPTN1000  KEYIN     *P17:12,*V2LON,OPTION:
                    *P17:12,*DV,OPTION
.
          MOVE      ZERO,UPDFLAG
.         BRANCH    OPTION,OPTN9999,OPTN9999,OPTN9999,OPTN9999
          BRANCH    OPTION,OPTN9999,OPTN9999,OPTN9999
.
          IF        OPTION=4
            MOVE      ONE,OPTION
            MOVE      ONE,UPDFLAG
            GOTO      OPTN9999
          ENDIF
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9999 IF EQUAL
          BEEP
          GOTO      OPTN1000
.
OPTN9999  RETURN
+
SOPT0000  DISPLAY   *P1:13,*EF:
                    *P1:13,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:14,*V2LON,ONE,*HOFF," = Inpatient":
                    *P1:15,*V2LON,TWO,*HOFF," = A&E":
                    *P1:16,*V2LON,THREE,*HOFF," = Outpatient":
                    *P1:17,*V2LON,FOUR,*HOFF," = All above":
                    *P1:18,"Select Option ? ";
.
SOPT1000  KEYIN     *P17:18,*V2LON,SUBOPT:
                    *P17:18,*DV,SUBOPT
.
          BRANCH    SUBOPT,SOPT2000,SOPT3000,SOPT4000,SOPT9999
          COMPARE   ZERO,SUBOPT
          GOTO      SOPT9999 IF EQUAL
          BEEP
          GOTO      SOPT1000
.
SOPT2000  DISPLAY   *P53:2,*V2LON," - Inpatient";
          GOTO      SOPT9999
.
SOPT3000  DISPLAY   *P53:2,*V2LON," - A&E";
          GOTO      SOPT9999
.
SOPT4000  DISPLAY   *P53:2,*V2LON," - Outpatient";
.
SOPT9999  RETURN
+
.
CRTT0000  DISPLAY   *P1:12,*EL,"Building temporary file.  Please wait...";
          CALL      INDZ0000
          DISPLAY   *P1:12,*EF;
          BRANCH    OPCND OF CRTT9000
.
          MOVE      ZERO,EXIT
          GOTO      CRTT9999
.
CRTT9000  MOVE      ONE,EXIT
.
CRTT9999  RETURN
+
.
.         Process invoices
.
INVN0000  DISPLAY    *P1:24,*EL,*P10:24,"Invoice Date :";
.
          MOVE       ZERO,NOPERIOD
          MOVE       ZERO,RECCNT
          MOVE       ZERO,UNIQKEY
          MOVE       ZERO,FROMPERD
          MOVE       ZERO,TOPERD
    MOVE    ZERO,TOTINVS
   MOVE   ZERO,TOTCRNT
          PACK       KEY16,STINDATE,SP20
          CALL       RDSINV2
.
.             Main Loop
.
INVN1000  CALL       RDKINV2
          BRANCH     OVRCD OF INVN9000
.
          ADD        ONE,RECCNT
          IF         (RECCNT%50) = 0
            DISPLAY    *P30:24,*V2LON,PINVDTE,SP1,RECCNT;
          ENDIF
.
.         Ignore cancelled invoices
.
          MATCH      ZEROVISN,PINVADM
          GOTO       INVN1000 IF EQUAL
.
          MOVE       SP70,ADATE
          MOVE       SP70,DDATE
          MOVE       ZERO,DAYFLG
          PACK       KEY8,PINVADM
          CALL       RDMISS1
          IF         OVRCD<>1 & ASTAT=3
            CALL       RDDSCH1
            MATCH      ADATE,DDATE
            IF         @EQUAL
              MOVE       ONE,DAYFLG
            ENDIF
          ENDIF
.
          MOVE       SP10,PMVXMHOS
          MOVE       PINVADM,KEY8
          CALL       RDPMVX11
          IF         IBCNMHOS=1
            MATCH      SP10,HOSPID
            IF         !@EQUAL
              MATCH      PMVXMHOS,HOSPID
              GOTO       INVN1000 IF NOT EQUAL
            ENDIF
          ENDIF
.
.         Determine which system this invoice is for
.
          BRANCH     PINVSYS,AAEI000,OUTI000,INPI000,OUTI000
.
          DISPLAY    *P1:12,*V2LON,*B,*EL,"** ERROR: Invalid PINVSYS of ":
                     PINVSYS," for invoice ",PINVNO," **";
          GOTO       INVN1000
.
.         Process this A+E invoice
.
AAEI000   BRANCH     SUBOPT,INVN1000,AAEI050,INVN1000
AAEI050   DISPLAY    *P50:24,"AE ";
          MOVE       PINVADM,ATNUMB
          PACK       KEY22,ATNUMB,PINVNO,SP10
          CALL       RDSDTRE1
AAEI100   CALL       RDKDTRE1
          BRANCH     OVRCD,AAEI600
.
          MATCH      PINVNO,ATINVNO
          GOTO       AAEI600 IF NOT EQUAL
.
          MATCH      ATNUMB,PINVADM
          GOTO       AAEI600 IF NOT EQUAL
.
.
.         Only interested in invoice section
.
          BRANCH     ATRECTYP,AAEI200,AAEI300,AAEI400,AAEI300,AAEI200
          GOTO       AAEI100
.
.         A+E misc charge
.
AAEI200   BRANCH     OPTION,AAEI205,AAEI100,AAEI100
.   
AAEI205   COMPARE    FIVE,ATRECTYP
          GOTO       AAEI210 IF EQUAL     * Procedure
.
          MATCH      SP3,ATMISGRP
          GOTO       AAEI210 IF NOT EQUAL
.
          MOVE       ATITEMNO,TITEMNO
          CALL       GETM0000             * Get the miscellaneous group code
          MOVE       MMSCGRP,ATMISGRP
.
AAEI210   MOVE       ANSA,FINTYPE
          MOVE       ONE,FINSYS
          MOVE       SP6,FINSITE
          IF         PTCNMFEE = 1
            MOVE       DATNUMB,KEY8
            CALL       RDDETA1
            PACK       FINCODE,ANSM,ADACOMP,ATMISGRP,SP20
          ELSE
            PACK       FINCODE,ANSM,ATMISGRP,ATITEMNO,SP20
          ENDIF
          MOVE       ATPATAMT,FINAMT
.
          PACK       FINDATE,PINVDTE
.         PACK       FINDATE,ATDTEFFD,SP10
          REP        " 0",FINDATE
          CALL       CFIN0000
.
          IF         IBCNUGST=2 & ATDTGSTM<>0
            MOVE       ANSG,FINTYPE
            MOVE       ATDTGSTM,FINAMT
            PACK       FINDATE,PINVDTE
.           PACK       FINDATE,ATDTEFFD
            REP        " 0",FINDATE
            CALL       CFIN0000
            MOVE       ANSA TO FINTYPE
          ENDIF
.
          BRANCH     NOPERIOD,INVN9000
          GOTO       AAEI100
.
.         Process this cash receipt
.
AAEI300   BRANCH     OPTION,AAEI100,AAEI310,AAEI100
.
AAEI310   COMPARE    FOUR,ATPAYTYP               * Merchant card ?
          GOTO       AAEI100 IF EQUAL            * Yes, ignore
.
          MOVE       ANSB TO FINTYPE
          MOVE       ONE,FINSYS
          MOVE       SP6,FINSITE
          MOVE       ATPATAMT TO FINAMT
          MULT       NEGONE,FINAMT
          PACK       FINDATE,ATDTEFFD
          REP        " 0",FINDATE
          PACK       FINCODE,SP20
          MOVE       "ACASH -  A/E   ",FINCODE
.
          CALL       CFIN0000
          BRANCH     NOPERIOD,INVN9000
          GOTO       AAEI100
.
.         Process this journal
.
AAEI400   BRANCH     OPTION,AAEI100,AAEI100,AAEI410
.  
AAEI410   MOVE       ANSC TO FINTYPE
          MOVE       ONE,FINSYS
          MOVE       SP6,FINSITE
          MOVE       ATPATAMT TO FINAMT
          MULT       NEGONE,FINAMT
.
          PACK       FINCODE,SP20
          PACK       FINCODE,ATTYPE,SP20
.         MOVE       "JOURNAL      ",FINCODE
          PACK       FINDATE,ATDTEFFD
          REP        " 0",FINDATE
          CALL       CFIN0000
          BRANCH     NOPERIOD,INVN9000
.
          IF         (IBCNUGST=3 | IBCNUGST=2) & ATDTGSTM<>0
            MOVE       ANSH,FINTYPE
            MOVE       ATDTGSTM,FINAMT
            MULT       NEGONE,FINAMT
            PACK       FINDATE,ATDTEFFD
            REP        " 0",FINDATE
            CALL       CFIN0000
          ENDIF
.
          GOTO       AAEI100
.
AAEI600   PACK       KEY22,PINVADM,PINVNO,SP70
          CALL       CRDN0000
          GOTO       INVN1000
+
...............................................................................
.       Process this outpatient invoice
.
OUTI000   BRANCH     SUBOPT,INVN1000,INVN1000
          DISPLAY    *P50:24,"OP ";
.
          MOVE       "out",OSTFILE
          MOVE       PINVSITE,KEY6
          CALL       RDSITA1
.
          CLOSE      OUTDTRO1
          PACK       CFNAME,OSTFILE,FILDTRO1
          OPEN       OUTDTRO1,CFNAME
.
          MOVE       PINVADM,OTNUMB
          PACK       KEY22,OTNUMB,PINVNO,SP10
          CALL       RDSDTRO1
OUTI100   CALL       RDKDTRO1
          BRANCH     OVRCD,OUTI600
.
          MATCH      PINVNO,OTINVNO
          GOTO       OUTI600 IF NOT EQUAL
.
          MATCH      OTNUMB,PINVADM
          GOTO       OUTI600 IF NOT EQUAL
.
.         Only interested in invoice section
.
          BRANCH     OTRECTYP,OUTI200,OUTI300,OUTI400
          COMPARE    SIX,OTRECTYP
          GOTO       OUTI200 IF EQUAL     * Theatre item
          GOTO       OUTI100
.
.         Outpatient misc charge
.
OUTI200   BRANCH     OPTION,OUTI205,OUTI100,OUTI100
.
OUTI205   MATCH      SP3,OTMISGRP
          GOTO       OUTI210 IF NOT EQUAL
.
          MOVE       OTITEMNO,TITEMNO
          CALL       GETM0000             * Get the miscellaneous group code
          MOVE       MMSCGRP,OTMISGRP
.
OUTI210   MOVE       ANSA,FINTYPE
          MOVE       TWO,FINSYS
          MOVE       PINVSITE,FINSITE
.
          IF         PTCNMFEE = 1
            MOVE       "out",OSTFILE
            MOVE       PINVSITE,KEY6
            CALL       RDSITA1
            PACK       CFNAME,OSTFILE,FILBB1A1
            CALL       OPOTBB11
.
            MOVE       OTNUMB,KEY8
            CALL       RDBOKB1
            PACK       FINCODE,ANSM,OBACOMP,OTMISGRP,SP20
          ELSE
            PACK       FINCODE,ANSM,OTMISGRP,OTITEMNO,SP20
          ENDIF
.
          MOVE       OTPATAMT,FINAMT
          PACK       FINDATE,PINVDTE
.         PACK       FINDATE,OTDTEFFD
          REP        " 0",FINDATE
          CALL       CFIN0000
          BRANCH     NOPERIOD,INVN9000
.
          IF         IBCNUGST=2 & OTDTGSTM<>0
            MOVE       ANSG,FINTYPE
            MOVE       OTDTGSTM,FINAMT
            PACK       FINDATE,PINVDTE
.           PACK       FINDATE,OTDTEFFD
            REP        " 0",FINDATE
            CALL       CFIN0000
            MOVE       ANSA TO FINTYPE
          ENDIF
.
          GOTO       OUTI100
.
.         Process this cash receipt
.
OUTI300   BRANCH     OPTION,OUTI100,OUTI310,OUTI100
.
OUTI310   COMPARE    FOUR,OTPAYTYP               * Merchant card ?
          GOTO       OUTI100 IF EQUAL            * Yes, ignore
.
          MOVE       ANSB TO FINTYPE
          MOVE       TWO,FINSYS
          MOVE       SP6,FINSITE
          MOVE       OTPATAMT TO FINAMT
          MULT       NEGONE,FINAMT
          PACK       FINDATE,OTDTEFFD
          REP        " 0",FINDATE
.
          PACK       FINCODE,SP20
          MOVE       "ACASH -  O/P ",FINCODE
          CALL       CFIN0000
          BRANCH     NOPERIOD,INVN9000
          GOTO       OUTI100
.
.         Process this journal
.
OUTI400   BRANCH     OPTION,OUTI100,OUTI100,OUTI410
.
OUTI410   MOVE       ANSC TO FINTYPE
          MOVE       TWO,FINSYS
          MOVE       SP6,FINSITE
          MOVE       OTPATAMT TO FINAMT
          MULT       NEGONE,FINAMT
.
          PACK       FINCODE,OTTYPE,SP20
.         MOVE       "JOURNAL - O/P",FINCODE
          PACK       FINDATE,OTDTEFFD
          REP        " 0",FINDATE
          CALL       CFIN0000
          BRANCH     NOPERIOD,INVN9000
.
          IF         (IBCNUGST=3 | IBCNUGST=2) & OTDTGSTM<>0
            MOVE       ANSH,FINTYPE
            MOVE       OTDTGSTM,FINAMT
            MULT       NEGONE,FINAMT
            PACK       FINDATE,OTDTEFFD
            REP        " 0",FINDATE
            CALL       CFIN0000
          ENDIF
.
          GOTO       OUTI100
.
OUTI600   PACK       KEY22,PINVADM,PINVNO,SP70
          CALL       CRDN0000
          GOTO       INVN1000
.
.         Process this inpatient invoice
.
INPI000   BRANCH     SUBOPT,INPI050,INVN1000,INVN1000
.
INPI050   DISPLAY    *P50:24,"IP ";
          MOVE       ZERO,TOTINVC
          PACK       KEY22,PINVADM,PINVNO,SP6
          CALL       RDSDTRN1
INPI100   CALL       RDKDTRN1
          BRANCH     OVRCD,INPI900
.
          MATCH      PINVNO,TREF
          GOTO       INPI900 IF NOT EQUAL
.
          MATCH      PINVADM,TADMNO
          GOTO       INPI101 IF EQUAL
.
          DISPLAY    *P1:24,*EL,"Invalid Admission No for Invoice ",TREF;
          KEYIN      ANS
.
.         Only interested in invoice section
.
INPI101   IF         CFEETYP=5
            BRANCH     TRECTYPE,INPI200,INPI300,INPI300,INPI100,INPI500:
                                INPI600,INPI700,INPI800       * SX
          ELSE
            IF         CFEETYP=9
              BRANCH     TRECTYPE,INPI200,INPI300,INPI300,INPI100,INPI500:
                                  INPI600       * Johns Hopkins
            ELSE
              BRANCH     TRECTYPE,INPI200,INPI300,INPI300,INPI100,INPI500:
                                  INPI600
            ENDIF
          ENDIF
          GOTO       INPI100
.
.         Inpatient accommodation
.
INPI200   BRANCH     OPTION,INPI210,INPI100,INPI100
.
INPI210   MOVE       ANSA TO FINTYPE
          MOVE       THREE,FINSYS
          MOVE       SP6,FINSITE
          MOVE       TPATAMTT TO FINAMT
          UNPACK     PINVDTE WITH CCENT,CYEAR,CMON,CDAY
.         SUB        TPATAMTG FROM FINAMT
.
          IF         CFEETYP=7
            CALL       NHOM0000         * Adjusting fins file for nursing home 
            GOTO       INPI100
          ENDIF
.
          IF         PTINCMIX=1
            PACK       FINCODE,ANSD,TDWARD,SP20
.           PACK       FINDATE,PTDTEFFD 
            PACK       FINDATE,PINVDTE
            REP        " 0",FINDATE
            CALL       CFIN0000
            BRANCH     NOPERIOD,INVN9000
.
            IF         IBCNUGST=2 & PTDTGSTM<>0
              MOVE       ANSG,FINTYPE
              MOVE       PTDTGSTM,FINAMT
              PACK       FINDATE,PINVDTE
              REP        " 0",FINDATE
              CALL       CFIN0000
              MOVE       ANSA TO FINTYPE
            ENDIF
.
            MATCH      SP3,TADMTYP
            GOTO       INPI220 IF NOT EQUAL
.
            MOVE       TADMNO,KEY8
            CALL       RDMISS1
            MOVE       ATYPE,TADMTYP
.
INPI220     MOVE       PTDTLSPT,FINAMT
            ADD        PTDTLSRB,FINAMT
            PACK       FINCODE,ANSC,DAYFLG,TADMTYP,SP20
.           PACK       FINDATE,PTDTEFFD
            PACK       FINDATE,PINVDTE
            REP        " 0",FINDATE
            CALL       CFIN0000
            BRANCH     NOPERIOD,INVN9000
.
            IF         IBCNUGST=2 & PTDTGSTL<>0
              MOVE       ANSG,FINTYPE
              MOVE       PTDTGSTL,FINAMT
              PACK       FINDATE,PINVDTE
              REP        " 0",FINDATE
              CALL       CFIN0000
              MOVE       ANSA TO FINTYPE
            ENDIF
            GOTO       INPI100
          ENDIF
.
          BRANCH     CACCFEE,INPI240:        * by Ward/claim
                             INPI250:        * by Ward/Adm.type
                             INPI240:        * by Ward/Claim/Adm.type
                             INPI240:        * by Ward/Adm.type/Claim
                             INPI232         * by Claim
.
.         Accommodation by admission type
.
          MATCH      SP3,TADMTYP
          GOTO       INPI230 IF NOT EQUAL
.
          MOVE       TADMNO,KEY8
          CALL       RDMISS1
          MOVE       ATYPE,TADMTYP
.
INPI230   PACK       FINCODE WITH ANSA,TADMTYP,SP20
          GOTO       INPI234
.
.         Accommodation by Claim type
.
INPI232   MATCH      SP3,TCLMTYP
          GOTO       INPI233 IF NOT EQUAL
.
          MOVE       PINVCLM,TCLMTYP
.
INPI233   PACK       FINCODE WITH ANSA,TCLMTYP,SP20
.
INPI234
.         PACK       FINDATE,PTDTEFFD
          PACK       FINDATE,PINVDTE
          REP        " 0",FINDATE
          CALL       CFIN0000
          BRANCH     NOPERIOD,INVN9000
.
          IF         IBCNUGST=2 & PTDTGSTM<>0
            MOVE       ANSG,FINTYPE
            MOVE       PTDTGSTM,FINAMT
            PACK       FINDATE,PINVDTE
            REP        " 0",FINDATE
            CALL       CFIN0000
          ENDIF
          GOTO       INPI100
.
.         Accommodation by ward/claim type
.
INPI240   MATCH      SP3,TCLMTYP
          GOTO       INPI250 IF NOT EQUAL
.
          MOVE       PINVCLM,TCLMTYP
.
INPI250   MATCH      SP3,TDWARD
          GOTO       INPI260 IF NOT EQUAL
.
          MOVE       "(",ANS
          SCAN       ANS,TDDESC
          GOTO       INPI255 IF NOT EQUAL
.
          BUMP       TDDESC
          MOVE       TDDESC,TDWARD
          GOTO       INPI260
.
INPI255   RESET      TDDESC,19
          MOVE       TDDESC,TDWARD
.
INPI260   BRANCH     CACCFEE,INPI261,INPI262,INPI263,INPI280,INPI100
          GOTO       INPI100
.
.         1 - Ward/claim type
.
INPI261   PACK       FINCODE,ANSA,TDWARD,TCLMTYP,SP20
.         PACK       FINDATE,PTDTEFFD
          PACK       FINDATE,PINVDTE
          REP        " 0",FINDATE
          CALL       CFIN0000
          BRANCH     NOPERIOD,INVN9000
          IF         IBCNUGST=2 & PTDTGSTM<>0
            MOVE       ANSG,FINTYPE
            MOVE       PTDTGSTM,FINAMT
            PACK       FINDATE,PINVDTE
            REP        " 0",FINDATE
            CALL       CFIN0000
          ENDIF
          GOTO       INPI100
.
.         2 - Ward/Admission type
.
INPI262   MATCH      SP3,TADMTYP
          IF         @EQUAL
            MOVE       TADMNO,KEY8
            CALL       RDMISS1
            MOVE       ATYPE,TADMTYP
          ENDIF
.
          PACK       FINCODE,ANSA,TDWARD,TADMTYP,SP20
.         PACK       FINDATE,PTDTEFFD
          PACK       FINDATE,PINVDTE
          REP        " 0",FINDATE
          CALL       CFIN0000
          BRANCH     NOPERIOD,INVN9000
          IF         IBCNUGST=2 & PTDTGSTM<>0
            MOVE       ANSG,FINTYPE
            MOVE       PTDTGSTM,FINAMT
            PACK       FINDATE,PINVDTE
            REP        " 0",FINDATE
            CALL       CFIN0000
          ENDIF
          GOTO       INPI100
.
.         3 - Ward/Claim/Admission type
.
INPI263   MATCH      SP3,TADMTYP
          GOTO       INPI272 IF NOT EQUAL
.
          MOVE       TADMNO,KEY8
          CALL       RDMISS1
          BRANCH     OVRCD,INPI272
          MOVE       ATYPE,TADMTYP
.
INPI272   PACK       FINCODE,ANSA,TDWARD,TCLMTYP,TADMTYP,SP70
.         PACK       FINDATE,PTDTEFFD
          PACK       FINDATE,PINVDTE
          REP        " 0",FINDATE
          CALL       CFIN0000
          BRANCH     NOPERIOD,INVN9000
          IF         IBCNUGST=2 & PTDTGSTM<>0
            MOVE       ANSG,FINTYPE
            MOVE       PTDTGSTM,FINAMT
            PACK       FINDATE,PINVDTE
            REP        " 0",FINDATE
            CALL       CFIN0000
          ENDIF
          GOTO       INPI100
.
.         4 - Ward/adm type/claim type
.
INPI280   MATCH      SP3,TADMTYP
          GOTO       INPI282 IF NOT EQUAL
.
          MOVE       TADMNO,KEY8
          CALL       RDMISS1
          BRANCH     OVRCD,INPI282
          MOVE       ATYPE,TADMTYP
.
INPI282   PACK       FINCODE,ANSA,TDWARD,TADMTYP,TCLMTYP,SP20
.         PACK       FINDATE,PTDTEFFD
          PACK       FINDATE,PINVDTE
          REP        " 0",FINDATE
          CALL       CFIN0000
          BRANCH     NOPERIOD,INVN9000
          IF         IBCNUGST=2 & PTDTGSTM<>0
            MOVE       ANSG,FINTYPE
            MOVE       PTDTGSTM,FINAMT
            PACK       FINDATE,PINVDTE
            REP        " 0",FINDATE
            CALL       CFIN0000
          ENDIF
          GOTO       INPI100
.
.         Inpatient misc item
.
INPI300   BRANCH     OPTION,INPI310,INPI100,INPI100
.
INPI310   MOVE       ANSA TO FINTYPE
          MATCH      SP3,TMISGRP
          GOTO       INPI320 IF NOT EQUAL
.
          CALL       GETM0000
          MOVE       MMSCGRP,TMISGRP
.
INPI320   MOVE       TADMNO,KEY8
          CALL       RDMISS1
.
          IF         PTINCMIX=1
            PACK       DIM1,DAYFLG,SP10
            IF         PTCNMFEE = 1
              PACK       FINCODE WITH ANST,DIM1,ACLAIM,TMISGRP,SP20
            ELSE
              PACK       FINCODE WITH ANST,DIM1,TMISGRP,TITEMNO,SP20
            ENDIF
          ELSE
            IF         PTCNMFEE = 1
              PACK       FINCODE WITH ANSM,ACLAIM,TMISGRP,SP20
            ELSE
              PACK       FINCODE WITH ANSM,TMISGRP,TITEMNO,SP20
            ENDIF
          ENDIF
.
          MOVE       THREE,FINSYS
          MOVE       SP6,FINSITE
          MOVE       TPATAMTT TO FINAMT
.         PACK       FINDATE,PTDTEFFD
          PACK       FINDATE,PINVDTE
          REP        " 0",FINDATE
          CALL       CFIN0000
          BRANCH     NOPERIOD,INVN9000
          IF         IBCNUGST=2 & PTDTGSTM<>0
            MOVE       ANSG,FINTYPE
            MOVE       PTDTGSTM,FINAMT
            PACK       FINDATE,PINVDTE
            REP        " 0",FINDATE
            CALL       CFIN0000
          ENDIF
          GOTO       INPI100
.
.         Cash
.
INPI500   BRANCH     OPTION,INPI100,INPI505,INPI100
.
INPI505   COMPARE    FOUR,TPAYTYPE           * merchant card ?
          GOTO       INPI100 IF EQUAL        * yes, ignore
.
          MOVE       ANSB,FINTYPE
          MOVE       THREE,FINSYS
          MOVE       SP6,FINSITE
          MOVE       TPATAMTT TO FINAMT
          MULT       NEGONE,FINAMT
          PACK       FINDATE,PTDTEFFD
.         PACK       FINDATE,PINVDTE
          REP        " 0",FINDATE
.
          MATCH      ANSP,TTYPEDEB
          GOTO       INP510 IF EQUAL
.
          MATCH      ANSH,TTYPEDEB
          GOTO       INP520 IF EQUAL
.
          MATCH      ANSI,TTYPEDEB
          GOTO       INP530 IF EQUAL
.
INP510    PACK       FINCODE,ANSA,SP20
          CALL       CFIN0000
          BRANCH     NOPERIOD,INVN9000
          GOTO       INPI100
.
INP520    MATCH      SP70,TDCODE
          IF         !@EQUAL
            MOVE       "0000    ",HFTABL
            UNPACK     TDCODE,HCODE
            PACK       KEY14,HCODE,HFTABL,SP70
            CALL       RDFUND1
            IF         OVRCD=0
              PACK       FINCODE,ANSB,TDCODE,SP20
              GOTO       INP525
            ENDIF
          ENDIF
          PACK       FINCODE,ANSB,AFUNDH,SP20
INP525    CALL       CFIN0000
          BRANCH     NOPERIOD,INVN9999
          GOTO       INPI100
.
INP530    PACK       FINCODE,ANSI,TDCODE,SP20
          CALL       CFIN0000
          BRANCH     NOPERIOD,INVN9999
          GOTO       INPI100
.
.         Process this journal
.
INPI600   BRANCH     OPTION,INPI100,INPI100,INPI610
.
INPI610   MOVE       ANSC TO FINTYPE
          MOVE       THREE,FINSYS
          MOVE       SP6,FINSITE
          MOVE       TPATAMTT TO FINAMT
          MULT       NEGONE,FINAMT
.
          PACK       FINCODE,TTYPE,SP20
          COMPARE    THREE,CACCFEE
          GOTO       INPI620 IF NOT EQUAL
          PACK       FINCODE,TTYPE,SP3,PINVCLM,SP20
.
INPI620   
          PACK       FINDATE,PTDTEFFD
.         PACK       FINDATE,PINVDTE
          REP        " 0",FINDATE
          CALL       CFIN0000
          BRANCH     NOPERIOD,INVN9000
.
          IF         (IBCNUGST=3 | IBCNUGST=2) & PTDTGSTM<>0
            MOVE       ANSH,FINTYPE
            MOVE       PTDTGSTM,FINAMT
            MULT       NEGONE,FINAMT
            PACK       FINDATE,PTDTEFFD
.           PACK       FINDATE,PINVDTE
            REP        " 0",FINDATE
            CALL       CFIN0000
          ENDIF
          GOTO       INPI100
.
.         Rounding
.
INPI700   BRANCH     OPTION,INPI710,INPI100,INPI100
.
INPI710   MOVE       ANSA TO FINTYPE
          MOVE       THREE,FINSYS
          MOVE       SP6,FINSITE
          MOVE       TPATAMTT TO FINAMT
.
          IF        PTCNMFEE = 1
            PACK      FINCODE WITH ANSM,ACLAIM,PTCNROUN,SP70
          ELSE
            PACK      FINCODE WITH ANSM,PTCNROUN,SP70
          ENDIF
          PACK       FINDATE,PINVDTE
          REP        " 0",FINDATE
          CALL       CFIN0000
          GOTO       INPI100
.
.         SX Procedure fees
.
INPI800   BRANCH     OPTION,INPI810,INPI100,INPI100
.
INPI810   MOVE       ANSA TO FINTYPE
          MOVE       THREE,FINSYS
          MOVE       SP6,FINSITE
          MOVE       TPATAMTT TO FINAMT
          PACK       FINDATE,PINVDTE
          REP        " 0",FINDATE
.
          BRANCH     PTDTDTYP,INPI820        * patient invoice
          GOTO       INPI840
.
.         Patient invoice
.
INPI820   MATCH     "1",PTDTCONT
          IF        @EQUAL
            PACK      FINCODE,ANSC,PTDTCNTR,PTDTCPRC,SP70
          ELSE
            PACK      FINCODE,ANSB,PTDTCNTR,PTDTCPRC,SP70
          ENDIF
          CALL      CFIN0000
          BRANCH    NOPERIOD,INVN9000
          IF        IBCNUGST=2 & PTDTGSTM<>0
            MOVE      ANSG,FINTYPE
            MOVE      PTDTGSTM,FINAMT
            PACK      FINDATE,PINVDTE
            REP       " 0",FINDATE
            CALL      CFIN0000
          ENDIF
          GOTO       INPI100
.
.         Funder invoice
.
INPI840   MATCH     "1",PTDTCONT
          IF        @EQUAL
            PACK      FINCODE,ANSF,PTDTCNTR,PTDTCPRC,SP70
          ELSE
            PACK      FINCODE,ANSD,PTDTCNTR,PTDTCPRC,SP70
          ENDIF
          CALL      CFIN0000
          BRANCH    NOPERIOD,INVN9000
          IF        IBCNUGST=2 & PTDTGSTM<>0
            MOVE      ANSG,FINTYPE
            MOVE      PTDTGSTM,FINAMT
            PACK      FINDATE,PINVDTE
            REP       " 0",FINDATE
            CALL      CFIN0000
          ENDIF
          GOTO      INPI100
.
INPI900   COMPARE   ONE,OPTION
          GOTO      INVN1000 IF NOT EQUAL
.
          UNPACK    PINVDTE,DIM8
          REP       " 0",DIM8
          MATCH     "00000000",DIM8
          IF        @EQUAL
            MOVE      PINVDTE,DIM8
            REP       " 0",DIM8
            MOVE      DIM8,FINDATE
          ENDIF
.
          MATCH     FROMDATE,DIM8
          GOTO      INVN1000 IF LESS
          MATCH     DIM8,TODATE
          GOTO      INVN1000 IF LESS
.
          ADD       TOTINVC,TOTINVS
          PRINT     *1,"PINVADM ",PINVADM," INVOICE NO: ",PINVNO:
                    " DATE ",PINVDTE," AMT ",TOTINVC," TOT ",TOTINVS
.
          IF        IBCNUGST=3
            PACK      FINCODE,ANSA,SP70
            MOVE      ANSG,FINTYPE
            MOVE      PTINGSTA,FINAMT
            PACK      FINCODE,ANSG,SP70
            PACK      FINDATE,PINVDTE
            REP       " 0",FINDATE
            CALL      CFIN0000
.
            ADD       PTINGSTA,TOTINVS
            MOVE      SP70,KEY8
            PRINT     *1,"        ",KEY8,"             ",KEY8," GST  ",KEY8:
                      " AMT ",PTINGSTA," TOT ",TOTINVS;
          ENDIF
.
          PACK      KEY22,PINVADM,PINVNO,SP70
          CALL      CRDN0000
          IF        NOCREDNT=0
            PRINT     *N;
          ENDIF
          GOTO      INVN1000
.
INVN9000  COMPARE   ONE,OPTION
          GOTO      INVN9999 IF NOT EQUAL
.
          CALL       UND132
          PRINT      *73,"TOT ",TOTINVS:
                     *95,"CRD NOTE:",TOTCRNT
          CALL       UND132
.
INVN9999  RETURN
+
.----------------------------------------------------------------
.         Check credit notes 
.----------------------------------------------------------------
CRDN0000  MOVE       ZERO,LUMPFLAG
          MOVE       ZERO,NOCREDNT
          MOVE       ZERO,FORM10P2
          COMPARE    ONE,OPTION
          GOTO       CRDN9000 IF NOT EQUAL
.
          MOVE       KEY22,SKEY22
          PACK       KEY30,SKEY22,SP70
          CALL       RSPMFCI1
CRDN1000  CALL       RKPMFCI1
          BRANCH     OVRCD,CRDN2000
          PACK       DIM22,PMFCVISN,PMFCINVN,SP70
          MATCH      SKEY22,DIM22
          GOTO       CRDN2000 IF NOT EQUAL
.
          MOVE       ONE,CREDFLAG
          MULT       "-1",PMFCCAMT
          MULT       "-1",PMFCPTLS
          MULT       "-1",PMFCRBLS
          MULT       "-1",PMFCGSTM
          MULT       "-1",PMFCCGST
          MULT       "-1",PMFCGSTL
          CALL       UPFIN000             * Update nzpfinaf
          GOTO       CRDN1000
.
CRDN2000  COMPARE    THREE,IBCNUGST
          GOTO       CRDN9000 IF NOT EQUAL
.
          PACK       KEY16,PINVNO,SP70
          CALL       RSPMCNO3
CRDN2200  CALL       RKPMCNO3
          BRANCH     OVRCD,CRDN9000
.
          MATCH      PINVNO,PMCNINVN       * Same invoice number
          GOTO       CRDN9000 IF NOT EQUAL
.
          COMPARE    ZERO,PMCNGSTM
          GOTO       CRDN2200 IF EQUAL
.
          MOVE       ONE,CREDFLAG
          PACK       FINCODE,ANSQ,SP70
          MOVE       PMCNGSTM,FINAMT
          MULT       "-1",FINAMT
          MOVE       "Y",FINTYPE            * GST Credit note
          MOVE       PMCNDATE,FINDATE
          REP        " 0",FINDATE
          CALL       CFIN0000
          GOTO       CRDN2200
.
CRDN9000  MOVE       ZERO,CREDFLAG
CRDN9999  RETURN
+
.----------------------------------------------------------------
.         Update nzpfinaf (financial file)
.----------------------------------------------------------------
UPFIN000  MOVE      PMFCCRDA,FINDATE
          REP       " 0",FINDATE
          MOVE      "F",FINTYPE             * for credit note
.
          MOVE      "0",KEY1
          MATCH     ADATE,DDATE
          IF        @EQUAL
            MOVE      "1",KEY1              * daycase
          ENDIF
.
.        update the summary financial file
.
          MOVE      PMFCRTYP,FORM1
          IF        CFEETYP=5
            BRANCH    FORM1,UPFIN020,UPFIN500,UPFIN500,UPFIN999:
                            UPFIN999,UPFIN999,UPFIN500,UPFIN600   * SX
          ELSE
            IF        CFEETYP=9
              BRANCH    FORM1,UPFIN020,UPFIN500,UPFIN500,UPFIN999:
                              UPFIN999,UPFIN999,UPFIN500       * Johns Hopkins
            ELSE
              BRANCH    FORM1,UPFIN020,UPFIN500,UPFIN500,UPFIN999:
                              UPFIN999,UPFIN999,UPFIN999,UPFIN500
            ENDIF
          ENDIF
          GOTO      UPFIN999
.
.         Accommodation
.
UPFIN020  COMPARE   ONE,PTINCMIX
          GOTO      UPFIN200 IF NOT EQUAL  * Not C/P
.
          MATCH     "1",PTINCRST
          GOTO      UPFIN100 IF NOT EQUAL  * Not fully creditted
.
          MOVE      PMFCPTLS,FORM82
          ADD       PMFCRBLS,FORM82
.
          COMPARE   ZERO,FORM82
          GOTO      UPFIN100 IF EQUAL      * no lumpsum amount
.
          BRANCH    LUMPFLAG,UPFIN1000     * Lumpsum already been done
          MOVE      ONE,LUMPFLAG
          PACK      FINCODE,ANSH,KEY1,PMFCATYP,SP70
          MOVE      FORM82,FINAMT
.
          MOVE      PINVSYS,FINSYS
          MOVE      PINVSITE,FINSITE
          MOVE      "F",FINTYPE            * for lumpsum accommodation
          CALL      CFIN0000
.
          IF        IBCNUGST=2 & PMFCGSTL<>0
            PACK      FINCODE,ANSQ,SP70
            MOVE      PMFCGSTL,FINAMT
            MOVE      "Y",FINTYPE          * for lumpsum accommodation
            MOVE      PMFCCRDA,FINDATE
            REP       " 0",FINDATE
            CALL      CFIN0000
          ENDIF
.
UPFIN100  PACK      FINCODE,ANSJ,PMFCWARD,SP70  * daily charge
          GOTO      UPFIN400
.
.         Store Fincode with subfix of G non CP, so that it appears before
.         C/P  credit notes
.
UPFIN200  MOVE      "G",ANS                * not C/P casemix credit note
          BRANCH    CACCFEE OF UPFIN210,UPFIN220,UPFIN230,UPFIN240,UPFIN250
.
.         Use Admission type as financial code
.
          PACK      FINCODE WITH ANS,PMFCATYP,SP70
          GOTO      UPFIN400
.
.         Use ward and claim type as financial code
.
UPFIN210  PACK      FINCODE WITH ANS,PMFCWARD,PMFCCTYP,SP70
          GOTO      UPFIN400
.
.         Use ward and admission type
.
UPFIN220  PACK      FINCODE,ANS,PMFCWARD,PMFCATYP,SP70
          GOTO      UPFIN400
.
.         Use ward,claim and admission type
.
UPFIN230  PACK      FINCODE,ANS,PMFCWARD,PMFCCTYP,PMFCATYP,SP70
          GOTO      UPFIN400
.
.         Use ward and claim type as financial code
.
UPFIN240  PACK      FINCODE,ANS,PMFCWARD,PMFCATYP,PMFCCTYP,SP70
          GOTO      UPFIN400
.
.         Use Claim type as financial code
.
UPFIN250  PACK      FINCODE WITH ANS,PMFCCTYP,SP70
          GOTO      UPFIN400
.
.         the amount from the financial summary file
.
UPFIN400  MOVE      PMFCCAMT,FINAMT
          MOVE      PMFCCRDA,FINDATE
          REP       " 0",FINDATE
.
          MOVE      PINVSYS,FINSYS
          MOVE      PINVSITE,FINSITE
          MOVE      "F",FINTYPE              * Credit note
          CALL      CFIN0000
.
          IF        IBCNUGST=2 & PMFCCGST<>0
            PACK      FINCODE,ANSQ,SP70
            MOVE      PMFCCGST,FINAMT
            MOVE      "Y",FINTYPE            * GST Credit note
            MOVE      PMFCCRDA,FINDATE
            REP       " 0",FINDATE
            CALL      CFIN0000
          ENDIF
          GOTO      UPFIN999
.
.        - Miscellaneous or Theatre record
.
UPFIN500  COMPARE   ONE,PTINCMIX
          GOTO      UPFIN520 IF NOT EQUAL
.
          IF        PTCNMFEE=1
            PACK      FINCODE,ANSP,KEY1,PMFCCTYP,PMFCMGRP,SP20
.                             Misc. Charge, Episode, Claim Code, Item Code
          ELSE
            PACK      FINCODE,ANSP,KEY1,PMFCMGRP,PMFCITEM,SP20
.                             Misc. Charge, Episode, Group Code, Item Code
          ENDIF
          GOTO      UPFIN580
.
UPFIN520  IF        PTCNMFEE=1
            PACK      FINCODE,ANSQ,PMFCCTYP,PMFCMGRP,SP20
.                             Misc. Charge, Claim Code, Item Code
          ELSE
            PACK      FINCODE,ANSQ,PMFCMGRP,PMFCITEM,SP20
.                             Misc. Charge, Group Code, Item Code
          ENDIF
.
UPFIN580  MOVE      PMFCCAMT,FINAMT
          MOVE      PINVSYS,FINSYS
          MOVE      PINVSITE,FINSITE
          MOVE      "F",FINTYPE              * Credit note
          MOVE      PMFCCRDA,FINDATE
          REP       " 0",FINDATE
          CALL      CFIN0000
.
          IF        IBCNUGST=2 & PMFCCGST<>0
            PACK      FINCODE,ANSQ,SP70
            MOVE      PMFCCGST,FINAMT
            MOVE      "Y",FINTYPE            * GST Credit note
            MOVE      PMFCCRDA,FINDATE
            REP       " 0",FINDATE
            CALL      CFIN0000
          ENDIF
          GOTO      UPFIN999
.
.         SX Procedure fees
.
UPFIN600  PACK      FINCODE,ANSO,PTINHFND,SP70
          PACK      KEY22,PMFCVISN,PMFCINVN,PMFCTRAN,SP70
          CALL      RDDTRN1
          IF        OVRCD=1
            CALL      RDNZRDT1
            BRANCH    OVRCD,UPFIN700
          ENDIF
.
          MATCH     SP70,PTDTSUNQ
          GOTO      UPFIN700 IF EQUAL       * No proc.uniq no
.
          PACK      KEY11,DTADMNO,PTDTSUNQ  * read procedure details file
          CALL      RDNZSPR1                * to check if it's funder contr.
          BRANCH    OVRCD,UPFIN700
.        
.         Check type of invoice
.
          MATCH     SP70,PTINHFND                               
          GOTO      UPFIN670 IF NOT EQUAL                       
.        
.         Patient Invoice
.
          MATCH     "1",NZSPCONT
          IF        @EQUAL
            PACK      FINCODE,ANSO,NZSPCNTR,NZSPCPRC
          ELSE
            PACK      FINCODE,ANSP,NZSPCNTR,NZSPCPRC
          ENDIF
          GOTO      UPFIN700
.
.         Health Fund Invoice
.
UPFIN670  MATCH     "1",NZSPCONT
          IF        @EQUAL
            PACK      FINCODE,ANSS,NZSPCNTR,NZSPCPRC
          ELSE
            PACK      FINCODE,ANSR,NZSPCNTR,NZSPCPRC
          ENDIF
.
UPFIN700  MOVE      PMFCCAMT,FINAMT
          MOVE      PINVSYS,FINSYS
          MOVE      PINVSITE,FINSITE
          MOVE      "F",FINTYPE              * Credit note
          MOVE      PMFCCRDA,FINDATE
          REP       " 0",FINDATE
          CALL      CFIN0000
.
          IF        IBCNUGST=2 & PMFCCGST<>0
            PACK      FINCODE,ANSQ,SP70
            MOVE      PMFCCGST,FINAMT
            MOVE      "Y",FINTYPE            * GST Credit note
            MOVE      PMFCCRDA,FINDATE
            REP       " 0",FINDATE
            CALL      CFIN0000
          ENDIF
.
          GOTO      UPFIN999
.
UPFIN999  RETURN
+
.*******************************************************************************
.*        NHOM0000 - Adjusting fins file for nursing home                      *
.*******************************************************************************
NHOM0000  PACK      FINCODE,ANSR,TMISGRP,SP20     * Default to account code
.
          MATCH     "Accommodation Charge   ",TDDESC
          IF        @EQUAL
            PACK       FINCODE,ANSA,SP20
          ELSE
            MATCH     "Income Tested fee      ",TDDESC
            IF        @EQUAL
              PACK      FINCODE,ANSI,SP20
            ENDIF
          ENDIF
.         PACK       FINDATE,PTDTEFFD 
          PACK       FINDATE,PINVDTE
          REP        " 0",FINDATE
          CALL       CFIN0000
          BRANCH     NOPERIOD,INVN9000
.
          IF         IBCNUGST=2 & PTDTGSTM<>0
            MOVE       ANSG,FINTYPE
            MOVE       PTDTGSTM,FINAMT
.           PACK       FINDATE,PTDTEFFD 
            PACK       FINDATE,PINVDTE
            REP        " 0",FINDATE
            CALL       CFIN0000
            MOVE       ANSA TO FINTYPE
          ENDIF
NHOM9999  RETURN
+
.*******************************************************************************
.*        PRNT0000 - Print records from tempfile                               *
.*******************************************************************************
PRNT0000  MOVE       "60",CLNO
          MOVE       ZERO,SFINSYS
          MOVE       SP10,SFINSITE
          MOVE       ZERO,SUBTOT
          MOVE       ZERO,GNDTOT
          MOVE       ZERO,RECFLAG
          MOVE       ZERO,DAYFLG
          MOVE       SP20,CASHDESC
          MOVE       SP20,SCASHDES
          MOVE       SP70,SFINCODE
          MOVE       SP1,MSCFLAG   
          MOVE       SP10,SFINTYPE
          MOVE       SP10,SFINHOSP
          PACK       DIM13,SP20
          MOVE       SP20,DIM13A
          MOVE       SP10,DIM10
          MOVE       SP10,SDIM10
          MOVE       SP10,SUBFIN
          MOVE       ZERO,PGSTTOT
          MOVE       SP70,PROCCODE
          MOVE       ZERO,CREDFLAG
          MOVE       ZERO,CRDFLAG
          MOVE       ZERO,SCRDFLAG
          MOVE       SP10,JRNLCOD
          MOVE       SP10,SJRNLCOD
          PACK       KEY52,SP70
          CALL       RDSTEMP
PRNT1000  CALL       RDKTEMP
          BRANCH     OVRCD,PRNT9000
.
.         Fincode with subfix of G for Non CP credit notes
.
          MOVE       ZERO,CRDFLAG
          MATCH      "F",FINTYPE
          GOTO       PRNT1002 IF EQUAL
          MATCH      "Y",FINTYPE
          GOTO       PRNT1004 IF NOT EQUAL
.
PRNT1002  REP        "FAYG",FINTYPE        * Credit notes for Accm and GST
          MOVE       ONE,CRDFLAG
          MOVE       SP10,ANS
          UNPACK     FINCODE,ANS,KEY12
          MATCH      "G",ANS
          IF         @EQUAL
            MOVE       "N",ANS               * non C/P Acc.
            PACK       FINCODE,ANS,KEY12
          ENDIF
.
.         COMPARE    "60",CLNO
.         GOTO       PRNT1500 IF EQUAL
.
.
PRNT1004  COMPARE    ONE,HOSPFLAG
          GOTO       PRNT1010 IF NOT EQUAL
.
          MATCH      SP10,SFINHOSP
          GOTO       PRNT1010 IF EQUAL
          MATCH      FINHOSP,SFINHOSP
          IF         !@EQUAL
            CALL       PSUB0000
            CALL       PTOT0000
            GOTO       PRNT1500
          ENDIF
.
PRNT1010  MATCH      SP70,SFINTYPE
          GOTO       PRNT1015 IF EQUAL
.
          MATCH      FINTYPE,SFINTYPE
          IF         !@EQUAL
            CALL       PSUB0000
            CALL       PTOT0000
            GOTO       PRNT1500
          ENDIF
.
PRNT1015  MATCH      "A",FINTYPE
          GOTO       PRNT1020 IF EQUAL     * Fees
.
          MATCH      "B",FINTYPE
          GOTO       PRNT1300 IF EQUAL     * cash
.
          MATCH      "C",FINTYPE
          GOTO       PRNT1400 IF EQUAL     * Journal
.
          MATCH      "G",FINTYPE
          GOTO       PRNT1420 IF EQUAL     * GST 
.
          MATCH      "H",FINTYPE
          GOTO       PRNT1440 IF EQUAL     * GST Journal
.
          GOTO       PRNT8000
.
PRNT1020  
.         MOVE       ZERO,CRDFLAG
          MOVE       SP10,JRNLCOD
          MOVE       SP10,SJRNLCOD
          CMATCH     "A",FINCODE
          GOTO       PRNT1200 IF EQUAL     * Accommodation
.
.         CMATCH     "C",FINCODE
.         GOTO       PRNT1200 IF EQUAL     * casemix Accommodation
.         CMATCH     "D",FINCODE
.         GOTO       PRNT1200 IF EQUAL     * casemix Accommodation
.
          CMATCH     "H",FINCODE
          GOTO       PRNT1190 IF EQUAL     * Lumpsum credit note accm.
          CMATCH     "J",FINCODE
          GOTO       PRNT1190 IF EQUAL     * Daily charge credit note accm.
          CMATCH     "N",FINCODE
          GOTO       PRNT1190 IF EQUAL     * Credit note Accm.
.
          CMATCH     "B",FINCODE
          GOTO       PRNT1490 IF EQUAL
          CMATCH     "C",FINCODE
          GOTO       PRNT1490 IF EQUAL
          CMATCH     "D",FINCODE
          GOTO       PRNT1490 IF EQUAL
          CMATCH     "F",FINCODE
          GOTO       PRNT1490 IF EQUAL
.
          CMATCH     "P",FINCODE
          GOTO       PRNT1480 IF EQUAL
          CMATCH     "O",FINCODE
          GOTO       PRNT1480 IF EQUAL
          CMATCH     "R",FINCODE
          GOTO       PRNT1480 IF EQUAL
          CMATCH     "S",FINCODE
          GOTO       PRNT1480 IF EQUAL
.
.
.         Item
.
PRNT1100  MOVE       SP1,DIM1
          UNPACK     FINCODE,ANS
.
          MATCH      "P",ANS
          GOTO       PRNT1110 IF EQUAL     * Credit Item C/P
          MATCH      "T",ANS
          GOTO       PRNT1130 IF NOT EQUAL * Not Item C/P
          GOTO       PRNT1120
.
.         Casepayment
.
PRNT1110  
.         MOVE       ONE,CRDFLAG
.
PRNT1120  IF         PTCNMFEE=1
            UNPACK     FINCODE,ANS,DIM1,DIM3,MISGRP  * episode/clm/misc.grp
            PACK       DIM10,DIM1,MISGRP,SP10
          ELSE
            UNPACK     FINCODE,ANS,DIM1,MISGRP,DIM9  * episode/misc.grp/itm
            PACK       DIM10,DIM1,MISGRP,SP10
            MATCH      SP3,MISGRP
            IF         @EQUAL
              PACK       DIM10,DIM1,DIM9,SP10
            ENDIF
          ENDIF
          GOTO      PRNT1150
.
.         Non Casepayment
.
PRNT1130  IF         PTCNMFEE=1
            UNPACK     FINCODE,ANS,DIM3,MISGRP       * claim/misc.group
            PACK       DIM10,DIM1,MISGRP,SP10
          ELSE
            UNPACK     FINCODE,ANS,MISGRP,DIM9       * misc.group/item code
            PACK       DIM10,DIM1,MISGRP,SP10
            MATCH      SP3,MISGRP
            IF         @EQUAL
              PACK       DIM10,DIM1,DIM9,SP10
            ENDIF
          ENDIF
.
PRNT1150  MATCH      DIM10,SDIM10
          GOTO       PRNT1450 IF NOT EQUAL
.
          MATCH      MSCFLAG,ANS
          GOTO       PRNT1500 IF EQUAL
          GOTO       PRNT1450
.
.         Accommodation
.
PRNT1190  
.         MOVE       ONE,CRDFLAG            * flag for credit note
          MATCH      FINCODE,DIM13
          GOTO       PRNT1500 IF EQUAL
.
          CALL       PSUB0000               * print sub total
          COMPARE    CRDFLAG,SCRDFLAG
          GOTO       PRNT1500 IF EQUAL
.
          CALL       PTOT0000
          MOVE       CRDFLAG,SCRDFLAG
          GOTO       PRNT1500
.
.         Accommodation
.
PRNT1200  MATCH      FINCODE,DIM13
          GOTO       PRNT1500 IF EQUAL
          GOTO       PRNT1450
.
.         Cash
.
PRNT1300  MOVE       SP10,JRNLCOD
          MOVE       SP10,SJRNLCOD
          MOVE       ZERO,CRDFLAG
          UNPACK     FINCODE,CASHTYP,CASHDESC
          IF         FINSYS=3
            MATCH      ANSA,CASHTYP
            IF         @EQUAL
              MOVE      "PATIENT     ",CASHDESC
            ELSE
              MATCH     ANSB,CASHTYP
              IF        @EQUAL
                MOVE      "HEALTH FUND ",CASHDESC
              ELSE
                MOVE      "INSURANCE   ",CASHDESC
              ENDIF
            ENDIF
          ELSE
            MOVE      "CASH/NON-INP",CASHDESC
          ENDIF
.
.         MATCH      CASHDESC,SCASHDES
          MATCH      FINCODE,SFINCODE
          GOTO       PRNT1500 IF EQUAL
          GOTO       PRNT1450
.
.         Journals
.
PRNT1400  UNPACK     FINCODE,JRNLCOD
          MATCH      JRNLCOD,SJRNLCOD
          GOTO       PRNT1500 IF EQUAL
          GOTO       PRNT1450
.
.         GST
.
PRNT1420  MOVE       SP10,JRNLCOD
          MOVE       SP10,SJRNLCOD
          MATCH      FINCODE,DIM13A
          GOTO       PRNT1500 IF EQUAL
          CALL       PSUB0000               * print sub total
.
          COMPARE    CRDFLAG,SCRDFLAG
          GOTO       PRNT1500 IF EQUAL
          CALL       PTOT0000
          MOVE       CRDFLAG,SCRDFLAG
          GOTO       PRNT1500
.
.         GST Journal
.
PRNT1440  UNPACK     FINCODE,JRNLCOD
          MATCH      JRNLCOD,SJRNGCOD
          GOTO       PRNT1500 IF EQUAL
          GOTO       PRNT1450
.
PRNT1450  CALL       PSUB0000               * print sub total
          COMPARE    CRDFLAG,SCRDFLAG
          GOTO       PRNT1500 IF EQUAL
.
          CALL       PTOT0000
          MOVE       CRDFLAG,SCRDFLAG
          GOTO       PRNT1500
.
PRNT1480  MATCH      FINCODE,PROCCODE
          GOTO       PRNT1500 IF EQUAL
.
          CALL       PSUB0000               * print sub total
          COMPARE    CRDFLAG,SCRDFLAG
          GOTO       PRNT1500 IF EQUAL
.
          CALL       PTOT0000
          MOVE       ONE,SCRDFLAG
          GOTO       PRNT1500
.
.         Procedures
.
PRNT1490  MATCH      FINCODE,PROCCODE
          GOTO       PRNT1500 IF EQUAL
          GOTO       PRNT1450
.
.         Print the record
.
PRNT1500  COMPARE    ZERO,FINAMT
          GOTO       PRNT8000 IF EQUAL
.
          COMPARE    "59",CLNO
          CALL       HEAD0000 IF NOT LESS
.
          MOVE       ZEROVISN,PINVADM
          PACK       KEY8,PINVNO
          CALL       RDINV1
.
          BRANCH     TOTONLY,PRNT2000
          IF         OVRCD = 1
            PRINT      *1,FINSYS,*10,FINSITE,*20,FINYEAR,*25,FINTYPE:
                       *30,FINCODE,*47,PINVDTE,*62,PINVNO:
                       *82,"********",*95,FINAMT
          ELSE
            PRINT      *1,FINSYS,*10,FINSITE,*20,FINYEAR,*25,FINTYPE:
                       *30,FINCODE,*47,PINVDTE,*62,PINVNO,*82,PINVADM,*95,FINAMT
          ENDIF
          ADD        ONE,CLNO
.
PRNT2000  MOVE       SP1,DIM1
          MOVE       SP1,MSCFLAG
.
          UNPACK     FINCODE,ANS
          MATCH      "A",FINTYPE
          GOTO       PRNT6000 IF NOT EQUAL
.         MOVE       ZERO,CRDFLAG
.
          MOVE       SP70,KEY1
          UNPACK     FINCODE,KEY1
          REP        "BXCXDXFX",KEY1
          REP        "PVOVRVSV",KEY1
.
          CMATCH     "X",KEY1
          GOTO       PRNT2300 IF EQUAL     * SX PRocedure
          CMATCH     "V",KEY1
          GOTO       PRNT2300 IF EQUAL     * SX Proc credit notes
.
          CMATCH     "M",ANS               * Misc.item ?
          GOTO       PRNT2200 IF EQUAL
          CMATCH     "Q",ANS
          GOTO       PRNT3000 IF NOT EQUAL
.
PRNT2200  IF         PTCNMFEE=1
            UNPACK     FINCODE,ANS,DIM3,MISGRP       * claim/misc.group
            MOVE       ANS,SUBFIN
            PACK       SDIM10,SP1,MISGRP,SP10
          ELSE
            UNPACK     FINCODE,ANS,MISGRP,DIM9       * misc.group/item code
            MOVE       ANS,SUBFIN
            PACK       SDIM10,SP1,MISGRP,SP10
            MATCH      SP3,MISGRP
            IF         @EQUAL
              PACK       SDIM10,SP1,DIM9,SP10
            ENDIF
          ENDIF
          MOVE       ONE,RECFLAG
          MOVE       ANS,MSCFLAG
          GOTO       PRNT8000
.
PRNT2300  UNPACK     FINCODE,ANS,KEY6,DIM9
          MOVE       ANS,SUBFIN
          MOVE       SEVEN,RECFLAG            * for procedure
          GOTO       PRNT8000
.
PRNT3000  CMATCH     "T",ANS
          GOTO       PRNT3200 IF EQUAL               * C/P Credit note item
          CMATCH     "P",ANS
          GOTO       PRNT4000 IF NOT EQUAL
.
PRNT3200  IF         PTCNMFEE=1
            UNPACK     FINCODE,ANS,DIM1,DIM3,MISGRP  * epsde/clm/misc.grp
            MOVE       ANS,SUBFIN
            PACK       SDIM10,DIM1,MISGRP,SP10
          ELSE
            UNPACK     FINCODE,ANS,DIM1,MISGRP,DIM9  * epsde/misc.grp/itm
            MOVE       ANS,SUBFIN
            PACK       SDIM10,DIM1,MISGRP,SP10
            MATCH      SP3,MISGRP
            IF         @EQUAL
              PACK       SDIM10,DIM1,DIM9,SP10
            ENDIF
          ENDIF
          MOVE      ONE,RECFLAG       * flag for mis.item
          MOVE      ANS,MSCFLAG       * "T" for casemix & "M" for normal
          GOTO      PRNT8000
.
PRNT4000  
          CMATCH    "N",ANS
          IF        @EQUAL
            MOVE      ONE,SCRDFLAG
          ENDIF
.
          MOVE      FINCODE,DIM13
          UNPACK    FINCODE,SUBFIN
          MOVE      TWO,RECFLAG       * accommodations
          GOTO      PRNT8000
.
PRNT6000  
.         MOVE      ZERO,CRDFLAG
          MATCH     "B",FINTYPE
          IF        @EQUAL
            IF        FINSYS = 3
              MOVE      ZERO,CRDFLAG
              UNPACK    FINCODE,CASHTYP,CASHDESC
              MATCH     ANSA,CASHTYP
              IF        @EQUAL
                MOVE      "PATIENT     ",CASHDESC
              ELSE
                MATCH     ANSB,CASHTYP
                IF        @EQUAL
                  MOVE      "HEALTH FUND ",CASHDESC
                ELSE
                  MOVE      "INSURANCE   ",CASHDESC
                ENDIF
              ENDIF
            ELSE
              MOVE      "CASH/NON-INP",CASHDESC
            ENDIF
.
            MOVE      CASHTYP,SCASHTYP
            MOVE      CASHDESC,SCASHDES
            MOVE      FINCODE,SFINCODE
            MOVE      THREE,RECFLAG          * cash
          ENDIF
.
          MATCH     "C",FINTYPE
          IF        @EQUAL
            UNPACK    FINCODE,JRNLCOD
            MOVE      JRNLCOD,SJRNLCOD
            MOVE      FOUR,RECFLAG           * journals
          ENDIF
.
          MATCH      "G",FINTYPE
          IF         @EQUAL
            MOVE       FIVE,RECFLAG          * GST
            MOVE       FINCODE,DIM13A
            MOVE       FINCODE,SUBFIN
          ENDIF
.
          MATCH      "H",FINTYPE
          IF         @EQUAL
            MOVE       SIX,RECFLAG          * GST Journal
            UNPACK     FINCODE,JRNLCOD
            MOVE       JRNLCOD,SJRNGCOD
          ENDIF
.
PRNT8000  MOVE       FINSYS,SFINSYS
          MOVE       FINSITE,SFINSITE
          MOVE       FINTYPE,SFINTYPE
          MOVE       FINHOSP,SFINHOSP
          ADD        FINAMT,SUBTOT
          ADD        FINAMT,GNDTOT
          MOVE       CRDFLAG,SCRDFLAG
          MOVE       FINCODE,PROCCODE
          GOTO       PRNT1000
.
PRNT9000  MOVE       SP20,FINCODE
          IF         CLNO=60
            CALL       HEAD0000
          ENDIF
          CALL       PSUB0000
          CALL       PTOT0000
          PRINT      *1,"** End of Report **"
.
PRNT9999  RETURN
+
.*******************************************************************************
.*        PSUB0000 - Print sub-totals                                          *
.*******************************************************************************
PSUB0000  COMPARE    ZERO,SUBTOT
          GOTO       PSUB9999 IF EQUAL
.
          MOVE       SP10,DIM3
          LOAD       DIM3,SFINSYS,AAE,OUT,INP
.
          IF         SFINSYS=2
            MOVE       SFINSITE,DIM3
          ENDIF
.
          BRANCH     TOTONLY,PSUB0200
          CALL       UND132
PSUB0200  BRANCH     RECFLAG,PSUB1000,PSUB2000,PSUB3000,PSUB4000,PSUB5000:
                             PSUB6000,PSUB7000
.
PSUB1000  MATCH      "Q",SUBFIN
          GOTO       PSUB1200 IF EQUAL
          MATCH      "P",SUBFIN
          GOTO       PSUB1400 IF NOT EQUAL
.
.         Credit notes
.
PSUB1200  IF         HOSPFLAG=1
            PRINT      *1,SFINHOSP:
                       *5,DIM3," SUB-TOTAL CRD NOTES(MISC.ITEM GROUP): ":
                       SDIM10;
          ELSE
            PRINT      *1,DIM3," SUB-TOTAL CRD NOTES(MISC.ITEM GROUP): ",SDIM10;
          ENDIF
          MATCH      "P",SUBFIN
          GOTO       PSUB1800 IF NOT EQUAL     * Not C/P
          GOTO       PSUB1600
.
PSUB1400  IF         HOSPFLAG=1
            PRINT      *1,SFINHOSP:
                       *5,DIM3," SUB-TOTAL (MISC.ITEM GROUP): ",SDIM10;
          ELSE
            PRINT      *1,DIM3," SUB-TOTAL (MISC.ITEM GROUP): ",SDIM10;
          ENDIF
          MATCH      "T",SUBFIN
          GOTO       PSUB1800 IF NOT EQUAL     * Not C/P
.
PSUB1600  UNPACK     SDIM10,KEY1
          MATCH      "1",KEY1
          IF         @EQUAL
            PRINT      " - SAMEDAY",*93,SUBTOT
          ELSE
            PRINT      " - MSO",*93,SUBTOT
          ENDIF
          GOTO       PSUB8000
.
PSUB1800  PRINT      *93,SUBTOT
          GOTO       PSUB8000
.
PSUB2000  MATCH      "H",SUBFIN
          GOTO       PSUB2300 IF EQUAL
          MATCH      "J",SUBFIN
          GOTO       PSUB2200 IF EQUAL
          MATCH      "N",SUBFIN
          GOTO       PSUB2400 IF NOT EQUAL
.
          IF         HOSPFLAG=1
            PRINT      *1,SFINHOSP,*5,DIM3," SUB-TOTAL (CRD NOTES ACCOM): ":
                       DIM13,*93,SUBTOT
          ELSE
            PRINT      *1,DIM3," SUB-TOTAL (CRD NOTES ACCOM): ":
                       DIM13,*93,SUBTOT
          ENDIF
          GOTO       PSUB8000
.
PSUB2200  IF         HOSPFLAG=1
            PRINT      *1,SFINHOSP:
                       *5,DIM3," SUB-TOTAL (CRD NOTES DAILY CHARGE ACCOM): ":
                       DIM13,*93,SUBTOT
          ELSE
            PRINT      *1,DIM3," SUB-TOTAL (CRD NOTES DAILY CHARGE ACCOM): ":
                       DIM13,*93,SUBTOT
          ENDIF
          GOTO       PSUB8000
.
PSUB2300  IF         HOSPFLAG=1
            PRINT      *1,SFINHOSP,*5,DIM3," SUB-TOTAL (CRD NOTES LUMPSUM): ":
                       DIM13,*93,SUBTOT
          ELSE
            PRINT      *1,DIM3," SUB-TOTAL (CRD NOTES LUMPSUM): ":
                       DIM13,*93,SUBTOT
          ENDIF
          GOTO       PSUB8000
.
PSUB2400  MATCH      "C",SUBFIN
          GOTO       PSUB2420 IF EQUAL
          MATCH      "D",SUBFIN
          GOTO       PSUB2430 IF EQUAL
.
          IF         HOSPFLAG=1
            PRINT      *1,SFINHOSP:
                       *5,DIM3," SUB-TOTAL (ACCOMMODATION): ",DIM13,*93,SUBTOT
          ELSE
            PRINT      *1,DIM3," SUB-TOTAL (ACCOMMODATION): ",DIM13,*93,SUBTOT
          ENDIF
          GOTO       PSUB8000
.
PSUB2420  IF         HOSPFLAG=1
            PRINT      *1,SFINHOSP:
                       *5,DIM3," SUB-TOTAL (LUMPSUM ACCOMMODATION): ":
                       DIM13,*93,SUBTOT
          ELSE
            PRINT      *1,DIM3," SUB-TOTAL (LUMPSUM ACCOMMODATION): ":
                       DIM13,*93,SUBTOT
          ENDIF
          GOTO       PSUB8000
.
PSUB2430  IF         HOSPFLAG=1
            PRINT      *1,SFINHOSP:
                       *5,DIM3," SUB-TOTAL (DAILY CHARGE ACCOMMODATION): ":
                       DIM13,*93,SUBTOT
          ELSE
            PRINT      *1,DIM3," SUB-TOTAL (DAILY CHARGE ACCOMMODATION): ":
                       DIM13,*93,SUBTOT
          ENDIF
          GOTO       PSUB8000
.
PSUB3000  IF         HOSPFLAG=1
            PRINT      *1,SFINHOSP:
.                      *5,DIM3," SUB-TOTAL (CASH): ",SCASHTYP," - ",SCASHDES:
                       *5,DIM3," SUB-TOTAL (CASH): ",SCASHTYP," - ",SFINCODE:
                       *93,SUBTOT
          ELSE
.           PRINT      *1,DIM3," SUB-TOTAL (CASH): ",SCASHTYP," - ",SCASHDES:
            PRINT      *1,DIM3," SUB-TOTAL (CASH): ",SCASHTYP," - ",SFINCODE:
                       *93,SUBTOT
          ENDIF
          GOTO       PSUB8000
.
PSUB4000  IF         HOSPFLAG=1
            PRINT      *1,SFINHOSP:
                       *5,"SUB-TOTAL (JOURNAL): ",SJRNLCOD,*93,SUBTOT
          ELSE
            PRINT      *1,"SUB-TOTAL (JOURNAL): ",SJRNLCOD,*93,SUBTOT
          ENDIF
          GOTO       PSUB8000
.
PSUB5000  MATCH      "H",SUBFIN
          GOTO       PSUB5200 IF EQUAL
          MATCH      "J",SUBFIN
          GOTO       PSUB5300 IF EQUAL
          MATCH      "N",SUBFIN
          GOTO       PSUB5400 IF EQUAL
.
          MATCH      "Q",SUBFIN
          GOTO       PSUB5500 IF EQUAL
          MATCH      "P",SUBFIN
          GOTO       PSUB5600 IF EQUAL
.
          MATCH      "P",SUBFIN
          GOTO       PSUB5100 IF EQUAL   * SX Procedure
          MATCH      "O",SUBFIN
          GOTO       PSUB5100 IF EQUAL   * SX Procedure
          MATCH      "R",SUBFIN
          GOTO       PSUB5100 IF EQUAL   * SX Procedure
          MATCH      "S",SUBFIN
          GOTO       PSUB5100 IF EQUAL   * SX Procedure
.
          IF         HOSPFLAG=1
            PRINT      *1,SFINHOSP,*5,"SUB-TOTAL (GST) : ",DIM13A,*93,SUBTOT
          ELSE
            PRINT      *1,"SUB-TOTAL (GST) : ",DIM13A,*93,SUBTOT
          ENDIF
          GOTO       PSUB8000
.
PSUB5100  PRINT      *1,SFINHOSP:
                       *5,"SUB-TOTAL (CRD NOTES GST PROCEDURE) : ":
                       DIM13A,*93,SUBTOT
          GOTO       PSUB8000
.
PSUB5200  IF         HOSPFLAG=1
            PRINT      *1,SFINHOSP:
                       *5,"SUB-TOTAL (CRD NOTES GST LUMPSUM) : ":
                       DIM13A,*93,SUBTOT
          ELSE
            PRINT      *1,"SUB-TOTAL (CRD NOTES GST LUMPSUM) : ":
                       DIM13A,*93,SUBTOT
          ENDIF
          GOTO       PSUB8000
.
PSUB5300  IF         HOSPFLAG=1
            PRINT      *1,SFINHOSP:
                       *5,"SUB-TOTAL (CRD NOTES GST DAILY CHARGE) : ":
                       DIM13A,*93,SUBTOT
          ELSE
            PRINT      *1,"SUB-TOTAL (CRD NOTES GST DAILY CHARGE) : ":
                       DIM13A,*93,SUBTOT
          ENDIF
          GOTO       PSUB8000
.
PSUB5400  IF         HOSPFLAG=1
            PRINT      *1,SFINHOSP:
                       *5,"SUB-TOTAL (CRD NOTES GST ACCOMMODATION) : ":
                       DIM13A,*93,SUBTOT
          ELSE
            PRINT      *1,"SUB-TOTAL (CRD NOTES GST ACCOMMODATION) : ":
                       DIM13A,*93,SUBTOT
          ENDIF
          GOTO       PSUB8000
.
PSUB5500  IF         HOSPFLAG=1
            PRINT      *1,SFINHOSP:
                       *5,"SUB-TOTAL (CRD NOTES GST MISC.ITEM) : ":
                       DIM13A,*93,SUBTOT
          ELSE
            PRINT      *1,"SUB-TOTAL (CRD NOTES GST MISC.ITEM) : ":
                       DIM13A,*93,SUBTOT
          ENDIF
          GOTO       PSUB8000
.
PSUB5600  IF         HOSPFLAG=1
            PRINT      *1,SFINHOSP:
                       *5,"SUB-TOTAL (CRD NOTES GST C/P MISC.ITEM) : ":
                       DIM13A,*93,SUBTOT
          ELSE
            PRINT      *1,"SUB-TOTAL (CRD NOTES GST C/P MISC.ITEM) : ":
                       DIM13A,*93,SUBTOT
          ENDIF
          GOTO       PSUB8000
.
PSUB6000  IF         HOSPFLAG=1
            PRINT      *1,SFINHOSP:
                       *5,"SUB-TOTAL (GST JOURNAL) : ",SJRNGCOD,*93,SUBTOT
          ELSE
            PRINT      *1,"SUB-TOTAL (GST JOURNAL) : ",SJRNGCOD,*93,SUBTOT
          ENDIF
          GOTO       PSUB8000
.
.         SX procedures
.
PSUB7000  MATCH      "P",SUBFIN
          GOTO       PSUB7020 IF EQUAL
          MATCH      "O",SUBFIN
          GOTO       PSUB7020 IF EQUAL
          MATCH      "R",SUBFIN
          GOTO       PSUB7020 IF EQUAL
          MATCH      "S",SUBFIN
          GOTO       PSUB7020 IF EQUAL
.
          PRINT      *1,SFINHOSP:
                     *5,DIM3," SUB-TOTAL (PROCEDURES): ":
                     PROCCODE;
          GOTO       PSUB1800
.
.         SX Credit notes procedures
.
PSUB7020  PRINT      *1,SFINHOSP:
                     *5,DIM3," SUB-TOTAL CRD NOTES(PROCEDURES): ":
                     PROCCODE;
          GOTO       PSUB1800
.
PSUB8000  CALL       UND132
          MOVE       ZERO,SUBTOT
PSUB9999  RETURN
+
.*******************************************************************************
.*        PTOT0000 - Print totals                                              *
.*******************************************************************************
PTOT0000  COMPARE    ZERO,GNDTOT
          GOTO       PTOT9999 IF EQUAL
          PRINT      *1,"TOTAL : ",*93,GNDTOT
          PRINT      "*=======================================":
                     "========================================":
                     "========================================":
                     "===========*"
          ADD        ONE,CLNO
          MOVE       ZERO,GNDTOT
PTOT9999  RETURN
+
.*******************************************************************************
.*        HEAD0000 - Heading                                                   *
.*******************************************************************************
HEAD0000  CALL       HEAD132
.
          MOVE       SP20,DIM10
          LOAD       DIM10,OPTION,INVOICE,CASH,JOURNAL,ALL
.
          MOVE       SP20,DIM10A
          LOAD       DIM10A,SUBOPT,INPAT,AAEPAT,OUTPAT,ALLPAT
.
          PRINT      *40,"System : ",DIM10A,"   ",DIM10
          IF        IBCNMHOS=1
            PRINT      *40,"Hospital ID : ",HOSPID," - ",HOSPNAME
          ENDIF
.
          UNPACK     FROMDATE,CCENT,CYEAR,CMON,CDAY
          CALL       PACDATE
          PRINT      *40,"Fromdate : ",CPCDATE;
          UNPACK     TODATE,CCENT,CYEAR,CMON,CDAY
          CALL       PACDATE
          PRINT      *65,"To : ",CPCDATE
.
          CALL       UND132
          PRINT      *1,"System",*10,"Site",*20,"Year",*25,"Type":
                     *30,"Financial Code",*47,"Invoice date",*62,"Invoice No":
                     *79,"Admission No.",*95,"    Amount"
          CALL       UND132
          ADD        THREE,CLNO
HEAD9999  RETURN
+
.*******************************************************************************
.         Subroutine to get the misc group of a given item
.*******************************************************************************
GETM0000   MOVE       SP3,MMSCGRP
.
.          Check if this charge is numeric (ie. a MBS Item number)
.
           TYPE      TITEMNO
           GOTO      GETM1000 IF NOT EQUAL
.
.          Code is numeric - check that it is a valid MBS item number
.
           PACK      KEY9 WITH TITEMNO,SP5
           PACK      KEY17,KEY9,CURRDATE,SP70
           CALL      PATITMRD
           BRANCH    OVRCD OF GETM1000        * It might be a valid misc charge
.
           MOVE      IMISGRP,MMSCGRP
           GOTO      GETM9999
.
GETM1000   MOVE      "0  ",MCLMCOD
           PACK      KEY29,MCLMCOD,SP6,SP3,TITEMNO,CURRDATE
           CALL      PATMCHRD                * read Misc.Charges file 
           COMPARE   ZERO,EXIT
           GOTO      GETM9999 IF EQUAL
.
GETM9000   MOVE       SP3,MMSCGRP
.
GETM9999   RETURN
+
.
.*******************************************************************************
.       Creating an indexed file based on port
.*******************************************************************************
INDZ0000  CALL          TFILENAM                 * Generate temporary file name
          MOVE          TFILNAME,FNAMEI
.
.       CHECK IF PREVIOUSLY EXISTS..IF SO DELETED IT 
.
          CALL          KILL0000
          DISPLAY       *P1:12,*EF
          CLEAR         CMDLINE
          APPEND        "isbuild ",CMDLINE
          APPEND        FNAMEI,CMDLINE
        APPEND   " 63 u1-3,4-4,5-10,11-14,15-15,16-28,29-36,37-44+45-52",CMDLINE
          RESET         CMDLINE
.
          EXECUTE      CMDLINE,TASKID
          MOVE         ZERO,OVRCD
          TRAP         OVERCOND IF IO
          OPEN         PATTEMP,FNAMEI
          TRAPCLR      IO
.
          MOVE         OVRCD,OPCND
          BRANCH       OPCND,INDZ8000
          GOTO         INDZ9999
.
INDZ8000  DISPLAY      *P20:24,*B,*V2LON:
                       "** The Index File Not Created **",*W2;
INDZ9999  RETURN
+
.*******************************************************************************
.       Deleting an indexed file based on port
.*******************************************************************************
KILL0000  CLOSE         PATTEMP
	  MOVE          ZERO,STATUS
.
          CLEAR         CMDLINE
          APPEND        "iserase ",CMDLINE
          APPEND        FNAMEI,CMDLINE
          RESET         CMDLINE
          EXECUTE       CMDLINE,TASKID
          DISPLAY       *P1:12,*EF
          RETURN
+
.*******************************************************************************
.         CFIN0000 - Find out which financial year and period this is in
.*******************************************************************************
CFIN0000  MOVE      ZERO,NOPERIOD
          UNPACK    FINDATE,DIM8
          REP       " 0",DIM8
          MATCH     "00000000",DIM8
          IF        @EQUAL
            MOVE      PINVDTE,DIM8
            REP       " 0",DIM8
            MOVE      DIM8,FINDATE
          ENDIF
.
          MATCH     FROMDATE,DIM8
          GOTO      CFIN9999 IF LESS
          MATCH     DIM8,TODATE
          GOTO      CFIN9999 IF LESS
.
          IF        CREDFLAG<>1
            ADD       FINAMT,TOTINVC
          ELSE
            ADD       FINAMT,TOTCRNT
            ADD       FINAMT,FORM10P2
            PRINT     *92,"...CRD NOTE:",FINAMT,"=",FORM10P2
            MOVE      ONE,NOCREDNT
          ENDIF
.
          OPEN      PATDRGA2,"patdrgaf" * Open the date range file
          PACK      KEY14,FINDATE,SP6   * Position to this date
          CALL      RDSDRGA2            * Go to this date in the file
          CALL      RDKDRGA2            * Get the first record on or after date
          BRANCH    OVRCD,CFIN6000      * No records in file.
.
          MATCH     DRGFDTE,FINDATE     * Is the date before the from date ?
          GOTO      CFIN6000 IF LESS    * Yes. Date not found in file.
.
.         Period record found
.
          MOVE      DRGYR,FINYEAR       * Save financial year
          MOVE      DRGNUM,FORM2
          MOVE      FORM2,FOFFSET      * Save period offset
.
.         Check if this is in a prior financial year
.
.          PACK      FINDATE,CCC,CYY,CMM,CDD
.          REP       " 0",FINDATE
.
.CFIN1000  PACK      KEY14,FINDATE,SP6   * Position to this date
.          CALL      RDSDRGA2            * Go to this date in the file
.          CALL      RDKDRGA2            * Get the first record on or after date
.          BRANCH    OVRCD,CFIN7000      * No records in file.
.
.          MATCH     DRGFDTE,FINDATE     * Is the date before the from date ?
.          GOTO      CFIN7000 IF LESS    * Yes. Date not found in file.
.
.          MATCH     DRGYR,FINYEAR       * Is this the same as the request date?
.          GOTO      CFIN5000 IF EQUAL   * Yes. Continue Processing
.
..         If this the first day of the financial year, then let all invoiced
..         data go through as normal
.
.          MATCH     "01",DRGNUM         * Is this the first period of year ?
.          GOTO      CFIN3000 IF NOT EQUAL
.
.          MATCH     FINDATE,DRGFDTE     * Is this date the start of period ?
.          GOTO      CFIN3000 IF NOT EQUAL
.
.         Only allow invoices to go into the actual year.
.
.          CMATCH    ANSA,FINTYPE
.          GOTO      CFIN3000 IF NOT EQUAL
.
.         Invoice on the first day of the financial year. Allow to go into
.         previous year.
.
.          GOTO      CFIN5000
.
.         Put the figures into the prior year column of report
.
.CFIN3000  MOVE      TEN4,FOFFSET        * Put into the Prior year variable
.          MOVE      DRGYR,FINYEAR       * Put into the current financial year
.
.         Post the data to the tempfile
.
CFIN5000  ADD       ONE,UNIQKEY
          MOVE      FOFFSET,FPERIOD
          MOVE      UNIQKEY,DUNIQKEY
          MOVE      PMVXMHOS,FINHOSP
          IF        HOSPFLAG=1
            PACK      KEY52,FINHOSP,FINSYS,FINSITE,FINYEAR,FINTYPE,FINCODE:
                            PINVDTE,PINVNO,DUNIQKEY,SP20
          ELSE
            MOVE      SP10,FINHOSP
            PACK      KEY52,FINHOSP,FINSYS,FINSITE,FINYEAR,FINTYPE,FINCODE:
                            PINVDTE,PINVNO,DUNIQKEY,SP20
          ENDIF
.
          CALL      WRTEMP 
          CALL      SAVPRD00
          GOTO      CFIN9000            * Finished
.
.         The requested date does not exist in the date range file.
.
CFIN6000  UNPACK    FINDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          KEYIN     *P1:24,*EL,*V2LON,*B:
                    *DV,PINVADM," ** Date ",*DV,CPCDATE," not in Period Range":
                    " file. Hit <ENTER> to retry ",*EOFF,ANS;
.
.         CMATCH    EXITCHAR,ANS
          CMATCH    "E",ANS
          GOTO      CFIN8000 IF EQUAL
          GOTO      CFIN0000
.
.         The current date does not exist in the date range file.
.
CFIN7000  UNPACK    FINDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          KEYIN     *P1:24,*EL,*V2LON,*B:
                    *DV,PINVADM," ** Date ",*DV,CPCDATE," not in Period Range":
                    " file. Hit <ENTER> to retry ",*EOFF,ANS;
.
.         CMATCH    EXITCHAR,ANS
          CMATCH    "E",ANS
          GOTO      CFIN8000 IF EQUAL
          GOTO      CFIN1000
.
.         Finished update of fees invoice file
.
CFIN8000  MOVE      ONE,NOPERIOD
CFIN9000  CLOSE     PATDRGA2
CFIN9999  RETURN
+
.*******************************************************************************
.         SAVPRD00 - Save period range
.*******************************************************************************
SAVPRD00  COMPARE   ZERO,FROMPERD
          GOTO      SAVPRD80 IF EQUAL     * must be first record
. 
          IF        FOFFSET<FROMPERD
            MOVE      FOFFSET,FROMPERD
          ENDIF
          IF        FOFFSET>TOPERD
            MOVE      FOFFSET,TOPERD
          ENDIF
          GOTO      SAVPRD99
.
SAVPRD80  MOVE      FOFFSET,FROMPERD
          MOVE      FOFFSET,TOPERD
SAVPRD99  RETURN
+
.*******************************************************************************
.         WFIN0000 - Write new figures to Fees Invoiced files
.*******************************************************************************
WFIN0000  COMPARE   ONE,HOSPFLAG
          GOTO      WFIN9999 IF NOT EQUAL   * Not available for all hospitals
.
          COMPARE   ONE,UPDFLAG
          GOTO      WFIN9999 IF NOT EQUAL
          COMPARE   ONE,SUBOPT
          GOTO      WFIN9999 IF NOT EQUAL
          COMPARE   ONE,TOTONLY
          GOTO      WFIN9999 IF NOT EQUAL
          MATCH     SP70,HOSPID
          GOTO      WFIN9999 IF EQUAL
.
.         IF        (OPTION<>1) | (SUBOPT<>1) | (TOTONLY=1)
.           GOTO      WFIN9999              * only for inp. invoices (detailed)
.         ENDIF
.
WFIN1000  DISPLAY   *P1:24,*EL,"Do you want new figures ":
                    "written to the Fees Invoiced files? ":
                    "(",*V2LON,ANSY,*HOFF,"/",*V2LON,ANSN,*HOFF,") ";
          KEYIN     ANS;
          REP       "yYnN",ANS
          CMATCH    ANSN,ANS
          GOTO      WFIN9999 IF EQUAL
          CMATCH    ANSY,ANS
          GOTO      WFIN2000 IF EQUAL
          BEEP
          GOTO      WFIN1000
.
WFIN2000  
.         CALL      COPY0000                * backup files
          CALL      CLFN0000                * clear nzpfinaf for this period
          CALL      CLFG0000                * clear nzpfigaf for this period
          CALL      WFEE0000
          CALL      WGST0000
WFIN9999  RETURN
+
.*******************************************************************************
.         Write Fees
.*******************************************************************************
WFEE0000  MOVE      ZERO,TOTAMT
          MOVE      ZERO,F1
          PACK      SAVKEY31,SP70
          PACK      KEY52,SP70
          CALL      RDSTEMP
WFEE2200  CALL      RDKTEMP                 * get first record
          BRANCH    OVRCD,WFEE8000
.
          PACK      KEY31,FINHOSP,FINSYS,FINSITE,FINYEAR,FINTYPE,FINCODE,FPERIOD
          MATCH     ANSY,FINTYPE
          GOTO      WFEE2200 IF EQUAL       * Skip Credit note GST
          MATCH     ANSG,FINTYPE
          GOTO      WFEE2200 IF EQUAL       * Skip GST
.
          UNPACK    SP70,KEY1,KEY12
          UNPACK    FINCODE,KEY1,KEY12
          CMATCH    "G",KEY1
          IF        @EQUAL
            PACK      FINCODE,ANSN,KEY12
          ENDIF
.
          MOVE      ANSA,FINTYPE
          PACK      KEY31,FINHOSP,FINSYS,FINSITE,FINYEAR,FINTYPE,FINCODE,FPERIOD
          COMPARE   ZERO,F1
          GOTO      WFEE7000 IF EQUAL
.
          MATCH     KEY31,SAVKEY31          * check if key has changed
          GOTO      WFEE7000 IF EQUAL
.
          MOVE      TOTAMT,SAVAMT         * total to be saved to FI file
          CALL      UPFN0000              * update nzpfinaf
          MOVE      KEY31,SAVKEY31        * save new key
          MOVE      FINAMT,TOTAMT         * start new FI total
          GOTO      WFEE2200
.
WFEE7000  ADD       FINAMT,TOTAMT         * accumulate FI amount
          MATCH     SP70,SAVKEY31
          IF        @EQUAL
            MOVE      KEY31,SAVKEY31        * save new key
          ENDIF
          MOVE      ONE,F1
          GOTO      WFEE2200
.
WFEE8000  COMPARE   ZERO,TOTAMT
          GOTO      WFEE9999 IF EQUAL
          MOVE      TOTAMT,SAVAMT         * total to be saved to FI file
          CALL      UPFN0000              * update nzpfinaf
WFEE9999  RETURN
+
.*******************************************************************************
.         CLFN0000 - Clear the fees invoiced for the selected period
.*******************************************************************************
CLFN0000  MOVE      SP70,KEY28
          MOVE      HOSPID,NZFIHOSP
          MOVE      "3",NZFISYST
          MOVE      SP70,NZFISITE
          MOVE      CURFYEAR,NZFIYEAR
          MOVE      ANSA,NZFITYPE
.
          PACK      KEY28,NZFIHOSP,NZFISYST,NZFISITE,NZFIYEAR:
                          NZFITYPE,SP70
          CALL      RSNZFIN2
CLFN1000  CALL      RKNZFIN2
          BRANCH    OVRCD,CLFN9999
.
          PACK      SKEY28,NZFIHOSP,NZFISYST,NZFISITE,NZFIYEAR,NZFITYPE,SP70
          MATCH     SKEY28,KEY28
          GOTO      CLFN9999 IF NOT EQUAL
          MOVE      FROMPERD,F2
.
CLFN2000  COMPARE   F2,TOPERD
          GOTO      CLFN8000 IF LESS
.
          STORE     ZERO,F2,NZFIMTH1,NZFIMTH2,NZFIMTH3:
                               NZFIMTH4,NZFIMTH5,NZFIMTH6:
                               NZFIMTH7,NZFIMTH8,NZFIMTH9:
                               NZFIMT10,NZFIMT11,NZFIMT12:
                               NZFIMT13
          ADD       ONE,F2
          GOTO      CLFN2000
.
CLFN8000  CALL      UPNZFIN2                * update record
          GOTO      CLFN1000
.
CLFN9999  RETURN
+
.*******************************************************************************
.         UPFN0000 - Update nzpfinaf
.*******************************************************************************
UPFN0000  COMPARE   ZERO,SAVAMT             * don't update with zero amounts
          GOTO      UPFN9999 IF EQUAL
.
UPFN1000  MOVE      SP70,KEY3
          UNPACK    SAVKEY31,KEY28,KEY3     * get key to nzpfinaf & period
          CALL      RDNZFIN2                * read nzpfinaf
          IF        OVRCD<>1
            GOTO      UPFN2000              * update existing record
          ENDIF
.
          CALL      CLFIN000
          UNPACK    KEY28,NZFIHOSP,NZFISYST,NZFISITE,NZFIYEAR,NZFITYPE,NZFICODE
          CALL      WRNZFIN2                * write new record with zeros
          GOTO      UPFN1000                * then update with amount
.
UPFN2000  MOVE      ZERO,FORM3
          MOVE      KEY3,FORM3
          MOVE      ZERO,FORM12P2
          LOAD      FORM12P2,FORM3,NZFIMTH1,NZFIMTH2,NZFIMTH3:
                                 NZFIMTH4,NZFIMTH5,NZFIMTH6:
                                 NZFIMTH7,NZFIMTH8,NZFIMTH9:
                                 NZFIMT10,NZFIMT11,NZFIMT12:
                                 NZFIMT13
          ADD       SAVAMT,FORM12P2
          STORE     FORM12P2,FORM3,NZFIMTH1,NZFIMTH2,NZFIMTH3:
                                 NZFIMTH4,NZFIMTH5,NZFIMTH6:
                                 NZFIMTH7,NZFIMTH8,NZFIMTH9:
                                 NZFIMT10,NZFIMT11,NZFIMT12:
                                 NZFIMT13
          CALL      UPNZFIN2                * update with new amount
UPFN9999  RETURN
+
.*******************************************************************************
.         Write GST
.*******************************************************************************
WGST0000  MOVE      ZERO,TOTAMT
          MOVE      ZERO,F1
          PACK      SAVKEY31,SP70
          PACK      KEY52,SP70
          CALL      RDSTEMP
WGST2200  CALL      RDKTEMP                 * get first record
          BRANCH    OVRCD,WGST8000
.
          MATCH     ANSY,FINTYPE
          GOTO      WGST5000 IF EQUAL
          MATCH     ANSG,FINTYPE
          GOTO      WGST2200 IF NOT EQUAL   * Not GST
.
          MOVE      ANSA,FINTYPE
          PACK      FINCODE,ANSA,SP70
          GOTO      WGST6000
.
WGST5000  MOVE      ANSA,FINTYPE
          PACK      FINCODE,ANSQ,SP70
WGST6000  PACK      KEY31,FINHOSP,FINSYS,FINSITE,FINYEAR,FINTYPE,FINCODE,FPERIOD
          COMPARE   ZERO,F1
          GOTO      WGST7000 IF EQUAL
.
          MATCH     KEY31,SAVKEY31          * check if key has changed
          GOTO      WGST7000 IF EQUAL
.
          MOVE      TOTAMT,SAVAMT         * total to be saved to FI file
          CALL      UPFG0000              * update nzpfigaf
          MOVE      KEY31,SAVKEY31        * save new key
          MOVE      FINAMT,TOTAMT         * start new FI total
          GOTO      WGST2200
.
WGST7000  ADD       FINAMT,TOTAMT           * accumulate FI amount
          MATCH     SP70,SAVKEY31
          IF        @EQUAL
            MOVE      KEY31,SAVKEY31        * save new key
          ENDIF
          MOVE      ONE,F1
          GOTO      WGST2200                * get next tempfile record
.
WGST8000  COMPARE   ZERO,TOTAMT
          GOTO      WGST9999 IF EQUAL
          MOVE      TOTAMT,SAVAMT         * total to be saved to FI file
          CALL      UPFG0000              * update nzpfigaf
WGST9999  RETURN
+
.*******************************************************************************
.         CLFG0000 - Clear the fees invoiced for the selected period
.*******************************************************************************
CLFG0000  MOVE      ZERO,F1
          MOVE      FROMPERD,F2           * set the from period
          MOVE      HOSPID,NZFIHOSP
          MOVE      "3",NZFISYST
          MOVE      SP70,NZFISITE
          MOVE      CURFYEAR,NZFIYEAR
          MOVE      ANSA,NZFITYPE
.
CLFG1000  COMPARE   TWO,F1
          GOTO      CLFG9000 IF EQUAL
.
          PACK      NZFICODE,ANSA,SP70    * GST 
          IF        F1=1
            PACK      NZFICODE,ANSQ,SP70  * Credit notes GST
          ENDIF
.
          ADD       ONE,F1
          PACK      KEY28,NZFIHOSP,NZFISYST,NZFISITE,NZFIYEAR:
                          NZFITYPE,NZFICODE,SP70
          CALL      RDNZFIG2
          BRANCH    OVRCD,CLFG8200
.
CLFG2000  COMPARE   F2,TOPERD
          GOTO      CLFG8000 IF LESS
.
          STORE     ZERO,F2,NZFIMTH1,NZFIMTH2,NZFIMTH3:
                               NZFIMTH4,NZFIMTH5,NZFIMTH6:
                               NZFIMTH7,NZFIMTH8,NZFIMTH9:
                               NZFIMT10,NZFIMT11,NZFIMT12:
                               NZFIMT13
          ADD       ONE,F2
          GOTO      CLFG2000
.
CLFG8000  CALL      UPNZFIG2                * update record
.
CLFG8200  MOVE      FROMPERD,F2           * set the from period
          GOTO      CLFG1000
.
.         Remove any records with FINCODE starts with G
.
CLFG9000  MOVE      HOSPID,NZFIHOSP
          MOVE      "3",NZFISYST
          MOVE      SP70,NZFISITE
          MOVE      CURFYEAR,NZFIYEAR
          MOVE      ANSA,NZFITYPE
.
          PACK      SKEY28,NZFIHOSP,NZFISYST,NZFISITE,NZFIYEAR:
                          NZFITYPE,SP70
.
          PACK      KEY28,NZFIHOSP,NZFISYST,NZFISITE,NZFIYEAR:
                          NZFITYPE,ANSQ,SP70
          CALL      RSNZFIG2
CLFG9200  CALL      RKNZFIG2
          BRANCH    OVRCD,CLFG9999
.
          PACK      KEY28,NZFIHOSP,NZFISYST,NZFISITE,NZFIYEAR:
                          NZFITYPE,SP70
          MATCH     KEY28,SKEY28
          GOTO      CLFG9999 IF NOT EQUAL
.
          UNPACK    SP70,KEY1,KEY6
          UNPACK    NZFICODE,KEY1,KEY6
          CMATCH    "Q",NZFICODE
          GOTO      CLFG9999 IF NOT EQUAL
          MATCH     SP70,KEY6
          GOTO      CLFG9200 IF EQUAL
.
          PACK      KEY28,NZFIHOSP,NZFISYST,NZFISITE,NZFIYEAR:
                          NZFITYPE,NZFICODE,SP70
          CALL      DENZFIG2
          GOTO      CLFG9000
.
CLFG9999  RETURN
+
.*******************************************************************************
.         GST
.*******************************************************************************
UPFG0000  COMPARE   ZERO,SAVAMT
          GOTO      UPFG9999 IF EQUAL
.
UPFG1000  UNPACK    SAVKEY31,KEY28,KEY3     * get key to nzpfinaf & period
          CALL      RDNZFIG2                * read nzpfinaf
          IF        OVRCD<>1
            GOTO      UPFG6000              * update existing record
          ENDIF
.
          CALL      CLFIN000
          UNPACK    KEY28,NZFIHOSP,NZFISYST,NZFISITE,NZFIYEAR,NZFITYPE,NZFICODE
          CALL      WRNZFIG2                * write new record with zeros
          GOTO      UPFG1000                * then update with amount
.
UPFG6000  MOVE      ZERO,FORM3
          MOVE      KEY3,FORM3
          MOVE      ZERO,FORM12P2
          LOAD      FORM12P2,FORM3,NZFIMTH1,NZFIMTH2,NZFIMTH3:
                                 NZFIMTH4,NZFIMTH5,NZFIMTH6:
                                 NZFIMTH7,NZFIMTH8,NZFIMTH9:
                                 NZFIMT10,NZFIMT11,NZFIMT12:
                                 NZFIMT13
          ADD       SAVAMT,FORM12P2
          STORE     FORM12P2,FORM3,NZFIMTH1,NZFIMTH2,NZFIMTH3:
                                 NZFIMTH4,NZFIMTH5,NZFIMTH6:
                                 NZFIMTH7,NZFIMTH8,NZFIMTH9:
                                 NZFIMT10,NZFIMT11,NZFIMT12:
                                 NZFIMT13
          CALL      UPNZFIG2                * update with new amount
UPFG9999  RETURN
+
.********************************************************************
.         Clear variables
.********************************************************************
CLFIN000  UNPACK    SP70,NZFIHOSP,NZFISYST,NZFISITE,NZFIYEAR,NZFITYPE,NZFICODE
          MOVE      ZERO,NZFIPERD
          MOVE      ZERO,NZFIMTH1
          MOVE      ZERO,NZFIMTH2
          MOVE      ZERO,NZFIMTH3
          MOVE      ZERO,NZFIMTH4
          MOVE      ZERO,NZFIMTH5
          MOVE      ZERO,NZFIMTH6
          MOVE      ZERO,NZFIMTH7
          MOVE      ZERO,NZFIMTH8
          MOVE      ZERO,NZFIMTH9
          MOVE      ZERO,NZFIMT10
          MOVE      ZERO,NZFIMT11
          MOVE      ZERO,NZFIMT12
          MOVE      ZERO,NZFIMT13
          MOVE      ZERO,NZFILSYR
          MOVE      SP30,NZFISPAR
CLFIN999  RETURN
+
.********************************************************************
.*                          COPY0000                                *
.*  Backup file nzpfinaf                                            *
.********************************************************************
COPY0000  CALL      DEFT0000                * get default path
.
          PACK      CMDLINE,SP70,SP70
          CLEAR     CMDLINE
          APPEND    "cp `ldat nzpfinaf.dat` ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    "sptfinsf.dat",CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      COPY9500
          ENDIF
.
          PACK      CMDLINE,SP70,SP70
          CLEAR     CMDLINE
          APPEND    "cp `ldat nzpfinaf.idx` ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    "sptfinsf.idx",CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      COPY9500
          ENDIF
.
          PACK      CMDLINE,SP70,SP70
          CLEAR     CMDLINE
          APPEND    "cp `ldat nzpfigaf.dat` ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    "sptfigaf.dat",CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      COPY9500
          ENDIF
.
          PACK      CMDLINE,SP70,SP70
          CLEAR     CMDLINE
          APPEND    "cp `ldat nzpfigaf.idx` ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    "sptfigaf.idx",CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
COPY9000  MOVE      ZERO,EXIT
          GOTO      COPY9999
.
COPY9500  MOVE      ONE,EXIT
          DISPLAY   *P1:24,*EL,*B,"Unable to backup file(s).  ":
                    "(Error: ",TASKID,")  ";
          CALL      HITENTER
COPY9999  RETURN
+
.**********************************************************************
.         Get the default directory
.**********************************************************************
DEFT0000  EXECUTE   "ldat nzpfinaf.dat > temp.txt",TASKID
.
          PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  DISPLAY   *P1:24,*EL,*B,"Error finding nzpfinaf.  ";
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
DEFT9999  RETURN
+
.*******************************************************************************
.         Subroutine to key in a valid date
.
KEYDTE    DISPLAY   *PCCOL:CVERT,*EL
          MOVE      IDAY,CDAY
          MOVE      IMON,CMON
          MOVE      IYEAR,CYEAR 
          MOVE      ICENT,CCENT
          MOVE      "1",CDEFDTE
          MOVE      "0",CHIGHLT
.
          CALL      KEYDATE
.
          MOVE      CDAY,DD
          MOVE      CMON,MM
          MOVE      CYEAR,YY
          MOVE      CCENT,CC
          MOVE      DD,IDAY
          MOVE      MM,IMON
          MOVE      YY,IYEAR
          MOVE      CC,ICENT
.          
          RETURN
+
.        ======================================================================
.        KHOS0000: Keyin hospital ID
.        ======================================================================
KHOS0000 DISPLAY    *P1:21,*EL,"Hospital Id : ";
         MOVE       TEN5,ECOL
         MOVE       "21",EVERT
         MOVE       SP3,CKYIDEF3
         MOVE       ZERO,CKYIMAND           * Not Mandatory
         OPEN       PATHSPA1,"pathspaf"
.
         CALL       PATHSPKY
         BRANCH     EXIT,KHOS1000,KHOS2000
         MOVE       PTHSHOSP,HOSPID
         MOVE       PTHSNAME,HOSPNAME
         MOVE       ONE,HOSPFLAG
         MOVE       ZERO,EXIT
         GOTO       KHOS9999
.
KHOS1000 MOVE       "All Hospitals",HOSPNAME
         MOVE       SP10,PTHSHOSP
         MOVE       ZERO,EXIT
         GOTO       KHOS9999
.
KHOS2000 MOVE       ONE,EXIT
KHOS9999 RETURN
.
.*******************************************************************************
.
WRTEMP   PACK      DPINVADM,PINVADM
         RESET     KEY52
         WRITE     PATTEMP,KEY52;KEY52,FINAMT,FPERIOD
         RETURN  
.  
RDSTEMP  RESET     KEY52
         READ      PATTEMP,KEY52;;
         RETURN 
.
RDKTEMP  MOVE      ZERO,OVRCD
         READKS    PATTEMP;FINHOSP,DFINSYS,FINSITE,FINYEAR,FINTYPE,FINCODE:
                           PINVDTE,PINVNO,DUNIQKEY,FINAMT,FPERIOD
         GOTO      OVERCOND IF OVER
         MOVE      DFINSYS,FINSYS
         MOVE      DUNIQKEY,UNIQKEY
         RETURN  
.
.
          INC       PATSTRYR
          INC       PATHSPKY
          INC       PATITMRD
          INC       PATMCHRD                * Miscellaneous Charges read routine
          INC       TFILENAM                     * Generate Temp File Name
.
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       WEBERRIO/INC                 * Web Server Error Logging
.
          INC       AAEDTRIO/INC
          INC       AAEDE1IO/INC
          INC       OUTDTRIO/INC
          INC       OUTSITIO/INC
          INC       OUTBB1IO/INC
         INCLUDE    PATDRGIO/INC
         INC       PATDSCIO/INC
         INC       PATDTRIO/INC
         INC       NZPFINIO/INC
         INC       NZPFIGIO/INC
         INC       PATHSPIO/INC
         INC       PATMCHIO/INC
         INC       PATMI1IO/INC
         INC       PATINVIO/INC
         INC       PATITMIO/INC
         INC       PMSFCIIO/INC
         INC       PMSCNOIO/INC
         INC       PMSVX1IO/INC
         INC       NZPSPRIO/INC
         INC       NZPRDTIO/INC
         INC       PATFN1IO/INC
         INCLUDE    STD001IO
.
ENDIT    CHAIN      PGM
         STOP

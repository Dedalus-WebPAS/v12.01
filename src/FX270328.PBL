.*******************************************************************************
.*    Program      : FX270328                                                  *
.*    Description  : Populate new table mltsitaf (multi O/P site security)     *
.*    Author       : Ania Pietrusewicz                                         *
.*    Date         : 12/11/2012                                                *
.*    Modifications:                                                           *
.*******************************************************************************
.
          INC       STD001FD
          INC       MLTSITFD/INC
          INC       WEBSECFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
CMDLINE   DIM       80
DIM6      DIM       6
DOPTION   DIM       1
FOPTION   FORM      1
RECCOUNT  FORM      6
.
FILENAME  INIT      "mltsitaf "
INDEX1    INIT      "67 u1-10,11-16"
ISBUILD   INIT      "isbuild "
.
PRGNAM    INIT      "Populate Multi O/P Site Security Table"
PRGID     INIT      "FX270328"
VERSION   INIT      "V12.01.00"
.

.*******************************************************************************
.*                                  MAIN0000                                   *
.*                             Controlling logic                               *
.*******************************************************************************
.
MAIN0000  CALL      INIT0000                         * Init and open files
.
MAIN1000  CALL      OPTN0000                         * Option Menu
          BRANCH    EXIT,MAIN9999                    * Exit
.
          GOTO      MAIN1000
.
MAIN9999  CHAIN     PGM
.
.*******************************************************************************
.*                                  INIT0000                                   *
.*               Display heading and open files for processing                 *
.*******************************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening ":
                    *P64:24,"websecaf"
          OPEN      WEBSECA1,"websecaf"
.
          DISPLAY   *P64:24,"mltsitaf"
          OPEN      MLTSITA1,"mltsitaf"
.
          MOVE      ZERO,RECCOUNT
.
INIT9999  RETURN
.
.*******************************************************************************
.*                                  OPTN0000                                   *
.*                            Display Options' Menu                            *
.*******************************************************************************
.
OPTN0000  MOVE      ZERO,EXIT
          MOVE      ZERO,RECCOUNT
.
          KEYIN     *P1:4,*EF,*V2LON,"0.",*HOFF," Exit":
                    *P1:5,*V2LON,"1.",*HOFF," Run Fixit":
                    *P1:7,"Select Option:":
                    *P16:7,*V2LON,COPTION
.
          BRANCH    COPTION,LOAD0000 
.
          COMPARE   "0", COPTION
          GOTO      OPTN9000 IF EQUAL
.
          BEEP
          GOTO      OPTN0000
.          
OPTN9000  MOVE      ONE,EXIT
.
OPTN9999  RETURN
.
.*******************************************************************************
.*                                  LOAD0000                                   *
.*                   Loads data from websecaf into mltsitaf                    *
.*******************************************************************************
.
LOAD0000  DISPLAY   *P1:23,*EF,"Records Added: ";
.
          PACK      KEY10,SP70
          CALL      RSWBSE1
LOAD1000  CALL      RKWBSE1                       * loop through each websec row
          BRANCH    OVRCD,LOAD9000
.
          MATCH     SP70,WBSESIT                  * is websec site blank?
          GOTO      LOAD1000 IF EQUAL             * yes - do not write record
.
          PACK      MLSTUSID,WBSEUID,SP70
          PACK      MLSTSITE,WBSESIT,SP70
          PACK      MLSTSPAR,SP70
.
          PACK      KEY16,MLSTUSID,MLSTSITE
          CALL      RAMLSIT1
          IF        OVRCD=1
            CALL      WRMLSIT1                      * write the new record
            ADD       ONE,RECCOUNT
            IF        (RECCOUNT%100)=1
              MOVE      RECCOUNT,DIM6
              LJUSTIFY  DIM6
              DISPLAY   *P19:23,*EL,*V2LON,DIM6
            ENDIF
          ENDIF
.
          GOTO      LOAD1000
.
LOAD9000  MOVE      RECCOUNT,DIM6
          LJUSTIFY  DIM6
          DISPLAY   *P16:23,*EL,DIM6:               * show total no. records added
                    *P1:24,"Fixit Complete. ";
          CALL      HITENTER
.
LOAD9999  RETURN
.
.*******************************************************************************
.
          INC       MLTSITIO/INC
          INC       WEBSECIO/INC
          INC       STD001IO
                                                                                                                  

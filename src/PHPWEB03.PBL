.-------------------------------------------------------------------------------
. Program       : PHPWEB02 
.
. Function      : CISAM Version of PHP Calls for Auto Suggest
.
. Modifications  :
.         V10.08.01  23/08/2016  Peter McMullen  0823848
.                    Add lower case login search to XMLRED00
.-------------------------------------------------------------------------------
.         V10.03.01  B.G. Ackland   CAR 264486 
.                    Program created.
.-------------------------------------------------------------------------------
          INC       IBAWEBDF    
.
          INC       WEBDATDF
          INC       PATCONFD/INC
          INC       MLTHCPFD/INC
          INC       PMSHCPFD/INC
          INC       PMSHKIFD/INC
          INC       PATDO1FD/INC
          INC       PATDKIFD/INC
          INC       PATVADFD/INC
.
. Variable Declarations  
.
ACTVSTAT  FORM      1          * Active/Inactive search status
DOCTSTAT  FORM      1          *
ARRCNT    FORM      3          * Array count
HCPSSTAT  FORM      2          * HCP Type
HCPARRAY  DIM       10[200]    * HCP displayed in search results
HCPAREND  FORM      3          * Number of HCPs in HCPARRAY[]
DOCARRAY  DIM       6[200]     * Doctors displayed in search results
DOCAREND  FORM      3          * Number of doctors in DOCARRAY[]
ROWCOUNT  FORM      3
SAVKEY6   DIM       6 
SAVKEY10  DIM       10
SAVKEY21  DIM       21
SAVKEY25  DIM       25
VALPTYPE  DIM       3
.
PRGID     INIT      "PHPWEB03"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "AutoSuggest PHP Services for CISAM"
.
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      XMLRED00       * Read Fifo WEBPID and USERID    * C-90207
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          PACK      KEY10,USERID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,MAIN1000
.
          CALL      XMLHED00
          BRANCH    REPORTNO,MAIN1100,MAIN1200,MAIN1300,MAIN1400,MAIN1500:
                             MAIN1600,MAIN1700
MAIN1100  MOVE      ZERO,HCPSSTAT
          MOVE      SP70,VALPTYPE
          CALL      GPSRH000
          GOTO      MAIN9000
.
MAIN1200  CALL      TRSRC000
          GOTO      MAIN9000
.
MAIN1300  MOVE      ZERO,DOCTSTAT
          CALL      PATDKA00
          GOTO      MAIN9000
.
MAIN1400  
          GOTO      MAIN9000
.
MAIN1500  
          GOTO      MAIN9000
.
MAIN1600  
          GOTO      MAIN9000
.
MAIN1700  
          GOTO      MAIN9000
.
MAIN9000  CALL      XMLEND00
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
.
PROFLD00
          RETURN
.-------------------------------------------------------------------------------
. Open Files and Initialize Variables
.-------------------------------------------------------------------------------
INIT0000  OPEN      PMSHCPA1,"pmshcpaf"
          OPEN      PMSHKIA1,"pmshkiaf"
          OPEN      PMSHKIA2,"pmshkiaf"
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDKIA1,"patdkiaf"
          OPEN      PATDKIA2,"patdkiaf"
          OPEN      PATVADA1,"patvadaf"
          OPEN      PATVADA2,"patvadaf"
.          OPEN      PATKHFA1,"patkhfaf"
.          OPEN      PATFN1A1,"patfn1af"
.
          READ      CONTROLF,NINTY1;*179,CWEBROOT
          READ      CONTROLF,HUND03;*183,PTCNACGP,*184,PTCNGHCP
          READ      CONTROLF,HUND10;*246,PTCNMHCP
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          IF        IBCNMHOS=1
            OPEN      MLTCODA1,"mltcodaf"
            OPEN      MLTCODA2,"mltcodaf"
            OPEN      MLTHCPA1,"mlthcpaf"
            OPEN      MLTHCPA2,"mlthcpaf"
          ENDIF
.
INIT9999  RETURN
.-------------------------------------------------------------------------------
. Clear Parameters
.-------------------------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO 
          PACK      KEYWORDS,SP70
          MOVE      "10",ROWCOUNT
.
CLRPAR99  RETURN
.-------------------------------------------------------------------------------
. Set Parameters
.-------------------------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "keywords=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,KEYWORDS
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "rowcount=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ROWCOUNT
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
.
.------------------------------------------------------------
. Doctor Search Results Table
.------------------------------------------------------------
PATDKA00  CALL      GETWRD00       * This routine Splits the KEYWORDS field
.                                  * into KEYWRD01 to KEYWRD10
          IF        ROWCOUNT=0
            MOVE      "10",ROWCOUNT       * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
          MOVE      ZERO,DOCAREND         * Number of doctors in DOCARRAY[]   
.
          WRITE     HTMLFILE;"[";
          STRIP     KEYWRD01
          PACK      KEY21,KEYWRD01,SP70
          CALL      RSPTDK2
PATDKA10  CALL      RKPTDK2
          BRANCH    OVRCD,PATDKA90
.
          MOVE      PTDKDOC,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD,PATDKA10
.
          CALL      PATDKB00
          BRANCH    OVRCD,PATDKA10,PATDKA90
.
          STRIP     DSNAME
          STRIP     DTITL
          STRIP     DGNAME
          STRIP     DCODE
          CLEAR     KEY50
          APPEND    DSNAME,KEY50
          APPEND    COMMA,KEY50
          APPEND    SP1,KEY50
          APPEND    DTITL,KEY50
          APPEND    SP1,KEY50
          APPEND    DGNAME,KEY50
          APPEND    " (",KEY50
          APPEND    DCODE,KEY50
          APPEND    ")",KEY50
          RESET     KEY50
.
          WRITE     HTMLFILE;"{#"code#":#"",DCODE,"#",":
                             "#"value#":#"",KEY50,"#",":
                             "#"subtext#":#"",dsadd1," ",dsadd2," ",dsadd3," ",dsadd4," ",dspost,"&nbsp;#"},";

          ADD       ONE,DISNUMB
          COMPARE   ROWCOUNT,DISNUMB
          GOTO      PATDKA10 IF NOT EQUAL
PATDKA90  
          WRITE     HTMLFILE;"]"
PATDKA99  RETURN
.------------------------------------------------------------
. Check Record
.------------------------------------------------------------
PATDKB00  MATCH     KEYWRD01,PTDKKWD
          GOTO      PATDKB92 IF NOT EQUAL
          IF        KEYWRDNO>1
            PACK      SAVKEY21,PTDKKWD,PTDKDOC
            CALL      PATDKC00
            BRANCH    EXIT,PATDKB91
            MOVE      SAVKEY21,KEY21
            CALL      RDPTDK2
          ENDIF
.
. DOCTSTAT - Set to 0 - Show All Active Doctors
.                   1 - Show All Doctors
.                   2 - Show Active in Hospital Only
.                   3 - Show Active Outside Hospital Only
.                   4 - Show All Doctors (Active/Inactive/Attending/Referring)
.
          IF        IBCNMHOS=1 
            MATCH    "1",PTDOHOSS
            IF       @EQUAL
              PACK     KEY13,WBSEHOSP,DCODE,SP70
              CALL     RDMLHCP1
              BRANCH   OVRCD,PATDKB91
            ELSE
              MATCH    "1",PTCNMHCP         * Only show matching hospitals
              GOTO     PATDKB91 IF EQUAL
            ENDIF
          ENDIF
.
          BRANCH    DOCTSTAT,PATDKB10,PATDKB10,PATDKB20,PATDKB90

PATDKB10  COMPARE    DOCTSTAT,DRSTAT
          IF        @EQUAL
            GOTO      PATDKB90
          ELSE
            GOTO      PATDKB91  
          ENDIF
.
PATDKB20  COMPARE   ONE,DRSTAT
          GOTO      PATDKB91 IF EQUAL
.
PATDKB90  CALL      CDOC0000                * Check if doctor has been 
          BRANCH    EXIT,PATDKB91           * displayed before
.
          MOVE      ZERO,OVRCD
          GOTO      PATDKB99
.
PATDKB91  MOVE      ONE,OVRCD
          GOTO      PATDKB99
.
PATDKB92  MOVE      TWO,OVRCD
.
PATDKB99  RETURN
.
.------------------------------------------------------------
. Check if this doctor has already been output in the search 
.------------------------------------------------------------
CDOC0000  MOVE       ZERO,F3
          MOVE       ZERO,EXIT
.
CDOC1000  ADD        ONE,F3
          IF         F3>DOCAREND
            IF         F3>200
              GOTO       CDOC9999
            ENDIF      
            MOVE       F3,DOCAREND
            MOVE       DCODE,DOCARRAY[F3]
            GOTO       CDOC9999
          ENDIF      
.
          MATCH      DCODE,DOCARRAY[F3]
          GOTO       CDOC1000 IF NOT EQUAL
.
          MOVE       ONE,EXIT
.
CDOC9999  RETURN
.------------------------------------------------------------
. Check for Matching Words
.------------------------------------------------------------
PATDKC00  MOVE      ONE,F2
          MOVE      ZERO,EXIT
          PACK      SAVKEY6,PTDKDOC,SP70
.
PATDKC10  COMPARE   KEYWRDNO,F2           * Loop through each Key Word in Search
          GOTO      PATDKC99 IF EQUAL     * Exit OK
          ADD       ONE,F2
          LOAD      KEYWRDXX,F2,KEYWRD01,KEYWRD02,KEYWRD03,KEYWRD04,KEYWRD05:
                             KEYWRD06,KEYWRD07,KEYWRD08,KEYWRD09,KEYWRD10
          PACK      KEY21,PTDKDOC,KEYWRDXX
          CALL      RDPTDK1               * Check for Exact Match
          COMPARE   ZERO,OVRCD            * Ok so Continue Loop
          GOTO      PATDKC10 IF EQUAL
.
          CALL      RKPTDK1               * Not Exact so Read Next Record
          BRANCH    OVRCD,PATDKC90        * No Match So Exit No Match
          PACK      KEY6,PTDKDOC,SP70
          MATCH     SAVKEY6,KEY6          * Match Key Fields
          GOTO      PATDKC90 IF NOT EQUAL * No Match So Exit
          STRIP     KEYWRDXX
          MATCH     PTDKKWD,KEYWRDXX         * Check Key Word
          GOTO      PATDKC90 IF NOT EQUAL * No Match So Exit
          GOTO      PATDKC10              * Check Next Search Word
.
PATDKC90  MOVE      ONE,EXIT
.
PATDKC99  RETURN 
.
.------------------------------------------------------------
. Search Table of GP's List by Name 
.------------------------------------------------------------
GPSRH000  CALL      GETWRD00
          IF        ROWCOUNT=0
            MOVE      "10",ROWCOUNT       * Set Default No Records to Display
          ENDIF
.
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
          MOVE      ZERO,HCPAREND         * Number of HCPs in HCPARRAY[]
.
          WRITE     HTMLFILE;"[";
          STRIP     KEYWRD01
          PACK      KEY25,KEYWRD01,SP70
          CALL      RSPMHKI2
GPSRH100  CALL      RKPMHKI2
          BRANCH    OVRCD,GPSRH999
.
          PACK      KEY10,PMHKHCPC,SP70
          CALL      RDPMHCP1                * read hcp table
          BRANCH    OVRCD,GPSRH100
.
          CALL      PMSHKB00
          BRANCH    EXIT,GPSRH100,GPSRH999
.
          STRIP     PMHCSNAM
          STRIP     PMHCTITL
          STRIP     PMHCGNAM
          STRIP     PMHCHCPC
          CLEAR     KEY50
          APPEND    PMHCSNAM,KEY50
          APPEND    COMMA,KEY50
          APPEND    SP1,KEY50
          APPEND    PMHCTITL,KEY50
          APPEND    SP1,KEY50
          APPEND    PMHCGNAM,KEY50
          APPEND    " (",KEY50
          APPEND    PMHCHCPC,KEY50
          APPEND    ")",KEY50
          RESET     KEY50
         
          WRITE     HTMLFILE;"{#"code#":#"",PMHCHCPC,"#",":
                             "#"value#":#"",KEY50,"#",":
                             "#"subtext#":#"",PMHCADR1," ",PMHCADR2," ",PMHCADR3," ",PMHCADR4," ",PMHCPOST,"&nbsp;#"},";
.
          ADD       ONE,DISNUMB
          COMPARE   ROWCOUNT,DISNUMB
          GOTO      GPSRH100 IF NOT EQUAL
.
GPSRH999  WRITE     HTMLFILE;"]";
          RETURN
.------------------------------------------------------------
. Check Record
.------------------------------------------------------------
PMSHKB00  MATCH     KEYWRD01,PMHKKKWD
          GOTO      PMSHKB92 IF NOT EQUAL
          IF        KEYWRDNO>1
            PACK      SAVKEY25,PMHKKKWD,PMHKHCPC
            CALL      PMSHKC00
            BRANCH    EXIT,PMSHKB91
            MOVE      SAVKEY25,KEY25
            CALL      RDPMHKI2
          ENDIF
.
. HCPSSTAT - Set to 0 - Show All HCP (Active Only)
.                   1 - Show Active Attending HCP
.                   2 - Show Active Referring HCP
.                   3 - Show Active GP HCP 
.                   4 - Show Active Other HCP 
.                   5 - Show All HCP (Both Active & Inactive all types)
.                   6 - Show Active Exclude GP
.                   7 - Show Other
.                   8 - Show Other Attending  
.                   9 - Show Other Referring  
.                  10 - Show Other Attending and Referring
.                  11 - Show Active Attending, Referring and Att.&Ref.HCP
.                  12 - Show HCP for Mental Healths(Doc.Type catg DT,TCINDC1='L'
.
          IF        IBCNMHOS=1
            MATCH    "1",PMHCHOSS
            IF       @EQUAL
              PACK     KEY13,WBSEHOSP,PMHCHCPC,SP70
              CALL     RDMLHCP1
              BRANCH   OVRCD,PMSHKB91
            ELSE
              MATCH    "1",PTCNMHCP         * Only show matching hospitals
              GOTO     PMSHKB91 IF EQUAL
            ENDIF
          ENDIF
.
          COMPARE   "5",HCPSSTAT
          GOTO      PMSHKB90 IF EQUAL
.
          IF        ACTVSTAT=0
            MATCH     "0",PMHCSTTS
            GOTO      PMSHKB91 IF NOT EQUAL    * display active only
          ENDIF
.
          IF        ACTVSTAT=1
            MATCH     "1",PMHCSTTS
            GOTO      PMSHKB91 IF NOT EQUAL    * display inactive only
          ENDIF
.
          BRANCH    HCPSSTAT,PMSHKB10,PMSHKB20,PMSHKB30,PMSHKB40,PMSHKB90:
                             PMSHKB60,PMSHKB90,PMSHKB90,PMSHKB90,PMSHKB90:
                             PMSHKB70,PMSHKB80,PMSHKB81
          GOTO      PMSHKB90
.
.                            HCP Type - 0, 2, 4, 6, 8, 10
PMSHKB10  BRANCH    PMHCHCST,PMSHKB91,PMSHKB90,PMSHKB91,PMSHKB90,PMSHKB91:
                             PMSHKB90,PMSHKB91,PMSHKB90,PMSHKB91,PMSHKB90
          GOTO      PMSHKB90
.
. Referring
.                            HCP Type - 1, 2, 5, 6, 9, 10
PMSHKB20  BRANCH    PMHCHCST,PMSHKB90,PMSHKB90,PMSHKB91,PMSHKB91,PMSHKB90:
                             PMSHKB90,PMSHKB91,PMSHKB91,PMSHKB90,PMSHKB90
          GOTO      PMSHKB91
.
. GP
.                            HCP Type - 3, 4, 5, 6
PMSHKB30  BRANCH    PMHCHCST,PMSHKB91,PMSHKB91,PMSHKB90,PMSHKB90,PMSHKB90:
                             PMSHKB90,PMSHKB91,PMSHKB91,PMSHKB91,PMSHKB91
          GOTO      PMSHKB91
.
. Other
.                            HCP Type - 7, 8, 9, 10
PMSHKB40  BRANCH    PMHCHCST,PMSHKB91,PMSHKB91,PMSHKB91,PMSHKB91,PMSHKB91:
                             PMSHKB91,PMSHKB90,PMSHKB90,PMSHKB90,PMSHKB90
          GOTO      PMSHKB91
.
. Active Exclude GP
.                            HCP Type - 0, 1, 2, 4, 5, 6, 7, 8, 9, 10
PMSHKB60  BRANCH    PMHCHCST,PMSHKB90,PMSHKB90,PMSHKB91,PMSHKB90,PMSHKB90:
                             PMSHKB90,PMSHKB90
          GOTO      PMSHKB90
.
. Active Attending and Ref Doctor
.                            HCP Type - 0, 1, 2
PMSHKB70  BRANCH    PMHCHCST,PMSHKB90,PMSHKB90,PMSHKB91,PMSHKB91,PMSHKB91:
                             PMSHKB91,PMSHKB91,PMSHKB91,PMSHKB91,PMSHKB91
          GOTO      PMSHKB90
.
. Responsible HCP for Mental Health
.
PMSHKB80  MOVE      "DT",TCODE
          PACK      KEY5,TCODE,PMHCSPC1,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,PMSHKB91
          MATCH     "L",TCINDC1
          GOTO      PMSHKB91 IF NOT EQUAL
          GOTO      PMSHKB90
.
. Active Exclude GP with related provider test
.         
PMSHKB81  MATCH     VALPTYPE,PMHCPTYP
          GOTO      PMSHKB91 IF NOT EQUAL
.
          BRANCH    PMHCHCST,PMSHKB90,PMSHKB90,PMSHKB91,PMSHKB90,PMSHKB90:
                             PMSHKB90,PMSHKB90
          GOTO      PMSHKB90
.
PMSHKB90  CALL      CHCP0000                * Check if hcp has been
          BRANCH    EXIT,PMSHKB91           * displayed before
.
          MOVE      ZERO,EXIT
          GOTO      PMSHKB99
.
PMSHKB91  MOVE      ONE,EXIT
          GOTO      PMSHKB99
.
PMSHKB92  MOVE      TWO,EXIT
.
PMSHKB99  RETURN
.
.------------------------------------------------------------
. Check if this HCP has already been output in the search
.------------------------------------------------------------
CHCP0000  MOVE       ZERO,F3
          MOVE       ZERO,EXIT
.
CHCP1000  ADD        ONE,F3
          IF         F3>HCPAREND
            IF         F3>200
              GOTO       CHCP9999
            ENDIF
            MOVE       F3,HCPAREND
            MOVE       PMHCHCPC,HCPARRAY[F3]
            GOTO       CHCP9999
          ENDIF
.
          MATCH      PMHCHCPC,HCPARRAY[F3]
          GOTO       CHCP1000 IF NOT EQUAL
.
          MOVE       ONE,EXIT
.
CHCP9999  RETURN
.------------------------------------------------------------
. Check for Matching Words
.------------------------------------------------------------
PMSHKC00  MOVE      ONE,F2
          MOVE      ZERO,EXIT
          PACK      SAVKEY10,PMHKHCPC,SP70
.
PMSHKC10  COMPARE   KEYWRDNO,F2           * Loop through each Key Word in Search
          GOTO      PMSHKC99 IF EQUAL     * Exit OK
          ADD       ONE,F2
          LOAD      KEYWRDXX,F2,KEYWRD01,KEYWRD02,KEYWRD03,KEYWRD04,KEYWRD05:
                                KEYWRD06,KEYWRD07,KEYWRD08,KEYWRD09,KEYWRD10
          PACK      KEY25,PMHCHCPC,KEYWRDXX
          CALL      RDPMHKI1              * Check for Exact Match
          COMPARE   ZERO,OVRCD            * Ok so Continue Loop
          GOTO      PMSHKC10 IF EQUAL
.
          CALL      RKPMHKI1              * Not Exact so Read Next Record
          BRANCH    OVRCD,PMSHKC90        * No Match So Exit No Match
          PACK      KEY10,PMHKHCPC,SP70
          MATCH     SAVKEY10,KEY10          * Match Key Fields
          GOTO      PMSHKC90 IF NOT EQUAL * No Match So Exit
          STRIP     KEYWRDXX
          MATCH     PMHKHCPC,KEYWRDXX         * Check Key Word
          GOTO      PMSHKC90 IF NOT EQUAL * No Match So Exit
          GOTO      PMSHKC10              * Check Next Search Word
.
PMSHKC90  MOVE      ONE,EXIT
.
PMSHKC99  RETURN 
.
.------------------------------------------------------------
. Transfer Source
.------------------------------------------------------------
TRSRC000 IF        ROWCOUNT=0
            MOVE      "10",ROWCOUNT       * Set Default No Records to Display
          ENDIF
.
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          WRITE     HTMLFILE;"[";
          MOVE      KEYWORDS,KEY5
          CALL      RSPTVAD1
TRSRC100  CALL      RKPTVAD1
          BRANCH    OVRCD,TRSRC200
          STRIP     KEY5
          MATCH     KEY5,PTVACODE
          GOTO      TRSRC200 IF NOT EQUAL
          MATCH     "I",PTVAACTV
          GOTO      TRSRC100 IF NOT EQUAL
.
          WRITE     HTMLFILE;"{#"code#":#"",PTVACODE,"#",":
                             "#"value#":#"",PTVADESC,"(",PTVACODE,")#",":
                             "#"subtext#":#"#"},";

          ADD       ONE,DISNUMB
          COMPARE   ROWCOUNT,DISNUMB
          GOTO      TRSRC100 IF NOT EQUAL
          GOTO      TRSRC999
.
TRSRC200  MOVE      KEYWORDS,KEY35
          CALL      RSPTVAD2
TRSRC300  CALL      RKPTVAD2
          BRANCH    OVRCD,TRSRC200
          STRIP     KEY35
          MATCH     KEY35,PTVADESC
          GOTO      TRSRC999 IF NOT EQUAL
          MATCH     "I",PTVAACTV
          GOTO      TRSRC300 IF NOT EQUAL
.
          WRITE     HTMLFILE;"{#"code#":#"",PTVACODE,"#",":
                             "#"value#":#"",PTVADESC,"(",PTVACODE,")#",":
                             "#"subtext#":#"#"},";
          ADD       ONE,DISNUMB
          COMPARE   ROWCOUNT,DISNUMB
          GOTO      TRSRC300 IF NOT EQUAL
.
TRSRC999  WRITE     HTMLFILE;"]";
          RETURN
.
          INC       MLTHCPIO/INC
          INC       PMSHCPIO/INC
          INC       PMSHKIIO/INC
          INC       PATVADIO/INC
          INC       PATDO1IO/INC
          INC       PATDKIIO/INC
.
          INC       DAYOFWEK
          INC       NAMSTRCD
          INC       WEBDATCD
          INC       IBAWEBCD
.

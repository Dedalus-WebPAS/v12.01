.*******************************************************************************
.* System         : AN-SNAP                                                    *
.* Program        : IBAHMS85.PBL                                               *
.* Name           : AN-SNAP Functional Gain Achieved Report                    *
.*******************************************************************************
.* Date           : 27/08/2013                                                 *
.* Author         : Don Nguyen                                                 *
.* Function       : Report showing the number of patients who have a           *
.*                  Functional Gain achieved; i.e. where a FIM score on        *
.*                  discharge is higher than the FIM score on admission.       *
.*                  This would be only for Programs that have a therapy end    *
.*                  date.                                                      *
.*                  Also reports patients who don't have a FIM score entered   *
.*                  for either Admission or Discharge.                         *
.*                                                                             *
.* Modifications  :                                                            *
.*                                                                             *
.*      V11.02.01   09/11/2022  J.Tan            TSK 0913474                   *
.*                  Added SEPMFLAG - Fixed Adm.FIM scores from 1st Admission   *
.*                  within the program regardless of any interruptions/visits  *
.*                  in between (as per Task 0903812 spec.)                     *
.*      V11.01.02   06/07/2021  Davin            TSK 0903812                   *
.*                  Skip separation mode validation for first program visit    *
.*                  (PRGVISNO/EXTP3000/EXTP3050)                               *
.*      V11.01.01   23/06/2021  Davin            TSK 0903812                   *
.*                  Mods to calculation of multi admission functional gain/loss*
.*                  - always use admission scores from the first visit         *
.*                  - always use discharge scores from the last visit          *
.*                  Modified: EXTP4000, EXTP4500, EXTP5000, WRTPA000, WRTPB000 *
.*******************************************************************************
.*      V11.00.01   11/02/2021  Tracey Nguyen    TSK 0901431                   *
.*                  Enabled a program to handle multi admission for the UR     *
.*                  Added - WRTPA000, WRTPB000, WRPTC000, DETEMP2, DETEMP3     *
.*                  Modified - EXTP2000, EXTP3000, EXPT5000, EXTP6000, EXTP7000*
.*      V10.08.02   18/08/2016  Ebon Clements    TSK 0304922                   *
.*                  Changed to report multiple therapy end dates per U/R       *
.*                  Fixed skip of visit for selected DDEST and DSTAT           *
.*      V10.08.01   12/08/2016  Ebon Clements    TSK 0304922                   *
.*                  Read discharge record prior to day case test -  EXTP2000   *
.*      V10.04.02   13/06/2014  Steve Armstrong  CAR 301639                    *
.*                  Moved calls to TFILENAM into INIT0000                      *
.*      V10.04.01   29/05/2014  Don Nguyen       CAR 299692                    *
.*                  Modified EXTP3000 to work with PTCNDRSM; skip patient      *
.*                  if TCINDC5 = 1,3,4,5,6 or 8.                               *
.*******************************************************************************
.*      V10.03.00   27/08/2013  Don Nguyen       CAR 283000                    *
.*                  Created Report                                             *
.*******************************************************************************
.
          INC       STD001FD                     * Standard Include
.
          INC       IBACONFD/INC                 * Control File
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       PATCODFD/INC                 * Category Codes
          INC       PATCONFD/INC                 * Control File
          INC       PATFIMFD/INC                 * FIM Assessment Scores
          INC       PATFN1FD/INC                 * Patient Health Fund File
          INC       PATDSCFD/INC                 * Patient Discharge File
          INC       PATHSPFD/INC                 * Hospital File
          INC       PATMA1FD/INC                 * Patient Master File
          INC       PATMI1FD/INC                 * Patient Admission File
          INC       PATTRNFD/INC                 * Patient Transfer File
          INC       PATVISFD/INC                 * Patient Visit File
          INC       PMSUPGFD/INC                 * Program Details File
          INC       PMSAPGFD/INC                 * Program Details Ext File
          INC       PMSVX1FD/INC                 * Patient Visit Ext File
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
.-------------------------------------------------------------------------------
.         Temporary file 1 - Patients with Functional Gain at Discharge
.
TEMP1     IFILE     SQL, FIXED=23, KEY="U1-8,15-22"
.
.Name     Type     Size    Physical   Start       Description
.----     ----     ----    --------   -----       -----------
X1URNUMB  DIM       8         8         1         Patient U/R Number
X1ADMFIM  DIM       3         3         9         Admission FIM score
X1DISFIM  DIM       3         3        12         Discharge FIM score
X1ENDDTE  DIM       8         8        15         Therapy End Date 
.End of record                         23
.
.-------------------------------------------------------------------------------
.         Temporary file 2 - Patients with no Functional Gain at Discharge
.
TEMP2     IFILE     SQL, FIXED=23, KEY="U1-8,15-22"
.
.Name     Type     Size    Physical   Start       Description
.----     ----     ----    --------   -----       -----------
X2URNUMB  DIM       8         8         1         Patient U/R Number
X2ADMFIM  DIM       3         3         9         Admission FIM score
X2DISFIM  DIM       3         3        12         Discharge FIM score
X2ENDDTE  DIM       8         8        15         Therapy End Date 
.End of record                         23
.
.-------------------------------------------------------------------------------
.         Temporary file 3 - Patients without Admission/Discharge FIM scores
.
TEMP3     IFILE     SQL, FIXED=9, KEY="U1-8"
.
.Name     Type     Size    Physical   Start       Description
.----     ----     ----    --------   -----       -----------
XXXXURNO  DIM       8         8         1         Patient U/R Number
.End of record                          9
.
.-------------------------------------------------------------------------------
.                           Variables
.-------------------------------------------------------------------------------
CHKADFIM  FORM      3                       * Check admission FIM score
CHKDSFIM  FORM      3                       * Check discharge FIM score
CMDLINE   DIM       50                      * Command Line
DATEFROM  DIM       8                       * Input From Date
DATETO    DIM       8                       * Input To Date
DIM5      DIM       5
DISPNAME  DIM       60                      * for NAMSTR00
FIMSCADM  FORM      3                       * FIM score for Admission
FIMSCDIS  FORM      3                       * FIM score for Discharge
FIMSCORE  FORM      3                       * FIM score
FNAME1    DIM       8                       * temporary file name
FNAME2    DIM       8                       * temporary file name
FNAME3    DIM       8                       * temporary file name
HOSPAPPR  DIM       10                      * Hospital Approval Number
HOSPCODE  DIM       3                       * Hospital Code
HOSPNAME  DIM       40                      * Hospital Name
INCLDAYC  DIM       1                       * Include Day Cases
INDICVAL  FORM      5.3                     * Clinical Indicator Value
INDPRINT  FORM      1.3                     * Clinical Indicator Value (decimal)
NAME35    DIM       35                      * for NAMSTR00
PATFNAME  DIM       35                      * Patient Full Name
PRGVISNO  FORM      3                       * program visit number count
SAVKEY24  DIM       24                      
SVADFIM1  FORM      3                       * Save admission FIM score 1
SVADFIM2  FORM      3                       * Save admission FIM score 2
TBLNUMBR  FORM      1                       * Table (temp file) to print from
TENDDATE  DIM       10                      * Therapy End Date (DD/MM/YYYY)
TODAY     DIM       8                       * Today's Date
TOTFGAIN  FORM      5                       * Total with Functional Gain
TOTREHDI  FORM      5                       * Total Rehab Discharges
URNADDED  FORM      1                       * Flag to indicate if U/R added
SEPMFLAG  FORM      1                       * Flag to indicate Separation mode
.-------------------------------------------------------------------------------
.                            Constants
.-------------------------------------------------------------------------------
CATD      INIT      "D "
CATDD     INIT      "DD"
DOWNCASE  INIT      "AaBbCcDdEeFfGgHhIiJjKkLlMmNnOoPpQqRrSsTtUuVvWwXxYyZz"
.
PRGID     INIT      "IBAHMS85"
PRGNAM    INIT      "Functional Gain Achieved Report"
VERSION   INIT      "V12.01.00"
.
.******************************************************************************
.*                           MAIN0000                                         *
.*                                                                            *
.*        Main program flow control                                           *
.******************************************************************************
MAIN0000  CALL      INIT0000                     * Initialise - begin descent
.
MAIN1000  IF        IBCNMHOS=1
            DISPLAY   *P25:6,*EL:
                      *P25:7,*EL:
                      *P25:8,*EL:
                      *P25:9,*EF;
            CALL      GHOS0000                   * Get Multi Hospital Id
            BRANCH    EXIT,MAIN9999
          ELSE
            DISPLAY   *P1:6,*EL:
                      *P25:7,*EL:
                      *P25:8,*EL:
                      *P25:9,*EF;
            PACK      HOSPAPPR,CAPPRVNO,SP70     * Hospital Approval Number
            PACK      HOSPNAME,CONAME,SP70       * Hospital Name
          ENDIF
.
          CALL      GSTD0000                     * Keyin the start date
          BRANCH    EXIT,MAIN9999                * Exit if nothing input
.
          CALL      GEND0000                     * Keyin the end date
          BRANCH    EXIT,MAIN1000                * Exit if nothing input
.
          CALL      GDAY0000                     * Keyin Include day cases
          BRANCH    EXIT,MAIN1000                * Nothing input
.
          CALL      GCON0000                     * OK to continue
          BRANCH    EXIT,MAIN1000                * restart if no selected
.
          CALL      CRET0000                     * Create temporary files
.
          CALL      EXTP0000                     * Extract Rehab Discharges
                                                 * with Accommodation and Carer
                                                 * details entered (Prior/Post)
.
          CALL      PRNT0000                     * Print the report
.
          CALL      KILL0000                     * Delete temporary files
.
          GOTO      MAIN1000                     * Next run
.
MAIN9999  CHAIN     PGM                          * Return from the nine circles
+
.******************************************************************************
.*                           INIT0000                    Called by : MAIN0000 *
.*                                                                            *
.*        Initialisation procedure                                            *
.******************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"patfimaf";
          OPEN      PATFIMA1,"patfimaf"
.
          DISPLAY   *P64:24,"patfn1af";
          OPEN      PATFN1A1,"patfn1af"
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A3,"patmi1af"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA2,"patvisaf"
.
          DISPLAY   *P64:24,"pmsupgaf";
          OPEN      PMSUPGA1,"pmsupgaf"
.
          DISPLAY   *P64:24,"pmsapgaf";
          OPEN      PMSAPGA1,"pmsapgaf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS        * 1 = Multi Hospital
.
          READ      CONTROLF,TEN;*79,CAPPRVNO         * Hospital Approval Number
.
          READ      CONTROLF,TWO;*4,CONAME            * Company Name
.
          READ      CONTROLF,TWENTY1;*45,PTCNDRSM     * Separation mode in PMS99
.                                                     *   1 = DSTAT, 2 = DDEST
.
          CALL      IBACLOCK
          PACK      TODAY,CCC,CYY,CMM,CDD
          REP       " 0",TODAY
.
          DISPLAY   *P1:6,*EL,"Hospital Id           : ";
          DISPLAY   *P1:7,"From Date             : ":
                    *P1:8,"To   Date             : ":
                    *P1:9,"Include Day Cases Y/N : ";
.
          MOVE      ZERO,TOTREHDI           * Init no of Rehab Discharges
          MOVE      ZERO,TOTFGAIN           * Total with Functional Gain
          MOVE      ZERO,INCLDAYC           * Include Day Cases Flag
          MOVE      ZERO,CPAGENO            * Init Page Count
          CLOCK     TIME,CTIMEIS            * Get the starting time
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAME1
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAME2
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAME3
.
INIT9999  RETURN
+
.*******************************************************************************
.*                           GHOS0000                      Called by: MAIN0000 *
.*                                                                             *
.*        Get Hospital ID                                                      *
.*******************************************************************************
GHOS0000  MOVE      ZERO,EXIT
          DISPLAY   *P25:6,*EL:
                    *P25:7,*EL:
                    *P25:8,*EL:
                    *P25:9,*EF;
.
          MOVE      SP70,HOSPCODE
          MOVE      SP70,PTHSHOSP
          MOVE      TWENTY5,ECOL                 * Column of key in
          MOVE      SIX,EVERT                    * Row of key in
          MOVE      SP3,CKYIDEF3                 * Defualt value
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,GHOS98000,GHOS98000
.
          PACK      HOSPCODE,KEY3,SP70           * Hospital Code
          PACK      HOSPAPPR,PTHSAPPR,SP70       * Hospital Approval Number
          PACK      HOSPNAME,PTHSNAME,SP70       * Hospital Name
.
          DISPLAY   *P25:6,*V2LON,PTHSNAME;
          MOVE      ZERO,EXIT
          GOTO      GHOS9999
.
GHOS98000  MOVE     ONE,EXIT
.
GHOS9999  RETURN
+
.******************************************************************************
.*                          GSTD0000                     Called by : MAIN0000 *
.*                                                                            *
.*        Get the start date of the report.                                   *
.*                                                                            *
.*          Return  EXIT = 1  if a nothing was input                          *
.******************************************************************************
GSTD0000  MOVE      ZERO,EXIT
.
          MOVE      SEVEN,CVERT                  * Line for keyin of from date
          MOVE      TWENTY5,CCOL                    * Col for keyin of from date
          MOVE      CVERT,EVERT                  * Col for error message
          MOVE      "40",ECOL                    * Col for error message
          MOVE      ZERO,CHIGHLT                 * No highlight
          MOVE      ONE,CDEFDTE
.
          UNPACK    SP6,CDAY,CMON,CYEAR          * Initialise date
          MOVE      SP8,DATEFROM
          MOVE      CCC,CCENT                    * Initialise century
.
          CALL      KEYDATE                      * Key in the date
          BRANCH    OVRCD,GSTD9000
.
          PACK      DATEFROM,CCENT,CYEAR,CMON,CDAY
          REP       " 0",DATEFROM
          MATCH     DATEFROM,TODAY
          GOTO      GSTD9999 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Date May Not be Greater than Today's ":
                    "Date.  ";
          CALL      HITENTER
          GOTO      GSTD0000
.
GSTD9000  MOVE      ONE,EXIT
.
GSTD9999  RETURN
+
.******************************************************************************
.*                           GEND0000                    Called by : MAIN0000 *
.*                                                                            *
.*        Get the end date of the report.                                     *
.*                                                                            *
.*          Return  EXIT = 1  if nothing was input                            *
.******************************************************************************
GEND0000  MOVE      ZERO,EXIT
.
          MOVE      EIGHT,CVERT                  * Line for keyin of to date
          MOVE      TWENTY5,CCOL                    * Col for keyin of to date
          MOVE      CVERT,EVERT                  * Col for error message
          MOVE      "40",ECOL                    * Col for error message
          MOVE      ZERO,CHIGHLT                 * No highlight
          MOVE      ONE,CDEFDTE
.
          MOVE      SP8,DATETO
.
          CALL      KEYDATE                      * Key in the date
          BRANCH    OVRCD,GEND9000
.
          PACK      DATETO,CCENT,CYEAR,CMON,CDAY
          REP       " 0",DATETO
          MATCH     DATETO,TODAY
          GOTO      GEND2000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Date May Not be Greater than Today's ":
                    "Date.  ";
          CALL      HITENTER
          GOTO      GEND0000
.
GEND2000  MATCH     DATEFROM,DATETO
          GOTO      GEND9999 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"To Date May Not be Less than From ":
                    "Date.  ";
          CALL      HITENTER
          GOTO      GEND0000
.
GEND9000  MOVE      ONE,EXIT
.
GEND9999  RETURN
+
.******************************************************************************
.*                           GDAY0000                    Called by : MAIN0000 *
.*                                                                            *
.*        Include/Exclude Day cases from report?                              *
.******************************************************************************
GDAY0000  MOVE      ZERO,EXIT
.
GDAY1000  KEYIN     *P25:9,"_",*P25:9,*V2LON,ANS;
.
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
.
          MATCH     ANS,ANSY
          IF        @EQUAL
            MOVE      ONE,INCLDAYC
            GOTO      GDAY9999
          ENDIF
.
          MATCH     ANS,ANSN
          IF        @EQUAL
            MOVE      ZERO,INCLDAYC
            GOTO      GDAY9999
          ENDIF
.
          BEEP
          GOTO      GDAY1000
.
GDAY9999  RETURN
+
.******************************************************************************
.*                           GCON0000                    Called by : MAIN0000 *
.*                                                                            *
.*        Ok. to continue (Y/N)                                               *
.******************************************************************************
GCON0000  MOVE      ZERO,EXIT
          DISPLAY   *P1:24,*EL,"Ok to Continue (":
                    *V2LON,ANSY,*HOFF,SLASH,*V2LON,ANSN,*HOFF,") ? ";
.
GCON1000  KEYIN     *P24:24,"_",*P24:24,*V2LON,ANS;
.
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
.
          MATCH     ANS,ANSY
          GOTO      GCON9999 IF EQUAL
.
          MATCH     ANS,ANSN
          GOTO      GCON9000 IF EQUAL
.
          BEEP
          GOTO      GCON1000
.
GCON9000  MOVE      ONE,EXIT
.
GCON9999  RETURN
+
.******************************************************************************
.*                           CRET0000                    Called by : MAIN0000 *
.*                                                                            *
.*        Create the temporary files                                          *
.******************************************************************************
CRET0000  CALL      KILL0000                     * Remove any existing files
.
          CLEAR     CMDLINE                      * Create temporary file
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME1,CMDLINE
          APPEND    " 23 U1-8,15-22",CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
          CLEAR     CMDLINE                      * Create temporary file
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME2,CMDLINE
          APPEND    " 23 U1-8,15-22",CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
          CLEAR     CMDLINE                      * Create temporary file
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME3,CMDLINE
          APPEND    " 9 U1-8",CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
          OPEN      TEMP1,FNAME1                 * Open temporary file 1
          OPEN      TEMP2,FNAME2                 * Open temporary file 2
          OPEN      TEMP3,FNAME3                 * Open temporary file 3
.
CRET9999  RETURN
+
.******************************************************************************
.*                           KILL0000                    Called by : MAIN0000 *
.*                                                                   CRET0000 *
.*        Delete the temporary files                                          *
.******************************************************************************
KILL0000  CLOSE     TEMP1                        * ensure temporary file closed
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME1,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID               * remove temporary file
.
          CLOSE     TEMP2                        * ensure temporary file closed
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME2,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID               * remove temporary file
.
          CLOSE     TEMP3                        * ensure temporary file closed
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME3,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID               * remove temporary file
.
KILL9999  RETURN
+
.******************************************************************************
.*                           CLFVAR00                    Called by : EXTP0000 *
.*                                                                            *
.*        Clears file vars                                                    *
.******************************************************************************
CLFVAR00  UNPACK    SP70,X1URNUMB,X1ADMFIM,X1DISFIM,X1ENDDTE
          UNPACK    SP70,X2URNUMB,X2ADMFIM,X2DISFIM,X2ENDDTE
          UNPACK    SP70,XXXXURNO
.
CLFVAR99  RETURN
+
.******************************************************************************
.*                           EXTP0000                    Called by : MAIN0000 *
.*                                                                            *
.*        Process all Programs for the period and extract all Patients that   *
.*        have been discharged with Accommodation & Carer details             *
.*        (Prior/Post Program).                                               *
.******************************************************************************
EXTP0000  DISPLAY   *P1:24,*EL,*P10:24,"Adm. No :":
                    *P58:24,"Scanning:";
.
          PACK      KEY31,SP70              * Read thru Program Details
          CALL      RDPMUPG1
EXTP1000  CALL      RKPMUPG1
          BRANCH    OVRCD,EXTP9999
.
.         Is the program completed?
.
          MATCH     SP70,PMUGENDD           * Program End Date entered?
          GOTO      EXTP1000 IF EQUAL       * No; get next Program
.
.         Is the program completed in the selected date range?
.
          MATCH     DATEFROM,PMUGENDD       * Program end before start range?
          GOTO      EXTP1000 IF LESS        * Yes; get next Program
.
          MATCH     PMUGENDD,DATETO         * Program end after end range?
          GOTO      EXTP1000 IF LESS        * Yes; get next Program
.
          MOVE      ZERO,PRGVISNO
          MOVE      ZERO,URNADDED
          MOVE      ZERO,FIMSCADM
          MOVE      ZERO,FIMSCDIS
          MOVE      ZERO,SVADFIM1
          MOVE      ZERO,SVADFIM2
          CALL      CLFVAR00
          MOVE      ZERO,SEPMFLAG
.
.         Pick up first visit from Program start date
.
          PACK      KEY24,PMUGURNO,PMUGSDAT,SP70
          CALL      RDSVISA2
EXTP2000  CALL      RDKVISA2
          BRANCH    OVRCD,EXTP1000
.
          MATCH     PVIURNO,PMUGURNO
          GOTO      EXTP1000 IF NOT EQUAL  * Check next Program  
.---------IF        !@EQUAL
.---------- IF        URNADDED=0
.----------   GOTO      EXTP1000            * Get next Program
.---------- ELSE
.----------   GOTO      EXTP4000            * Check FIM scores
.---------- ENDIF
.---------ENDIF
.
          MATCH     SP70,PMUGEDAT
          IF        !@EQUAL
            MATCH     PVIDATE,PMUGEDAT      * check Expiry Date
            GOTO      EXTP2000 IF LESS
          ENDIF
.
          COMPARE   THREE,PVITYPE           * Inpatient?
          GOTO      EXTP2000 IF NOT EQUAL   * No; get next visit
.
          PACK      KEY8,PVIBILL,SP70
          CALL      RDPTMIS1
          BRANCH    OVRCD,EXTP2000
.
          COMPARE   THREE,ASTAT             * Discharged?
          GOTO      EXTP2000 IF NOT EQUAL   * No; get next visit
.
          PACK      KEY8,PVIBILL,SP70       * Read Discharge File
          CALL      RDDSCH1
          BRANCH    OVRCD,EXTP2000
.
.         Include Day Cases?
.
          MATCH     "1",INCLDAYC
          GOTO      EXTP2500 IF EQUAL
.
          MATCH     ADATE,DDATE
          GOTO      EXTP2000 IF EQUAL
.
EXTP2500  IF        IBCNMHOS=1
            MATCH     PMVXMHOS,HOSPCODE     * Visit is for same hospital?
            GOTO      EXTP2000 IF NOT EQUAL * No; get next visit
          ENDIF
.
          MATCH     PMUGATYP,ATYPE          * Same Admission Type (Cat A)?
          GOTO      EXTP2000 IF NOT EQUAL   * No; get next visit
.
          MATCH     SP70,PMUGCLAM           * Claim Code?
          IF        !@EQUAL
            MATCH     PMUGCLAM,ACLAIM       * Matches Admission Claim Code?
            GOTO      EXTP2000 IF NOT EQUAL * No; get next visit
          ENDIF
.
          MATCH     SP70,PMUGFUND           * Health Fund?
          GOTO      EXTP3000 IF EQUAL       * No; skip check
.
          MATCH     PMUGFUND,AFUNDH         * Matches Admission Health Fund?
          GOTO      EXTP2000 IF NOT EQUAL   * No; get next visit
.
          PACK      KEY14,AFUNDH,AFNDTB,SP10
          CALL      RDFUND1
          BRANCH    OVRCD,EXTP2000
.
          MATCH     PMUGTABT,PTHFBCAT       * Matches Health Fund Table Type?
          GOTO      EXTP2000 IF NOT EQUAL   * No; get next visit
.
.         Found a matching visit for this program...
.
EXTP3000  DISPLAY   *P20:24,*V2LON,AURNO," ",DAADMNO;
.
.         ADD       ONE,PRGVISNO            * program visit number count
.         COMPARE   ONE,PRGVISNO
.         GOTO      EXTP3050 IF EQUAL       * skip sep mode check for visit 1
.
.         Exclude patients with a particular type of Separation mode
.
          COMPARE   ONE,PTCNDRSM
          IF        @EQUAL
            PACK      KEY5,CATD,DSTAT,SP70
            CALL      RDCODE1
            IF        OVRCD=0
              MOVE      ZERO,F1
              MOVE      TCINDC5,F1
              IF        F1=1 | F1=3 | F1=4 | F1=5 | F1=6 | F1=8
                MOVE      ONE,SEPMFLAG
.               GOTO      EXTP2000     * Skip patient
              ENDIF
            ENDIF
          ENDIF
.
          COMPARE   TWO,PTCNDRSM
          IF        @EQUAL
            PACK      KEY5,CATDD,DDEST,SP70
            CALL      RDCODE1
            IF        OVRCD=0
              MOVE      ZERO,F1
              MOVE      TCINDC5,F1
              IF        F1=1 | F1=3 | F1=4 | F1=5 | F1=6 | F1=8
                MOVE      ONE,SEPMFLAG
.               GOTO      EXTP2000     * Skip patient
              ENDIF
            ENDIF
          ENDIF
.
.---------IF        URNADDED=0
.---------  MOVE      ONE,URNADDED
.---------ENDIF
.
EXTP3050  PACK      SAVKEY24,PVIURNO,PVIDATE,DPVIBILL,SP70
.
.         Get FIM scores...
.
          MOVE      ZERO,FIMSCADM          
          MOVE      ZERO,FIMSCDIS          
.
          PACK      KEY24,DDADMNO,SP70
          CALL      RSPTFIM1
EXTP3100  CALL      RKPTFIM1
          IF        OVRCD=1
.-----------MOVE    SAVKEY24,KEY24
.-----------CALL    RDVISA2
.-----------GOTO    EXTP2000
            GOTO    EXTP4000                * Check FIM scores
          ENDIF
.
          MATCH     PTFIVISN,DDADMNO
          IF        !@EQUAL
.-----------MOVE    SAVKEY24,KEY24
.-----------CALL    RDVISA2
.-----------GOTO    EXTP2000
            GOTO    EXTP4000
          ENDIF
.
          MATCH     "1",PTFIDELT            * Deleted?
          GOTO      EXTP3100 IF EQUAL       * Yes; get next FIM score
.
.         Admission FIM score...
.
          COMPARE   ZERO,FIMSCADM           * Already got first adm FIM score?
          GOTO      EXTP3200 IF NOT EQUAL   * yes; now get discharge FIM
.
          MATCH     "A",PTFITYPE
          IF        @EQUAL
            MOVE      ZERO,FIMSCORE
            ASSIGN    PTFIEATS+PTFIGRMS+PTFIBTHS+FIMSCORE,FIMSCORE
            ASSIGN    PTFIUPBS+PTFILWBS+PTFITLTS+FIMSCORE,FIMSCORE
            ASSIGN    PTFIBLMS+PTFIBWMS+PTFITBDS+FIMSCORE,FIMSCORE
            ASSIGN    PTFITTLS+PTFITBTS+PTFILOCS+FIMSCORE,FIMSCORE
            ASSIGN    PTFISTRS+PTFICOMS+PTFIEXPS+FIMSCORE,FIMSCORE
            ASSIGN    PTFISCIS+PTFIPRBS+PTFIMEMS+FIMSCORE,FIMSCORE
            MOVE      FIMSCORE,FIMSCADM
.
.           Save Admission FIM Scores from first Admission within the program
.
            IF        SEPMFLAG=1
              MOVE      ZERO,SEPMFLAG
              GOTO      EXTP2000              * Skip patient
            ENDIF
          ENDIF

.
.         Discharge FIM score...
.
EXTP3200  MATCH     "D",PTFITYPE
          IF        @EQUAL
            MOVE      ZERO,FIMSCORE
            ASSIGN    PTFIEATS+PTFIGRMS+PTFIBTHS+FIMSCORE,FIMSCORE
            ASSIGN    PTFIUPBS+PTFILWBS+PTFITLTS+FIMSCORE,FIMSCORE
            ASSIGN    PTFIBLMS+PTFIBWMS+PTFITBDS+FIMSCORE,FIMSCORE
            ASSIGN    PTFITTLS+PTFITBTS+PTFILOCS+FIMSCORE,FIMSCORE
            ASSIGN    PTFISTRS+PTFICOMS+PTFIEXPS+FIMSCORE,FIMSCORE
            ASSIGN    PTFISCIS+PTFIPRBS+PTFIMEMS+FIMSCORE,FIMSCORE
            MOVE      FIMSCORE,FIMSCDIS
          ENDIF

          GOTO      EXTP3100           * Get next FIM score for this visit
.
.         Do we have FIM score for both admission & discharge?
.
EXTP4000  IF        SEPMFLAG=1
            MOVE      ZERO,SEPMFLAG
            GOTO      EXTP7200              * Skip patient
          ENDIF
.
          COMPARE   ZERO,FIMSCADM           * Admission FIM score?
          GOTO      EXTP7000 IF EQUAL       * No
.
          COMPARE   ZERO,FIMSCDIS           * Discharge FIM score?
          GOTO      EXTP7000 IF EQUAL       * No
.
          MOVE      FIMSCADM,CHKADFIM       * latest admission FIM
          MOVE      FIMSCDIS,CHKDSFIM       * latest discharge FIM
.
          COMPARE   ZERO,SVADFIM1
.
          IF        !@EQUAL
            MOVE      SVADFIM1,CHKADFIM     * existing temp1 admission FIM
          ENDIF
          COMPARE   ZERO,SVADFIM2
          IF        !@EQUAL
            MOVE      SVADFIM2,CHKADFIM     * existing temp2 admission FIM
          ENDIF
.
.         Compare FIM scores (Discharge vs Admission)
.
          COMPARE   CHKADFIM,CHKDSFIM
          IF        @LESS | @EQUAL
            GOTO      EXTP6000              * No Functional Gain
          ELSE
            GOTO      EXTP5000              * Functional Gain
          ENDIF     
.
.         Write to temp file 1 (Patients with a Functional Gain at Discharge)
.
EXTP5000  PACK      KEY16,AURNO,PMUGENDD,SP70
          CALL      RDTEMP1
          IF        OVRCD=1
.-----------MOVE      AURNO,X1URNUMB
.-----------MOVE      FIMSCADM,X1ADMFIM
.-----------MOVE      FIMSCDIS,X1DISFIM
.-----------MOVE      PMUGENDD,X1ENDDTE
.-----------CALL      WRTEMP1               * Write to temp file 1
            ADD       ONE,TOTREHDI          * Increment Total Rehab Discharges
            CALL      WRTPA000              * Validate and write to temp file 1
            ADD       ONE,TOTFGAIN          * Total with Functional Gain
          ELSE
            MOVE      FIMSCDIS,X1DISFIM     * latest Discharge FIM score
            CALL      UPTEMP1               * Update to temp file 1
          ENDIF
.
          MOVE      SAVKEY24,KEY24
          CALL      RDVISA2                 * Restore savekey24
          GOTO      EXTP2000                * Read next visit in program
.---------GOTO      EXTP1000
.
.         Write to temp file 2 (Patients with no Functional Gain at Discharge)
.
EXTP6000  PACK      KEY16,AURNO,PMUGENDD,SP70
          CALL      RDTEMP2
          IF        OVRCD=1
.-----------MOVE      AURNO,X2URNUMB
.-----------MOVE      FIMSCADM,X2ADMFIM
.-----------MOVE      FIMSCDIS,X2DISFIM
.-----------MOVE      PMUGENDD,X2ENDDTE
.-----------CALL      WRTEMP2               * Write to temp file 2
            ADD       ONE,TOTREHDI          * Increment Total Rehab Discharges
            CALL      WRTPB000              * Validate and write to temp file 2
          ELSE
            MOVE      FIMSCDIS,X2DISFIM     * latest Discharge FIM score
            CALL      UPTEMP2               * Update to temp file 2
          ENDIF
.
          MOVE      SAVKEY24,KEY24
          CALL      RDVISA2                 * Restore savekey24
          GOTO      EXTP2000                * Read next visit in program
.---------GOTO      EXTP1000
.
.         Write to temp file 3 (no FIM score for either admission and discharge)
.
EXTP7000  IF        SEPMFLAG=1
            MOVE      ZERO,SEPMFLAG
            GOTO      EXTP7200              * Skip patient
          ENDIF
.
          PACK      KEY8,AURNO,SP70
          CALL      RDTEMP3
          IF        OVRCD=1
.-----------MOVE      AURNO,XXXXURNO
.-----------CALL      WRTEMP3
            CALL      WRTPC000              * Validate and write to temp file 3
            ADD       ONE,TOTREHDI          * Increment Total Rehab Discharges
          ENDIF
.
EXTP7200  MOVE      SAVKEY24,KEY24
          CALL      RDVISA2                 * Restore savekey24
          GOTO      EXTP2000                * Read next visit in program
.---------GOTO      EXTP1000
.
EXTP9999  RETURN
+
.******************************************************************************
.*                           WRTPA000                     Called by: EXPT0000 *
.*                                                                            *
.*      IF the same UR already exists on temp file 2 THEN                     *
.*          Write to temp file 1 with the current Admission FIM score and the *
.*          latest Discharge FIM score AND                                    *
.*          Delete record in temp file 2                                      *
.*      ELSE                                                                  *
.*          Write to temp file 1                                              *
.*      ENDIF                                                                 *
.*                                                                            *
.*      IF the same UR already exists on temp file 3 THEN                     *
.*         Delete record in temp file 3                                       *
.******************************************************************************
WRTPA000  PACK      KEY8,AURNO,SP70
          CALL      RDTEMP3                 * UR exists in temp file 3?
          BRANCH    OVRCD,WRTPA300          * No,check if exists in temp file 2
          CALL      DETEMP3                 * Yes, delete record in temp file 3
.
WRTPA300  PACK      KEY16,AURNO,PMUGENDD,SP70
          CALL      RDTEMP2                 * UR already exists on temp file 2?
          BRANCH    OVRCD,WRTPA500
.
          MOVE      AURNO,X1URNUMB
          MOVE      X2ADMFIM,X1ADMFIM       * existing adm FIM for multi visit
          MOVE      FIMSCDIS,X1DISFIM       * latest dis FIM for multi visit
          MOVE      PMUGENDD,X1ENDDTE
          CALL      WRTEMP1                 * Write to temp file 1
.
          CALL      DETEMP2                 * Delete record in temp file 2
          GOTO      WRTPA900
.
WRTPA500  MOVE      AURNO,X1URNUMB
          MOVE      FIMSCADM,X1ADMFIM
          MOVE      FIMSCDIS,X1DISFIM
          MOVE      PMUGENDD,X1ENDDTE
          CALL      WRTEMP1                 * Write to temp file 1
.
WRTPA900  MOVE      X1ADMFIM,SVADFIM1       * save existing adm FIM
          MOVE      ZERO,SVADFIM2
.
WRTPA999  RETURN
+
.******************************************************************************
.*                           WRTPB000                     Called by: EXPT0000 *
.*                                                                            *
.*      IF the same UR already exists on temp file 1 THEN                     *
.*          Write to temp file 2 with the current Admission FIM score and the *
.*          latest Discharge FIM score AND                                    *
.*          Delete record in temp file 1                                      *
.*      ELSE                                                                  *
.*         Write to temp file 2                                               *
.*      ENDIF                                                                 *
.*                                                                            *
.*      IF the same UR already exists on temp file 3 THEN                     *
.*         Delete record in temp file 3                                       *
.******************************************************************************
WRTPB000  PACK      KEY8,AURNO,SP70
          CALL      RDTEMP3                 * UR exists in temp file 3?
          BRANCH    OVRCD,WRTPB300          * No,check if exists in temp file 2
          CALL      DETEMP3                 * Yes, delete record in temp file 3
.
WRTPB300  PACK      KEY16,AURNO,PMUGENDD,SP70
          CALL      RDTEMP1                 * UR already exists on temp file 1?
          BRANCH    OVRCD,WRTPB500
.
          MOVE      AURNO,X2URNUMB
          MOVE      X1ADMFIM,X2ADMFIM       * existing adm FIM for multi visit
          MOVE      FIMSCDIS,X2DISFIM       * latest dis FIM for multi visit
          MOVE      PMUGENDD,X2ENDDTE
          CALL      WRTEMP2                 * Write to temp file 2
.
          CALL      DETEMP1                 * Delete record in temp file 1
          GOTO      WRTPB900
.
WRTPB500  MOVE      AURNO,X2URNUMB
          MOVE      FIMSCADM,X2ADMFIM
          MOVE      FIMSCDIS,X2DISFIM
          MOVE      PMUGENDD,X2ENDDTE
          CALL      WRTEMP2               * Write to temp file 2
.
WRTPB900  MOVE      X2ADMFIM,SVADFIM2     * save existing adm FIM
          MOVE      ZERO,SVADFIM1
.
WRTPB999  RETURN
+
.******************************************************************************
.*                           WRTPC000                     Called by: EXPT0000 *
.*                                                                            *
.*        If the same UR already exists on temp file 1/2 then do not write    *
.*        to temp file 3                                                      *
.******************************************************************************
WRTPC000  PACK      KEY16,AURNO,PMUGENDD,SP70
          CALL      RDTEMP1
          BRANCH    OVRCD,WRTPC300
.
          GOTO      WRTPC999
.
WRTPC300  PACK      KEY16,AURNO,PMUGENDD,SP70
          CALL      RDTEMP2
          BRANCH    OVRCD,WRTPC500
.
          GOTO      WRTPC999
.
WRTPC500  MOVE      AURNO,XXXXURNO
          CALL      WRTEMP3                    * Write to temp file 3
          GOTO      WRTPC999
.
WRTPC999  RETURN
+
.******************************************************************************
.*                           HEAD0000                     Called by: PRNT0000 *
.*                                                                            *
.*        Print Report standard page heading                                  *
.******************************************************************************
HEAD0000  CALL      HEAD132                      * Standard heading
.
          UNPACK    DATEFROM,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PRINT     *N,*17,"Hospital : ",HOSPNAME
          PRINT     *N,*15,"Date Range : ",CPCDATE," to ";
.
          UNPACK    DATETO,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PRINT     CPCDATE,*N
          ADD       FOUR,CLNO
.
HEAD9999  RETURN
+
.******************************************************************************
.*                           COLHED00           Called by: TBHDFG00,TBHDNG00  *
.*                                                                            *
.*        Print column header row                                             *
.******************************************************************************
COLHED00  PRINT     *1,"| U/R Number":
                    *14,"| Patient Name":
                    *51,"| Admission FIM Score":
                    *73,"| Discharge FIM Score":
                    *95,"| Therapy End Date":
                    *132,"|"
          ADD       ONE,CLNO
.
COLHED99  RETURN
+
.******************************************************************************
.*                           TBHDFG00                     Called by: PRNT0000 *
.*                                                                            *
.*        Print heading for first table (Patients with a Functional Gain)     *
.******************************************************************************
TBHDFG00  CALL      UND132
          PRINT     *1,"|":
                    *20,"Patients with a Functional Gain at Discharge":
                    *132,"|"
          ADD       ONE,CLNO
          CALL      UND132
          CALL      COLHED00           * Print table column header row
          CALL      UND132
.
TBHDFG99  RETURN
+
.******************************************************************************
.*                           TBHDNG00                     Called by: PRNT0000 *
.*                                                                            *
.*        Print heading for second table (Patients with No Functional Gain)   *
.******************************************************************************
TBHDNG00  CALL      UND132
          PRINT     *1,"|":
                    *20,"Patients with no Functional Gain at Discharge":
                    *132,"|"
          ADD       ONE,CLNO
          CALL      UND132
          CALL      COLHED00           * Print table column header row
          CALL      UND132
.
TBHDNG99  RETURN
+
.******************************************************************************
.*                           PRTR0000                     Called by: PRNT0000 *
.*                                                                            *
.*        Print table row of values from the temp file                        *
.*                                                                            *
.*          Params : TBLNUMBR - The table (temp file) number; 1 or 2          *
.*                   PATFNAME - Patient Full Name                             *
.*                   TENDDATE - Therapy End Date                              *
.******************************************************************************
PRTR0000  COMPARE   ONE,TBLNUMBR
          GOTO      PRTR1000 IF NOT EQUAL
.
          PRINT     *1,"|",*4,X1URNUMB:
                    *14,"|",*16,PATFNAME:
                    *51,"|",*61,X1ADMFIM:
                    *73,"|",*83,X1DISFIM:
                    *95,"|",*97,TENDDATE:
                    *132,"|"
          ADD       ONE,CLNO
          GOTO      PRTR9999
.
PRTR1000  COMPARE   TWO,TBLNUMBR
          GOTO      PRTR9999 IF NOT EQUAL
.
          PRINT     *1,"|",*4,X2URNUMB:
                    *14,"|",*16,PATFNAME:
                    *51,"|",*61,X2ADMFIM:
                    *73,"|",*83,X2DISFIM:
                    *95,"|",*97,TENDDATE:
                    *132,"|"
          ADD       ONE,CLNO
.
PRTR9999  RETURN
+
.******************************************************************************
.*                           PRNT0000                     Called by: MAIN0000 *
.*                                                                            *
.*        Print list of patients with Rehab Programs that with/without a      *
.*        functional gain. Also print the FIM scores on Admission and         *
.*        Discharge.                                                          *
.******************************************************************************
PRNT0000  DISPLAY   *P1:24,*EL,*P10:24,"Printing: ";
.
.         Print first table of Patients (with Functional Gain at Discharge)
.
          CALL      HEAD0000
          PRINT     *N,"Total No Of Rehabilitation Patients with a":
                       " Functional Gain at Discharge",*75,":",*80,TOTFGAIN
          PRINT     *N,"Total No Of Rehabilitation Discharges for the":
                       " period",*75,":",*80,TOTREHDI,*N,*N
          ADD       SIX,CLNO
.
          CALL      TBHDFG00
.
          MOVE      ONE,TBLNUMBR
          PACK      KEY16,SP70                   * Position temporary file
          CALL      RDSTEMP1
PRNT1000  CALL      RDKTEMP1                     * Get the next record
          BRANCH    OVRCD,PRNT2000               * End of file reached
.
          DISPLAY   *P20:24,*V2LON,X1URNUMB
.
          PACK      KEY8,X1URNUMB
          CALL      RDMAST1                      * Read a master file record
          BRANCH    OVRCD,PRNT1000
.
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,PATFNAME
.
          COMPARE   "60",CLNO
          IF        !@LESS
            CALL      HEAD0000
            CALL      TBHDFG00
          ENDIF
.
          UNPACK    X1ENDDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          REP       " 0",CPCDATE
          MOVE      CPCDATE,TENDDATE
.
          CALL      PRTR0000                     * Print table row
          GOTO      PRNT1000                     * Get next record
.
.         Print 2nd table of Patients (with no Functional Gain at Discharge)
.
PRNT2000  CALL      UND132
          COMPARE   "60",CLNO                    * New page?
          IF        !@LESS
            CALL      HEAD0000
            CALL      TBHDNG00
          ELSE
            COMPARE   "53",CLNO
            IF        @LESS
              PRINT     *N,*N
              ADD       TWO,CLNO
              CALL      TBHDNG00
            ELSE
              CALL      HEAD0000
              CALL      TBHDNG00
            ENDIF
          ENDIF
.
          MOVE      TWO,TBLNUMBR
          PACK      KEY16,SP70
          CALL      RDSTEMP2
PRNT2100  CALL      RDKTEMP2
          BRANCH    OVRCD,PRNT3000
.
          DISPLAY   *P20:24,*V2LON,X2URNUMB
.
          PACK      KEY8,X2URNUMB
          CALL      RDMAST1                      * Read a master file record
          BRANCH    OVRCD,PRNT1000
.
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,PATFNAME
.
          COMPARE   "60",CLNO
          IF        !@LESS
            CALL      HEAD0000
            CALL      TBHDNG00
          ENDIF
.
          UNPACK    X2ENDDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          REP       " 0",CPCDATE
          MOVE      CPCDATE,TENDDATE
.
          CALL      PRTR0000                     * Print table row
          GOTO      PRNT2100                     * Get next record
.
PRNT3000  CALL      UND132
          COMPARE   "60",CLNO
          IF        !@LESS
            CALL      HEAD0000
          ENDIF
.
          MOVE      TOTFGAIN,INDICVAL
          DIV       TOTREHDI,INDICVAL
          MOVE      INDICVAL,INDPRINT
          MOVE      INDPRINT,DIM5
          REP       " 0",DIM5
.
          COMPARE   "57",CLNO
          IF        !@LESS
            CALL      HEAD0000
          ENDIF
          PRINT     *N,*N,"Clinical Indicator"
          ADD       THREE,CLNO
.
          COMPARE   "58",CLNO
          IF        !@LESS
            CALL      HEAD0000
          ENDIF
          PRINT     *N,"Total Numerator",*27,"=",*30,TOTFGAIN 
          ADD       TWO,CLNO
.
          COMPARE   "58",CLNO
          IF        !@LESS
            CALL      HEAD0000
          ENDIF
          PRINT     *N,"Total Denominator",*27,"=",*30,TOTREHDI 
          ADD       TWO,CLNO
.
          COMPARE   "58",CLNO
          IF        !@LESS
            CALL      HEAD0000
          ENDIF
          PRINT     *N,"Clinical Indicator Value",*27,"=",*30,DIM5
          ADD       TWO,CLNO
.
          COMPARE   "58",CLNO
          IF        !@LESS
            CALL      HEAD0000
          ENDIF
          PRINT     *N
          ADD       ONE,CLNO
.
.         Print any Patients wit no FIM scores (Admission or Discharge)
.
PRNT4000  PACK      KEY8,SP70
          CALL      RDSTEMP3
PRNT4100  CALL      RDKTEMP3
          BRANCH    OVRCD,PRNT9000
.
          COMPARE   "60",CLNO
          IF        !@LESS
            CALL      HEAD0000
          ENDIF
.
          PRINT     *1,"*** Warning: U/R Number ",XXXXURNO," has a":
                       " Therapy End Date but missing Admission or Discharge":
                       " FIM scores."
          ADD       ONE,CLNO
          GOTO      PRNT4100
.
PRNT9000  COMPARE   "60",CLNO
          IF        !@LESS
            CALL      HEAD0000
          ENDIF
.
          IF        CPAGENO > 0
            PRINT     *N,*N,"*** End of Report ***"
          ENDIF
.
PRNT9999  RETURN
+
.******************************************************************************
.*        I/O routines for temp file 1                                        *
.******************************************************************************
RDSTEMP1  RESET     KEY16
          READ      TEMP1,KEY16;;
          RETURN
.
RDKTEMP1  MOVE      ZERO,OVRCD
          READKS    TEMP1;X1URNUMB,X1ADMFIM,X1DISFIM,X1ENDDTE
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTEMP1   RESET     KEY16
          MOVE      ZERO,OVRCD
          READ      TEMP1,KEY16;X1URNUMB,X1ADMFIM,X1DISFIM,X1ENDDTE
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   RESET     KEY16
          WRITE     TEMP1,KEY16;X1URNUMB,X1ADMFIM,X1DISFIM,X1ENDDTE
          RETURN
.
UPTEMP1   UPDATE    TEMP1;X1URNUMB,X1ADMFIM,X1DISFIM,X1ENDDTE
          RETURN
.
DETEMP1  MOVE      ZERO,OVRCD
         RESET     KEY16
         DELETE    TEMP1,KEY16
         RETURN
+
.******************************************************************************
.*        I/O routines for temp file 2                                        *
.******************************************************************************
RDSTEMP2  RESET     KEY16
          READ      TEMP2,KEY16;;
          RETURN
.
RDKTEMP2  MOVE      ZERO,OVRCD
          READKS    TEMP2;X2URNUMB,X2ADMFIM,X2DISFIM,X2ENDDTE
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTEMP2   RESET     KEY16
          MOVE      ZERO,OVRCD
          READ      TEMP2,KEY16;X2URNUMB,X2ADMFIM,X2DISFIM,X2ENDDTE
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP2   RESET     KEY16
          WRITE     TEMP2,KEY16;X2URNUMB,X2ADMFIM,X2DISFIM,X2ENDDTE
          RETURN
.
UPTEMP2   UPDATE    TEMP2;X2URNUMB,X2ADMFIM,X2DISFIM,X2ENDDTE
          RETURN
.
DETEMP2  MOVE      ZERO,OVRCD
         RESET     KEY16
         DELETE    TEMP2,KEY16
         RETURN
+
.******************************************************************************
.*        I/O routines for temp file 3                                        *
.******************************************************************************
RDSTEMP3  RESET     KEY8
          READ      TEMP3,KEY8;;
          RETURN
.
RDKTEMP3  MOVE      ZERO,OVRCD
          READKS    TEMP3;XXXXURNO
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTEMP3   RESET     KEY8
          MOVE      ZERO,OVRCD
          READ      TEMP3,KEY8;XXXXURNO
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP3   RESET     KEY8
          WRITE     TEMP3,KEY8;XXXXURNO
          RETURN
.
DETEMP3  MOVE      ZERO,OVRCD
         RESET     KEY8
         DELETE    TEMP3,KEY8
         RETURN
+
.
.*******************************************************************************
.
          INC       STD001IO                     * System Standard Routines
.
          INC       CLPATHSP                     * Clear Hospital Table variables
          INC       CLPATDSC                     * Clear Disharge Record
          INC       NAMSTRCD
          INC       PATHSPKY                     * Keyin of a Hospital Id/code
          INC       TFILENAM                     * Generate Temp File Name
.
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       PATCODIO/INC                 * Category Codes
          INC       PATFIMIO/INC                 * FIM Assessment Scores
          INC       PATFN1IO/INC                 * Patient Discharge File
          INC       PATDSCIO/INC                 * Patient Discharge File
          INC       PATHSPIO/INC                 * Hospital File
          INC       PATMA1IO/INC                 * Patient Master File
          INC       PATMI1IO/INC                 * Patient Admission File
          INC       PATTRNIO/INC                 * Patient Transfer File
          INC       PATVISIO/INC                 * Patient Visit File
          INC       PMSUPGIO/INC                 * Program Details File
          INC       PMSAPGIO/INC                 * Program Details Ext File
          INC       PMSVX1IO/INC                 * Patient Visit Ext File
          INC       WEBERRIO/INC                 * Web Server Error Logging

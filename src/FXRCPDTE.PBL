.*****************************************************************************
.* System    :   Receiptiong                                                 *
.* Program   :   FXRCPDTE                                                    *
.* Desc      :   Program to change RCDTALLO to 2 for Unallocated Suspense Reg*
.*****************************************************************************
.* Date      :   23/08/2016                                                  *
.* Author    :   J.Tan                                                       *
.* Function  :   This program will change value of RCDTALLO to 2 for         *
.*               Unallocated Suspense receipt                                *
.* Mods      :   V10.08.00 23/08/2016 J.Tan   TSK 0819992                    *
.*               Created program                                             *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       RCPBNKFD/INC
          INC       RCPDTEFD/INC
          INC       RCPREGFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
COUNTER   FORM      8
DIM3      DIM       3
SAVKEY16  DIM       16
.
.
PRGID     INIT      "FXRCPDTE"
PRGNAM    INIT      "Fixit for Unallocated Suspense Receipt"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
.
          BRANCH    COPTION,MAIN1000       * proceed with fixit
          GOTO      MAIN9999
.
MAIN1000  CALL      MAINQST                * Prompt user for action
.
          COMPARE   ZERO,CCITEM            * Post selected ?
          GOTO      MAIN2000 IF EQUAL      * Yes
          GOTO      MAIN9999 IF LESS       * Cancel selected
.
MAIN2000  CALL      PROC0000               * process
          GOTO      MAIN0100
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                     * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          OPEN      RCPBNKA1,"rcpbnkaf"
          OPEN      RCPDTEA4,"rcpdteaf"
          OPEN      RCPREGA1,"rcpregaf"     * Register table
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run fixit                               *
.*                        TRUE  (1)  Exit option selected                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*****************************************************************************
PROC0000  DISPLAY   *P1:24,*EL,"Processing:";
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
          DISPLAY   *P1:18,"Started  : ",CDATE,SP1,CTIMEIS
.
          CALL      HEAD0000
.
          MOVE      ZERO,COUNTER                 * initialise update count
          MOVE      ZERO,CPAGENO                 * initialise print variables
          MOVE      ZERO,CNOUNDLN
          CALL      IBACLOCK
.
          PACK      KEY16,ZERO,SP70
          CALL      RSRCDTE4
PROC1000  CALL      RKRCDTE4
          BRANCH    OVRCD,PROC9000
.
          MATCH     "0",RCDTALLO
          GOTO      PROC9000 IF NOT EQUAL
.
          PACK      KEY12,RCDTRECN,SP70
          CALL      RDRCBNK1
          BRANCH    OVRCD,PROC1000
.
          MATCH     "1",RCBNSTAT          * skip cancelled receipts
          GOTO      PROC1000 IF EQUAL
.
          PACK      KEY3,RCDTREGI,SP70
          CALL      RDREGA1
          BRANCH    OVRCD,PROC1000
.
          COMPARE   "99",REGIBACD                   * Suspense register?
          GOTO      PROC1000 IF NOT EQUAL
.
          DISPLAY   *P20:24,RCDTRECN;
.
          UNPACK    RCDTTDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *1,RCDTRECN,*20,CPCDATE
.
          PACK      SAVKEY16,RCDTALLO,RCDTRECN,RCDTTCNT,SP70
.
          MOVE      TWO,RCDTALLO
          CALL      UPRCDTE4
.
          ADD       ONE,COUNTER
.
          MOVE      SAVKEY16,KEY16
          CALL      RSRCDTE4
.
          GOTO      PROC1000
.
PROC9000  CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
.
          DISPLAY   *P1:20,*EF,*V2LON,COUNTER:
                    " records updated. ";
.
          DISPLAY   *P1:22,"Finished : ",CDATE,SP1,CTIMEIS,".  ";
          CALL      HITENTER
.
          IF        COUNTER <> 0
            CALL      LINE0000
          ENDIF
.
          PRINT     *N,*1,"No of records updated : ",COUNTER:
                    *N,*N,*1,"*** End of Report ***"
.
PROC9999  RETURN
+
.*****************************************************************************
.*                               HEAD0000              Called by: MAIN0000   *
.*****************************************************************************
HEAD0000  CALL      HEAD132                      * display page header
          PRINT     *40,"Unallocated Suspense Receipts",*N
.
          PRINT     *1,"Receipt Number ",*20,"Receipt Date"
.
          CALL      LINE0000                     * draw line across page
          MOVE      EIGHT,CLNO                   * increment line count
.
HEAD9999  RETURN
+
.****************************************************************************
.*                            LINE0000                 Called by: PROC0000  *
.*                      Draw line across page                     HEAD0000  *
.****************************************************************************
.
LINE0000  PRINT     "*-----------------------------------------------":
                    "-----------------------------------*"
.
LINE9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       RCPBNKIO/INC
          INC       RCPDTEIO/INC
          INC       RCPREGIO/INC
          INC       STD001IO

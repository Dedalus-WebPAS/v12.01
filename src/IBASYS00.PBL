. *****************************************************************************
. *              P R O G R A M  S U M M A R Y               		      *
. *              -------------  -------------                                 *
. *                                                                           *
. *     PROGRAM NAME:     IBASYS00                                            *
. *                                                                           *
. *     FUNCTION:         PRINTER CODE MAINTENANCE                            *
. *                                                                           *
. *     DATE WRITTEN:     23/04/87                                            *
. *                                                                           *
. *     ADAPTED BY :      ERIC PHAN (I.B.A) FROM IBAPAT11                     *
. *                       AND IBAPAT51.                                       *
. *                                                                           *
. *                * A T T E N T I O N *                                      *
. *                - - - - - - - - - - -                                      *
. *          This   program   is  the   property  of                          *
. *          Information Builders Australia  Pty Ltd                          *
. *          and must not be utilised,  modified  or                          *
. *          copied in any manner whatsoever without                          *
. *          the  express   written   permission  of                          *
. *          Information Builders Australia Pty Ltd.                          *
. *                                                                           *
. *****************************************************************************
. * Mods     : 			                                              *
. *            V10.04.01 15/04/2014  Ebon Clements    CAR 299449              * 
. *                      Fixed key pack for printer file read                 *
. *            V9.02.03  12/02/2002  Dean Cramer                              * 
. *                      Fix wrong labels                                     *
. *            V9.02.02  06/02/2002  Dean Cramer                              * 
. *                      Fix lower case Y/N entries rejected                  *
. *            C2.09.01  08/08/2001  Dave Matthews SRF 11884                  * 
. *                      Recompile for change to DPRTDSPL.PBL - losing printer*
. *                      details that should have been displayed on last line.*
. *****************************************************************************
. *            V5.09.B01 13/11/2000  Ebon Clements                            * 
. *                      Display error on invalid printer/department selection*
. *            V5.09.B00 27/09/2000  Caleb Sharman                            * 
. *                      Mods to include a printer & department enquiry       *
. *            V5.07.B02 19/01/1999  Nicole Harrington                        *
. *                      507 rebound - header date                            *
. *            V5.07     06/10/98  Jim Stathopoulos                           *
. *                      507 Changes                                          *
. *****************************************************************************
. *                       V5.01.02  11/10/89  L.P.                            *
. *                       Fixed to delete codes properly.                     *
. *                       SRF102306                                           *
. *                       V5.01.03  03/07/90  i.c.                            *
. *                       Added ? option for Department                       *
. *                       SRF105354                                           *
. *                       V6.02.01  02/05/95  Gabrielle                       *
. *                       Changed report to check for  different category     *
. *                       rather than a blank code                            *
. *****************************************************************************
.
          INC       STD001FD
.
          INC       IBAPRTFD/INC
.
.
+
.------------------------------------------------------------------------------
.         Variables
.------------------------------------------------------------------------------
.
ADD       DIM       2
AMM       DIM       2
AYY       DIM       2
ACC       DIM       2
ACTION    DIM       1
ANS2      DIM       2
ANS3      DIM       3
.
CALLPOSN  FORM      1
CDESC     DIM       20
CONAME    DIM       35
COUNT     FORM      3
.
DANS      DIM       3
DANSOFF   INIT      "Off"
DANSON    INIT      "On "
.
FCODE     FORM      1
FIELD     FORM      2
FLAG      FORM      1
.
LNO       FORM      2
.
NEWDESC   DIM       20
.
OPERCODE  DIM       4
.
PAGENO    FORM      3
PRDIM2    DIM       2
PRDIM3    DIM       3
PRTFLAG   FORM      1
QUESTFLG  FORM      1                  * ? mark option flag (0=not used, 1=used)
.
SAVCATA   DIM       2
SEC       FORM      2
SEC1      FORM      "00001"
SPRXCATA  DIM       2
SPRXCODE  DIM       3
SPRXDESC  DIM       20
SAVTCODE  DIM       2
.
TMPKEY    DIM       5[28]
TMPKE2    DIM       3[28]
.
PRGID     INIT      "IBASYS00"
PRGNAM    INIT      "Printer Code File Maintenance"
VERSION   INIT      "V12.01.00"
+
.------------------------------------------------------------------------------
.         Start of program
.------------------------------------------------------------------------------
.
          DISPLAY   *ES,PRGID:
                    *P56:24,"Opening ibacodef";
          OPEN      IBACODE1,"ibacodef"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"ibaprtaf";
          OPEN      IBAPRTA1,"ibaprtaf"
.
          READ      CONTROLF,TWO;*249,PRTFLAG
.
          CALL       DISPHEAD
.
START     DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Printer Master Register Listing":
                    *P1:6,*V2LON,TWO,*HOFF," = Set up Printer Maintenance":
                    *P1:7,*V2LON,THREE,*HOFF," = Set up Department Codes":
                    *P1:8,*V2LON,FOUR,*HOFF:
                    " = Printer System On/Off Indicator":
                    *P1:9,*V2LON,FIVE,*HOFF," = Department Enquiry":
                    *P1:10,*V2LON,SIX,*HOFF," = Printer Enquiry"
.
          COMPARE   NINE TO SECLEV
          GOTO      START0 IF NOT EQUAL
.
          DISPLAY   *P1:11,*V2LON,SEVEN,*HOFF," = Codes Maintenance"
          DISPLAY   *P1:12,*V2LON,EIGHT,*HOFF," = Category Maintenance"
.
START0    KEYIN     *P1:14,*EL,"Option : ",*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
          BRANCH    OPTION OF PRNT0000,STARTC,ADCATA,ONOFFI,DPDS0000,PRTA0000
.
          COMPARE   NINE TO SECLEV
          GOTO      START1 IF NOT EQUAL
.
          BRANCH    OPTION OF PRNT0000,STARTC,ADCATA,ONOFFI,DPDS0000:
                    PRTA0000,ADCODE,ACCODE
.
START1
          DISPLAY   *P35:14,*B,*V2LON,"** Invalid Selection **",*W2;
          GOTO      START0
+
.------------------------------------------------------------------------------
.         Add a Department
.------------------------------------------------------------------------------
.
ADCATA    DISPLAY   *P1:3,*EF,*P30:5,*ULON,"ADD A DEPARTMENT",*HOFF:
                    *P10:7,"Department        : ":
                    *P10:9,"Description       :"
.
ADCATA1   KEYIN     *P1:24,*EL,*P30:9,*EL,*P30:7,*EL,*V2LON,PRXCATA;
          RESET     PRXCATA
          GOTO      START IF EOS
.
.       Ensure that category is one of 5 legal categories
.
          ENDSET    PRXCATA
          APPEND    SP3,PRXCATA
          RESET     PRXCATA
.
NOERR     PACK      KEY5 WITH PRXCATA,SP3
          MOVE      ZERO TO FLAG
          CALL      RDICOD1
          BRANCH    OVRCD OF ADCATA3
.
          DISPLAY   *P30:9,*EL,*V2LON,PRXDESC
          MOVE      ONE TO FLAG
.
NOERR1    KEYIN     *P1:24,"** Do you want to change the description (Y/N) ? ":
                    ANS,*P1:24,*EL
.
          REP       "nNyY",ANS    
          CMATCH    "N" TO ANS
          GOTO      ADCATA1 IF EQUAL
.
          CMATCH    "Y" TO ANS
          GOTO      ADCATA3 IF EQUAL
.
          BEEP
          GOTO      NOERR1
.
ADCATA3   KEYIN     *P30:9,*EL,*V2LON,PRXDESC
.
          ENDSET    PRXDESC
          APPEND    SP30,PRXDESC
          RESET     PRXDESC
.
ADCATA3A  KEYIN     *P10:24,*EL,"Correct Information (":
                    *V2LON,"Y",*HOFF,*DV,SLASH,*V2LON,"N",*HOFF:
                    ") ? ",*V2LON,ANS;
.
          REP       "nNyY",ANS    
          CMATCH    "N" TO ANS
          GOTO      ADCATA5 IF EQUAL
.
          CMATCH    "Y" TO ANS
          GOTO      ADCATA3B IF EQUAL
. 
          BEEP
          GOTO      ADCATA3A
.
ADCATA3B  BRANCH    FLAG OF ADCATA4
.
          CALL      WRICOD1
          DISPLAY   *P56:24,*V2LON,"** ADDED **"
          GOTO      ADCATA5
.
ADCATA4
          CALL      UPICOD1
          DISPLAY   *P56:24,*V2LON,"** UPDATED **"
.
ADCATA5   DISPLAY   *P30:7,*EL,*P30:9,*EL,*P1:24,*EL;
.
          GOTO      ADCATA1
+
.------------------------------------------------------------------------------
.         Code Maintenance
.------------------------------------------------------------------------------
.
STARTC    DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Insert a Printer to a Dept.":
                    *P1:6,*V2LON,TWO,*HOFF," = Delete a Printer from a Dept."
.
START2    KEYIN     *P1:8,*EL,"Option : ",*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      START IF EQUAL
.
REPEAT    BRANCH    OPTION OF ADENTY,DEENTY
          DISPLAY   *P35:8,*B,*V2LON,"** Invalid Selection **",*W2;
          GOTO      START2
+
.
.------------------------------------------------------------------------------
.       Add a code
.------------------------------------------------------------------------------
.  
ADENTY    MOVE      FALSE,QUESTFLG
.
          DISPLAY   *P1:3,*EF,*P30:5,*ULON,"ADD A PRINTER",*HOFF: 
                    *P10:7,"Department        : ": 
                    *P10:9,"Printer           :" 
.
ADENTY1   KEYIN     *P1:24,*EL,*P30:11,*EL,*P30:9,*EL,*P30:7,*EL,*V2LON,PRXCATA;
.
ADENTY1A  RESET     PRXCATA
          GOTO      STARTC IF EOS
.
          ENDSET    PRXCATA
          APPEND    SP3,PRXCATA
          RESET     PRXCATA
.
          CMATCH    QUEST,PRXCATA
          GOTO      ADENTY1C IF NOT EQUAL
.
          MOVE      TRUE,QUESTFLG
          CALL      IBACODDS
          BRANCH    OVRCD,STARTC
          DISPLAY   *P1:24,*EL,"Department : "
.
ADENTY1B  KEYIN     *P14:24,*EL,*DV,UNDLN2,*P14:24,*V2LON,PRXCATA
          GOTO      ADENTY1A
.
ADENTY1C  MATCH     "CM",PRXCATA
          GOTO      ADENTY1D IF EQUAL
.
          PACK      KEY5 WITH PRXCATA,SP3
          CALL      RDICOD1
          COMPARE   ZERO,OVRCD
          GOTO      ADENTY2 IF EQUAL
.
ADENTY1D
          BRANCH    QUESTFLG,ADENTY1E
          DISPLAY   *P35:7,*B,*V2LON,"** Invalid Department ** ",*W2;
          GOTO      ADENTY1
.
ADENTY1E
          DISPLAY   *P35:24,*B,*V2LON,"** Invalid Department ** ",*W2;
          GOTO      ADENTY1B
.
ADENTY2
          BRANCH    QUESTFLG,ADENTY2A
          GOTO      ADENTY2B
.
ADENTY2A  DISPLAY   *P1:3,*EF,*P30:5,*ULON,"ADD A PRINTER",*HOFF: 
                    *P10:7,"Department        : ":
                    *P10:9,"Printer           : ":
                    *P30:7,*V2LON,PRXCATA
.
ADENTY2B  DISPLAY   *P35:7,PRXDESC
.
ADENTY2C  MOVE      SP3,PRXCODE
.
OKAD2A    MOVE      SP3,PRXCODE
          KEYIN     *P1:24,*EL,*P30:9,*EL,*V2LON,*JR,PRXCODE;
          RESET     PRXCODE
          GOTO      ADENTY1 IF EOS
.
MKADKY    PACK      KEY5 WITH PRXCATA,PRXCODE
          CALL      RDICOD1
          BRANCH    OVRCD OF ADENTY3
.
          DISPLAY   *P56:24,*B,*V2LON,"** Already on File **",*W2;
          GOTO      ADENTY2C
.
.  Display the description from the IBAPRTAF file
.
ADENTY3   MATCH     " SF" TO PRXCODE
          GOTO      ADENTY3A IF EQUAL
.
          MATCH     " CN" TO PRXCODE
          GOTO      ADENTY3B IF EQUAL
.
          PACK      KEY6,SP3,PRXCODE,SP70
          CALL      RDPRTA1
          BRANCH    OVRCD OF ADENTY5
.
          MOVE      PRTDESC,PRXDESC
          DISPLAY   *P35:9,*V2LON,PRTDESC
          GOTO      ADENTY6
.
ADENTY3A  MOVE      "Generate Spool File          ",PRTDESC
          MOVE      PRTDESC,PRXDESC
          PACK      KEY5 WITH PRXCATA,PRXCODE
          DISPLAY   *P35:9,*V2LON,PRTDESC
          GOTO      ADENTY6
.
ADENTY3B  MOVE      "Cancel Spool File          ",PRTDESC
          MOVE      PRTDESC,PRXDESC
          PACK      KEY5 WITH PRXCATA,PRXCODE
          DISPLAY   *P35:9,*V2LON,PRTDESC
          GOTO      ADENTY6
.
ADENTY5
          BEEP
          KEYIN     *P10:24,*EL,"This printer does NOT exist. Tap 'ENTER'":
                    " to continue  ",ANS,*P10:24,*EL
          GOTO      OKAD2A
.
ADENTY6
          KEYIN     *P10:24,*EL,"Correct Information (":
                    *V2LON,"Y",*HOFF,*DV,SLASH,*V2LON,"N",*HOFF:
                    ") ? ",*V2LON,ANS;
.
          REP       "nNyY",ANS
          CMATCH    "N" TO ANS
          GOTO      ADENTY7 IF EQUAL
.
          CMATCH    "Y" TO ANS
          GOTO      ADENTY6A IF EQUAL
.
          BEEP
          GOTO      ADENTY6
.
ADENTY6A  CALL      WRICOD1
.
          DISPLAY   *P56:24,*V2LON,"** ADDED **"
.
ADENTY7   DISPLAY   *P30:9,*EL,*P30:11,*EL,*P1:24,*EL;
.
          GOTO      ADENTY2C
+
.------------------------------------------------------------------------------
.         Delete a code
.------------------------------------------------------------------------------
.
DEENTY    DISPLAY   *P1:3,*EF,*P30:5,*ULON,"DELETE A PRINTER",*HOFF:
                    *P10:7,"Category          : ":
                    *P10:9,"Printer           :"
.
DEENTY1   KEYIN     *P1:24,*EL,*P30:9,*EL,*P30:11,*EL,*P30:7,*EL,*V2LON,PRXCATA;
          RESET     PRXCATA
          GOTO      STARTC IF EOS
.
          ENDSET    PRXCATA
          APPEND    SP3,PRXCATA
          RESET     PRXCATA
.
          PACK      KEY5 WITH PRXCATA,SP3
          CALL      RDICOD1
          COMPARE   ZERO,OVRCD
          GOTO      DEENTY2 IF EQUAL
          DISPLAY   *P35:7,*B,*V2LON,"** Invalid Category ** ",*W2;
          GOTO      DEENTY1
.
DEENTY2   DISPLAY   *P35:7,*V2LON,PRXDESC
.
DEENTY2A  MOVE      SP3,PRXCODE
.
OKDE2A    KEYIN     *P1:24,*EL,*P30:9,*EL,*V2LON,*JR,PRXCODE;
          RESET     PRXCODE
          GOTO      DEENTY1 IF EOS
.
.  Display the description from the IBAPRTAF file
.
CNTKEYD   PACK      KEY5 WITH PRXCATA,PRXCODE
          CALL      RDICOD1
          COMPARE   ZERO,OVRCD
          GOTO      DEENTY3 IF EQUAL
.
NOTONF    DISPLAY   *P56:24,*B,*V2LON,"** Not on File **",*W2;
          GOTO      DEENTY1
.
DEENTY2B  MOVE      "Generate Spool File        ",PRXDESC
          GOTO      DEENTY3
.
DEENTY2C  MOVE      "Cancel Spool File          ",PRXDESC
          GOTO      DEENTY3
.
DEENTY3   DISPLAY   *P35:9,*V2LON,PRXDESC
          KEYIN     *P10:24,"Correct Printer (":
                    *V2LON,"Y",*HOFF,*DV,SLASH,*V2LON,"N",*HOFF:
                    *DV,SLASH,*V2LON,"C",*HOFF:
                    ") ? ",*V2LON,ANS;
.
          REP       "nNyYcC",ANS
          CMATCH    "N" TO ANS
          GOTO      DEENTY5 IF EQUAL
.
          CMATCH    "C" TO ANS
          GOTO      DEENTY IF EQUAL
.
          CMATCH    "Y" TO ANS
          GOTO      DEENTY4 IF EQUAL
.
          DISPLAY   *P10:24,*EL
          BEEP
          GOTO      DEENTY3
.
DEENTY4   KEYIN     *P1:24,"Are you sure you want to DELETE this ":
                    "Printer (":
                    *V2LON,"Y",*HOFF,*DV,SLASH,*V2LON,"N",*HOFF:
                    ") ? ",*V2LON,ANS;
.
          REP       "nNyY",ANS
          CMATCH    "N" TO ANS
          GOTO      DEENTY5 IF EQUAL
.
          CMATCH    "Y" TO ANS
          GOTO      DELCDC IF EQUAL
          BEEP
          GOTO      DEENTY4
.
DELCDC    PACK      KEY5 WITH PRXCATA,PRXCODE
          CALL      RDICOD1
          BRANCH    OVRCD,NOTONF
          CALL      DEICOD1
.
ENDDC5    DISPLAY   *P56:24,*V2LON,"**  DELETED  **"
.
DEENTY5   DISPLAY   *P30:9,*EL,*P30:11,*EL,*P1:24,*EL;
          GOTO      DEENTY2A
+
.------------------------------------------------------------------------------
.       Codes Master Register Listing ( Modified from IBAPAT51 ) 
.       Added 23/4/87  E. Phan
.------------------------------------------------------------------------------
PRNT0000  MOVE      ZERO,PAGENO
.
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Number Sequence"
.
PRNT0100  KEYIN     *P1:7,*EL,"Option : ",*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      START IF EQUAL
.
          BRANCH    OPTION OF PCOD0000
.
          DISPLAY   *P35:7,*EL,*B,*V2LON,"** Invalid Selection **",*W2;
          GOTO      PRNT0100
+
.******************************************************************************
.* PCOD0000
.* Print via codes number
.******************************************************************************
PCOD0000  MOVE      SP5,KEY5
          MOVE      ZERO,LNO
          MOVE      SP3,SPRXCATA
          CALL      RDSICOD1
PCOD1000  CALL      RDKICOD1
          BRANCH    OVRCD,PCOD9999
.
          MATCH     SPRXCATA,PRXCATA
          IF        @EQUAL
            CALL      PDAT0000               * Print Actual Data
            DISPLAY   *P20:24,*EL,"Code Number : ",*V2LON,PRXCODE
          ELSE
            MOVE      PRXDESC,CDESC
            DISPLAY   *P20:24,*EL,"Category : ",*V2LON,PRXCATA,"  ",PRXDESC
            CALL      HEAD0000
            MOVE      PRXCATA,SPRXCATA       * Save Current Category
            MATCH     SP3,PRXCODE
            CALL      PDAT0000 IF NOT EQUAL
          ENDIF
          GOTO      PCOD1000
.
.-------  Print last line and display message
.
PCOD9999  COMPARE   ZERO,PAGENO
          IF        @EQUAL
            DISPLAY   *P20:24,*B,*EL,*V2LON:
                      "** No Categories on File **",*W2;
          ELSE
            CALL      PAGEND
            PRINT     *N,"  ***  END OF REPORT  ***"
            DISPLAY   *P40:24,*EL,*V2LON:
                      "** Report Generation Completed **"
          ENDIF
          GOTO      START
+
.******************************************************************************
.* PDAT0000
.* Print actual data
.******************************************************************************
PDAT0000  COMPARE   "55",LNO
          CALL      HEAD0000 IF NOT LESS
.
          PRINT     *1,"!  ",PRXCODE,*14,"!  ",PRXDESC,*51,"! "
          ADD       ONE,LNO
PDAT9999  RETURN
+
.******************************************************************************
.* PAGEND
.* Last line on the page
.******************************************************************************
PAGEND    PRINT     *1,"*-------------------------------------------------*"
          RETURN
+
.******************************************************************************
.* HEAD0000
.* Headings for output
.******************************************************************************
HEAD0000  ADD       ONE,PAGENO
.
          COMPARE   ONE,PAGENO
          CALL      PAGEND IF NOT EQUAL
          MOVE      ONE,CNOUNDLN
          CALL      IBACLOCK
          CALL      HEAD80
.
          PRINT     *N,*20,"*** PRINTER CODES REGISTER LISTING ***":
                    *N,*N,*1,"Category  : ",PRXCATA,"  ",CDESC,*N:
                    *N,*1,"*--------------------------":
                    "-----------------------*":
                    *N,*1,"!  Code":
                    *14,"!  Description",*51,"!":
                    *N,*1,"*--------------------------":
                    "-----------------------*"
.
          MOVE      SIX,LNO
HEAD9999  RETURN
+
.------------------------------------------------------------------------------
.        Sub menus
.------------------------------------------------------------------------------
.
DISPSBCL  DISPLAY   *P50:18,*EL,*P50:19,*EL,*P50:20,*EL,*P50:21,*EL:
                    *P50:22,*EL,*P50:23,*EL,*P50:24,*EL;
          RETURN
.
.------------------------------------------------------------------------------
.         Save variables
.------------------------------------------------------------------------------
.
ACTBSAV   MOVE      PRXCATA,SPRXCATA
          MOVE      PRXCODE,SPRXCODE
          MOVE      PRXDESC,SPRXDESC
          RETURN
+
.------------------------------------------------------------------------------
.         Switch on/off the printer system
.------------------------------------------------------------------------------
.
ONOFFI    COMPARE   NINE,SECLEV
          GOTO      DSPONOFF IF EQUAL
.
          DISPLAY   *P1:24,*EL,*V2LON:
                    "*** Clearance Level Inadequate ***",*W2;
          GOTO      START
.
.        Display the current setting of the printer system indicator
.
DSPONOFF  MOVE      DANSON,DANS
          LOAD      DANS USING PRTFLAG OF DANSOFF
.
          DISPLAY   *P1:12,*EL,"Printer System Currently ",*V2LON,DANS
.
ONOFFKY   KEYIN     *P1:24,*EL,"Do you want to alter this (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ? ":
                    *V2LON,ANS;
.
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
.
          REP       "nNyY",ANS    
          CMATCH    ANSY,ANS
          GOTO      CHGIND IF EQUAL
.
          CMATCH    ANSN,ANS
          GOTO      START IF EQUAL
.
          BEEP
          GOTO      ONOFFKY
.
CHGIND    MOVE      DANSOFF,DANS
          LOAD      DANS USING PRTFLAG OF DANSON
.
          KEYIN     *P1:24,*EL,"Are you really sure you want to switch ":
                    "the Printer System ",*V2LON,*DV,DANS,*HOFF," (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ? ":
                    *V2LON,ANS;
.
          REP       "nNyY",ANS
          CMATCH    "Y",ANS
          GOTO      PSTIND IF EQUAL
.
          CMATCH    "N",ANS
          GOTO      START IF EQUAL
.
          BEEP
          GOTO      CHGIND
.
.        Post the change
.
PSTIND    MOVE      ONE,FORM1
          LOAD      FORM1 USING PRTFLAG OF ZERO
.
          MOVE      FORM1,PRTFLAG
          WRITAB    CONTROLF,TWO;*249,PRTFLAG
.
          DISPLAY   *P1:24,*EL,*P40:24,*V2LON:
                    "** Change Posted **"
          GOTO      START
.
+
.------------------------------------------------------------------------------
.         Code Maintenance
.------------------------------------------------------------------------------
.
ADCODE    DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Insert a code":
                    *P1:6,*V2LON,TWO,*HOFF," = Change a code":
                    *P1:7,*V2LON,THREE,*HOFF," = Delete a code"
.
ADCODE1   KEYIN     *P1:9,*EL,"Option : ",*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      START IF EQUAL
.
ADCODE3   BRANCH    OPTION OF ADCODE10,ADCODE20,ADCODE30
          BEEP
          GOTO      ADCODE1
+
.
.------------------------------------------------------------------------------
.       Add a code
.------------------------------------------------------------------------------
.
ADCODE10  DISPLAY   *P1:3,*EF,*P30:5,*ULON,"ADD A CODE",*HOFF:
                    *P10:7,"Category          : ":
                    *P10:9,"Code              :":
                    *P10:11,"Description       :"
.
ADCODE11  KEYIN     *P1:24,*EL,*P30:11,*EL,*P30:9,*EL,*P30:7,*EL,*V2LON,PRXCATA;
          RESET     PRXCATA
          GOTO      ADCODE IF EOS
.
          ENDSET    PRXCATA
          APPEND    SP3,PRXCATA
          RESET     PRXCATA
.
          PACK      KEY5 WITH PRXCATA,SP3
          CALL      RDICOD1
          COMPARE   ZERO,OVRCD
          GOTO      ADCODE12 IF EQUAL
          DISPLAY   *P35:7,*B,*V2LON,"** Invalid Category ** ",*W2;
          GOTO      ADCODE11
.
ADCODE12  DISPLAY   *P35:7,*V2LON,PRXDESC
.
ADCODE13  MOVE      SP3,PRXCODE
.
ADCODE14  KEYIN     *P1:24,*EL,*P30:9,*EL,*V2LON,PRXCODE;
          RESET     PRXCODE
.          GOTO      ADCODE11 IF EOS
.
          ENDSET    PRXCODE
          APPEND    SP3,PRXCODE
          RESET     PRXCODE
.
ADCODE15  PACK      KEY5 WITH PRXCATA,PRXCODE
          CALL      RDICOD1
          BRANCH    OVRCD OF ADCODE16
.
          DISPLAY   *P56:24,*B,*V2LON,"** Already on File **",*W2;
          GOTO      ADCODE13
.
.  Keyin   the description
.
ADCODE16  MOVE      SP30,PRXDESC
          KEYIN     *P30:11,*EL,*V2LON,PRXDESC
          ENDSET    PRXDESC
          APPEND    SP30,PRXDESC
          RESET     PRXDESC
.
ADCODE17  KEYIN     *P10:24,*EL,"Correct Information (":
                    *V2LON,"Y",*HOFF,*DV,SLASH,*V2LON,"N",*HOFF:
                    ") ? ",*V2LON,ANS;
.
          REP       "nNyY",ANS    
          CMATCH    "N" TO ANS
          GOTO      ADCODE19 IF EQUAL
.
          CMATCH    "Y" TO ANS
          GOTO      ADCODE18 IF EQUAL
.
          BEEP
          GOTO      ADCODE17
.
ADCODE18  CALL      WRICOD1
.
          DISPLAY   *P56:24,*V2LON,"** ADDED **",*W2;
.
ADCODE19  DISPLAY   *P30:9,*EL,*P30:11,*EL,*P1:24,*EL;
.
          GOTO      ADCODE13
+
.------------------------------------------------------------------------------
.       Modify a code
.------------------------------------------------------------------------------
.
ADCODE20  DISPLAY   *P1:3,*EF,*P30:5,*ULON,"MODIFY CODE DETAILS",*HOFF:
                    *P10:7,"Category          : ":
                    *P10:9,"Code              :":
                    *P10:11,"Description       :":
                    *P10:13,"New  Description  :"
.
MOENTY1   KEYIN     *P1:24,*EL,*P30:9,*EL,*P30:11,*EL,*P30:7,*EL,*V2LON,PRXCATA;
          RESET     PRXCATA
          GOTO      ADCODE IF EOS
.
          ENDSET    PRXCATA
          APPEND    SP3,PRXCATA
          RESET     PRXCATA
.
          PACK      KEY5 WITH PRXCATA,SP3
          CALL      RDICOD1
          COMPARE   ZERO,OVRCD
          GOTO      MOENTY2 IF EQUAL
          DISPLAY   *P35:7,*B,*V2LON,"** Invalid Category ** ",*W2;
          GOTO      MOENTY1
.
MOENTY2   DISPLAY   *P35:7,*V2LON,PRXDESC
.
.------------------------------------------------------------------------------
.       Make sure devices are only accessed by security level 9 only
.------------------------------------------------------------------------------
.
MOENTY2A  MOVE      SP3,PRXCODE
          MATCH     "DV",PRXCATA
          GOTO      MOENTDV IF EQUAL
.
          MATCH     "PT",PRXCATA
          GOTO      OKMO2A IF NOT EQUAL
.
.      PAPER TYPES MUST BE FORM 2
.
          KEYIN     *P1:24,*EL,*P30:9,*EL,*V2LON,FORM2
          PACK      PRXCODE WITH FORM2,SP3
          GOTO      CNTKEYM
.
OKMO2A    KEYIN     *P1:24,*EL,*P30:9,*EL,*V2LON,PRXCODE;
          RESET     PRXCODE
          GOTO      MOENTY1 IF EOS
.
          ENDSET    PRXCODE
          APPEND    SP3,PRXCODE
          RESET     PRXCODE
.
CNTKEYM   PACK      KEY5 WITH PRXCATA,PRXCODE
          CALL      RDICOD1
          COMPARE   ZERO,OVRCD
          GOTO      MOENTY3 IF EQUAL
.
          DISPLAY   *P56:24,*B,*V2LON,"** Not on File **",*W2;
          GOTO      MOENTY1
.
MOENTDV   COMPARE   NINE,SECLEV
          GOTO      OKMO2A IF EQUAL
.
          DISPLAY   *P1:24,*EL,*V2LON:
                    "*** Clearance Level Inadequate ***",*W2;
          GOTO      MOENTY1
.
MOENTY3   DISPLAY   *P30:11,*V2LON,PRXDESC
          KEYIN     *P50:9,"Correct Code (":
                    *V2LON,"Y",*HOFF,*DV,SLASH,*V2LON,"N",*HOFF:
                    *DV,SLASH,*V2LON,"C",*HOFF:
                    ") ? ",*V2LON,ANS;
.
          REP       "nNyYcC",ANS
          CMATCH    "N" TO ANS
          GOTO      MOENTY5 IF EQUAL
.
          CMATCH    "C" TO ANS
          GOTO      ADCODE IF EQUAL
.
          CMATCH    "Y" TO ANS
          GOTO      MOENTY3A IF EQUAL
.
          BEEP
          GOTO      MOENTY3
.
MOENTY3A  CALL      ACTBSAV
.
          DISPLAY   *P50:9,*EL
.
DESREP    KEYIN     *P30:13,*EL,*P30:13,*V2LON,NEWDESC
.
          ENDSET    NEWDESC
          APPEND    SP30,NEWDESC
          RESET     NEWDESC
.
KEYMOC2   KEYIN     *P10:24,*EL,"New Description O.K. (":
                    *V2LON,"Y",*HOFF,*DV,SLASH,*V2LON,"N",*HOFF:
                    ") ? ",*V2LON,ANS;
.
          REP       "nNyY",ANS
          CMATCH    "N" TO ANS
          GOTO      DESREP IF EQUAL
.
          CMATCH    "Y" TO ANS
          GOTO      UPMOC IF EQUAL
          BEEP
          GOTO      KEYMOC2
.
UPMOC     MOVE      NEWDESC,PRXDESC
          CALL      UPICOD1
.
ENDCC5    DISPLAY   *P56:24,*V2LON,"** MODIFIED **";
.
MOENTY5   DISPLAY   *P30:9,*EL,*P30:11,*EL,*P30:13,*EL,*P1:24,*EL;
          GOTO      MOENTY2A
+
.------------------------------------------------------------------------------
.         Delete a code
.------------------------------------------------------------------------------
.
ADCODE30  DISPLAY   *P1:3,*EF,*P30:5,*ULON,"DELETE A CODE",*HOFF:
                    *P10:7,"Category          : ":
                    *P10:9,"Code              :":
                    *P10:11,"Description       :"
.
ADCODE31
          KEYIN     *P1:24,*EL,*P30:9,*EL,*P30:11,*EL,*P30:7,*EL,*V2LON,PRXCATA;
          RESET     PRXCATA
          GOTO      ADCODE IF EOS
.
          ENDSET    PRXCATA
          APPEND    SP3,PRXCATA
          RESET     PRXCATA
.
          PACK      KEY5 WITH PRXCATA,SP3
          CALL      RDICOD1
          COMPARE   ZERO,OVRCD
          GOTO      ADCODE32 IF EQUAL
          DISPLAY   *P35:7,*B,*V2LON,"** Invalid Category ** ",*W2;
          GOTO      ADCODE31
.
ADCODE32  DISPLAY   *P35:7,*V2LON,PRXDESC
.
ADCODE33  MOVE      SP3,PRXCODE
.
ADCODE34  KEYIN     *P1:24,*EL,*P30:9,*EL,*V2LON,PRXCODE;
          RESET     PRXCODE
          GOTO      ADCODE31 IF EOS
.
          ENDSET    PRXCODE
          APPEND    SP3,PRXCODE
          RESET     PRXCODE
.
ADCODE35  PACK      KEY5 WITH PRXCATA,PRXCODE
          CALL      RDICOD1
          COMPARE   ZERO,OVRCD
          GOTO      ADCODE36 IF EQUAL
.
          DISPLAY   *P56:24,*B,*V2LON,"** Not on File **",*W2;
          GOTO      ADCODE31
.
ADCODE36  DISPLAY   *P30:11,*V2LON,PRXDESC
          KEYIN     *P50:9,"Correct Code (":
                    *V2LON,"Y",*HOFF,*DV,SLASH,*V2LON,"N",*HOFF:
                    *DV,SLASH,*V2LON,"C",*HOFF:
                    ") ? ",*V2LON,ANS;
.
          REP       "nNyYcC",ANS
          CMATCH    "N" TO ANS
          GOTO      ADCODE40 IF EQUAL
.
          CMATCH    "C" TO ANS
          GOTO      ADCODE30 IF EQUAL
.
          CMATCH    "Y" TO ANS
          GOTO      DCODE36A IF EQUAL
.
          BEEP
          GOTO      ADCODE36
.
DCODE36A  DISPLAY   *P55:9,*EL
.
ADCODE37  KEYIN     *P1:24,"Are you sure you want to DELETE this ":
                    "Code (":
                    *V2LON,"Y",*HOFF,*DV,SLASH,*V2LON,"N",*HOFF:
                    ") ? ",*V2LON,ANS;
.
          REP       "nNyY",ANS
          CMATCH    "N" TO ANS
          GOTO      ADCODE40 IF EQUAL
.
          CMATCH    "Y" TO ANS
          GOTO      ADCODE38 IF EQUAL
          BEEP
          GOTO      ADCODE37
.
ADCODE38  CALL      DEICOD1
.
ADCODE39  DISPLAY   *P56:24,*V2LON,"**  DELETED  **",*W2;
.
ADCODE40  DISPLAY   *P30:9,*EL,*P30:11,*EL,*P1:24,*EL;
          GOTO      ADCODE33
+
.------------------------------------------------------------------------------
.         Category Maintenance
.------------------------------------------------------------------------------
.
ACCODE    DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Insert a category":
                    *P1:6,*V2LON,TWO,*HOFF," = Change a category":
                    *P1:7,*V2LON,THREE,*HOFF," = Delete a category"
.
ACCODE1   KEYIN     *P1:9,*EL,"Option : ",*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      START IF EQUAL
.
ACCODE3   BRANCH    OPTION OF ACCODE10,ACCODE20,ACCODE30
          BEEP
          GOTO      ACCODE1
.
.------------------------------------------------------------------------------
.       Add a category 
.------------------------------------------------------------------------------
.
ACCODE10  DISPLAY   *P1:3,*EF,*P30:5,*ULON,"ADD A CATEGORY",*HOFF:
                    *P10:7,"Category          : ":
                    *P10:9,"Description       :"
.
ACCODE11  KEYIN     *P1:24,*EL,*P30:11,*EL,*P30:9,*EL,*P30:7,*EL,*V2LON,PRXCATA;
          RESET     PRXCATA
          GOTO      ACCODE IF EOS
.
          ENDSET    PRXCATA
          APPEND    SP3,PRXCATA
          RESET     PRXCATA
.
          PACK      KEY5 WITH PRXCATA,SP3
          CALL      RDICOD1
          COMPARE   ONE,OVRCD
          GOTO      ACCODE13 IF EQUAL
          DISPLAY   *P35:7,*B,*V2LON,"** Category Already Exists ** ",*W2;
          GOTO      ACCODE11
.
ACCODE13  MOVE      SP3,PRXCODE
.
.  Keyin   the description
.
ACCODE16  MOVE      SP30,PRXDESC
          KEYIN     *P30:9,*EL,*V2LON,PRXDESC
          ENDSET    PRXDESC
          APPEND    SP30,PRXDESC
          RESET     PRXDESC
.
ACCODE17  KEYIN     *P10:24,*EL,"Correct Information (":
                    *V2LON,"Y",*HOFF,*DV,SLASH,*V2LON,"N",*HOFF:
                    ") ? ",*V2LON,ANS;
.
          REP       "nNyY",ANS
          CMATCH    "N" TO ANS
          GOTO      ACCODE19 IF EQUAL
.
          CMATCH    "Y" TO ANS
          GOTO      ACCODE18 IF EQUAL
.
          BEEP
          GOTO      ACCODE17 
.
ACCODE18  CALL      WRICOD1
.
          DISPLAY   *P56:24,*V2LON,"** ADDED **",*W2;
.
ACCODE19  DISPLAY   *P30:9,*EL,*P30:11,*EL,*P1:24,*EL;
.
          GOTO      ACCODE11
.
.------------------------------------------------------------------------------
.       Modify a category description
.------------------------------------------------------------------------------
.
ACCODE20  DISPLAY   *P1:3,*EF,*P30:5,*ULON,"MODIFY CATEGORY DESCRIPTION",*HOFF:
                    *P10:7,"Category          : ":
                    *P10:9,"Description       :":
                    *P10:11,"New  Description  :"
.
MOENTC1   KEYIN     *P1:24,*EL,*P30:9,*EL,*P30:11,*EL,*P30:7,*EL,*V2LON,PRXCATA;
          RESET     PRXCATA
          GOTO      ACCODE IF EOS
.
          ENDSET    PRXCATA
          APPEND    SP3,PRXCATA
          RESET     PRXCATA
.
          PACK      KEY5 WITH PRXCATA,SP3
          CALL      RDICOD1
          COMPARE   ZERO,OVRCD
          GOTO      MOENTC2 IF EQUAL
          DISPLAY   *P35:7,*B,*V2LON,"** Invalid Category ** ",*W2;
          GOTO      MOENTC1 
.
MOENTC2   DISPLAY   *P30:9,*V2LON,PRXDESC
.
DESREPC   KEYIN     *P30:11,*EL,*P30:11,*V2LON,NEWDESC
.
          ENDSET    NEWDESC
          APPEND    SP30,NEWDESC
          RESET     NEWDESC
.
KEYMCC2   KEYIN     *P10:24,*EL,"New Description O.K. (":
                    *V2LON,"C",*HOFF,*DV,SLASH,"Y",*HOFF,*DV,SLASH,*V2LON,"N": 
                    *HOFF,") ? ",*V2LON,ANS;
.
          REP       "nNyYcC",ANS
          CMATCH    "N" TO ANS
          GOTO      DESREPC IF EQUAL
.
          CMATCH    "Y" TO ANS
          GOTO      UPMCCC IF EQUAL
.
          CMATCH    "C" TO ANS
          GOTO      ACCODE IF EQUAL
.
          BEEP
          GOTO      KEYMCC2
.
UPMCCC    MOVE      NEWDESC,PRXDESC
          CALL      UPICOD1
.
ENDCCC5   DISPLAY   *P56:24,*V2LON,"** MODIFIED **";
.
          DISPLAY   *P30:9,*EL,*P30:11,*EL,*P30:13,*EL,*P1:24,*EL;
          GOTO      MOENTC1 
+
.------------------------------------------------------------------------------
.         Delete a code
.------------------------------------------------------------------------------
.
ACCODE30  DISPLAY   *P1:3,*EF,*P30:5,*ULON,"DELETE A CATEGORY",*HOFF:
                    *P10:7,"Category          : ":
                    *P10:9,"Description       :"
.
ACCODE31
          KEYIN     *P1:24,*EL,*P30:9,*EL,*P30:11,*EL,*P30:7,*EL,*V2LON,PRXCATA;
          RESET     PRXCATA
          GOTO      ACCODE IF EOS
.
          ENDSET    PRXCATA
          APPEND    SP3,PRXCATA
          RESET     PRXCATA
.
          PACK      KEY5 WITH PRXCATA,SP3
          CALL      RDICOD1
          COMPARE   ZERO,OVRCD
          GOTO      ACCODE32 IF EQUAL
          DISPLAY   *P30:9,*B,*V2LON,"** Invalid Category ** ",*W2;
          GOTO      ACCODE31
.
ACCODE32  DISPLAY   *P30:9,*V2LON,PRXDESC
.
          MOVE      SP3,PRXCODE
.
ACCODE36  KEYIN     *P50:9,"Correct Category (":
                    *V2LON,"Y",*HOFF,*DV,SLASH,*V2LON,"N",*HOFF:
                    *DV,SLASH,*V2LON,"C",*HOFF:
                    ") ? ",*V2LON,ANS;
.
          REP       "nNyYcC",ANS
          CMATCH    "N" TO ANS
          GOTO      ACCODE40 IF EQUAL
.
          CMATCH    "C" TO ANS
          GOTO      ACCODE IF EQUAL
.
          CMATCH    "Y" TO ANS
          GOTO      ACCODE37 IF EQUAL
.
          BEEP
          GOTO      ACCODE36
.
ACCODE37  MOVE      PRXCATA,SAVCATA
          CALL      RDKICOD1
.
          MATCH     PRXCATA,SAVCATA
          GOTO      NODEL1 IF EQUAL
.
          GOTO      DELIT1
.
NODEL1    DISPLAY   *P1:24,*EL,*B,"Cannot delete - has codes attached. ";
          CALL      HITENTER
          GOTO      ACCODE30 
.           
DELIT1    DISPLAY   *P55:9,*EL
.
          KEYIN     *P1:24,"Are you sure you want to DELETE this ":
                    "Category (":
                    *V2LON,"Y",*HOFF,*DV,SLASH,*V2LON,"N",*HOFF:
                    ") ? ",*V2LON,ANS;
.
          REP       "nNyY",ANS
          CMATCH    "N" TO ANS
          GOTO      ACCODE40 IF EQUAL
.
          CMATCH    "Y" TO ANS
          GOTO      ACCODE38 IF EQUAL
.
          BEEP
          GOTO      ACCODE37
.
ACCODE38  CALL      DEICOD1
.
ACCODE39  DISPLAY   *P56:24,*V2LON,"**  DELETED  **",*W2;
.
ACCODE40  DISPLAY   *P30:9,*EL,*P30:11,*EL,*P1:24,*EL;
          GOTO      ACCODE30
+
.****************************************************************************
.*                         DPDS0000                CALLED BY:               *
.*            Display a list of department associated printers              *
.*        								    *
.****************************************************************************
DPDS0000  DISPLAY   *P1:3,*EF
.
DPDS1000  DISPLAY   *P12:4,*V2LON,"Department : ";
.
          MOVE      SP2,ANS2
          KEYIN     *P26:4,ANS2
.
          MATCH     SP2,ANS2
          GOTO      START IF EQUAL            * exit if empty
          GOTO      START IF EOS
.
          MATCH     "? ",ANS2
          GOTO      DSDP8000 IF EQUAL         * enquiry ?    
.
          CALL      DSPR0000                  * goto display
          BRANCH    EXIT,START,DPDS0000
          GOTO      DSDP9999         
.
DSDP8000  CALL      DSEQ0000                  * goto enquiry
          BRANCH    EXIT,START,DPDS1000
.      
DSDP9999  GOTO      START
+
.****************************************************************************
.*                         PRTA0000                CALLED BY:               *
.*          Display a list of departments associated with a certain printer *
.*                                                                          *
.****************************************************************************
PRTA0000  DISPLAY   *P1:3,*EF
.
PRTA1000  DISPLAY   *P1:3,*EL:
                    *P12:4,*V2LON,"Printer    : ";
.
          MOVE      SP3,ANS3
          KEYIN     *P26:4,*JR,ANS3
.
          MATCH     SP3,ANS3
          GOTO      START IF EQUAL             * exit if empty
          GOTO      START IF EOS
.
          MATCH     "  ?",ANS3
          GOTO      PRTA8000 IF EQUAL          * enquiry ?
.
          CALL      PRPR0000                   * goto display
          BRANCH    EXIT,START,PRTA0000
          GOTO      PRTA9999
.
PRTA8000  CALL      PREQ0000                   * goto enquiry
          BRANCH    EXIT,START,PRTA1000
.
PRTA9999  GOTO      START
+
.------------------------------------------------------------------------------
          INC       IBAPRTIO/INC
          INC       DPRTDSPL            * department display
          INC       DPRTENQR            * department enquiry
          INC       PRNTENQR            * printer enquiry
          INC       PRNTLOCS            * printer location display
          INC       STD001IO
          INC       IBACODDS
.
ENDIT     CHAIN     PGM
          STOP
............................................................................

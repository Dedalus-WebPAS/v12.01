.******************************************************************************
.* System         : Waiting Lists                (Taranaki)                   *
.* Program        : IBAWAT52                                                  *
.* Desc.          : Booking Register Report                                   *
.******************************************************************************
.* Date           : 24/01/1994                                                *
.* Author         : Rob Leonard                                               *
.* Modifications  :                                                           *
.*                                                                            *
.******************************************************************************
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899                         *
.*           Recompiled as WATTR1FD changes                                   *
.******************************************************************************
.*       V10.05.01  01/08/2015  Ania P                      CAR 261630        *
.*                  Removed use of PORT for temp file naming                  *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V9.07.01  14/09/2006  Mike Laming   CAR 114669                      *
.*                  Change RPTMPn to RSTMPn at PRWD1000 and PRDC1000          *
.*        V9.02.01  24/09/2001  Dean Cramer                                   *
.*                  Fix overlap in printing                                   *
.*        V5.08.01  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.07.B03 12/01/1999  Nicole Harrington  507 changes Error 39       *
.*                  Fixed lining up of report                                 *
.*        V5.07.B02 23/11/1998  Nicole Harrington                             *
.*                  507 changes                                               * 
.*        V5.01.02  25/03/1994    Justin Coates                               *
.*                  added booking number by reducing length of name           *
.*        V5.01.03  26/05/1994 Ian Rutt                                       *
.*                  Fixed Global Recompile Bugs                               * 
.*        V5.01.04  09/06/1994   Rob Leonard   QUOTE 8033  SRF 600955         *
.*                  Add date range keyin. Sort by adm date/time. Column head. *
.******************************************************************************
.
          INC       STD001FD
          INC       BOKRC1FD/INC 
          INC       OPRDEAFD/INC
          INC       PATCODFD/INC                 * Codes file
          INC       PATCONFD/INC
          INC       PATDO1FD/INC                 * Doctors file
          INC       PATMA1FD/INC                 * Patients Masterfile details
          INC       PATMI1FD/INC                 * Inpatients details file
          INC       PMSVX1FD/INC                 * Inpatients details file
          INC       PATPR1FD/INC
          INC       PATTRNFD/INC                 * Transfer file          
          INC       PATWR1FD/INC                 * Ward Bed file
          INC       WATTR1FD/INC
          INC       IBASEQFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
.========================================================
.                   VARIABLES
.========================================================
CCFRDATE  DIM        8
CCTODATE  DIM        8
CMDLINE   DIM       80
DIM10     DIM       10
DIM13     DIM       13
DIM3      DIM        3
DIM28     DIM       28
DIM30     DIM       30
DOCCODE   DIM        6
FROMDOCT  DIM        6
FROMWARD  DIM        3
LASTDOCT  DIM        6
LASTWARD  DIM        3
OUTADATE  DIM        8 
OUTATIME  DIM        5
OUTBDATE  DIM        10
OUTPTIM   DIM        5
OUTRPDAT  DIM        8 
PRFRDATE  DIM       10
PRFRDOCT  DIM        6
PRFRWARD  DIM        6
PRTODATE  DIM       10
PRTODOCT  DIM        6
PRTOWARD  DIM        6
REPEND    FORM       1
TPWRDBED  DIM        7
TODOCT    DIM        6
TOWARD    DIM        3
.
FINISH    INIT       "Finish "
START     INIT       "Start  "
UNKDOCT   INIT       "Unkown Doctor "
UNKWARD   INIT       "Unkown Ward   "
Z6        INIT       "zzzzzz"
Z8        INIT       "zzzzzzzz"
.
.========================================================
.         TEMP FILE DEFINITIONS
.========================================================
TMPFL1    IFILE     SQL, FIXED=124, KEY="D1-6,7-14"
TMPFL2    IFILE     SQL, FIXED=124, KEY="D76-78,79-81,7-14"
TMPFL3    IFILE     SQL, FIXED=124, KEY="D1-6,104-111,112-119,7-14"
TMPFL4    IFILE     SQL, FIXED=124, KEY="D76-78,104-111,112-119,7-14"
.
.Name     Type    Length    Physical     Start
.----     ----    ------    --------     -----
.BKREADOC  DIM       6            6         1
.PURNO     DIM       8            8         7
TMPBOOKN   DIM       8            8        15
.WMDESC    DIM      50           50        23
TMPAANAE   DIM       3            3        73
TMPWARD    DIM       3            3        76
TMPBED     DIM       3            3        79
TMPDOCT    DIM       6            6        82
.BKREPDAT  DIM       8            8        88
.BKREPTIM  DIM       8            8        96
TMPADATE   DIM       8            8       104
TMPATIME   DIM       8            8       112
TMPAEXPD   FORM      4            4       120
.
.End of Record                            124
.
PRGID     INIT      "IBAWAT52"
PRGNAM    INIT      "Booking Register"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                       MAINLINE                                     *
.**********************************************************************
ML0000    CALL      INIT0000           * initialisation
.
ML1000    CALL      CLR0000            * clear variables
          CALL      SCR0000            * select option
          BRANCH    EXIT,ML9000
.
ML1500    BRANCH    OPTION,ML2000,ML4000
.
. ------- Doctor Code Range -------
.
ML2000    CALL      DOCC0000           * keyin Doctor Code range
          BRANCH    EXIT,ML1000
          CALL      PRDC0000           * print by doct code range
          GOTO      ML1000
.
. ------- Ward Range -------
.
ML4000    CALL      WARD0000           * keyin ward/bed sequence 
          BRANCH    EXIT,ML1000
          CALL      PRWD0000           * print by ward code range
          GOTO      ML1000
.
ML9000    CALL      KILL0000           * kill temp file
.
ML9999    CHAIN     PGM
+
.**********************************************************************
.*                       INIT0000                Called by : ML0000   *
.*                    Initialisation                                  *
.**********************************************************************
INIT0000  CALL      DISPHEAD
          CALL      TFILENAM
.
          DISPLAY   *P56:24,"Opening patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A2,"patmi1af"
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATWR1A2,"patwr1af"
          OPEN      PATWR1A3,"patwr1af"
          DISPLAY   *P64:24,"patpramf"
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          DISPLAY   *P64:24,"bokrc1af"
          OPEN      BOKRC1A1,"bokrc1af"
          OPEN      BOKRC1A3,"bokrc1af"
          DISPLAY   *P64:24,"wattr1af"
          OPEN      WATTR1A1,"wattr1af"
          OPEN      WATTR1A2,"wattr1af"
          DISPLAY   *P64:24,"oprdetaf"
          OPEN      OPRDETA2,"oprdetaf"
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          MOVE      ZERO,CKYIMAND 
          MOVE      ONE,CNEWDS 
.
INIT9999  RETURN
+
.**********************************************************************
.*                       CLR0000                 Called by : ML0000   *
.*                    Clear Variables      DOCC0000 PRDC0000 PRWD0000 *
.**********************************************************************
CLR0000   MOVE      SP20,PSNAME
          MOVE      SP1,PSEX
          MOVE      SP8,PBDATE
          PACK      WMDESC,SP20,SP20,SP20
          MOVE      SP3,TMPAANAE
          MOVE      SP3,TMPWARD
          MOVE      SP3,TMPBED
          MOVE      SP6,TMPDOCT
          MOVE      SP8,BKREPDAT
          MOVE      SP8,BKREPTIM
          MOVE      SP8,BKREEDAT 
          MOVE      SP8,TMPATIME  
          MOVE      ZERO,TMPAEXPD 
. 
CLR9999   RETURN
+
.**********************************************************************
.*                       SCR0000                 Called by : ML0000   *
.*                    Select Option                                   *
.**********************************************************************
SCR0000   MOVE      FALSE,EXIT
          HLINE     *G37,2,48,80
          DISPLAY   *P1:3,*EF: 
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Doctor Code Range":
                    *P1:6,*V2LON,TWO,*HOFF," = Ward Range":
                    *P1:8,"Select Option :";
.
SCR1000   KEYIN     *P17:8,*EL,*DV,UNDLN1,*P17:8,*V2LON,OPTION
.
          COMPARE   ZERO,OPTION
          GOTO      SCR9000 IF EQUAL
.
          BRANCH    OPTION,SCR7000,SCR8000
          BEEP
          GOTO      SCR1000
.
SCR7000   DISPLAY   *P48:2,*V2LON,"- Doctor Code Range "
          GOTO      SCR9999
.
SCR8000   DISPLAY   *P48:2,*V2LON,"- Ward Range "
          GOTO      SCR9999
.
SCR9000   MOVE      TRUE,EXIT
.
SCR9999   RETURN
+
.**********************************************************************
.*                       DOCC0000                Called by : ML0000   *
.*          Keyin Doctor Code Range and write to temp file            *
.**********************************************************************
DOCC0000  MOVE      FALSE,EXIT
          DISPLAY   *P1:11,*EF,*P1:11,"From Doctor Code : ":
                    *P1:12,"To   Doctor Code : "
.
DOCC1000  MOVE      TWENTY,ECOL                  * From Doctor code
          MOVE      TEN1,EVERT
          MOVE      SP6,CKYIDEF6
          DISPLAY   *P20:EVERT,*EL
          CALL      PATDOCKY
          MOVE      DOCCODE,FROMDOCT
          BRANCH    EXIT,DOCC1500,DOCC9000
.
          MOVE      FROMDOCT,PRFRDOCT
          CALL      FDCN0000                * format doctors name
          DISPLAY   *P30:EVERT,KEY40
          GOTO      DOCC2000
.
DOCC1500  DISPLAY   *P20:EVERT,*V2LON,START
          MOVE      START,PRFRDOCT
          MOVE      SP6,FROMDOCT
.
DOCC2000  MOVE      TEN2,EVERT                   * To Doctor code
          DISPLAY   *P20:EVERT,*EL
          MOVE      FROMDOCT,CKYIDEF6
          CALL      PATDOCKY
          MOVE      DOCCODE,TODOCT
          BRANCH    EXIT,DOCC2500,DOCC9000
.
          MOVE      TODOCT,PRTODOCT
          CALL      FDCN0000                * format doctors name
          DISPLAY   *P30:EVERT,KEY40
          GOTO      DOCC2700
.
DOCC2500  DISPLAY   *P20:EVERT,*V2LON,FINISH
          MOVE      FINISH,PRTODOCT
          MOVE      Z6,TODOCT
.
DOCC2700  MOVE      FALSE,EXIT                  * reset exit flag
          MATCH     FROMDOCT,TODOCT
          GOTO      DOCC8000 IF LESS
.
          CALL      KDAT0000                     * keyin date range
.
          CALL      CONTQST                      * Continue ?
          BRANCH    CEXIT,DOCC3000,DOCC0000,DOCC9000
.
.-------- obtain the data --------
.
DOCC3000  DISPLAY   *P1:24,*EL,"Obtaining data ... ";
          CALL      CTMP0000                     * create temporary file
          PACK      KEY22,FROMDOCT,SP10,SP6
          CALL      RSBKREC3
.
DOCC4000  CALL      RKBKREC3
          BRANCH    OVRCD,DOCC9999
          MATCH     BKREADOC,TODOCT
          GOTO      DOCC9999 IF LESS        * Doctor > To Doctor
.
          MATCH     SP6,BKREADOC
          GOTO      DOCC4000 IF EQUAL       * no doctor code
.
          MATCH     CCFRDATE,BKREEDAT       * check from date range
          GOTO      DOCC4000 IF LESS
.
          MATCH     BKREEDAT,CCTODATE       * check to date range
          GOTO      DOCC4000 IF LESS
.
. ------- The patient must be Booked or Pre-Admitted ------------
.
          DISPLAY   *P40:24,BKREADOC,SP1,BKREBOOK
          IF        !(BKRESTAT=ZERO | BKRESTAT=ONE)
            GOTO      DOCC4000
          ENDIF
.
          MOVE      BKREURNO,PURNO
          MOVE      BKREBOOK,TMPBOOKN
          MOVE      BKREADOC,TMPDOCT        * Move to temp file vars
          MOVE      BKREEDAT,TMPADATE
          MOVE      BKREATIM,TMPATIME
          MOVE      BKREWARD,TMPWARD
          MOVE      BKREEBED,TMPBED 
.
          BRANCH    BKRESTAT,DOCC5000       * Pre-Admitted
          GOTO      DOCC6000                * booked
.
.-------- If Pre-Admitted obtain current info from Admission file if not blank
.
DOCC5000  MOVE      BKREBOOK,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,DOCC6000          * not pre-admitted
.
          MATCH     SP3,AWARD
          GOTO      DOCC5100 IF EQUAL       * Blank field 
          MOVE      AWARD,TMPWARD
          MOVE      ABED,TMPBED
.          
DOCC5100  PACK      ADATE,ADATE,SP8
          MATCH     SP8,ADATE
          GOTO      DOCC5200 IF EQUAL       * Blank field
          PACK      TMPADATE,ADATE
.
DOCC5200  PACK      ATIME,ATIME,SP8
          MATCH     SP6,ATIME
          GOTO      DOCC6000 IF EQUAL       * Blank field
          MOVE      ATIME,TMPATIME
.
DOCC6000  
          PACK      KEY19,PURNO,BKREPROC,BKRECNT
          CALL      RDTREA1                 * Get Proc Desc from WATTR1FD
.
          PACK      KEY31,DBKREBOO,SP20,SP3
          CALL      RSOPDEA2                * Get Anaesthetic type and oprtn
          CALL      RKOPDEA2                * duration from OPRDEAFD
          BRANCH    OVRCD,DOCC7000
          MATCH     DBKREBOO,OPDAADMN       * check correct adm no.  
          GOTO      DOCC7000 IF NOT EQUAL
.
          MOVE      OPDAANAE,TMPAANAE
          MOVE      OPDAEXPD,TMPAEXPD
.
DOCC7000  PACK      KEY14,BKREADOC,PURNO
          CALL      WRTTMP1                 * Write to temp file
          CALL      CLR0000
          GOTO      DOCC4000                * Get next record
.
DOCC8000  DISPLAY   *P1:24,*B,"Invalid Range.  ",*EL;
          CALL      HITENTER
          GOTO      DOCC2000
.
DOCC9000  MOVE      TRUE,EXIT
.
DOCC9999  RETURN
+
.**********************************************************************
.*                       WARD0000                Called by : ML0000   *
.*          Keyin Ward Range and write to temp file                   *
.**********************************************************************
WARD0000  MOVE      FALSE,EXIT
          DISPLAY   *P1:11,*EF,*P1:11,"From Ward : ":
                    *P1:12,"To   Ward : "
.
WARD1000  MOVE      TEN1,EVERT                   * From Ward
          MOVE      TEN3,ECOL
          MOVE      SP3,CKYIDEF3
          DISPLAY   *P11:EVERT,*EL
          CALL      PATWRDKY
          MOVE      WWARD,FROMWARD
          BRANCH    EXIT,WARD1500,WARD9000
          MOVE      FROMWARD,PRFRWARD
          DISPLAY   *P20:EVERT,WBDESC
          GOTO      WARD2000
.
WARD1500  DISPLAY   *P13:EVERT,*V2LON,START
          MOVE      START,PRFRWARD
          MOVE      SP3,FROMWARD
.
WARD2000  MOVE      TEN2,EVERT                   * To Ward
          MOVE      FROMWARD,CKYIDEF3
          DISPLAY   *P13:EVERT,*EL
          CALL      PATWRDKY
          MOVE      WWARD,TOWARD
          BRANCH    EXIT,WARD2500,WARD9000
          MOVE      TOWARD,PRTOWARD
          DISPLAY   *P20:EVERT,WBDESC
          GOTO      WARD2700
.
WARD2500  DISPLAY   *P13:EVERT,*V2LON,FINISH
          MOVE      FINISH,PRTOWARD
          MOVE      Z6,TOWARD
.
WARD2700  MOVE      FALSE,EXIT              * reset exit flag
          MATCH     FROMWARD,TOWARD
          GOTO      WARD8000 IF LESS
.
          CALL      KDAT0000                     * keyin date range
.
          CALL      CONTQST
          BRANCH    CEXIT,WARD3000,WARD0000,WARD9000
.
WARD3000  DISPLAY   *P1:24,*EL,"Obtaining data ... ";
          CALL      CTMP0000               * create temporary file
.
.-------- Patient must have Proc Status of 2=Scheduled or 3=Pre-Admitted ---
.
          PACK      KEY20,TWO,SP20
          CALL      RDSTREA2
          CALL      RDKTREA2
          BRANCH    OVRCD,WARD9999 
.
          CALL      RDPTREA2
.
WARD4000  CALL      RDKTREA2
          BRANCH    OVRCD,WARD9999
.
          DISPLAY   *P40:24,WMSTAT,SP1,WMURNO
          BRANCH    WMSTAT,WARD4000,WARD4100,WARD4100,WARD9999,WARD9999:
                    WARD9999
          GOTO      WARD9999
.
WARD4100  MOVE      WMBOOK,KEY8
          CALL      RDBKREC1
          BRANCH    OVRCD,WARD4000
.
          MATCH     FROMWARD,BKREWARD       * Check that Ward in range
          GOTO      WARD4000 IF LESS
.
          MATCH     BKREWARD,TOWARD
          GOTO      WARD4000 IF LESS
.
          MATCH     SP3,BKREWARD
          GOTO      WARD4000 IF EQUAL       * ignore if no ward
.
          MATCH     CCFRDATE,BKREEDAT       * check from date range
          GOTO      WARD4000 IF LESS
.
          MATCH     BKREEDAT,CCTODATE       * check to date range
          GOTO      WARD4000 IF LESS
.
          MOVE      BKREURNO,PURNO
          MOVE      BKREBOOK,TMPBOOKN
          MOVE      BKREWARD,TMPWARD        * Move to temp file vars
          MOVE      BKREEBED,TMPBED
          MOVE      BKREEDAT,TMPADATE
          MOVE      BKREATIM,TMPATIME
          MOVE      BKREADOC,TMPDOCT
.
          BRANCH    BKRESTAT,WARD5000       * Pre-Admitted
          GOTO      WARD6000                * Not
.
.-------- If Pre-Admitted obtain current info from Admission file if not blank
.
WARD5000  MOVE      BKREBOOK,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,WARD6000
.
          PACK      ADOCTA,ADOCTA,SP6
          MATCH     SP3,ADOCTA
          GOTO      WARD5100 IF EQUAL       * Blank field 
          MOVE      ADOCTA,TMPDOCT
.          
WARD5100  PACK      ABED,ABED,SP3
          MATCH     SP3,ABED
          GOTO      WARD5200 IF EQUAL       * Blank field
          MOVE      ABED,TMPBED
.
WARD5200  PACK      ATIME,ATIME,SP8
          MATCH     SP8,ATIME
          GOTO      WARD5300 IF EQUAL       * Blank field
          MOVE      ATIME,TMPATIME
.
WARD5300  PACK      ADATE,ADATE,SP8
          MATCH     SP8,ADATE
          GOTO      WARD6000 IF EQUAL       * Blank field
.
WARD6000  PACK      KEY19,PURNO,BKREPROC,BKRECNT
          CALL      RDTREA1                 * Get Proc Desc from WATTR1FD
.
          PACK      KEY31,DBKREBOO,SP20,SP3
          CALL      RSOPDEA2                * Get Anaesthetic type and opr
          CALL      RKOPDEA2                * duration from OPRDEAFD
          BRANCH    OVRCD,WARD7000
          MATCH     DBKREBOO,OPDAADMN       * check correct adm no.  
          GOTO      WARD7000 IF NOT EQUAL
.
          MOVE      OPDAANAE,TMPAANAE
          MOVE      OPDAEXPD,TMPAEXPD
.
WARD7000  PACK      KEY14,TMPWARD,TMPBED,PURNO
          CALL      WRTTMP2                 * Write to temp file
          GOTO      WARD4000
.
WARD8000  DISPLAY   *P1:24,*B,"Invalid Range.  ",*EL;
          CALL      HITENTER
          GOTO      WARD2000
.
WARD9000  MOVE      TRUE,EXIT
.
WARD9999  RETURN
+
.**********************************************************************
.*                       CTMP0000                Called by : DOCC0000 *
.*                Create Temporary file                      WARD0000 *
.**********************************************************************
CTMP0000  CALL      KILL0000                * Kill temp file if exists 
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    TFILNAME,CMDLINE
          APPEND    SP1,CMDLINE
          BRANCH    OPTION,CTMP1000
.
.-------- Temp file 2,4 --------
.
          APPEND    "124 D76-78,79-81,7-14 D76-78,104-111,112-119,7-14",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          TRAP      CTMP9000 IF IO
          OPEN      TMPFL2,TFILNAME
          OPEN      TMPFL4,TFILNAME
          TRAPCLR   IO
          GOTO      CTMP9999
.
.-------- Temp file 1,3 --------
.
CTMP1000  APPEND    "124 D1-6,7-14 D1-6,104-111,112-119,7-14",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          TRAP      CTMP9000 IF IO
          OPEN      TMPFL1,TFILNAME
          OPEN      TMPFL3,TFILNAME
          TRAPCLR   IO
          GOTO      CTMP9999
.
CTMP9000  DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Temporary Index File ",TFILNAME," Not Found **",*W;
          TRAPCLR   IO
.
CTMP9999  RETURN
+
.**********************************************************************
.*                       KILL0000                Called by : CTMP0000 *
.*                 Delete the Temporary file                 ML0000   *
.**********************************************************************
KILL0000  CLOSE     TMPFL2
          CLOSE     TMPFL4
          CLOSE     TMPFL1
          CLOSE     TMPFL3
.
KILL2000  CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    TFILNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
KILL9999  RETURN
+
.**********************************************************************
.*                       HEAD0000                Called by : PRDC0000 *
.*               Print the headings for the reports          PRWD0000 *
.**********************************************************************
HEAD0000  CLOCK     TIME,CTIMEIS
          CALL      HEAD132
          MOVE      THREE,CLNO
          STRIP     PRFRDOCT
          STRIP     PRFRDATE
.
          BRANCH    OPTION,HEAD1000,HEAD2000    * Doctor, Ward 
.                        
.-------- Doctor Sequence ------------
.
HEAD1000  CALL      GTDC0000                * get doctor name
.
          PRINT     *N,*40,"Doctor : ",*+,PRFRDOCT," to ",PRTODOCT,*N:
                    *40,"Date   : ",*+,PRFRDATE," to ",PRTODATE
          IF        REPEND <> ONE
            PRINT     *N,"Doctor :  ",BKREADOC," - ",*+,DTITL,SP1,*+,DGNAME:
                      SP1,*+,DSNAME
          ENDIF
          PRINT     "*----------------------------------------------":
                    "-----------------------------------------------":
                    "-------------------------------------*",*N:
                    "| U/R No.| Patient Name          Booking|Sex|":
                    " D.O.B.   | Procedure Description",*85,"|Ana|":
                    "Ward   |Pre-Assessment| Admission    |Durn|",*N:
                    "*----------------------------------------------":
                    "-----------------------------------------------":
                    "-------------------------------------*"
          ADD       FIVE,CLNO
          GOTO      HEAD9999
.
.-------- Ward Sequence ------------
.
HEAD2000  CALL      GTWD0000                * Get ward description
.
          PRINT     *N,*40,"Ward : ",*+,PRFRWARD," to ",PRTOWARD,*N:
                    *40,"Date : ",*+,PRFRDATE," to ",PRTODATE
          IF        REPEND <> ONE
            PRINT     *N,"Ward :  ",TMPWARD," - ",*+,WBDESC
          ENDIF
          PRINT     "*----------------------------------------------":
                    "-----------------------------------------------":
                    "-------------------------------------*",*N:
                    "| U/R No.| Patient Name          Booking|Sex|":
                    " D.O.B.   | Procedure Description",*85,"|Ana|":
                    "Consult|Pre-Assessment| Admission    |Durn|",*N:
                    "*----------------------------------------------":
                    "-----------------------------------------------":
                    "-------------------------------------*"
          ADD       FIVE,CLNO
.
HEAD9999  RETURN
+
.**********************************************************************
.*                       GTDC0000                Called by : HEAD0000 *
.*                 Get Doctor Details                                 *
.**********************************************************************
GTDC0000  CLEAR     DTITL
          CLEAR     DGNAME
          CLEAR     DSNAME
          MOVE      BKREADOC,KEY6
          CALL      RDDOCT1                 * get doctor name
.
          PACK      BKREADOC,BKREADOC,SP6
          MATCH     BKREADOC,SP6
          IF        @EQUAL
            MOVE      UNKDOCT,DGNAME        * unkown doctor
          ENDIF
.
          STRIP     DTITL
          STRIP     DGNAME
.
GTDC9999  RETURN
+
.**********************************************************************
.*                       GTWD0000                Called by : HEAD0000 *
.*                  Get Ward Description                              *
.**********************************************************************
GTWD0000  CLEAR     WBDESC
          PACK      KEY6,TMPWARD,SP3
          CALL      RDWARD1                 * get ward description
.
          PACK      TMPWARD,TMPWARD,SP3
          MATCH     TMPWARD,SP3
          IF        @EQUAL
            MOVE      UNKWARD,WBDESC        * unkown ward
          ENDIF
.
GTWD9999  RETURN
+
.**********************************************************************
.*                       PRDC0000                Called by : ML0000   *
.*                 Print Doctor Code Report                           *
.**********************************************************************
PRDC0000  DISPLAY   *P1:24,*EL,"Printing report ..."
          MOVE      ZERO,CPAGENO
          MOVE      ONE,CLNO
          MOVE      FALSE,REPEND
          MOVE      " - Doctor Seq.",CPHDROPT
          CALL      CLR0000                 * clear vars
.
          PACK      KEY30,SP20,SP20
          CALL      RSTMP3
          CALL      RKTMP3
          IF        OVRCD=TRUE
            MOVE      TRUE,REPEND 
          ENDIF
.
          CALL      HEAD0000                * Print page heading
          BRANCH    REPEND,PRDC9000         * End of report ?
          MOVE      BKREADOC,LASTDOCT       * Save current doct in use
...       CALL      RPTMP3
          CALL      RSTMP3                  * re-posit. on 1st recd.  *C-114669
.
PRDC1000  CALL      RKTMP3                  * Read temp file
          BRANCH    OVRCD,PRDC9000
.
          DISPLAY   *P40:24,TMPDOCT,TMPBOOKN
          MATCH     BKREADOC,LASTDOCT
          GOTO      PRDC1500 IF NOT EQUAL   * New page for new doctor
.
          COMPARE   FIFTY5,CLNO
          GOTO      PRDC2000 IF LESS
.
PRDC1500  MOVE      ONE,CLNO                * New page
          CALL      HEAD0000
          MOVE      BKREADOC,LASTDOCT
.
PRDC2000  MOVE      WMDESC,DIM28
          CLEAR     PGNAME
          MOVE      "Invalid PMI details",PSNAME
          CLEAR     PBDATE
.
          MATCH     PURNO,ZEROUR
          GOTO      PRDC2500 IF EQUAL       * Zero U/R ?
.
          MOVE      PURNO,KEY8              * Have U/R, get info from patma1af
          CALL      RDMAST1
          GOTO      PRDC3000
.
PRDC2500  MOVE      TMPBOOKN,KEY8           * Zero, U/R, get info from patpramf
          CALL      RDPRAM1
          MOVE      ZEROUR,PURNO
.
PRDC3000  UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,OUTBDATE  
.
          UNPACK    BKREPDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPDATE,OUTRPDAT
.
          MOVE      BKREPTIM,OUTPTIM
          MOVE      TMPATIME,OUTATIME
.
          UNPACK    TMPADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPDATE,OUTADATE
.
          MOVE      PGNAME,PACGNAME
          MOVE      PSNAME,PACSNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,KEY20
          PACK      KEY30,KEY20,SP2,TMPBOOKN
.
          PRINT     "|",PURNO,"|",KEY30,*41,"|":
                    SP1,PSEX,SP1,*45,"|",OUTBDATE,*56,"|",DIM28,*85,"|":
                    TMPAANAE,*89,"|",TMPWARD,*97,"|",OUTRPDAT,SP1:
                    OUTPTIM,*112,"|",OUTADATE,SP1,OUTATIME,*127,"|",TMPAEXPD,"|"
          ADD       ONE,CLNO
          GOTO      PRDC1000
.
PRDC9000  PRINT     "*----------------------------------------------":
                    "-----------------------------------------------":
                    "-------------------------------------*",*N:
                    *N,"*** End of Report ***"
.
PRDC9999  RETURN        
.**********************************************************************
.*                       PRWD0000                Called by : ML0000   *
.*                 Print Ward Code Report                             *
.**********************************************************************
PRWD0000  DISPLAY   *P1:24,*EL,"Printing report ... ";
          MOVE      ZERO,CPAGENO
          MOVE      ONE,CLNO
          MOVE      FALSE,REPEND
          MOVE      " - Ward Seq.",CPHDROPT
          CALL      CLR0000                 * clear vars
.
          PACK      KEY25,SP20,SP20
          CALL      RSTMP4
          CALL      RKTMP4
          IF        OVRCD=TRUE
            MOVE      TRUE,REPEND 
          ENDIF
.
          CALL      HEAD0000                * Print page heading
          BRANCH    REPEND,PRWD9000
          MOVE      TMPWARD,LASTWARD        * Save current ward in use
...       CALL      RPTMP4
          CALL      RSTMP4                  * re-posit. on 1st recd.  *C-114669
.
PRWD1000  CALL      RKTMP4                  * Read temp file
          BRANCH    OVRCD,PRWD9000
.
          DISPLAY   *P40:24,TMPWARD,TMPBOOKN
          MATCH     TMPWARD,LASTWARD
          GOTO      PRWD1500 IF NOT EQUAL   * New page for new ward
.
          COMPARE   FIFTY5,CLNO
          GOTO      PRWD2000 IF LESS
.
PRWD1500  MOVE      ONE,CLNO
          CALL      HEAD0000
          MOVE      TMPWARD,LASTWARD
.
PRWD2000  MOVE      WMDESC,DIM28
          CLEAR     PGNAME
          MOVE      "Invalid PMI details",PSNAME
          CLEAR     PBDATE
.
          MATCH     PURNO,ZEROUR
          GOTO      PRWD2500 IF EQUAL       * Zero U/R ?
.
          MOVE      PURNO,KEY8              * Have U/R, get info from patma1af  
          CALL      RDMAST1
          GOTO      PRWD3000
.
PRWD2500  MOVE      TMPBOOKN,KEY8           * Zero U/R, get info from patpramf
          CALL      RDPRAM1
          MOVE      ZEROUR,PURNO
.
PRWD3000  UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY * format for printing
          CALL      PACDATE
          MOVE      CPCDATE,OUTBDATE  
.
          UNPACK    BKREPDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,OUTRPDAT
.
          MOVE      BKREPTIM,OUTPTIM
          MOVE      TMPATIME,OUTATIME
.
          UNPACK    TMPADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,OUTADATE
.
          MOVE      PGNAME,PACGNAME
          MOVE      PSNAME,PACSNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,KEY20
          PACK      KEY30,KEY20,SP2,TMPBOOKN
.
          PRINT     "|",PURNO,"|",KEY30,*41,"|":
                    SP1,PSEX,SP1,*45,"|",OUTBDATE,*56,"|",DIM28,*85,"|":
                    TMPAANAE,*89,"|",TMPDOCT,*97,"|",OUTRPDAT,SP1:
                    OUTPTIM,*112,"|",OUTADATE,SP1,OUTATIME,*127,"|",TMPAEXPD,"|"
          ADD       ONE,CLNO
          GOTO      PRWD1000
.
PRWD9000  PRINT     "*----------------------------------------------":
                    "-----------------------------------------------":
                    "-------------------------------------*",*N:
                    *N,"*** End of Report ***"
.
PRWD9999  RETURN
+
.*********************************************************************
.*        Enter the date ranges
.*********************************************************************
KDAT0000  MOVE      TEN4,CVERT
          MOVE      TEN3,CCOL
          UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          DISPLAY   *P1:14,"From Date : __/__/__":
                    *P1:15,"To   Date : __/__/__"
.
KDAT1000  CALL      KEYDATE                      * start Date
          BRANCH    CQUEST,KDAT0000
          IF        OVRCD=1 
            PACK      CCFRDATE,SP10
            PACK      PRFRDATE,START
            DISPLAY   *P13:14,*EL,*V2LON,START
            GOTO      KDAT2000
          ENDIF
.
          PACK      CCFRDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,PRFRDATE             * formatted end date
.
KDAT2000  MOVE      TEN5,CVERT
          MOVE      TEN3,CCOL
          UNPACK    CCFRDATE,CCENT,CYEAR,CMON,CDAY
.
          CALL      KEYDATE                      * end Date
          BRANCH    CQUEST,KDAT2000
          IF        OVRCD=1 
            PACK      CCTODATE,Z8
            PACK      PRTODATE,FINISH
            DISPLAY   *P13:15,*EL,*V2LON,FINISH
            GOTO      KDAT3000
          ENDIF
.
          PACK      CCTODATE,CCENT,CYEAR,CMON,CDAY
.
          CALL      PACDATE
          MOVE      CPCDATE,PRTODATE             * formatted end date
.
KDAT3000  MOVE      ZERO,EXIT
          MATCH     CCFRDATE,CCTODATE
          GOTO      KDAT9999 IF NOT LESS         * date is <= start date
.
          DISPLAY   *P1:24,*B,"Ending date must be after Starting date.  ",*EL;
          CALL      HITENTER 
          GOTO      KDAT0000
.
KDAT9100  MOVE      ONE,EXIT
.
KDAT9999  RETURN
.*********************************************************************
.         format doctors name
.*********************************************************************
FDCN0000  MOVE      DSNAME,PACSNAME
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,KEY40
FDCN9999  RETURN
+
.======================================================================
.            TEMP FILE I/O ROUTINES
.======================================================================
WRTTMP1   RESET     KEY14
          WRITE     TMPFL1,KEY14;BKREADOC,PURNO,TMPBOOKN:
                           WMDESC,TMPAANAE,TMPWARD,TMPBED,TMPDOCT,BKREPDAT:
                           BKREPTIM,TMPADATE,TMPATIME,TMPAEXPD
          RETURN
.
WRTTMP2   RESET     KEY14
          WRITE     TMPFL2,KEY14;BKREADOC,PURNO,TMPBOOKN:
                           WMDESC,TMPAANAE,TMPWARD,TMPBED,TMPDOCT,BKREPDAT:
                           BKREPTIM,TMPADATE,TMPATIME,TMPAEXPD
          RETURN
.
RDTMP1    MOVE      ZERO,OVRCD
          RESET     KEY14
          READ      TMPFL1,KEY14;BKREADOC,PURNO,TMPBOOKN:
                           WMDESC,TMPAANAE,TMPWARD,TMPBED,TMPDOCT,BKREPDAT:
                           BKREPTIM,TMPADATE,TMPATIME,TMPAEXPD
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTMP2    MOVE      ZERO,OVRCD
          RESET     KEY14
          READ      TMPFL2,KEY14;BKREADOC,PURNO,TMPBOOKN:
                           WMDESC,TMPAANAE,TMPWARD,TMPBED,TMPDOCT,BKREPDAT:
                           BKREPTIM,TMPADATE,TMPATIME,TMPAEXPD
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTMP3    MOVE      ZERO,OVRCD
          RESET     KEY30
          READ      TMPFL3,KEY30;BKREADOC,PURNO,TMPBOOKN:
                           WMDESC,TMPAANAE,TMPWARD,TMPBED,TMPDOCT,BKREPDAT:
                           BKREPTIM,TMPADATE,TMPATIME,TMPAEXPD
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTMP4    MOVE      ZERO,OVRCD
          RESET     KEY25
          READ      TMPFL4,KEY25;BKREADOC,PURNO,TMPBOOKN:
                           WMDESC,TMPAANAE,TMPWARD,TMPBED,TMPDOCT,BKREPDAT:
                           BKREPTIM,TMPADATE,TMPATIME,TMPAEXPD
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTMP1    MOVE      ZERO,OVRCD
          READ      TMPFL1,KEY14;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTMP2    MOVE      ZERO,OVRCD
          READ      TMPFL2,KEY14;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTMP3    MOVE      ZERO,OVRCD
          READ      TMPFL3,KEY30;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTMP4    MOVE      ZERO,OVRCD
          READ      TMPFL4,KEY25;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RKTMP1    MOVE      ZERO,OVRCD
          READKS    TMPFL1;BKREADOC,PURNO,TMPBOOKN:
                           WMDESC,TMPAANAE,TMPWARD,TMPBED,TMPDOCT,BKREPDAT:
                           BKREPTIM,TMPADATE,TMPATIME,TMPAEXPD
          GOTO      OVERCOND IF OVER
          RETURN
.
RKTMP2    MOVE      ZERO,OVRCD
          READKS    TMPFL2;BKREADOC,PURNO,TMPBOOKN:
                           WMDESC,TMPAANAE,TMPWARD,TMPBED,TMPDOCT,BKREPDAT:
                           BKREPTIM,TMPADATE,TMPATIME,TMPAEXPD
          GOTO      OVERCOND IF OVER
          RETURN
.
RKTMP3    MOVE      ZERO,OVRCD
          READKS    TMPFL3;BKREADOC,PURNO,TMPBOOKN:
                           WMDESC,TMPAANAE,TMPWARD,TMPBED,TMPDOCT,BKREPDAT:
                           BKREPTIM,TMPADATE,TMPATIME,TMPAEXPD
          GOTO      OVERCOND IF OVER
          RETURN
.
RKTMP4    MOVE      ZERO,OVRCD
          READKS    TMPFL4;BKREADOC,PURNO,TMPBOOKN:
                           WMDESC,TMPAANAE,TMPWARD,TMPBED,TMPDOCT,BKREPDAT:
                           BKREPTIM,TMPADATE,TMPATIME,TMPAEXPD
          GOTO      OVERCOND IF OVER
          RETURN
.
RPTMP1    MOVE      ZERO,OVRCD
          READKP    TMPFL1;BKREADOC,PURNO,TMPBOOKN:
                           WMDESC,TMPAANAE,TMPWARD,TMPBED,TMPDOCT,BKREPDAT:
                           BKREPTIM,TMPADATE,TMPATIME,TMPAEXPD
          GOTO      OVERCOND IF OVER
          RETURN
.
RPTMP2    MOVE      ZERO,OVRCD
          READKP    TMPFL2;BKREADOC,PURNO,TMPBOOKN:
                           WMDESC,TMPAANAE,TMPWARD,TMPBED,TMPDOCT,BKREPDAT:
                           BKREPTIM,TMPADATE,TMPATIME,TMPAEXPD
          GOTO      OVERCOND IF OVER
          RETURN
.
RPTMP3    MOVE      ZERO,OVRCD
          READKP    TMPFL3;BKREADOC,PURNO,TMPBOOKN:
                           WMDESC,TMPAANAE,TMPWARD,TMPBED,TMPDOCT,BKREPDAT:
                           BKREPTIM,TMPADATE,TMPATIME,TMPAEXPD
          GOTO      OVERCOND IF OVER
          RETURN
.
RPTMP4    MOVE      ZERO,OVRCD
          READKP    TMPFL4;BKREADOC,PURNO,TMPBOOKN:
                           WMDESC,TMPAANAE,TMPWARD,TMPBED,TMPDOCT,BKREPDAT:
                           BKREPTIM,TMPADATE,TMPATIME,TMPAEXPD
          GOTO      OVERCOND IF OVER
          RETURN
.
.======================================================================
.             I/O INCLUDES
.======================================================================
          INC       STD001IO
          INC       BOKRC1IO/INC
          INC       PATWRDKY/PBL
          INC       OPRDEAIO/INC
          INC       PATDOCKY/PBL
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATPR1IO/INC
          INC       PATTRNIO/INC
          INC       PATWR1IO/INC
          INC       WATTR1IO/INC
          INC       TFILENAM
          INC       IBASEQIO/INC
          INC       WEBERRIO/INC
.
...............................................................................

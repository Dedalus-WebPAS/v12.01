.***************************************************************************
.* System    :   Patient Management                                        *
.* Program   :   FA222781                                                  *
.* Desc      :   Fixit for Program for Outpatient billing charges          *
.***************************************************************************
.* Date      :   31/05/2010                                                *
.* Author    :   J.Tan                                                     *
.* Function  :   This program will remove patipenf and pmsmtiaf for all    *
.*               Unattended outpatients                                    *
.* Modifications  :                                                        *
.*        V10.00.00 06/08/2010  J.Tan      CAR 222781                      *
.*                  Created program                                        *
.***************************************************************************
.
.
          INC       STD001FD
.
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PMSMTIFD/INC
          INC       PATIPEFD/INC
          INC       OUTBOAFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
RECNUM    FORM      8
.
PRGID     INIT      "FA222781"
PRGNAM    INIT      "Fixit for Unattended Outpatient"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000       * proceed with fixit
.
MAIN1000  CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN2000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN2000  CALL      PROC0000               * process files with coded fields
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: MAIN0000  *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"pmsmtiaf";
          OPEN      PMSMTIA1,"pmsmtiaf"
.
          DISPLAY   *P64:24,"patipenf";
          OPEN      PATIPEN1,"patipenf"
.
          DISPLAY   *P64:24,"outbokaf";
          OPEN      OUTBOKA6,"outbokaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: MAIN0000  *
.*                        get user options or exit                          *
.*    Returns:  EXIT    = FALSE (0)  Run fixit                              *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*****************************************************************************
.
PROC0000  DISPLAY   *P1:24,*EF,"Processing:";
.
          MOVE      ZERO,RECNUM
          PACK      KEY36,Z70
          CALL      RDSBOKA6
PROC1000  CALL      RDPBOKA6
          BRANCH    OVRCD,PROC9000
.
          MATCH     "       0",DOBAOUTN
          GOTO      PROC9000 IF EQUAL
.
          DISPLAY   *P13:24,*V2LON,DOBAOUTN;
.
          COMPARE   FIVE,OBASTAT                 * Un-Attended?
          GOTO      PROC1000 IF NOT EQUAL        * No
.
.         Unattended outpatient should not have records in patipen and pmsmtiaf
.
          MOVE      DOBAOUTN,KEY8
          CALL      RDIPEN1
          IF        OVRCD=0
            CALL      DEIPEN1
            MOVE      ONE,F1
          ENDIF
.
PROC4000  PACK      KEY14,DOBAOUTN,SP70
          CALL      RSPMMTI1                     * position in file
          CALL      RKPMMTI1                     * read next record
          BRANCH    OVRCD,PROC8000               * eof 
.
          MATCH     DOBAOUTN,PMMIVISN
          GOTO      PROC8000 IF NOT EQUAL
.
          PACK      KEY14,PMMIVISN,PMMITRAN,SP70
          CALL      DEPMMTI1
          MOVE      ONE,F1
          GOTO      PROC4000
.
PROC8000  COMPARE   ZERO,F1
          GOTO      PROC1000 IF EQUAL
.
          PRINT     *1,"UR: ",OBAURNO," Visit: ",DOBAOUTN
          ADD       ONE,RECNUM
          MOVE      ZERO,F1
          GOTO      PROC1000
.
PROC9000  DISPLAY   *P1:23,*EL,"Fixit complete.  Number of Records found: ":
                    RECNUM
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PMSMTIIO/INC
          INC       PATIPEIO/INC
          INC       OUTBOAIO/INC

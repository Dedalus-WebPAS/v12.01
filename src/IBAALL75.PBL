.***************************************************************************
.* System    :   Allied Health                                             *
.* Program   :   IBAALL75                                                  *
.* Desc      :   Delete records from VINAH extraction history file allhdtaf*
.***************************************************************************
.* Date      :   21/05/2008                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   This program will delete the records from allhdtaf for    *
.*           :   a given date range. An edit check will be performed to    *
.*           :   make sure there are no records with a status of extracted *
.* Mods      :                                                             *
.*         V10.14.01 17/04/2019  Steve Armstrong  TSK 0868837              *
.*                   Recompiled for changes to ALLHDTFD.                   *
.***************************************************************************
.*         V10.04.01 15/05/2014  Steve Armstrong   CAR 285465              *
.*                   Recompiled for changes to ALLHDTFD                    *
.***************************************************************************
.*         V10.03.01 03/12/2012  Ebon Clements     CAR 272756              *
.*                   Changes for VINAH8 for 2012-2013                      *
.***************************************************************************
.*         V10.00.02 28/05/2010  Steve Armstrong   CAR 220289              *
.*                   Recompiled for changes to ALLHDTFD                    *
.*         V10.00.01 22/04/2010  Steve Armstrong   CAR 220289              *
.*                   Recompiled for changes to ALLHDTFD                    *
.***************************************************************************
.*           :   V9.10.00  22/05/2008  Ebon Clements     CAR 168984        *
.*           :             Created program                                 *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       ALLHDTFD/INC
          INC       ALLSEDFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
COUNTER   FORM      2
DIM8      DIM       8
FROMDATE  DIM       8
STATUS    DIM       2
TODATE    DIM       8
.
PRGID     INIT      "IBAALL75"
PRGNAM    INIT      "Delete VINAH History"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      DATE0000               * keyin from and to date range
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
          GOTO      ML0100
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"allhdtaf";
          OPEN      ALLHDTA1,"allhdtaf" 
          OPEN      ALLHDTA4,"allhdtaf" 
.
          DISPLAY   *P64:24,"allsedaf";
          OPEN      ALLSEDA1,"allsedaf" 
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Delete waiting/ignore allhdtaf ":
                                           "data by date/range"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.***************************************************************************
.*                               DATE0000                                  *
.*                          accept date range                              *
.***************************************************************************
.
.---------- keyin first date range -------
.
DATE0000    MOVE       FALSE,EXIT
            DISPLAY    *P1:9,*EF,"Date From :":
                          *P1:10,"Date To   :";
.                         
            MOVE      "9",CVERT
            MOVE      "9",EVERT
            MOVE      "13",CCOL
            CALL      IBACLOCK 
            MOVE      CCC,CCENT
            MOVE      CYY,CYEAR
            MOVE      CMM,CMON
            MOVE      CDD,CDAY
            CALL      KEYDATE                          * enter the from date
            PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY   * move to form field
            REP       " 0",FROMDATE                    * from date
.
            MOVE      "10",CVERT
            MOVE      "10",EVERT
            MOVE      "13",CCOL
            CALL      KEYDATE                          * enter the from date
            PACK      TODATE,CCENT,CYEAR,CMON,CDAY     * move to form field
            REP       " 0",FROMDATE                    * from date
.
            MATCH      FROMDATE,TODATE
            GOTO       DATE9999 IF NOT LESS
.
            DISPLAY    *P1:24,*EL,*B,"2nd Range Must Be Greater Than 1st ":
                       "Range - Hit <",*V2LON,"ENTER",*HOFF,"> To Continue ";
            KEYIN      ANS;
            DISPLAY    *P1:24,*EL;
            GOTO       DATE0000
.
DATE9999    RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
PROC0000  MOVE      SIX,COUNTER                 * initialise counter
          WHILE     COUNTER <=10
            MOVE      COUNTER,STATUS
            CALL      CHKS0000
            BRANCH    EXIT,PROC9000
            ADD       ONE,COUNTER
          DO
.
PROC5000  MOVE      ZERO,COUNTER                 * initialise counter
          WHILE     COUNTER <= 5
            MOVE      COUNTER,STATUS
            CALL      DELR0000
            ADD       ONE,COUNTER
          DO
          GOTO      PROC9999
.
PROC9000  PRINT     *N,*1,"ERROR, Records for this date range ":
                          "have been extracted.";
.
          DISPLAY   *P1:24,*EL,*B,"ERROR, Records for this date range ":
                                  "have been extracted.";
          CALL      HITENTER
          GOTO      PROC9999
.
PROC9999  RETURN
+
.**************************************************************************
.*                               CHKS0000              Called by: PROC0000*
.* Check if there are any record for the given status 
.*          6 = Extracted
.*          7 = Submitted
.*          8 = Rejected
.*          9 = Resubmitted
.*         10 = Accepted
.**************************************************************************
CHKS0000  PACK      KEY42,STATUS,FROMDATE,SP70
          CALL      RSALHDT4
CHKS1000  CALL      RKALHDT4
          BRANCH    OVRCD,CHKS9000
.
          MATCH     STATUS,ALHDSTAT
          GOTO      CHKS9000 IF NOT EQUAL
.
          MOVE      ALHDDTTM,DIM8
          MATCH     DIM8,TODATE
          GOTO      CHKS9100 IF NOT LESS
.
CHKS9000  MOVE      ZERO,EXIT
          GOTO      CHKS9999
.
CHKS9100  MOVE      ONE,EXIT
          GOTO      CHKS9999
.
CHKS9999  RETURN
+
.**************************************************************************
.*                               DELR0000              Called by: PROC0000*
.* Check if there are any record for the given status and delete them
.*          0 = Waiting
.*          1 = Error on extract validation
.*          2 = Ignore (non VINAH record)
.*          3 = Ignore (not required)
.*          4 = Ignore (later message exists)
.*          5 = Ignore (associated deletion)
.**************************************************************************
DELR0000  PACK      KEY42,STATUS,FROMDATE,SP70
          CALL      RSALHDT4
          CALL      RKALHDT4
          BRANCH    OVRCD,DELR9999
.
          MATCH     STATUS,ALHDSTAT
          GOTO      DELR9999 IF NOT EQUAL
.
          MOVE      ALHDDTTM,DIM8
          MATCH     DIM8,TODATE
          GOTO      DELR9999 IF LESS
.
          PACK      KEY42,ALHDSTAT,ALHDURNO,ALHDVISN,ALHDCONT:
                          ALHDDTTM,ALHDRTYP,ALHDACTN,SP70
          CALL      DEALHDT1
.
          CALL      DSED0000                   * Delete error records
.
          GOTO      DELR0000
.
DELR9999  RETURN
+
.******************************************************************************
.  Delete all error detail records for this message
.******************************************************************************
DSED0000  PACK      KEY57,ALHDURNO,ALHDVISN,ALHDCONT,ALHDDTTM,ALHDMTYP,SP70
          CALL      RSALSED1
          CALL      RKALSED1
          BRANCH    OVRCD,DSED9999
.
          MATCH     ALHDURNO,ALSEURNO
          GOTO      DSED9999 IF NOT EQUAL
.
          MATCH     ALHDVISN,ALSEVISN
          GOTO      DSED9999 IF NOT EQUAL
.
          MATCH     ALHDCONT,ALSEENCT
          GOTO      DSED9999 IF NOT EQUAL
.
          MATCH     ALHDDTTM,ALSEDTTM
          GOTO      DSED9999 IF NOT EQUAL
.
          MATCH     ALHDMTYP,ALSEMTYP
          GOTO      DSED9999 IF NOT EQUAL
.
          PACK      KEY57,ALSEURNO,ALSEVISN,ALSEENCT,ALSEDTTM,ALSEMTYP,ALSEERID:
                          ALSEECNT,SP70
          CALL      DEALSED1
          GOTO      DSED0000
.
DSED9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       ALLHDTIO/INC
          INC       ALLSEDIO/INC

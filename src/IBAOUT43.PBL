.***************************************************************************
.* System    :   OUTPATIENTS                                               *
.* Program   :   IBAOUT43                                                  *
.* Desc      :   Attendance by Locality Report                             *
.***************************************************************************
.* Date      :   04/10/89                                                  *
.* Author    :   Eric Phan                                                 *
.* Function  :   This program prints attendance statistics from the        *
.*               location file.  Modified from IBAOUT43 V5.50.             *
.* Mods      :                                                             *
.*       V10.05.01  15/07/2014  Ebon Clements     CAR 261630               *
.*                  Removed port number from temp file name                *
.*       V10.02.01  27/06/2011  Steve Armstrong   CAR 245347               *
.*                  Mods for changes to OUTLOCIO call routines             *
.******************************************************************************
.*       V5.07.01   15/10/1999  Steve Armstrong                            *
.*                  Removed reference to CSTRTYR (not used)                *
.*           V5.07.B01 18/01/1999  Nicole Harrington 507 rebound 095       *
.*                     Mods to use HEAD132                                 *
.*            V5.01.01 14/12/89 E.Phan                                     *
.*                     Modified to use Periods.                            *
.*            V5.01.02 19/02/90 J.Tan                                      *
.*                     Fixed duplicate key errors SRF 103321               *
.*           :V5.01.03 05/03/90 D.Di.Paola  SRF 102421 & 102422            *
.*               Rewrote initial part of extraction as it was incorrect... *
.*               ie. date ranges and flags                                 *
.*           :V5.01.04 20/05/91 J.Tan                                      *
.*               Fixed 13 week periods                                     *
.*                                                                         *
.***************************************************************************
.
          INC       STD001FD
          INC       IBASEQFD/INC
          INC       PATCOMM/INC
          INC       OUTLOCFD/INC
          INC       OUTCTYFD/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATDRGFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
CMDLINE   DIM       50
CODECG    INIT      "CG"          * Group Code
CODECH    INIT      "CH"          * Catchment Code
COUNT     FORM      2
COUNTY    FORM      2
CSTPER    DIM       6
CURPER    DIM       6
.
DIM3      DIM       3
DIM4      DIM       4
DIM6      DIM       6
DIM9      DIM       9
.
FIRSTREC  FORM      1
FORM7     FORM      7
FORM7Y    FORM      7
.
.
MPERREQD  FORM      2         * Required period (calculated for every record)
MPERUSED  FORM      2         * Number of months used (calculated initially)
MPRTPOS   FORM      3         * Variable print position
.
PENDDATE  DIM       7
PSTRDATE  DIM       7
.
NPYEAR    FORM      2
.
OUTTMP    DIM       8
OUTTMPXX  IFILE     SQL, FIXED=77, KEY="U1-12"
.
PDDESC    DIM       6         * Period date description (mmm yy)
.
SAVCATCH  DIM       3
SAVCTYPE  DIM       6
SAVGROUP  DIM       3
.
TGROUP    DIM       3         * (3) OUTTMPXX - Group Code
TCTYPE    DIM       6         * (6)          - Clinic Type
TCCATCH   DIM       3         * (3)          - Catchment Code
TCPER1    FORM      7         * (5)          - Totals for period 1
TCPER2    FORM      7         * (5)                       period 2
TCPER3    FORM      7         * (5)                       period 3
TCPER4    FORM      7         * (5)                       period 4
TCPER5    FORM      7         * (5)                       period 5
TCPER6    FORM      7         * (5)                       period 6
TCPER7    FORM      7         * (5)                       period 7
TCPER8    FORM      7         * (5)                       period 8
TCPER9    FORM      7         * (5)                       period 9
TCPER10   FORM      7         * (5)                       period 10
TCPER11   FORM      7         * (5)                       period 11
TCPER12   FORM      7         * (5)                       period 12
TCPER13   FORM      7         * (5)                       period 13
TCPERTOT  FORM      7         * period totals for Clinic ID
.
TTPER1    FORM      7         *** CLINIC TYPE TOTALS ***
TTPER2    FORM      7
TTPER3    FORM      7
TTPER4    FORM      7
TTPER5    FORM      7
TTPER6    FORM      7
TTPER7    FORM      7
TTPER8    FORM      7
TTPER9    FORM      7
TTPER10   FORM      7
TTPER11   FORM      7
TTPER12   FORM      7
TTPER13   FORM      7
TTPERTOT  FORM      7
.
TGPER1    FORM      7         *** GROUP TOTALS ***
TGPER2    FORM      7
TGPER3    FORM      7
TGPER4    FORM      7
TGPER5    FORM      7
TGPER6    FORM      7
TGPER7    FORM      7
TGPER8    FORM      7
TGPER9    FORM      7
TGPER10   FORM      7
TGPER11   FORM      7
TGPER12   FORM      7
TGPER13   FORM      7
TGPERTOT  FORM      7
.
TZPER1    FORM      7         *** GRAND TOTALS ***
TZPER2    FORM      7
TZPER3    FORM      7
TZPER4    FORM      7
TZPER5    FORM      7
TZPER6    FORM      7
TZPER7    FORM      7
TZPER8    FORM      7
TZPER9    FORM      7
TZPER10   FORM      7
TZPER11   FORM      7
TZPER12   FORM      7
TZPER13   FORM      7
TZPERTOT  FORM      7
.
VENDDATE  DIM       6         * End month   - CCYYMM
VSTRDATE  DIM       6         * Start month - CCYYMM
.
PRGID     INIT      "IBAOUT43"
PRGNAM    INIT      "Attendance By Locality Report"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              MAINLINE                                  *
.*                            Controlling Logic                           *
.**************************************************************************
.
          CALL      INIT0000          * initialization and open files
.
ML0100    CALL      OPTN0000          * select options
          BRANCH    EXIT,ML9900       * EXIT=1 if "0" chosen in menu
.
          CALL      PROC0000          * Process data
          BRANCH    FIRSTREC,ML9500   * If set, no records found
.
          CALL      PRNT0000          * Print data
.
          GOTO      ML0100
.
.--------------------------------------------------------------------------
.         No records found in processing pass ...
.--------------------------------------------------------------------------
.
ML9500    DISPLAY   *P1:24,*EL,*B,*V2LON,"*** No records found *** ";
          CALL      HITENTER
          GOTO      ML0100
.
.--------------------------------------------------------------------------
.         The end ...
.--------------------------------------------------------------------------
.
ML9900    CALL      KILL0000
          CHAIN     PGM               * chain back to appropriate menu
+
.****************************************************************************
.*                                INIT0000                                  *
.*                             initialization                               *
.****************************************************************************
.
INIT0000  CALL       DISPHEAD
.         DISPLAY    *ES,PRGID,*P20:1,CNAME,*P72:1,CDATE:
.                     *P1:2,VERSION,*P20:2,PRGNAM
.
          DISPLAY    *P56:24,"Opening controlf"
          OPEN       CONTROLF,"controlf"
.
          DISPLAY    *P64:24,"patcodes"
          OPEN       PATCODE1,"patcodes"
.
          DISPLAY    *P64:24,"patdrgaf"
          OPEN       PATDRGA1,"patdrgaf"
          OPEN       PATDRGA3,"patdrgaf"
.
          PACK       CFNAME,UID,FILCTYA1
          DISPLAY    *P64:24,CFNAME
          OPEN       OUTCTYA1,CFNAME
.
          PACK       CFNAME,UID,FILLOCA1
          DISPLAY    *P64:24,CFNAME
          OPEN       OUTLOCA1,CFNAME
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,OUTTMP
.
          DISPLAY    *P1:24,*EL
.
.         Work out what the current period is, and the first period in the
.         current financial year
.
          PACK       CPTDATE,CCC,CYY,CMM,CDD
          REP        " 0",CPTDATE
          CALL       GPERD                  * Find the current period
          BRANCH     EXIT,INIT9000          * No current period found
.
          PACK       CURPER,DRGCYR,DRGCNUM
.
          PACK       KEY6,DRGYR,SP2
          CALL       RDSDRGA1
          CALL       RDKDRGA1
.
          PACK       CSTPER,DRGCYR,DRGCNUM
.
.         Now count the number of periods in a financial year
.
          MOVE       ZERO,NPYEAR
          MOVE       DRGYR,DIM4
          PACK       KEY6,DRGYR,SP2
          CALL       RDSDRGA1
INIT8000  CALL       RDKDRGA1
          BRANCH     OVRCD,INIT8100
.
          MATCH      DRGYR,DIM4
          GOTO       INIT8100 IF NOT EQUAL
.
          ADD        ONE,NPYEAR
          GOTO       INIT8000
.
.         Finished initialisation
.
INIT8100  DISPLAY    *P1:24,*EL
          MOVE       ZERO,EXIT
          GOTO       INIT9999
.
.         No current period on file
.
INIT9000  KEYIN      *P1:24,*EL,*B,*V2LON:
                     "** Error: Current date not found in Period file. ":
                     "Hit <ENTER> to exit ",*EOFF,ANS,*P1:24;
          MOVE       ONE,EXIT
.
INIT9999  RETURN
.
+
.****************************************************************************
.*                                OPTN0000                                  *
.*                        get user options or exit                          *
.****************************************************************************
.
OPTN0000   MOVE       ONE,EXIT
           DISPLAY    *P1:3,*EF:
                      *P1:4,*V2LON,ZERO,*HOFF,". Exit":
                      *P1:5,*V2LON,ONE,*HOFF,". Group Code sequence"
OPTN0500   KEYIN      *P1:7,"Please Select : ",*V2LON,OPTION
           COMPARE    ZERO,OPTION
           GOTO       OPTN9999 IF EQUAL
           MOVE       ZERO,EXIT
           BRANCH     OPTION,OPTN3000
           BEEP
           GOTO       OPTN0500
.
.----------------------------------------------------------------------------
.          Keyin dates
.----------------------------------------------------------------------------
.
OPTN3000  DISPLAY   *P1:9,*EF,"Generate Report from "
          UNPACK    CSTPER,CCENT,CYEAR,CMON
          MOVE      ZERO,CHIGHLT
          MOVE      TWENTY2,CCOL
          MOVE      NINE,CVERT
          CALL      KEYPER
          BRANCH    OVRCD,OPTN0000
.
.         Validate the date entered
.
          PACK      KEY6,CCENT,CYEAR,CMON
          REP       " 0",KEY6
          CALL      RDDRGA3
          BRANCH    OVRCD,OPTN3050
.
          PACK      VSTRDATE,DRGYR,DRGNUM
          PACK      PSTRDATE,DRGCNUM,SLASH,DRGCYR
.
.         Get the to date
.
OPTN3020  DISPLAY   *P30:9,"to "
          UNPACK    CURPER,CCENT,CYEAR,CMON
          MOVE      "33",CCOL
          CALL      KEYPER
          BRANCH    OVRCD,OPTN3000
.
.         Validate the date entered
.
          PACK      KEY6,CCENT,CYEAR,CMON
          REP       " 0",KEY6
          CALL      RDDRGA3
          BRANCH    OVRCD,OPTN3060
.
          PACK      VENDDATE,DRGYR,DRGNUM
          PACK      PENDDATE,DRGCNUM,SLASH,DRGCYR
.
          MATCH     VSTRDATE,VENDDATE
          GOTO      OPTN3100 IF NOT LESS
          DISPLAY   *P10:24,*V2LON,*B,"*** TO date cannot be ":
                    "before FROM date *** ",*W,*P1:24,*EL;
          GOTO      OPTN3000
.
.         From date not on file
.
OPTN3050  DISPLAY   *P10:24,*V2LON,*B,"*** Invalid Date *** ",*W,*P1:24,*EL;
          GOTO      OPTN3000
.
.         To date not on file
.
OPTN3060  DISPLAY   *P10:24,*V2LON,*B,"*** Invalid Date *** ",*W,*P1:24,*EL;
          GOTO      OPTN3020
.
.         Calculate MPERUSED - # of periods used
.
OPTN3100  MOVE      ZERO,MPERUSED
          PACK      KEY6,VSTRDATE
          CALL      RDDRGA1
          BRANCH    OVRCD,OPTN3200
          GOTO      OPTN3250
OPTN3200  CALL      RDKDRGA1
          BRANCH    OVRCD,OPTN3300
.
OPTN3250  PACK      XDATE,DRGYR,DRGNUM
          MATCH     XDATE,VENDDATE
          GOTO      OPTN3300 IF LESS
.
          ADD       ONE,MPERUSED
          GOTO      OPTN3200
.
.         Validate the number of periods
.
OPTN3300  COMPARE   TEN4,MPERUSED
          GOTO      OPTN9000 IF LESS
.
          DISPLAY   *P10:24,*V2LON,*B,"*** Maximum 13 periods allowed *** ":
                    *W,*P1:24,*EL;
          GOTO      OPTN3000
.
.----------------------------------------------------------------------------
.         Ok to proceed ?
.----------------------------------------------------------------------------
.
OPTN9000  MOVE      SP1,ANS
          KEYIN     *P1:24,"Ok to proceed (",*V2LON,*DV,ANSY,*HOFF:
                    *DV,SLASH,*V2LON,*DV,ANSN,*HOFF,") ? ",*V2LON,ANS
          CMATCH    ANSY,ANS
          GOTO      OPTN9999 IF EQUAL
          CMATCH    ANSN,ANS
          GOTO      OPTN0000 IF EQUAL
          BEEP
          GOTO      OPTN9000
.
OPTN9999  RETURN
.
+
.**************************************************************************
.*                              PROC0000                                  *
.*                            Process data                                *
.**************************************************************************
.
PROC0000  CALL      CREA0000               * Create temp file
          MOVE      ONE,FIRSTREC           * 1st record not read yet
          MOVE      ZERO,EXIT              * Exit flag not set
          UNPACK    SP30,SAVGROUP,SAVCTYPE,SAVCATCH
.
          DISPLAY   *P1:11,*EF,*V2LON,"*** Processing Records ***",*HOFF:
                    *P1:13,"Group Code  :",*P1:14,"Clinic Type :":
                    *P1:15,"Catchment   :",*P1:16,"Date        :"
.
.------------------------------------------------------------------------------
.         Calculate attendees figures
.------------------------------------------------------------------------------
.
PROC3000  PACK      KEY24,IDDD,SP30          * All records for this site
          CALL      RSOTLOC1
PROC3100  CALL      RKOTLOC1
          BRANCH    OVRCD,PROC3300           * End of file - Finished
.
          MATCH     LOASITE,IDDD
          GOTO      PROC3300 IF NOT EQUAL    * Diff site - Finished
.
          MATCH     VSTRDATE,LOADATE
          GOTO      PROC3100 IF LESS         * Not in range yet - keep reading
          MATCH     LOADATE,VENDDATE
          GOTO      PROC3100 IF LESS         * out of range - keep reading
.
          MATCH     SP3,SAVGROUP             * 1st time round ?
          GOTO      PROC3200 IF EQUAL        * 1st time round - save & add
.
PROC3110  MATCH     LOAGRUP,SAVGROUP
          GOTO      PROC4000 IF NOT EQUAL    * Diff group - write to file
          MATCH     LOATYPE,SAVCTYPE
          GOTO      PROC4000 IF NOT EQUAL    * Diff type - write to file
          MATCH     LOACACH,SAVCATCH
          GOTO      PROC4000 IF NOT EQUAL    * Diff catchment - write to file
.
.-------- valid record, calculate period -----
.
          UNPACK    LOADATE,CCENT,CYEAR,CMON
          DISPLAY   *P15:13,*V2LON,LOAGRUP,*P15:14,LOATYPE:
                    *P15:15,LOACACH,*P15:16,CMON,SLASH,CCENT,CYEAR
.
          CALL      CALM0000                 * Calculate period # for this month
          MOVE      ZERO,FIRSTREC            * Found a record
.
          STORE     LOANUMB,MPERREQD,TCPER1,TCPER2,TCPER3,TCPER4:
                                     TCPER5,TCPER6,TCPER7,TCPER8:
                                     TCPER9,TCPER10,TCPER11,TCPER12:
                                     TCPER13
          GOTO      PROC3100                 * Read another record
.
. ------- New record -------
.
PROC3200
          MOVE      LOAGRUP,SAVGROUP         * Save group code
          MOVE      LOATYPE,SAVCTYPE         * Save clinic type
          MOVE      LOACACH,SAVCATCH         * Save catchment code
.
          CALL      CLRV0000                 * Clear month totals
          GOTO      PROC3110
.
. ------- No more records -------
.
PROC3300  MOVE      ONE,EXIT
.
. ------- Write to datafile -------
.
PROC4000  BRANCH    FIRSTREC,PROC9999        * No records found - no write
.
          MOVE      TCPER1,TTPER1            * Save the variables
          MOVE      TCPER2,TTPER2
          MOVE      TCPER3,TTPER3
          MOVE      TCPER4,TTPER4
          MOVE      TCPER5,TTPER5
          MOVE      TCPER6,TTPER6
          MOVE      TCPER7,TTPER7
          MOVE      TCPER8,TTPER8
          MOVE      TCPER9,TTPER9
          MOVE      TCPER10,TTPER10
          MOVE      TCPER11,TTPER11
          MOVE      TCPER12,TTPER12
          MOVE      TCPER13,TTPER13
.
          PACK      KEY12,SAVGROUP,SAVCTYPE,SAVCATCH
          CALL      RDTMP1
          BRANCH    OVRCD,PROC5000           * There shouldn't be a record there
.
          MOVE      TTPER1,TCPER1
          MOVE      TTPER2,TCPER2
          MOVE      TTPER3,TCPER3
          MOVE      TTPER4,TCPER4
          MOVE      TTPER5,TCPER5
          MOVE      TTPER6,TCPER6
          MOVE      TTPER7,TCPER7
          MOVE      TTPER8,TCPER8
          MOVE      TTPER9,TCPER9
          MOVE      TTPER10,TCPER10
          MOVE      TTPER11,TCPER11
          MOVE      TTPER12,TCPER12
          MOVE      TTPER13,TCPER13
.
          CALL      UPTMP1
          GOTO      PROC6000
.
PROC5000  MOVE      SAVGROUP,TGROUP
          MOVE      SAVCTYPE,TCTYPE
          MOVE      SAVCATCH,TCCATCH
          CALL      WRTMP1
.
PROC6000  BRANCH    EXIT,PROC9999            * Finished
          GOTO      PROC3200
.
PROC9999  RETURN
.
.----------------------------------------------------------------------------
.         Calculate the period for a particular month - value in MPERREQD
.----------------------------------------------------------------------------
.
CALM0000  UNPACK    VSTRDATE,XCENT,XYEAR,XMON
          MOVE      XMON,IMON
          MOVE      XYEAR,IYEAR
          MOVE      XCENT,ICENT
.
          UNPACK    LOADATE,CCENT,CYEAR,CMON
          MOVE      CMON,MPERREQD
.
          MATCH     XYEAR,CYEAR
          GOTO      CALM1000 IF EQUAL
.
          ADD       NPYEAR,MPERREQD          * Different year
.
CALM1000  SUB       IMON,MPERREQD          * Same year
          ADD       ONE,MPERREQD
.
CALM9999  RETURN
.
.----------------------------------------------------------------------------
.         Clear temp variables
.----------------------------------------------------------------------------
.         Clinic ID
.
CLRV0000  MOVE      ZERO,COUNT
CLRV0100  ADD       ONE,COUNT
          COMPARE   COUNT,TEN3
          RETURN    IF LESS
          STORE     ZERO,COUNT,TCPER1,TCPER2,TCPER3,TCPER4,TCPER5:
                               TCPER6,TCPER7,TCPER8,TCPER9,TCPER10:
                               TCPER11,TCPER12,TCPER13
          GOTO      CLRV0100
.
.         Clinic type
.
CLRV1000  MOVE      ZERO,COUNT
CLRV1100  ADD       ONE,COUNT
          COMPARE   COUNT,TEN3
          RETURN    IF LESS
          STORE     ZERO,COUNT,TTPER1,TTPER2,TTPER3,TTPER4,TTPER5:
                               TTPER6,TTPER7,TTPER8,TTPER9,TTPER10:
                               TTPER11,TTPER12,TTPER13
          GOTO      CLRV1100
.
.         Group type
.
CLRV2000  MOVE      ZERO,COUNT
CLRV2100  ADD       ONE,COUNT
          COMPARE   COUNT,TEN3
          RETURN    IF LESS
          STORE     ZERO,COUNT,TGPER1,TGPER2,TGPER3,TGPER4,TGPER5:
                               TGPER6,TGPER7,TGPER8,TGPER9,TGPER10:
                               TGPER11,TGPER12,TGPER13
          GOTO      CLRV2100
.
.         Grand totals
.
CLRV3000  MOVE      ZERO,COUNT
CLRV3100  ADD       ONE,COUNT
          COMPARE   COUNT,TEN3
          RETURN    IF LESS
          STORE     ZERO,COUNT,TZPER1,TZPER2,TZPER3,TZPER4,TZPER5:
                               TZPER6,TZPER7,TZPER8,TZPER9,TZPER10:
                               TZPER11,TZPER12,TZPER13
          GOTO      CLRV3100
.
+
.**************************************************************************
.*                              PRNT0000                                  *
.*                              Print data                                *
.**************************************************************************
.
PRNT0000  MOVE      "80",CLNO
          MOVE      ZERO,CPAGENO
          MOVE      SP6,SAVCTYPE
          CALL      CLRV1000
          CALL      CLRV2000
          CALL      CLRV3000
.
          DISPLAY   *P1:11,*EF,*V2LON,"*** Printing Records ***",*HOFF:
                    *P1:13,"Group Code  :",*P1:14,"Clinic Type :":
                    *P1:15,"Catchment   :"
          MOVE      SP20,KEY12
          CALL      RDSTMP1
PRNT1000  CALL      RDKTMP1
          BRANCH    OVRCD,PRNT9000
.
          DISPLAY   *P15:13,*V2LON,TGROUP,*P15:14,TCTYPE,*P15:15,TCCATCH
.
          MATCH     SP6,SAVCTYPE
          GOTO      PRNT2000 IF EQUAL        * 1st time round
          MATCH     SAVCTYPE,TCTYPE
          CALL      PRNX3000 IF NOT EQUAL    * Print last clinic type totals
          MATCH     SAVGROUP,TGROUP
          CALL      PRNX4000 IF NOT EQUAL    * Print last group totals
.
PRNT2000  MOVE      TCTYPE,SAVCTYPE
          MOVE      TGROUP,SAVGROUP
          CALL      PRNX1000                 * Print record
          GOTO      PRNT1000
.
PRNT9000  CALL      PRNX3000                 * Print last clinic type totals
          CALL      PRNX4000                 * Print last group totals
          CALL      PRNX5000                 * Print grand totals
          CALL      PRNX8000                 * Print end of report
          RETURN
.
+
.****************************************************************************
.*                                                                          *
.*                        Printing subroutines                              *
.*                                                                          *
.****************************************************************************
.         Print Record
.----------------------------------------------------------------------------
.
PRNX1000  COMPARE   "55",CLNO
          GOTO      PRNX1100 IF LESS
          CALL      PRNX2000                 * Print new page head
.
PRNX1100  MOVE      "*Unknown Catchment* ",TDESC
          MATCH     SP3,TCCATCH
          GOTO      PRNX1110 IF EQUAL        * No catchment code
          PACK      KEY5,CODECH,TCCATCH
          CALL      RDCODE1
.
PRNX1110  PRINT     *1,TCCATCH,*8,TDESC;
.
          MOVE      ZERO,TCPERTOT
          MOVE      "28",MPRTPOS             * Print position
          MOVE      ZERO,COUNT
.
PRNX1200  ADD       SEVEN,MPRTPOS
          ADD       ONE,COUNT
          COMPARE   COUNT,MPERUSED
          GOTO      PRNX1500 IF LESS
          LOAD      FORM7,COUNT,TCPER1,TCPER2,TCPER3,TCPER4,TCPER5,TCPER6:
                                TCPER7,TCPER8,TCPER9,TCPER10,TCPER11,TCPER12:
                                TCPER13
          PRINT     *MPRTPOS,FORM7;
.
.         Add to clinic type monthly figures
.
          LOAD      FORM7Y,COUNT,TTPER1,TTPER2,TTPER3,TTPER4,TTPER5,TTPER6:
                                 TTPER7,TTPER8,TTPER9,TTPER10,TTPER11,TTPER12:
                                 TTPER13
          ADD       FORM7,FORM7Y
          STORE     FORM7Y,COUNT,TTPER1,TTPER2,TTPER3,TTPER4,TTPER5,TTPER6:
                                 TTPER7,TTPER8,TTPER9,TTPER10,TTPER11,TTPER12:
                                 TTPER13
          ADD       FORM7,TCPERTOT     *  Add to clinic id total
          GOTO      PRNX1200
.
PRNX1500  PRINT     *MPRTPOS,TCPERTOT
          ADD       ONE,CLNO
          RETURN
+
.----------------------------------------------------------------------------
.         Print New Page
.----------------------------------------------------------------------------
.
PRNX2000  MOVE      ONE,CNOUNDLN
          CALL      IBACLOCK    
.
PRNX2100  CALL      HEAD132
          PRINT     *N,*40,"PERIOD FROM ",PSTRDATE," TO ":
                       PENDDATE:
                    *N,*N,"Catchment/Description";
.
.         Loop over the period file and get the description for each column
.
          MOVE      "35",MPRTPOS  * Location to start actual print
          MOVE      VSTRDATE,KEY6
          CALL      RDDRGA1
PRNX2200  PACK      XDATE,DRGYR,DRGNUM
.
          MATCH     XDATE,VENDDATE
          GOTO      PRNX2300 IF LESS
.
          UNPACK    DRGCYR,CCENT,CYEAR
.
          PRINT     *MPRTPOS,SP1,DRGSDSC,SP1,CYEAR;
          ADD       SEVEN,MPRTPOS
.
          CALL      RDKDRGA1
          BRANCH    OVRCD,PRNX2300
          GOTO      PRNX2200
.
PRNX2300  PRINT     *MPRTPOS,"  Total":
                    *N,"----------------------------------";
.
          CALL      PRNX6000         * Print rest of separator lines
.
          MOVE      SEVEN,CLNO
          RETURN
.
.----------------------------------------------------------------------------
.         Print clinic type totals
.----------------------------------------------------------------------------
.
PRNX3000  CALL      PRNX6000           * Print separators
          COMPARE   "55",CLNO
          GOTO      PRNX3100 IF LESS
          CALL      PRNX2000           * Print new page head
.
PRNX3100  PRINT     "Total for Clinic Type ",SAVCTYPE;
.
          MOVE      ZERO,TTPERTOT
          MOVE      "28",MPRTPOS          * Print position
          MOVE      ZERO,COUNT
.
PRNX3200  ADD       SEVEN,MPRTPOS
          ADD       ONE,COUNT
          COMPARE   COUNT,MPERUSED
          GOTO      PRNX3500 IF LESS
          LOAD      FORM7,COUNT,TTPER1,TTPER2,TTPER3,TTPER4,TTPER5,TTPER6:
                                TTPER7,TTPER8,TTPER9,TTPER10,TTPER11,TTPER12:
                                TTPER13
          PRINT     *MPRTPOS,FORM7;
.
.         Add to group type monthly figures
.
          LOAD      FORM7Y,COUNT,TGPER1,TGPER2,TGPER3,TGPER4,TGPER5,TGPER6:
                                 TGPER7,TGPER8,TGPER9,TGPER10,TGPER11,TGPER12:
                                 TGPER13
          ADD       FORM7,FORM7Y
          STORE     FORM7Y,COUNT,TGPER1,TGPER2,TGPER3,TGPER4,TGPER5,TGPER6:
                                 TGPER7,TGPER8,TGPER9,TGPER10,TGPER11,TGPER12:
                                 TGPER13
          ADD       FORM7,TTPERTOT     *  Add to clinic type total
          GOTO      PRNX3200
.
PRNX3500  PRINT     *MPRTPOS,TTPERTOT
          CALL      PRNX6000           * Print separators
          ADD       THREE,CLNO
          CALL      CLRV1000           * Clear clinic type variables
          RETURN
.
.----------------------------------------------------------------------------
.         Print group totals
.----------------------------------------------------------------------------
.
PRNX4000  COMPARE   "55",CLNO
          GOTO      PRNX4100 IF LESS
          CALL      PRNX2000           * Print new page head
.
PRNX4100  MOVE      "*Unknown Group*     ",TDESC
          PACK      KEY5,CODECG,SAVGROUP
          CALL      RDCODE1
          MOVE      TDESC,DIM9
          PRINT     "Total for Group Type ",SAVGROUP,SP1,DIM9;
.
          MOVE      ZERO,TGPERTOT
          MOVE      "28",MPRTPOS          * Print position
          MOVE      ZERO,COUNT
.
PRNX4200  ADD       SEVEN,MPRTPOS
          ADD       ONE,COUNT
          COMPARE   COUNT,MPERUSED
          GOTO      PRNX4500 IF LESS
          LOAD      FORM7,COUNT,TGPER1,TGPER2,TGPER3,TGPER4,TGPER5,TGPER6:
                                TGPER7,TGPER8,TGPER9,TGPER10,TGPER11,TGPER12:
                                TGPER13
          PRINT     *MPRTPOS,FORM7;
.
.         Add to grand total figures
.
          LOAD      FORM7Y,COUNT,TZPER1,TZPER2,TZPER3,TZPER4,TZPER5,TZPER6:
                                 TZPER7,TZPER8,TZPER9,TZPER10,TZPER11,TZPER12:
                                 TZPER13
          ADD       FORM7,FORM7Y
          STORE     FORM7Y,COUNT,TZPER1,TZPER2,TZPER3,TZPER4,TZPER5,TZPER6:
                                 TZPER7,TZPER8,TZPER9,TZPER10,TZPER11,TZPER12:
                                 TZPER13
          ADD       FORM7,TGPERTOT     *  Add to group type total
          GOTO      PRNX4200
.
PRNX4500  PRINT     *MPRTPOS,TGPERTOT:
                    *N,"----------------------------------";
          CALL      PRNX6000           * Print separators
          ADD       TWO,CLNO
          CALL      CLRV2000           * Clear group type variables
          RETURN
.
.----------------------------------------------------------------------------
.         Print grand totals
.----------------------------------------------------------------------------
.
PRNX5000  COMPARE   "55",CLNO
          GOTO      PRNX5100 IF LESS
          CALL      PRNX2000           * Print new page head
.
PRNX5100  PRINT     "Grand Total";
.
          MOVE      ZERO,TZPERTOT
          MOVE      "28",MPRTPOS          * Print position
          MOVE      ZERO,COUNT
.
PRNX5200  ADD       SEVEN,MPRTPOS
          ADD       ONE,COUNT
          COMPARE   COUNT,MPERUSED
          GOTO      PRNX5500 IF LESS
          LOAD      FORM7,COUNT,TZPER1,TZPER2,TZPER3,TZPER4,TZPER5,TZPER6:
                                TZPER7,TZPER8,TZPER9,TZPER10,TZPER11,TZPER12:
                                TZPER13
          PRINT     *MPRTPOS,FORM7;
          ADD       FORM7,TZPERTOT     *  Add to group type total
          GOTO      PRNX5200
.
PRNX5500  PRINT     *MPRTPOS,TZPERTOT
          CALL      PRNX7000           * Print separators
          CALL      CLRV3000
          RETURN
.
.----------------------------------------------------------------------------
.         Print page separator type 1
.----------------------------------------------------------------------------
.
PRNX6000  MOVE      "28",MPRTPOS
          MOVE      ZERO,COUNT
PRNX6100  ADD       SEVEN,MPRTPOS
          ADD       ONE,COUNT
          COMPARE   COUNT,MPERUSED
          GOTO      PRNX6200 IF LESS
          PRINT     *MPRTPOS," ------";
          GOTO      PRNX6100
PRNX6200  PRINT     *MPRTPOS," ------"
          RETURN
.
.----------------------------------------------------------------------------
.         Print page separator type 2
.----------------------------------------------------------------------------
.
PRNX7000  MOVE      "28",MPRTPOS
          MOVE      ZERO,COUNT
PRNX7100  ADD       SEVEN,MPRTPOS
          ADD       ONE,COUNT
          COMPARE   COUNT,MPERUSED
          GOTO      PRNX7200 IF LESS
          PRINT     *MPRTPOS," ======";
          GOTO      PRNX7100
PRNX7200  PRINT     *MPRTPOS," ======"
          RETURN
.
.----------------------------------------------------------------------------
.         Print tail of report
.----------------------------------------------------------------------------
.
PRNX8000  PRINT     *N,*N,"*** END OF REPORT ***"
          RETURN
.
+
.**************************************************************************
.*                              CREA0000                                  *
.*                   Create temp file                                     *
.**************************************************************************
.
CREA0000  CALL      KILL0000
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    OUTTMP,CMDLINE
          APPEND    " 77 u1-12",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      OUTTMPXX,OUTTMP
          RETURN
+
.**************************************************************************
.*                              KILL0000                                  *
.*                   Remove temp file                                     *
.**************************************************************************
.
KILL0000  PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    OUTTMP,CMDLINE
          RESET     CMDLINE
          CLOSE     OUTTMPXX
          EXECUTE   CMDLINE,TASKID
          RETURN
+
.------------------------------------------------------------------------------
.         Temp file I/O's
.------------------------------------------------------------------------------
.
RDATMP1   MOVE      ZERO,OVRCD
          RESET     KEY12
          READ      OUTTMPXX,KEY12;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RDSTMP1   MOVE      ZERO,OVRCD
          RESET     KEY12
          READ      OUTTMPXX,KEY12;;
          RETURN
.
RDKTMP1   MOVE      ZERO,OVRCD
          READKS    OUTTMPXX;TGROUP,TCTYPE,TCCATCH:
                             TCPER1,TCPER2,TCPER3,TCPER4:
                             TCPER5,TCPER6,TCPER7,TCPER8:
                             TCPER9,TCPER10,TCPER11,TCPER12:
                             TCPER13
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTMP1    MOVE      ZERO,OVRCD
          RESET     KEY12
          READ      OUTTMPXX,KEY12;TGROUP,TCTYPE,TCCATCH:
                             TCPER1,TCPER2,TCPER3,TCPER4:
                             TCPER5,TCPER6,TCPER7,TCPER8:
                             TCPER9,TCPER10,TCPER11,TCPER12:
                             TCPER13
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTMP1    MOVE      ZERO,OVRCD
          RESET     KEY12
          WRITE     OUTTMPXX,KEY12;KEY12:
                                  TCPER1,TCPER2,TCPER3,TCPER4:
                                  TCPER5,TCPER6,TCPER7,TCPER8:
                                  TCPER9,TCPER10,TCPER11,TCPER12:
                                  TCPER13
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTMP1    UPDATE    OUTTMPXX;TGROUP,TCTYPE,TCCATCH:
                             TCPER1,TCPER2,TCPER3,TCPER4:
                             TCPER5,TCPER6,TCPER7,TCPER8:
                             TCPER9,TCPER10,TCPER11,TCPER12:
                             TCPER13
          RETURN
+
. =========================================================================
. Includes for all file I/O would be included here.
. Includes for all procedural routines would be included here.
. =========================================================================
.
           INC        STD001IO
           INC        PATCOMN3
           INC       TFILENAM
           INC       IBASEQIO/INC
           INC       OUTCTYIO/INC
           INC       OUTLOCIO/INC
           INC       PATCODIO/INC
           INC        PATDRGIO/INC
           INC       WEBERRIO/INC
.

.*****************************************************************************
.*              P R O G R A M  S U M M A R Y                                 *
.*              -------------  -------------                                 *
.*                                                                           *
.*     PROGRAM NAME:     IBAHOS15                                            *
.*                                                                           *
.*     FUNCTION:         This program will find all Current Patients at      *
.*                       Midnight & Sameday Disc.for the previous day with a *
.*                       specified Health fund and charge the selected Misc. *
.*                       Charge to the Pending Invoice.                      *
.*                                                                           *
.*     AUTHOR:           JENI TAN                                            *
.*                                                                           *
.*     DATE WRITTEN:     18/12/2012                                          *
.*                                                                           *
.*     MODIFICATIONS:                                                        *
.*****************************************************************************
.* V12.00.01 16/05/2025 Thanh T         TSK 0955096                          *
.*           Changed for alphanumeric visit number                           *
.*****************************************************************************
.* V11.05.01 29/11/2024  Thanh T        TSK 0939466                          *
.*           Added Distance in kms field                                     *
.*****************************************************************************
.*                   V11.01.01 18/02/2021  J.Tan           TSK 0888639       *
.*                             Added PMMITMNO - Theatre Team no              *
.*                   V10.13.03 23/10/2018  Tracey Nguyen    TSK 0859743      *
.*                             Added PMMIHOSI-Hopsital Indicator on PMSMTIFD *
.*                   V10.13.02 19/09/2018  J.Tan            TSK 0863727      *
.*                             Added Restrictive Override code               *
.*                   V10.13.01 21/08/2017  J.Tan        TSK 0859742          *
.*                             Added ECLIPSE new fields from pmsmtiaf        *
.*                   V10.11.01 31/08/2017  J.Tan        TSK 0837479          *
.*                             Added PMMICTYP - Consumption Type             *
.*                   V10.05.01 05/08/2014  Peter Vela   CAR 302164           *
.*                             Added PMMIRBRS - Rebate Reason CAT Rr         *
.*                   V10.04.02 16/04/2014  J.Tan        CAR 261630           *
.*                             Removed pataum audit files                    *
.*                   V10.04.01 17/01/2014  J.Tan        CAR 295008           *
.*                             Mods to set PMMIDTCR,PMMITMCR,PMMIIDCR        *
.*                             20/01/2014  J.Tan        CAR 249828           *
.*                             Added PMMIPMBS - Pat.MBS Team and Counter     *
.*                   V10.03.01 16/01/2013 J.Tan   CAR 279483                 *
.*                             Mods to include discharges from yesterday only*
.*                                                                           *
.******************************************************************************
.
          INC       STD001FD 
.
          INC       IBAPASFD/INC
          INC       PATMI1FD/INC
          INC       PATMA1FD/INC

          INC       PATMCHFD/INC
          INC       PATCODFD/INC
          INC       PATDSCFD/INC
. 
          INC       PATFN1FD/INC
          INC       PATGTUFD/INC
          INC       PATIPEFD/INC
          INC       PMSMTIFD/INC
.
          INC       PATTRNFD/INC
          INC       PATVISFD/INC
          INC       IBACONFD/INC
          INC       PATHSPFD/INC
          INC       PMSVX1FD/INC
.
+
CALDAYS   FORM      4
FROMDATE  DIM       8
FNAMEB    DIM       8
HEADSEC   FORM      5
.
KEY21A    DIM       21
DATFIELD  DATE      8
NBRDAYS   FORM      4
ONAME     DIM       30
OPERCODE  DIM       4
.
PTCNDCLM  DIM       3
PTCNIPEN  FORM      1
.
SEC1      FORM      "00001"
SAVTRNO   FORM      3
.
TODATE    DIM       8
TIMEIS    DIM       8
.
ADDRFLAG  FORM      1
.
XHFUND    DIM       6
XHFTAB    DIM       3
XITEMC    DIM       9
XITEMD    DIM       30
XNDAYS    FORM      4
XYDATE    DIM       8
XNOPAT    FORM      8
XSDATE    DIM       8
.
PRGID     INIT      "IBAHOS15"
PRGNAM    INIT      "Charge Patients with Misc.Item"
VERSION   INIT      "V12.01.00"
+
.         START OF PROGRAM
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
MAIN1000  CALL      DSCR0000                      * Display screen
.
          CALL      KHOS0000                      * Keyin Hospital ID
          BRANCH    EXIT,MAIN9999
.
          CALL      KHFN0000                      * Keyin Health fund
          BRANCH    EXIT,MAIN9999
.
          CALL      KTAB0000                      * Keyin Fund table type
          CALL      KITM0000                      * keyin Misc.items
.
          CALL      KDAY0000                      * keyin no of days to charge
          BRANCH    EXIT,MAIN1000
.
          CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN2000,MAIN1000,MAIN9999 * Yes, no, cancel
.
MAIN2000  CALL      PROC0000                      * processing
.
MAIN9999  CHAIN     PGM
          STOP
+
.******************************************************************************
.*                          INIT0000                                          *
.*  Display heading and check that the files needed exist&open them           *
.******************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A2,"patmi1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.         OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"patmchgf";
          OPEN	    PATMCHG1,"patmchgf"
.
          DISPLAY   *P64:24,"pmsmtiaf";
          OPEN	    PMSMTIA1,"pmsmtiaf"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN 	    PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN 	    PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA1,"patvisaf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patfn1af";
          OPEN      PATFN1A1,"patfn1af"
.
          DISPLAY   *P64:24,"patgtuaf";
          OPEN      PATGTUA1,"patgtuaf"
.
          DISPLAY   *P64:24,"patipenf";
          OPEN      PATIPEN1,"patipenf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"ihapassf";
          CALL       OPPASS1
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TWENTY;*193,PTCNDCLM
          READ      CONTROLF,SEVENTY9;*248,PTCNIPEN
.
          PACK      XCDATE,CCC,CYY,CMM,CDD  * Current Date
          REP       " 0",XCDATE
.
INIT9999  RETURN
+
.******************************************************************************
.         Display Screen
.******************************************************************************
DSCR0000  IF        IBCNMHOS=1
            DISPLAY   *P1:10,*EL,"Hospital Id         : ";
          ENDIF
.
          DISPLAY   *P1:11,*EL,"Health Fund         :":
                    *P1:12,*EL,"Fund Table Type     :":
                    *P1:13,*EL,"Misc.Charge Code    :":
                    *P1:14,*EL,"No of Days to Charge:"
          MOVE      ZERO,XNOPAT
.
DSCR9999  RETURN
+
.******************************************************************************
.*                                  KHOS0000              Called by: ML0000   *
.*                            Enter The hospital                              *
.******************************************************************************
KHOS0000  MOVE      ZERO,EXIT
          COMPARE   ONE,IBCNMHOS
          GOTO      KHOS9999 IF NOT EQUAL
.
          MOVE      TWENTY3,ECOL
          MOVE      TEN,EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS2000,KHOS9500
          GOTO      KHOS3000
.
KHOS2000  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          MOVE      SP10,PTHSDCLM
          MOVE      ZERO,EXIT
.
KHOS3000  DISPLAY   *P40:10,*V2LON,PTHSNAME;
          GOTO      KHOS9999
.
KHOS9500  MOVE      ONE,EXIT
KHOS9999  RETURN
+
.******************************************************************************
.       Keyin Health Fund
.******************************************************************************
KHFN0000  KEYIN     *P23:11,*EL,*V2LON,XHFUND;
          ENDSET    XHFUND
          APPEND    SP70,XHFUND
          RESET     XHFUND
.
          MOVE      "0000    ",HFTABL
          PACK      KEY14,XHFUND,HFTABL,SP70
          CALL      RDFUND1
          BRANCH    OVRCD,KHFN9000
.
          DISPLAY   *P35:11,*V2LON,HFNAME;
          MOVE      ZERO,EXIT
          GOTO      KHFN9999
.
KHFN9000  MOVE      ONE,EXIT
          DISPLAY   *P1:24,*EL,"Invalid Health fund";
KHFN9999  RETURN
+
.******************************************************************************
.       Keyin Health Fund table type
.******************************************************************************
KTAB0000  KEYIN     *P23:12,*EL,*V2LON,XHFTAB;
          ENDSET    XHFTAB
          APPEND    SP70,XHFTAB
          RESET     XHFTAB
.
          MOVE      "HT",KEY2
          PACK      KEY5,KEY2,XHFTAB
          CALL      RDCODE1
          IF        OVRCD=0
            DISPLAY   *P35:12,*V2LON,TDESC;
          ENDIF
.
KTAB9999  RETURN
+
.******************************************************************************
.       Keyin Misc.Item
.******************************************************************************
KITM0000  KEYIN     *P23:13,*EL,*V2LON,XITEMC;
          ENDSET    XITEMC
          APPEND    SP70,XITEMC
          RESET     XITEMC
.
          MOVE      SP70,XITEMD
          OPEN      PATMCHG4,"patmchgf"
          PACK      KEY29,XITEMC,Z70
          CALL      RDSMCHG4
          CALL      RDPMCHG4
          IF        OVRCD=0
            MATCH     XITEMC,MCHARGE
            IF         @EQUAL
              PACK      XITEMD,MDESC,SP70
            ENDIF
          ENDIF
.
          PACK      KEY8,CCC,CYY,CMM,CDD          * Current date
          REP       " 0",KEY8
          MOVE      KEY8,DATFIELD
          SUB       ONE,DATFIELD                  * set to yesterday date
          MOVE      DATFIELD,XYDATE
.
KITM9999  RETURN
+
.******************************************************************************
.       Keyin Number of days to Charge
.******************************************************************************
KDAY0000  MOVE      ZERO,EXIT
          MOVE      ZERO,XNDAYS
          KEYIN     *P23:14,*EL,*V2LON,XNDAYS;
          COMPARE   XNDAYS,ZERO
          GOTO      KDAY9999 IF LESS
.
KDAY9000  MOVE      ONE,EXIT
KDAY9999  RETURN
+
.******************************************************************************
.       Get the current patient
.******************************************************************************
PROC0000  DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** Processing Current Admission **";
.
          MOVE      "60",CLNO
          PACK      KEY9 WITH TWO,SP70
          CALL      RDSMISS2
PROC1000  CALL      RDKMISS2
          BRANCH    OVRCD OF PROC2000
.
          COMPARE   TWO,ASTAT
          GOTO      PROC2000 IF NOT EQUAL
.
          MATCH     XCDATE,ADATE
          GOTO      PROC1000 IF EQUAL       * Skip if admitted on Current date
.
          DISPLAY   *P66:24,AADMNO;
.
          CALL      CITM0000                * Charge daily misc.item
          GOTO      PROC1000
.
.         Get on leave patient
.
PROC2000  DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** Processing On Leave Admission **";
.
          PACK      KEY9 WITH FOUR,SP70
          CALL      RDSMISS2
PROC2200  CALL      RDKMISS2
          BRANCH    OVRCD OF PROC3000
.
          COMPARE   FOUR,ASTAT
          GOTO      PROC3000 IF NOT EQUAL
.
          MATCH     XCDATE,ADATE
          GOTO      PROC2200 IF EQUAL       * Skip if admitted on Current date
.
          DISPLAY   *P66:24,AADMNO;
.
          CALL      CITM0000                * Charge misc.code
          GOTO      PROC2200
.
.         Get discharges
.
PROC3000  DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** Processing Discharges **";
.
          PACK      KEY16,XYDATE,SP70       * position on yesterday date
          CALL      RDSDSCH2
PROC3200  CALL      RDKDSCH2
          BRANCH    OVRCD,PROC8000
.
          MATCH     DDATE,XCDATE            * not include today discharges
          GOTO      PROC8000 IF EQUAL
.
          MOVE      DADMNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,PROC3200
.
          DISPLAY   *P66:24,AADMNO;
.
          CALL      CITM0000                * Charge misc.code
          GOTO      PROC3200
.
PROC8000  CALL      ENDP0000                * Check End of Report
.
PROC9999  RETURN
+
.******************************************************************************
.         Charge daily Misc.Charge
.******************************************************************************
CITM0000  MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF CITM9999
.
          COMPARE   ONE,IBCNMHOS
          GOTO      CITM1000 IF NOT EQUAL   * Not multi hospital
.
          MATCH     SP70,PTHSHOSP           * All Hospitals
          IF        !@EQUAL
            PACK      KEY8,AADMNO,SP70
            CALL      RDPMVX11
            IF        OVRCD=0
              MATCH     PMVXMHOS,PTHSHOSP   * Correct hospital
              GOTO      CITM9999 IF NOT EQUAL
            ENDIF
          ENDIF
.
CITM1000  CALL      CFND0000                * Check for Selected HF/Table
          BRANCH    EXIT,CITM9999
.
          CALL      CLOS0000                * Check LOS < no of days to charge
          BRANCH    EXIT,CITM9999
.
          MOVE      ZERO,ADDRFLAG           * flag for adding item to pending
          CALL      CMSC0000                * Charge Misc.Item
          COMPARE   ZERO,ADDRFLAG
          GOTO      CITM9999 IF EQUAL       * Item has already been added
.
          CALL      UGTU0000                * Write to GST unknown if necessary
.
.         Update next transaction number in admission record
.
          ADD       ONE,ATRANNO
          MOVE      ATRANNO,SAVTRNO
          MOVE      AADMNO,KEY8
          CALL      RDMISS1
.
.         Update & Release Admission record
.
          MOVE      SAVTRNO,ATRANNO
          CALL      UPMISS1
.
          CALL      WIPE0000         * Write a record Invoice pendi
.
          CALL      PLIN0000         * Print patient details
.
CITM9999  RETURN
+
.******************************************************************************
.         Check health fund
.******************************************************************************
CFND0000  MOVE      ONE,EXIT
          MATCH     AFUNDH,XHFUND
          GOTO      CFND9999 IF NOT EQUAL     * different health fund
.
          PACK      KEY14,AFUNDH,AFNDTB,SP70
          CALL      RDFUND1
          BRANCH    OVRCD,CFND9999
.
          MATCH     PTHFBCAT,XHFTAB           * Same table type?
          GOTO      CFND9999 IF NOT EQUAL
.
.         Check for Admission type Exclusion indicator
.
          MOVE      "A ",KEY2
          PACK      KEY5,KEY2,ATYPE
          CALL      RDCODE1
          BRANCH    OVRCD,CFND9999
          MATCH     "1",TCINDC7
          GOTO      CFND9999 IF EQUAL         * Excl.from daily Incidental chrge
.
          MOVE      ZERO,EXIT
CFND9999  RETURN
+
.******************************************************************************
.         Check if LOS less than or equal to  No of days to charge
.******************************************************************************
CLOS0000  PACK      TODATE,CCC,CYY,CMM,CDD    * Default to Current date
          COMPARE   THREE,ASTAT
          GOTO      CLOS1000 IF NOT EQUAL
.
          MATCH     ADATE,DDATE
          GOTO      CLOS8000 IF EQUAL       * Daycase
          MOVE      DDATE,TODATE
.
CLOS1000  REP       " 0",TODATE
          MOVE      ADATE,FROMDATE
.
          CALL      NHOSPDAY
          COMPARE   ZERO,NBRDAYS
          GOTO      CLOS9000 IF EQUAL
          ADD       ADYHOSP,NBRDAYS           * Includes Days of Hospitalisation
.
.         Exclude patient's with LOS more than the 'No of Days to charge'
.
          COMPARE   NBRDAYS,XNDAYS
          GOTO      CLOS9000 IF LESS 
.
CLOS8000  MOVE      ZERO,EXIT
          GOTO      CLOS9999
.
CLOS9000  MOVE      ONE,EXIT
CLOS9999  RETURN
+
.******************************************************************************
.         Get Misc.Charge
.******************************************************************************
MCHR0000  UNPACK    SP20,PTHFBCAT      * change "H/F Table" to "Table Type"
          PACK      KEY14,AFUNDH,AFNDTB,SP20
          CALL      RDFUND1            * "Table Type code" now in PTHFBCAT

          PACK      KEY29,ACLAIM,AFUNDH,PTHFBCAT,XITEMC,XSDATE,SP10
          CALL      PATMCHRD                * find Miscellaneous Charge Item
          IF        EXIT = 1
            PACK      KEY29,ACLAIM,SP6,SP3,XITEMC,XSDATE,SP10
            CALL      PATMCHRD              * find Miscellaneous Charge Item
            IF        EXIT = 1
              CALL      DCLM0000            * set Hospital default
              PACK      KEY29,PTCNDCLM,SP6,SP3,XITEMC,XSDATE,SP10
              CALL      PATMCHRD            * find Miscellaneous Charge Item
              BRANCH    EXIT,MCHR9999       * not found or invalid
            ENDIF
          ENDIF
MCHR9000  MOVE      ZERO,EXIT
MCHR9999  RETURN
+
.******************************************************************************
.         Charge the selected Misc.Item from Admission date
.******************************************************************************
CMSC0000  MOVE      ADATE,XSDATE            * Start from Admission date
          CALL      WITM0000                * Write a record to pmsmtiaf
          IF        ASTAT=3
            MATCH     ADATE,DDATE
            GOTO      CMSC9999 IF EQUAL     * Daycase
          ENDIF
.
CMSC1000  MOVE      XSDATE,DATFIELD
          ADD       ONE,DATFIELD            * next date
          MOVE      DATFIELD,XSDATE
.
          IF        ASTAT=3
            MATCH     DDATE,XSDATE
            GOTO      CMSC9999 IF NOT LESS  * do not charge on Discharged date
          ENDIF
.
          MATCH     XSDATE,XYDATE           * only charge up to yesterday
          GOTO      CMSC9999 IF LESS
.
          CALL      CLEV0000                * Check if patient on leave
          BRANCH    EXIT,CMSC1000           * patient On leave, check next day
.
          CALL      WITM0000                * Write a record to pmsmtiaf
          GOTO      CMSC1000
.
CMSC9999  RETURN
+
.****************************************************************************** 
.         Check if patient is on leave on the date
.******************************************************************************
CLEV0000  PACK      KEY30,AADMNO,XSDATE,Z70
          CALL      RDSTRAN2
CLEV1000  CALL      RDPTRAN2
          BRANCH    OVRCD,CLEV9000
.
          MATCH     AADMNO,TADMN
          GOTO      CLEV9000 IF NOT EQUAL
.
          CMATCH    "L",TMOVE
          GOTO      CLEV9000 IF EQUAL        * On leave
.
CLEV8000  MOVE      ZERO,EXIT
          GOTO      CLEV9999
.
CLEV9000  MOVE      ONE,EXIT
CLEV9999  RETURN
+
.****************************************************************************** 
.         Check if the Misc.Charge had already been charged for the day
.******************************************************************************
CHKI0000  MOVE      ZERO,EXIT
          REP       " 0",XSDATE
          PACK      KEY14,AADMNO,SP70
          CALL      RSPMMTI1
CHKI1000  CALL      RKPMMTI1
          BRANCH    OVRCD,CHKI9999
.
          MATCH     DAADMNO,PMMIVISN
          GOTO      CHKI9999 IF NOT EQUAL    * No charges
.
          MATCH     PMMITDAT,XSDATE          * Same date?
          GOTO      CHKI1000 IF NOT EQUAL
.
          MATCH     PMMIITEM,XITEMC          * Same item code?
          GOTO      CHKI1000 IF NOT EQUAL
          MOVE      ONE,EXIT
.
CHKI9999  RETURN
+
.**********************************************************************
.*                              WITM0000                              *
.*                   Write an item charge                             *
.**********************************************************************
WITM0000  CALL      CHKI0000                * Check if Item had been charged
          BRANCH    EXIT,WITM9999           * already been charged, try next day
.
          MOVE      AADMNO,KEY8
WITM1000  CALL      TRVISA1                 * Get the last transaction number
          MOVE      PVITRAN,PMMITRAN
          MOVE      PMMITRAN,ATRANNO
          MOVE      AADMNO,PMMIVISN
.
          PACK      KEY14 WITH PMMIVISN,PMMITRAN,SP70
          CALL      RAPMMTI1
          COMPARE   ZERO,OVRCD
          GOTO      WITM1000 IF EQUAL       * Already exist
.
          CALL      MCHR0000                * Get Misc.Charge
          IF        EXIT=1
            MOVE      ZERO,MPATPOR
            MOVE      ZERO,MHFPOR
            MOVE      XITEMC,MCHARGE
            MOVE      XITEMD,MDESC
            MOVE      ZERO,PTMCGSTA
            MOVE      SP70,PTMCGSTC
            MOVE      SP70,MMSCGRP
            MOVE      THREE,MITMTYP
            MOVE      ONE,MNINV
          ENDIF
.
          MOVE      PVIURNO,PMMIURNO
          MOVE      PVITYPE,PMMISYST
          CALL      STRN0000
.
          PACK      KEY14 WITH PMMIVISN,PMMITRAN,SP70
          CALL      WRPMMTI1
          ADD       ONE,ADDRFLAG
.
WITM9999  RETURN
+
.******************************************************************************
.         Subroutine to setup the DTRAN record ready for posting
.******************************************************************************
STRN0000  MOVE      SP2,PMMISESS
          MOVE      MMSCGRP,PMMIMGRP
          MOVE      ONE,PMMIBTCH
          MOVE      ZERO,PMMIAMTG
          MOVE      ZERO,PMMIAMTH
          MOVE      ZERO,PMMIAMTP
          MOVE      SP70,PMMIREFN
          MOVE      MITMTYP,PMMIRTYP
          MOVE      ZERO,PMMIMVBR
          MOVE      MNINV,PMMIINVN
.
          MOVE      MPATPOR,PMMIAMTP
          MOVE      MPATPOR,PMMIAMTT
          ADD       MHFPOR,PMMIAMTT
          MOVE      MHFPOR,PMMIRBAT
          MOVE      XSDATE,PMMITDAT
.
          MOVE      MCHARGE,PMMIITEM
          MOVE      MDESC,PMMIDESC
          MOVE      SP70,PMMIDES2
          MOVE      SP6,PMMITDOC
          MOVE      SP6,PMMIODOC
          MOVE      ONE,PMMISERV
          MOVE      ZERO,PMMIGSTA
          MOVE      SP6,PMMIGSTC
          IF        IBCNUGST=2
            MOVE      PTMCGSTA,PMMIGSTA
            MOVE      PTMCGSTC,PMMIGSTC
          ENDIF
          MOVE      ZERO,PMMIGSTM
          MOVE      SP70,PMMIUNIQ
          MOVE      SP70,PMMICNTR
          MOVE      SP70,PMMISUNQ
          MOVE      SP70,PMMIINCT
          MOVE      SP70,PMMISUBN
          MOVE      SP70,PMMIEDAD
          MOVE      SP70,PMMISVTM
          MOVE      SP70,PMMICCON
          MOVE      ZERO,PMMISCHF
          MOVE      SP70,PMMIITYP
          MOVE      SP70,PMMIDUPD
          MOVE      SP70,PMMITUPD
          MOVE      SP70,PMMIWUSR
          PACK      PMMIDTCR,CCC,CYY,CMM,CDD
          REP       " 0",PMMIDTCR
          CALL      IBACLOCK
          MOVE      CTIMEIS,PMMITMCR
          MOVE      PASSCODE,PMMIIDCR
          MOVE      SP70,PMMITMNO
          MOVE      SP70,PMMIPMBS
          MOVE      SP70,PMMIRBRS
          MOVE      SP70,PMMICTYP
          PACK      PMMIACOI,SP70       * After Care Override Indicator
          PACK      PMMIDSOV,SP70       * Duplicate Service Override
          PACK      PMMISTXT,SP70       * Service Text
          PACK      PMMILSPN,SP70       * Location Specific Practice No(LSPN)
          PACK      PMMIMPOV,SP70       * Multi Procedure Override
          PACK      PMMINMPT,SP70       * Number of Patients Seen
          PACK      PMMISDCD,SP70       * Self Deem Code (Cat Sd)
          PACK      PMMITDUR,SP70       * Time Duration (Mins)
          PACK      PMMIROVR,SP70       * Restricted Override Code (Cat Ro)
          PACK      PMMIHOSI,SP70       * Hospital Indicator         *T0859743
          MOVE      SP70,PMMIDKSM       * Distance in kms
.
          MOVE      SP70,PMMISPAR
STRN9999  RETURN
+
.**********************************************************************
.*                              UGTU0000                              *
.*                   Write/Update unknown GST file if necessary       *
.**********************************************************************
UGTU0000  COMPARE   TWO,PTMCGSTA
          GOTO      UGTU9999 IF NOT EQUAL   * Not unknown
.
          MOVE      "2",PTMCGSTA            * Unknown GST Applicable flag
          MOVE      SP8,PTMCGSTC
          MOVE      AADMNO,PTGTADMN
          PACK      KEY8,AADMNO
          CALL      RDPTGTU1                * Check if a record exists
          IF        OVRCD=0
            MOVE      ONE,PTGTGSTM          * set up unknown GST misc.
            CALL      UPPTGTU1
          ELSE
            MOVE      ZERO,PTGTGSTA
            MOVE      ONE,PTGTGSTM          * set up unknown GST misc.
            CALL      WRPTGTU1
          ENDIF
UGTU9999  RETURN
+
.******************************************************************************
.         Write Invoice pending if necessary
.******************************************************************************
WIPE0000  COMPARE   ZERO,PTCNIPEN
          GOTO      WIPE9999 IF NOT EQUAL           * Not using Invoice pending
.
          MOVE      IPADMNO,KEY8
          CALL      RDIPEN1
          COMPARE   ZERO,OVRCD
          GOTO      WIPE9999 IF EQUAL               * already exists
.
          MOVE      PVIBILL,IPADMNO
          MOVE      PVITYPE,IPSYSTM
          MOVE      SP70,IPSITE
          MOVE      SP70,PTIPRHLD
          MOVE      SP70,PTIPRDES
          MOVE      "7",PTIPRSTA                    * Add item
          MOVE      SP70,PTIPSVAR
          CALL      WRIPEN1
.
WIPE9999  RETURN
+
.******************************************************************************
.         Print patient details
.******************************************************************************
PLIN0000  COMPARE   "50",CLNO
          CALL      PHED0000 IF NOT LESS
.
          MOVE      PTITL,PACTITLE
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      TWO,PACFRMT
          CALL      PACNAME                 * Pack Full Name
.
          PRINT     *1,"| ",AADMNO,*13,"| ",AURNO,*26,"| ",XITEMC:
                    *39,"| ",PACFNAME,*132,"|"
          ADD       ONE,CLNO
          ADD       ONE,XNOPAT
.
PLIN9999  RETURN
+
.******************************************************************************
.         Heading
.******************************************************************************
PHED0000  CALL      IBACLOCK
          MOVE      ZERO,CNOUNDLN
          CALL      HEAD132
.
          UNPACK    SP70,HFNAME,TDESC
          MOVE      "0000    ",HFTABL
          PACK      KEY14,XHFUND,HFTABL,SP70
          CALL      RDFUND1
          IF        OVRCD=0
            MOVE      "HT",KEY2
            PACK      KEY5,KEY2,XHFTAB
            CALL      RDCODE1
          ENDIF
.
          IF        IBCNMHOS =1
            PRINT     *40,"Hospital            : ",PTHSHOSP,*72,PTHSNAME
            MOVE      FOUR,CLNO
          ELSE
            MOVE      THREE,CLNO
          ENDIF
.
          PRINT     *40,"Health Fund         : ",XHFUND,*72,HFNAME:
                    *N,*40,"Fund Table Type     : ",XHFTAB,*72,TDESC:
                    *N,*40,"Misc.Charge Code    : ",XITEMC,*72,XITEMD:
                    *N,*40,"No of Days to Charge: ",XNDAYS:
                    *N
          ADD       FIVE,CLNO
.
          CALL      UND132
          PRINT     *1,"| Admission",*13,"| U/R Number ",*26,"| Misc.Charge":
                    *39,"| Patient Charged",*132,"|"
          ADD       ONE,CLNO
          CALL      UND132
.
PHED9999  RETURN
+
.******************************************************************************
.         End of Report
.******************************************************************************
ENDP0000  COMPARE   ZERO,XNOPAT
          GOTO      ENDP1000 IF NOT EQUAL
.
          CALL      NDAT0000               * No data
          CALL      PHED0000               * print headings
          GOTO      ENDP8000
.
ENDP1000  CALL      UND132
          PRINT     *N,*2,"Number of Patients Charged : ",XNOPAT
.
          DISPLAY   *P1:24,*EL,*P22:24,*V2LON:
                    "***  Report Generation Completed  ***",*W2;
.
ENDP8000  PRINT     *N," ***  End of Report  ***"
.
ENDP9999  RETURN
+
.**********************************************************************
.*                              NDAT0000                              *
.*                           No Data Available                        *
.**********************************************************************
NDAT0000  DISPLAY   *P1:24,*EL,*P25:24,*B,*V2LON:
                    "** No available data on file **",*W2;
          RETURN
+
.******************************************************************************
         INC       NHOSPDAY
         INC       PATHSPKY
         INC       PATMCHRD                * Miscellaneous Charges read routine
         INC       PATDEFCL
         INC       IBAPASIO/INC
         INC       PATMI1IO/INC
         INC       PATMA1IO/INC
         INC       PATMCHIO/INC
         INC       PATCODIO/INC
         INC       PATDSCIO/INC
.
         INC       PATFN1IO/INC
         INC       PATGTUIO/INC
         INC       PATIPEIO/INC
         INC       PMSMTIIO/INC
.
         INC       PATTRNIO/INC
         INC       PATVISIO/INC
         INC       PATHSPIO/INC
         INC       PMSVX1IO/INC
.
         INC        STD001IO
.

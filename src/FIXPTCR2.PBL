.***************************************************************************
.* System    :   Inpatients                                                *
.* Program   :   F93PTCNO                                                  *
.* Desc      :   Fix program for Credit note file                          *
.***************************************************************************
.* Date      :   22/10/2004                                                *
.* Author    :   J.Tan     CAR 52603                                       *
.* Function  :   This program will check the total credit notes amount     *
.*               from pmscnoaf against credit notes amount from invoice    *
.*               file                                                      *
.*                                                                         *
.* Modifications : V9.09.01 12/10/2007 J.Tan    CAR 152177                 *
.*                 Mods to include lumpsum amount form pmsfciaf file       *
.*                                                                         *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATINVFD/INC
          INC       PMSCNOFD/INC
          INC       PMSFCIFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
SAVCRNO   DIM      8
ERRFLAG   FORM     1
FORM10    FORM     10.2
FORM12    FORM     12.2
FORM12A   FORM     12.2
RECNUM    FORM      8                       * Record counter
CURRDATE  DIM       8                       * Current Date
.
PRGID     INIT      "F93PTCN2"
PRGNAM    INIT      "Fix program for patcnoaf"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                * Display heading
.
          MOVE      ZERO,RECNUM             * Initialize Record counter
.
          CALL      IBACLOCK                * Get current date
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          OPEN      PATINVR1,"patinvrf"
          OPEN      PMSCNOA3,"pmscnoaf"
          OPEN      PMSFCIA1,"pmsfciaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Checking Credit Notes amount"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
PROC0000  PRINT     *1,"Adm.No",*10,"Inv.No.",*20,"filename",*30,"Amount":
                    *45,"filename",*55,"Amount":
                    *70,"filename",*80,"Amount"
          PACK      KEY8,SP70
          CALL      RDSINV1 
PROC0100  CALL      RDKINV1
          BRANCH    OVRCD,PROC9999
          ADD       ONE,RECNUM                   * Update record counter
          IF        (RECNUM%1000) = 0
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          MOVE      ZERO,ERRFLAG
          MOVE      ZERO,FORM1
          MOVE      PTINCRST,FORM1
          BRANCH    FORM1,PROC0200,PROC0200
          GOTO      PROC0100
.
PROC0200  
          CALL      CFCI0000               * Check pmsfciaf file
          COMPARE   FORM12,PTINCRTT
.         GOTO      PROC0300 IF EQUAL
.
          GOTO      PROC0100 IF EQUAL
          MOVE      ONE,ERRFLAG
.
PROC0300  
.         CALL      CPCN0000
.         COMPARE   FORM12A,PTINCRTT
.         GOTO      PROC4000 IF EQUAL
.
.         SUB       PTINROUN,FORM12A
.
.         COMPARE   FORM12A,PTINCRTT
.         GOTO      PROC4000 IF EQUAL
.         GOTO      PROC0100 IF EQUAL
.
          MOVE      ONE,ERRFLAG
.
PROC4000  COMPARE   ZERO,ERRFLAG
          GOTO      PROC0100 IF EQUAL      * No error
.
          PRINT     *1,PINVADM,*10,PINVNO,*20,"  patinv",*30,PTINCRTT:
                    *45,"  pmsfci",*55,FORM12
.
.                   *70,"  pmscno",*80,FORM12A
          GOTO      PROC0100
.
PROC9999  RETURN
+
. =========================================================================
.         Check pmsfciaf file
. =========================================================================
CFCI0000  MOVE      ZERO,FORM12
          PACK      KEY30,PINVADM,PINVNO,SP70
          CALL      RSPMFCI1
CFCI1000  CALL      RKPMFCI1
          BRANCH    OVRCD,CFCI9999
.
          MATCH     DPINVADM,PMFCVISN
          GOTO      CFCI9999 IF NOT EQUAL
          MATCH     PINVNO,PMFCINVN
          GOTO      CFCI9999 IF NOT EQUAL
.
          ADD       PMFCCAMT,FORM12
          ADD       PMFCCGST,FORM12
          ADD       PMFCPTLS,FORM12
          ADD       PMFCRBLS,FORM12
          GOTO      CFCI1000
.
CFCI9999  RETURN
.
. =========================================================================
.         check pmscnoaf
. =========================================================================
CPCN0000  MOVE      ZERO,FORM12A
      MOVE    SP70,SAVCRNO
          PACK      KEY16,PINVNO,SP70
          CALL      RSPMCNO3
CPCN1000  CALL      RKPMCNO3
          BRANCH    OVRCD,CPCN9999
          MATCH     PINVNO,PMCNINVN
          GOTO      CPCN9999 IF NOT EQUAL
          ADD       PMCNAMNT,FORM12A
   MOVE   PMCNCRNO,SAVCRNO
          GOTO      CPCN1000
.
CPCN9999  RETURN
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       PATINVIO/INC
          INC       PMSCNOIO/INC
          INC       PMSFCIIO/INC

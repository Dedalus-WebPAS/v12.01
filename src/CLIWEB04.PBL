.----------------------------------------------------------------------
. Program       : CLIWEB04
.
. Function      : Clinical Result Detail Server
.
. Modifications : 
.----------------------------------------------------------------------
. V11.02.01 09/02/2022  Thanh T       TSK 0905641
.           Recompiled as OUTMA1FD (PATMASTM) changes
. V11.00.01 09/12/2020  Thanh T        TSK 0872840
.           Added PMSCCDFD/PMSCCDIO as PMSPX2TM changes
. V10.11.02 27/11/2017  Thanh Tieu     Task 0821710
.           Recompiled as PATMASTM changed
. V10.11.01 30/07/2017  Thanh T.       TSK 0821710
.           Audit admission/outpatient/UR comments. changed PATMASTM/PMSPX2TM.
.----------------------------------------------------------------------
. V10.04.01 07/04/2014  Peter Vela      CAR 286258
.           Recompiled for PATMASTM
. V10.03.01 23/11/2011  Ebon Clements   CAR 255738
.           Show ward beds in short description order
. V9.08.01  31/01/2007  Peter Vela     CAR 127930
.           Recompiled for MRTMASFD
. V9.05.01  09/03/2006  Ebon Clements CAR 78533
.           Mods for PATCODTM - Hospital specific category codes
. V9.03.02  07/06/2004 Peter Vela CAR 49016
.           2004 Statutory Changes - recompiled for PMSPX2TM
. V9.03.01  26/03/2004 Peter Vela CAR 13038
.           Recompiled for PATDOCCD - Doctor Name format Parameter
.           10/02/2004 Peter Vela CAR 38290
.           Added functionality for opening yearly RESLNKFD files (reln1999,
.           reln2000, reln2001, reln2002, etc...)
.           Added functionality for opening yearly RESAUDFD files (reau1999,
.           reau2000, reau2001, reau2002, etc...)
. V9.02.03  07/02/2003 Tonii CAR 35992
.           Recompiled for PATMISTM - inactive codes in patfundf
. V9.02.02  09/10/2002 Peter Vela               SRF 17693
.           Recompiled for PATMISTM - Admitting Point
. V9.02.01  03/12/2001 Katie Nelson/Harvinder   SRF 648628(nz)
.           Recompiled for NHI tags added to PATMASTM
. V9.01.02  16.08.2001 J.Tan
.           Recompiled for PATMASTM
. V9.01.01  13.08.2001 J.Tan
.           Recompiled for PATMASTM
. V9.00.B02 13.07.2001 Sandra Barcham
.           Replace GP with HCP
.----------------------------------------------------------------------
.                 V5.09.02 12/01/2001 Bronko Sosic  
.                          Recompiled for PATMASTM / PATMISTM
.                 V5.08.03 28/09/2000 Bronko Sosic  
.                          Recompiled for PATMASTM / PATMISTM
.                 V5.08.02  16/08/2000  Caleb Sharman
.                          Changed Health Fund variables to be 8 chars
.                V5.08.01 07/06/2000 Bronko Sosic
.                          Recompiled for PATMASTM
.----------------------------------------------------------------------
.                 V5.07.04 02/02/2000 Steven Watkins
.                          Recompiled for PATMASTM / PATMISTM
.                 V5.07.03 28.01.2000 B.G.Ackland    
.                          Recompiled for IBAWEBDF/IBAWEBCD/PATWRDTM      
.                 V5.07.02 20.12.1999 K.C.Nelson     
.                          Recompiled for IBAWEBDF/IBAWEBCD/PATWRDTM      
.                 V5.07.01 03.11.1999 K.C.Nelson     
.                          Recompiled for WEBDATCD/DAYOFWEK      
.                 V5.07.00 27.10.1999 K.C.Nelson     
.                          Port from 6.06 to 5.07                
.----------------------------------------------------------------------
.                 V6.06.05 25.06.1999 B.G.Ackland
.                          Results by Collection Date 
.
.                 V6.06.04 18.06.1999 B.G.Ackland
.                          Option to allow show/hide for header sectons of res.
.
.                 V6.06.03 01.06.1999 B.G.Ackland
.                          Option to relink Result to another patient U.R
.
.                 V6.06.02 31.05.1999 B.G.Ackland
.                          Option to show result details only
.
.                 V6.06.01 13.05.1999 B.G.Ackland
.                          Added Next Next and Last Links
.
.                 V6.06.00 31.03.1999 K.C.Nelson
.                          Port from 6.05 to 6.06
.
.                 V5.06.01 03.12.1998 B.G.Ackland
.
.----------------------------------------------------------------------
          INC       IBAWEBDF    
.
          INC       WEBDATDF
.
          INC       APSMASFD/INC
          INC       AAEDE1FD/INC
          INC       PATHSPFD/INC
          INC       PATIN1FD/INC
          INC       PATPR1FD/INC
          INC       PATMA1FD/INC
          INC       PMSHCPFD/INC
          INC       PMSHCGFD/INC
          INC       PMSHCPTD/INC
          INC       PMSHCGTD/INC
          INC       PATMI1FD/INC
          INC       PMSPX2FD/INC
          INC       PMSPX2TD/INC
          INC       PMSVX1FD/INC
          INC       PMSUCHFD/INC
          INC       PMSUCDFD/INC
          INC       PATMASTD/INC
          INC       PATCONFD/INC
          INC       PATDHEAD/INC
.
          INC       RESAUDFD/INC
          INC       RESLNKFD/INC
          INC       RESLOGFD/INC
          INC       RESPHRFD/INC
          INC       RESPDSFD/INC
          INC       RESPIDFD/INC
.
          INC       PATCALAG/INC
          INC       PATDO1FD/INC
          INC       PATNURFD/INC
          INC       PATVISFD/INC
          INC       PATWR1FD/INC
          INC       PATALRFD/INC
          INC       PMSCCDFD/INC            * Concession Card Details
.
          INC       OUTSITFD/INC
          INC       MRTLOCFD/INC
          INC       SCRMASFD/INC
          INC       PATFN1FD/INC
          INC       MRTMASFD/INC
          INC       NHIMASFD/INC
          INC       NHIETHFD/INC
.
FORM8     FORM      8
PITF200   FORM      1
PITF300   FORM      1
DHMNAM    DIM       12
DHMLOW    DIM       5 
DHMHIG    DIM       5 
DHMVAL01  DIM       10
DHMVAL02  DIM       10
DHMVAL03  DIM       10
DHMVAL04  DIM       10
DHMVAL05  DIM       10
DHMVAL06  DIM       10
DHMVAL07  DIM       10
.
TEXTFLAG  FORM      1
GRPFLAG   FORM      1
GRPHWID   DIM       3
GRPHHGH   DIM       3
DIM8      DIM       8
F6P4      FORM      6.4
F5P3      FORM      5.3
REFRLOW   FORM      5.3
REFRHIG   FORM      5.3
CUMLGPH   FORM      1
STRFLG    FORM      1
POSCNT    FORM      3
CNTVAL    FORM      3
HEADCOL   FORM      3
CMLROWNO  FORM      3
ENDROWNO  FORM      3
HEADDAT   DIM       12[10]
HEADSTR   FORM      3[10]
HEADEND   FORM      3[10]
CMLROWHD  DIM       25[50]
DSETDATE  DIM       80[50]
DSET0VAL  DIM       127[50]
DSET1VAL  DIM       127[50]
DSET2VAL  DIM       127[50]
.
PIDFLAG   FORM      1
TXT127    DIM       127
COLORSTR  DIM       12
TXTLEN    FORM      3
HIGHCODE  DIM       4
.
SAVEWUID  DIM       10
RESULTYR  DIM       4
YEAROPEN  DIM       4
TABLEFMT  DIM       1
.
RESFNAME  DIM       8
.
LOCNCODE  DIM       4
.
RESTPRG   DIM       8
RESTREP   DIM       2
RESTTEM   DIM       3
LINKTYPE  DIM       2
LINKKEYF  DIM       10
LINKKEYS  DIM       10
RESLNKFL  DIM       8               * I CAR 38290
RESULTKY  DIM       17
RESKEY    DIM       17
COLLECTK  DIM       13
NEXTCOLK  DIM       13
PREVCOLK  DIM       13
AUDITKEY  DIM       40
STATIMG   DIM       20
RESTIMG   DIM       20
DIM3      DIM       3
DIM4      DIM       4
DIM5      DIM       5
KEY3A     DIM       3
DAYTIME   DIM       3
DATETIME  DIM       25
DOCTCODE  DIM       6
WARDCODE  DIM       3
TEAMCODE  DIM       3
IMAGE     DIM       44
ICOIMAGE  DIM       30
.
PRELEN    INIT      "reln"          * I CAR 38290
PREAEA    INIT      "reau"          * I CAR 38290
.
. Cumulative Results Array
.----------------------------------------
CUMLREST  DIM       25[8][99]
CUMLROW   FORM      1
CUMLCOL   FORM      3
SETCOL    FORM      3
.
.------------------------------------------------------------
PRGID     INIT      "CLIWEB04"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "Clinical Result Detail Server"
.------------------------------------------------------------
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200,MAIN1300,MAIN1400,MAIN1500
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "INVALID OPTION",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  CALL      RESDET00      * Result Details
          GOTO      MAIN1000
.
MAIN1200  CALL      RESMRK00      * Mark Result as Seen
          GOTO      MAIN1000
.
MAIN1300  CALL      UPDRES00      * Update Result to match a patient
          GOTO      MAIN1000
.
MAIN1400  CALL      RELINK00      * Update Result to match a new patient
          GOTO      MAIN1000
.
MAIN1500  CALL      RESCOL00      * Result Details by Collection Date
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
.------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------
INIT0000  OPEN      IHAPASS1,"ihapassf"
          OPEN      AAEDE1A1,"aaede1af"
          CALL      INTMAS00
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATHSPA1,"pathspaf"
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATWR1A8,"patwr1af"
          OPEN      PATVISA1,"patvisaf"
          OPEN      PATCODE1,"patcodes"
          OPEN      WEBSECA1,"websecaf"
.
.         OPEN      RESAUDA1,"resaudaf"            * D CAR 38290
.         OPEN      RESLNKA1,"reslnkaf"            * D CAR 38290
.         OPEN      RESLNKA2,"reslnkaf"            * D CAR 38290
.         OPEN      RESLNKA3,"reslnkaf"            * D CAR 38290
          OPEN      RESLOGA1,"reslogaf"
          OPEN      RESPHRA1,"resphraf"
          OPEN      RESPHRA2,"resphraf"
          OPEN      RESPDSA1,"respdsaf"
          OPEN      RESPDSA2,"respdsaf"
          OPEN      RESPIDA1,"respidaf"
.
          CALL      IBACLOCK
          PACK      YEAROPEN,CCC,CYY
          PACK      RESULTYR,CCC,CYY
          REP       " 0",YEAROPEN
          REP       " 0",RESULTYR
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          IF        IBCNMHOS=1
            OPEN      MLTCODA1,"mltcodaf"
            OPEN      MLTCODA2,"mltcodaf"
          ENDIF
.
          RETURN
.------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          MOVE      SP70,TEMPLATE
          MOVE      SP70,URNUMBER
          MOVE      SP70,ADMISSNO
          MOVE      SP70,RESULTKY
          MOVE      SP70,AUDITKEY
          MOVE      SP70,COLLECTK
.
          RETURN
.------------------------------------------------------------
. Set Parameters
.------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "resultky=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,RESULTKY
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "collectk=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,COLLECTK
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "urnumber=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,URNUMBER
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "admissno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ADMISSNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "auditkey=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,AUDITKEY
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
.------------------------------------------------------------
. Results Details
.------------------------------------------------------------
RESDET00  MOVE      RESULTKY,KEY17
          CALL      RDREPH1            * Check Result on File
          BRANCH    OVRCD,RESDET90
          MOVE      ZERO,CUMLGPH
.
          CALL      WRTAUD00           * Write Audit
.
          CALL      GETPID00           * Get Patient
          BRANCH    OVRCD,RESDET91
.
          CLEAR     WEBTITLE
          APPEND    "Result Details",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,RESDET99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          GOTO      RESDET99
.
RESDET90  MOVE      "Invalid Result Key",WEBTITLE
          CALL      WEBERR00
          GOTO      RESDET99
.
RESDET91  MOVE      "Invalid Patient ID Key",WEBTITLE
          CALL      WEBERR00
          GOTO      RESDET99
.
RESDET99  RETURN
.------------------------------------------------------------
. Collection Date Results Summary
.------------------------------------------------------------
RESCOL00  CALL      NXTCOL00           * Get Next Collection Date
          CALL      PRECOL00           * Get Last Collection Date
          PACK      KEY46,URNUMBER,SP8,COLLECTK,SP70
          CALL      RSREPH2            * Check Result on File
          CALL      RKREPH2            * Check Result on File
          BRANCH    OVRCD,RESCOL90
          MATCH     URNUMBER,REPHPID
          GOTO      RESCOL90 IF NOT EQUAL
          PACK      KEY13,REPHCDT,REPHCTM
          MATCH     COLLECTK,KEY13
          GOTO      RESCOL90 IF NOT EQUAL
.
          PACK      RESULTKY,REPHRDT,REPHRTM,REPHRUN,SP70
.
          CALL      GETPID00
          BRANCH    OVRCD,RESCOL91
.
          CLEAR     WEBTITLE
          APPEND    "Result Details",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,RESCOL99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          GOTO      RESCOL99
.
RESCOL90  MOVE      "Invalid Collection Key",WEBTITLE
          CALL      WEBERR00
          GOTO      RESCOL99
.
RESCOL91  MOVE      "Invalid Patient ID Key",WEBTITLE
          CALL      WEBERR00
          GOTO      RESCOL99
.
RESCOL99  RETURN
.------------------------------------------------------------
. Write Audit Record for who has seen the records
.------------------------------------------------------------
WRTAUD00  PACK      REAUSDT,CCC,CYY,CMM,CDD
          REP       " 0",REAUSDT
          CLOCK     TIME,REAUSTM
          MOVE      WBSEUID,REAUUID
          PACK      KEY40,REPHRDT,REPHRTM,REPHRUN,REAUSDT,REAUSTM,REAUUID,SP70
          UNPACK    KEY40,REAURDT,REAURTM,REAURUN,REAUSDT,REAUSTM,REAUUID
          MOVE      KEY40,AUDITKEY
          MOVE      ZERO,REAUMRK
          MOVE      SP70,REAUSPA
.
          PACK      RESLNKFL,PREAEA,REAURDT           * I CAR 38290
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      RESAUDA1,RESLNKFL
          TRAPCLR   IO
.
          COMPARE   ONE,OVRCD
          GOTO      WRTAUD99 IF EQUAL                 * end I CAR 38290
.
          CALL      RDREAU1
          IF        OVRCD=1
            CALL      WRREAU1 
          ENDIF
WRTAUD99  RETURN
.------------------------------------------------------------
. Get Patient Details
.------------------------------------------------------------
GETPID00  MOVE      ZERO,PIDFLAG
          MATCH     "0",REPHFUR
          GOTO      GETPID10 IF EQUAL
.
          MOVE      REPHPID,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,GETPID99
.
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          IF        OVRCD=1
            CALL      CLPMSPX2
          ENDIF
.         
          MOVE      PSNAME,REPISNM
          MOVE      PGNAME,REPIGNM
          MOVE      SP1,REPIINT   
          MOVE      PBDATE,REPIDOB
          MOVE      PSEX,REPISEX  
          MOVE      PADD1,REPIAD1 
          MOVE      PADD2,REPIAD2 
          MOVE      PSUBRB,REPIAD3
          MOVE      SP70,REPIAD4 
          MOVE      PPOST,REPIPC  
          MOVE      PTELEP,REPIPHH
          MOVE      PTELEB,REPIPHB
          GOTO      GETPID99
.
GETPID10  MOVE      REPHPID,KEY16 
          CALL      RDREPI1
          MOVE      OVRCD,PIDFLAG
          MOVE      ZERO,OVRCD
.
GETPID99  RETURN
.------------------------------------------------------------
. Process Templated Fields
.------------------------------------------------------------
PROFLD00  BRANCH    REPORTNO,PROFLD10,PROFLD99,PROFLD99,PROFLD99,PROFLD50
          GOTO      PROFLD99
.
. Result Details
.--------------------------
PROFLD10  BRANCH    FLDSETNO,PROFLD11,PROFLD12,PROFLD13
          GOTO      PROFLD99
.
PROFLD11  MATCH     "0",REPHFUR           * Result for a U/R 
          GOTO      PROFLD99 IF EQUAL
.
          CALL      MASF0000
          GOTO      PROFLD99
.
PROFLD12  BRANCH    PIDFLAG,PROFLD99
.
          CALL      PIDF0000
          GOTO      PROFLD99
.
PROFLD13  CALL      RESP0000
          GOTO      PROFLD99
.
.
. Result Details by Collection Date
.----------------------------------
PROFLD50  BRANCH    FLDSETNO,PROFLD51,PROFLD52,PROFLD53
          GOTO      PROFLD99
.
PROFLD51  MATCH     "0",REPHFUR           * Result for a U/R 
          GOTO      PROFLD99 IF EQUAL
.
          CALL      MASF0000
          GOTO      PROFLD99
.
PROFLD52  BRANCH    PIDFLAG,PROFLD99
.
          CALL      PIDF0000
          GOTO      PROFLD99
.
PROFLD53  CALL      SCOL0000
          GOTO      PROFLD99
.
PROFLD99  RETURN
.------------------------------------------------------------
. Mark Results as Read
.------------------------------------------------------------
RESMRK00  UNPACK    AUDITKEY,KEY30,KEY10
          MATCH     KEY10,WBSEUID
          GOTO      RESMRK95 IF NOT EQUAL
.
          MOVE      AUDITKEY,RESULTKY
          MOVE      RESULTKY,KEY17
          CALL      RDREPH1            * Check Result on File
          BRANCH    OVRCD,RESMRK90
.
          PACK      RESLNKFL,PREAEA,AUDITKEY          * I CAR 38290
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      RESAUDA1,RESLNKFL
          TRAPCLR   IO
.
          COMPARE   ONE,OVRCD
          GOTO      RESMRK05 IF EQUAL                 * end I CAR 38290
.
          MOVE      AUDITKEY,KEY40
          CALL      RDREAU1
          IF        OVRCD=0
            MOVE      ONE,REAUMRK
            CALL      UPREAU1 
          ENDIF
.
RESMRK05  PACK      RESLNKFL,PRELEN,RESULTKY          * I CAR 38290
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      RESLNKA1,RESLNKFL
          TRAPCLR   IO
.
          COMPARE   ONE,OVRCD
          GOTO      RESMRK90 IF EQUAL                 * end I CAR 38290
.
          MATCH     SP70,WBSEDOC
          GOTO      RESMRK10 IF EQUAL
.
          MOVE      "03",LINKTYPE
          PACK      LINKKEYF,WBSEDOC,SP70
          PACK      KEY29,LINKTYPE,LINKKEYF,RESULTKY
          CALL      RDRELN1
          IF        OVRCD=0
            MOVE      ONE,RELNMSN
            MOVE      REAUSDT,RELNSDT
            MOVE      REAUSTM,RELNSTM
            MOVE      PASSCODE,RELNSPC
            CALL      UPRELN1
          ENDIF
.
RESMRK10  MATCH     SP70,WBSEWRD
          GOTO      RESMRK20 IF EQUAL
.
          MOVE      "04",LINKTYPE
          PACK      LINKKEYF,WBSEWRD,SP70
          PACK      KEY29,LINKTYPE,LINKKEYF,RESULTKY
          CALL      RDRELN1
          IF        OVRCD=0
            MOVE      ONE,RELNMSN
            MOVE      REAUSDT,RELNSDT
            MOVE      REAUSTM,RELNSTM
            MOVE      PASSCODE,RELNSPC
            CALL      UPRELN1
          ENDIF
.
RESMRK20  MOVE      "Result Marked as Read",WEBTITLE
          CALL      OPENHTML
          BRANCH    EXIT,RESMRK99
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "    alert(#"",WEBTITLE,"#");":
                             "    history.go(-2);":
                             "}":
                             "</script>"
          CALL      CLOSHTML
          GOTO      RESMRK99
.
RESMRK90  MOVE      "Invalid Result Key",WEBTITLE
          CALL      WEBERR00
          GOTO      RESMRK99
.
RESMRK95  MOVE      "Invalid Security for Audit Key",WEBTITLE
          CALL      WEBERR00
          GOTO      RESMRK99
.
RESMRK99  RETURN
.------------------------------------------------------------
. Re-Link Result to match a patient
.------------------------------------------------------------
RELINK00  UNPACK    RESULTKEY,REPHRDT,REPHRTM,REPHRUN
          PACK      KEY17,REPHRDT,REPHRTM,REPHRUN
          CALL      RDREPH1
          IF        OVRCD=1
            MOVE      "Invalid Result Key",WEBTITLE
            CALL      WEBERR00
            MOVE      ONE,EXIT
            GOTO      RELINK90
          ENDIF
          REP       "+ ",URNUMBER 
          PACK      KEY8,URNUMBER
          CALL      RDMAST1
          IF        OVRCD=1
            MOVE      "Invalid U/R Number",WEBTITLE
            CALL      WEBERR00
            MOVE      ONE,EXIT
            GOTO      RELINK90
          ENDIF
.
          CALL      REMLNK00                * Remove All Links
.
          CALL      UPDHE000                * Update header details
          BRANCH    EXIT,RELINK90
.
          CALL      WRTLNK00                * write link records
.
RELINK90  IF        EXIT=1
            CALL      DISALT00    * back to display update form
          ELSE
            MOVE      "Result Matched to Patient",WEBTITLE
            CALL      OPENHTML
            BRANCH    EXIT,RELINK99
            WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                               "    alert(#"",WEBTITLE,"#");":
                               "    history.go(-2);":
                               "}":
                               "</script>"
            CALL      CLOSHTML
          ENDIF
RELINK99  RETURN
.------------------------------------------------------------
. Remove all links for a Result
.------------------------------------------------------------
REMLNK00  PACK      RESLNKFL,PRELEN,RESULTKY          * I CAR 38290
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      RESLNKA3,RESLNKFL
          TRAPCLR   IO
.
          COMPARE   ONE,OVRCD
          GOTO      REMLNK90 IF EQUAL                 * end I CAR 38290
.
REMLNK50  PACK      KEY29,RESULTKY,SP70
          CALL      RSRELN3
          CALL      RKRELN3
          BRANCH    OVRCD,REMLNK90
          PACK      KEY17,RELNRDT,RELNRTM,RELNRUN
          MATCH     KEY17,RESULTKY
          GOTO      REMLNK90 IF NOT EQUAL
          PACK      KEY29,RELNRDT,RELNRTM,RELNRUN,RELNLTY,RELNLKY
          CALL      DERELN3                 * delete error link
          GOTO      REMLNK50
.        
REMLNK90
REMLNK99  RETURN
.------------------------------------------------------------
. Update Result to match a patient
.------------------------------------------------------------
UPDRES00  CALL      LOCLNK00                * Lock link file 
          BRANCH    EXIT,UPDRES90
.
          CALL      UPDHE000                * Update header details
          BRANCH    EXIT,UPDRES90
.
          CALL      WRTLNK00                * write link records
.
          CALL      DELLNK00                * delete error link
          BRANCH    EXIT,UPDRES90
.
UPDRES90  IF        EXIT=1
            CALL      DISALT00    * back to display update form
          ELSE
            MOVE      "Result Matched to Patient",WEBTITLE
            CALL      OPENHTML
            BRANCH    EXIT,UPDRES99
            WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                               "    alert(#"",WEBTITLE,"#");":
                               "    history.go(-2);":
                               "}":
                               "</script>"
            CALL      CLOSHTML
          ENDIF
.
UPDRES99  RETURN
.
.------------------------------------------------------------
.   Update header details
.------------------------------------------------------------
UPDHE000  MOVE      ZERO,EXIT 
          REP       "+ ",URNUMBER 
          PACK      KEY8,URNUMBER
          CALL      RDMAST1
          BRANCH    OVRCD,UPDHE900
.
          UNPACK    RESULTKEY,REPHRDT,REPHRTM,REPHRUN
          PACK      KEY17,REPHRDT,REPHRTM,REPHRUN
          CALL      RDREPH1
          IF        OVRCD=0
            PACK      REPHPID,URNUMBER,SP70 
            MOVE      ONE,REPHFUR
            PACK      REPHVIS,ADMISSNO,SP70
            PACK      KEY17,REPHRDT,REPHRTM,REPHRUN
            CALL      UPREPH1
          ENDIF
          GOTO      UPDHE999
.
UPDHE900  MOVE      "Invalid U/R Number",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
.           
UPDHE999  RETURN
.------------------------------------------------------------
.    Write link records
.------------------------------------------------------------
WRTLNK00  MOVE      "01",LINKTYPE
          MOVE      REPHPID,DIM8
          PACK      LINKKEYS,DIM8,SP70
          CALL      NEWLNK00
.
          MOVE      ADMISSNO,KEY8
          CALL      RDVISA1
          BRANCH    OVRCD,WRTLNK99     * Not Valid
.
          MOVE      "02",LINKTYPE
          PACK      LINKKEYS,PVIBILL,SP70
          CALL      NEWLNK00
.
          BRANCH    PVITYPE,WRTLNK10,WRTLNK20,WRTLNK30
.
WRTLNK10  PACK      KEY8,PVIBILL
          CALL      RDDETA1
          BRANCH    OVRCD,WRTLNK99
.
          MOVE      "03",LINKTYPE
          PACK      LINKKEYS,ADADOCT,SP70
          CALL      NEWLNK00
.
          MOVE      "03",LINKTYPE
          PACK      LINKKEYS,AEDARDOC,SP70
          CALL      NEWLNK00
.
          GOTO      WRTLNK99
.
WRTLNK20   PACK      KEY8,REPHPID
.          CALL      RDBOKB1
.          BRANCH    OVRCD,WRTLNK99
.
.          MOVE      "03",LINKTYPE
.          PACK      LINKKEYS,OBADOCT,SP70
.          CALL      NEWLNK00
.
.WRTLNK21   PACK      KEY12,PVISITE,PVIDOCT
.          CALL      RDCLIA1
.          BRANCH    OVRCD,WRTLNK99
..
.          MOVE      "03",LINKTYPE
.          PACK      LINKKEYS,OCADOCT,SP70
.          CALL      NEWLNK00
.
          GOTO      WRTLNK99
..
WRTLNK30  PACK      KEY8,PVIBILL
          CALL      RDMISS1
          BRANCH    OVRCD,WRTLNK99          * No record
.
          MOVE      "04",LINKTYPE           * Ward link
          PACK      LINKKEYS,AWARD,SP70
          CALL      NEWLNK00
.
          MOVE      "03",LINKTYPE           * Attending doctor link
          PACK      LINKKEYS,ADOCTA,SP70
          CALL      NEWLNK00
.
          MOVE      "03",LINKTYPE           * Treating doctor link
          PACK      LINKKEYS,ADOCTT,SP70
          CALL      NEWLNK00
.
          MOVE      "03",LINKTYPE           * Referring Doctor link
          PACK      LINKKEYS,ADOCTR,SP70
          CALL      NEWLNK00
          GOTO      WRTLNK99
.          
WRTLNK99  RETURN
.------------------------------------------------------------
. New link record
.------------------------------------------------------------
NEWLNK00  MATCH     SP70,LINKKEYS
          GOTO      NEWLNK99 IF EQUAL
.
          PACK      KEY29,LINKTYPE,LINKKEYS,REPHRDT,REPHRTM,REPHRUN
          UNPACK    KEY29,RELNLTY,RELNLKY,RELNRDT,RELNRTM,RELNRUN
.
          PACK      RESLNKFL,PRELEN,RELNRDT           * I CAR 38290
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      RESLNKA1,RESLNKFL
          TRAPCLR   IO
.
          COMPARE   ONE,OVRCD
          GOTO      NEWLNK99 IF EQUAL                 * end I CAR 38290
.
          CALL      RDRELN1
          IF        OVRCD=1
            MOVE      ZERO,RELNMSN            * 0=no , 1=yes
            MOVE      SP70,RELNSDT
            MOVE      SP70,RELNSTM
            MOVE      SP70,RELNSPC
            MOVE      REPHFUR,RELNFUR
            MOVE      REPHPID,RELNPID
            MOVE      SP70,RELNUCS
            MOVE      SP70,RELNUSC
            MOVE      SP70,RELNDSS
            MOVE      REPHNOR,RELNNOR
.
            PACK      KEY29,RELNLTY,RELNLKY,RELNRDT,RELNRTM,RELNRUN
            CALL      WRRELN1
          ENDIF
.
NEWLNK99  RETURN
.------------------------------------------------------------
.  Lock error link
.------------------------------------------------------------
LOCLNK00  PACK      RESLNKFL,PRELEN,RESULTKY          * I CAR 38290
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      RESLNKA3,RESLNKFL
          TRAPCLR   IO
.
          COMPARE   ONE,OVRCD
          GOTO      LOCLNK90 IF EQUAL                 * end I CAR 38290
.
          MOVE      "99",LINKTYPE
          PACK      KEY29,RESULTKY,LINKTYPE,SP70
          CALL      RSRELN3
          CALL      RKRELN3
          BRANCH    OVRCD,LOCLNK90
          PACK      KEY17,RELNRDT,RELNRTM,RELNRUN
          MATCH     KEY17,RESULTKY
          GOTO      LOCLNK90 IF NOT EQUAL
          MATCH     LINKTYPE,RELNLTY
          GOTO      LOCLNK90 IF NOT EQUAL
          PACK      KEY29,RELNRDT,RELNRTM,RELNRUN,RELNLTY,RELNLKY,SP70
          CALL      RLRELN3
          BRANCH    OVRCD,LOCLNK90,LOCLNK95
          MOVE      ZERO,EXIT
          GOTO      LOCLNK99
.
LOCLNK90  MOVE      "ERROR CAN'T FIND ERROR LINK",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      LOCLNK99
.
LOCLNK95  MOVE      "ERROR LINK LOCKED",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      LOCLNK99
.
LOCLNK99  RETURN
.------------------------------------------------------------
.    Delete error link
.------------------------------------------------------------
DELLNK00  PACK      RESLNKFL,PRELEN,RESULTKY          * I CAR 38290
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      RESLNKA3,RESLNKFL
          TRAPCLR   IO
.
          COMPARE   ONE,OVRCD
          GOTO      DELLNK90 IF EQUAL                 * end I CAR 38290
.
          MOVE      "99",LINKTYPE
          PACK      KEY29,RESULTKY,LINKTYPE,SP70
          CALL      RSRELN3
          CALL      RKRELN3
          BRANCH    OVRCD,DELLNK90
          PACK      KEY17,RELNRDT,RELNRTM,RELNRUN
          MATCH     KEY17,RESULTKY
          GOTO      DELLNK90 IF NOT EQUAL
          MATCH     LINKTYPE,RELNLTY
          GOTO      DELLNK90 IF NOT EQUAL
.
          PACK      KEY29,RELNRDT,RELNRTM,RELNRUN,RELNLTY,RELNLKY
          CALL      DERELN3                 * delete error link
.        
          MOVE      RELNLKY,KEY10  
          CALL      RLRELG1                 * Lock the log file
          BRANCH    OVRCD,DELLNK90,DELLNK95
.
          PACK      KEY10,RELGUID
          CALL      RDRELG1
          IF        OVRCD=0
            SUB       ONE,RELGERR 
            CALL      UPRELG1               * update the log file
          ENDIF
.
          MOVE      ZERO,EXIT
          CALL      UURELN1                 * unlock error link
          CALL      UURELG1                 * Unlock the log file
          GOTO      DELLNK99
.
DELLNK90  MOVE      "ERROR CAN'T FIND LOG RECORD",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      LOCLNK99
.
DELLNK95  MOVE      "LOG RECORD LOCKED",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      LOCLNK99
.
DELLNK99  RETURN
.------------------------------------------------------------
.----------------------------------------------------------------------
. Get Next Collection Date Key
.----------------------------------------------------------------------
NXTCOL00  PACK      KEY46,URNUMBER,SP8,COLLECTK,Z70
          CALL      RSREPH2
NXTCOL10  CALL      RKREPH2
          BRANCH    OVRCD,NXTCOL90
          MATCH     URNUMBER,REPHPID
          GOTO      NXTCOL90 IF NOT EQUAL
          PACK      KEY13,REPHCDT,REPHCTM
          MATCH     COLLECTK,KEY13
          GOTO      NXTCOL10 IF EQUAL
.
          PACK      NEXTCOLK,REPHCDT,REPHCTM
          GOTO      NXTCOL99 
.
NXTCOL90  MOVE      SP70,NEXTCOLK
NXTCOL99  RETURN
.----------------------------------------------------------------------
. Get Previous Collection Date Key
.----------------------------------------------------------------------
PRECOL00  PACK      KEY46,URNUMBER,SP8,COLLECTK,SP70
          CALL      RSREPH2
PRECOL10  CALL      RPREPH2
          BRANCH    OVRCD,PRECOL90
          MATCH     URNUMBER,REPHPID
          GOTO      PRECOL90 IF NOT EQUAL
          PACK      KEY13,REPHCDT,REPHCTM
          MATCH     COLLECTK,KEY13
          GOTO      PRECOL10 IF EQUAL
.
          PACK      PREVCOLK,REPHCDT,REPHCTM
          GOTO      PRECOL99 
.
PRECOL90  MOVE      SP70,PREVCOLK
PRECOL99  RETURN
.------------------------------------------------------------
. Result Details
.------------------------------------------------------------
SCOL0000  BRANCH    FLDITMNO,SCOL0100,SCOL0200,SCOL0300,SCOL0400,SCOL0500:
                             SCOL0600,SCOL0700,SCOL0800,SCOL0900
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      SCOL9999
.
SCOL0100  UNPACK    COLLECTK,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      SCOL9999
.
SCOL0200  UNPACK    COLLECTK,CCENT,CYEAR,CMON,CDAY,KEY5
          WRITE     HTMLFILE;KEY5;
          GOTO      SCOL9999
.
SCOL0300  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
          MATCH     SP70,NEXTCOLK
          IF        @EQUAL
            WRITE     HTMLFILE;"alert(#"No More Patient Results#")";
          ELSE 
            REP       " +",NEXTCOLK
            REP       " +",URNUMBER
            REP       " +",ADMISSNO
            WRITE     HTMLFILE;"location.href=#"",LINKPRG,".pbl":
                               "?reportno=",LINKREP:
                               "&template=",LINKTEM:
                               "&urnumber=",URNUMBER:
                               "&admissno=",ADMISSNO:
                               "&collectk=",NEXTCOLK,"#"";
            REP       "+ ",NEXTCOLK
            REP       "+ ",URNUMBER
            REP       "+ ",ADMISSNO
          ENDIF
          GOTO      SCOL9999
.
.
SCOL0400  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
          MATCH     SP70,PREVCOLK
          IF        @EQUAL
            WRITE     HTMLFILE;"alert(#"No More Patient Results#")";
          ELSE 
            REP       " +",PREVCOLK
            REP       " +",URNUMBER
            REP       " +",ADMISSNO
            WRITE     HTMLFILE;"location.href=#"",LINKPRG,".pbl":
                               "?reportno=",LINKREP:
                               "&template=",LINKTEM:
                               "&urnumber=",URNUMBER:
                               "&admissno=",ADMISSNO:
                               "&collectk=",PREVCOLK,"#"";
            REP       "+ ",PREVCOLK
            REP       "+ ",URNUMBER
            REP       "+ ",ADMISSNO
          ENDIF
          GOTO      SCOL9999
.
SCOL0500  CALL      PATHED00    * Patient Header
          GOTO      SCOL9999
.
SCOL0600  CALL      RESHED00    * Result Header
          GOTO      SCOL9999
.
SCOL0700  CALL      COLSUM00    * All Result for Collection Date
          GOTO      SCOL9999
.
SCOL0800  CALL      AUDTAB00    * Audit Table
          GOTO      SCOL9999
.
SCOL0900  WRITE     HTMLFILE;REPHLAB;
          GOTO      SCOL9999
.
SCOL9999  RETURN
.------------------------------------------------------------
. First Result Patient Details for Results by Collection Date
.------------------------------------------------------------
PATHED00  MOVE      ZERO,CUMLGPH
          MOVE      ZERO,PITF200
          MOVE      ZERO,PITF300
          WRITE     HTMLFILE;"<tr>":
                             "<td align=center bgcolor=##CCCCCC width=100%>":
                             "<b>First Result Patient Details</b></td>":
                             "<td align=right bgcolor=##CCCCCC>":
                             "<input type=button ":
                             "class=OpenMoreButton id=pit1 ":
                             "onclick='ShowPatientHeader()'></td></tr>"
          WRITE     HTMLFILE;"<tr><td colspan=2><div id=pit100 class=pit100>":
                             "<pre>"
          PACK      KEY20,RESULTKY,SP70
          CALL      RSREPD1
PATHED10  CALL      RKREPD1
          BRANCH    OVRCD,PATHED99
          PACK      KEY17,REPDRDT,REPDRTM,REPDRUN,SP70
          MATCH     KEY17,RESULTKY
          GOTO      PATHED99 IF NOT EQUAL
          MATCH     "2",REPDPTL
          GOTO      PATHED99 IF EQUAL
.
          CALL      PROLIN00
.
          GOTO      PATHED10
.
PATHED99  WRITE     HTMLFILE;"</pre></div></td></tr>"
          RETURN
.
.------------------------------------------------------------
. First Result Header Details for Results by Collection Date
.------------------------------------------------------------
RESHED00  WRITE     HTMLFILE;"<tr>":
                             "<td align=center bgcolor=##CCCCCC width=100%>":
                             "<b>First Result Header</b></td>":
                             "<td align=right bgcolor=##CCCCCC>":
                             "<input type=button ":
                             "class=OpenMoreButton id=pit2 ":
                             "onclick='ShowResultHeader()'></td></tr>"
          WRITE     HTMLFILE;"<tr><td colspan=2><div id=pit200 class=pit200>":
                             "<pre>"
          PACK      KEY20,RESULTKY,SP70
          CALL      RSREPD1
RESHED10  CALL      RKREPD1
          BRANCH    OVRCD,RESHED99
          PACK      KEY17,REPDRDT,REPDRTM,REPDRUN,SP70
          MATCH     KEY17,RESULTKY
          GOTO      RESHED99 IF NOT EQUAL
          MATCH     "1",REPDPTL
          GOTO      RESHED10 IF EQUAL
          MATCH     "3",REPDPTL
          GOTO      RESHED99 IF EQUAL
          MATCH     "299",REPDPTL
          GOTO      CRDETL99 IF EQUAL
.
          CALL      PROLIN00
.
          GOTO      RESHED10
.
RESHED99  WRITE     HTMLFILE;"</pre></div></td></tr>"
          RETURN
.------------------------------------------------------------
. Result Details for Results by Collection Date
.------------------------------------------------------------
COLSUM00  PACK      KEY46,URNUMBER,SP8,COLLECTK,SP70
          CALL      RSREPH2            * Check Result on File
COLSUM10  CALL      RKREPH2            * Check Result on File
          BRANCH    OVRCD,COLSUM99
          MATCH     URNUMBER,REPHPID
          GOTO      COLSUM99 IF NOT EQUAL
          PACK      KEY13,REPHCDT,REPHCTM
          MATCH     COLLECTK,KEY13
          GOTO      COLSUM99 IF NOT EQUAL
.
          PACK      RESKEY,REPHRDT,REPHRTM,REPHRUN,SP70
          CALL      CRDETL00           * Output Details
          GOTO      COLSUM10
.
COLSUM99  RETURN
.------------------------------------------------------------
. Result Details for Results by Collection Date
.------------------------------------------------------------
CRDETL00  CALL      WRTAUD00           * Write Audit
.
          WRITE     HTMLFILE;"<tr>":
                             "<td align=center bgcolor=##CCCCCC width=100%>":
                             "<b>",REPHNAM,"Reported ";
          UNPACK    REPHRDT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR," at ",REPHRTM:
                          "</b></td>":
                          "<td align=right bgcolor=##CCCCCC>":
                          "<input type=button class=OpenResultButton value=u ":
                          "onclick='ShowResult(#"",RESKEY,"#")'></td></tr>"
          WRITE     HTMLFILE;"<tr><td colspan=2><pre>"
          PACK      KEY23,RESKEY,THREE,SP70
          CALL      RSREPD2
CRDETL10  CALL      RKREPD2
          BRANCH    OVRCD,CRDETL99
          PACK      KEY17,REPDRDT,REPDRTM,REPDRUN,SP70
          MATCH     KEY17,RESKEY
          GOTO      CRDETL99 IF NOT EQUAL
          MATCH     "390",REPDPTL
          GOTO      CRDETL10 IF EQUAL
          MATCH     "399",REPDPTL
          GOTO      CRDETL10 IF EQUAL
.
          CALL      PROLIN00
.
          GOTO      CRDETL10
.
CRDETL99  WRITE     HTMLFILE;"</pre></td></tr>"
          RETURN
.
          INC       IBAWEBCD    
.
          INC       APSMASIO/INC
          INC       AAEDE1IO/INC
          INC       PATHSPIO/INC
          INC       PMSUCHIO/INC
          INC       PMSUCDIO/INC
          INC       PATVISIO/INC
          INC       PATIN1IO/INC
          INC       PATPR1IO/INC
          INC       PATMA1IO/INC
          INC       PMSHCPIO/INC
          INC       PMSHCGIO/INC
          INC       PMSHCPTM
          INC       PMSHCGTM
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
          INC       PATMI1IO/INC
          INC       PATNURIO/INC
          INC       PATDO1IO/INC
          INC       PATWR1IO/INC
          INC       RESAUDIO/INC
          INC       RESLNKIO/INC
          INC       RESLOGIO/INC
          INC       RESPDSIO/INC
          INC       RESPHRIO/INC
          INC       RESPIDIO/INC
          INC       NHIMASIO/INC
          INC       NHIETHIO/INC
          INC       CLNHIMAS 
          INC       RESPITTM
          INC       PATMASTM    
          INC       CLPMSPX2
          INC       PMSPX2TM
          INC       PATMSXTM
          INC       PATDOCTM
          INC       PATDOCCD
          INC       PATWRDTM
          INC       DAYOFWEK
          INC       CALCAGE
          INC       NAMSTRCD
          INC       WEBDATCD
          INC       MRTLOCIO/INC
          INC       SCRMASIO/INC
          INC       PATALRIO/INC
          INC       OUTSITIO/INC
          INC       PATFN1IO/INC
          INC       PMSCCDIO/INC            * Concession Card Details
          INC       MRTMASIO/INC

.***************************************************************************
.* System    :   WEB Printing                                              *
.* Program   :   FIXSPOLF                                                  *
.* Desc      :   Fix ibaspolf                                              *
.***************************************************************************
.* Date      :   07/03/2011                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   This program will delete any ibaspolf records and their   *  
.*           :   IBA*****.prt files if they are not in webschaf            *  
.* Modifications  :                                                        *
.*        V11.02.01 19/05/2022  Ebon Clements TSK 0918581                  *
.*                  Fixed current date value if run overnight              *
.*                  Display record count and file name every 10 records    *
.*        V11.01.01 22/10/2021  Davin         TSK 0912924                  *
.*                  Added program display counters                         *
.*        V10.01.00 07/03/2011  Ebon Clements CAR                          *
.*                  Created program                                        *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       IBASPOFD/INC      
          INC       WEBSCHFD/INC
.
SPLTFILE  FILE   ASCII, FIXED=256
.
. LOCAL VARIABLE DEFINITION
. -------------------------
CURRDATE  DIM       8
DELNUM    FORM      12
DISNUM    FORM      12
FILENAME  DIM       12
IBPREXTN  INIT      "/prt"
RECNUM    FORM      12
.
PRGID     INIT      "FIXSPOLF"
PRGNAM    INIT      "Fix ibaspolf"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
          MOVE      ZERO,RECNUM
          MOVE      ZERO,DELNUM
          MOVE      ZERO,DISNUM
.
          DISPLAY   *P64:24,"ibaspolf";
          OPEN      IBASPOL1,"ibaspolf"
.
          DISPLAY   *P64:24,"webschaf";
          OPEN      WEBSCHA1,"webschaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS;
.
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          DISPLAY   *P1:20,*EL,"ibaspolf Record No.      : ";
.
          PACK      KEY8,SP70
          CALL      RDSSPOL1
PROC1000  CALL      RDKSPOL1                     * Loop ibaspolf
          BRANCH    OVRCD,PROC9000
.
          ADD       ONE,RECNUM                    * update record counter
          ADD       ONE,DISNUM                    * Display counter
.
          IF        DISNUM = 10  | RECNUM = 1
            MOVE      ZERO,DISNUM
            DISPLAY   *P28:20,*V2LON,RECNUM," File Name : ",CSPNAME
.
            CALL      IBACLOCK
            PACK      CURRDATE,CCC,CYY,CMM,CDD
            REP       " 0",CURRDATE
          ENDIF
.
          MATCH     CURRDATE,CSPDATE             * Exclude today
          GOTO      PROC1000 IF EQUAL
.
          PACK      KEY8,SP70
          CALL      RSWBSC1                      * Check scheduler for spool 
PROC2000  CALL      RKWBSC1                      * file. No key so must check
          BRANCH    OVRCD,PROC5000               * every record
.
          MATCH     CSPNAME,WBSCSPOO             * Valid spool file if in       
          GOTO      PROC1000 IF EQUAL            * webschaf
.
          GOTO      PROC2000
.
PROC5000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO               * Delete .prt file if there
          PACK      FILENAME,CSPNAME,IBPREXTN,SP70
          OPEN      SPLTFILE,FILENAME
          TRAPCLR   IO
          IF        OVRCD=0
            CLOSE     SPLTFILE,DELETE
          ENDIF
.
          PACK      KEY8,CSPNAME,SP70
          CALL      DESPOL1                      * Delete ibaspolf record
          ADD       ONE,DELNUM                   * delete records counter
.
          PACK      KEY8,CSPNAME,SP70
          CALL      RDSSPOL1                     * Reposition
.
          GOTO      PROC1000
.
PROC9000  CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
          DISPLAY   *P28:20,*V2LON,RECNUM,*HOFF;
          DISPLAY   *P1:21,"ibaspolf records deleted : ",*V2LON,DELNUM;
          DISPLAY   *P1:22,"Ended   : ",CDATE,SP1,CTIMEIS;
          DISPLAY   *P1:24,*EL;
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       IBASPOIO/INC      
          INC       WEBSCHIO/INC

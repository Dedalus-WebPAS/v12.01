.******************************************************************************
.* System         : Private Practice                                          *
.* Program        : IBAPRI13.PBL                                              *
.* Name           : Small Balance Write Off                                   *
.******************************************************************************
.* Date           : 03/11/2016                                                *
.* Author         : J.Tan                                                     *
.* Function       : Ability to automatically write off small balances         *
.* Modifications  :                                                           *
.*        V10.13.01 26/07/2018  J.Tan         TSK 0848506                     *
.*                  Changed PP Doctor from DIM6 to DIM10                      *
.*                  07/08/2018  Tracey Nguyen TSK 0848506                     *
.*                  Moved SP10 to PRDTREFD                                    *
.*                  V10.11.01 02/08/2017 J.Tan     TSK                        *
.*                  Added CLPRIDTR - clear pridtraf fields                    *
.*                  V10.09.01 20/12/2016 J.Tan     TSK 302781                 *
.*                  Changed Min Amount Outstanding to Max amount outstanding  *
.******************************************************************************

          INC       STD001FD
          INC       IBACONFD/INC
          INC       IBASECFD/INC
          INC       IBAPASFD/INC
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBSECFD/INC
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
          INC       PRISTAFD/INC                 * financial statistics file
          INC       PRIPRAFD/INC                 * medical practice file
          INC       PRIHREFD/INC                 * doctor referral details file
          INC       PRICONFD/INC                 * control file
.
          INC       PATCONFD/INC
          INC       PATCODFD/INC
          INC       PATDRGFD/INC
          INC       PATMA1FD/INC
          INC       PRIINVFD/INC
          INC       PRIDTRFD/INC
          INC       PRIFINFD/INC
          INC       PRIFIGFD/INC
          INC       PATFN1FD/INC
          INC       PRIJRNFD/INC
          INC       PRICNOFD/INC
          INC       PMSEVTFD/INC
          INC       RCPDTEFD/INC
          INC       RCPBNKFD/INC
          INC       RCPREGFD/INC
          INC       PATSELDT/INC
.
ASATDAY   FORM      3
ASATYER   FORM      2
ASATCCC   FORM      2
DAYSINYR  FORM      5
DIM10     DIM       10
DIM12     DIM       12
.
INVDAYS   FORM      3
CNYRASAT  DIM       4
CNYRPINV  DIM       4
CODE      DIM       2
CODECL    INIT      "CL"
CMDLINE   DIM       80
.
FSTDATE   DIM       8
FSTAMNT   FORM      12.2
FSTWORK   FORM      12.2
FGSTFLAG  FORM      1
FNAMEJ    DIM       8
FNAMET    DIM       8
FORM12    FORM      12
FORM5DAY  FORM      5
F12P2     FORM      12.2
FORM12P2  FORM      12.2
JNLAMNT   FORM      12.2            * journal amount
NXTRAN    FORM      6               * next transaction number
SAVKEY12  DIM       12
PERNUM    FORM      2               * date range period
.
XMNDBAMT  FORM      12.2
XJRNLDEB  DIM       3               * Journal type (debit)
XMXCRAMT  FORM      12.2
XJRNLCRD  DIM       3               * Journal type (Credit)
XBALTYPE  FORM      1
.
TMPPAID   FORM      12.2
USERID    DIM       10
.
CODEJ     INIT      "J "
FIVE9     FORM      "59"
.
.         Temporary File Definitions
.         --------------------------
TMPFILE1  IFILE SQL, FIXED=56, KEY="U17-17,9-16,1-8"
.
.NAME     TYPE    LENGTH     PHYSICAL     START     DESCRIPTION
.----     ----    ------     --------     -----     -----------
.PRINNUMB DIM       8           8           1       Invoice Number
.PRINDATE DIM       8           8           9       Invoice date
XJRNINDC  DIM       1           1          17       Journal Type Indicator
.                                                   A=Debit,B=Credit
XJRNLCOD  DIM       3           3          18       Journal Code
XJRNLDAT  DIM       8           8          21       Journal Date
XJRNAMNT  FORM     12.2         8          29       Journal Amount
.PRINCLAM DIM       3           3          37       Claim code
XPRHRBCD  DIM       6           6          40       Health Fund
.WBSEUID  DIM      10          10          46       User ID
.EOR                                       56
.
PRGID     INIT      "IBAPRI13"
PRGNAM    INIT      "Small Balance Write Off"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                   MAIN0000                                 *
.*                                 Main Module                                *
.******************************************************************************
.
MAIN0000  CALL      INIT0000                * Initialise variables & open files
          CALL      CTMP0000                * Create tempfiles
.
MAIN1000  CALL      OPTN0000                * Choose main option
          BRANCH    EXIT,MAIN9000           * EXIT
.
          CALL      SCRN0000                * display input screen
.
          CALL      GOST0000                * keyin maximum outstanding
          CALL      GJRD0000                * keyin Journal Type (Debit)
          CALL      GCRD0000                * keyin maximum amount in credit
          CALL      GJRC0000                * input Journal Type (Credit)
          CALL      GBAL0000                * All balance greater than
          CALL      KUSR0000                * keyin user id
          BRANCH    EXIT,MAIN9000
.
          MOVE      "99",CLNO               * force Page header
          CALL      CONTQST                 * Continue Processing ?
          BRANCH    CEXIT,MAIN2000:         * 1 = Yes
                          MAIN1000:         * 2 = No
                          MAIN9000          * 3 = Cancel
          GOTO      MAIN1000
.
MAIN2000  CALL      LOAD0000                * load Invoices to temp file
          BRANCH    EXIT,MAIN1000
.
          CALL      PROC0000                * Process temp file
          GOTO      MAIN1000
.
MAIN9000  CALL      KILL0000                * Kill tempfile
.
MAIN9999  CHAIN     PGM
+
.******************************************************************************
.*                                  INIT0000              Called by: MAIN0000 *
.*                       Initialise Variable & Open Files                     *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen heading
.
          DISPLAY   *P56:24,"Opening patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"priinvof";
          OPEN      PRIINVO1,"priinvof"
          OPEN      PRIINVO2,"priinvof"
.
          DISPLAY   *P64:24,"pridtraf";
          OPEN      PRIDTRA1,"pridtraf"
          OPEN      PRIDTRA3,"pridtraf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"pricnoaf";
          OPEN      PRICNOA3,"pricnoaf"
.
          DISPLAY   *P64:24,"patfn1af";
          OPEN      PATFN1A1,"patfn1af"
.
          DISPLAY   *P64:24,"prihreff";
          OPEN      PRIHREF1,"prihreff"          * doctor referral details
.
          DISPLAY   *P64:24,"pristatf";
          OPEN      PRISTAT1,"pristatf"          * statistics file
.
          OPEN      WEBSECA1,"websecaf"
          OPEN      PATDRGA1,"patdrgaf"
          OPEN      PRIJRNL1,"prijrnlf"
          OPEN      PRIFINS1,"prifinsf"
.
          OPEN      RCPDTEA3,"rcpdteaf"
          OPEN      RCPBNKA1,"rcpbnkaf"
          OPEN      RCPREGA1,"rcpregaf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*85,IBCNUGST
          READ      CONTROLF,THIRTY4;*199,PRCNAFEE
.
          CLOCK     TIME,CTIMEIS
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAMET
.
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000              Called by: MAIN0000 *
.*                                Select Option                               *
.******************************************************************************
OPTN0000  MOVE      ZERO,EXIT
          MOVE      ZERO,OPTION
          DISPLAY   *P40:4,*EF:
                    *P1:6,*V2LON,"0",*HOFF," = Exit":
                    *P1:7,*V2LON,"1",*HOFF," = Report Only":
                    *P1:8,*V2LON,"2",*HOFF," = Report and Journal":
                    *P1:10,"Select Option : ";
.
OPTN1000  KEYIN     *P17:10,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN2000:             * HF extract
                           OPTN3000              * Report and Journal
          BEEP
          GOTO      OPTN1000
.
OPTN2000  DISPLAY   *P57:2,*V2LON,"- Report Only";
          GOTO      OPTN9000
.
OPTN3000  DISPLAY   *P57:2,*V2LON,"- Report and Journal";
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.***************************************************************************
.*                               GOST0000              Called by: ML0000   *
.*                     keyin Maximum outstanding                           *
.***************************************************************************
GOST0000  KEYIN     *P29:4,*EL,*V2LON,"$",XMNDBAMT:
                    *P29:4,"$",*DV,XMNDBAMT;
GOST9999  RETURN
+
.***************************************************************************
.*                               GJRD0000              Called by: ML0000   *
.*                     keyin Journal Type (Debit)                          *
.***************************************************************************
GJRD0000  DISPLAY   *P29:5,*EL;
          MOVE      FIVE,EVERT
          MOVE      TWENTY9,ECOL
          MOVE      CODEJ,CODE                   * load category
.
GJRD0500  CALL      PATCODKY                     * get start code
          BRANCH    EXIT,GJRD9800:               * nothing entered
                         GJRD9800                * exitchar entered
.
          MOVE      ACODE,XJRNLDEB               * save journal code
          MOVE      ZERO,EXIT
          GOTO      GJRD9999
.
GJRD9800  MOVE      ONE,EXIT
GJRD9999  RETURN
+
.***************************************************************************
.*                               GCRD0000              Called by: ML0000   *
.*                     keyin maximum amount in credit                      *
.***************************************************************************
GCRD0000  KEYIN     *P29:6,*EL,*V2LON,"$",XMXCRAMT:
                    *P29:6,"$",*DV,XMXCRAMT;
GCRD9999  RETURN
+
.***************************************************************************
.*                               GJRC0000              Called by: ML0000   *
.*                     keyin Journal Type (Credit)                         *
.***************************************************************************
GJRC0000  DISPLAY   *P29:7,*EL;
          MOVE      SEVEN,EVERT
          MOVE      TWENTY9,ECOL
          MOVE      CODEJ,CODE                   * load category
.
GJRC0500  CALL      PATCODKY                     * get start code
          BRANCH    EXIT,GJRC9800:               * nothing entered
                         GJRC9800                * exitchar entered
.
          MOVE      ACODE,XJRNLCRD               * save journal code
          MOVE      ZERO,EXIT
          GOTO      GJRC9999
.
GJRC9800  MOVE      ONE,EXIT
GJRC9999  RETURN
+
.***************************************************************************
.*                                GBAL0000             Called by: ML0000   *
.*                     All balance greater than                            *
.***************************************************************************
GBAL0000  DISPLAY   *P1:20,*EL:
                        "1=Less 30 Days, 2=30 Days plus, 3=60 Days plus, ":
                        " 4=90 Days plus";
          MOVE      ZERO,FORM1
          KEYIN     *P29:9,*EL,*V2LON,FORM1
          BRANCH    FORM1,GBAL1000,GBAL2000,GBAL3000,GBAL4000
          GOTO      GBAL9999
.
GBAL1000  DISPLAY   *P29:9,*V2LON,"Less than 30 Days";
          GOTO      GBAL9000
.
GBAL2000  DISPLAY   *P29:9,*V2LON,"30 Days plus";
          GOTO      GBAL9000
.
GBAL3000  DISPLAY   *P29:9,*V2LON,"60 Days plus";
          GOTO      GBAL9000
.
GBAL4000  DISPLAY   *P29:9,*V2LON,"90 Days plus";
          GOTO      GBAL9000
.
GBAL9000  MOVE      FORM1,XBALTYPE
          DISPLAY   *P1:20,*EL;
GBAL9999  RETURN
+
.******************************************************************************
.*                                  KUSR0000                                  *
.*                     Keyin user id                                          *
.******************************************************************************
KUSR0000  KEYIN     *P29:10,*EL,*V2LON,USERID;
          PACK      USERID,USERID,SP70
          MATCH     SP70,USERID
          GOTO      KUSR9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KUSR9999
.
KUSR9500  MOVE      ONE,EXIT
KUSR9999  RETURN
+
.***************************************************************************
.*                                LOAD0000             Called by: ML0000   *
.*                     Load tempfile                                       *
.***************************************************************************
LOAD0000  DISPLAY   *P1:24,*EL,*P20:24,"Surname : ":
                               *P50:24,"Admission Number :";
.
          MOVE      USERID,KEY10
          CALL      RDWBSE1
          BRANCH    OVRCD,LOAD9000
.
.         Set Current date as the As at date
.
          MOVE      CCC,CC
          MOVE      CYY,YY
          MOVE      CMM,MM
          MOVE      CDD,DD
          CALL      DMYCON
          MOVE      JULDAY TO ASATDAY
          MOVE      JULYR TO ASATYER
          MOVE      JULCC,ASATCCC
.
          PACK      XJRNLDAT,CCC,CYY,CMM,CDD
          REP       " 0",XJRNLDAT             * Set journal date - Current date
.
LOAD0800  PACK      KEY16,SP70
          CALL      RSPRIN2
LOAD1000  CALL      RKPRIN2
          BRANCH    OVRCD TO LOAD8000
.
          MOVE      PRINDEBT,PURNO          * PMI debtor
          MOVE      PURNO,KEY8
          CALL      RDMAST1                 * read master record
          BRANCH    OVRCD,LOAD1000          * not on file
.
.         Display invoice details
.
          DISPLAY   *P31:24,*V2LON,PRINNAME,*P70:24,PRINNUMB
.
.         loop through DTR file for transactions for this invoice
.
          MOVE      SP70,XPRHRBCD           * initialise health fund
          MOVE      ZERO,TMPPAID            * initialise total payments
.
          PACK      KEY24,PRINDEBT,PRINSCAN,PRINNUMB,SP10
          CALL      RSPRDT3                 * position in file
LOAD1500  CALL      RKPRDT3                 * read next record
          BRANCH    OVRCD,LOAD2500          * end of file
.
          MATCH     PRINDEBT,PRDTDEBT       * same debtor number ?
          GOTO      LOAD2500 IF NOT EQUAL   * no
.
          COMPARE   PRINSCAN,PRDTSCAN       * same scan flag ?
          GOTO      LOAD2500 IF NOT EQUAL   * no
.
          MATCH     PRINNUMB,PRDTINVN       * same invoice number ?
          GOTO      LOAD2500 IF NOT EQUAL   * no
.
          MATCH     SP70,XPRHRBCD
          IF        @EQUAL
            PACK      KEY27,PRDTUNIQ,PRDTPRAC,PRDTDOCT,PRDTCODE
            CALL      RDPRHR1
            IF        OVRCD<>1
              MOVE      PRHRBCOD,XPRHRBCD     
            ENDIF
          ENDIF
.
.         Valid transaction found, so process only receipt and journal entries
.
          BRANCH    PRDTRTYP,LOAD1500,LOAD2000,LOAD2000
.
LOAD2000  ADD       PRDTAMNT,TMPPAID        * increment total payments
          IF        IBCNUGST=2 & PRDTRTYP=3
            ADD       PRDTGSTM,TMPPAID      * GST Journal
          ENDIF
          GOTO      LOAD1500                * get next DTR record
.
.         Check if invoice has zero balance
.
LOAD2500  CALL      CRDN0000                * Check credit note
          ADD       FORM12P2,PRINITOT
.
          MOVE      PRINITOT,FORM12P2
          ADD       TMPPAID,FORM12P2
.
          COMPARE   ZERO,FORM12P2           * Invoice outstanding ?
          GOTO      LOAD1000 IF EQUAL       * no - ignore
.
.         Check for Minimum and Maximum amount
.
          IF        FORM12P2<0
            COMPARE   ZERO,XMXCRAMT
            GOTO      LOAD1000 IF EQUAL
            MOVE      FORM12P2,F12P2
            MULT      "-1",F12P2
            COMPARE   F12P2,XMXCRAMT    * check against Max.amt in Credit
            GOTO      LOAD1000 IF LESS
          ELSE
            COMPARE   ZERO,XMNDBAMT
            GOTO      LOAD1000 IF EQUAL
            COMPARE   FORM12P2,XMNDBAMT * check against Max amt outstanding
            GOTO      LOAD1000 IF LESS  * ignore if greater
          ENDIF
.
          CALL      CAGE0000             * Check days since invoice raised
          GOTO      LOAD1000
.
LOAD8000  MOVE      ZERO,EXIT
          GOTO      LOAD9999
.
LOAD9000  MOVE      ONE,EXIT
LOAD9999  RETURN
+
.***************************************************************************
.         Check number of days since the invoice was raised
.***************************************************************************
CAGE0000  UNPACK    PRINDATE INTO XCENT,XYEAR,XMON,XDAY
          MOVE      XCENT,CC
          MOVE      XYEAR TO YY
          MOVE      XMON TO MM
          MOVE      XDAY TO DD
          CALL      DMYCON
.
          PACK      CNYRPINV,CC,YY              * Patient Invoice date
          REP       " 0",CNYRPINV
          MOVE      JULDAY,INVDAYS
.
          PACK      CNYRASAT,ASATCCC,ASATYER,SP4
          REP       " 0",CNYRASAT
          MOVE      ASATDAY,FORM5DAY
.
          MATCH     CNYRASAT,CNYRPINV
          GOTO      CAGE2000 IF EQUAL       * same year, check day and month
          GOTO      CAGE1000 IF LESS
          GOTO      CAGE9999
.
CAGE1000  MOVE      CNYRASAT TO DAYSINYR
          MOVE      CNYRPINV,FORM4
          SUB       FORM4 FROM DAYSINYR     * calculate years difference
          MULT      "365" BY DAYSINYR       * convert to days
          ADD       DAYSINYR,FORM5DAY
.
CAGE2000  SUB       INVDAYS FROM FORM5DAY
.
          COMPARE   ZERO,FORM5DAY
          GOTO      CAGE9999 IF LESS
.
          MOVE      FORM12P2,XJRNAMNT       * the Journal Amount
.
          BRANCH    XBALTYPE,CAGE3000,CAGE4000,CAGE5000,CAGE6000
.
.         less 30 days (include 30 days)
.
CAGE3000  COMPARE   "31",FORM5DAY
          GOTO      CAGE8000 IF LESS
          GOTO      CAGE9999
.
.         30 - 60 days only (include 60 days)
.
CAGE4000  COMPARE   "31",FORM5DAY
          GOTO      CAGE9999 IF LESS
          COMPARE   "61",FORM5DAY
          GOTO      CAGE8000 IF LESS
.
          GOTO      CAGE9999
.
.         60 - 90 Days only (include 90 days)
.
CAGE5000  COMPARE   "61",FORM5DAY
          GOTO      CAGE9999 IF LESS
          COMPARE   "91",FORM5DAY
          GOTO      CAGE8000 IF LESS
.
          GOTO      CAGE9999
.
.         90 Days plus
.
CAGE6000  COMPARE   "91",FORM5DAY
          GOTO      CAGE8000 IF NOT LESS
.
          GOTO      CAGE9999
.
.         Write to tempfile
.
CAGE8000  IF        XJRNAMNT>0
            MOVE      XJRNLDEB,XJRNLCOD            * Journal type (debit)
            MOVE      "A",XJRNINDC
          ELSE
            MOVE      XJRNLCRD,XJRNLCOD            * Journal type (credit)
            MOVE      "B",XJRNINDC
          ENDIF
.
          PACK      XJRNLDAT,CCC,CYY,CMM,CDD
          REP       " 0",XJRNLDAT             * Set journal date - Current date
.
          PACK      KEY17,XJRNINDC,PRINDATE,PRINNUMB
          CALL      WRTEMP1
.
CAGE9999  RETURN
+
.******************************************************************************
.*        CRDN0000 - Credit for credit note                                   *
.******************************************************************************
CRDN0000  MOVE      ZERO,FORM12P2
          PACK      KEY16,PRINNUMB,SP70
          CALL      RSPRCNO3
CRDN1000  CALL      RKPRCNO3
          BRANCH    OVRCD,CRDN9999
          MATCH     PRCOINVN,PRINNUMB       * Same invoice number?
          GOTO      CRDN9999 IF NOT EQUAL   * No
.
          ADD       PRCOAMNT,FORM12P2
          GOTO      CRDN10000
.
CRDN9999  RETURN
+
.***************************************************************************
.*                                PROC0000             Called by: ML0000   *
.
.*                     Process tempfile                                    *
.***************************************************************************
PROC0000  CALL      PHED0000                   * Print heading
.
          MOVE      SP70,KEY17
          CALL      RSTEMP1
PROC1000  CALL      RKTEMP1
          BRANCH    OVRCD,PROC9999
.
          MOVE      PRINNUMB,KEY8
          CALL      RDPRIN1
          BRANCH    OVRCD,PROC1000
.
          MOVE      PRINDEBT,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,PROC1000
.
          MOVE      PSNAME,PACSNAME            * set up patients name
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
.
          UNPACK    PRINDATE,XCENT,XYEAR,XMON,XDAY
          PACK      DIM10,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
          REP       " 0",DIM10               * Invoice date
.
          PACK      KEY10,CDD,SLASH,CMM,SLASH,CCC,CYY
          REP       " 0",KEY10               * journal date will be current date
.
          MOVE      XJRNAMNT,FORM12P2
          MULT      "-1",FORM12P2
          PRINT     "<tr><td>",PRINNUMB,"</td>":
                      "<td>",DIM10,"</td>":
                      "<td>",FORM12P2,"</td>":
                      "<td>",XJRNLCOD,"</td>":
                      "<td>",KEY10,"</td>":
                      "<td>",PRINDEBT,"</td>":
                      "<td>",PACFNAME,"</td>":
                      "<td>",PRINCLAM,"</td>":
                      "<td>",XPRHRBCD,"</td>":
                      "<td>",USERID,"</td></tr>"
.
          BRANCH    OPTION,PROC1000               * Report Only
.
          PACK      KEY5,CODEJ,XJRNLCOD,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,PROC1000
.
          CALL      UPJR0000                      * Update Journal Record
          GOTO      PROC1000
.
PROC9999  RETURN
+
.**********************************************************************
.         Update Journal files
.**********************************************************************
UPJR0000  MOVE      ZERO,EXIT
          CALL      CLPRIDTR
.
          MOVE      TDESC,PRDTDESC
.
          MOVE      XJRNAMNT,PRDTAMNT
          MULT      SEQ,PRDTAMNT         * store as negative amount
.
          ADD       PRDTAMNT,PRINJAMT
          ADD       PRDTAMNT,PRINPEND
.
UPJR2200  MOVE      PRINTRAN,NXTRAN              * get DTRAN number
          ADD       ONE,PRINTRAN                 * increment DTRAN number
          PACK      PRINUDAT,CCC,CYY,CMM,CDD
          REP       " 0",PRINUDAT             * date updated
          CLOCK     TIME,PRINUTIM             * time updated
          MOVE      USERID,PRINUUID           * web user id
          CALL      UPPRIN1                   * update invoice record
.
          IF        PRINSCAN = 1
            PROC      FLAGDEBP                * Flag PMI Outstanding Debt
          ENDIF
.
. ------- Write the DTR record ----------------
.         Make sure transaction not already on file
.
          PACK      KEY22,PRINUNIQ,PRINNUMB,NXTRAN
          CALL      RAPRDT1
          IF        OVRCD=0
            MOVE      PRINNUMB,KEY8
            CALL      RDPRIN1
            BRANCH    OVRCD,UPJR9999
            GOTO      UPJR2200        * Transaction # on file, get next trans. #
          ENDIF

          MOVE      PRINDEBT,PRDTDEBT             * load file variables
          MOVE      ONE,PRDTSCAN
          MOVE      XJRNLDAT,PRDTSDAT
          MOVE      SP6,PRDTSTIM
          MOVE      PRINPRAC,PRDTPRAC
          MOVE      PRINDOCT,PRDTDOCT
          MOVE      ONE,PRDTSERV
          MOVE      SP10,PRDTREFD
          PACK      PRDTREFT,SP30,SP20
          MOVE      PRINCLAM,PRDTCLAM
          MOVE      THREE,PRDTRTYP
.
          MOVE      XJRNLCOD,PRDTTTYP
          MOVE      ONE,PRDTINVP
          MOVE      PRDTSDAT,PRDTEFFD
.
          PACK      KEY22,PRINUNIQ,PRINNUMB,NXTRAN,SP70
          CALL      WRPRDT1                      * write DTR record
.
          CALL      CBAL0000                     * Check if invoice now balanced
.
          PACK      KEY22,PRINUNIQ,PRINNUMB,NXTRAN,SP70
          CALL      RDPRDT1                      * Re read DTR record
.
. ------- Write the journal record -----------------
.
          MOVE      SP4,PRJRSPAR
          PACK      KEY35,XJRNLCOD,XJRNLDAT,PRDTDEBT,PRDTSCAN,NXTRAN,PRDTINVN
          CALL      WRPRJR1
.
. ------- Update financial summary file ------------
.
          MOVE      ANSC,PRFNTYPE                * set financial type - journals
          IF        PRCNAFEE=1
            PACK      PRFNCODE,PRDTDOCT,PRDTTTYP,SP30
          ELSE
            PACK      PRFNCODE,PRDTTTYP,SP30
          ENDIF
          MOVE      PRDTPRAC,PRFNMPRA
          UNPACK    PRDTSDAT,XCENT,XYEAR,XMON,XDAY
          PACK      FSTDATE,XCENT,XYEAR,XMON,XDAY
          REP       " 0",FSTDATE
.
          MOVE      PRDTAMNT,FSTAMNT
          MULT      SEQ,FSTAMNT                  * reverse sign for fees
          MOVE      ZERO,FGSTFLAG
          CALL      PRICALFS
          BRANCH    EXIT,UPJR9100                * date range not found
.
. ------- Update stats file ------------------------
.
.         Get the period from the date range period file
.
          MOVE      ZERO,JNLAMNT
          MOVE      DRGNUM,PERNUM
.
          MOVE      PRINPRAC,PRSTPRAC
          MOVE      PRINDOCT,PRSTDOCT
          PACK      KEY20,PRFNYEAR,PRSTPRAC,PRSTDOCT
          CALL      RDPRST1                      * record exists ?
          BRANCH    OVRCD,UPJR5000               * no
.
.         Update the existing journal amount for the appropriate period
.
          LOAD      JNLAMNT,PERNUM,PRSTJP01,PRSTJP02,PRSTJP03,PRSTJP04,PRSTJP05:
                    PRSTJP06,PRSTJP07,PRSTJP08,PRSTJP09,PRSTJP10,PRSTJP11:
                    PRSTJP12,PRSTJP13
.
          ADD       PRDTAMNT,JNLAMNT             * add journal trax. amount
          ADD       PRDTGSTM,JNLAMNT
.
          STORE     JNLAMNT,PERNUM,PRSTJP01,PRSTJP02,PRSTJP03,PRSTJP04,PRSTJP05:
                    PRSTJP06,PRSTJP07,PRSTJP08,PRSTJP09,PRSTJP10,PRSTJP11:
                    PRSTJP12,PRSTJP13
.
          CALL      UPPRST1
          GOTO      UPJR9999
.
.         Statistics record not found - so create one
.
UPJR5000  MOVE      ZERO,FORM2                   * initialise counter
UPJR5200  ADD       ONE,FORM2                    * increment counter
.
          COMPARE   FORM2,FORTY                  * all fields initialised ?
          GOTO      UPJR6000 IF LESS             * yes
.
          STORE     ZERO,FORM2,PRSTOBAL,PRSTIP01,PRSTIP02,PRSTIP03,PRSTIP04:
                    PRSTIP05,PRSTIP06,PRSTIP07,PRSTIP08,PRSTIP09,PRSTIP10:
                    PRSTIP11,PRSTIP12,PRSTIP13,PRSTCP01,PRSTCP02,PRSTCP03:
                    PRSTCP04,PRSTCP05,PRSTCP06,PRSTCP07,PRSTCP08,PRSTCP09:
                    PRSTCP10,PRSTCP11,PRSTCP12,PRSTCP13,PRSTJP01,PRSTJP02:
                    PRSTJP03,PRSTJP04,PRSTJP05,PRSTJP06,PRSTJP07,PRSTJP08:
                    PRSTJP09,PRSTJP10,PRSTJP11,PRSTJP12,PRSTJP13
          GOTO      UPJR5200
.
UPJR6000  MOVE      SP20,PRSTSPAR
.
          ADD       PRDTAMNT,JNLAMNT             * add journal trax. amount
          ADD       PRDTGSTM,JNLAMNT
.
          STORE     JNLAMNT,PERNUM,PRSTJP01,PRSTJP02,PRSTJP03,PRSTJP04,PRSTJP05:
                    PRSTJP06,PRSTJP07,PRSTJP08,PRSTJP09,PRSTJP10,PRSTJP11:
                    PRSTJP12,PRSTJP13
          CALL      WRPRST1
          GOTO      UPJR9999
.
UPJR9100  MOVE      TWO,EXIT
UPJR9999  RETURN
+
.**************************************************************************
. CBAL0000 : Check if invoice is balance
.**************************************************************************
CBAL0000  MOVE      PRINITOT,FORM12P2
          ADD       PRINPAMT,FORM12P2
          ADD       PRINHAMT,FORM12P2
          ADD       PRINIAMT,FORM12P2
          ADD       PRINMAMT,FORM12P2
          ADD       PRINVAMT,FORM12P2
          ADD       PRINOTHA,FORM12P2
          ADD       PRINJAMT,FORM12P2
.
          COMPARE   ZERO,FORM12P2
          GOTO      CBAL9999 IF NOT EQUAL       * invoice not balanced
.
          PACK      KEY22,PRINUNIQ,PRINNUMB,SP20
          CALL      RSPRDT1
CBAL1000  CALL      RKPRDT1
          BRANCH    OVRCD,CBAL9999
.
          COMPARE   PRINUNIQ,PRDTUNIQ
          GOTO      CBAL9999 IF NOT EQUAL       * different unique number
.
          MATCH     PRINNUMB,PRDTINVN
          GOTO      CBAL9999 IF NOT EQUAL       * different invoice number
.
          BRANCH    PRDTRTYP,CBAL2000,CBAL1000,CBAL2000  * only item & journal
.
CBAL2000  MOVE      TWO,PRDTAPAY                * accounts payable flag- balance
          CALL      UPPRDT1
          GOTO      CBAL1000
.
CBAL9999  RETURN
+
.***************************************************************************
.         Print heading
.***************************************************************************
PHED0000  PRINT     "<table border=1><tr><td width=10%>Invoice Number</td>":
                    "<td>Invoice Date</td>":
                    "<td>Journal Amount</td>":
                    "<td>Journal Code</td>":
                    "<td>Journal Date</td>":
                    "<td>U/R No.</td>":
                    "<td>Patient Name</td>":
                    "<td>Claim Code</td>":
                    "<td>Health Fund</td>":
                    "<td>User Id</td></tr>"

PHED9999  RETURN
+
.***************************************************************************
.*                                SCRN0000             Called by: ML0000   *
.*                     display the input screen                            *
.***************************************************************************
SCRN0000  DISPLAY   *P1:3,*EF,*P2:4," Minimum Amount O/S":
                    *P27:4,COLON:
                    *P2:5," Journal Type (Debit)":
                    *P27:5,COLON:
                    *P2:6," Maximum Amount in Credit":
                    *P27:6,COLON:
                    *P2:7," Journal Type (Credit)":
                    *P27:7,COLON:
                    *P2:9," Balances greater than":
                    *P27:9,COLON:
                    *P2:10," User ID":
                    *P27:10,COLON

SCRN9999  RETURN
+
.*************************************************************************
.       Create Tempfile
.*************************************************************************
CTMP0000  CALL      KILL0000
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMET,CMDLINE
          APPEND    " 56 ",CMDLINE
          APPEND    "U17-17,9-16,1-8",CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      TMPFILE1,FNAMET
.
CTMP9999  RETURN
+
.******************************************************************************
.         Delete records from tempfile
.******************************************************************************
KILL0000  CLOSE     TMPFILE1
          PACK      CMDLINE,SP70,SP70
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMET,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
KILL9999  RETURN
+
.==============================================================================
.         temp file
.==============================================================================
RSTEMP1   READ      TMPFILE1,KEY17;;
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TMPFILE1;PRINNUMB,PRINDATE,XJRNINDC,XJRNLCOD,XJRNLDAT:
                             XJRNAMNT,PRINCLAM,XPRHRBCD,WBSEUID
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTEMP1   UPDATE    TMPFILE1;PRINNUMB,PRINDATE,XJRNINDC,XJRNLCOD,XJRNLDAT:
                             XJRNAMNT,PRINCLAM,XPRHRBCD,WBSEUID
          RETURN
.
WRTEMP1   WRITE     TMPFILE1,KEY17;PRINNUMB,PRINDATE,XJRNINDC,XJRNLCOD:
                             XJRNLDAT,XJRNAMNT,PRINCLAM,XPRHRBCD,WBSEUID
          RETURN
.
+
.==============================================================================
.
          INC       STD001IO
          INC       PATCODKY
          INC       TFILENAM                     * Generate Temp File Name
          INC       PRICALFS
          INC       PMSOSPTM
          INC       CLPRIDTR
.
          INC       WEBSECIO/INC
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       WEBERRIO/INC                 * Web Server Error Logging
.
          INC       IBAPASIO/INC
          INC       IBASECIO/INC
.
          INC       PRISTAIO/INC                 * financial statistics file
          INC       PRIPRAIO/INC                 * medical practice file
          INC       PRIHREIO/INC                 * doctor referral details file
.
          INC       PATCODIO/INC
          INC       PATDRGIO/INC
          INC       PRIFINIO/INC
          INC       PRIFIGIO/INC
          INC       PATFN1IO/INC
          INC       PRIJRNIO/INC
          INC       PRIINVIO/INC
          INC       PRIDTRIO/INC
          INC       PATMA1IO/INC
          INC       PRICNOIO/INC
          INC       RCPDTEIO/INC
          INC       RCPBNKIO/INC
          INC       RCPREGIO/INC
.
.
+

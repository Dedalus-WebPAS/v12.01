.***************************************************************************
.* System    :   Patient                                                   *
.* Program   :   FX884286                                                  *
.* Desc      :   Fix category codes 'Ou','Tk' and 'Tq' mapping on oprsrgaf *
.*               and oprardaf                                              *
.***************************************************************************
.* Date      :   01/06/2020                                                *
.* Author    :   Tracey Nguyen                                             *
.* Function  :   Fix category codes 'Ou','Tk' and 'Tq' mapping on oprsrgaf *
.*               and oprardaf as follows:                                  *
.*               Step 1) Copy oprsgraf.opsguc06 to oprsrgaf.opsguc12 and   *
.*                       reset oprsrgaf.opsguc06 to spaces                 *
.*                       (this step is moving the data that currently      *
.*                        resides against Category 'Tk' and move it to a   *
.*                        new category 'Tq')                               *
.*               Step 2) Copy oprardaf.oparrdcd to oprsagaf.opsguc06 and   *
.*                       reset oprardaf.oparrdcd to spaces                 *
.*                       (this step is moving the data that currently      *
.*                        resides against Category 'Ou' and move it to     *
.*                        category 'Tk')                                   *
.* Modifications  :                                                        *
.*        V11.00.02 22/06/2020  Ebon Clements     TSK 0884286              *
.*                  Cabrini went live with V11.00 on 22/06/2020 at 2am     *
.*                  So don't update records on or after 22/06/2020         *
.*        V11.00.01 01/06/2020  Tracey Nguyen     TSK 0884286              *
.*                  Copied from FX877621.PBL                               *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       OPRSRGFD/INC
          INC       OPRARDFD/INC
          INC       OPRDEAFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
RECCOUNT  FORM      8
RECCOUN1  FORM      8
TOTALCNT  FORM      8
TOTALCN1  FORM      8
SAVGUC12  DIM       3
FOUNDSRG  FORM      1
.
PRGID     INIT      "FX884286"
PRGNAM    INIT      "Fix New fields moved to oprsrgaf"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000,ML1000  * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process oprsrgaf
          CALL      PARD0000               * process oprardaf
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P64:24,"oprsrgaf";
          OPEN      OPRSRGA1,"oprsrgaf"
.
          DISPLAY   *P64:24,"oprardaf"
          OPEN      OPRARDA1,"oprardaf"
.
          DISPLAY   *P64:24,"oprdetaf"
          OPEN      OPRDETA3,"oprdetaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". List Program":
                    *P1:6,*V2LON,TWO,*HOFF,". Fix Program"
.
OPTN0500  KEYIN     *P1:10,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:10,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000,OPTN9000
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
PROC0000  CALL      IBACLOCK
          MOVE      "Fixit oprsrgaf ",PRGNAM
.
          CALL      HEAD80
          MOVE      ZERO,RECCOUNT       * Number of oprsrgaf records updated
          MOVE      ZERO,TOTALCNT       * Total number of oprsrgaf records read
.
          CALL      UND80
          PRINT     *1,"| Operating Surgical Details ":
                    *40,"| oprsrgaf.OPSGUC06 ":
                    *60,"| oprsrgaf.OPSGUC12 ",*80,"|"
          CALL      UND80
.
          PACK      KEY11,SP70
          CALL      RSOPSRG1
PROC1000  CALL      RKOPSRG1               * Loop through oprsrgaf file       
          BRANCH    OVRCD,PROC9000
          ADD       ONE,TOTALCNT    
.
          DISPLAY   *P30:24,"Operation Surgical Id.: ",OPSGUNID,OPSGTMNO
.
          PACK      KEY10,OPSGUNID,SP70
          CALL      RDOPDEA3               * Read operation details
          BRANCH    OVRCD,PROC1000
.
          MATCH     "20200622",OPDADATE    * After V11 go live date
          GOTO      PROC1000 IF NOT LESS
.
          CALL      CBDI0000               * Check if any values to update
          BRANCH    EXIT,PROC1000 
.
          PACK      OPSGUC12,SAVGUC12,SP70
          PACK      OPSGUC06,SP70          * Reset to spaces

          IF        COPTION=2
            CALL      UPOPSRG1             * Update oprsrgaf
          ENDIF
.
          ADD       ONE,RECCOUNT           * Number of oprsrgaf records updated
. 
          PRINT     *1,"| ",OPSGUNID,OPSGTMNO:
                    *40,"| ",OPSGUC06,*60,"| ",OPSGUC12,*80,"|"
.
          GOTO      PROC1000
.
.
PROC9000 
          IF        COPTION=2
            CALL      UND80
            PRINT     *N,*1,"Total OPRSRGAF records updated: ",RECCOUNT
          ELSE
            CALL      UND80
            PRINT     *N,*1,"Total OPRSRGAF records to update: ",RECCOUNT
          ENDIF
.
          PRINT     *N,*1,"Total number of OPRSRGAF records Read: ",TOTALCNT

          DISPLAY   *P1:21,*EL,"Total OPRSRGAF records to update: ":
                    *V2LON,RECCOUNT,*HOFF,SP5;
.
          DISPLAY   *P1:23;
          CALL      HITENTER
.
PROC9999  RETURN
+
.**************************************************************************
.*                               CBDI0000              Called by: PROC0000*
.*       Check if any values should be updated                           .*
.**************************************************************************
CBDI0000  MOVE      ZERO,EXIT
          MOVE      SP70,SAVGUC12
.
          MATCH     SP70,OPSGUC12                
          GOTO      CBDI9000 IF NOT EQUAL        * no update required 
.
          MATCH     SP70,OPSGUC06                
          GOTO      CBDI9000 IF EQUAL            * no update required
.
          PACK      SAVGUC12,OPSGUC06,SP70
.
CBDI8000  MOVE      ZERO,EXIT                    * update required        
          GOTO      CBDI9999
.
CBDI9000  MOVE      ONE,EXIT                     * no update required            
CBDI9999  RETURN
.
.**************************************************************************
.*                               PARD0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
PARD0000  CALL      IBACLOCK
          MOVE      "Fixit oprsrgaf and oprardaf ",PRGNAM
.
          CALL      HEAD80

          MOVE      ZERO,RECCOUNT       * Number of oprsrgaf records updated
          MOVE      ZERO,RECCOUN1       * Number of oprardaf records updated
          MOVE      ZERO,TOTALCNT       * Total number of oprsrgaf records read
          MOVE      ZERO,TOTALCN1       * Total number of oprardaf records read
.
          CALL      UND80
          PRINT     *1,"| Operating Surgical Details ":
                    *40,"| oprsrgaf.OPSGUC06 ":
                    *60,"| oprardaf.OPARDCD  ",*80,"|"
          CALL      UND80
.
          PACK      KEY10,SP70
          CALL      RSOPARD1
PARD1000  CALL      RKOPARD1               * Loop through oprardaf file       
          BRANCH    OVRCD,PARD9000
          ADD       ONE,TOTALCN1
.
          DISPLAY   *P30:24,"Operation Id.: ",OPARUNID
.
          MATCH     SP70,OPARRDCD
          GOTO      PARD1000 IF EQUAL
.
          PACK      KEY10,OPARUNID,SP70
          CALL      RDOPDEA3               * Read operation details
          BRANCH    OVRCD,PARD1000
.
          MATCH     "20200622",OPDADATE    * After V11 go live date
          GOTO      PARD1000 IF NOT LESS
.
          CALL      CARD0000               * Check if any values to update
          BRANCH    FOUNDSRG,PARD1000      * Not found any oprsrgaf to update
.
          IF        COPTION=2
            MOVE    SP70,OPARRDCD        * Reset field OPARRDCD
            CALL    UPOPARD1             * Update oprardaf          
          ENDIF
.
          ADD       ONE,RECCOUN1           * oprardaf update counter
          GOTO      PARD1000
.
.
PARD9000 
          IF        COPTION=2
            CALL      UND80
            PRINT     *N,*1,"Total OPRSRGAF records updated: ",RECCOUNT
            PRINT     *N,*1,"Total OPRARDAF records updated: ",RECCOUN1
          ELSE
            CALL      UND80
            PRINT     *N,*1,"Total OPRSRGAF records to update: ",RECCOUNT
            PRINT     *N,*1,"Total OPRARDAF records to update: ",RECCOUN1
          ENDIF
.
          PRINT     *N,*1,"Total number of OPRSRGAF records Read: ",TOTALCNT
          PRINT     *N,*1,"Total number of OPRARDAF records Read: ",TOTALCN1

          DISPLAY   *P1:21,*EL,"Total OPRSRGAF records to update: ":
                    *V2LON,RECCOUNT,*HOFF,SP5;
          DISPLAY   *P1:22,*EL,"Total OPRARDAF records to update: ":
                    *V2LON,RECCOUN1,*HOFF,SP5;
.
          DISPLAY   *P1:23;
          CALL      HITENTER
.
PARD9999  RETURN
+
.**************************************************************************
.*                               CARD0000              Called by: PARD0000*
.*       Check if any values should be updated                           .*
.**************************************************************************
CARD0000  MOVE      ONE,FOUNDSRG
.
          PACK      KEY11,OPARUNID,SP70
          CALL      RSOPSRG1
CARD1000  CALL      RKOPSRG1                      * Loop through oprsrgaf file
          BRANCH    OVRCD,CARD9999
          ADD       ONE,TOTALCNT
.
          MATCH     OPARUNID,OPSGUNID             * Same id
          GOTO      CARD9999 IF NOT EQUAL         * No, exit
.
          MATCH     SP70,OPSGUC06                                  
          GOTO      CARD1000 IF NOT EQUAL           
.
          MOVE      OPARRDCD,OPSGUC06
          IF        COPTION=2
            CALL      UPOPSRG1                    * Update oprsrgaf
          ENDIF
.
          PRINT     *1,"| ",OPSGUNID,OPSGTMNO:
                    *40,"| ",OPSGUC06,*60,"| ",*80,"|"
.
          ADD       ONE,RECCOUNT                      * opsrgaf update counter
          MOVE      ZERO,FOUNDSRG                     * Update required
          GOTO      CARD1000
.   
CARD9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       OPRSRGIO/INC
          INC       OPRARDIO/INC
          INC       OPRDEAIO/INC
         

. ******************************************************************
. *                                                                *
. *                 P R O G R A M  S U M M A R Y                   *
. *                 - - - - - - -  - - - - - - -                   *
. *                                                                *
. *     Program Name   :  MAINSYST                                 *
. *                                                                *
. *     Function       :  Maintenance of systable file             *
. *                                                                *
. *     Modifications  :                                        *
. *       V9.03.01  09/09/2003  CAR 43237   Mike Laming                       *
. *                 Modify to handle "systables" of either 177 or 561 chars   *
. *                 - change in Infomix SE                                    *
. *                                                                           *
. *                       V5.01.01 26.05.89 B.G.Ackland (I.B.A.)   *
. *                                Remove Need For Linking systable*
. *                                                                *
. *                       V5.01.02 26.06.90 B.G.Ackland (I.B.A.)   *
. *                                Remove Need isadd U1-17         *
. *                                                                *
. *                       V5.01.03 24.08.90 B.G.Ackland (I.B.A.)   *
. *                                Allow Change in Owner           *
. *                                                                *
. *                       V6.00.04 12.03.93 B.G.Ackland (I.B.A.)   *
. *                                *RV on Keyins                   *
. *                                                                *
. *                                                                *
. *                * A T T E N T I O N *                           *
. *                ---------------------                           *
. *        This   program   is  the   property  of                 *
. *        Information Builders Australia  Pty Ltd                 *
. *        and must not be utilised,  ENQified  or                 *
. *        copied in any manner whatsoever without                 *
. *        the  express   written   permission  of                 *
. *        Information Builders Australia Pty Ltd.                 *
. *                                                                *
. ******************************************************************
.
          INC       STD001FD
          INC       SYSTABFD/INC
.
. working variables
.
LLCNT     FORM      2
LONGRECD  FORM      1                                                  *I-90301
TABNAME   DIM       18
SYS       DIM       3
PATH      DIM       25
SQLDIR    DIM       40
OPENSYS   DIM       50
.
SP18      INIT      "                  "
SP40      INIT      "                                        "
SP50      INIT      "                                                  "
.
PRGID     INIT      "MAINSYST"
PRGNAM    INIT      "Informix System Tables Maintenance"
VERSION   INIT      "V12.01.00"
.
          CALL      DISPHEAD
          MOVE      SP40,TABNAME
MAIN0100  MOVE      SP40,SQLDIR
MAIN1000  KEYIN     *P5:5,*EF,"Systable Path   : ",*V2LON,*RV,SQLDIR
          ENDSET    SQLDIR
          APPEND    SP40,SQLDIR
          RESET     SQLDIR
          MATCH     SP40,SQLDIR
          GOTO      MAIN9999 IF EQUAL
          CALL      SETP0000
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      SYSTABLE,OPENSYS
          TRAPCLR   IO
.
.* ****************************************** Start of Addition        *I-90301
          MOVE      ZERO,LONGRECD
          IF        OVRCD = 1
            MOVE      ZERO,OVRCD
            MOVE      ONE,LONGRECD
            TRAP      OVERCOND IF IO
            OPEN      SYSTABLX,OPENSYS
            TRAPCLR   IO
          ENDIF
.* ****************************************** End of Addition          *I-90301
.
          COMPARE   ZERO,OVRCD
          GOTO      MAIN1500 IF EQUAL
          KEYIN     *P1:24,"Could NOT Open ",*DV,OPENSYS," Tap Enter.",ANS
          GOTO      MAIN1000
.
MAIN1500  KEYIN     *P23:5,*EL,*V2LON,*DV,SQLDIR,*HOFF,*P65:5,"Ok (":
                    *V2LON,"y",*HOFF,"/",*V2LON,"n",*HOFF,") ? ",*V2LON,ANS
          REP       UPPLOW,ANS
          MATCH     "N",ANS
          GOTO      MAIN1000 IF EQUAL
          MATCH     "Y",ANS
          GOTO      MAIN1500 IF NOT EQUAL
.
MAIN2000  KEYIN     *P5:7,*EF,"Table To Change : ",*V2LON,*RV,TABNAME
          ENDSET    TABNAME
          APPEND    SP18,TABNAME
          RESET     TABNAME
          MATCH     SP18,TABNAME
          GOTO      MAIN0100 IF EQUAL
          PACK      KEY26,TABNAME,SP8
.
          IF        LONGRECD = 0
            CALL      RSSYTB1               * read 177 byte record
            CALL      RKSYTB1               * read 177 byte record
            MOVE      SYTBPATH,SYTBPATX                                *I-90301
          ELSE                                                         *I-90301
            CALL      RSSYTX1               * read 561 byte record     *I-90301
            CALL      RKSYTX1               * read 561 byte record     *I-90301
            MOVE      SYTBPATX,SYTBPATH                                *I-90301
          ENDIF                                                        *I-90301
.
          BRANCH    OVRCD,MAIN2500
          MATCH     TABNAME,SYTBNAME
          GOTO      MAIN3000 IF EQUAL
          BEEP
MAIN2500  KEYIN     *P1:24,"*** Table Not on File. Tap Enter. ***":
                    ANS,*P1:24,*EL
          GOTO      MAIN2000
MAIN3000  DISPLAY   *P23:7,*EL,*V2LON,SYTBNAME,*P1:9," 1.",*P1:11," 2.",*HOFF:
                    *P5:9,"Current Path    : ",*P23:9,*V2LON,SYTBPATH,*HOFF:
                   *P5:11,"Current Owner   : ",*P23:11,*V2LON,SYTBOWN
.                          12345678901234567890
          CALL     SEL0000
          MOVE     SP40,TABNAME
          GOTO     MAIN2000
.
MAIN9999  CHAIN     PGM
.--------------------------------
. set directory path
.--------------------------------
SETP0000  MOVE      "40",LLCNT           * set up acct dir path
SETP1000  RESET     SQLDIR,LLCNT
          CMATCH    SP1,SQLDIR
          GOTO      SETP9000 IF NOT EQUAL
.
          SUB       ONE FROM LLCNT
          COMPARE   ZERO,LLCNT
          GOTO      SETP1000 IF NOT EQUAL
.
          CLEAR     SQLDIR
          GOTO      SETP9999
.
SETP9000  LENSET    SQLDIR
          RESET     SQLDIR
SETP9999  CLEAR     OPENSYS
          APPEND    SQLDIR,OPENSYS
          APPEND    "systables",OPENSYS
          RESET     OPENSYS
          RETURN
.
.
SEL0000   MOVE      FALSE,EXIT
          CALL      MAINQST
          COMPARE   ZERO,CCITEM                 * Post
          GOTO      SEL9000 IF EQUAL
          COMPARE   SEQ,CCITEM                  * Cancel
          GOTO      SEL9999 IF EQUAL
          BRANCH    CCITEM,SEL1000,SEL2000
          BEEP
          GOTO      SEL0000
.
SEL1000   KEYIN     *P23:9,*EL,*V2LON,*RV,SYTBPATX                     *C-90301
          ENDSET    SYTBPATX                                           *C-90301
          APPEND    SP50,SYTBPATX                                      *C-90301
          APPEND    SP50,SYTBPATX                                      *C-90301
          APPEND    SP50,SYTBPATX                                      *I-90301
          RESET     SYTBPATX                                           *C-90301
          MOVE      SYTBPATX,SYTBPATH                                  *I-90301
          DISPLAY   *P23:9,*EL,*V2LON,SYTBPATH
          GOTO      SEL0000
.
SEL2000   KEYIN     *P23:11,*EL,*V2LON,*RV,SYTBOWN
          ENDSET    SYTBOWN
          APPEND    SP40,SYTBOWN
          APPEND    SP40,SYTBOWN
          RESET     SYTBOWN
          DISPLAY   *P23:11,*EL,*V2LON,SYTBOWN
          GOTO      SEL0000
.
SEL9000   MOVE      ZERO,OVRCD                                         *C-90301
          IF        LONGRECD = 0
            CALL      UPSYTB1
          ELSE                                                         *I-90301
            CALL      UPSYTX1                                          *I-90301
          ENDIF                                                        *I-90301
.
SEL9999   RETURN
.
          INC       SYSTABIO/INC
          INC       STD001IO
.

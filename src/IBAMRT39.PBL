.******************************************************************************
.* System   :  MEDICAL RECORDS TRACKING SYSTEM                                *
.* Program  :  IBAMRT39                                                       *
.* Desc     :  RECORD MOVEMENT REPORT                                         *
.******************************************************************************
.* Date     :  19/06/89                                                       *
.* Author   :  D.Di.Paola                                                     *
.* Comments :  This program will produce a report on the movement of          *
.*          :  a specific record or a range of records within a date          *
.*          :  range. It used to keep track of the movement history           *
.*          :  of records showing recent movements first.                     *
.* Changes  :                                                                 *
.*        V10.04.01 14/07/2014  Ebon Clements  CAR 261630                     *
.*                  Removed port number from temp file name                   *
.*        V10.02.01 04/07/2011  Ebon Clements  CAR 240724                     *
.*                  Mods for MRT hospital/location - Added hospital           *
.*                  to location key                                           *
.*        V9.08.01  25/01/2007  Peter Vela CAR 127930                         *
.*                  Recompiled for MRTMASFD                                   *
.*        V9.03.01  21/11/2003  Mike Laming  CAR 43318                        *
.*                  Change EXTR3000 to not read to EOF if avoidable           *
.*        V9.02.02  01/11/2001  Greg Horvat                                   *
.*                  Recompiled for ACTCOMN1 - Added xtra option to the        *
.*                  parameter PNSWRACE                                        *
.*        V9.02.01  19/09/2001 Dean Cramer                                    *
.*                  Make so endless loop not encountered when name is         *
.*                  spaces.                                                   *
.*        V5.10.B01 23/03/2001 Glenn Saunders                                 *
.*                  Remove all references to PATSUR file (old surname)        *
.*        V5.09.02  18.01.2001 Bronko Sosic                           *
.*                  Recompiled for MRTHISFD                           *
.*        V5.09.01  20.12.2000 Bronko Sosic                           *
.*                  Recompiled for MRTHISFD                           *
.*        V5.08.02  25.10.2000 J.Tan                                  *
.*                  Recompiled for WEB Report Scheduler               *
.*        V5.08.01  21/08/2000  Caleb Sharman                         *
.*                  Changed Health Fund variables to be 8 chars       *
.*        V5.07.02  02/07/1999  Jill Habasque SRF 645708              *
.*                  Changed page length                               *
.*        V5.07.01  11/03/1999  Jim Stathopoulos                      *
.*                  Increased DATFORM1 and DATFORM2                   *
.*             V5.07.B02  15/01/1999  Nicole Harrington 507 rebound 67*
.*                        Mods to use HEAD132                         *
.*          :   V5.01.02  30/08/89 Graeme Williams                    *
.*          :             New PATCOMN1 (allows for merged U/R numbers)*
.*          :   V5.01.03  12/09/89 D.Di.Paola                         *
.*          :             Fixed testing of code ranges                * 
.*          :   V5.01.04  23/10/89 D.Di.Paola                         *
.*          :             Put in Readkp on history file               *
.*          :   V5.01.05  05/06/90 D.Di.Paola       SRF 103881        *
.*          :             Put option for "1" Date/Time Sequence or    *
.*          :                            "2" Surname Sequence         *
.*          :             Added tempfile1 to cater for this....       *
.*          :   V5.01.06  24/07/90 Paul Duncan                        *
.*          :            implemented second surname file              *
.*          :   V5.01.121 25/01/91 Justin Coates                      *
.*                        Added BOKRC1FD/IO                           *
.*              V5.01.122 13/08/1992  Graeme Williams                 *
.*                        Re-compiled for PATCOMN1 (V5.01.07)         *
.*              V5.01.123 17/05/1993  Robert Sumsion                  *
.*                        Converted vars and includes to mrt in       *
.*                        standard form.                              *
.*              V5.01.124 25/08/1993  ROD                             *
.*                        Recompiled for NZHIS                        *
.*              V5.01.125 25/05/1994  Sandra Barcham                  *
.*                        Fix for global recompile                    *
.**********************************************************************
.
          INC       STD001FD
          INC       NZHISIFD/INC                 * Include for NZHIS vars
          INC       NHIMASFD/INC                 * Include for NZHIS vars
          INC       PATCONFD/INC                 * Include for NZHIS vars
          INC       BOKRC1FD/INC                 * booking file
          INC       IBASEQFD/INC
          INC       PATCOMM/INC
          INC       PATGSRFD/INC
          INC       PATMA1FD/INC
          INC       PATPR1FD/INC
          INC       PATMI1FD/INC
          INC       PATCODFD/INC
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
          INC       TFILEVAR/INC
          INC       OUTPREFD/INC
          INC       MRTMASFD/INC
          INC       MRTLOCFD/INC
          INC       MRTHISFD/INC
          INC       MRTMOVFD/INC
          INC       WEBERRFD/INC
.
           INC        PATDHEAD/INC
           INC        PATCALAG/INC
.
.--------- temporary file definition used for Surname Sequence -------
.
TEMPFIL1   IFILE      SQL, FIXED=121, KEY="u1-35"
.
. NAME    TYPE   LENGTH    PHYSICAL  START    DESCRIPTION
.-------  -----  --------  --------  -----    -----------
NAMEDIM   DIM       25        25       1      Patient name
DUNIQUE   DIM       10        10      26      Unique Count
BORDATE   DIM       10        10      36      Borrowing Date
BTIME5    DIM        5         5      46      Borrowing Time
.MRMAURNO DIM        8         8      51      U/R No.
.MRMAHHOS DIM        3         3      59      Home Location Hospital
.MRMAHLOC DIM        4         4      62      Home location
.MRMADOTY DIM        3         3      66      Document type
.MRMAVOLN DIM        2         2      69      Volume no.
LOCATION  DIM       25        25      71      Location
REASON    DIM       26        26      96      Reason 
.
.                             e.o.r  121
.
.--------- temporary file definition used for totals -------
.
TEMPFIL2   IFILE      SQL, FIXED=35, KEY="u1-30"
.
. NAME    TYPE   LENGTH    PHYSICAL  START    DESCRIPTION
.-------  -----  --------  --------  -----    -----------
TOTLOCN   DIM       30        30       1      location description
TOTCNT    FORM      5         4       31      total no. of movements
.
.                             e-o-r = 35 
.
.**********************************************************************
.*                         CONSTANTS                                  *
.**********************************************************************
.
INTINDIC   INIT       "1"
EXTINDIC   INIT       "2"
HEADER1    INIT       "Internal Locations"
HEADER2    INIT       "External Locations"
HEADER3    INIT       "Specific U/R Number"
HEADER4    INIT       "All Records Within Date Range"
.
SEQHEAD1   INIT       "Date/Time Sequence"
SEQHEAD2   INIT       "Surname Sequence"
.
Z20        INIT       "ZZZZZZZZZZZZZZZZZZZZ"
.
.**********************************************************************
.*                         GLOBAL VARIABLES                           *
.**********************************************************************
.
BEGINCNT   FORM       1
BJDAY      FORM       3
.
CJDAY      FORM       3
CMDLINE    DIM        60
CODE       DIM        2
CODEDES1   DIM        6
CODEDES2   DIM        6
CLOSDATE   DIM        10
.
DATFORM1   FORM       8
DATFORM2   FORM       8
DESCRPT1   DIM        30
DESCRPT2   DIM        30
DIM30      DIM        30
.
ENDCODE    DIM        7
.
FROMDATE   DIM        8 
FNAMEI     DIM        8
FNAMER     DIM        8
FRSTGNAM   DIM       40
FULLGNAM   DIM       80
.
INDICAT    DIM        1
.
.
LINECNT    FORM       2
LOCNTEST   DIM        7
NMPNUMB    DIM        20
OPENDATE   DIM        10
.
PAGENO     FORM       8
RDAY       DIM        2
RMON       DIM        2
RYEAR      DIM        2
RCENT      DIM        2
RECOUNT    FORM       1
RECDCNT    FORM       5
.
SCNDGNAM   DIM       40
SEQOPTN    FORM       1
SEQTYPE    DIM        30
SEQUENCE   DIM        30
STRTCODE   DIM        7
.
TEMPUR     DIM        8 
TIMEIS     DIM        8
TODATE     DIM        8  
.
UNIQUE     FORM       10
.
PRGID     INIT      "IBAMRT39"
PRGNAM    INIT      "Record Movement Report"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                           MAINLINE                                 *
.*                      controlling logic                             *
.**********************************************************************
ML0000     CALL       INIT0000                      * intialization
ML0200     CALL       CLR0000                       * clear input variables
           CALL       DATE0000                      * Date Ranges 
           BRANCH     EXIT,ML9999
ML0500     CALL       OPTN0000                      * option screen
           BRANCH     EXIT,ML0200
           CALL       CREA0000                      * create temp files
           BRANCH     OPTION,ML1000,ML1000,ML2000,ML1200
           GOTO       ML0200 
.
.--------- External Locations and Internal Locations, All Records --------
.
ML1000     CALL       RNGE0000                    * input code ranges
.    
ML1200     CALL       SOPT0000                    * Sequence option 
           BRANCH     EXIT,ML0500                 * exit
           CALL       CONT0000                    * ok to continue...
           BRANCH     EXIT,ML1200,ML0200          * exit
           CALL       EXTR0000                    * Extraction routine 
           BRANCH     SEQOPTN,ML1500              * "1" = Date/Time Sequence
           CALL       PRSN0000                    * print by Surname Sequence
ML1500     CALL       TEMP0000                    * read tempfile & print totals
           GOTO       ML0200
.
.--------- Specific U/R Number ------
.
ML2000     CALL       PAT0000                     * enter U/R Number
           BRANCH     EXIT,ML0200,ML2000          * exit
           CALL       CONT0000                    * ok to continue
           BRANCH     EXIT,ML0500,ML0200
           CALL       EXTR0000                    * Extraction routine
           CALL       TEMP0000                    * read tempfile & print totals
           GOTO       ML0200  
.
ML9999     CALL       KILL0000                    * Delete temp files
           CHAIN      PGM
+
.**********************************************************************
.*                           INIT0000                                 *
.*                   call header routine and open files               *
.**********************************************************************
INIT0000    CALL      DISPHEAD                        * display header
            DISPLAY   *P64:24,"Opening mrtmasaf";
            OPEN      MRTMASA1,"mrtmasaf"             * open files
            DISPLAY   *P64:24,"Opening mrtmasaf";
            OPEN      MRTMASA2,"mrtmasaf"             * open files
            DISPLAY   *P64:24,"Opening mrtlocaf";
            OPEN      MRTLOCA1,"mrtlocaf"             
            DISPLAY   *P64:24,"Opening mrtlocaf";
            OPEN      MRTLOCA2,"mrtlocaf"             
            DISPLAY   *P64:24,"Opening mrtmovaf";
            OPEN      MRTMOVA1,"mrtmovaf"
            DISPLAY   *P64:24,"Opening mrthisaf";
            OPEN      MRTHISA1,"mrthisaf"
            DISPLAY   *P64:24,"Opening mrthisaf";
            OPEN      MRTHISA2,"mrthisaf"
            DISPLAY   *P64:24,"Opening mrthisaf";
            OPEN      MRTHISA3,"mrthisaf"
            DISPLAY   *P64:24,"Opening patma1af"
            OPEN      PATMA1A1,"patma1af"
            DISPLAY   *P72:24,"patmx1af";
            OPEN      PATMX1A1,"patmx1af"
            DISPLAY   *P64:24,"Opening patma1af";
          DISPLAY   *P64:24,"patgsrnf"
          OPEN      PATGSRN3,"patgsrnf"
          DISPLAY   *P64:24,"patgsrnf"
          OPEN      PATGSRN4,"patgsrnf"
          DISPLAY   *P64:24,"patgsrnf"
          OPEN      PATGSRN2,"patgsrnf"
            DISPLAY   *P64:24,"Opening patmi1af";
            OPEN      PATMI1A1,"patmi1af"
            DISPLAY   *P64:24,"Opening patpramf";
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
. ---- setup temporary file for Surname Sequence ----
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,FNAMEI
.
. ---- setup temporary file for Totals -------
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,FNAMER
.
INIT9999  RETURN
.
.***********************************************************************
.*                             CREA0000                                *
.*                    create tempfile for details                      *
.***********************************************************************
CREA0000
            DISPLAY   *P40:23,*EL,*V2LON,"Creating Temporary File - ":
                      *BLINKON,"Please Wait",*HOFF;
.
            CLEAR     CMDLINE
            APPEND    "isbuild ",CMDLINE                 * build tempfile
            APPEND    FNAMEI,CMDLINE
            APPEND    " 121 u1-35",CMDLINE
            RESET     CMDLINE
.
            EXECUTE   CMDLINE,TASKID
            OPEN      TEMPFIL1,FNAMEI
.
            CLEAR     CMDLINE
            APPEND    "isbuild ",CMDLINE                 * build tempfile
            APPEND    FNAMER,CMDLINE
            APPEND    " 35 u1-30",CMDLINE
            RESET     CMDLINE
.
            EXECUTE   CMDLINE,TASKID
            OPEN      TEMPFIL2,FNAMER
.
            DISPLAY   *P1:23,*EL;                    * clear message line
.
CREA9999    RETURN
+
.**********************************************************************
.*                           KILL0000                                 *
.*                       Kill tempfile                                *
.**********************************************************************
KILL0000  CLEAR     CMDLINE                        * erase filename
          CLOSE     TEMPFIL1
          APPEND    "iserase ",CMDLINE             * ie. mrttmr
          APPEND    FNAMEI,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
          CLEAR     CMDLINE                        * erase filename
          CLOSE     TEMPFIL2
          APPEND    "iserase ",CMDLINE             * ie. mrttmt
          APPEND    FNAMER,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
KILL9999  RETURN
+
.**********************************************************************
.*                           CLR0000                                  *
.*                       clear input variables                        *
.**********************************************************************
CLR0000     MOVE       ZERO,PAGENO
            MOVE       SP30,LOCATION
            MOVE       SP30,REASON
            UNPACK     SP30,SEQTYPE
            UNPACK     SP30,SEQUENCE
            UNPACK     SP30,RDAY,RMON,RYEAR,RCENT
            MOVE       ZERO,BEGINCNT
            MOVE       SP8,TEMPUR
            MOVE       SP7,STRTCODE
            MOVE       SP7,ENDCODE
            MOVE       SP2,CODE
            UNPACK     SP20,CODEDES1,CODEDES2
            MOVE       SP30,DIM30
            MOVE       ZERO,OPTION
            MOVE       SP30,DESCRPT1
            MOVE       SP30,DESCRPT2
            MOVE       SP1,INDICAT
.
CLR9999   RETURN
.
.**********************************************************************
.*                           OPTN0000                                 *
.*                    select option screen                            *
.**********************************************************************
OPTN0000    MOVE       FALSE,EXIT
            MOVE       "80",LINECNT
.
            HLINE      *G37,2,51,80
            DISPLAY    *P1:6,*EF:
                       *P1:7,*V2LON,"0 ",*HOFF,"= Exit":
                       *P1:8,*V2LON,"1 ",*HOFF,"=":
                       *P1:9,*V2LON,"2 ",*HOFF,"=":
                       *P1:10,*V2LON,"3 ",*HOFF,"=":
                       *P1:11,*V2LON,"4 ",*HOFF,"=":
                       *P5:8,"Internal Locations":
                       *P5:9,"External Locations":
                       *P5:10,"Specific U/R Number":
                       *P5:11,"All Records Within Date Range":
                       *P1:13,"Please Select : ";
.
OPTN1000    KEYIN      *P17:13,*V2LON,OPTION,*HOFF
            COMPARE    ZERO,OPTION
            GOTO       OPTN9000 IF EQUAL
            COMPARE    "5",OPTION   
            GOTO       OPTN0000 IF NOT LESS
.
.-------- if option 1 and 2, determine codes ie. 1 = Internal, 2 = External --
.
            MOVE       SP1,INDICAT
            LOAD       INDICAT,OPTION,INTINDIC,EXTINDIC
.
.-------- determine header -----
.
            UNPACK     SP30,SEQTYPE
            LOAD       SEQTYPE,OPTION,HEADER1,HEADER2,HEADER3,HEADER4
            DISPLAY    *P51:2,*V2LON,"- ",*+,SEQTYPE,SP1;
.
.----if option "3" chosen, = Specific U/R No. default to Date/Time Sequence ----
.                                             * surname seq. not needed
            MOVE       ONE,SEQOPTN            * date/time sequence
.
            GOTO       OPTN9999
. 
OPTN9000    MOVE       TRUE,EXIT                * set exit flag
.
OPTN9999    RETURN
+
.**********************************************************************
.*                           SOPT0000                                 *
.*                    select option screen                            *
.**********************************************************************
SOPT0000    MOVE       FALSE,EXIT
.
            DISPLAY    *P1:18,*EF:
                       *P1:18,*V2LON,"0 ",*HOFF,"= Exit":
                       *P1:19,*V2LON,"1 ",*HOFF,"=":
                       *P1:20,*V2LON,"2 ",*HOFF,"=":
                       *P5:19,"Date/Time Sequence":
                       *P5:20,"Surname Sequence  ":
                       *P1:22,"Please Select : ";
.
SOPT1000    KEYIN      *P17:22,*V2LON,SEQOPTN,*HOFF
            COMPARE    ZERO,SEQOPTN
            GOTO       SOPT9000 IF EQUAL
            COMPARE    "3",SEQOPTN  
            GOTO       SOPT0000 IF NOT LESS
.
.-------- if option 1 and 2, determine sequence, 1 = Date/time 2 = Surname --
.-------- determine header -----
.
            UNPACK     SP30,SEQUENCE
            LOAD       SEQUENCE,SEQOPTN,SEQHEAD1,SEQHEAD2
            DISPLAY    *P35:22,SEQUENCE;
            GOTO       SOPT9999
. 
SOPT9000    MOVE       TRUE,EXIT                * set exit flag
.
SOPT9999    RETURN
+
.********************************************************************** 
.*                             RNGE0000                               *
.*                    keyin the range for procedure codes             *
.**********************************************************************
RNGE0000  DISPLAY   *P1:14,*EF:
                    *P1:15,"Location Code From : ":
                    *P1:16,"Location Code To   : ";
.
. -----  Keyin the first location code range ----
.
RNGE0500  MOVE      SP30,DESCRPT1
          DISPLAY   *P22:15,*EL,UNDLN7;
          KEYIN     *P22:15,*V2LON,STRTCODE;             * keyin 1st code 
          DISPLAY   *P22:15,*V2LON,STRTCODE;
.
          MATCH     QUEST,STRTCODE
          GOTO      RNGE8000 IF EQUAL               * display locations routine
.
          ENDSET    STRTCODE
          APPEND    SP7,STRTCODE
          RESET     STRTCODE
.
          MATCH     SP7,STRTCODE                      * check for spaces
          GOTO      RNGE0900 IF NOT EQUAL           
.
          DISPLAY   *P22:15,*V2LON,"Start ";   
          MOVE      "Start ",CODEDES1
          GOTO      RNGE1000
.
.-------- check input to see if exists ----
.
RNGE0900  PACK      KEY8,INDICAT,STRTCODE
          CALL      RDMRLOC2                          * read locations file 
          BRANCH    OVRCD,RNGE0950
          MOVE      STRTCODE,CODEDES1 
          MOVE      MRLODESC,DESCRPT1                  * get description 
          DISPLAY   *P35:15,DESCRPT1;
          GOTO      RNGE1000
.
.------- invalid code entered ----
.
RNGE0950  DISPLAY   *P1:24,*B,*EL,"Code Does Not Exist - Hit ",*V2LON,"<ENTER>":
                    *HOFF," To Continue  ";
          KEYIN     ANS;
          DISPLAY   *P1:24,*EL;
          GOTO      RNGE0500 
.
. -----  Keyin the 2nd range code -----
.
RNGE1000  MOVE      SP30,DESCRPT2
          DISPLAY   *P22:16,*EL,UNDLN7;
          KEYIN     *P22:16,*V2LON,ENDCODE;
          DISPLAY   *P22:16,*V2LON,ENDCODE;
.
          MATCH     QUEST,ENDCODE
          GOTO      RNGE8500 IF EQUAL               * display locations routine
.
          ENDSET     ENDCODE
          APPEND     SP7,ENDCODE
          RESET      ENDCODE
.
          MATCH      SP7,ENDCODE
          GOTO       RNGE9000 IF NOT EQUAL
.
          DISPLAY    *P22:16,*V2LON,"Finish";
          MOVE       "Finish",CODEDES2
          MOVE       "ZZZZZZZ",ENDCODE
          GOTO       RNGE9999
.
.-------- call location summary screen include to display codes -----
.
RNGE8000  CALL       CODE0000                         * "?" on 1st range
          CALL       DISP0000                         * redisplay screen
          GOTO       RNGE0500                         * re keyin 
.
RNGE8500  CALL       CODE0000                         * "?" on 2nd range
          CALL       DISP0000                         * redisplay screen
          GOTO       RNGE1000                         * re -keyin
.
RNGE9000  PACK      KEY8,INDICAT,ENDCODE
          CALL      RDMRLOC2                          * read locations file 
          BRANCH    OVRCD,RNGE9500
          MOVE      ENDCODE,CODEDES2                 * code exists continue..
          MOVE      MRLODESC,DESCRPT2                  * get description
          DISPLAY   *P35:16,DESCRPT2;
.
.------- check if ranges are valid ------
.
          MATCH     STRTCODE,ENDCODE
          GOTO      RNGE7000 IF LESS
          GOTO      RNGE9999
.
.------- invalid code ranges -----
.
RNGE7000  DISPLAY   *P1:24,*B,*EL,"2nd Range Must Be Greater Than 1st Range ":
                               "- Hit <",*V2LON,"ENTER",*HOFF,"> To Continue ";
          KEYIN     ANS;
          DISPLAY   *P1:24,*EL;
          GOTO      RNGE1000                            * re enter
.
.------- invalid code entered ----
.
RNGE9500  DISPLAY   *P1:24,*B,*EL,"Code Does Not Exist - Hit ",*V2LON,"<ENTER>":
                    *HOFF," to continue : ";
          KEYIN     ANS;
          DISPLAY   *P1:24,*EL;
          GOTO      RNGE1000                           * re enter
.
RNGE9999  RETURN
+
.*****************************************************************************
.*                        CODE0000                                           *
.* This include displays Internal or External entries in the locations file. *
.*****************************************************************************
CODE0000
.
.-------- branch on option depending on Internal or External Locations ---
.
          PACK      KEY8,INDICAT,SP4                   * Internal = 1
          CALL      RSMRLOC2                           * External = 2
          CALL      RKMRLOC2
          BRANCH    OVRCD,CODE2000
.
.------ Display Category Heading, and then loop over entries ------
.
          DISPLAY   *P1:4,*EF,*P30:4,*V2LON,*ULON,"LOCATIONS";
          MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
.
.>>>>>>>  TRAP      CODE3000 IF F1
.
          PACK      KEY8,INDICAT,SP4
          CALL      RSMRLOC2
.
CODE1000  CALL      RKMRLOC2
          BRANCH    OVRCD,CODE4000
.
.-------- test if Internal or External, depending on what option was chosen ---
.
          MATCH     INDICAT,MRLOINDC                * Indicat = 1, Internal
          GOTO      CODE1000 IF NOT EQUAL          * Indicat = 2, External
.
.-------- test for active records only -----
.
          COMPARE   "0",MRLOSTAT                     * 0 = Active
          GOTO      CODE1000 IF NOT EQUAL
.
.-------- get description ----
.
          MOVE      MRLODESC,DIM30                   * get description
.
.------- display locations ----
.
          DISPLAY   *PCCOL:CVERT,*V2LON,MRLOHOSP," - ",MRLOCODE,SP2,*HOFF,DIM30;
          ADD       ONE,CVERT
          COMPARE   TWENTY3,CVERT
          GOTO      CODE1000 IF LESS
.
          MOVE      FIVE,CVERT
          ADD       "40",CCOL
          COMPARE   "70",CCOL
          GOTO      CODE1000 IF LESS
.
.>>>>>>>  TRAPCLR   F1
.
          KEYIN     *P1:24,*EL," (",*V2LON,"E",*HOFF,")xit, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          CMATCH    ANSE,ANS
          GOTO      CODE5000 IF EQUAL
.
          MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
          DISPLAY   *P1:5,*EF
.
.>>>>>>>  TRAP      CODE3000 IF F1
.
          GOTO      CODE1000
.
.------ If missing category show code missing ------
.
CODE2000  DISPLAY   *P1:4,*EF,*P30:4,*V2LON,*BLINKON:
                    "No Locations on file";
          GOTO      CODE4000
.
CODE3000  NORETURN
.>>>>>>>  TRAPCLR   F1
.
.------ End of codes ------
.
CODE4000  KEYIN     *P1:24,*EL,"Hit ",*V2LON,"<RETURN>",*HOFF:
                               " to continue ",*EOFF,ANS;
.
CODE5000  MOVE      ZERO,OVRCD
.
CODE9999  RETURN
+
.**********************************************************************
.*                            DISP0000                                *
.*                        redisplay after "?" option                  *
.**********************************************************************
DISP0000  DISPLAY   *P1:4,*EF:
                    *P1:5,"Date Range    : ",OPENDATE," TO ",CLOSDATE:
                    *P2:7,*V2LON,"0",*HOFF," = Exit":
                    *P2:8,*V2LON,"1",*HOFF," = Internal Locations":
                    *P2:9,*V2LON,"2",*HOFF," = External Locations":
                    *P2:10,*V2LON,"3",*HOFF," = Specific U/R Number":
                    *P2:11,*V2LON,"4",*HOFF," = All Records Within Date Range":
                    *P1:13,"Please Select : ",OPTION;
.
.------- branch on option to redisplay screen ------
.
          BRANCH    OPTION,DISP0500,DISP0500,DISP5000
DISP0500  DISPLAY   *P1:15,"Location Code From : ":
                    *P1:16,"Location Code To   : ";
          MATCH     SP7,STRTCODE                     * if spaces entered 
          GOTO      DISP1000 IF NOT EQUAL            * default to 'start'
          DISPLAY   *P22:15,*V2LON,CODEDES1;
          GOTO      DISP2000   
DISP1000  DISPLAY   *P22:15,*V2LON,STRTCODE,*HOFF:   * if code entered, display
                    *P35:15,DESCRPT1;
.
DISP2000  MATCH     SP7,ENDCODE                      * if spaces entered
          GOTO      DISP3000 IF NOT EQUAL            * default to 'finish'
          DISPLAY   *P22:16,*V2LON,CODEDES2;
          GOTO      DISP9999
DISP3000  DISPLAY   *P22:16,*V2LON,ENDCODE,*HOFF:    * if code entered, display
                    *P35:16,DESCRPT2;
          GOTO      DISP9999
.
.-------- option 3 is enter specific u/r no. therefore display accordingly ---
.
DISP5000  DISPLAY   *P1:15,*EL,"U/R Number    : ";
DISP9999  RETURN
.
.***************************************************************************
.*                                 CONT0000                                *
.*                           Ok To Continue    Y/N/C                       *
.***************************************************************************
CONT0000    MOVE       FALSE,EXIT
            CALL       CONTQST
            MATCH      "1",ANS                       * 1 = yes 
            GOTO       CONT9999 IF EQUAL
            MATCH      "2",ANS                       * 2 = no
            GOTO       CONT9000 IF EQUAL
            MATCH      "3",ANS                       * 3 = cancel
            GOTO       CONT8000 IF EQUAL
            GOTO       CONT0000
.
CONT8000    MOVE       TWO,EXIT                     * cancel flag
            GOTO       CONT9999
.
CONT9000    MOVE      TRUE,EXIT                     * no
            MOVE      SP7,STRTCODE
            MOVE      SP7,ENDCODE
            MOVE      SP6,CODEDES1
            MOVE      SP6,CODEDES2
.
CONT9999    DISPLAY   *P1:24,*EL;
            RETURN
+
.***************************************************************************
.*                               DATE0000                                  *
.*                          accept date range                              *
.***************************************************************************
.
.---------- keyin first date range -------
.
DATE0000    MOVE       FALSE,EXIT
            PACK       OPENDATE,SP10                   * move sp date range
            PACK       CLOSDATE,SP10
            MOVE       "17",CCOL                       * set up keyin date
            MOVE       CCC,CCENT
            MOVE       "5",CVERT
            MOVE       "3",CDEFDTE
            MOVE       "45",ECOL
            MOVE       "5",EVERT
.
            DISPLAY    *P1:5,*EF,"Date Range    :":
                       *P27:5," TO ";
            MOVE       SP2,CDAY                        
            MOVE       SP2,CMON
            MOVE       SP2,CYEAR
.
            CALL       KEYDATE                        * call keyin date routine
            MATCH      SP2,CDAY
            GOTO       DATE9000 IF EQUAL
.
            PACK       OPENDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR    
            PACK       FROMDATE,CCENT,CYEAR,CMON,CDAY      * move to form field
            MOVE       FROMDATE,DATFORM1                   * used for comparison
.
.---------- keyin second date range -------
.
DATE1000    MOVE       "32",CCOL                       * set up keyin date 
            MOVE       "5",CVERT                       * parameters
            MOVE       CCC,CCENT
            MOVE       "3",CDEFDTE
            MOVE       "5",EVERT
            MOVE       "45",ECOL
.
            MOVE       SP2,CDAY
            MOVE       SP2,CMON
            MOVE       SP2,CYEAR
.
            CALL       KEYDATE                         * call keyin date routine
            MATCH      SP2,CDAY
            GOTO       DATE0000 IF EQUAL
.
            PACK       CLOSDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
            PACK       TODATE,CCENT,CYEAR,CMON,CDAY    * move to form field
            MOVE       TODATE,DATFORM2                 * for comparison  
.
            COMPARE    DATFORM1,DATFORM2               * compare 1st range to
            GOTO       DATE8000 IF LESS                * 2nd range. 1st date
            GOTO       DATE9999                        * must be less than 2nd 
.
DATE8000    DISPLAY    *P1:24,*B,"Invalid entry for date range - Hit ":
                       "<",*V2LON,"RETURN",*HOFF,">";
            KEYIN      ANS;                            * error message
            GOTO       DATE0000
DATE9000    MOVE       TRUE,EXIT
.
DATE9999    RETURN
+
.************************************************************************
.*                           EXTR0000                                   *
.*                     Location Extraction Routine                      *
.*            Read necessary files and Extract relevant information     * 
.************************************************************************
EXTR0000  MOVE      FALSE,EXIT
          MOVE      ONE,RECOUNT
          MOVE      ONE,BEGINCNT
          MOVE      ZERO,RECDCNT
          MOVE      ZERO,TOTCNT
          MOVE      ZERO,UNIQUE
.
.-------- extract details form history file first ------
.-------- using 3rd key, ie. movement date -----
.
EXTR2000  PACK      KEY26,TODATE,Z20 
          CALL      RSMRHIS3                   * position on history file
.
.--------- Read Tracking History File -----
.
EXTR3000  CALL       RPMRHIS3                     * read key sequential
          BRANCH     OVRCD,EXTR9000
.
.---------- validate date ranges ------------
.
          MATCH      FROMDATE,MRHIDATE      * test if 1st date is within range
          GOTO       EXTR9000 IF LESS                   * was EXTR3000 *C-61101
.
          MATCH      MRHIDATE,TODATE        * check if 2nd date is within range
          GOTO       EXTR9000 IF LESS
.
.---------- unpack dates and retrieve relevant data ------
.
          UNPACK     MRHIDATE,RCENT,RYEAR,RMON,RDAY
          PACK       BORDATE,RDAY,SLASH,RMON,SLASH,RCENT,RYEAR
          MOVE       MRHITIME,BTIME5
.
          DISPLAY    *P1:24,*P20:24,"Date : ",*V2LON,BORDATE,*HOFF;
.
.--------- Extract other relevant details  -----
.
          PACK      KEY7,MRHIDHOS,MRHILOC     * read location file to get 
          CALL      RDMRLOC1                  * description
          BRANCH    OVRCD,EXTR3500 
          MOVE      MRLODESC,LOCATION          * location desc for report 
          MOVE      MRLODESC,TOTLOCN           * location desc for tempfile
.
.------- check if option 3 & 4, then skip location code and indicator test ----
.------- OPTION = 3, Specific U/R Number -----
.------- OPTION = 4, All Records Within Date Range  ------
.
EXTR3500  BRANCH    OPTION,EXTR4000,EXTR4000,EXTR6000,EXTR6000
.
EXTR4000  PACK      LOCNTEST,MRHIDHOS,MRHILOC,SP70
          MATCH     STRTCODE,LOCNTEST        * test if location is within 
          GOTO      EXTR3000 IF LESS         * location range
.
          MATCH     LOCNTEST,ENDCODE          * test if within range
          GOTO      EXTR3000 IF LESS
.
.-------- branch according to option chosen by user -----
.
          BRANCH    OPTION,EXTR5200,EXTR5300
EXTR5200  MATCH     "1",MRLOINDC                    * check if internal
          GOTO      EXTR3000 IF NOT EQUAL          * OPTION =1, Internal Locns
          GOTO      EXTR6000 
.
EXTR5300  MATCH     "2",MRLOINDC                    * check if external
          GOTO      EXTR3000 IF NOT EQUAL          * OPTION =2, External Locns
.
.------- if details ok within parameters then extract additional info ----
.
EXTR6000  PACK      KEY4,MRHIREAS
          CALL      RDMRMOV1                        * read movement reason file
          BRANCH    OVRCD,EXTR6200 
          MOVE      MRMODESC,REASON
.
.-------  extract details from tracking master file -----
.
EXTR6200  PACK      KEY10,MRHIKEY
          CALL      RDMRMAS2                        * read tracking masterfile
          BRANCH    OVRCD,EXTR8000
.
.
EXTR6300  MOVE      MRMAURNO,PURNO
          MOVE      PURNO,KEY8
          CALL      RDMAST1                        * read patient master
          BRANCH    OVRCD,EXTR8000
.
.------- branch according to option chosen by user -----
.
          COMPARE   "3",OPTION                     * OPTION = 3, Specific U/R.
          GOTO      EXTR6350 IF EQUAL
          GOTO      EXTR6370
.
EXTR6350  MATCH     TEMPUR,MRMAURNO                  * check for specific U/R. 
          GOTO      EXTR8000 IF NOT EQUAL
.
.------- if it's the U/R we want, then display it !!!! ----
.
EXTR6370  DISPLAY    *P40:24,"Record Id : ",*V2LON,MRMAURNO,SLASH,MRMAHHOS:
                              SLASH,MRMAHLOC:
                              SLASH,MRMADOTY,SLASH,MRMAVOLN,*HOFF;
.
EXTR6380  PACK      NAMEDIM,SP30
          MOVE      PSNAME,NAMEDIM                 * reset pointers
          ENDSET    NAMEDIM
.
EXTR6400  CMATCH    " ",NAMEDIM
          GOTO      EXTR6500 IF NOT EQUAL
          BUMP      NAMEDIM,-1                     * bump back by 1
          GOTO      EXTR6500 IF EOS
. 
          GOTO      EXTR6400
.
EXTR6500  APPEND    ", ",NAMEDIM                   * if no more spaces 
          APPEND    PGNAME,NAMEDIM                 * append given name to field
          ENDSET    NAMEDIM
          APPEND    SP30,NAMEDIM
          RESET     NAMEDIM
.
.-------- If using Surname Sequence we write details to tempfile -------
.-------- If using borrowing date/time sequence we continue to print ------
.
EXTR7000  
          BRANCH    SEQOPTN,EXTR7200               * "1" = Date/Time Sequence
          CALL      WRSN0000                       * "2" = Surname Sequence
          GOTO      EXTR73000
EXTR7200  CALL      PRNT0000                       * call print routine
EXTR7300 
          MOVE      TWO,RECOUNT                    * 1st rec. read successful
.
.------- call temp file write module ------
.
          CALL      WRTP0000                       * write location to tempfile
          ADD       ONE,RECDCNT                    * increment record counter
.
.------- clear working variables -----
.
EXTR8000  MOVE      SP30,TDESC
          MOVE      SP30,MRLODESC
          MOVE      SP8,MRHIDATE
          MOVE      SP8,MRHITIME
          MOVE      SP4,MRHIREAS
          MOVE      SP4,MRHILOC
          MOVE      SP10,MRHIKEY
          MOVE      SP30,PSNAME
          MOVE      SP30,PGNAME
          MOVE      SP30,NAMEDIM
          MOVE      SP30,REASON
          MOVE      SP30,LOCATION
          MOVE      SP30,TOTLOCN
.
          GOTO      EXTR3000                         * re-loop
.
EXTR9000  COMPARE   ONE,RECOUNT
          GOTO      EXTR9500 IF NOT EQUAL
          DISPLAY   *P1:24,*B,*EL,"Details Not On File - Hit ":
                               "<",*V2LON,"ENTER",*HOFF,"> to continue ";
          CALL      HITENTER
          CALL      PRNT8150
.
.-------- report details completed, now print total lines -----
.
EXTR9500  COMPARE   ONE,SEQOPTN                      * "1" = Date/Time Seq.
          GOTO      EXTR9999 IF NOT EQUAL            * "2" = Surname Seq.
          CALL      LNPR0000                         * print final line
.
EXTR9999  RETURN
+
.*************************************************************************
.*                             WRTP0000                                  *
.*                 write details to temp file for report totals          *
.*************************************************************************
WRTP0000
          PACK      KEY30,TOTLOCN
          CALL      RDTEMP2
          BRANCH    OVRCD,WRTP2000
.
.-------  if record already exists increment counter by 1 and update -----
.    
          ADD       ONE,TOTCNT
          CALL      UPTEMP2
          GOTO      WRTP9999
.
.-------  if record doesn't exist write new record with counter of 1 -----
.
WRTP2000  MOVE      ONE,TOTCNT
          PACK      KEY30,TOTLOCN,TOTCNT
          CALL      WRTEMP2
WRTP9999  RETURN  
.
.*************************************************************************
.*                             WRSN0000                                  *
.*                 write details to temp file for surname sequence       *
.*************************************************************************
WRSN0000  ADD       ONE,UNIQUE                  * inrement counter by 1.
          MOVE      UNIQUE,DUNIQUE
          PACK      KEY35,NAMEDIM,DUNIQUE
          CALL      WRTEMP1
.
WRSN9999  RETURN  
.
.*************************************************************************
.*                             PAT0000                                   *
.*                      Enter Patient U/R Number & validate              *
.*                Use KURN facility to key in U/R No.                    *
.*************************************************************************
PAT0000     MOVE       FALSE,EXIT
            MOVE       "17",CCOL
            MOVE       "15",CVERT
            MOVE       TWO,SRCHOPT
            MOVE       ZERO,SRCHSYS
            MOVE       ZERO,OVRCD
.
            DISPLAY   *P1:15,*EL,"U/R Number  : ";
.
            OPEN      CONTROLF,"controlf"
            CALL      KURN                     * call keyin U/R No. Routine
            CLOSE     CONTROLF
            BRANCH    OVRCD,PAT2000
            BRANCH    EXIT,PAT9999
            MOVE      PURNO,TEMPUR
.
            PACK      NAMEDIM,SP30
            MOVE      PSNAME,NAMEDIM                 * reset pointers
            ENDSET    NAMEDIM
.
PAT1400     CMATCH    " ",NAMEDIM
            GOTO      PAT1500 IF NOT EQUAL
            BUMP      NAMEDIM,-1
            GOTO      PAT1400
.
PAT1500     APPEND    ", ",NAMEDIM
            APPEND    PGNAME,NAMEDIM
            ENDSET    NAMEDIM
            APPEND    SP30,NAMEDIM
            RESET     NAMEDIM
.
            DISPLAY   *P35:15,NAMEDIM;
            GOTO       PAT9999
.
PAT2000     DISPLAY    *P1:24,*B,*EL,"U/R Number Does Not Exist - Hit <":
                                 *V2LON,"ENTER",*HOFF,"> to continue ";
            KEYIN      ANS;
            DISPLAY    *P1:24,*EL;
            MOVE       TWO,EXIT                 * U/R number does not exist
.
PAT9999     RETURN
+
.*************************************************************************
.*                             REDISPS                                   *
.*                       Redisplay used by KURN facility                 *
.*************************************************************************
REDISPS     CALL       DISP0000
            RETURN 
.
GETSVAR     RETURN                          * used by kurn
.
.*************************************************************************
.*                             PRNT0000                                  *
.*                  print movement reason details                        *
.*************************************************************************
PRNT0000    CLOCK      TIME,TIMEIS
            COMPARE    "45",LINECNT
            CALL       PRNT8000 IF NOT LESS
.
            PRINT      *N,"|",*4,BORDATE,SP3,BTIME5,*23,"|":
                          *25,MRMAURNO,MRMAHHOS,MRMAHLOC:
                        MRMADOTY,MRMAVOLN,*47,"|",*49,NAMEDIM,*75,"|":
                          *77,LOCATION,*103,"|",*105,REASON,*132,"|";
            ADD        "1",LINECNT
            RETURN
.
PRNT8000    BRANCH     BEGINCNT,PRNT8150
PRNT8100    CALL       LNPR0000
PRNT8150    ADD        ONE,PAGENO 
            MOVE       ONE,CNOUNDLN
            CALL       IBACLOCK
            CALL       HEAD132
            PRINT      *N,*40,SEQTYPE:
                       *N,*40,SEQUENCE;
            BRANCH     OPTION,PRNT8200,PRNT8200,PRNT8400,PRNT8500
PRNT8200    PRINT      *N,*40,"DATE RANGE : ",OPENDATE," TO ",CLOSDATE;
            PRINT      *N,*N,*40,"CODE RANGE : ",CODEDES1," TO ",CODEDES2;
            GOTO       PRNT8600
PRNT8400    PRINT      *N,*40,"DATE RANGE : ",OPENDATE," TO ",CLOSDATE;
            PRINT      *N,*N,*40,"U/R NUMBER : ",PURNO;
            GOTO       PRNT8600
PRNT8500    PRINT      *N,*40,"DATE RANGE : ",OPENDATE," TO ",CLOSDATE;
PRNT8600    PRINT      *N:
                       *N,*N,"*------------------------------":
                             "----------------------------------------":
                             "----------------------------------------":
                             "--------------------*":
                       *N,"|",*3,"Borrowing Date/Time",*23,"|":
                          *25,"Record Identification",*47,"|":
                          *49,"Patient Name",*75,"|",*77,"Location",*103,"|":
                       *105,"Reason",*132,"|":
                       *N,"*---------------------------------------":
                          "----------------------------------------":
                          "----------------------------------------":
                          "-----------*";
            MOVE       "4",LINECNT
            MOVE       TWO,BEGINCNT
.
PRNT9999    RETURN
.
EXPR0000    PRINT      *N,"**** END OF REPORT ****";
            RETURN
.
LNPR0000    PRINT      *N,"*----------------------------------------":
                          "-----------------------------------------":
                          "--------------------------------------":
                          "-----------*";
            RETURN
+
.*************************************************************************
.*                             PRSN0000                                  *
.*                   print details for surname sequence                  *
.*************************************************************************
PRSN0000    
            PACK       KEY35,SP30,SP10
            CALL       RDSTEMP1                      * read temp file
PRSN1000    CALL       RDKTEMP1
            BRANCH     OVRCD,PRSN9000
            CALL       PRNT0000                      * call print routine
            GOTO       PRSN1000
PRSN9000    CALL       LNPR0000                      * print line
PRSN9999    RETURN
+
.*************************************************************************
.*                             TEMP0000                                  *
.*                  read temp file and print totals for locations        *
.*************************************************************************
TEMP0000    DISPLAY    *P1:24,*EL,*P40:24,*V2LON,"**** Generating Report ****":
                       *HOFF; 
            READ       TEMPFIL2,SP30;;
TEMP1000    READKS     TEMPFIL2;TOTLOCN,TOTCNT
            GOTO       TEMP9000 IF OVER
.           
            COMPARE    "45",LINECNT                 * test for paging
            CALL       TEMP8000 IF NOT LESS
.
            PRINT      *N,TOTLOCN,SP2,COLON,SP1,TOTCNT;
            ADD        "1",LINECNT
            GOTO       TEMP1000
.
TEMP8000    ADD        ONE,PAGENO 
            MOVE       ONE,CNOUNDLN
            CALL       IBACLOCK
            CALL       HEAD132
            PRINT      *N,*40,SEQTYPE:
                       *N,*40;
            BRANCH     OPTION,TEMP8200,TEMP8200,TEMP8400,TEMP8600
TEMP8200    PRINT      *N,*40,"DATE RANGE : ",OPENDATE," TO ",CLOSDATE;
            PRINT      *N,*N,*40,"CODE RANGE : ",CODEDES1," TO ",CODEDES2;
            GOTO       TEMP8700
TEMP8400    PRINT      *N,*40,"DATE RANGE : ",OPENDATE," TO ",CLOSDATE;
            PRINT      *N,*N,*40,"U/R NUMBER : ",PURNO;
            GOTO       TEMP8700
TEMP8600    PRINT      *N,*N,*40,"DATE RANGE : ",OPENDATE," TO ",CLOSDATE;
TEMP8700    MOVE       "4",LINECNT
            RETURN
.
TEMP9000    PRINT      *N,*N,"TOTAL MOVEMENTS                 : ",RECDCNT,*N;
            CALL       EXPR0000                        * print end of report
TEMP9999    RETURN
.
.**************************************************************************
.*                            I/O SUBROUTINES                             *
.**************************************************************************
.
.-------- io subroutines for temp file for Surname Sequence -----
.
.
RDSTEMP1    MOVE       ZERO,OVRCD
            RESET      KEY35
            READ       TEMPFIL1,KEY35;;
            GOTO       OVERCOND IF OVER
            RETURN
.
RDKTEMP1    MOVE       ZERO,OVRCD
            READKS     TEMPFIL1;NAMEDIM,DUNIQUE,BORDATE,BTIME5,MRMAURNO:
                                MRMAHHOS,MRMAHLOC,MRMADOTY,MRMAVOLN,LOCATION:
                                REASON
            GOTO       OVERCOND IF OVER
            RETURN
.
WRTEMP1     MOVE       ZERO,OVRCD
            RESET      KEY35
            WRITE      TEMPFIL1,KEY35;KEY35,BORDATE,BTIME5,MRMAURNO:
                                MRMAHHOS,MRMAHLOC,MRMADOTY,MRMAVOLN,LOCATION:
                                REASON
            RETURN
.
.-------- io subroutines for temp file - For totals -------
.
RDTEMP2     MOVE       ZERO,OVRCD
            RESET      KEY30
            READ       TEMPFIL2,KEY30;TOTLOCN,TOTCNT
            GOTO       OVERCOND IF OVER
            RETURN
.
UPTEMP2     
            UPDATE     TEMPFIL2;TOTLOCN,TOTCNT
            RETURN
.
WRTEMP2     MOVE       ZERO,OVRCD
            RESET      KEY30
            WRITE      TEMPFIL2,KEY30;KEY30,TOTCNT
            RETURN
.
.-------- io subroutine includes ------
.
            INC        STD001IO
            INC        TFILENAM
.
            INC        BOKRC1IO/INC                  * booking file
            INC        IBASEQIO/INC
            INC        OUTPREIO/INC
            INC        PATCOMN1                      * kurn routine
            INC        PATCODKY                      * codes summary screen
            INC        PATCODIO/INC
            INC        PATMA1IO/INC
            INC        PATMI1IO/INC
            INC        PATPR1IO/INC
            INC        PATGSRIO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
            INC        PATSNDX                       * surname soundex search
            INC        PATSNX2                       * surname soundex search
            INC        CALCAGE
            INC        MRTHISIO/INC
            INC        MRTLOCIO/INC
            INC        MRTMASIO/INC
            INC        MRTMOVIO/INC
            INC        NZCOMPIO/INC
            INC        WEBERRIO/INC
AGECHK
CLPATMAS  RETURN
.

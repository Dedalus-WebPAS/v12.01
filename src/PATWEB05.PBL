.------------------------------------------------------------------------------
. Program       : PATWEB05
.
. Function      : Print Patient Statement Server      
.
. Modifications : 
.------------------------------------------------------------------------------
.V11.05.02  26/03/2025  Davin          TSK 0956210
.           Added DRG120 for DRG 12.0 - Re-compile for DRGSTMNT
.V11.05.01  20/03/2025  Davin          TSK 0956210
.           Add ICD10 Ed.13 - Go-live Date PTCNGDX3 (Sect.130 Pos.86)
.V11.02.04  14.09.2022  DA Sarkies     TSK 0909756
.           Recompiled for PRTMITEM
.V11.02.03  20/04/2022  Davin          TSK 0917793
.           Added DRG110 for DRG 11.0 - Re-compile for DRGSTMNT
.V11.02.02  31/03/2022  Davin          TSK 0917793
.           Add ICD10 Ed.12 - Go-live Date PTCNGDX2 (Sect.128 Pos.231)
.V11.02.01  10/02/2022  Thanh T       TSK 0905641
.           Recompiled as OUTMA1FD (PATMASTM) changes
.V11.01.01  05/05/2021  Ebon Clements  TSK 0902615
.           Added read of NWAU parameters MRCNUNWA and MRCNENWA
.V11.00.01  09/12/2020  Thanh T        TSK 0872840
.           Added PMSCCDFD/PMSCCDIO as PMSPX2TM changes
.V11.00.03  08/10/2020  Ebon Clements  TSK 0893634
.           Recompiled for WIESCALC - WIES27 Aboriginal and Torres Strait
.           Islander loading
.V11.00.02  23/09/2020  Ebon Clements  TSK 0893634
.           Recompiled for WIESCALC - Added WIES27 for 2020/2021
.V11.00.01  25/05/2020  Davin          TSK 0890603
.           Recompiled for WEBGROUP/WIESCALC - WIES for private facility
.V10.14.02  26/08/2019  Davin          TSK 0880255
.           Recompiled for WIESCALC - Added WIES26 for 2019/2020
.V10.14.01  14/03/2019  Don Nguyen     TSK 0869412
.           Added read of ICD10 Ed.11 Go-live Date parameter (PTCNGDX1)
.           28/02/2019  Richa Phogat   TSK 0870439
.           Added DRG100 for DRG 10.0 - Re-compile for DRGSTMNT
.------------------------------------------------------------------------------
.V10.13.01  21/08/2018  Davin          TSK 0860935
.           Recompiled for WIESCALC - Added WIES25 for 2018/2019
.V10.12.01  23/03/2018  Tracey Nguyen  TSK 0852793
.           Added DRG090 for DRG 9.0 - Re-compile for DRGSTMNT
.------------------------------------------------------------------------------
. V10.11.05 27/11/2017  Thanh Tieu     Task 0821710
.           Recompiled as PATMASTM changed
. V10.11.04 15/08/2017  Davin            TSK 0842626
.           Recompiled for WIESCALC - Added WIES24 for 2017/2018
. V10.11.03 30/07/2017  Thanh T.       TSK 0821710
.           Audit admission/outpatient/UR comments. changed PATMASTM/PMSPX2TM.
. V10.11.02 26/07/2017  Thanh T.       TSK 0821710
.           Recompiled PASMISTM
. V10.11.01 14/07/2017  Thanh T.       TSK 0821710
.           Audit Amission/outpatient visit comments. PATMISTM changed.
.------------------------------------------------------------------------------
.V10.10.01  17/03/2017  Davin          TSK 0833093     HDP 2017/2018
.           Add ICD10 Ed.10 - Go-live Date PTCNGDVX (Sect.110 Pos.85)
.V10.08.02  02/09/2016  Davin         TSK 0825157
.           Recompiled for WIESCALC                      
.V10.08.01  18/07/2016  Davin         TSK 0822753
.           Recompiled for WIESCALC - Added WIES23 for 2016/2017
.V10.07.03  04/11/2015  Peter Vela    CAR 323303
.           Recompiled for PRTMITEM
.V10.07.02  20/10/2015  J.Tan         CAR 314089
.           Added WINECL00 - parent Window close
.V10.06.04  18/09/2015  J.Tan         CAR 314089
.           Added new option to print Misc.Item (NZ)
.V10.06.03  29/05/2015  Ebon Clements CAR 317094
.           Added DRG080 for DRG 8.0 - Re-compile for DRGSTMNT
.V10.06.02  12/04/2015  J.Tan         CAR 304813
.           Changes for WIES 22  -  Re-compile for DRGSTMNT (Added WIES 22)
.V10.06.01  17/03/2015  Davin         CAR 311669      HDP 2015/16
.           Add PTCNGDV9 for ICD10 Ed.9 Go-live Date (Sect.110 Pos.77)
.------------------------------------------------------------------------------
.V10.05.01  09/09/2014  Davin         CAR 296980
.           Changes for WIES 21  -  Re-compile for DRGSTMNT (Added WIES 21)
.------------------------------------------------------------------------------
.V10.04.02  07/04/2014  Peter Vela      CAR 286258
.           Recompiled for PATMASTM
.V10.04.01  31.03.2014  B.G.Ackland  CAR 299363
.           Replace META Tag Redirect with Javascript in Common RDIREC00
.           Routine
.------------------------------------------------------------------------------
.V10.03.06  08/08/2013  Davin         CAR 284960
.           Changes for WIES 20  -  Re-compile for DRGSTMNT (Added WIES 20)
.V10.03.05  03/05/2013  Davin            CAR 284669 HDP 2013 DRG 7.0
.           Added DRG070 for DRG 7.0 - Re-compile for DRGSTMNT
.V10.03.04  23/04/2013  Davin         CAR 284420      HDP 2013/14
.           Add PTCNGDV8 for ICD10 Ed.8 Go-live Date (Sect.110 Pos.69)
.V10.03.03  08/08/2012  Davin         CAR 270114
.           Changes for WIES 19  -  Re-compile for DRGSTMNT (Added WIES 19)
.V10.03.02  28/06/2012  Mike Laming      CAR 267797 HDP 2012 DRG 6.2
.           Added DRG062 for DRG 6.2 - Re-compile for DRGSTMNT
.V10.03.01  02/12/2011  Davin          CAR 251748
.           Recompiled for changes to PATWIS18
.V10.02.05  31/10/2011  Mike Laming   CAR 250267
.           Change CCUTIME, ICUTIME & MECHVENT to form 5 for PTICU fields
.V10.02.04  11/08/2011  Davin         CAR 238236
.           Changes for WIES 18  -  Re-compile for DRGSTMNT (Added WIES 18)
.V10.02.03  11/10/2011  Mark Coupland    CAR 252429
.           Recompiled for PMSPX2TM and PATMASTM
.V10.02.02  13/09/2011 Jill Parkinson  CAR 248652
.           Recompiled for PMSPX2TM - Added read of pmsvx1af in GETCURIP
.V10.02.01  04/04/2011  Jill Parkinson CAR 239574
.           Removed open of ibaprntf
.V10.00.02  07/04/2010  Mike Laming   CAR 219246      HDP 2010
.           Add PTCNGDV7 for ICD10 Ed.7 Go-live Date (Sect.110 Pos.61
.V10.00.01  24/03/2010  Davin         CAR 216505
.           Changes for WIES 17  -  Re-compile for DRGSTMNT (Added WIES 17)
. V9.12.02  01/07/2009  Davin         CAR 199724
.           Changes for WIES 16  -  Re-compile for DRGSTMNT (Added WIES 16)
. V9.12.01  19/06/2009  Mike Laming      CAR 197814 HDP 2009 DRG 6.0
.           Added DRG060 for DRG 6.0 - Re-compile for DRGSTMNT
. V9.10.01  17/07/2008  Davin         CAR 163993
.           Changes for WIES 15  -  Re-compile for DRGSTMNT (Added WIES 15)
. V9.09.05  20/03/2008  Mike Laming   CAR 163918     HDP 2008  
.           Add PTCNGDV6 for ICD10 Ed.6 Go-live date
. V9.09.04  14/01/2008  Ebon Clements CAR 157297
.           Recompiled for DRGSTMNT - Diagnosis code print
. V9.09.03  10/01/2008  Ebon Clements CAR 157297
.           Added open of ibapdfaf to INIT0000
. V9.09.02  02/08/2007  Steve Armstrong  CAR 145161
.           Removed references to WIES 7, 8, 9 & 10 - no longer supported.
.           Recompiled for changes to DRGSTMNT.
. V9.09.01  18/07/2007  Mike Laming   CAR 145161
.           Changes for WIES 14  -  Re-compile for DRGSTMNT (Added WIES 14)
.------------------------------------------------------------------------------
. V9.08.03  17/04/2007  Mike Laming   CAR 137125 HDP 2007 DRG 5.2  
.           Re-compile for DRGSTMNT - Added DRG 5.1, WIES 13 and DRG 5.2
. V9.08.02  09/02/2007  Janet George  CAR 130263
.           Added Item No Search for SA Extract
. V9.08.01  31/01/2007  Peter Vela     CAR 127930
.           Recompiled for MRTMASFD
.------------------------------------------------------------------------------
. V9.07.01  11/07/2006  Davin S       CAR 111764
.           Added 2006/2007 WIES13 file (patw13af)
.------------------------------------------------------------------------------
. V9.06.01  15/05/2006  Mike Laming   CAR 105244      2006 HDP
.           ADD PTCNGDV5 for ICD10 Ed.5 Go-live
.------------------------------------------------------------------------------
. V9.05.01  09/03/2006  Ebon Clements CAR 78533
.           Mods for PATCODTM - Hospital specific category codes
. V9.05.B02 30/01/2006  Ebon Clements CAR 93186
.           Mods for REDIRECT length change. Recompiled for IBAWEBDF
.------------------------------------------------------------------------------
. V9.04.03  21/11/2005  Peter Vela    CAR 68675
.           Recompiled for PATDSCTM
. V9.04.02  26/07/2005  Peter Vela    CAR 68562
.           Added 2005/2006 WIES12 file (patw12bf)
. V9.04.01  01/03/2005  Peter Vela    CAR 57547
.           Recompiled for PATVADFD and PATVADIO
.------------------------------------------------------------------------------
. V9.03.06  07/06/2004 Peter Vela CAR 49016
.           2004 Statutory Changes - recompiled for PMSPX2TM
. V9.03.05  26/03/2004 Peter Vela       CAR 13038
.           Recompiled for PATDOCCD - Doctor Name format Parameter
. V9.03.04  29/12/2003  Sylvek Litewka  CAR 20222
.           Modified array variables DISARRAY and OPRARRAY to store Disease and
.           Operation Code Descriptions.
.           23/12/2003  Pat Dirito      CAR 44742
.           Added code for calculating WIES Dollar Value for Print DRG, also
.           fixed some alignment issues.
.           11/11/2003  Sylvek Litewka  CAR 44742
.           Recompiled for changes made to DRGSTMNT.PBL.
. V9.03.03  07/11/2003  Sylvek Litewka  CAR 44328
.           Recompiled for PATECOFD changes.
. V9.03.02  02/10/2003  Sylvek Litewka  CAR 42784
.           Recompiled for changes made to DRGSTMNT.PBL.
. V9.03.01  15/08/2003  Sylvek Litewka  CAR 41293
.           Modified procedure DRGSTM00 to populate SCHEDDSC variable
.           with description of the document being printed.
.           15/08/2003  Sylvek Litewka  CAR 20019
.           Added PCCLVAL variable to display 'PCCL Value" field on the
.           DRG Statement.
. V9.03.00  30/06/2003  Tonii CAR 20019
.           Created Server
.------------------------------------------------------------------------------
          INC       IBAWEBDF
          INC       WEBDATDF
.
          INC       APSMASFD/INC   
          INC       MRTCONFD/INC
          INC       MRTMASFD/INC
          INC       NHIMASFD/INC
          INC       NHIETHFD/INC
          INC       OUTSITFD/INC
          INC       PATALRFD/INC
          INC       PATASFFD/INC
          INC       PATCALAG/INC
          INC       PATCMCFD/INC
          INC       VISMDTFD/INC
          INC       VISMTXFD/INC
          INC       PMSUCHFD/INC
          INC       PMSUCDFD/INC
          INC       PATMASTD/INC
          INC       PATCONFD/INC
          INC       PATDADFD/INC
          INC       PATDGWFD/INC
          INC       PATDO1FD/INC   * Doctor File
          INC       PATDSCFD/INC
          INC       PATECDFD/INC         * Disease codes
          INC       PATECOFD/INC         * Operation codes
          INC       PATFN1FD/INC
          INC       PATHSPFD/INC
          INC       PATITMFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATIN1FD/INC
          INC       PATPGRFD/INC         * Patient Grouper details
          INC       PATPR1FD/INC
          INC       PATICUFD/INC
          INC       PATRE1FD/INC
          INC       PATTRNFD/INC         * Patient Transfer details
          INC       PATVADFD/INC
          INC       PATVISFD/INC         * Patient Visit table
          INC       PATW11FD/INC         * WIES11
          INC       PATW12FD/INC         * WIES12
          INC       PTW12BFD/INC         * WIES12B
          INC       PATW13FD/INC         * WIES13
          INC       PATW14FD/INC         * Cost Weights for WIES14    *I-145161
          INC       PATW15FD/INC         * Cost Weights for WIES15
          INC       PATW16FD/INC         * Cost Weights for WIES16
          INC       PATW17FD/INC         * Cost Weights for WIES17
          INC       PATW18FD/INC         * Cost Weights for WIES18
          INC       PATW19FD/INC         * Cost Weights for WIES19
          INC       PATW20FD/INC         * Cost Weights for WIES20
          INC       PATW21FD/INC         * Cost Weights for WIES21
          INC       PATWIEFD/INC         * Cost Weights for WIES
          INC       PATWR1FD/INC
.
          INC       PMSCCDFD/INC   * Concession Card Details
          INC       PMSHCGTD/INC   
          INC       PMSHCGFD/INC   
          INC       PMSHCPFD/INC
          INC       PMSHCPTD/INC
          INC       PMSPX2TD/INC
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC   * Visit Extension File
          INC       PMSWDVFD/INC   * WIES Dollar Value   
          INC       PATSAIFD/INC
          INC       PATMISTD/INC
.
.----------------------------------------------------------------------
CATD1     INIT      "D1"
CATD2     INIT      "D2"
CATD3     INIT      "D3"
CATD4     INIT      "D4"
CATD5     INIT      "D5"
CATU6     INIT      "U6"
CATU7     INIT      "U7"
DRG042    INIT      "042"
DRG050    INIT      "050"
DRG051    INIT      "051"
DRG052    INIT      "052"                                             *I-137125
DRG060    INIT      "060"                                             *I-197814
DRG062    INIT      "062"                                             *I-267797
DRG070    INIT      "070"                                             *I-284669
DRG080    INIT      "080"
DRG090    INIT      "090"
DRG100    INIT      "100"
DRG110    INIT      "110"
DRG120    INIT      "120"
NEXTPAGE  DIM       1       * Nextpage parameter CGI parameter
UPDTTYPE  DIM       1       * Update Type
STARTKEY  DIM       10      * Starting Key
CURRDATE  DIM       10      * Current Date
FILTFDAT  DIM       8
FILTTDAT  DIM       8
FORM8     FORM      8 
FORM3     FORM      3 
DOCTCODE  DIM       8
PSAGETYP  DIM       3
MBSDESC   DIM       70
MENUFLAG  DIM       1 
PSAGECON  DIM       3
PUSRNAME  FORM      1
MONEWIES  FORM      10
CUSTFLAG  FORM      1       * Custom Site Flag
LINK2PRT  FORM      1         * Update parent
SAITEMNO  DIM       3       * Casemix Type for search
.-------------------------------------------------
.    DRG Statement variables
.-------------------------------------------------
ADMDATE   DIM       10        *admission date
CCUTIME   FORM      5         * CCU time                              *C-250267
CONSLTNT  DIM       30        * Consultant
COUNTER   FORM      3
DISARRAY  DIM       80[99]
DISCODE   DIM       9
DISCOUNT  FORM      3
DISDESC   DIM       40
DSCHDATE  DIM       10        * discharge date
DSCHWARD  DIM       3         * discharge ward
DSCWDDSC  DIM       25        * discharge ward description
DRGCODE   DIM       4
DRGDESC   DIM       30
DRGDRVER  DIM       1
DRGVERSN  DIM       3
FPATNAME  DIM       30
ICUTIME   FORM      5         * ICU time                              *C-250267
LOS       FORM      4         * Length of stay
MECHVENT  FORM      5         * Mechanical Ventilation                *C-250267
OPRARRAY  DIM       79[99]
OPRCODE   DIM       9
OPRCOUNT  FORM      3
OPRDESC   DIM       40
PCCLVAL   DIM       1         * PCCL value (Patient CC class level)
SOPTION   FORM      1
TRMAVLOS  FORM      4.2       * trim points average LOS
TRIMHIGH  FORM      3         * trim points high
TRIMLOW   FORM      3         * trim points low
UNIT      DIM       30
UNKNOWN   INIT      "Unknown"
WIESALL   FORM      12.4      * Chargeable Ptient - WIES(All)
WGHTALL   FORM      6.4       * weight (ALL)
WGHTDAY   FORM      5         * weight (DAY)
.------------------------------------------------
.
.----------------------------------------------------------------------
PRGID     INIT      "PATWEB05"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "Print Patient Statements"
.----------------------------------------------------------------------
.
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200,MAIN1300
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "Invalid Option",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  CALL      DRGSTM00        * DRG Statement          
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1200  CALL      ITMSRC00        * SA Item Search
          BRANCH    EXIT,MAIN1000  
          CALL      NXTPAG00       
          GOTO      MAIN1000
.
MAIN1300  CALL      MSCITM00        * Print Misc.Items
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
.------------------------------------------------------------
. Output Next Page Based on CGI Parameter nextpage
.------------------------------------------------------------
NXTPAG00  MOVE      ZERO,F1
          MOVE      NEXTPAGE,F1
          BRANCH    F1,NXTPAG10,NXTPAG20,NXTPAG30,NXTPAG40,NXTPAG50
.
          CALL      WINCLS00
          GOTO      NXTPAG99
.
NXTPAG10  CALL      RDIREC00
          GOTO      NXTPAG99
.
NXTPAG20  CALL      GOBACK00      * Go Back 1 Html page
          GOTO      NXTPAG99
.
NXTPAG30  CALL      DAH20000      * Go Back 2 html pages
          GOTO      NXTPAG99
.
NXTPAG40  CALL      PREDIR00      * Redirect Parent
          GOTO      NXTPAG99
.
NXTPAG50  CALL      WINECL00
          GOTO      NXTPAG99
.
NXTPAG99  RETURN
.
.------------------------------------------------------------------------------
.         Parent Window Close
.------------------------------------------------------------------------------
WINECL00  CALL      OPENHTML
          BRANCH    EXIT,WINECL99
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             " if (self.name.match(/PopUpFrame/)) {":
  "  parent.document.getElementById('PopUpScreen').style.display=#"none#"; ":
                             "  parent.window.close();":
                             "} else { ":
                             "  self.close(); }":
                             "}":
                             "</script>"
          CALL      CLOSHTML     * End of Document
WINECL99  RETURN
+
.------------------------------------------------------------------------------
.  Redirect Parent URL
.------------------------------------------------------------------------------
PREDIR00  CALL      OPENHTML
          BRANCH    EXIT,PREDIR90
.
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "alert(#"",WEBTITLE,"#");":
                             " parent.location.href=#"",REDIRECT,"#";":
                             "}":
                             "</script>"
          CALL      CLOSHTML
          GOTO      PREDIR99
.
PREDIR90  DISPLAY   "*** Fifo Not Found ***"
.
PREDIR99  RETURN
.-------------------------------------------------------------------------------
.  Goes back 1 page 
.-------------------------------------------------------------------------------
GOBACK00  CALL      OPENHTML
          BRANCH    EXIT,GOBACK99
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "    alert(#"",WEBTITLE,"#");":
                             "    self.close();":
                             "    opener.history.go(-1);":
                             "}":
                             "</script>"
          CALL      CLOSHTML
GOBACK99  RETURN
.------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------
INIT0000  CALL      INTMAS00     * Open routine for PATMASTM 
          OPEN      CONTROLF,"controlf"
          OPEN      IBASPOL1,"ibaspolf"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATDGWA1,"patdgwaf"
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATECDA1,"patecdaf"
          OPEN      PATECOA1,"patecoaf"
          OPEN      PATHSPA1,"pathspaf"
          OPEN      PATICUA1,"paticuaf"
          OPEN      PATPGRA1,"patpgraf"
          OPEN      PATTRAN2,"pattranf"
          OPEN      PATVISA2,"patvisaf"
          OPEN      PATW11A1,"patw11af"
          OPEN      PATW12A1,"patw12af"
          OPEN      PATW12B1,"patw12bf"
          OPEN      PATW13A1,"patw13af"
          OPEN      PATW14A1,"patw14af"                               *I-145161
          OPEN      PATW15A1,"patw15af"
          OPEN      PATW16A1,"patw16af"
          OPEN      PATW17A1,"patw17af"
          OPEN      PATW18A1,"patw18af"
          OPEN      PATW19A1,"patw19af"
          OPEN      PATW20A1,"patw20af"
          OPEN      PATW21A1,"patw21af"
          OPEN      PATWIEA1,"patwieaf"
.
          OPEN      PMSCCDA1,"pmsccdaf"
          OPEN      PMSWDVA1,"pmswdvaf"
          OPEN      PATSAIA1,"patsaiaf"
          OPEN      IBAPDFA1,"ibapdfaf"     * For default printer
          OPEN      VISMDTA1,"vismdtaf"
          OPEN      VISMTXA1,"vismtxaf"
.
          CALL      IBACLOCK
          MOVE      CCC,CCENT
          MOVE      CYY,CYEAR
          MOVE      CMM,CMON
          MOVE      CDD,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,CURRDATE
.
          READ      CONTROLF,SIXTY1;*240,MRCNUNWA,*241,MRCNENWA

          READ      CONTROLF,EIGHTY;*250,PTCNHDPS
          READ      CONTROLF,HUND05;*158,PTCNDGPV,*217,PTCNGDV4:
                                    *227,PTCNGDV5                     *I-105244
          READ      CONTROLF,HUND09;*151,PTCNGDV3
          READ      CONTROLF,HUND12;*239,PTCNGDV6                     *I-163918
          READ      CONTROLF,HUND10;*61,PTCNGDV7:    * ICD10 Ed.7     *I-219246
                                    *69,PTCNGDV8:    * ICD10 Ed.8     *I-284420
                                    *77,PTCNGDV9:    * ICD10 Ed.9     *I-311669
                                    *85,PTCNGDVX     * ICD10 Ed.10    *I-0833093
          READ      CONTROLF,HUND17;*220,PTCNGDX1
          READ      CONTROLF,HUND28;*231,PTCNGDX2                     *I-0917793
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,HUND30;*86,PTCNGDX3                      *I-0956210
.
          IF        IBCNMHOS=1
            OPEN      MLTCODA1,"mltcodaf"
            OPEN      MLTCODA2,"mltcodaf"
          ENDIF
.
INIT9999  RETURN
+
.------------------------------------------------------------
.  Print DRG Statment
.------------------------------------------------------------
DRGSTM00  MOVE      ZERO,EXIT
          MATCH     SP70,UPDTTYPE
          GOTO      DRGSTM30 IF EQUAL
.
          MATCH     "P",UPDTTYPE
          GOTO      DRGSTM10 IF EQUAL
.
          GOTO      DRGSTM90
.
DRGSTM10  MOVE      "DRG Statement Printing",SCHEDDSC
          CALL      PRTDRGST
          MOVE      SP70,SCHEDDSC
          GOTO      DRGSTM99
.
DRGSTM30  CLEAR     WEBTITLE
          MOVE      "Print DRG Statement",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,DRGSTM99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      DRGSTM99
.
DRGSTM90  MOVE      "Invalid Formaction",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
.
DRGSTM99  RETURN
+
.------------------------------------------------------------
.  Print Misc.Items
.------------------------------------------------------------
MSCITM00  MOVE      ZERO,EXIT
          MATCH     SP70,UPDTTYPE
          GOTO      MSCITM30 IF EQUAL
.
          MATCH     "P",UPDTTYPE
          GOTO      MSCITM10 IF EQUAL
.
          GOTO      MSCITM90
.
MSCITM10  MOVE      "Print Misc Items",SCHEDDSC
          CALL      OPNPRINT
          PROC      PRTMITEM
          CALL      CLSPRINT
.
          MOVE      SP70,SCHEDDSC
          GOTO      MSCITM99
.
MSCITM30  MOVE      URNUMBER,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,MSCITM91
          CALL      RDPMPX21
          BRANCH    OVRCD,MSCITM91
.
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          IF        OVRCD=1
            CALL      CLPMSPX2
          ENDIF
.
          MOVE      ADMISSNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,MSCITM92
.
          MOVE      SP70,DDATE
          IF        ASTAT=3
            CALL      RDDSCH1
            BRANCH    OVRCD,MSCITM93
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Print Misc Items",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,MSCITM99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      MSCITM99
.
MSCITM90  MOVE      "Invalid Formaction",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      MSCITM99
.
MSCITM91  MOVE      "Can't find Patient Master details",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      MSCITM99
.
MSCITM92  MOVE      "Invalid Visit Number",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      MSCITM99
.
MSCITM93  MOVE      "Missing Discharge record",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      MSCITM99
.
MSCITM99  RETURN
+
.------------------------------------------------------------
. ITMSRC00-South Australian Item Search
.------------------------------------------------------------
ITMSRC00  MATCH     SP70,UPDTTYPE
          GOTO      ITMSRC70 IF EQUAL
.         
..          MATCH     "A",UPDTTYPE    * XXXXXXXXXXXXXXXX
..          GOTO      ITMSRC10 IF EQUAL
.         
          GOTO      ITMSRC90
.         
ITMSRC70  CLEAR     WEBTITLE   
          MOVE      "Casemix Type Search",WEBTITLE
          RESET     WEBTITLE
.         
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,ITMSRC99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      ITMSRC99
.
ITMSRC90  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ITMSRC99
.
ITMSRC99  RETURN
+
.------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          MOVE      SP70,TEMPLATE
          MOVE      SP70,NEXTPAGE
          PACK      REDIRECT,SP70,SP70,SP70,SP70
          MOVE      SP70,STARTKEY
          MOVE      SP70,UPDTTYPE
          MOVE      ZERO,CUSTFLAG
          MOVE      ZERO,STARTNUM
          MOVE      ZERO,NORECORD
          MOVE      SP70,KEYWORDS
          MOVE      SP70,SAITEMNO
.
          MOVE      SP70,FILTFDAT
          MOVE      SP70,FILTTDAT
          MOVE      ZERO,PUSRNAME
          MOVE      SP70,MENUFLAG
.
CLRPAR99  RETURN
.------------------------------------------------------------
. Set Parameters
.------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updttype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDTTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "urnumber=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,URNUMBER
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nocopies",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NOCOPIES
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "selprint",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SELPRINT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "admissno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ADMISSNO
            GOTO      SETPAR99
          ENDIF
.  
          MATCH     "nextpage=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NEXTPAGE
            GOTO      SETPAR99
          ENDIF 
.
          MATCH     "redirect=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REDIRECT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "custflag=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,CUSTFLAG
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "norecord=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NORECORD
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startnum=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTNUM
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "saitemno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SAITEMNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "keywords=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,KEYWORDS
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtfdat=",QRYNAME
          IF        @EQUAL
            UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
            CALL      SETMTH00
            PACK      FILTFDAT,CCENT,CYEAR,CMON,CDAY
            REP       " 0",FILTFDAT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filttdat=",QRYNAME
          IF        @EQUAL
            UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
            CALL      SETMTH00
            PACK      FILTTDAT,CCENT,CYEAR,CMON,CDAY
            REP       " 0",FILTTDAT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "pusrname=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,PUSRNAME
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "menuflag=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,MENUFLAG
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
.------------------------------------------------------------
. Process Fields    
.------------------------------------------------------------
PROFLD00  BRANCH    REPORTNO,PROFLD10,PROFLD20,PROFLD30
.
          GOTO      PROFLD99 
.
PROFLD10  BRANCH    FLDSETNO,PROFLD11,PROFLD12,PROFLD13
          GOTO      PROFLD99
.
PROFLD11  CALL      MASF0000    * Master File Info
          GOTO      PROFLD99
.
PROFLD12  CALL      MISF0000    * Admission File
          GOTO      PROFLD99    
.
PROFLD13  CALL      DSCF0000    * Discharge File
          GOTO      PROFLD99
.
PROFLD20  BRANCH    FLDSETNO,PROFLD21
          GOTO      PROFLD99
.
PROFLD21  CALL      SAITM000    * SA Item Search
          GOTO      PROFLD99
.
PROFLD30  BRANCH    FLDSETNO,PROFLD31,PROFLD32,PROFLD33,PROFLD34
          GOTO      PROFLD99
.
PROFLD31  CALL      MASF0000    * Master File Info
          GOTO      PROFLD99
.
PROFLD32  CALL      MISF0000    * Admission File
          GOTO      PROFLD99
.
PROFLD33  CALL      MITM0000    * print misc.item
          GOTO      PROFLD99
.
PROFLD34  CALL      DSCF0000    * Discharge File
          GOTO      PROFLD99
.
PROFLD99  RETURN
+
.-----------------------------------------------------------
.         print misc.item
.-----------------------------------------------------------
MITM0000  BRANCH    FLDITMNO,MITM1000,MITM2000,MITM3000
          GOTO      MITM9999
.
MITM1000  MOVE      ADMISSNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,MITM9999
.
          MATCH     FILTFDAT,SP70
          GOTO      MITM1200 IF NOT EQUAL
.
          PACK      FILTFDAT,CCC,CYY,CMM,CDD
          REP       " 0",FILTFDAT
          IF        ASTAT=3
            MOVE      ADATE,FILTFDAT            * default to admission date
          ENDIF
.
MITM1200  UNPACK    FILTFDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      MITM9999
.
MITM2000  MOVE      ADMISSNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,MITM9999
.
          MATCH     FILTTDAT,SP70
          GOTO      MITM2200 IF NOT EQUAL
.
          PACK      FILTTDAT,CCC,CYY,CMM,CDD    * default to current date
          REP       " 0",FILTTDAT
          IF        ASTAT=3
            CALL      RDDSCH1
            IF        OVRCD=0
              MOVE      DDATE,FILTTDAT        * default to discharge date
            ENDIF
          ENDIF
.
MITM2200  UNPACK    FILTTDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      MITM9999
.
MITM3000  WRITE     HTMLFILE;MENUFLAG;
          GOTO      MITM9999
.
MITM9999  RETURN
+
.-----------------------------------------------------------
.-----------------------------------------------------------
. Link to Next Group
.-----------------------------------------------------------
NEXTX000  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
.
          REP       " +",STARTKEY
          REP       "'~",STARTKEY
          REP       " +",KEYWORDS
          REP       "'~",KEYWORDS
          REP       " +",SAITEMNO
.
          WRITE     HTMLFILE;"patweb05.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&keywords=",KEYWORDS:
                                 "&startkey=",STARTKEY:
                                 "&saitemno=",SAITEMNO;
.
          REP       "+ ",STARTKEY
          REP       "~'",STARTKEY
          REP       "+ ",KEYWORDS
          REP       "~'",KEYWORDS
          REP       "+ ",SAITEMNO
.
NEXTX999  RETURN
.-------------------------------------------------------------
. Link to Prev Group
.-------------------------------------------------------------
PREVX000  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3
          IF        F3<0
            MOVE      ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
.
          REP       " +",STARTKEY
          REP       "'~",STARTKEY
          REP       " +",KEYWORDS
          REP       "'~",KEYWORDS
          REP       " +",SAITEMNO
.
          WRITE     HTMLFILE;"patweb05.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&startkey=",STARTKEY:
                                 "&keywords=",KEYWORDS:
                                 "&saitemno=",SAITEMNO;
.
          REP       "+ ",STARTKEY
          REP       "~'",STARTKEY
          REP       "+ ",KEYWORDS
          REP       "~'",KEYWORDS
          REP       "+ ",SAITEMNO
.
PREVX999  RETURN    
.------------------------------------------------------------
.  SA Item Search Tags 
.------------------------------------------------------------
SAITM000  BRANCH    FLDITMNO,SAITM100,SAITM200,SAITM300
          GOTO      SAITM999
.         
SAITM100  MOVE      ONE,LINK2PRT 
          CALL      SAITEM00                  * SA Item Search
          GOTO      SAITM999     
.                                
SAITM200  CALL      PREVX000                     * Prev Page
          GOTO      SAITM999     
.                                
SAITM300  CALL      NEXTX000                     * Next Page
          GOTO      SAITM999
.         
SAITM999  RETURN    
.------------------------------------------------------------
. Display SA Item Search Results
.------------------------------------------------------------
SAITEM00  WRITE     HTMLFILE;"<tr align=center>":
                             "<td class=HeadingCell colspan=4><b>":
                             "You Searched for ":
                               KEYWORDS,"</td>":
                             "</tr>":
                             "<tr>";
            WRITE     HTMLFILE;"<td class=HeadingCell><b>Item Number</b></td>":
                             "<td class=HeadingCell><b>Description</b></td>":
                             "</tr>"
.
          IF        LINK2PRT<>1
            UNPACK    DIM127,D1,LINKPRG,DIM127
            UNPACK    DIM127,D1,LINKREP,DIM127
            UNPACK    DIM127,D1,LINKTEM,DIM127
          ENDIF
.
          IF        NORECORD=0
            MOVE      "10",NORECORD       * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY3,KEYWORDS,SP70
          CALL      RDPASAI1 
          IF        OVRCD=0    
            CALL      RPPASAI1 
          ENDIF              
SAITEM10  CALL      RKPASAI1
          BRANCH    OVRCD,SAITEM90
.                            
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      SAITEM10
          ENDIF
.           
          BRANCH    LINK2PRT,SAITEM20
.
SAITEM20  REP       "#', ",PTSADESC
          REP       "#", ",PTSADESC       
          RESET     PTSADESC
.
          WRITE     HTMLFILE;"<tr valign=top>":
                              "<td><a href=#"":
                              "javascript:UpdateParent10('",PTSADESC:
                              "','",PTSAINO,"')#">":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                              " border=0 align=absmiddle></a>":
                              PTSAINO,"</td>":
                              "<td>&nbsp;",PTSADESC,"</td>":
                             "</tr>"
.
          GOTO      SAITEM80
.
SAITEM80  ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      SAITEM10 IF NOT EQUAL
SAITEM90
SAITEM99  RETURN
.------------------------------------------------------------
          INC       IBAWEBCD
          INC       WEBDATCD
.
          INC       APSMASIO/INC   
          INC       MRTMASIO/INC
          INC       NHIMASIO/INC
          INC       NHIETHIO/INC
          INC       OUTSITIO/INC
          INC       PATALRIO/INC
          INC       PATASFIO/INC
          INC       PMSUCHIO/INC
          INC       PMSUCDIO/INC
          INC       PATCMCIO/INC
          INC       VISMDTIO/INC
          INC       VISMTXIO/INC
          INC       PATDADIO/INC
          INC       PATDGWIO/INC
          INC       PATDO1IO/INC   * Doctor File
          INC       PATDSCIO/INC
          INC       PATECDIO/INC         * Disease codes
          INC       PATECOIO/INC         * Operation codes
          INC       PATFN1IO/INC
          INC       PATICUIO/INC
          INC       PATHSPIO/INC
          INC       PATIN1IO/INC
          INC       PATITMIO/INC
          INC       PATMI1IO/INC
          INC       PATMA1IO/INC
          INC       PATPGRIO/INC         * Patient Grouper details
          INC       PATPR1IO/INC
          INC       PATRE1IO/INC
          INC       PATTRNIO/INC         * Patient Transfer details
          INC       PATVADIO/INC
          INC       PATVISIO/INC   * Patients Visits File
          INC       PATW11IO/INC
          INC       PATW12IO/INC
          INC       PTW12BIO/INC
          INC       PATW13IO/INC
          INC       PATW14IO/INC         * Cost Weights for WIES14    *I-145161
          INC       PATW15IO/INC         * Cost Weights for WIES15
          INC       PATW16IO/INC         * Cost Weights for WIES16
          INC       PATW17IO/INC         * Cost Weights for WIES17
          INC       PATW18IO/INC         * Cost Weights for WIES18
          INC       PATW19IO/INC         * Cost Weights for WIES19
          INC       PATW20IO/INC         * Cost Weights for WIES20
          INC       PATW21IO/INC         * Cost Weights for WIES21
          INC       PATWIEIO/INC         * Cost Weights for WIES
          INC       PATWR1IO/INC
.
          INC       PMSCCDIO/INC   * Concession Card Details
          INC       PMSHCGIO/INC
          INC       PMSHCPIO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC   * Visit Extension File
          INC       PMSWDVIO/INC
          INC       PATSAIIO/INC
.
          INC       PRTMITEM
          INC       DRGSTMNT
          INC       CLNHIMAS                   * Clear nhimasaf file variables
          INC       CLPMSPX2
          INC       PATMASTM
          INC       PATDOCTM
          INC       PMSHCGTM
          INC       PMSHCPTM
          INC       PATDSCTM
          INC       PMSPX2TM
          INC       PATMSXTM
          INC       PATMISTM
          INC       PATDOCCD
          INC       CALCAGE
          INC       NAMSTRCD
          INC       DAYOFWEK
.

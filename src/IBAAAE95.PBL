.
.*******************************************************************************
.* System   : Accident & Emergency                                             *
.* Program  : IBAAAE95                                                         *
.* Desc     : Print Audit Reports                                              *
.*******************************************************************************
.* Date     : 21/05/90                                                         *
.* Author   : Eric Phan                                                        *
.* Function : This program prints & clears A&E Audit files.                    *
.* Mods     : 
.*        V5.08.01  15/08/2000  Caleb Sharman                                  *
.*                  Changed Health Fund table variables to 8 chars             *
.*        V5.01.01  21/05/90  E.Phan                                           *
.*                  Created program.                                           *
.*        V5.01.02  30/05/90  E.Phan                                           *
.*                  Removed "aaemchgf" desc from screen.                       *
.*******************************************************************************
.
          INC       STD001FD
          INC       AAEMCHFD/INC
.
.
RECCOUNT  FORM      8
.
TOTALADD  FORM      8
TOTALCHG  FORM      8
TOTALDEL  FORM      8
.
PRGID     INIT      "IBAAAE95"
PRGNAM    INIT      "Audit Reports"
VERSION   INIT      "V12.01.00"
.
.*******************************************************************************
.*        Mainline - Control Block                                             *
.*******************************************************************************
.
          CALL      INIT0000            * Open files/initialise parameters
.
ML01000   CALL      OPTN0000            * Select option
          BRANCH    EXIT,ML99999        * Exit
          COMPARE   "99",OPTION         * ALL files ?
          GOTO      ML90000 IF EQUAL    * Yes
          BRANCH    OPTION,ML10000:     * Miscellaneous Charges file
                           ML99990      * Clear all audit files
.
. ------- Miscellaneous Charges File -------
.
ML10000   CALL      OPEN1000            * Open audit file
          BRANCH    EXIT,ML01000        * Cannot open - select next option
          CALL      CONF0000            * Ok to Print (Y/N) ?
          BRANCH    EXIT,ML01000        * No - select next option
          CALL      PRNT1000            * Print aaemchgf file
          CALL      CONF1000            * Delete audit file (Y/N) ?
          BRANCH    EXIT,ML01000        * No - select next option
          CALL      CLRA1000            * Clear this audit file
          GOTO      ML01000             * Select next option
.
. ------- Print ALL audit files -------
.
ML90000   CALL      CONF2000            * Ok to print ALL files (Y/N) ?
          BRANCH    EXIT,ML01000        * No - select next option
          CALL      OPEN1000            * Open audit file
          BRANCH    EXIT,ML90000        * Cannot open - clear audits
          CALL      PRNT1000            * Print aaemchgf file
.
. ------- Clear all printed audit records -------
.
ML99990   CALL      CONF1000            * Ok to clear ALL files (Y/N) ?
          BRANCH    EXIT,ML01000        * No - select next option
          CALL      OPEN1000            * Open audit file
          BRANCH    EXIT,ML01000        * Cannot open - select next option
          CALL      CLRA1000            * Clear this audit file
          GOTO      ML01000             * Select next option
.
ML99999   CHAIN     PGM                 * Exit to menu
.
.*******************************************************************************
.*        Initialise                                                           *
.*******************************************************************************
.
INIT0000  CALL      DISPHEAD
INIT9999  RETURN
.
.*******************************************************************************
.*        Clear Audit files                                                    *
.*        Note : This routine assumes the file required is OPENed.             *
.*******************************************************************************
.
CLRA1000  MOVE      ZERO,RECCOUNT
          PACK      KEY19,SP20               * Start from the very beginning
          DISPLAY   *P1:19,*V2LON,"*** Clearing Misc Charges Audit ***",*HOFF:
                    *P1:21,"Date   :":
                    *P1:22,"Record :"
CLRA1100  CALL      ASAEMCH                  * Positioning read
CLRA1200  CALL      AKAEMCH                  * Read next record
          BRANCH    OVRCD,CLRA1999           * No more records - exit
          COMPARE   TWO,AEMCAUDS             * Record printed yet ?
          GOTO      CLRA1200 IF NOT EQUAL    * No - ignore this record
          ADD       ONE,RECCOUNT
          UNPACK    AEMCAUDD,CCENT,CYEAR,CMON,CDAY
          DISPLAY   *P10:21,*V2LON,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR:
                    *P10:22,RECCOUNT
          PACK      KEY19,AEMCAUDD,AEMCAUDT,AEMCAUDP,AEMCAUDR
          CALL      ADAEMCH                  * Delete this record
          GOTO      CLRA1100                 * Reposition pointer
CLRA1999  RETURN                             * Finished
.
.*******************************************************************************
.*        Confirmation                                                         *
.*******************************************************************************
.
CONF0000  DISPLAY   *P1:24,*EL,"Ok to print (";
          MOVE      ONE,FORM1
          GOTO      CONF9000
.
CONF1000  DISPLAY   *P1:24,*EL,"Delete all printed records (";
          MOVE      TWO,FORM1
          GOTO      CONF9000
.
CONF2000  DISPLAY   *P1:24,*EL,"Ok to print ALL files (";
          MOVE      THREE,FORM1
          GOTO      CONF9000
.
CONF9000  KEYIN     *V2LON,*DV,ANSY,*HOFF,*DV,SLASH,*V2LON:
                    *DV,ANSN,*HOFF,") ? ",*V2LON,ANS,*P1:24,*EL;
          CMATCH    ANSY,ANS
          GOTO      CONF9997 IF EQUAL
          CMATCH    ANSN,ANS
          GOTO      CONF9998 IF EQUAL
          BEEP
          BRANCH    FORM1,CONF0000,CONF1000,CONF2000
          GOTO      CONF9998
.
CONF9997  MOVE      ZERO,EXIT
          GOTO      CONF9999
CONF9998  MOVE      ONE,EXIT
CONF9999  RETURN
.
.*******************************************************************************
.*        Open files                                                           *
.*******************************************************************************
.
OPEN1000  TRAP      OPEN9998 IF IO
          OPEN      AAEAUDMC,"aaeaudmc"
          GOTO      OPEN9997
.
OPEN9997  MOVE      ZERO,EXIT
          GOTO      OPEN9999
OPEN9998  MOVE      ONE,EXIT
          NORETURN
OPEN9999  TRAPCLR   IO
          RETURN
.
.*******************************************************************************
.*        Select Options                                                       *
.*******************************************************************************
.
OPTN0000  HLINE     *G37,2,60,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Miscellaneous Charges File":
                    *P1:6,*V2LON,TWO,*HOFF," = Clear all audit files"
OPTN1000  KEYIN     *P1:24,"Select Audit Report ('99'=All) : ",*V2LON,OPTION
          COMPARE   ZERO,OPTION
          GOTO      OPTN9998 IF EQUAL
          COMPARE   "99",OPTION
          GOTO      OPTN9000 IF EQUAL
          BRANCH    OPTION,OPTN2100,OPTN2200
          BEEP
          GOTO      OPTN1000
.
OPTN2100  DISPLAY   *P60:2,*V2LON," Misc. Charges "
          GOTO      OPTN9997
.
OPTN2200  DISPLAY   *P60:2,*V2LON," Clear All Audits "
          GOTO      OPTN9997
.
OPTN9000  DISPLAY   *P60:2,*V2LON," Print All Audits "
          GOTO      OPTN9997
.
OPTN9997  MOVE      ZERO,EXIT
          GOTO      OPTN9999
OPTN9998  MOVE      ONE,EXIT
OPTN9999  RETURN
.
.*******************************************************************************
.*        Print Reports                                                        *
.*******************************************************************************
.
PRNT1000  MOVE      ZERO,TOTALADD            * Clear totals
          MOVE      ZERO,TOTALCHG
          MOVE      ZERO,TOTALDEL
          MOVE      ZERO,CPAGENO
          MOVE      ZERO,RECCOUNT
          MOVE      "80",CLNO                * Initialise line count
          DISPLAY   *P1:19,*V2LON,"*** Printing Misc Charges Audit ***",*HOFF:
                    *P1:21,"Date   :":
                    *P1:22,"Record :"
          PACK      KEY19,SP20               * Start from the very beginning
          CALL      ASAEMCH                  * Positioning read
PRNT1100  CALL      AKAEMCH                  * Read next record
          BRANCH    OVRCD,PRNT1999           * No more records
          ADD       ONE,RECCOUNT
          UNPACK    AEMCAUDD,CCENT,CYEAR,CMON,CDAY
          DISPLAY   *P10:21,*V2LON,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR:
                    *P10:22,RECCOUNT
          CALL      PRRC1000                 * Print record
          COMPARE   TWO,AEMCAUDS             * Flagged as printed ?
          GOTO      PRNT1100 IF EQUAL        * Yes - no need to update
          MOVE      TWO,AEMCAUDS             * Otherwise flag as printed
          CALL      AUAEMCH                  * Update record
          GOTO      PRNT1100                 * Read next record
PRNT1999  CALL      PRTL0000                 * Finished - print tail
          RETURN
.
.*******************************************************************************
.*        Actual Print Routines                                                *
.*******************************************************************************
.
. ------- Print Page Header -------
.
PRHD0000  CLOCK     TIME,CTIMEIS             * Get the current time
          ADD       ONE,CPAGENO              * Next page number
          COMPARE   ONE,CPAGENO              * 1st page ?
          GOTO      PRHD1000 IF EQUAL        * Yes - no line separator
          CALL      PRLN0000                 * Print line separator at end of pg
PRHD1000  PRINT     *F,*1,PRGID,*40,CNAME,*118,"DATE : ",CDATE:
                    *N,*1,VERSION,*40,PRGNAM," - Miscellaneous Charges ":
                       *118,"TIME : ",CTIMEIS:
                    *N,*118,"PAGE :     ",CPAGENO
          CALL      PRLN0000
          PRINT     *1,"|   Date   |   Time   |Port|Type| User |Audit Data":
                    *132,"|":
                    *N,"|-------------------------------------------":
                       "--------------------------------------------":
                       "-------------------------------------------|"
          MOVE      SEVEN,CLNO
          RETURN
.
. ------- Print Line Separator -------
.
PRLN0000  PRINT     *1,"*-------------------------------------------":
                       "--------------------------------------------":
                       "-------------------------------------------*"
          RETURN
.
. ------- Print Record -------
.         Note : Assumes that date already unpacked in display routine
.
PRRC1000  COMPARE   "60",CLNO
          GOTO      PRRC1100 IF LESS
          CALL      PRHD0000
PRRC1100  PRINT     *1,"|",*3,CDAY,SLASH,CMON,SLASH,CYEAR:
                    *12,"|",*14,AEMCAUDT,*23,"|",*25,AEMCAUDP:
                    *28,"|",*31,AEMCAUDR,*33,"|",*35,AEMCAUDO:
                    *40,"|",AEMCCLM,"~",AEMCFUND,"~",AEMCTAB,"~",AEMCCLSS:
                        "~",AEMCMBS,"~",AEMCPAT,"~",AEMCREB,"~",AEMCSPAR:
                        "~",*132,"|"
          ADD       ONE,CLNO
          MATCH     ANSA,AEMCAUDR
          GOTO      PRRC1200 IF EQUAL        * Add Record
          MATCH     ANSB,AEMCAUDR
          GOTO      PRRC1999 IF EQUAL        * Before Change - don't add
          MATCH     ANSC,AEMCAUDR
          GOTO      PRRC1300 IF EQUAL        * Change Record
          MATCH     ANSD,AEMCAUDR
          GOTO      PRRC1400 IF EQUAL        * Delete Record
          GOTO      PRRC1999
.
PRRC1200  ADD       ONE,TOTALADD
          GOTO      PRRC1999
PRRC1300  ADD       ONE,TOTALCHG
          GOTO      PRRC1999
PRRC1400  ADD       ONE,TOTALDEL
          GOTO      PRRC1999
PRRC1999  RETURN
.
. ------- Print Tail of Report -------
.
PRTL0000  CALL      PRLN0000
          PRINT     *N,"Total Additions Printed : ",TOTALADD:
                    *N,"Total Changes   Printed : ",TOTALCHG:
                    *N,"Total Deletions Printed : ",TOTALDEL:
                    *N,"*** End of Report ***",*N
          RETURN
.
.*******************************************************************************
.*        I/O Routines                                                         *
.*******************************************************************************
.
          INC       AAEMCHIO/INC
          INC       STD001IO
.

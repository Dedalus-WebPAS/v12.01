.******************************************************************************
.* System         : Waiting Lists                                             *
.* Program        : IBAWAT79.PBL                                              *
.* Name           : Elective Surgery Activity Return                          *
.******************************************************************************
.* Date           : 15/07/1991                                                *
.* Author         : Justin Coates                                             *
.* Function       : To generate new stats report for Victorian Health Dept.   *
.* Comments       : This program should've been given to Deon the Stats KING  *
.*                  to write as I am not a Stats KING and I don't want to be  *
.*                  one.  As a result there is probably little, annoying bugs *
.*                  located within the nightmare that this program is.        *
.* Modifications  :                                                           *
.*                                                                            *
. *****************************************************************************
.* V12.00.01 16/05/2025  J.Tan          TSK 0955096                           *
.*           Added alpanumeric visit number generation                        *
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899                         *
.*           Recompiled as WATTR1FD changes                                   *
.******************************************************************************
.*       V10.05.01  01/08/2015  Ania P                      CAR 261630        *
.*                  Removed use of PORT for temp file naming                  *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V5.08.01  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.07.B02 23/11/1998  Nicole Harrington  507 changes                *
.*        V5.06.02  21/05/1998  Greg Horvat      SRF 623736 (Rebound)         *
.*                  Fixed problem below                                       *
.*        V5.06.01  30/04/1998  Greg Horvat      SRF 623736                   *
.*                  Changed to not give a priority to patients when the 1st   *
.*                  indicator of category TP is set to blank                  *
.*        V5.01.05  20/07/1994  bez              SRF 126706                   *
.*                  Re-wrote.                                                 *
.*                  Calculate stats in this program, and not in               *
.*                  IBAWAT90 because it is totally stuffed.                   *
.******************************************************************************
.
.======================================== 
.  FD INCLUDES
.======================================== 
.
          INC       STD001FD/PBL            * standard variables
          INC       BOKRC1FD/INC            * booking file
          INC       PATCODFD/INC            * codes file
          INC       PATDRGFD/INC            * date range file
          INC       PATMI1FD/INC            * admission file
          INC       PMSVX1FD/INC            * admission file
          INC       PATTRNFD/INC            * transfer file
          INC       WATCATFD/INC            * category change file
          INC       WATCONFD/INC            * control file
          INC       WATTR1FD/INC            * WL treatment file
          INC       IBASEQFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
.======================================== 
. VARIABLE LIST
.======================================== 
.
ADMIDATE  DIM       8         * Admission date
CDYSDAYS  FORM      6         * number of days calculated 
CDYSFDTE  DIM       8         * from date
CDYSTDTE  DIM       8         * to date
CDYSYEAR  FORM      2         * work year
CFNAMEA   DIM       8         * temp file A name
CFNAMEB   DIM       8         * temp file B name
CMDLINE   DIM       50        * for execute command
COLDFLAG  FORM      1         * Column D flag
CPTDATE   DIM       8         * for patcomn3
DATEFR10  DIM       10        * fromdate dd/mm/ccyy
DATEFR8   DIM       8         * fromdate ccyymmdd
DATETO10  DIM       10        * to  date dd/mm/ccyy
DATETO8   DIM       8         * to  date ccyymmdd
DAYPFLAG  FORM      1         * Daypatient flag
DESCPERD  DIM       20        * period description
FRDATE8   DIM       8         * calander from date ccyymmdd
GDAYFLAG  FORM      1         * Get daypaient flag
GINSFLAG  FORM      1         * Get insurance flag
INSUFLAG  FORM      1         * insurance flag
NEWA      FORM      1         * new record for file A flag
NEWB      FORM      1         * new record for file B flag
PERIOD6   DIM       6         * period ccyymm   (financial)
PERIOD7   DIM       7         * period mm/ccyy  (calender)
PRIORITY  FORM      1         * Priority flag
SURGFLAG  FORM      1         * Surgical unit?
SAVKEY21  DIM       21        * save var
TEMP1     FORM      4         * temp variable
TEMP2     FORM      4         * temp variable
TEMP3     FORM      4         * temp variable
TODATE8   DIM       8         * calander to date ccyymmdd
PUTBACK   FORM      1         * put back on wlist?
WHENBACK  FORM      1         * when put back on wlist after cancelled booking?
WHENADMI  FORM      1         * when admitted
WHENBOOK  FORM      1         * when booked
WHENBCAN  FORM      1         * when booked cancelled
WHENCANC  FORM      1         * when cancelled
WHENWAIT  FORM      1         * when put on waiting list
.
.======================================== 
. CONSTANTS
.======================================== 
CODEBC    INIT      "BC"
CODETP    INIT      "TP"
ISBUILD   INIT      "isbuild "
ISERASE   INIT      "iserase "
KEYA      INIT      " 90 U1-10,11-13"
KEYB      INIT      " 111 U1-20,21-23"
PIPE      INIT      "|"
PRFIXA    INIT      "WAT79A"
PRFIXB    INIT      "WAT79B"
RANGE1    FORM      "30"      * days waiting range 1
RANGE2    FORM      "90"      * days waiting range 2
RANGE3    FORM      "180"     * days waiting range 3
.
.======================================== 
. TEMPORARY FILE DEFINITIONS
.======================================== 
.
.-------- PAGE ONE OF REPORT --------
.
TEMPA     IFILE     SQL, FIXED=90, KEY="U1-10,11-13"
.
.Name     Type      Length  Physical  Start  Description
.-------  ----      ------  --------  -----  -------------------------------
XSPDESC   DIM       10          10        1  Speciality description
XSPECL    DIM       3            3       11  Speciality 
XADAYP    FORM      5            4       14  # booked day patients        (A)
XAINP     FORM      5            4       18  # booked in-patients         (A)
XB1CAT1   FORM      4            3       22  Range 1 - category 1         (B1)
XB1CAT2   FORM      4            3       25  Range 1 - category 2         (B1)
XB1CAT3   FORM      4            3       28  Range 1 - category 3         (B1)
XB1TOTAL  FORM      5            4       31  Range 1 - Total              (B1)
XB2CAT1   FORM      4            3       35  Range 2 - category 1         (B2)
XB2CAT2   FORM      4            3       38  Range 2 - category 2         (B2)
XB2CAT3   FORM      4            3       41  Range 2 - category 3         (B2)
XB2TOTAL  FORM      5            4       44  Range 2 - Total              (B2)
XB3CAT1   FORM      4            3       48  Range 3 - category 1         (B3)
XB3CAT2   FORM      4            3       51  Range 3 - category 2         (B3)
XB3CAT3   FORM      4            3       54  Range 3 - category 3         (B3)
XB3TOTAL  FORM      5            4       57  Range 3 - Total              (B3)
XB4CAT1   FORM      4            3       61  Range 4 - category 1         (B4)
XB4CAT2   FORM      4            3       64  Range 4 - category 2         (B4)
XB4CAT3   FORM      4            3       67  Range 4 - category 3         (B4)
XB4TOTAL  FORM      5            4       70  Range 4 - Total              (B4)
XCCAT1    FORM      5            4       74  Total Ranges - category 1    (C) 
XCCAT2    FORM      5            4       78  Total Ranges - category 2    (C) 
XCCAT3    FORM      5            4       82  Total Ranges - category 3    (C) 
XCTOTAL   FORM      5            4       86  Total Ranges - Total         (C) 
.
.End of Record                           90
.
.-------- PAGE TWO OF REPORT --------
.
TEMPB     IFILE     SQL, FIXED=111, KEY="U1-20,21-23"
.
.Name     Type      Length  Physical  Start  Description
.-------  ----      ------  --------  -----  -------------------------------
YSPDESC   DIM       20          20        1  Speciality Description
YSPECL    DIM       3            3       21  Speciality 
YDCAT1    FORM      3            3       24  Waiting/Delayed Cat 1          (D)
YDCAT2    FORM      3            3       27  Waiting/Delayed Cat 2          (D)
YDCAT3    FORM      3            3       30  Waiting/Delayed Cat 3          (D)
YDTOTAL   FORM      4            3       33  Waiting/Delayed Total          (D)
YECAT1    FORM      3            3       36  Booked Cat 1                   (E)
YECAT2    FORM      3            3       39  Booked Cat 2                   (E)
YECAT3    FORM      3            3       42  Booked Cat 3                   (E)
YETOTAL   FORM      4            3       45  Booked Total                   (E)
YFCAT1    FORM      3            3       48  Admitted direct Cat 1          (F)
YFCAT2    FORM      3            3       51  Admitted direct Cat 2          (F)
YFCAT3    FORM      3            3       54  Admitted direct Cat 3          (F)
YFTOTAL   FORM      4            3       57  Admitted direct Total          (F)
YGCAT1    FORM      3            3       60  Cancelled Deleted Cat 1
YGCAT2    FORM      3            3       63  Cancelled Deleted Cat 2
YGCAT3    FORM      3            3       66  Cancelled Deleted Cat 3
YGTOTAL   FORM      3            3       69  Cancelled/Deleted Total        (G)
YHCAT1    FORM      3            3       72  New Waiting/Delayed Cat 1      (H)
YHCAT2    FORM      3            3       75  New Waiting/Delayed Cat 2      (H)
YHCAT3    FORM      3            3       78  New Waiting/Delayed Cat 3      (H)
YHTOTAL   FORM      4            3       81  New Waiting/Delayed Total      (H)
YICAT1    FORM      3            3       84  Total Waiting/Delayed Cat 1    (I)
YICAT2    FORM      3            3       87  Total Waiting/Delayed Cat 2    (I)
YICAT3    FORM      3            3       90  Total Waiting/Delayed Cat 3    (I)
YITOTAL   FORM      4            3       93  Total Waiting/Delayed Total    (I)
YJCAT1    FORM      3            3       96  Inpatient Admitted Cat 1       (J)
YJCAT2    FORM      3            3       99  Inpatient Admitted Cat 2       (J)
YJCAT3    FORM      3            3      102  Inpatient Admitted Cat 3       (J)
YJDAYP    FORM      3            3      105  Daypatient Admitted            (J)
YJTOTAL   FORM      4            3      108  Total Admitted                 (J)
.
.End of Record                          111
.
.===============================
. TOTAL VARIABLES
.===============================
.
TXADAYP    FORM      5         
TXAINP     FORM      5        
TXB1CAT1   FORM      4       
TXB1CAT2   FORM      4      
TXB1CAT3   FORM      4     
TXB1TOTA   FORM      5    
TXB2CAT1   FORM      4   
TXB2CAT2   FORM      4  
TXB2CAT3   FORM      4 
TXB2TOTA   FORM      5
TXB3CAT1   FORM      4          
TXB3CAT2   FORM      4         
TXB3CAT3   FORM      4        
TXB3TOTA   FORM      5          
TXB4CAT1   FORM      4         
TXB4CAT2   FORM      4        
TXB4CAT3   FORM      4          
TXB4TOTA   FORM      5         
TXCCAT1    FORM      5        
TXCCAT2    FORM      5       
TXCCAT3    FORM      5      
TXCTOTAL   FORM      5     
.
TYDCAT1    FORM      3          
TYDCAT2    FORM      3         
TYDCAT3    FORM      3        
TYDTOTAL   FORM      4       
TYECAT1    FORM      3      
TYECAT2    FORM      3           
TYECAT3    FORM      3          
TYETOTAL   FORM      4         
TYFCAT1    FORM      3        
TYFCAT2    FORM      3       
TYFCAT3    FORM      3      
TYFTOTAL   FORM      4     
TYGTOTAL   FORM      3 
TYHCAT1    FORM      3         
TYHCAT2    FORM      3        
TYHCAT3    FORM      3       
TYHTOTAL   FORM      4      
TYICAT1    FORM      3     
TYICAT2    FORM      3        
TYICAT3    FORM      3       
TYITOTAL   FORM      4      
TYJCAT1    FORM      3     
TYJCAT2    FORM      3    
TYJCAT3    FORM      3   
TYJDAYP    FORM      3  
TYJTOTAL   FORM      4 
.
.=======================
. OTHER STATS VARIABLES
.=======================
A1DAYP    FORM      5                        Medicine Daypatients         (A1)
A1INP     FORM      5                        Medicine Inpatients          (A1)
A1WAIT    FORM      5                        Medicine Waiting/Delayed     (A1)
A2INSUR   FORM      5                        Insured Booked               (A2)
A2NONIN   FORM      5                        Non-Insured Booked           (A2)
A2TOTAL   FORM      5                        Total Booked                 (A2)
C1INSUR   FORM      5                        Insured Waiting/Delayed      (C1)
C1NONIN   FORM      5                        Non-Insured Waiting Delayed  (C1)
C1TOTAL   FORM      5                        Total Waiting/Delayed        (C1)
F1PUB     FORM      3                        Admitted Public              (F1)
F1PRIV    FORM      3                        Admitted Private             (F1)
F1TOTAL   FORM      3                        Admitted Total               (F1)
KTOTAL    FORM      4                        Emergency Surgical Admissions(K)
LTOTAL    FORM      4                        Medical & Other Admissions   (L)
MTOTAL    FORM      4                        Total K + L                  (M)
NTOTAL    FORM      5                        Day Procedure Admissions     (N)
XTOT      FORM      6                        Total X variables
YTOT      FORM      6                        Total Y variables
.
++
PRGID     INIT      "IBAWAT79"
PRGNAM    INIT      "Elective Surgery Activity Return"
VERSION   INIT      "V12.01.00"
.
.*********************************************************************
.* ML0000 : MAINLINE 
.*********************************************************************
ML0000    CALL      INIT0000                * initialization
.
ML1000    CALL      OPTN0000                * select option
          BRANCH    MOPTION,ML2000          * run report
          GOTO      ML9999                  * exit
.
ML2000    CALL      PERD0000                * enter period for report
          BRANCH    EXIT,ML1000             * exit
.
          CALL      CREA0000                * Create temp files
.
          CALL      CALC0000                * Calculate the data
          CALL      REPT0000                * produce report
.
ML9000    CALL      KILL0000                * Delete temp file
          GOTO      ML2000
.
ML9999    CHAIN     PGM
+
.
.*********************************************************************
.* INIT0000 : Initialization / open files  
.*********************************************************************
INIT0000  CALL      DISPHEAD
          CALL      IBACLOCK
.
          DISPLAY   *P50:24,"Opening ",*EL;
          DISPLAY   *P58:24,"bokrc1af"
          OPEN      BOKRC1A1,"bokrc1af"
          DISPLAY   *P58:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
          DISPLAY   *P58:24,"patdrgaf"
          OPEN      PATDRGA1,"patdrgaf"
          OPEN      PATDRGA3,"patdrgaf"
          DISPLAY   *P58:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      PATMI1A3,"patmi1af"
          DISPLAY   *P58:24,"pattranf"
          OPEN      PATTRAN2,"pattranf"
          DISPLAY   *P58:24,"wattr1af"
          OPEN      WATTR1A1,"wattr1af"
          OPEN      WATTR1A3,"wattr1af"
          DISPLAY   *P58:24,"watcataf"
          OPEN      WATCATA2,"watcataf"
.
          DISPLAY   *P58:24,"controlf"
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,THIRTY7;*225,WTCNDPWD
.
INIT9999  RETURN
+
.
.*********************************************************************
.*                  OPTN0000                    Called by : ML1000   *
.*        Run the report ?                                           *
.*********************************************************************
OPTN0000  DISPLAY   *P1:3,*EF,*V2LON:
                    *P1:4,"0",*P1:5,"1",*HOFF:
                    *P2:4,". Exit":
                    *P2:5,". Run Report":
                    *P1:7,"Select option : ";
.
OPTN1000  KEYIN     *P17:7,*DV,UNDLN1:
                    *P17:7,*V2LON,MOPTION
.
          BRANCH    MOPTION,OPTN9999
.
          COMPARE   ZERO,MOPTION
          GOTO      OPTN9999 IF EQUAL       * exit selected
.
          BEEP
          GOTO      OPTN1000
.
OPTN9999  RETURN
+
.
.*********************************************************************
.*                  PERD0000                    Called by : ML2000   *
.*        Enter the period for the report                            *
.*********************************************************************
PERD0000  DISPLAY   *P1:10,"Month  : ",*EF;
.
          MOVE      "0",CHIGHLT             * use highlighting
          MOVE      "10",CVERT              * keyin row
          MOVE      "10",CCOL               * keyin column
          MOVE      "10",EVERT              * error row
          MOVE      "41",ECOL               * error column
.
PERD1000  UNPACK    SP4,CYEAR,CMON          * no defaults
          MOVE      CCC,CCENT               * set century
          CALL      KEYPER
          BRANCH    CQUEST,PERD1000         * ? entered
          BRANCH    OVRCD,PERD9100          * exit
.
.         validate calender period entered
.
          PACK      KEY6,ICENT,IYEAR,IMON
          REP       " 0",KEY6
          CALL      RDDRGA3
          BRANCH    OVRCD,PERD5000
.
          PACK      PERIOD6,DRGYR,DRGNUM          (financial!)
          REP       " 0",PERIOD6 
.
          PACK      PERIOD7,DRGCNUM,SLASH,DRGCYR  (calender!)
          REP       " 0",PERIOD7
.
          MOVE      DRGFDTE,DATEFR8         * fromdate ccyymmdd
          MOVE      DRGTDTE,DATETO8         * to  date ccyymmdd
          MOVE      DRGLDSC,DESCPERD        * period description
.
          UNPACK    DATEFR8,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DATEFR10        * fromdate dd/mm/ccyy
.
          UNPACK    DATETO8,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DATETO10        * to  date dd/mm/ccyy
.
          DISPLAY   *P30:10,DESCPERD:
                    *P30:11,DATEFR10," to ",DATETO10
.
.         OK to Continue (Y/N/C)
.
          CALL      CONTQST
          BRANCH    CEXIT,PERD4000,PERD0000,PERD9100
.                         Yes      No       Cancel
.
PERD4000  MOVE      FALSE,EXIT              * valid period
          GOTO      PERD9999
.
PERD5000  DISPLAY   *P1:24,*B,"Invalid Period.  ",*EL;
          CALL      HITENTER 
          GOTO      PERD1000
.
PERD9100  MOVE      TRUE,EXIT               * exit
.
PERD9999  RETURN
+
.
.*********************************************************************
. CALC0000 : Calculate the stats and write to temp file  
.*********************************************************************
CALC0000  DISPLAY   *P1:13,*EF,"Waiting List Date : "
.
          CALL      CTMP0000                * clear temp file data
          CALL      COTH0000                * clear other data
.
          PACK      KEY27,SP20,SP10
          CALL      RDSTREA3                * position in file
CALC0100  CALL      RDKTREA3                * read next record
          BRANCH    OVRCD,CALC9000          * end of file
.
          MATCH     WMDATE1,DRGTDTE         * date on list past end of month?
          GOTO      CALC9000 IF LESS        * yes - finish
.
.-------- Format date for display
.
          UNPACK    WMDATE1,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 
.
          DISPLAY   *P22:13,*V2LON,CPCDATE
.
          CALL      SURG0000                 * is patient in a Surgical unit?
          CALL      READ0000                 * Read the temp files
.
          CALL      GPRI0000                 * get priority (1,2,3)
          BRANCH    EXIT,CALC0100
.
          CALL      WWAT0000                 * when was patient put on Wlist?
          CALL      WBOK0000                 * when was patient booked?
          CALL      WADM0000                 * when was patient admitted?
          CALL      WCAN0000                 * when was wlist cancelled? 
.
.-------- Page 1
.
          CALL      BOOK0000                 * handle booked patients
          MOVE      "0",COLDFLAG             * not doing Column D
          CALL      WAIT0000                 * handle waiting/delayed patients
.
.-------- Page 2
.
          COMPARE   "1",SURGFLAG             * surgery?
          GOTO      CALC8000 IF NOT EQUAL    * no
.
          CALL      COLE0000                 * Get Column E
          CALL      COLF0000                 * Get Column F
          CALL      COF10000                 * Get Column F1
          CALL      COLG0000                 * Get Column G
          CALL      COLJ0000                 * Get Column J
.
.--------  update or write new file if surgical
.
          CALL      COLD0000                 * Column D  (do last)
          CALL      COLH0000                 * Get Column H (do after D)
          CALL      WRTA0000                 * write to A
          CALL      WRTB0000                 * write to B
.
CALC8000  GOTO      CALC0100                 * Get next record
.
CALC9000  CALL      ADMI0000                 * Get Admissions (K,L,M,N)
.
CALC9999  RETURN
+
.
.********************************************************************** 
. SURG0000 : Is patient in a Surgical unit?
. returns  : SURGFLAG = 0 (No)
.                     = 1 (Yes)
.            XSPDESC  = specialty description
.            XSPECL   = specialty code
.            YSPDESC  = specialty description
.            YSPECL   = specialty code
.********************************************************************** 
SURG0000  PACK      KEY5,ANSW,ANSU,WMUNIT 
          CALL      RDCODE1
          BRANCH    OVRCD,SURG9000          * invalid unit
.
          MATCH     "S",TCINDC1             * surgical unit ?
          GOTO      SURG9000 IF NOT EQUAL
.
          MOVE      "1",SURGFLAG            * is a surgical
          MOVE      TDESC,XSPDESC           * specialty description
          MOVE      TDESC,YSPDESC           * specialty description
          MOVE      ACODE,XSPECL            * specialty code
          MOVE      ACODE,YSPECL            * specialty code
          GOTO      SURG9999
.
SURG9000  MOVE      "0",SURGFLAG            * not a surgical
.
SURG9999  RETURN
+
.
.********************************************************************** 
. READ0000 : Read temp file data 
.********************************************************************** 
READ0000  COMPARE   "1",SURGFLAG             * surgery?
          GOTO      READ9999 IF NOT EQUAL
.
          CALL      CTMP0000                 * clear vars
.
          PACK      KEY13,XSPDESC,XSPECL,SP20
          CALL      RDTEMPA
          IF        OVRCD=1
            MOVE      "1",NEWA               * new record
          ELSE
            MOVE      "0",NEWA               * not new record
          ENDIF
.
          PACK      KEY23,YSPDESC,YSPECL,SP20
          CALL      RDTEMPB
          IF        OVRCD=1
            MOVE      "1",NEWB               * new record
          ELSE
            MOVE      "0",NEWB               * not new record
          ENDIF
.
READ9999  RETURN
+
.
.********************************************************************** 
. GPRI0000 : Get priority
. returns  : PRIORITY = 1  (Category 1)
.                       2  (Category 2)
.                       3  (Category 3)
.********************************************************************** 
GPRI0000  
.
.--------  Check Category change file to see if priority changed.
.
          PACK      SAVKEY21,WMURNO,WMCODE,WMCNT,CODETP,SP30
          PACK      KEY29,SAVKEY21,SP30
          CALL      RSWTCAT2                 * positional read
GPRI1000  CALL      RKWTCAT2                 * read next
          BRANCH    OVRCD,GPRI5000
.
          PACK      KEY21,WTCAURNO,WTCAPROC,WTCAPCNT,WTCACATF,SP30
.
          MATCH     SAVKEY21,KEY21           * same key?
          GOTO      GPRI5000 IF NOT EQUAL    * no, no more changes
.
          MATCH     DRGFDTE,WTCAEDAT         * less than period?
          GOTO      GPRI1000 IF LESS         * yes, get next record
.
          MATCH     WTCAEDAT,DRGTDTE         * greater than period
          GOTO      GPRI5000 IF NOT LESS     * yes, no more changes
.
.--------  We have a changed priority within period.
.
          MOVE      WTCACODT,WMPTY           * set priority
          GOTO      GPRI1000                 * check for another change
.
.--------  Validate Priority
.
GPRI5000  MATCH     SP3,WMPTY
          GOTO      GPRI9500 IF EQUAL       * Blank priority
.
          PACK      KEY5,ANST,ANSP,WMPTY
          CALL      RDCODE1
          BRANCH    OVRCD,GPRI9500          * invalid priority code
.
          MATCH     "U",TCINDC1
          GOTO      GPRI8100 IF EQUAL       * Urgent
.
          MATCH     "S",TCINDC1
          GOTO      GPRI8200 IF EQUAL       * Semi-Urgent
.
          MATCH     "R",TCINDC1
          GOTO      GPRI8300 IF EQUAL       * Routine
          GOTO      GPRI9500
.
GPRI8100  MOVE      "1",PRIORITY
          GOTO      GPRI9000
.
GPRI8200  MOVE      "2",PRIORITY
          GOTO      GPRI9000
.
GPRI8300  MOVE      "3",PRIORITY
.
GPRI9000  MOVE      ZERO,EXIT
          GOTO      GPRI9999
.
GPRI9500  MOVE      ONE,EXIT
GPRI9999  RETURN
+
.
.********************************************************************** 
. WWAT0000 : When was patient put on Waiting List?
. returns  : WHENWAIT = 0 (No Date)
.                     = 1 (put on Wlist before period)
.                     = 2 (put on Wlist during period)
.                     = 3 (put on Wlist after period)
.********************************************************************** 
WWAT0000  MOVE      "0",WHENWAIT             * no date
.
          MATCH     SP8,WMDATE1
          IF        @EQUAL
            DISPLAY   *P1:24,*EL,*B,"U/R ",WMURNO," has no waiting List date. ";
            CALL      HITENTER
            GOTO      WWAT9999
          ENDIF
.
          REP       " 0",WMDATE1             * no spaces!
.
          MATCH     DRGFDTE,WMDATE1
          GOTO      WWAT9100 IF LESS
.
          MATCH     WMDATE1,DRGTDTE
          GOTO      WWAT9200 IF NOT LESS
.
          MOVE      "3",WHENWAIT             * put on Wlist after period
          GOTO      WWAT9999
.
WWAT9100  MOVE      "1",WHENWAIT             * put on WList before period
          GOTO      WWAT9999
.
WWAT9200  MOVE      "2",WHENWAIT             * put on WList during period
          GOTO      WWAT9999
.
WWAT9999  RETURN
+
.
.********************************************************************** 
. WBOK0000 : when was patient booked?
. returns  : WHENBOOK = 0 (Not booked) 
.                     = 1 (booked before period)
.                     = 2 (booked during period)
.                     = 3 (booked after period)
.********************************************************************** 
WBOK0000  MOVE      "0",WHENBOOK             * not booked 
          MOVE      "0",WHENBCAN
.
          PACK      KEY8,WMBOOK,SP10
          CALL      RDBKREC1                 * read the booking file
          BRANCH    OVRCD,WBOK9900           * not booked
.
          MATCH     SP8,BKREADAT
          IF        @EQUAL
            DISPLAY   *P1:24,*EL,*B,"Booking ",WMBOOK," has no booking date. ";
            CALL      HITENTER
            GOTO      WBOK9900
          ENDIF
.
          REP       " 0",BKREADAT            * no spaces!
.
          MATCH     DRGFDTE,BKREADAT       
          GOTO      WBOK9100 IF LESS
.
          MATCH     BKREADAT,DRGTDTE
          GOTO      WBOK9200 IF NOT LESS
.
          MOVE      "3",WHENBOOK             * booked after period
          GOTO      WBOK9950
.
WBOK9100  MOVE      "1",WHENBOOK             * booked before period
          GOTO      WBOK9950
.
WBOK9200  MOVE      "2",WHENBOOK             * booked during period
          GOTO      WBOK9950
.
WBOK9900  UNPACK    SP20,BKREATYP,BKRECLMT,BKREADAT * clear data
          GOTO      WBOK9999                 * not booked
.
WBOK9950  IF        BKRESTAT=2
            CALL      WBCA0000               * when was booking cancelled?
          ENDIF
.
WBOK9999  RETURN
+
. 
.********************************************************************** 
. WADM0000 : When was patient admitted?
. returns  : WHENADMI = 0 (Not admitted)
.                     = 1 (admitted before period)
.                     = 2 (admitted during period)
.                     = 3 (admitted after period)
.********************************************************************** 
WADM0000  MOVE      "0",WHENADMI             * not admitted
.
          PACK      KEY30,WMBOOK,SP20,SP20
          CALL      RDSTRAN2
WADM1000  CALL      RDKTRAN2                 * read A record
.
          MATCH     WMBOOK,TADMN             * same admission no.?
.kkkk
          GOTO      WADM9900 IF NOT EQUAL    * not admitted
.
.-------- We have an admission, read admission record for 
.         claim type, and admission type
.
          PACK      KEY8,WMBOOK,SP10
          CALL      RDMISS1                  * read admission record
          BRANCH    OVRCD,WADM9900           * missing admission record
.
          PACK      ADMIDATE,ADATE
.
          MATCH     SP8,ADMIDATE
          IF        @EQUAL
            DISPLAY   *P1:24,*EL,*B,"Admission ",WMBOOK:
                                    " has no admission date. ";
            CALL      HITENTER
            GOTO      WADM9900
          ENDIF
.
          REP       " 0",ADMIDATE            * no spaces!
.
          MATCH     DRGFDTE,ADMIDATE
          GOTO      WADM9100 IF LESS
.
          MATCH     ADMIDATE,DRGTDTE
          GOTO      WADM9200 IF NOT LESS
.
          MOVE      "3",WHENADMI             * admitted after period
          GOTO      WADM9999
.
WADM9100  MOVE      "1",WHENADMI             * admitted before period
          GOTO      WADM9999
.
WADM9200  MOVE      "2",WHENADMI             * admitted during period
          GOTO      WADM9999
.
WADM9900  UNPACK    SP20,ACLAIM,ATYPE        * clear vars
.
WADM9999  RETURN
+
.
.********************************************************************** 
. WCAN0000 : When was patient Cancelled/Removed from Wlist?
. returns  : WHENCANC = 0 (Not cancelled)
.                     = 1 (cancelled before period)
.                     = 2 (cancelled during period)
.                     = 3 (cancelled after period)
.********************************************************************** 
WCAN0000  MOVE      "0",WHENCANC
.
          COMPARE   "6",WMSTAT
          GOTO      WCAN9999 IF NOT EQUAL    * not cancelled
.
          MATCH     SP8,WMDATE4
          IF        @EQUAL
            DISPLAY   *P1:24,*EL,*B,"U/R ",WMURNO:
                                    " has no Waiting list cancellation date. ";
            CALL      HITENTER
            GOTO      WCAN9999
          ENDIF
.
          REP       " 0",WMDATE4             * no spaces!
.
          MATCH     DRGFDTE,WMDATE4
          GOTO      WCAN9100 IF LESS
.
          MATCH     WMDATE4,DRGTDTE
          GOTO      WCAN9200 IF NOT LESS
.
          MOVE      "3",WHENCANC             * cancelled after period
          GOTO      WCAN9999
.
WCAN9100  MOVE      "1",WHENCANC             * cancelled before period
          GOTO      WCAN9999
.
WCAN9200  MOVE      "2",WHENCANC             * cancelled during period
          GOTO      WCAN9999
.
WCAN9999  RETURN
+
.
.********************************************************************** 
. WBCA0000 : When was Booking Cancelled?
. returns  : WHENBCAN = 0 (Not cancelled)
.                     = 1 (cancelled before period)
.                     = 2 (cancelled during period)
.                     = 3 (cancelled after period)
.********************************************************************** 
WBCA0000  MOVE      "0",WHENBCAN
          MOVE      "0",PUTBACK
.
.--------  Check Category change file to see if booking cancelled.
.
          PACK      SAVKEY21,WMURNO,WMCODE,WMCNT,CODEBC,SP30
          PACK      KEY29,SAVKEY21,SP30
          CALL      RSWTCAT2                 * positional read
WBCA1000  CALL      RKWTCAT2                 * read next
          BRANCH    OVRCD,WBCA9999
.
          PACK      KEY21,WTCAURNO,WTCAPROC,WTCAPCNT,WTCACATF,SP30
.
          MATCH     SAVKEY21,KEY21           * same key?
          GOTO      WBCA9999 IF NOT EQUAL    * no, not cancelled
.
          MATCH     DRGFDTE,WTCAEDAT         * cancelled before period?
          GOTO      WBCA9100 IF LESS         * yes
.
          MATCH     WTCAEDAT,DRGTDTE         * cancelled during period?
          GOTO      WBCA9200 IF NOT LESS     * yes
.
          MOVE      "3",WHENBCAN             * booking cancelled after period
          GOTO      WBCA9900
.
WBCA9100  MOVE      "1",WHENBCAN             * booking cancelled before period
          GOTO      WBCA9900
.
WBCA9200  MOVE      "2",WHENBCAN             * booking cancelled during period
          GOTO      WBCA9900
.
WBCA9900  MOVE      "1",PUTBACK              * assume put back on wlist
          IF        WMSTAT=6
            MATCH     WMDATE4,WTCAEDAT       * same cancellation date?
            IF        @EQUAL
              MOVE      "0",PUTBACK          * patient NOT put back on w/list
            ELSE
              MOVE      "1",PUTBACK          * patient was put back on wlist
            ENDIF
          ENDIF
.
          IF        PUTBACK=1
            MOVE      WHENBCAN,WHENBACK      * date cancelled=date putback
          ELSE
            MOVE      "0",WHENBACK
          ENDIF
.
WBCA9999  RETURN
+
.
.********************************************************************** 
. WAIT0000 : Handle Waiting/Delayed Patients
.********************************************************************** 
WAIT0000  
          IF        WHENWAIT=3
            GOTO      WAIT9999               * put on list after period
          ENDIF
.
.-------- Check if booked
.
          IF        WHENBOOK=1 | WHENBOOK=2
            IF        WHENBCAN=0 | WHENBCAN=3
              GOTO      WAIT9999             * was booked at end of period.
            ELSE
              COMPARE     "1",PUTBACK        * put back on wlist?
              GOTO        WAIT9999 IF NOT EQUAL  * no
            ENDIF
          ENDIF
.
.
.------ Patient status at end of period was not booked,
.       now check they were admitted directly or cancelled from wlist
.       before end of period.
.
          IF        WHENADMI=1 | WHENADMI=2
            GOTO      WAIT9999               * admitted before or during period
          ENDIF
.
          IF        WHENCANC=1 | WHENCANC=2
            GOTO      WAIT9999               * cancelled before or during period
          ENDIF
.
.
.=========================================================== 
. Patient had a status of waiting/delayed at end of period.
.=========================================================== 
.
.-------- Do the stats for Column D
          IF        COLDFLAG=1
            MOVE      ZERO,TEMP1
            MOVE      ZERO,TEMP2
            MOVE      ZERO,TEMP3
            ADD       "1",YDTOTAL
            STORE     ONE,PRIORITY,TEMP1,TEMP2,TEMP3
            ADD       TEMP1,YDCAT1
            ADD       TEMP2,YDCAT2
            ADD       TEMP3,YDCAT3
            GOTO      WAIT9999
          ENDIF
.
          ADD       "1",C1TOTAL              * Total waiting/delayed
.
          MOVE      "1",GINSFLAG             * set flag for a waiting list
          CALL      GINS0000                 * get insurance status
          IF        INSUFLAG=1
            ADD       "1",C1NONIN            * non-insured waiting/del. patients
          ELSE
            ADD       "1",C1INSUR            * insured waiting/delayed patients
          ENDIF
.
          IF        SURGFLAG=1
            CALL      COLB0000               * Handle column B
          ELSE
            ADD       "1",A1WAIT             * medicine waiting/delayed
          ENDIF
.
WAIT9999  RETURN
+
.
.********************************************************************** 
. BOOK0000 : Handle Booked Patients
.********************************************************************** 
BOOK0000  
          IF        WHENBOOK=0 | WHENBOOK=3
            GOTO      BOOK9999               * not booked or booked after period
          ENDIF
.
.------ Patient was booked before or during period,
.       now check if still booked at end of period.
.
          IF        WHENBCAN=1 | WHENBCAN=2
            GOTO      BOOK9999               * booking cancelled 
          ENDIF
.
          IF        WHENADMI=1 | WHENADMI=2
            GOTO      BOOK9999               * admitted before or during period
          ENDIF
.
          IF        WHENCANC=1 | WHENCANC=2
            GOTO      BOOK9999               * wlist cancelled 
          ENDIF
.
.================================================== 
. Patient had a status of booked at end of period.
.================================================== 
.
          ADD       "1",A2TOTAL              * Total booked
.
          MOVE      "2",GINSFLAG             * set flag for a booking
          CALL      GINS0000                 * get insurance status
          IF        INSUFLAG=1
            ADD       "1",A2NONIN            * non-insured booked patients
          ELSE
            ADD       "1",A2INSUR            * insured booked patients
          ENDIF
.
          MOVE      "1",GDAYFLAG             * set flag for a booking
          CALL      GDAY0000                 * Get daypatint/inpatient status
          IF        DAYPFLAG=1
            IF        SURGFLAG=1
              ADD       "1",XADAYP           * Surgical booked daypatients
            ELSE
              ADD       "1",A1DAYP           * Medicine booked daypatients
            ENDIF
          ELSE
            IF        SURGFLAG=1
              ADD       "1",XAINP            * Surgical booked inpatients
            ELSE
              ADD       "1",A1INP            * Medicine booked inpatients
            ENDIF
          ENDIF
.
BOOK9999  RETURN
+
.
.********************************************************************** 
. COLE0000 : Calculate Column E 
.********************************************************************** 
COLE0000  MOVE      ZERO,TEMP1
          MOVE      ZERO,TEMP2
          MOVE      ZERO,TEMP3
.
.-------- Patients Booked during the period.
.
          IF        WHENBOOK=2 
            IF        WHENBCAN=2 & PUTBACK=1
              GOTO      COLE9999             * Booking Cancelled!
            ENDIF
            ADD       "1",YETOTAL            * total
            STORE     ONE,PRIORITY,TEMP1,TEMP2,TEMP3
            ADD       TEMP1,YECAT1           
            ADD       TEMP2,YECAT2
            ADD       TEMP3,YECAT3
          ENDIF
.
COLE9999  RETURN
+
.
.********************************************************************** 
. COLF0000 : Calculate Column F
.********************************************************************** 
COLF0000  MOVE      ZERO,TEMP1
          MOVE      ZERO,TEMP2
          MOVE      ZERO,TEMP3
.
.--------  Admitted Directly during period (Without a Booking)
.
          IF        WHENBOOK=0 & WHENADMI=2
            ADD       "1",YFTOTAL            * total
            STORE     ONE,PRIORITY,TEMP1,TEMP2,TEMP3
            ADD       TEMP1,YFCAT1
            ADD       TEMP2,YFCAT2
            ADD       TEMP3,YFCAT3
          ENDIF
.
COLF9999  RETURN
+
.
.********************************************************************** 
. COF10000 : Calculate Column F1
.********************************************************************** 
COF10000  
.
.-------- Get only Patients Admitted during Period
.
          IF        WHENADMI<>2
            GOTO      COF19999               * not admitted during period.
          ENDIF
.
          ADD       "1",F1TOTAL              * Total
.
          MOVE      "3",GINSFLAG             * set flag for a admissions
          CALL      GINS0000                 * get insurance status
          IF        INSUFLAG=1
            ADD       "1",F1PUB              * public
          ELSE
            ADD       "1",F1PRIV             * private
          ENDIF
.
COF19999  RETURN
+
.
.********************************************************************** 
. COLG0000 : Calculate Column G
.********************************************************************** 
COLG0000  MOVE      ZERO,TEMP1
          MOVE      ZERO,TEMP2
          MOVE      ZERO,TEMP3
.
.-------- See if booked at the end of the period.
.
          IF        WHENBOOK=1 | WHENBOOK=2
            IF        WHENBCAN=0 | WHENBCAN=3
              GOTO      COLG9999             * still booked
            ELSE
              COMPARE   "1",PUTBACK        
              GOTO      COLG9999 IF NOT EQUAL  * not put back on wlist
            ENDIF
          ENDIF
.
.--------  Not booked, see if cancelled from W/List
.
          IF        WHENCANC=2 
            ADD       "1",YGTOTAL
            STORE     ONE,PRIORITY,TEMP1,TEMP2,TEMP3
            ADD       TEMP1,YGCAT1
            ADD       TEMP2,YGCAT2
            ADD       TEMP3,YGCAT3
          ENDIF
.
COLG9999  RETURN
+
.
.********************************************************************** 
. COLH0000 : Calculate Column H
.********************************************************************** 
COLH0000  
.
.-------- Do it this way so we pick up people added to the waiting list
.         during the period (WHENWAIT=2) and also those bookings who were 
.         cancelled and then returned to the waiting list.
.
          ASSIGN(YICAT1-YDCAT1+YECAT1+YFCAT1+YGCAT1),YHCAT1
          ASSIGN(YICAT2-YDCAT2+YECAT2+YFCAT2+YGCAT2),YHCAT2
          ASSIGN(YICAT3-YDCAT3+YECAT3+YFCAT3+YGCAT3),YHCAT3
.
          ASSIGN(YHCAT1+YHCAT2+YHCAT3),YHTOTAL
.
COLH9999  RETURN
+
.
.********************************************************************** 
. COLJ0000 : Calculate Column J
.********************************************************************** 
COLJ0000  MOVE      ZERO,TEMP1
          MOVE      ZERO,TEMP2
          MOVE      ZERO,TEMP3
.
.-------- Get only Patients Admitted during Period
.
          IF        WHENADMI<>2
            GOTO      COLJ9999               * not admitted during period.
          ENDIF
.
          ADD       "1",YJTOTAL              * total
.
          MOVE      "2",GDAYFLAG             * get from admissions
          CALL      GDAY0000
          IF        DAYPFLAG=1
            ADD       "1",YJDAYP
          ELSE
            STORE     ONE,PRIORITY,TEMP1,TEMP2,TEMP3
            ADD       TEMP1,YJCAT1
            ADD       TEMP2,YJCAT2
            ADD       TEMP3,YJCAT3
          ENDIF
.
COLJ9999  RETURN
+
.
.********************************************************************** 
. COLD0000 : Calculate Column D
. NB: THIS SHOULD BE THE LAST ROUTINE CALLED BECAUSE IT 
.     CHANGES FLAGS TO USE THE PREVIOUS PERIOD
.********************************************************************** 
COLD0000
.-------- Column D = Column C of previous period
.
          CALL      GPRE0000                 * get previous period.
.
          CALL      WWAT0000                 * when was patient put on Wlist?
          CALL      WBOK0000                 * when was patient booked?
          CALL      WADM0000                 * when was patient admitted?
          CALL      WCAN0000                 * when was patient cancelled? 
.
          MOVE      "1",COLDFLAG             * Doing Column D
          CALL      WAIT0000                 
.
          MOVE      DATEFR8,DRGFDTE          * restore from date
          MOVE      DATETO8,DRGTDTE          * restore to date
.
COLD9999  RETURN
+
.
.********************************************************************** 
. ADMI0000 : handle admission details
.********************************************************************** 
ADMI0000  UNPACK    DRGFDTE,FRDATE8
          UNPACK    DRGTDTE,TODATE8
          PACK      KEY16,FRDATE8,SP20       * set up with from date
          CALL      RDSMISS3                 * pos
ADMI1000  CALL      RDKMISS3                 * next
          BRANCH    OVRCD,ADMI9999
.
.-------- Check dates on admission file
.
          MATCH     ADATE,TODATE8
          GOTO      ADMI9999 IF LESS         * past end of period
.
.
.--------  Read transfer file to make sure they were admitted
.
          PACK      KEY30,AADMNO,SP20,SP20
          CALL      RDSTRAN2
          CALL      RDKTRAN2                 * read A record
.
          MATCH     AADMNO,TADMN             * same admission no.?
          GOTO      ADMI1000 IF NOT EQUAL    * no, not admitted
.
.-------- We have an admission!
.
          MATCH     ADATE,SP8
          IF        @EQUAL
            DISPLAY   *P1:24,*EL,*B,"Admission : ",AADMNO," has no admission ":
                        "date. ";
            CALL      HITENTER
            GOTO      ADMI1000
          ENDIF
.
.-------- Check if a Day procedure admission (waiting list & non-waiting list)
.
          MATCH     TWARD,WTCNDPWD           * day procedure ward?
          IF        @EQUAL
            ADD       "1",NTOTAL             * Day procedure admission (N)
          ENDIF
.
.-------- Make sure NOT on waiting list
.        
          PACK      KEY19,AURNO,SP20
          CALL      RDSTREA1
ADMI1500  CALL      RDKTREA1
.
          MATCH     AURNO,WMURNO
          GOTO      ADMI2000 IF NOT EQUAL    * not on W/List, proceed
.
          MATCH     WMBOOK,AADMNO
          GOTO      ADMI1000 IF EQUAL        * ignore W/list patients
.
          GOTO      ADMI1500                 * get next record
.
.
.-------- Check if an emergency or other admission
.
ADMI2000  PACK      KEY5,ANSA,SP1,ATYPE      * admission
          CALL      RDCODE1
          BRANCH    OVRCD,ADMI6000
          MATCH     "S",TCINDC3
          GOTO      ADMI6000 IF NOT EQUAL
.
          ADD       "1",KTOTAL               * emergency surgical admission
          GOTO      ADMI7000
.
ADMI6000  ADD       "1",LTOTAL               * medical or other admission
.
ADMI7000  GOTO      ADMI1000
.
ADMI9999  RETURN
+
.
.********************************************************************** 
. ADDX0000 : Add the X vars to total
.********************************************************************** 
ADDX0000
          ADD       XADAYP,TXADAYP
          ADD       XAINP,TXAINP     
          ADD       XB1CAT1,TXB1CAT1  
          ADD       XB1CAT2,TXB1CAT2  
          ADD       XB1CAT3,TXB1CAT3  
          ADD       XB1TOTAL,TXB1TOTA
          ADD       XB2CAT1,TXB2CAT1  
          ADD       XB2CAT2,TXB2CAT2  
          ADD       XB2CAT3,TXB2CAT3  
          ADD       XB2TOTAL,TXB2TOTA 
          ADD       XB3CAT1,TXB3CAT1  
          ADD       XB3CAT2,TXB3CAT2  
          ADD       XB3CAT3,TXB3CAT3  
          ADD       XB3TOTAL,TXB3TOTA
          ADD       XB4CAT1,TXB4CAT1   
          ADD       XB4CAT2,TXB4CAT2   
          ADD       XB4CAT3,TXB4CAT3  
          ADD       XB4TOTAL,TXB4TOTA 
          ADD       XCCAT1,TXCCAT1   
          ADD       XCCAT2,TXCCAT2   
          ADD       XCCAT3,TXCCAT3    
          ADD       XCTOTAL,TXCTOTAL  
.
ADDX9999  RETURN
+
.
.********************************************************************** 
. ADDY0000 : Add the Y vars to total
.********************************************************************** 
ADDY0000
          ADD       YDCAT1,TYDCAT1    
          ADD       YDCAT2,TYDCAT2   
          ADD       YDCAT3,TYDCAT3  
          ADD       YDTOTAL,TYDTOTAL   
          ADD       YECAT1,TYECAT1    
          ADD       YECAT2,TYECAT2    
          ADD       YECAT3,TYECAT3   
          ADD       YETOTAL,TYETOTAL 
          ADD       YFCAT1,TYFCAT1  
          ADD       YFCAT2,TYFCAT2  
          ADD       YFCAT3,TYFCAT3  
          ADD       YFTOTAL,TYFTOTAL  
          ADD       YGTOTAL,TYGTOTAL   
          ADD       YHCAT1,TYHCAT1  
          ADD       YHCAT2,TYHCAT2  
          ADD       YHCAT3,TYHCAT3   
          ADD       YHTOTAL,TYHTOTAL  
          ADD       YICAT1,TYICAT1   
          ADD       YICAT2,TYICAT2   
          ADD       YICAT3,TYICAT3   
          ADD       YITOTAL,TYITOTAL  
          ADD       YJCAT1,TYJCAT1  
          ADD       YJCAT2,TYJCAT2  
          ADD       YJCAT3,TYJCAT3  
          ADD       YJDAYP,TYJDAYP  
          ADD       YJTOTAL,TYJTOTAL  
.
ADDY9999  RETURN
+
.
.********************************************************************** 
. CTOT0000 : clear total vars
.********************************************************************** 
CTOT0000  MOVE      ZERO,TXADAYP    
          MOVE      ZERO,TXAINP    
          MOVE      ZERO,TXB1CAT1  
          MOVE      ZERO,TXB1CAT2  
          MOVE      ZERO,TXB1CAT3  
          MOVE      ZERO,TXB1TOTA  
          MOVE      ZERO,TXB2CAT1  
          MOVE      ZERO,TXB2CAT2  
          MOVE      ZERO,TXB2CAT3  
          MOVE      ZERO,TXB2TOTA 
          MOVE      ZERO,TXB3CAT1  
          MOVE      ZERO,TXB3CAT2  
          MOVE      ZERO,TXB3CAT3  
          MOVE      ZERO,TXB3TOTA 
          MOVE      ZERO,TXB4CAT1  
          MOVE      ZERO,TXB4CAT2  
          MOVE      ZERO,TXB4CAT3  
          MOVE      ZERO,TXB4TOTA 
          MOVE      ZERO,TXCCAT1   
          MOVE      ZERO,TXCCAT2   
          MOVE      ZERO,TXCCAT3    
          MOVE      ZERO,TXCTOTAL  
.
          MOVE      ZERO,TYDCAT1    
          MOVE      ZERO,TYDCAT2   
          MOVE      ZERO,TYDCAT3  
          MOVE      ZERO,TYDTOTAL   
          MOVE      ZERO,TYECAT1    
          MOVE      ZERO,TYECAT2    
          MOVE      ZERO,TYECAT3   
          MOVE      ZERO,TYETOTAL 
          MOVE      ZERO,TYFCAT1  
          MOVE      ZERO,TYFCAT2  
          MOVE      ZERO,TYFCAT3  
          MOVE      ZERO,TYFTOTAL 
          MOVE      ZERO,TYGTOTAL  
          MOVE      ZERO,TYHCAT1  
          MOVE      ZERO,TYHCAT2  
          MOVE      ZERO,TYHCAT3   
          MOVE      ZERO,TYHTOTAL  
          MOVE      ZERO,TYICAT1   
          MOVE      ZERO,TYICAT2   
          MOVE      ZERO,TYICAT3   
          MOVE      ZERO,TYITOTAL  
          MOVE      ZERO,TYJCAT1  
          MOVE      ZERO,TYJCAT2  
          MOVE      ZERO,TYJCAT3  
          MOVE      ZERO,TYJDAYP  
          MOVE      ZERO,TYJTOTAL  
.
CTOT9999  RETURN
+
.
.********************************************************************** 
. COLM0000 : column M
.********************************************************************** 
COLM0000  
          ASSIGN    (TYJTOTAL + KTOTAL + LTOTAL),MTOTAL
.
COLM9999  RETURN
+
.
.********************************************************************** 
. CTMP0000 : clear temp file data
.********************************************************************** 
CTMP0000  MOVE      ZERO,XADAYP    
          MOVE      ZERO,XAINP    
          MOVE      ZERO,XB1CAT1  
          MOVE      ZERO,XB1CAT2  
          MOVE      ZERO,XB1CAT3  
          MOVE      ZERO,XB1TOTAL 
          MOVE      ZERO,XB2CAT1  
          MOVE      ZERO,XB2CAT2  
          MOVE      ZERO,XB2CAT3  
          MOVE      ZERO,XB2TOTAL 
          MOVE      ZERO,XB3CAT1  
          MOVE      ZERO,XB3CAT2  
          MOVE      ZERO,XB3CAT3  
          MOVE      ZERO,XB3TOTAL 
          MOVE      ZERO,XB4CAT1  
          MOVE      ZERO,XB4CAT2  
          MOVE      ZERO,XB4CAT3  
          MOVE      ZERO,XB4TOTAL 
          MOVE      ZERO,XCCAT1   
          MOVE      ZERO,XCCAT2   
          MOVE      ZERO,XCCAT3    
          MOVE      ZERO,XCTOTAL  
.
          MOVE      ZERO,YDCAT1    
          MOVE      ZERO,YDCAT2   
          MOVE      ZERO,YDCAT3  
          MOVE      ZERO,YDTOTAL   
          MOVE      ZERO,YECAT1    
          MOVE      ZERO,YECAT2    
          MOVE      ZERO,YECAT3   
          MOVE      ZERO,YETOTAL 
          MOVE      ZERO,YFCAT1  
          MOVE      ZERO,YFCAT2  
          MOVE      ZERO,YFCAT3  
          MOVE      ZERO,YFTOTAL 
          MOVE      ZERO,YGCAT1
          MOVE      ZERO,YGCAT2
          MOVE      ZERO,YGCAT3
          MOVE      ZERO,YGTOTAL  
          MOVE      ZERO,YHCAT1  
          MOVE      ZERO,YHCAT2  
          MOVE      ZERO,YHCAT3   
          MOVE      ZERO,YHTOTAL  
          MOVE      ZERO,YICAT1   
          MOVE      ZERO,YICAT2   
          MOVE      ZERO,YICAT3   
          MOVE      ZERO,YITOTAL  
          MOVE      ZERO,YJCAT1  
          MOVE      ZERO,YJCAT2  
          MOVE      ZERO,YJCAT3  
          MOVE      ZERO,YJDAYP  
          MOVE      ZERO,YJTOTAL  
.
CTMP9999  RETURN
+
.
.********************************************************************** 
. COTH0000  : clear other data
.********************************************************************** 
COTH0000  MOVE      ZERO,A1DAYP  
          MOVE      ZERO,A1INP  
          MOVE      ZERO,A1WAIT   
          MOVE      ZERO,A2INSUR   
          MOVE      ZERO,A2NONIN   
          MOVE      ZERO,A2TOTAL   
          MOVE      ZERO,C1INSUR  
          MOVE      ZERO,C1NONIN 
          MOVE      ZERO,C1TOTAL 
          MOVE      ZERO,F1PUB  
          MOVE      ZERO,F1PRIV  
          MOVE      ZERO,F1TOTAL 
          MOVE      ZERO,KTOTAL 
          MOVE      ZERO,LTOTAL   
          MOVE      ZERO,MTOTAL  
          MOVE      ZERO,NTOTAL 
.
COTH9999  RETURN
+
.
.********************************************************************** 
. COLB0000 : Handle Column B
.********************************************************************** 
COLB0000  MOVE      "0",TEMP1
          MOVE      "0",TEMP2
          MOVE      "0",TEMP3
.
          MOVE      WMDATE1,CDYSFDTE         * set from date as date put on WL
          MOVE      DRGTDTE,CDYSTDTE         * set to date as end of last period
          CALL      CALCDAYS                 * calculate # of days
.
.-------- Range 1
.
          IF        CDYSDAYS <= RANGE1
            ADD       "1",XB1TOTAL
            STORE     ONE,PRIORITY,TEMP1,TEMP2,TEMP3
            ADD       TEMP1,XB1CAT1  
            ADD       TEMP2,XB1CAT2  
            ADD       TEMP3,XB1CAT3  
            GOTO      COLB5000
          ENDIF
.       
.-------- Range 2
.
          IF        CDYSDAYS <= RANGE2
            ADD       "1",XB2TOTAL
            STORE     ONE,PRIORITY,TEMP1,TEMP2,TEMP3
            ADD       TEMP1,XB2CAT1  
            ADD       TEMP2,XB2CAT2  
            ADD       TEMP3,XB2CAT3  
            GOTO      COLB5000
          ENDIF
.       
.-------- Range 3
.
          IF        CDYSDAYS <= RANGE3
            ADD       "1",XB3TOTAL
            STORE     ONE,PRIORITY,TEMP1,TEMP2,TEMP3
            ADD       TEMP1,XB3CAT1  
            ADD       TEMP2,XB3CAT2  
            ADD       TEMP3,XB3CAT3  
            GOTO      COLB5000
          ENDIF
.       
.-------- Range 4
.
          STORE     ONE,PRIORITY,TEMP1,TEMP2,TEMP3
          ADD       "1",XB4TOTAL
          ADD       TEMP1,XB4CAT1  
          ADD       TEMP2,XB4CAT2  
          ADD       TEMP3,XB4CAT3  
.
.-------- Totals
.       
COLB5000  MOVE      "0",TEMP1
          MOVE      "0",TEMP2
          MOVE      "0",TEMP3
          STORE     ONE,PRIORITY,TEMP1,TEMP2,TEMP3
          ADD       "1",XCTOTAL
          ADD       TEMP1,XCCAT1
          ADD       TEMP2,XCCAT2
          ADD       TEMP3,XCCAT3
.
.-------- Column I=Column C
.
          ADD       "1",YITOTAL
          ADD       TEMP1,YICAT1
          ADD       TEMP2,YICAT2
          ADD       TEMP3,YICAT3
.
COLB9999  RETURN
+
.
.********************************************************************** 
. GDAY0000   : Get Daypatient Status
. parameters : GDAYFLAG = 1 (get from booking) 
.                       = 2 (get from admission)
. returns    : DAYPFLAG = 1 (public)
.                       = 2 (private)
.********************************************************************** 
GDAY0000  BRANCH    GDAYFLAG,GDAY1000:       * get from booking
                             GDAY2000        * get from admission
.
.-------- Get Daypatient status from booking
.
GDAY1000  PACK      KEY8,WMBOOK,SP10
          CALL      RDBKREC1                 * read the booking file
          BRANCH    OVRCD,GDAY9100           * not booked
          PACK      KEY5,ANSA,SP1,BKREATYP
          MOVE      "1",GDAYFLAG
          GOTO      GDAY5000
.
.-------- Get Daypatient status from admission
.
GDAY2000  PACK      KEY8,WMBOOK,SP10
          CALL      RDMISS1                  * read admission record
          BRANCH    OVRCD,GDAY2000           * not admitted
          PACK      KEY5,ANSA,SP1,ATYPE  
          MOVE      "2",GDAYFLAG
          GOTO      GDAY5000
.
GDAY5000  CALL      RDCODE1
          BRANCH    OVRCD,GDAY5100
          MATCH     SP3,ACODE
          GOTO      GDAY5100 IF EQUAL
.
          GOTO      GDAY6000               * everything ok
.
GDAY5100  BRANCH    GDAYFLAG,GDAY9100:     * not on bokrc1af, assume inpatient
                             GDAY1000      * not on patmi1af, try bokrc1af
.
GDAY6000  MATCH     "1",TCINDC2
          GOTO      GDAY9200 IF EQUAL
.
GDAY9100  MOVE      "2",DAYPFLAG             * inpatient
          GOTO      GDAY9999
.
GDAY9200  MOVE      "1",DAYPFLAG             * daypatient
          GOTO      GDAY9999
.
GDAY9999  RETURN
+
.
.********************************************************************** 
. GINS0000   : Get Insurance Status
. parameters : GINSFLAG = 1 (get from waiting list)
.                       = 2 (get from booking) 
.                       = 3 (get from admission)
. returns    : INSUFLAG = 1 (public)
.                       = 2 (private)
.********************************************************************** 
GINS0000  BRANCH    GINSFLAG,GINS1000:       * get from waiting list
                             GINS2000:       * get from booking
                             GINS3000        * get from admission
.
.-------- Claim Type from waiting list
.
GINS1000  PACK      KEY5,ANSC,ANSL,WTWMACAT  * waiting list
          MOVE      "1",GINSFLAG
          GOTO      GINS5000
.
.-------- Claim type from Bookings
.
GINS2000  PACK      KEY8,WMBOOK,SP10
          CALL      RDBKREC1                 * read the booking file
          BRANCH    OVRCD,GINS1000           * not booked
          PACK      KEY5,ANSC,ANSL,BKRECLMT 
          MOVE      "2",GINSFLAG
          GOTO      GINS5000
.
.-------- Claim Type from admissions
.
GINS3000  PACK      KEY8,WMBOOK,SP10
          CALL      RDMISS1                  * read admission record
          BRANCH    OVRCD,GINS2000           * not admitted
          PACK      KEY5,ANSC,ANSL,ACLAIM    
          MOVE      "3",GINSFLAG
          GOTO      GINS5000
        
GINS5000  CALL      RDCODE1
          BRANCH    OVRCD,GINS5100
          MATCH     SP3,ACODE
          GOTO      GINS5100 IF EQUAL
.
          GOTO      GINS6000               * everything ok
.
GINS5100  BRANCH    GINSFLAG,GINS9100:     * not on wattr1af, assume public
                             GINS1000:     * not on bokrc1af, try wattr1af
                             GINS2000      * not on patmi1af, try bokrc1af
.
GINS6000  MATCH     "P",TCINDC1
          GOTO      GINS9200 IF EQUAL
.
          MATCH     "P",TCINDC2
          GOTO      GINS9200 IF EQUAL
.
          MATCH     "P",TCINDC3
          GOTO      GINS9200 IF EQUAL
.
          MATCH     "P",TCINDC4
          GOTO      GINS9200 IF EQUAL
.
          MATCH     "P",TCINDC5
          GOTO      GINS9200 IF EQUAL
.
GINS9100  MOVE      "1",INSUFLAG             * public
          GOTO      GINS9999
.
GINS9200  MOVE      "2",INSUFLAG             * private
          GOTO      GINS9999
.
GINS9999  RETURN
+
.
.*********************************************************************
.* REPT0000 : Loop over temp files to produce the report            
.*********************************************************************
REPT0000  MOVE      ZERO,CPAGENO            * clear page counter
          MOVE      "0",CNOUNDLN            * print underline
          CALL      CTOT0000                * Clear the total vars
.
          DISPLAY   *P1:13,*EL,"Printing..."
.
.         print Section 1 of the report
.
          CALL      PGEA0000                * print page heading
          MOVE      SP20,KEY13
          CALL      RSTEMPA                 * positional read
.
REPT1000  CALL      RKTEMPA
          BRANCH    OVRCD,REPT2000          * finished temp file A
.
          CALL      PSCA0000                * print line of report - Section 1
          CALL      ADDX0000                * add vars for total line A
          GOTO      REPT1000
.
REPT2000  CALL      TOTA0000                * total line for page 1
          CALL      MEDA0000                * medicine & other details (page 1)
.
.         print Section 2 of the report
.
REPT5000  CALL      PGEB0000                * print page heading
          MOVE      SP30,KEY23
          CALL      RSTEMPB
.
REPT6000  CALL      RKTEMPB
          BRANCH    OVRCD,REPT7000          * end of file
.
          CALL      PSCB0000                * print line of report - Section 2
          CALL      ADDY0000                * add vars for total line Y
          GOTO      REPT6000
.
REPT7000  CALL      TOTB0000                * total line for page 2
          CALL      COLM0000                * Calculate column M now!
          CALL      MEDB0000                * medicine & other details (page 2)
.
REPT8000  PRINT     *N,*N,"*** END OF REPORT ***",*N
.
REPT9999  RETURN
+
.
.*********************************************************************
.*                  PGEA0000                    Called by : PSCA0000 *
.*        Print the page heading for Section 1                       *
.*********************************************************************
PGEA0000  CALL      HEAD132
          PRINT     *40,"Report Period : ",PERIOD7,SP5,DESCPERD
          PRINT     *68,DATEFR10," to ",DATETO10
          PRINT     *1,"Hospital Code : ",CHOSINDX
          PRINT     *1,"*---------------------------------------":
                       "----------------------------------------":
                       "----------------------------------------":
                       "-----------*"
          PRINT     *1,"|",*12,"|---Booked--|       0 - 30";
          PRINT     *45,"|     31 - 90";
          PRINT     *66,"|",*72,"91 - 180";
          PRINT     *87,"|",*96,"181+",*108,"|":
                    *118,"Total",*132,"|"
          PRINT     *1,"|",*12,"| Day | In- |":
                    "--------------------|--------------------|":
                    "--------------------|--------------------|":
                    "-----------------------|"
          PRINT     *1,"|Speciality|Patnt|Patnt|":
                    "  1 |  2 |  3 | Tot |  1 |  2 |  3 | Tot |":
                    "  1 |  2 |  3 | Tot |  1 |  2 |  3 | Tot |":
                    "  1  |  2  |  3  | Tot |"
          PRINT     *1,"*---------------------------------------":
                       "----------------------------------------":
                       "----------------------------------------":
                       "-----------*"
.
          MOVE      "11",CLNO               * set line number
.
PGEA9999  RETURN
+
.
.*********************************************************************
.*                  PSCA0000                    Called by : REPT1000 *
.*        Print a line of the report - Section 1                     *
.*********************************************************************
PSCA0000  COMPARE   "55",CLNO
          CALL      PGEA0000 IF NOT LESS    * print heading if needed
.
          PRINT     *1,"|",XSPDESC,"|",XADAYP,"|",XAINP,"|":
                    XB1CAT1,"|",XB1CAT2,"|",XB1CAT3,"|",XB1TOTAL,"|":
                    XB2CAT1,"|",XB2CAT2,"|",XB2CAT3,"|",XB2TOTAL,"|":
                    XB3CAT1,"|",XB3CAT2,"|",XB3CAT3,"|",XB3TOTAL,"|":
                    XB4CAT1,"|",XB4CAT2,"|",XB4CAT3,"|",XB4TOTAL,"|":
                    XCCAT1,"|",XCCAT2,"|",XCCAT3,"|",XCTOTAL,"|"
.
          ADD       "1",CLNO
.
PSCA9999  RETURN
+
.
.*********************************************************************
. TOTA0000 : Total line for Page 1
.*********************************************************************
TOTA0000  CALL      UND132
          PRINT     *1,"|TOTAL     |",TXADAYP,"|",TXAINP,"|":
                    TXB1CAT1,"|",TXB1CAT2,"|",TXB1CAT3,"|",TXB1TOTA,"|":
                    TXB2CAT1,"|",TXB2CAT2,"|",TXB2CAT3,"|",TXB2TOTA,"|":
                    TXB3CAT1,"|",TXB3CAT2,"|",TXB3CAT3,"|",TXB3TOTA,"|":
                    TXB4CAT1,"|",TXB4CAT2,"|",TXB4CAT3,"|",TXB4TOTA,"|":
                    TXCCAT1,"|",TXCCAT2,"|",TXCCAT3,"|",TXCTOTAL,"|"
          ADD       ONE,CLNO
          CALL      UND132
.
TOTA9999  RETURN
+
.
.********************************************************************** 
. MEDA0000 : Print Medicine line for Page 1 of report
.********************************************************************** 
MEDA0000
          PRINT     *N,*24,"Booked",*33,"Booked",*N:
                    *24,"Day",*33,"In-",*42,"Waiting/",*76,"Non-",*117:
                    "Non-",*N:
                    *24,"Patients",*33,"Patients",*42,"Delayed",*68,"Insured":
                    *76,"Insured",*84,"Total",*109,"Insured",*117,"Insured":
                    *125,"Total",*N:
                    *24,"*-------------------------*":
                    *67,"*-----------------------*":
                    *98,"Waiting/",*108,"*-----------------------*",*N:
                    *24,PIPE,*26,A1DAYP,*32,PIPE,*35,A1INP,*41,PIPE:
                    *44,A1WAIT,*50,PIPE:
                    *57,"Booked",*67,PIPE,*69,A2INSUR,*75,PIPE,*77,A2NONIN:
                    *83,PIPE,*85,A2TOTAL,*91,PIPE,*98,"Delayed",*108,PIPE:
                    *110,C1INSUR,*116,PIPE,*118,C1NONIN,*124,PIPE:
                    *126,C1TOTAL,*132,PIPE,*N:
                    *14,"Medicine:",*24,"*-------------------------*":
                    *57,"Patients:",*67,"*-----------------------*":
                    *98,"Patients:",*108,"*-----------------------*"
.
MEDA9999  RETURN
+
.
.*********************************************************************
. TOTB0000 : Total line for Page 2
.*********************************************************************
TOTB0000  
          CALL      UND132
          PRINT     *1,"|TOTAL               |":
                    TYDCAT1,"|",TYDCAT2,"|",TYDCAT3,"|",TYDTOTAL:
                    "|",TYECAT1,"|",TYECAT2,"|",TYECAT3,"|",TYETOTAL:
                    "|",TYFCAT1,"|",TYFCAT2,"|",TYFCAT3,"|",TYFTOTAL,"|":
                    TYGTOTAL:
                    "|",TYHCAT1,"|",TYHCAT2,"|",TYHCAT3,"|",TYHTOTAL:
                    "|",TYICAT1,"|",TYICAT2,"|",TYICAT3,"|",TYITOTAL,"|":
                    TYJCAT1,"|",TYJCAT2,"|",TYJCAT3,"|",TYJDAYP,"|",TYJTOTAL,"|"
          CALL      UND132
          ADD       ONE,CLNO
.
TOTB9999  RETURN
+
.
.*********************************************************************
.*                  PSCB0000                    Called by : REPT6000 *
.*        Print a line of the report for Section 2                   *
.*********************************************************************
PSCB0000  COMPARE   "55",CLNO
          CALL      PGEB0000 IF NOT LESS    * print heading 
.
PSCB1500  PRINT     *1,"|",YSPDESC,"|",YDCAT1,"|",YDCAT2,"|",YDCAT3,"|",YDTOTAL:
                    "|",YECAT1,"|",YECAT2,"|",YECAT3,"|",YETOTAL:
                    "|",YFCAT1,"|",YFCAT2,"|",YFCAT3,"|",YFTOTAL,"|",YGTOTAL:
                    "|",YHCAT1,"|",YHCAT2,"|",YHCAT3,"|",YHTOTAL:
                    "|",YICAT1,"|",YICAT2,"|",YICAT3,"|",YITOTAL,"|":
                    YJCAT1,"|",YJCAT2,"|",YJCAT3,"|",YJDAYP,"|",YJTOTAL,"|"
          ADD       ONE,CLNO
.
PSCB9999  RETURN
+
.
.********************************************************************** 
. MEDB0000 : Medicine line for Page 2
.********************************************************************** 
MEDB0000  
          PRINT     *90,"Emergency Surgical Admissions",*127,PIPE,KTOTAL:
                    *132,PIPE,*N:
                    *25,"Public",*34,"Private",*42,"Total":
                    *90,"Total Medical (and Other) Admissions",*127,PIPE:
                    LTOTAL,*132,PIPE,*N:
                    *24,"*------------------------*":
                    *54,"Total Day Procedure",*75,"*--------*",*90:
                    *127,"*----*",*N:
                    *2,"Patients Admitted",*24,PIPE,*26,F1PUB,*32,PIPE:
                    *35,F1PRIV,*41,PIPE,*43,F1TOTAL,*49,PIPE:
                    *54,"Admissions(Medical &",*75,PIPE,*78,NTOTAL,*84,PIPE:
                    *90,"TOTAL HOSPITAL ADMISSIONS",*127,PIPE,MTOTAL:
                    *132,PIPE,*N:
                    *2,"for Elective Surgery:",*24,"*------------------------*":
                    *54,"Surgical):",*75,"*--------*",*127,"*----*"
MEDB9999  RETURN
+
.
.*********************************************************************
.*                  PGEB0000                    Called by : PSCB0000 *
.*        Print the page heading for Section 2                       *
.*********************************************************************
PGEB0000  CALL      HEAD132
          PRINT     *40,"Report Period : ",PERIOD7,SP5,DESCPERD
          PRINT     *68,DATEFR10," to ",DATETO10
          PRINT     *1,"Hospital Code : ",CHOSINDX
.
          CALL      UND132
          PRINT     *1,"|",*22,"|",*39,"|",*56,"C H A N G E S ":
                    *70," D U R I N G  T H E  P R E V I O U S  M O N T H":
                    *132,"|"
          PRINT     *1,"|",*22,"|",*39,"|------------------------------------":
                    "------------------------------------------------------":
                    "--|"
          PRINT     *1,"|",*22,"| Total Number of|",*49,"D E L E T I O N S":
                    *77,"|    ADDITIONS",*94,"|",*111,"|",*132,"|"
          PRINT     *1,"|",*22,"| patients wait/ |---------------------------":
                    "----------|----------------|",*111,"| TOTAL ELECTIVE":
                    *132,"|"
          PRINT     *1,"|",*22,"| delayed on the | Patients Booked| Patients ":
                    "Admit.|C D| Patients newly | Total Number of| SURGERY ":
                    "ADMISSIONS |"
          PRINT     *1,"| Specialty",*22,"| 1st of the     |   previously   |":
                    " Directly -     |a e| registered as  | Waiting/Delayed|":
                    "--------------------|"
          PRINT     *1,"|",*22,"| last month     |Waiting/Delayed | Prev Wait":
                    "/Delyd|n l| Waiting/Delayed|",*111,"|Inpatients |   |    |"
          PRINT     *1,"|-------------------------------------|--------------":
                    "--|----------------|c e|----------------|-------------":
                    "---|-----------|   |    |"
          PRINT     *1,"|",*22,"|  Category      |     Category   |    Categ":
                    "ory    |e t|   Category     |   Category     | Category":
                    "  |   |    |"
          PRINT     *1,"|",*22,"|----------------|--------------":
                    "--|----------------|l e|----------------|-------------":
                    "---|-----------|   |    |"
          PRINT     *1,"|",*22,"| 1 | 2 | 3 |TOTL| 1 | 2 | 3 |TOTL| 1 | 2 | 3 ":
                    "|TOTL|  d| 1 | 2 | 3 |TOTL| 1 | 2 | 3 |TOTL| 1 | 2 | 3 |":
                    "DAY|TOTL|"
.
          ADD       "14",CLNO
          CALL      UND132
.
PGEB9999  RETURN
+
.
.*********************************************************************
.* CREA0000 : Create temp files
.*********************************************************************
CREA0000  CALL      KILL0000                * delete temp file
.
          CLEAR     CMDLINE                 * clear command line
          PACK      CMDLINE,ISBUILD,CFNAMEA,KEYA,SP30      * set command
          RESET     CMDLINE                 * reset FP
          EXECUTE   CMDLINE,TASKID          * build the temp file
          OPEN      TEMPA,CFNAMEA           * open the temp file
.
          CLEAR     CMDLINE                 * clear command line
          PACK      CMDLINE,ISBUILD,CFNAMEB,KEYB,SP30      * set command
          RESET     CMDLINE                 * reset FP
          EXECUTE   CMDLINE,TASKID          * build the temp file
          OPEN      TEMPB,CFNAMEB           * open the temp file
.
CREA9999  RETURN
+
.
.*********************************************************************
.* KILL0000 : Delete temp files      
.*********************************************************************
KILL0000  CALL      TFILENAM
          PACK      CFNAMEA,TFILNAME        * set the port dependant file name
          CALL      TFILENAM
          PACK      CFNAMEB,TFILNAME        * set the port dependant file name
.
          CLOSE     TEMPA                   * close temp file
          CLEAR     CMDLINE                 * clear command line
          PACK      CMDLINE,ISERASE,CFNAMEA * set command line for erase
          RESET     CMDLINE                 * reset FP
          EXECUTE   CMDLINE,TASKID          * delete the temp file
.
          CLOSE     TEMPB                   * close temp file
          CLEAR     CMDLINE                 * clear command line
          PACK      CMDLINE,ISERASE,CFNAMEB * set command line for erase
          RESET     CMDLINE                 * reset FP
          EXECUTE   CMDLINE,TASKID          * delete the temp file
.
KILL9999  RETURN
+
.
.********************************************************************** 
. GPRE0000   : Get Previous Period
. parameters : PERIOD6 (current period)
.********************************************************************** 
GPRE0000  PACK      KEY6,PERIOD6
          CALL      RDDRGA1
          CALL      RDPDRGA1
          IF        OVRCD=1
            DISPLAY   *P1:24,*EL,*B,"ERROR : Previous period missing ":
                                    "from date range file. ";
          ENDIF
.
GPRE9999  RETURN
+
.
.********************************************************************** 
. WRTA0000 : write to A
.********************************************************************** 
WRTA0000
          CALL      MODX0000                 * Any Mods to X values?
          BRANCH    EXIT,WRTA9999            * Mo
.
          IF        NEWA=1
            CALL      WRTEMPA
          ELSE
            CALL      UPTEMPA
          ENDIF
.
WRTA9999  RETURN
+
.
.********************************************************************** 
. MODX0000 : Check if X values where modified
. returns  : EXIT = 0 (changes where made)
.                 = 1 (no mods)
.********************************************************************** 
MODX0000  MOVE      ZERO,EXIT
.
          ASSIGN (XADAYP+XAINP+XB1TOTAL+XB2TOTAL+XB3TOTAL+XB4TOTAL+XCTOTAL),XTOT
.
          COMPARE   ZERO,XTOT
          GOTO      MODX9999 IF NOT EQUAL        * mods made
.
          MOVE      "1",EXIT                     * no mods made
.
MODX9999  RETURN
+
.
.********************************************************************** 
. WRTB0000 : write to B
.********************************************************************** 
WRTB0000
          CALL      MODY0000                 * Any Mods to Y values?
          BRANCH    EXIT,WRTB9999            * No
.
          IF        NEWB=1
            CALL      WRTEMPB
          ELSE
            CALL      UPTEMPB
          ENDIF
.
WRTB9999  RETURN
+
.
.********************************************************************** 
. MODY0000 : Check if Y values where modified
. returns  : EXIT = 0 (changes where made)
.                 = 1 (no mods)
.********************************************************************** 
MODY0000  MOVE      ZERO,EXIT
.
          ASSIGN  (YDTOTAL+YETOTAL+YFTOTAL+YGTOTAL+YHTOTAL+YITOTAL+YJTOTAL),YTOT
.
          COMPARE   ZERO,YTOT
          GOTO      MODY9999 IF NOT EQUAL        * mods made
.
          MOVE      "1",EXIT                     * no mods made
.
MODY9999  RETURN
+
.
.======================================== 
. TEMPORARY FILE I/O
.======================================== 
.
. ==================== 
.   TEMP FILE A
. ==================== 
RATEMPA   MOVE      ZERO,OVRCD              * exists read
          READ      TEMPA,KEY13;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTEMPA   RESET     KEY13                   * positional read
          READ      TEMPA,KEY13;;
          RETURN
.
RDTEMPA   MOVE      ZERO,OVRCD              * exact read
          RESET     KEY13
          READ      TEMPA,KEY13;XSPDESC,XSPECL:
                      XADAYP,XAINP,XB1CAT1,XB1CAT2,XB1CAT3,XB1TOTAL:
                      XB2CAT1,XB2CAT2,XB2CAT3,XB2TOTAL,XB3CAT1,XB3CAT2:
                      XB3CAT3,XB3TOTAL,XB4CAT1,XB4CAT2,XB4CAT3,XB4TOTAL:
                      XCCAT1,XCCAT2,XCCAT3,XCTOTAL

          GOTO      OVERCOND IF OVER
          RETURN
.
RKTEMPA   MOVE      ZERO,OVRCD              * next read
          READKS    TEMPA;XSPDESC,XSPECL:
                      XADAYP,XAINP,XB1CAT1,XB1CAT2,XB1CAT3,XB1TOTAL:
                      XB2CAT1,XB2CAT2,XB2CAT3,XB2TOTAL,XB3CAT1,XB3CAT2:
                      XB3CAT3,XB3TOTAL,XB4CAT1,XB4CAT2,XB4CAT3,XB4TOTAL:
                      XCCAT1,XCCAT2,XCCAT3,XCTOTAL
          GOTO      OVERCOND IF OVER
          RETURN
.
RPTEMPA   MOVE      ZERO,OVRCD              * previous read
          READKP    TEMPA;XSPDESC,XSPECL:
                      XADAYP,XAINP,XB1CAT1,XB1CAT2,XB1CAT3,XB1TOTAL:
                      XB2CAT1,XB2CAT2,XB2CAT3,XB2TOTAL,XB3CAT1,XB3CAT2:
                      XB3CAT3,XB3TOTAL,XB4CAT1,XB4CAT2,XB4CAT3,XB4TOTAL:
                      XCCAT1,XCCAT2,XCCAT3,XCTOTAL
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMPA   RESET     KEY13                    * write
          WRITE     TEMPA,KEY13;XSPDESC,XSPECL:
                      XADAYP,XAINP,XB1CAT1,XB1CAT2,XB1CAT3,XB1TOTAL:
                      XB2CAT1,XB2CAT2,XB2CAT3,XB2TOTAL,XB3CAT1,XB3CAT2:
                      XB3CAT3,XB3TOTAL,XB4CAT1,XB4CAT2,XB4CAT3,XB4TOTAL:
                      XCCAT1,XCCAT2,XCCAT3,XCTOTAL
          RETURN
.
UPTEMPA   UPDATE    TEMPA;XSPDESC,XSPECL:
                      XADAYP,XAINP,XB1CAT1,XB1CAT2,XB1CAT3,XB1TOTAL:
                      XB2CAT1,XB2CAT2,XB2CAT3,XB2TOTAL,XB3CAT1,XB3CAT2:
                      XB3CAT3,XB3TOTAL,XB4CAT1,XB4CAT2,XB4CAT3,XB4TOTAL:
                      XCCAT1,XCCAT2,XCCAT3,XCTOTAL
          RETURN
.
. ==================== 
.   TEMP FILE B
. ==================== 
.
RATEMPB   MOVE      ZERO,OVRCD              * exists read
          READ      TEMPB,KEY23;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTEMPB   RESET     KEY23                   * positional read
          READ      TEMPB,KEY23;;
          RETURN
.
RDTEMPB   MOVE      ZERO,OVRCD              * exact read
          RESET     KEY23
          READ      TEMPB,KEY23;YSPDESC,YSPECL:
                      YDCAT1,YDCAT2,YDCAT3,YDTOTAL,YECAT1,YECAT2:
                      YECAT3,YETOTAL,YFCAT1,YFCAT2,YFCAT3,YFTOTAL:
                      YGCAT1,YGCAT2,YGCAT3,YGTOTAL,YHCAT1,YHCAT2:
                      YHCAT3,YHTOTAL,YICAT1,YICAT2,YICAT3,YITOTAL:
                      YJCAT1,YJCAT2,YJCAT3,YJDAYP,YJTOTAL
          GOTO      OVERCOND IF OVER
          RETURN
.
RKTEMPB   MOVE      ZERO,OVRCD              * next read
          READKS    TEMPB;YSPDESC,YSPECL:
                      YDCAT1,YDCAT2,YDCAT3,YDTOTAL,YECAT1,YECAT2:
                      YECAT3,YETOTAL,YFCAT1,YFCAT2,YFCAT3,YFTOTAL:
                      YGCAT1,YGCAT2,YGCAT3,YGTOTAL,YHCAT1,YHCAT2:
                      YHCAT3,YHTOTAL,YICAT1,YICAT2,YICAT3,YITOTAL:
                      YJCAT1,YJCAT2,YJCAT3,YJDAYP,YJTOTAL
          GOTO      OVERCOND IF OVER
          RETURN
.
RPTEMPB   MOVE      ZERO,OVRCD              * previous read
          READKP    TEMPB;YSPDESC,YSPECL:
                      YDCAT1,YDCAT2,YDCAT3,YDTOTAL,YECAT1,YECAT2:
                      YECAT3,YETOTAL,YFCAT1,YFCAT2,YFCAT3,YFTOTAL:
                      YGCAT1,YGCAT2,YGCAT3,YGTOTAL,YHCAT1,YHCAT2:
                      YHCAT3,YHTOTAL,YICAT1,YICAT2,YICAT3,YITOTAL:
                      YJCAT1,YJCAT2,YJCAT3,YJDAYP,YJTOTAL
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMPB   RESET     KEY23                    * write
          WRITE     TEMPB,KEY23;YSPDESC,YSPECL:
                      YDCAT1,YDCAT2,YDCAT3,YDTOTAL,YECAT1,YECAT2:
                      YECAT3,YETOTAL,YFCAT1,YFCAT2,YFCAT3,YFTOTAL:
                      YGCAT1,YGCAT2,YGCAT3,YGTOTAL,YHCAT1,YHCAT2:
                      YHCAT3,YHTOTAL,YICAT1,YICAT2,YICAT3,YITOTAL:
                      YJCAT1,YJCAT2,YJCAT3,YJDAYP,YJTOTAL
          RETURN
.
UPTEMPB   UPDATE    TEMPB;YSPDESC,YSPECL:
                      YDCAT1,YDCAT2,YDCAT3,YDTOTAL,YECAT1,YECAT2:
                      YECAT3,YETOTAL,YFCAT1,YFCAT2,YFCAT3,YFTOTAL:
                      YGCAT1,YGCAT2,YGCAT3,YGTOTAL,YHCAT1,YHCAT2:
                      YHCAT3,YHTOTAL,YICAT1,YICAT2,YICAT3,YITOTAL:
                      YJCAT1,YJCAT2,YJCAT3,YJDAYP,YJTOTAL
.
          RETURN
+
.
.======================================== 
. I/O INCLUDES 
.======================================== 
.
          INC       STD001IO/PBL            * standard routines
          INC       BOKRC1IO/INC            * booking file
          INC       CALCDAYS
          INC       PATCOMN3/PBL            * for KEYPER
          INC       PATDRGIO/INC            * date range file
          INC       PATCODIO/INC            * codes file
          INC       PATMI1IO/INC            * admission file
          INC       PMSVX1IO/INC            * admission file
          INC       PATTRNIO/INC            * transfer file
          INC       WATCATIO/INC            * category change file
          INC       WATTR1IO/INC            * WL treatment file
          INC       TFILENAM
          INC       IBASEQIO/INC
          INC       WEBERRIO/INC

.***************************************************************************
.* System    :   Patient Management                                        *
.* Program   :   FX230690                                                  *
.* Desc      :   Fixit for Program for Emergency Takeup Outstanding Debts  *
.***************************************************************************
.* Date      :   12/10/2010                                                *
.* Author    :   J.Tan                                                     *
.* Function  :   This program will write record to emrvisaf for EMR takeup *
.*               Outstanding debts                                         *
.* Modifications  :                                                        *
.*        V10.00.00 12/10/2010  J.Tan      CAR 230690                      *
.*                  Created program                                        *
.***************************************************************************
.
.
          INC       STD001FD
.
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       EMRVISFD/INC
          INC       AAEDE1FD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
RECNUM    FORM      8
.
PRGID     INIT      "FX230690"
PRGNAM    INIT      "Fixit for Emergency Takeup Outstanding Debt"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000       * proceed with fixit
.
MAIN1000  CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN2000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN2000  CALL      PROC0000               * process files with coded fields
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: MAIN0000  *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"aaede1af";
          OPEN      AAEDE1A1,"aaede1af"
.
          DISPLAY   *P64:24,"emrvisaf";
          OPEN      EMRVISA1,"emrvisaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: MAIN0000  *
.*                        get user options or exit                          *
.*    Returns:  EXIT    = FALSE (0)  Run fixit                              *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*     Fix all Psych and Rehab suffixes                                      *
.*****************************************************************************
.
PROC0000  DISPLAY   *P1:24,*EF,"Processing:";
.
          MOVE      ZERO,RECNUM
          PACK      KEY8,SP70
          CALL      RDSDETA1                     * position in file
PROC1000  CALL      RDKDETA1                     * read next record
          BRANCH    OVRCD,PROC9000               * eof 
.
          DISPLAY   *P13:24,*V2LON,ADANUMB
.
          MATCH     "00000000",ADADATE
          GOTO      PROC1000 IF NOT EQUAL        * Not Takeup outstaning debts
.
          MOVE      ADANUMB,KEY8
          CALL      RDEMVIS1
          COMPARE   ZERO,OVRCD
          GOTO      PROC1000 IF EQUAL
.
          CALL      CLEMRVIS
          MOVE      ADANUMB,EMVIADMN
          MOVE      "3",EMVISTAT
          MOVE      ADAURNO,EMVIURNO
          MOVE      ADADATE,EMVIDATE
          CALL      WREMVIS1
.
          PRINT     *1,"Visit No. ",EMVIADMN
          ADD       ONE,RECNUM
          GOTO      PROC1000
.
PROC9000  DISPLAY   *P1:24,*EL,"Fixit complete.  Number of Records found: ":
                    RECNUM,"   ";
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       CLEMRVIS
.
          INC       EMRVISIO/INC
          INC       AAEDE1IO/INC
.

.******************************************************************************
.* System   : Waiting Lists                                                   *
.* Program  : IBAWAT80                                                        *
.* Desc.    : WAITING TIME ANALYSIS REPORT                                    *
.******************************************************************************
.* Date     : 22/11/1988                                                      *
.* Author   : N.S.                                                            *
.* Mods :                                                                     *
.*                                                                            *
. *****************************************************************************
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899                         *
.*           Recompiled as WATTR1FD changes                                   *
.******************************************************************************
.*       V10.05.01  01/08/2015  Ania P                      CAR 261630        *
.*                  Removed use of PORT for temp file naming                  *
.*        V5.07.02  27/10/1999 J Habasque                                     *
.*                  Recompiled for changes to WATPROFD - Active/Inactive codes*
.*        V5.07.01  11/09/1999  Steve Downing                                 *
.*                  Mods to use DAYSEP                                        *
.******************************************************************************
.*        V5.01.01  19/06/89 M.Ohleiter                                       *
.*                  Changes for new release 5.01                              *
.*                  09/07/89 J.Clark                                          *
.*                  Added Key14 variable required by WATST1FD                 *
.*        V5.01.02  20/02/90 E.Phan                                           *
.*                  Modified program to use user defined ranges.              *
.*        V5.01.03  23/02/90 E.Phan                                           *
.*                  Recompiled for WATPROFD.                                  *
.*        V5.01.04  27/06/90 Steve O'Gorman    SRF 105308                     *
.*                  Cleared temp file variables between procedures            *
.*        V5.01.10  15/08/90 Bill Dew                                         *
.*                  Fix program to use new waiting list files.                *
.*                  17/08/90 Justin Coates                                    *
.*                  Added WTCNTPDS                                            *
.*                  06/09/90 Bill Dew                                         *
.*                  This is the most poorly structured code I've seen -       *
.*                  for starters the Main Line sucks to the maximum.  I'm     *
.*                  getting rid of the median value and increasing the        *
.*                  procedure description                                     *
.*                  19/09/90 Bill Dew                                         *
.*                  I * D found writing to tempfile.                          *
.*        V5.01.11  07/11/90 D.Di.Paola        SRF 107011                     *
.*                  This report shows people still on waiting list as         *
.*                  at the period specified, not within period range.         *
.*                  Therefore fix extraction routine to process from          *
.*                  start of treatment file, not from start period date       *
.*                  (I * D found writing to tempfile, because file            *
.*                  corrupted, looked like morse code shiting itself..)       *
.*        V5.01.12  11/01/91 i chung           SRF 107536                     *
.*                  Fixed "Max" figure being less than "Mean" in some cases   *
.*        V5.01.13  15/04/91 i chung           SRF 108333                     *
.*                  Modify so as not to give negative "Mean" results          *
.*        V5.01.14  22/02/1993    Justin Coates  (DUDLEY)                     *
.*                  Allow for status 7 and 8                                  *
.******************************************************************************
.
          INC       STD001FD
          INC       PATCODFD/INC
          INC       PATCOMM/INC
          INC       PATDRGFD/INC
          INC       WATCONFD/INC
          INC       WATPNPFD/INC
          INC       WATPROFD/INC
          INC       WATTR1FD/INC
          INC       IBASEQFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
+
.***************************************************************************
.*                          VARIABLES DECLARATION                          *
.***************************************************************************
CMDLINE   DIM       30
CODEWG    INIT      "WG"
.
DIM25     DIM       25
DIM32     DIM       32
.
FORM3A    FORM      3
FORM3B    FORM      3
.
.
SVGRUP    DIM       3
PDATE     DIM       7        * period date display format
.
.
STRDATE  DIM       8
ENDDATE  DIM       8
.
WGRNG1    FORM      7         * Group Totals for Date Range 1
WGRNG2    FORM      7         * Group Totals for Date Range 2
WGRNG3    FORM      7         * Group Totals for Date Range 3
WGRNG4    FORM      7         * Group Totals for Date Range 4
WGRNG5    FORM      7         * Group Totals for Date Range 5
WGRNG6    FORM      7         * Group Totals for Date Range 6
WGRNG7    FORM      7         * Group Totals for Date Range 7
WGTOTL    FORM      7         * Group Total for Total Patients
WZRNG1    FORM      7         * Grand Totals for Date Range 1
WZRNG2    FORM      7         * Grand Totals for Date Range 2
WZRNG3    FORM      7         * Grand Totals for Date Range 3
WZRNG4    FORM      7         * Grand Totals for Date Range 4
WZRNG5    FORM      7         * Grand Totals for Date Range 5
WZRNG6    FORM      7         * Grand Totals for Date Range 6
WZRNG7    FORM      7         * Grand Totals for Date Range 7
WZTOTL    FORM      7         * Grand Total for Total Patients
.
XENDDAY   FORM      3         * Ending day in julian
XENDYR    FORM      2         * Ending year
XSTRDAY   FORM      3         * Starting day in julian
XSTRYR    FORM      2         * Starting year
XWAITDAY  FORM      7         * Number of waiting days
XWAITYR   FORM      6         * Number of waiting years
.
. ------- Temp file for collation of data -------
.
WATTMP    DIM       8
WATTMPXX  IFILE     SQL, FIXED=62, KEY="U1-3,4-12"
.
WXGRUP    DIM       3        3        1        Group Code
WXPROC    DIM       9        9        4        Procedure Code
WXRNG1    FORM      7        5        13       Totals for Date Range 1
WXRNG2    FORM      7        5        18       Totals for Date Range 2
WXRNG3    FORM      7        5        23       Totals for Date Range 3
WXRNG4    FORM      7        5        28       Totals for Date Range 4
WXRNG5    FORM      7        5        33       Totals for Date Range 5
WXRNG6    FORM      7        5        38       Totals for Date Range 6
WXRNG7    FORM      7        5        43       Totals for Date Range 7
WXMEAN    FORM      7        5        48       Mean value
WXPATS    FORM      6        4        53       Number of patients 
WXMAXI    FORM      7        5        57       Maximum # of waiting days
. End of Record                       62
.
PRGID     INIT      "IBAWAT80"
PRGNAM    INIT      "Time Analysis Report"
VERSION   INIT      "V12.01.00"
.
.**************************************************************************
.*                              MAINLINE                                  *
.**************************************************************************
ML0000    CALL      INIT0000          * Initialize & open files
.
ML10000   CALL      OPTN0000          * Select option
          BRANCH    OPTION,ML1111
.
ML99999   CALL      KILL0000          * Remove temp file
          CHAIN     PGM
ML1111
          CALL      GDAT0000          * get date period
          CALL      CONTQST
          BRANCH    CEXIT,ML2222,ML1111,ML10000
ML2222
          CALL      CREA0000          * Create temp file
          CALL      SCRN0000          * Display processing screen
          CALL      EXTR0000          * Extract data for printing
.
. ------- Printing Section -------
.
ML50000   CALL      SCRN1000           * Disp printing screen & init print vars
          CALL      CLER0000           * clear stats variables
          PACK      KEY12,SP20        * Read all records
          CALL      RDSTMPXX          * Positioning read on temp file
ML60000
          CALL      RDKTMPXX          * Read next temp record
          BRANCH    OVRCD,ML80000     * No more records - print totals
.
          MATCH     "///",SVGRUP      * 1st time ?
          GOTO      ML70000 IF EQUAL  * Yes - just print record
          MATCH     SVGRUP,WXGRUP     * Same group
          GOTO      ML70000 IF EQUAL  * Yes - just print record
          CALL      PRNT2000          * Print Group Totals
.
ML70000   MOVE      WXGRUP,SVGRUP     * Current group is saved group code
          DISPLAY   *P18:16,*V2LON,WXGRUP,*P18:17,WXPROC;
          CALL      PRNT1000          * Print Record
          GOTO      ML60000           * Read next temp record
.
ML80000   CALL      PRNT2000          * Print Group Totals
          CALL      PRNT3000          * Print Grand Totals
          GOTO      ML10000           * Finished printing - get options again
.
.***************************************************************************
.*        Initialisation & Open files                                      *
.***************************************************************************
.
INIT0000  CALL      DISPHEAD
          CALL      TFILENAM
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,THIRTY7;*140,WTCNTRG1,WTCNTRG2,WTCNTRG3:
                                          WTCNTRG4,WTCNTRG5,WTCNTRG6:
                                     *185,WTCNTPDS
          CLOSE     CONTROLF
.
          DISPLAY   *P64:24,"watproaf";
          OPEN      WATPROA1,"watproaf"
.
          DISPLAY   *P64:24,"watpnpaf";
          OPEN      WATPNPA1,"watpnpaf"
.
          DISPLAY   *P64:24,"wattr1af";
          OPEN      WATTR1A3,"wattr1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdrgaf";
          OPEN      PATDRGA1,"patdrgaf"
          OPEN      PATDRGA3,"patdrgaf"
.
          RETURN
+
.******************************************************************************
.                                 OPTN0000
.******************************************************************************
OPTN0000  DISPLAY   *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Time Analysis Report":
                    *P1:7,"Select Option :";
OPTN1000
          KEYIN     *P17:7,*EL,*DV,UNDLN1,*P17:7,*V2LON,OPTION;
          COMPARE   ZERO,OPTION
          GOTO      OPTN9999 IF EQUAL
          BRANCH    OPTION,OPTN9999
          BEEP
          GOTO      OPTN1000
.
OPTN9999  RETURN
+
.******************************************************************************
.                                 GDAT0000
.******************************************************************************
GDAT0000  DISPLAY   *P1:10,*EF,"Report Period Date : ";
.
          PACK      CPTDATE,CCC,CYY,CMM,CDD
          REP       " 0",CPTDATE
          CALL      GPERD
          BRANCH    EXIT,GDAT2000 
.
          PACK      KEY6,DRGYR,DRGNUM
.
          CALL      RDSDRGA1
          CALL      RDPDRGA1
          PACK      KEY6,DRGCYR,DRGCNUM
          UNPACK    KEY6,CCENT,CYEAR,CMON
          MOVE      ZERO,CHIGHLT                 * 0 = highlight
          MOVE      "22",CCOL                    * column for keyin
          MOVE      TEN,CVERT                    * row for keyin
          CALL      KEYPER
          BRANCH    OVRCD,GDAT0000               * no date keyed in
.
.         Validate the date entered
.
          PACK      PDATE,CMON,SLASH,CCENT,CYEAR
          PACK      KEY6,CCENT,CYEAR,CMON
          REP       " 0",KEY6
          CALL      RDDRGA3
          BRANCH    OVRCD,GDAT3000
.
          MOVE      DRGFDTE,STRDATE              * copy filedate variables
          MOVE      DRGTDTE,ENDDATE
.
          UNPACK    DRGFDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,KEY10
          UNPACK    DRGTDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P35:10,*EL,DRGLDSC:
                    *P35:11,*EL,KEY10," to ",CPCDATE;
          GOTO      GDAT9999
GDAT2000
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Current date not found in Period file **    ";
          CALL      HITENTER
          GOTO      GDAT0000
GDAT3000
          DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid Date **    ";
          CALL      HITENTER
          GOTO      GDAT0000
.
GDAT9999  RETURN
+
.***************************************************************************
.*        Deleting  an indexed file based on port                          *
.***************************************************************************
KILL0000  CLOSE     WATTMPXX
          PACK      CMDLINE,SP30
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    TFILNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          RETURN
+
.***************************************************************************
.*        Creating an indexed file based on port                           *
.***************************************************************************
CREA0000  CALL      KILL0000
          PACK      CMDLINE,SP30
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    TFILNAME,CMDLINE
          APPEND    " 62 u1-3,4-12",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      WATTMPXX,TFILNAME
          RETURN
+
.***************************************************************************
.*        Display Processing screen                                        *
.***************************************************************************
SCRN0000  DISPLAY   *P1:14,*EF,*V2LON,"*** ",*BLINKON,"Extracting Data",*HOFF:
                               *V2LON," ***",*HOFF:
                    *P1:16,WTCNTPDS," Code : ":
                    *P1:17,"U/R Number     : ":
                    *P1:18,"Date on List   : "
          RETURN
+
.***************************************************************************
.*                                EXTR0000                                 *
.*   Created 06/09/1990 Bill Dew                                           *
.*   Loop thru procedure codes file than loop thru wattr1af beginning at   *
.*   starting period for corresponding procedure codes.                    *
.*                                                                         *
.***************************************************************************
EXTR0000  
          PACK      KEY27,SP30         * Read all records in until end range
          CALL      RDSTREA3           * Positioning read
EXTR2222
          CALL      RDKTREA3           * Read next record
          BRANCH    OVRCD,EXTR9999     * No more records
.
          MATCH     WMDATE1,ENDDATE    * Gone past end date ?
          GOTO      EXTR9999 IF LESS   * Yes - finished for this proc
.
          MOVE      SP3,WPGRP
          PACK      KEY9,WMCODE,SP9    * test code exist on procedure code file
          CALL      RDPROA1            * also get group code
          BRANCH    OVRCD,EXTR2222
.
          DISPLAY   *P18:16,*V2LON,WMCODE," (",WPGRP,")";
          UNPACK    WMDATE1,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P18:17,*V2LON,WMURNO:
                    *P18:18,CPCDATE;
.
          CALL      CALC0000           * Calculate & collate data for this proc
          CALL      WTMP0000           * Write record for this procedure
          GOTO      EXTR2222           * Get next procedure
.
EXTR9999  RETURN
+
.*****************************************************************************
.*                                CALC0000                                   *
.*   Calculation routines                                                    *
.*   Modified by Bill Dew 10/09/1990                                         *
.*   If patient is unscheduled Booked or pre-admitted (1 2 or 3) use WMDATE1 *
.*   date on list to end date of period for days on list calculation.        *
.*   If patient is admitted or discharged use WMDATE1 date on list to WMDATE3*
.*   scheduled admission date for days on list calculation.                  *
.*   If patient is removed then use WMDATE1 to WMDATE4 to calculate days on  *
.*   list                                                                    *
.*   This routine calls CLER0000 which uses the WGRNGX variables as flags to *
.*   indicate which range is incremented.  WGRNGX vars are also used later   *
.*   for storing grand totals within each range.                             *
.*   For wmstat = 7 (performed), use wmdate1 to end of period                *
.*   For wmstat = 8 (not-performed), must check file watpnpaf to check the   *
.*                  date returned to list                                    *
.*****************************************************************************
CALC0000  BRANCH    WMSTAT,CALC3000,CALC3000,CALC3000,CALC1400:
                           CALC1400,CALC1600,CALC3000,CALC1800
.
. ------- Use scheduled adm date -------
.
CALC1400  DAYSEP    WMDATE1,WMDATE3,XWAITDAY
          GOTO      CALC7000                 * Do conversion
.
. ------- Use removal date -------
.
CALC1600  DAYSEP    WMDATE1,WMDATE4,XWAITDAY
          GOTO      CALC7000                 * Do conversion
.
. ------- check file patpnpaf for end date to check -------
.
CALC1800  MOVE      WMBOOK,KEY8
          CALL      RDWTPNP1
          BRANCH    OVRCD,CALC3000          * not on file so use current date
.
. this means a new W/L record was written, so must include the number of days
. for the old record and this is wmdate1 -> wpnpdret.
. the new record was written with wmdate1 = wpnpdret so the calculation on the
. new record will pick up the rest of the days
.
          DAYSEP    WMDATE1,WTNPDRET,XWAITDAY
          GOTO      CALC7000
.
. ------- Use current date -------
.
CALC3000  DAYSEP    WMDATE1,ENDDATE,XWAITDAY
          GOTO      CALC7000
.
. ------- Work out where to put figures -------
.
CALC7000  CALL      CLER0000
.
          COMPARE   XWAITDAY,WTCNTRG1
          GOTO      CALC9100 IF NOT LESS     * Range 1
          COMPARE   XWAITDAY,WTCNTRG2
          GOTO      CALC9200 IF NOT LESS     * Range 2
          COMPARE   XWAITDAY,WTCNTRG3
          GOTO      CALC9300 IF NOT LESS     * Range 3
          COMPARE   XWAITDAY,WTCNTRG4
          GOTO      CALC9400 IF NOT LESS     * Range 4
          COMPARE   XWAITDAY,WTCNTRG5
          GOTO      CALC9500 IF NOT LESS     * Range 5
          COMPARE   XWAITDAY,WTCNTRG6
          GOTO      CALC9600 IF NOT LESS     * Range 6
          GOTO      CALC9700                 * Range 7
.
CALC9100  ADD       ONE,WGRNG1               * Range 1
          GOTO      CALC9999
CALC9200  ADD       ONE,WGRNG2               * Range 2
          GOTO      CALC9999
CALC9300  ADD       ONE,WGRNG3               * Range 3
          GOTO      CALC9999
CALC9400  ADD       ONE,WGRNG4               * Range 4
          GOTO      CALC9999
CALC9500  ADD       ONE,WGRNG5               * Range 5
          GOTO      CALC9999
CALC9600  ADD       ONE,WGRNG6               * Range 6
          GOTO      CALC9999
CALC9700  ADD       ONE,WGRNG7               * Range 7
.
CALC9999  RETURN
+
.***************************************************************************
.*        Clear temp file variables
.***************************************************************************
CLER0000  MOVE      ZERO,WGRNG1
          MOVE      ZERO,WGRNG2
          MOVE      ZERO,WGRNG3
          MOVE      ZERO,WGRNG4
          MOVE      ZERO,WGRNG5
          MOVE      ZERO,WGRNG6
          MOVE      ZERO,WGRNG7
.
CLER9999  RETURN
+
.***************************************************************************
.        Display Printing screen
.***************************************************************************
SCRN1000  DISPLAY   *P1:14,*EF,*V2LON,"*** ",*BLINKON,"Generating Report":
                               *HOFF,*V2LON," ***",*HOFF:
                    *P1:16,"Group Code     : ":
                    *P1:17,WTCNTPDS," Code : "
          MOVE      "80",CLNO
          MOVE      ZERO,CPAGENO
          MOVE      "///",SVGRUP
          RETURN
+
.****************************************************************************
.                                 WTMP0000
.    Modified 05/09/1990 Bill Dew
.****************************************************************************
WTMP0000  PACK      KEY12,WPGRP,WMCODE,SP20
          CALL      RDTMPXX                 * check if record already exist
          BRANCH    OVRCD,WTMP5555
.
          COMPARE   XWAITDAY,WXMAXI
          GOTO      WTMP1111 IF NOT LESS    * Not more than max - ignore
.         MOVE      XWAITDAY,XMAXDAYS
          MOVE      XWAITDAY,WXMAXI         * SRF 107536 - moved to wrong var.
.                                                          previously
WTMP1111  ADD       XWAITDAY,WXMEAN         * add up the total first
          ADD       ONE,WXPATS              * increment patient counter
          ADD       WGRNG1,WXRNG1
          ADD       WGRNG2,WXRNG2
          ADD       WGRNG3,WXRNG3
          ADD       WGRNG4,WXRNG4
          ADD       WGRNG5,WXRNG5
          ADD       WGRNG6,WXRNG6
          ADD       WGRNG7,WXRNG7
.
          CALL      UPTMPXX                 * update record
          GOTO      WTMP9999
WTMP5555
          MOVE      XWAITDAY,WXMEAN         * set total wait days
          MOVE      XWAITDAY,WXMAXI         * set maximum number of wait days
          MOVE      ONE,WXPATS              * set number of patients
          MOVE      WGRNG1,WXRNG1
          MOVE      WGRNG2,WXRNG2
          MOVE      WGRNG3,WXRNG3
          MOVE      WGRNG4,WXRNG4
          MOVE      WGRNG5,WXRNG5
          MOVE      WGRNG6,WXRNG6
          MOVE      WGRNG7,WXRNG7
          CALL      WRTMPXX                 * write new record
.
WTMP9999  RETURN
+
.****************************************************************************
.*        Printing Subroutines                                              *
.****************************************************************************
.
. ------- Print Procedure -------
.
PRNT1000  COMPARE   "60",CLNO
          GOTO      PRNT1100 IF LESS
          CALL      PRNT4000                 * Print header
PRNT1100 
          PACK      WPDESC,SP30,SP30
          CLEAR     WPDESC
          APPEND    "Invalid ",WPDESC
          APPEND    WTCNTPDS,WPDESC
          APPEND    " Code ",WPDESC
          MOVE      WXPROC,KEY9
          CALL      RDPROA1
          MOVE      WPDESC,DIM32
.
          DIV       WXPATS,WXMEAN
.
          PRINT     *1,"|",*3,WXPROC,*13,DIM32,*46,"|",*48,WXRNG1,*56,WXRNG2:
                    *64,WXRNG3,*72,WXRNG4,*80,WXRNG5,*88,WXRNG6,*96,WXRNG7:
                    *104,"| ",WXPATS,*114,"| ",WXMEAN," ",WXMAXI,*132,"|"
          ADD       ONE,CLNO
.
          ADD       WXRNG1,WGRNG1            * Add to Group Totals
          ADD       WXRNG2,WGRNG2
          ADD       WXRNG3,WGRNG3
          ADD       WXRNG4,WGRNG4
          ADD       WXRNG5,WGRNG5
          ADD       WXRNG6,WGRNG6
          ADD       WXRNG7,WGRNG7
          RETURN
.
. ------- Print Group Total -------
.
PRNT2000  COMPARE   "60",CLNO
          GOTO      PRNT2100 IF LESS
          CALL      PRNT4000                 * Print header
.
PRNT2100  MOVE      "Unknown Group       ",TDESC
          MATCH     SP3,SVGRUP
          GOTO      PRNT2111 IF EQUAL
          PACK      KEY5,CODEWG,SVGRUP
          CALL      RDCODE1
PRNT2111
          MOVE      TDESC,DIM25
.
          MOVE      WGRNG1,WGTOTL            * Add to Totals
          ADD       WGRNG2,WGTOTL
          ADD       WGRNG3,WGTOTL
          ADD       WGRNG4,WGTOTL
          ADD       WGRNG5,WGTOTL
          ADD       WGRNG6,WGTOTL
          ADD       WGRNG7,WGTOTL
.
          CALL      PRNT5000
          PRINT     *1,"|",*3,"Subtotal for ",SVGRUP,*20,DIM25,*46,"|":
                    *48,WGRNG1,*56,WGRNG2,*64,WGRNG3,*72,WGRNG4,*80,WGRNG5:
                    *88,WGRNG6,*96,WGRNG7,*104,"|",WGTOTL,*114,"|",*132,"|"
          ADD       ONE,CLNO
          CALL      PRNT6000
.
          ADD       WGRNG1,WZRNG1            * Add to Grand Totals
          ADD       WGRNG2,WZRNG2
          ADD       WGRNG3,WZRNG3
          ADD       WGRNG4,WZRNG4
          ADD       WGRNG5,WZRNG5
          ADD       WGRNG6,WZRNG6
          ADD       WGRNG7,WZRNG7
.
          CALL      CLER0000
          RETURN
.
. ------- Print Grand Total -------
.
PRNT3000  COMPARE   "60",CLNO
          GOTO      PRNT3100 IF LESS
          CALL      PRNT4000                 * Print header
.
PRNT3100  MOVE      WZRNG1,WZTOTL            * Add to Totals
          ADD       WZRNG2,WZTOTL
          ADD       WZRNG3,WZTOTL
          ADD       WZRNG4,WZTOTL
          ADD       WZRNG5,WZTOTL
          ADD       WZRNG6,WZTOTL
          ADD       WZRNG7,WZTOTL
.
          PRINT     *1,"|",*3,"Grand Total",*46,"|",*48,WZRNG1,*56,WZRNG2:
                    *64,WZRNG3,*72,WZRNG4,*80,WZRNG5,*88,WZRNG6,*96,WZRNG7:
                    *104,"|",WZTOTL,*114,"|",*132,"|"
.
          CALL      PRNT7000
          PRINT     *N,*N,"*** End of Report ***"
          DISPLAY   *P1:24,*EL,*P28:24,*V2LON,"** Generation Completed **",*W2;
.
          MOVE      ZERO,WZRNG1              * Clear Grand Totals
          MOVE      ZERO,WZRNG2
          MOVE      ZERO,WZRNG3
          MOVE      ZERO,WZRNG4
          MOVE      ZERO,WZRNG5
          MOVE      ZERO,WZRNG6
          MOVE      ZERO,WZRNG7
          RETURN
.
. ------- Heading for print -------
.
PRNT4000  CLOCK     TIME,CTIMEIS
          CALL      HEAD132
.
          PRINT     *40,"Period : ",PDATE,*59,DRGLDSC
          UNPACK    STRDATE,CCENT,CYEAR,CMON,CDAY
          UNPACK    ENDDATE,XCENT,XYEAR,XMON,XDAY
          PRINT     *59,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR:
                    " to ",XDAY,SLASH,XMON,SLASH,XCENT,XYEAR,*N
          CALL      PRNT7000
          PRINT     *1,"| ",WTCNTPDS,*46,"|      ":
                       "                 Distribution               ":
                       "       |         |                 |":
                    *N,"| Code      Description",*46,"|";
.
          MOVE      ZERO,FORM3A
          MOVE      FORM3A,FORM3B
          ADD       WTCNTRG1,FORM3B
          PRINT     SP1,FORM3A,"-",FORM3B;
.
          MOVE      FORM3B,FORM3A
          ADD       ONE,FORM3A
          MOVE      WTCNTRG2,FORM3B
          PRINT     SP1,FORM3A,"-",FORM3B;
.
          MOVE      FORM3B,FORM3A
          ADD       ONE,FORM3A
          MOVE      WTCNTRG3,FORM3B
          PRINT     SP1,FORM3A,"-",FORM3B;
.
          MOVE      FORM3B,FORM3A
          ADD       ONE,FORM3A
          MOVE      WTCNTRG4,FORM3B
          PRINT     SP1,FORM3A,"-",FORM3B;
.
          MOVE      FORM3B,FORM3A
          ADD       ONE,FORM3A
          MOVE      WTCNTRG5,FORM3B
          PRINT     SP1,FORM3A,"-",FORM3B;
.
          MOVE      FORM3B,FORM3A
          ADD       ONE,FORM3A
          MOVE      WTCNTRG6,FORM3B
          PRINT     SP1,FORM3A,"-",FORM3B;
.
          MOVE      FORM3B,FORM4
          ADD       ONE,FORM4
          PRINT     SP1,FORM4,"-> ";
.
          PRINT     " |  Total  |  Mean     Max   |"
          CALL      PRNT7000
          MOVE      TEN,CLNO
          RETURN
.
. ------- Line separator type 1 -------
.
PRNT5000  PRINT     *1,"|",*46,"|------":
                       "--------------------------------------------":
                       "-------|---------|-----------------|"
          ADD       ONE,CLNO
          RETURN
.
. ------- Line separator type 2 -------
.
PRNT6000  PRINT     *1,"|--------------------------------------------|------":
                       "--------------------------------------------":
                       "-------|---------|-----------------|"
          ADD       ONE,CLNO
          RETURN
.
. ------- Line separator type 3 -------
.
PRNT7000  PRINT     *1,"*-------------------------------------------":
                       "--------------------------------------------":
                       "-------------------------------------------*"
          ADD       ONE,CLNO
          RETURN
+
.****************************************************************************
.*        Temp file I/O subroutines                                         *
.****************************************************************************
RDTMPXX   MOVE      ZERO,OVRCD
          RESET     KEY12
          READ      WATTMPXX,KEY12;WXGRUP,WXPROC:
                                   WXRNG1,WXRNG2,WXRNG3,WXRNG4,WXRNG5,WXRNG6:
                                   WXRNG7,WXMEAN,WXPATS,WXMAXI
          GOTO      OVERCOND IF OVER
          RETURN
.
RDSTMPXX  MOVE      ZERO,OVRCD
          RESET     KEY12
          READ      WATTMPXX,KEY12;;
          RETURN
.
RDKTMPXX  MOVE      ZERO,OVRCD
          READKS    WATTMPXX;WXGRUP,WXPROC,WXRNG1,WXRNG2,WXRNG3,WXRNG4,WXRNG5:
                                           WXRNG6,WXRNG7,WXMEAN,WXPATS,WXMAXI
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTMPXX   MOVE      ZERO,OVRCD
          RESET     KEY12
          WRITE     WATTMPXX,KEY12;KEY12,WXRNG1,WXRNG2,WXRNG3,WXRNG4,WXRNG5:
                                         WXRNG6,WXRNG7,WXMEAN,WXPATS,WXMAXI
          RETURN
.
UPTMPXX   UPDATE    WATTMPXX;WXGRUP,WXPROC,WXRNG1,WXRNG2,WXRNG3,WXRNG4,WXRNG5:
                                         WXRNG6,WXRNG7,WXMEAN,WXPATS,WXMAXI
          RETURN
+
.****************************************************************************
.
          INC       STD001IO
          INC       PATCOMN3
          INC       PATDRGIO/INC
          INC       PATCODIO/INC
          INC       WATPROIO/INC
          INC       WATTR1IO/INC
          INC       WATPNPIO/INC
          INC       TFILENAM
          INC       IBASEQIO/INC
          INC       WEBERRIO/INC
.

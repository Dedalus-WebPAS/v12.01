.***************************************************************************
.* System    :   Outpatients                                               *
.* Program   :   IBAWEBFX                                                  *
.* Desc      :                                                             *
.***************************************************************************
.* Date      :   27/07/2000                                                *
.* Author    :   Bronko Sosic                                              *
.* Function  :   This program will Transfer File and Load Files            *
.***************************************************************************
.
. V10.14.01 15/04/2019  Davin            TSK 0838558
.           Removed delete of ibawebfx (TRANFILE) on program exit
.           Fixed option = 0 (OPTN9500) to allow program exit
. V10.13.01 05/12/2018  Don Nguyen       TSK 0838558
.           Deleted temp files (TRANFILE) on program exit
. V10.07.02 27.10.2015  B.G.Ackland      CAR 312297
.           Added update option
. V10.07.01 23.10.2015  B.G.Ackland      CAR 312297
.           Extend WEBTPLAF for Menu Generation Elements
.
.***************************************************************************
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
.
          INC      WEBSPGFD/INC 
          INC      WEBSOPFD/INC 
          INC      WEBTPLFD/INC 
.
. LOCAL VARIABLE DEFINITION
. -------------------------
SVTPMTY   DIM       1      Menu Link Type                     
SVTPTAR   DIM       1      Menu target                        
SVTPWID   DIM       5      Open Width Default                 
SVTPHIG   DIM       5      Open Height Default                
SVTPLP1   DIM       8      Additional Link Parameter 1        
SVTPLP2   DIM       8      Additional Link Parameter 2        
SVTPLP3   DIM       8      Additional Link Parameter 3        
SVTPLP4   DIM       8      Additional Link Parameter 4        
.
RECTYPE   FORM      1
TRANFILE  FILE      ASCII, FIXED=250
.
PRGID     INIT      "IBAWEBFX"
PRGNAM    INIT      "Make Transfer File/Load Transfer File"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000,ML2000,ML2500
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML3000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML4000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2500    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML5000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML3000    PREP      TRANFILE,"ibawebfx"     
          CALL      MAKE0000               * Make Transfer File
          GOTO      ML9999
.
ML4000    MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      TRANFILE,"ibawebfx"     
          TRAPCLR   IO
          BRANCH    OVRCD,ML9900
          CALL      LOAD0000               * Load Transfer File
          GOTO      ML9999 
.
ML5000    MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      TRANFILE,"ibawebfx"     
          TRAPCLR   IO
          BRANCH    OVRCD,ML9900
          CALL      UPDT0000               * Update Template File
          GOTO      ML9999 
.
ML9900    DISPLAY   *P1:24,*EL,"Export File ibawebfx Not Found.";
          CALL      HITENTER
.
ML9999    CHAIN     PGM                    * chain back to program
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
          OPEN      WEBSPGA1,"webspgaf"
          OPEN      WEBSOPA1,"websopaf"
          OPEN      WEBTPLA1,"webtplaf"
.
INIT9999  RETURN
+
.
RDTRTR1   READ      TRANFILE,SEQ;RECTYPE
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTRTR1   WRITE     TRANFILE,SEQ;RECTYPE
          RETURN
.
RDTSPG1   READ      TRANFILE,SEQ;WBSPPRG,WBSPNAM,WBSPFIFO,WBSPOUTD,WBSPDEFT:
                       WBSPUR1,WBSPUR2,WBSPUD1,WBSPUD2,WBSPUY1,WBSPUY2,WBSPSPA
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTSPG1   WRITE     TRANFILE,SEQ;WBSPPRG,WBSPNAM,WBSPFIFO,WBSPOUTD,WBSPDEFT:
                       WBSPUR1,WBSPUR2,WBSPUD1,WBSPUD2,WBSPUY1,WBSPUY2,WBSPSPA
          RETURN
.
RDTSOP1   READ      TRANFILE,SEQ;WBSOPRG,WBSOOPT,WBSODES,WBSOUR1,WBSOUR2:
                       WBSOUD1,WBSOUD2,WBSOUY1,WBSOUY2,WBSOSPA
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTSOP1   WRITE     TRANFILE,SEQ;WBSOPRG,WBSOOPT,WBSODES,WBSOUR1,WBSOUR2:
                       WBSOUD1,WBSOUD2,WBSOUY1,WBSOUY2,WBSOSPA
          RETURN
.
RDTTPL1   READ      TRANFILE,SEQ;WBTPPRG,WBTPOPT,WBTPTPL,WBTPDES,WBTPFIL:
                       WBTPLEV,WBTPMSC,WBTPMTY,WBTPTAR,WBTPWID,WBTPHIG:
                       WBTPLP1,WBTPLP2,WBTPLP3,WBTPLP4,WBTPSPA
          GOTO      OVERCOND IF OVER
          RETURN
. Read Menu Variable to Save Variables for Update
RDTTPL2   READ      TRANFILE,SEQ;WBTPPRG,WBTPOPT,WBTPTPL,WBTPDES,WBTPFIL:
                       WBTPLEV,WBTPMSC,SVTPMTY,SVTPTAR,SVTPWID,SVTPHIG:
                       SVTPLP1,SVTPLP2,SVTPLP3,SVTPLP4,WBTPSPA
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTTPL1   WRITE     TRANFILE,SEQ;WBTPPRG,WBTPOPT,WBTPTPL,WBTPDES,WBTPFIL:
                       WBTPLEV,WBTPMSC,WBTPMTY,WBTPTAR,WBTPWID,WBTPHIG:
                       WBTPLP1,WBTPLP2,WBTPLP3,WBTPLP4,WBTPSPA
          RETURN
.
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.****************************************************************************
OPTN0000  MOVE       ZERO,CPAGENO      * very strange operation
          MOVE       "60",CLNO
.
          HLINE     *G37,2,52,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Make Transfer File":
                    *P1:6,*V2LON,TWO,*HOFF," = Load Transfer File":
                    *P1:7,*V2LON,THREE,*HOFF," = Update Template Menu Details":
                    *P1:9,"Select Option :";
.
OPTN1000  KEYIN     *P17:9,*EL,*DV,UNDLN1,*P17:9,*V2LON,COPTION;
.
          COMPARE   ZERO,COPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    COPTION,OPTN1111,OPTN2222,OPTN3333
          BEEP
          GOTO      OPTN1000
.
OPTN1111  DISPLAY   *P52:2,*V2LON,"- Make Transfer File ";
          MOVE      " Make Transfer File. ",CPHDROPT
          GOTO      OPTN9000

OPTN2222  DISPLAY   *P52:2,*V2LON,"- Load Transfer File ";
          MOVE      " Load Transfer File. ",CPHDROPT
          GOTO      OPTN9000
.
OPTN3333  DISPLAY   *P52:2,*V2LON,"- Load Transfer File ";
          MOVE      " Update Template Menu Details. ",CPHDROPT
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT                     * exit program
OPTN9999  RETURN
+
.**************************************************************************
.*                               MAKE0000              Called by: ML0000  *
.*                     This makes the Transfer File                       *
.**************************************************************************
.
. Record Type 1 - Web Server Program Details
.
MAKE0000  MOVE      ONE,RECTYPE
          PACK      KEY8,SP70
          CALL      RSWBSP1
MAKE1000  CALL      RKWBSP1
          BRANCH    OVRCD,MAKE2000
          CALL      WRTRTR1
          CALL      WRTSPG1
          GOTO      MAKE1000
.
. Record Type 2 - Web Server Options
.
MAKE2000  MOVE      TWO,RECTYPE
          PACK      KEY10,SP70
          CALL      RDWBSO1
MAKE2500  CALL      RKWBSO1
          BRANCH    OVRCD,MAKE3000
          CALL      WRTRTR1
          CALL      WRTSOP1
          GOTO      MAKE2500
.
. Record Type 3 - Template Master Details
.
MAKE3000  MOVE      THREE,RECTYPE
          PACK      KEY13,SP70
          CALL      RDWBTP1
MAKE3500  CALL      RKWBTP1
          BRANCH    OVRCD,MAKE9999
          CALL      WRTRTR1
          CALL      WRTTPL1
          GOTO      MAKE3500
.
MAKE9999  RETURN
+
.**************************************************************************
.*                               LOAD0000              Called by: ML0000  *
.*                     This Loads the Transfer File                       *
.**************************************************************************
.
. Record Type 1 - Web Server Program Details
.
LOAD0000  MOVE      ZERO,OVRCD
          CALL      RDTRTR1
          BRANCH    OVRCD,LOAD9999
          BRANCH    RECTYPE,LOAD1000,LOAD2000,LOAD3000
.
LOAD1000  CALL      RDTSPG1
          PACK      KEY8,WBSPPRG
          CALL      RDWBSP1                  
          IF        OVRCD=0
            CALL      UPWBSP1
          ELSE
            CALL      WRWBSP1
          ENDIF
          GOTO      LOAD0000
.
LOAD2000  CALL      RDTSOP1
          PACK      KEY10,WBSOPRG,WBSOOPT
          CALL      RDWBSO1                  
          IF        OVRCD=0
            CALL      UPWBSO1
          ELSE
            CALL      WRWBSO1
          ENDIF
          GOTO      LOAD0000
.
LOAD3000  CALL      RDTTPL1
          PACK      KEY13,WBTPPRG,WBTPOPT,WBTPTPL
          CALL      RDWBTP1                  
          IF        OVRCD=1
            CALL      WRWBTP1
          ENDIF
          GOTO      LOAD0000
.
LOAD9999  RETURN
.**************************************************************************
.*                               UPDT0000              Called by: ML0000  *
.*       This Updates the Menu Details for the Templates                  *
.**************************************************************************
UPDT0000  MOVE      ZERO,OVRCD
          CALL      RDTRTR1
          BRANCH    OVRCD,UPDT9999
          BRANCH    RECTYPE,UPDT0000,UPDT0000,UPDT1000
.
UPDT1000  CALL      RDTTPL2
          PACK      KEY13,WBTPPRG,WBTPOPT,WBTPTPL
          CALL      RDWBTP1                  
          IF        OVRCD=0
            MOVE      SVTPMTY,WBTPMTY 
            MOVE      SVTPTAR,WBTPTAR 
            MOVE      SVTPWID,WBTPWID 
            MOVE      SVTPHIG,WBTPHIG 
            MOVE      SVTPLP1,WBTPLP1 
            MOVE      SVTPLP2,WBTPLP2 
            MOVE      SVTPLP3,WBTPLP3 
            MOVE      SVTPLP4,WBTPLP4 
            CALL      UPWBTP1
          ENDIF
          GOTO      UPDT0000
.
UPDT9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
          INC      STD001IO
          INC      WEBSPGIO/INC
          INC      WEBSOPIO/INC
          INC      WEBTPLIO/INC

.***************************************************************************
.* System    :   Allied Health                                             *
.* Program   :   IBAALL72                                                  *
.* Desc      :   Transfer allied health referrals                          *
.***************************************************************************
.* Date      :   24/07/2007                                                *
.* Author    :   Jill Habasque                                             *
.* Function  :   This program will loop the allied health referral file    *
.*           :   and print the HCP and Department with next referral dates *
.* Mods      :                                                             *
.*           :   V9.09.01 19/12/2007 Ebon Clements CAR 157606              *
.*           :             Changed status description of Open to Active    *
.*           :   V9.09.00 24/07/2007 Jill Habasque CAR 104983              *
.*           :             Created program                                 *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
.
          INC       ALLCONFD/INC
          INC       ALLREFFD/INC
          INC       ALLENCFD/INC
          INC       PATCODFD/INC
          INC       PATMA1FD/INC
          INC       PMSHCPFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
CODE      DIM       2
DIM35     DIM       35
DISPDATE  DIM       11
DISPINCL  DIM       50
DISPSTAT  DIM       9
DOCCODE   DIM       10
DOCFNAME  DIM       16
FMTSNAME  DIM       35
FMTGNAME  DIM       35
FMTTITLE  DIM       10
FROMDEPT  DIM       3
FROMDDES  DIM       20
FROMDATE  DIM       8 
LSTENDAT  DIM       10
TODATE    DIM       8 
SAVKEY22  DIM       22
TODEPART  DIM       3
TODEPDES  DIM       20
TOHCPCOD  DIM       10
TOHCPDES  DIM       40
INCLUDEW  FORM      1
INCLUDEA  FORM      1
INCLUDEI  FORM      1
INCLUDEC  FORM      1
INCLUDEX  FORM      1
INCLUDER  FORM      1
RECCOUNT  FORM      8
REVWDATE  DIM       10
UPDATEFL  FORM      1
.
PRGID     INIT      "IBAALL72"
PRGNAM    INIT      "Next Review Date Referrals"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
          GOTO      ML9999
.
ML1000    CALL      DEPF0000               * get department from
          BRANCH    EXIT,ML9999
.
ML2000    CALL      DEPT0000               * get department to
          BRANCH    EXIT,ML9999
.
ML2500    CALL      DATE0000               * get date range
          BRANCH    EXIT,ML9999
.
ML3000    CALL      INCW0000               * Include waiting referrals   
          BRANCH    EXIT,ML9999
.
ML3500    CALL      INCA0000               * Include active referrals
          BRANCH    EXIT,ML9999
.
ML4000    CALL      INCI0000               * Include inactive referrals
          BRANCH    EXIT,ML9999
.
ML4500    CALL      INCC0000               * Include closed referrals
          BRANCH    EXIT,ML9999
.
ML5000    CALL      INCX0000               * Include cancelled referrals
          BRANCH    EXIT,ML9999
.
ML5500    CALL      INCR0000               * Include rejected referrals
          BRANCH    EXIT,ML9999
.
ML8000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML9000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML9000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P60:24,"opening controlf"
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P68:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P68:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P68:24,"allrefaf"
          OPEN      ALLREFA4,"allrefaf"
.
          DISPLAY   *P68:24,"allencaf"
          OPEN      ALLENCA6,"allencaf"
.
          DISPLAY   *P60:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P68:24,"pmshcpaf"
          OPEN      PMSHCPA1,"pmshcpaf"
          OPEN      PMSHCPA2,"pmshcpaf"
.
          READ      CONTROLF,HUND06;*27,ALCNRAUD,*28,ALCNEAUD

          MATCH     "1",ALCNRAUD            
          IF        @EQUAL
            OPEN      ALLAUDRE,"allaudre"
          ENDIF
.
          CALL      IBACLOCK
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Next Review Report"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.****************************************************************************
.*                                DEPF0000             Called by: ML1000    *
.* Keyin department from                                                    *
.****************************************************************************
DEPF0000  DISPLAY   *P1:9,"Department From : ",SP30
          MOVE      SP70,FROMDEPT
          MOVE      SP70,FROMDDES
          MOVE      "18",ECOL
          MOVE      "9",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP70,CKYIDEF3
          PACK      CODE,ANSC,ANSG
          DISPLAY   *PECOL:EVERT,SP6
.
          CALL      PATCODKY
          BRANCH    EXIT,DEPF9100,DEPF9100
.
          DISPLAY   *PECOL:EVERT,*V2LON,ACODE,*HOFF,*P25:EVERT,TDESC
          MOVE      ACODE,FROMDEPT
          MOVE      TDESC,FROMDDES
.
DEPF9000  MOVE      ZERO,EXIT
          GOTO      DEPF9999
.
DEPF9100  MOVE      ONE,EXIT
          GOTO      DEPF9999
.
DEPF9999  RETURN
+
.***************************************************************************
.*                               DATE0000                                  *
.*                          accept date range                              *
.***************************************************************************
.
.---------- keyin first date range -------
.
DATE0000    MOVE       FALSE,EXIT
            DISPLAY    *P1:11,*EF,"Service Date From :":
                          *P1:12,"Service Date To   :";
.
            MOVE      "11",CVERT
            MOVE      "11",EVERT
            CALL      IBACLOCK
            MOVE      CCC,CCENT
            MOVE      CYY,CYEAR
            MOVE      CMM,CMON
            MOVE      CDD,CDAY
            CALL      KEYDATE                          * enter the from date
            PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY   * move to form field
            REP       " 0",FROMDATE                    * from date
.
            MOVE       "12",CVERT
            MOVE       "12",EVERT
            CALL      KEYDATE                          * enter the from date
            PACK      TODATE,CCENT,CYEAR,CMON,CDAY     * move to form field
            REP       " 0",FROMDATE                    * from date
.
            MATCH      FROMDATE,TODATE
            GOTO       DATE9999 IF NOT LESS
.
            DISPLAY    *P1:24,*EL,*B,"2nd Range Must Be Greater Than 1st ":
                       "Range - Hit <",*V2LON,"ENTER",*HOFF,"> To Continue ";
            KEYIN      ANS;
            DISPLAY    *P1:24,*EL;
            GOTO       DATE0000
.
DATE9999    RETURN
+
.****************************************************************************
.*                                DEPT0000             Called by: ML1000    *
.* Keyin department from                                                    *
.****************************************************************************
DEPT0000  DISPLAY   *P1:10,"Department To   : ",SP30
          MOVE      SP70,TODEPART
          MOVE      SP70,TODEPDES
          MOVE      "18",ECOL
          MOVE      "10",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP70,CKYIDEF3
          PACK      CODE,ANSC,ANSG
          DISPLAY   *PECOL:EVERT,SP6
.
          CALL      PATCODKY
          BRANCH    EXIT,DEPT9100,DEPT9100
.
          DISPLAY   *PECOL:EVERT,*V2LON,ACODE,*HOFF,*P25:EVERT,TDESC
          MOVE      ACODE,TODEPART
          MOVE      TDESC,TODEPDES
.
DEPT9000  MOVE      ZERO,EXIT
          GOTO      DEPT9999
.
DEPT9100  MOVE      ONE,EXIT
          GOTO      DEPT9999
.
DEPT9999  RETURN
+
.****************************************************************************
.*                                INCW0000             Called by: ML3000    *
.* Include waiting referrals                                                *
.****************************************************************************
INCW0000  MOVE      ZERO,INCLUDEW
          MOVE      ZERO,EXIT
.
INCW1000  DISPLAY   *P1:13,"Include Waiting Patients : "
          KEYIN     *P32:13,*V2LON,*RV,DIM1:
                    *P32:13,*DV,DIM1
          REP       UPPLOW,DIM1
          DISPLAY   *P32:13,*V2LON,DIM1
.
          MOVE      ONE,INCLUDEW
          MATCH     "Y",DIM1
          GOTO      INCW9999 IF EQUAL
.
          MOVE      ZERO,INCLUDEW
          MATCH     "N",DIM1
          GOTO      INCW9999 IF EQUAL
.
          MATCH    SP70,DIM1
          GOTO     INCW9000 IF EQUAL
.
          BEEP
          GOTO      INCW1000
.
INCW9000  MOVE      ONE,EXIT
          GOTO      INCW9999
.
INCW9999  RETURN
.
+
.****************************************************************************
.*                                INCA0000             Called by: ML3500    *
.* Include active referrals                                                 *
.****************************************************************************
INCA0000  MOVE      ZERO,INCLUDEA
          MOVE      ZERO,EXIT
.
INCA1000  DISPLAY   *P1:14,"Include Active Patients : "
          KEYIN     *P32:14,*V2LON,*RV,DIM1:
                    *P32:14,*DV,DIM1
          REP       UPPLOW,DIM1
          DISPLAY   *P32:14,*V2LON,DIM1
.
          MOVE      ONE,INCLUDEA
          MATCH     "Y",DIM1
          GOTO      INCA9999 IF EQUAL
.
          MOVE      ZERO,INCLUDEA
          MATCH     "N",DIM1
          GOTO      INCA9999 IF EQUAL
.
          MATCH    SP70,DIM1
          GOTO     INCA9000 IF EQUAL
.
          BEEP
          GOTO      INCA1000
.
INCA9000  MOVE      ONE,EXIT
          GOTO      INCA9999
.
INCA9999  RETURN
+
.****************************************************************************
.*                                INCI0000             Called by: ML4000    *
.* Include inactive referrals                                                *
.****************************************************************************
INCI0000  MOVE      ZERO,INCLUDEI
          MOVE      ZERO,EXIT
.
INCI1000  DISPLAY   *P1:15,"Include Inactive Patients : "
          KEYIN     *P32:15,*V2LON,*RV,DIM1:
                    *P32:15,*DV,DIM1
          REP       UPPLOW,DIM1
          DISPLAY   *P32:15,*V2LON,DIM1
.
          MOVE      ONE,INCLUDEI
          MATCH     "Y",DIM1
          GOTO      INCI9999 IF EQUAL
.
          MOVE      ZERO,INCLUDEI
          MATCH     "N",DIM1
          GOTO      INCI9999 IF EQUAL
.
          MATCH    SP70,DIM1
          GOTO     INCI9000 IF EQUAL
.
          BEEP
          GOTO      INCI1000
.
INCI9000  MOVE      ONE,EXIT
          GOTO      INCI9999
.
INCI9999  RETURN
+
.****************************************************************************
.*                                INCC0000             Called by: ML4500    *
.* Include closed referrals                                                 *
.****************************************************************************
INCC0000  MOVE      ZERO,INCLUDEC
          MOVE      ZERO,EXIT
.
INCC1000  DISPLAY   *P1:16,"Include Closed Patients : "
          KEYIN     *P32:16,*V2LON,*RV,DIM1:
                    *P32:16,*DV,DIM1
          REP       UPPLOW,DIM1
          DISPLAY   *P32:16,*V2LON,DIM1
.
          MOVE      ONE,INCLUDEC
          MATCH     "Y",DIM1
          GOTO      INCC9999 IF EQUAL
.
          MOVE      ZERO,INCLUDEC
          MATCH     "N",DIM1
          GOTO      INCC9999 IF EQUAL
.
          MATCH    SP70,DIM1
          GOTO     INCC9000 IF EQUAL
.
          BEEP
          GOTO      INCC1000
.
INCC9000  MOVE      ONE,EXIT
          GOTO      INCC9999
.
INCC9999  RETURN
+
.****************************************************************************
.*                                INCX0000             Called by: ML5000    *
.* Include cancelled referrals                                              *
.****************************************************************************
INCX0000  MOVE      ZERO,INCLUDEX
          MOVE      ZERO,EXIT
.
INCX1000  DISPLAY   *P1:17,"Include Cancelled Patients : "
          KEYIN     *P32:17,*V2LON,*RV,DIM1:
                    *P32:17,*DV,DIM1
          REP       UPPLOW,DIM1
          DISPLAY   *P32:17,*V2LON,DIM1
.
          MOVE      ONE,INCLUDEX
          MATCH     "Y",DIM1
          GOTO      INCX9999 IF EQUAL
.
          MOVE      ZERO,INCLUDEX
          MATCH     "N",DIM1
          GOTO      INCX9999 IF EQUAL
.
          MATCH    SP70,DIM1
          GOTO     INCX9000 IF EQUAL
.
          BEEP
          GOTO      INCX1000
.
INCX9000  MOVE      ONE,EXIT
          GOTO      INCX9999
.
INCX9999  RETURN
+
.****************************************************************************
.*                                INCR0000             Called by: ML5500    *
.* Include rejected referrals                                               *
.****************************************************************************
INCR0000  MOVE      ZERO,INCLUDER
          MOVE      ZERO,EXIT
.
INCR1000  DISPLAY   *P1:18,"Include Rejected Patients : "
          KEYIN     *P32:18,*V2LON,*RV,DIM1:
                    *P32:18,*DV,DIM1
          REP       UPPLOW,DIM1
          DISPLAY   *P32:18,*V2LON,DIM1
.
          MOVE      ONE,INCLUDER
          MATCH     "Y",DIM1
          GOTO      INCR9999 IF EQUAL
.
          MOVE      ZERO,INCLUDER
          MATCH     "N",DIM1
          GOTO      INCR9999 IF EQUAL
.
          MATCH    SP70,DIM1
          GOTO     INCR9000 IF EQUAL
.
          BEEP
          GOTO      INCR1000
.
INCR9000  MOVE      ONE,EXIT
          GOTO      INCR9999
.
INCR9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
PROC0000  CALL      HEAD0000 
          MOVE      ZERO,RECCOUNT
          MOVE      ZERO,CPAGENO
.
          PACK      KEY22,FROMDEPT,SP70
          CALL      RSALREF4
PROC1000  CALL      RKALREF4
          BRANCH    OVRCD,PROC9000
.
          MATCH     ALREDEPT,FROMDEPT       * Check department
          GOTO      PROC1100 IF EQUAL     
          GOTO      PROC9000 IF NOT LESS     
.
PROC1100  MATCH     ALREDEPT,TODEPART         * Check department
          GOTO      PROC1200 IF EQUAL     
          GOTO      PROC9000 IF LESS     
.
PROC1200  MATCH     SP70,ALREDREV
          GOTO      PROC1000 IF EQUAL
.
          MATCH     FROMDATE,ALREDREV
          GOTO      PROC1000 IF LESS
.
          MATCH     ALREDREV,TODATE
          GOTO      PROC1000 IF LESS
.
          IF        INCLUDEW=1
            MATCH     "0",ALRESTAT          * Waiting
            GOTO      PROC2000 IF EQUAL
          ENDIF
.
          IF        INCLUDEA=1
            MATCH     "1",ALRESTAT          * Active
            GOTO      PROC2000 IF EQUAL
          ENDIF
.
          IF        INCLUDEC=1
            MATCH     "2",ALRESTAT          * Closed
            GOTO      PROC2000 IF EQUAL
          ENDIF
.
          IF        INCLUDEI=1
            MATCH     "3",ALRESTAT          * Inactive
            GOTO      PROC2000 IF EQUAL
          ENDIF
.
          IF        INCLUDEX=1
            MATCH     "4",ALRESTAT          * Cancelled
            GOTO      PROC2000 IF EQUAL
          ENDIF
.
          IF        INCLUDER=1
            MATCH     "5",ALRESTAT          * Rejected
            GOTO      PROC2000 IF EQUAL
          ENDIF
.
          GOTO      PROC1000
.
PROC2000  CALL      PREF0000                * Print U/R Line details
.
          GOTO      PROC1000
.
PROC9000  CALL      TAIL0000                * Print End of Report
          GOTO      PROC9999
.
PROC9999  RETURN
+
.************************************************************************
.*                        HEAD0000                                      *
.*                 Produce printout heading                             *
.************************************************************************
HEAD0000  MOVE      ZERO,CLNO
          CALL      HEAD132
.
          PRINT     *25,"Department From : ",FROMDDES:
                        *80,"Department To   : ",TODEPDES
          PRINT     *25,"Date From : ",FROMDATE:
                        *80,"Date To  : ",TODATE
.
          CLEAR     DISPINCL
.
          IF        INCLUDEW=1
            APPEND    "Waiting ",DISPINCL
          ENDIF
.
          IF        INCLUDEA=1
            APPEND    "Active ",DISPINCL
          ENDIF
.
          IF        INCLUDEI=1
            APPEND    "Inactive ",DISPINCL
          ENDIF
.
          IF        INCLUDEC=1
            APPEND    "Closed ",DISPINCL
          ENDIF
.
          IF        INCLUDEX=1
            APPEND    "Cancelled ",DISPINCL
          ENDIF
.
          IF        INCLUDER=1
            APPEND    "Rejected ",DISPINCL
          ENDIF
.
          PRINT       *25,"Include Status : ",DISPINCL
.
          CALL      UND132
.
          PRINT     *1,"| U/R No",*12,"| Patient Name",*48,"|Sex":
                    *52,"| DOB",*63,"|Referral No":
                    *75,"|Referral Date",*89,"|Last EOC Date":
                    *103,"|Review Date",*115,"| HCP",*132,"|" 
.
          CALL      UND132
          ADD       EIGHT,CLNO
.
HEAD9999  RETURN
.
.****************************************************************************
.*        Print UR and Name                                                 *
.****************************************************************************
PREF0000  ADD       ONE,RECCOUNT
          PACK      KEY8,ALREURNO,SP70
          CALL      RDMAST1
          IF        OVRCD=1
            MOVE      "Invalid U/R Number",DIM35
            GOTO      PREF1000
          ENDIF
.
          MOVE      PTITL,PACTITLE
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      TWO,PACFRMT
          CALL      PACNAME                 * Pack Full Name
          MOVE      PACFNAME,DIM35
.
PREF1000  MATCH     "0",ALRESTAT
          IF         @EQUAL
           MOVE       "Waiting",DISPSTAT
          ENDIF
.
          MATCH     "1",ALRESTAT
          IF         @EQUAL
           MOVE       "Active",DISPSTAT
          ENDIF
.
          MATCH     "2",ALRESTAT
          IF         @EQUAL
           MOVE       "Closed",DISPSTAT
          ENDIF
.
          MATCH     "3",ALRESTAT
          IF         @EQUAL
           MOVE       "Inactive",DISPSTAT
          ENDIF
.
          MATCH     "4",ALRESTAT
          IF         @EQUAL
           MOVE       "Cancelled",DISPSTAT
          ENDIF
.
          MATCH     "5",ALRESTAT
          IF         @EQUAL
           MOVE       "Rejected",DISPSTAT
          ENDIF
.
          MOVE      SP70,LSTENDAT
          PACK      KEY32,ALREVISN,Z70
          CALL      RSALENC6
          CALL      RPALENC6
          BRANCH    OVRCD,PREF2000
.
          MATCH     ALREVISN,ALENVISN
          GOTO      PREF2000 IF NOT EQUAL
.
          MATCH     ALENSDAT,SP70
          IF        !@EQUAL
            UNPACK    ALENSDAT,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            MOVE      CPCDATE,LSTENDAT
          ENDIF
.
PREF2000  UNPACK    ALRERDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DISPDATE
.
          UNPACK    ALREDREV,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,REVWDATE
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PACK      KEY10,ALRERHCP,SP70
          CALL      RDPMHCP1
          IF        OVRCD=0
            MOVE      PMHCGNAM,ANS
            PACK      DOCFNAME,ANS,DOT,PMHCSNAM
            RESET     DOCFNAME
          ELSE
            MOVE      SP70,DOCFNAME
          ENDIF
.
          COMPARE   "60",CLNO
          GOTO      PREF8000 IF LESS
          CALL      HEAD0000                 * Print  header
.
PREF8000  PRINT     *1,"| ",ALREURNO,*12,"|",DIM35:
                    *48,"| ",PSEX,*52,"|",CPCDATE:
                    *63,"| ",ALREVISN,*73,"| ",DISPDATE:
                    *89,"| ",LSTENDAT,*103,"| ",REVWDATE:
                    *115,"|",DOCFNAME:
                    *132,"|"
.
          ADD       ONE,CLNO
          GOTO      PREF9999
.
PREF9999  RETURN
.
.****************************************************************************
.*        Trailer Print Routine                                             *
.****************************************************************************
TAIL0000  COMPARE   "60",CLNO
          GOTO      TAIL1000 IF LESS
          CALL      HEAD0000                 * Print header
.
TAIL1000  CALL      UND132
          PRINT     *1,"Records Selected ",RECCOUNT
          CALL      UND132
          PRINT     *N,"*** End of Report ***"
.
TAIL9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       PATCODKY
          INC       PMSHCPKY
.
          INC       ALLREFIO/INC
          INC       ALLENCIO/INC
          INC       PATCODIO/INC
          INC       PATMA1IO/INC
          INC       PMSHCPIO/INC
             

.************************************************************************
.*  Program   :  IBASYS61                                               *
.*  Function  :  Purge Call Repository                                  *
.*  Author    :  ROD   21/10/1996                                       *
.************************************************************************
.   Modifications :                                                     *
.                                                                       *    
.   V9.03.01  07.06.2004 Ebon Clements    CAR 49527                     *    
.             Ported from 6.11                                          *
.   VC2.09.01            Ebon Clements    SRF 4354                      *    
.             Change purge routine to purge all transactions            *
.             billed or not billed for the given date range             *
.************************************************************************
          INC       STD001FD
          INC       IBAPBXFD/INC
.
FROMDATE  DIM       8
.
PRGID     INIT      "IBASYS61"
PRGNAM    INIT      "Purge Call Repository"
VERSION   INIT      "V12.01.00"
+
.************************************************************************
.*  ML0000  :  Mainline                                                 *
.************************************************************************
ML0000    CALL      INIT0000                      * initialisation
.
ML1000    CALL      FDAT0000                      * keyin date
          BRANCH    EXIT,ML9999                   * nothing input
.
          CALL      CONTQST                       * Ok to Continue?
          BRANCH    CEXIT,ML5000,ML1000,ML9999
.
ML5000    CALL      PURG0000                      * delete all required records
.
ML9999    CHAIN     PGM
+
.************************************************************************
.*  INIT0000  :  Initialisation                                         *
.************************************************************************
INIT0000  CALL      DISPHEAD
.
          OPEN      IBAPBXA2,"ibapabxf"
.
INIT9999  RETURN
+
.**************************************************************************
.*  FDAT0000  :  keyin From date                                          *
.**************************************************************************
FDAT0000  DISPLAY   *P1:4,*EF,"Purge to Date : "
          UNPACK    SP10,CDAY,CMON,CYEAR
          MOVE      "17",CCOL
          MOVE      FOUR,CVERT
          MOVE      ONE,CCENTRY
          MOVE      ZERO,CDEFDTE
          MOVE      CCC,CCENT
.
FDAT1000  CALL      KEYDATE
          BRANCH    CQUEST,FDAT1000
          BRANCH    OVRCD,FDAT9000                * anything input
.
          PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",FROMDATE
          MOVE      ZERO,EXIT
.
          DISPLAY   *P1:7,"NB: All call transactions ":
                    "up to and including the above ":
                    "date, will be deleted."
          GOTO      FDAT9999
.
FDAT9000  MOVE      ONE,EXIT
.
FDAT9999  RETURN
+
.**************************************************************************
.*  PURG0000  :  purge all required records                               *
.**************************************************************************
PURG0000  PACK      KEY20,SP20
          CALL      RSIBPBX2
PURG1000  CALL      RKIBPBX2
          BRANCH    OVRCD,PURG9999
.
          MATCH     IBPBSDAT,FROMDATE
          GOTO      PURG9999 IF LESS
.
          PACK      KEY20,IBPBSDAT,IBPBSTIM,IBPBEXTN
          CALL      DEIBPBX2              * Delete record 
          CALL      RSIBPBX2              * Positional read after delete
          GOTO      PURG1000
.
PURG9999  RETURN
+
.
.
          INC       STD001IO              * required for Debtors
          INC       IBAPBXIO/INC

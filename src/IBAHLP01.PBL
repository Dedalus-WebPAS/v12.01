+
.*******************************************************************************
.* System   : Function Key Help Program                                        *
.* Program  : IBAHLP01                                                         *
.* Desc     : Function Key Help Screen Maintenance Program                     *
.*******************************************************************************
.* Date     : 04/07/91                                                         *
.* Author   : Steve O'Gorman                                                   *
.* Function : To provide a means by which the help screens may be maintained   *
.* MODS     :                                                                  *
.*                                                                             *
.*  V10.13.01  05/12/2018  Don Nguyen       TSK 0838558                        *
.*             Deleted temp file (TEMPFILE) on program exit                    *
.*******************************************************************************
.*  V10.05.01  27/01/2015  Ania P           CAR 261630                         *
.*             Removed use of PORT for temp file naming                        *
.*******************************************************************************
.
          INC       STD001FD
          INC       IBAHLPFD/INC
          INC       IBASEQFD/INC  
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
.**********************************************************************
.*                           CONSTANTS                                *
.**********************************************************************
.
PERCENT   INIT      "%"
TEMPFILE  FILE      ASCII,FIXED=79
.
.**********************************************************************
.*                            VARIABLES                               *
.**********************************************************************
.
BOTTOM    FORM      2
.
DIM76     DIM       76
.
FNAMER    DIM       8
.
HELPNAME  DIM       8
.
RC        FORM      1
.
WIDTH     FORM      3
.
PRGID     INIT      "IBAHLP01"
PRGNAM    INIT      "Help Screen File Maintenance"
VERSION   INIT      "V12.01.00"
.
.**************************************************************************
.*                              MAINLINE                                  *
.*                            Controlling Logic                           *
.**************************************************************************
.
          CALL      INIT0000               * initialization and open files
.
ML0100    CALL      CLR0000                * clear input variables
          CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000,ML2000,ML3000
.
. ---- add a help screen ----
.
ML1000    CALL      SCR0000                * display screen format
          CALL      HELP0000               * input help screen name
          BRANCH    EXIT,ML0100
.
          CALL      KILL0000               * Kill existing Temp File
.
          CALL      WORD0000               * Call the Word Processor
          BRANCH    EXIT,ML1000
.
          CALL      POST0000               * Ok to Post (Y/N)
          BRANCH    EXIT,ML1000
.
          CALL      WRIT0000               * write to file
          GOTO      ML1000
.
. ---- modify a help screen ----
.
ML2000    CALL      SCR0000                * display screen format
          CALL      HELP0000               * input help screen
          BRANCH    EXIT,ML0100
.
          CALL      BULD0000               * Build the Temp File
.
          CALL      READ0000               * Read in help screen data
.
          CALL      WORD0000               * Call the Word Processor
          BRANCH    EXIT,ML2000
.
          CALL      POST0000               * Ok to Post (Y/N)
          BRANCH    EXIT,ML2000
.
          CALL      UPDT0000               * update record
          BRANCH    EXIT,ML0100            * error within file
          GOTO      ML2000
.
. ---- delete a help screen ----
.
ML3000    CALL      SCR0000                * display screen format
          CALL      HELP0000               * input help screen
          BRANCH    EXIT,ML0100
.
          CALL      BULD0000               * Build the Temp File
.
          CALL      DISP0000               * Display the Help Screen
.
          CALL      DQST0000               * Ok to Delete (Y/N)
          BRANCH    EXIT,ML3000
.
          CALL      DELT0000               * delete this record
          GOTO      ML3000
.
ML9999    CALL      KILL0000               * Kill existing Temp File
          CHAIN     PGM
+
.****************************************************************************
.*                                INIT0000                                  *
.*                             initialization                               *
.*  The initialization routine is used to display headings and open files.  *
.*  Any other general initialization can also be performed here.            *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P64:24,"Opening"
.
          DISPLAY   *P72:24,"ibahlpaf"
          OPEN      IBAHLPA1,"ibahlpaf"
          OPEN      IBAHLPA2,"ibahlpaf"
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMER
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000                                  *
.*                        get user options or exit                          *
.*                                                                          *
.*    valid options     --->  (0) Exit                                      *
.*                            (1) Add                                       *
.*                            (2) Modify                                    *
.*                            (3) Delete                                    *
.****************************************************************************
.
. ---- display option screen ----
.
OPTN0000  MOVE      FALSE,EXIT
.
          DISPLAY   *P53:2,*EL;
.
          CALL      SELOPTN                        * select option
          COMPARE   ZERO,COPTION
          GOTO      OPTN9000 IF EQUAL              * exit
.
          BRANCH    COPTION,OPTN9999,OPTN9999,OPTN9999
          GOTO      OPTN0000
.
OPTN9000  MOVE      TRUE,EXIT                      * set EXIT = 1
OPTN9999  RETURN
+
.**************************************************************************
.*                                  CLR0000                               *
.*                            clear global variables                      *
.**************************************************************************
.
CLR0000   MOVE      SP8,IBHPNAME
          MOVE      SP8,IBHPLINK
          PACK      IBHPTEXT,SP30,SP30,SP30
          MOVE      ZERO,IBHPLINE
          MOVE      SP8,HELPNAME
.
CLR9999   RETURN
+
.**************************************************************************
.*                               PROC0000                                 *
.*                       input indentification code                       *
.**************************************************************************
.
HELP0000  MOVE      FALSE,EXIT
.
          DISPLAY   *P15:4,UNDLN8;
          KEYIN     *P15:4,*V2LON,HELPNAME;
.
          RESET     HELPNAME
          GOTO      HELP9000 IF EOS
.
          ENDSET    HELPNAME
          APPEND    SP8,HELPNAME
          RESET     HELPNAME
.
          DISPLAY   *P15:4,*V2LON,HELPNAME;
.
          MATCH     QUEST,HELPNAME
          GOTO      HELP0500 IF NOT EQUAL
.
          CALL      QEST0000
          CALL      SCR0000
.
          GOTO      HELP0000
.
HELP0500  BRANCH    OPTION,HELP1000,HELP2000,HELP2000
.
HELP1000  PACK      KEY11,HELPNAME,SP20
.
          CALL      RSIBHLP1
          CALL      RKIBHLP1
          BRANCH    OVRCD,HELP9999
.
          MATCH     IBHPNAME,HELPNAME
          GOTO      HELP9999 IF NOT EQUAL
.
          DISPLAY   *P1:24,*EL,"Help Screen Already on File - Hit <",*V2LON:
                    "ENTER",*HOFF,"> to Continue ";
          KEYIN     ANS;
.
          DISPLAY   *P1:24,*EL;
          GOTO      HELP0000
.
HELP2000  PACK      KEY11,HELPNAME,SP20
.
          CALL      RSIBHLP1
          CALL      RKIBHLP1
          BRANCH    OVRCD,HELP9999
.
          MATCH     IBHPNAME,HELPNAME
          GOTO      HELP9999 IF EQUAL
.
          DISPLAY   *P1:24,*EL,"Help Screen Not on File - Hit <",*V2LON:
                    "ENTER",*HOFF,"> to Continue ";
          KEYIN     ANS;
.
          DISPLAY   *P1:24,*EL;
          GOTO      HELP0000
.
HELP9000  MOVE      TRUE,EXIT
.
HELP9999  RETURN
+
.**************************************************************************
.*                                  SCR0000                               *
.*                         display screen format                          *
.**************************************************************************
.
SCR0000   DISPLAY   *P1:3,*EF:
                    *P1:4,"Help Screen : ";
.
SCR9999   RETURN
+
.**************************************************************************
.*                                WPEX0000                                *
.*                         Execute the Word Processor                     *
.**************************************************************************
.
WORD0000  MOVE      "23",BOTTOM
          MOVE      "78",WIDTH
.
          MOVE      "0",RC
.
          WORD      FNAMER,999,1,3,WIDTH,BOTTOM,RC
.
          IF        RC = 1
            MOVE      FALSE,EXIT
          ELSE
            MOVE      TRUE,EXIT
          ENDIF
.
          RETURN
+
.**************************************************************************
.*                                READ0000                                *
.*                     Read Data into the Wordprocessor                   *
.**************************************************************************
.
READ0000  PREP      TEMPFILE,FNAMER
.
          DISPLAY   *P1:4,*EF:
                    *P1:24,"Loading Data - ":
                    *BLINKON,*V2LON,"Please Wait";
.
          PACK      KEY11,HELPNAME,SP20
          CALL      RSIBHLP1

READ1000  CALL      RKIBHLP1
          BRANCH    OVRCD,READ9999
.
          MATCH     IBHPNAME,HELPNAME
          GOTO      READ9999 IF NOT EQUAL
.
          MATCH     SP8,IBHPLINK
          GOTO      READ8000 IF EQUAL
.
          CLEAR     DIM76
          PACK      DIM76,PERCENT,IBHPLINK,PERCENT,IBHPTEXT
          MOVE      DIM76,IBHPTEXT
.
READ8000  WRITE     TEMPFILE,SEQ;IBHPTEXT
          GOTO      READ1000
.
READ9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.*************************************************************************
.                              POST0000                                  *
.*                      Question O.K. to Post                            *
.*************************************************************************
+
POST0000  MOVE      FALSE,EXIT
          DISPLAY   *P1:24,"O.K. to Post Data (",*V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ";
.
          KEYIN     *V2LON,ANS;
          DISPLAY   *P1:24,*EL;
.
          MATCH     "Y",ANS
          GOTO      POST9999 IF EQUAL
          MATCH     "N",ANS
          GOTO      POST9000 IF EQUAL
          GOTO      POST0000
.
POST9000  MOVE      TRUE,EXIT
.
POST9999  RETURN
.**************************************************************************
.*                              WRIT0000                                  *
.*                        write a new record                              *
.**************************************************************************
.
WRIT0000  MOVE      ZERO,IBHPLINE
.
          CLOSE     TEMPFILE
          OPEN      TEMPFILE,FNAMER
.
WRIT1000  READ      TEMPFILE,SEQ;DIM76
          GOTO      WRIT9999 IF OVER
.
          PACK      IBHPTEXT,SP30,SP30,SP30
          MOVE      SP8,IBHPLINK
.
          CMATCH    PERCENT,DIM76
          GOTO      WRIT7000 IF NOT EQUAL
.
          UNPACK    DIM76,ANS,IBHPLINK,ANS,IBHPTEXT
          GOTO      WRIT8000
.
WRIT7000  MOVE      DIM76,IBHPTEXT
.
WRIT8000  PACK      KEY11,HELPNAME,IBHPLINE
          MOVE      HELPNAME,IBHPNAME
          CALL      WRIBHLP1
.
          ADD       ONE,IBHPLINE
          GOTO      WRIT1000
.
WRIT9999  RETURN
+
.***************************************************************************
.*                               UPDT0000                                  *
.*             Update text in the report file for use in a report          *
.***************************************************************************
.
UPDT0000  CALL      DELT0000                * Delete Old Data from the File
.
          CALL      WRIT0000                * Write New Data to the File
.
UPDT9999  RETURN
+
.****************************************************************************
.*                                   DISP0000                               *
.*             Display a summary screen of report text to allow             *
.*             verification in the delete option                            *
.****************************************************************************
.
DISP0000  MOVE      FOUR,CVERT
.
          PACK      KEY11,HELPNAME,SP20
          CALL      RSIBHLP1
.
DISP1000  CALL      RKIBHLP1
          BRANCH    OVRCD,DISP9999
.
          MATCH     IBHPNAME,HELPNAME
          GOTO      DISP9999 IF NOT EQUAL
.
          MATCH     SP8,IBHPLINK
          GOTO      DISP1100 IF EQUAL
.
          PACK      DIM76,PERCENT,IBHPLINK,PERCENT,IBHPTEXT
          MOVE      DIM76,IBHPTEXT
.
DISP1100  DISPLAY   *P1:CVERT,IBHPTEXT;
.
          COMPARE   "23",CVERT
          GOTO      DISP9999 IF EQUAL
.
          ADD       ONE,CVERT
          GOTO      DISP1000
.
DISP9999  RETURN
+
.****************************************************************************
.*                              DQST000                                     *
.*                       Ok to delete record (y/n)                          *
.****************************************************************************
.
DQST0000  MOVE      FALSE,EXIT
.
          DISPLAY   *P1:24,"O.K. to Delete (",*V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ";
.
          KEYIN     *V2LON,ANS;
          DISPLAY   *P1:24,*EL;
.
          MATCH     "Y",ANS
          GOTO      DQST9999 IF EQUAL
          MATCH     "N",ANS
          GOTO      DQST9000 IF EQUAL
          GOTO      DQST0000
.
DQST9000  MOVE      TRUE,EXIT
.
DQST9999  RETURN
+
.****************************************************************************
.*                              DEL0000                                     *
.*                         delete an existing record                        *
.****************************************************************************
.
DELT0000  PACK      KEY11,HELPNAME,SP20
          CALL      RSIBHLP1
          CALL      RKIBHLP1
          BRANCH    OVRCD,DELT9999
.
          MATCH     HELPNAME,IBHPNAME
          GOTO      DELT9999 IF NOT EQUAL
.
          PACK      KEY11,IBHPNAME,IBHPLINE
          CALL      DEIBHLP1
          GOTO      DELT0000
.
DELT9999  RETURN
.
.**********************************************************************
.*                              QEST0000                              *
.*               Display all Codes on Report File for "?"             *
.**********************************************************************
.
.         Display Category Heading, and then loop over entries
.
QEST0000  DISPLAY   *P1:4,*EF,*P30:4,*V2LON,*ULON,"HELP SCREENS"
.
          MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
.
          PACK      KEY11,SP20
          CALL      RSIBHLP2
.
QEST1000  CALL      RKIBHLP2
          BRANCH    OVRCD,QEST2000
.
          COMPARE   ZERO,IBHPLINE
          GOTO      QEST4000 IF NOT EQUAL
.
          DISPLAY   *PCCOL:CVERT,*V2LON,IBHPNAME
.
          ADD       ONE,CVERT
          COMPARE   TWENTY3,CVERT
          GOTO      QEST1000 IF LESS
.
          MOVE      FIVE,CVERT
          ADD       "40",CCOL
          COMPARE   "70",CCOL
          GOTO      QEST1000 IF LESS
.
          KEYIN     *P1:24,*EL," (",*V2LON,"E",*HOFF,")xit, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          CMATCH    ANSE,ANS
          GOTO      QEST5000 IF EQUAL
.
          MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
          DISPLAY   *P1:5,*EF
.
          GOTO      QEST1000
.
.
QEST2000  DISPLAY   *P1:24,*EF,"No Records Entered in Report File - Hit <":
                    *V2LON,"ENTER",*HOFF,"> to Continue ";
          KEYIN     ANS;
          GOTO      QEST5000
.
QEST4000  KEYIN     *P1:24,*EL,"Hit ",*V2LON,"<RETURN>",*HOFF:
                    " to continue ",*EOFF,ANS;
QEST5000  MOVE      ZERO,OVRCD
.
QEST9999  RETURN
.
.**********************************************************************
.*                            BULD0000                                *
.*                      Build the Temp File                           *
.**********************************************************************
.
BULD0000  CLOSE     TEMPFILE
          PREP      TEMPFILE,FNAMER
.
          RETURN
.
.**********************************************************************
.*                            KILL0000                                *
.*                        Kill the Temp File                          *
.**********************************************************************
.
KILL0000  CLOSE     TEMPFILE
          PREP      TEMPFILE,FNAMER
          CLOSE     TEMPFILE,DELETE        * Remove temp file
.
          RETURN
.
.********************************************************************
.*                          I/O INCLUDES                            *
.********************************************************************
.
          INC       STD001IO
          INC       IBAHLPIO/INC
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC
.

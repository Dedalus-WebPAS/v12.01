.******************************************************************************
.*    Program      : IBAWAT67  -  Bed Occupancy by Day                        *
.*    Description  : Print Number of patients in bed for a day, ward          *
.*                   and patient specialty.                                   *
.*                                                                            *
.*    Author       : ROD                                                      *
.*    Date         : 26/04/93                                                 *
.*    Modifications:                                                          *
.*                                                                            *
.*        V12.00.01 30/05/2025  Don Nguyen       TSK 0955096                  *
.*                  Alphanumeric visit number changes                         *
.*        V10.05.01 01/08/2015  Ania P                      CAR 261630        *
.*                  Removed use of PORT for temp file naming                  *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V5.08.01  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*                   V5.01.02  05/10/93  ROD                                  *
.*                             Exclude day case patients & fixed print error  *
.*        V5.01.03  26/05/1994 Ian Rutt                                       *
.*                  Fixed Global Recompile Bugs                               * 
.*        V5.01.04  06/07/1994 Rob Leonard                                    *
.*                  Fixed Hospital keyin                                      *
.******************************************************************************
.
          INC       STD001FD
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATDSCFD/INC
          INC       PATWR1FD/INC
          INC       PATTRNFD/INC
          INC       PATHSPFD/INC
          INC       PATCODFD/INC
          INC       PATDAYFD/INC
          INC       IBASEQFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
.
JAN       INIT      "January"
FEB       INIT      "February"
MAR       INIT      "March"
APR       INIT      "April"
MAY       INIT      "May"
JUN       INIT      "June"
JUL       INIT      "July"
AUG       INIT      "August"
SEP       INIT      "September"
OCT       INIT      "October"
NOV       INIT      "November"
DEC       INIT      "December"
.
CMDLINE   DIM       70
CODE      DIM       2
COUNT     FORM      3
DAYTARRY  FORM      3[31]
DAYFNAME  DIM       8
DIFFRNCE  FORM      2
DIM2A     DIM       2
DIM6      DIM       6
DMONTH    DIM       9
FHDR      INIT      "wat67a"
JUSTLINE  INIT      "-------------------------------":
                    "-----------------------------------"
KHOSP     DIM       3
KMONTH    DIM       6
KSPEC     DIM       3
MNTHONLY  FORM      2
PREVDAY   FORM      2
PRNTYR    DIM       4
SAVSPEC   DIM       3
SAVWARD   DIM       3
SPECTOT   FORM      4
WARDTOT   FORM      4
Z30       INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
.
. Temp file definition
TEMPFILE  IFILE     SQL, FIXED=12, KEY="U1-3,4-6,7-8"
XXSPEC    DIM       3      1      * specialty
XXWARD    DIM       3      4      * ward
DXXDAY    DIM       2      7      * Day (01-31)
XXNOPATS  FORM      3      9      * No. patients
.       End of record     12
XXDAY     FORM      2
.
PRGID     INIT      "IBAWAT67"
PRGNAM    INIT      "Bed Occupancy By Day"
VERSION   INIT      "V12.01.00"
+
.***********************************************************************
.*  ML0000  :  Mainline code                                           *
.***********************************************************************
ML0000    CALL      DISPHEAD
          CALL      INIT0000                      * initialisation
.
ML1000    CALL      MENU0000                      * menu
          BRANCH    EXIT,ML9999                   * Exit selected
.
ML2000    CALL      PROC0000                      * process field keyins
          BRANCH    EXIT,ML1000                   * nothing input
.
          CALL      CONTQST                       * Ok to Continue
          BRANCH    CEXIT,ML5000,ML2000,ML1000
.
ML5000    CALL      CRET0000                      * create temp file
.
          CALL      LOAD0000                      * load data into temp file
          CALL      RPRT0000                      * print report (from temp fil)
.
          CALL      KILL0000                      * erase temp file
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.***********************************************************************
.*  INIT0000  :  Initialisation                                        *
.***********************************************************************
INIT0000  DISPLAY   *P55:24,"pathspaf"
          OPEN      PATHSPA1,"pathspaf"
          DISPLAY   *P55:24,"patdschf"
          OPEN      PATDSCH1,"patdschf"
          DISPLAY   *P55:24,"pattranf"
          OPEN      PATTRAN2,"pattranf"
          DISPLAY   *P55:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
          DISPLAY   *P55:24,"patwr1af"
          OPEN      PATWR1A1,"patwr1af"
          DISPLAY   *P55:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          MOVE      ONE,CNEWDS
          CALL      TFILENAM
.
INIT9999  RETURN
+
.***********************************************************************
.*  MENU0000  :  Main Menu                                             *
.***********************************************************************
MENU0000  DISPLAY   *P1:4,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Print Report":
                    *P1:7,"Select option ? "
.
MENU1000  KEYIN     *P17:7,*DV,UNDLN1:
                    *P17:7,*V2LON,MOPTION;
.
          MOVE      ZERO,EXIT
          BRANCH    MOPTION,MENU9999
          IF        MOPTION<>0
            BEEP
            GOTO      MENU1000
          ENDIF
          MOVE      ONE,EXIT                      * Exit
.
MENU9999  RETURN
+
.***********************************************************************
.*  PROC0000  :  Process fields                                        *
.***********************************************************************
PROC0000  DISPLAY   *P1:10,*EF,"Hospital   : "
          MOVE      "10",EVERT
          MOVE      "14",ECOL
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND
.
          CALL      PATHSPKY
          BRANCH    EXIT,PROC9999,PROC0000
.
          MOVE      KEY3,KHOSP
          DISPLAY   *P23:10,PTHSNAME
          MOVE      PTHSNAME,CNAME
.
. keyin month
.
PROC1000  DISPLAY   *P1:12,*EF,"Month/Year : "
          MOVE      "14",CCOL
          MOVE      "12",CVERT
          UNPACK    SP10,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
.
          CALL      KEYMONTH
          BRANCH    CQUEST,PROC1000
          BRANCH    OVRCD,PROC0000
.
          PACK      KMONTH,CCENT,CYEAR,CMON
          REP       " 0",KMONTH
          MOVE      CMON,MNTHONLY                 * save just the month
          LOAD      DMONTH,MNTHONLY,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP:
                                    OCT,NOV,DEC
          PACK      PRNTYR,CCENT,CYEAR
          DISPLAY   *P23:12,DMONTH
.
. keyin specialty
.
PROC2000  DISPLAY   *P1:14,*EF,"Specialty  : "
          MOVE      "14",EVERT
          MOVE      "14",ECOL
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND
          MOVE      "A ",CODE
.
          CALL      PATCODKY
          BRANCH    EXIT,PROC2500,PROC9000
.
          MOVE      ACODE,KSPEC
          DISPLAY   *P23:14,TDESC
          GOTO      PROC9999
.
PROC2500  MOVE      SP3,KSPEC
          DISPLAY   *P14:14,*V2LON,"All"
          MOVE      ZERO,EXIT
          GOTO      PROC9999
.
PROC9000  MOVE      ONE,EXIT                      * EXITCHAR
.
PROC9999  RETURN
+
.***********************************************************************
.*  LOAD0000  :  Load data into temp file                              *
.***********************************************************************
LOAD0000  DISPLAY   *P1:24,*EL,*P20:24,"Day:",*P30:24,"Patient:";
          UNPACK    KMONTH,CCENT,CYEAR
          PACK      DAYFNAME,FILDAYA1,CCENT,CYEAR
          REP       " 0",DAYFNAME
          OPEN      PATDAYA1,DAYFNAME             * open day file
.
. get ALL patients in hospital for period
.
          MOVE      MNTHONLY,KEY2
          REP       " 0",KEY2
          PACK      KEY12,KEY2,ZERO,ONE,SP10
.
          CALL      RSPTDAY1
LOAD1000  CALL      RKPTDAY1
          BRANCH    OVRCD,LOAD9999                * no more records
.
          UNPACK    PTDYDATE,DIM2,DIM2A
          MOVE      DIM2,FORM2
          COMPARE   FORM2,MNTHONLY                * still same month?
          GOTO      LOAD9999 IF NOT EQUAL
.
. check ward is in correct hospital
.
          PACK      KEY30,PTDYADMN,CYEAR,PTDYDATE,Z30
          CALL      RDSTRAN2
          CALL      RDPTRAN2
          BRANCH    OVRCD,LOAD1000
.
          MATCH     PTDYADMN,TADMN
          GOTO      LOAD1000 IF NOT EQUAL
.
          PACK      KEY6,TWARD,TBED
          CALL      RDWARD1
          BRANCH    OVRCD,LOAD1000
.
          MATCH     KHOSP,PTWDHOSN                * same hospital?
          GOTO      LOAD1000 IF NOT EQUAL
.
. check specialty of this patient
.
          MATCH     SP3,KSPEC                     * All Specialties?
          IF        !@EQUAL
            MATCH     KSPEC,TATYPE                * same specialty?
            GOTO      LOAD1000 IF NOT EQUAL
          ENDIF
.
. make sure patient is not a day case
.
          PACK      KEY8,PTDYADMN
          CALL      RDMISS1
          BRANCH    OVRCD,LOAD1000
.
          PACK      KEY5,ANSP,SP1,ACLSS           * check intended managment
          CALL      RDCODE1
          BRANCH    OVRCD,LOAD1000
.
          MATCH     "2",TCINDC2                   * Day case?
          GOTO      LOAD5000 IF NOT EQUAL         * no
.
          PACK      KEY8,PTDYADMN
          CALL      RDDSCH1
          BRANCH    OVRCD,LOAD5000
.
          MATCH     ADATE,DDATE
          GOTO      LOAD1000 IF EQUAL             * patient is a day case
.
. write this patient to the file as a bed occupancy
.
LOAD5000  UNPACK    PTDYDATE,KEY2,KEY2            * get day
          MOVE      TATYPE,XXSPEC
          MOVE      TWARD,XXWARD
          MOVE      KEY2,XXDAY
.
          PACK      KEY8,XXSPEC,XXWARD,XXDAY  
          CALL      RDTEMP1
          IF        OVRCD=0
            ADD       ONE,XXNOPATS
            CALL      UPTEMP1
          ELSE
            MOVE      ONE,XXNOPATS
            CALL      WRTEMP1
          ENDIF
.
          DISPLAY   *P24:24,XXDAY,*P38:24,PTDYADMN;
          GOTO      LOAD1000
.
LOAD9999  RETURN
+
.***********************************************************************
.*  RPRT0000  :  Print Report                                          *
.***********************************************************************
RPRT0000  DISPLAY   *P1:24,*EL,*P35:24,"Printing...";
.
          CALL      IBACLOCK
          MOVE      ZERO,CPAGENO
          MOVE      ONE,CNOUNDLN
          MOVE      "60",CLNO                     * force new page
          MOVE      SP3,SAVWARD
          MOVE      SP3,SAVSPEC
          MOVE      ZERO,SPECTOT  
          MOVE      ZERO,WARDTOT  
          CALL      IDAT0000
.
          PACK      KEY8,SP8
          CALL      RSTEMP1  
RPRT0500  CALL      RKTEMP1                       * get next record
          BRANCH    OVRCD,RPRT9000                * no more records
.
. check if we need a new page
.
          COMPARE   "55",CLNO
          GOTO      RPRT5000 IF LESS
.
. we need a new page
.
          CALL      HEAD132                  
          ENDSET    DMONTH
          PRINT     *40,"Period : ",*+,DMONTH,SP1,PRNTYR,*N
          MOVE      "<<Missing>>",TDESC
          PACK      KEY5,ANSA,SP1,XXSPEC
          CALL      RDCODE1
          PRINT     *1,"Specialty : ",XXSPEC,SP2,TDESC
          PRINT   *N,*1,"Ward  1   2   3   4   5   6   7   8   9  10  11  12  ":
                  "13  14  15  16  17  18  19  20  21  22  23  24  25  26  ":
                  "27  28  29  30  31  TOT"
          PRINT     *1,JUSTLINE,JUSTLINE
          MOVE      TEN,CLNO
.
. check if we need a new specialty
.
RPRT5000  MATCH     SP3,SAVSPEC                   * 1st specialty?
          GOTO      RPRT5300 IF EQUAL
.
          MATCH     XXSPEC,SAVSPEC                * new specialty?
          GOTO      RPRT5200 IF EQUAL
.
.         we have a new specialty
.
          PRINT     *129,WARDTOT                  * print ward total
          PRINT     *1,JUSTLINE,JUSTLINE
          CALL      PSTO0000                      * print totals for a specialty
.
.         print a new page for the new specialty
.
          CALL      HEAD132                  
          ENDSET    DMONTH
          PRINT     *40,"Period : ",*+,DMONTH,SP1,PRNTYR,*N
          MOVE      "<<Missing>>",TDESC
          PACK      KEY5,ANSA,SP1,XXSPEC
          CALL      RDCODE1
          PRINT     *N,*1,"Specialty : ",XXSPEC,SP2,TDESC
          PRINT   *N,*1,"Ward  1   2   3   4   5   6   7   8   9  10  11  12  ":
                  "13  14  15  16  17  18  19  20  21  22  23  24  25  26  ":
                  "27  28  29  30  31  TOT"
          PRINT     *1,JUSTLINE,JUSTLINE
          MOVE      SP3,SAVWARD
          MOVE      ZERO,SPECTOT
          CALL      IDAT0000
          MOVE      TEN,CLNO 
          GOTO      RPRT5300
.
. check if we need to print a new ward
. 
RPRT5200  MATCH     XXWARD,SAVWARD                * new ward?
          GOTO      RPRT5500 IF EQUAL
.
.         print a new ward
.
RPRT5300  MATCH     SP3,SAVWARD                   * printing 1st ward?
          IF        !@EQUAL
            PRINT     *129,WARDTOT                * print ward total
          ENDIF
.
          PRINT     *1,XXWARD,SP1;
          MOVE      ZERO,PREVDAY
          MOVE      ZERO,WARDTOT
          ADD       ONE,CLNO                      * increment line counter
.
. get horizontal position for next print
.
RPRT5500  ASSIGN    (XXDAY-PREVDAY),DIFFRNCE
          ASSIGN    ((DIFFRNCE-1)*4),COUNT
          WHILE     COUNT>0
            PRINT     SP1;                      * goto correct horizontal pos.
            SUB       ONE,COUNT
          DO
.
. print a line
.
          PRINT     XXNOPATS,SP1;                 * print current figure
.  
          ADD       XXNOPATS,DAYTARRY[XXDAY]
          ADD       XXNOPATS,WARDTOT
          ADD       XXNOPATS,SPECTOT
          MOVE      XXWARD,SAVWARD                * save consultant printed
          MOVE      XXDAY,PREVDAY
          MOVE      XXSPEC,SAVSPEC
          GOTO      RPRT0500
.
. no more records
.
RPRT9000  PRINT     *129,WARDTOT                  * print ward total
          PRINT     *1,JUSTLINE,JUSTLINE
          CALL      PSTO0000                      * print totals for a specialty
          PRINT     *N,*1,"*** End of report ***":
                    *N,*N,*1,"NB. Report includes only those patients who were":
                    " in bed at midnight and who are not day case patients."
.
RPRT9999  RETURN
+
.*************************************************************************
.*  PSTO0000  :  Print Totals for a Specialty                            *
.*************************************************************************
PSTO0000  PRINT     *1,"TOT ";
          MOVE      ONE,COUNT
          WHILE     COUNT<32
            IF        COUNT=31
              IF        DAYTARRY[COUNT]=0
                PRINT     SP3;
              ELSE
                PRINT     DAYTARRY[COUNT];
              ENDIF
            ELSE
              IF        DAYTARRY[COUNT]=0
                PRINT     SP4;
              ELSE
                PRINT     DAYTARRY[COUNT],SP1;
              ENDIF
            ENDIF
            ADD       ONE,COUNT
          DO
          PRINT     *129,SPECTOT
.
PSTO9999  RETURN
+
.*************************************************************************
.*  IDAT0000  :  Initialise array                                        *
.*************************************************************************
IDAT0000  MOVE      ONE,COUNT
          WHILE     COUNT<32
            MOVE      ZERO,DAYTARRY[COUNT]
            ADD       ONE,COUNT
          DO
.
IDAT9999  RETURN
+
.*************************************************************************
.*  Temp file IO's                                                       *
.*************************************************************************
RSTEMP1   RESET     KEY8
          READ      TEMPFILE,KEY8;;
          RETURN
.
RDTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READ      TEMPFILE,KEY8;XXSPEC,XXWARD,DXXDAY,XXNOPATS
          GOTO      OVERCOND IF OVER
          MOVE      DXXDAY,XXDAY
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMPFILE;XXSPEC,XXWARD,DXXDAY,XXNOPATS
          GOTO      OVERCOND IF OVER
          MOVE      DXXDAY,XXDAY
          RETURN
.
WRTEMP1   RESET     KEY8
          MOVE      XXDAY,DXXDAY
          WRITE     TEMPFILE,KEY8;XXSPEC,XXWARD,DXXDAY,XXNOPATS
          RETURN
.
UPTEMP1   MOVE      XXDAY,DXXDAY
          UPDATE    TEMPFILE;XXSPEC,XXWARD,DXXDAY,XXNOPATS
          RETURN
+
.*************************************************************************
.*  KILL0000  :  erase temporary file                                    *
.*************************************************************************
KILL0000  CLOSE     TEMPFILE
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    TFILNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
KILL9999  RETURN
+
.*************************************************************************
.*  CRET0000  :  create temporary file                                   *
.*************************************************************************
CRET0000  CALL      KILL0000 
          CLEAR     CMDLINE 
          APPEND    "isbuild ",CMDLINE
          APPEND    TFILNAME,CMDLINE
          APPEND    " 12 U1-3,4-6,7-8",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPFILE,TFILNAME
.
CRET9999  RETURN
.
.
          INC       STD001IO
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATWR1IO/INC
          INC       PATHSPIO/INC
          INC       PATTRNIO/INC
          INC       PATDAYIO/INC
          INC       PATCODIO/INC
          INC       PATDSCIO/INC
          INC       PATHSPKY
          INC       PATCODKY
          INC       TFILENAM
          INC       IBASEQIO/INC
          INC       WEBERRIO/INC
.

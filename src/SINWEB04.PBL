.----------------------------------------------------------------------
. Program       : SINWEB04
.
. Function      : Equipment Orders Web Server
.
. Modifications : 
.----------------------------------------------------------------------
. V11.02.01 10/02/2022  Thanh T          TSK 0889899
.           Recompiled as WATTR1FD changes
. V11.00.01 09/12/2020  Thanh T        TSK 0872840
.           Added PMSCCDFD/PMSCCDIO as PMSPX2TM changes
. V10.11.03 27/11/2017  Thanh T        TSk 0821710
.           Recompiled as PATMASTM changed
. V10.11.02 30/07/2017  Thanh T.       TSK 0821710
.           Audit admission/outpatient/UR comments. changed PATMASTM/PMSPX2TM.
. V10.11.01 14/07/2017  Thanh T.       TSK 0821710
.           Audit Amission/outpatient visit comments. PATMISTM changed
.----------------------------------------------------------------------
. V10.04.02 07.04.2014  Peter Vela   CAR 286158
.           Recompiled for PATMASTM
. V10.04.01 31.03.2014  B.G.Ackland  CAR 299363
.           Replace META Tag Redirect with Javascript in Common RDIREC00
.           Routine
.----------------------------------------------------------------------
. V10.02.01 07/04/2011  Jill Parkinson CAR 239574
.           Removed open of ibaprntf
.----------------------------------------------------------------------
. V9.11.01  06/11/2008  Peter McMullen CAR 174725
.           convert internal javascript to W3C standards
.----------------------------------------------------------------------
. V9.08.01  31/01/2007  Peter Vela     CAR 127930
.           Recompiled for MRTMASFD
.----------------------------------------------------------------------
. V9.05.01  09/03/2006  Ebon Clements CAR 78533
.           Mods for PATCODTM - Hospital specific category codes
. V9.05.B02 30/01/2006  Ebon Clements CAR 93186
.           Mods for REDIRECT length change. Recompiled for IBAWEBDF
. V9.04.04  21/11/2005  Peter Vela    CAR 68675
.           Recompiled for PATDSCTM
. V9.04.03  15/04/2005  Peter Vela    CAR 59696
.           Recompiled for SINSUAFD
. V9.04.02  01/03/2005  Peter Vela    CAR 57547
.           Recompiled for PATVADFD and PATVADIO
. V9.04.01  07/12/2004  Peter Vela               CAR HEALTHSMARTDEMO
.           Created Web Server.
.----------------------------------------------------------------------
          INC       IBAWEBDF    
          INC       WEBDATDF
          INC       RSHWEBDF
.
          INC       PATDO1FD/INC   * Doctor File
          INC       PATMI1FD/INC
          INC       PATMA1FD/INC
          INC       PATVISFD/INC   * Patients Visits File
          INC       PMSVX1FD/INC   * Visit Extension File
          INC       PATCONFD/INC
          INC       PATALRFD/INC
          INC       PATHSPFD/INC
          INC       OUTSITFD/INC
          INC       PMSUCHFD/INC
          INC       PMSUCDFD/INC
          INC       PATMASTD/INC
          INC       PATPR1FD/INC
          INC       PMSPX2FD/INC
          INC       PATFN1FD/INC
          INC       PATWR1FD/INC
          INC       MRTMASFD/INC
          INC       NHIMASFD/INC
          INC       NHIETHFD/INC
          INC       PATCALAG/INC
          INC       PMSPX2TD/INC
          INC       PATRE1FD/INC
          INC       PATDGWFD/INC
          INC       PATDRGFD/INC
          INC       PATITMFD/INC
          INC       PATDADFD/INC
          INC       PATVADFD/INC
          INC       PATCMCFD/INC
          INC       PATASFFD/INC
.
          INC       PMSCCDFD/INC   * Concession Card Details
          INC       PMSHCGTD/INC
          INC       PMSHCGFD/INC
          INC       PMSHCPFD/INC
          INC       PMSHCPTD/INC
          INC       APSMASFD/INC
          INC       PATIN1FD/INC
          INC       PATDSCFD/INC
          INC       VARWISCM/INC
          INC       IBATMDFD/INC
.
          INC       ALLCONFD/INC
          INC       ALLLONFD/INC
          INC       ALLEQUFD/INC
          INC       ALLMAIFD/INC
.
          INC       APSPPDFD/INC
.
          INC       SINCONFD/INC
          INC       SINPOAFD/INC
          INC       SINPOBFD/INC
          INC       SINPOCFD/INC
          INC       SINPODFD/INC
          INC       SINPOFFD/INC
          INC       SINWARFD/INC
          INC       SINTMPDF/INC
          INC       SINCODFD/INC
          INC       SINORIFD/INC
          INC       SINCCAFD/INC
          INC       SINDPCFD/INC
          INC       SINCICFD/INC
          INC       SINCIAFD/INC
          INC       SINSCDFD/INC
          INC       SINSUAFD/INC
          INC       SINWSEFD/INC
.
.----------------------------------------------------------------------
NEXTPAGE  DIM       1       * Nextpage parameter CGI parameter
UPDTTYPE  DIM       1       * Update Type
STARTKEY  DIM       10      * Starting Key
.CURRDATE  DIM       8       * Current Date
DUTPRC10  DIM       10
DTOGST10  DIM       10
FORM3P2   FORM      3.2
FORM4P2   FORM      4.2
FORM5P2   FORM      5.2
FORM8P2   FORM      8.2
FORM12P2  FORM      12.2
FORM8     FORM      8 
FORM3     FORM      3 
CURRROW   FORM      3
LASTROW   FORM      3
LOANTYPE  DIM       20
DOCTCODE  DIM       8
D15A      DIM       15
D11       DIM       11
D12       DIM       12
D35       DIM       35
D40       DIM       40
D45A      DIM       45
D50       DIM       50
DIM3      DIM       3
DIM5      DIM       5
DIM8      DIM       8
PSAGETYP  DIM       3
PNLN1     DIM       13
PNLN2     DIM       13
MBSDESC   DIM       70
PSAGECON  DIM       3
CODERROR  DIM       125     * Output Web error
CATU1     INIT      "U1"
CATU2     INIT      "U2"
CATU3     INIT      "U3"
CATU4     INIT      "U4"
CATU5     INIT      "U5"
CATU6     INIT      "U6"
CATU7     INIT      "U7"
CATVI     INIT      "VI"
OFFUSE    INIT      "Office Use Only: "
COMMENT1  DIM       60
COMMENT2  DIM       60
COMMENT3  DIM       60
COMMENT4  DIM       60
DIM12     DIM       12
DIM15     DIM       15
DIM15A    DIM       15
DIM15B    DIM       15
DIM35     DIM       35
EARLYDEL  DIM       10
FAXNUMB   DIM       30
FUNCTYPE  FORM      2
FUNCCODE  DIM       20
HYPHEN    INIT      "-"
MHGPRT    DIM       9
STATUS    DIM       20
SQUOTE    INIT      "'"
STRCON    DIM       3
ENDCON    DIM       3
STRMSG    DIM       3
ENDMSG    DIM       3
ORIGCODE  DIM       3
INCPRTYN  FORM      1 
INCCOMYN  FORM      1
STRPON    DIM       7
ENDPON    DIM       7
CRELIN1   DIM       35
CRELIN2   DIM       35
CRELIN3   DIM       35
CRELIN4   DIM       35
CRELIN5   DIM       35
SUPLIN1   DIM       35
SUPLIN2   DIM       35
SUPLIN3   DIM       35
SUPLIN4   DIM       35
SUPLIN5   DIM       35
TOTAL     FORM      8.2
TF7P2B    FORM      7.2
TF6P2     FORM      6.2
PACKLINK  DIM       70      * Test
PRNTSTRT  FORM      3
VARIABLE  DIM       127
LOANDATE  DIM       11
EXPRETRN  DIM       11
FONTR     INIT      "<font color=red>"
FONTREND  INIT      "</font>"
WVHAMESS  DIM       40
.
ALLON001  DIM       8      * U/R Number
ALLON002  DIM       8      * Loan Number
ALLON003  DIM       3      * Loan Group (Category QC)
ALLON004  DIM       10     * Equipment Code (allequaf)
ALLON005  DIM       8      * Loan Date (CCYYMMDD)
ALLON006  DIM       8      * Loan Time (HH:MM:SS)
ALLON007  DIM       10     * Therapist WEB User ID (websecaf)
ALLON008  DIM       3      * Type of Loan (Category QL)
ALLON009  DIM       1      * Status
ALLON010  DIM       8      * Expected Return Date (CCYYMMDD)
ALLON011  DIM       8      * Review Date (CCYYMMDD)
ALLON012  DIM       8      * Quantity Issued
ALLON013  DIM       10     * Issuing WEB User ID (websecaf)
ALLON014  DIM       8      * Return Date (CCYYMMDD)
ALLON015  DIM       8      * Return Time (HH:MM:SS)
ALLON016  DIM       10     * Returning WEB User ID (websecaf)
ALLON017  DIM       3      * Condition of Return (Category QO)
ALLON018  DIM       8      * Date Record Created (CCYYMMDD)
ALLON019  DIM       8      * Time Record Created (HH:MM:SS)
ALLON020  DIM       10     * WEB User ID Created (websecaf)
ALLON021  DIM       8      * Date Record Updated (CCYYMMDD)
ALLON022  DIM       8      * Time Record Updated (HH:MM:SS)
ALLON023  DIM       10     * WEB User ID Updated (websecaf)
ALLON024  DIM       8      * Next Review Date 
ALLON025  DIM       3      * Reason for Review 
.
SETNPRNT  FORM      1      * Print Info Sheet
.
ALMAI001  DIM       10     * Equipment Code
ALMAI002  FORM      8      * Maintenance Number
ALMAI003  DIM       8      * Maintenance Date (CCYYMMDD)
ALMAI004  DIM       8      * Maintenance Time (HH:MM:SS)
ALMAI005  DIM       3      * Condition After Main (Cat QM)
ALMAI006  DIM       8      * Hazard Notice Date (CCYYMMDD)
ALMAI007  DIM       10     * Hazard Notice Ref No.
.
LOANNUMB  DIM       8      * Loan Number for loading details off intial List   
HOMELOCA  DIM       3      * Home Location CGI
EQIPTYPE  DIM       3      * Equipment Type CGI
EQIPSTAT  DIM       1      * Equipment Status CGI
YNCFRGHT  DIM       1
TMPPRICE  FORM      12.4
TMPFRGHT  FORM      12.4   
TMPFRGHB  FORM      12.4
TMPDIVIS  FORM      1.1
.
SNORD001  DIM       5      * Cost Centre
SNORD002  DIM       10     * Equipment Code
SNORD003  DIM       20     * Serial Number
SNORD004  DIM       1      * GST Applicable
SNORD005  DIM       8      * Exp'd Rec. Date
SNORD006  FORM      12.2   * Quantity Req'd
SNORD007  FORM      12.4   * Exp. Cost + GST
SNORD008  FORM      12.4   * Exp. GST
SNORD009  FORM      12.2   * Total GST
SNORD010  FORM      12.2   * Total Cost
SNORD011  DIM       1      * Print Order or Fax
SNORD012  DIM       5      * Supplier Code
SNORD013  DIM       7      * Purchase Order Number
SNORD014  DIM       8      * Cancelled Date
SNORD015  DIM       8      * Date Recieved
SNORD016  DIM       15     * Delivery Docket
SNORD017  DIM       1      * Freight Ind
SNORD018  FORM      12.4   * Freight Amount
.
.----------------------------------------------------------------------
PRGID     INIT      "SINWEB04"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "Equipment Orders Web Server"
.----------------------------------------------------------------------
.
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200,MAIN1300
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "Invalid Option",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  CALL      SNORDR00         * Purchase Orders
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1200  CALL      VALSIN00         * Remote Script Validate Purchase Orders
          GOTO      MAIN1000
.
MAIN1300  CALL      REMOTE00         * Remote Script Validate Equipment Code
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
.------------------------------------------------------------
. Output Next Page Based on CGI Parameter nextpage
.------------------------------------------------------------
NXTPAG00  MOVE      ZERO,F1
          MOVE      NEXTPAGE,F1
          BRANCH    F1,NXTPAG10,NXTPAG20,NXTPAG30,NXTPAG40
.
          CALL      SPCCLS00
          GOTO      NXTPAG99
.
NXTPAG10  CALL      RDIREC00
          GOTO      NXTPAG99
.
NXTPAG20  CALL      GOBACK00      * Go Back 1 Html page
          GOTO      NXTPAG99
.
NXTPAG30  CALL      DAH20000      * Go Back 2 html pages
          GOTO      NXTPAG99
.
NXTPAG40  CALL      PREDIR00      * Redirect Parent
          GOTO      NXTPAG99
.
NXTPAG99  RETURN
.------------------------------------------------------------
. Refresh Parent and Close Window
.------------------------------------------------------------
SPCCLS00  CALL      OPENHTML
          BRANCH    EXIT,SPCCLS99
          WRITE     HTMLFILE;"<script type=#"text/javascript#" ":
                             "src=#"../js/Standard.js#"></script>":
                             "<script type=#"text/javascript#" ":
                             "src=#"../js/Custom.js#"></script>":
                             "<script type=#"text/javascript#"> ":
                             " if (self.name==#"PopUpFrame#") {":
                             "  reloadParentFrame();}":
                             " else if (self.name==#"PopUpFrame1#") {":
                             "  reload2ParentFrame();}":
                             " else { ":
                             "  opener.history.go(0);":
                             "  self.close(); }":
                             "</script>"
          CALL      CLOSHTML     * End of Document
SPCCLS99  RETURN
.------------------------------------------------------------------------------
.  Redirect Parent URL
.------------------------------------------------------------------------------
PREDIR00  CALL      OPENHTML
          BRANCH    EXIT,PREDIR90
.
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             " parent.location.href=#"",REDIRECT,"#";":
                             "}":
                             "</script>"
          CALL      CLOSHTML
          GOTO      PREDIR99
.
PREDIR90  DISPLAY   "*** Fifo Not Found ***"
.
PREDIR99  RETURN
.-------------------------------------------------------------------------------
.  Goes back 1 page
.-------------------------------------------------------------------------------
GOBACK00  CALL      OPENHTML
          BRANCH    EXIT,GOBACK99
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "    alert(#"",WEBTITLE,"#");":
                             "    self.close();":
                             "    opener.history.go(-1);":
                             "}":
                             "</script>"
          CALL      CLOSHTML
GOBACK99  RETURN
.------------------------------------------------------------
. Output HTML Error File
.------------------------------------------------------------
CODERR00  CALL      OPENHTML
          BRANCH    EXIT,CODERR95
.
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> ":
          " var PopUpScreen = parent.document.getElementById('PopUpScreen');":
                    "  alert(#"",CODERROR,"#");":
                    "  if (window.name.substr(0,4)=='Hide') {":
                    "    self.close();} else {":
                    "  if (PopUpScreen) {":
                    "    PopUpScreen.style.display='none'; } else {":
                    "    history.go(-1); } }":
                    "</script>",012
.
          CALL      CLOSHTML     * End of Document
          GOTO      CODERR99
.
CODERR95  DISPLAY   "*** Fifo Not Found - ",FILNAM," ***"
.
CODERR99  RETURN
.------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------
INIT0000  CALL      INTMAS00     * Open routine for PATMASTM 
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
          OPEN      PATVISA2,"patvisaf"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      PATRE1A1,"patre1af"
          OPEN      PATDGWA1,"patdgwaf"
          OPEN      PATHSPA1,"pathspaf"
          OPEN      PATITEM1,"patitemn"
          OPEN      PATASFA1,"patasfaf"
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATDRGA3,"patdrgaf"
          OPEN      IBATDET1,"ibatmdaf"
          OPEN      IBASPOL1,"ibaspolf"
          OPEN      IBAPRTA1,"ibaprtaf"
.
          OPEN      ALLLONA1,"alllonaf"
          OPEN      ALLEQUA1,"allequaf"
          OPEN      ALLMAIA1,"allmaiaf"
          OPEN      ALLMAIA2,"allmaiaf"
          OPEN      APSMASA1,"apsmasaf"
          OPEN      APSPPDA1,"apsppdaf"
.
          OPEN      SINPOAA1,"sinpoaaf"
          OPEN      SINPOAA3,"sinpoaaf"
          OPEN      SINPOBA1,"sinpobaf"
          OPEN      SINPOCA1,"sinpocaf"
          OPEN      SINPODA1,"sinpodaf"
          OPEN      SINPOFA1,"sinpofaf"
          OPEN      SINWARA1,"sinwaraf"
          OPEN      SINCCAA1,"sinccaaf"
          OPEN      SINDPCA1,"sindpcaf"
          OPEN      SINCICA1,"sincicaf"
          OPEN      SINCIAA1,"sinciaaf"
          OPEN      SINSCDA1,"sinscdaf"
          OPEN      SINSUAA1,"sinsuaaf"
          OPEN      SINORIA1,"sinoriaf"
          OPEN      SINWSEA1,"sinwseaf"
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,HUND06;*21,ALCNSCOD
          READ      CONTROLF,TWENTY3;*143,HDEFWH
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          IF        IBCNMHOS=1
            OPEN      MLTCODA1,"mltcodaf"
            OPEN      MLTCODA2,"mltcodaf"
          ENDIF
.
INIT9999  RETURN
.------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          MOVE      SP70,TEMPLATE
          MOVE      SP70,NEXTPAGE
          PACK      REDIRECT,SP70,SP70,SP70,SP70
          MOVE      SP70,STARTKEY
          MOVE      SP70,UPDTTYPE
          MOVE      ZERO,SETNPRNT
          MOVE      ZERO,STARTNUM
          MOVE      ZERO,NORECORD
          MOVE      SP70,LOANNUMB
          MOVE      SP70,HOMELOCA
          MOVE      SP70,EQIPTYPE
          MOVE      SP70,EQIPSTAT
          MOVE      ZERO,FUNCTYPE
          MOVE      SP70,FUNCCODE
          MOVE      SP70,SELPRINT
.
          MOVE      Z70,ALLON001
          MOVE      Z70,ALLON002
          MOVE      Z70,ALLON003
          MOVE      Z70,ALLON004
          MOVE      Z70,ALLON005
          MOVE      Z70,ALLON006
          MOVE      Z70,ALLON007
          MOVE      Z70,ALLON008
          MOVE      Z70,ALLON009
          MOVE      Z70,ALLON010
          MOVE      Z70,ALLON011
          MOVE      Z70,ALLON012
          MOVE      Z70,ALLON013
          MOVE      Z70,ALLON014
          MOVE      Z70,ALLON015
          MOVE      Z70,ALLON016
          MOVE      Z70,ALLON017
          MOVE      Z70,ALLON018
          MOVE      Z70,ALLON019
          MOVE      Z70,ALLON020
          MOVE      Z70,ALLON021
          MOVE      Z70,ALLON022
          MOVE      Z70,ALLON023
          MOVE      Z70,ALLON024
          MOVE      Z70,ALLON025
.
          MOVE      SP70,ALMAI001
          MOVE      ZERO,ALMAI002
          MOVE      SP70,ALMAI003
          MOVE      SP70,ALMAI004
          MOVE      SP70,ALMAI005
          MOVE      SP70,ALMAI006
          MOVE      SP70,ALMAI007
.
          MOVE      SP70,SNORD001
          MOVE      SP70,SNORD002
          MOVE      SP70,SNORD003
          MOVE      SP70,SNORD004
          MOVE      SP70,SNORD005
          MOVE      ZERO,SNORD006
          MOVE      ZERO,SNORD007
          MOVE      ZERO,SNORD008
          MOVE      ZERO,SNORD009
          MOVE      ZERO,SNORD010
          MOVE      SP70,SNORD011
          MOVE      SP70,SNORD012
          MOVE      SP70,SNORD013
          MOVE      SP70,SNORD014
          MOVE      SP70,SNORD015
          MOVE      SP70,SNORD016
          MOVE      SP70,SNORD017
          MOVE      ZERO,SNORD018
          MOVE      ZERO,TMPPRICE
          MOVE      SP70,YNCFRGHT
.
CLRPAR99  RETURN
.------------------------------------------------------------
. Set Parameters
.------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "norecord=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NORECORD
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startnum=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTNUM
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updttype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDTTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "urnumber=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,URNUMBER
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "admissno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ADMISSNO
            GOTO      SETPAR99
          ENDIF
.  
          MATCH     "nextpage=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NEXTPAGE
            GOTO      SETPAR99
          ENDIF 
.  
          MATCH     "setnprnt=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SETNPRNT
            GOTO      SETPAR99
          ENDIF 
.
          MATCH     "redirect=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REDIRECT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "homeloca=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,HOMELOCA
            PACK      HOMELOCA,HOMELOCA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "loannumb=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,LOANNUMB
            PACK      LOANNUMB,LOANNUMB,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "eqiptype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,EQIPTYPE
            PACK      EQIPTYPE,EQIPTYPE,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "eqipstat=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,EQIPSTAT
            PACK      EQIPSTAT,EQIPSTAT,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "allnprnt=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SELPRINT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "allon",QRYNAME
          IF        @EQUAL
            CALL      STALN000
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "almai",QRYNAME
          IF        @EQUAL
            CALL      STMAI000
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "selprint=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SELPRINT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "snord",QRYNAME
          IF        @EQUAL
            CALL      STORD000
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "functype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FUNCTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "funccode=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FUNCCODE
            PACK      FUNCCODE,FUNCCODE,SP70
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
.-------------------------------------------------------------------------------
.   Set Parameters for Option 1 - Equipment Loans        
.-------------------------------------------------------------------------------
STALN000  UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,STALN001,STALN002,STALN003,STALN004,STALN005:
                       STALN006,STALN007,STALN008,STALN009,STALN010:
                       STALN011,STALN012,STALN013,STALN014,STALN015:
                       STALN016,STALN017,STALN018,STALN019,STALN020:
                       STALN021,STALN022,STALN023,STALN024,STALN025
          GOTO      STALN999
.
STALN001  MOVE      QRYDATA,ALLON001
          PACK      ALLON001,ALLON001,SP70
          GOTO      STALN999
.
STALN002  MOVE      QRYDATA,ALLON002
          PACK      ALLON002,ALLON002,SP70
          GOTO      STALN999
.
STALN003  MOVE      QRYDATA,ALLON003
          GOTO      STALN999
.
STALN004  SQUEEZE   QRYDATA
          MOVE      QRYDATA,ALLON004
          PACK      ALLON004,ALLON004,SP70
          GOTO      STALN999
.
STALN005  UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00
          PACK      ALLON005,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ALLON005
          GOTO      STALN999
.
STALN006  MOVE      QRYDATA,ALLON006
          GOTO      STALN999
.
STALN007  MOVE      QRYDATA,ALLON007
          GOTO      STALN999
.
STALN008  MOVE      QRYDATA,ALLON008
          GOTO      STALN999
.
STALN009  MOVE      QRYDATA,ALLON009
          GOTO      STALN999
.
STALN010  RESET     QRYDATA 
          MATCH     SP70,QRYDATA
          GOTO      STALN999 IF EQUAL 
.
          UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00
          PACK      ALLON010,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ALLON010
          GOTO      STALN999
.
STALN011  RESET     QRYDATA 
          MATCH     SP70,QRYDATA
          GOTO      STALN999 IF EQUAL 
.
          UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00
          PACK      ALLON011,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ALLON011
          GOTO      STALN999
.
STALN012  MOVE      QRYDATA,ALLON012
          GOTO      STALN999
.
STALN013  MOVE      QRYDATA,ALLON013
          GOTO      STALN999
.
STALN014  UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00
          PACK      ALLON014,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ALLON014
          GOTO      STALN999
.
STALN015  MOVE      QRYDATA,ALLON015
          GOTO      STALN999
.
STALN016  MOVE      QRYDATA,ALLON016
          GOTO      STALN999
.
STALN017  MOVE      QRYDATA,ALLON017
          GOTO      STALN999
.
STALN018  MOVE      QRYDATA,ALLON018
          GOTO      STALN999
.
STALN019  MOVE      QRYDATA,ALLON019
          GOTO      STALN999
.
STALN020  MOVE      QRYDATA,ALLON020
          GOTO      STALN999
.
STALN021  MOVE      QRYDATA,ALLON021
          GOTO      STALN999
.
STALN022  MOVE      QRYDATA,ALLON022
          GOTO      STALN999
.
STALN023  MOVE      QRYDATA,ALLON023
          GOTO      STALN999
.
STALN024  RESET     QRYDATA
          MATCH     SP70,QRYDATA
          GOTO      STALN999 IF EQUAL 
.
          UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00
          PACK      ALLON024,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ALLON024
          GOTO      STALN999
.
STALN025  MOVE      QRYDATA,ALLON025
          GOTO      STALN999
.
STALN999  RETURN
.-------------------------------------------------------------------------------
.   Set Parameters for Option 2 - Equipment Service Maintenance
.-------------------------------------------------------------------------------
STMAI000  UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,STMAI001,STMAI002,STMAI003,STMAI004,STMAI005:
                       STMAI006,STMAI007
          GOTO      STMAI999
.
STMAI001  MOVE      QRYDATA,ALMAI001
          PACK      ALMAI001,ALMAI001,SP70
          GOTO      STMAI999
.
STMAI002  MOVE      QRYDATA,ALMAI002
          GOTO      STMAI999
.
STMAI003  UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00
          PACK      ALMAI003,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ALMAI003
          GOTO      STMAI999
.
STMAI004  MOVE      QRYDATA,ALMAI004
          GOTO      STMAI999
.
STMAI005  MOVE      QRYDATA,ALMAI005
          GOTO      STMAI999
.
STMAI006  RESET     QRYDATA 
          MATCH     SP70,QRYDATA
          GOTO      STMAI999 IF EQUAL 
.
          UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00
          PACK      ALMAI006,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ALMAI006
          GOTO      STMAI999
.
STMAI007  MOVE      QRYDATA,ALMAI007
          GOTO      STMAI999
.
STMAI999  RETURN
.-------------------------------------------------------------------------------
.   Set Parameters for Option 1 - Equipment  Orders
.-------------------------------------------------------------------------------
STORD000  UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,STORD001,STORD002,STORD003,STORD004,STORD005:
                       STORD006,STORD007,STORD008,STORD009,STORD010:
                       STORD011,STORD012,STORD013,STORD014,STORD015:
                       STORD016,STORD017,STORD018
          GOTO      STORD999
.
STORD001  MOVE      QRYDATA,SNORD001
          GOTO      STORD999
.
STORD002  MOVE      QRYDATA,SNORD002
          GOTO      STORD999
.
STORD003  MOVE      QRYDATA,SNORD003
          GOTO      STORD999
.
STORD004  MOVE      QRYDATA,SNORD004
          GOTO      STORD999
.
STORD005  RESET     QRYDATA
          MATCH     SP70,QRYDATA
          GOTO      STORD999 IF EQUAL
.
          UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00
          PACK      SNORD005,CCENT,CYEAR,CMON,CDAY
          REP       " 0",SNORD005
          GOTO      STORD999
.
STORD006  MOVE      QRYDATA,SNORD006
          GOTO      STORD999
.
STORD007  MOVE      QRYDATA,SNORD007
          GOTO      STORD999
.
STORD008  MOVE      QRYDATA,SNORD008
          GOTO      STORD999
.
STORD009  MOVE      QRYDATA,SNORD009
          GOTO      STORD999
.
STORD010  MOVE      QRYDATA,SNORD010
          GOTO      STORD999
.
STORD011  MOVE      QRYDATA,SNORD011
          GOTO      STORD999
.
STORD012  MOVE      QRYDATA,SNORD012
          GOTO      STORD999
.
STORD013  MOVE      QRYDATA,SNORD013
          GOTO      STORD999
.
STORD014  RESET     QRYDATA
          MATCH     SP70,QRYDATA
          GOTO      STORD999 IF EQUAL
.
          UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00
          PACK      SNORD014,CCENT,CYEAR,CMON,CDAY
          REP       " 0",SNORD014
          GOTO      STORD999
.
STORD015  RESET     QRYDATA
          MATCH     SP70,QRYDATA
          GOTO      STORD999 IF EQUAL
.
          UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00
          PACK      SNORD015,CCENT,CYEAR,CMON,CDAY
          REP       " 0",SNORD015
          GOTO      STORD999
.
STORD016  MOVE      QRYDATA,SNORD016
          GOTO      STORD999
.
STORD017  MOVE      QRYDATA,SNORD017
          GOTO      STORD999
.
STORD018  MOVE      QRYDATA,SNORD018
          GOTO      STORD999
.
STORD999  RETURN
.------------------------------------------------------------
. Equipment Orders
.------------------------------------------------------------
SNORDR00  PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          MATCH     SP70,UPDTTYPE
          GOTO      SNORDR70 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add Formaction
          GOTO      SNORDR10 IF EQUAL
.
          MATCH     "U",UPDTTYPE     * Update Formaction
          GOTO      SNORDR20 IF EQUAL
.
          MATCH     "D",UPDTTYPE     * Update Formaction
          GOTO      SNORDR30 IF EQUAL
.
          GOTO      SNORDR90
.
.   ************ ADD FORMACTION ************
.
SNORDR10  PACK      KEY5,SNORD001,SP70         * Validate Cost Centre
          CALL      RDSICA1
          BRANCH    OVRCD,SNORDR96
.
          PACK      KEY5,SNORD012,SP70         * Validate Supplier
          CALL      RDAPMA1
          BRANCH    OVRCD,SNORDR91
.
          PACK      KEY5,HDEFWH,SP70         * Validate Warehouse
          CALL      RDSIWA1
          BRANCH    OVRCD,SNORDR92
.
          PACK      KEY10,SNORD002,SP70        * Validate Equipment
          CALL      RDALEQU1
          BRANCH    OVRCD,SNORDR93
.
          PACK      SIWAWAR,HDEFWH,SP70      * Generate Purchase Order Number
          CALL      SINORDNO
          BRANCH    OVRCD,SNORDR94
          MOVE      SIPAPON,SIPCPON
          MOVE      SIPAPON,SIPFPON
.
          CALL      CLRPOA00
          CALL      SAVPOA00
          MOVE      CURRDATE,SIPADIN           * Date Inputted
          MOVE      HDEFWH,SIPAWAR             * Warehouse
.
          CALL      WRSIPA1                    * Write Purchase Order Header
.
          CALL      CLRPOC00
          CALL      SAVPOC00
          MOVE      "  1",SIPCITM              * Item Number
          MOVE      HDEFWH,SIPCWAR             * Warehouse
          MOVE      ALEQDESC,SIPCPD            * Part Description
.
          PACK      KEY6,CCC,CYY,CMM,SP70      * Fin Year Period
          REP       " 0",KEY6
          CALL      RDDRGA3        
          BRANCH    OVRCD,SNORDR95
          PACK      SIPCDAT,DRGYR,DRGNUM
.
          CALL      WRSIPC1                    * Write Purchase Order Detail
.
          MATCH     "1",SNORD017               * If Freight tickbox is checked
          IF        @EQUAL
            MOVE      "  2",SIPCITM
            MOVE      "FREIGHT",SIPCPD
            MOVE      SP70,SIPCPN
            MOVE      SNORD018,SIPCECT
            MOVE      SNORD018,TMPFRGHB
.
            MOVE      ZERO,SIPCGSTA
            MATCH     "1",SNORD004
            IF        @EQUAL
              MOVE      "1.1",TMPDIVIS
              MOVE      SNORD018,TMPFRGHT
              DIVIDE    TMPDIVIS,TMPFRGHT
              SUB       TMPFRGHT,TMPFRGHB
              MOVE      TMPFRGHB,SIPCGSTA
            ENDIF
            MOVE      ONE,SIPCOQT
.
            CALL      WRSIPC1
          ENDIF
.
.         CALL      CLRPOF00
.         CALL      SAVPOF00
.         MOVE      "  1",SIPFITM
.         MOVE      " 1",SIPFREC
.
.         CALL      WRSIPF1          
.
.         ***** Print or Fax ******
.
          MATCH     "1",SNORD011
          IF        @EQUAL
            MOVE      SIPAPON,STRPON
            MOVE      SIPAPON,ENDPON
            MOVE      SP70,ORIGCODE
            MOVE      ZERO,INCCOMYN
            MOVE      ZERO,INCPRTYN
            MOVE      SP70,STRMSG
            MOVE      Z70,ENDMSG
            MOVE      SP70,STRCON
            MOVE      Z70,ENDCON 
            CALL      FAXPO000
          ELSE
            MATCH     SP70,SELPRINT
            GOTO      SNORDR15 IF EQUAL
            GOTO      SNORDR15 IF EOS
            CALL      SINPRT00
          ENDIF
.
SNORDR15  GOTO      SNORDR99
.
.
.   ************ UPDATE FORMACTION ************
.
SNORDR20  CALL      SRPO0000
          GOTO      SNORDR99
.
.
.   ************ DELETE FORMACTION ************
.
SNORDR30  CALL      SCPO0000
          GOTO      SNORDR99
.
.
.   ************ DISPLAY FORMACTION ***********
.
SNORDR70  RJUSTIFY  SNORD013
          PACK      KEY7,SNORD013,SP70
          CALL      RDSIPA1
          IF        OVRCD
            MOVE      SP70,SIPAPON
            CALL      CLRPOA00
          ENDIF
.
          PACK      KEY10,SNORD013,SP1,SP1,ONE,SP70 
          CALL      RDSIPC1
          IF        OVRCD
            MOVE      SP70,SIPCPON
            CALL      CLRPOC00
          ELSE
            CALL      RKSIPC1
            IF        OVRCD
              MOVE      SP70,SIPCPON
              CALL      CLRPOC00
            ELSE
              MATCH     SNORD013,SIPCPON
              IF        @EQUAL
                MOVE      SIPCECT,TMPPRICE
                MOVE      ONE,YNCFRGHT
              ELSE
                MOVE      ZERO,TMPPRICE
                MOVE      ZERO,YNCFRGHT
              ENDIF
            ENDIF 
            PACK      KEY10,SNORD013,SP1,SP1,ONE,SP70
            CALL      RDSIPC1
          ENDIF
.
SNORDR75  PACK      KEY12,SNORD013,SP1,SP1,ONE,SP1,ONE,SP70
          CALL      RDSIPF1
          IF        OVRCD
            MOVE      SP70,SIPFPON
            CALL      CLRPOF00
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Purchase Orders",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,SNORDR99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      SNORDR99
.
SNORDR90  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SNORDR99
.
SNORDR91  MOVE      "Invalid Supplier.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SNORDR99
.
SNORDR92  MOVE      "Invalid Warehouse.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SNORDR99
.
SNORDR93  MOVE      "Invalid Equipment.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SNORDR99
.
SNORDR94  MOVE      "Warehouse Record Locked.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SNORDR99
.
SNORDR95  MOVE      "Invalid Financial Year Period.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SNORDR99
.
SNORDR96  MOVE      "Invalid Cost Centre.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SNORDR99
.
SNORDR99  RETURN
.********************************************************************
. Single Order Print
.********************************************************************
SINPRT00
          REP       " 0",SIPADCN
          MATCH     "00000000",SIPADCN           * cancelled ?
          IF        !@EQUAL
            MOVE      "Purchase order has been cancelled.",WEBTITLE
            CALL      WEBERR00
            MOVE      ONE,EXIT
            GOTO      SINPRT99
          ELSE
            REP       " 0",SIPADCM
            MATCH     "00000000",SIPADCM           * completed ?
            IF        !@EQUAL
              MOVE      "Purchase order has been completed.",WEBTITLE
              CALL      WEBERR00
              MOVE      ONE,EXIT
              GOTO      SINPRT99
            ENDIF
          ENDIF
.
          MOVE      ZERO,NOPRINT
          CALL      SINORDPR                     * print purchase order
          MOVE      ZERO,EXIT
.
SINPRT99  RETURN
.------------------------------------------------------------
. Process Fields 
.------------------------------------------------------------
PROFLD00  BRANCH    REPORTNO,PROFLD10
.
          GOTO      PROFLD99 
.
PROFLD10  BRANCH    FLDSETNO,PROFLD11
          GOTO      PROFLD99
.
PROFLD11  CALL      SINN0000    * Equipment Orders
          GOTO      PROFLD99  
.
PROFLD99  RETURN
.------------------------------------------------------------------------------
. Equipment Orders
.------------------------------------------------------------------------------
SINN0000  BRANCH    FLDITMNO,SINN0100,SINN0200,SINN0300,SINN0400,SINN0500:
                             SINN0600,SINN0700,SINN0800,SINN0900,SINN1000:
                             SINN1100,SINN1200,SINN1300,SINN1400,SINN1500
. 
          GOTO      SINN9999
.
SINN0100  WRITE     HTMLFILE;SIPACST;               * Cost Centre
          GOTO      SINN9999
.
SINN0200  WRITE     HTMLFILE;SIPAPON;               * P/O Number
          GOTO      SINN9999
.
SINN0300  WRITE     HTMLFILE;SIPCPD;                * Part Description
          GOTO      SINN9999
.
SINN0400  WRITE     HTMLFILE;SIPASUP;               * Supplier Code
          GOTO      SINN9999
.
SINN0500  PACK      KEY5,SIPASUP,SP70               * Supplier Description
          CALL      RDAPMA1
          IF        OVRCD
            WRITE     HTMLFILE;SP30;
          ELSE
            WRITE     HTMLFILE;APMASN1;
          ENDIF
          GOTO      SINN9999
.
SINN0600  MATCH     "AUSGST",SIPCGST                * GST Applicable
          IF        @EQUAL
            WRITE     HTMLFILE;"1";
          ELSE
            WRITE     HTMLFILE;"0";
          ENDIF
          GOTO      SINN9999
.
SINN0700  WRITE     HTMLFILE;SIPCOQT;               * Quantity Required
          GOTO      SINN9999
.
SINN0800  WRITE     HTMLFILE;SIPCECT;               * Expected Cost + GST
          GOTO      SINN9999
.
SINN0900  WRITE     HTMLFILE;SIPCPN;                * Part/Serial Number
          GOTO      SINN9999
.
SINN1000  MATCH     SP70,SIPCEDD
          IF        !@EQUAL
            UNPACK    SIPCEDD,CCENT,CYEAR,CMON,CDAY   * Expected Date
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          ENDIF
          GOTO      SINN9999
.
SINN1100  WRITE     HTMLFILE;SIPCGSTA;              * Expected GST
          GOTO      SINN9999
.
SINN1200  MATCH     SP70,SIPADCM
          IF        !@EQUAL
            UNPACK    SIPADCM,CCENT,CYEAR,CMON,CDAY   * Completed Date
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          ENDIF
          GOTO      SINN9999
.
SINN1300  MATCH     SP70,SIPADCN
          IF        !@EQUAL
            UNPACK    SIPADCN,CCENT,CYEAR,CMON,CDAY   * Cancelled Date
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          ENDIF
          GOTO      SINN9999
.
SINN1400  WRITE     HTMLFILE;TMPPRICE;
          GOTO      SINN9999
.
SINN1500  WRITE     HTMLFILE;YNCFRGHT;
          GOTO      SINN9999
.
SINN9999  RETURN
.-----------------------------------------------------------
. Link to Next List Page
.-----------------------------------------------------------
NEXT0000  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
.
          REP       " +",ALMAI001
.
          WRITE     HTMLFILE;"allweb04.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&almai001=",ALMAI001;
.
          REP       "+ ",ALMAI001
.
NEXT9999  RETURN
.-----------------------------------------------------------
. Link to Prev List Page
.-----------------------------------------------------------
PREV0000  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3
          IF        F3<0
            MOVE      ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
.
          REP       " +",ALMAI001
.
          WRITE     HTMLFILE;"allweb04.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&almai001=",ALMAI001;
.
          REP       "+ ",ALMAI001
.
PREV9999  RETURN
.----------------------------------------------------------------------
. Include to allocate Next Purchase Order Number
. Input
. SIWAWAR - Warehouse Code
.
. Output
. SIPAPON - Purchase Order Number
.----------------------------------------------------------------------
SINORDNO
          MOVE     ZERO,COUNT
          MOVE     SIWAWAR,KEY5
.
SINORD05  CALL     RLSIWA1
          BRANCH   OVRCD,SINORD99,SINORD10
          MOVE     SIWANPO,ORDERNO
          SUB      ONE,ORDERNO
.
          REPEAT
            ADD      ONE,ORDERNO
            MOVE     ORDERNO,KEY7
            CALL     RASIPA1
          UNTIL    (OVRCD=1)
.
          MOVE     ORDERNO,SIPAPON
          ADD      ONE,ORDERNO
          MOVE     ORDERNO,SIWANPO
          CALL     UPSIWA1
          CALL     UUSIWA1
          MOVE     ZERO,OVRCD
          GOTO     SINORD99
.
SINORD10  ADD      ONE,COUNT
          DISPLAY  *P1:24,*W;
          COMPARE  FIVE,COUNT
          GOTO     SINORD05 IF NOT EQUAL
          MOVE     ZERO,COUNT
.
SINORD20  MOVE     ONE,OVRCD
.
SINORD99  RETURN
.----------------------------------------------------------------------
. Clear SINPOAFD
.----------------------------------------------------------------------
CLRPOA00  MOVE     SP70,SIPASUP
          MOVE     SP70,SIPAORI
          MOVE     SP70,SIPAICN
          MOVE     SP70,SIPASCN  
          MOVE     SP70,SIPADEL
          MOVE     SP70,SIPAMES
          MOVE     SP70,SIPADIN  
          MOVE     SP70,SIPADPR  
          MOVE     SP70,SIPADCN  
          MOVE     SP70,SIPADCM 
          MOVE     SP70,SIPACON  
          MOVE     SP70,SIPAIDP 
          MOVE     SP70,SIPAUR1
          MOVE     SP70,SIPAUR2 
          MOVE     SP70,SIPAUD1  
          MOVE     SP70,SIPAUD2 
          MOVE     SP70,SIPAUC1 
          MOVE     SP70,SIPAUC2 
          MOVE     SP70,SIPAUY1 
          MOVE     SP70,SIPAUY2 
          MOVE     SP70,SIPACST  
          MOVE     SP70,SIPAWAR 
          MOVE     SP70,SIPAPNT  
          MOVE     SP70,SIPAMDP  
          MOVE     SP70,SIPASPA   
.
CLRPOA99  RETURN
.----------------------------------------------------------------------
. Clear SINPOCFD
.----------------------------------------------------------------------
CLRPOC00  MOVE     SP70,SIPCITM
          MOVE     SP70,SIPCCST
          MOVE     SP70,SIPCPRD
          MOVE     SP70,SIPCDAT
          MOVE     SP70,SIPCWAR
          MOVE     SP70,SIPCCAT
          MOVE     SP70,SIPCPN
          MOVE     SP70,SIPCPD
          MOVE     SP70,SIPCCON
          MOVE     SP70,SIPCCDT
          MOVE     SP70,SIPCSUT
          MOVE     SP70,SIPCEDD
          MOVE     SP70,SIPCFDD
          MOVE     ZERO,SIPCOQT
          MOVE     ZERO,SIPCIQT
          MOVE     ZERO,SIPCRQT
          MOVE     ZERO,SIPCECT
          MOVE     ZERO,SIPCGSTA
          MOVE     ZERO,SIPCTCR
          MOVE     ZERO,SIPCTGS
          MOVE     ZERO,SIPCTIN
          MOVE     ZERO,SIPCIGS
          MOVE     ZERO,SIPCOQO
          MOVE     ZERO,SIPCOOP
          MOVE     ZERO,SIPCOGS 
          MOVE     SP70,SIPCGST
          MOVE     SP70,SIPCUR1
          MOVE     SP70,SIPCUR2
          MOVE     SP70,SIPCUD1
          MOVE     SP70,SIPCUD2
          MOVE     SP70,SIPCUY1
          MOVE     SP70,SIPCUY2
          MOVE     SP70,SIPCSPA
.
CLRPOC99  RETURN
.----------------------------------------------------------------------
. Clear SINPOFFD
.----------------------------------------------------------------------
CLRPOF00  MOVE      SP70,SIPFITM
          MOVE      SP70,SIPFREC
          MOVE      SP70,SIPFDAT
          MOVE      SP70,SIPFDEL
          MOVE      ZERO,SIPFQTY
          MOVE      ZERO,SIPFCST
          MOVE      ZERO,SIPFGST
          MOVE      SP70,SIPFSUT
          MOVE      SP70,SIPFNDD
          MOVE      SP70,SIPFDUP
          MOVE      SP70,SIPFUDT
          MOVE      SP70,SIPFEDD
          MOVE      SP70,SIPFSPA
.
CLRPOF99  RETURN
.----------------------------------------------------------------------
. Assign SINPOAFD Variables
.----------------------------------------------------------------------
SAVPOA00  MATCH     Z70,SNORD012
          IF        !@EQUAL
            MOVE      SNORD012,SIPASUP         * Supplier Code
          ENDIF
.
          MATCH     Z70,SNORD001
          IF        !@EQUAL
            MOVE      SNORD001,SIPACST         * Cost Centre
          ENDIF
.
SAVPOA99  RETURN
.----------------------------------------------------------------------
. Assign SINPOCFD Variables
.----------------------------------------------------------------------
SAVPOC00  MATCH     Z70,SNORD001
          IF        !@EQUAL
            MOVE      SNORD001,SIPCCST         * Cost Centre
          ENDIF
.
          MATCH     Z70,SNORD003
          IF        !@EQUAL
            MOVE      SNORD003,SIPCPN          * Part Number
          ENDIF
.
          MATCH     Z70,SNORD005
          IF        !@EQUAL
            MOVE      SNORD005,SIPCEDD         * Expected Delivery Date
          ENDIF
.
          COMPARE   ZERO,SNORD006
          IF        !@EQUAL
            MOVE      SNORD006,SIPCOQT         * Quantity Req'd
          ENDIF
.
          COMPARE   ZERO,SNORD007
          IF        !@EQUAL
            MOVE      SNORD007,SIPCECT         * Expected Cost
          ENDIF
.
          COMPARE   ZERO,SNORD008
          IF        !@EQUAL
            MOVE      SNORD008,SIPCGSTA        * Expected GST
          ENDIF
.
          MATCH     Z70,SNORD004
          IF        !@EQUAL
            MATCH     "1",SNORD004
            IF        @EQUAL
              MOVE      "AUSGST",SIPCGST       * If GST applicable then AUSGST
            ENDIF 
          ENDIF 
.
SAVPOC99  RETURN
.----------------------------------------------------------------------
. Assign SINPOFFD Variables
.----------------------------------------------------------------------
SAVPOF00  COMPARE   ZERO,SNORD007
          IF        !@EQUAL
            MOVE      SNORD007,SIPFCST         * Supplier Code
          ENDIF
.
          COMPARE   ZERO,SNORD008
          IF        !@EQUAL
            MOVE      SNORD008,SIPFGST         * Cost Centre
          ENDIF
.
          MATCH     Z70,SNORD005
          IF        !@EQUAL
            MOVE      SNORD005,SIPFEDD         * Expected Delivery Date
          ENDIF
.
SAVPOF99  RETURN
.------------------------------------------------------------
. FAXPO - Fax Purchase Orders.
.------------------------------------------------------------
FAXPO000  MOVE      ONE,NOPRINT                  * we'll control printing here !
.         MOVE      ZERO,HLEF
.         CALL      GETSCR00
          IF        INCPRTYN=1
            CALL      FXALL000
          ELSE
            CALL      FXOUT000
          ENDIF
.         CALL      PUTSCR00
          RETURN
.------------------------------------------------------------
. Fax Orders Not Yet Printed
.------------------------------------------------------------
FXOUT000  PACK      KEY15,SP8,STRPON,SP70
          CALL      RSSIPA3
          CALL      RPSIPA3
.
FXOUT100  CALL      RKSIPA3
          BRANCH    OVRCD,FXOUT900
          MATCH     SIPADPR,SP70
          GOTO      FXOUT900 IF NOT EQUAL        * no more outstanding ?
          MATCH     SIPAPON,ENDPON
          GOTO      FXOUT900 IF LESS             * out of range ?
.
. check warehouse security
.
          MATCH     SP70,SIPAWAR
          IF        !@EQUAL
            PACK      KEY9,PASSCODE,SIPAWAR,SP70
            CALL      RDSIWS1
            BRANCH    OVRCD,FXOUT100
          ENDIF
.
          CALL      FXRAN000
          BRANCH    EXIT,FXOUT100
.
          CALL      SINORDFX                     * fax purchase order
          GOTO      FXOUT100                     * get next record
.
FXOUT900  MOVE      ZERO,EXIT
          RETURN
.------------------------------------------------------------
. Fax All Orders in Range
.------------------------------------------------------------
FXALL000  PACK      KEY7,STRPON,SP70
          CALL      RDSIPA1
          IF        OVRCD=0
            CALL      RPSIPA1
          ENDIF
FXALL100  CALL      RKSIPA1
          BRANCH    OVRCD,FXALL900
          MATCH     SIPAPON,ENDPON
          GOTO      FXALL900 IF LESS             * out of range ?
.
. check warehouse security
.
          MATCH     SP70,SIPAWAR
          IF        !@EQUAL
            PACK      KEY9,PASSCODE,SIPAWAR,SP70
            CALL      RDSIWS1
            BRANCH    OVRCD,FXALL100
          ENDIF
.
          CALL      FXRAN000
          BRANCH    EXIT,FXALL100
.
          CALL      SINORDFX                     * fax purchase order
          GOTO      FXALL100                     * get next record
.
FXALL900  MOVE      ZERO,EXIT
          RETURN
.------------------------------------------------------------
. Check Range Details
.------------------------------------------------------------
FXRAN000  MOVE      ZERO,EXIT
          MATCH     ORIGCODE,SP70
          IF        !@EQUAL
            MATCH     ORIGCODE,SIPAORI
            GOTO      FXRAN900 IF NOT EQUAL   * correct originator ?
          ENDIF
.
          IF        INCCOMYN=0
            REP       " 0",SIPADCM
            MATCH     "00000000",SIPADCM
            GOTO      FXRAN900 IF NOT EQUAL   * completed ?
          ENDIF
.
          REP       " 0",SIPADCN
          MATCH     "00000000",SIPADCN
          GOTO      FXRAN900 IF NOT EQUAL     * cancelled ?
.
          MATCH     STRMSG,SIPAMES
          GOTO      FXRAN900 IF LESS
          MATCH     SIPAMES,ENDMSG
          GOTO      FXRAN900 IF LESS
.
          MATCH     STRCON,SIPACON
          GOTO      FXRAN900 IF LESS
          MATCH     SIPACON,ENDCON
          GOTO      FXRAN900 IF LESS
.
          MOVE      SIPASUP,KEY5
          CALL      RDAPMA1
          BRANCH    OVRCD,FXRAN900
          MATCH     SP70,APMAUR2              * Must have a Fax Number to Fax
          GOTO      FXRAN900 IF EQUAL
.
          GOTO      FXRAN999
.
FXRAN900  MOVE      ONE,EXIT
FXRAN999  RETURN
.**********************************************************************
.*                            SPLDELET                                *
.*                     Delete the Spool File                          *
.**********************************************************************
SPLDELET  PACK        CSPDESC,CSPDESC,SP20
          MATCH       SP20,CSPDESC
          IF          !(@EQUAL)
            PREPARE     SPLTFILE,CSPDESC
            CLOSE       SPLTFILE
.
            OPEN        IBASPOL1,"ibaspolf"
            MOVE        SPLFILE,KEY8
            CALL        DESPOL1              * delete from files spooled
          ENDIF
          MOVE        SP8,SPLFILE
          RETURN
.
.------------------------------------------------------------------------------
.*                             SRPO0000                                       *
.*           Schedule reciept of purchase order (IBASIN57)                    *
.------------------------------------------------------------------------------
SRPO0000  MOVE      "IBASIN57",SCHDPGID
          MOVE      "Update New to Purchase Order Receivals File",SCHDDESC
          PACK      SCHDDATE,CCC,CYY,CMM,CDD
          REP       " 0",SCHDDATE
          CLOCK     TIME,SCHDTIME
.
.         Write Keyin parameters for Extract
.
          MOVE      SNORD013,KEYSTARR[1]            * Purchase order Number
.
          UNPACK    SNORD015,CCENT,CYEAR,CMON,CDAY          
          PACK      DIM8,CDAY,CMON,CCENT,CYEAR
          MOVE      DIM8,KEYSTARR[2]                * Recieved Date
.
          MOVE      SNORD016,KEYSTARR[3]            * Delivery Docket
.
          MOVE      ANSN,KEYSTARR[4]                * Part Delivery No
.
          MOVE      ANSP,KEYSTARR[5]                * Post
.
          MOVE      ANSP,KEYSTARR[6]                * Post Again
.
          MOVE      SIX,KEYSTCNT                    * Number of Keyins
.
          CALL      SCHPRO00   * Schedule Extract
.
SRPO9999  RETURN
.
.------------------------------------------------------------------------------
.*                             SCPO0000                                       *
.*           Schedule cancel of purchase order (IBASIN58)                     *
.------------------------------------------------------------------------------
SCPO0000  MOVE      "IBASIN58",SCHDPGID
          MOVE      "Cancel Purchase Order",SCHDDESC
          PACK      SCHDDATE,CCC,CYY,CMM,CDD
          REP       " 0",SCHDDATE
          CLOCK     TIME,SCHDTIME
.
.         Write Keyin parameters for Extract
.
          MOVE      SNORD013,KEYSTARR[1]            * Purchase order Number
.
          MOVE      TWO,KEYSTARR[2]                 * Option Number 2
.
          UNPACK    SNORD014,CCENT,CYEAR,CMON,CDAY
          PACK      DIM8,CDAY,CMON,CCENT,CYEAR
          MOVE      DIM8,KEYSTARR[3]                * Cancel Date
.
          MOVE      ANSP,KEYSTARR[4]                * Post
.
          MOVE      FOUR,KEYSTCNT                   * Number of Keyins
.
          CALL      SCHPRO00   * Schedule Extract
.
SCPO9999  RETURN
.
.**********************************************************************
.*                            OPTNS000                                *
.**********************************************************************
OPTNS000
OPTNS999  RETURN
.**********************************************************************
.*                            CLSBOX00                                *
.**********************************************************************
CLRBX000
CLRBX999  RETURN
.------------------------------------------------------------
. Validate Purchase Order Number
.------------------------------------------------------------
VALSIN00  CALL      XMLHED00
          BRANCH    EXIT,VALSIN99
          PACK      KEY7,SNORD013,SP70
          CALL      RDSIPA1
          BRANCH    OVRCD,VALSIN90
.
          WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>":
                             SIPAPON:
                             "</RETURN_VALUE>"
.
          GOTO      VALSIN98
.
VALSIN90  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=ERROR>":
                             "Invalid P/O Number- ",KEY7:
                             "</RETURN_VALUE>"
          GOTO      VALSIN98
.
VALSIN98  CALL      XMLEND00
VALSIN99  RETURN
.
.------------------------------------------------------------
. Remote Scripting Functions
.   Required  - FUNCCODE
.             - FUNCTYPE
.               1. Validate Equipment Code
.------------------------------------------------------------
REMOTE00  BRANCH    FUNCTYPE,REMOTE10
          GOTO      REMOTE90
.
REMOTE10  CALL      VALEQU00     * Validate Equipment Code
          GOTO      REMOTE99
.
REMOTE90  CALL      XMLHED00
          WRITE     HTMLFILE;"<RETURN_VALUE TYPE=ERROR>":
                             "Invalid Validation Type":
                             "</RETURN_VALUE>"
          CALL      XMLEND00
          GOTO      REMOTE99
.
REMOTE99  RETURN
.------------------------------------------------------------
. Validate Equipment Code
. INPUTS: FUNCCODE - Equipment Code
.------------------------------------------------------------
VALEQU00  CALL      XMLHED00
          BRANCH    EXIT,VALEQU99
.
          PACK      KEY10,FUNCCODE,SP70
          CALL      RDALEQU1
          BRANCH    OVRCD,VALEQU90
.
.         MATCH     "0",ALEQSTAT      * Check Status
.         GOTO      VALEQU91 IF NOT EQUAL
.
.         COMPARE   ZERO,ALEQCBAL     * Check Current Balance
.         GOTO      VALEQU92 IF EQUAL
.
          MOVE      "CG",CATEGORY     * Get Department Description
          MOVE      ALEQDEPT,CODE
          CALL      GETCOD00
.
          WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>":
         ALEQDESC,"|",ALEQDEPT,"|",TDESC,"|",ALEQCONS,"|",ALEQSERI,"|",ALEQSUPP:
                  "|",ALEQUNIT,"</RETURN_VALUE>"
.
          GOTO      VALEQU98
.
VALEQU90  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=ERROR>":
                             FUNCCODE," - Equipment Code not found. ":
                             "</RETURN_VALUE>"
          GOTO      VALEQU98
.
.VALEQU91  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=ERROR>":
.                             FUNCCODE," - is not available for loan. ":
.                             "</RETURN_VALUE>"
.          GOTO      VALEQU98
.
.VALEQU92  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=ERROR>":
.                             FUNCCODE," - has a current balance of 0. ":
.                             "</RETURN_VALUE>"
.          GOTO      VALEQU98
.
VALEQU98  CALL      XMLEND00
VALEQU99  RETURN
.------------------------------------------------------------
.
          INC       IBAWEBCD
          INC       WEBDATCD
.
          INC       PATDO1IO/INC   * Doctor File
          INC       PATMI1IO/INC
          INC       PATMA1IO/INC
          INC       PATVISIO/INC   * Patients Visits File
          INC       PMSVX1IO/INC   * Visit Extension File
          INC       PATALRIO/INC
          INC       OUTSITIO/INC
          INC       PMSUCHIO/INC
          INC       PMSUCDIO/INC
          INC       PATPR1IO/INC
          INC       PMSPX2IO/INC
          INC       PATFN1IO/INC
          INC       PATHSPIO/INC
          INC       PATWR1IO/INC
          INC       MRTMASIO/INC
          INC       NHIMASIO/INC
          INC       NHIETHIO/INC
          INC       PATRE1IO/INC
          INC       PATDGWIO/INC
          INC       PATDRGIO/INC
          INC       PATITMIO/INC
          INC       PATDADIO/INC
          INC       PATVADIO/INC
          INC       PATCMCIO/INC
          INC       PATASFIO/INC
.
          INC       PMSCCDIO/INC   * Concession Card Details
          INC       PMSHCGIO/INC
          INC       PMSHCPIO/INC
          INC       APSMASIO/INC
          INC       PATIN1IO/INC
          INC       PATDSCIO/INC
          INC       IBATMDIO/INC
.
          INC       ALLLONIO/INC
          INC       ALLEQUIO/INC
          INC       ALLMAIIO/INC
          INC       APSPPDIO/INC
.
          INC       SINCODIO/INC
          INC       SINORIIO/INC
          INC       SINPOAIO/INC
          INC       SINPOBIO/INC
          INC       SINPOCIO/INC
          INC       SINPODIO/INC
          INC       SINPOFIO/INC
          INC       SINWARIO/INC
          INC       SINCCAIO/INC
          INC       SINDPCIO/INC
          INC       SINCICIO/INC
          INC       SINCIAIO/INC
          INC       SINSCDIO/INC
          INC       SINSUAIO/INC
          INC       SINWSEIO/INC
.
          INC       CLNHIMAS                   * Clear nhimasaf file variables
          INC       CLPMSPX2
          INC       PATMASTM
          INC       PATDOCTM
          INC       PMSHCGTM
          INC       PMSHCPTM
          INC       PATDSCTM
          INC       PMSPX2TM
          INC       PATMSXTM
          INC       PATDOCCD
          INC       CALCAGE
          INC       NAMSTRCD
          INC       DAYOFWEK
          INC       PATNAMCD
.
          INC       SINORDPR
.         INC       SINORDFX
.
          INC       RSHWEBCD
.
.
.----------------------------------------------------------------------
. SINORDFX - Fax Purchase Order
.
.  Returns  EXIT    (0=faxed 1=missing or locked)
.
.  Requires SIPAPON (Purchase order number)
.           NOPRINT (0=print from this routine, 1=print elsewhere)
.           SELPRINT called prior to this routine   } These two requirements
.           Open Files : IBACODE1 + IBAPRINT1       } exist in function SELPRT00
.           Includes   : IBACODFD/INC IBAPRNFD/INC
.                        IBAIOCOD/INC IBAIOPRN/INC IBAPRINT
.                        SINTMPDF/INC SINCODFD/IO  SINORIFD/IO  APSMASFD/IO
.                        SINSIAFD/IO  SINPOAFD/IO  SINPOBFD/INC SINPOCFD/INC
.                        SINPODFD/IO
.           Note : already includes IBAPASFD & IBAPASIO
.
.  Mods:  18/03/2003  Davin  Quote 15040
.                     Print new delivery point in header (if parameter on)
.----------------------------------------------------------------------
SINORDFX  READ      CONTROLF,TWENTY3;*124,HORDDES
          READ      CONTROLF,TWENTY6;*248,HPURCHP
          CALL      LCKPO000                * Lock P/O
          BRANCH    OVRCD,SINFAX99,SINFAX99
.
          CALL      RDCON000                * Read Control File Details
.
          CALL      OPNPRINT
.
          CALL      FORD0000                * print routine
          BRANCH    EXIT,SINFAX99           * could not print ?
.                                           * check if anything to print
          CALL      UUSIPA1
.
          IF        NOPRINT=0
            IF        !(CPAGENO>0)
              DISPLAY   *P1:24,*B,*EL,"Nothing to Print for PO ",SIPAPON," - ";
              CALL      HITENTER
              DISPLAY   *P1:24,*EL;
              GOTO      SINFAX99
            ENDIF
            DISPLAY   *P1:24,*EL;
          ENDIF
.
          MOVE      APMAUR2,FAXNUMB
          CALL      FAXPRINT
.
SINFAX99  RETURN
.------------------------------------------------------------
. Lock Purchase Order
.------------------------------------------------------------
LCKPO000  PACK      KEY7,SIPAPON,SP70            * lock purchase order record
          CALL      RLSIPA1
          BRANCH    OVRCD,LCKPO100,LCKPO200             * 1=missing, 2=locked
          GOTO      LCKPO999
.
LCKPO100  DISPLAY   *P1:24,*EL,"Purchase Order ":
                    *V2LON,SIPAPON,*HOFF," Does Not Exist - ";
          CALL      HITENTER
          MOVE      ONE,OVRCD
          GOTO      LCKPO999
.
LCKPO200  KEYIN     *P1:24,*EL,"Purchase Order Locked - (":
                    *V2LON,"R",*HOFF,")etry or e(":
                    *V2LON,"X",*HOFF,")it ? ",*V2LON,ANS
          PACK      ANS,ANS,SP70
          REP       UPPLOW,ANS
          MOVE      TWO,OVRCD
          MATCH     "X",ANS
          GOTO      LCKPO999 IF EQUAL
          MATCH     "E",ANS
          GOTO      LCKPO999 IF EQUAL
          GOTO      LCKPO000
.
LCKPO999  RETURN
.------------------------------------------------------------
. Read Control File Details
.------------------------------------------------------------
RDCON000  READ      CONTROLF,TEN3;CHADD1,CHADD2,CHPOST:
                                 *129,CHTELEB
.
          MOVE      "30",MAXITEMS  * NEED TO ADJUST
.
          RETURN
.------------------------------------------------------------
. FORD - Fax Order
.        Requires SIPAPON (Purchase order number)
.        Returns  EXIT    (0=printed 1=missing or locked)
.------------------------------------------------------------
FORD0000  DISPLAY   *P1:24,*EL,"Faxing Purchase Order ",*V2LON,SIPAPON;
          MOVE      ZERO,CPAGENO                 * reset page no.
          MOVE      "999",CLNO                   * init last line number
          MOVE      ZERO,TF7P2                   * clear total
          MOVE      "1",COUNTFL1                 * count number of pages
          MOVE      "0",COUNTFL2
          CALL      WFAX0000                     * print details
.
          MOVE      CPAGENO,F2B                  * save total page number
          MOVE      ZERO,CPAGENO                 * reset page no.
          MOVE      "999",CLNO                   * init last line number
          MOVE      ZERO,TF7P2                   * clear total
          MOVE      "0",COUNTFL1                 * count number of pages
          CALL      WFAX0000                     * print details
.
          COMPARE   ZERO,CPAGENO
          CALL      ENDF0000 IF NOT EQUAL        * print end of purchase order
.
FORD9000  MOVE      ZERO,EXIT                    * everything is cool !!
FORD9999  RETURN
.----------------------------------------------------------------------
. FHED - print report                               Called by PORD
.----------------------------------------------------------------------
FHED0000  IF        (COUNTFL1=0)&(COUNTFL2=0)&(CPAGENO>0)
            CALL      FILPAG00              * fill to end of page if printing
          ENDIF
.
          ADD       ONE,CPAGENO             * next page
          MOVE      "0",CLNO                * reset line number
.
          COMPARE   ONE,COUNTFL1
          GOTO      FHED9999 IF EQUAL       * just counting pages ?
          COMPARE   ONE,COUNTFL2
          GOTO      FHED9999 IF EQUAL       * just counting pages ?
.
          REP       " 0",SIPADCN            * check if cancelled
          MATCH     "00000000",SIPADCN
          IF        @EQUAL
            REP       " 0",SIPADCM          * check if completed
            MATCH     "00000000",SIPADCM
            IF        @EQUAL
              REP       " 0",SIPADPR        * check if reprint
              MATCH     "00000000",SIPADPR
              IF        @EQUAL
                MOVE      SP70,KEY20
              ELSE
                MOVE      " Reprint            ",KEY20
              ENDIF
            ELSE
              MOVE      "Completed ",KEY10
              UNPACK    SIPADCM,CCENT,CYEAR,CMON,CDAY
              CALL      PACDATE
              PACK      KEY20,KEY10,CPCDATE,SP70
            ENDIF
          ELSE
            MOVE      "Cancelled ",KEY10
            UNPACK    SIPADCN,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            PACK      KEY20,KEY10,CPCDATE,SP70
          ENDIF
.
          PACK      KEY10,CCC,CYY,CMM,CDD,SP70
          REP       " 0",KEY10
          UNPACK    KEY10,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          UNPACK    SP70,SIORHOSP,SIOREXT
          PACK      KEY3,SIPAORI,SP70
          CALL      RDSIOR1
.
          UNPACK    SP70,APMASN1,APMASN2
          UNPACK    SP70,APMASA1,APMASA2
          UNPACK    SP70,APMASA3,APMASPC,APMASTEL,APMAUR2
          UNPACK    SP70,APMASCON
          PACK      KEY5,SIPASUP,SP70
          CALL      RDAPMA1
.
          MOVE      SP70,SICDDESC
          MATCH     SP70,SIPAMES
          IF        !@EQUAL
            MOVE      "ME ",KEY3
            PACK      KEY6,KEY3,SIPAMES,SP70
            CALL      RDSICD1
          ENDIF
          PACK      MESSDESK,SICDDESC,SP70
.
          MOVE      SP70,SICDDESC
          MATCH     SP70,SIPACON
          IF        !(@EQUAL)
            MOVE      "CF ",KEY3
            PACK      KEY6,KEY3,SIPACON,SP70
            CALL      RDSICD1
          ENDIF
          PACK      CONFDESK,SICDDESC,SP70
.
          MOVE      CPAGENO,F2A
.
          MOVE      SP70,SIWADES
          PACK      KEY5,SIPAWAR,SP70
          CALL      RDSIWA1
.
          CALL      FSUPP000             * Format Supplier Name & Address
          CALL      FCRED000             * Format Creditor
.
          READ      CONTROLF,SIXTY3;*241,SNCOMDPC   * multiple delivery point
          COMPARE   ONE,SNCOMDPC
          IF        @EQUAL
            MOVE      SIPAMDP,KEY3
            CALL      RDSIDP1
          ENDIF
.
.         IF        HPURCHP=16
.           PRINT        "To Supplier: ",APMACRD:
.                        *38,"Deliver To:":
.                        *79,"Order:  ",SIPAPON:
.                     *N,SUPLIN1:
.                        *38,SIPADEL:
.                        *79,"Page :",F2A,"  of ",F2B:
.                     *N,SUPLIN2:
.                        *38,MESSDESK:
.                        *79,"Date : ",CPCDATE:
.                     *N,SUPLIN3:
.                        *38,"Enquiries: ",SIPAICN:
.                     *N,SUPLIN4:
.                        *38,"Phn:       ",SIOREXT:
.                     *N,SUPLIN5:
.                        *38,"Fax:       ",SIORUR1:
.                     *N,"Ph.: ",APMASTEL:
.                        *38,KEY20:
.                     *N,"Fax: ",APMASFAX:
.                        *38,CONFDESK:
.                     *N,"----------------------------------------------":
.                       "-----------------------------------------------":
.                     *N,"Line No  Supplier Part Number         Quantity":
.                       "  Unit and       Cost      Unit Price     Value":
.                     *N," Stk No  and Description                      ":
.                       "  Delivery Date  Centre":
.                     *N,"----------------------------------------------":
.                       "-----------------------------------------------"
.         ELSE
.           IF        SNCOMDPC=1
.           PRINT        "To Supplier: ",APMACRD:
.                        *38,"Deliver To:":
.                        *79,"Order:  ",SIPAPON:
.                     *N,SUPLIN1:
.                        *38,SIDPADD1:
.                        *79,"Page :",F2A,"  of ",F2B:
.                     *N,SUPLIN2:
.                        *38,SIDPADD2:
.                        *79,"Date : ",CPCDATE:
.                     *N,SUPLIN3:
.                        *38,"Enquiries: ",SIPAICN:
.                     *N,SUPLIN4:
.                        *38,"Phn:       ",SIOREXT:
.                     *N,SUPLIN5:
.                        *38,"Fax:       ",SIORUR1:
.                     *N,"Ph.: ",APMASTEL:
.                        *38,CONFDESK:
.                     *N,"Fax: ",APMASFAX:
.                        *38,MESSDESK,*76,KEY20:
.                     *N,"----------------------------------------------":
.                       "-----------------------------------------------":
.                     *N,"Line No  Supplier Part Number         Quantity":
.                       "  Unit and       Cost      Unit Price     Value":
.                     *N," Stk No  and Description                      ":
.                       "  Delivery Date  Centre":
.                     *N,"----------------------------------------------":
.                       "-----------------------------------------------"
.           ELSE
.           PRINT        "To Supplier: ",APMACRD:
.                        *38,"Deliver To:":
.                        *79,"Order:  ",SIPAPON:
.                     *N,SUPLIN1:
.                        *38,SIPADEL:
.                        *79,"Page :",F2A,"  of ",F2B:
.                     *N,SUPLIN2:
.                        *38,"Enquiries: ",SIPAICN:
.                        *79,"Date : ",CPCDATE:
.                     *N,SUPLIN3:
.                        *38,"Phn:       ",SIOREXT:
.                     *N,SUPLIN4:
.                        *38,"Fax:       ",SIORUR1:
.                     *N,SUPLIN5:
.                        *38,KEY20:
.                     *N,"Ph.: ",APMASTEL:
.                        *38,CONFDESK:
.                     *N,"Fax: ",APMASFAX:
.                        *38,MESSDESK:
.                     *N,"----------------------------------------------":
.                       "-----------------------------------------------":
.                     *N,"Line No  Supplier Part Number         Quantity":
.                       "  Unit and       Cost      Unit Price     Value":
.                     *N," Stk No  and Description                      ":
.                       "  Delivery Date  Centre":
.                     *N,"----------------------------------------------":
.                       "-----------------------------------------------"
.           ENDIF
.         ENDIF
.
FHED0500  STRIP     SUPLIN1
          STRIP     SUPLIN2
          STRIP     SUPLIN3
          STRIP     SUPLIN4
          STRIP     SUPLIN5 
.
          APPEND    SUPLIN1,D40
          APPEND    SP1,D40
          APPEND    SUPLIN2,D40
          APPEND    SP1,D40
          APPEND    SUPLIN3,D40
          APPEND    SP1,D40
          APPEND    SUPLIN4,D40
          APPEND    SP1,D40
          APPEND    SUPLIN5,D40
.
          RESET     D40
.
          PRINT        "To Supplier: ",APMACRD:
                       *38,"Deliver To:":
                       *79,"Order:  ",SIPAPON:
                    *N,CRELIN1:
                       *38,D40:
                       *79,"Page :",F2A,"  of ",F2B:
                    *N,CRELIN2:
                       *79,"Date : ",CPCDATE:
                    *N,CRELIN3:
                       *38,"Ph.:       ",APMASTEL:
                    *N,CRELIN4:
                       *38,"Fax:       ",APMASFAX:
                    *N,CRELIN5:
                       *38,KEY20:
                    *N,"Ph.: ",APMACTEL:
                    *N,"Fax: ",APMACFAX:
                    *N,"----------------------------------------------":
                      "-----------------------------------------------":
                    *N,"Line No  Supplier Part Number         Quantity":
                      "  Unit and       Cost      Unit Price     Value":
                    *N," Stk No  and Description                      ":
                      "  Delivery Date  Centre":
                    *N,"----------------------------------------------":
                      "-----------------------------------------------"
.
          UNPACK    SP70,DELDT1,DELDT2,DELDT3,DELDT4,DELDT5
          UNPACK    SP70,DELDT6,DELDT7,DELDT8,DELDT9,DELDT10
.
FHED9999  RETURN
.----------------------------------------------------------------------
. WFAX - print data line                            Called by PORD
.        Requires : COUNTFL1 (1=don't print)
.----------------------------------------------------------------------
WFAX0000  PACK      KEY10,SIPAPON,SP70         * loop through line items
          CALL      RSSIPC1                    * line item loop
WFAX1000  CALL      RKSIPC1                    * get next line item
          BRANCH    OVRCD,WFAX5000
          MATCH     SIPCPON,SIPAPON            * at end of purchase order ?
          IF        !@EQUAL
            CALL      RPSIPC1                  * get previous details for print
            GOTO      WFAX5000
          ENDIF
.
WFAX2000  MOVE      "1",COUNTFL2               * just counting !!
          MOVE      CLNO,SCLNO                 * save current line number
          MOVE      CPAGENO,SCPAGENO           * save current page number
          CALL      FAXA0000                   * count line item data lines
.                                              * check if page will be filled
.                                              * and if item goes over 1 page
          IF        (CPAGENO-1=SCPAGENO)&!(CLNO>SCLNO)&!(999=SCLNO)
            MOVE      SCLNO,CLNO               * restore current line number
            MOVE      SCPAGENO,CPAGENO         * restore current page number
            MOVE      "0",COUNTFL2
            CALL      FHED0000                 * get next page
            GOTO      WFAX2000                 * restart from next page
          ENDIF
.
          IF        COUNTFL1=0
            MOVE      SCLNO,CLNO               * restore current line number
            MOVE      SCPAGENO,CPAGENO         * restore current page number
            MOVE      "0",COUNTFL2
            CALL      FAXA0000                 * print line item data lines
          ENDIF
.
          GOTO      WFAX1000                   * get next line item
.
.---- purchase order comment loop ----
.
WFAX5000  PACK      KEY10,SIPAPON,SP70         * loop through purch ord comments
          CALL      RSSIPB1
WFAX6000  CALL      RKSIPB1                    * get next line item comment
          BRANCH    OVRCD,WFAX9000
          MATCH     SIPBPON,SIPAPON            * at end of comments ?
          GOTO      WFAX9000 IF NOT EQUAL
          MATCH     SIPBCOM,SP70               * blank line ?
          GOTO      WFAX6000 IF EQUAL
.
WFAX7000  COMPARE   MAXITEMS,CLNO              * end of page ?
          CALL      FHED0000 IF NOT LESS       * start new page
          ADD       "1",CLNO                   * increment line number
          IF        COUNTFL1=0
            PRINT     *10,SIPBCOM
          ENDIF
          GOTO      WFAX6000
.
WFAX9000
WFAX9999  RETURN
.----------------------------------------------------------------------
. FAXA - print line items                           Called by WFAX
.        Requires : COUNTFL2 (1=dont print)
.----------------------------------------------------------------------
FAXA0000  CALL      CLRLIN00
          PACK      LINE1,SIPCPN,SP70        * Get Part Number
          MATCH     SP70,SIPCCAT             * Catalog Blank ?
          IF        @EQUAL
            MOVE      SIPCPD,LINE2           * Not Catloged
          ELSE
            MOVE      SP70,SIICPD1
            MOVE      SP70,SIICPD2
            PACK      KEY27,SIPCCAT,SIPASUP,SIPCSUT,SP70
            CALL      RDSIIC1
            IF        OVRCD=0
              MOVE      SIICPD1,LINE2        * Use Supplier Part Description
              MOVE      SIICPD2,LINE3
            ELSE
              MOVE      SIPCPD,LINE2
            ENDIF
          ENDIF
.
.---- get purchase order references from contract details ----
.
          PACK      SISDPO1,SP70
          PACK      SISDPO2,SP70
          PACK      KEY45,SIPCCON,SIPASUP,SIPCCAT,SIPCCDT,SIPCSUT,SP70
          CALL      RDSISD1
          UNPACK    SISDPO1,LINE4
          UNPACK    SISDPO2,LINE5
.
          MOVE      SIPCOQT,F7
          ASSIGN    SIPCOQT*SIPCECT,F7P2
          MOVE      SIPCECT,F8P4
          IF        !(SIPCECT=-1)&(COUNTFL1=0)&(COUNTFL2=0)
            ADD       F7P2,TF7P2
          ENDIF
.
.     ---- line item comment loop ----
.
          MOVE      "0",PRTFLAG                * nothing printed yet !!
          PACK      KEY13,SIPCPON,SIPCITM,SP70 * loop through line item comments
          CALL      RSSIPD1
FAXA2000  CALL      RKSIPD1                    * get next line item comment
          BRANCH    OVRCD,FAXA4000
          MATCH     SIPDPON,SIPCPON            * at end of comments ?
          GOTO      FAXA4000 IF NOT EQUAL
          MATCH     SIPDITM,SIPCITM            * at end of comments ?
          GOTO      FAXA4000 IF NOT EQUAL
          MATCH     SIPDCOM,SP70               * blank line ?
          GOTO      FAXA2000 IF EQUAL
.
          CALL      FAXB0000                   * print crap
          GOTO      FAXA2000
.
FAXA4000  MOVE      SP70,SIPDCOM
          CALL      FAXB0000                   * make sure crap printed
.
FAXA9999  RETURN
.------------------------------------------------------------
. Clear LINE1..7
.------------------------------------------------------------
CLRLIN00  MOVE      SP70,LINE1                 * clear lines
          MOVE      SP70,LINE2
          MOVE      SP70,LINE3
          MOVE      SP70,LINE4
          MOVE      SP70,LINE5
          MOVE      SP70,LINE6
          MOVE      SP70,LINE7
          RETURN
.----------------------------------------------------------------------
. FAXB - print line items                           Called by FAXA
.        Requires : COUNTFL2 (1=don't print)
.                   PRTFLAG  (1=main data already printed)
.----------------------------------------------------------------------
FAXB0000  BRANCH    PRTFLAG,FAXB1000
.
          MATCH     LINE6,SP70              * save line
          IF        @EQUAL
            PACK      LINE6,SIPDCOM,SP70
            MATCH     SIPDCOM,SP70          * check if it is time to unload
            GOTO      FAXB9999 IF NOT EQUAL
          ELSE
            PACK      LINE7,SIPDCOM,SP70
          ENDIF
.
FAXB0100  MOVE      "0",SECTOR              * consolidate data lines
          MATCH     SP70,LINE1
          WHILE     @EQUAL & (SECTOR<7)
            ADD       ONE,SECTOR
            MOVE      LINE2,LINE1
            MOVE      LINE3,LINE2
            MOVE      LINE4,LINE3
            MOVE      LINE5,LINE4
            MOVE      LINE6,LINE5
            MOVE      LINE7,LINE6
            MOVE      SP70,LINE7
            MATCH     SP70,LINE1
          DO
          MATCH     SP70,LINE2
          WHILE     @EQUAL & (SECTOR<7)
            ADD       ONE,SECTOR
            MOVE      LINE3,LINE2
            MOVE      LINE4,LINE3
            MOVE      LINE5,LINE4
            MOVE      LINE6,LINE5
            MOVE      LINE7,LINE6
            MOVE      SP70,LINE7
            MATCH     SP70,LINE2
          DO
          MATCH     SP70,LINE3
          WHILE     @EQUAL & (SECTOR<7)
            ADD       ONE,SECTOR
            MOVE      LINE4,LINE3
            MOVE      LINE5,LINE4
            MOVE      LINE6,LINE5
            MOVE      LINE7,LINE6
            MOVE      SP70,LINE7
            MATCH     SP70,LINE3
          DO
          MATCH     SP70,LINE4
          WHILE     @EQUAL & (SECTOR<7)
            ADD       ONE,SECTOR
            MOVE      LINE5,LINE4
            MOVE      LINE6,LINE5
            MOVE      LINE7,LINE6
            MOVE      SP70,LINE7
            MATCH     SP70,LINE4
          DO
          MATCH     SP70,LINE5
          WHILE     @EQUAL & (SECTOR<7)
            ADD       ONE,SECTOR
            MOVE      LINE6,LINE5
            MOVE      LINE7,LINE6
            MOVE      SP70,LINE7
            MATCH     SP70,LINE5
          DO
          MATCH     SP70,LINE6
          WHILE     @EQUAL & (SECTOR<7)
            ADD       ONE,SECTOR
            MOVE      LINE7,LINE6
            MOVE      SP70,LINE7
            MATCH     SP70,LINE6
          DO
.
          COMPARE   MAXITEMS,CLNO
          CALL      FHED0000 IF NOT LESS
.
          MOVE      LINE1,KEY60                * print main line 1
          CALL      LINBK000                 * Break up line to 30
.
          ADD       ONE,CLNO                   * increment line number
          IF        COUNTFL2=0
            IF        F8P4=-1
              MOVE      "  T.B.A.       ",KEY13
              MOVE      "  T.B.A.       ",KEY10
            ELSE
              MOVE      F8P4,KEY13
              MOVE      F7P2,KEY10
            ENDIF
.
            PRINT     *5,SIPCITM,*10,KEY30,F7:
                      SP2,SIPCSUT,SIPCCST,SP2,KEY13,KEY10
          ENDIF
.
          COMPARE   MAXITEMS,CLNO              * end of page ?
          CALL      FHED0000 IF NOT LESS       * start new page
.
          MOVE      LINE1,KEY60
          CALL      LINBK000                   * break up line neatly
.
          PACK      KEY60,KEY60,SP70           * print main line 2
          MATCH     SP70,KEY60
          IF        @EQUAL
            MOVE      LINE2,KEY60
            MOVE      LINE3,LINE2
            MOVE      LINE4,LINE3
            MOVE      LINE5,LINE4
            MOVE      LINE6,LINE5
            MOVE      SP70,LINE6
          ENDIF
          PACK      KEY60,KEY60,SP70
          CALL      LINBK000                 * Break up line to 38
.
          UNPACK    SIPCEDD,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          ADD       "1",CLNO                   * increment line number
          IF        COUNTFL2=0
            PRINT     SIPCCAT,*10,KEY30,*52,CPCDATE
            MATCH     SP70,CPCDATE
            IF        !@EQUAL
              MOVE      ZERO,F1
              REPEAT
                ADD       ONE,F1
                MOVE      SP70,KEY15
                LOAD      KEY15,F1,DELDT1,DELDT2,DELDT3,DELDT4,DELDT5:
                                   DELDT6,DELDT7,DELDT8,DELDT9,DELDT10
                MATCH     SP70,KEY15
              UNTIL     @EQUAL
              PACK      KEY15,SIPCITM,DOT,SP1,CPCDATE,SP70
              STORE     KEY15,F1,DELDT1,DELDT2,DELDT3,DELDT4,DELDT5:
                                 DELDT6,DELDT7,DELDT8,DELDT9,DELDT10
            ENDIF
          ENDIF
.
          MATCH     SP70,KEY60                 * print rest of data line
          IF        !@EQUAL
            COMPARE   MAXITEMS,CLNO            * end of page ?
            CALL      FHED0000 IF NOT LESS     * start new page
            ADD       "1",CLNO                 * increment line number
            IF        COUNTFL2=0
              PRINT     *10,KEY60
            ENDIF
          ENDIF
.
          MATCH     SP70,LINE2                 * print data line 2
          IF        !@EQUAL
            COMPARE   MAXITEMS,CLNO            * end of page ?
            CALL      FHED0000 IF NOT LESS     * start new page
            ADD       "1",CLNO                 * increment line number
            IF        COUNTFL2=0
              PRINT     *10,LINE2
            ENDIF
          ENDIF
.
          MATCH     SP70,LINE3                 * print data line 3
          IF        !@EQUAL
            COMPARE   MAXITEMS,CLNO            * end of page ?
            CALL      FHED0000 IF NOT LESS     * start new page
            ADD       "1",CLNO                 * increment line number
            IF        COUNTFL2=0
              PRINT     *10,LINE3
            ENDIF
          ENDIF
.
          MATCH     SP70,LINE4                 * print data line 4
          IF        !@EQUAL
            COMPARE   MAXITEMS,CLNO            * end of page ?
            CALL      FHED0000 IF NOT LESS     * start new page
            ADD       "1",CLNO                 * increment line number
            IF        COUNTFL2=0
              PRINT     *10,LINE4
            ENDIF
          ENDIF
.
          MATCH     SP70,LINE5                 * print data line 5
          IF        !@EQUAL
            COMPARE   MAXITEMS,CLNO            * end of page ?
            CALL      FHED0000 IF NOT LESS     * start new page
            ADD       "1",CLNO                 * increment line number
            IF        COUNTFL2=0
              PRINT     *10,LINE5
            ENDIF
          ENDIF
.
          MATCH     SP70,LINE6                 * print data line 6
          IF        !@EQUAL
            COMPARE   MAXITEMS,CLNO            * end of page ?
            CALL      FHED0000 IF NOT LESS     * start new page
            ADD       "1",CLNO                 * increment line number
            IF        COUNTFL2=0
              PRINT     *10,LINE6
            ENDIF
          ENDIF
.
          MATCH     SP70,LINE7                 * print data line 7
          IF        !@EQUAL
            COMPARE   MAXITEMS,CLNO            * end of page ?
            CALL      FHED0000 IF NOT LESS     * start new page
            ADD       "1",CLNO                 * increment line number
            IF        COUNTFL2=0
              PRINT     *10,LINE7
            ENDIF
          ENDIF
.
          MOVE      ONE,PRTFLAG                * main data printed
          GOTO      FAXB9999
.
.---- print comments data ----
.
FAXB1000  MATCH     SP70,SIPDCOM               * print data line if not blank
          IF        !@EQUAL
            COMPARE   MAXITEMS,CLNO            * end of page ?
            CALL      FHED0000 IF NOT LESS     * start new page
            ADD       "1",CLNO                 * increment line number
            IF        COUNTFL2=0
              PRINT     *10,SIPDCOM
            ENDIF
          ENDIF
.
FAXB9999  RETURN
.----------------------------------------------------------------------
. LINBK - Break up KEY60 into two neat halves        Called by lots
.         Requires - KEY60   (original line)
.         Returns  - KEY30   (line 1)
.                    KEY60   (line 2)
.----------------------------------------------------------------------
LINBK000  RESET     KEY60,31
          CMATCH    SP70,KEY60
          WHILE     !@EQUAL&!@EOS
            BUMP      KEY60,-1
            IF        !@EOS
              CMATCH    SP70,KEY60
            ENDIF
          DO
.
          IF        @EOS
            SETLPTR   KEY60
            RESET     KEY60
            UNPACK    KEY60,KEY30,KEY60
          ELSE
            LENSET    KEY60
            RESET     KEY60
            MOVE      KEY60,KEY30
            PACK      KEY30,KEY30,SP70
.
            ENDSET    KEY60
            SETLPTR   KEY60
            BUMP      KEY60
            PACK      KEY60,KEY60,SP70
          ENDIF
.
          RETURN
.----------------------------------------------------------------------
. FILP - fill to end of purchase order                Called by FHED
.----------------------------------------------------------------------
FILPAG00  WHILE     MAXITEMS>CLNO
            ADD       "1",CLNO                   * print blank lines
            PRINT     SP1
          DO
          PRINT     *N,*N,*77," CONTINUED",*N,*N
          RETURN
.----------------------------------------------------------------------
. ENDF - fax end of purchase order                Called by PORD
.----------------------------------------------------------------------
ENDF0000  WHILE     MAXITEMS>CLNO
            ADD       "1",CLNO                   * print blank lines
            PRINT     SP1
          DO
.
.---- print total ----
.
          PRINT     *N,"----------------------------------------------":
                      "-----------------------------------------------":
                    *N,*77,"Total: ",TF7P2:
                    *N,"----------------------------------------------":
                      "-----------------------------------------------"
.
          MOVE      "2",AUDTTYPE
          CALL      WASIPA00               * Write to Audit file
.
          PACK      KEY7,SIPAPON,SP70
          CALL      RDSIPA1
          IF        OVRCD=0
            PACK      SIPADPR,CCC,CYY,CMM,CDD,SP70
            REP       " 0",SIPADPR
            CALL      UPSIPA1                    * update date printed
          ENDIF
.
          MOVE      "3",AUDTTYPE
          CALL      WASIPA00               * Write to Audit file
.
ENDF9999  RETURN
.------------------------------------------------------------
. Format Supplier Name & Address (remove blank lines)
.------------------------------------------------------------
FSUPP000  PACK     APMASN1,APMASN1,SP70
          PACK     APMASN2,APMASN2,SP70
          PACK     APMASA1,APMASA1,SP70
          PACK     APMASA2,APMASA2,SP70
          PACK     APMASA3,APMASA3,SP70
.
          MOVE     SP70,SUPLIN1
          MOVE     SP70,SUPLIN2
          MOVE     APMASA1,SUPLIN3
          MOVE     APMASA2,SUPLIN4
          MOVE     APMASA3,SUPLIN5
.
          MOVE      "0",SECTOR              * consolidate data lines
          MATCH     SP70,SUPLIN1
          WHILE     @EQUAL & (SECTOR<5)
            ADD       ONE,SECTOR
            MOVE      SUPLIN2,SUPLIN1
            MOVE      SUPLIN3,SUPLIN2
            MOVE      SUPLIN4,SUPLIN3
            MOVE      SUPLIN5,SUPLIN4
            MOVE      SP70,SUPLIN5
            MATCH     SP70,SUPLIN1
          DO
          MATCH     SP70,SUPLIN2
          WHILE     @EQUAL & (SECTOR<5)
            ADD       ONE,SECTOR
            MOVE      SUPLIN3,SUPLIN2
            MOVE      SUPLIN4,SUPLIN3
            MOVE      SUPLIN5,SUPLIN4
            MOVE      SP70,SUPLIN5
            MATCH     SP70,SUPLIN2
          DO
          MATCH     SP70,SUPLIN3
          WHILE     @EQUAL & (SECTOR<5)
            ADD       ONE,SECTOR
            MOVE      SUPLIN4,SUPLIN3
            MOVE      SUPLIN5,SUPLIN4
            MOVE      SP70,SUPLIN5
            MATCH     SP70,SUPLIN3
          DO
          MATCH     SP70,SUPLIN4
          WHILE     @EQUAL & (SECTOR<5)
            ADD       ONE,SECTOR
            MOVE      SUPLIN5,SUPLIN4
            MOVE      SP70,SUPLIN5
            MATCH     SP70,SUPLIN4
          DO
.
          PACK      SUPLIN1,SUPLIN1,SP70
          PACK      SUPLIN2,SUPLIN2,SP70
          PACK      SUPLIN3,SUPLIN3,SP70
          PACK      SUPLIN4,SUPLIN4,SP70
          PACK      SUPLIN5,SUPLIN5,SP70
.
          MATCH     SP70,SUPLIN5
          IF        !@EQUAL
            STRIP     SUPLIN5
            ENDSET    SUPLIN5
            APPEND    ", ",SUPLIN5
            APPEND    APMASPC,SUPLIN5
            APPEND    SP70,SUPLIN5
            RESET     SUPLIN5
            GOTO      FSUPP999
         ENDIF
.
          MATCH     SP70,SUPLIN4
          IF        !@EQUAL
            STRIP     SUPLIN4
            ENDSET    SUPLIN4
            APPEND    ", ",SUPLIN4
            APPEND    APMASPC,SUPLIN4
            APPEND    SP70,SUPLIN4
            RESET     SUPLIN4
            GOTO      FSUPP999
          ENDIF
.
          MATCH     SP70,SUPLIN3
          IF        !@EQUAL
            STRIP     SUPLIN3
            ENDSET    SUPLIN3
            APPEND    ", ",SUPLIN3
            APPEND    APMASPC,SUPLIN3
            APPEND    SP70,SUPLIN3
            RESET     SUPLIN3
            GOTO      FSUPP999
          ENDIF
.
          MATCH     SP70,SUPLIN2
          IF        !@EQUAL
            STRIP     SUPLIN2
            ENDSET    SUPLIN2
            APPEND    ", ",SUPLIN2
            APPEND    APMASPC,SUPLIN2
            APPEND    SP70,SUPLIN2
            RESET     SUPLIN2
            GOTO      FSUPP999
          ENDIF
.
FSUPP999  RETURN
.
.------------------------------------------------------------
. Format Creditor Address (remove blank lines)
.------------------------------------------------------------
FCRED000  PACK     APMACN1,APMACN1,SP70
          PACK     APMACN2,APMACN2,SP70
          PACK     APMACA1,APMACA1,SP70
          PACK     APMACA2,APMACA2,SP70
          PACK     APMACA3,APMACA3,SP70
.
          MOVE     APMACN1,CRELIN1
          MOVE     APMACN2,CRELIN2
          MOVE     APMACA1,CRELIN3
          MOVE     APMACA2,CRELIN4
          MOVE     APMACA3,CRELIN5
.
          MOVE      "0",SECTOR              * consolidate data lines
          MATCH     SP70,CRELIN1
          WHILE     @EQUAL & (SECTOR<5)
            ADD       ONE,SECTOR
            MOVE      CRELIN2,CRELIN1
            MOVE      CRELIN3,CRELIN2
            MOVE      CRELIN4,CRELIN3
            MOVE      CRELIN5,CRELIN4
            MOVE      SP70,CRELIN5
            MATCH     SP70,CRELIN1
          DO
          MATCH     SP70,CRELIN2
          WHILE     @EQUAL & (SECTOR<5)
            ADD       ONE,SECTOR
            MOVE      CRELIN3,CRELIN2
            MOVE      CRELIN4,CRELIN3
            MOVE      CRELIN5,CRELIN4
            MOVE      SP70,CRELIN5
            MATCH     SP70,CRELIN2
          DO
          MATCH     SP70,CRELIN3
          WHILE     @EQUAL & (SECTOR<5)
            ADD       ONE,SECTOR
            MOVE      CRELIN4,CRELIN3
            MOVE      CRELIN5,CRELIN4
            MOVE      SP70,CRELIN5
            MATCH     SP70,CRELIN3
          DO
          MATCH     SP70,CRELIN4
          WHILE     @EQUAL & (SECTOR<5)
            ADD       ONE,SECTOR
            MOVE      SUPLIN5,CRELIN4
            MOVE      SP70,CRELIN5
            MATCH     SP70,CRELIN4
          DO
.
          PACK      CRELIN1,CRELIN1,SP70
          PACK      CRELIN2,CRELIN2,SP70
          PACK      CRELIN3,CRELIN3,SP70
          PACK      CRELIN4,CRELIN4,SP70
          PACK      CRELIN5,CRELIN5,SP70
.
          MATCH     SP70,CRELIN5
          IF        !@EQUAL
            STRIP     CRELIN5
            ENDSET    CRELIN5
            APPEND    ", ",CRELIN5
            APPEND    APMACPC,CRELIN5
            APPEND    SP70,CRELIN5
            RESET     CRELIN5
            GOTO      FCRED999
         ENDIF
.
          MATCH     SP70,CRELIN4
          IF        !@EQUAL
            STRIP     CRELIN4
            ENDSET    CRELIN4
            APPEND    ", ",CRELIN4
            APPEND    APMACPC,CRELIN4
            APPEND    SP70,CRELIN4
            RESET     CRELIN4
            GOTO      FCRED999
          ENDIF
.
          MATCH     SP70,CRELIN3
          IF        !@EQUAL
            STRIP     CRELIN3
            ENDSET    CRELIN3
            APPEND    ", ",CRELIN3
            APPEND    APMACPC,CRELIN3
            APPEND    SP70,CRELIN3
            RESET     CRELIN3
            GOTO      FCRED999
          ENDIF
.
          MATCH     SP70,CRELIN2
          IF        !@EQUAL
            STRIP     CRELIN2
            ENDSET    CRELIN2
            APPEND    ", ",CRELIN2
            APPEND    APMACPC,CRELIN2
            APPEND    SP70,CRELIN2
            RESET     CRELIN2
            GOTO      FCRED999
          ENDIF
.
FCRED999  RETURN
.
.********************************************************************
.*                          FAXPRINT                                *
.*                       Fax a Print File                           *
.********************************************************************
FAXPRINT  COMPARE   ZERO,CPRTFLG
          GOTO      FAXPR999 IF NOT EQUAL
.
.         Check if file is of non-zero size
.
          CALL      SPLEMPTY
          BRANCH    OVRCD,FAXPR800,FAXPR900
.
          CLEAR     KEY80
          APPEND    "ibasin59.us1 ",KEY80
          APPEND    SPLFILE,KEY80
          APPEND    SP1,KEY80
          APPEND    "#"",KEY80
          APPEND    FAXNUMB,KEY80
          APPEND    "#"",KEY80
          APPEND    SP1,KEY80
          APPEND    PASSCODE,KEY80
          RESET     KEY80
.
          MOVE      "23",EVERT
          MOVE      "3",ECOL
          GETVAR    DIM2,KEY2,ECOL,EVERT
          MATCH     "eX",DIM2
          IF        @EQUAL
            CALL      CLRBX000                * Clear the Box
            DISPLAY   *P20:23,*V2LON,"Fax being queued. Please wait.";
            EXECUTE   KEY80,TASKID
            CALL      OPTNS000                * Redisplay the Option Line
          ELSE
            DISPLAY   *P20:24,*EL,*V2LON,"Fax being queued. Please wait";
            EXECUTE   KEY80,TASKID
          ENDIF
.
          GOTO      FAXPR900
.
.         Quick delete of spool file
.
FAXPR800  MOVE      FILENAME,CSPDESC       * name of file to be deleted
          CALL      SPLDELET
.
FAXPR900  MOVE      SP8,SPLFILE
          GOTO      FAXPR9999
.
FAXPR999  RETURN
.

.******************************************************************************
.* System         : Outpatients                                               *
.* Program        : IBAOUT61.PBL                                              *
.* Name           : Site Master Listing                                       *
.******************************************************************************
.* Date           : 21/06/1988                                                *
.* Author         : N. Spijkman                                               *
.* Function       : To print all the current sites on file                    *
.* Modifications  :                                                           *
.*        V9.02.00  28/11/2002    Lina Vo                                     *
.*                  Remove site specific open of outsitaf                     *
.*        V5.01.02  04/02/1992    Justin Coates  (Southland changes)          *
.*                  Add the fields OTSTXBOK and OTSTXATT                      *
.******************************************************************************
.
          INC       STD001FD
          INC       OUTSITFD/INC
          INC       OUTCONFD/INC
.
.               Extra variables
.
START30   DIM       30
END30     DIM       30
.
TMPSTR    DIM       30
DIM30D    DIM       30
.
XDIM30    DIM       30
YDIM30    DIM       30
.
START6    DIM       6
END6      DIM       6
.
XDIM6     DIM       6
YDIM6     DIM       6
.
FSTREC    INIT      "Start "
LSTREC    INIT      "Finish"
.
LN55      FORM      "55"
.
FIELD     FORM      1
.
OUT       INIT      "out"
PRGID     INIT      "IBAOUT61"
PRGNAM    INIT      "Site Master Listing"
VERSION   INIT      "V12.01.00"
+
.         Start of program
.
          CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening";
.
          DISPLAY   *P64:24,"outsitaf";
          OPEN      OUTSITA1,"outsitaf"
.
          DISPLAY   *P64:24,"outsitaf";
          OPEN      OUTSITA2,"outsitaf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
MENU      DISPLAY   *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Site Code Sequence":
                    *P1:6,*V2LON,TWO,*HOFF," = Site Description Sequence"
.
BEGIN     KEYIN     *P1:8,*EF,"Please Select : ",*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
          BRANCH    OPTION OF START,START1
.
          BEEP
          GOTO      BEGIN
.
.       Code sequence
.
START     DISPLAY   *P1:11,*EL,"Site Code From   :":
                    *P1:13,*EL,"          To     :"
.
START0    MOVE      UNDLN,START6
          DISPLAY   *P20:11,*EL,START6
          MOVE      SP6,START6
          KEYIN     *P20:11,*V2LON,START6,*P20:11,*DV,START6
.
          ENDSET    START6
          APPEND    SP6,START6
          RESET     START6
.
          MATCH     SP6,START6
          GOTO      CHKSRT IF NOT EQUAL
          DISPLAY   *P20:11,*V2LON,FSTREC
          GOTO      KEYNXT1
.
CHKSRT    MOVE      START6,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD OF INVCODE
.
KEYNXT1   MOVE      UNDLN,END6
          DISPLAY   *P20:13,END6
          MOVE      SP6,END6
          KEYIN     *P20:13,*V2LON,END6,*P20:13,*DV,END6
.
          ENDSET    END6
          APPEND    SP6,END6
          RESET     END6
.
          MATCH     SP6,END6
          GOTO      CHKBOTH IF NOT EQUAL
          DISPLAY   *P20:13,*V2LON,LSTREC
          GOTO      CONFIRM
.
CHKBOTH   MATCH     START6,SP6
          GOTO      CONFIRM IF EQUAL
.
          MATCH     START6,END6
          GOTO      CONFIRM IF NOT LESS
.
          DISPLAY   *P1:24,*B,"FROM Site greater than To Site.  ",*EL;
          CALL      HITENTER
          GOTO      START
.
INVCODE   DISPLAY   *P1:24,*B,"Invalid Code.  ",*EL;
          CALL      HITENTER
          GOTO      START0
.
.       Description sequence
.
START1    DISPLAY   *P1:11,*EL,"Site description From :":
                    *P1:13,*EL,"                 To   :"
.
          MOVE      UNDLN,START30
          DISPLAY   *P25:11,*EL,START30
          MOVE      SP30,START30
          KEYIN     *P25:11,*V2LON,START30,*P25:11,*DV,START30
.
          ENDSET    START30
          APPEND    SP30,START30
          RESET     START30
.
          MATCH     SP30,START30
          GOTO      KEYNXT2 IF NOT EQUAL
          DISPLAY   *P25:11,*V2LON,FSTREC
.
KEYNXT2   MOVE      UNDLN,END30
          DISPLAY   *P25:13,END30
          MOVE      SP30,END30
          KEYIN     *P25:13,*V2LON,TMPSTR
          MOVE      TMPSTR,END30
          DISPLAY   *P25:13,*V2LON,END30
.
          ENDSET    END30
          APPEND    SP30,END30
          RESET     END30
.
          MATCH     SP30,END30
          GOTO      CHKBOT1 IF NOT EQUAL
          DISPLAY   *P25:13,*V2LON,LSTREC
          GOTO      CONFIRM
.
CHKBOT1   MATCH     START30,SP30
          GOTO      CONFIRM IF EQUAL
.
          MATCH     START30,END30
          GOTO      CONFIRM IF NOT LESS
.
          DISPLAY   *P1:24,*B,"From Description must be before ":
                    "ending Description.  ",*EL;
          CALL      HITENTER
          GOTO      START1
.
.
CONFIRM   KEYIN     *P1:24,*EL,"Ok to Continue (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF:
                    ") ? ",*V2LON,ANS;
          REP       UPPLOW,ANS
.
          MATCH     "N",ANS
          GOTO      BEGIN IF EQUAL
.
          MATCH     "Y",ANS
          GOTO      OKCONT IF EQUAL
.
          BEEP
          GOTO      CONFIRM
.
.
OKCONT    DISPLAY   *P1:24,*EL,*P20:24,*V2LON:
                    "*** Generating ***",*W2;
.
          CALL      HEADSET
.
          MOVE      ZERO,CPAGENO
.
          BRANCH    OPTION OF CSEQ3,DSEQ3
.
CSEQ3     DISPLAY   *P1:24,*EL,*P20:24,"Printing Site Code :"
.
          MATCH     SP6,START6
          GOTO      NEXT1 IF NOT EQUAL
.
          MOVE      START6,KEY6
          CALL      RDSSITA1
.
LOOPX     CALL      RDKSITA1
          BRANCH    OVRCD OF ENDP
          MATCH     SP6,END6
          GOTO      NEXT1 IF EQUAL
.
          MATCH     OSTSITE,END6
          GOTO      ENDP IF LESS
.
NEXT1     DISPLAY   *P46:24,*V2LON,OSTSITE;
.
          CALL      DISPDA
          GOTO      LOOPX
.
.
DSEQ3     DISPLAY   *P1:24,*EL,"Printing Site Description :"
.
          PACK      KEY36 WITH START30,SP15
          CALL      RDSSITA2
.
LOOPY     CALL      RDKSITA2
          BRANCH    OVRCD OF ENDP
.
          MATCH     SP30,END30
          GOTO      NEXT12 IF EQUAL
.
          MATCH     OSTDESC,TMPSTR
          GOTO      ENDP IF LESS
.
NEXT12    MOVE      OSTDESC,DIM30D
          DISPLAY   *P35:24,*V2LON,DIM30D;
.
          CALL      DISPDA
          GOTO      LOOPY
.
DISPDA    COMPARE   ZERO,CPAGENO
          CALL      PRINTH IF EQUAL
.
          COMPARE   LN55,CLNO
          CALL      PRINTH IF NOT LESS
.
          PRINT     *26,"|",*30,OSTSITE,*39,"|",*43,OSTDESC:
                    *76,"|  ",OTSTXBOK,"  |  ",OTSTXATT,"  |"
.
          ADD       ONE,CLNO
          RETURN
.
.
ENDP      COMPARE   ZERO,CPAGENO
          GOTO      NOREC IF EQUAL
.
          PRINT     *26,"|",*39,"|",*76,"|",*83,"|",*90,"|",*N:
                    *26,"*------------------------------------":
                    "---------------------------*",*N:
                    *N,*N,"***  END OF REPORT  ***"
          DISPLAY   *P1:24,*EL,*P15:24,*V2LON:
                    "*** Report Generation Completed ***",*W2;
          GOTO      BEGIN
.
NOREC     DISPLAY   *P1:24,*EL,*P20:24,*V2LON,*B:
                    "*** No Site Code selected ***",*W2;
          GOTO      BEGIN
.
PRINTH    ADD       ONE,CPAGENO
.
          CLOCK     TIME,CTIMEIS
.
          PRINT     *F,PRGID,*36,CNAME,*89,"DATE : ",*96,CDATE,*N:
                    *1,VERSION,*36,"*** SITE MASTER LISTING ***":
                    *89,"TIME :",*96,CTIMEIS,*N:
                    *89,"PAGE :",*100,CPAGENO,*N
.
          BRANCH    OPTION OF CSEQ1,DSEQ1
.
CSEQ1     PRINT     *36,"Site Code Sequence From ":
                    XDIM6," To ",YDIM6,*N
          MOVE      "7",CLNO
          GOTO      DISPSC
.
DSEQ1     PRINT     *36,"Site Description Sequence",*N:
                    *36,"From :  ",XDIM30,*N:
                    *36,"To   :  ",YDIM30,*N
          MOVE      "9",CLNO
.
DISPSC    PRINT     *26,"*-------------------------------------":
                    "--------------------------*",*N:
                    *26,"|",*30,"SITE",*39,"|",*43,"DESCRIPTION":
                    *76,"| Book | Att. |",*N:
                    *26,"*-------------------------------------":
                    "--------------------------*",*N:
                    *26,"|",*39,"|",*76,"|",*83,"|",*90,"|"
.
          RETURN
.
HEADSET   BRANCH    OPTION OF CSEQ2,DSEQ2
.
CSEQ2     MOVE      SP6,XDIM6
          MOVE      SP6,YDIM6
.
          MOVE      START6,XDIM6
          MOVE      END6,YDIM6
.
          MATCH     SP6,START6
          GOTO      CONT6 IF NOT EQUAL
          MOVE      FSTREC,XDIM6
.
CONT6     MATCH     SP6,END6
          RETURN    IF NOT EQUAL
          MOVE      LSTREC,YDIM6
          RETURN
.
DSEQ2     MOVE      SP30,XDIM30
          MOVE      SP30,YDIM30
.
          MOVE      START30,XDIM30
          MOVE      END30,YDIM30
.
          MATCH     SP30,START30
          GOTO      CONT61 IF NOT EQUAL
          MOVE      FSTREC,XDIM30
.
CONT61    MATCH     SP30,END30
          RETURN    IF NOT EQUAL
          MOVE      LSTREC,YDIM30
          RETURN
.
          INC       OUTSITIO/INC
          INC       STD001IO
.
ENDIT     CHAIN     PGM
          STOP
...............................................................................

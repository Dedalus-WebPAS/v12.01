.******************************************************************************
.* System   :  BILL                                                           *
.* Program  :  IBAWAT83                                                       *
.* Desc     :  W/L EDI EXTRACT - write a Delete or Erase record to watnbraf   *
.******************************************************************************
.* Date     :  20/07/2000                                                     *
.* Author   :  Jill Habasque                                                  *
.* Mods.    :                                                                 *
.*                                                                            *
. *****************************************************************************
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899                         *
.*           Recompiled as WATTR1FD changes                                   *
.******************************************************************************
.* V10.06.01 12/03/2015  Steve Armstrong  CAR 314108                          *
.*           Removed reference to PATCGPFD (No Longer in use)                 *
.******************************************************************************
.* V10.05.01 01/08/2015  Ania P                      CAR 261630               *
.*           Removed use of PORT for temp file naming                         *
.******************************************************************************
.* V10.04.01 20/03/2014  Ebon Clements    CAR 298038                          *
.*           Removed read of WTCNTPRO as it is now obsolete                   *
.******************************************************************************
.* V10.03.01 26/07/2013  Steve Armstrong  CAR 287787                          *
.*           Recompiled for changes to DGCLICUA.                              *
.******************************************************************************
.* V10.02.01 25/06/2011 Steve Armstrong    CAR 240722                         *
.*           Recompiled for changes to PATLOCFD.                              *
.*           Recompiled for changes to PMSCURFD.                              *
.******************************************************************************
.* V9.09.01  27/08/2007  Peter Vela   CAR 147747                              *
.*           Added variables for PATCOMN2 Cabrini Health UR format            *
.* V9.04.01  24/08/2005  Jill Habasque CAR 51002                              *
.*           Changed to use WTWMECNT instead of WTWMUNIQ                      *
.******************************************************************************
.* V9.03.02  29/10/2003 Sandra Barcham        43783                           *
.*           Recompiled for NZHISIIO and NZHISIUP                             *
.* V9.03.01  22/08/2003  Sandra Barcham       41128                           *
.*           Ensure write to pmspx2af when creating U/R                       *
.******************************************************************************
.* V5.08.04  27/10/2000  Ebon Clements                                        *
.*           Added option to change sent flag to No                           *
.* V5.08.03  16/08/2000  Caleb Sharman                                        *
.*           Changed Health Fund variables to be 8 chars                      *
.* V5.08.02  27/07/2000  Jill Habasque                                        *
.*           Added UR search                                                  *
.* V5.08.01  27/07/2000  Jill Habasque                                        *
.*           Added wtwmuniq to be stored in wtnbbnum                          *
.******************************************************************************
.
          INC       STD001FD
          INC       PATCALAG/INC
          INC       PATCOMM/INC
          INC       PATDHEAD/INC
          INC       PMIVARS/INC
          INC       IBACONFD/INC
.
          INC       BOKRC1FD/INC
          INC       PATAK1FD/INC
          INC       PATAM1FD/INC
          INC       PATALRFD/INC
          INC       PATCONFD/INC
          INC       PATINVFD/INC
          INC       PATHSPFD/INC
          INC       PATLOCFD/INC
          INC       PATMA1FD/INC
          INC       PMSPX2FD/INC
          INC       PATMI1FD/INC
          INC       PATNIDFD/INC
          INC       PATPR1FD/INC
          INC       PATGSRFD/INC
          INC       PATURAFD/INC
          INC       PATVISFD/INC
          INC       PMSVX1FD/INC
          INC       PMSCURFD/INC
          INC       NHIMASFD/INC
          INC       NZHISIFD/INC
          INC       OUTPREFD/INC
          INC       WATCONFD/INC
          INC       WATTR1FD/INC
          INC       WATHISFD/INC
          INC       WATNBRFD/INC
          INC       WATPROFD/INC
          INC       WEBSECFD/INC
          INC       IBASEQFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
.**********************************************************************
.*                           VARIABLES                                *
.**********************************************************************
.
ADD       DIM       2
AMM       DIM       2
AYY       DIM       2
ACC       DIM       2
ACCEPT    FORM      1
ACTION    DIM       1
BJDAY     FORM      3
CJDAY     FORM      3
.
COL       FORM      2
COUNT     FORM      2
COUNTER   FORM      3
DIM4      DIM       4
DIM5      DIM       5
DIM6      DIM       6
DIM8      DIM       8
DIM13     DIM       13
DISPACT   FORM      1
CMDLINE   DIM       60
.
ENDDATE   DIM       8        * to date keyed in (ccyymmdd)
ERRFLG    FORM      5
ERRMSG    DIM       40
.
FILENAM1  INIT      "wat83"
FNAMEA    DIM       8        * name of the tempfile
FORM3     FORM      3
FORM8     FORM      8
FRSTGNAM  DIM       40
FULLGNAM  DIM       80
.
KYURNO    DIM       8        * keyed in UR
KYPCOD    DIM       9        * keyed in Procedure code
KYPCNT    DIM       2        * keyed in Procedure count
KYDTE     DIM       8        * Keyed in effective date from
.
OPERCODE  DIM       4
.
SCNDGNAM  DIM       40
SEC1      FORM      "00001"
SELECT    FORM      2
.
TIMEIS    DIM       8
TODAY     DIM       8        * Current date
TOTALREC  FORM      5
TYPEREC   DIM       1        * Type of Record (C,D or E)
TYPERECF  FORM      1        * 1=change 2=delete 3=erase
.
VERT      FORM      2
Z30       INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
.
TEMP1     IFILE SQL, FIXED=34, KEY="U1-2"
.
URCHKDGT  FORM      4
URWTARAY  INIT      "234567"
URNLNGTH  FORM      2
URCHKPRD  FORM      2
URNOWGHT  FORM      1
USERID    DIM       10
.
XCOUNT    DIM       2     1
XURNO     DIM       8     3
XPROC     DIM       9    11
XIPCN     DIM       2    20
XEDAT     DIM       8    22
XIUCN     DIM       4    30
+
.
PRGID     INIT      "IBAWAT83"
PRGNAM    INIT      "W/L DATA EXTRACTION - DELETE OR ERASE RECORD"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                           MAINLINE                                 *
.**********************************************************************
.
ML0000    CALL      INIT0000               * initialization 
.
          CALL      SCRN0000               * display screen
          BRANCH    EXIT,ML9999
.
ML1000    CALL      KYUR0000               * input UR
          BRANCH    EXIT,ML0000
.
          CALL      PROC0000               * input procedure code            
          BRANCH    EXIT,ML1000
.
          CALL      PCNT0000               * input procedure count           
          BRANCH    EXIT,ML1000
.
          IF        OPTION=3
            CALL      KYDT0000             * Keyin date for change sent flag
            CALL      MODR0000             * Modify records set flag to no
            GOTO      ML1000
          ENDIF
.
          IF        OPTION=2
            CALL      ERAS0000             * erase record
            GOTO      ML1000
          ENDIF
.
          CALL      DISR0000               * display wathisaf records
          BRANCH    EXIT,ML1000
.
          CALL      DELR0000               * delete record  
          GOTO      ML1000
.
ML9999    CALL      KILL0000               * kill tempfile
          CHAIN     PGM
.
+
.**********************************************************************
.*                           INIT0000                                 *
.*                     Initialise variables                           *
.*                     Called : ML0000                                *
.**********************************************************************
.
INIT0000  CALL      DISPHEAD
          CALL      TFILENAM
.
          DISPLAY   *P57:24,"Opening controlf"
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P65:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P65:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P65:24,"pmspx2af"
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P65:24,"wattr1af"
          OPEN      WATTR1A1,"wattr1af"
.
          DISPLAY   *P65:24,"wathisaf"
          OPEN      WATHISA1,"wathisaf"
.
          DISPLAY   *P65:24,"wathisaf"
          OPEN      WATHISA2,"wathisaf"
.
          DISPLAY   *P65:24,"watnbraf"
          OPEN      WATNBRA1,"watnbraf"
.
          DISPLAY   *P65:24,"watnbraf"
          OPEN      WATNBRA2,"watnbraf"
.
          DISPLAY   *P65:24,"watproaf"
          OPEN      WATPROA1,"watproaf"
.
          DISPLAY   *P65:24,"watproaf"
          OPEN      WATPROA3,"watproaf"
.
          DISPLAY   *P65:24,"pathspaf"
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P65:24,"patpr1af"
          OPEN      PATPR1A1,"patpr1af"
          DISPLAY   *P65:24,"patpx1af"
          OPEN      PATPX1A1,"patpx1af"
.
          DISPLAY   *P65:24,"patgsrnf"
          OPEN      PATGSRN1,"patgsrnf"
          DISPLAY   *P65:24,"patgsrnf"
          OPEN      PATGSRN2,"patgsrnf"
          DISPLAY   *P65:24,"patgsrnf"
          OPEN      PATGSRN3,"patgsrnf"
          DISPLAY   *P65:24,"patgsrnf"
          OPEN      PATGSRN4,"patgsrnf"
.
          DISPLAY   *P65:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
.
INIT2000  PACK      TODAY,CCC,CYY,CMM,CDD     * todays date
          REP       " 0",TODAY
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TEN;*54,CFILENO,*244,CHOSPNUM
          READ      CONTROLF,TWENTY1;*138,PTCNNHII
          READ      CONTROLF,THIRTY7;*185,WTCNTPDS,*236,WTCNBRSR
          READ      CONTROLF,EIGHTY8;*56,PTCNHADF
.
          IF        PTCNNHII = 1
            DISPLAY   *P65:24,"nhimasaf"
            OPEN      NHIMASA1,"nhimasaf"
            OPEN      NHIMASA2,"nhimasaf"
          ELSE
            DISPLAY   *P65:24,"patnidaf"
            OPEN      PATNIDA1,"patnidaf"
          ENDIF
.
INIT9999  RETURN
+
.**********************************************************************
.*                             SCRN0000                               *
.*                        Main Option Screen                          *
.*                        Called : ML0000                             *
.**********************************************************************
+
SCRN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Delete Record":
                    *P1:6,*V2LON,TWO,*HOFF," = Erase Record":
                    *P1:7,*V2LON,THREE,*HOFF," = Change Record Sent Flag to No"
.
SCRN1000  KEYIN     *P1:9,"Select Option : ",*EF,*DV,UNDLN1:
                    *P17:9,*V2LON,OPTION;
.
          BRANCH    OPTION,SCRN8000,SCRN8000,SCRN8000
.
          COMPARE   ZERO,OPTION
          GOTO      SCRN9000 IF EQUAL
.
          BEEP
          GOTO      SCRN1000
.
SCRN8000  MOVE      FALSE,EXIT
          GOTO      SCRN9999
.
SCRN9000  MOVE      TRUE,EXIT
.
SCRN9999  RETURN
+
.*******************************************************************************
.*               KYUR0000                                                      *
.*  -- Key in a U/R number                                                     *
.*  -- Check if U/R number exists in wattrefd, if it does then key in other UR *
.*******************************************************************************
KYUR0000  CALL      CREA0000
          MOVE      "4",CVERT
          MOVE      "13",CCOL
          DISPLAY   *P1:3,*EF,*P1:4,"U/R Number     : "
          MOVE      TEN7,CCOL
.
          CALL      KURN
          BRANCH    OVRCD,KYUR9500
          BRANCH    EXIT,KYUR9000
          MOVE      PURNO,KYURNO
.
          MATCH     "       0",KYURNO
          GOTO      KYUR9000 IF EQUAL
          GOTO      KYUR9000 IF EOS
.
          PACK      KEY8,KYURNO
          CALL      RDMAST1
          BRANCH    OVRCD,KYUR9500
.
          PACK      KEY19,KYURNO,SP20
          CALL      RDSTREA1
          CALL      RDKTREA1
          BRANCH    OVRCD,KYUR9600          * WL patient?
.
          MATCH     KYURNO,WMURNO
          GOTO      KYUR9600 IF NOT EQUAL   * WL patient?
.
          MOVE      ZERO,EXIT               * Valid U/R Number input
          GOTO      KYUR9999
.
KYUR9000  MOVE      ONE,EXIT                * nothing input
          GOTO      KYUR9999
.
KYUR9500  DISPLAY   *P1:24,*EL,*B,"Invalid U/R. ";
          CALL      HITENTER
          GOTO      KYUR0000
.
KYUR9600  DISPLAY   *P1:24,*EL,*B,"Patient does not have WL procedure. ";
          CALL      HITENTER
          GOTO      KYUR0000
.
KYUR9999  RETURN
+
.*****************************************************************************
.                            PROC0000
.                     Key in the procedure code                           
.*****************************************************************************
PROC0000  MOVE      SP9,KYPCOD
          DISPLAY   *P1:5,*EF,*P1:5,"Procedure Code : "
.
          KEYIN     *P18:5,*V2LON,KYPCOD
.
          PACK      KYPCOD,KYPCOD,SP10
          CMATCH    SP9,KYPCOD
          IF        @EQUAL|@EOS
            GOTO      PROC9500                * nothing entered?
          ENDIF
.
          CMATCH    QUEST,KYPCOD              * ? option
          GOTO      PROC1000 IF EQUAL
.
          PACK      KEY9,KYPCOD
          CALL      RDPROA1
          BRANCH    OVRCD,PROC9000            * invalid code
.
          DISPLAY   *P18:5,*V2LON,KYPCOD;
          GOTO      PROC9999                  * valid code entered
.
PROC1000  MOVE      ZERO,HLEF                    * Whole Screen
          CALL      GETSCR00                     * save screen
.
          MOVE      ZERO,DISPACT
          CALL      WATDSPRO                     
          CALL      PUTSCR00
          GOTO      PROC0000
.
PROC9000  DISPLAY   *P1:24,"Invalid Code. ";
          CALL      HITENTER
          GOTO      PROC0000
.
PROC9500  MOVE      ONE,EXIT
.
PROC9999  RETURN
+
.*****************************************************************************
.                            PCNT0000
.                     Key in the procedure count
.*****************************************************************************
PCNT0000  MOVE      ZERO,EXIT
          MOVE      ZERO,FORM2
          DISPLAY   *P1:6,*EF,*P1:6,"Procedure Count: "
.
          KEYIN     *P18:6,*V2LON,FORM2
.
          MOVE      FORM2,KYPCNT
          PACK      KEY19,KYURNO,KYPCOD,KYPCNT
          CALL      RDTREA1
          IF        OVRCD<>1
            GOTO      PCNT9999
          ENDIF
.
          DISPLAY   *P1:24,*EL,*V2LON,"No record found with these details. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
PCNT9999  RETURN
+
.*******************************************************************************
.*                             ERAS0000                                        *
.*                 Write an erase record to watnbraf                           *
.*******************************************************************************
ERAS0000  PACK      KEY19,KYURNO,KYPCOD,KYPCNT
          CALL      RDTREA1
          BRANCH    OVRCD,ERAS9000
.
          KEYIN     *P1:24,*V2LON,"Are you sure you want to erase ":
                    "this record? ",*HOFF,ANS
          REP       UPPLOW,ANS
.
          MATCH     ANSN,ANS
          GOTO      ERAS9999 IF EQUAL
.
          MATCH     ANSY,ANS
          GOTO      ERAS0000 IF NOT EQUAL
.
          MOVE      ANSE,WTNBRTYP
          MOVE      KYURNO,WTNBURNO
          MOVE      KYPCOD,WTNBCODE
          MOVE      KYPCNT,WTNBCNT
          CALL      IBACLOCK
          PACK      WTNBEDAT,CCC,CYY,CMM,CDD
          REP       " 0",WTNBEDAT
          PACK      WTNBETIM,CTIMEIS 
          MOVE      ANSN,WTNBRSFL
          MOVE      WTWMECNT,WTNBBNUM
.
          PACK      KEY35,WTNBURNO,WTNBCODE,WTNBCNT,WTNBEDAT,WTNBETIM
          CALL      WRWTNBR2              * write a record to watnbraf
          GOTO      ERAS9999
.
ERAS9000  DISPLAY   *P1:24,*V2LON,"Treatment record does not exist. ";
          CALL      HITENTER
.
ERAS9999  RETURN
+
.*******************************************************************************
.*                             DISR0000                                        *
.*             Display valid records for this UR, procedure & date             *
.*******************************************************************************
DISR0000  DISPLAY   *P8:8,*V2LON,*ULON,"Date     ":
                    *P22:8,"Time    ":
                    *P34:8,"Booking Status ",*P49:8,"Priority ",*P60:8,"Rank  ":
                    *P68:8,"Sent "
          MOVE      NINE,CVERT
          MOVE      ONE,COUNT 
          PACK      KEY31,KYURNO,KYPCOD,SP30
          CALL      RSWTHIS1
DISR1000  CALL      RKWTHIS1
          BRANCH    OVRCD,DISR9999
.
          MATCH     WTHIURNO,KYURNO           * same UR?
          GOTO      DISR9999 IF NOT EQUAL
.
          MATCH     WTHIPROC,KYPCOD           * same procedure?
          GOTO      DISR9999 IF NOT EQUAL
.
          MATCH     DWTHIPCN,KYPCNT           * same proc count?
          GOTO      DISR9999 IF NOT EQUAL
.
          UNPACK    WTHIEDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P1:CVERT,*V2LON,COUNT,*HOFF:
                    *P8:CVERT,CPCDATE:        * display details
                    *P22:CVERT,WTHIETIM:
                    *P34:CVERT,WTHIBSCD:
                    *P49:CVERT,WTHIPRIO:
                    *P60:CVERT,WTHIRANK:
                    *P68:CVERT,WTHIRECS
          ADD       ONE,CVERT
          MOVE      COUNT,KEY2
.
          MOVE      WTHIURNO,XURNO           * write to tempfile
          MOVE      WTHIPROC,XPROC
          MOVE      DWTHIPCN,XIPCN
          MOVE      WTHIEDAT,XEDAT
          MOVE      DWTHIUCN,XIUCN
.
          WRITE     TEMP1,KEY2;KEY2,XURNO,XPROC,XIPCN:
                              XEDAT,XIUCN
          ADD       ONE,COUNT
          GOTO      DISR1000
.
DISR9999  IF        COUNT=1
            DISPLAY   *P1:24,"No records found. ";
            CALL      HITENTER
            MOVE      ONE,EXIT
          ENDIF
          RETURN
+
.*****************************************************************************
.                            DELR0000
. Write a delete record to watnbraf                                      
.*****************************************************************************
DELR0000  PACK      KEY19,KYURNO,KYPCOD,KYPCNT 
          CALL      RDTREA1 
          BRANCH    OVRCD,DELR9999
.
          KEYIN     *P1:24,*EL,*V2LON,"Are you sure you want to delete the ":
                    "last booking status change (Y/N)? ",ANS
          REP       UPPLOW,ANS
          MATCH     ANSY,ANS
          GOTO      DELR1000 IF EQUAL
          MATCH     ANSN,ANS
          GOTO      DELR9999 IF EQUAL
          GOTO      DELR0000
.
DELR1000  MOVE      WMURNO,WTNBURNO
          MOVE      WMCODE,WTNBCODE
          MOVE      DWMCNT,WTNBCNT
          MOVE      ANSD,WTNBRTYP
          MOVE      ANSN,WTNBRSFL
          CALL      IBACLOCK
          PACK      WTNBEDAT,CCC,CYY,CMM,CDD
          REP       " 0",WTNBEDAT
          PACK      WTNBETIM,CTIMEIS
          MOVE      WTWMECNT,WTNBBNUM
.
          PACK      KEY35,WTNBURNO,WTNBCODE,WTNBCNT,WTNBEDAT,WTNBETIM
          CALL      WRWTNBR2
.
DELR9999  RETURN
+
.*****************************************************************************
.*                         CREA0000                                          *
.*                  Create a temp file                                       *
.*****************************************************************************
CREA0000  PACK      FNAMEA,FILENAM1,TFILNAME
          CALL      KILL0000                * Kill tempfile
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMEA,CMDLINE
          APPEND    " 34 U1-2",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID          * build the tempfile
          OPEN      TEMP1,FNAMEA
.
CREA9999  RETURN
+
.*****************************************************************************
.*                         KILL0000                                          *
.*                   Kill  a temp file                                       *
.*****************************************************************************
KILL0000  CLOSE     TEMP1                   * close tempfile
          CLEAR     CMDLINE                 * clear command line
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMEA,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID          * delete the tempfile
.
KILL9999  RETURN
+
.*****************************************************************************
.*                         KYDT0000                                          *
.*                   Keyin date for the change of send flag to no            *
.*****************************************************************************
KYDT0000  CALL       INDT0000
          MOVE       "18",CCOL
          MOVE       "7",CVERT
          MOVE       ZERO,CHIGHLT
          DISPLAY   *P1:7,*EF,*P1:7,"Effective Date : "
          CALL      KEYDATE
          BRANCH    OVRCD,KYDT0000
          PACK      KYDTE,CCENT,CYEAR,CMON,CDAY
          REPLACE   " 0",KYDTE
KYDT9999  RETURN
+
.***************************************************************************
.                         INDT0000   : initialize date
.                         Called by  : KYDT0000  
.***************************************************************************
INDT0000  MOVE      CCC,CCENT
          MOVE      SP2,CYEAR
          MOVE      SP2,CMON
          MOVE      SP2,CDAY
INDT9999  RETURN
+
.***************************************************************************
.                         MODR0000   : modify records 
.                         Called by  : ML1000              
.***************************************************************************
MODR0000  MOVE      ZERO,COUNTER 
          KEYIN     *P1:24,*HOFF,"Are you sure you want to modify ":
                    "these records? : ",*V2LON,ANS
          REP       UPPLOW,ANS
.
          MATCH     ANSN,ANS
          GOTO      MODR9999 IF EQUAL
.
          MATCH     ANSY,ANS
          GOTO      MODR1000 IF EQUAL
.
          BEEP
          GOTO      MODR0000
.
MODR1000  PACK      KEY35,KYURNO,KYPCOD,KYPCNT,KYDTE,SP30
          CALL      RSWTNBR2
MODR1500  CALL      RKWTNBR2
          BRANCH    OVRCD,MODR5000
.
          MATCH     WTNBURNO,KYURNO
          GOTO      MODR5000 IF NOT EQUAL
.
          MATCH     WTNBCODE,KYPCOD
          GOTO      MODR5000 IF NOT EQUAL
.
          MATCH     WTNBCNT,KYPCNT
          GOTO      MODR5000 IF NOT EQUAL
.
          ADD       ONE,COUNTER
          MOVE      ANSN,WTNBRSFL
          CALL      UPWTNBR2
          GOTO      MODR1500
.          
MODR5000  PACK      KEY31,KYURNO,KYPCOD,KYPCNT,KYDTE,SP30
          CALL      RSWTHIS1
MODR5500  CALL      RKWTHIS1
          BRANCH    OVRCD,MODR9000
.
          MATCH     WTHIURNO,KYURNO
          GOTO      MODR9000 IF NOT EQUAL
.
          MATCH     WTHIPROC,KYPCOD
          GOTO      MODR9000 IF NOT EQUAL
.
          MATCH     DWTHIPCN,KYPCNT
          GOTO      MODR9000 IF NOT EQUAL
.
          ADD       ONE,COUNTER
          MOVE      ANSN,WTHIRECS
          CALL      UPWTHIS1
          GOTO      MODR5500
.
MODR9000  DISPLAY   *P1:24,*HOFF,"Number of records updated. ":
                           *V2LON,COUNTER,*HOFF,"  " ;
          CALL      HITENTER
.
MODR9999  RETURN
+
GETSVAR   RETURN
.**********************************************************************
.*              I/O INCLUDES FOR FILES AND GENERAL ROUTINES           *
.**********************************************************************
.
          INC       STD001IO
          INC       PATCOMN1
          INC       PATCOMN2
.
          INC       CLPATMAS
          INC       CALCAGE
          INC       NZHISIIO
          INC       NZIBSRCH
          INC       WATDSPRO
          INC       BOKRC1IO/INC
          INC       PATAK1IO/INC
          INC       PATAM1IO/INC
          INC       PATALRIO/INC
          INC       PATHSPIO/INC
          INC       PATINVIO/INC
          INC       PATNIDIO/INC
          INC       PATLOCIO/INC
          INC       PATMA1IO/INC
          INC       PMSPX2IO/INC
          INC       PATMI1IO/INC
          INC       PATPR1IO/INC
          INC       PATGSRIO/INC
          INC       PATURAIO/INC
          INC       PATVISIO/INC
          INC       PMSVX1IO/INC
          INC       PMSCURIO/INC
          INC       NHIMASIO/INC
          INC       OUTPREIO/INC
          INC       WATTR1IO/INC
          INC       WATHISIO/INC
          INC       WATNBRIO/INC
          INC       WATPROIO/INC
          INC       TFILENAM
          INC       IBASEQIO/INC
          INC       WEBERRIO/INC
.

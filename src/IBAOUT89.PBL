.*****************************************************************************
.*    Program      : IBAOUT89                                                *
.*    Description  : Outstandings Outcomes for Outpatients Report            *
.*                                                                           *
.*    Author       : ROD                                                     *
.*    Date         : 18/01/1994                                              *
.*                                                                           *
.*    Modifications:                                                         *
.*        V10.07.01 30/10/2015  Ebon Clements CAR 268970                     *
.*                  Change to use OUTCLIFD index 1 instead of index 2        *
.*        V9.09.02  04/03/2008  Peter Vela    CAR 162914                      *
.*                  Recompiled for KYOUTCLI - readk of outcliaf               *
.*        V9.09.01  28/07/2007  Jill Habasque CAR 103645                      *
.*                  Recompiled for KYOUTCLI - read of pmshcpaf                *
.*        V5.08.02  19/09/2000  Steve Armstrong   srf 647263                  *
.*                  Recompiled for changes to OUTDSCLN                        *
.*        V5.08.01  29/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*       V5.08.B01  15/03/2000  Steve Armstrong     5.08 Mods                *
.*                  Recompiled for changes to OUTCLIFD (effective date)      *
.*****************************************************************************
.*       V5.07.B01  02/11/1998    Jim Stathopoulos                           *
.*                  507 Changes                                              *
.*****************************************************************************
.*        V5.01.02  30/03/1994    Judy Clark                                 *
.*                  Changed to include Booked Patients                       *
.*                  Key should use obastrt not obatime                       *
.*        V5.01.03  17/05/1994    Paul Howells      SRF440726                *
.*                  Page break on change of Site and Clinic only             *
.*        V5.03.01  13/03/1995    Max White         SRF441100                *
.*                  Fixed hospital keyin.                                    *
.*                  Changed to include Booked Patients.                      *
.*                  Also fixed page throw on change of hospital and when     *
.*                  running report several times without returning to menu.  *
.*****************************************************************************
.
          INC       STD001FD
          INC       PATPR1FD/INC
          INC       PATMA1FD/INC
          INC       PATHSPFD/INC
          INC       PATDO1FD/INC
          INC       PMSHCPFD/INC            * National HCP file
          INC       OUTCLIFD/INC
          INC       OUTCONFD/INC
          INC       OUTOSFFD/INC
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
.
BOOKED    INIT      "Booked        "
ATTENDED  INIT      "Attended      "
NOATTEND  INIT      "Did Not Attend"
DIM40     DIM       40
STATUS    DIM       14
CLINDESE  DIM       40
CLINDESS  DIM       40
KYHOSP    DIM       3
KYCLINE   DIM       6
KYCLINS   DIM       6
FDDATE    DIM       10
FNDDATA   FORM      1
FROMDATE  DIM       8
PATNAME   DIM       26
PRDOB     DIM       8
SAVSESS   DIM       25
SAVCLIN   DIM       6
SAVHOSP   DIM       3
SAVSITE   DIM       6
SAVDATE   DIM       8
SAVTIME   DIM       5
TODAY     DIM       8
Z20       INIT      "zzzzzzzzzzzzzzzzzzzz"
.
PRGID     INIT      "IBAOUT89"
PRGNAM    INIT      "Outstanding Outcomes for Outpats Report"
VERSION   INIT      "V12.01.00"
+
.***********************************************************************
.*  ML0000  :  Mainline code                                           *
.***********************************************************************
ML0000    CALL      INIT0000                      * initialisation
.
ML1000    CALL      MENU0000                      * menu
          BRANCH    EXIT,ML9999                   * Exit selected
.
ML2000    CALL      PROC0000                      * process keyin fields
          BRANCH    EXIT,ML1000                   * nothing input
.
ML5000    CALL      RPRT0000                      * Print Report
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.***********************************************************************
.*  INIT0000  :  Initialisation                                        *
.***********************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P55:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          DISPLAY   *P55:24,"pathspaf"
          OPEN      PATHSPA1,"pathspaf"
          DISPLAY   *P55:24,"patpramf"
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
          DISPLAY   *P55:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P64:24,"pmshcpaf";
          OPEN      PMSHCPA1,"pmshcpaf"
.
          PACK      CFNAME,UID,FILCLIA1
          DISPLAY   *P55:24,CFNAME
          OPEN      OUTCLIA1,CFNAME
.
          PACK      CFNAME,UID,FILBB1A1
          DISPLAY   *P55:24,CFNAME
          CALL      OPOTBB11
.
          PACK      CFNAME,UID,FILBOKA1
          DISPLAY   *P55:24,CFNAME
          OPEN      OUTBOKA1,CFNAME
.
          DISPLAY   *P55:24,"outosfaf"
          OPEN      OUTOSFA1,"outosfaf"
.
          MOVE      ONE,CNEWDS
.
INIT9999  RETURN
+
.***********************************************************************
.*  MENU0000  :  Main Menu                                             *
.***********************************************************************
MENU0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Print report":
                    *P1:7,"Select option ? "
.
MENU1000  KEYIN     *P17:7,*DV,UNDLN1:
                    *P17:7,*V2LON,MOPTION;
.
          MOVE      ZERO,EXIT
          BRANCH    MOPTION,MENU9999
          IF        MOPTION<>0
            BEEP
            GOTO      MENU1000
          ENDIF
          MOVE      ONE,EXIT                      * Exit
.
MENU9999  RETURN
+
.**************************************************************************
.*  PROC0000  :   process keyin fields                                    *
.**************************************************************************
PROC0000  CALL      DSCR0000                      * display screen
.
          CALL      KHOS0000                      * keyin hospital
          BRANCH    EXIT,PROC9000                 * EXITCHAR input
.
          CALL      KCLI0000                      * keyin Clinic Id range
          BRANCH    EXIT,PROC9000                 * EXITCHAR input
.
          CALL      KDAT0000                      * keyin as at date 
.
. ask Ok to Continue?
.
          CALL      CONTQST
          BRANCH    CEXIT,PROC8000,PROC0000,PROC9000
.
PROC8000  MOVE      ZERO,EXIT                     * all is Ok. so continue
          GOTO      PROC9999
.
PROC9000  MOVE      ONE,EXIT                      * return to menu
.
PROC9999  RETURN
+
.**************************************************************************
.*  DSCR0000  :   Display screen layout                                   *
.**************************************************************************
DSCR0000  DISPLAY   *P1:10,*EF:
                    *P1:10,"Hospital       :":
                    *P1:12,"Clinic Id from :":
                    *P1:13,"          to   :":
                    *P1:15,"As at Date     :"
DSCR9999  RETURN
+
.**************************************************************************
.*  KCLI0000  :  Keyin a Clinic Id range                                  *
.**************************************************************************
KCLI0000  MOVE      "18",ECOL
          MOVE      "12",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          DISPLAY   *P18:12,*EL,*P18:13,*EL
.
          CALL      KYOUTCLI
          BRANCH    EXIT,KCLI2000,KCLI9000,KCLI2000      * nothing or EXITCHAR
.
          MOVE      OCACLIN,KYCLINS               * valid input
          DISPLAY   *P25:12,OCADESC
          PACK      CLINDESS,OCACLIN,SP2,OCADESC
          GOTO      KCLI5000
.
KCLI2000  MOVE      SP6,KYCLINS                   * nothing input
          DISPLAY   *P18:12,*V2LON,"Start"
          MOVE      "Start",CLINDESS              * print var
.
. keyin End clinic
.
KCLI5000  MOVE      "18",ECOL
          MOVE      "13",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
.
          CALL      KYOUTCLI
          BRANCH    EXIT,KCLI6000,KCLI9000,KCLI6000      * nothing or EXITCHAR
.
          MOVE      OCACLIN,KYCLINE               * valid input
          DISPLAY   *P25:13,OCADESC
          PACK      CLINDESE,OCACLIN,SP2,OCADESC
.
. check if valid range
.
          MATCH     KYCLINS,KYCLINE
          GOTO      KCLI8000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Invalid range.  ";
          CALL      HITENTER
          GOTO      KCLI0000
.
KCLI6000  MOVE      "zzzzzz",KYCLINE              * nothing input
          DISPLAY   *P18:13,*V2LON,"Finish"
          MOVE      "Finish",CLINDESE
.
KCLI8000  MOVE      ZERO,EXIT                     * All is howdy dudey
          GOTO      KCLI9999
.
KCLI9000  MOVE      ONE,EXIT                      * EXITCHAR
.
KCLI9999  RETURN
+
.**************************************************************************
.*  KDAT0000  :   Keyin a date range                                      *
.**************************************************************************
KDAT0000  PACK      TODAY,CCC,CYY,CMM,CDD         * get todays date
          REP       " 0",TODAY
.
. keyin as at date
.
          UNPACK    TODAY,CCENT,CYEAR,CMON,CDAY
          MOVE      "18",CCOL
          MOVE      TEN5,CVERT
          MOVE      ZERO,CDEFDTE
.
KDAT1000  CALL      KEYDATE
          BRANCH    CQUEST,KDAT1000
          BRANCH    OVRCD,KDAT9000                * anything input
.
          PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY
          PACK      FDDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR    * display var
          REP       " 0",FROMDATE 
          REP       " 0",FDDATE 
.
          MATCH     FROMDATE,TODAY
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Date can't be in the future.  ";
            CALL      HITENTER
            GOTO      KDAT0000
          ENDIF
.
          MOVE      ZERO,EXIT                     * Valid range
          GOTO      KDAT9999
.
KDAT9000  MOVE      ONE,EXIT                      * nothing input
.
KDAT9999  RETURN
+
.***********************************************************************
.*  KHOS0000  :  Keyin hospital                                        *
.***********************************************************************
KHOS0000  MOVE      "10",EVERT
          MOVE      "18",ECOL
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS8000,KHOS9000
.
          MOVE      KEY3,KYHOSP
          DISPLAY   *P25:10,PTHSNAME
          MOVE      PTHSNAME,CNAME
          MOVE      ZERO,EXIT
          GOTO      KHOS9999
.
KHOS8000  DISPLAY   *P18:10,*V2LON,"All"
          MOVE      SP3,KYHOSP
          MOVE      ZERO,EXIT
          GOTO      KHOS9999
.
KHOS9000  MOVE      ONE,EXIT                      * EXITCHAR
.
KHOS9999  RETURN
+
.***********************************************************************
.*  RPRT0000  :  Print report                                          *
.***********************************************************************
RPRT0000  DISPLAY   *P1:24,*EL,*P35:24,"Scanning...";
.
          CALL      IBACLOCK
          MOVE      ZERO,CPAGENO
          MOVE      ZERO,FNDDATA
          MOVE      SP3,SAVHOSP
          MOVE      SP6,SAVSITE
          MOVE      SP6,SAVCLIN
          MOVE      SP8,SAVDATE
          MOVE      SP5,SAVTIME
.
          PACK      KEY34,KYHOSP,IDDD,SP20,SP20
          CALL      RSOTOSF1
RPRT1000  CALL      RKOTOSF1
          BRANCH    OVRCD,RPRT9000
.
          MATCH     SP3,KYHOSP
          IF        !@EQUAL
            MATCH     KYHOSP,OTOSHOSP               * same hospital?
            GOTO      RPRT9000 IF NOT EQUAL
.
            MATCH     IDDD,OTOSSITE                 * same site?
            GOTO      RPRT9000 IF NOT EQUAL
          ELSE
            MATCH     IDDD,OTOSSITE                 * same site?
            GOTO      RPRT1000 IF NOT EQUAL
          ENDIF
.
          MATCH     KYCLINS,OTOSCLID                * clinic Id in range?
          GOTO      RPRT1000 IF LESS
          MATCH     OTOSCLID,KYCLINE
          GOTO      RPRT1000 IF LESS
.
          MATCH     OTOSDATE,FROMDATE
          GOTO      RPRT1000 IF LESS
.
. now get all corresponding boka records
.
          PACK      KEY28,OTOSSITE,OTOSCLID,OTOSDATE,OTOSTIME,SP30
          CALL      RDSBOKA1
RPRT2000  CALL      RDKBOKA1
          BRANCH    OVRCD,RPRT1000
.
          DISPLAY   *P50:24,OTOSHOSP,SP1,OBACLIN,OBADATE,OBATIME
.
          PACK      KEY25,OBASITE,OBACLIN,OBADATE,OBASTRT
          MATCH     KEY25,KEY28
          GOTO      RPRT1000 IF NOT EQUAL
.
. make sure slot is booked/attended/Not attended
.
          IF        (OBASTAT<>1) & (OBASTAT<>4) & (OBASTAT<>5)
            GOTO      RPRT2000
          ENDIF
.
          PACK      KEY8,OBAOUTNO
          CALL      RDBOKB1
          BRANCH    OVRCD,RPRT2000
.
          MATCH     SP3,OTBBOUTC                  * outcome code blank?
          GOTO      RPRT2000 IF NOT EQUAL
.
. get patient details
.
          MATCH     ZEROUR,OBAURNO
          IF        @EQUAL
            PACK      KEY8,OBAOUTNO
            CALL      RDPRAM1
            BRANCH    OVRCD,RPRT2000
          ELSE
            PACK      KEY8,OBAURNO
            CALL      RDMAST1
            BRANCH    OVRCD,RPRT2000
          ENDIF
.
. check if we need a new page
.
          MATCH     SAVHOSP,OTOSHOSP
          IF        @EQUAL
            MATCH     SAVSITE,OTOSSITE
            IF        @EQUAL
              MATCH     SAVCLIN,OTOSCLID
              IF        @EQUAL
                 COMPARE   "55",CLNO
                 IF        @LESS
                   MATCH     OTOSDATE,SAVDATE 
                   IF        !@EQUAL
                     CALL      UND80
                     CALL      TABL0000
                     GOTO      RPRT5000
                   ELSE
                     MATCH     OTOSTIME,SAVTIME
                     GOTO      RPRT5000 IF EQUAL
                     CALL      UND80
                     CALL      TABL0000
                     GOTO      RPRT5000
                   ENDIF
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
.          MATCH     KEY25,SAVSESS
.          IF        @EQUAL
.            COMPARE   "55",CLNO
.            GOTO      RPRT5000 IF LESS
.          ENDIF
.
          CALL      PAGE0000                      * print a new page
.
. print a record
.
RPRT5000  CALL      FRMT0000                      * format data for printing
.
          PRINT     *1,"|  ",OBASLOT,*8,"| ",OBAURNO,*19,"| ",PATNAME,*48:
                    "| ",PRDOB,*59,"| ",STATUS,*80,"|"
...       PACK      SAVSESS,OTOSSITE,OTOSCLID,OTOSDATE,OTOSTIME   * save session
          MOVE      OTOSHOSP,SAVHOSP
          MOVE      OTOSSITE,SAVSITE
          MOVE      OTOSCLID,SAVCLIN
          MOVE      OTOSDATE,SAVDATE
          MOVE      OTOSTIME,SAVTIME
.
          ADD       ONE,CLNO
          MOVE      ONE,FNDDATA  
          GOTO      RPRT2000
.
. no more records to print
.
RPRT9000  IF        FNDDATA=1
            CALL      UND80
            PRINT     *N,*1,"*** End of report ***" 
            GOTO      RPRT9999
          ENDIF
.
          DISPLAY   *P1:24,*EL,*B,"No data found.  ";
          CALL      HITENTER

RPRT9999  RETURN
+
.*************************************************************************
.*  FRMT0000  :  Format data for printing                                *
.*************************************************************************
FRMT0000  MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      SP4,PACTITLE
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,PATNAME              * shorten name
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPDATE,PRDOB
.
          LOAD      STATUS,OBASTAT,BOOKED,SP1,SP1,ATTENDED,NOATTEND
.
FRMT9999  RETURN
+
.*************************************************************************
.*  PAGE0000  :  print report header                                     *
.*************************************************************************
PAGE0000  MATCH     SP3,KYHOSP
          IF        @EQUAL
            PACK      KEY3,OTOSHOSP
            CALL      RDPTHSP1
            MOVE      PTHSNAME,CNAME
          ENDIF
.
          CALL      HEAD80
.
          PRINT     *20,"Clinic Id from : ",CLINDESS:
                    *N,*20,"            to : ",CLINDESE:
                    *N,*20,"    As at Date : ",FDDATE
.
          MOVE      FIVE,CLNO
          CALL      TABL0000
.
PAGE9999  RETURN
+
.*************************************************************************
.*  TABL0000  :  print Table                                             *
.*************************************************************************
TABL0000
.
. print table heading
.
          UNPACK    OBADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PRINT     *N,*1,"Clinic: ",OBACLIN,SP3,"Date: ",CPCDATE,SP3:
                    "Time: ",OTOSTIME
          CALL      UND80
          PRINT     *1,"| Slot |  U/R No. | Patient Name",*48,"|  D.O.B.  | ":
                    "Status",*80,"|"
          CALL      UND80
.
          ADD      FIVE,CLNO
.
TABL9999  RETURN
.
          INC       STD001IO
          INC       PATPR1IO/INC
          INC       PATMA1IO/INC
          INC       PATHSPIO/INC
          INC       PATDO1IO/INC
          INC       PMSHCPIO/INC            * National HCP file
          INC       OUTBOAIO/INC
          INC       OUTBB1IO/INC
          INC       OUTCLIIO/INC
          INC       OUTOSFIO/INC
          INC       KYOUTCLI
          INC       PATHSPKY
          INC       OUTDSCLN

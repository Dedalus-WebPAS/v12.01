.******************************************************************************
.* System    :  WAITING LISTS                                                 *
.* Program   :  IBAWAT21                                                      *
.* Desc      :  Failed Admissions Report                                      *
.* Function  :  List of patients who were not admitted at scheduled admission *
.*              date                                                          *
.******************************************************************************
.* Date      :  19/10/1988                                                    *
.* Author    :  N.S.                                                          *
.* Comments  :                                                                *
.* Mods.     :                                                                *
.******************************************************************************
.* V12.00.01 22/05/2025  Alina Bhari   TSK 0955096                            *
.*           Added alpanumeric visit number generation -CLR1000               *
.* V11.02.01 09/02/2022  Thanh T          TSK 0889899                         *
.*           Added WMDESC2 - Procedure Description 2                          *
.*                 WMDESC3 - Procedure Description 3                          *
.* V11.00.01 10/03/2020  Jill Parkinson Task 0879163                          *
.*           Added SEXDSCIO include                                           *
.******************************************************************************
.*        V10.05.01 05/01/2015  Ebon Clements CAR 261630                      *
.*                  Removed port number from temp file name                   *
.*        V9.03.02  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2      *
.*                  - Add Sex Description routine SEXDSC00 with Code "R" -    *
.*                  "Intersex" added                                          *
.*        V9.03.01  23/10/2003  Ebon Clements  CAR 44643                      *
.*                  Changed report to include cancelled Admissions            *
.*        V9.02.01  01/11/2001  Greg Horvat                                   *
.*                  Recompiled for ACTCOMN1 - Added xtra option to the        *
.*                  parameter PNSWRACE                                        *
.*        V9.00.B01 01/06/2001  Jill Habasque                                 *
.*                  Added printing of Admitting Point                         *
.*        V5.10.B01 28/03/2001  Glenn Saunders                                *
.*                  Remove access to PTCNSNDX and associated processing.      *
.*                  Remove all references to PATSUR file (old surname file).  *
.*        V5.08.03  01/09/2000  Tonii                                         *
.*                  Mods to accept Unknown and Indeterminate as valid patient *
.*                  sex description                                           *
.*        V5.08.02  21/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.01  11/05/2000  J.Tan                                         *
.*                  Recompiled for changing record length of WATTR1           *
.*        V5.08.B01  30/12/1999  Steve Armstrong                              *
.*                   Fixed global recompile bugs                              *
.******************************************************************************
.*                  22/10/1998  Glenn Berry      5.07 changes                 *
.******************************************************************************
.*              V5.01.01  19/06/89 M. Ohleiter                                *
.*                        Changes for release 5.01                            *
.*              V5.01.02  09/07/89 J. Clark                                   *
.*                        Added Key26 variable added to PATIODOC by SOG       *
.*              V5.01.03  12/10/89 S. Barcham         SRF 102641              *
.*                        Changed Heading Unit To Referral                    *
.*              V5.01.10  22/05/90 Bill Dew                                   *
.*                        Upgrade and enhance WAT21 to program specs provided *
.*                        by S. Barcham.  This program has become a rewrite.  *
.*                        25/07/90 Paul Duncan                                *
.*                        Implemented second surname file                     *
.*                        24/07/1990 Bill Dew                                 *
.*                        Get rid of HTYPREPT from control file.  Also fix    *
.*                        the surname sequence report.                        *
.*                        30/10/90 Graeme Williams                            *
.*                        Changed option names to "Date / ...." sequence      *
.*                        Was always in this sequence, option names were just *
.*                        misleading                                          *
.*        V5.01.121 08/01/1990  i chung           SRF 107498                  *
.*                  Fixed Date/Surname sequence option - report did not       *
.*                  come out in surname sequence                              *
.*        V5.01.122 25/01/1991  Justin Coates                                 *
.*                  Added BOKRC1FD/IO                                         *
.*        V5.01.123 23/05/1991  Steve O'Gorman                                *
.*                  Fixed errors found by the new Compiler                    *
.*        V5.01.124 08/07/1991  Justin Coates     SRF 109384                  *
.*                  Added the status of patients found by report              *
.*        V5.01.125 22/02/1993  Justin  Coates                                *
.*                  Allow for status of 8 - not performed                     *
.*        V5.01.126 02/03/1993  Neeriem "I Hate Support" Dye (I.B.A.)         *
.*                  Reformat print for new address lengths                    *
.*        V5.01.127 27/03/1993  i chung           (Dudley change)             *
.*                  Added user security check                                 *
.*        V5.01.128 26/08/93  ROD                                             *
.*                  RECOMPILED FOR nzhis                                      *
.*        V5.01.129 24/01/94  WHIRLPOOL          SRF 440558                   *
.*                  Made report run by hospital & change the index it uses    *
.*        V5.01.130 26/05/1994 Ian Rutt                                       *
.*                  Fixed Global Recompile Bugs                               * 
.*        V5.01.131 20/07/1994  Rob Leonard                                   *
.*                  Fixed hospital keyin                                      * 
.*----------------------------------------------------------------------------*
.*        V5.03.01  26/10/1994  Max White        SRF 441145 (Quote 440048)    *
.*                  Consultant added to selection criteria.                   * 
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
          INC       NZHISIFD/INC                 * Include for NZHIS vars
          INC       NHIMASFD/INC                 * Include for NZHIS vars
          INC       PATCOMM/INC            * vars for KURN
.
          INC       BOKRC1FD/INC           * booking file
          INC       IBASEQFD/INC
          INC       OUTPREFD/INC
          INC       PATCALAG/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATDHEAD/INC
          INC       PATDO1FD/INC
          INC       PATGSRFD/INC
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATPR1FD/INC
          INC       PATWR1FD/INC
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
          INC       TFILEVAR/INC
          INC       WATCONFD/INC
          INC       WATSEAFD/INC
          INC       WATSEIFD/INC
          INC       WATTR1FD/INC
          INC       WATTX1FD/INC
          INC       WEBERRFD/INC
+
.******************************************************************************
.                            VARIABLE DECLARATIONS
.******************************************************************************
ADMPOINT  DIM       3
ADDRESS   DIM       50
BJDAY     FORM      3
CJDAY     FORM      3
COUNTER   FORM      3
CODE      DIM       2
.
DATE1     DIM       10
DATE2     DIM       10
DESCSTAT  DIM       12        * status description
DIM31     DIM       31
DIM3      DIM       3        * Required for PATDOCKY
DOCTNAME  DIM       20       * doctor name to print
DOCCODE   DIM       6
.
ENDCR     DIM       8
ENDCONS   DIM       6        * Finish Consultant Code
ENDNAME   DIM       30
FDATE1    DIM       8        * FROMdate ccyymmdd
FDATE2    DIM       8        * TOdate ccyymmdd
FILEDATE  DIM       8
FNAME1    DIM       8
FNAME2    DIM       8
FORM3     FORM      3
FORM8     FORM      8
FRSTGNAM  DIM       40
FULLGNAM  DIM       80
IBCNMHOS  FORM      1
INDEX     FORM      2
.
NORMDATE  DIM       10
NMPNUMB   DIM       20
PATDOB    DIM       10       * patient DOB to print
PGCONS    DIM       1        * Page Throw for each Consultant?  Y/N
PNAME     DIM       50       * Patient name suname, title, given name
PRINTADM  DIM       20     
PROCNUMB  DIM       2
.
SAVCONS   DIM       6
SAVHOSP   DIM       3
SCNDGNAM  DIM       40
SP50      DIM       50
SEXDESC   DIM       10
STARTCR   DIM       8
STRTCONS  DIM       6        * Start Consultant Code
STATUS    FORM      1
STRTNAME  DIM       30
.
TODAY     DIM       8        * current date ccyymmdd
UREND     DIM       8        * end U/R in range keyed in
URNUMBER  DIM       8
URSTART   DIM       8        * start U/R in range keyed in
WARDDESC  DIM       20
.
.---------------------------< TEMPFILE SETUP VARS >---------------------------
.
.    1-8    Scheduled admission date
.    9-16   Patient U/R Number
.    17-25  Procedure Code
.    26-27  Procedure Code counter
.    78-97  Patient Surname
.   327-329 Hospital code **** SET TO SPACES WHEN NOT MULTI HOSPITAL ****
.   311-316 Consultant Code
.
WATTMP1   IFILE     SQL, FIXED=353, KEY="u327-329,311-316,1-8,9-16,17-25,26-27"
WATTMP2   IFILE     SQL, FIXED=353, KEY="u327-329,311-316,1-8,78-97,9-16,17-25,26-27"
TFINDEX1  INIT      " 353 u327-329,311-316,1-8,9-16,17-25,26-27 u327-329,311-316,1-8,78-97,9-16,17-25,26-27"
DELCOMM   DIM       20
DOSCMND1  DIM       120
TEMPNAM1  DIM       8
.
.--------------------------------< CONSTANTS >---------------------------------
.
NUM37     FORM      "37"
NUM59     FORM      "59"
NOTPERF   INIT      "Not Performd"
PREADM    INIT      "Pre-Admitted"
REFER     INIT      "Refer"
SCHED     INIT      "Scheduled   "
UNIT      INIT      "Unit "
UNSCHED   INIT      "Unscheduled "
.
.
PRGID     INIT      "IBAWAT21"
PRGNAM    INIT      "Failed Admissions Report"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              MAINLINE                                  *
.*                            Controlling Logic                           *
.*        ML modified 22/05/90 Bill Dew : rewrite                         *
.**************************************************************************
ML0000    CALL      INIT0000           * open files     
.
ML1000    CALL      OPTN0000           * select options
          BRANCH    MOPTION,ML1111,ML1111
          GOTO      ML9999
.
ML1111    CALL      DRNG0000           * keyin from/to date
          BRANCH    EXIT,ML1000
.
          CALL      RANG0000           * keyin U/R range
.
          IF        IBCNMHOS
            CALL      KHSP0000         * keyin the hospital
            BRANCH    EXIT,ML9999
          ENDIF
.
          CALL      CRNG0000           * keyin the consultant range
          BRANCH    EXIT,ML1000
.
          CALL      PCON0000           * New page for each Consultant ?
.
          CALL      ADMP0000           * Keyin the admitting point
          BRANCH    EXIT,ML1000
.
          CALL      CONT0000           * Ok to continue ?
          BRANCH    EXIT,ML1111,ML1000
.
          CALL      CLR0000            * clear IO vars patmas, wait,surf,doc
          CALL      TRAN0000           * read wattrefd, write watmp1
          BRANCH    EXIT,ML1000        * empty tempfile
.
          CALL      PRTT0000           * print
          GOTO      ML1000
.
ML9999    CALL      DELTEMP1
          CHAIN     PGM
+
.***************************************************************************
.*        Initialisation , opening files                                   *
.***************************************************************************
INIT0000  CALL      DISPHEAD
.

          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,NUM37;*211,WTCNWLSC
.
          IF        IBCNMHOS = 1
            DISPLAY   *P64:24,"pathspaf";
            OPEN      PATHSPA1,"pathspaf"
            DISPLAY   *P64:24,"bokrc1af";
            OPEN      BOKRC1A1,"bokrc1af"
          ENDIF
          
.
            DISPLAY   *P64:24,"patwrdaf";
            OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"patpramf";
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          PACK      SP50,SP30,SP30
.
INIT1000  DISPLAY   *P64:24,"patgsrnf"
          OPEN      PATGSRN4,"patgsrnf"
          OPEN      PATGSRN3,"patgsrnf"
          OPEN      PATGSRN2,"patgsrnf"
          OPEN      PATGSRN1,"patgsrnf"
.
INIT2000  DISPLAY   *P64:24,"wattr1af";
          OPEN      WATTR1A1,"wattr1af"
          OPEN      WATTR1A2,"wattr1af"
          OPEN      WATTR1A3,"wattr1af"
          OPEN      WATTX1A1,"wattx1af"
.
          IF        WTCNWLSC = 1
            DISPLAY   *P64:24,"watseaaf";
            OPEN      WATSEAA1,"watseaaf"
            DISPLAY   *P64:24,"watseiaf";
            OPEN      WATSEIA2,"watseiaf"
          ENDIF
.
          CALL      IBACLOCK
          PACK      TODAY,CCC,CYY,CMM,CDD
          REP       " 0",TODAY
.
          CALL      SETUPTMP
.
          MOVE      ONE,CNOUNDLN
.
          MOVE      ONE,CNEWDS
.
INIT9999  RETURN
+
.**************************************************************************
.                                 SETUPTMP
.         Setup temp file usage
.
.**************************************************************************
.
.---- Setup DOSCMND1 : creates the tempfile for tray items and instruments ----
.
SETUPTMP  CALL      TFILENAM * Get temp file name in create
          MOVE      TFILNAME,TEMPNAM1
.
          PACK      DOSCMND1,SP30,SP30,SP30,SP30
          CLEAR     DOSCMND1
          APPEND    "isbuild ",DOSCMND1
          APPEND    TEMPNAM1,DOSCMND1
          APPEND    TFINDEX1,DOSCMND1
          RESET     DOSCMND1
.
.STMPQQQ   DISPLAY   *P1:3,DOSCMND1;
.
STMP9999  RETURN
+
.*****************************************************************************
.                                 OPENTMP1
.*****************************************************************************
OPENTMP1  DISPLAY   *P1:24,*EL,*V2LON,"Creating temporary work file.  ":
                           *BLINKON,"Please wait...";
.
          CALL      DELTEMP1           * delete the temp file
          EXECUTE   DOSCMND1,TASKID    * temp index file for item code order
          OPEN      WATTMP1,TEMPNAM1   * open the tempfile file pointer
          OPEN      WATTMP2,TEMPNAM1   * open the tempfile file pointer
          RETURN
+
.*****************************************************************************
.                                 DELTEMP1
.*****************************************************************************
DELTEMP1  CLOSE      WATTMP1           * close 1st and 2nd index
          CLOSE      WATTMP2           * close 1st and 2nd index
          CLEAR      DELCOMM
          APPEND     "iserase ",DELCOMM 
          APPEND     TEMPNAM1,DELCOMM 
          RESET      DELCOMM 
          EXECUTE    DELCOMM,TASKID
          RETURN
+
.**************************************************************************
.*                                OPTN0000                                *
.*   OPTN modified 22/05/1990 Bill Dew : This routine handles main menu   *
.*   selection displays and options.                                      *
.**************************************************************************
OPTN0000  MOVE       ZERO,CPAGENO      * very strange operation
          MOVE       "60",CLNO
.
          HLINE     *G37,2,52,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Date / U/R Number Sequence":
                    *P1:6,*V2LON,TWO,*HOFF," = Date / Surname Sequence":
                    *P1:8,"Select Option :";
.
OPTN1000  KEYIN     *P17:8,*EL,*DV,UNDLN1,*P17:8,*V2LON,MOPTION;
.
          COMPARE   ZERO,MOPTION
          GOTO      OPTN9999 IF EQUAL
.
          BRANCH    MOPTION,OPTN1111,OPTN2222
          BEEP
          GOTO      OPTN1000
.
OPTN1111  DISPLAY   *P52:2,*V2LON,"- by Date / U/R Number ";
          MOVE      " Date / U/R No. Seq. ",CPHDROPT
          GOTO      OPTN9999
.
OPTN2222  DISPLAY   *P52:2,*V2LON,"- by Date / Surname ";
          MOVE      " Date / Surname Seq. ",CPHDROPT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                                DRNG0000                                   *
.*   Get date range Store dates in DATE1 DATE2 and FDATE1 FDATE2             *
.*   This is a Bill Dew routine replacing the Old date range function.       *
.*****************************************************************************
DRNG0000  DISPLAY    *P1:10,*EF,"From Scheduled Admission Date :":
                     *P1:11,"To   Scheduled Admission Date :";
.
          MOVE      "33",CCOL               * setup screen position 1st keyin
          MOVE      "10",CVERT
          MOVE      CCC,CCENT
          UNPACK    SP6,CDAY,CMON,CYEAR
          CALL      GDAT0000                * get month and year calender
          COMPARE   ZERO,EXIT               * test if return hit 
          GOTO      DRNG1000 IF EQUAL
          MOVE      "Start",DATE1           * set from the starting record
          MOVE      SP10,FDATE1             * start of file is spaces
          DISPLAY   *PCCOL:CVERT,*EL,*V2LON,"Start",*HOFF;
          GOTO      DRNG2000
.
DRNG1000  MOVE      NORMDATE,DATE1
          MOVE      FILEDATE,FDATE1
.
DRNG2000  MOVE      "33",CCOL
          MOVE      "11",CVERT
          UNPACK    FDATE1,CCENT,CYEAR,CMON,CDAY
          CALL      GDAT0000                * get end date range
          BRANCH    EXIT,DRNG9999
          MOVE      NORMDATE,DATE2
          MOVE      FILEDATE,FDATE2
.
          MATCH     FDATE1,FDATE2
          GOTO      DRNG9999 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*V2LON,*B:
                    "** To Date must be greater than or equal From Date **   ";
          CALL      HITENTER
          GOTO      DRNG2000
.
DRNG9999  RETURN
+
.*****************************************************************************
.*        Get date : FILEDATE (ccyymmdd);  NORMDATE (ddmmyycc)
.*****************************************************************************
.
GDAT0000  CALL      KEYDATE                 * keyin the period
          BRANCH    CQUEST,GDAT0000
          BRANCH    OVRCD,GDAT9000
          PACK      FILEDATE,CCENT,CYEAR,CMON,CDAY
          REPLACE   " 0",FILEDATE           * replace blanks with zeros
          CALL      PACDATE
          PACK      NORMDATE,CPCDATE,SP10
          REPLACE   " 0",NORMDATE           * replace blanks with zeros
.
GDAT9000  MOVE      OVRCD,EXIT
.
GDAT9999  RETURN
+
.**************************************************************************
.*                                RANG0000                                *
.*        Keyin range of UR (from - to)                                   *
.*        RANG was copied from (WAT20 modified by P.Duncan) modified by   *
.*        Bill Dew for WAT21                                              *
.**************************************************************************
RANG0000  DISPLAY   *P1:13,"From U/R Number   : ",*P21:13,*EL,UNDLN8:   
                    *P1:14,"To   U/R Number   : ";
.
          MOVE      FALSE,EXIT              * reset exit 
          MOVE      SP8,STARTCR
          MOVE      SP30,STRTNAME
          MOVE      "       0",URSTART
.
          MOVE      "13",CVERT
          MOVE      TWENTY1,CCOL
          MOVE      TWO,SRCHOPT
          MOVE      ZERO,SRCHSYS 
          CALL      KURN
          BRANCH    EXIT,RANG0500
          BRANCH    OVRCD,RANG8000
          MOVE      PURNO,URSTART
          MATCH     "       0",URSTART
          GOTO      RANG1000 IF NOT EQUAL
          GOTO      RANG2000
.
RANG0500  MOVE      "Start   ",STARTCR  
          PACK      PNAME,SP30,SP30
          GOTO      RANG2000
.
RANG1000  MOVE      URSTART,STARTCR
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,STRTNAME
.
RANG2000  DISPLAY   *P21:13,*EL,*V2LON,STARTCR,*HOFF,*P35:13,STRTNAME;
.
RANG2100  DISPLAY   *P21:14,*EL,UNDLN8;
.
          MOVE      SP30,ENDNAME
          MOVE      "99999999",UREND
.
          MOVE      "14",CVERT
          MOVE      TWENTY1,CCOL
          MOVE      TWO,SRCHOPT
          MOVE      ZERO,SRCHSYS
          CALL      KURN
          BRANCH    EXIT,RANG2500
          BRANCH    OVRCD,RANG8800
          MOVE      PURNO,UREND
.
          MATCH     "99999999",UREND
          GOTO      RANG3000 IF NOT EQUAL
          GOTO      RANG4000
.
RANG2500  MOVE      "Finish  ",ENDCR
          PACK      PNAME,SP30,SP30
          GOTO      RANG4000
.
RANG3000  MOVE      UREND,ENDCR
          MOVE      PSNAME,PACSNAME     
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,ENDNAME
. 
RANG4000  DISPLAY   *P21:14,*EL,*V2LON,ENDCR,*HOFF,*P35:14,ENDNAME;
.
          BRANCH    MOPTION,RANG5000,RANG9999
          GOTO      RANG9999
.
RANG5000  MATCH     URSTART,UREND
          GOTO      RANG9999 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*V2LON,*B:
                    "** To U/R must be greater than or equal to From U/R **  ";
          CALL      HITENTER
          GOTO      RANG2100
.
RANG8000  DISPLAY   *P1:24,*EL,*V2LON,*B:
                    "** Invalid U/R Number for Waiting Lists file **    ";
          CALL      HITENTER
          GOTO      RANG0000
.
RANG8800  DISPLAY   *P1:24,*EL,*V2LON,*B:
                    "** Invalid U/R Number for Waiting Lists file **    ";
          CALL      HITENTER
          GOTO      RANG2000
.
RANG9999  RETURN
+
.**************************************************************************
GETSVAR   RETURN
+
.**************************************************************************
REDISPS   DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Date / U/R Number Sequence":
                    *P1:6,*V2LON,TWO,*HOFF," = Date / Surname Sequence":
                    *P1:8,"Select Option : ",*V2LON,MOPTION;
.
          DISPLAY   *P1:12,"From Scheduled Admission Date : ":
                    *V2LON,DATE1,*HOFF:
                    *P1:13,"To   Scheduled Admission Date : ",*V2LON,DATE2;
.
          DISPLAY   *P1:15,"From U/R Number   : ",*V2LON,STARTCR,*HOFF:
                    *P35:15,STRTNAME:
                    *P1:16,"To   U/R Number   : ";
REDI9999  RETURN
+
.**************************************************************************
.*                                CONT0000                                *
.*        Ok to continue ? (Y/N)                                          *
.**************************************************************************
CONT0000  MOVE       FALSE,EXIT                       * reset exit
          DISPLAY    *P1:24,*EL,"Ok to Continue (":
                     *V2LON,ANSY,*HOFF,SLASH:
                     *V2LON,ANSN,*HOFF,SLASH:
                     *V2LON,ANSC,*HOFF,") ?";
.
CONT1000  KEYIN      *P26:24,*EL,*DV,UNDLN1,*P26:24,*V2LON,ANS;
          REPLACE    UPPLOW,ANS
          DISPLAY    *P26:24,*V2LON,ANS;
.
          MATCH      ANSN,ANS
          GOTO       CONT9000 IF EQUAL
          MATCH      ANSY,ANS
          GOTO       CONT9999 IF EQUAL
          MATCH      ANSC,ANS
          GOTO       CONT8000 IF EQUAL
          BEEP
          GOTO       CONT1000
.
CONT8000  MOVE       TWO,EXIT               * "Cancel" option selected
          GOTO       CONT9999
.
CONT9000  MOVE       TRUE,EXIT              * "No" option selected
.
CONT9999  RETURN
+
.*****************************************************************************
.*        Clear PATMA1FD variables                                           *
.*****************************************************************************
CLR0000   MOVE      SP8,PURNO
.         MOVE      ZERO,PURNO
.
          MOVE      SP30,PTITL
          MOVE      SP30,PSNAME
          MOVE      SP30,PGNAME
          MOVE      SP30,PPSNAME
          MOVE      SP50,PADD1
          MOVE      SP50,PADD2
          MOVE      SP50,PSUBRB
          MOVE      SP30,PPOST
          MOVE      SP30,PTELEP
          MOVE      SP30,PSEX
          MOVE      SP30,PBDATE
          MOVE      ZERO,PSAGE
          MOVE      SP30,PCONT
          MOVE      SP30,PREG
          MOVE      SP30,PTYPE
          MOVE      SP30,POCCUP
          MOVE      SP30,PMEDI
          MOVE      SP30,PPENNO
          MOVE      ZERO,PYRRES
          PACK      PNAME,SP30,SP30
          PACK      PATDOB,SP10
.
          PACK      GSRSKEY,SP10
          PACK      GSRGKEY1,SP8 
          PACK      GSRGKEY2,SP8 
          PACK      GSRSNAM,SP70
          PACK      GSRGNAM,SP70
          MOVE      ZERO,GSRURNO
.
          PACK      DCODE,SP10
          PACK      DTITL,SP10
          PACK      DGNAME,SP30
          PACK      DSNAME,SP20
          PACK      DRTYPE,SP10
          PACK      DRNAME,SP30
          PACK      DSADD1,SP50
          PACK      DSADD2,SP50
          PACK      DSADD3,SP50
          PACK      DSPOST,SP50
          PACK      DTELES,SP20
          PACK      DTELEP,SP20
          MOVE      ZERO,DPAGER
          PACK      DASSOC1,SP30
          PACK      DASSOC2,SP30
          PACK      DOCTNAME,SP30
.
CLR1000   MOVE      ZERO,WMURNO
          MOVE      SP30,WMCODE
          MOVE      ZERO,WMCNT
          PACK      WMDESC,SP30,SP30,SP10
          MOVE      ZERO,WMTIME
          PACK      WMREASON,SP30,SP30
          MOVE      SP30,WMDATE4
          MOVE      SP30,WMREMOVE
          MOVE      SP30,WMUDEF1
          MOVE      SP30,WMUDEF2
          MOVE      SP30,WMUDEF3
          MOVE      SP30,WMUDEF4
          MOVE      ZEROVISN,WMBOOK
          MOVE      SP70,WTWMRADT
          PACK      WMDESC2,SP70,SP70
          PACK      WMDESC3,SP70,SP70
          PACK      WTWMSPAR,SP30,SP30
          MOVE      ZERO,WMSTAT
          MOVE      SP30,WMDATE1
          MOVE      SP30,WMDATE2
          MOVE      SP30,WMDATE3
          MOVE      SP30,WMUNIT
          MOVE      ZERO,WMDOCTOR
          MOVE      ZERO,WMSTAY
          MOVE      SP30,WMPTY
          RETURN
+
.*****************************************************************************
.*                                TRAN0000                                   *
.*   Read data from wattr1af.dat between specified date range and transfer   *
.*   all patient records with treatment status of 1 2 3 Unscheduled, Booked, *
.*   pre-admitted.                                                           *
.*   TRAN was created on 25/05/90 by Bill Dew.                               *
.*****************************************************************************
TRAN0000  CALL      OPENTMP1
.
          DISPLAY   *P1:24,*EL,*V2LON,*P28:24,"***  Extracting Data  ***",*W2:
                    *HOFF,*P16:24,*EL,"U/R Number : ",*P47:24,"Scanning : ";
.
          MOVE      ZERO,FORM8              * zero the record counter
          PACK      KEY20,TWO,SP20
          CALL      RDSTREA2
.
. ------ read next record ------
.
TRAN1111  CALL      RDKTREA2
          BRANCH    OVRCD TO TRAN7777
          DISPLAY   *P58:24,*EL,WMURNO;
          PACK      KEY19,WMURNO,WMCODE,DWMCNT
          CALL      RDWTTX11
          BRANCH    OVRCD,TRAN1111
.
          PACK      WARDDESC,SP70
          PACK      KEY6,WTTXADMW,SP70
          CALL      RDWARD1
          IF        OVRCD<>1
            PACK      WARDDESC,WBDESC
          ENDIF
.
          BRANCH    WMSTAT,TRAN1111,TRAN1200,TRAN1200
          IF        WMSTAT <> 9
            PACK      KEY20,NINE,SP20
            CALL      RDSTREA2
            GOTO      TRAN1111
          ENDIF
.
TRAN1200  MATCH     URSTART,WMURNO          * is read in U/R < Start U/R
          GOTO      TRAN1111 IF LESS        * yes, so invalid
.
          MATCH     WMURNO,UREND            * is read in U/R > End U/R
          GOTO      TRAN1111 IF LESS        * yes, so invalid
.
          MATCH     SP8,WMDATE3             * is there a sch. ad. date ??
          GOTO      TRAN1111 IF EQUAL       * no WMDATE3 -> no print
.
          MATCH     FDATE1,WMDATE3
          GOTO      TRAN1111 IF LESS        * DATE1 < From Date so invalid
.
          MATCH     WMDATE3,FDATE2
          GOTO      TRAN1111 IF LESS        * DATE1 > To Date so invalid
.
          MATCH     TODAY,WMDATE3
          GOTO      TRAN1111 IF NOT LESS    * WMDATE3 >= current date
.
          MATCH     STRTCONS,WMDOCTOR
          GOTO      TRAN1111 IF LESS        * Consultant < From Cons. so invalid
.
          MATCH     WMDOCTOR,ENDCONS
          GOTO      TRAN1111 IF LESS        * Consultant > To Cons. so invalid
.
          MATCH     SP70,ADMPOINT           * Admitting point
          IF        !@EQUAL
            MATCH     ADMPOINT,WTTXADMW
            GOTO      TRAN1111 IF NOT EQUAL
          ENDIF
.
          IF        IBCNMHOS = 1
            PACK      KEY8,WMBOOK
            CALL      RDBKREC1
            BRANCH    OVRCD,TRAN1111
.
            PACK      KEY6,BKREWARD,SP6
            CALL      RDWARD1
            BRANCH    OVRCD,TRAN1111
.
            MATCH     SP3,SAVHOSP 
            IF        @EQUAL
              MOVE      PTWDHOSN,PTHSHOSP
              GOTO      TRAN2222 
            ENDIF
.
            MATCH     SAVHOSP,PTWDHOSN
            GOTO      TRAN1111 IF NOT EQUAL
            MOVE      PTWDHOSN,PTHSHOSP
          ENDIF
          
.
.         check if user has security access to this record
.
TRAN2222  COMPARE   ONE,WTCNWLSC
          GOTO      TRAN3333 IF NOT EQUAL
.
          PACK      KEY4,PASSCODE
          CALL      RDWTSEA1
          COMPARE   ZERO,OVRCD
          GOTO      TRAN3333 IF EQUAL       * All access
          PACK      KEY13,PASSCODE,WMDOCTOR,WMUNIT
          CALL      RDWTSEI2
          BRANCH    OVRCD,TRAN1111          * no access
.
TRAN3333  MOVE      WMURNO,KEY8             * r5.01
          CALL      RDMAST1                 * read pat. details from master file
          BRANCH    OVRCD,TRAN6666
.
          DISPLAY   *P29:24,*V2LON,WMURNO;
.
          ADD       ONE,FORM8               * increment the record counter
          MOVE      WMURNO,URNUMBER
          MOVE      WMCNT,PROCNUMB
.
          BRANCH    MOPTION,TRAN4444,TRAN5555
          GOTO      TRAN1111
.
TRAN4444  PACK      KEY36,PTHSHOSP,WMDOCTOR,WMDATE3,WMURNO,WMCODE,WMCNT
          RESET     KEY36
          WRITE     WATTMP1,KEY36;WMDATE3,URNUMBER,WMCODE,PROCNUMB,WMDESC:
                                  PSNAME,PGNAME,PTITL,PADD1,PADD2,PSUBRB,PPOST:
                                  PTELEP,PTELEB,PBDATE,PSEX,WMPTY:
                                  WMDATE1,WMDATE2,WMUNIT,WMDOCTOR,WMSTAY,WMTIME:
                                  WMSTAT,PTHSHOSP,WTTXADMW,WARDDESC
          GOTO      TRAN1111
.
TRAN5555  PACK      KEY56,PTHSHOSP,WMDOCTOR,WMDATE3,PSNAME,WMURNO,WMCODE,WMCNT
          RESET     KEY56
          WRITE     WATTMP2,KEY56;WMDATE3,URNUMBER,WMCODE,PROCNUMB,WMDESC:
                                  PSNAME,PGNAME,PTITL,PADD1,PADD2,PSUBRB,PPOST:
                                  PTELEP,PTELEB,PBDATE,PSEX,WMPTY:
                                  WMDATE1,WMDATE2,WMUNIT,WMDOCTOR,WMSTAY,WMTIME:
                                  WMSTAT,PTHSHOSP,WTTXADMW,WARDDESC
          GOTO      TRAN1111
.
TRAN6666  DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** No Patient Master record for U/R ",WMURNO," **    ";
          CALL      HITENTER
          DISPLAY   *P1:24,*EL,*P16:24,"U/R Number : ",*P47:24,"Scanning : ";
          GOTO      TRAN1111
TRAN7777
          MOVE      ZERO,EXIT
          COMPARE   ZERO,FORM8              * test for any records found
          GOTO      TRAN9999 IF NOT EQUAL
.
          DISPLAY   *P1:24,*EL,*B,*P10:24,*V2LON,"***  No Available Data  ***":
                    SP10;
          CALL      HITENTER
          MOVE      ONE,EXIT
.
TRAN9999  RETURN
+
.****************************************************************************
.*                                PRTT0000                                  *
.*        Print tempfile                                                    *
.*   This print routine was copied from IBAWAT20 ( modified by Paul Duncan )*
.*   and modified again by Bill Dew for IBAWAT21.                           *
.****************************************************************************
PRTT0000  DISPLAY   *P1:24,*EL,*P27:24,"***  ",*V2LON,*BLINKON:
                           "Generating Report",*HOFF,"  ***";
.
          MOVE      SP3,SAVHOSP
          MOVE      SP6,SAVCONS
          MOVE      "Failed Admissions Report -",PRGNAM
          MOVE      ZERO,FORM8              * reset patient counter
.
          BRANCH    MOPTION,PRTT1000,PRTT2000
.
PRTT1000  PACK      KEY36,SP30,SP30
          RESET     KEY36
          READ      WATTMP1,KEY36;;         * position the fptr
          GOTO      PRTT3000
.
PRTT2000  PACK      KEY56,SP30,SP30
          RESET     KEY56
          READ      WATTMP2,KEY56;;         * position fptr
          GOTO      PRTT4000
.
PRTT3000  READKS    WATTMP1;WMDATE3,URNUMBER,WMCODE,PROCNUMB,WMDESC,PSNAME:
                            PGNAME,PTITL,PADD1,PADD2,PSUBRB,PPOST,PTELEP:
                            PTELEB,PBDATE,PSEX,WMPTY,WMDATE1,WMDATE2:
                            WMUNIT,WMDOCTOR,WMSTAY,WMTIME,WMSTAT,PTHSHOSP:
                            WTTXADMW,WARDDESC
          GOTO      PRTT9000 IF OVER  
          GOTO      PRTT5000
.
PRTT4000  READKS    WATTMP2;WMDATE3,URNUMBER,WMCODE,PROCNUMB,WMDESC,PSNAME:
                            PGNAME,PTITL,PADD1,PADD2,PSUBRB,PPOST,PTELEP:
                            PTELEB,PBDATE,PSEX,WMPTY,WMDATE1,WMDATE2:
                            WMUNIT,WMDOCTOR,WMSTAY,WMTIME,WMSTAT,PTHSHOSP
          GOTO      PRTT9000 IF OVER  
.
PRTT5000  MATCH     SAVCONS,WMDOCTOR
          GOTO      PRTT6000 IF EQUAL
.
          CALL      DOCN0000              * get new consultants name
.
.         Force new page for each consultant ?
.
          MATCH     PGCONS,ANSY
          IF        @EQUAL
            MOVE      "99",CLNO           * Force new page if required
          ENDIF
.
.         Enough room on page for new consultant ?
.
          COMPARE   "49",CLNO
          IF        !@LESS
            MOVE      "99",CLNO           * New page required
          ENDIF
.
PRTT6000  IF        IBCNMHOS = 1
            MATCH     SAVHOSP,PTHSHOSP
            IF        !@EQUAL
              MOVE      PTHSHOSP,SAVHOSP
              PACK      PTHSNAME,SP30,SP30
              PACK      KEY3,PTHSHOSP
              CALL      RDPTHSP1
              MOVE      "99",CLNO
            ENDIF
          ENDIF
.
          COMPARE   "55",CLNO
          CALL      HEAD000 IF NOT LESS
.
          MATCH     SAVCONS,WMDOCTOR
          GOTO      PRTT7000 IF EQUAL
.
.         New consultant heading required unless already at top of form
.
          COMPARE   "12",CLNO
          GOTO      PRTT7000 IF EQUAL
.
          PRINT     *N,*1,"Consultant  : ",WMDOCTOR,SP2,DOCTNAME,*N
          ADD       THREE,CLNO
.
          CALL      PRNTLINE
          PRINT      *1,"|  U/R No",*11,"Personal Details":
                    *61,"| Medical Details":
                    *94,"| Waiting List Details",*132,"|"
          ADD       ONE,CLNO
.
          CALL      PRNTLINE
.
PRTT7000  CALL      RDPD0000                * format patient master details
.
          MOVE      WMDESC,DIM31            * DIM31 is truncated procedure desc
          UNPACK    WMDATE1,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,NORMDATE
.
          PRINT     *1,"|",URNUMBER,*11,PNAME,*61,"| ",DIM31:
                    *94,"| Added    : ",NORMDATE,*132,"|"
.
          UNPACK    WMDATE2,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,NORMDATE
.
          PRINT     *1,"|",*11,ADDRESS,*61,"| ",DOCTNAME:
                    *94,"| Review   : ",NORMDATE,*132,"|"
.
          PACK      KEY5,ANST,ANSP,WMPTY
          CALL      CODE0000
          MOVE      TDESC,KEY20             * save priority description in KEY20
          PACK      KEY5,ANSW,ANSU,WMUNIT
          CALL      CODE0000
.
          PRINT     *1,"|",*11,"Home ",PTELEP,SP1,"Bus ",PTELEB,*61,"| Unit : ":
                    TDESC,*94,"| Priority : ",WMPTY,SP2,KEY20,*132,"|"
.
          UNPACK    WMDATE3,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,NORMDATE
          MOVE      SP20,DESCSTAT           * clear status
          LOAD      DESCSTAT,WMSTAT,UNSCHED,SCHED,PREADM
          IF        WMSTAT=EIGHT
            MOVE      NOTPERF,DESCSTAT
          ENDIF
.
          PRINT      *1,"|",*11,"Sex  : ",SEXDESC,*37,"D.O.B.   : ",PATDOB:
                    *61,"| Stay : ",WMSTAY,SP1,"Proc. Time : ",WMTIME:
                    *94,"| Scheduled: ",NORMDATE,SP2,DESCSTAT,*132,"|":
                    *N,*1,"|",*61,"|":
                    *94,"| Adm Point: ",WTTXADMW,SP2,WARDDESC,*132,"|"
.
          ADD       FOUR,CLNO
          CALL      PRNTLINE
          ADD       ONE,FORM8               * add one to patient counter
          MOVE      WMDOCTOR,SAVCONS
.
          BRANCH    MOPTION,PRTT3000,PRTT4000
.
. ---- Last record in tempfile read ----
.
PRTT9000  CALL      PRTL0000                * print last line
          MOVE      "Failed Admissions Report",PRGNAM
.
PRTT9999  RETURN
+
.*****************************************************************************
.*        Get code description from codes file
.*****************************************************************************
.
CODE0000  UNPACK    KEY5,TCODE,ACODE
          MATCH     SP3,ACODE
          GOTO      CODE9000 IF EQUAL
          CALL      RDCODE1
          BRANCH    OVRCD,CODE9000
          GOTO      CODE9999
.
CODE9000  MOVE      SP20,TDESC
.
CODE9999  RETURN
+
.*****************************************************************************
.*        Printing of last line 
.*****************************************************************************
PRTL0000  COMPARE   "55",CLNO
          CALL      HEAD000 IF NOT LESS
.
          PRINT     *N,*N,"Grand Total of Patients printed : ",FORM8
          IF        IBCNMHOS = 1
            PRINT     *N,*N,*1,"NOTE : Patients without expected wards are ignored by this report. ":
                      *1,"NOTE : Patients without expected wards are ignored by this report. " 
          ENDIF
          PRINT     *N,*N,"***  End of Report  ***"
          DISPLAY   *P1:24,*EL,*V2LON,*P22:24:
                    "***  Report Generation Completed  ***",*W2;
          RETURN
+
.****************************************************************************
.*        Heading for print                                                 *
.****************************************************************************
HEAD000   CLOCK     TIME,CTIMEIS
          CALL      HEAD132
.
          PRINT      *1,"From U/R Number : ",STARTCR:
                    *40,"From Scheduled Admission Date :  ",DATE1
          PRINT      *1,"To   U/R Number : ",ENDCR:
                    *40,"To   Scheduled Admission Date :  ",DATE2
.
          ADD       THREE,CLNO
.
          PRINT      *1,"Admitting Point  : ",PRINTADM,*N
          ADD        ONE,CLNO
.
          IF        IBCNMHOS = 1
            PRINT       "Hospital Id : ",PTHSHOSP,SP5,PTHSNAME
            ADD        ONE,CLNO
          ENDIF
.
          PRINT      *1,"Consultant  : ",WMDOCTOR,SP2,DOCTNAME,*N
          ADD       TWO,CLNO
.
          CALL      PRNTLINE
          PRINT      *1,"|  U/R No",*11,"Personal Details":
                    *61,"| Medical Details":
                    *94,"| Waiting List Details",*132,"|"
          ADD       ONE,CLNO
.
          CALL      PRNTLINE
          RETURN
+
.****************************************************************************
.*        Print underline
.****************************************************************************
PRNTLINE  PRINT     *1,"*---------------------------------------------":
                       "----------------------------------------------":
                       "---------------------------------------*"
          ADD       ONE,CLNO
          RETURN
+
.*****************************************************************************
.*                                RDPD0000                                   *
.*                         Read patient details                              *
.*        Get patient name in a DIM45  and patdob (dd/mm/ccyy)               *
.*****************************************************************************
RDPD0000  PACK      ADDRESS,SP30,SP30
          MOVE      SP10,PATDOB
          PACK      PTELEP,SP10
          PACK      PTELEB,SP10
          MOVE      SP10,SEXDESC
.
          MOVE      URNUMBER,KEY8           * r5.01
.         UNPACK    URNUMBER,KEY2,KEY6      * r5
          CALL      RDMAST1                 * read pat. details from master file
.                                           * overcondition would not occur here
          CALL      PATN0000                * set patient's name
.
RDPD1000  UNPACK    PBDATE,XCENT,XYEAR,XMON,XDAY
          PACK      PATDOB,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
.
.* ****************************************** Start of Changes         *C-49016
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          MOVE      DSEXDESC,SEXDESC
.* ******************************************   End of Changes         *C-49016
.
RDPD3000  CALL      ADDR0000  
.
RDPD9999  RETURN
+
.**********************************************************************
.*                             ADDR0000                               *
.*                     pack patient's ADDRess                         *
.**********************************************************************
ADDR0000  PACK      ADDRESS,SP70
          CLEAR     ADDRESS
.
          MATCH     SP70,PADD1
          IF        !@EQUAL
            STRIP     PADD1
            APPEND    PADD1,ADDRESS
            APPEND    ",",ADDRESS
          ENDIF
.
          MATCH     SP70,PADD2
          IF        !@EQUAL
            STRIP     PADD2
            APPEND    PADD2,ADDRESS
            APPEND    ",",ADDRESS
          ENDIF
.
          MATCH     SP70,PSUBRB
          IF        !@EQUAL
            STRIP     PSUBRB
            APPEND    PSUBRB,ADDRESS
            APPEND    ",",ADDRESS
          ENDIF
.
          MATCH     SP70,PADD4
          IF        !@EQUAL
            STRIP     PADD4
            APPEND    PADD4,ADDRESS
            APPEND    " ",ADDRESS
          ENDIF
.
          MATCH     SP70,PPOST
          IF        !@EQUAL
            SQUEEZE   PPOST
            APPEND    PPOST,ADDRESS
          ENDIF
.
          RESET     ADDRESS
          RETURN
+
.*****************************************************************************
.*                                DOCN0000                                   *
.*                    setup doctor's name in DOCtName                        *
.*****************************************************************************
DOCN0000  PACK      KEY6,WMDOCTOR
          CALL      RDDOCT1            * check for valid doctor
          BRANCH    OVRCD,DOCN9990     * tell user his code not on file
.
          MOVE      TWO,PACFRMT        * title name surname
          MOVE      DSNAME,PACSNAME    * use pacdate to fix doctors name
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          CALL      PACNAME
          MOVE      PACFNAME,DOCTNAME
          MOVE      ZERO,EXIT
          GOTO      DOCN9999
.
DOCN9990  MOVE      ONE,EXIT
          MOVE      SP30,DOCTNAME
.
DOCN9999  RETURN
+
.*****************************************************************************
.         set up PATient's Name in PNAME
.*****************************************************************************
PATN0000  MOVE      TWO,PACFRMT        * title name surname
          MOVE      PSNAME,PACSNAME    * use pacdate to fix doctors name
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          CALL      PACNAME
          MOVE      PACFNAME,PNAME
          MOVE      ZERO,EXIT
          RETURN
+
.******************************************************************************
.*                           KHSP0000                                         *
.*                    Keyin the Hospital Code                                 *
.******************************************************************************
.
KHSP0000  DISPLAY   *P1:16,"Hospital Id : "
.
          MOVE      TEN6,EVERT
          MOVE      TEN5,ECOL
          MOVE      ZERO,CKYIMAND
          MOVE      SP3,CKYIDEF3
          CALL      PATHSPKY
          IF        EXIT = 2
            MOVE      ONE,EXIT
            GOTO      KHSP9999
          ELSE
            IF        EXIT = 1
              MOVE      ZERO,EXIT
              DISPLAY   *P15:16,*V2LON,"All Hospitals",*HOFF
              MOVE      SP3,PTHSHOSP
            ELSE
              DISPLAY   *P20:16,PTHSNAME         
              MOVE      KEY3,PTHSHOSP
            ENDIF
          ENDIF
          MOVE      PTHSHOSP,SAVHOSP
.
KHSP9999  RETURN
+
.*************************************************************************
.*  CRNG0000  :  keyin Consultant                                        *
.*************************************************************************
CRNG0000  DISPLAY   *P1:18,*EF:                   * Input Starting Consultant
                    *P1:18,"From Consultant :";
.
          MOVE      "19",ECOL
          MOVE      "18",EVERT
          MOVE      SP6,CKYIDEF6                  * no default
          MOVE      ZERO,CKYIMAND                 * not mandatory
.
          CALL      PATDOCKY
.
          IF        EXIT=2
            MOVE      ONE,EXIT                    * EXITCHAR input
            GOTO      CRNG9999
          ENDIF
.
          IF        EXIT=1
            MOVE      ZERO,EXIT                   * Nothing Input
            MOVE      SP6,STRTCONS
            DISPLAY   *P19:18,*V2LON,"Start",*HOFF;
            GOTO      CRNG5000
          ENDIF
.
          MOVE      DOCCODE,STRTCONS
.
          MOVE      DGNAME,PACGNAME
          MOVE      DSNAME,PACSNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
.
          DISPLAY   *P26:18,PACFNAME;
.
CRNG5000  DISPLAY   *P1:19,*EF:                   * Input Finishing Consultant
                    *P1:19,"To   Consultant :";
.
          MOVE      "19",ECOL
          MOVE      "19",EVERT
          MOVE      SP6,CKYIDEF6                  * no default
          MOVE      ZERO,CKYIMAND                 * not mandatory
.
          CALL      PATDOCKY
.
          IF        EXIT=2
            MOVE      ONE,EXIT                    * EXITCHAR input
            GOTO      CRNG9999
          ENDIF
.
          IF        EXIT=1
            MOVE      ZERO,EXIT                   * Nothing Input
            MOVE      "zzzzzz",ENDCONS
            DISPLAY   *P19:19,*V2LON,"Finish",*HOFF;
            GOTO      CRNG9000
          ENDIF
.
          MOVE      DOCCODE,ENDCONS
.
          MOVE      DGNAME,PACGNAME
          MOVE      DSNAME,PACSNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
.
          DISPLAY   *P26:19,PACFNAME;

CRNG9000  MATCH     STRTCONS,ENDCONS
          GOTO      CRNG9999 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*V2LON,*B:
                    "** To Cons. must not be less than ":
                    "From Cons. **   ";
          CALL      HITENTER
          GOTO      CRNG5000
.
CRNG9999  RETURN
+
.*********************************************************************
.*  PCON0000  :  Ask if want new page per Consultant                 *
.*********************************************************************
PCON0000  MOVE      ANSN,PGCONS                   * assume not required
.
          DISPLAY   *P1:21,"Do you want to print each Consultant ":
                           "on a new page? (Y/N) : ";
PCON1000  KEYIN     *P61:21,*EL,*DV,*V2LON,PGCONS:
                    *P61:21,*RV,*V2LON,PGCONS
.
          REP       UPPLOW,PGCONS
          DISPLAY   *P61:21,*V2LON,PGCONS
.
          MATCH     ANSY,PGCONS
          GOTO      PCON9999 IF EQUAL
          MATCH     ANSN,PGCONS
          GOTO      PCON9999 IF EQUAL
          BEEP
          GOTO      PCON1000
.
PCON9999  RETURN
.
.***************************************************************************
.*                               ADMP0000                                  *
.*                          Close referral reason                          *
.***************************************************************************
ADMP0000  MOVE      "19",ECOL
          MOVE      "22",EVERT
          MOVE      "ap",CODE
          MOVE      SP3,ADMPOINT
          MOVE      SP70,PRINTADM
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND
          MOVE      ZERO,CKYIMODE
.
          DISPLAY   *P1:EVERT,"Admitting Point :",*EF;
          CALL      PATCODKY                * Keyin a codes file code
          BRANCH    EXIT,ADMP9000,ADMP9100
.
          MOVE      ACODE,ADMPOINT
          DISPLAY   *P23:EVERT,*EL,TDESC;
          MOVE      TDESC,PRINTADM
          MOVE      ZERO,EXIT
          GOTO      ADMP9999
.
ADMP9000  DISPLAY   *P23:EVERT,*EL,"All";
          MOVE      "All",PRINTADM
          MOVE      ZERO,EXIT
          GOTO      ADMP9999
.
ADMP9100  MOVE      ONE,EXIT
          GOTO      ADMP9999
.
ADMP9999  RETURN
.
          INC       STD001IO
          INC       CALCAGE
          INC       PATCOMN1
          INC       PATSNDX
          INC       PATSNX2
          INC       SEXDSCIO
.
          INC       PATDOCKY
          INC       PATCODKY
          INC       PATHSPKY
          INC       TFILENAM
.
          INC       BOKRC1IO/INC           * booking file
          INC       IBASEQIO/INC
          INC       OUTPREIO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATGSRIO/INC
          INC       PATHSPIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATPR1IO/INC
          INC       PATWR1IO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
          INC       WATTR1IO/INC
          INC       WATTX1IO/INC
          INC       WATSEAIO/INC
          INC       WATSEIIO/INC
          INC       WEBERRIO/INC
          INC       NZCOMPIO/INC
AGECHK
CLPATMAS  RETURN
+

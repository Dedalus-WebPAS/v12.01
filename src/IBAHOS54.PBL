.******************************************************************************
.*                                                                            *
.*                        P R O G R A M   S U M M A R Y                       *
.*                        -------------   -------------                       *
.*                                                                            *
.*     PROGRAM NAME:     IBAHOS54                                             *
.*                                                                            *
.*     FUNCTION:         UNIT DISCHARGE ANALYSIS REPORT                       *
.*                                                                            *
.*     DATE WRITTEN:     07/06/1988                                           *
.*                                                                            *
.*     AUTHOR:           J.TAN                                                *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.*                                                                            *
.******************************************************************************
.* V12.00.01 22/05/2025 Thanh T         TSK 0955096                           *
.*           Changed for alphanumeric visit number                            *
.******************************************************************************
.*        V10.13.01 05/12/2018  Don Nguyen    TSK 0838558                     *
.*                  Deleted temp file (PSTROL) after processing               *
.*        V10.01.02 06/04/2017 Ania P         CAR 261630                      *
.*                  Removed use of PORT                                       *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*                    V9.09.03  08/04/2008 Peter Vela CAR 163627              *
.*                              Added ATYPE to tempfile                       *
.*                    V9.09.02  01/04/2008 Peter Vela CAR 163627              *
.*                              Fixed report layout                           *
.*                    V9.09.01  17/03/2008 Peter Vela CAR 163627              *
.*                              Ported program to 9.09                        *
.*                    V6.05.02  08/12/1998 Nicole Harrington                  *
.*                              Removed CCENTRY                               *
.*                    V6.05.01  05/11/1998 Nicole Harrington                  *
.*                              Changed to print century on dates             *
.******************************************************************************
.*                       V4.01  31/01/89 J.Tan                                *
.*                              Mods not to include current admissions        *
.*                       V5.00  11/05/89 S.Barcham                            *
.*                              Fix 5244 I * D                                *
.*                       V5.01  18/05/89 DIG                                  *
.*                              Changed to standard and modified print for 8  *
.*                              digit U/R's etc                               *
.*                       V5.02  27/05/89 J.Tan             SRF 100750         *
.*                              Fixed Mother Adm. to Form 8 (PATMISFD).       *
.*                       V5.03  06/06/89 J.Tan             SRF 100809         *
.*                              Mods Locking master file (PATIOMAS).          *
.*                       V5.51  04/03/91 i chung           SRF 107924         *
.*                              Fixed one patient per Doctor per page problem *
.*                              that starts at page 48 -wrong var was compared*
.*                              Added ? option for Doctors range              *
.*        V6.03.01  11/10/1995  Greg Horvat      SRF 611811                   *
.*                  Changed to use CALCDAYS instead of CALC when calculating  *
.*                  the difference between two dates                          *
.*        V6.03.02  27/10/1995  Greg Horvat      SRF 612064 612205 612206     *
.*                  Recompiled for changes to NHOSPDAY                        *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
          INC       IBACONFD/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATDO1FD/INC
          INC       PATDSCFD/INC
          INC       PATMA1FD/INC
          INC       HL7BMTFD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATTRNFD/INC
          INC       IBASEQFD/INC 
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
+
.
.          Variables Definitions
.
PSTSRT    FILE      ASCII, FIXED=256
PSTROL    FILE      ASCII, FIXED=256
.
GNDFILE   IFILE     SQL, FIXED=9, KEY="u1-8"
TEMPFILE  IFILE     SQL, FIXED=140, KEY="u1-42"
.
. ----- Temp Index file consists of following variables -----
.
. Name    Type    Length    Physical      Start   Description
. ----    ----    ------    --------      -----   -----------
.XDOCTOR  DIM        6          6            1
.PSNAME   DIM       20         20            7
XDAADMNO  DIM        8          8           27
DUNIQUE   DIM        8          8           35
.AURNO    FORM       8          5           43
.PTITL    DIM        4          4           48
.PGNAME   DIM       25         25           52
.XDATIN   DIM        8          8           77
.XDATOUT  DIM        8          8           85
.XWARD    DIM        3          3           93   
.XBED     DIM        3          3           96
.PSAGE    FORM       3          3           99
.PSEX     DIM        1          1          102
.ACLSS    DIM        3          3          103
.XDOCTO   DIM        6          6          106
.XDOCFR   DIM        6          6          112
.ASTAT    FORM       1          2          118
.ADATE    DIM        8          8          120
.ATYPE    DIM        3          3          128
.
.End of Record                             140
. -----------------------------------------------------------
.
AVSTAY    FORM      5.2
CALDAYS   FORM      4
CDYSDAYS  FORM      6
CDYSFDTE  DIM       8
CDYSTDTE  DIM       8
CDYSYEAR  FORM      2
CHGWRD    FORM      1
CMDLINE   DIM       50
CUDATE    DIM       8
.
DIM3      DIM       3                  * required by PATDOCDS
DNAME1    DIM       12
DNAME2    DIM       13
DOCSCAN   FORM      1                  * Doctors file ? option flag
DOCCODE   DIM       6
.
FNAMEG    DIM       8
FNAMEI    DIM       8
FNAMER    DIM       8
.
INDAY     FORM      2
INMON     FORM      2
INYEAR    FORM      2
INCENT    FORM      2
JYEAR     FORM      2
.
.
LNO       FORM      2
NBRDAYS   FORM      4
OPCND     FORM      1
OUTDOCF   DIM       6                  * Output Doctor From
OUTDOCT   DIM       6                  * Output Doctor To
PAGENO    FORM      3
.
SAVADMN   DIM       8
SAVBED    DIM       3
SAVDOC    DIM       6
SAVWARD   DIM       3
SEC       FORM      2
STATUS    FORM      1
.
STARTD    DIM       6                  * Start Doctor
ENDD      DIM       6                  * End Doctor
.
STRDATE   DIM       8                  * Start Date
ENDATE    DIM       8                  * End Date
.
FROMDATE  DIM       8                  * FROM DATE IN (CCYYMMDD) FORMAT
TODATE    DIM       8                  * TO DATE IN (CCYYMMDD) FORMAT
.
TAVSTAY   FORM      5.2
TGNDPAT   FORM      5
TIMEIS    DIM       8
TOTDAY    FORM      5
TOTPAT    FORM      5
TTOTDAY   FORM      5
UNIQUE    FORM      8
VERT      FORM      2
.
WDATE1    DIM       8
WDATE2    DIM       8
WORKDAT   DIM       8
WSTAT     FORM      1
.
XDAY1     DIM       2
XMON1     DIM       2
XYEAR1    DIM       2
XCENT1    DIM       2
XDAY2     DIM       2
XMON2     DIM       2
XYEAR2    DIM       2
XCENT2    DIM       2
XSNAME    DIM       14
XGNAME    DIM       10
XDOCTOR   DIM       6
XDATIN    DIM       8
XDATOUT   DIM       8
XWARD     DIM       3
XBED      DIM       3
XDOCTO    DIM       6
XDOCFR    DIM       6
.
.         Constants Declaration
.
CODEP     INIT      "P "
DOCFLAG   FORM      "0"
ENDFLAG   FORM      "0"
ELEVEN    FORM      "11"
FINISH    INIT      "Finish"
FIRST     FORM      "0"
SEVEN9    FORM      "79"
.
SP11      INIT      "           "
SP12      INIT      "            "
SP13      INIT      "             "
SP14      INIT      "              "
SP50      INIT      "                                                  "
START     INIT      "Start"
.
PRGID     INIT      "IBAHOS54"
PRGNAM    INIT      "UNIT DISCHARGE ANALYSIS REPORT"
VERSION   INIT      "V12.01.00"
+
.         Display screen header and open files
.
          CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdoctf";
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,SEVEN9;*115,PTCNDGUP,*116,PTCNDGUA,*239,PTCNDGAD:
                                    *240,PTCNDGDS,*241,PTCNDGTR,*242,PTCNDGRG
.
.         set up today's date for comparison purposes
.
          PACK      CUDATE WITH CCC,CYY,CMM,CDD
          REP       " 0",CUDATE
.
.         set Doctors file scan new standards indicator on
.
          MOVE      ONE,CNEWDS
.
.         set up KEYDATE input requirements
.
          MOVE      ZERO,CDEFDTE
          MOVE      ZERO,CHIGHLT
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMEG
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMEI
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMER
.
.
.         START OF PROGRAM
.
START     DISPLAY   *P1:4,*EF:
                    *P1:4," Starting  Date    :":
                    *P1:5," Ending    Date    :":
                    *P1:7," From Doctor Code  :":
                    *P1:8," To   Doctor Code  :";
.
. ------  Keyin range of dates  ------
.
          MOVE      "23",CCOL
          MOVE      "53",ECOL
.
.         Starting Date
.
KSDAT     DISPLAY   *P23:4,*EL;
.
KSDAT1    UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          MOVE      FOUR,CVERT
          MOVE      FOUR,EVERT
.
          CALL      KEYDATE
          BRANCH    CQUEST,KSDAT
          BRANCH    OVRCD,ENDIT
.
          PACK      WORKDAT WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",WORKDAT
.
          MATCH     WORKDAT,CUDATE
          GOTO      KSDAT2 IF NOT LESS
.
          DISPLAY   *P1:24,*B,*EL,*V2LON,"Start Date must be less than":
                                         " or equal to today's date. ";
          CALL      HITENTER
          GOTO      KSDAT1
.
KSDAT2    PACK      STRDATE WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRDATE
.
          UNPACK    STRDATE,XCENT1,XYEAR1,XMON1,XDAY1   * From Date for printout
.
.         Ending Date
.
KEDAT     DISPLAY   *P23:5,*EL;
.
KEDAT1    UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          MOVE      FIVE,CVERT
          MOVE      FIVE,EVERT
.
          CALL      KEYDATE
          BRANCH    CQUEST,KEDAT1
          BRANCH    OVRCD,KSDAT
.
          PACK      WORKDAT WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",WORKDAT
.
          MATCH     WORKDAT,CUDATE
          GOTO      KEDAT2 IF NOT LESS
.
          DISPLAY   *P1:24,*B,*EL,*V2LON,"Ending Date must be less than or":
                                         " equal to today's date. ";
          CALL      HITENTER
          GOTO      KEDAT1
.
KEDAT2    PACK      ENDATE WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDATE
.
          MATCH     STRDATE,ENDATE
          GOTO      KEDAT3 IF NOT LESS
.
          DISPLAY   *P1:24,*B,*EL,*V2LON,"Ending Date must be > or equal to ":
                                         "Starting Date.  ";
          CALL      HITENTER
          GOTO      KEDAT1
.
KEDAT3    UNPACK    ENDATE,XCENT2,XYEAR2,XMON2,XDAY2    * To Date for printout
.
. ------  Keyin range of doctors  ------
.
.         From Doctor
.
KFDOC     MOVE      ZERO,DOCSCAN
          MOVE      SEVEN,CVERT
.
KFDOC0    DISPLAY   *P55:7,*EL,"(Hit <",*V2LON,"?",*HOFF,"> for scan)";
.
KFDOC1    KEYIN     *P23:CVERT,*DV,SP6,*P23:CVERT,*DV,UNDLN6:
                    *P23:CVERT,*V2LON,STARTD;
          RESET     STARTD
          GOTO      KFDOC4 IF NOT EOS
.
          MOVE      SP6,STARTD
.
KFDOC2    MOVE      START,OUTDOCF
          MOVE      SP20,DNAME1
          COMPARE   ZERO,DOCSCAN
          GOTO      KFDOC3 IF EQUAL
          CALL      REDISP
.
KFDOC3    DISPLAY   *P23:7,*EL,*V2LON,START;
          GOTO      KTDOC
.
KFDOC4    ENDSET    STARTD
          APPEND    SP6,STARTD
          RESET     STARTD
.
          MATCH     SP6,STARTD
          GOTO      KFDOC2 IF EQUAL
.
          CMATCH    QUEST,STARTD
          GOTO      KFDOC6 IF NOT EQUAL
          CALL      PATDOCDS
          MOVE      ONE,DOCSCAN
          MOVE      "24",CVERT
.
KFDOC5    DISPLAY   *P1:24,*EL," From Doctor Code  :",*P50:24,"(Hit <",*V2LON:
                               "ENTER",*HOFF,"> for ",*V2LON,START,*HOFF,")";
          GOTO      KFDOC1
.
KFDOC6    DISPLAY   *P23:CVERT,*EL,*V2LON,STARTD;
          MOVE      STARTD,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD,KFDOC8
.
          MOVE      DGNAME,ANS
          PACK      DNAME1 WITH ANS,DOT,DSNAME
          COMPARE   ZERO,DOCSCAN
          GOTO      KFDOC7 IF EQUAL
          CALL      REDISP
.
KFDOC7    DISPLAY   *P34:7,DNAME1;
          MOVE      STARTD,OUTDOCF
          GOTO      KTDOC
.
KFDOC8    DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid Doctor Code **    ";
          CALL      HITENTER
          BRANCH    DOCSCAN,KFDOC5
          GOTO      KFDOC0
.
KTDOC     MOVE      ZERO,DOCSCAN
          MOVE      EIGHT,CVERT
.
KTDOC0    DISPLAY   *P55:8,*EL,"(Hit <",*V2LON,"?",*HOFF,"> for scan)";
.
KTDOC1    KEYIN     *P23:CVERT,*DV,SP6,*P23:CVERT,*DV,UNDLN6:
                    *P23:CVERT,*V2LON,ENDD;
          RESET     ENDD
          GOTO      KTDOC4 IF NOT EOS
.
KTDOC2    MOVE      "zzzzzz",ENDD
          MOVE      FINISH,OUTDOCT
          COMPARE   ZERO,DOCSCAN
          GOTO      KTDOC3 IF EQUAL
          CALL      REDISP
.
KTDOC3    DISPLAY   *P23:8,*EL,*V2LON,FINISH;
          GOTO      REKEY
.
KTDOC4    ENDSET    ENDD
          APPEND    SP6,ENDD
          RESET     ENDD
.
          MATCH     SP6,ENDD
          GOTO      KTDOC2 IF EQUAL
.
          CMATCH    QUEST,ENDD
          GOTO      KTDOC6 IF NOT EQUAL
          CALL      PATDOCDS
          MOVE      ONE,DOCSCAN
          MOVE      "24",CVERT
.
KTDOC5    DISPLAY   *P1:24,*EL,"  To  Doctor Code  :",*P50:24,"(Hit <",*V2LON:
                               "ENTER",*HOFF,"> for ",*V2LON,FINISH,*HOFF,")";
          GOTO      KTDOC1
.
KTDOC6    DISPLAY   *P23:CVERT,*EL,*V2LON,ENDD;
          MOVE      ENDD,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD,KTDOC9
.
          MATCH     STARTD,ENDD
          GOTO      KTDOC7 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,*V2LON,"To Doctor Code must be > or equal":
                                         " to From Doctor Code. ";
          CALL      HITENTER
          BRANCH    DOCSCAN,KTDOC5
          GOTO      KTDOC0
.
KTDOC7    MOVE      DGNAME,ANS
          PACK      DNAME2 WITH ANS,DOT,DSNAME
          COMPARE   ZERO,DOCSCAN
          GOTO      KTDOC8 IF EQUAL
          CALL      REDISP
.
KTDOC8    DISPLAY   *P23:8,*EL,*V2LON,ENDD,*HOFF,*P34:8,DNAME2;
          MOVE      ENDD,OUTDOCT
          GOTO      REKEY
.
KTDOC9    DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid Doctor Code **    ";
          CALL      HITENTER
          BRANCH    DOCSCAN,KTDOC5
          GOTO      KTDOC0
+
.******************************************************************************
.         REDISP - routine to re-display screen after Doctors file scan
.
REDISP    DISPLAY   *P1:3,*EF:
                    *P1:4," Starting  Date    :  ",*V2LON,XDAY1,*HOFF,"/":
                       *V2LON,XMON1,*HOFF,"/",*V2LON,XCENT1,XCENT1,XYEAR1,*HOFF:
                    *P1:5," Ending    Date    :  ",*V2LON,XDAY2,*HOFF,"/":
                       *V2LON,XMON2,*HOFF,"/",*V2LON,XCENT2,XCENT2,XYEAR2,*HOFF:
                    *P1:7," From Doctor Code  :":
                    *P1:8," To   Doctor Code  :";
.
          CMATCH    QUEST,STARTD
          GOTO      REDISP9 IF EQUAL
.
          MATCH     SP6,STARTD
          GOTO      REDISP1 IF EQUAL
          DISPLAY   *P23:7,*V2LON,STARTD,*HOFF,*P34:7,DNAME1;
          GOTO      REDISP9
.
REDISP1   DISPLAY   *P23:7,*V2LON,START;
.
REDISP9   RETURN
+
.******************************************************************************
.
REKEY     KEYIN     *P1:24,*EF," Ok to Continue (",*V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,"/",*V2LON,"C",*HOFF,") ? ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          MATCH     "Y" TO ANS
          GOTO      CNTCHK IF EQUAL
.
          MATCH     "N" TO ANS
          GOTO      START IF EQUAL
.
          MATCH     "C" TO ANS
          GOTO      ENDIT IF EQUAL
          BEEP
          GOTO      REKEY
.
.            PROCESS START
.
CNTCHK    DISPLAY   *P1:24,*EL,*P17:24,*V2LON:
                    "** Creating and Initializing Temp Index File **",*W2;
.
          MOVE      ONE,UNIQUE
          CALL      INDZD
          BRANCH    OPCND OF ENDIT
          CLOCK     TIME,TIMEIS
.
          DISPLAY   *P1:24,*EL,*P22:24,*V2LON:
                    "** Processing Discharged Admissions **",*W2,*HOFF:
                    *P1:24,*EL,*P12:24,"Discharged Admission :":
                    *P51:24,"Scanning :";
.
          PACK      KEY16 WITH STRDATE,SP6
          CALL      RDSDSCH2
NXTDSH    CALL      RDKDSCH2
          BRANCH    OVRCD OF REPORT
          DISPLAY   *P62:24,DADMNO;
.
          UNPACK    DDATE INTO XCENT,XYEAR,XMON,XDAY
          PACK      XDATE WITH XCENT,XYEAR,XMON,XDAY
          REP       " 0",XDATE
.
.         If the patient discharged after the ENDATE, ignore it
.
          MATCH     XDATE,ENDATE
          GOTO      NXTDSH IF LESS
.
          MOVE      DADMNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD OF NXTDSH
.
          CALL      TRANS  
          GOTO      NXTDSH
.
.         Check if attending doctor was changed for this patient
.
TRANS     MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF NOMAST
.
          CALL      INTVAR
          MOVE      SP6,STADOCT
          MOVE      ZERO,DOCFLAG        * Flag for the begining of a new doctor
          MOVE      ZERO,ENDFLAG        * Flag for the last record
          MOVE      ZERO,FIRST          * Flag for the start of loop
.
          MOVE      SP6,SAVDOC
          MOVE      SP3,SAVWARD
          MOVE      SP3,SAVBED
          MOVE      SP1,STMOVE
          MOVE      SP8,STDATE
.
          PACK      KEY30 WITH AADMNO,SP20
          CALL      RDSTRAN2
NEXTTR    CALL      RDKTRAN2
          COMPARE   ONE,OVRCD
          RETURN    IF EQUAL
.
          MATCH     AADMNO,TADMN
          GOTO      NEXT10 IF EQUAL
.
          MOVE      ONE,ENDFLAG        *  Last record
          GOTO      NEXT50
.
.         Check if att.doctor was changed for the patient
.
NEXT10    MATCH     TADOCT,STADOCT
          GOTO      NEXT40 IF EQUAL
.
.         If not the first round then write the previous doctor to temp.file
.
          BRANCH    FIRST OF NEXT50
          MOVE      ONE,FIRST
.
.         Check if the doctor is within the range
.
NEXT17    MATCH     STARTD,TADOCT
          GOTO      NEXT40 IF LESS
.
          MATCH     TADOCT,ENDD
          GOTO      NEXT40 IF LESS
.
          BRANCH    DOCFLAG OF NEXT55
          MOVE      TADOCT,STADOCT
.
.         Store the doctor name who the patient transferring from
.
          MOVE      SAVDOC,XDOCFR
          MOVE      TDATE,XDATIN
          MOVE      ONE,DOCFLAG     * Flag for the begining of the new doctor
.
.         Save the record 
.
NEXT40    MOVE      TADOCT,SAVDOC
          MOVE      TWARD,SAVWARD
          MOVE      TBED,SAVBED
          MOVE      TMOVE,STMOVE
          MOVE      TDATE,STDATE
          GOTO      NEXTTR
.
.         Check if the previous doctor was satisfied in the range
.
NEXT50    COMPARE   ZERO,DOCFLAG
          GOTO      NEXT55 IF NOT EQUAL
.
.       If not then either goback to the next doctor or return if it is last rec
.
          COMPARE   ONE,ENDFLAG
          GOTO      NEXT17 IF NOT EQUAL
          RETURN
+
.
NEXT55    MOVE      SAVWARD,XWARD
          MOVE      SAVBED,XBED
.
          BRANCH    ENDFLAG OF NEXT60
.
.         Get the date and doctor that the patient transfer out to
.
NEXT57    MOVE      TDATE,XDATOUT
          MOVE      TADOCT,XDOCTO
          GOTO      NEXT70
.
NEXT60    CMATCH    "D",STMOVE
          GOTO      NEXT65 IF EQUAL
.
.         If this is the last trans. record, then no Trans.out doctor and date
.
          MOVE      SP8,XDATOUT
          MOVE      SP8,XDOCTO
          GOTO      NEXT70
.
.         If the last record was discharge move then use last date as tran.out
.
NEXT65    MOVE      STDATE,XDATOUT
          MOVE      SP6,XDOCTO
.
NEXT70    MOVE      ZERO,DOCFLAG
          MOVE      STADOCT,XDOCTOR
.
          DISPLAY   *P35:24,*V2LON,AADMNO;
.
          MOVE      AADMNO,XDAADMNO
          MOVE      UNIQUE,DUNIQUE
          PACK      KEY42 WITH XDOCTOR,PSNAME,XDAADMNO,DUNIQUE
          WRITE     TEMPFILE,KEY42;XDOCTOR,PSNAME,XDAADMNO,DUNIQUE,AURNO:
                             PTITL,PGNAME,XDATIN,XDATOUT,XWARD,XBED:
                             PSAGE,PSEX,ACLSS,XDOCTO,XDOCFR,ASTAT,ADATE:
                             ATYPE
          ADD       ONE,UNIQUE
.
          COMPARE   ONE,ENDFLAG
          RETURN    IF EQUAL
.
          CALL      INTVAR
          GOTO      NEXT17
.
.          Initialize the record
.
INTVAR    MOVE      SP6,XDOCTOR
          MOVE      SP8,XDATIN
          MOVE      SP8,XDATOUT
          MOVE      SP3,XWARD
          MOVE      SP3,XBED
          MOVE      SP6,XDOCTO
          MOVE      SP6,XDOCFR
          RETURN
.
NOMAST    DISPLAY   *P1:22,*EL,"** Patient Master Record missing for ":
                    "Admission ",*V2LON,AADMNO,*HOFF,".  Contact I.B.A. **",*W3;
          RETURN
+
.
REPORT    DISPLAY   *P1:24,*EL,*V2LON,"** ",*BLINKON,"Generating Report":
                         *HOFF,*V2LON," **";
.
          MOVE      "60",LNO
          MOVE      SP6,SAVDOC
          MOVE      ZEROVISN,SAVADMN
.
          MOVE      ZERO,PAGENO
          MOVE      ZERO,TOTPAT
          MOVE      ZERO,TOTDAY
          MOVE      ZERO,AVSTAY
.
          MOVE      ZERO,TGNDPAT
          MOVE      ZERO,TTOTDAY
          MOVE      ZERO,TAVSTAY
.
          MOVE      ZERO,FIRST
          MOVE      ZERO,ENDFLAG
.
          DISPLAY   *P37:24,"Doctor :",*P61:24,"Adm No :";
.
          PACK      KEY42 WITH SP20,SP20,SP10
          READ      TEMPFILE,KEY42;;
NEXTMP    READKS    TEMPFILE;XDOCTOR,PSNAME,XDAADMNO,DUNIQUE,AURNO:
                             PTITL,PGNAME,XDATIN,XDATOUT,XWARD,XBED:
                             PSAGE,PSEX,ACLSS,XDOCTO,XDOCFR,ASTAT,ADATE:
                             ATYPE
          GOTO      ENDP IF OVER
          MOVE      XDAADMNO,AADMNO
          MOVE      DUNIQUE,UNIQUE
.
          MATCH     STRDATE,XDATIN
          GOTO      CHKOUT IF LESS
.
          MATCH     XDATIN,ENDATE
          GOTO      OKPRT IF NOT LESS
          GOTO      NEXTMP
.
CHKOUT    MATCH     STRDATE,XDATOUT
          GOTO      NEXTMP IF LESS
.
OKPRT     MATCH     SP8,XDATOUT
          GOTO      PRT10 IF EQUAL
.
.         If the Transfer in/out is within the range
.
          MATCH     STRDATE,XDATOUT
          GOTO      NEXTMP IF LESS
.
.
PRT10     DISPLAY   *P47:24,*V2LON,XDOCTOR,*P71:24,AADMNO;
.
.         Check if still the same doctor
.
          MATCH     SAVDOC,XDOCTOR
          GOTO      SAMED IF EQUAL
.
          COMPARE   ONE,FIRST
          CALL      ENDREP IF EQUAL
.
.         New page for each doctor
.
          MOVE      XDOCTOR,SAVDOC
          CALL      HEAD
          MOVE      ONE,FIRST
.
.         Calculate number of patient for each doctor
.
SAMED     MATCH     AADMNO,SAVADMN
          GOTO      CHKGND IF EQUAL
.
          ADD       ONE,TOTPAT
          MOVE      AADMNO,SAVADMN
.
.         Calculate Total number of patients
.
CHKGND    MOVE      AADMNO,XDAADMNO
          MOVE      AADMNO,KEY8
          READ      GNDFILE,KEY8;ANS
          GOTO      SAMEP IF NOT OVER
.
          ADD       ONE,TGNDPAT
          WRITE     GNDFILE,KEY8;KEY8
.
.         Calculate number of bed days
.
SAMEP     MOVE      ONE,NBRDAYS
.
.         If the patient was transfered in and out at the same day, then 
.         number of bed days is one
.
          MATCH     XDATIN,XDATOUT
          GOTO      CHDAT3 IF EQUAL
.
          MOVE      XDATIN,FROMDATE
          REP       " 0",FROMDATE
.
          MATCH     SP8,XDATOUT
          GOTO      CHDAT1 IF NOT EQUAL
.
.         Use the end date if still currently with the doctor
.
          MOVE      ENDATE,TODATE
          GOTO      CHDAT2
.
CHDAT1    MOVE      XDATOUT,TODATE
.
CHDAT2    REP       " 0",TODATE
          CALL      NHOSPDAY
.
CHDAT3    ADD       NBRDAYS,TOTDAY
.
          COMPARE   "55",LNO
          GOTO      CHDAT5 IF LESS
          COMPARE   ONE,CPAGENO
          GOTO      CHDAT4 IF EQUAL
          CALL      PRTLN
.
CHDAT4    CALL      HEAD
.
CHDAT5    MOVE      SP13,DNAME1
          MOVE      SP13,DNAME2
.
          MOVE      XDOCTO,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD OF NODOC1
.
          MOVE      SP1,ANS
          MOVE      DGNAME,ANS
          PACK      DNAME1 WITH ANS,DOT,DSNAME
.
NODOC1    MOVE      XDOCFR,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD OF NODOC2
.
          MOVE      SP1,ANS
          MOVE      DGNAME,ANS
          PACK      DNAME2 WITH ANS,DOT,DSNAME
.
NODOC2    COMPARE   "55",LNO
          GOTO      NODOC4 IF LESS
          COMPARE   ONE,CPAGENO
          GOTO      NODOC3 IF EQUAL
          CALL      PRTLN
.
NODOC3    CALL      HEAD
.
NODOC4    MOVE      PSNAME,XSNAME
          MOVE      PGNAME,XGNAME
.
          UNPACK    XDATIN WITH XCENT,XYEAR,XMON,XDAY
          PRINT     *1,"|",XSNAME,*17,PTITL,*22,XGNAME,*32,"|",AADMNO:
                    *41,"|",AURNO,*50,"|",XDAY,SLASH,XMON,SLASH,XCENT,XYEAR:
                    *61,"|";
.
          MATCH     SP8,XDATOUT
          GOTO      PRT20 IF EQUAL
.
          UNPACK    XDATOUT WITH XCENT,XYEAR,XMON,XDAY
          PRINT     *62,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR;
.
PRT20     PRINT     *72,"|",XWARD," |",XBED,*81,"| ",PSEX:
                    *85,"|",PSAGE,*89,"|",NBRDAYS,*94,"|",ATYPE:
                    *99,"| ",ACLSS,*105,"|",DNAME1,*118,"|",DNAME2,*132,"|"
          ADD       ONE,LNO
          GOTO      NEXTMP
.
.         Print end report of each doctor
.
ENDREP    MOVE      TOTDAY,AVSTAY
          DIV       TOTPAT,AVSTAY
.
          CALL      PRTLN
.
          COMPARE   "54",LNO
          CALL      HEAD IF NOT LESS
.
          PRINT     *N,*N,*40,"-----Totals for Doctor Code ",SAVDOC,"--------":
                    *N,*N,*40,"Total Number of Patients : ",TOTPAT:
                    *N,*40,"Bed Days                 : ",TOTDAY:
                    *N,*40,"Average Length of Stay   : ",AVSTAY
.
          ADD       TOTDAY,TTOTDAY
          ADD       AVSTAY,TAVSTAY
.
          MOVE      ZERO,TOTPAT
          MOVE      ZERO,TOTDAY
          MOVE      ZERO,AVSTAY
          RETURN
+
.
ENDP      COMPARE   ZERO,CPAGENO
          GOTO      ENDP10 IF NOT EQUAL
.
          DISPLAY   *P28:24,*EL,*V2LON,"** No Report Generated **",*W2;
          GOTO      ENDP20
.
ENDP10    CALL      ENDREP
          MOVE      ONE,ENDFLAG
          CALL      HEAD
          MOVE      TTOTDAY,TAVSTAY
          DIV       TGNDPAT,TAVSTAY
.
          PRINT     *N,*N,*N,*N,*N,*N,*N,*N:
                    *37,"-------   G R A N D    T O T A L S   ---------",*N,*N:
                    *41,"Total Number of Patients :  ",TGNDPAT,*N:
                    *41,"Bed Days                 :  ",TTOTDAY,*N:
                    *41,"Average Length Of Stay   :  ",TAVSTAY:
                    "  (Total Bed Days/Number of Patients)":
                    *N,*N,*N,*N,*N,*47,"***  End of Report  ***"
.
          DISPLAY   *P1:24,*EL,*P24:24,*V2LON:
                    "** Report Generation Completed **",*W2;
.
ENDP20    CALL      KILLIDZ
          GOTO      START
+
.            HEADINGS FOR OUTPUT
.
HEAD      CALL      IBACLOCK
          MOVE      ONE,CNOUNDLN
.
          CALL      HEAD132
          PRINT     *N,*40,"From Date   : ",XDAY1,"/",XMON1,"/",XCENT1,XYEAR1:
                    *71,"To Date   : ",XDAY2,"/",XMON2,"/",XCENT2,XYEAR2,*N:
                    *40,"From Doctor : ",OUTDOCF:
                    *71,"To Doctor : ",OUTDOCT,*N
          MOVE      SEVEN,LNO 
.
.         Print the Sub-Heading only when not printing the Grand Total
.
          COMPARE   ONE,ENDFLAG
          RETURN    IF EQUAL
.
          MOVE      SP13,DNAME1
          MOVE      XDOCTOR,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD OF NODOC0
.
          MOVE      SP1,ANS
          MOVE      DGNAME,ANS
          PACK      DNAME1 WITH ANS,DOT,DSNAME
.
NODOC0    PRINT     *2,"Doctor Name: ",XDOCTOR,"- ",DNAME1
          CALL      PRTLN
          PRINT     *1,"|",*32,"|",*41,"|",*50,"|Adm Date":
                    *61,"|Dis Date",*72,"|",*77,"|",*81,"|",*85,"|":
                    *89,"|Bed",*94,"|  Patient",*105,"|":
                    *118,"|",*132,"|":
                    *N,*1,"|Patient Name",*32,"| Adm No",*41,"| U/R No":
                    *50,"|In  Date",*61,"|Out Date":
                    *72,"|Ward|Bed",*81,"|Sex",*85,"|Age":
                    *89,"|Days",*94,"|Clas",*99,"|Catg.",*105,"|Trans.Out To":
                    *118,"|Trans.In From",*132,"|"
          CALL      PRTLN
          MOVE      "12",LNO
          RETURN
+
.
PRTLN     PRINT     *1,"*----------------------------":
                    *30,"--------------------------------------------------":
                    *80,"----------------------------------------------------*"
          RETURN
+
.         Initialize the temporary files
.
INDZD     MOVE      ZERO,STATUS
.
.         Check if previously exists.. if so delete it 
.
          CALL      KILLIDZ
.
          CALL      CREAIDZ
          OPEN      TEMPFILE,FNAMEI
          OPEN      GNDFILE,FNAMEG
          RETURN
.
.
CREAIDZ   PREP      PSTROL,FNAMER
          WRITE     PSTROL,SEQ;"isbuild ",FNAMEI," 140 u1-42",015:
                                "isbuild ",FNAMEG," 9 u1-8"
          WEOF      PSTROL,SEQ
.
          MOVE      ONE,STATUS
          CALL      ROLLF
          RETURN
+
.
.         Deleting an indexed file based on port
.
KILLIDZ   CLOSE     TEMPFILE
          CLOSE     GNDFILE
          MOVE      ZERO,STATUS
.
          DISPLAY   *P1:24,*EL,*P20:24,*V2LON,"** Deleting Indexed File **",*W;
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMEG,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          RETURN
+
.
ROLLF     CLEAR     CMDLINE
          APPEND    "sh ",CMDLINE
          APPEND    FNAMER,CMDLINE
          APPEND    ".txt",CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
          MOVE      ZERO,OPCND
.
          BRANCH    STATUS OF DELIDZFI
.
          MOVE      ONE,OPCND
          DISPLAY   *P20:24,*B,*V2LON,"** The Index File not deleted **",*W2;
.
DELIDZFI  CLOSE     PSTROL,DELETE
          RETURN
+
.
ENDIT     CHAIN     PGM
          STOP
.
.==============================================================================
.
          INC       STD001IO
          INC       CALCDAYS
          INC       NHOSPDAY
          INC       PATDOCDS
          INC       TFILENAM
.
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATDSCIO/INC
          INC       PATMA1IO/INC
          INC       HL7BMTIO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATTRNIO/INC
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC


.***************************************************************************
.* System    :   Medical Records                                           *
.* Program   :   FX164807                                                  *
.* Desc      :   Fix medical records location                              *
.***************************************************************************
.* Date      :   07/10/1998                                                *
.* Author    :   Jill Habasque                                             *
.* Function  :   This program will                                         *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATMA1FD/INC
          INC       MRTMASFD/INC
          INC       MRTHISFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
PRGID     INIT      "FX164807"
PRGNAM    INIT      "Fix medical record current location"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      MRTMASA1,"mrtmasaf"
          OPEN      MRTHISA1,"mrthisaf"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Print report only":
                    *P1:6,*V2LON,TWO,*HOFF,". Print report and Update Records"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000,OPTN9000    * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  CALL      UND132
          PRINT     *1,"| UR Number",*15,"| Name",*60,"|Home Loc":
                    *70,"|Doc Type",*80,"|Volume",*90,"|Unique ID":
                    *110,"|Cur Loc":
                    *120,"|Latest Loc",*132,"|"
          CALL      UND132
.
          PACK      KEY17,SP70
          CALL      RSMRMAS1
PROC1000  CALL      RKMRMAS1
          BRANCH    OVRCD,PROC9999
.
          PACK      KEY26,MRMAKEY,Z70
          CALL      RSMRHIS1
          CALL      RPMRHIS1
          BRANCH    OVRCD,PROC1000
.
          MATCH     MRMAKEY,MRHIKEY
          GOTO      PROC1000 IF NOT EQUAL
.
          MATCH     MRMACLOC,MRHILOC
          GOTO      PROC1000 IF EQUAL
.
          PACK      KEY8,MRMAURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,PROC2000
.
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      ONE,PACFRMT
          CALL      PACNAME                 * Format the patient name
          MOVE      PACFNAME,KEY40
          GOTO      PROC3000
.
PROC2000  MOVE      "Missing master record             ",KEY40
.
PROC3000  PRINT     *1,"|",MRMAURNO,*15,"|",KEY40,*60,"|",MRMAHLOC:
                    *70,"|",MRMADOTY,*80,"|",DMRMAVOL,*90,"|",MRMAKEY:
                    *110,"|",MRMACLOC:
                    *120,"|",MRHILOC,*132,"|"
.
          BRANCH    COPTION,PROC1000
.
          MOVE      MRHILOC,MRMACLOC
          CALL      UPMRMAS1
.
          GOTO      PROC1000
.
PROC9999  CALL      UND132
          PRINT     *1,"******  End of Report ******"
          RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       MRTMASIO/INC
          INC       MRTHISIO/INC
          INC       PATMA1IO/INC
.

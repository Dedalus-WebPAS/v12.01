.*******************************************************************************
.* System         : Waiting List                                               *
.* Program        : IBAHMS77.PBL                                               *
.* Name           : AN-SNAP Inpatient Mortality Report                         *
.*******************************************************************************
.* Date           : 30/05/2012                                                 *
.* Author         : Patrick Adair                                              *
.* Function       : AN-SNAP Inpatient Mortality Report                         *
.*                  Report showing the number of Patient Deaths within the     *
.*                  Rehabilitation unit                                        *
.* Modifications  :                                                            *
.*---------------------------------------------------------------------------- *
.* V10.03.02  27/03/2013 Patrick Adair                              CAR 280945 *
.*                       Corrected Patmast read                                *
.* V10.03.01  23/11/2012 Patrick Adair                              CAR 275451 *
.*                       Changed to include/exclude day cases                  *
.* V10.03.00  11/07/2012 Patrick Adair                              CAR 257776 *
.*                       Created Report                                        *
.*******************************************************************************
.
          INC       STD001FD                     * Standard Include
.
          INC       IBACONFD/INC                 * Control File
          INC       PATCODFD/INC                 * Category Codes
          INC       PATCONFD/INC                 * Control File
          INC       PATDSCFD/INC                 * Patient Discharge File
          INC       PATHSPFD/INC                 * Hospital File
          INC       PATMA1FD/INC                 * Patient Master File
          INC       PATMI1FD/INC                 * Patient Admission File
          INC       PMSVX1FD/INC                 * Patient Visit Ext File
.
.-------------------------------------------------------------------------------
.                           Variables
.-------------------------------------------------------------------------------
CARECATG  DIM       2                            * category for care type
CMDLINE   DIM       50                           * Command Line
DATEFROM  DIM       8                            * Input From Date
DATETO    DIM       8                            * Input To Date
DAYCNT    FORM      2                            * Number of Days Difference
HOSPAPPR  DIM       10                           * Hospital Approval Number
HOSPCODE  DIM       3                            * Hospital Code
HOSPNAME  DIM       40                           * Hospital Name
INCLDAYC  DIM       1                            * Include Day Cases
.                                                  0 = No
.                                                  1 = Yes
TODAY     DIM       8                            * Today's Date
TOTDEATH  FORM      6                            * Total Interruptions
TOTRADMS  FORM      6                            * Total Rehab Admissions
.-------------------------------------------------------------------------------
.                            Constants
.-------------------------------------------------------------------------------
CATA      INIT      "A "
CATCC     INIT      "CC"
CATRZ     INIT      "RZ"
ONEINIT   INIT      "1"
.
PRGID     INIT      "IBAHMS77"
PRGNAM    INIT      "Inpatient Mortality Report"
VERSION   INIT      "V12.01.00"
.
. ******************************************************************************
. *                                  MAIN0000                                  *
. *                                                                            *
. * Process Flow Control                                                       *
. ******************************************************************************
.
MAIN0000  CALL      INIT0000                     * Initialisation - begin descent
.
MAIN1000  IF        IBCNMHOS=1
            DISPLAY   *P25:6,*EL:
                      *P25:7,*EL:
                      *P25:8,*EL:
                      *P25:9,*EF;
            CALL      GHOS0000                   * Get Multi Hospital Id
            BRANCH    EXIT,MAIN9999
          ELSE
            DISPLAY   *P1:6,*EL:
                      *P25:7,*EL:
                      *P25:8,*EL:
                      *P25:9,*EF;
            PACK      HOSPAPPR,CAPPRVNO,SP70     * Hospital Approval Number
            PACK      HOSPNAME,CONAME,SP70       * Hospital Name
          ENDIF
.
          CALL      GSTD0000                     * Keyin the start date
          BRANCH    EXIT,MAIN9999                * Exit if nothing input
.
          CALL      GEND0000                     * Keyin the ending date
          BRANCH    EXIT,MAIN1000                * Exit if nothing input
.
          CALL      GDAY0000                     * Keyin Include day cases
          BRANCH    EXIT,MAIN1000                * Nothing input
.
          CALL      GCON0000                     * OK to continue
          BRANCH    EXIT,MAIN1000                * restart if no selected
.
          CALL      EXTR0000                     * Calculate Rehab Admissions
          BRANCH    EXIT,MAIN0000                * mortalities
.
          CALL      PRNT0000                     * Print the report
.
          GOTO      MAIN0000                     * Next run
.
MAIN9999  CHAIN     PGM                          * Return from the nine circles
.
. ******************************************************************************
. *                                 INIT0000              Called by : MAIN0000 *
. *                                                                            *
. * Initialisation procedure                                                   *
. ******************************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A3,"patmi1af"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.                                                * 1 = Multi Hospital
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.                                                * Hospital Approval Number
          READ      CONTROLF,TEN;*79,CAPPRVNO
.                                                * Hospital Name
          READ      CONTROLF,TWO;*4,CONAME
.
          CALL      IBACLOCK
          PACK      TODAY,CCC,CYY,CMM,CDD
          REP       " 0",TODAY
.
          DISPLAY   *P1:6,*EL,"Hospital Id           : ";
          DISPLAY   *P1:7,"From Date             : ":
                    *P1:8,"To   Date             : ":
                    *P1:9,"Include Day Cases Y/N : ";
.
          MOVE      ZERO,TOTRADMS                * Init no of Rehab Admissions
          MOVE      ZERO,TOTDEATH                * Init No of Deaths
          MOVE      ZERO,INCLDAYC                * Include Day Cases Flag
          MOVE      SEVENTY,CLNO                 * Init Line Count
          MOVE      ZERO,CPAGENO                 * Init Page Count
          CLOCK     TIME,CTIMEIS                 * Get the starting time
.
INIT9999  RETURN
.
.*******************************************************************************
.*                                  GHOS0000               Called by: MAIN0000 *
.*                                                                             *
.* Get Hospital Id                                                             *
.*******************************************************************************
GHOS0000  MOVE      ZERO,EXIT
          DISPLAY   *P25:6,*EL:
                    *P25:7,*EL:
                    *P25:8,*EL:
                    *P25:9,*EF;
.
          MOVE      SP70,HOSPCODE
          MOVE      SP70,PTHSHOSP
          MOVE      TWENTY5,ECOL                    * Column of key in
          MOVE      SIX,EVERT                    * Row of key in
          MOVE      SP3,CKYIDEF3                 * Default value
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,GHOS98000,GHOS98000
.
          PACK      HOSPCODE,KEY3,SP70           * Hospital Code
          PACK      HOSPAPPR,PTHSAPPR,SP70       * Hospital Approval Number
          PACK      HOSPNAME,PTHSNAME,SP70       * Hospital Name
.
          DISPLAY   *P25:6,*V2LON,PTHSNAME;
          MOVE      ZERO,EXIT
          GOTO      GHOS9999
.
GHOS98000  MOVE     ONE,EXIT
.
GHOS9999  RETURN
.
. ******************************************************************************
. *                                  GSTD0000             Called by : MAIN0000 *
. *                                                                            *
. * Get the start  date of the report                                          *
. *            EXIT = 1   if a nothing was input                               *
. ******************************************************************************
.
GSTD0000  MOVE      ZERO,EXIT
.
          MOVE      SEVEN,CVERT                  * Line for keyin of from date
          MOVE      TWENTY5,CCOL                    * Col for keyin of from date
          MOVE      CVERT,EVERT                  * Col for error message
          MOVE      "40",ECOL                    * Col for error message
          MOVE      ZERO,CHIGHLT                 * No highlight
          MOVE      ONE,CDEFDTE
.
          UNPACK    SP6,CDAY,CMON,CYEAR          * Initialise date
          MOVE      SP8,DATEFROM
          MOVE      CCC,CCENT                    * Initialise century
.
          CALL      KEYDATE                      * Key in the date
          BRANCH    OVRCD,GSTD9000
.
          PACK      DATEFROM,CCENT,CYEAR,CMON,CDAY
          REP       " 0",DATEFROM
          MATCH     DATEFROM,TODAY
          GOTO      GSTD9999 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Date May Not be Greater than Today's ":
                    "Date.  ";
          CALL      HITENTER
          GOTO      GSTD0000
.
GSTD9000  MOVE      ONE,EXIT
.
GSTD9999  RETURN
.
. ******************************************************************************
. *                                  GEND0000             Called by : MAIN0000 *
. *                                                                            *
. * Get the end date of the report                                             *
. *            EXIT = 1   if a nothing was input                               *
. ******************************************************************************
.
GEND0000  MOVE      ZERO,EXIT
.
          MOVE      EIGHT,CVERT                  * Line for keyin of to date
          MOVE      TWENTY5,CCOL                    * Col for keyin of to date
          MOVE      CVERT,EVERT                  * Col for error message
          MOVE      "40",ECOL                    * Col for error message
          MOVE      ZERO,CHIGHLT                 * No highlight
          MOVE      ONE,CDEFDTE
.
          MOVE      SP8,DATETO
.
          CALL      KEYDATE                      * Key in the date
          BRANCH    OVRCD,GEND9000
.
          PACK      DATETO,CCENT,CYEAR,CMON,CDAY
          REP       " 0",DATETO
          MATCH     DATETO,TODAY
          GOTO      GEND2000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Date May Not be Greater than Today's ":
                    "Date.  ";
          CALL      HITENTER
          GOTO      GEND0000
.
GEND2000  MATCH     DATEFROM,DATETO
          GOTO      GEND9999 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"To Date May Not be Less than From ":
                    "Date.  ";
          CALL      HITENTER
          GOTO      GEND0000
.
GEND9000  MOVE      ONE,EXIT
.
GEND9999  RETURN
.
. ******************************************************************************
. *                                  GDAY0000             Called by : MAIN0000 *
. *                                                                            *
. * Include/Exclude Day cases from report?                                     *
. ******************************************************************************
.
GDAY0000  MOVE      ZERO,EXIT
.
GDAY1000  KEYIN     *P25:9,"_",*P25:9,*V2LON,ANS;
.
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
.
          MATCH     ANS,ANSY
          IF        @EQUAL
            MOVE      ONE,INCLDAYC
            GOTO      GDAY9999
          ENDIF
.
          MATCH     ANS,ANSN
          IF        @EQUAL
            MOVE      ZERO,INCLDAYC
            GOTO      GDAY9999
          ENDIF
.
          BEEP
          GOTO      GDAY1000
.
GDAY9999  RETURN
.
. ******************************************************************************
. *                                  GCON0000             Called by : MAIN0000 *
. *                                                                            *
. * Ok. to continue (Y/N)                                                      *
. ******************************************************************************
.
GCON0000  MOVE      ZERO,EXIT
          DISPLAY   *P1:24,*EL,"Ok to Continue (":
                    *V2LON,ANSY,*HOFF,SLASH,*V2LON,ANSN,*HOFF,") ? ";
.
GCON1000  KEYIN     *P24:24,"_",*P24:24,*V2LON,ANS;
.
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
.
          MATCH     ANS,ANSY
          GOTO      GCON9999 IF EQUAL
.
          MATCH     ANS,ANSN
          GOTO      GCON9000 IF EQUAL
.
          BEEP
          GOTO      GCON1000
.
GCON9000  MOVE      ONE,EXIT
.
GCON9999  RETURN
.
. ******************************************************************************
. *                                  EXTR0000             Called by : MAIN0000 *
. *                                                                            *
. * Count all Rehab admissions for the period                                  *
. ******************************************************************************
.
EXTR0000  MOVE      ZERO,EXIT
          DISPLAY   *P1:24,*EL,*P10:24,"Adm. No :":
                    *P58:24,"Scanning:";
.
          READ      CONTROLF,SEVENTY9;*80,PTCNQLDB
          READ      CONTROLF,EIGHTY;*250,PTCNHDPS
          IF        (PTCNHDPS=4 & PTCNQLDB=6)
            MOVE      CATA,CARECATG
          ELSE
            MOVE      CATCC,CARECATG
          ENDIF
.
          PACK      KEY16,DATEFROM               * Read thru Admission File
          CALL      RDSMISS3
EXTR1000  CALL      RDKMISS3
          BRANCH    OVRCD,EXTR9000
.
          MATCH     ADATE,DATETO
          GOTO      EXTR9000 IF LESS
.
          IF        ASTAT<2 | ASTAT>4
            GOTO      EXTR1000                   * only process admissions
          ENDIF
.
          IF        IBCNMHOS=1
            MATCH     PMVXMHOS,HOSPCODE          * check visit for hospital
            GOTO      EXTR1000 IF NOT EQUAL
          ENDIF
.
          MATCH     "A ",CARECATG                * Read relevant category type
          IF        @EQUAL
            PACK      KEY5,CARECATG,ATYPE,SP70
          ELSE
            PACK      KEY5,CARECATG,ACARE,SP70
          ENDIF
.
          CALL      RDCODE1
          BRANCH    OVRCD,EXTR1000
.
          MATCH     "A ",CARECATG                * Check if Rehab Visit
          IF        @EQUAL
            MATCH   "9",TCINDC8
            GOTO    EXTR2000 IF EQUAL
          ELSE
            MATCH   "2",TCINDC6
            GOTO    EXTR2000 IF EQUAL
          ENDIF
.
          GOTO      EXTR1000                     * Get Next Admission Record
.
EXTR2000  MATCH     "1",INCLDAYC                 * Include Day Visits
          GOTO      EXTR3000 IF EQUAL
.
EXTR2100  PACK      KEY8,DAADMNO,SP70            * Read Discharge File
          CALL      RDDSCH1
          IF        OVRCD=1
            CALL      CLPATDSC
          ENDIF
.
          MATCH     ADATE,DDATE
          GOTO      EXTR1000 IF EQUAL            * Exclude Day Visits
.
EXTR3000  DISPLAY   *P20:24,*V2LON,AURNO," ",DAADMNO;
          ADD       ONE,TOTRADMS                 * Increment Total Rehab Admiss
.
          PACK      KEY8,AURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,EXTR1000               * No master file exists (wt ?)
.
          COMPARE   ONE,PCEASE                   * is patient deceased?
          GOTO      EXTR1000 IF NOT EQUAL
.
          MATCH     SP70,PDECDTE                 * unknown date of death
          GOTO      EXTR1000 IF EQUAL
.
          MATCH     PDECDTE,DATETO               * Deceased after date Range
          GOTO      EXTR1000 IF LESS
.
          MATCH     DATEFROM,PDECDTE             * Deceased b4 date Range
          GOTO      EXTR1000 IF LESS
.
          MATCH     ADATE,PDECDTE                * Deceased b4 Admission date
          GOTO      EXTR1000 IF LESS
.
          PACK      KEY8,DAADMNO,SP70            * Read Discharge File
          CALL      RDDSCH1
          IF        OVRCD=1
            PACK      DDATE,SP70
          ENDIF
.
          MATCH     PDECDTE,DDATE                * Deceased after Discharge date
          GOTO      EXTR1000 IF LESS
.
          ADD       ONE,TOTDEATH                 * Increment Rehab Patient Death
.
          GOTO      EXTR1000                     * get next Admission Record
.
EXTR9000  IF        TOTRADMS < 1
            MOVE      ONE,EXIT
          ENDIF
.
EXTR9999  RETURN
.
. ******************************************************************************
. *                                  PRNT0000              Called by: MAIN0000 *
. *                                                                            *
. * Print list of patients without Admission FIM Details                       *
. ******************************************************************************
.
PRNT0000  DISPLAY   *P1:24,*EL,*P10:24,"Printing: ";
.
          COMPARE   "60",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PRINT     *N,"Total No Of Rehabilitation Patient Deaths     ":
                       "       : ",TOTDEATH
          PRINT     *N,"Total No of Rehabilitation Admissions for the ":
                       "Period : ",TOTRADMS
.
          ADD       FOUR,CLNO
.
          PRINT     *N,*N,"*** End of Report ***"
.
PRNT9999  RETURN
.
. ******************************************************************************
. *                                  HEAD0000              Called by: PRNT0000 *
. *                                                                            *
. * Print Report Heading                                                       *
. ******************************************************************************
.
HEAD0000  CALL      HEAD80                      * Standard heading
.
          UNPACK    DATEFROM,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PRINT     *N,*17,"Hospital : ",HOSPNAME
.
          PRINT     *N,*15,"Date Range : ",CPCDATE," to ";
.
          UNPACK    DATETO,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PRINT     CPCDATE,*N
.
HEAD9999  RETURN
.
.*******************************************************************************
.
          INC       STD001IO                     * System Standard Routines
.
          INC       CLPATHSP                     * Clear Hospital Table variables
          INC       CLPATDSC                     * Clear Disharge Record
          INC       PATHSPKY                     * Keyin of a Hospital Id/code
.
          INC       PATCODIO/INC                 * Category Codes
          INC       PATDSCIO/INC                 * Patient Discharge File
          INC       PATHSPIO/INC                 * Hospital File
          INC       PATMA1IO/INC                 * Patient Master File
          INC       PATMI1IO/INC                 * Patient Admission File
          INC       PMSVX1IO/INC                 * Patient Visit Ext File

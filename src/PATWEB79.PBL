.------------------------------------------------------------------------------
. Program       : PATWEB79
.
. Function      : System Maintenance Server for 
.                 Report 1: Bulk Invoice Selection                    
.                 Report 2: Remote script to exclude Invoice from Bulk Printing 
.                 Report 3: Bulk backdate Invoice selection
.                 Report 4: Remote script to exclude backdate Invoice from Bulk
.
. Modifications :
.------------------------------------------------------------------------------
.        V11.04.01 23/09/2024 J.Tan           TSK 0950577
.                   Recompiled for PATIPERH - Fixed Hold Invoice audit issue
.        V11.03.02  04/04/2023  Bella Turco    TSK 0911166
.                   Recompiled for PATIPERH - Hold Invoice Audit             
.        V11.03.01  19/12/2022  Bella Turco    TSK 0921957
.                   Added NOMORE00 - Display "No more records" at end of list
.                   Added PTCNNRTD - Displays number of rows set by parameter 
.                   value
.        V11.02.01  26/07/2022  Bella Turco   TSK 0914892
.                   Added two table columns to display NZRPCDAT and WBSENAM in 
.                   RINV0000  
.        V11.01.01  08/11/2021  J.Tan         TSK 0880410
.                   Recompiled for PATIPERH - added Hospital and System
.        V11.00.04  11/02/2021  J.Tan         TSK 0902415
.                   Mod PBIROW00 - added <tr> for Crossbrowser
.        V11.00.03  10/12/2020  Thanh T       TSK 0872840
.                   Added PMSCCDFD/PMSCCDIO as PMSPX2TM changes
.        V11.00.02  02/11/2020  J.Tan         TSK 0892773
.                   Added <td> to PBIROW00
.        V11.00.01  22/10/2020  J.Tan         TSK 0898866
.                   Recompiled for PATIPERH - on hold invoice
.        V10.14.01  15/03/2019  J.Tan          CAR 0871472
.                   Mod checking for '#' and '&' in RP Invoice No field
.        V10.07.01  24/12/2015  J.Tan          CAR 0319116  
.                   Added ONHOLD00 (checking for contract hold invoice)  
.        V10.04.03  13/06/2014  Jill Parkinson CAR 301639   
.                   Moved CRTM0000 from INIT000 to RTAB0000 only
.        V10.04.02  16/04/2014  J.Tan            CAR 261630
.                   Removed port number from temp files
.        V10.04.01  31.03.2014  B.G.Ackland  CAR 299363
.                   Replace META Tag Redirect with Javascript in Common RDIREC00
.                   Routine
.------------------------------------------------------------------------------
.*       V10.03.01  09/05/2012  J.Tan           CAR 264867                    *
.*                  Fixed I*C on HTMLFILE                                     *
.------------------------------------------------------------------------------
.*       V10.02.01  23/06/2011  Peter McMullen  CAR 240725                    *
.*                  Change ward selection list to sort by name, not code      *
.------------------------------------------------------------------------------
.*        V9.12.01  28/10/2009  Jill Parkinson  CAR 174079                    *
.*                  Added report option 7 - Action Plan Maintenance           *
.------------------------------------------------------------------------------
.*        V9.10.01  14/07/2008  Ebon Clements   CAR 164125                    *
.*                  Only display funder invoices in the related provider      *
.*                  invoice list that are not fully credited RTAB0000         *
.*        V9.09.05  19/11/2007  Ebon Clements   CAR 155139                    *
.*                  Replaced NZPIFPAY with a calculation of the inv balance   *
.*        V9.09.04  25/06/2007  Ebon Clements   CAR 143038                    *
.*                  Fixed DeleteBInvoice() in PRIBROW00                       *
.*        V9.09.03  20/06/2007  Ebon Clements   CAR 142594                    *
.*                  Added hospital filter to provider invoice list RTAB0000   *
.*        V9.09.02  20/06/2007  Ebon Clements   CAR 143038                    *
.*                  Fixed GOTO in DISBIL00 for norecord and table sort count  *
.*        V9.09.01  15/06/2007  Ebon Clements   CAR 142667                    *
.*                  Changed RTAB0000 to check status filter on master records *
.*        V9.08.07  17/05/2007  Ebon Clements   CAR 133432                    *
.*                  Created table sort list for print bulk back dated invoice *
.*        V9.08.06  07/03/2007  Ebon Clements   CAR 126779                    *
.*                  Fixed display of procedure date in RDET0000               *
.*        V9.08.05  15/02/2007  Ebon Clements   CAR 126779                    *
.*                  Added anaesthetist to filtprac test in RTAB0000           *
.*        V9.08.04  10/01/2007  Ebon Clements   CAR 126779                    *
.*                  Added surgeon/anaesthetist remote script to report 6      *
.*        V9.08.03  07/12/2006  Ebon Clements   CAR 126779                    *
.*                  Mods to related provider invoice AP interface             *
.*        V9.08.02  07/12/2006  Ebon Clements   CAR 126779                    *
.*                  Added export functionality to provider invoice processing *
.*        V9.08.01  04/12/2006  Ebon Clements   CAR 126779                    *
.*                  Added report 5 - Provider invoice processing              *
.*        V9.07.01  07/09/2006  Jill Habasque   CAR 109852                    *
.*                  Recompiled for PATINVHL                                   *
.*        V9.06.01  15/06/2006  Neelkamal Pyla  CAR 96145                     *
.*                  Mods for displaying wards based on HospitalId - Multihosp *
.*        V9.05.01  09/03/2006  Ebon Clements   CAR 78533                     *
.*                  Mods for PATCODTM - Hospital specific category codes      *
.*        V9.05.B01 30/01/2006  Ebon Clements CAR 93186                       *
.*                  Mods for REDIRECT length change. Recompiled for IBAWEBDF  *
.*        V9.04.01  03/09/2004 J.Tan  CAR 53073                               *
.*                  Mods for multi hospital                                   *
.*        V9.03.02  03/08/2004 Davin  CAR 52036                               *
.*                  Mods for printing bulk invoices by claim type             *
.*        V9.03.01  23/04/2004 J.Tan  CAR 45547                               *
.*                  Mods for printing backdated invoice                       *
.------------------------------------------------------------------------------
.
.------------------------------------------------------------------------------
. FD Includes
.------------------------------------------------------------------------------
.
          INC       IBAWEBDF
          INC       WEBDATDF
          INC       RSHWEBDF
.
          INC       TFILEVAR/INC
          INC       PATCOMM/INC    * IBA Control file
.
          INC       MLTSECFD/INC
          INC       PATCALAG/INC
          INC       PATCMCFD/INC
          INC       PATCONFD/INC   * Patient Control file
          INC       PATDO1FD/INC
          INC       PATDSCFD/INC   * Patient Discharge file
          INC       PATFAPFD/INC
          INC       PATFAPTD/INC
          INC       PATFN1FD/INC
          INC       PATHSPFD/INC   * Hospital file
          INC       PATINVFD/INC
          INC       PATMA1FD/INC   * Patient Master file
          INC       PATMI1FD/INC   * Patient Admission file
          INC       PATONHFD/INC 
          INC       NZPSPRFD/INC
          INC       NZPPICFD/INC
          INC       NZPRPIFD/INC
          INC       NZPRPITD/INC
          INC       NZPPTYFD/INC
          INC       PATVISFD/INC   * Visit File
          INC       PATWR1FD/INC   * Ward Bed File
          INC       PMSHCGFD/INC
          INC       PMSHCPFD/INC
          INC       PMSPX2FD/INC
          INC       PMSPX2TD/INC
          INC       PMSCCDFD/INC   * Concession Card Details
.
          INC       PATCFAFD/INC
          INC       PATIPEFD/INC
          INC       PATFX1FD/INC
          INC       PATFHIFD/INC
          INC       EMRVISFD/INC
          INC       OUTSITFD/INC
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
.
          INC       PMSBLIFD/INC   * Bulk Invoice selection List
          INC       PMSBIIFD/INC   * Bulk Invoice List Index
          INC       PMSBBIFD/INC   * Bulk backdate Invoice selection List
          INC       PMSBISFD/INC   * Bulk backdate Invoice List Index
          INC       PMSVX1FD/INC   * Visit Extension File
.
.
.-----------------------------------------------------------------------------
. Temp Files
.-----------------------------------------------------------------------------
.         Filename : tmpw79xx.dat          (xx = port id)
TEMPFILE  IFILE SQL, FIXED=17, KEY="U1-8,9-16"
.
. NAME    TYPE   LENGTH   PHYSICAL  START LOC    DESCRIPTION
. ----    ----   ------   --------  ---------    --------------
TEMPADMN  DIM       8      8        1     Admission Number
TEMPINVN  DIM       8      8        9     Invoice Number
.
. End of record                     17 
.
.-----------------------------------------------------------------------------
. Variable Declarations
.-----------------------------------------------------------------------------
.
. CGI Parameters
. **************
.
ATYPDESC  DIM       20
CAPTSTAT  DIM       1
CAREDESC  DIM       20
CLAIMDES  DIM       25
DOCTCODE  DIM       10
FILTCONT  DIM       6         
FILTFDAT  DIM       8
FILTPRAC  DIM       10
FILTSTAT  DIM       1
FILTTDAT  DIM       8
FORM12P2  FORM      12.2
NZPICKEY  DIM       22
INVOICNO  DIM       8
DISPAPIS  DIM       10
DISPCAPS  DIM       10
DISPFPAY  DIM       40
HTMLLNK3  DIM       227
NEXTPAGE  FORM      1         * Nextpage parameter
UPDTTYPE  DIM       1         * Update Type (A)dd,(U)pdate,(D)elete,Blank=disp
STARTKEY  DIM       3         * Starting Key for search
SORTFLAG  DIM       1
.
PENDSDAT  DIM       8
PENDADAT  DIM       8
PENDDDAT  DIM       8
PENDFUND  DIM       6
PENDCLAM  DIM       3
PENDHOSP  DIM       3
PENDSYST  DIM       2
PMSBL001  DIM       4         * Selection ID
PMSBL002  DIM       35        * Selection Description
PMSBL003  DIM       1         * Print bulk invoice type  
PMSBL004  DIM       6         * Starting Name
PMSBL005  DIM       6         * Ending Name  
PMSBL006  FORM      4         * Number of days
PMSBL007  DIM       8         * Starting Date
PMSBL008  DIM       8         * Ending Date 
PMSBL009  FORM      6.2       * Minimum amount owing
PMSBL010  DIM       3         * Discharge Ward
PMSBL011  DIM       3         * Claim Type (Cat CL)
PMSBL012  DIM       3         * Hospital ID
. 
PMSBI001  DIM       4         * Selection ID
PMSBI002  DIM       8         * Invoice date
PMSBI003  DIM       3         * Hospital ID
.
DIM3      DIM       3
DELTSTAT  DIM       1         * Delete Status            
. 
. Program Variables
. *****************
.
CMDLINE   DIM       127
CODCOMPL  DIM       3
DISPDATE  DIM       11
DISPDAT2  DIM       11
DISPDAT3  DIM       11
DISPDAT4  DIM       11
FILTFLAG  FORM      1
FILTHOSP  DIM       3
FULLYCRD  FORM      1
FORM1A    FORM      1        
FORM8     FORM      8
FUNDCODE  DIM       6         * Health Fund Code
FUNDPAID  FORM      1         * Funder paid(invoice balance = 0)
FNAME1    DIM       8
PRINTED   DIM       3
PSAGETYP  DIM       6         * age type description
PSAGECON  DIM       3         * age converted value
RECTOTAL  FORM      5         * Total rexords
SAVKEY3   DIM       3         
SAVKEY22  DIM       22
UPDTLINK  DIM       127
.
. Program Constants
. *****************
CODECL    INIT      "CL"
FIVE9     FORM      "59"
SEVEN9    FORM      "79"
.
CATPT     INIT      "pt"
ERASE     INIT      "iserase "
                             "#"",NZRPCDAT,"#",":     *5
                             "#"",NZRPCUID,"#",":     *6
INVOICON  INIT      "InvoiceIcon.gif"
INVNDAYS  INIT      "All Invoice sent more than N days"
INVKDATE  INIT      "All Disch. patient by Date range"
INVXDLLR  INIT      "All Disch. patient by Min. amount owing"
INVGWARD  INIT      "All Disch. patient by given Ward"
INVGCLTP  INIT      "All Disch. patient by given Claim Type"
.
NBSP      INIT      "&nbsp;"
+
.-----------------------------------------------------------------------------
.
PRGID     INIT      "PATWEB79"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "Billing Maintenance Server"
+
.------------------------------------------------------------------------------
.
          CALL      WEBINT00                * Initialise Fifo Etc.
          CALL      INIT0000                * Open Files
.
MAIN1000  CALL      WEBRED00                * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO          * End Server Process if Option=999
          GOTO      MAIN9999 IF EQUAL
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200,MAIN1300,MAIN1400:
                             MAIN1500,MAIN1600,MAIN1700,MAIN1800
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "Invalid Option",WEBTITLE
          CALL      WEBERR00                * Output File & Write HTML Pg Header
          GOTO      MAIN1000
.
MAIN1100  CALL      BUKINV00                * Bulk Invoice Selection
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00                * next page
          GOTO      MAIN1000
. 
MAIN1200  CALL      DELINV00                * Remote script to delete invoice
          GOTO      MAIN1000
.
MAIN1300  CALL      BBKINV00                * Bulk Backdate Invoice Selection
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00                * next page
          GOTO      MAIN1000
. 
MAIN1400  CALL      DEBINV00                * Remote script to delete invoice
          GOTO      MAIN1000                * from bulk backdate invoice
.
MAIN1500  CALL      PINV0000                * Provider Invoice Processing
          BRANCH    EXIT,MAIN1000
.
          CALL      NXTPAG00                * next page
          GOTO      MAIN1000
. 
MAIN1600  CALL      REMINV00                * Remote script to RP invoice export
          GOTO      MAIN1000
.
MAIN1700  CALL      ACPLM000                * Action Plan Maintenance
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00                * next page
          GOTO      MAIN1000
.
MAIN1800  CALL      ONHOLD00                * Remote script to hold invoice
          GOTO      MAIN1000                * from bulk backdate invoice
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
+
.------------------------------------------------------------------------------
.  Next Page URL
.------------------------------------------------------------------------------
.
NXTPAG00  MOVE      ZERO,F1
          MOVE      NEXTPAGE,F1
          BRANCH    F1,NXTPAG10,NXTPAG20,NXTPAG30
.
          CALL      WINCLS00              * Refresh parent and close window
          GOTO      NXTPAG99

NXTPAG10  CALL      RDIREC00              * Redirect URL
          GOTO      NXTPAG99
.
NXTPAG20  CALL      GOBACK00              * Go Back 1 html page
          GOTO      NXTPAG99
.
NXTPAG30  CALL      DAH20000              * Go Back 2 html pages
          GOTO      NXTPAG99
.
NXTPAG99  RETURN
.------------------------------------------------------------------------------
.  Goes back 1 page
.------------------------------------------------------------------------------
.
GOBACK00  CALL      OPENHTML
          BRANCH    EXIT,GOBACK99
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "    alert(#"",WEBTITLE,"#");":
                             "    self.close();":
                             "    opener.history.go(-1);":
                             "}":
                             "</script>"

          CALL      CLOSHTML
.
GOBACK99  RETURN
+
.------------------------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------------------------
.
INIT0000  OPEN      WEBSECA1,"websecaf"
          OPEN      WEBSECA3,"websecaf"
.
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
          OPEN      PATFN1A1,"patfn1af"
          OPEN      PATFX1A1,"patfx1af"
          OPEN      PATDSCH1,"patdschf"
.
          OPEN      PATFAPA1,"patfapaf"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      NZPSPRA1,"nzpspraf"
          OPEN      NZPPICA1,"nzppicaf"
          OPEN      NZPPICA2,"nzppicaf"
          OPEN      NZPRPIA1,"nzprpiaf"
          OPEN      NZPPTYA1,"nzpptyaf"
          OPEN      NZPPTYA2,"nzpptyaf"
          OPEN      PATVISA1,"patvisaf"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      PATWR1A7,"patwr1af"
          OPEN      PATCMCA1,"patcmcaf"
.
          OPEN      EMRVISA1,"emrvisaf"
          OPEN      PATIPEN1,"patipenf"
          OPEN      PATCFAA1,"patcfaaf"
.
          OPEN      PMSBLIA1,"pmsbliaf"
          OPEN      PMSBIIA1,"pmsbiiaf"
          OPEN      PMSBBIA1,"pmsbbiaf"
          OPEN      PMSBISA1,"pmsbisaf"
          OPEN      PMSHCPA1,"pmshcpaf"
          OPEN      PMSPX2A1,"pmspx2af"
          OPEN      MLTSECA1,"mltsecaf"
          OPEN      PATINVR1,"patinvrf"
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,FNAME1
.
.-------------------------------------------------------------------------------
. Open and Read the Control file variables
.-------------------------------------------------------------------------------
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS,*85,IBCNUGST
          READ      CONTROLF,HUND25;*238,PTCNNRTD
.
          MOVE      ZERO,F2
          MOVE      PTCNNRTD,F2
          IF        F2 = 0
            MOVE      "10",PTCNNRTD
          ENDIF
.
          IF        IBCNMHOS=1
            OPEN      MLTCODA1,"mltcodaf"
            OPEN      MLTCODA2,"mltcodaf"
          ENDIF
.
.
INIT9999  RETURN
+
.------------------------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          MOVE      SP70,TEMPLATE
          MOVE      SP70,UPDTTYPE
          MOVE      ZERO,NEXTPAGE
          PACK      REDIRECT,SP70,SP70,SP70,SP70
          MOVE      ZERO,STARTNUM
          MOVE      ZERO,NORECORD
          MOVE      SP70,STARTKEY
          MOVE      SP70,ADMISSNO
          MOVE      SP70,INVOICNO
          MOVE      SP70,DELTSTAT
          MOVE      SP70,NZPICKEY
.
          MOVE      SP70,PMSBL001
          MOVE      SP70,PMSBL002
          MOVE      SP70,PMSBL003
          MOVE      SP70,PMSBL004
          MOVE      SP70,PMSBL005
          MOVE      ZERO,PMSBL006
          MOVE      SP70,PMSBL007
          MOVE      SP70,PMSBL008
          MOVE      ZERO,PMSBL009
          MOVE      SP70,PMSBL010
          MOVE      SP70,PMSBL011
          MOVE      SP70,PMSBL012
.
          MOVE      SP70,PMSBI001      * Selection ID
          MOVE      SP70,PMSBI002      * Invoice date
          MOVE      SP70,PMSBI003      * Hospital ID
.
          MOVE      SP70,FILTCONT
          MOVE      SP70,FILTFDAT
          MOVE      SP70,FILTPRAC
          MOVE      SP70,FILTSTAT
          MOVE      SP70,FILTTDAT
          MOVE      Z70,CAPTSTAT
          MOVE      ZERO,FILTFLAG
          MOVE      SP70,FILTHOSP
.
          CALL      CPNZRP00
.
          CALL      CPPTFP00
.
CLRPAR99  RETURN
+
.------------------------------------------------------------------------------
. Set Parameters
.------------------------------------------------------------------------------
.
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updttype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDTTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nextpage=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NEXTPAGE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startkey=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTKEY
            PACK      STARTKEY,STARTKEY,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "redirect=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REDIRECT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "norecord=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NORECORD
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startnum=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTNUM
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "pmsbl",QRYNAME
          IF        @EQUAL
            CALL      STBLI000
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "pmsbi",QRYNAME
          IF        @EQUAL
            CALL      STBBI000
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "ptfap",QRYNAME
          IF        @EQUAL
            CALL      SPFAP000
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "admissno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ADMISSNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "invoicno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,INVOICNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "deltstat=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,DELTSTAT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtcont=",QRYNAME
          IF        @EQUAL
            PACK      FILTCONT,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtfdat=",QRYNAME
          IF        @EQUAL
            UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
            CALL      SETMTH00                *Set CMON from MTHNAM3
            PACK      FILTFDAT,CCENT,CYEAR,CMON,CDAY
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filttdat=",QRYNAME
          IF        @EQUAL
            UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
            CALL      SETMTH00                *Set CMON from MTHNAM3
            PACK      FILTTDAT,CCENT,CYEAR,CMON,CDAY
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtstat=",QRYNAME
          IF        @EQUAL
            PACK      FILTSTAT,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtprac=",QRYNAME
          IF        @EQUAL
            PACK      FILTPRAC,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "captstat=",QRYNAME
          IF        @EQUAL
            PACK      CAPTSTAT,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nzpickey=",QRYNAME
          IF        @EQUAL
            PACK      NZPICKEY,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nzrpi",QRYNAME
          IF        @EQUAL
            CALL      SPNZRP00
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtflag=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FILTFLAG
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filthosp=",QRYNAME
          IF        @EQUAL
            PACK      FILTHOSP,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
+
.------------------------------------------------------------------------------
. Set Parameters for Bulk invoice printing
.------------------------------------------------------------------------------
STBLI000  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
.
          BRANCH    F3,STBLI001,STBLI002,STBLI003,STBLI004,STBLI005:
                       STBLI006,STBLI007,STBLI008,STBLI009,STBLI010:
                       STBLI011,STBLI012
.
          MOVE      ONE,EXIT
          GOTO      STBLI999
.
STBLI001  MOVE      QRYDATA,PMSBL001
          PACK      PMSBL001,PMSBL001,SP70
          GOTO      STBLI999
.
STBLI002  MOVE      QRYDATA,PMSBL002
          PACK      PMSBL002,PMSBL002,SP70
          GOTO      STBLI999
.
STBLI003  MOVE      QRYDATA,PMSBL003
          PACK      PMSBL003,PMSBL003,SP70
          GOTO      STBLI999
.
STBLI004  MOVE      QRYDATA,PMSBL004
          PACK      PMSBL004,PMSBL004,SP70
          GOTO      STBLI999
.
STBLI005  MOVE      QRYDATA,PMSBL005
          PACK      PMSBL005,PMSBL005,SP70
          GOTO      STBLI999
.
STBLI006  MOVE      QRYDATA,PMSBL006
          GOTO      STBLI999
.
STBLI007  PACK      QRYDATA,QRYDATA,SP70
          MATCH     SP70,QRYDATA
          GOTO      STBLI999 IF EQUAL
          UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00                *Set CMON from MTHNAM3
          PACK      PMSBL007,CCENT,CYEAR,CMON,CDAY
          GOTO      STBLI999
.
STBLI008  PACK      QRYDATA,QRYDATA,SP70
          MATCH     SP70,QRYDATA
          GOTO      STBLI999 IF EQUAL
          UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00                *Set CMON from MTHNAM3
          PACK      PMSBL008,CCENT,CYEAR,CMON,CDAY
          GOTO      STBLI999
.
STBLI009  MOVE      QRYDATA,PMSBL009
          GOTO      STBLI999
.
STBLI010  MOVE      QRYDATA,PMSBL010
          PACK      PMSBL010,PMSBL010,SP70
          GOTO      STBLI999
.
STBLI011  MOVE      QRYDATA,PMSBL011
          PACK      PMSBL011,PMSBL011,SP70
          GOTO      STBLI999
.
STBLI012  MOVE      QRYDATA,PMSBL012
          PACK      PMSBL012,PMSBL012,SP70
          GOTO      STBLI999
.
STBLI999  RETURN
+
.------------------------------------------------------------------------------
. Set Parameters for Bulk backdate invoice printing
.------------------------------------------------------------------------------
STBBI000  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
.
          BRANCH    F3,STBBI001,STBBI002,STBBI003
.
          MOVE      ONE,EXIT
          GOTO      STBBI999
.
STBBI001  MOVE      QRYDATA,PMSBI001
          PACK      PMSBI001,PMSBI001,SP70
          GOTO      STBBI999
.
STBBI002  PACK      QRYDATA,QRYDATA,SP70
          MATCH     SP70,QRYDATA
          GOTO      STBBI999 IF EQUAL
          UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00                *Set CMON from MTHNAM3
          PACK      PMSBI002,CCENT,CYEAR,CMON,CDAY
          GOTO      STBBI999
.
STBBI003  MOVE      QRYDATA,PMSBI003
          PACK      PMSBI003,PMSBI003,SP70
          GOTO      STBBI999
.
STBBI999  RETURN
+
.------------------------------------------------------------------------------
. Print Invoces in Bulk    
.------------------------------------------------------------------------------
.
BUKINV00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE            * Display formaction
          GOTO      BUKINV80 IF EQUAL
.
          RESET     UPDTTYPE
          MATCH     "A",UPDTTYPE            * Display formaction
          GOTO      BUKINV10 IF EQUAL
.
          RESET     UPDTTYPE
          MATCH     "U",UPDTTYPE            * Display formaction
          GOTO      BUKINV20 IF EQUAL
.
          RESET     UPDTTYPE
          MATCH     "D",UPDTTYPE            * Display formaction
          GOTO      BUKINV30 IF EQUAL
.
          GOTO      BUKINV91
.
.         ************ ADD FORMACTION **********
.
BUKINV10  PACK      KEY4,PMSBL001,SP70
          CALL      RDPMBLI1         * read bulk invoice selection
          COMPARE   ZERO,OVRCD
          GOTO      BUKINV93 IF EQUAL
          CALL      SAVBLI00
          CALL      WRPMBLI1         * write new invoice selection options
          CALL      SCHSEL00         * schedule bulk invoice selection
          GOTO      BUKINV99
.
.         ************ UPDATE FORMACTION **********
.
BUKINV20  PACK      KEY4,PMSBL001,SP70
          CALL      RDPMBLI1         * read bulk invoice selection
          BRANCH    OVRCD,BUKINV92
          CALL      SAVBLI00
          CALL      UPPMBLI1         * update new invoice selection options
          CALL      SCHSEL00         * schedule bulk invoice selection
          GOTO      BUKINV99
.
.         ************ DELETE FORMACTION **********
.
BUKINV30  PACK      KEY4,PMSBL001,SP70
          CALL      RDPMBLI1         * Add into Current File
          BRANCH    OVRCD,BUKINV92
          CALL      DEPMBLI1
.
          PACK      KEY12,PMSBL001,SP70
          CALL      RSPMBII1
BUKINV31  CALL      RKPMBII1
          BRANCH    OVRCD,BUKINV99
.
          MATCH     PMSBL001,PMBISEID
          GOTO      BUKINV99 IF NOT EQUAL
.
          PACK      KEY12,PMBISEID,PMBIVISN
          CALL      DEPMBII1
          GOTO      BUKINV31 
.
.         ************ DISPLAY FORMACTION **********
.
BUKINV80  PACK      KEY4,PMSBL001,SP10
          CALL      RDPMBLI1
          IF        OVRCD=1        
            CALL      CLRBLI00
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Print Invoices in Bulk",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00                * Output File & Write HTML Pg Header
          BRANCH    EXIT,BUKINV99
          CALL      WEBBOD00                * Process Web Body
          CALL      WEBEND00                * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      BUKINV99
.
.         ************ DISPLAY ERROR MESSAGE **********
.
BUKINV91  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      BUKINV99
.
BUKINV92  MOVE      "Record does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      BUKINV99
.
BUKINV93  MOVE      "Record already exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      BUKINV99
.
BUKINV99  RETURN
+
.------------------------------------------------------------------------------
. Save bulk invoice printing variables
.------------------------------------------------------------------------------
.
SAVBLI00  MOVE     PMSBL001,PMBLSEID
          MOVE     PMSBL002,PMBLSEDC
          MOVE     PMSBL003,PMBLTYPE
.
          MATCH    "1",PMBLTYPE
          GOTO     SAVBLI10 IF EQUAL
.
          MATCH    "2",PMBLTYPE
          GOTO     SAVBLI20 IF EQUAL
.
          MATCH    "3",PMBLTYPE
          GOTO     SAVBLI30 IF EQUAL
.
          MATCH    "4",PMBLTYPE
          GOTO     SAVBLI40 IF EQUAL
.
          MATCH    "5",PMBLTYPE
          GOTO     SAVBLI50 IF EQUAL
.
          GOTO     SAVBLI99
.
SAVBLI10  MOVE     PMSBL004,PMBLSTNM
          MOVE     PMSBL005,PMBLENNM
          MOVE     PMSBL006,PMBLDAYS
          MOVE     SP10,PMBLSTDT
          MOVE     SP10,PMBLENDT
          MOVE     ZERO,PMBLMAMT
          MOVE     SP10,PMBLWARD
          GOTO     SAVBLI90
.
SAVBLI20  MOVE     PMSBL004,PMBLSTNM
          MOVE     PMSBL005,PMBLENNM
          MOVE     ZERO,PMBLDAYS
          MOVE     PMSBL007,PMBLSTDT
          MOVE     PMSBL008,PMBLENDT
          MOVE     ZERO,PMBLMAMT
          MOVE     SP10,PMBLWARD
          GOTO     SAVBLI90
.
SAVBLI30  MOVE     PMSBL004,PMBLSTNM
          MOVE     PMSBL005,PMBLENNM
          MOVE     ZERO,PMBLDAYS
          MOVE     SP10,PMBLSTDT
          MOVE     SP10,PMBLENDT
          MOVE     PMSBL009,PMBLMAMT
          MOVE     SP10,PMBLWARD
          GOTO     SAVBLI90
.
SAVBLI40  MOVE     PMSBL004,PMBLSTNM
          MOVE     PMSBL005,PMBLENNM
          MOVE     ZERO,PMBLDAYS
          MOVE     PMSBL007,PMBLSTDT
          MOVE     PMSBL008,PMBLENDT
          MOVE     ZERO,PMBLMAMT
          MOVE     PMSBL010,PMBLWARD
          GOTO     SAVBLI90
.
SAVBLI50  MOVE     PMSBL004,PMBLSTNM
          MOVE     PMSBL005,PMBLENNM
          MOVE     ZERO,PMBLDAYS
          MOVE     PMSBL007,PMBLSTDT
          MOVE     PMSBL008,PMBLENDT
          MOVE     ZERO,PMBLMAMT
          MOVE     PMSBL011,PMBLWARD        * claim type
          GOTO     SAVBLI90
.
SAVBLI90  MOVE     SP10,PMBLHOSP
          IF       IBCNMHOS=1
            MOVE     PMSBL012,PMBLHOSP
          ENDIF
SAVBLI99  RETURN
+
.------------------------------------------------------------------------------
. Print backdate Invoices in Bulk    
.------------------------------------------------------------------------------
BBKINV00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE            * Display formaction
          GOTO      BBKINV80 IF EQUAL
.
          RESET     UPDTTYPE
          MATCH     "A",UPDTTYPE            * Display formaction
          GOTO      BBKINV10 IF EQUAL
.
          RESET     UPDTTYPE
          MATCH     "U",UPDTTYPE            * Regenerate formaction
          GOTO      BBKINV20 IF EQUAL
.
          RESET     UPDTTYPE
          MATCH     "D",UPDTTYPE            * Display formaction
          GOTO      BBKINV30 IF EQUAL
.
          GOTO      BBKINV91
.
.         ************ ADD FORMACTION **********
.
BBKINV10  PACK      KEY4,PMSBI001,SP70
          CALL      RDPMBIS1         * read bulk invoice selection
          COMPARE   ZERO,OVRCD
          GOTO      BBKINV93 IF EQUAL

          MOVE      PMSBI001,PMBSSEID
          MOVE      PMSBI002,PMBSINVD
          MOVE      PMSBI003,PMBSHOSP
          PACK      PMBSASAT,CCC,CYY,CMM,CDD
          REP       " 0",PMBSASAT    * Set as at date to current date
          CALL      WRPMBIS1         * write new invoice selection options
          CALL      SCBSEL00         * schedule bulk invoice selection
          GOTO      BBKINV99
.
.         ************ GENERATE FORMACTION **********
.
BBKINV20  PACK      KEY4,PMSBI001,SP70
          CALL      RDPMBIS1         * read bulk invoice selection
          BRANCH    OVRCD,BBKINV92
.
          CALL      SCBSEL00         * schedule bulk invoice selection
          GOTO      BBKINV99
.
.         ************ DELETE FORMACTION **********
.
BBKINV30  PACK      KEY4,PMSBI001,SP70
          CALL      RDPMBIS1         * Add into Current File
          BRANCH    OVRCD,BBKINV92
          CALL      DEPMBIS1
.
          PACK      KEY12,PMSBI001,SP70
          CALL      RSPMBBI1
BBKINV31  CALL      RKPMBBI1
          BRANCH    OVRCD,BBKINV99
.
          MATCH     PMSBI001,PMBBSEID
          GOTO      BBKINV99 IF NOT EQUAL
.
          PACK      KEY12,PMBBSEID,PMBBVISN
          CALL      DEPMBBI1
          GOTO      BBKINV31 
.
.         ************ DISPLAY FORMACTION **********
.
BBKINV80  PACK      KEY4,PMSBI001,SP10
          CALL      RDPMBIS1
          IF        OVRCD=1        
            CALL      CLRBIS00
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Print Backdate Invoices in Bulk",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00                * Output File & Write HTML Pg Header
          BRANCH    EXIT,BBKINV99
          CALL      WEBBOD00                * Process Web Body
          CALL      WEBEND00                * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      BBKINV99
.
.         ************ DISPLAY ERROR MESSAGE **********
.
BBKINV91  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      BBKINV99
.
BBKINV92  MOVE      "Record does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      BBKINV99
.
BBKINV93  MOVE      "Record already exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      BBKINV99
.
BBKINV99  RETURN
+
.------------------------------------------------------------------------------
. Save bulk invoice printing variables
.------------------------------------------------------------------------------
.
CLRBLI00  MOVE     SP70,PMBLSEID
          MOVE     SP70,PMBLSEDC
          MOVE     SP70,PMBLTYPE
          MOVE     SP70,PMBLSTNM
          MOVE     SP70,PMBLENNM
          MOVE     ZERO,PMBLDAYS
          MOVE     SP70,PMBLSTDT
          MOVE     SP70,PMBLENDT
          MOVE     ZERO,PMBLMAMT
          MOVE     SP70,PMBLWARD
.
CLRBLI99  RETURN
+
.------------------------------------------------------------------------------
. Save bulk backdate invoice printing variables
.------------------------------------------------------------------------------
CLRBIS00  MOVE     SP70,PMBSSEID
          MOVE     SP70,PMBSINVD
          MOVE     SP70,PMBSASAT
CLRBIS99  RETURN
+
.--------------------------------------------------------------------------
.*                             SCHSEL00                                   *
.*               Schedule bulk invoice selections in IBARSH11             *
.--------------------------------------------------------------------------
SCHSEL00  MOVE      "IBARSH11",SCHDPGID
          MOVE      "Bulk Invoice selection",SCHDDESC
          PACK      SCHDDATE,CCC,CYY,CMM,CDD
          REP       " 0",SCHDDATE
          CLOCK     TIME,SCHDTIME
.
.         Write Keyin parameters
.
          MOVE      1,KEYSTARR[1]            * Option 1 bulk invoice selection
          MOVE      PMSBL001,KEYSTARR[2]     * Selection id               
          IF        IBCNMHOS=1
            MOVE      PMSBL012,KEYSTARR[3]   * Hospital id               
            MOVE      THREE,KEYSTCNT         * Number of Keyins
          ELSE
            MOVE      TWO,KEYSTCNT           * Number of Keyins
          ENDIF
.
          CALL      SCHPRO00   * Schedule Extract
.
SCHSEL99  RETURN
+
.------------------------------------------------------------
. Delete invoice Indicator Update
.------------------------------------------------------------
DELINV00  CALL      XMLHED00
          BRANCH    EXIT,DELINV99
.
DELINV10  PACK      KEY4,PMSBL001,SP70
          CALL      RDPMBLI1
          BRANCH    OVRCD,DELINV90
.
          PACK      KEY12,PMSBL001,ADMISSNO,SP70
          CALL      RDPMBII1
          BRANCH    OVRCD,DELINV90
.
          MOVE      DELTSTAT,PMBISTAT
          CALL      UPPMBII1
.
          WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>":
                             "Update Complete":
                             "</RETURN_VALUE>"
          GOTO      DELINV95
.
DELINV90  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=ERROR>":
                             "Invalid Patient":
                             "</RETURN_VALUE>"
.
DELINV95  CALL      XMLEND00
.
DELINV99  RETURN
+
.------------------------------------------------------------
. Delete invoice Indicator Update from bulk backdate invoice
.------------------------------------------------------------
DEBINV00  CALL      XMLHED00
          BRANCH    EXIT,DEBINV99
.
DEBINV10  PACK      KEY4,PMSBI001,SP70
          CALL      RDPMBIS1
          BRANCH    OVRCD,DEBINV90
.
          PACK      KEY12,PMSBI001,ADMISSNO,SP70
          CALL      RDPMBBI1
          BRANCH    OVRCD,DEBINV90
.
          MOVE      DELTSTAT,PMBBSTAT
          CALL      UPPMBBI1
.
          WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>":
                             "Update Complete":
                             "</RETURN_VALUE>"
          GOTO      DEBINV95
.
DEBINV90  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=ERROR>":
                             "Invalid Patient":
                             "</RETURN_VALUE>"
.
DEBINV95  CALL      XMLEND00
.
DEBINV99  RETURN
.
.------------------------------------------------------------
. Remove script to flag contract admissions for export 
.------------------------------------------------------------
REMINV00  CALL      XMLHED00
          BRANCH    EXIT,REMINV99
.
          MATCH     ANSE,UPDTTYPE           * Flag record for export
          GOTO      REMINV10 IF EQUAL
.
          MATCh     ANSS,UPDTTYPE           * Select surgeons for this adm
          GOTO      REMINV20 IF EQUAL
.
          MATCh     ANSA,UPDTTYPE           * Select anaesthetists for this adm
          GOTO      REMINV20 IF EQUAL
.
          WRITE     HTMLFILE;"<RETURN_VALUE TYPE=ERROR>":
                    " INVALID UPDATE TYPE ":
                    "</RETURN_VALUE>"
          GOTO      REMINV95
.
REMINV10  CALL      REME0000
          GOTO      REMINV95
.
REMINV20  CALL      RSUR0000
          GOTO      REMINV95
.
REMINV90  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=ERROR>":
                             "Invalid Contract Admission Record",KEY22:
                             "</RETURN_VALUE>"
.
REMINV95  CALL      XMLEND00
.
REMINV99  RETURN
.
.------------------------------------------------------------
.         Check for Contract holding invoice
.------------------------------------------------------------
ONHOLD00  CALL      XMLHED00
          BRANCH    EXIT,ONHOLD99
.
          PACK      KEY8,ADMISSNO,SP70
          CALL      RDIPEN1
          BRANCH    OVRCD,ONHOLD92
.
          MOVE      SP70,PENDHOSP
          IF        IBCNMHOS=1
            PACK      KEY8,IPADMNO,SP70
            CALL      RDPMVX11
            IF        OVRCD=0
              MOVE      PMVXMHOS,PENDHOSP     * Hospital ID
            ENDIF
          ENDIF
.
          BRANCH    IPSYSTM,ONHOLD10,ONHOLD20,ONHOLD30
          GOTO      ONHOLD92
.
.         Emergency
.
ONHOLD10  CALL      RDEMVIS1
          BRANCH    OVRCD,ONHOLD92
.
          MOVE      EMVIDATE,PENDSDAT        * as at date
          MOVE      EMVIDATE,PENDADAT        * visit date
          MOVE      SP70,PENDDDAT
          IF        EMVISTAT=2 | EMVISTAT=3
            MOVE      EMVIDDAT,PENDDDAT      * discharged date
          ENDIF
          MOVE      EMVIFUND,PENDFUND               * ED
          MOVE      EMVICOMP,PENDCLAM
          MOVE      " 1",PENDSYST
          GOTO      ONHOLD60
.
.         Outpatient
.
ONHOLD20  OPEN      OUTSITA1,"outsitaf"     * Open the site file
          MOVE      "out",OSTFILE           * Save Current File Prefix
          MOVE      IPSITE,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD,ONHOLD92
.
          PACK      CFNAME,OSTFILE,FILBB1A1  * Get the name of the BOKA3 file
          CLOSE     OUTBB1A1
          OPEN      OUTBB1A1,CFNAME
.
          CALL      RDBOKB1
          BRANCH    OVRCD,ONHOLD92
.
          MOVE      OBADATE,PENDSDAT         * as at date
          MOVE      OBADATE,PENDADAT         * Visit date
          MOVE      OBADATE,PENDDDAT         * discharged date
          MOVE      OTBBFUND,PENDFUND               * OP
          MOVE      OBACOMP,PENDCLAM
          MOVE      " 2",PENDSYST
          GOTO      ONHOLD60
.
.         Inpatient
.
ONHOLD30  CALL      RDMISS1
          BRANCH    OVRCD,ONHOLD92
.
          MOVE      ADATE,PENDSDAT             * as at date
          MOVE      ADATE,PENDADAT                * admission date
          MOVE      SP70,PENDDDAT
          IF        ASTAT=3
            CALL      RDDSCH1
            IF        OVRCD=0
              MOVE      DDATE,PENDDDAT            * discharged date
            ENDIF
          ENDIF
          MOVE      AFUNDH,PENDFUND               * IP
          MOVE      ACLAIM,PENDCLAM
          MOVE      " 3",PENDSYST
.
ONHOLD60  UNPACK    SP70,HFNAME,KEY1
          PACK      PTIPRDES,SP70,SP70
.
          PACK      KEY9,PENDFUND,PENDCLAM
          MATCH     SP70,KEY9
          GOTO      ONHOLD90 IF EQUAL
.
          MOVE      SP70,PTFXRHCD
          MOVE      SP70,PTFXRHFT
          MOVE      ZERO,EXIT
          CALL      IPRH0000                      * check Claim code/HF hold Inv
.
          PACK      KEY83,PTIPRHLD,PTIPRDES,SP70,SP70
          MATCH     SP70,KEY83
          GOTO      ONHOLD90 IF EQUAL
.
          MATCH     SP70,PENDFUND
          GOTO      ONHOLD62 IF EQUAL
.
          PACK      KEY83,PTFXRHCD,PTFXRHFT
          MATCH     SP70,KEY83
          GOTO      ONHOLD62 IF EQUAL        * Must be claim code hold invoice
.
          MOVE      "1",KEY1                 * Health fund on hold
          MATCH     SP70,HFNAME
          GOTO      ONHOLD80 IF NOT EQUAL
          MOVE      SP70,KEY1
.
ONHOLD62  PACK      KEY5,CODECL,PENDCLAM
          CALL      RDCODE1
          IF        OVRCD=0
            PACK      HFNAME,TDESC,SP70
            MOVE      "2",KEY1               * Claim code on hold
          ENDIF
.
.         Get Reason On hold, Code description or txt free
.
ONHOLD80  MATCH     SP70,PTIPRHLD
          IF        !@EQUAL
            MOVE      "rh",KEY2
            PACK      KEY5,KEY2,PTIPRHLD
            CALL      RDCODE1
            IF        OVRCD=0
              PACK      PTIPRDES,TDESC,SP70  * reason for on hold
            ENDIF
          ENDIF
.
.         KEY1=1 Health fund on hold, KEY1=2 Claim code on hold
.
ONHOLD90  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>":
                               KEY1,"|",HFNAME,"|",PTIPRDES:
                             "</RETURN_VALUE>"
          GOTO      ONHOLD98
.
ONHOLD92  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=ERROR>":
                             "Invalid Admission / Visit Type- ":
                              ADMISSNO:
                             "</RETURN_VALUE>"
          GOTO      ONHOLD98
.
ONHOLD98  CALL      XMLEND00
ONHOLD99  RETURN
+
.------------------------------------------------------------
. Create a selection list of valid surgeons/anaesthetists for 
. this admission
.------------------------------------------------------------
RSUR0000  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>";
.
          PACK      KEY11,ADMISSNO,SP70
          CALL      RSNZSPR1
RSUR1000  CALL      RKNZSPR1
          BRANCH    OVRCD,RSUR9000
.
          MATCH     ADMISSNO,NZSPADMN
          GOTO      RSUR9000 IF NOT EQUAL
.
          MATCH     ANSS,UPDTTYPE
          IF        @EQUAL
            MATCH     SP70,NZSPSURG
            GOTO      RSUR1000 IF EQUAL
.
            PACK      KEY10,NZSPSURG,SP70
          ENDIF
.
          MATCH     ANSA,UPDTTYPE
          IF        @EQUAL
            MATCH     SP70,NZSPANAE
            GOTO      RSUR1000 IF EQUAL
.
            PACK      KEY10,NZSPANAE,SP70
          ENDIF
.
          CALL      RDPMHCP1
          BRANCH    OVRCD,RSUR1000
.
          MOVE      PMHCTITL,FMTTITLE
          MOVE      PMHCGNAM,FMTGNAME
          MOVE      PMHCSNAM,FMTSNAME
          CALL      DOCNM000
.
          WRITE     HTMLFILE;PMHCHCPC,",",DOCFNAME,"|";
.
          GOTO      RSUR1000
.
RSUR9000  WRITE     HTMLFILE;"</RETURN_VALUE>"
RSUR9999  RETURN
.
.------------------------------------------------------------
. Remove script to flag contract admissions for export
.------------------------------------------------------------
REME0000  PACK      KEY22,NZPICKEY,SP70
          CALL      RDNZPIC1
          BRANCH    OVRCD,REME9000
.
          MATCH     "1",NZPIEXPT
          IF        @EQUAL
            MOVE      ZERO,NZPIEXPT
          ELSE
            MOVE      ONE,NZPIEXPT
          ENDIF
.
          CALL      UPNZPIC1
.
          WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>":
                             "Update Complete":
                             "</RETURN_VALUE>"
.
          GOTO      REME9999
.
REME9000  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=ERROR>":
                             "Invalid Contract Admission Record",KEY22:
                             "</RETURN_VALUE>"
.
REME9999  RETURN
+
.--------------------------------------------------------------------------
.*                             SCBSEL00                                   *
.*      Schedule bulk backdate invoice selections in IBARSH11             *
.--------------------------------------------------------------------------
SCBSEL00  MOVE      "IBAINP25",SCHDPGID
          MOVE      "Bulk Backdate Invoice selection",SCHDDESC
          PACK      SCHDDATE,CCC,CYY,CMM,CDD
          REP       " 0",SCHDDATE
          CLOCK     TIME,SCHDTIME
.
.         Write Keyin parameters
.
          MOVE      THREE,KEYSTARR[1]        * Option 3 bulk invoice selection
          MOVE      PMSBI001,KEYSTARR[2]     * Selection id               
          UNPACK    PMSBI002,XCENT,XYEAR,XMON,XDAY
          PACK      PMSBI002,XDAY,XMON,XCENT,XYEAR
          MOVE      PMSBI002,KEYSTARR[3]     * Selection id               
.
          IF        IBCNMHOS=1
            MOVE      PMSBI003,KEYSTARR[4]     * Hospital id
            MOVE      SP70,KEYSTARR[5]         * Hit enter for printer selection
            MOVE      FIVE,KEYSTCNT            * Number of Keyins
          ELSE
            MOVE      SP70,KEYSTARR[4]         * Hit enter for printer selection
            MOVE      FOUR,KEYSTCNT            * Number of Keyins
          ENDIF
.
          CALL      SCHPRO00   * Schedule Extract
.
SCBSEL99  RETURN
+
.------------------------------------------------------------------------------
.         Action Plan Maintenance: Display,Add,Update,Delete
.------------------------------------------------------------------------------
ACPLM000  RESET     UPDTTYPE 
          MATCH     SP70,UPDTTYPE           * Display formaction
          GOTO      ACPLM800 IF EQUAL
.
          MATCH     "A",UPDTTYPE            * Add formaction
          GOTO      ACPLM010 IF EQUAL
.
          MATCH     "U",UPDTTYPE            * Update formaction
          GOTO      ACPLM100 IF EQUAL
.
          MATCH     "D",UPDTTYPE            * Delete formaction
          GOTO      ACPLM200 IF EQUAL
.
          GOTO      ACPLM930
.
.   **********  ADD FORMACTION  **********
.
ACPLM010  PACK      KEY6,PTFAP001,PTFAP002,SP70
          CALL      RSPTFAP1
          CALL      RKPTFAP1
          BRANCH    OVRCD,ACPLM020
.
          MATCH     PTFPCLAI,PTFAP001
          GOTO      ACPLM020 IF NOT EQUAL
.
          MATCH     PTFPACPL,PTFAP002
          GOTO      ACPLM990 IF EQUAL
.
ACPLM020  CALL      CLRPTFAP
          CALL      MVPTFP00
          PACK      KEY6,PTFPCLAI,PTFPACPL,SP70
          CALL      RAPTFAP1
          IF        OVRCD=1
            CALL      WRPTFAP1
          ENDIF
          GOTO      ACPLM998
.
.
.   **********  UPDATE FORMACTION  **********
.
ACPLM100  PACK      KEY6,PTFAP001,PTFAP002,SP70
          CALL      RDPTFAP1                 * Update into Current File
          BRANCH    OVRCD,ACPLM910
.
          CALL      MVPTFP00
          CALL      UPPTFAP1
          GOTO      ACPLM998
.
.
.   **********  DELETE FORMACTION  **********
.
ACPLM200  PACK      KEY6,PTFAP001,PTFAP002,SP70
          CALL      RDPTFAP1                 * Update into Current File
          BRANCH    OVRCD,ACPLM910
.
          CALL      DEPTFAP1
          GOTO      ACPLM998
.
.   **********  DISPLAY FORMACTION  **********
.
ACPLM800  MOVE      ZERO,EXIT
.
          PACK      KEY6,PTFAP001,PTFAP002,SP70
          CALL      RDPTFAP1
          IF        OVRCD=1
            CALL      CLRPTFAP
          ENDIF
          GOTO      ACPLM820
.
ACPLM820  CLEAR     WEBTITLE
          MOVE      "Action Plan Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,ACPLM999
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      ACPLM999
.
ACPLM910  MOVE      "Record does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ACPLM999
.
ACPLM930  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ACPLM999
.
ACPLM990  MOVE      "Record already exists.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ACPLM999
.
ACPLM998  MOVE      ZERO,EXIT
ACPLM999  RETURN
+
.------------------------------------------------------------------------------
. Process Fields called from WEBBOD00
.------------------------------------------------------------------------------
.
PROFLD00  BRANCH    REPORTNO,PROFLD10,PROFLD99,PROFLD30,PROFLD99,PROFLD50:
                             PROFLD99,PROFLD70
.
          GOTO      PROFLD99
.
PROFLD10  BRANCH    FLDSETNO,PROFLD11
          GOTO      PROFLD99
.
PROFLD11  CALL      BINV0000                                                 
          GOTO      PROFLD99
.
PROFLD30  BRANCH    FLDSETNO,PROFLD31
          GOTO      PROFLD99
.
PROFLD31  CALL      BDIV0000        * Backdate invoice
          GOTO      PROFLD99
.
PROFLD50  BRANCH    FLDSETNO,PROFLD51,PROFLD52
          GOTO      PROFLD99
.
PROFLD51  CALL      RTAG0000        * Related Provider Invoice Tags
          GOTO      PROFLD99
.
PROFLD52  CALL      NZRP0000        
          GOTO      PROFLD99
.
PROFLD70  BRANCH    FLDSETNO,PROFLD71
          GOTO      PROFLD99
.
PROFLD71  CALL      PTFP0000        * Future Action Plan Tags
          GOTO      PROFLD99
.
PROFLD99  RETURN
+
.------------------------------------------------------------------------------
. Bulk Invoice Printing             
.------------------------------------------------------------------------------
.
BINV0000  BRANCH    FLDITMNO,BINV0100,BINV0200,BINV0300,BINV0400,BINV0500:
                             BINV0600,BINV0700,BINV0800,BINV0900,BINV1000:
                             BINV1100,BINV1200,BINV1300,BINV1400,BINV1500: 
                             BINV1600,BINV1700,BINV1800,BINV1900,BINV2000
          GOTO      BINV9999
.
BINV0100  CALL      BLITAB00
          GOTO      BINV9999
.
BINV0200  PACK      KEY29,SP70                                                                                                                     
          IF        IBCNMHOS = 1                                                                                                                   
            PACK      KEY29,SP3,WBSEHOSP,SP70                                                                                                      
          ENDIF                                                                                                                                    
          CALL      RDSWARD7                                                                                                                       
BINV0210  CALL      RDKWARD7                                                                                                                       
          BRANCH    OVRCD,BINV9999                                                                                                                 
.                                                                                                                                                  
.                         if we have a bed number we have passed all the ward                                                                      
.                         master records, so exit                                                                                                           
          MATCH     SP70,WBED                                                                                                                      
          GOTO      BINV9999 IF NOT EQUAL                                                                                                          
.                                                                                                                                                  
          COMPARE   WACTIVE,ZERO                 *List only active wards                                                                           
          GOTO      BINV0210 IF NOT EQUAL                                                                                                          
.                                                                                                                                                  
          IF        IBCNMHOS = 1                                                                                                                   
            MATCH      SP3,WBSEHOSP                                                                                                                
            IF        !@EQUAL                                                                                                                      
              MATCH     WBSEHOSP,PTWDHOSN                                                                                                          
              GOTO      BINV9999 IF NOT EQUAL                                                                                                      
            ENDIF                                                                                                                                  
          ENDIF                                                                                                                                    
.                                                                                                                                                  
.                       write the select option.                                                                                                   
          WRITE     HTMLFILE;"<option value=#"",WWARD,"#"";                                                                                        
          MATCH     PMBLWARD,WWARD     
          IF        @EQUAL                                                                                                                         
            WRITE     HTMLFILE;" selected";                                                                                                        
          ENDIF                                                                                                                                    
          WRITE     HTMLFILE;">",WBDESC;
.
          IF        IBCNMHOS = 1
            MATCH      SP3,WBSEHOSP
            IF        @EQUAL
              WRITE     HTMLFILE;" - ",PTWDHOSN;
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;"</option>"
          GOTO      BINV0210                                                                                                                       
.                                                                                                                                                  
.
BINV0300  MATCH     PMBLSEID,SP70
          GOTO      BINV9999 IF EQUAL
          WRITE     HTMLFILE;PMBLSEID;
          GOTO      BINV9999
.
BINV0400  MATCH     PMBLSEDC,SP70
          GOTO      BINV9999 IF EQUAL
          WRITE     HTMLFILE;PMBLSEDC;
          GOTO      BINV9999
.
BINV0500  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,BINV0510
.
          MOVE      ZERO,FORM1
          MOVE      PMBLTYPE,FORM1
          MOVE      SP70,KEY40
          LOAD      KEY40,FORM1,INVNDAYS,INVKDATE,INVXDLLR,INVGWARD,INVGCLTP
          WRITE     HTMLFILE;KEY40;
          GOTO      BINV9999
          
BINV0510  WRITE     HTMLFILE;PMBLTYPE;
          GOTO      BINV9999
.
BINV0600  MATCH     PMBLSTNM,SP70
          GOTO      BINV9999 IF EQUAL
          WRITE     HTMLFILE;PMBLSTNM;
          GOTO      BINV9999
.
BINV0700  MATCH     PMBLENNM,SP70
          GOTO      BINV9999 IF EQUAL
          WRITE     HTMLFILE;PMBLENNM;
          GOTO      BINV9999
.
BINV0800  WRITE     HTMLFILE;PMBLDAYS;
          GOTO      BINV9999
.
BINV0900  MATCH     PMBLSTDT,SP70
          GOTO      BINV9999 IF EQUAL
          UNPACK    PMBLSTDT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      BINV9999
.
BINV1000  MATCH     PMBLENDT,SP70
          GOTO      BINV9999 IF EQUAL
          UNPACK    PMBLENDT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      BINV9999
.
BINV1100  WRITE     HTMLFILE;PMBLMAMT;
          GOTO      BINV9999
.
BINV1200  MATCH     PMBLWARD,SP70
          GOTO      BINV9999 IF EQUAL
          WRITE     HTMLFILE;PMBLWARD;
          GOTO      BINV9999
.
BINV1300  CALL      DISTPI00
          GOTO      BINV9999
.
BINV1400  CALL      NEXTPI00
          GOTO      BINV9999
.
BINV1500  CALL      PREVPI00
          GOTO      BINV9999
.
BINV1600  MOVE      ZERO,RECTOTAL
          PACK      KEY12,PMSBL001,SP70
          CALL      RSPMBII1
BINV1610  CALL      RKPMBII1
          BRANCH    OVRCD,BINV1690

          MATCH     PMSBL001,PMBISEID
          GOTO      BINV1690 IF NOT EQUAL

          ADD       ONE,RECTOTAL
          GOTO      BINV1610 
.
BINV1690  WRITE     HTMLFILE;RECTOTAL;
          GOTO      BINV9999
.
BINV1700  MOVE      "CL",CATEGORY
          MOVE      PMBLWARD,CODE
          CALL      CODITM00
          GOTO      BINV9999
.
BINV1800  MOVE      IBCNMHOS,FORM1
          IF        FORM1<>1
            MOVE      ZERO,FORM1
          ENDIF
          WRITE     HTMLFILE;FORM1;
          GOTO      BINV9999
.
BINV1900  MOVE      PMBLHOSP,DIM3
          CALL      HOSPID00                   * Hospital ID
          GOTO      BINV9999
.
BINV2000  MOVE      WBSEHOSP,DIM3
          CALL      HOSPID00                   * Hospital ID
          GOTO      BINV9999
.
BINV9999  RETURN
+
.------------------------------------------------------------
. Hospital ID
.------------------------------------------------------------
HOSPID00  OPEN      PATHSPA1,"pathspaf"
.
          PACK      KEY3,SP10
          CALL      RSPTHSP1
HOSPID10  CALL      RKPTHSP1
          BRANCH    OVRCD,HOSPID90
.
          PACK      KEY5,QUOTE,PTHSHOSP,QUOTE
          BRANCH    IBCNMHOS,HOSPID20
          WRITE     HTMLFILE;"<option value=",KEY5,">":
                             PTHSNAME,"</option>"
          GOTO      HOSPID10
.
.         Mult-Hospital
.
HOSPID20  MATCH     DIM3,PTHSHOSP
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=",KEY5," selected>":
                               PTHSNAME,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=",KEY5,">":
                               PTHSNAME,"</option>"
          ENDIF
          GOTO      HOSPID10
.
HOSPID90  
HOSPID99  RETURN
+
.------------------------------------------------------------
. List of patient lists
.------------------------------------------------------------
BLITAB00  MOVE      SP70,KEY4
          CALL      RSPMBLI1
BLITAB10  CALL      RKPMBLI1
          BRANCH    OVRCD,BLITAB99
.
          CLEAR     HTMLLINK
          APPEND    "javascript:UpdateSelections('",HTMLLINK
          APPEND    PMBLSEID,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
          MOVE      ZERO,FORM1
          MOVE      PMBLTYPE,FORM1
          MOVE      SP70,KEY40
          LOAD      KEY40,FORM1,INVNDAYS,INVKDATE,INVXDLLR,INVGWARD,INVGCLTP
.
          WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":
                             "#"",PMBLSEID,"#",":
                             "#"",PMBLSEDC,"#",":
                             "#"",KEY40,"#",":
                             "#"",PMBLSTNM,"#",":
                             "#"",PMBLENNM,"#");"
.
          GOTO      BLITAB10
.
BLITAB99  RETURN
+
.------------------------------------------------------------------------------
. Bulk backdate Invoice Printing             
.------------------------------------------------------------------------------
BDIV0000  BRANCH    FLDITMNO,BDIV0100,BDIV0200,BDIV0300,BDIV0400,BDIV0500:
                             BDIV0600,BDIV0700,BDIV0800,BDIV0900,BDIV1000:
                             BDIV1100
          GOTO      BDIV9999
.
BDIV0100  CALL      BBLTAB00          * Bulk backdate invoice list
          GOTO      BDIV9999
.
BDIV0200  MATCH     PMBSSEID,SP70
          GOTO      BDIV9999 IF EQUAL
          WRITE     HTMLFILE;PMBSSEID;
          GOTO      BDIV9999
.
BDIV0300  MATCH     PMBSINVD,SP70
          GOTO      BDIV9999 IF EQUAL
          UNPACK    PMBSINVD,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      BDIV9999
.
BDIV0400  MATCH     PMBSASAT,SP70
          GOTO      BDIV9999 IF EQUAL
          UNPACK    PMBSASAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      BDIV9999
.
BDIV0500  MOVE      ZERO,RECTOTAL
          PACK      KEY12,PMSBI001,SP70
          CALL      RSPMBBI1
BDIV0510  CALL      RKPMBBI1
          BRANCH    OVRCD,BDIV0590

          MATCH     PMSBI001,PMBBSEID
          GOTO      BDIV0590 IF NOT EQUAL

          ADD       ONE,RECTOTAL
          GOTO      BDIV0510 
.
BDIV0590  WRITE     HTMLFILE;RECTOTAL;
          GOTO      BDIV9999
.
BDIV0600  CALL      DISBIL00          * Bulk backdate invoice list
          GOTO      BDIV9999
.
BDIV0700  CALL      NEXTBI00          * Next patient list
          GOTO      BDIV9999
.
BDIV0800  CALL      PREVBI00          * Previous patient list
          GOTO      BDIV9999
.
BDIV0900  MOVE      IBCNMHOS,FORM1
          IF        FORM1<>1
            MOVE      ZERO,FORM1
          ENDIF
          WRITE     HTMLFILE;FORM1;
          GOTO      BDIV9999
.
BDIV1000  MOVE      PMBSHOSP,DIM3
          CALL      HOSPID00                   * Hospital ID
          GOTO      BDIV9999
.
BDIV1100  MOVE      WBSEHOSP,DIM3
          CALL      HOSPID00                   * Hospital ID
          GOTO      BDIV9999
.
BDIV9999  RETURN
+
.------------------------------------------------------------
. List of Bulk backdate invoice 
.------------------------------------------------------------
BBLTAB00  MOVE      SP70,KEY4
          CALL      RSPMBIS1
BBLTAB10  CALL      RKPMBIS1
          BRANCH    OVRCD,BBLTAB99
.
          CLEAR     HTMLLINK
          APPEND    "javascript:DeleteSelections('",HTMLLINK
          APPEND    PMBSSEID,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
          UNPACK    PMBSINVD,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DISPDAT2,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          UNPACK    PMBSASAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DISPDAT3,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":
                             "#"",PMBSSEID,"#",":
                             "#"",DISPDAT2,"#",":
                             "#"",DISPDAT3,"#");"
.
          GOTO      BBLTAB10
.
BBLTAB99  RETURN
+
.------------------------------------------------------------
. Selected Bulk backdate Invoice List
.------------------------------------------------------------
DISBIL00  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          MOVE      ZERO,SORTFLAG
          UNPACK    DIM127,D1,SORTFLAG,DIM127   * tablesort flag
.
          MATCH     "1",SORTFLAG
          IF        !@EQUAL
            CALL      DIPBHD00              * Bulk Invoice List Table Heading
          ENDIF
.
          IF        NORECORD=0
            MOVE      "14",NORECORD       * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          MOVE      PMSBI001,KEY4
          CALL      RDPMBIS1
          BRANCH    OVRCD,DISBIL99
.
          PACK      KEY12,PMSBI001,SP70
          CALL      RSPMBBI1
DISBIL10  CALL      RKPMBBI1
          BRANCH    OVRCD,DISBIL99
.
          MATCH     PMSBI001,PMBBSEID
          GOTO      DISBIL99 IF NOT EQUAL
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      DISBIL10
          ENDIF
.
          CALL      PBIROW00
.
          MATCH     "1",SORTFLAG
          IF        !@EQUAL
            ADD       ONE,DISNUMB
            COMPARE   NORECORD,DISNUMB
            GOTO      DISBIL10 IF NOT EQUAL
            GOTO      DISBIL99
          ELSE
            GOTO      DISBIL10
          ENDIF
.
DISBIL99  RETURN
+
.------------------------------------------------------------
. Read Selected Backdate invoice list 
.------------------------------------------------------------
PBIROW00  PACK      KEY8,PMBBVISN,SP70
          CALL      RDMISS1
          IF        OVRCD=1
            MOVE      "Invalid Admission",FORMATNM
          ENDIF
.
          PACK      KEY8,AURNO,SP70
          CALL      RDMAST1
          IF        OVRCD=1
            MOVE      "Invalid U/R",FORMATNM
          ELSE
          MOVE      AURNO,URNUMBER
            IF        PCEASE=1
              MOVE      PDECDTE,AGEDATE            * Date of Death
            ELSE
              PACK      AGEDATE,CCC,CYY,CMM,CDD    * Today's date
            ENDIF
            REP       " 0",AGEDATE
            CALL      CALCAGE
            CALL      PATLN000
          ENDIF
.
          MATCH     PMBBADDT,SP8
          IF        @EQUAL 
            MOVE    "&nbsp;",DISPDAT2
          ELSE
            UNPACK    PMBBADDT,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      DISPDAT2,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          ENDIF
.
          MATCH     PMBBIFDT,SP8
          IF        @EQUAL 
            MOVE    "&nbsp;",DISPDAT3
          ELSE
            UNPACK    PMBBIFDT,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      DISPDAT3,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          ENDIF
.
          MATCH     PMBBITDT,SP8
          IF        @EQUAL 
            MOVE    "&nbsp;",DISPDAT4
          ELSE
            UNPACK    PMBBITDT,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      DISPDAT4,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          ENDIF
.
          MATCH     "1",PMBBPRNT
          IF        @EQUAL
            MOVE      "Yes",PRINTED
          ELSE
            MOVE      "No",PRINTED
          ENDIF
.
          MATCH     "1",PMBBCFLG
          IF        @EQUAL
            MOVE      "Yes",CODCOMPL
          ELSE
            MOVE      "No",CODCOMPL
          ENDIF
.
          MATCH     PMBBPDRG,SP10
          IF        @EQUAL
            MOVE    "&nbsp;",PMBBPDRG
          ENDIF
.           
          MOVE      SP70,CAREDESC
          MATCH     SP70,PMBBCARE
          IF        !@EQUAL
            PACK      KEY5,ANSC,ANSC,PMBBCARE,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      PMBBCARE,CAREDESC
            ELSE
              MOVE      TDESC,CAREDESC
            ENDIF
          ENDIF
.
          MOVE      SP70,ATYPDESC
          MATCH     SP70,PMBBAMBS
          IF        !@EQUAL
            PACK      KEY5,ANSA,SP1,PMBBAMBS,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      PMBBAMBS,ATYPDESC
            ELSE
              MOVE      TDESC,ATYPDESC
            ENDIF
          ENDIF
.
          MOVE      PMBBVISN,ADMISSNO
          REP       " +",ADMISSNO
          REP       " +",URNUMBER
.
          MATCH     "1",SORTFLAG
          GOTO      PBIROW50 IF EQUAL
.
          MATCH     "1",PMBBPRNT
          IF        @EQUAL
            WRITE   HTMLFILE;"<tr><td>":
                              FORMATNM,"</td>";
          ELSE
            WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:ViewInvoice":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&urnumber=",URNUMBER:
                             "&admissno=",ADMISSNO:
                             "&selectid=",PMBBSEID:
                             "&chngdate=",PMBBITDT:
                             "&prntflag=",PMBBPRNT,"#")'>":
                              "<img src=../images/MaintenanceIcon.gif hspace=4":
                              " border=0 align=absmiddle></a>":
                              FORMATNM,"</td>";
          ENDIF
.
          WRITE     HTMLFILE;"<td>",PMBBURNO,"</td>":
                              "<td>",PMBBVISN,"</td>":
                              "<td>",DISPDAT2,"</td>":
                              "<td>",DISPDAT3,"</td>":
                              "<td>",DISPDAT4,"</td>":
                              "<td align=right>",PMBBAMNT,"</td>":
                              "<td>",PMBBPDRG,"</td>":
                              "<td>",CODCOMPL,"</td>":
                              "<td>",PRINTED,"</td>";
.
          REP       "+ ",URNUMBER
          REP       "+ ",ADMISSNO
          MATCH     "1",PMBBSTAT
          IF        @EQUAL
            MATCH     "1",PMBBPRNT
            IF        @EQUAL
              WRITE     HTMLFILE;"<td><input type=checkbox name=deltstat":
                                 " value=1 checked disabled></td>"; 
            ELSE
              WRITE     HTMLFILE;"<td><input type=checkbox name=deltstat":
                                 " value=1 onclick=#"DeleteBInvoice(this":
                                 ",'",PMBBSEID,"','",PMBBVISN,"'":
                                 ");#" checked></td>";
            ENDIF
          ELSE
            MATCH     "1",PMBBPRNT
            IF        @EQUAL
              WRITE     HTMLFILE;"<td><input type=checkbox name=deltstat":
                                 " value=1 disabled></td>"; 
            ELSE
              WRITE     HTMLFILE;"<td><input type=checkbox name=deltstat":
                                 " value=1 onclick=#"DeleteBInvoice(this":
                                 ",'",PMBBSEID,"','",PMBBVISN,"'":
                                 ");#"></td>";
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;"</tr>"
          GOTO      PBIROW99
.
PBIROW50  MATCH     "1",PMBBPRNT
          IF        @EQUAL
            MOVE      SP70,HTMLLNK3
            MOVE      ZERO,F1
          ELSE
            CLEAR     HTMLLNK3
            APPEND    "javascript:ViewInvoice('",HTMLLNK3
            APPEND    LINKPRG,HTMLLNK3
            APPEND    ".pbl?reportno=",HTMLLNK3
            APPEND    LINKREP,HTMLLNK3
            APPEND    "&template=",HTMLLNK3
            APPEND    LINKTEM,HTMLLNK3
            APPEND    "&urnumber=",HTMLLNK3
            APPEND    URNUMBER,HTMLLNK3
            APPEND    "&admissno=",HTMLLNK3
            APPEND    ADMISSNO,HTMLLNK3
            APPEND    "&selectid=",HTMLLNK3
            APPEND    PMBBSEID,HTMLLNK3
            APPEND    "&chngdate=",HTMLLNK3
            APPEND    PMBBITDT,HTMLLNK3
            APPEND    "&prntflag=",HTMLLNK3
            APPEND    PMBBPRNT,HTMLLNK3
            APPEND    "')",HTMLLNK3
            RESET     HTMLLNK3
            MOVE      ONE,F1
          ENDIF
.
          WRITE     HTMLFILE;"t.addRow(#"",HTMLLNK3,"#",":  *0
                             "#"",F1,"#",":                 *1
                             "#"",FORMATNM,"#",":           *2
                             "#"",PMBBSEID,"#",":           *3
                             "#"",PMBBVISN,"#",":           *4
                             "#"",PMBBURNO,"#",":           *5
                             "#"",PMBBADDT,"#",":           *6
                             "#"",PMBBIFDT,"#",":           *7
                             "#"",PMBBITDT,"#",":           *8
                             "#"",PMBBAMNT,"#",":           *9
                             "#"",PMBBPDRG,"#",":           *10
                             "#"",PMBBCFLG,"#",":           *11
                             "#"",PMBBSTAT,"#",":           *12
                             "#"",PMBBPRNT,"#",":           *13
                             "#"",PMBBWARD,"#",":           *14
                             "#"",PMBBAMBS,"#",":           *15
                             "#"",PMBBCARE,"#",":           *16
                             "#"",CODCOMPL,"#",":           *17
                             "#"",PRINTED,"#",":            *18
                             "#"",CAREDESC,"#",";           *19
.
          REP       " +",PMBBSEID
          REP       " +",PMBBVISN
.
          MATCH     "1",PMBBSTAT
          IF        @EQUAL
            MATCH     "1",PMBBPRNT
            IF        @EQUAL
              WRITE     HTMLFILE;"#"<input type=checkbox name=deltstat":
                                 " value=1 checked disabled>";
            ELSE
              WRITE     HTMLFILE;"#"<input type=checkbox name=deltstat":
                         " value=1 onclick=DeleteBInvoice(this":
                         ",'",PMBBSEID,"','",PMBBVISN,"'":
                                 ") checked>";
            ENDIF
          ELSE
            MATCH     "1",PMBBPRNT
            IF        @EQUAL
              WRITE     HTMLFILE;"#"<input type=checkbox name=deltstat":
                                 " value=1 disabled>";
            ELSE
              WRITE     HTMLFILE;"#"<input type=checkbox name=deltstat":
                             " value=1 onclick=DeleteBInvoice(this":
                             ",'",PMBBSEID,"','",PMBBVISN,"'":
                                 ")>";
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;"#",#"",ATYPDESC,"#")"            *21
.
          REP       "+ ",PMBBSEID
          REP       "+ ",PMBBVISN
          REP       "+ ",URNUMBER
          REP       "+ ",ADMISSNO
.
PBIROW99  RETURN
+
.------------------------------------------------------------
.  Table Selected Print Invoice (TABLE HEADING)
.------------------------------------------------------------
DIBILD00  WRITE     HTMLFILE;"<tr>":
                              "<td class=HeadingCell>Patient</td>":
       "<td class=HeadingCell width=8%>Admission</td>":
       "<td class=HeadingCell width=8%>Admission Date</td>":
       "<td class=HeadingCell width=8%>Discharge Date</td>":
       "<td class=HeadingCell width=8%>Invoice From Date</td>":
       "<td class=HeadingCell width=8%>Invoice To Date</td>":
       "<td class=HeadingCell width=8%>Invoice Total</td>":
       "<td class=HeadingCell width=8%>Prov. DRG</td>":
       "<td class=HeadingCell width=5%>Coded</td>":
       "<td class=HeadingCell width=5%>Printed</td>":
                              "<td class=HeadingCell width=8%>Exclude</td>":
                             "</tr>"
DIBILD999  RETURN
+
.------------------------------------------------------------
. Selected Bulk Invoice List
.------------------------------------------------------------
DISTPI00  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          CALL      DIPIHD00              * Bulk Invoice List Table Heading
.
          IF        NORECORD=0
            MOVE      "14",NORECORD       * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          MOVE      PMSBL001,KEY4
          CALL      RDPMBLI1
          BRANCH    OVRCD,DISTPI99
.
          PACK      KEY12,PMSBL001,SP70
          CALL      RSPMBII1
DISTPI10  CALL      RKPMBII1
          BRANCH    OVRCD,DISTPI99
.
          MATCH     PMSBL001,PMBISEID
          GOTO      DISTPI99 IF NOT EQUAL
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      DISTPI10
          ENDIF
.
          CALL      PINROW00
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      DISTPI10 IF NOT EQUAL
          GOTO      DISTPI99
.
DISTPI99  RETURN
.------------------------------------------------------------
.  Table Selected Print Invoice (TABLE HEADING)
.------------------------------------------------------------
DIPIHD00  WRITE     HTMLFILE;"<tr>":
                              "<td class=HeadingCell>Patient</td>":
       "<td class=HeadingCell width=8%>Admission</td>":
       "<td class=HeadingCell width=8%>Admission Date</td>":
       "<td class=HeadingCell width=8%>Discharge Date</td>":
       "<td class=HeadingCell width=8%>Invoice From Date</td>":
       "<td class=HeadingCell width=8%>Invoice To Date</td>":
       "<td class=HeadingCell width=8%>Invoice Total</td>":
       "<td class=HeadingCell width=8%>Prov. DRG</td>":
       "<td class=HeadingCell width=5%>Coded</td>":
       "<td class=HeadingCell width=5%>Printed</td>":
                              "<td class=HeadingCell width=8%>Exclude</td>":
                             "</tr>"
DIPIHD999  RETURN
.
.------------------------------------------------------------
.  Table Selected Print Invoice (TABLE HEADING)
.------------------------------------------------------------
DIPBHD00  WRITE     HTMLFILE;"<tr>":
                              "<td class=HeadingCell>Patient</td>":
       "<td class=HeadingCell width=8%>U/R Number </td>":
       "<td class=HeadingCell width=8%>Admission Number</td>":
       "<td class=HeadingCell width=8%>Admission Date</td>":
       "<td class=HeadingCell width=8%>Invoice From Date</td>":
       "<td class=HeadingCell width=8%>Invoice To Date</td>":
       "<td class=HeadingCell width=8%>Invoice Total</td>":
       "<td class=HeadingCell width=8%>Prov. DRG</td>":
       "<td class=HeadingCell width=5%>Coded</td>":
       "<td class=HeadingCell width=5%>Printed</td>":
                              "<td class=HeadingCell width=8%>Exclude</td>":
                             "</tr>"
DIPBHD999  RETURN
+
.------------------------------------------------------------
. Read Selected Finance  Letter details and write the
. Selected Finance letter row to HTMLFILE
.------------------------------------------------------------
PINROW00  PACK      KEY8,PMBIVISN,SP70
          CALL      RDMISS1
          IF        OVRCD=1
            MOVE      "Invalid Admission",FORMATNM
          ENDIF
.
          PACK      KEY8,AURNO,SP70
          CALL      RDMAST1
          IF        OVRCD=1
            MOVE      "Invalid U/R",FORMATNM
          ELSE
          MOVE      AURNO,URNUMBER
            IF        PCEASE=1
              MOVE      PDECDTE,AGEDATE            * Date of Death
            ELSE
              PACK      AGEDATE,CCC,CYY,CMM,CDD    * Today's date
            ENDIF
            REP       " 0",AGEDATE
            CALL      CALCAGE
            CALL      PATLN000
          ENDIF
.
          MATCH     PMBIADDT,SP8
          IF        @EQUAL 
            MOVE    "&nbsp;",DISPDAT2
          ELSE
            UNPACK    PMBIADDT,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      DISPDAT2,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          ENDIF
.
          MATCH     PMBIDSDT,SP8
          IF        @EQUAL 
            MOVE    "&nbsp;",DISPDATE
          ELSE
            UNPACK    PMBIDSDT,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      DISPDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          ENDIF
.
          MATCH     PMBIIFDT,SP8
          IF        @EQUAL 
            MOVE    "&nbsp;",DISPDAT3
          ELSE
            UNPACK    PMBIIFDT,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      DISPDAT3,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          ENDIF
.
          MATCH     PMBIITDT,SP8
          IF        @EQUAL 
            MOVE    "&nbsp;",DISPDAT4
          ELSE
            UNPACK    PMBIITDT,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      DISPDAT4,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          ENDIF
.
          MATCH     "1",PMBIPRNT
          IF        @EQUAL
            MOVE      "Yes",PRINTED
          ELSE
            MOVE      "No",PRINTED
          ENDIF
.
          MATCH     "1",PMBICFLG
          IF        @EQUAL
            MOVE      "Yes",CODCOMPL
          ELSE
            MOVE      "No",CODCOMPL
          ENDIF
.
          MATCH     PMBIPDRG,SP10
          IF        @EQUAL
            MOVE    "&nbsp;",PMBIPDRG
          ENDIF
.           
          MOVE      PMBIVISN,ADMISSNO
          REP       " +",ADMISSNO
          REP       " +",URNUMBER
.
          WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:ViewInvoice":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&urnumber=",URNUMBER:
                             "&admissno=",ADMISSNO:
                             "&selectid=",PMBISEID:
                             "&prntflag=",PMBIPRNT,"#")'>":
                              "<img src=../images/MaintenanceIcon.gif hspace=4":
                              " border=0 align=absmiddle></a>":
                              FORMATNM,"</td>":
                              "<td>",PMBIVISN,"</td>":
                              "<td>",DISPDAT2,"</td><td nowrap>":
                              DISPDATE,"</td>":
                              "<td>",DISPDAT3,"</td>":
                              "<td>",DISPDAT4,"</td>":
                              "<td align=right>",PMBIIAMT,"</td>":
                              "<td>",PMBIPDRG,"</td>":
                              "<td>",CODCOMPL,"</td>":
                              "<td>",PRINTED,"</td>";
.
          REP       "+ ",URNUMBER
          REP       "+ ",ADMISSNO
          MATCH     "1",PMBISTAT
          IF        @EQUAL
            MATCH     "1",PMBIPRNT
            IF        @EQUAL
              WRITE     HTMLFILE;"<td><input type=checkbox name=deltstat":
                                 " value=1 checked disabled></td>"; 
            ELSE
              WRITE     HTMLFILE;"<td><input type=checkbox name=deltstat":
                                 " value=1 onclick=#"DeleteInvoice(this":
                                 ",'",PMBISEID,"','",PMBIVISN,"'":
                                 ");#" checked></td>";
            ENDIF
          ELSE
            MATCH     "1",PMBIPRNT
            IF        @EQUAL
              WRITE     HTMLFILE;"<td><input type=checkbox name=deltstat":
                                 " value=1 disabled></td>"; 
            ELSE
              WRITE     HTMLFILE;"<td><input type=checkbox name=deltstat":
                                 " value=1 onclick=#"DeleteInvoice(this":
                                 ",'",PMBISEID,"','",PMBIVISN,"'":
                                 ");#"></td>";
            ENDIF
          ENDIF
.
.
          WRITE     HTMLFILE;"</tr>"
.
PINROW99  RETURN
.-----------------------------------------------------------
. Link to Next Group of patients
.-----------------------------------------------------------
NEXTPI00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F2
          REP       " +",PMSBL001
.
          WRITE     HTMLFILE;"patweb79.pbl":
                                 "?reportno=",F2:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&pmsbl001=",PMSBL001;
.
          REP       "+ ",PMSBL001
.
NEXTPI99  RETURN
.-------------------------------------------------------------
. Link to Prev Group of patients
.-------------------------------------------------------------
PREVPI00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3
          IF        F3<0
            MOVE      ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F2
          REP       " +",PMSBL001
.
          WRITE     HTMLFILE;"patweb79.pbl":
                                 "?reportno=",F2:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&pmsbl001=",PMSBL001;
.
          REP       "+ ",PMSBL001
.
PREVPI99  RETURN
.
.-----------------------------------------------------------
. Link to Next Group of patients
.-----------------------------------------------------------
NEXTBI00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F2
          REP       " +",PMSBI001
.
          WRITE     HTMLFILE;"patweb79.pbl":
                                 "?reportno=",F2:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&pmsbi001=",PMSBI001;
.
          REP       "+ ",PMSBI001
.
NEXTBI99  RETURN
.-------------------------------------------------------------
. Link to Prev Group of patients
.-------------------------------------------------------------
PREVBI00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3
          IF        F3<0
            MOVE      ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F2
          REP       " +",PMSBI001
.
          WRITE     HTMLFILE;"patweb79.pbl":
                                 "?reportno=",F2:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&pmsbi001=",PMSBI001;
.
          REP       "+ ",PMSBI001
.
PREVBI99  RETURN
.
.------------------------------------------------------------------------------
. Provider Invoice Processing
.------------------------------------------------------------------------------
PINV0000  RESET     UPDTTYPE
          MATCH     SP70,UPDTTYPE           * Display formaction
          GOTO      PINV8000 IF EQUAL
.
          MATCH     "A",UPDTTYPE            * Add
          GOTO      PINV1000 IF EQUAL
.
          MATCH     "U",UPDTTYPE            * Update
          GOTO      PINV2000 IF EQUAL
.
          MATCH     "D",UPDTTYPE            * Delete
          GOTO      PINV3000 IF EQUAL
.
          MATCH     "E",UPDTTYPE            * Update Provider Admission
          GOTO      PINV4000 IF EQUAL
.
          MATCH     "S",UPDTTYPE            * Schedule Export Contract Adm 
          GOTO      PINV5000 IF EQUAL
.
          GOTO      PINV9100
.
.         ************ ADD FORMACTION **********
.
PINV1000  CALL      CLNZRP00
.
          MOVE      "##",KEY1
          SCAN      KEY1,NZRPI003
          GOTO      PINV9400 IF EQUAL
          MOVE      "&",KEY1
          SCAN      KEY1,NZRPI003
          GOTO      PINV9400 IF EQUAL
.
          PACK      KEY36,NZRPI001,NZRPI002,NZRPI003,SP70
          CALL      RDNZRPI1
          COMPARE   ZERO,OVRCD
          GOTO      PINV9300 IF EQUAL
.
          CALL      MVNZRP00
.
          PACK      NZRPCDAT,CCC,CYY,CMM,CDD
          REP       " 0",NZRPCDAT                * Systen date
          CLOCK     TIME,NZRPCTIM                * System time
          MOVE      USERID,NZRPCUID              * web user id
          MOVE      SP70,NZRPUDAT
          MOVE      SP70,NZRPUTIM
          MOVE      SP70,NZRPUUID
.
          CALL      PEDT0000                     * Check edits
          BRANCH    EXIT,PINV9999
.
          MOVE      "112",PRXCODE      * System Lock Sector 112
          CALL      GETSLK00           * Get System Lock of Sector 112
          READ      CONTROLF,HUND12;*11,PTCNRPIS
.
          MOVE      ZERO,FORM8
          SQUEEZE   PTCNRPIS
          MOVE      PTCNRPIS,FORM8
          ADD       ONE,FORM8
          MOVE      FORM8,PTCNRPIS
.
          WRITAB    CONTROLF,HUND12;*11,PTCNRPIS
          CALL      RELSLK00           * Release System Lock of Sector 112
.
          SUB       ONE,FORM8
          MOVE      FORM8,PTCNRPIS
          MOVE      PTCNRPIS,NZRPRPIS  * got next RPI sequence
.
          CALL      WRNZRPI1
.
          PACK      KEY22,NZRPI001,NZRPI002,SP5,ZERO,SP70 
          CALL      RDNZPIC1
          BRANCH    OVRCD,PINV9200
.         
          MATCH     SP70,NZPICAPS
          IF        @EQUAL
            MOVE      ONE,NZPICAPS          * Set status to processing
          ENDIF
.
          CALL      UPNZPIC1
.
          MOVE      ZERO,EXIT
          GOTO      PINV9999
.
.         ************ UPDATE FORMACTION **********
.
PINV2000  PACK      KEY36,NZRPI001,NZRPI002,NZRPI003,SP70
          CALL      RDNZRPI1
          BRANCH    OVRCD,PINV9200
.
          CALL      MVNZRP00
.
          PACK      NZRPUDAT,CCC,CYY,CMM,CDD
          REP       " 0",NZRPUDAT                * Systen date
          CLOCK     TIME,NZRPUTIM                * System time
          MOVE      USERID,NZRPUUID              * web user id
.
          CALL      UPNZRPI1
.
          MOVE      ZERO,EXIT
          GOTO      PINV9999
.
.         ************ DELETE FORMACTION **********
.
PINV3000  PACK      KEY36,NZRPI001,NZRPI002,NZRPI003,SP70
          CALL      RDNZRPI1
          BRANCH    OVRCD,PINV9200
.         
          CALL      DENZRPI1
.         
          PACK      KEY36,NZRPI001,NZRPI002,SP70
          CALL      RSNZRPI1
          CALL      RKNZRPI1
          BRANCH    OVRCD,PINV3500
.  
          MATCH     NZRPI001,NZRPADMN
          GOTO      PINV3500 IF NOT EQUAL
.
          MATCH     NZRPI002,NZRPINVN
          GOTO      PINV3500 IF NOT EQUAL
.
          GOTO      PINV9999
.
PINV3500  PACK      KEY22,NZRPI001,NZRPI002,SP5,ZERO,SP70
          CALL      RDNZPIC1
          BRANCH    OVRCD,PINV9200               
.         
          MOVE      SP70,NZPICAPS          * Set status to null
.         
          CALL      UPNZPIC1
.
          MOVE      ZERO,EXIT
          GOTO      PINV9999
.
.         ************ UPDATE PROVIDER CONTRACT ADMISSION FORMACTION **********
.
PINV4000  PACK      KEY22,ADMISSNO,INVOICNO,SP5,ZERO,SP70
          CALL      RDNZPIC1
          BRANCH    OVRCD,PINV9200
.
          MATCH     Z70,CAPTSTAT
          IF        !@EQUAL
            MOVE      CAPTSTAT,NZPICAPS
          ENDIF
.
          PACK      NZPIUDAT,CCC,CYY,CMM,CDD
          REP       " 0",NZPIUDAT                * Systen date
          CLOCK     TIME,NZPIUTIM                * System time
          MOVE      USERID,NZPIUUID              * web user id
.         
          CALL      REDT0000                     * Check edits
          BRANCH    EXIT,PINV9999
.
          CALL      UPNZPIC1
.         
          MOVE      ZERO,EXIT
          GOTO      PINV9999
.
.
.         ************ SCHEDULE EXPORT CONTRACT ADMISSION FORMACTION **********
.
PINV5000  CALL      EXPT0000
.
          MOVE      ZERO,EXIT
          GOTO      PINV9999
.
.         ************ DISPLAY FORMACTION **********
.
PINV8000  PACK      KEY22,ADMISSNO,INVOICNO,SP5,ZERO,SP70
          CALL      RDNZPIC1
          IF        OVRCD=1
            CALL      CLNZPI00
          ENDIF
.
          PACK      KEY36,NZRPI001,NZRPI002,NZRPI003,SP70
          CALL      RDNZRPI1
          IF        OVRCD=1
            CALL      CLNZRP00
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Provider Invoice Processing",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00                * Output File & Write HTML Pg Header
          BRANCH    EXIT,PINV9999
.
          CALL      WEBBOD00                * Process Web Body
          CALL      WEBEND00                * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      PINV9999
.
.         ************ DISPLAY ERROR MESSAGE **********
.
PINV9100  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      PINV9999
.
PINV9200  MOVE      "Record does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      PINV9999
.
PINV9300  MOVE      "Record already exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      PINV9999
.
PINV9400  MOVE      "Can NOT have '##' or '&' in RP Invoice No. field",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      PINV9999
.
PINV9999  RETURN
.
.------------------------------------------------------------------------------
. Provider Invoice Processing Edits
.------------------------------------------------------------------------------
PEDT0000  COMPARE   ZERO,NZRPAMNT
          GOTO      PEDT9000 IF EQUAL
.
          MATCH     SP70,NZRPI007
          GOTO      PEDT9100 IF EQUAL
.
          MATCH     Z70,NZRPI007
          GOTO      PEDT9100 IF EQUAL
.
          MATCH     SP70,NZRPI006
          GOTO      PEDT9300 IF EQUAL
.
          MATCH     Z70,NZRPI006
          GOTO      PEDT9300 IF EQUAL
.
          PACK      KEY3,NZRPI006,SP70
          CALL      RDNZPTY1
          BRANCH    OVRCD,PEDT9400
.
          MATCH     SP70,NZPTPIND
          IF        @EQUAL
            PACK      KEY10,NZRPI007,SP70
            CALL      RDPMHCP1
            BRANCH    OVRCD,PEDT9200
.
            MATCH     NZRPI006,PMHCPTYP
            GOTO      PEDT9400 IF NOT EQUAL
          ENDIF
.
PEDT5000  MOVE      ZERO,EXIT
          GOTO      PEDT9999
.
PEDT9000  MOVE      "RP invoice amount must be greater than zero",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      PEDT9999
.
PEDT9100  MOVE      "Provider code is a mandatory field",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      PEDT9999
.
PEDT9200  MOVE      "Invalid provider code",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
.
PEDT9300  MOVE      "Provider type is a mandatory field",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      PEDT9999
.
PEDT9400  MOVE      "Invalid provider type for this provider",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      PEDT9999
.
PEDT9999  RETURN
.
.------------------------------------------------------------------------------
. Provider Invoice Contract Processing Edits
.------------------------------------------------------------------------------
REDT0000  MATCH     "2",NZPICAPS
          GOTO      REDT2000 IF NOT EQUAL
.
          CALL      IBAL0000                     * Check funder paid status
.
          COMPARE   ONE,FUNDPAID
          IF        !@EQUAL
            CLEAR     WEBTITLE
            APPEND    " Funder paid must be yes for a capture status",WEBTITLE
            APPEND    " of complete",WEBTITLE
            RESET     WEBTITLE
            CALL      WEBERR00
            GOTO      REDT9000
          ENDIF
.
          PACK      KEY36,NZPIADMN,NZPIINVN,SP70
          CALL      RSNZRPI1
REDT1000  CALL      RKNZRPI1
          BRANCH    OVRCD,REDT2000
.         
          MATCH     NZPIADMN,NZRPADMN
          GOTO      REDT2000 IF NOT EQUAL
.         
          MATCH     NZPIINVN,NZRPINVN
          GOTO      REDT2000 IF NOT EQUAL
.
          MATCH     "2",NZRPAPIS
          GOTO      REDT1000 IF EQUAL
.
          CLEAR     WEBTITLE
          APPEND    " Admission/Contract can't be finalised, not all",WEBTITLE
          APPEND    " related provider invoices have been paid",WEBTITLE
          RESET     WEBTITLE
          CALL      WEBERR00
          GOTO      REDT9000
.
REDT2000
          MOVE      ZERO,EXIT
          GOTO      REDT9999
.
REDT9000  MOVE      ONE,EXIT
          GOTO      REDT9999
.
REDT9999  RETURN
.
.------------------------------------------------------------------------------
. Related Provider invoice Tags
.------------------------------------------------------------------------------
RTAG0000  BRANCH    FLDITMNO,RTAG0100,RTAG0200,RTAG0300,RTAG0400,RTAG0500:
                             RTAG0600,RTAG0700,RTAG0800,RTAG0900,RTAG1000:
                             RTAG1100,RTAG1200,RTAG1300,RTAG1400
.
          GOTO      RTAG9999
.
RTAG0100  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,RTAG0110
.
          PACK      KEY10,FILTPRAC,SP70
          CALL      RDPMHCP1
          BRANCH    OVRCD,RTAG9999
.
          MOVE      PMHCTITL,FMTTITLE
          MOVE      PMHCGNAM,FMTGNAME
          MOVE      PMHCSNAM,FMTSNAME
          CALL      DOCNM000
          WRITE     HTMLFILE;DOCFNAME;
          GOTO      RTAG9999
.
RTAG0110  WRITE     HTMLFILE;FILTPRAC;
          GOTO      RTAG9999
.
RTAG0200  MATCH     FILTFDAT,SP70
          GOTO      RTAG9999 IF EQUAL
.
          UNPACK    FILTFDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      RTAG9999
.
RTAG0300  MATCH     FILTTDAT,SP70
          GOTO      RTAG9999 IF EQUAL
.
          UNPACK    FILTTDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      RTAG9999
.
RTAG0400  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,RTAG4010
.
          MATCH     SP70,FILTCONT
          GOTO      RTAG9999 IF EQUAL
.
          WRITE     HTMLFILE;FILTCONT;
          GOTO      RTAG9999
.
RTAG4010  PACK      KEY14,FILTCONT,ZERO,ZERO,ZERO,ZERO,SP70
          CALL      RDFUND1
          IF        OVRCD=0
            WRITE     HTMLFILE;HFNAME;
          ELSE
            WRITE     HTMLFILE;FILTCONT;
          ENDIF
          GOTO      RTAG9999
.
RTAG0500  WRITE     HTMLFILE;FILTSTAT;
          GOTO      RTAG9999
.
RTAG0600  CALL      RTAB0000           * List related provider contract
          GOTO      RTAG9999           * admissions
.
RTAG0700  WRITE     HTMLFILE;ADMISSNO;
          GOTO      RTAG9999
.
RTAG0800  WRITE     HTMLFILE;INVOICNO;
          GOTO      RTAG9999
.
RTAG0900  CALL      RDET0000
          GOTO      RTAG9999
.
RTAG1000  CALL      RINV0000
          GOTO      RTAG9999
.
RTAG1100  WRITE     HTMLFILE;NZPICAPS;
          GOTO      RTAG9999
.
RTAG1200  MATCH     SP70,FILTHOSP
          IF        @EQUAL
            IF        FILTFLAG=0
              MOVE      WBSEHOSP,FILTHOSP
            ENDIF
          ENDIF
          CALL      HSEL0000
          GOTO      RTAG9999
.
RTAG1300  WRITE     HTMLFILE;FILTHOSP;
          GOTO      RTAG9999
.
RTAG1400  WRITE     HTMLFILE;FILTFLAG;
          GOTO      RTAG9999
.
RTAG9999  RETURN
.
.------------------------------------------------------------
. List of related provider contract admissions
.------------------------------------------------------------
RTAB0000  CALL      CRTM0000            * create temp table used in this routine
.
          MATCH     SP70,FILTFDAT
          GOTO      RTAB9999 IF EQUAL
.
          MATCH     SP70,FILTTDAT
          GOTO      RTAB9999 IF EQUAL
.
          MATCH     SP70,FILTCONT
          GOTO      RTAB0500 IF NOT EQUAL
.
          MATCH     SP70,FILTPRAC
          GOTO      RTAB0500 IF NOT EQUAL
.
          GOTO      RTAB9999
.
RTAB0500  CALL      DTMP0000                     * Delete records from temp file
.
          PACK      KEY30,FILTFDAT,SP70
          CALL      RSNZPIC2
RTAB1000  CALL      RKNZPIC2
          BRANCH    OVRCD,RTAB5000
.         
          MATCH     NZPIPDAT,FILTTDAT            * To date filter
          GOTO      RTAB5000 IF LESS
.
          PACK      KEY8,NZPIINVN,SP70
          CALL      RDPTINV1
          BRANCH    OVRCD,RTAB1000
.
          CALL      CHKR0000                     * Always display if related
          BRANCH    EXIT,RTAB1050                * provider invoices exits
.
          MATCH     SP70,PTINHFND                * Only display funder
          GOTO      RTAB1000 IF EQUAL            * invoices
.
          MATCH     "1",PTINCRST                 * Dont display fully credited
          GOTO      RTAB1000 IF EQUAL
.
RTAB1050  MATCH     SP70,FILTCONT                * Contract filter
          IF        !@EQUAL
            MATCH     FILTCONT,NZPICNTR
            GOTO      RTAB1000 IF NOT EQUAL
          ENDIF
.
          PACK      KEY8,NZPIADMN,SP70
          CALL      RDPMVX11
          BRANCH    OVRCD,RTAB1000
.
          IF        IBCNMHOS=1
            MATCH     SP70,FILTHOSP
            IF        !@EQUAL
              MATCH     FILTHOSP,PMVXMHOS
              GOTO      RTAB1000 IF NOT EQUAL
            ELSE
              PACK      KEY13,USERID,SP70              * All hospital access
              CALL      RDMLSEC1
              IF        OVRCD=1
                PACK      KEY13,USERID,PMVXMHOS,SP70   * This hospital access
                CALL      RDMLSEC1
                BRANCH    OVRCD,RTAB1000
              ENDIF
            ENDIF
          ENDIF
.
          MATCH     SP70,FILTPRAC                * Practitioner filter
          IF        !@EQUAL
            MATCH     FILTPRAC,NZPISURG          * Surgeon
            GOTO      RTAB1100 IF EQUAL
.
            MATCH     FILTPRAC,NZPIANAE          * Anaesthetist
            GOTO      RTAB1100 IF EQUAL
.
            GOTO      RTAB1000 
          ENDIF
.
RTAB1100  MOVE      NZPIADMN,TEMPADMN
          MOVE      NZPIINVN,TEMPINVN
          PACK      KEY16,TEMPADMN,TEMPINVN,SP70
          CALL      RDTEMP1
          IF        OVRCD=1
            CALL      WRTEMP1
          ENDIF
.
          GOTO      RTAB1000
.
RTAB5000  PACK      KEY16,SP70
          CALL      RSTEMP1
RTAB6000  CALL      RKTEMP1
          BRANCH    OVRCD,RTAB9500
.
          PACK      KEY22,TEMPADMN,TEMPINVN,SP70
          CALL      RSNZPIC1
          CALL      RKNZPIC1
          BRANCH    OVRCD,RTAB6000
.
          MATCH     TEMPADMN,NZPIADMN
          GOTO      RTAB6000 IF NOT EQUAL
.
          MATCH     TEMPINVN,NZPIINVN
          GOTO      RTAB6000 IF NOT EQUAL
.
          MATCH     "     0",NZPIUNIQ            * Header records only
          GOTO      RTAB6000 IF NOT EQUAL
.
          MATCH     SP70,FILTSTAT                * Status filter
          IF        !@EQUAL
            MATCH     FILTSTAT,NZPICAPS
            GOTO      RTAB6000 IF NOT EQUAL
          ENDIF
.
          PACK      SAVKEY22,NZPIADMN,NZPIINVN,NZPIUNIQ,SP70
.
          PACK      KEY8,NZPIURNO,SP70
          CALL      RDMAST1
          IF        OVRCD=1
            CALL      CLPATMAS
          ENDIF
.
          CALL      RDPMPX21
          IF        OVRCD=1
            CALL      CLPMSPX2
          ENDIF
.
          PACK      AGEDATE,CCC,CYY,CMM,CDD    * Today's date
          REP       " 0",AGEDATE
          CALL      CALCAGE
          CALL      PATLN000
          CALL      PATFLD00                * Set Patient Folder suffix
.
          CALL      IBAL0000                     * Check funder paid status
.
          MOVE      DNO,DISPFPAY
          IF        FUNDPAID=1
            IF        FULLYCRD=1
              MOVE      "<font color=red>Credited</font>",DISPFPAY
            ELSE
              MOVE      DYES,DISPFPAY
            ENDIF
          ENDIF
.
          MOVE      SP70,DISPCAPS
          MATCH     "1",NZPICAPS
          IF        @EQUAL
            MOVE      "Processing",DISPCAPS
          ENDIF
          MATCH     "2",NZPICAPS
          IF        @EQUAL
            MOVE      "Complete",DISPCAPS
          ENDIF
          MATCH     "3",NZPICAPS
          IF        @EQUAL
            MOVE      "Finalised",DISPCAPS
          ENDIF
.
          CLEAR     HTMLLINK
          APPEND    "javascript:UpdateDetails('",HTMLLINK
          APPEND    NZPIADMN,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    NZPIINVN,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
          CLEAR     UPDTLINK
          APPEND    "javascript:SelectPatient('",UPDTLINK
          APPEND    NZPIADMN,UPDTLINK
          APPEND    "','",UPDTLINK
          APPEND    NZPIURNO,UPDTLINK
          APPEND    "')",UPDTLINK
          RESET     UPDTLINK
.
          WRITE     HTMLFILE;"t.addRow(#"",UPDTLINK,"#",":  *0
                             "#"",FORMATNM,"#",":     *1        
                             "#"",KEY3,"#",":         *2
                             "#"",NZPIADMN,"#",":     *3
                             "#"",NZPIINVN,"#",":     *4
                             "#"",NZPIUNIQ,"#",":     *5 
                             "#"",NZPIURNO,"#",":     *6
                             "#"",NZPICNTR,"#",":     *7
                             "#"",NZPICLMN,"#",":     *8
                             "#"",NZPISURG,"#",":     *9
                             "#"",DISPFPAY,"#",":     *10
                             "#"",DISPCAPS,"#",";     *11
.
          MATCH    "1",NZPIEXPT
          IF       @EQUAL
            WRITE     HTMLFILE;"#"<input type=checkbox name=nzpickey":
                      " onclick=Export(this) checked value='",SAVKEY22,"'>":
                      "#",";
          ELSE
            WRITE     HTMLFILE;"#"<input type=checkbox name=nzpickey":
                      " onclick=Export(this) value='",SAVKEY22,"'>";
.
            MATCH    "2",NZPIEXPT
            IF       @EQUAL
              WRITE     HTMLFILE;"Exported";
            ENDIF
            WRITE     HTMLFILE;"#",";
          ENDIF
.
          WRITE     HTMLFILE;"#"",HTMLLINK,"#",";     *13
.
RTAB7000  WRITE     HTMLFILE;"#"";
.
          PACK      KEY22,TEMPADMN,TEMPINVN,SP70
          CALL      RSNZPIC1
RTAB7100  CALL      RKNZPIC1
          BRANCH    OVRCD,RTAB8000
.
          MATCH     TEMPADMN,NZPIADMN
          GOTO      RTAB8000 IF NOT EQUAL
.
          MATCH     TEMPINVN,NZPIINVN
          GOTO      RTAB8000 IF NOT EQUAL
.
          MATCH     "     0",NZPIUNIQ            * Header records only
          GOTO      RTAB7100 IF EQUAL
.
          PACK      KEY9,NZPICPRC,SP70
          CALL      RDPTCMC1
          IF        OVRCD=1
            MOVE      NZPICPRC,PTCADES1
          ENDIF
.
          WRITE     HTMLFILE;NZPICPRC," ",PTCADES1,"<br>";     *14
          GOTO      RTAB7100
.
RTAB8000  WRITE     HTMLFILE;"#",#"";
.
          PACK      KEY22,TEMPADMN,TEMPINVN,SP70
          CALL      RSNZPIC1
RTAB8100  CALL      RKNZPIC1
          BRANCH    OVRCD,RTAB9000
.
          MATCH     TEMPADMN,NZPIADMN
          GOTO      RTAB9000 IF NOT EQUAL
.
          MATCH     TEMPINVN,NZPIINVN
          GOTO      RTAB9000 IF NOT EQUAL
.
          MATCH     "     0",NZPIUNIQ            * Header records only
          GOTO      RTAB8100 IF EQUAL
.
          MATCH     SP70,NZPIPDAT
          IF        @EQUAL
            MOVE    "&nbsp;",DISPDATE
          ELSE
            UNPACK    NZPIPDAT,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      DISPDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          ENDIF
.
          WRITE     HTMLFILE;DISPDATE,"<br>";    *15
.
          GOTO      RTAB8100
.
RTAB9000  WRITE     HTMLFILE;"#",":
                             "#"",INVOICON,"#");"
          GOTO      RTAB6000
.
RTAB9500  PACK      KEY22,NZRPI001,NZRPI002,SP5,ZERO,SP70
          CALL      RDNZPIC1
          IF        OVRCD=1
            CALL      CLNZPI00
          ENDIF
.
RTAB9999  CALL      KILL0000              * remove temporary table
          RETURN
.
.------------------------------------------------------------------
. Check if there are any related provider invoices for this invoice
.------------------------------------------------------------------
CHKR0000  PACK      KEY36,NZPIADMN,NZPIINVN,SP70
          CALL      RSNZRPI1
CHKR1000  CALL      RKNZRPI1
          BRANCH    OVRCD,CHKR9000
.
          MATCH     NZPIADMN,NZRPADMN
          GOTO      CHKR9000 IF NOT EQUAL
.
          MATCH     NZPIINVN,NZRPINVN
          GOTO      CHKR9000 IF NOT EQUAL
.
          MOVE      ONE,EXIT
          GOTO      CHKR9999
.
CHKR9000  MOVE      ZERO,EXIT
.
CHKR9999  RETURN
.------------------------------------------------------------
. Delete all records from the temp file
.------------------------------------------------------------
DTMP0000  PACK      KEY16,SP70
          CALL      RSTEMP1
          CALL      RKTEMP1
          BRANCH    OVRCD,DTMP9999
.
          PACK      KEY16,TEMPADMN,TEMPINVN,SP70
          CALL      DETEMP1
.
          GOTO      DTMP0000
.
DTMP9999  RETURN
.
.------------------------------------------------------------
. Open and create the temporary file 
.------------------------------------------------------------
CRTM0000  CALL      KILL0000
.
CRTM1000  CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME1,CMDLINE
          APPEND    " 17 U1-8,9-16",CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPFILE,FNAME1
.
CRTM9999  RETURN
.
.****************************************************************************
.*                              KILL0000               Called by:           *
.*               close and erase the temporary file                         *
.****************************************************************************
.
KILL0000  CLOSE     TEMPFILE                     * close file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ERASE,FNAME1         * set file erase command
          EXECUTE   CMDLINE,TASKID               * erase temp file
.
KILL9999 RETURN
+
.------------------------------------------------------------
. List of related provider contract admissions
.------------------------------------------------------------
RDET0000  PACK      KEY22,ADMISSNO,INVOICNO,SP70
          CALL      RSNZPIC1
RDET1000  CALL      RKNZPIC1
          BRANCH    OVRCD,RDET9999
.
          MATCH     ADMISSNO,NZPIADMN
          GOTO      RDET9999 IF NOT EQUAL
.
          MATCH     INVOICNO,NZPIINVN
          GOTO      RDET9999 IF NOT EQUAL
.
          MATCH     "     0",NZPIUNIQ            * Header records only
          GOTO      RDET9999 IF NOT EQUAL
.
          PACK      KEY8,NZPIURNO,SP70
          CALL      RDMAST1
          IF        OVRCD=1
            CALL      CLPATMAS
          ENDIF
.
          CALL      RDPMPX21
          IF        OVRCD=1
            CALL      CLPMSPX2
          ENDIF
.
          PACK      AGEDATE,CCC,CYY,CMM,CDD    * Today's date
          REP       " 0",AGEDATE
          CALL      CALCAGE
          CALL      PATLN000
          CALL      PATFLD00                * Set Patient Folder suffix
.
          CALL      IBAL0000                     * Check funder paid status
.
          MOVE      DNO,DISPFPAY
          IF        FUNDPAID=1
            IF        FULLYCRD=1
              MOVE      "<font color=red>Credited</font>",DISPFPAY
            ELSE
              MOVE      DYES,DISPFPAY
            ENDIF
          ENDIF
.
          MOVE      SP70,DISPCAPS
          MATCH     "1",NZPICAPS
          IF        @EQUAL
            MOVE      "Processing",DISPCAPS
          ENDIF
          MATCH     "2",NZPICAPS
          IF        @EQUAL
            MOVE      "Complete",DISPCAPS
          ENDIF
          MATCH     "3",NZPICAPS
          IF        @EQUAL
            MOVE      "Finalised",DISPCAPS
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td>",FORMATNM,"</td>":
                             "<td>",NZPIINVN,"</td>":
                             "<td>",NZPICNTR,"</td>";
.
RDET7000  WRITE     HTMLFILE;"<td>";
.
          PACK      KEY22,ADMISSNO,INVOICNO,SP70
          CALL      RSNZPIC1
RDET7100  CALL      RKNZPIC1
          BRANCH    OVRCD,RDET8000
.
          MATCH     ADMISSNO,NZPIADMN
          GOTO      RDET8000 IF NOT EQUAL
.
          MATCH     INVOICNO,NZPIINVN
          GOTO      RDET8000 IF NOT EQUAL
.
          MATCH     "     0",NZPIUNIQ            * Header record
          GOTO      RDET7100 IF EQUAL
.
          PACK      KEY9,NZPICPRC,SP70
          CALL      RDPTCMC1
          IF        OVRCD=1
            MOVE      NZPICPRC,PTCADES1
          ENDIF
.
          WRITE     HTMLFILE;NZPICPRC," ",PTCADES1,"<br>"; 
          GOTO      RDET7100
.
RDET8000  WRITE     HTMLFILE;"</td><td>";
.
          PACK      KEY22,ADMISSNO,INVOICNO,SP70
          CALL      RSNZPIC1
RDET8100  CALL      RKNZPIC1
          BRANCH    OVRCD,RDET9000
.
          MATCH     ADMISSNO,NZPIADMN
          GOTO      RDET9000 IF NOT EQUAL
.
          MATCH     INVOICNO,NZPIINVN
          GOTO      RDET9000 IF NOT EQUAL
.
          MATCH     "     0",NZPIUNIQ            * Header record
          GOTO      RDET8100 IF EQUAL
.
          MATCH     SP70,NZPIPDAT
          IF        @EQUAL
            MOVE    "&nbsp;",DISPDATE
          ELSE
            UNPACK    NZPIPDAT,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      DISPDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          ENDIF
.
          WRITE     HTMLFILE;DISPDATE,"<br>";
.
          GOTO      RDET8100
.
RDET9000  PACK      KEY22,ADMISSNO,INVOICNO,SP70
          CALL      RSNZPIC1
RDET9100  CALL      RKNZPIC1
          BRANCH    OVRCD,RDET9999
.
          MATCH     ADMISSNO,NZPIADMN
          GOTO      RDET9999 IF NOT EQUAL
.
          MATCH     INVOICNO,NZPIINVN
          GOTO      RDET9999 IF NOT EQUAL
.
          MATCH     "     0",NZPIUNIQ            * Header record
          GOTO      RDET9999 IF NOT EQUAL
.
          WRITE     HTMLFILE;"</td>":
                             "<td>",DISPFPAY,"</td>";
.
          WRITE     HTMLFILE;"<td><select name=captstat class=SelectMed";
.
          MATCH     "3",NZPICAPS
          IF        @EQUAL
            WRITE     HTMLFILE;" disabled>";
          ELSE
            WRITE     HTMLFILE;">";
          ENDIF
          WRITE     HTMLFILE;"<option value=#"#"></option>";
          MATCH     "1",NZPICAPS
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=#"1#" selected>Processing</option>";
          ELSE
            WRITE     HTMLFILE;"<option value=#"1#">Processing</option>";
          ENDIF
.
          MATCH     "2",NZPICAPS
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=#"2#" selected>Completed</option>";
          ELSE
            WRITE     HTMLFILE;"<option value=#"2#">Completed</option>";
          ENDIF
.
          MATCH     "3",NZPICAPS
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=#"3#" selected>Finalised</option>";
          ELSE
            WRITE     HTMLFILE;"<option value=#"3#">Finalised</option>";
          ENDIF
.
          WRITE     HTMLFILE;"</select></td></td>"
.
RDET9999  RETURN
.
.------------------------------------------------------------
. List of related provider invoices
.------------------------------------------------------------
RINV0000  PACK      KEY36,ADMISSNO,INVOICNO,SP70
          CALL      RSNZRPI1
RINV1000  CALL      RKNZRPI1
          BRANCH    OVRCD,RINV9999
.
          MATCH     ADMISSNO,NZRPADMN
          GOTO      RINV9999 IF NOT EQUAL
.
          MATCH     INVOICNO,NZRPINVN
          GOTO      RINV9999 IF NOT EQUAL
.
          MOVE      SP70,TDESC
          MATCH     SP70,NZRPPTYP
          IF        !@EQUAL
            PACK      KEY5,CATPT,NZRPPTYP,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      NZRPPTYP,TDESC
            ENDIF
          ENDIF
.
          MOVE     "Unsent",DISPAPIS
          MATCH    "1",NZRPAPIS
          IF       @EQUAL
            MOVE     "Sent",DISPAPIS
          ENDIF
          MATCH    "2",NZRPAPIS
          IF       @EQUAL
            MOVE     "Paid",DISPAPIS
          ENDIF
          MATCH    "3",NZRPAPIS
          IF       @EQUAL
            MOVE     "Info",DISPAPIS
          ENDIF
.
          PACK      KEY10,NZRPPCOD,SP70
          CALL      RDPMHCP1
          IF        OVRCD=1
            MOVE      NZRPPCOD,DOCFNAME
          ELSE
            MOVE      PMHCTITL,FMTTITLE
            MOVE      PMHCGNAM,FMTGNAME
            MOVE      PMHCSNAM,FMTSNAME
            CALL      DOCNM000
          ENDIF
.
          MOVE      SP70,WBSENAM
          MATCH     SP10,NZRPCUID                 * Web User Id
          IF        !@EQUAL
            PACK      KEY10,NZRPCUID,SP70
            CALL      RDWBSE1
            IF        OVRCD=1
              PACK     WBSENAM,SP70
            ENDIF
          ENDIF
.
          CLEAR     HTMLLINK
          APPEND    "javascript:UpdRPInvoice('",HTMLLINK
          APPEND    NZRPADMN,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    NZRPINVN,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    NZRPRINV,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
          WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":  *0
                             "#"",NZRPRINV,"#",":     *1
                             "#"",INVOICON,"#",":     *2
                             "#"",NZRPDATE,"#",":     *3
                             "#"",NZRPAMNT,"#",":     *4
                             "#"",TDESC,"#",":        *5
                             "#"",DOCFNAME,"#",":     *6
                             "#"",DISPAPIS,"#",":     *7
                             "#"",NZRPAPDT,"#",":     *8
                             "#"",NZRPCDAT,"#",":     *9
                             "#"",WBSENAM,"#");";     *10
          GOTO      RINV1000
.
RINV9999  RETURN
.
.--------------------------------------------------------------------------
.*                             EXPT0000                                   *
.*   Schedule export of contract admission details                        *
.--------------------------------------------------------------------------
EXPT0000  MOVE      "IBAPMS42",SCHDPGID
          MOVE      "Export RP Contract Admission Information",SCHDDESC
          PACK      SCHDDATE,CCC,CYY,CMM,CDD
          REP       " 0",SCHDDATE
          CLOCK     TIME,SCHDTIME
.
.         Write Keyin parameters for Extract
.
          MOVE      THREE,KEYSTARR[1]            * Option 3 
          MOVE      FILTCONT,KEYSTARR[2]         
.
          UNPACK    FILTFDAT,CCENT,CYEAR,CMON,CDAY
          PACK      KEYSTARR[3],CDAY,CMON,CCENT,CYEAR     * from Date
.
          UNPACK    FILTTDAT,CCENT,CYEAR,CMON,CDAY
          PACK      KEYSTARR[4],CDAY,CMON,CCENT,CYEAR     * from Date
.
          MOVE      FILTPRAC,KEYSTARR[5]         
          MOVE      FILTSTAT,KEYSTARR[6]         
.
          MOVE      ANSY,KEYSTARR[7]         
          MOVE      SP70,KEYSTARR[8]         
          MOVE      EIGHT,KEYSTCNT               * Number of Keyins
.
          CALL      SCHPRO00   * Schedule Extract
.
EXPT9999  RETURN
.
.------------------------------------------------------------
. Hospital Selection
.------------------------------------------------------------
HSEL0000  PACK      KEY3,SP70
          CALL      RSPTHSP1
HSEL0100  CALL      RKPTHSP1
          BRANCH    OVRCD,HSEL9999
.
          PACK      KEY13,USERID,SP70                * All hospital access
          CALL      RDMLSEC1
          IF        OVRCD=1
            PACK      KEY13,USERID,PTHSHOSP,SP70     * This hospital access
            CALL      RDMLSEC1
            BRANCH    OVRCD,HSEL0100
          ENDIF
.
          MOVE      PTHSNAME,KEY50
          PACK      KEY5,QUOTE,PTHSHOSP,QUOTE
.
          MATCH     FILTHOSP,PTHSHOSP
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=",KEY5," selected>":
                               KEY50,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=",KEY5,">",KEY50,"</option>"
          ENDIF
.
          GOTO      HSEL0100
.
HSEL9999  RETURN
.
.------------------------------------------------------------
. Check if an invoice has a zero balance.
. Returns FUNDPAID = 0 - Funder NOT paid invoice balance not 0
.                    1 - Funder PAID invoice balance = 0
.         FULLYCRD = 0 - Invoice not fully credited
.                    1 - Invoice fully credited
.------------------------------------------------------------
IBAL0000  MOVE      ZERO,FUNDPAID
          MOVE      ZERO,FULLYCRD
.
          PACK      KEY8,NZPIINVN,SP70
          CALL      RDPTINV1
          BRANCH    OVRCD,IBAL9999
.
          MOVE      PINVAMT,FORM12P2
          ADD       PINVPATA,FORM12P2
          ADD       PINVHFDA,FORM12P2
          ADD       PINVOTHA,FORM12P2
          ADD       PINVJOUR,FORM12P2
          ADD       PTINCRTT,FORM12P2
          IF        IBCNUGST=2 | IBCNUGST=3
            ADD       PTINGSTJ,FORM12P2
          ENDIF
.
          MATCH     "1",PTINCRST
          IF        @EQUAL
            MOVE      ONE,FULLYCRD        * invoice fully credited
          ENDIF
.
          COMPARE   ZERO,FORM12P2          * invoice balanced ?
          GOTO      IBAL9999 IF NOT EQUAL
.
          MOVE      ONE,FUNDPAID             * Funder paid yes
          GOTO      IBAL9999
.
IBAL9999  RETURN
.
.------------------------------------------------------------------------------
. Temp File I/O
.------------------------------------------------------------------------------
RSTEMP1   RESET     KEY16
          READ      TEMPFILE,KEY16;;
          RETURN    
.                            
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMPFILE;TEMPADMN,TEMPINVN
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY16
          READ      TEMPFILE,KEY16;TEMPADMN,TEMPINVN
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   WRITE     TEMPFILE,KEY16;TEMPADMN,TEMPINVN
          RETURN
.
DETEMP1   RESET     KEY16
          DELETE    TEMPFILE,KEY16
          RETURN
.------------------------------------------------------------
. NOMORE00 - Display "No more records" at end of list
.------------------------------------------------------------
NOMORE00  WRITE     HTMLFILE;"<tr>":
                             "<td class=RedCell colspan=20 align=center>":
                             "No more records</td>"
.
NOMORE99  RETURN
+
.
.------------------------------------------------------------------------------
. I/O and Subroutine Includes
.------------------------------------------------------------------------------
.
          INC       CALCAGE
          INC       CLNZPRPI
          INC       CLNZPPIC
          INC       CLPATMAS
          INC       CLPMSPX2
          INC       IBAWEBCD
          INC       DAYOFWEK
          INC       NAMSTRCD
          INC       PATDOCCD
          INC       PATDOCTM
          INC       PATNAMCD
          INC       PATFAPTM
          INC       PMSPX2TM
          INC       NZPRPITM
          INC       RSHWEBCD
          INC       WEBDATCD
          INC       TFILENAM
          INC       PATIPERH
.
          INC       MLTSECIO/INC
          INC       PATCMCIO/INC
          INC       PATDSCIO/INC   * Patient Discharge file
          INC       PATDO1IO/INC
          INC       PATFN1IO/INC
          INC       PATHSPIO/INC   * Hospital file
          INC       PATINVIO/INC
          INC       PATMA1IO/INC   * Patient Master file
          INC       PATMI1IO/INC   * Patient Admission file
          INC       NZPSPRIO/INC
          INC       NZPPICIO/INC
          INC       NZPRPIIO/INC
          INC       NZPPTYIO/INC
          INC       PATFAPIO/INC
          INC       PATVISIO/INC   * Visit File
          INC       PATWR1IO/INC   * Ward Bed File
          INC       PATONHIO/INC
          INC       PMSHCPIO/INC
          INC       PMSPX2IO/INC
.
          INC       PATCFAIO/INC
          INC       PATIPEIO/INC
          INC       PATFX1IO/INC
          INC       PATFHIIO/INC
          INC       EMRVISIO/INC
          INC       OUTSITIO/INC
          INC       OUTBB1IO/INC
.
          INC       PMSBLIIO/INC   * Bulk Invoice selection List
          INC       PMSBIIIO/INC   * Bulk Invoice List Index
          INC       PMSBBIIO/INC   * Bulk backdate Invoice selection List
          INC       PMSBISIO/INC   * Bulk backdate Invoice List Index
          INC       PMSVX1IO/INC   * Visit Extension File
          INC       PMSCCDIO/INC
+
.------------------------------------------------------------------------------
. End Of Program
.------------------------------------------------------------------------------

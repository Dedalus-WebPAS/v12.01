. *****************************************************************************
. *              P R O G R A M  S U M M A R Y                                 *
. *              -------------  -------------                                 *
. *                                                                           *
. *     PROGRAM NAME:    IBAAAE31                                             *
. *                                                                           *
. *     FUNCTION:        Summary Doctor Revenue Report                        *
. *                                                                           *
. *     DATE WRITTEN:    18/06/2007                                           *
. *                                                                           *
. *     AUTHOR:          PETER VELA                                           *
. *                                                                           *
. *     MODIFICATIONS:                                                        *
. *                     V10.04.02 12/06/2014 Steve Armstrong  CAR 301639      *
. *                               Added call to KILL0000 on exiting the       *
. *                               program.                                    *
. *                     V10.04.01 07/03.2014 Ania P     CAR 261630            *
. *                               Remove the use of PORT for temp file names  *
. *****************************************************************************
. *                      V9.11.01 15/10/2008 J.Tan      CAR 180771            *
. *                               Fixed calculating YTD figures               *
. *                      V9.09.03 18/01/2008 Peter Vela CAR 149792            *
. *                               Modified program to validate by Invoice     *
. *                               date instead of service date                *
. *                      V9.09.02 10/08/2007 Peter Vela CAR 142173            *
. *                               Fixed Doctor initial                        *
. *                      V9.09.01 18/06/2007 Peter Vela CAR 142173            *
. *                               Created Program                             *
. *****************************************************************************
.
          INC       STD001FD
.
          INC       AAEDTRFD/INC
.
          INC       IBASEQFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
          INC       PATDRGFD/INC
          INC       PMSHCPFD/INC
          INC       PATVISFD/INC
          INC       PATCODFD/INC
          INC       PATDO1FD/INC          * doctor file pat
          INC       PATMA1FD/INC          * doctor file pat
          INC       PATINVFD/INC
.
.
CMDLINE   DIM       60
CPTDATE   DIM       8
DIM2A     DIM       2
FNAMET    DIM       8
FROMDATE  DIM       8        * FROMdate ccyymmdd
FROMYTDT  DIM       8
FROMDTRN  DIM       8
NOSERNGE  FORM      3
RESERNGE  FORM      12.2
NOSEYTDT  FORM      3
RESEYTDT  FORM      12.2
.
TEMPFILE  IFILE     SQL,   FIXED=67, KEY="U1-10,11-18,19-27,28-33"
.
TMPDRCD   DIM       10       10       1
TMPINDT   DIM       8        8        11
TMPITNO   DIM       9        9        19
TMPTRNO   DIM       6        6        28
TMPINVN   DIM       8        8        34
TMPVISN   DIM       8        8        42
TMPTAMT   DIM       15       15       50
TMPSRVS   DIM       2        2        65
.
TODATE    DIM       8        * TOdate ccyymmdd
TOYTDT    DIM       8
TOTAMT    FORM      12.2        * Total number of patients
TOTDOC    FORM      12.2
TOSERNGE  FORM      3
TORERNGE  FORM      12.2
TOSEYTDT  FORM      3
TOREYTDT  FORM      12.2
.
CURDATE   DIM      8         * Current Date dd/mm/yy
DIM3      DIM      3         
DIM10     DIM      10
DIM32     DIM      32        * patient name to print
DIM28     DIM      28        * doctor name to print
DOCCODE   DIM       6
DCODE1    DIM       10
DCODE2    DIM       10
.
LSTDOCT   DIM       10
DESCOPT   DIM       23        * option description in heading
.
PTCNDCQS  FORM      1
UNIQUE    FORM      6         * temporary file counter
.
VAR       FORM      1
.
XCENT1    DIM       2
XYEAR1    DIM       2
XMON1     DIM       2
XDAY1     DIM       2
.
PRGID     INIT      "IBAAAE31"
PRGNAM    INIT      "Summary Doctor Revenue Report"
VERSION   INIT      "V12.01.00"
+
.         Start of program
.
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening aaedtref";
          OPEN      AAEDTRE4,"aaedtref"
          OPEN      AAEDTRE6,"aaedtref"
.
          DISPLAY   *P64:24,"pmshcpaf";
          OPEN      PMSHCPA1,"pmshcpaf"
.
          DISPLAY   *P64:24,"pmshcpaf";
          OPEN      PMSHCPA2,"pmshcpaf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA1,"patvisaf"
.
          DISPLAY   *P64:24,"patinvrf";
          OPEN      PATINVR1,"patinvrf"
          OPEN      PATINVR2,"patinvrf"
.
          DISPLAY   *P64:24,"patdrgaf";
          OPEN      PATDRGA1,"patdrgaf"
          OPEN      PATDRGA3,"patdrgaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*2,CDD,CMM,CYY,CCC
          READ      CONTROLF,TWENTY1;*247,PTCNDCQS
.
          CALL      TFILENAM
          PACK      FNAMET,TFILNAME,SP70 
.
.         Main menu
.
START     MOVE      ZERO,CPAGENO
          MOVE      "60",CLNO
          MOVE      SP6,LSTDOCT
          MOVE      ZERO,TOTAMT
          MOVE      ZERO,TOTDOC
          MOVE      ZERO,NOSERNGE
          MOVE      ZERO,RESERNGE
          MOVE      ZERO,NOSEYTDT
          MOVE      ZERO,RESERNGE
          MOVE      ZERO,TOSERNGE
          MOVE      ZERO,TORERNGE
          MOVE      ZERO,TOSEYTDT
          MOVE      ZERO,TOREYTDT
          MOVE      ZERO,RESEYTDT
.
          HLINE     *G37,2,50,80
          DISPLAY   *P39:4,*EF:
                    *P1:5,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:6,*V2LON,ONE,*HOFF," = Run Report"
.
          KEYIN     *P1:11,"Option : ",*V2LON,OPTION
.
          COMPARE   ZERO,OPTION
          GOTO      ENDIT IF EQUAL
.
BRNCH     BRANCH    OPTION OF DDOCT
          BEEP
          GOTO      START
.
DDOCT     DISPLAY   *P50:2,*V2LON,"- Doctor Sequence "
          MOVE      "Doctor Sequence          ",DESCOPT
.
START1    DISPLAY   *P1:12,*EF:
                    *P1:12,"From  Date  :":
                    *P1:13,"To    Date  :"
.
          UNPACK    SP6 INTO CYEAR,CMON,CDAY
.
KIBA25    MOVE      TEN5,CCOL
          MOVE      TEN2,CVERT
.
          MOVE      "40",ECOL
          MOVE      CVERT,EVERT
          MOVE      CCC,CCENT
.
FIXMON    CALL      KEYDATE
          BRANCH    OVRCD OF START
.
          PACK      FROMDATE WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",FROMDATE
.
KIBA26    UNPACK    SP6 INTO CYEAR,CMON,CDAY
.
          MOVE      TEN5,CCOL
          MOVE      TEN3,CVERT
          MOVE      "40",ECOL
          MOVE      CVERT,EVERT
.
          MOVE      CCC,CCENT
.
FIXMON2   CALL      KEYDATE
          BRANCH    OVRCD OF KIBA25
.
          PACK      TODATE WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE
.
          MATCH     FROMDATE,TODATE
          GOTO      ERR1 IF LESS
.
          CALL      YTDRNG00
          BRANCH    EXIT,ENDIT
.
          GOTO      CONFIRM
.
ERR1      DISPLAY   *P1:24,*B,*V2LON:
                    "** FROM Date Greater Than TO Date ** ";
          CALL      HITENTER
          DISPLAY   *P1:24,*EL;
          GOTO      START1
.
.
.
CONFIRM   KEYIN     *P1:15,*EL,"Ok to Continue (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF:
                    ") ? ",*V2LON,ANS;
.
          CMATCH    ANSN,ANS
          GOTO      START IF EQUAL
.
          CMATCH    ANSY,ANS
          GOTO      KSING IF EQUAL
.
          BEEP
          GOTO      CONFIRM
.............................................................................
.         Doctor range
.
KSING     DISPLAY   *P1:15,*EL,"From Doctor Code : "
          MOVE      SP70,DCODE1 
          MOVE      "20",ECOL
          MOVE      "15",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP70,CKYIDEF8
          MOVE      ZERO,CKYINACT
          CALL      PMSHCPKY
          BRANCH    EXIT,KSING1,KSING  
          MOVE      PMHCHCPC,DCODE1
          GOTO      KSING2
.
KSING1    MOVE      SP70,DCODE1
          DISPLAY   *P20:15,*EL,"Start"          
.
KSING2    DISPLAY   *P1:16,*EL,"To Doctor Code : "
          MOVE      SP70,DCODE2
          MOVE      "18",ECOL
          MOVE      "16",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP70,CKYIDEF8 
          MOVE      ZERO,CKYINACT
          CALL      PMSHCPKY
          BRANCH    EXIT,KSING3,KSING
          MOVE      PMHCHCPC,DCODE2
          GOTO      KSING4
.
KSING3    MOVE      Z70,DCODE2
          DISPLAY   *P18:16,*EL,"Finish"  
.
KSING4    MATCH     DCODE1,DCODE2
          GOTO      ERR2 IF LESS
          GOTO      PROC1
.
ERR2      DISPLAY   *P1:24,*B,*V2LON:
                    "** From Doctor Code Greater than To Doctor Code. **";
          CALL      HITENTER
          DISPLAY   *P1:24,*EL;
          GOTO      KSING
.
.************************
PROC1     CALL      KILL0000
          CALL      CREA0000
.
          OPEN      TEMPFILE,FNAMET
          MOVE      ONE,UNIQUE
.
          DISPLAY   *P1:24,*EL:
                    *P10:24,"Invoice No: ":
                    *P40:24,"Scanning: "
.
          PACK      KEY16,FROMYTDT,SP70
          CALL      RSPTINV2
PROC1A    CALL      RKPTINV2
          BRANCH    OVRCD,DOCT1
.
          MATCH     PINVDTE,TOYTDT
          GOTO      DOCT1 IF LESS
.
          PACK      KEY23,PINVNO,FIVE,SP70
          CALL      RDSDTRE4
PROC1B    CALL      RDKDTRE4
          BRANCH    OVRCD,PROC1A
.
          MATCH     PINVNO,ATINVNO
          GOTO      PROC1A IF NOT EQUAL
.
          MATCH     SP70,ATDTEDAD
          GOTO      PROC1B IF EQUAL
          GOTO      PROC1B IF EOS
.
          MATCH     DCODE1,ATDTEDAD
          GOTO      PROC1B IF LESS
          MATCH     ATDTEDAD,DCODE2
          GOTO      PROC1B IF LESS
.
          IF        ATRECTYP<>FIVE
            GOTO      PROC1B
          ENDIF
.
          DISPLAY   *P24:24,*V2LON,PINVNO;
.
          PACK      TMPDRCD,ATDTEDAD,SP70
          PACK      TMPINDT,PINVDTE,SP70
          PACK      TMPITNO,ATITEMNO,SP70
          PACK      TMPTRNO,UNIQUE,SP70
          PACK      TMPINVN,ATINVNO,SP70
          PACK      TMPVISN,DATNUMB,SP70
          PACK      TMPTAMT,ATPATAMT,SP70
          PACK      TMPSRVS,ATSERVS,SP70
.
          PACK      KEY33,ATDTEDAD,PINVDTE,ATITEMNO,DATTRANS,SP70
.
          WRITE     TEMPFILE,KEY33;TMPDRCD,TMPINDT,TMPITNO,TMPTRNO,TMPINVN:
                                   TMPVISN,TMPTAMT,TMPSRVS
.
          ADD       ONE,UNIQUE
          GOTO      PROC1B    
.************************
.
.         Subroutine for doctor sequence
.
.DOCT1     PACK      KEY40,DCODE1,FROMDTRN,SP70
.          CALL      RDSDTRE6
..
.NEXTD4    CALL      RDKDTRE6
..         BRANCH    OVRCD OF ENDP
.          IF        OVRCD=1
.            CALL      PRTLDC00
.            GOTO      ENDP
.          ENDIF
..
.          MATCH     SP70,ATDTEDAD
.          GOTO      NEXTD4 IF EQUAL
..
.          MATCH     ATDTEDAD,DCODE2
..         GOTO      ENDP IF LESS
.          IF        @LESS
.            CALL      PRTLDC00
.            GOTO      ENDP
.          ENDIF
..
.          MATCH     ATDDATE,TODATE
..         GOTO      ENDP IF LESS
..         IF        @LESS
..           CALL      PRTLDC00
..           GOTO      ENDP
..         ENDIF
.          GOTO      NEXTD4 IF LESS
..
.          IF        ATRECTYP<>FIVE
.            GOTO      NEXTD4
.          ENDIF
..
.          CALL      PRNTDA
.          GOTO      NEXTD4
..
DOCT1     PACK      KEY33,SP70
          READ      TEMPFILE,KEY33;;
.
NEXTD4    READKS    TEMPFILE;TMPDRCD,TMPINDT,TMPITNO,TMPTRNO,TMPINVN,TMPVISN:
                             TMPTAMT,TMPSRVS
.
.         IF        OVER=1
.           CALL      PRTLDC00
.           GOTO      ENDP
.         ENDIF
          GOTO      ENDP IF OVER
.
          PACK      ATDTEDAD,TMPDRCD,SP70
          PACK      PINVDTE,TMPINDT,SP70
          PACK      ATITEMNO,TMPITNO,SP70
          PACK      DATTRANS,TMPTRNO,SP70
          PACK      ATINVNO,TMPINVN,SP70
          PACK      DATNUMB,TMPVISN,SP70
          MOVE      TMPTAMT,ATPATAMT
          MOVE      TMPSRVS,ATSERVS
.
          CALL      PRNTDA
          GOTO      NEXTD4
.
PRNTDA    COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
.
          DISPLAY   *P55:24,"Date : ",ATDDATE,SP1,ATDTSVTM;
.
          MATCH     SP70,LSTDOCT
          IF        @EQUAL
            MOVE      ATDTEDAD,LSTDOCT
          ENDIF
.
          MATCH     ATDTEDAD,LSTDOCT
          GOTO      PRNTDA1 IF NOT EQUAL
.
          CALL      GETRNG00 
          GOTO      PRNTD99
.
PRNTDA1   CALL      DOCN00
.
          PRINT     *1,"|";
.
          PRINT     LSTDOCT,SP2,DIM28;
.
          PRINT     *38,"|    ",NOSERNGE;
.
          PRINT     *49,"| ",RESERNGE,*71,"|   ",NOSEYTDT,*82,"| ",RESEYTDT:
                    *132,"|"
          ADD       ONE,CLNO
.
          MOVE      ZERO,NOSERNGE
          MOVE      ZERO,RESERNGE
          MOVE      ZERO,NOSEYTDT
          MOVE      ZERO,RESEYTDT
          CALL      GETRNG00
.
PRNTD99   RETURN
.
..............................................................................
.         Printing of grand totals
.
ENDP      CALL      PRTLDC00
          CALL      KILL0000
          COMPARE   ZERO,CPAGENO
          GOTO      NODATA IF EQUAL
.
          CALL      PAGEND
          PRINT     *1,"| Grand Total";
          PRINT     *38,"|    ",TOSERNGE;
          PRINT     *49,"| ",TORERNGE,*71,"|   ",TOSEYTDT,*82,"| ",TOREYTDT:
                    *132,"|"
          CALL      PAGEND
.
          PRINT      *N,*N,"** END OF REPORT **";
          DISPLAY   *P1:24,*EL:
                    *V2LON,"*** Generation Completed *** ";
          CALL      HITENTER
          GOTO      START
.
NODATA    DISPLAY   *P1:24,*EL,*B:
                    *V2LON,"*** No Available Data *** ";
          CALL      HITENTER
          GOTO      START
.
............................................................................
.         Heading
.
HEAD      COMPARE   ZERO,CPAGENO
          CALL      PAGEND IF NOT EQUAL
.
          MOVE      ONE,CNOUNDLN 
          MOVE      DESCOPT,CPHDROPT
          CALL      IBACLOCK    
          CALL      HEAD132
.
          UNPACK    FROMDATE INTO CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          MOVE      DCODE1,DIM10
          MATCH     SP70,DCODE1
          IF        @EQUAL
            MOVE      "Start",DIM10
          ENDIF 
          PRINT     *50,"From : ",*58,CPCDATE," ",DIM10
.
          UNPACK    TODATE INTO CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          MOVE      DCODE2,DIM10
          MATCH     Z70,DCODE2
          IF        @EQUAL
            MOVE      "Finish",DIM10
          ENDIF
          PRINT     *N,*50,"To   : ",*58,CPCDATE," ",DIM10
.
          PRINT     *N
          CALL      PAGEND
 PRINT       *1,"|Doctor      Doctor Name",*38,"|  No Ser",*49,"|     Rev Ser":
                      *71,"|  No YTD":
                      *82,"|        Rev YTD ":
                      *132,"|"
          CALL      PAGEND
          MOVE      TEN2,CLNO
.
          MOVE      SP6,LSTDOCT     ** PRINT DOC NAME ON 1ST LINE NEW PAGE
          RETURN
.
.............................................................................
.
PAGEND    PRINT     *1,"*--------------------------------------------":
                    "--------------------------------------------":
                    "------------------------------------------*"
          ADD       ONE,CLNO
          RETURN
.
.
ENDIT     CALL      KILL0000
          CHAIN     PGM
          STOP
.
.*******************************************************************************
.* DOCN00                                                                      *
.*******************************************************************************
DOCN00    PACK      KEY10,LSTDOCT,SP70
          CALL      RDPMHCP1
          IF        OVRCD=0
.
            ENDSET      PMHCTITL
DOCN10      CMATCH      SP1,PMHCTITL
            GOTO        DOCN20 IF NOT EQUAL
            BUMP        PMHCTITL BY -1
            GOTO        DOCN10 IF NOT EOS
.
DOCN20      LENSET      PMHCTITL
            RESET       PMHCTITL
.
            MOVE        PMHCGNAM,ANS
            PACK        DIM28 WITH PMHCTITL,SP1,ANS,DOT,PMHCSNAM
          ELSE
            MOVE      "Invalid Doctor",DIM28
          ENDIF
.
DOCN99    RETURN
+
.*******************************************************************************
.* GETRNG00                                                                    *
.*******************************************************************************
GETRNG00  ADD      ATSERVS,NOSEYTDT        * YTD
          ADD      ATPATAMT,RESEYTDT
          ADD      ATSERVS,TOSEYTDT
          ADD      ATPATAMT,TOREYTDT
.
.
.         Check if this is in the range of the selected dates
.
          MATCH    FROMDATE,PINVDTE
          GOTO     GETRNG90 IF LESS
          MATCH    PINVDTE,TODATE
          GOTO     GETRNG90 IF LESS
.
          ADD      ATSERVS,NOSERNGE
          ADD      ATPATAMT,RESERNGE
          ADD      ATSERVS,TOSERNGE
          ADD      ATPATAMT,TORERNGE
.
GETRNG90  MOVE     ATDTEDAD,LSTDOCT
.
GETRNG99  RETURN
+
.*******************************************************************************
.* YTDRNG00                                                                    *
.*******************************************************************************
YTDRNG00  MOVE     ZERO,EXIT
          CALL     IBACLOCK
.
.         Financial year of Fromdate and Todate must be the same
.
          MOVE     FROMDATE,CPTDATE
          CALL     GPERD
          MOVE     DRGYR,KEY4          * Save financial year of fromdate
.
          MOVE     TODATE,CPTDATE
          CALL     GPERD
          MATCH    KEY4,DRGYR
          GOTO     YTDRNG50 IF NOT EQUAL
.
.         Get Start and End date of the financial year
.
          PACK     KEY6,KEY4,SP2
          CALL     RDSDRGA1
          CALL     RDKDRGA1
          BRANCH   OVRCD,YTDRNG70
          MATCH    KEY4,DRGYR
          GOTO     YTDRNG70 IF NOT EQUAL
.
          PACK     FROMYTDT,DRGFDTE,SP70     * start date of financial year
          REP      " 0",FROMYTDT
.
          PACK     KEY6,KEY4,Z70
          CALL     RDSDRGA1
          CALL     RDPDRGA1
          BRANCH   OVRCD,YTDRNG70
          MATCH    KEY4,DRGYR
          GOTO     YTDRNG70 IF NOT EQUAL
.
          PACK     TOYTDT,DRGTDTE,SP70
          REP      " 0",TOYTDT
.
          GOTO     YTDRNG99
.
YTDRNG50  DISPLAY  *P1:24,*EL:
                   "From date and To date must have same Financial Year";
          CALL     HITENTER
          GOTO     YTDRNG90
.
YTDRNG60  DISPLAY  *P1:24,*EL,KEY8," is Missing from Period Range File.";
          CALL     HITENTER
          GOTO     YTDRNG90
.
YTDRNG70  DISPLAY  *P1:24,*EL,KEY4,"is Missing from Period Range File.";
          CALL     HITENTER
          GOTO     YTDRNG90
.
YTDRNG80  DISPLAY  *P1:24,*EL,"Current date is Missing from Period Range File.";
          CALL     HITENTER
          GOTO     YTDRNG90
.
YTDRNG90  MOVE     ONE,EXIT
.
YTDRNG99  RETURN
+
.*******************************************************************************
.* PRTLDC00                                                                    *
.*******************************************************************************
PRTLDC00  IF        ZERO=NOSERNGE & ZERO=RESERNGE & ZERO=NOSEYTDT & ZERO=RESEYTDT
            GOTO     PRTLDC99
          ENDIF
.
          CALL      DOCN00
.
          PRINT     *1,"|";
.
          PRINT     LSTDOCT,SP2,DIM28;
.
          PRINT     *38,"|    ",NOSERNGE;
.
          PRINT     *49,"| ",RESERNGE,*71,"|   ",NOSEYTDT,*82,"| ",RESEYTDT:
                    *132,"|"
          ADD       ONE,CLNO
.
PRTLDC99  RETURN
+
KILL0000  CLOSE     TEMPFILE,DELETE
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMET,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          DISPLAY   *P1:14,*EF;
KILL9999  RETURN
.
CREA0000  CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMET,CMDLINE
          APPEND    " 67 U1-10,11-18,19-27,28-33",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
CREA9999  RETURN
.
          INC       AAEDTRIO/INC
          INC       PMSHCPIO/INC
          INC       PATVISIO/INC
.
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC
.
          INC       PATCODIO/INC
          INC       PATMA1IO/INC
          INC       PATINVIO/INC
          INC       PATDRGIO/INC
.
          INC      PATCOMN3
          INC      PMSHCPKY
          INCLUDE  STD001IO
.

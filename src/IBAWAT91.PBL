.******************************************************************************
.* System        :  Waiting Lists                                             *
.* Program       :  IBAWAT91.PBL                                              *
.* Description   :  Change Procedure Status                                   *
.******************************************************************************
.* Date          :  27/07/1990                                                *
.* Author        :  Justin Coates                                             *
.* Function      :  To change the Procedure status (wmstat) on the            *
.*                  Procedure file (wattr1af)                                 *
.* Modifications :                                                            * 
.******************************************************************************
.* V12.00.01 29.05.2025  DA Sarkies       TSK 0955096                         *
.*           Alphanumeric visit number change                                 *
.******************************************************************************
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899                         *
.*           Recompiled as WATTR1FD changes                                   *
.******************************************************************************
.* V9.03.02  13/08/2003  Sandra Barcham    42279                              *
.*           Defined WATHI002 with tilda's to put current date into           *
.*           WTHIEDAT to stop I * D                                           *
.* V9.03.01  27.06.2003  Sandra Barcham    40344                              *
.*           Fix 00AEA2 I * C (patvisaf)                                      *
.******************************************************************************
.* V9.02.01  27/02/2003  Jill Habasque SRF 34660                              *
.*           Recompiled for WLHISSUB                                          *
.******************************************************************************
.*        V5.08.05  21/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.04  27/07/2000  Jill Habasque                                 *
.*                  Recompiled for WLHISSUB                                   *
.*        V5.08.03  11/07/2000  Jill Habasque                                 *
.*                  Recompiled for WLHISSUB                                   *
.*        V5.08.02  29/06/2000  Jill Habasque                                 *
.*                  Recompiled for WATHISFD                                   *
.*        V5.08.01  13/06/2000 Tonii Tang                                     *
.*                  Mods so that if:                                          *
.*                  WMSTAT = 1 then WTWMBSCD = 05 (deferred)                  *
.*                  WMSTAT = 2 and the Scheduled Admission date is changed,   * 
.*                  then WTWMBSCD = 06 (Re-booked)                            *
.*                  WMSTAT = 5 then WTWMBSCD = 20 (Exited)                    *
.*                                                                            *
.******************************************************************************
.*        V5.07.01  27/10/1999 J Habasque                                     *
.*                  Recompiled for changes to WATPROFD - Active/Inactive codes*
. *       V5.04.02  15/04/1997  howellsy   RHA                                *
. *                 Write to W/L History                                      *
.*        V5.04.01  03/10/1996  Howellsy          EOC & ACC                   *
.*                  Generate Booking No when placed on the Waiting List       *
.******************************************************************************
.*                  08/08/1990 Bill Dew                                       *
.*                  Display description for waiting list status               *
.*               :  V5.01.02  07/11/1990 Karin Peak                           *
.*                  Fixed screen display in WATDSTRE                          *
.*               :  V5.01.03  24/01/1991  Justin Coates                       *
.*                  Changed to use BOKRC1FD and not BOKMASFD                  *
.*                  V5.01.04  26/08/93  ROD                                   *
.*                  RECOMPILED FOR nzhis                                      *
.*                  V5.01.05  24/05/1995  Matt Surridge                       *
.*                  Fixed bugs for global recompile.                          *
.*                                                                            *
.******************************************************************************
.
. NOTE: at time of writing audits to the booking file weren't implemented
.       The code is here to do the required things but it is commented out
.
.$$$$$
.         IBAWAT91.PBL        Change Procedure Status
.         ============        =======================
.
.         KPUR0000 - Enter the UR number of desired patient (uses KURN)
.         KPCC0000 - Enter the Procedure Code and Count required
.                    must be valid for the particular UR number
.         UPST0000 - Update the procedure status
.                    KNPS0000 - keyin the status
.
.$$$$$
.
. ------ FD includes needed ------
.
          INC       STD001FD                * standard variables
          INC       NZHISIFD/INC                 * Include for NZHIS vars
          INC       NHIMASFD/INC                 * Include for NZHIS vars
          INC       BOKRC1FD/INC            * booking file
          INC       PATCALAG/INC
          INC       PATCOMM/INC             * special variables
          INC       PATCONFD/INC            * patient control file
          INC       PATDHEAD/INC
          INC       PATMA1FD/INC            * patient master file
          INC       PATGSRFD/INC            * patient surname file
          INC       PATVISFD/INC
          INC       WATCONFD/INC            * WL control file
          INC       WATPROFD/INC            * WL procedure file
          INC       WATTR1FD/INC            * WL treatment file
          INC       WATHISFD/INC            * WL treatment file
          INC       WATNBRFD/INC            * WL treatment file
          INC       PATCODFD/INC            * WL treatment file
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
          INC       WEBSECFD/INC
.
. ------ needed for 'patcomn1' ------
.
          INC       PATDSCFD/INC            * admission file
          INC       PATMI1FD/INC            * admission file
          INC       PATPR1FD/INC            * pre-admission file
          INC       OUTPREFD/INC            * OP pre-attend. file
.
. ------ program variables ------
.
ADDWLFLG  FORM      1
CODE      DIM       2
BJDAY     FORM      3
CJDAY     FORM      3
.
ENTUR     DIM       8                       * UR number entered
ENTCODE   DIM       9                       * procedure code entered
ENTCNT    FORM      2                       * procedure count entered
ENTDATE   DIM       8                       * entered date ccyymmdd
ENTDESC   DIM       35                      * entered code description
ENTNAME   DIM       35                      * entered UR name
ENTSTAT   FORM      1                       * procedure status entered
.
FRSTGNAM  DIM       40
FULLGNAM  DIM       80
.
LASTDATE  DIM       8                       * last review date
NMPNUMB   DIM       20
SCNDGNAM  DIM       40
STATDESC  DIM       12                      * waiting list status description
STAT1     INIT      "Unscheduled "
STAT2     INIT      "Scheduled   "
STAT3     INIT      "Pre-Admitted"
STAT4     INIT      "Admitted    "
STAT5     INIT      "Discharged  "
STAT6     INIT      "Removed     "
.
WATHI002  INIT      "~~~~~~~~"
QUPER     FORM      1                       * ? performed flag
Z20       INIT      "zzzzzzzzzzzzzzzzzzzz"
.
. ------ program constants ------
.
.
PRGNAM    DIM       23                      * set up in init0000
PRGID     INIT      "IBAWAT91"
VERSION   INIT      "V12.01.00"
.
.*********************************************************************
.*                  ML0000                                           *
.*        Mainline Code and Controlling Logic                        *
.*********************************************************************
ML0000    CALL      INIT0000                * initialization & open files
.
ML1000    CALL      DISP0000                * display the screen
          CALL      KPUR0000                * enter the UR number
          BRANCH    EXIT,ML9999             * exit chosen 
.
ML2000    CALL      KPCC0000                * enter procedure code and count
          BRANCH    EXIT,ML1000             * nothing entered, reenter UR
.
ML3000    CALL      LOCK0000                * lock the patients record
          BRANCH    EXIT,ML2000             * cant continue, reenter code
.
ML4000    CALL      SAVHIS00                * save the history  
          CALL      UPST0000                * update the procedure status
.
          GOTO      ML1000                  * enter another UR number
.
ML9999    CHAIN     PGM
+
.*********************************************************************
.*                  INIT0000                    Called by : ML0000   *
.*        Initialization and open files                              *
.*********************************************************************
INIT0000  DISPLAY   *P70:24,"controlf"
          OPEN      CONTROLF,"controlf"     * control file
          READ      CONTROLF,THIRTY7;*122,WTCNTPUC,*185,WTCNTPDS
          CLEAR     PRGNAM
          APPEND    "CHANGE ",PRGNAM
          APPEND    WTCNTPUC,PRGNAM
          APPEND    " STATUS",PRGNAM
          RESET     PRGNAM
.
          CALL      DISPHEAD                * display program details
          MOVE      ONE,CNEWDS              * new ? option
.
          READ      CONTROLF,THIRTY7;*44,HWAUDB * pat.treat. file audits
.
          DISPLAY   *P60:24,"opening"
          DISPLAY   *P70:24,"bokrc1af"
          OPEN      BOKRC1A1,"bokrc1af"     * booking file
.>>>>>    DISPLAY   *P70:24,"bokaudba"
.>>>>>    OPEN      BOKAUDBA,"bokaudba"     * booking audit file
          DISPLAY   *P70:24,"wataudwm"
          OPEN      WATAUTR1,"watautr1"     * WL treatment file audits
          DISPLAY   *P70:24,"watproaf"
          OPEN      WATPROA1,"watproaf"     * WL procedure file
          DISPLAY   *P70:24,"wattr1af"
          OPEN      WATTR1A1,"wattr1af"     * WL treatment file
          DISPLAY   *P70:24,"patvisaf"
          OPEN      PATVISA1,"patvisaf"     * visit file
          DISPLAY   *P70:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"     * master file
          DISPLAY   *P70:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"     * master extension file
          DISPLAY   *P70:24,"pamspx2f"
          OPEN      PMSPX2A1,"pmspx2af"     * master extension file
          DISPLAY   *P70:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"     * admission file
          DISPLAY   *P70:24,"patpramf"
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
.
          DISPLAY   *P70:24,"patgsrnf"
          OPEN      PATGSRN1,"patgsrnf"     * given name search file
          OPEN      PATGSRN2,"patgsrnf"     * given name search file
          OPEN      PATGSRN3,"patgsrnf"     * given name search file
          OPEN      PATGSRN4,"patgsrnf"     * given name search file
.
INIT9000  DISPLAY   *P1:24,*EL              * clear the line of opening ........
.
INIT9999  RETURN
+
.*********************************************************************
.*                  DISP0000                    Called by : ML0000   *
.*        Display the input prompts for the screen                   *
.*********************************************************************
DISP0000  DISPLAY   *P1:3,*EF:
                    *P5:4,"U/R Number           : ":
                    *P5:5,WTCNTPDS," Code/Count : ":
                    *P1:7,*V2LON,ONE,*HOFF,*P2:7,".  ",WTCNTPDS," Status     : "
DISP9999  RETURN
+
.*********************************************************************
.*                  KPUR000                     Called by : ML0000   *
.*        Keyin the UR number                                        *
.*        Returns : ENTUR         the UR number                      *
.*                  EXIT = 1      no UR number entered               *
.*********************************************************************
KPUR0000  MOVE      "28",CCOL               * column for input
          MOVE      "04",CVERT              * row for input
          MOVE      TWO,SRCHOPT             *
          MOVE      ZERO,SRCHSYS            * search all systems
.
          CALL      KURN                    * enter UR number
          BRANCH    EXIT,KPUR9000           * nothing entered
          BRANCH    OVRCD,KPUR7000          * invalid UR number
.
          MOVE      PURNO,ENTUR             * set up input variable
          MOVE      PURNO,KEY8              * UR number as key
          CALL      RDMAST1                 * read master file
          BRANCH    OVRCD,KPUR7000          * invalid UR number
.
          MOVE      PSNAME,PACSNAME         * surname
          MOVE      PGNAME,PACGNAME         * given name
          MOVE      PTITL,PACTITLE          * title
          MOVE      TWO,PACFRMT             * format
          CALL      PACNAME                 * format name
          MOVE      PACFNAME,ENTNAME        * set up display variable
.
          DISPLAY   *P28:04,*V2LON,ENTUR,*HOFF,*P45:04,ENTNAME
.
          MOVE      FALSE,EXIT              * valid answer
          GOTO      KPUR9999
.
KPUR7000  DISPLAY   *P1:24,*B,"Invalid UR number entered.  ",*EL;
          CALL      HITENTER
          CALL      DISP0000                * redisplay the screen
          GOTO      KPUR0000
.
KPUR9000  MOVE      TRUE,EXIT               * nothing entered 
.
KPUR9999  RETURN
+
.*********************************************************************
.*                  KPCC0000                    Called by : ML0000   *
.*        Enter the procedure code and count                         *
.*        Returns : ENTCODE       the procedure code                 *
.*                  ENTCNT        the procedure count                *
.*                  EXIT = 1      nothing entered                    *
.*********************************************************************
KPCC0000  MOVE      FIVE,CVERT              * row for input
          MOVE      ONE,QUPER               * set ? as not performed
.
KPCC0100  KEYIN     *P28:CVERT,*DV,UNDLN9,*EL:
                    *P28:CVERT,*V2LON,ENTCODE:
                    *P28:CVERT,*DV,ENTCODE
.
          ENDSET    ENTCODE
          APPEND    SP9,ENTCODE
          RESET     ENTCODE
.
. ------ check on what was entered ------
.
          MOVE      TRUE,EXIT               * set exit flag
          MATCH     SP9,ENTCODE             * was return hit ??
          GOTO      KPCC3000 IF EQUAL       * yes, so exit
.
. ------ see if ? entered ------
.
          MATCH     QUEST,ENTCODE           * was ? entered ??
          GOTO      KPCC1000 IF NOT EQUAL   * no, so validate what was
.
. ------ ? entered ------
.
          MOVE      ZERO,QUPER              * set ? as performed
          CALL      WATR0000                * ? routine
          MOVE      "24",CVERT              * row for input
          DISPLAY   *P1:24,"    ",WTCNTPDS," Code/Count : ",*EL
          GOTO      KPCC0100
.
. ------ validate what was entered ------
.
KPCC1000  MOVE      ENTCODE,KEY9            * code as the key
          CALL      RDPROA1                 * read procedure file
          BRANCH    OVRCD,KPCC7000          * invalid code entered
.
. ------ enter the procedure count ------
.
KPCC2000  KEYIN     *P37:CVERT,*DV,SLASH,*P38:CVERT,*DV,UNDLN2:
                    *P38:CVERT,*V2LON,ENTCNT:
                    *P38:CVERT,*DV,ENTCNT
.
          COMPARE   ZERO,ENTCNT             * was return hit ??
          GOTO      KPCC0100 IF EQUAL       * yes, re-enter code
.
. ------ now see if the code and count is valid for the patient ------
.
          PACK      KEY19,ENTUR,ENTCODE,ENTCNT * key of entered values
          CALL      RDTREA1                 * read treatment file
          BRANCH    OVRCD,KPCC7100          * invalid combination
.
. ------ entered information is valid ------
.
          MOVE      WMDESC,ENTDESC          * the description
          MOVE      FALSE,EXIT              * valid code
.
KPCC3000  BRANCH    QUPER,KPCC9000          * ? not entered
.
. ------ redisplay the screen ------
.
          CALL      DISP0000                * display prompts
          DISPLAY   *P28:04,*V2LON,ENTUR,*HOFF,*P45:04,ENTNAME:
                    *P28:05,*V2LON,ENTCODE,SLASH,ENTCNT,*HOFF,*P45:05,ENTDESC
.
          MATCH     SP9,ENTCODE             * re-enter code if nothing entered
          GOTO      KPCC0000 IF EQUAL       * at a ? option
.
          GOTO      KPCC9999
.
. ------ invalid code entered ------
.
KPCC7000  DISPLAY   *P1:24,*B,WTCNTPDS," code does not exist.  ",*EL;
          CALL      HITENTER
.
          BRANCH    QUPER,KPCC0100          * input on line 5
.
          DISPLAY   *P1:24,"    ",WTCNTPDS," Code/Count : ",*EL
          GOTO      KPCC0100                * input on line 24
.
. ------ invalid code for the patient ------
.
KPCC7100  DISPLAY   *P1:24,*B,"Code is not valid for this patient.  ",*EL;
          CALL      HITENTER
.
          BRANCH    QUPER,KPCC2000          * input on line 5
.
          DISPLAY   *P5:24,WTCNTPDS," Code/Count : ",*V2LON,ENTCODE,SLASH,*EL
          GOTO      KPCC2000                * input on line 24
.
KPCC9000  DISPLAY   *P45:05,ENTDESC
.
KPCC9999  RETURN 
+
.*********************************************************************
.*                  LOCK0000                    Called by : ML3000   *
.*        Lock the patients treatment file record                    *
.*********************************************************************
LOCK0000  PACK      KEY19,ENTUR,ENTCODE,ENTCNT * key for the read
          CALL      RLWTTRE1                   * locking read the treatment file
          BRANCH    OVRCD,LOCK7000:            * invalid record
                          LOCK7100             * record already locked
.
          MOVE      FALSE,EXIT
          GOTO      LOCK9999
.
. ------ code not valid for the patient ------
.
LOCK7000  DISPLAY   *P1:24,*B,"Code is not valid for this patient.  ",*EL;
          CALL      HITENTER
          GOTO      LOCK9000
.
. ------ record locked elsewhere ------
.
LOCK7100  DISPLAY   *P1:24,*B,"Waiting list record is locked on port ",WTWMPORT:
                    ".  ",*EL;
          CALL      HITENTER
          GOTO      LOCK9000
.
LOCK9000  MOVE      TRUE,EXIT
.
LOCK9999  RETURN
+
.*********************************************************************
.*                  UPST0000                    Called by : ML4000   *
.*        Update the procedure status and relevant details           *
.*********************************************************************
UPST0000  CALL      KNPS0000                * enter new status
.
UPST1000  CALL      MAINQST                 * continue prompt
.
          BRANCH    CCITEM,UPST2000         * 1 entered

          COMPARE   ZERO,CCITEM             * validate
          GOTO      UPST5000 IF EQUAL       * Post entered
          GOTO      UPST9000 IF LESS        * Cancel entered
.
          BEEP
          GOTO      UPST1000
.
. ------ 1 entered ------
.
UPST2000  CALL      KNPS0000                * change the status
          GOTO      UPST1000
.
. ------ Post selected ------
.
UPST5000  COMPARE   WMSTAT,ENTSTAT          * was the value changed ??
          GOTO      UPST9999 IF EQUAL       * no, so dont do anything
.
          BRANCH    HWAUDB,UPST5050         * audits not on
.
. ------ write the before audit on the treatment file ------
.
          CALL      IBACLOCK                * get current date and time
          PACK      WTWMAUDD,CCC,CYY,CMM,CDD * the date
          REP       " 0",WTWMAUDD           * zero fill
          MOVE      CTIMEIS,WTWMAUDT        * the time
          MOVE      PORT,WTWMAUDP           * port doing change
          MOVE      ANSB,WTWMAUDR           * record type is before change
          MOVE      ONE,WTWMAUDS            * set status to not printed
          MOVE      PASSCODE,WTWMAUDO       * operator id
          CALL      AWTREA1                 * write the before audit
          DISPLAY   *P1:24,"Before audit record written.",*EL
.
. ------ update the values for the treatment file ------
.
UPST5050  MOVE      ENTSTAT,WMSTAT          * update the status
          MOVE      LASTDATE,WMDATE2        * update review date
.
          BRANCH    ENTSTAT,UPST5100,UPST5200,UPST5300,UPST5300,UPST5300
.
. -------------------------------------
. ------ wmstat changed to one   ------
. ------ change the date-on-list ------
. -------------------------------------
.
UPST5100  MOVE      ENTDATE,WMDATE1         * change date-on-list
.
. ------ if they have a booking record, must delete it ------
.
          MOVE      WMBOOK,KEY8             * booking number as key
          CALL      RDBKREC1                * read booking file
          BRANCH    OVRCD,UPST5150          * no record, update treatment file
.
          CALL      DEBKREC1                * delete the booking file
.
UPST5150  CALL      GENBOKNO           * generate booking number
          MOVE      "05",WTWMBSCD           * Deferred booking
          GOTO      UPST6000                * update treatment file
.
. -------------------------------------------------
. ------ wmstat changed to two               ------
. ------ change the scheduled admission date ------
. -------------------------------------------------
.
UPST5200  MOVE      ENTDATE,WMDATE3         * change sch. ad. date
          MATCH     WMDATE3,SAVESCHA        * Check if Admission Date's changed
          GOTO      UPST5300 IF EQUAL
.
          MOVE      "06",WTWMBSCD           * Re-booked
.
. ----------------------------------------------------
. ------ wmstat changed to 3,4,5                ------
. ------ changing the booking details so        ------
. ------ have to update the booking master file ------
. ----------------------------------------------------
.
UPST5300  MOVE      WMBOOK,KEY8             * booking number as key
          CALL      RLBKREC1                * locking read the booking record
          BRANCH    OVRCD,UPST5700:         * invalid booking record
                          UPST5800          * record locked elsewhere
.
.>>>>>>   BRANCH    HWAUDx,UPST5400         * audits not on
.
. ------ write the before audit on the booking master file ------
.
.>>>>>>   PACK      BKREAUDD,CCC,CYY,CMM,CDD * the date
.>>>>>>   MOVE      CTIMEIS,BKREAUDT        * the time
.>>>>>>   MOVE      PORT,BKREAUDP           * port doing change
.>>>>>>   MOVE      ANSB,BKREAUDR           * record type is before change
.>>>>>>   MOVE      ONE,BKREAUDS            * set status to not printed
.>>>>>>   MOVE      PASSCODE,BKREAUDO       * operator id
.>>>>>>   CALL      AUBKREC                 * write before audit on master file
.
. ------ change the status on the booking file ------
. ------ entstat = 2 : bkrestat <- zero  (booked)       ------
. ------ entstat = 3 : bkrestat <- one   (pre-admitted) ------
. ------ entstat = 4 : bkrestat <- three (admitted)     ------
. ------ entstat = 5 : bkrestat <- four  (discharged)   ------
.
UPST5400  LOAD      BKRESTAT,ENTSTAT,ZERO,ZERO,ONE,THREE,FOUR
.
          COMPARE   TWO,ENTSTAT             * was the status changed to two ??
          GOTO      UPST5600 IF NOT EQUAL   * no, so just update
.
          UNPACK    ENTDATE,CCENT,CYEAR,CMON,CDAY
          PACK      BKREEDAT,CCENT,CYEAR,CMON,CDAY * set the exp. booking date
.
UPST5600  COMPARE   FIVE,ENTSTAT
          IF        @EQUAL
            MOVE      "20",WTWMBSCD         * Exited
          ENDIF
. 
          CALL      UPBKREC1                * update booking file
          DISPLAY   *P1:24,"Booking file updated.",*EL
.>>>>>    BRANCH    HWAUDx,UPST5650         * audits off
.>>>>>
.>>>>>    MOVE      ANSC,BKREAUDR           * record type after change
.>>>>>    CALL      AWMASA1                 * write the after change audit
.
UPST5650  CALL      UUBKREC1                * unlock master file
          GOTO      UPST6000                * update treatment file
.
. ------ invalid booking number : dont update the treatment file ------
.
UPST5700  DISPLAY   *P1:24,*B,"Patient has no booking information.  ",*EL;
          CALL      HITENTER
          CALL      ADTREA1                 * delete the audit record
          GOTO      UPST9999                * dont update treatment file
.
. ------ booking file record locked elsewhere ------
.
UPST5800  DISPLAY   *P1:24,*B,"Booking file record locked on port ":
                    *V2LON,BKRELOCK,*HOFF,".  ",*EL;
          CALL      HITENTER
          GOTO      UPST5300
.
. ------ update the treatment file ------
.
UPST6000  CALL      UPTREA1                 * update the treatment file
.
          MOVE      ONE,NBRFLAG
          CALL      WRTHIS00                * write to the history
          DISPLAY   *P1:24,"Treatment file updated.",*EL
.
          BRANCH    HWAUDB,UPST9500         * audits not on
.
. ------ write the after audit on the treatment file ------
.
UPST6100  MOVE      ANSC,WTWMAUDR           * record type is after change
          CALL      AWTREA1                 * write the after audit
          DISPLAY   *P1:24,"After change audit written.",*EL
          GOTO      UPST9500
.
. ------ Cancel selected ------
.
UPST9000  DISPLAY   *P1:24,"No changes made to procedure status.  ",*EL;
.
. ------ unlock the treatment file ------
.
UPST9500  PACK      KEY19,ENTUR,ENTCODE,ENTCNT
          CALL      UUWTTRE1                * unlock treatment file record
          DISPLAY   *P1:24,"Record is unlocked.",*EL
.
UPST9999  RETURN
+
.*********************************************************************
.*                  KNPS0000                    Called by : UPST0000 *
.*        Enter the new procedure status                             *
.*        Returns : ENTSTAT       the procedure status               *
.*                  ENTDATE       entered date (if new status = 1,2) *
.*                  LASTDATE      the last review date               *
.*                                                                   *
.*   Modified 08/08/1990 Bill Dew : display treatment status desc.   *
.*********************************************************************
KNPS0000  DISPLAY   *P1:8,*EL,*P1:13,*EL,*P1:24,*EL:
                    *V2LON,"1",*HOFF,"=Unscheduled, ":
                    *V2LON,"2",*HOFF,"=Scheduled, ":
                    *V2LON,"3",*HOFF,"=Pre-Admitted, ":
                    *V2LON,"4",*HOFF,"=Admitted, ":
                    *V2LON,"5",*HOFF,"=Discharged, ":
                    *V2LON,"6",*HOFF,"=Removed"
.
          LOAD      STATDESC,WMSTAT,STAT1,STAT2,STAT3,STAT4,STAT5,STAT6 * orig.
.
          KEYIN     *P28:7,*DV,WMSTAT,*P36:7,*DV,STATDESC:
                    *P28:7,*V2LON,ENTSTAT:
                    *P28:7,*DV,ENTSTAT,*P1:24,*EL
.
          COMPARE   ZERO,ENTSTAT            * was anything entered ??
          GOTO      KNPS9995 IF EQUAL       * no, exit
.
          COMPARE   ENTSTAT,WMSTAT          * was something different entered ?
          GOTO      KNPS9995 IF EQUAL       * no, exit
.
          LOAD      STATDESC,ENTSTAT,STAT1,STAT2,STAT3,STAT4,STAT5,STAT6 * new
          DISPLAY   *P36:7,STATDESC         * display new status description
.
          BRANCH    ENTSTAT,KNPS1000,KNPS2000,KNPS3000,KNPS3000:
                            KNPS3000,KNPS6000
.
          BEEP
          GOTO      KNPS0000                * invalid entry
.
. ------ 1 entered : set status to unscheduled ------
. ------             enter new date on list    ------
.
KNPS1000  MOVE      EIGHT,CVERT             * row for input
          MOVE      "28",CCOL               * column for input
          DISPLAY   *P1:8,"Enter Date-on-list       : ",*EL
          UNPACK    WMDATE1,CCENT,CYEAR,CMON,CDAY * default to original
          CALL      KDAT0000                * enter the date
.
          DISPLAY   *P1:13,*V2LON,"NOTE:",*HOFF," Selecting Post will result ":
                    "in the Booking Data being removed."
.
          MATCH     "00",CDAY               * was anything entered ??
          GOTO      KNPS9000 IF NOT EQUAL   * yes
.
          BEEP
          GOTO      KNPS1000
.
. ------ 2 entered : set status to scheduled ------
. ------             enter shed. admiss date ------
.
KNPS2000  MOVE      WMBOOK,KEY8             * is there a booking record ??
          CALL      RDBKREC1                * read booking file
          BRANCH    OVRCD,KNPS3500          * invalid booking record
.
          MOVE      WMDATE3,SAVESCHA
.
KNPS2500  MOVE      EIGHT,CVERT             * row for input
          MOVE      "28",CCOL               * column for input
          DISPLAY   *P1:8,"Enter Sched Admiss. Date : ",*EL
          UNPACK    WMDATE3,CCENT,CYEAR,CMON,CDAY * default to original
          CALL      KDAT0000                * enter the date
.
          MATCH     "00",CDAY               * was anything entered ??
          IF        !@EQUAL
            MOVE      ENTDATE,WMDATE3         * change sch. ad. date
            CALL      GETRSC00              * get reason for change
            MOVE      SAVESCHA,WMDATE3
            GOTO      KNPS9000
          ENDIF
.
          BEEP
          GOTO      KNPS2500
.
. ------ 3,4 or 5 entered : make sure they have a valid booking ------
. ------ record  otherwise will create bad data                 ------
.
KNPS3000  MOVE      WMBOOK,KEY8             * is there a booking record ??
          CALL      RDBKREC1                * read booking file
          BRANCH    OVRCD,KNPS3500          * invalid booking record
.
          GOTO      KNPS9000
.
. ------ ERROR: no booking record ------
.
KNPS3500  DISPLAY   *P1:24,*B,"There is no booking record for this ":
                    WTCNTPDS,".  ",*EL;
          CALL      HITENTER
          GOTO      KNPS0000
.
. ------ 6 entered : give error message ------
.
KNPS6000  DISPLAY   *P1:24,*B,"Use the 'Remove ",WTCNTPDS,"' option.  ",*EL;
          CALL      HITENTER
          GOTO      KNPS0000
.
. ------ update the last-review date for all procedure statuses ------
.
KNPS9000  CALL      IBACLOCK                * get current date
          PACK      LASTDATE,CCC,CYY,CMM,CDD * set up date of change
          REP       " 0",LASTDATE           * zero fill the date
          GOTO      KNPS9999
.
. ------ only enter hit, redisplay the defualt ------
.
KNPS9995  DISPLAY   *P28:7,*V2LON,WMSTAT
          MOVE      WMSTAT,ENTSTAT
.
KNPS9999  RETURN
+
.*********************************************************************
.*                  KDAT0000                    Called by : KNPS0000 *
.*        Enter a date at change of procedure status to 1 or 2       *
.*        Para's  : CVERT         row for input                      *
.*                  CCOL          column for input                   *
.*        Returns : ENTDATE       the date entered                   *
.*********************************************************************
KDAT0000  CALL      IBACLOCK                * get current date
.
          MOVE      ONE,CCANLDTE            * can cancel default
          MOVE      ONE,CDEFDTE             * hit enter once to acc. default
          MOVE      ZERO,CHIGHLT            * use highlighting
.
          MATCH     SP2,CCENT               * is there a default century ??
          GOTO      KDAT1000 IF NOT EQUAL   * yes there is
.
          MOVE      CCC,CCENT               * default to current century
.
KDAT1000  CALL      KEYDATE
.
          PACK      ENTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENTDATE            * zero fill date
.
KDAT9999  RETURN
+
.*********************************************************************
.*                  WATR0000                    Called by : KPCC0000 *
.*        ? mark routine for treatments for a patient to show all    *
.*        treatments that they have (not just unscheduled ones)      *
.*        (Copied WATDSTRE but removed check on status)              *
.*********************************************************************
WATR0000  DISPLAY    *P1:3,*EF,*P20:4,*V2LON:
                               *ULON," ",WTCNTPDS," codes for U/R : ",PURNO,SP1
.
          MOVE       SIX,CVERT
.
          MOVE       PURNO,WMURNO 
          PACK       KEY19,WMURNO,SP20
          CALL       RDSTREA1
.
WATR1000  CALL       RDKTREA1
          BRANCH     OVRCD OF WATR8000
.
          MATCH      PURNO,WMURNO
          GOTO       WATR8000 IF NOT EQUAL
.
          DISPLAY    *P1:CVERT,*V2LON,WMCODE,SLASH,WMCNT,SP1,*HOFF,WMDESC
.
          ADD        ONE,CVERT
          COMPARE    "24",CVERT
          GOTO       WATR1000 IF LESS
.
          MOVE       SIX,CVERT
.
.         Check for new standards question, or old question
.
          BRANCH    CNEWDS,WATR2000
.
          KEYIN     *P1:24,*EL," (",*V2LON,"E",*HOFF,")xit, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          CMATCH    ANSE,ANS
          GOTO      WATR9999 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      WATR0000 IF EQUAL
.
          GOTO      WATR4000
.
.         Use the new standards question
.
WATR2000  KEYIN     *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          CMATCH    ANSC,ANS
          GOTO      WATR9999 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      WATR0000 IF EQUAL
.
WATR4000  MOVE      ONE,CCOL
          DISPLAY   *PCCOL:CVERT,*EF
          GOTO      WATR1000
.
.         End of TREcedure codes
.
WATR8000  BRANCH    CNEWDS,WATR9999
          DISPLAY   *P1:24,*EL;
          CALL      HITENTER
.
WATR9999  RETURN
+
.
. ------ I/O includes needed ------
.
          INC       STD001IO                * standard routines
          INC       CALCAGE
          INC       NZCOMPIO/INC                 * IO Include for NZHIS
          INC       BOKRC1IO/INC            * booking master file
          INC       PATCOMN1                * for 'kurn'
          INC       PATMA1IO/INC            * patient master file
          INC       WATPROIO/INC            * procedure file
          INC       PATGSRIO/INC            * patient surname file
          INC       PATVISIO/INC
          INC       WATTR1IO/INC            * WL treatment file
          INC       PATSNDX                 * ? routine on UR number
          INC       PATSNX2                 * ? routine on UR number
          INC       GENBOKNO           * generate booking number
          INC       WATHISIO/INC            * WL treatment file
          INC       WATNBRIO/INC            * WL treatment file
          INC       WLHISSUB                                    
          INC       PATCODKY                                    
          INC       PATCODIO/INC                                
          INC       WEBSECIO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
.
. ------ needed for 'patcomn1.pbl' ------
.
          INC       PATDSCIO/INC            * admission file
          INC       PATMI1IO/INC            * admission file
          INC       PATPR1IO/INC            * pre-admission file
          INC       OUTPREIO/INC            * OP pre-attend. file
          INC       GENANVIS
.
GETSVAR   RETURN                            * dummy label 
.
. ------ needed for 'kurn' to redisplay screen ------
.
REDISPS   CALL      DISP0000                * redisplay screen
          RETURN
AGECHK
CLPATMAS  RETURN
................................................................................

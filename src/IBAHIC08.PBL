.***************************************************************************
.* System    :   HIC Claims                                                *
.* Program   :   IBAHIC08                                                  *
.* Desc      :   Aged Analysis Report                                      *
.***************************************************************************
.* Date      :   01/06/2005                                                *
.* Author    :   J.Tan                                                     *
.* Function  :   Debtors Aged Analysis Fee for Service Patients            *
.*                                                                         *
.* V10.11.01 25/07/2017  Ebon Clements  TSK 0268970                        *
.*                       Change to use OUTCLIFD index 1 instead of index 2 *
.* V10.04.01 17/04/2014  J.Tan          CAR 261630                         *
.*                       Removed port number from temp file name           *
.* V9.05.01  08/03/2006  J.Tan          CAR 96071                          *
.*                       Fixed printing todate on heading report           *
.* V9.05.B01 23/02/2006  J.Tan          CAR 96071                          *
.*                       Mods to use claim date to calculate ageing        *
.* V9.04.01  22/02/2006  J.Tan          CAR 96071                          *
.*                       Fixed calculating ageing for full report          *
.* V9.04.00  07/06/2005  J.Tan          CAR 56701                          *
.*                       Created program                                   *
.***************************************************************************
.
          INC       STD001FD
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       HICCLMFD/INC
          INC       HICCITFD/INC
          INC       PMSHCPFD/INC
          INC       PRIPRAFD/INC
          INC       PRIDOCFD/INC
          INC       OUTSITFD/INC
          INC       OUTCLIFD/INC
          INC       PATCODFD/INC
          INC       WEBSECFD/INC
          INC       PRIALSFD/INC
          INC       PRISECFD/INC
          INC       PATMA1FD/INC
          INC       HICITIFD/INC
.
.         Temporary File Definition
.         -------------------------
.
.         Filename : hic08axx.dat          (xx = port id)
.
HICTEMA1  IFILE SQL, FIXED=132, KEY="UC23-30"
HICTEMA2  IFILE SQL, FIXED=132, KEY="UC1-6,87-106,23-30"
HICTEMA3  IFILE SQL, FIXED=132, KEY="UC7-12,87-106,23-30"
HICTEMA4  IFILE SQL, FIXED=132, KEY="UC13-22,87-106,23-30"
.
.NAME     TYPE    LENGTH     PHYSICAL     START     DESCRIPTION
.----     ----    ------     --------     -----     -----------
TEMPCLID  DIM       6      6        1     Clinic Id
TEMPMPRA  DIM       6      6        7     Medical Practice
TEMPPSRV  DIM       10     10       13    Service Provider Number
TEMPURNO  DIM       8      8        23    U/R Number
TEMPPAID  FORM     12.2    8        31    Amount Received
TEMPCURD  FORM     12.2    8        39    Current outstanding amount
TEMP30DY  FORM     12.2    8        47    30 Days outstanding amount
TEMP60DY  FORM     12.2    8        55    60 Days outstanding amount
TEMP90DY  FORM     12.2    8        63    90 Days outstanding amount
TEMP120D  FORM     12.2    8        71    120 Days outstanding amount
TEMPOUTS  FORM     12.2    8        79    Total Amount outstanding
TEMPSNAM  DIM       20     20       87    Surname
TEMPGNAM  DIM       25     25       107   Given Name
.End of Record                      132
.
HICTEMB1  IFILE SQL, FIXED=116, KEY="UC55-62"
HICTEMB2  IFILE SQL, FIXED=116, KEY="UC1-6,71-90,55-62"
HICTEMB3  IFILE SQL, FIXED=116, KEY="UC7-12,71-90,55-62"
HICTEMB4  IFILE SQL, FIXED=116, KEY="UC13-22,71-90,55-62"
.NAME     TYPE    LENGTH     PHYSICAL     START     DESCRIPTION
.----     ----    ------     --------     -----     -----------
.TEMPCLID  DIM       6      6        1     Clinic Id
.TEMPMPRA  DIM       6      6        7     Medical Practice
.TEMPPSRV  DIM       10     10       13    Service Provider Number
.TEMPURNO  DIM       8      8        23    U/R Number
TEMPAMNT  FORM     12.2    8        31    Amount Claimed
.TEMPPAID  FORM     12.2    8        39    Amount Received
.TEMPOUTS  FORM     12.2   8        47    Current Outstanding Amount
TEMPCLMN  DIM       8      8        55    Claim Number
TEMPCDAT  DIM       8      8        63    Claim Date
.TEMPSNAM  DIM       20     20       71    Surname
.TEMPGNAM  DIM       25     25       91    Given Name
.End of Record                      116
.
.
.         Temporary File Variables
.
CUROUTS   FORM     12.2
OUT30D    FORM     12.2
OUT60D    FORM     12.2
OUT90D    FORM     12.2
OUT120D   FORM     12.2
.
ERASE     INIT      "iserase "
ISBUILD   INIT      "isbuild "
TEMPFILA  DIM       8
TEMPFILB  DIM       8
UKEYA     INIT      " 132 UC23-30 UC1-6,87-106,23-30 UC7-12,87-106,23-30 UC13-22,87-106,23-30"
UKEYB     INIT      " 116 UC55-62 UC1-6,71-90,55-62 UC7-12,71-90,55-62 UC13-22,71-90,55-62"

. LOCAL VARIABLE DEFINITION
. -------------------------
.
ALACCESS  FORM      1
CLINFROM  DIM       6
CLINICTO  DIM       6
CMDLINE   DIM       127
DATPER1   DIM       8                            * date PRCNPER1 days prior
DATPER2   DIM       8                            * date PRCNPER2 days prior
DATPER3   DIM       8                            * date PRCNPER3 days prior
DATPER4   DIM       8                            * date PRCNPER4 days prior
DISPNAME  DIM       60
DOCFNAME  DIM       50
DAYS      FORM      3                  * number of days ageing
DOPTION   FORM      1                  * option for days ageing
.                                        1 = Full report
.                                        2 = Current only
.                                        3 = 30 Days Only":
.                                        4 = 60 Days Only":
.                                        5 = 90 Days Only":
.                                        6 = 120+ Days Only";
FORM102   FORM      10.2
FILTPROV  DIM       10
HEADFLAG  FORM      1
HEADING1  DIM       30
HEADING2  DIM       30
HEADING3  DIM       40
HEADING4  DIM       10
HEADING5  DIM       30
HEADING6  DIM       30    
HEADING7  DIM       30    
HEADING8  DIM       30    
HEADING9  DIM       30    
HEADING0  DIM       30    
JDAYS     FORM      4                            * julian days
NAME35    DIM       35
PRACTIFR  DIM       6
PRACTITO  DIM       6
REPTYPE   DIM       14
DCURDAY   INIT      "Current Report"
D30DAYS   INIT      "30 Days only"
D60DAYS   INIT      "60 Days only"
D90DAYS   INIT      "90 Days only"
D120DAYS  INIT      "120 Days only"
.
ENDDATE   DIM       8                            * end date for period search
STRTDATE  DIM       8                            * start date for period search
SAVCC     FORM      2                            * saved century
SAVDAY    FORM      3                            * saved julian day
SAVYR     FORM      2                            * saved julian day
SITE      DIM       6
SAVEVAR   DIM       10
SORTFLAG  FORM      1
STOTPAID  FORM     12.2
STOTCURR  FORM     12.2
STOT30DY  FORM     12.2
STOT60DY  FORM     12.2
STOT90DY  FORM     12.2
STO120DY  FORM     12.2
STOTAMNT  FORM     12.2
STOTCLMN  FORM     12.2
STOTRCVA  FORM     12.2
.
TOTPAID   FORM     12.2
TOTCURR   FORM     12.2
TOT30DAY  FORM     12.2
TOT60DAY  FORM     12.2
TOT90DAY  FORM     12.2
TO120DAY  FORM     12.2
TOTAMNT   FORM     12.2
TOTCLMN   FORM     12.2
TOTRCVA   FORM     12.2
.
TODATE    DIM       8
TOTPER    FORM      1        * total periods on report
TOTLFLAG  FORM      1                            * print total only flag
.                                                    0 = no
.                                                    1 = yes
WEBUSRID  DIM       10
.
PRGID     INIT      "IBAHIC08"
PRGNAM    INIT      "Aged Analysis"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
          BRANCH    EXIT,ML9999
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
.
          CALL      PRCF0000               * keyin practice from
          BRANCH    EXIT,ML0100
.
          CALL      PRCT0000               * keyin practice to
          BRANCH    EXIT,ML0100
.
          CALL      FCLN0000               * keyin clinic from
          BRANCH    EXIT,ML0100
.
          CALL      TCLN0000               * keyin clinic to
          BRANCH    EXIT,ML0100
.
          CALL      KPRV0000               * keyin provider
.
          CALL      ASAT0000               * keyin as at date
.
          CALL      PTOT0000               * Total only
.
          CALL      DAYS0000               * Period
.
          CALL      WEBU0000               * keyin user
          BRANCH    EXIT,ML0100
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML6000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML6000    CALL      CONV0000               * calculate ageing cutoff dates
.
          CALL      CREA0000               * Create tempfile
.
          CALL      PROC0000               * process clinic id/medical prac
.
          CALL      PRIN0000               * Print data
          CALL      ENDP0000               * End of report
.
          CALL      KILL0000               * Delete tempfile
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          CALL      IBACLOCK
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,TEMPFILA
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,TEMPFILB
.
          OPEN      HICCITA1,"hiccitaf"       * item claim file
          OPEN      HICCLMA1,"hicclmaf"       * claim file
          OPEN      HICCLMA4,"hicclmaf"       * claim file
          OPEN      HICCLMA6,"hicclmaf"       * claim file
          OPEN      HICCLMA8,"hicclmaf"       * claim file
          OPEN      PRIPRAC1,"pripracf"       * private practice
          OPEN      PRIDOCT1,"pridoctf"       * practice doctors
          OPEN      OUTSITA1,"outsitaf"       * outpatient site file
          OPEN      PATCODE1,"patcodes"       * codes file
          OPEN      PMSHCPA1,"pmshcpaf"       * HCP file
          OPEN      WEBSECA1,"websecaf"       * web user id
          OPEN      PRIALSA1,"prialsaf"       * all practice security
          OPEN      PRISECA1,"prisecaf"       * practice security
          OPEN      PATMA1A1,"patma1af"       * master file
          OPEN      PATMX1A1,"patmx1af"
          OPEN      HICITIA3,"hicitiaf"
.
          OPEN      OUTSITA1,"outsitaf"
          MOVE      IDDD,SITE
          MOVE      IDDD,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD,INIT9000
.
          PACK      CFNAME WITH UID,FILCLIA1        * OUTCLIFD
          OPEN      OUTCLIA1,CFNAME
.
          MOVE      ZERO,EXIT
          GOTO      INIT9999
.
INIT9000  DISPLAY   *P1:24,*EL,*B,"Site Code not on file. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
          GOTO      INIT9999
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:6,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:6,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          MOVE      SP70,CLINFROM
          MOVE      SP70,CLINICTO
          MOVE      SP70,PRACTIFR
          MOVE      SP70,PRACTITO
          MOVE      SP70,FILTPROV
          MOVE      SP70,TODATE
          MOVE      ZERO,TOTLFLAG
.
          BRANCH    COPTION,OPTN9000          
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
.
.**************************************************************************
.*                              CREA0000               Called by:         *
.*             create a new temporary file                                *
.**************************************************************************
.
CREA0000  CALL      KILL0000                     * remove existing file
.
          BRANCH    DOPTION,CREA2000             * full report
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,TEMPFILB,UKEYB
          EXECUTE   CMDLINE,TASKID               * create temporary index file
.
          OPEN      HICTEMB1,TEMPFILB            * open temp index file
          OPEN      HICTEMB2,TEMPFILB            * open temp index file
          OPEN      HICTEMB3,TEMPFILB            * open temp index file
          OPEN      HICTEMB4,TEMPFILB            * open temp index file
          GOTO      CREA4000
.
CREA2000  CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,TEMPFILA,UKEYA
          EXECUTE   CMDLINE,TASKID               * create temporary index file
.
          OPEN      HICTEMA1,TEMPFILA            * open temp index file
          OPEN      HICTEMA2,TEMPFILA            * open temp index file
          OPEN      HICTEMA3,TEMPFILA            * open temp index file
          OPEN      HICTEMA4,TEMPFILA            * open temp index file
.
CREA4000  MOVE      ZERO,ALACCESS                * Set all access to no
          PACK      KEY10,WEBUSRID,SP70
          CALL      RDPRALS1
          IF        OVRCD=0
            MOVE      ONE,ALACCESS
          ENDIF
.
CREA9999  RETURN
+
.****************************************************************************
.*                              KILL0000               Called by:           *
.*               close and erase the temporary file                         *
.****************************************************************************
.
KILL0000  CLOSE     HICTEMA1                     * close file
          CLOSE     HICTEMA2
          CLOSE     HICTEMA3
          CLOSE     HICTEMA4
          CLOSE     HICTEMB1
          CLOSE     HICTEMB2
          CLOSE     HICTEMB3
          CLOSE     HICTEMB4
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ERASE,TEMPFILA       * set file erase command
          EXECUTE   CMDLINE,TASKID               * erase temp file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ERASE,TEMPFILB       * set file erase command
          EXECUTE   CMDLINE,TASKID               * erase temp file
.
KILL9999 RETURN
+
.**********************************************************************
.*                         PROC0000                                   *
.*                    By clinic id and medical practice               *
.**********************************************************************
PROC0000  MOVE      ONE,SORTFLAG         * sort by clinic id
          MATCH     SP70,CLINFROM
          GOTO      PROC1000 IF NOT EQUAL
.
          MOVE      TWO,SORTFLAG         * default to sort by medical practice
          MATCH     SP70,PRACTIFR
          GOTO      PROC1000 IF NOT EQUAL
.
          MOVE      ONE,SORTFLAG         * sort by clinic id
          MATCH     SP70,FILTPROV
          GOTO      PROC1000 IF EQUAL    * sort by clinic id if all blank
.
          MOVE      THREE,SORTFLAG       * sort by provider
.
.         Loop through the Claim file
.
PROC1000  UNPACK    SP70,TEMPSNAM,TEMPGNAM
          MOVE      " 1",HCCLSTAT
          PACK      KEY18,HCCLSTAT,SP70
.
          MOVE      TODATE,ENDDATE
          BRANCH    DOPTION,PROC2000
.
          LOAD      STRTDATE,DOPTION,SP8,DATPER1,DATPER2,DATPER3,DATPER4,SP8
          LOAD      ENDDATE,DOPTION,SP8,TODATE,DATPER1,DATPER2,DATPER3,DATPER4
          PACK      KEY18,HCCLSTAT,STRTDATE,SP70
.
PROC2000  CALL      RSHCCLM8
PROC2200  CALL      RKHCCLM8
          BRANCH    OVRCD,PROC9999
          MATCH     " 1",HCCLSTAT           * Batched?
          GOTO      PROC9999 IF NOT EQUAL
.
          MATCH     HCCLDATE,ENDDATE        * date in range ?
          GOTO      PROC9999 IF LESS        * no - finish
.
.         Check if paid
.
          MOVE      ZERO,FORM102
          MOVE      HCCLCAMN,FORM102
          SUB       HCCLRCVA,FORM102        * amount received
          ADD       HCCLADJM,FORM102        * - adjustment
          SUB       HCCLWOFF,FORM102        * - written off
.
          COMPARE   ZERO,FORM102
          GOTO      PROC2200 IF EQUAL       * no outstanding amount
.
          CALL      CRNG0000                * Check the selected range
          BRANCH    EXIT,PROC2200
.
          BRANCH    ALACCESS,PROC5000       * Access to all practices 
.
          PACK      KEY16,HCCLMPRA,WEBUSRID,SP70
          CALL      RDPRSEC1
          BRANCH    OVRCD,PROC2200          * No access to this practice
.
PROC5000  UNPACK    SP70,TEMPSNAM,TEMPGNAM
          MOVE      HCCLURNO,KEY8
          CALL      RDMAST1
          IF        OVRCD=0
            MOVE      PSNAME,TEMPSNAM       * patient surname
            MOVE      PGNAME,TEMPGNAM       * patient given name
          ENDIF
.
          CALL      WTMP0000                * write to tempfile
          GOTO      PROC2200
.
PROC9999  RETURN
+
.**********************************************************************
.*                         CRNG0000                                   *
.*                        Check the selected range                    *
.**********************************************************************
CRNG0000  MOVE      ZERO,EXIT
          MATCH     SP70,CLINFROM             * check range of clinic id
          IF        !@EQUAL
            MATCH     CLINFROM,HCCLCLID
            GOTO      CRNG9000 IF LESS
          ENDIF
.
          MATCH     SP70,CLINICTO             * check range of clinic id
          IF        !@EQUAL
            MATCH     HCCLCLID,CLINICTO
            GOTO      CRNG9000 IF LESS
          ENDIF
.
          MATCH     SP70,PRACTIFR             * check range of medical practice
          IF        !@EQUAL
            MATCH     PRACTIFR,HCCLMPRA
            GOTO      CRNG9000 IF LESS
          ENDIF
.
          MATCH     SP70,PRACTITO             * check range of medical practice
          IF        !@EQUAL
            MATCH     HCCLMPRA,PRACTITO
            GOTO      CRNG9000 IF LESS
          ENDIF
.
          MATCH     SP70,FILTPROV             * check provider number
          IF        !@EQUAL
            MATCH     HCCLPSRV,FILTPROV
            GOTO      CRNG9000 IF NOT EQUAL
          ENDIF
.
          MATCH     IDDD,HCCLSITE           * Same Site
          GOTO      CRNG9000 IF NOT EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      CRNG9999
.
CRNG9000  MOVE      ONE,EXIT
CRNG9999  RETURN
+
.**********************************************************************
.*                         WTMP0000                                   *
.*                        Write to tempfile                           *
.**********************************************************************
WTMP0000  BRANCH    DOPTION,WTMP6000        * full report
.
          MOVE      HCCLCLMN,TEMPCLMN       * claim Number
          PACK      KEY8,TEMPCLMN,SP70
          CALL      RDTEMPB
          BRANCH    OVRCD,WTMP2000
.
          ADD       HCCLCAMN,TEMPAMNT
          ADD       HCCLRCVA,TEMPPAID
.
          MOVE      HCCLCAMN,TEMPCURD
          SUB       HCCLRCVA,TEMPCURD
          ADD       TEMPCURD,TEMPOUTS
.
          CALL      UPTEMPB
          GOTO      WTMP9999
.
WTMP2000  CALL      SETFLD00               * setup fields
          CALL      WRTEMPB
          GOTO      WTMP9999
.
.         Full report
.
WTMP6000  MOVE      HCCLCAMN,FORM102
          SUB       HCCLRCVA,FORM102
          MOVE      HCCLDATE,TEMPCDAT       * Claim Date
          CALL      CALP0000                * calculate age period amount
.
          MOVE      HCCLURNO,TEMPURNO
          PACK      KEY8,TEMPURNO,SP70
          CALL      RDTEMPA
          BRANCH    OVRCD,WTMP8000
.
          ADD       HCCLRCVA,TEMPPAID       * amount received
          ADD       CUROUTS,TEMPCURD
          ADD       OUT30D,TEMP30DY
          ADD       OUT60D,TEMP60DY
          ADD       OUT90D,TEMP90DY
          ADD       OUT120D,TEMP120D
          ADD       FORM102,TEMPOUTS
          CALL      UPTEMPA
          GOTO      WTMP9999
.
WTMP8000  CALL      SETFLD00
          CALL      WRTEMPA
.
WTMP9999  RETURN
.
.**********************************************************************
.                          SETFLD00
.**********************************************************************
SETFLD00  MOVE      HCCLCLID,TEMPCLID
          MOVE      HCCLMPRA,TEMPMPRA
          MOVE      HCCLPSRV,TEMPPSRV
          MOVE      HCCLURNO,TEMPURNO
.
          MOVE      HCCLCAMN,TEMPOUTS
          SUB       HCCLRCVA,TEMPOUTS
          MOVE      HCCLRCVA,TEMPPAID       * amount received
.
          IF        DOPTION=1
            MOVE      CUROUTS,TEMPCURD
            MOVE      OUT30D,TEMP30DY
            MOVE      OUT60D,TEMP60DY
            MOVE      OUT90D,TEMP90DY
            MOVE      OUT120D,TEMP120D
          ELSE
            MOVE      HCCLCAMN,TEMPAMNT
            MOVE      HCCLCLMN,TEMPCLMN       * Claim Number
            MOVE      HCCLDATE,TEMPCDAT       * Claim Date
          ENDIF
          MOVE      PGNAME,TEMPGNAM
          MOVE      PSNAME,TEMPSNAM
SETFLD99  RETURN
+
.****************************************************************************
.*                               CALP0000              Called by: FULL0000  *
.*                add Claimed amount to correct age period        PATF0000  *
.*                for full report
.****************************************************************************
CALP0000  MOVE      ZERO,CUROUTS
          MOVE      ZERO,OUT30D 
          MOVE      ZERO,OUT60D 
          MOVE      ZERO,OUT90D
          MOVE      ZERO,OUT120D 
.
CALP0500  MATCH     TEMPCDAT,DATPER4             * In 120 day period ?
          GOTO      CALP3000 IF LESS             * no - check 90 day
          ADD       FORM102,OUT120D              * yes
          GOTO      CALP9000
.
CALP3000  MATCH     TEMPCDAT,DATPER3             * In 90 day period ?
          GOTO      CALP4000 IF LESS             * no - check 60 day
          ADD       FORM102,OUT90D               * yes
          GOTO      CALP9000
.
CALP4000  MATCH     TEMPCDAT,DATPER2             * In 60 day period ?
          GOTO      CALP5000 IF LESS             * no - check 30 day
          ADD       FORM102,OUT60D               * yes
          GOTO      CALP9000
.
CALP5000  MATCH     TEMPCDAT,DATPER1             * In 30 day period ?
          GOTO      CALP6000 IF LESS             * no - current period
          ADD       FORM102,OUT30D               * yes
          GOTO      CALP9000
.
CALP6000  ADD       FORM102,CUROUTS              * increment current period
.
CALP9000  
CALP9999  RETURN
+
.**********************************************************************
.*                         PRIN0000                                   *
.*                        Print Data                                  *
.**********************************************************************
PRIN0000  MOVE      ZERO,CPAGENO
          MOVE      ZERO,STOTPAID
          MOVE      ZERO,STOT30DY
          MOVE      ZERO,STOT60DY
          MOVE      ZERO,STOT90DY
          MOVE      ZERO,STO120DY
          MOVE      ZERO,STOTAMNT
          MOVE      ZERO,STOTCLMN
          MOVE      ZERO,STOTRCVA
.
          MOVE      ZERO,TOTPAID
          MOVE      ZERO,TOTCURR
          MOVE      ZERO,TOT30DAY
          MOVE      ZERO,TOT60DAY
          MOVE      ZERO,TOT90DAY
          MOVE      ZERO,TO120DAY
          MOVE      ZERO,TOTAMNT
          MOVE      ZERO,TOTCLMN
          MOVE      ZERO,TOTRCVA
.
	  MOVE      SP70,SAVEVAR
	  MOVE      SP70,KEY34
          MOVE      SP70,KEY36
          MOVE      SP70,KEY38
          CALL      RSTEMPF
PRIN1000  CALL      RKTEMPF
          BRANCH    OVRCD,PRIN9000
.
          MATCH     SP70,SAVEVAR
          GOTO      PRIN1200 IF EQUAL            * first record
          CALL      CVAR0000                     * check if sort field changed
          BRANCH    EXIT,PRIN1100                * changed
.
          GOTO      PRIN1300
.
PRIN1100  CALL      PSUB0000         * Print sub-total
          MOVE      ONE,CNOUNDLN                 * no underline
.
PRIN1200  CALL      HEAD0000
          MOVE      ZERO,CNOUNDLN
          CALL      SVAR0000                     * save sort variable
.
PRIN1300  UNPACK    SP70,KEY28
          MOVE      TEMPURNO,KEY8
          CALL      RDMAST1
          IF        OVRCD=0
            MOVE      PGNAME TO ANS
            PACK      KEY17 WITH ANS,DOT,PSNAME
            MOVE      PSNAME,PACSNAME
            MOVE      PGNAME,PACGNAME
            MOVE      PTITL,PACTITLE
            MOVE      ONE,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,KEY28
          ENDIF
.
          COMPARE   "48",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          BRANCH    TOTLFLAG,PRIN3000           * Print total only
          BRANCH    DOPTION,PRIN2000
.
          MATCH     SP70,TEMPCDAT               * Claim date
          IF        !@EQUAL
            UNPACK    TEMPCDAT,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
          ENDIF
          PRINT     *1,TEMPURNO,*10,KEY28:
                    *41,PTELEP,*61,TEMPAMNT:
                    *76,TEMPPAID,*91,TEMPOUTS:
                    *110,TEMPCLMN,*121,CPCDATE
.
          ADD       ONE,CLNO
          GOTO      PRIN3000
.
PRIN2000  PRINT     *1,TEMPURNO,*10,KEY17:
                    *27,TEMPPAID,*42,TEMPCURD:
                    *57,TEMP30DY,*72,TEMP60DY:
                    *87,TEMP90DY,*102,TEMP120D:
                    *117,TEMPOUTS
          ADD       ONE,CLNO
.
PRIN3000  MOVE      ZERO,HEADFLAG
          CALL      ATOT0000              * add totals
          GOTO      PRIN1000              * read next record
.
PRIN9000  IF        CPAGENO=0
            CALL      HEAD0000
          ELSE
            CALL      PSUB0000            * print sub-total
          ENDIF
.
PRIN9999  RETURN
+
.*********************************************************************
.         Print sub-total
.*********************************************************************
PSUB0000  CALL      UND132
          COMPARE   THREE,SORTFLAG
          GOTO      PSUB9999 IF EQUAL     * by provider number
.
          BRANCH    DOPTION,PSUB1000
          PRINT     *9,"SUB-TOTAL",*61,STOTCLMN:
                    *76,STOTRCVA,*91,STOTAMNT
          CALL      UND132
          ADD       TWO,CLNO
          GOTO      PSUB2000
.
PSUB1000  PRINT     *9,"SUB-TOTAL",*27,STOTPAID,*42,STOTCURR:
                    *57,STOT30DY,*72,STOT60DY,*87,STOT90DY:
                    *102,STO120DY,*117,STOTAMNT
          CALL      UND132
          ADD       TWO,CLNO
.
PSUB2000  MOVE      ZERO,STOTPAID      * Amount Paid
          MOVE      ZERO,STOTCURR      * Current outstanding amount
          MOVE      ZERO,STOT30DY      * 30 Days outstanding amount
          MOVE      ZERO,STOT60DY      * 60 Days outstanding amount
          MOVE      ZERO,STOT90DY      * 90 Days outstanding amount
          MOVE      ZERO,STO120DY      * 120 Days outstanding amount
          MOVE      ZERO,STOTAMNT      * Total Amount outstanding
          MOVE      ZERO,STOTCLMN      * Total Amount claimed
          MOVE      ZERO,STOTRCVA      * Total Amount received
.
PSUB9999  RETURN
.
.*********************************************************************
.         Add totals
.*********************************************************************
ATOT0000  ADD       TEMPPAID,STOTPAID       * Amount Paid
          ADD       TEMPCURD,STOTCURR       * Current outstanding amount
          ADD       TEMP30DY,STOT30DY       * 30 Days outstanding amount
          ADD       TEMP60DY,STOT60DY       * 60 Days outstanding amount
          ADD       TEMP90DY,STOT90DY       * 90 Days outstanding amount
          ADD       TEMP120D,STO120DY       * 120 Days outstanding amount
          ADD       TEMPOUTS,STOTAMNT       * Total Amount outstanding
          ADD       TEMPAMNT,STOTCLMN       * Total Amount claimed
          ADD       TEMPPAID,STOTRCVA       * Total Amount received
.
          ADD       TEMPPAID,TOTPAID        * Amount Paid
          ADD       TEMPCURD,TOTCURR        * Current outstanding amount
          ADD       TEMP30DY,TOT30DAY       * 30 Days outstanding amount
          ADD       TEMP60DY,TOT60DAY       * 60 Days outstanding amount
          ADD       TEMP90DY,TOT90DAY       * 90 Days outstanding amount
          ADD       TEMP120D,TO120DAY       * 120 Days outstanding amount
          ADD       TEMPOUTS,TOTAMNT        * Total Amount outstanding
          ADD       TEMPAMNT,TOTCLMN        * Total Amount claimed
          ADD       TEMPPAID,TOTRCVA        * Total Amount received
.
ATOT9999  RETURN
+
.*********************************************************************
.         Save sort variable
.*********************************************************************
SVAR0000  MOVE      SP70,SAVEVAR
          COMPARE   ONE,SORTFLAG
          GOTO      SVAR4000 IF EQUAL    * sort by clinic id
          COMPARE   TWO,SORTFLAG
          GOTO      SVAR6000 IF EQUAL    * sort by medical practice
.
.         Sort by service provider
.
          PACK      SAVEVAR,TEMPPSRV,SP70
          GOTO      SVAR9999
.
.         Sort by clinic id
.
SVAR4000  PACK      SAVEVAR,TEMPCLID,SP70
          GOTO      SVAR9999
.
.         Sort by medical practice
.
SVAR6000  PACK      SAVEVAR,TEMPMPRA,SP70
.
SVAR9999  RETURN
+
.*********************************************************************
.         check if sort variable change
.*********************************************************************
CVAR0000  MOVE      ZERO,EXIT
          COMPARE   ONE,SORTFLAG
          GOTO      CVAR4000 IF EQUAL    * sort by clinic id
          COMPARE   TWO,SORTFLAG
          GOTO      CVAR6000 IF EQUAL    * sort by medical practice
.
          MATCH     SAVEVAR,TEMPPSRV     * check service provider
          GOTO      CVAR9999 IF EQUAL
          GOTO      CVAR9000
.
CVAR4000  MATCH     SAVEVAR,TEMPCLID     * check clinic id
          GOTO      CVAR9999 IF EQUAL
          GOTO      CVAR9000
.
CVAR6000  MATCH     SAVEVAR,TEMPMPRA     * check medical practice
          GOTO      CVAR9999 IF EQUAL
.
CVAR9000  MOVE      ONE,EXIT
CVAR9999  RETURN

.*********************************************************************
.         print heading
.*********************************************************************
PRHD0000  COMPARE   ONE,SORTFLAG
          GOTO      PRHD4000 IF EQUAL    * sort by clinic id
          COMPARE   TWO,SORTFLAG
          GOTO      PRHD6000 IF EQUAL    * sort by medical practice
          GOTO      PRHD8000             * by provider no
.
PRHD4000  PACK      KEY20,IDDD,TEMPCLID,Z70
          CALL      RDSCLIA1
          CALL      RDPCLIA1                *To get Clinic ID Description
          IF        OVRCD=0
            MATCH     IDDD,OCASITE
            IF        @EQUAL
              MATCH     TEMPCLID,OCACLIN
              GOTO      PRHD4200 IF EQUAL
            ENDIF
          ENDIF
.
          PACK      KEY10,OCADOCT,SP70
          CALL      RDPMHCP1
          IF        OVRCD=0
            MOVE      PMHCSNAM,PACSNAME
            MOVE      PMHCGNAM,PACGNAME
            MOVE      PMHCTITL,PACTITLE
            MOVE      ONE,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,OCADESC
            GOTO      PRHD4200
          ENDIF
          MOVE      SP70,OCADESC
.
PRHD4200  PRINT     *N,*1,"Clinic Id : ",OCADESC,"(",TEMPPSRV,")"
          GOTO      PRHD9000
.
PRHD6000  PACK      PRPRDESC,SP70
          PACK      KEY6,TEMPMPRA
          CALL      RDPRPR1
          PRINT     *N,*1,"Medical Practice : ",PRPRDESC
          GOTO      PRHD9000
.
PRHD8000  MATCH     "All",HEADING0
          GOTO      PRHD9999 IF NOT EQUAL
          PRINT     *N,*1,"Provider No.: ",TEMPPSRV
.
PRHD9000  ADD       TWO,CLNO
PRHD9999  RETURN
+
.*********************************************************************
.         print a new page
.*********************************************************************
HEAD0000  CALL      HEAD132
.
          MOVE      SP70,REPTYPE
          MOVE      DOPTION,FORM1
          SUB       ONE,FORM1
          MOVE      "Full Report",REPTYPE
          LOAD      REPTYPE,FORM1,DCURDAY,D30DAYS,D60DAYS,D90DAYS,D120DAYS
.
          UNPACK    TODATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *40,"As at : ",CPCDATE,"  ( ",REPTYPE,")"
.
          PRINT     *1,"Medical Practice From : ",HEADING7," To : ",HEADING8
          PRINT     *1,"Clinic From           : ",HEADING5," To : ",HEADING6
          PRINT     *1,"Provider Number       : ",HEADING0
.
          ADD       FIVE,CLNO
.
          CALL      PRHD0000                     * print sort heading
          CALL      UND132
          BRANCH    DOPTION,HEAD2000
.
          PRINT     *1,"  U/R No",*10,"Patient Name":
                    *41,"Telephone No",*61,"         Amount":
                    *76,"        Amount ",*91,"         Amount":
                    *110,"  Claim  ",*121,"   Claim  ":
                    *N,*61,"        Claimed":
                    *76,"       Received ",*91,"    Outstanding":
                    *110,"  Number",*121,"   Date"
          GOTO      HEAD4000
.
HEAD2000  PRINT     *2," U/R No",*10,"Patient Name":
                    *27,"           Paid",*42,"        Current":
                    *57,"        30 days",*72,"        60 days":
                    *87,"        90 days",*102,"       120 days":
                    *117,"          Total"
.
HEAD4000  ADD       TWO,CLNO
          CALL      UND132
          MOVE      ONE,HEADFLAG
.
HEAD9999  RETURN
+
.**********************************************************************
.*                           ENDP0000                                 *
.*                      Print the end of the report                   *
.**********************************************************************
ENDP0000  COMPARE   ZERO,CPAGENO
          GOTO      ENDP0100 IF NOT EQUAL
          CALL      HEAD0000

ENDP0100  BRANCH    DOPTION,ENDP1000
.
          PRINT     *9,"TOTAL",*61,TOTCLMN:
                    *76,TOTRCVA,*91,TOTAMNT
          CALL      UND132
          GOTO      ENDP9000
.
ENDP1000  PRINT     *9,"TOTAL",*27,TOTPAID,*42,TOTCURR:
                    *57,TOT30DAY,*72,TOT60DAY,*87,TOT90DAY:
                    *102,TO120DAY,*117,TOTAMNT
          CALL      UND132
.
ENDP9000  PRINT     *N,*50,"*** End of Report ***"
ENDP9999  RETURN
+
.******************************************************************************
.*                                   FCLN0000             Called by: ML0000   *
.*                             Keyin The Clinic Code From                     *
.******************************************************************************
FCLN0000  KEYIN     *P1:8,*EL,"Clinic Id From : ",*V2LON,CLINFROM
.
          PACK      CLINFROM,CLINFROM,SP70
          MATCH     SP70,CLINFROM
          GOTO      FCLN6000 IF EQUAL
.
          PACK      KEY20,IDDD,CLINFROM,Z70
          CALL      RDSCLIA1
FCLN1000  CALL      RDPCLIA1
          BRANCH    OVRCD,FCLN9500
.
          MATCH     OCASITE,IDDD         * Must be in the Same Site
          GOTO      FCLN9500 IF NOT EQUAL
.
          MATCH     OCACLIN,CLINFROM     * Must be in the Same clinic
          GOTO      FCLN9500 IF NOT EQUAL
.
          MATCH     "1",OTCLIACT
          GOTO      FCLN1000 IF EQUAL
.
          MATCH     SP70,OCADESC            * Read HCP if no doctor
          GOTO      FCLN5000 IF NOT EQUAL
.
          PACK      KEY10,OCADOCT,SP70
          CALL      RDPMHCP1
          IF        OVRCD=0
            MOVE      PMHCSNAM,PACSNAME
            MOVE      PMHCGNAM,PACGNAME
            MOVE      PMHCTITL,PACTITLE
            MOVE      ONE,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,OCADESC
          ELSE
            MOVE      "Invalid clinic HCP Code",OCADESC
          ENDIF
.
FCLN5000  MOVE      OCADESC,HEADING5
          DISPLAY   *P25:8,*EL,*V2LON,OCADESC;
          MOVE      ZERO,EXIT
          GOTO      FCLN9999
.
FCLN6000  MOVE      "Start",HEADING5
          DISPLAY   *P25:8,*EL,*V2LON,"Start";
          MOVE      ZERO,EXIT
          GOTO      FCLN9999
.
FCLN9500  DISPLAY   *P1:24,*EL,*B,"Invalid Clinic id. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
FCLN9999  RETURN
+
.******************************************************************************
.*                                   TCLN0000             Called by: ML0000   *
.*                             Keyin The Clinic Code To                       *
.******************************************************************************
TCLN0000  KEYIN     *P1:9,*EL,"Clinic Id To   : ",*V2LON,CLINICTO
.
          PACK      CLINICTO,CLINICTO,SP70
          MATCH     SP70,CLINICTO
          GOTO      TCLN6000 IF EQUAL
.
          PACK      KEY20,IDDD,CLINICTO,Z70
          CALL      RDSCLIA1
TCLN1000  CALL      RDPCLIA1
          BRANCH    OVRCD,TCLN9500
.
          MATCH     OCASITE,IDDD         * Must be in the Same Site
          GOTO      TCLN9500 IF NOT EQUAL
.
          MATCH     OCACLIN,CLINICTO     * Must be in the Same clinic
          GOTO      TCLN9500 IF NOT EQUAL
.
          MATCH     "1",OTCLIACT
          GOTO      TCLN1000 IF EQUAL
.
          MATCH     SP70,OCADESC            * Read HCP if no doctor
          GOTO      TCLN5000 IF NOT EQUAL
.
          PACK      KEY10,OCADOCT,SP70
          CALL      RDPMHCP1
          IF        OVRCD=0
            MOVE      PMHCSNAM,PACSNAME
            MOVE      PMHCGNAM,PACGNAME
            MOVE      PMHCTITL,PACTITLE
            MOVE      ONE,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,OCADESC
          ELSE
            MOVE      "Invalid clinic HCP Code",OCADESC
          ENDIF
.
TCLN5000  MOVE      OCADESC,HEADING6
          DISPLAY   *P25:9,*EL,*V2LON,OCADESC;
          MOVE      ZERO,EXIT
          GOTO      TCLN9999
.
TCLN6000  MOVE      "Finish",HEADING6
          DISPLAY   *P25:9,*EL,*V2LON,"Finish";
          MOVE      ZERO,EXIT
          GOTO      TCLN9999
.
TCLN9500  DISPLAY   *P1:24,*EL,*B,"Invalid Clinic id. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
TCLN9999  RETURN
.
+
.******************************************************************************
.*                                   PRCF0000             Called by: ML0000   *
.*                             Keyin The medical practice from                *
.******************************************************************************
PRCF0000  KEYIN     *P1:10,*EL,"Medical Practice : ",*V2LON,PRACTIFR
.
          PACK      PRACTIFR,PRACTIFR,SP70
          MATCH     SP70,PRACTIFR
          GOTO      PRCF6000 IF EQUAL
.
          PACK      KEY6,PRACTIFR,SP70
          CALL      RDPRPR1
          BRANCH    OVRCD,PRCF9500
.
PRCF5000  MOVE      PRPRDESC,HEADING7
          DISPLAY   *P27:10,*EL,*V2LON,PRPRDESC;
          MOVE      ZERO,EXIT
          GOTO      PRCF9999
.
PRCF6000  MOVE      "Start",HEADING7
          DISPLAY   *P27:10,*EL,*V2LON,"Start";
          MOVE      ZERO,EXIT
          GOTO      PRCF9999
.
PRCF9500  DISPLAY   *P1:24,*EL,*B,"Invalid Medical Practice. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
PRCF9999  RETURN
+
.******************************************************************************
.*                                   PRCT0000             Called by: ML0000   *
.*                             Keyin The medical practice from                *
.******************************************************************************
PRCT0000  KEYIN     *P1:11,*EL,"Medical Practice : ",*V2LON,PRACTITO
.
          PACK      PRACTITO,PRACTITO,SP70
          MATCH     SP70,PRACTITO
          GOTO      PRCT6000 IF EQUAL
.
          PACK      KEY6,PRACTITO,SP70
          CALL      RDPRPR1
          BRANCH    OVRCD,PRCT9500
.
PRCT5000  MOVE      PRPRDESC,HEADING8
          DISPLAY   *P27:11,*EL,*V2LON,PRPRDESC;
          MOVE      ZERO,EXIT
          GOTO      PRCT9999
.
PRCT6000  MOVE      "Finish",HEADING8
          DISPLAY   *P27:11,*EL,*V2LON,"Finish";
          MOVE      ZERO,EXIT
          GOTO      PRCT9999
.
PRCT9500  DISPLAY   *P1:24,*EL,*B,"Invalid Medical Practice. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
PRCT9999  RETURN
.
+
.******************************************************************************
.*                                   KPRV0000             Called by: ML0000   *
.*                             Keyin the provider number                      *
.******************************************************************************
KPRV0000  KEYIN     *P1:12,*EL,"Provider Number : ",*V2LON,FILTPROV
.
          PACK      FILTPROV,FILTPROV,SP70
          MATCH     SP70,FILTPROV
          GOTO      KPRV6000 IF EQUAL
.
KPRV5000  MOVE      FILTPROV,HEADING0
          DISPLAY   *P29:12,*EL,*V2LON,FILTPROV;
          MOVE      ZERO,EXIT
          GOTO      KPRV9999
.
KPRV6000  MOVE      "All",HEADING0
          DISPLAY   *P29:12,*EL,*V2LON,"All";
          MOVE      ZERO,EXIT
          GOTO      KPRV9999
.
KPRV9999  RETURN
+
.******************************************************************************
.*                                   ASAT0000             Called by: ML0000   *
.*                             Keyin the as at date                           *
.******************************************************************************
ASAT0000  DISPLAY   *P1:13,*EL,"As at Date : "
          PACK      TODATE,CCC,CYY,CMM,CDD
          REP       " 0",TODATE
          MOVE      ZERO,CHIGHLT
          MOVE      ZERO,CDEFDTE
          MOVE      TEN3,CVERT
          MOVE      TEN3,CCOL
          UNPACK    TODATE,CCENT,CYEAR,CMON,CDAY
          CALL      KEYDATE
          BRANCH    OVRCD,DRNG9999
          PACK      TODATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE
.
DRNG9999  RETURN
+
.****************************************************************************
.*                               PTOT0000              Called by: ML0000    *
.*                see if totals only are to be printed                      *
.****************************************************************************
PTOT0000  KEYIN     *P1:14,*EL,"Print totals only (":
                    *V2LON,*DV,ANSY,*HOFF,*DV,SLASH,*V2LON,*DV,ANSN,*HOFF:
                    ") ? ",*DV,ANSN,*P27:14,*V2LON,ANS;
          ENDSET    ANS
          APPEND    SP1,ANS
          RESET     ANS
.
          MATCH     SP1,ANS                 * anything entered ?
          GOTO      PTOT1000 IF EQUAL       * no
          REP       UPPLOW,ANS
          MATCH     ANSN,ANS                * N entered ?
          GOTO      PTOT1000 IF EQUAL       * yes
          MATCH     ANSY,ANS                * Y entered ?
          GOTO      PTOT2000 IF EQUAL       * yes
          BEEP
          GOTO      PTOT0000                * invalid
.
PTOT1000  DISPLAY   *P27:14,*V2LON,NO;
          MOVE      ZERO,TOTLFLAG
          GOTO      PTOT9999
.
PTOT2000  DISPLAY   *P27:14,*V2LON,YES;
          MOVE      ONE,TOTLFLAG
.
PTOT9999  RETURN
+
.******************************************************************************
.*                                   DAYS0000             Called by: ML0000   *
.*                             Keyin Period number of days                    *
.******************************************************************************
DAYS0000  DISPLAY   *P1:16,*EF,*V2LON,ONE,*HOFF," = Full Report":
                    *P1:17,*V2LON,TWO,*HOFF," = Current Only":
                    *P1:18,*V2LON,THREE,*HOFF," = 30 Days Only":
                    *P1:19,*V2LON,FOUR,*HOFF," = 60 Days Only":
                    *P1:20,*V2LON,FIVE,*HOFF," = 90 Days Only":
                    *P1:21,*V2LON,SIX,*HOFF," = 120+ Days Only";
.
          KEYIN     *P1:22,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:22,*V2LON,DOPTION;
.
          COMPARE   ZERO,DOPTION                 * anything input ?
          GOTO      DAYS9500 IF EQUAL            * no
.
          MOVE      ZERO,EXIT
          BRANCH    DOPTION,DAYS9999,DAYS9999,DAYS9999:
                            DAYS9999,DAYS9999,DAYS9999
          GOTO      DAYS9999
.
DAYS9500  MOVE      ONE,EXIT
.
DAYS9999  RETURN
+
.******************************************************************************
.*                                   WEBU0000             Called by: ML0000   *
.*                             Keyin The web user id                          *
.******************************************************************************
WEBU0000  MOVE      SP70,WEBUSRID
          KEYIN     *P1:23,*EL,"User Id : ",*V2LON,WEBUSRID
.
          PACK      WEBUSRID,WEBUSRID,SP70
.
          PACK      KEY10,WEBUSRID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,WEBU9500
.
          MOVE      ZERO,EXIT
          GOTO      WEBU9999
.
WEBU9500  DISPLAY   *P1:24,*EL,*B,"Invalid User Id. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
WEBU9999  RETURN
+
.****************************************************************************
.*                               CONV0000              Called by: ML0000    *
.*              calculate the dates for ageing                              *
.****************************************************************************
CONV0000  MOVE      SP70,DATPER1
          MOVE      SP70,DATPER2
          MOVE      SP70,DATPER3
          MOVE      SP70,DATPER4
.
          UNPACK    TODATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,CC
          MOVE      CYEAR,YY
          MOVE      CMON,MM
          MOVE      CDAY,DD
          CALL      DMYCON                       * convert "as at date" to
.                                                  julian date
          MOVE      JULDAY,JDAYS
          MOVE      JDAYS,SAVDAY                 * save beginning julian date
          MOVE      JULYR,SAVYR
          MOVE      JULCC,SAVCC
          MOVE      "31",DAYS
          CALL      CALC0000                     * Get date
          PACK      DATPER1,CC,YY,MM,DD
          REP       " 0",DATPER1
.
          MOVE      SAVDAY,JDAYS   
          MOVE      SAVYR,JULYR
          MOVE      SAVCC,JULCC
          MOVE      "61",DAYS
          CALL      CALC0000                     * Get date
          PACK      DATPER2,CC,YY,MM,DD
          REP       " 0",DATPER2
.
          MOVE      SAVDAY,JDAYS
          MOVE      SAVYR,JULYR
          MOVE      SAVCC,JULCC
          MOVE      "91",DAYS
          CALL      CALC0000                     * Get date
          PACK      DATPER3,CC,YY,MM,DD
          REP       " 0",DATPER3
.
          MOVE      SAVDAY,JDAYS
          MOVE      SAVYR,JULYR
          MOVE      SAVCC,JULCC
          MOVE      "121",DAYS
          CALL      CALC0000                     * Get date
          PACK      DATPER4,CC,YY,MM,DD
          REP       " 0",DATPER4
.
CONV9999  RETURN
+
.****************************************************************************
.*                              CALC0000               Called by: CONV0000  *
.*              calculate the aged date for each period defined             *
.*          N.B. JDAYS used instead of JULDAY to cater for a form4 figure   *
.*          when handling 2 year aged date range                            *
.****************************************************************************
CALC0000  SUB       DAYS,JDAYS              * subtract number of days
.
          COMPARE   JDAYS,ZERO              * date from previous year ?
          GOTO      CALC1500 IF LESS        * no
.
CALC1000  ADD       "365",JDAYS             * calculate last year day
.
          COMPARE   ZERO,JULYR
          IF        @EQUAL
            MOVE      "99",JULYR
            SUB       ONE,JULCC
          ELSE
            SUB       ONE,JULYR               * last year
          ENDIF
.
          COMPARE   ONE,JDAYS               * date from previous year ?
          GOTO      CALC1000 IF LESS        * no
.
          COMPARE   JULYR,SEQ               * date from previous century ?
          IF        @LESS
.
.            See if previous year was a leap year.  If so add another day
.            to the julian day
.
             MOVE      JULYR,FORM2
             DIV       FOUR,FORM2
             MULT      FOUR,FORM2           * leap year ?
             COMPARE   JULYR,FORM2
             IF        @EQUAL
                ADD       ONE,JDAYS         * yes
             ENDIF
.
          ENDIF
.
CALC1500  MOVE      JDAYS,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON                  * convert back to calender
.                                             format
CALC9999  RETURN
+
.****************************************************************************
.*        IO ROUTINES FOR TEMPORARY FILE                                    *
.****************************************************************************
UPTEMPA   UPDATE    HICTEMA1;TEMPCLID,TEMPMPRA,TEMPPSRV,TEMPURNO:
                               TEMPPAID,TEMPCURD,TEMP30DY,TEMP60DY:
                               TEMP90DY,TEMP120D,TEMPOUTS,TEMPSNAM:
                               TEMPGNAM
          RETURN
.
UPTEMPB   UPDATE    HICTEMB1;TEMPCLID,TEMPMPRA,TEMPPSRV,TEMPURNO:
                             TEMPAMNT,TEMPPAID,TEMPOUTS:
                             TEMPCLMN,TEMPCDAT,TEMPSNAM,TEMPGNAM
          RETURN
.
.****************************************************************************
.
WRTEMPA   WRITE     HICTEMA1,KEY8;TEMPCLID,TEMPMPRA,TEMPPSRV,TEMPURNO:
                               TEMPPAID,TEMPCURD,TEMP30DY,TEMP60DY:
                               TEMP90DY,TEMP120D,TEMPOUTS,TEMPSNAM:
                               TEMPGNAM
          RETURN
.
WRTEMPB   WRITE     HICTEMB1,KEY8;TEMPCLID,TEMPMPRA,TEMPPSRV,TEMPURNO:
                             TEMPAMNT,TEMPPAID,TEMPOUTS:
                             TEMPCLMN,TEMPCDAT,TEMPSNAM,TEMPGNAM
          RETURN
.
.****************************************************************************
.
RSTEMPF   BRANCH    DOPTION,RSTEMP1
.
          MOVE      SP70,KEY8
          IF        SORTFLAG=1
            READ      HICTEMB2,KEY36;;
          ELSE
            IF        SORTFLAG=2
              READ      HICTEMB3,KEY34;;
            ELSE
              READ      HICTEMB4,KEY38;;
            ENDIF
          ENDIF
          RETURN
.
RSTEMP1   MOVE      SP70,KEY8
          IF        SORTFLAG=1
            READ      HICTEMA2,KEY36;;
          ELSE
            IF        SORTFLAG=2
              READ      HICTEMA3,KEY34;;
            ELSE
              READ      HICTEMA4,KEY38;;
            ENDIF
          ENDIF
          RETURN
.
.****************************************************************************
.
RKTEMPF   BRANCH    DOPTION,RKTEMP1
          MOVE      ZERO,OVRCD
          IF        SORTFLAG=1
            READKS    HICTEMB2;TEMPCLID,TEMPMPRA,TEMPPSRV,TEMPURNO:
                               TEMPAMNT,TEMPPAID,TEMPOUTS:
                               TEMPCLMN,TEMPCDAT,TEMPSNAM,TEMPGNAM
          ELSE
            IF        SORTFLAG=2
              READKS    HICTEMB3;TEMPCLID,TEMPMPRA,TEMPPSRV,TEMPURNO:
                                 TEMPAMNT,TEMPPAID,TEMPOUTS:
                                 TEMPCLMN,TEMPCDAT,TEMPSNAM,TEMPGNAM
            ELSE
              READKS    HICTEMB4;TEMPCLID,TEMPMPRA,TEMPPSRV,TEMPURNO:
                                 TEMPAMNT,TEMPPAID,TEMPOUTS:
                                 TEMPCLMN,TEMPCDAT,TEMPSNAM,TEMPGNAM
            ENDIF
          ENDIF
          GOTO      OVERCOND IF OVER
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          IF        SORTFLAG=1
            READKS    HICTEMA2;TEMPCLID,TEMPMPRA,TEMPPSRV,TEMPURNO:
                               TEMPPAID,TEMPCURD,TEMP30DY,TEMP60DY:
                               TEMP90DY,TEMP120D,TEMPOUTS,TEMPSNAM:
                               TEMPGNAM
          ELSE
            IF        SORTFLAG=2
              READKS    HICTEMA3;TEMPCLID,TEMPMPRA,TEMPPSRV,TEMPURNO:
                                 TEMPPAID,TEMPCURD,TEMP30DY,TEMP60DY:
                                 TEMP90DY,TEMP120D,TEMPOUTS,TEMPSNAM:
                                 TEMPGNAM
            ELSE
              READKS    HICTEMA4;TEMPCLID,TEMPMPRA,TEMPPSRV,TEMPURNO:
                                 TEMPPAID,TEMPCURD,TEMP30DY,TEMP60DY:
                                 TEMP90DY,TEMP120D,TEMPOUTS,TEMPSNAM:
                                 TEMPGNAM
            ENDIF
          ENDIF
          GOTO      OVERCOND IF OVER
          RETURN
.
.****************************************************************************
RDTEMPA   RESET     KEY8
          MOVE      ZERO,OVRCD
          READ      HICTEMA1,KEY8;TEMPCLID,TEMPMPRA,TEMPPSRV,TEMPURNO:
                             TEMPPAID,TEMPCURD,TEMP30DY,TEMP60DY:
                             TEMP90DY,TEMP120D,TEMPOUTS,TEMPSNAM:
                             TEMPGNAM
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTEMPB   RESET     KEY8
          MOVE      ZERO,OVRCD
          READ      HICTEMB1,KEY8;TEMPCLID,TEMPMPRA,TEMPPSRV,TEMPURNO:
                                  TEMPAMNT,TEMPPAID,TEMPOUTS:
                                  TEMPCLMN,TEMPCDAT,TEMPSNAM,TEMPGNAM
          GOTO      OVERCOND IF OVER
          RETURN
.
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       TFILENAM                * Generate Temp File Name
.
          INC       IBASEQIO/INC            * Sequential Numbers File
          INC       WEBERRIO/INC            * Web Server Error Logging
          INC       HICCLMIO/INC
          INC       HICCITIO/INC
          INC       PRIPRAIO/INC
          INC       PRIDOCIO/INC
          INC       OUTSITIO/INC
          INC       OUTCLIIO/INC
          INC       PATCODIO/INC
          INC       WEBSECIO/INC
          INC       PMSHCPIO/INC
          INC       PRIALSIO/INC
          INC       PRISECIO/INC
          INC       PATMA1IO/INC
          INC       HICITIIO/INC
.

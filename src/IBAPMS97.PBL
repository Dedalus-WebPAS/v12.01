.******************************************************************************
.* System    :   Patient Management                                           *
.* Program   :   IBAPMS97                                                     *
.* Desc      :   Update a patient's GP and Practice details                   *
.******************************************************************************
.* Date      :   26/07/2012                                                   *
.* Author    :   Don Nguyen                                                   *
.* Function  :   This program will loop the patients (pmspx2af) with matching *
.*               HCP and Practice codes and update their record with the new  *
.*               HCP and Practice codes.                                      *
.* Mods      :                                                                *
.* V11.05.02  26/11/2024  Bella Turco        TSK 0952905                      *
.*            Save Previous Practice Count to pmsdauaf audit file DRAUDT10    *
.*               V11.05.01  18/11/2024  Bella Turco        TSK 0952905        *
.*                          Call DRAUDT00 if Update Records set to Yes        *
.*               V11.04.01  07/11/2024  Bella Turco        TSK 0952905        *
.*                          New routine DRAUDT00 to write HCP Audit record    *
.*               V10.03.00  26/07/2012  Don Nguyen         CAR 267480         *
.*                          Created program                                   *
.******************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
.
          INC       IBACONFD/INC
          INC       PATCODFD/INC
          INC       PATMA1FD/INC
          INC       PMSDAUFD/INC
          INC       PMSHCGFD/INC
.         INC       PMSHCLFD/INC
          INC       PMSHCPFD/INC
          INC       PMSPX2FD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
DIM35     DIM       35
DOCCODE   DIM       10            * populated by PMSHCPKY
FRPRACOD  DIM       10            * Practice code (From)
FRPRADES  DIM       35            * Practice description (From)
FRPRACNT  DIM       2             * Practice count (From)
FRHCPCOD  DIM       10            * HCP code (From)
FRHCPDES  DIM       35            * HCP description (From)
KEYNTYPE  FORM      1             * 0 = From, 1 = To
KEYNLINE  FORM      2             * Line number to display keyin
KEYNSTRN  DIM       20            * Keyin string to display
PRACTCOD  DIM       10            * Practice code
PRACTCNT  DIM       2             * Practice count
TOPRACNT  DIM       2             * Practice count (To)
TOPRACOD  DIM       10            * Practice code (To)
TOPRADES  DIM       35            * Practice description (To)
TOHCPCOD  DIM       10            * HCP code (To)
TOHCPDES  DIM       35            * HCP description (To)
RECCOUNT  FORM      8
UPDATEFL  FORM      1             * Update flag; whether or not to update record
UPDTURNO  DIM       8             * U/R to update
WEBUSRID  DIM       10            * Web User ID
.
. Variables for PMSHCGKY include
.
.CKACTIVE  FORM      1
.CKLINKED  FORM      1
.SCCOL     FORM      3
.SVERT     FORM      2
.
. Variables for PMSHCGDS include
.
.PMHGFLAG  FORM      "0"           * Flag for display of active GP prac's in ?
.PMHGQARR  DIM       12[17]        * Array for pmshcgds
.PMHGQCNT  FORM      2             * Count of items on screen
.PMHLFLAG  FORM      "0"           * Flag to display all links in ?
.                                  *   0 = Don't display inactive links
.                                  *   1 = Display all links
.
. PROGRAM CONSTANTS
. -----------------
.
PRGID     INIT      "IBAPMS97"
PRGNAM    INIT      "Update Patient GP and Practice"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                              MAIN0000                                      *
.*                      Controlling Logic (Mainline code)                     *
.******************************************************************************
.
MAIN0000  CALL      INIT0000               * Initialisation and open files
.
MAIN1000  CALL      OPTN0000               * Select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN2000       * Proceed to process
.
          GOTO      MAIN1000               * Invalid selection; get again
.
MAIN2000  MOVE      ZERO,KEYNTYPE
          MOVE      TEN,KEYNLINE
          CALL      HCPK0000               * Get HCP from
          BRANCH    EXIT,MAIN2500
.
MAIN2500  MOVE      ZERO,KEYNTYPE
          MOVE      TEN1,KEYNLINE
          CALL      PRAK0000               * Get Practice from
          BRANCH    EXIT,MAIN9999
.
MAIN3000  MOVE      ONE,KEYNTYPE
          MOVE      TEN2,KEYNLINE
          CALL      HCPK0000               * Get HCP to
          BRANCH    EXIT,MAIN9999
.
MAIN3500  MOVE      ONE,KEYNTYPE
          MOVE      TEN3,KEYNLINE
          CALL      PRAK0000               * Get Practice to
          BRANCH    EXIT,MAIN9999
.
          MOVE      TWENTY,KEYNLINE          
          CALL      WBUK0000               * Get Web User ID
.
MAIN4000  CALL      VALD0000               * Print report and or update
          BRANCH    EXIT,MAIN9999
.
MAIN5000  CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN9000:        * yes
                          MAIN1000:        * no
                          MAIN1000         * cancel
.
MAIN9000  CALL      PROC0000               * Process affected patients
.
MAIN9999  CHAIN     PGM                    * Chain back to program
+
.******************************************************************************
.*                                INIT0000             Called by: MAIN0000    *
.*                             initialisation                                 *
.*  The initialisation routine is used to display headings and open files.    *
.******************************************************************************
INIT0000  CALL      DISPHEAD                      * display heading
.
          DISPLAY   *P60:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P68:24,"opening patma1af"
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P68:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P68:24,"pmspx2af"
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P68:24,"pmsdauaf"
          OPEN      PMSDAUA1,"pmsdauaf"
.
          DISPLAY   *P68:24,"pmshcpaf"
          OPEN      PMSHCPA1,"pmshcpaf"
          OPEN      PMSHCPA2,"pmshcpaf"
.
          DISPLAY   *P68:24,"pmshcgaf"
          OPEN      PMSHCGA1,"pmshcgaf"
.
          CALL      IBACLOCK
.
INIT9999  RETURN
+
.******************************************************************************
.*                                OPTN0000             Called by: MAIN0000    *
.*                        Get user options or exit                            *
.*                                                                            *
.*    Returns:  EXIT    = FALSE (0) - Valid option selected                   *
.*                        TRUE  (1) - Exit option selected                    *
.*                                                                            *
.*              COPTION = 0 Exit                                              *
.*                        1 Run Report                                        *
.******************************************************************************
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Update Patient GP and/or Practice"
.
OPTN1000  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.******************************************************************************
.*                                HCPK0000             Called by: MAIN3000    *
.*                            Keyin HCP code                                  *
.*                                                                            *
.*  Parameters    : KEYNTYPE - Type of keyin (0=From, 1=To)                   *
.*                  KEYNLINE - Line number of keyin                           *
.*                                                                            *
.*    Returns     : FRHCPCOD - HCP code (from)                                *
.*                  TOHCPCOD - HCP code (to)                                  *
.*                  FRHCPDES - HCP description (from)                         *
.*                  TOHCPDES - HCP description (to)                           *
.******************************************************************************
HCPK0000  MOVE      SP70,PMHCSNAM
          MOVE      SP70,PMHCGNAM
          MOVE      SP10,PMHCTITL
.
          COMPARE   KEYNTYPE,ONE
          IF        @EQUAL
            MOVE      SP70,TOHCPCOD
            MOVE      SP70,TOHCPDES
          ELSE
            MOVE      SP70,FRHCPCOD 
            MOVE      SP70,FRHCPDES 
          ENDIF
.
          COMPARE   KEYNTYPE,ONE
          IF        @EQUAL
            MOVE      "To GP",KEYNSTRN
          ELSE
            MOVE      "From GP",KEYNSTRN
          ENDIF
.
          DISPLAY   *P1:KEYNLINE,KEYNSTRN,*P17:KEYNLINE,": "
.
          MOVE      "19",ECOL
          MOVE      KEYNLINE,EVERT
          MOVE      ZERO,CKYIMAND           * keyin not mandatory
          MOVE      ZERO,CKYINACT           * don't check doctor status
.
          CALL      PMSHCPKY                * Keyin HCP code
.
.         BRANCH    EXIT,HCPK9100,HCPK9100
.                        Spaces   Exitchar
.
          COMPARE   KEYNTYPE,ONE
          IF        @EQUAL
            MOVE      DOCCODE,TOHCPCOD
          ELSE
            MOVE      DOCCODE,FRHCPCOD
          ENDIF
.
          MOVE      PMHCSNAM,PACSNAME
          MOVE      PMHCGNAM,PACGNAME
          MOVE      PMHCTITL,PACTITLE
          MOVE      ONE,PACFRMT
          CALL      PACNAME                 * Format HCP name
.
          COMPARE   KEYNTYPE,ONE
          IF        @EQUAL
            MOVE      PACFNAME,TOHCPDES
          ELSE
            MOVE      PACFNAME,FRHCPDES
          ENDIF
.
          DISPLAY   *P42:KEYNLINE,PACFNAME
.
HCPK9000  MOVE      ZERO,EXIT
          GOTO      HCPK9999
.
HCPK9100  MOVE      ONE,EXIT                * spaces or exit char
          GOTO      HCPK9999
.
HCPK9999  RETURN
+
.******************************************************************************
.*                                PRAK0000             Called by: MAIN2500    *
.*                          Keyin Practice code                               *
.*                                                                            *
.*  Parameters    : KEYNTYPE - Type of keyin (0=From, 1=To)                   *
.*                  KEYNLINE - Line number of keyin                           *
.*                                                                            *
.*    Returns     : FRPRACOD - Practice code (from)                           *
.*                  TOPRACOD - Practice code (to)                             *
.*                  FRPRADES - Practice description (from)                    *
.*                  TOPRADES - Practice description (to)                      *
.******************************************************************************
PRAK0000  COMPARE   KEYNTYPE,ONE
          IF        @EQUAL
            MOVE      SP70,TOPRACOD
            MOVE      SP70,TOPRADES
            MOVE      "To Practice",KEYNSTRN
          ELSE
            MOVE      SP70,FRPRACOD
            MOVE      SP70,FRPRADES
            MOVE      "From Practice",KEYNSTRN
          ENDIF
.
          DISPLAY   *P1:KEYNLINE,*EL,KEYNSTRN,*P17:KEYNLINE,": " 
          KEYIN     *P19:KEYNLINE,*DV,UNDLN10:
                    *P19:KEYNLINE,*V2LON,PRACTCOD:
                    *P19:KEYNLINE,*DV,PRACTCOD
.
          PACK      PRACTCOD,PRACTCOD,SP10
          COMPARE   KEYNTYPE,ONE
          IF        @EQUAL
            MOVE      PRACTCOD,TOPRACOD
          ELSE
            MOVE      PRACTCOD,FRPRACOD
          ENDIF
.
          DISPLAY   *P30:KEYNLINE,*HOFF,*EL,"Count: "
          KEYIN     *P37:KEYNLINE,*DV,UNDLN2:
                    *P37:KEYNLINE,*V2LON,PRACTCNT:
                    *P37:KEYNLINE,*DV,PRACTCNT
.
          PACK      KEY12,PRACTCOD,PRACTCNT,SP70
          CALL      RDPMHCG1
          BRANCH    OVRCD,PRAK9999
.
          COMPARE   KEYNTYPE,ONE
          IF        @EQUAL
            MOVE      PMHGPNAM,TOPRADES
            MOVE      PRACTCNT,TOPRACNT
          ELSE
            MOVE      PMHGPNAM,FRPRADES
            MOVE      PRACTCNT,FRPRACNT
          ENDIF
.
          DISPLAY   *P42:KEYNLINE,PMHGPNAM
.
.
.         DISPLAY   *P1:11,"From Practice   : ",SP30
.         MOVE      "19",CCOL
.         MOVE      KEYNLINE,CVERT
.
.         MOVE      FRHCPCOD,CKYIDEF8       * Referring GP default
.         MOVE      SP70,CKYIDEF6           * default practice code
.         MOVE      " 1",CKYIDEF2           * default practice count
.         MOVE      ZERO,CKYIMAND           * mandatory?
.         MOVE      ZERO,CKACTIVE
.
.         CALL      PMSHCGKY                * keyin GP Practice code
.         IF        EXIT = 2
.           MOVE      ONE,EXIT
.           GOTO      PRAK9999
.         ENDIF
.
.         MOVE      CKYIDEF6,TOPRACOD       * set GP practice code
.         MOVE      CKYIDEF2,WTWMGPPT       * set GP practice count

.         BRANCH    EXIT,PRAK9100,PRAK9100
.
.         DISPLAY   *PCCOL:CVERT,*V2LON,CKYIDEF6,*HOFF,*P25:CVERT,TOPRADES
.         MOVE      ACODE,TOPRACOD
.         MOVE      TDESC,TOPRADES
.
PRAK9000  MOVE      ZERO,EXIT
          GOTO      PRAK9999
.
PRAK9100  MOVE      ONE,EXIT
          GOTO      PRAK9999
.
PRAK9999  RETURN
+
.******************************************************************************
.*                                WBUK0000             Called by: MAIN2500    *
.*                          Keyin Web User ID                                 *
.*                                                                            *
.*  Parameters    : KEYNLINE - Line number of keyin                           *
.*    Returns     : WEBUSRID - Web User ID                                    *
.******************************************************************************
WBUK0000  MOVE      SP70,WEBUSRID
          DISPLAY   *P1:KEYNLINE,*EL,"Web User ID",*P17:KEYNLINE,": " 
          KEYIN     *P19:KEYNLINE,*DV,UNDLN10:
                    *P19:KEYNLINE,*V2LON,WEBUSRID:
                    *P19:KEYNLINE,*DV,WEBUSRID
.
WBUK9999  RETURN
+
.******************************************************************************
.*                        HEAD0000                                            *
.*                  Produce printout heading                                  *
.******************************************************************************
HEAD0000  MOVE      ZERO,CLNO
          CALL      HEAD132
.
          PRINT     *15,"GP From        : ",FRHCPDES:
                    *70,"GP To       : ",TOHCPDES
          PRINT     *15,"Practice From  : ",FRPRADES:
                    *70,"Practice To : ",TOPRADES
.
          IF        UPDATEFL=1
            PRINT     *15,"Update Records : ",DYES
          ELSE
            PRINT     *15,"Update Records : ",DNO
          ENDIF
.
          PRINT     *15,"Web User ID    : ",WEBUSRID
          CALL      UND132
.
          PRINT     *1,"| U/R No",*12,"| Patient Name":
                    *50,"| GP From ",*63,"| Practice From ":
                    *79,"| GP To ",*92,"| Practice To ",*132,"|"
.
          CALL      UND132
          ADD       SEVEN,CLNO
.
HEAD9999  RETURN
+
.******************************************************************************
.*                                TAIL0000             Called by: PROC0000    *
.*                  Trailer Print Routine                                     *
.******************************************************************************
TAIL0000  COMPARE   "60",CLNO
          GOTO      TAIL1000 IF LESS
          CALL      HEAD0000                 * Print header
.
TAIL1000  CALL      UND132
          PRINT     *1,"| Records Selected: ",RECCOUNT,*132,"|"
          CALL      UND132
          PRINT     *N,"*** End of Report ***"
.
TAIL9999  RETURN
+
.******************************************************************************
.*                                PREF0000             Called by: PROC0000    *
.*                  Print U/R and Name                                        *
.******************************************************************************
PREF0000  ADD       ONE,RECCOUNT
.
          PACK      KEY8,UPDTURNO,SP70
          CALL      RDMAST1
          IF        OVRCD=1
            MOVE      "(Invalid U/R Number)",DIM35
            GOTO      PREF1000
          ENDIF
.
          PACK      KEY8,UPDTURNO,SP70
          CALL      RDPMPX21
          IF        OVRCD=1
            CALL      CLPMSPX2
            GOTO      PREF1000
          ENDIF
.
          MOVE      PTITL,PACTITLE
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      TWO,PACFRMT
          CALL      PACNAME                 * Pack Full Name
          MOVE      PACFNAME,DIM35
.
PREF1000  COMPARE   "60",CLNO
          GOTO      PREF2000 IF LESS
          CALL      HEAD0000                * Print header
.
PREF2000  PRINT     *1,"| ",UPDTURNO,*12,"| ",DIM35:
                    *50,"| ",FRHCPCOD,*63,"| ",FRPRACOD:
                    *79,"| ",TOHCPCOD,*92,"| ",TOPRACOD,*132,"|"
.
          ADD       ONE,CLNO
          GOTO      PREF9999
.
PREF9999  RETURN
+
.******************************************************************************
.*                                VALD0000             Called by: MAIN0000    *
.*                  Keyin whether to print report or update records           *
.******************************************************************************
VALD0000  MOVE      ZERO,UPDATEFL                  * No to update records
          MOVE      ZERO,EXIT
.
VALD1000  DISPLAY   *P1:21,"Update Records",*P17:21,"? "
          KEYIN     *P19:21,*V2LON,*RV,DIM1:
                    *P19:21,*DV,DIM1
          REP       UPPLOW,DIM1
          DISPLAY   *P19:21,*V2LON,DIM1
          MOVE      ONE,UPDATEFL
          MATCH     "Y",DIM1
          GOTO      VALD9999 IF EQUAL
.
          MOVE      ZERO,UPDATEFL
          MATCH     "N",DIM1
          GOTO      VALD9999 IF EQUAL
.
          MATCH    SP70,DIM1
          GOTO     VALD9000 IF EQUAL
.
          BEEP
          GOTO      VALD1000
.
VALD9000  MOVE      ONE,EXIT
          GOTO      VALD9999
.
VALD9999  RETURN
+
.******************************************************************************
.*                               PROC0000              Called by: MAIN0000    *
.*        Process all relevant patients with matching HCP and Practice        *
.******************************************************************************
PROC0000  CALL      HEAD0000 
          MOVE      ZERO,RECCOUNT
          MOVE      ZERO,CPAGENO
.
          PACK      KEY8,SP70
          CALL      RSPMPX21
PROC1000  CALL      RKPMPX21
          BRANCH    OVRCD,PROC9000
.
          MATCH     PMPXRHC1,FRHCPCOD       * same HCP code?
          GOTO      PROC1000 IF NOT EQUAL   * no
.
          MATCH     PMPXRH1G,FRPRACOD       * same Practice code?
          GOTO      PROC1000 IF NOT EQUAL   * no
.
          MOVE      PMPXURNO,UPDTURNO
          CALL      PREF0000                * Print U/R Line details
          IF        UPDATEFL=1
            MOVE      TOHCPCOD,PMPXRHC1
            MOVE      TOPRACOD,PMPXRH1G
            MOVE      PRACTCNT,PMPXR1GC
            CALL      UPPMPX21
          ENDIF
.
          MATCH     "Y",DIM1                  * Update Records "Yes"
          IF        @EQUAL
            CALL      DRAUDT00                * Write Doctor Audit pmsdauaf
          ENDIF
.
          GOTO      PROC1000
.
PROC9000  CALL      TAIL0000                * Print End of Report
          GOTO      PROC9999
.
PROC9999  RETURN
+
.******************************************************************************
.*                               DRAUDT00              Called by: PROC0000    *
.*        Write an audit record for the doctor change to pmsdauaf             *
.******************************************************************************
DRAUDT00  CALL      CLPMSDAU
          MOVE      PMPXURNO,PMDEURNO
          MOVE      "002",PMDERTYP               * HCP Details
.
DRAUDT10  CALL      IBACLOCK
          PACK      PMDECDAT,CCC,CYY,CMM,CDD
          REP       " 0",PMDECDAT
          PACK      PMDECTIM,CTIMEIS
.
          PACK      KEY27,PMPXURNO,PMDECDAT,PMDECTIM,PMDERTYP,SP70
          CALL      RDPMDAU1
          COMPARE   ZERO,OVRCD
          GOTO      DRAUDT10 IF EQUAL
.
          PACK      PMDEFL01,FRHCPCOD,SP70  * Previous HCP
          PACK      PMDEFL02,FRPRACOD,SP70  * Previous Practice
          PACK      PMDEFL03,FRPRACNT       * Previous Practice Count
          PACK      PMDECUID,WEBUSRID,SP70  * User ID 
.
DRAUDT95  CALL      WRPMDAU1                * write record pmsdauaf
.
DRAUDT99  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       CLPMSPX2           * Clear variables of PMI Extension file
          INC       CLPMSDAU           * Clear variables of Audit file
.         INC       PMSHCGKY           * Keyin of a GP Practice code (PMSHCGFD)
          INC       PMSHCPKY           * Keyin of a Doctor file entry (PMSHCPFD)
.
          INC       PATCODIO/INC
          INC       PATMA1IO/INC
          INC       PMSDAUIO/INC
          INC       PMSHCGIO/INC
.         INC       PMSHCLIO/INC
          INC       PMSHCPIO/INC
          INC       PMSPX2IO/INC

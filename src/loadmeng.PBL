.-----------------------------------------------------------------------------
.   Program       : loadmeng
.   Function      : Install Site Transfer File <menuname>.men.Z
.   Modifications :   
.
.   V9.09.01 17.04.2008 Sandra Barcham     166599
.            Changed from compress to gzip
.   V6.00.01 13.05.93 B.G.Ackland
.            New Program
.-----------------------------------------------------------------------------
          INC       IBACOMM/INC
          INC       IBAKEYS/INC
          INC       MENUSEFD/INC
          INC       IBAPASFD/INC
.=========================
.Local Variable Definition
.-------------------------
OLDPID    DIM      8
.
PROGNAME  DIM       8
RECTYPE   FORM      1
DRECTYPE   DIM      1
CEXTENS   INIT      ".men.gz"
MENU      INIT      ".men"
DCOMPRES  INIT      "gzip -d "
COMPRESS  INIT      "gzip "
EXTENS    INIT      "/men"
FILE      DIM       12
TRANFILE  FILE      ASCII, FIXED=250
INEXT     INIT      ".in"
.
PRGID     INIT      "loadmeng"
PRGNAM    INIT      "Load Menu Generator <filename>.men.gz"
VERSION   INIT      "V12.01.00"
.--------------------------------
. MAINLINE - Controlling Logic
.--------------------------------
ML0000    CALL      INIT0000
          BRANCH    OVRCD,ML9000
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      TRANFILE,"loadmenu.txt"
          TRAPCLR   IO
          IF        OVRCD=1
            DISPLAY  *P1:1,*ES,"Unable to Open input file loadmenu.txt",*W2
            GOTO     ML9999
          ENDIF
.
          READ      TRANFILE,SEQ;PROGNAME
.
          CLOSE     TRANFILE,DELETE
.
          PACK      KEY15,PROGNAME,CEXTENS
          SQUEEZE   KEY15
.
          PACK      KEY50,DCOMPRES,KEY15
          EXECUTE   KEY50,KEY8
.
          DISPLAY   *ES,*P1:1,*HON,SP20,SP20,SP20,SP20:
                        *P1:1,*HON,PRGID,"/",VERSION,SP2,"Loading ",KEY15
          PACK      FILE,PROGNAME,EXTENS
          SQUEEZE   FILE 
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      TRANFILE,FILE
          TRAPCLR   IO
          IF        OVRCD=1
            DISPLAY   *P1:1,*B,*ES,"Menu ",FILE," File Not Found. ",*W2
            GOTO      ML8000
          ENDIF
          MOVE      ZERO,SECTOR
.
          CALL      UPSEC000    * Update Security File
.
          CALL      DELMEN00    * Clear Current Values
          BRANCH    EXIT,ML8000
.
ML0100    CALL      RDTRFL1
          BRANCH    OVRCD,ML8000
.
          PACK      KEY15,MEUSMENU,MEUSK2,MEUSK3,MEUSK4
          CALL      RAMEUS1
          IF        OVRCD=0
            CALL      UPMEUS1
          ELSE
            CALL      WRMEUS1
          ENDIF
.
          GOTO      ML0100
.
ML8000    CLOSE     TRANFILE
          PACK      KEY13,PROGNAME,MENU
          SQUEEZE   KEY13
          PACK      KEY50,COMPRESS,KEY13
          EXECUTE   KEY50,KEY8
.
          GOTO      ML9999
.
ML9000    DISPLAY   *P1:1,*B,"Unable to Open menuserf. ",*W2
.
ML9999    SHUTDOWN  "" 
.---------------------------------------
. INIT - Open Files & Display Heading
.---------------------------------------
INIT0000  MOVE      "180",MRECLEN
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      MENUSER1,"menuserf"
          TRAPCLR   IO
.
          IF        OVRCD=1
            MOVE      "181",MRECLEN
            MOVE      ZERO,OVRCD
            TRAP      OVERCOND IF IO
            OPEN      MENUSER1,"menuserf"
            TRAPCLR   IO
          ENDIF
          IF        OVRCD=1
            DISPLAY   "Can't Open menuserf"
            SHUTDOWN  "" 
          ENDIF
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      IHAPASS1,"ihapassf"
          TRAPCLR   IO
          IF        OVRCD=1
            DISPLAY   "Can't Open ihapassf"
            SHUTDOWN  "" 
          ENDIF
          RETURN
.
.---------------------------------------------------
. Delete Existing Menu
.----------------------------------------------------
DELMEN00  PACK      KEY15,PROGNAME,SP20
          CALL      RSMEUS1
          CALL      RKMEUS1
          BRANCH    OVRCD,DELMEN90
          MATCH     PROGNAME,MEUSMENU
          GOTO      DELMEN90 IF NOT EQUAL
.
DELMEN10  KEYIN     *P1:2,*B,"Menu Already Exists. Overwrite Menu (":
                             *V2LON,"Y",*HOFF,"/":
                             *V2LON,"N",*HOFF,") ? ",*V2LON,ANS
          REP       UPPLOW,ANS
          MATCH     "N",ANS
          GOTO      DELMEN95 IF EQUAL
          MATCH     "Y",ANS
          GOTO      DELMEN10 IF NOT EQUAL
.
DELMEN20  PACK      KEY15,PROGNAME,SP20
          CALL      RSMEUS1
          CALL      RKMEUS1
          BRANCH    OVRCD,DELMEN90
          MATCH     PROGNAME,MEUSMENU
          GOTO      DELMEN90 IF NOT EQUAL
          PACK      KEY15,MEUSMENU,MEUSK2,MEUSK3,MEUSK4
          CALL      DEMEUS1
          GOTO      DELMEN20
.
DELMEN90  MOVE      ZERO,EXIT
          GOTO      DELMEN99
DELMEN95  MOVE      ONE,EXIT
DELMEN99  RETURN
.
HITENTER  KEYIN     ANS
          RETURN
.
UPSEC000  UNPACK    PROGNAME,KEY2,KEY2,KEY4
          CALL      RDPASS1
          IF        OVRCD=0
            MOVE      PROGNAME,SECMENU
            CALL      UPPASS1
            GOTO      UPSEC999
          ENDIF
          CALL      CLRPAS00
          MOVE      KEY4,SECODE
          MOVE      PROGNAME,SECMENU
          MOVE      KEY4,SECODE
.
UPSEC100  KEYIN     *P1:2,*EL,"Enter User Name : ",*V2LON,SECUSER,*HOFF:
                    "  Ok Y/N ? ",*V2LON,ANS
          REP       UPPLOW,ANS
          MATCH     "N",ANS
          GOTO      UPSEC100 IF EQUAL
          CALL      WRPASS1
UPSEC999  RETURN
.
CLRPAS00  MOVE      SP70,SECODE
          MOVE      SP70,SECMENU
          MOVE      SP70,SECUSER 
.
          MOVE      SP70,SECMNNUM
          MOVE      SP70,SECNUMB 
          MOVE      SP70,SECACCT
          MOVE      SP70,SECNUMB1
          MOVE      SP70,SECNUMB2
          MOVE      SP70,SECDIRCT
          MOVE      SP70,SECPRINT
          MOVE      SP70,SECDEPT
          MOVE      SP70,SECMAIL 
          MOVE      SP70,SECMNNUM
          MOVE      SP70,SECUALEV
          MOVE      SP70,SECSPARE
          RETURN
.
RDTRFL1   MOVE      ZERO,OVRCD
          READ      TRANFILE,SEQ;MEUSMENU,MEUSK2,MEUSK3,MEUSK4,MEUSD1:
                                 MEUSD2
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTRFL1   WRITE     TRANFILE,SEQ;MEUSMENU,MEUSK2,MEUSK3,MEUSK4,MEUSD1:
                                 MEUSD2
          RETURN
.
          INC       MENUSEIO/INC
          INC       IBAPASIO/INC
          INC       IBAOVRIO/INC

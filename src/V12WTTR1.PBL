.*****************************************************************************
. Program      : V12WTTR1                                    
. Description  : Conversion wattr1af to new File layout      
.*****************************************************************************
. Modifications:           
.*****************************************************************************
. V12.00.00  15/04/2025 Thanh T       TSK 0955096 
.            Initial Version
.*****************************************************************************
.
.  using vi Global change command      :%s/FromString/ToString/g
.
.  Global change V12WTTR1  eg F57PTDOC
.  Global change v12wttr1  eg f57ptdoc
.  Global change WATTR1FD  eg PATDOCFD (new layout)
.  Global change wattr1af  eg patdoctf
.  Global change WATTR1A1  eg PATDOCT1
.  Global change KEY19      eg KEY19
.  Global change KEY19      eg KEY9
.  Global change WRWTTRA1   eg WRDOCT1
.  Global change swattr1f  eg sptdoctf
.
.  Global changes if Oracle Option is required -
.
.  Global Change answeryn  to Yes
.  Global change RSWTTRA1  eg RSPTDOC1
.  Global change RKWTTRA1  eg RKPTDOC1
.  Global change UPWTTRA1  eg UPPTDOC1
.
.  Global changes if Oracle Option is NOT required -

.  Global Change answeryn  to No
.  Global change RSWTTRA1  to OFDUMYIO
.  Global change RKWTTRA1  to OFDUMYIO
.  Global change UPWTTRA1  to OFDUMYIO
.
          INC       STD001FD
.
FINDFILE  FILE      ASCII, FIXED=256
LOGFILE   FILE      ASCII, FIXED=256
.
. Enter the first or the shortest index from the original FD and rename
. the index OLDFILE1
.
.                                     ******** copy the old FD here *******
.                                     * comment out all continuing fields and
.                                     * prefix changing fields with "OLD" -
.                                     * remove old Indexes and Redefines     
.
..OLDFILE1  IFILE     SQL, FIXED=xxx, KEY="Uxx-xx"
OLDFILE1  IFILE     SQL, FIXED=816, KEY="U1-8,9-17,18-19"
.
.NAME     TYPE      LENGTH  PHYS   START  DESCRIPTION
.----     ----      ------  ----   -----  -----------
.WMURNO    DIM       8       8         1   U/R Number
.WMCODE    DIM       9       9         9   Procedure Code
.DWMCNT    DIM       2       2        18   Procedure Count
.
.WMTIME    FORM      5       4        20   Procedure Time (Minutes)
.WMDESC    DIM       80     80        24   Procedure Description
.WMREASON  DIM       80     80       104   Reason/Diagnosis for Procedure
.DWMSTAT   DIM       1       1       184   Procedure Status
.                                           1 - Unscheduled
.                                           2 - Scheduled
.                                           3 - Pre-Admitted
.                                           4 - Admitted
.                                           5 - Discharged
.                                           6 - Removed from Waiting List
.                                           7 - Performed (No longer used)
.                                           8 - Not Performed (No longer used)
.                                           9 - Cancelled Admission
.WMDATE1   DIM       8       8       185   Date on List (ccyymmdd)
.WMDATE2   DIM       8       8       193   Date of Last Review (ccyymmdd) /
.                                         Final Outcome of Offer
.WMDATE3   DIM       8       8       201   Scheduled Admit. Date (ccyymmdd)
.WMDATE4   DIM       8       8       209   Removal Date (ccyymmdd)
.WMREMOVE  DIM       3       3       217   Removal Reason (Category WR/BC)
.WMUNIT    DIM       3       3       220   Unit/Clinic (Category WU)
.WMDOCTOR  DIM       6       6       223   Attending Doctor
.WMSTAY    FORM      5       4       229   Estimated Stay (Days)
.WMPTY     DIM       3       3       233   Priority (Category TP)
.WMNOTICE  DIM       3       3       236   Short Notice (Category WS)
.WMPTYPE   DIM       3       3       239   Resident Status (Category T)
.WMPCAT    DIM       3       3       242   Admission Type (Cat A)
.WMUDEF1   DIM       3       3       245   User Defined 1 (Category W1)
.WMUDEF2   DIM       3       3       248   User Defined 2 (Category W2)
.WMUDEF3   DIM       3       3       251   User Defined 3 (Category W3)
.WMUDEF4   DIM       3       3       254   User Defined 4 (Category W4)
.
OWMBOOK    FORM      8       5       257   Booking Number
.WMKEYDT   DIM       8       8       262   Keyin Date For Procedure
.                                                 (ccyymmdd)
.WTWMPORT  DIM       2       2       270   Locking Indicator
.WTWMREAD  DIM       8       8       272   Re-Assessment Date (ccyymmdd)
.WTWMDEDA  DIM       8       8       280   Deferred or Staged Adm Date
.                                               (ccyymmdd)
. The following two fields are used for Transmission to the SAHC
.
.WTWMUNIQ  FORM      5       4       288   Unique Identifier
.WTWMSENT  FORM      1       2       292   Procedure Status 4 & 5 records
.                                         already sent = 1, otherwise = 0
.WTWMCLSS  DIM       3       3       294   Patient Cat/Adm Class (Cat P)
.
.WTWMDTAD  DIM       8       8       297   Decision to Admit Date
.WTWMACAT  DIM       3       3       305   Administrative Category  "CL"
.WTWMGADD  DIM       8       8       308   Guaranteed Admission Date
.WTWMDABD  DIM       8       8       316   Do Not Admit Before Date/Proposed
.                                         Operation Date
.WTWMEATY  DIM       3       3       324   Elective Admission Type  "CC"
.WTWMADMC  DIM       3       3       327   Admission Category   "WA"
.WTWMECRA  DIM       20      20      330   ECR Approval Number
.WTWMGPFA  DIM       20      20      350   GPFH Approval Number
.WTWMRFGP  DIM       8       8       370   Referring GP
.WTWMGPPC  DIM       6       6       378   GP Practice code
.WTWMNAFR  DIM       8       8       384   Not available from date
.WTWMNATO  DIM       8       8       392   Not available to date
.WTWMGPPT  DIM       2       2       400   GP Practice count
.WTWMDOFR  DIM       3       3       402   District of Residence (Cat DA)
.WTWMOPER  DIM       4       4       405   Operator who added proc to W/L
.WTWMIHSP  DIM       3       3       409   Intended Hospital (pathspaf)
.WTWMVLSF  DIM       1       1       412   Validation Letter Sent Flag (Y/N)
.WTWMPRO2  DIM       9       9       413   Procedure Code 2
.WTWMPRO3  DIM       9       9       422   Procedure Code 3
.WTWMTRPA  DIM       8       8       431   Pre-Anaesthetic Date (CCYYMMDD)
.WMUDEF5   DIM       3       3       439   User Defined 5 (Category X5)
.WMUDEF6   DIM       3       3       442   User Defined 6 (Category X6)
.WMUDEF7   DIM       3       3       445   User Defined 7 (Category X7)
.WMUDEF8   DIM       3       3       448   User Defined 8 (Category X8)
.WTWMRANK  DIM       6       6       451   Patient Rank
.WTWMSRCR  DIM       3       3       457   Source of Referral (S)
.WTWMDRSA  DIM       8       8       460   Date of ref for 1st spec. assessment
.WTWMDOSA  DIM       8       8       468   Date of 1st specialised assessment
.WTWMPHSP  DIM       3       3       476   Principal Health serv Purchaser Cat HP
.WTWMBSCD  DIM       2       2       479   Booking Status Code
.WTWMRSFL  DIM       1       1       481   Record sent flag (Y or N)
.WTWMECNT  DIM       9       9       482   ESIS unique count eg. CCYY?????
.WTWMCSST  DIM       1       1       491   Consent Obtained (N=NO,Y=YES)
.WTWMCSDT  DIM       8       8       492   Consent Changed Date (ccyymmdd)
.WTWMASSR  DIM       10      10      500   Assessor Code (pmshcpaf)
.WTWMACPR  DIM       8       8       510   Actual Procedure Event Number
.WTWMPDSC  DIM       80      80      518   Procedure Description Comments
.WTWMRADT  DIM       8       8       598   Referral Accepted Date
.WMDESC2   DIM       80      80      606   Procedure Description 2
.WMDESC3   DIM       80      80      686   Procedure Description 3
OWTWMSPA  DIM       50      50      766   Spare
.End of Record                      816
.
.
.                                     ******** copy the new FD here *******
.
WATTR1A1  IFILE    SQL, FIXED=816, KEY="U1-8,9-17,18-19"
WATTR1A2  IFILE    SQL, FIXED=816, KEY="U184-184,1-8,9-17,18-19"
WATTR1A3  IFILE    SQL, FIXED=816, KEY="U185-192,1-8,9-17,18-19"
WATTR1A4  IFILE    SQL, FIXED=816, KEY="U1-8,185-192,9-17,18-19"
WATTR1A5  IFILE    SQL, FIXED=816, KEY="U184-184,223-228,220-222,233-235,1-8,9-17,18-19"
WATTR1A6  IFILE    SQL, FIXED=816, KEY="U300-307,184-184,1-8,9-17,18-19"
WATTR1A7  IFILE    SQL, FIXED=816, KEY="U184-184,220-222,185-192,1-8,9-17,18-19"
WATTR1A8  IFILE    SQL, FIXED=816, KEY="U484-484,1-8,9-17,18-19"
.
.NAME     TYPE      LENGTH  PHYS   START  DESCRIPTION
.----     ----      ------  ----   -----  -----------
WMURNO    DIM       8       8         1   U/R Number
WMCODE    DIM       9       9         9   Procedure Code
DWMCNT    DIM       2       2        18   Procedure Count
.
WMTIME    FORM      5       4        20   Procedure Time (Minutes)
WMDESC    DIM       80     80        24   Procedure Description
WMREASON  DIM       80     80       104   Reason/Diagnosis for Procedure
DWMSTAT   DIM       1       1       184   Procedure Status
.                                           1 - Unscheduled
.                                           2 - Scheduled
.                                           3 - Pre-Admitted
.                                           4 - Admitted
.                                           5 - Discharged
.                                           6 - Removed from Waiting List
.                                           7 - Performed (No longer used)
.                                           8 - Not Performed (No longer used)
.                                           9 - Cancelled Admission
WMDATE1   DIM       8       8       185   Date on List (ccyymmdd)
WMDATE2   DIM       8       8       193   Date of Last Review (ccyymmdd) /
.                                         Final Outcome of Offer
WMDATE3   DIM       8       8       201   Scheduled Admit. Date (ccyymmdd)
WMDATE4   DIM       8       8       209   Removal Date (ccyymmdd)
WMREMOVE  DIM       3       3       217   Removal Reason (Category WR/BC)
WMUNIT    DIM       3       3       220   Unit/Clinic (Category WU)
WMDOCTOR  DIM       6       6       223   Attending Doctor
WMSTAY    FORM      5       4       229   Estimated Stay (Days)
WMPTY     DIM       3       3       233   Priority (Category TP)
WMNOTICE  DIM       3       3       236   Short Notice (Category WS)
WMPTYPE   DIM       3       3       239   Resident Status (Category T)
WMPCAT    DIM       3       3       242   Admission Type (Cat A)
WMUDEF1   DIM       3       3       245   User Defined 1 (Category W1)
WMUDEF2   DIM       3       3       248   User Defined 2 (Category W2)
WMUDEF3   DIM       3       3       251   User Defined 3 (Category W3)
WMUDEF4   DIM       3       3       254   User Defined 4 (Category W4)
.
WMBOOK    DIM       8       8       257   Booking Number
WMKEYDT   DIM       8       8       265   Keyin Date For Procedure
.                                                 (ccyymmdd)
WTWMPORT  DIM       2       2       273   Locking Indicator
WTWMREAD  DIM       8       8       275   Re-Assessment Date (ccyymmdd)
WTWMDEDA  DIM       8       8       283   Deferred or Staged Adm Date
.                                               (ccyymmdd)
. The following two fields are used for Transmission to the SAHC
.
WTWMUNIQ  FORM      5       4       291   Unique Identifier
WTWMSENT  FORM      1       2       295   Procedure Status 4 & 5 records
.                                         already sent = 1, otherwise = 0
WTWMCLSS  DIM       3       3       297   Patient Cat/Adm Class (Cat P)
.
WTWMDTAD  DIM       8       8       300   Decision to Admit Date
WTWMACAT  DIM       3       3       308   Administrative Category  "CL"
WTWMGADD  DIM       8       8       311   Guaranteed Admission Date
WTWMDABD  DIM       8       8       319   Do Not Admit Before Date/Proposed
.                                         Operation Date
WTWMEATY  DIM       3       3       327   Elective Admission Type  "CC"
WTWMADMC  DIM       3       3       330   Admission Category   "WA"
WTWMECRA  DIM       20      20      333   ECR Approval Number
WTWMGPFA  DIM       20      20      353   GPFH Approval Number
WTWMRFGP  DIM       8       8       373   Referring GP
WTWMGPPC  DIM       6       6       381   GP Practice code
WTWMNAFR  DIM       8       8       387   Not available from date
WTWMNATO  DIM       8       8       395   Not available to date
WTWMGPPT  DIM       2       2       403   GP Practice count
WTWMDOFR  DIM       3       3       405   District of Residence (Cat DA)
WTWMOPER  DIM       4       4       408   Operator who added proc to W/L
WTWMIHSP  DIM       3       3       412   Intended Hospital (pathspaf)
WTWMVLSF  DIM       1       1       415   Validation Letter Sent Flag (Y/N)
WTWMPRO2  DIM       9       9       416   Procedure Code 2
WTWMPRO3  DIM       9       9       425   Procedure Code 3
WTWMTRPA  DIM       8       8       434   Pre-Anaesthetic Date (CCYYMMDD)
WMUDEF5   DIM       3       3       442   User Defined 5 (Category X5)
WMUDEF6   DIM       3       3       445   User Defined 6 (Category X6)
WMUDEF7   DIM       3       3       448   User Defined 7 (Category X7)
WMUDEF8   DIM       3       3       451   User Defined 8 (Category X8)
WTWMRANK  DIM       6       6       454   Patient Rank
WTWMSRCR  DIM       3       3       460   Source of Referral (S)
WTWMDRSA  DIM       8       8       463   Date of ref for 1st spec. assessment
WTWMDOSA  DIM       8       8       471   Date of 1st specialised assessment
WTWMPHSP  DIM       3       3       479   Principal Health serv Purchaser Cat HP
WTWMBSCD  DIM       2       2       482   Booking Status Code
WTWMRSFL  DIM       1       1       484   Record sent flag (Y or N)
WTWMECNT  DIM       9       9       485   ESIS unique count eg. CCYY?????
WTWMCSST  DIM       1       1       494   Consent Obtained (N=NO,Y=YES)
WTWMCSDT  DIM       8       8       495   Consent Changed Date (ccyymmdd)
WTWMASSR  DIM       10      10      503   Assessor Code (pmshcpaf)
WTWMACPR  DIM       8       8       513   Actual Procedure Event Number
WTWMPDSC  DIM       80      80      521   Procedure Description Comments
WTWMRADT  DIM       8       8       601   Referral Accepted Date
WMDESC2   DIM       80      80      609   Procedure Description 2
WMDESC3   DIM       80      80      689   Procedure Description 3
WTWMSPAR  DIM       47      47      769   Spare
.End of Record                      816
.
. LOCAL VARIABLES
. ---------------
CMDLINE   DIM       200
.
DEFPATH   DIM       60
DIM60     DIM       60
DISNUM    FORM      4
.
ERORTEXT  DIM       80
.
INPFILE   DIM       8
.
MINUS3    FORM      "-3"
.
NEWFILE   DIM       60
NEWPATH   DIM       60
.
OLDPATH   DIM       60
.
RECNUM    FORM      8
.
SAVEFLG   FORM      1
.
USINGORC  DIM       1
.
.
. PROGRAM CONSTANTS
. -----------------
CREATEXT  INIT    "Log file created "
DASHLINE  INIT      "--------------------------------------------------":
                    "------------------------------"
ENDGTEXT  INIT    "Ending   fixit (Option "
OPENTEXT  INIT    "Log file opened "
SP60      INIT    "                                                            "
STARTTXT  INIT    "Starting fixit (Option "
.
.
.
PRGNAM    INIT      "CONVERSION WATTR1FD"
PRGID     INIT      "V12WTTR1"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                             MAIN0000                                      *
.*                         Program Mainline                                  *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000                      * init and open files
.
          CALL      OLOG0000                      * create/open log file
MAIN0500  CALL      OPTN0000                      * get option
          BRANCH    COPTION,MAIN1000:             * run c-isam fixit
                            MAIN2000              * run oracle fixit
          GOTO      MAIN9999                      * exit selected
.
MAIN1000  CALL      KDIR0000                      * Keyin directory
          BRANCH    EXIT,MAIN9000
.
MAIN2000  CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN5000:               * yes
                          MAIN0500:               * no
                          MAIN9000                * cancel
.
MAIN5000  BRANCH    COPTION,MAIN6000:             * c-isam fixit
                            MAIN7000              * oracle fixit
.
.         Running c-isam fix
.
MAIN6000  CALL      PREP0000                      * preparing files
          BRANCH    EXIT,MAIN9000
.
          CALL      READ0000                      * read old records and write
          CALL      ENDP0000                      * save/zip saved file
          GOTO      MAIN9000
.
.         Running oracle fix
.
MAIN7000  CALL      OFIX0000                      * run oracle fix
.
MAIN9000  CALL      CLOG0000                      * close log file
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
.
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
          MOVE      ZERO,DISNUM
          
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        Get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Ok to proceed                           *
.*                        TRUE  (1)  Exit option selected                    *
.*              COPTION - option selected                                    *
.*****************************************************************************
.
OPTN0000  MOVE      "No",USINGORC
          REP       "yYnN",USINGORC
          MOVE      USINGORC,KEY1
          REP       "Y-N-",KEY1
          MATCH     "-",KEY1
          GOTO      OPTN9200 IF NOT EQUAL
.
          DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit (c-isam only)"
          MATCH     "Y",USINGORC
          IF        @EQUAL
            DISPLAY   *P1:6,*V2LON,TWO,*HOFF,". Run Fixit (Oracle only)"
          ENDIF
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ":
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000:            * run c-isam fixit
                            OPTN9000             * run oracle fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9200  DISPLAY   *P1:3,*EF,*P1:4," Oracle Option has not been set up ":
                          "correctly":
                    *P1:6," Program will terminate    - ";
          MOVE      "Oracle Option has not been set up correctly",ERORTEXT
          WRITE     LOGFILE,SEQ;*+,ERORTEXT
          CALL      HITENTER
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.********************************************************************
.*                          KDIR0000                                *
.*  Keyin directories                                               *
.********************************************************************
.
KDIR0000  MOVE      ONE,SAVEFLG        * Default to keep file
          KEYIN     *P1:10,*EL,"Do you want to save the original file (Y/N) ? ":
                    ANS;
          REP       "yYnN",ANS
          CMATCH    "Y",ANS
          GOTO      KDIR1000 IF EQUAL
          MOVE      ZERO,SAVEFLG       * not to keep file
          CMATCH    "N",ANS
          GOTO      KDIR1000 IF EQUAL
          BEEP
          GOTO      KDIR0000
.
KDIR1000  CALL      DEFT0000                * Get the default path
          BRANCH    EXIT,KDIR9999
          MOVE      SP60,OLDPATH
.
          DISPLAY   *P1:24,*EL,"The directory path must end with a slash (/) ";
.
.
          MOVE      DEFPATH,OLDPATH
          BRANCH    SAVEFLG,KDIR2000
.
.         Keyin the path for the original file
.
          KEYIN     *P1:12,*EL,"Keyin path for temporarily saving original ":
                    "file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
          GOTO      KDIR3000
.
KDIR2000  KEYIN     *P1:12,*EL,"Keyin path for saving original file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
.
KDIR3000  ENDSET    OLDPATH
          APPEND    SP60,OLDPATH
          RESET     OLDPATH
.
          MATCH     SP60,OLDPATH
          IF        @EQUAL
            PACK      OLDPATH,DEFPATH      * use the default
            DISPLAY   *P10:13,*EL,*V2LON,OLDPATH;
          ENDIF
.
          SQUEEZE   OLDPATH
          CMATCH    EXITCHAR,OLDPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,OLDPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR1000
.
.         Keyin the path for the new file
.
KDIR5000  MOVE      SP60,NEWPATH
          MOVE      DEFPATH,NEWPATH
          KEYIN     *P1:15,*EL,"Keyin path for the new file: ":
                    *P10:16,*EL,*DV,*RV,NEWPATH:
                    *P10:16,NEWPATH;
          ENDSET    NEWPATH
          APPEND    SP60,NEWPATH
          RESET     NEWPATH
.
          MATCH     SP60,NEWPATH
          IF        @EQUAL
            PACK      NEWPATH,DEFPATH      * use the default
            DISPLAY   *P10:16,*V2LON,NEWPATH;
          ENDIF
.
          SQUEEZE   NEWPATH
          CMATCH    EXITCHAR,NEWPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,NEWPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR5000
          GOTO      KDIR9999
.
KDIR9000  MOVE      ONE,EXIT
.
KDIR9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.********************************************************************
.*                          PREP0000                                *
.*  Preparing files                                                 *
.********************************************************************
.
PREP0000  CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
.
          CALL      SLOG0000                      * write start to log file
.
          MOVE      "wattr1af",INPFILE
          CALL      CHEK0000             * check file exists or has been changed
          BRANCH    EXIT,PREP9000
.
          CLOSE     OLDFILE1
          DISPLAY   *P1:24,*EL,"Copying files ... Please wait !! ";
.
.         Copy old file to the keyin directory
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat wattr1af.dat` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "swattr1f.dat",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat wattr1af.idx` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "swattr1f.idx",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
.         Remove the original files
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    "wattr1af",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            MOVE      "Unable to remove the original file.  ",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
            DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
.         Opening old file after being copied
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60
          APPEND    "swattr1f",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE1,DIM60
          TRAPCLR   IO
          IF        OVRCD = 1
            MOVE      "Unable to open saved original file.  ",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
            DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
.         Create the new file
.
          DISPLAY   *P1:24,*EL,"Creating new files ... Please wait !! ";
          PACK      NEWFILE,SP60
          CLEAR     NEWFILE
          APPEND    NEWPATH,NEWFILE
          APPEND    "wattr1af",NEWFILE
          APPEND    SP60,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
PREP1000  PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
.
.  The files new record length and unique key must be written here
.      eg    APPEND    " 195 UC9-16",CMDLINE
.
. Index 1
          APPEND    " 816 UC1-8,9-17,18-19",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.  If the file has more than one index, this piece of code below has 
.  to be repeated with for each individual index
.
. Index 2
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " UC184-184,1-8,9-17,18-19",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
. Index 3
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " UC185-192,1-8,9-17,18-19",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
. Index 4
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " UC1-8,185-192,9-17,18-19",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
. Index 5
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " UC184-184,223-228,220-222,233-235,1-8,9-17,18-19",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
. Index 6
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " UC300-307,184-184,1-8,9-17,18-19",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
. Index 7
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " UC184-184,220-222,185-192,1-8,9-17,18-19",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
. Index 8
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " UC484-484,1-8,9-17,18-19",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
          OPEN      WATTR1A1,NEWFILE
.
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          GOTO      PREP9999
.
PREP5000  MOVE      "Old file not found.  ",ERORTEXT
          WRITE     LOGFILE,SEQ;ERORTEXT
          DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
          CALL      HITENTER
          GOTO      PREP9000
.
PREP8000  MOVE      "Unable to copy original file.  (Error: ",ERORTEXT
          ENDSET    ERORTEXT
          APPEND    TASKID,ERORTEXT
          APPEND    ")  ",ERORTEXT
          RESET     ERORTEXT
          WRITE     LOGFILE,SEQ;ERORTEXT
          DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
          CALL      HITENTER
.
PREP9000  MOVE      ONE,EXIT
.
PREP9999  RETURN
+
.********************************************************************
.*                          CHEK0000                                *
.*  Check if file exists, or has already been converted             *
.********************************************************************
.
CHEK0000  MOVE      ZERO,EXIT
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND GIVING DIM60 IF IO
          OPEN      OLDFILE1,INPFILE
          TRAPCLR   IO
          BRANCH    OVRCD,CHEK2000       * Original file does not exist
          CLOSE     OLDFILE1
          GOTO      CHEK9999
.
CHEK2000  RESET     DIM60
          SCAN      "I * I",DIM60
          DISPLAY   *P1:23,*EF,"File '",INPFILE,"' ";
          IF        @EQUAL
            DISPLAY   "does not exist - ";
            MOVE      "File does not exist",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
          ELSE
            RESET     DIM60
            SCAN      "I * L",DIM60
            IF        @EQUAL
              DISPLAY   "has already been converted - ";
              MOVE      "File has already been converted",ERORTEXT
              WRITE     LOGFILE,SEQ;ERORTEXT
            ELSE
              DISPLAY   "has caused an IO error - ";
              MOVE      "File has caused an IO Error",ERORTEXT
              WRITE     LOGFILE,SEQ;ERORTEXT
            ENDIF
          ENDIF
          MOVE      "File will be bypassed",ERORTEXT
          WRITE     LOGFILE,SEQ;ERORTEXT
          DISPLAY   *+,ERORTEXT
.
          CALL      HITENTER
          DISPLAY   *P1:23,*EF;
.
CHEK9000  MOVE      ONE,EXIT
.
CHEK9999  RETURN
+
.**********************************************************************
.*                               READ0000                             *
.*             loop thru old file and write each record               *
.**********************************************************************
.
READ0000  DISPLAY   *P1:20,*EL,"Record No. : ";
.
          PACK      KEY19,SP60
          CALL      READSOLD                      * position at start
READ1000  CALL      READKOLD                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
.
          ADD       ONE,RECNUM                    * update record counter
          ADD       ONE,DISNUM                    * Display counter
.
          IF        DISNUM = 5000  | RECNUM = 1
            MOVE      ZERO,DISNUM
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
.  All necessary changes to the files field lengths, date changes
.  or any other sort of modification must be made here
.
.    eg    PACK      DSPARE,SP70
.          PACK      PTDSDMDC,SP10
.          PACK      PTDSDDRG,SP10
.
          MOVE      OWMBOOK,WMBOOK 
          MOVE      SP70,WTWMSPAR
.                                               * Pack key here
          PACK      KEY19,WMURNO,WMCODE,DWMCNT,SP70
          CALL      WRWTTRA1                    * write to new file

          GOTO      READ1000                      * get next record
.
READ6000  CLOSE     WATTR1A1                      * close new file
          CLOSE     OLDFILE1                      * close old file
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
          DISPLAY   *P15:20,*V2LON,RECNUM,*HOFF:
                    *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS:
                    *P1:24;
.
READ9999  RETURN
+
.**********************************************************************
.*                               VALD0000                             *
.*        Check if it a valid directory                               *
.**********************************************************************
.
VALD0000  MOVE      ZERO,EXIT
.
          SQUEEZE   DIM60
          ENDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            MOVE      "Directory path must end with a slash (/) ",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
            DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            MOVE      "Directory does not exist ",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
            DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT            * Not valid
.
VALD9999  RETURN
+
.**********************************************************************
.*                               ENDP0000                             *
.*        Remove or zip save file                                     *
.**********************************************************************
.
ENDP0000  MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60        * set up the saved file with path
          APPEND    "swattr1f*",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
          SQUEEZE   DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          IF        SAVEFLG = 1
            APPEND    "gzip -f ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Compressing original file.. ":
                      "Please wait !!  ";
          ELSE
            APPEND    "rm ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Removing original file.. Please wait !!  ";
          ENDIF
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            MOVE      "Saved file not zipped or removed.  ",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
            DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
            CALL      HITENTER
            GOTO      ENDP9999
          ENDIF
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
          DISPLAY   *P1:24,*EL,*B,"Finished processing  ",CDATE,SP1,CTIMEIS:
                    ".  ";
          CALL      HITENTER
.
          CALL      ELOG0000                     * write an end log entry
.
ENDP9999  RETURN
+
.**********************************************************************
.*        DEFT0000 -  Get the default directory                       *
.**********************************************************************
.
DEFT0000  EXECUTE   "ldat wattr1af.dat > temp.txt",TASKID
.
          PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  MOVE      "Error finding 'wattr1af'.  ",ERORTEXT
          WRITE     LOGFILE,SEQ;ERORTEXT
          DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
.
DEFT9999  RETURN
+
.*****************************************************************************
.*                            OLOG0000                                       *
.*                    Open the log file                                      *
.*****************************************************************************
.
OLOG0000  CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      LOGFILE,"v12wttr1.log"       * open log file
          TRAPCLR   IO
          IF        OVRCD = 1
            PREP      LOGFILE,"v12wttr1.log"     * create and open log file
            WRITE     LOGFILE,MINUS3;DASHLINE
            WRITE     LOGFILE,SEQ;CREATEXT,CDATE,SP1,CTIMEIS
          ELSE
            WRITE     LOGFILE,MINUS3;DASHLINE
            WRITE     LOGFILE,SEQ;OPENTEXT,CDATE,SP1,CTIMEIS
          ENDIF
.
OLOG9999  RETURN
+
.*****************************************************************************
.*                            CLOG0000                                       *
.*                    Close the log file                                     *
.*****************************************************************************
.
CLOG0000  CLOSE     LOGFILE
.
CLOG9999  RETURN
+
.*****************************************************************************
.*                            SLOG0000                                       *
.*                    Write a start log to the log file                      *
.*****************************************************************************
.
SLOG0000  WRITE     LOGFILE,SEQ;DASHLINE
          WRITE     LOGFILE,SEQ;STARTTXT,COPTION,RBRACK,SP1,CDATE,SP1:
                                   CTIMEIS
.
SLOG9999  RETURN
+
.*****************************************************************************
.*                            ELOG0000                                       *
.*                    Write an end log to the log file                       *
.*****************************************************************************
.
ELOG0000  WRITE     LOGFILE,SEQ;ENDGTEXT,COPTION,RBRACK,SP1,CDATE,SP1:
                                   CTIMEIS
ELOG9999  RETURN
+
.*****************************************************************************
.*                            OFIX0000                                       *
.*                    Oracle fix to "WATTR1FD"                               *
.*****************************************************************************
.
OFIX0000  DISPLAY   *P1:24,*EL;
          CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,*EL,"Record No. :";
.
          CALL      SLOG0000                      * write start to log file
.
          OPEN      WATTR1A1,"wattr1af"
.
          PACK      KEY19,SP70
          CALL      RSWTTRA1                      * position at start of file
OFIX0500  CALL      RKWTTRA1                      * read next record
          BRANCH    OVRCD,OFIX9000                * eof - finished
.
          ADD       ONE,RECNUM                    * increment record counter
          ADD       ONE,DISNUM                    * Display counter
.         
          IF        DISNUM = 5000  | RECNUM = 1
            MOVE      ZERO,DISNUM 
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
.>>>>>>>  Add code here to populate/update variables
.
          CALL      UPWTTRA1
.
          GOTO      OFIX0500                     * get next record
.         
OFIX9000  CLOSE     WATTR1A1
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
          DISPLAY   *P15:20,*V2LON,RECNUM,*HOFF:
                    *P1:22,"Update completed ",CDATE,SP1,CTIMEIS:
                    *P1:24;
          CALL      HITENTER
.
          CALL      ELOG0000                     * write an end log entry
.
OFIX9999  RETURN
.
OFDUMYIO  RETURN
+
.*****************************************************************************
.*        I/O Routines                                                       *
.*****************************************************************************
.
.                  ******************************    OLD IO ROUTINES
.                  Copy the Positional Read (RS or RDS) from original I/O file,
.                  - change Label to READSOLD and the index name to OLDFILE1
.
READSOLD  RESET     KEY19
          MOVE      ZERO,OVRCD
          READ      OLDFILE1,KEY19;;
          RETURN
.
.                  Copy the Read Next (RK or RDK) from the original I/O file
.                  - change Label to READKOLD and the index name to OLDFILE1
.                  - change any fields that are changing to a prefix of OLD,
.                    eg. PTEDSPAR becomes OLDDSPAR
.
READKOLD  MOVE      ZERO,OVRCD
          READKS    OLDFILE1;WMURNO,WMCODE,DWMCNT,WMTIME,WMDESC:
                             WMREASON,DWMSTAT,WMDATE1,WMDATE2,WMDATE3:
                             WMDATE4,WMREMOVE,WMUNIT,WMDOCTOR,WMSTAY:
                             WMPTY,WMNOTICE,WMPTYPE,WMPCAT:
                             WMUDEF1,WMUDEF2,WMUDEF3,WMUDEF4:
                             OWMBOOK,WMKEYDT,WTWMPORT,WTWMREAD:
                             WTWMDEDA,WTWMUNIQ,WTWMSENT,WTWMCLSS:
                             WTWMDTAD,WTWMACAT,WTWMGADD,WTWMDABD:
                             WTWMEATY,WTWMADMC,WTWMECRA,WTWMGPFA:
                             WTWMRFGP,WTWMGPPC,WTWMNAFR,WTWMNATO:
                             WTWMGPPT,WTWMDOFR,WTWMOPER,WTWMIHSP:
                             WTWMVLSF,WTWMPRO2,WTWMPRO3,WTWMTRPA:
                             WMUDEF5,WMUDEF6,WMUDEF7,WMUDEF8:
                             WTWMRANK,WTWMSRCR,WTWMDRSA,WTWMDOSA:
                             WTWMPHSP,WTWMBSCD,WTWMRSFL,WTWMECNT:
                             WTWMCSST,WTWMCSDT,WTWMASSR,WTWMACPR:
                             WTWMPDSC,WTWMRADT,WMDESC2,WMDESC3,OWTWMSPA
          GOTO      OVERCOND IF OVER
          RETURN
.
.                  ******************************    NEW IO ROUTINES
.                  Copy the Write statement from the new I/O file           
.                  (no changes are needed to this routine)               
.
.                  For Oracle fixits, you will probably also need to copy
.                  the RS.., RK.... & UP.... I/O routines for the new FD.
.
RSWTTRA1  RESET     KEY19
          READ      WATTR1A1,KEY19;;
          RETURN
.
RKWTTRA1  MOVE      ZERO,OVRCD
          READKS    WATTR1A1;WMURNO,WMCODE,DWMCNT,WMTIME,WMDESC:
                             WMREASON,DWMSTAT,WMDATE1,WMDATE2,WMDATE3:
                             WMDATE4,WMREMOVE,WMUNIT,WMDOCTOR,WMSTAY:
                             WMPTY,WMNOTICE,WMPTYPE,WMPCAT:
                             WMUDEF1,WMUDEF2,WMUDEF3,WMUDEF4:
                             WMBOOK,WMKEYDT,WTWMPORT,WTWMREAD:
                             WTWMDEDA,WTWMUNIQ,WTWMSENT,WTWMCLSS:
                             WTWMDTAD,WTWMACAT,WTWMGADD,WTWMDABD:
                             WTWMEATY,WTWMADMC,WTWMECRA,WTWMGPFA:
                             WTWMRFGP,WTWMGPPC,WTWMNAFR,WTWMNATO:
                             WTWMGPPT,WTWMDOFR,WTWMOPER,WTWMIHSP:
                             WTWMVLSF,WTWMPRO2,WTWMPRO3,WTWMTRPA:
                             WMUDEF5,WMUDEF6,WMUDEF7,WMUDEF8:
                             WTWMRANK,WTWMSRCR,WTWMDRSA,WTWMDOSA:
                             WTWMPHSP,WTWMBSCD,WTWMRSFL,WTWMECNT:
                             WTWMCSST,WTWMCSDT,WTWMASSR,WTWMACPR:
                             WTWMPDSC,WTWMRADT,WMDESC2,WMDESC3,WTWMSPAR
.
          GOTO      OVERCOND IF OVER
.
          RETURN
.
UPWTTRA1  MOVE      ZERO,OVRCD 
.
          UPDATE    WATTR1A1;WMURNO,WMCODE,DWMCNT,WMTIME,WMDESC:
                             WMREASON,DWMSTAT,WMDATE1,WMDATE2,WMDATE3:
                             WMDATE4,WMREMOVE,WMUNIT,WMDOCTOR,WMSTAY:
                             WMPTY,WMNOTICE,WMPTYPE,WMPCAT:
                             WMUDEF1,WMUDEF2,WMUDEF3,WMUDEF4:
                             WMBOOK,WMKEYDT,WTWMPORT,WTWMREAD:
                             WTWMDEDA,WTWMUNIQ,WTWMSENT,WTWMCLSS:
                             WTWMDTAD,WTWMACAT,WTWMGADD,WTWMDABD:
                             WTWMEATY,WTWMADMC,WTWMECRA,WTWMGPFA:
                             WTWMRFGP,WTWMGPPC,WTWMNAFR,WTWMNATO:
                             WTWMGPPT,WTWMDOFR,WTWMOPER,WTWMIHSP:
                             WTWMVLSF,WTWMPRO2,WTWMPRO3,WTWMTRPA:
                             WMUDEF5,WMUDEF6,WMUDEF7,WMUDEF8:
                             WTWMRANK,WTWMSRCR,WTWMDRSA,WTWMDOSA:
                             WTWMPHSP,WTWMBSCD,WTWMRSFL,WTWMECNT:
                             WTWMCSST,WTWMCSDT,WTWMASSR,WTWMACPR:
                             WTWMPDSC,WTWMRADT,WMDESC2,WMDESC3,WTWMSPAR
.
          RETURN
.
WRWTTRA1  RESET     KEY19
          MOVE      ZERO,OVRCD
.
          WRITE     WATTR1A1,KEY19;WMURNO,WMCODE,DWMCNT,WMTIME,WMDESC:
                                   WMREASON,DWMSTAT,WMDATE1,WMDATE2,WMDATE3:
                                   WMDATE4,WMREMOVE,WMUNIT,WMDOCTOR,WMSTAY:
                                   WMPTY,WMNOTICE,WMPTYPE,WMPCAT:
                                   WMUDEF1,WMUDEF2,WMUDEF3,WMUDEF4:
                                   WMBOOK,WMKEYDT,WTWMPORT,WTWMREAD:
                                   WTWMDEDA,WTWMUNIQ,WTWMSENT,WTWMCLSS:
                                   WTWMDTAD,WTWMACAT,WTWMGADD,WTWMDABD:
                                   WTWMEATY,WTWMADMC,WTWMECRA,WTWMGPFA:
                                   WTWMRFGP,WTWMGPPC,WTWMNAFR,WTWMNATO:
                                   WTWMGPPT,WTWMDOFR,WTWMOPER,WTWMIHSP:
                                   WTWMVLSF,WTWMPRO2,WTWMPRO3,WTWMTRPA:
                                   WMUDEF5,WMUDEF6,WMUDEF7,WMUDEF8:
                                   WTWMRANK,WTWMSRCR,WTWMDRSA,WTWMDOSA:
                                   WTWMPHSP,WTWMBSCD,WTWMRSFL,WTWMECNT:
                                   WTWMCSST,WTWMCSDT,WTWMASSR,WTWMACPR:
                                   WTWMPDSC,WTWMRADT,WMDESC2,WMDESC3,WTWMSPAR
          RETURN
.
          INC       STD001IO

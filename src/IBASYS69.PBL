+
.**********************************************************************
.* System   :  System Managment Maintenance                           *
.* Program  :  IBASYS69                                               *
.* Desc     :  Illegal Menu Security Access Audit Report              *
.**********************************************************************
.* Date     :  30/01/90                                               *
.* Author   :  S O'Gorman                                             *
.* Comment  :                                                         *
.**********************************************************************
.
          INC       STD001FD
.
          INC       IBAPASFD/INC
          INC       IBAIMNFD/INC
          INC       IBAPORFD/INC
+
.**********************************************************************
.*                           CONSTANTS                                *
.**********************************************************************
.
ZEDS      INIT      "ZZZZZZZZ"
.
CFINISH   INIT      "Finish  "
CSTART    INIT      "Start   "
.
.
PIPE      INIT      "|"
.
UNDLN77   INIT      "----------------------------------------":
                    "-------------------------------------"
+
.**********************************************************************
.*                       GLOBAL VARIABLES                             *
.**********************************************************************
.
COL       FORM       2
.
DISPTIME  DIM        5
DIM30     DIM       30
.
FLAGUSER  FORM       1
FROMCODE  DIM        8
FROMDATE  DIM        8
FROMDDTE  DIM        8
FROMUSER  DIM        4
FROMPORT  DIM        2
FROMPROG  DIM        8
FUSRDESC  DIM       30
.
KEYPORT   DIM        2
.
RECCOUNT  FORM       5
RDISOPTN  FORM       1
.
TOCODE    DIM        8
TODATE    DIM        8
TODDTE    DIM        8
TODAY     DIM        8
TOUSER    DIM        4
TOPORT    DIM        2
TOPROG    DIM        8
TUSRDESC  DIM       30
.
USER      DIM        4
.
VERT      FORM       2
.
+
PRGID     INIT      "IBASYS69"
PRGNAM    INIT      "Illegal Menu Security Access Audit Report"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                           MAINLINE                                 *
.**********************************************************************
.
ML0000    CALL      INIT0000           * initialization
.
ML0100    CALL      CLR0000            * Clear Working Variables
          CALL      SCRN0000           * Main Option Screen
          BRANCH    EXIT,ML9999
.
ML0200    BRANCH    OPTION,ML1000,ML2000,ML3000,ML4000
          GOTO      ML9999
.
.         Range of user id's
.
ML1000
          CALL      USR0000           * Input User Range
          BRANCH    EXIT,ML0100
.
          CALL      DATE0000           * Input Report Date Range
          BRANCH    EXIT,ML1000
          GOTO      ML5000
.
.         Range of dates
.
ML2000
          CALL      DATE0000           * Input Report Date Range
          BRANCH    EXIT,ML0100
          GOTO      ML5000
.
.         Range of ports
.
ML3000
          CALL      PORT0000           * Input Port Range
          BRANCH    EXIT,ML0100
.
          CALL      DATE0000           * Input Report Date Range
          BRANCH    EXIT,ML3000
          GOTO      ML5000
.
.         Range of programs
.
ML4000
          CALL      PROG0000           * Input Program Range
          BRANCH    EXIT,ML0100
          CALL      DATE0000           * Input Report Date Range
          BRANCH    EXIT,ML4000
ML5000
          CALL      CONT0000           * Ok to Continue
          BRANCH    EXIT,ML0200,ML0100
.
          CALL      EXTR0000           * Extract Records in relevant Range
.
          GOTO      ML0100
.
ML9999    CHAIN     PGM
+
.**********************************************************************
.*                           INIT0000                                 *
.**********************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P64:24,"Opening"
.
          DISPLAY   *P72:24,"ihapassf";
          OPEN      IHAPASS1,"ihapassf"
.
          DISPLAY   *P72:24,"ibaimenf";
          OPEN      IBAIMEN1,"ibaimenf"
          OPEN      IBAIMEN2,"ibaimenf"
          OPEN      IBAIMEN3,"ibaimenf"
          OPEN      IBAIMEN4,"ibaimenf"
.
          DISPLAY   *P72:24,"ibaportf"
          OPEN      IBAPORT1,"ibaportf"
.
          PACK      TODAY,CCC,CYY,CMM,CDD
          REP       " 0",TODAY
INIT9999
          RETURN
.
.********************************************************************** 
.*                            CLR0000                                 *
.*                   Clear all Working Variables                      *
.********************************************************************** 
.
CLR0000
          MOVE      ZERO,CPAGENO                 * Page Number
          MOVE      ZERO,RECCOUNT                * Record Count
.
          MOVE      SP30,FUSRDESC                * From Description
          MOVE      SP30,TUSRDESC                * To Description
.
          RETURN
.
.********************************************************************** 
.*                            SCRN0000                                *
.*                        Main Option Screen                          *
.********************************************************************** 
.
SCRN0000
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON," 0",*HOFF," = Exit":
                    *P1:5,*V2LON," 1",*HOFF," = User Range":
                    *P1:6,*V2LON," 2",*HOFF," = Date Range":
                    *P1:7,*V2LON," 3",*HOFF," = Port Range":
                    *P1:8,*V2LON," 4",*HOFF," = Program Range";
.
          DISPLAY   *P1:10,"Please Select : ",UNDLN2;
SCRN1000
          KEYIN     *P17:10,*EL,*DV,UNDLN2,*P17:10,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      SCRN9000 IF EQUAL
.
          DISPLAY   *P17:10,*V2LON,OPTION
          BRANCH    OPTION,SCRN9900,SCRN9900,SCRN9900,SCRN9900
.
          BEEP
          GOTO      SCRN1000
SCRN9000
          MOVE      TRUE,EXIT
          GOTO      SCRN9999
SCRN9900
          MOVE      FALSE,EXIT
SCRN9999
          RETURN
.
.********************************************************************** 
.*                            CONT0000                                *
.*                      Ok to Continue (Y/N/C)                        *
.********************************************************************** 
.
CONT0000
          CALL      CONTQST
.
          LOAD      EXIT,CEXIT,ZERO,ONE,TWO
.
          RETURN
.
.********************************************************************** 
.*                            DATE0000                                *
.*                   Keyin Date Range for Report                      *
.********************************************************************** 
.
DATE0000
          DISPLAY   *P1:17,*EF,"Report Date From : ";
.
          UNPACK    SP30,CYEAR,CMON,CDAY         * Set-up Variables for the 
          MOVE      CCC,CCENT                    * KEYDATE Routine
          MOVE      TEN7,CVERT
          MOVE      TWENTY,CCOL
          MOVE      CVERT,EVERT
          MOVE      "40",ECOL
          MOVE      ZERO,CHIGHLT
          MOVE      ONE,CCENTRY
          MOVE      ZERO,CDEFDTE
.
          CALL      KEYDATE
.
          BRANCH    CQUEST,DATE0000              * Re-Key if a "?"
          MATCH     SP2,CDAY
          GOTO      DATE9000 IF EQUAL
.
          PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY    * Pack From Date
          REP       "0",FROMDATE
.
          MATCH     FROMDATE,TODAY               * Check From Date < Today
          GOTO      DATE1000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Date Must be Less than Today's Date - Hit <":
                               *V2LON,"ENTER",*HOFF,"> to Continue ";
          KEYIN     *EOFF,ANS;
          DISPLAY   *P1:24,*EL;
.
          GOTO      DATE0000
DATE1000
          DISPLAY   *P1:19,*EF,"Report Date To   : ";
.
          UNPACK    SP30,CYEAR,CMON,CDAY         * Set-up Variables for the 
          MOVE      CCC,CCENT                    * KEYDATE Routine
          MOVE      TEN9,CVERT
          MOVE      TWENTY,CCOL
          MOVE      CVERT,EVERT
          MOVE      "40",ECOL
          MOVE      ZERO,CHIGHLT
          MOVE      ONE,CCENTRY
          MOVE      ZERO,CDEFDTE
.
          CALL      KEYDATE
.
          BRANCH    CQUEST,DATE1000              * Re-Key if a "?"
          MATCH     SP2,CDAY
          GOTO      DATE0000 IF EQUAL
.
          PACK      TODATE,CCENT,CYEAR,CMON,CDAY * Pack the To Date
          REP       "0",TODATE
.
          MATCH     TODATE,TODAY                 * Check if To Date < Today
          GOTO      DATE2000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Date Must be Less than Today's Date - Hit <":
                               *V2LON,"ENTER",*HOFF,"> to Continue ";
          KEYIN     *EOFF,ANS;
          DISPLAY   *P1:24,*EL;
.
          GOTO      DATE1000
DATE2000
          MATCH     FROMDATE,TODATE              * Check if From Date < To Date
          GOTO      DATE3000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"From Date Must be Greater ":
                                  "than the To Date - Hit <":
                               *V2LON,"ENTER",*HOFF,"> to Continue ";
          KEYIN     *EOFF,ANS;
          DISPLAY   *P1:24,*EL;
.
          GOTO      DATE1000
.
DATE3000
          UNPACK    FROMDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPDATE,FROMDDTE
.
          UNPACK    TODATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPDATE,TODDTE
.
          MOVE      FALSE,EXIT
          GOTO      DATE9999
DATE9000
          MOVE      TRUE,EXIT
DATE9999  
          RETURN
+
.**************************************************************************
.*                               USR0000                                  *
.*                     Input User Code Report Range                       *
.**************************************************************************
.
USR0000
          MOVE        TWENTY,COL
          MOVE        TEN3,VERT
.
          DISPLAY    *P1:VERT,*EF,"Report User From : ",UNDLN4;
.
          MOVE        ONE,RDISOPTN
          MOVE        ONE,FLAGUSER
          CALL        USER0000                   * Input User Code
.
          BRANCH      EXIT,USR9999
.
          MOVE        USER,FROMUSER
.
          MATCH       SP4,USER
          GOTO        USR1000 IF EQUAL
.
          MOVE        SECUSER,FUSRDESC
USR1000
          MOVE        TEN5,VERT
.
          DISPLAY    *P1:VERT,*EF,"Report User To   : ",UNDLN4;
.
          MOVE        TWO,RDISOPTN
          MOVE        TWO,FLAGUSER
          CALL        USER0000                   * Input User Code
.
          BRANCH      EXIT,USR0000
.
          MOVE        USER,TOUSER
.
          MATCH       SP4,USER
          GOTO        USR2000 IF EQUAL
.
          MOVE        SECUSER,TUSRDESC
.
USR2000   MATCH       FROMUSER,TOUSER
          GOTO        USR9999 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"From User Must be Less than To User - Hit <":
                               *V2LON,"ENTER",*HOFF,"> to Continue ";
          KEYIN     *EOFF,ANS;
          DISPLAY   *P1:24,*EL;
.
          GOTO      USR1000
.
USR9999
          RETURN
+
.**************************************************************************
.*                               USER0000                                 *
.*                           Input User Code                              *
.**************************************************************************
.
USER0000
            MOVE       SP4,USER
.
            DISPLAY    *PCOL:VERT,UNDLN4;
.
            KEYIN      *PCOL:VERT,*V2LON,USER;    * keyin user code
.
            ENDSET     USER
            APPEND     SP4,USER
            RESET      USER
.
            BRANCH     FLAGUSER,USER0100,USER0200
USER0100
            PACK       FROMCODE,USER,SP10
            GOTO       USER0300
USER0200
            PACK       TOCODE,USER,SP10
USER0300
            DISPLAY    *PCOL:VERT,*V2LON,USER;
.
            MATCH      SP4,USER               * test for spaces
            GOTO       USER0400 IF NOT EQUAL
.
            BRANCH     FLAGUSER,USER0310,USER0320
USER0310
            MOVE       CSTART,FROMCODE
            MOVE       SP10,USER
.
            MOVE       FALSE,EXIT
            DISPLAY    *PCOL:VERT,*V2LON,FROMCODE;
            MOVE       SP30,SECUSER
            GOTO       USER9999
USER0320
            MOVE       CFINISH,TOCODE
            MOVE       ZEDS,USER
.
            MOVE       FALSE,EXIT
            DISPLAY    *PCOL:VERT,*V2LON,TOCODE;
            MOVE       SP30,SECUSER
            GOTO       USER9999
USER0400
            MATCH      QUEST,USER             * test for ?
            GOTO       USER1000 IF NOT EQUAL
.
USER0500    CALL       IHAPASDS               * display all security codes
            BRANCH     OVRCD,USER9000         * no user codes on file
.
.------ keyin the user code after a ? option ------
.
USER0550
            MOVE       SP4,USER
            DISPLAY    *P1:24,*EL,"User Code : ",UNDLN4;
            KEYIN      *P13:24,*V2LON,*RV,USER;
            DISPLAY    *P13:24,*V2LON,USER;
.
            ENDSET     USER
            APPEND     SP4,USER
            RESET      USER
.
            MATCH      QUEST,USER            * test for ?
            GOTO       USER0500 IF EQUAL
.
            BRANCH     FLAGUSER,USER0560,USER0570
USER0560
            PACK       FROMCODE,USER,SP10
            GOTO       USER0580
USER0570
            PACK       TOCODE,USER,SP10
USER0580
            MATCH      SP4,USER              * test for spaces
            GOTO       USER0900 IF NOT EQUAL
.
            BRANCH     FLAGUSER,USER0600,USER0700
USER0600
            MOVE       CSTART,FROMCODE
            MOVE       SP10,USER
.
            CALL       RDIS0000
            MOVE       FALSE,EXIT
            DISPLAY    *PCOL:VERT,*V2LON,FROMCODE;
            GOTO       USER9999
USER0700
            MOVE       CFINISH,TOCODE
            MOVE       ZEDS,USER
.
            CALL       RDIS0000
            MOVE       FALSE,EXIT
            DISPLAY    *PCOL:VERT,*V2LON,TOCODE;
            GOTO       USER9999
.
.------ user code has been entered after a ? option ------
.
USER0900    MOVE       USER,KEY4
            CALL       RDPASS1                * read security file
.
            BRANCH     OVRCD,USER7000
.
            CALL       RDIS0000               * Redisplay Screen
.
            GOTO       USER9500
.
.------ user code has been entered ------
.
USER1000    MOVE       USER,KEY4
            CALL       RDPASS1                * read security file
.
            BRANCH     OVRCD,USER8000
            GOTO       USER9500
.
. ---- user code does not exist in ihapass for ? option keyin ----
.
USER7000    DISPLAY    *P1:24,*EL:
                       *P1:24,*B,"Security code not in Security file - Hit <":
                              *V2LON,"ENTER",*HOFF,"> to Continue ";
            KEYIN      *EOFF,ANS;
            DISPLAY    *P1:24,*EL;
            GOTO       USER0550
.
. ---- user code does not exist in ihapass ----
.
USER8000    DISPLAY    *P1:24,*EL:
                       *P1:24,*B,"Security code not in Security file - Hit <":
                              *V2LON,"ENTER",*HOFF,"> to Continue ";
            KEYIN      *EOFF,ANS;
            DISPLAY    *P1:24,*EL;
            GOTO       USER0000
.
.------ No user code entered ------
.
USER9000    MOVE       TRUE,EXIT
            GOTO       USER9999
.
.------ display user details ------
.
USER9500    DISPLAY    *P20:VERT,*V2LON,USER:
                       *P40:VERT,*HOFF,SECUSER;
.
USER9999
            RETURN
+
.
.********************************************************************** 
.*                            RDIS0000                                *
.*                   Redisplay Screen After a "?"                     *
.********************************************************************** 
.
RDIS0000
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON," 0",*HOFF," = Exit":
                    *P1:5,*V2LON," 1",*HOFF," = Date Range":
                    *P1:6,*V2LON," 2",*HOFF," = User Range":
                    *P1:7,*V2LON," 3",*HOFF," = Port Range":
                    *P1:8,*V2LON," 4",*HOFF," = Program Range":
                    *P1:10,"Please Select : ",*V2LON,OPTION;
.
          BRANCH    RDISOPTN,RDIS1000,RDIS2000,RDIS3000,RDIS4000
RDIS1000
          DISPLAY   *P1:13,"Report User From : ":
                    *P20:13,*V2LON,FROMCODE:
                    *P40:13,*HOFF,FUSRDESC;
.
          GOTO      RDIS9999
RDIS2000
          DISPLAY   *P1:13,"Report User From : ":
                    *P20:13,*V2LON,FROMCODE:
                    *P40:13,*HOFF,FUSRDESC:
                    *P1:15,"Report User To   : ":
                    *P20:15,*V2LON,TOCODE:
                    *P40:15,*HOFF,TUSRDESC;
.
          GOTO      RDIS9999
RDIS3000
          DISPLAY   *P1:13,"Report Port From : ":
                    *P20:13,*V2LON,FROMCODE;
.
          GOTO      RDIS9999
RDIS4000
          DISPLAY   *P1:13,"Report Port From : ":
                    *P20:13,*V2LON,FROMCODE,*HOFF:
                    *P1:15,"Report Port To   : ":
                    *P20:15,*V2LON,TOCODE;
RDIS9999
          RETURN
.
.********************************************************************** 
.*                            PORT0000                                *
.*                   Keyin Port Range for Report                      *
.********************************************************************** 
.
PORT0000
          DISPLAY   *P1:13,*EF,"Report Port From : ",UNDLN2;
.
          KEYIN     *P20:13,*V2LON,FROMPORT;
.
          ENDSET    FROMPORT
          APPEND    SP2,FROMPORT
          RESET     FROMPORT
.
          PACK      FROMCODE,FROMPORT,SP10
          DISPLAY   *P20:13,*V2LON,FROMCODE;
.
          MATCH     QUEST,FROMPORT
          GOTO      PORT0100 IF NOT EQUAL
.
          CALL      PQST0000
          MOVE      KEYPORT,FROMPORT
          PACK      FROMCODE,FROMPORT,SP10
.
          MOVE      THREE,RDISOPTN
          CALL      RDIS0000
.
          BRANCH    EXIT,PORT9000                * Spaces entered after "?"
          BRANCH    OVRCD,PORT9000               * No Codes on File
          MOVE      KEYPORT,FROMPORT
PORT0100
          MATCH     SP2,FROMPORT
          GOTO      PORT0500 IF NOT EQUAL
.
          MOVE      CSTART,FROMCODE
          MOVE      SP2,FROMPORT
.
          DISPLAY   *P20:13,*V2LON,FROMCODE;
.
          GOTO      PORT1000
PORT0500
          PACK      KEY2,FROMPORT
          CALL      RDPORT1
          COMPARE   ZERO,OVRCD
          GOTO      PORT1000 IF EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"Port Not on File - Hit <":
                               *V2LON,"ENTER",*HOFF,"> to Continue ";
          KEYIN     *EOFF,ANS;
          DISPLAY   *P1:24,*EL;
.
          GOTO      PORT0000
PORT1000
.
          DISPLAY   *P1:15,*EF,"Report Port To   : ",UNDLN2;
.
          KEYIN     *P20:15,*V2LON,TOPORT;
.
          ENDSET    TOPORT
          APPEND    SP2,TOPORT
          RESET     TOPORT
.
          PACK      TOCODE,TOPORT,SP10
          DISPLAY   *P20:15,*V2LON,TOCODE;
.
          MATCH     SP2,TOPORT
          GOTO      PORT1100 IF NOT EQUAL
.
          MOVE      CFINISH,TOCODE
          MOVE      ZEDS,TOPORT
.
          DISPLAY   *P20:15,*V2LON,TOCODE;
          GOTO      PORT9900
PORT1100
          MATCH     QUEST,TOPORT
          GOTO      PORT1200 IF NOT EQUAL
.
          CALL      PQST0000
          MOVE      KEYPORT,TOPORT
          PACK      TOCODE,TOPORT,SP10
.
          MOVE      FOUR,RDISOPTN
          CALL      RDIS0000
.
          BRANCH    EXIT,PORT0000                * Spaces entered after "?"
          BRANCH    OVRCD,PORT9000               * No Codes on File
          MOVE      KEYPORT,TOPORT
PORT1200
          MATCH     SP2,TOPORT
          GOTO      PORT1500 IF NOT EQUAL
.
          MOVE      CFINISH,TOCODE
          MOVE      ZEDS,TOPORT
.
          DISPLAY   *P20:15,*V2LON,TOCODE;
.
          PACK      TOCODE,TOPORT,SP10
          GOTO      PORT9900
PORT1500
          PACK      KEY2,TOPORT
          CALL      RDPORT1
          COMPARE   ZERO,OVRCD
          GOTO      PORT2000 IF EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"Port Not on File - Hit <":
                               *V2LON,"ENTER",*HOFF,"> to Continue ";
          KEYIN     *EOFF,ANS;
          DISPLAY   *P1:24,*EL;
.
          GOTO      PORT1000
PORT2000
          MATCH     FROMPORT,TOPORT
          GOTO      PORT9900 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"From Port Must be Less than To Port - Hit <":
                               *V2LON,"ENTER",*HOFF,"> to Continue ";
          KEYIN     *EOFF,ANS;
          DISPLAY   *P1:24,*EL;
.
          GOTO      PORT1000
PORT9000
          MOVE      TRUE,EXIT
          GOTO      PORT9999
PORT9900
          MOVE      FALSE,EXIT
PORT9999
          RETURN
.
.********************************************************************** 
.*                            PQST0000                                *
.*                  Question Mark Routine for Port                    *
.********************************************************************** 
.
PQST0000
          DISPLAY   *P1:3,*EF:
                    *P35:4,*V2LON,*ULON,"VALID PORTS";
.
          MOVE      SIX,CVERT
          MOVE      FIVE,CCOL
.
          PACK      KEY2,SP2
          CALL      RDSPORT1
.
          CALL      RDKPORT1
          BRANCH    OVRCD,PQST8000               * No Ports on File
.
          CALL      RDSPORT1
PQST1000
          CALL      RDKPORT1
          BRANCH    OVRCD,PQST5000               * End of Ports on File
.
          DISPLAY   *PCCOL:CVERT,*V2LON,IBAPPORT;
          ADD       ONE,CVERT
.
          COMPARE   TWENTY3,CVERT
          GOTO      PQST7000 IF EQUAL            * End of column
.
          GOTO      PQST1000
PQST5000
          DISPLAY   *P1:24,*EL,"Port : ",UNDLN2;
.
          KEYIN     *P8:24,*V2LON,KEYPORT;
.
          ENDSET    KEYPORT
          APPEND    SP2,KEYPORT
          RESET     KEYPORT
.
          MATCH     SP2,KEYPORT
          GOTO      PQST8500 IF EQUAL
.
          MATCH     QUEST,KEYPORT
          GOTO      PQST0000 IF EQUAL
.
          PACK      KEY2,KEYPORT
          CALL      RDPORT1
          COMPARE   ZERO,OVRCD
          GOTO      PQST9000 IF EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"Port Not on File - Hit <":
                               *V2LON,"ENTER",*HOFF,"> to Continue ";
          KEYIN     *EOFF,ANS;
          DISPLAY   *P1:24,*EL;
.
          GOTO      PQST5000
PQST7000
          MOVE      SIX,CVERT
          ADD       FIVE,CCOL
.
          COMPARE   "74",CCOL                    * Check for full Screen
          GOTO      PQST1000 IF LESS
.
          DISPLAY   *P1:24,*EL,*B,"Hit Enter to Continue - Hit <":
                               *V2LON,"ENTER",*HOFF,"> to Continue ";
          KEYIN     *EOFF,ANS;
.
          DISPLAY   *P1:4,*EF;
          MOVE      ONE,CCOL
          MOVE      SIX,CVERT
          GOTO      PQST1000
PQST8000
          DISPLAY   *P1:24,*EL,*B,"No Ports on File - Hit <":
                               *V2LON,"ENTER",*HOFF,"> to Continue ";
          KEYIN     *EOFF,ANS;
          DISPLAY   *P1:24,*EL;
          MOVE      ONE,OVRCD
          GOTO      PQST9999
PQST8500
          MOVE      ZERO,OVRCD
          MOVE      TRUE,EXIT
PQST9000
          MOVE      ZERO,OVRCD
          MOVE      FALSE,EXIT
PQST9999
          RETURN
.
.********************************************************************** 
.*                            PROG0000                                *
.*                  Keyin Program Range for Report                    *
.********************************************************************** 
.
PROG0000
          DISPLAY   *P1:13,*EF,"Report Program From : ",UNDLN8;
.
          KEYIN     *P23:13,*V2LON,FROMPROG;
.
          ENDSET    FROMPROG
          APPEND    SP8,FROMPROG
          RESET     FROMPROG
.
          PACK      FROMCODE,FROMPROG,SP10
          DISPLAY   *P23:13,*V2LON,FROMCODE;
.
          MATCH     SP8,FROMPROG
          GOTO      PROG0500 IF NOT EQUAL
.
          MOVE      CSTART,FROMCODE
          MOVE      SP8,FROMPROG
.
          DISPLAY   *P23:13,*V2LON,FROMCODE;
.
          GOTO      PROG1000
PROG0500
          MATCH     QUEST,FROMPROG
          GOTO      PROG1000 IF NOT EQUAL
.
          BEEP
          GOTO      PROG0000
PROG1000
          DISPLAY   *P1:15,*EF,"Report Program To   : ",UNDLN8;
.
          KEYIN     *P23:15,*V2LON,TOPROG;
.
          ENDSET    TOPROG
          APPEND    SP8,TOPROG
          RESET     TOPROG
.
          DISPLAY   *P23:15,*V2LON,TOPROG;
          PACK      TOCODE,TOPROG,SP10
.
          MATCH     SP8,TOPROG
          GOTO      PROG1500 IF NOT EQUAL
.
          MOVE      CFINISH,TOCODE
          MOVE      SP2,TOPROG
.
          MOVE      ZEDS,TOPROG
          DISPLAY   *P23:15,*V2LON,TOCODE;
.
          GOTO      PROG9900
PROG1500
          MATCH     QUEST,TOPROG
          GOTO      PROG5000 IF NOT EQUAL
.
          BEEP
          GOTO      PROG0000
PROG5000
          MATCH     FROMPROG,TOPROG
          GOTO      PROG9900 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"From Program Must be Less than ":
                                  "To Program - Hit <":
                               *V2LON,"ENTER",*HOFF,"> to Continue ";
          KEYIN     *EOFF,ANS;
          DISPLAY   *P1:24,*EL;
.
          GOTO      PROG1000
PROG9000
          MOVE      TRUE,EXIT
          GOTO      PROG9999
PROG9900
          MOVE      FALSE,EXIT
PROG9999
          RETURN
.
.********************************************************************** 
.*                            EXTR0000                                *
.*                    Extract Data from ibaimenf                      *
.********************************************************************** 
.
EXTR0000
          PRINT     *N,*1,"";
          CALL      HEAD0000
.
.         Position at the start of the requested data.
.
          BRANCH    OPTION,EXTR0100,EXTR0200,EXTR0300,EXTR0400
.
EXTR0100
          PACK      KEY22,FROMUSER,FROMDATE,SP30
          CALL      RSIBIMN1
          DISPLAY   *P1:24,*EL,*P10:24,"User: ",*P30:24,"Port: ":
                    *P60:24,"Date: ";
          GOTO      EXTR0500
.
EXTR0200
          PACK      KEY18,FROMDATE,SP30
          CALL      RSIBIMN2
          DISPLAY   *P1:24,*EL,*P10:24,"Date: ",*P30:24,"Time: ":
                    *P60:24,"Port: ";
          GOTO      EXTR0500
.
EXTR0300
          PACK      KEY18,FROMPORT,FROMDATE,SP30
          CALL      RSIBIMN3
          DISPLAY   *P1:24,*EL,*P10:24,"Port: ",*P30:24,"Date: ":
                    *P60:24,"Time: ";
          GOTO      EXTR0500
.
EXTR0400
          PACK      KEY26,FROMPROG,FROMDATE,SP30
          DISPLAY   *P1:24,*EL,*P10:24,"Prog: ",*P30:24,"Port: ":
                    *P60:24,"Date: ";
          CALL      RSIBIMN4
.
.         Loop through the data and print the records found
.
EXTR0500
          BRANCH    OPTION,EXTR1000,EXTR2000,EXTR3000,EXTR4000
EXTR1000
          CALL      RKIBIMN1
          BRANCH    OVRCD,EXTR8000               * No More Records on File
.
          MATCH     IBIMUSER,TOUSER
          GOTO      EXTR8000 IF LESS
.
          MATCH     FROMDATE,IBIMDATE
          GOTO      EXTR1100 IF LESS
.
          MATCH     IBIMDATE,TODATE
          GOTO      EXTR1200 IF LESS
.
          UNPACK    IBIMDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P16:24,*V2LON,IBIMUSER:
                    *P36:24,IBIMPORT:
                    *P66:24,CPCDATE;
.
          CALL      PRNT0000
          GOTO      EXTR1000
.
EXTR1100  PACK      KEY22,IBIMUSER,FROMDATE,SP30
          CALL      RSIBIMN1
          GOTO      EXTR1000
.
EXTR1200  PACK      KEY22,IBIMUSER,ZEDS,ZEDS,ZEDS
          CALL      RSIBIMN1
          GOTO      EXTR1000
EXTR2000
          CALL      RKIBIMN2
          BRANCH    OVRCD,EXTR8000               * No More Records on File
.
          MATCH     IBIMDATE,TODATE
          GOTO      EXTR8000 IF LESS
.
          UNPACK    IBIMDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P16:24,*V2LON,CPCDATE:
                    *P36:24,IBIMTIME:
                    *P66:24,IBIMPORT;
.
          CALL      PRNT0000
          GOTO      EXTR2000
.
EXTR3000
          CALL      RKIBIMN3
          BRANCH    OVRCD,EXTR8000               * No More Records on File
.
          MATCH     IBIMPORT,TOPORT
          GOTO      EXTR8000 IF LESS
.
          MATCH     FROMDATE,IBIMDATE
          GOTO      EXTR3100 IF LESS
.
          MATCH     IBIMDATE,TODATE
          GOTO      EXTR3200 IF LESS
.
          UNPACK    IBIMDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P16:24,*V2LON,IBIMPORT,*P36:24,CPCDATE,*P66:24,IBIMTIME;
.
          CALL      PRNT0000
          GOTO      EXTR3000
.
EXTR3100  PACK      KEY18,IBIMPORT,FROMDATE,SP30
          CALL      RSIBIMN1
          GOTO      EXTR3000
.
EXTR3200  PACK      KEY18,IBIMPORT,ZEDS,ZEDS,ZEDS
          CALL      RSIBIMN1
          GOTO      EXTR3000
.
EXTR4000
          CALL      RKIBIMN4
          BRANCH    OVRCD,EXTR8000               * No More Records on File
.
          MATCH     IBIMPROG,TOPROG
          GOTO      EXTR8000 IF LESS
.
          MATCH     FROMDATE,IBIMDATE
          GOTO      EXTR4100 IF LESS
.
          MATCH     IBIMDATE,TODATE
          GOTO      EXTR4200 IF LESS
.
          UNPACK    IBIMDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P16:24,*V2LON,IBIMPROG,*P36:24,IBIMTIME,*P66:24,CPCDATE;
.
          CALL      PRNT0000
          GOTO      EXTR4000
.
EXTR4100  PACK      KEY26,IBIMPROG,FROMDATE,SP30
          CALL      RSIBIMN1
          GOTO      EXTR4000
.
EXTR4200  PACK      KEY26,IBIMPROG,ZEDS,ZEDS,ZEDS
          CALL      RSIBIMN1
          GOTO      EXTR4000
.
.         End of data. Check if anything was printed.
.
EXTR8000  COMPARE   ZERO,RECCOUNT
          GOTO      EXTR9000 IF EQUAL
.
          DISPLAY   *P1:24,"End of Processing";
          GOTO      EXTR9900
.
.         No data found
.
EXTR9000  DISPLAY   *P1:24,*EL,*B,"No Data on File for the Range - ";
          CALL      HITENTER
.
.         Print the totals, and the end of report message
.
EXTR9900  CALL      END0000                      * Print End of Report
.
EXTR9999  RETURN
.
.********************************************************************** 
.*                            PRNT0000                                *
.*                       Routine Description                          *
.********************************************************************** 
.
END0000
          PRINT     *N,ASK,UNDLN77,ASK:
                    *N:
                    *N,"Number of Records ",RECCOUNT:
                    *N:
                    *N:
                    *N,*1,"****  End Of Report  ****":
                    *N;
.
          RETURN
.
.
.********************************************************************** 
.*                            PRNT0000                                *
.*                       Routine Description                          *
.********************************************************************** 
.
PRNT0000
          ADD        ONE,RECCOUNT
.
          MOVE       SP30,SECUSER
          MOVE       IBIMUSER,KEY4
          CALL       RDPASS1                * read security file
.
          UNPACK     IBIMDATE,CCENT,CYEAR,CMON,CDAY
          CALL       PACDATE
.
          MOVE       IBIMTIME,DISPTIME
.
          BRANCH    OPTION,PRNT1000,PRNT2000,PRNT3000,PRNT4000
PRNT1000
          PRINT     *N,*1,PIPE,SP1,SECODE,SP2,SECUSER:
                      *40,PIPE,SP1,CPCDATE:
                      *53,PIPE,SP1,DISPTIME:
                      *61,PIPE,SP2,IBIMPORT:
                      *68,PIPE,SP1,IBIMPROG:
                      *79,PIPE;
.
          GOTO      PRNT9000
PRNT2000
          PRINT     *N,*1,PIPE,SP1,CPCDATE:
                      *14,PIPE,SP1,DISPTIME:
                      *22,PIPE,SP2,IBIMPORT:
                      *29,PIPE,SP1,SECODE,SP2,SECUSER:
                      *68,PIPE,SP1,IBIMPROG:
                      *79,PIPE;
.
          GOTO      PRNT9000
PRNT3000
          PRINT     *N,*1,PIPE,SP2,IBIMPORT:
                      *8,PIPE,SP1,CPCDATE:
                      *21,PIPE,SP1,DISPTIME:
                      *29,PIPE,SP1,SECODE,SP2,SECUSER: 
                      *68,PIPE,SP1,IBIMPROG:
                      *79,PIPE;
.
          GOTO      PRNT9000
PRNT4000
          PRINT     *N,*1,PIPE,SP1,IBIMPROG:
                      *12,PIPE,SP1,CPCDATE:
                      *25,PIPE,SP1,DISPTIME:
                      *33,PIPE,SP1,SECODE,SP2,SECUSER: 
                      *72,PIPE,SP2,IBIMPORT:
                      *79,PIPE;
.
PRNT9000
          ADD       ONE,CLNO
.
          COMPARE   FIFTY5,CLNO
          GOTO      PRNT9999 IF LESS
.
          PRINT     *N,*1,ASK,UNDLN77,ASK;
.
          PRINT     *N,*1,"";
          CALL      HEAD0000
PRNT9999
          RETURN
.
.********************************************************************** 
.*                            HEAD0000                                *
.*                        Report Page Header                          *
.********************************************************************** 
.
HEAD0000
          MOVE      ONE,CNOUNDLN
          CLOCK     TIME,CTIMEIS
          CALL      HEAD80

          BRANCH    OPTION,HEAD1000,HEAD2000,HEAD3000,HEAD4000
HEAD1000
          PRINT     *20,"User ",FROMCODE," to ",TOCODE:
                    *N,*20,"Date ",FROMDDTE," to ",TODDTE:
                    *N:
                    *N,*1,ASK,UNDLN77,ASK:
                    *N,*1,PIPE,SP1,"User":
                      *40,PIPE,SP4,"Date":
                      *53,PIPE,SP2,"Time":
                      *61,PIPE,SP1,"Port":
                      *68,PIPE,SP1,"Program":
                      *79,PIPE:
                    *N,*1,ASK,UNDLN77,ASK;
.
          GOTO      HEAD9000
HEAD2000
          PRINT     *N,*20,"Date ",FROMDDTE," to ",TODDTE:
                    *N:
                    *N,*1,ASK,UNDLN77,ASK:
                    *N,*1,PIPE,SP4,"Date":
                      *14,PIPE,SP2,"Time":
                      *22,PIPE,SP1,"Port":
                      *29,PIPE,SP1,"User":
                      *68,PIPE,SP1,"Program":
                      *79,PIPE:
                    *N,*1,ASK,UNDLN77,ASK;
.
          GOTO      HEAD9000
HEAD3000
          PRINT     *20,"Port ",FROMCODE," to ",TOCODE:
                    *N,*20,"Date ",FROMDDTE," to ",TODDTE:
                    *N:
                    *N,*1,ASK,UNDLN77,ASK:
                    *N,*1,PIPE,SP1,"Port":
                      *8,PIPE,SP4,"Date":
                      *21,PIPE,SP1,"Time":
                      *29,PIPE,SP1,"User":
                      *68,PIPE,SP1,"Program":
                      *79,PIPE:
                    *N,*1,ASK,UNDLN77,ASK;
.
          GOTO      HEAD9000
HEAD4000
          PRINT     *20,"Program ",FROMCODE," to ",TOCODE:
                    *N,*20,"Date    ",FROMDDTE," to ",TODDTE:
                    *N:
                    *N,*1,ASK,UNDLN77,ASK:
                    *N,*1,PIPE,SP1,"Program":
                      *12,PIPE,SP4,"Date":
                      *25,PIPE,SP1,"Time":
                      *33,PIPE,SP1,"User":
                      *72,PIPE,SP1,"Port":
                      *79,PIPE:
                    *N,*1,ASK,UNDLN77,ASK;
.
HEAD9000  ADD       SIX,CLNO
          RETURN
+
.**********************************************************************
.*              I/O INCLUDES FOR FILES AND GENERAL ROUTINES           *
.**********************************************************************
          INC       STD001IO
.
          INC       IBAPASDS
.
          INC       IBAIMNIO/INC
          INC       IBAPORIO/INC
          INC       IBAPASIO/INC
.
.
.

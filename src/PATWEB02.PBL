.----------------------------------------------------------------------
. Program       : PATWEB02
.
. Function      : Patient List by Web User ID
.
. Modifications : 
.----------------------------------------------------------------------
.         V10.11.02 10/10/2017  Thanh T         TSK 0846088
.                   Fixed blank Review date update error in patweb0202003
.         V10.11.01 18/08/2017  Ania P          TSK 0843179
.                   Recompiled for IBAWEBCD
.----------------------------------------------------------------------
.         V10.04.01 31.03.2014  B.G.Ackland  CAR 299363
.                   Replace META Tag Redirect with Javascript in Common RDIREC00
.                   Routine
.----------------------------------------------------------------------
.         V9.12.01  08/10/2009  WeeLee Koo    CAR 206553
.                   Changed from AWARD to WARDBED (LSTPAT20)
.----------------------------------------------------------------------
.         V9.05.01  09/03/2006  Ebon Clements CAR 78533
.                   Mods for PATCODTM - Hospital specific category codes
.*        V9.05.B01 30/01/2006  Ebon Clements CAR 93186
.*                  Mods for REDIRECT length change. Recompiled for IBAWEBDF
.----------------------------------------------------------------------
.*        V9.03.03  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2      *
.*                  - Add Sex Description routine SEXDSC00 with Code "R" -    *
.*                  "Intersex" added                                          *
.                 V9.03.02  12.05.2004 Sylvek Litewka  CAR 49209
.                           Replaced PTMXSIN5 with new variable PRIVFLAG as
.                           PTMXSIN5 is now used as 'Legacy Visits' indicator. 
.                           Prior to this change PTMXSIN5 was a spare variable
.                           and it was always populated with SPACES. Therefore
.                           new field PRIVFLAG is set to SPACES before use.
.                 V9.03.01  18.03.2004 Peter Vela   CAR 13038
.                           Changed call DOCNAM00 to DOCNM000 (parameter driven 
.                           doctor name format) for sort lists.
.                 V9.02.02  03.10.2001 Jill Habasque
.                           Commented out replace of apostrophies with space.
.                 V9.02.01 30/08/2001 Jill Habasque
.                          Fixed storing of UR
.                 V5.10.B01 12/02/2001 Jill Habasque
.                           Created Program
.
.----------------------------------------------------------------------
          INC       IBAWEBDF    
          INC       PATCALAG/INC  
          INC       PATCONFD/INC
          INC       PATDO1FD/INC
          INC       PATDGWFD/INC
          INC       PATDSCFD/INC
          INC       PATICUFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATVISFD/INC
          INC       PATWR1FD/INC
          INC       PMSVX1FD/INC
          INC       WEBDATDF
          INC       WEBPATFD/INC
          INC       WEBPLDFD/INC
.
WARDBED   DIM       8                       * Ward/Bed
WEBPT001  DIM       10                      * Web User ID
WEBPT002  DIM       3                       * List Number
WEBPT003  DIM       8                       * Patient UR Number  
WEBPT004  DIM       8                       * Patient Visit Number
WEBPT005  DIM       8                       * Date Created
WEBPT006  DIM       5                       * Time Created
WEBPT007  DIM       8                       * Review Date
WEBPT008  DIM       3                       * Status (Cat wv)
WEBPT009  DIM       60                      * Comment 1
WEBPT010  DIM       60                      * Comment 2
.
WEBPD001  DIM       10                      * Web User ID
WEBPD002  DIM       3                       * List Number
WEBPD003  DIM       30                      * Description
WEBPD004  DIM       3                       * Business Server Page
.
ADDPATHD  INIT      "Add Patient "
ADDLSTHD  INIT      "Add List "
CATWV     INIT      "wv"
DEFTWARD  DIM       3
DOCTCODE  DIM       6
DISPDATE  DIM       13
HYPHEN    INIT      " - "
.
LSTDETHD  INIT      "List Details "
NEXTPAGE  DIM       1                       * Next page parameter 
.
PATDETHD  INIT      "Patient Details "
PRIVFLAG  DIM       1                  * Privacy Indicator - replaced PTMXSIN5
PSAGETYP  DIM       6                        * age type description
PSAGECON  DIM       3                        * age converted value
.
STATDESC  DIM       20                      * Status Description
.
UPDTTYPE  DIM       1                       * Update Type
FORM8     FORM      8
ADMNDATE  DIM       11                      * Formatted Admission Date
CONDESC   DIM       25
COLDAT04  DIM       30
DSCHDATE  DIM       11                      * Formatted Discharge Date
PATLINK   DIM       127                     * Link to the next patient info
LPCONDD   DIM       10
.
.-----------------------------------------------------------------------
PRGID     INIT      "PATWEB02"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "Patient Lists by Web User"
.-----------------------------------------------------------------------
.
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "Invalid Option",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  CALL      WEBPLD00                * Web User Id Lists       
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1200  CALL      WEBPAT00                * Display Patients on List
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
.------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------
INIT0000  OPEN      PATCODE1,"patcodes" 
          OPEN      PATCODE2,"patcodes" 
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDGWA1,"patdgwaf"
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATVISA2,"patvisaf" 
          OPEN      PATWR1A1,"patwr1af" 
          OPEN      PATWR1A3,"patwr1af" 
          OPEN      PATWR1A4,"patwr1af" 
          OPEN      PMSVX1A1,"pmsvx1af" 
          OPEN      WEBPLDA1,"webpldaf"
          OPEN      WEBPATA1,"webpataf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          IF        IBCNMHOS=1
            OPEN      MLTCODA1,"mltcodaf"
            OPEN      MLTCODA2,"mltcodaf"
          ENDIF
.
          RETURN
.------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          MOVE      SP70,TEMPLATE
          MOVE      SP70,UPDTTYPE
          MOVE      ZERO,STARTNUM
          MOVE      ZERO,NEXTPAGE
          PACK      REDIRECT,SP70,SP70,SP70,SP70
.
          MOVE      Z70,WEBPD001
          MOVE      Z70,WEBPD002
          MOVE      Z70,WEBPD003
          MOVE      Z70,WEBPD004
          MOVE      Z70,WEBPT001
          MOVE      Z70,WEBPT002
          MOVE      Z70,WEBPT003
          MOVE      Z70,WEBPT004
          MOVE      Z70,WEBPT005
          MOVE      Z70,WEBPT006
          MOVE      Z70,WEBPT007
          MOVE      Z70,WEBPT008
          MOVE      Z70,WEBPT009
          MOVE      Z70,WEBPT010
.
          RETURN
.------------------------------------------------------------
. Set Parameters
.------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updttype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDTTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nextpage",QRYNAME         
          IF        @EQUAL
            MOVE      QRYDATA,NEXTPAGE  
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "webpd",QRYNAME            * list details
          IF        @EQUAL
            CALL      SPWBD000          
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "webpt",QRYNAME            * patient details
          IF        @EQUAL
            CALL      SPWEB000          
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
+
.------------------------------------------------------------
. Process Fields 
.------------------------------------------------------------
PROFLD00  BRANCH    REPORTNO,PROFLD10,PROFLD20
          GOTO      PROFLD99
.
PROFLD10  BRANCH    FLDSETNO,PROFLD11,PROFLD11
          GOTO      PROFLD99
.
PROFLD11  CALL      LDET0000                * List Details
          GOTO      PROFLD99
.
PROFLD20  BRANCH    FLDSETNO,PROFLD21,PROFLD22
          GOTO      PROFLD99
.
PROFLD21  CALL      EXPD0000                * Patient Details
          GOTO      PROFLD99
.
PROFLD22  CALL      LDET0000                * List Details
          GOTO      PROFLD99
.
PROFLD99  RETURN

.------------------------------------------------------------
. Web User ID Lists
.------------------------------------------------------------
WEBPLD00  CALL      CLWEBPLD                * Clear User ID List variables
          MATCH     SP70,UPDTTYPE           * Display
          GOTO      WEBPLD50 IF EQUAL
.
          MATCH     "A",UPDTTYPE            * Add
          GOTO      WEBPLD10 IF EQUAL
.
          MATCH     "U",UPDTTYPE            * Update
          GOTO      WEBPLD20 IF EQUAL
.
          MATCH     "D",UPDTTYPE            * Delete
          GOTO      WEBPLD30 IF EQUAL
.
          MOVE      "Invalid Update Type",WEBTITLE
          CALL      WEBERR00
          GOTO      WEBPLD99
.
WEBPLD10  CALL      ADDPLD00                * add new list for user ID
          GOTO      WEBPLD99
.
WEBPLD20  CALL      UPDPLD00                * update list details
          GOTO      WEBPLD99
.
WEBPLD30  CALL      DELPLD00                * delete list
          GOTO      WEBPLD99
.
WEBPLD50  PACK      KEY13,USERID,WEBPD002
          CALL      RDWBPLD1
          IF        OVRCD=1
            CALL      CLWEBPLD              * Clear List Details
          ENDIF
.  
          PACK      WEBTITLE,ADDLSTHD
          MATCH     SP70,WBPDDESC
          IF        !@EQUAL
            PACK      WEBTITLE,LSTDETHD,HYPHEN,WBPDDESC
          ENDIF
.
          CALL      WEBHED00
          BRANCH    EXIT,WEBPLD99
          CALL      WEBBOD00
          CALL      WEBEND00
          MOVE      ONE,EXIT
.
WEBPLD99  RETURN 
.------------------------------------------------------------
. Patients on List
.------------------------------------------------------------
WEBPAT00  MATCH     SP70,UPDTTYPE           * Display
          GOTO      WEBPAT50 IF EQUAL
.
          MATCH     "A",UPDTTYPE            * Add
          GOTO      WEBPAT10 IF EQUAL
.
          MATCH     "U",UPDTTYPE            * Update
          GOTO      WEBPAT20 IF EQUAL
.
          MATCH     "D",UPDTTYPE            * Delete
          GOTO      WEBPAT30 IF EQUAL
.
          MOVE      "Invalid Update Type",WEBTITLE
          CALL      WEBERR00
          GOTO      WEBPAT99
.
WEBPAT10  CALL      ADDPAT00                 * Add new patient
          GOTO      WEBPAT99
.
WEBPAT20  CALL      UPDPAT00                 * Update patients details
          GOTO      WEBPAT99
.
WEBPAT30  CALL      DELPAT00                 * Delete patient from list
          GOTO      WEBPAT99
.
WEBPAT50  MOVE      SP70,PATFNAME
          PACK      KEY21,USERID,WEBPD002,WEBPT003
          CALL      RDWBPAT1
          IF        OVRCD=1
            CALL      CLWEBPAT              * Clear List Details
          ELSE
            PACK      KEY8,WBPTURNO
            CALL      RDMAST1
            BRANCH    OVRCD,WEBPAT96
            CALL      PATGNAME
            PACK      WEBTITLE,PATDETHD,HYPHEN,PATFNAME 
          ENDIF
.
          PACK      KEY13,USERID,WEBPD002,SP70
          CALL      RDWBPLD1
          IF        OVRCD=1
            CALL      CLWEBPLD
          ELSE
            MATCH     SP70,PATFNAME
            IF        @EQUAL
              PACK      WEBTITLE,ADDPATHD,HYPHEN,WBPDDESC 
            ENDIF
          ENDIF
.
          CALL      WEBHED00
          BRANCH    EXIT,WEBPAT99
          CALL      WEBBOD00
          CALL      WEBEND00
          MOVE      ONE,EXIT
          GOTO      WEBPAT99
.
WEBPAT95  MOVE      "Invalid List Number",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      WEBPAT99
.
WEBPAT96  MOVE      "Invalid UR Number",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      WEBPAT99
.
WEBPAT99  RETURN
.------------------------------------------------------------
. Clear List Variables
.------------------------------------------------------------
CLWEBPLD  MOVE      SP70,WBPDWUID 
          MOVE      SP70,WBPDLNUM 
          MOVE      SP70,WBPDDESC 
          MOVE      SP70,WBPDBSPG 
          MOVE      SP70,WBPDSPAR 
          RETURN
.
.------------------------------------------------------------
. Clear Patient on List Variables
.------------------------------------------------------------
CLWEBPAT  MOVE      SP70,WBPTWUID
          MOVE      SP70,WBPTLNUM
          MOVE      SP70,WBPTURNO
          MOVE      SP70,WBPTVNUM
          MOVE      SP70,WBPTDATC
          MOVE      SP70,WBPTTIMC
          MOVE      SP70,WBPTRDAT
          MOVE      SP70,WBPTSTAT
          MOVE      SP70,WBPTCOM1
          MOVE      SP70,WBPTCOM2
          MOVE      SP70,WBPTSPAR
          RETURN 
.------------------------------------------------------------
. List Details
.------------------------------------------------------------
LDET0000  BRANCH    FLDITMNO,LDET0010,LDET0020,LDET0030,LDET0040
.
          WRITE     HTMLFILE;"Invalid Tag";
          GOTO      LDET9999
.
LDET0010  WRITE     HTMLFILE;WBPDWUID;
          GOTO      LDET9999
.
LDET0020  WRITE     HTMLFILE;WBPDLNUM;
          GOTO      LDET9999
.
LDET0030  WRITE     HTMLFILE;WBPDDESC;
          GOTO      LDET9999 
.
LDET0040  WRITE     HTMLFILE;WBPDBSPG;
          GOTO      LDET9999
.
LDET9999  RETURN
.------------------------------------------------------------
. Select Patient List  
.------------------------------------------------------------
SELLST00  PACK      KEY13,USERID,SP70
          CALL      RSWBPLD1       
SELLST10  CALL      RKWBPLD1       
          BRANCH    OVRCD,SELLST99
.
          MATCH     USERID,WBPDWUID
          GOTO      SELLST99 IF NOT EQUAL
.
          MATCH     WEBPD002,WBPDLNUM
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=#"",WBPDLNUM,"#" selected>":
                             WBPDDESC,"</option>";
          ELSE
            WRITE     HTMLFILE;"<option value=#"",WBPDLNUM,"#">":
                             WBPDDESC,"</option>";
          ENDIF
.
          GOTO      SELLST10
.
SELLST99  RETURN 
+
.------------------------------------------------------------
. Patient Details
.------------------------------------------------------------
EXPD0000  BRANCH    FLDITMNO,EXPD0010,EXPD0020,EXPD0030,EXPD0040:
                             EXPD0050,EXPD0060,EXPD0070,EXPD0080:
                             EXPD0090,EXPD0100,EXPD0110,EXPD0120:
                             EXPD0130
.
          WRITE     HTMLFILE;"Invalid Tag";
          GOTO      EXPD9999
.
EXPD0010  WRITE     HTMLFILE;WBPTWUID;
          GOTO      EXPD9999
.
EXPD0020  WRITE     HTMLFILE;WBPTLNUM;
          GOTO      EXPD9999
.
EXPD0030  WRITE     HTMLFILE;WBPTURNO;
          GOTO      EXPD9999
.
EXPD0040  WRITE     HTMLFILE;WBPTVNUM;
          GOTO      EXPD9999
.
EXPD0050  MATCH     SP70,WBPTDATC
          GOTO      EXPD9999 IF EQUAL
          UNPACK    WBPTDATC,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT:
                    NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      EXPD9999
.
EXPD0060  WRITE     HTMLFILE;WBPTTIMC;
          GOTO      EXPD9999
.
EXPD0070  MATCH     SP70,WBPTRDAT
          GOTO      EXPD9999 IF EQUAL
          UNPACK    WBPTRDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT:
                    NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      EXPD9999
.
EXPD0080  MOVE      "wv",CATEGORY
          MOVE      WBPTSTAT,CODE
          CALL      CODITM00
          GOTO      EXPD9999
.
EXPD0090  WRITE     HTMLFILE;WBPTCOM1;
          GOTO      EXPD9999
.
EXPD0100  WRITE     HTMLFILE;WBPTCOM2;
          GOTO      EXPD9999
.
EXPD0110  CALL      SELLST00                * Select list     
          GOTO      EXPD9999
.
EXPD0120  CALL      LSTPAT00                * List of Patients
          GOTO      EXPD9999
.
EXPD0130  PACK      KEY8,WEBPT003
          CALL      RDMAST1
          IF        OVRCD=0
            MOVE      TWO,PACFRMT
            MOVE      PSNAME,PACSNAME
            MOVE      PGNAME,PACGNAME
            MOVE      PTITL,PACTITLE
            CALL      PACNAME
            WRITE     HTMLFILE;PACFNAME;
          ENDIF
          GOTO      EXPD9999
.
EXPD9999  RETURN
+
.------------------------------------------------------------
. Display the list of patients 
.------------------------------------------------------------
LSTPAT00  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          MOVE      WEBPD002,WEBPT002
          PACK      KEY21,USERID,WEBPD002,SP70
          CALL      RSWBPAT1      
LSTPAT10  CALL      RKWBPAT1      
          BRANCH    OVRCD,LSTPAT99
.
          MATCH     USERID,WBPTWUID               * same user id
          GOTO      LSTPAT99 IF NOT EQUAL
.
          MATCH     WEBPD002,WBPTLNUM             * same list
          GOTO      LSTPAT99 IF NOT EQUAL
.
          PACK      KEY8,WBPTURNO
          CALL      RDMAST1
          BRANCH    OVRCD,LSTPAT99
.
.* ****************************************** Start of Changes         *C-49016
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          MOVE      DSEXDESC,DISPSEX
.* ******************************************   End of Changes         *C-49016
.
          UNPACK    PBDATE,CDAY,CMON,CYEAR,CCENT
          CALL      IBACLOCK
          PACK      AGEDATE,CCC,CYY,CMM,CDD
          REP       " 0",AGEDATE
          CALL      CALCAGE
          CALL      PATNAM00                      * format patient name
.
          PACK      KEY24,WBPTURNO,SP70
          CALL      RSPTVIS2
LSTPAT20  CALL      RKPTVIS2
          BRANCH    OVRCD,LSTPAT30
.
          MATCH     WBPTURNO,PVIURNO
          GOTO      LSTPAT30 IF NOT EQUAL
.
          MOVE      SP70,WARDBED
          PACK      KEY8,DPVIBILL
          CALL      RDMISS1
          IF        OVRCD=0 & ASTAT=2
            MOVE      DPVIBILL,WBPTVNUM
            PACK      WARDBED,AWARD,SLASH,ABED
            GOTO      LSTPAT40
          ELSE
            GOTO      LSTPAT20                      * read next visit
          ENDIF
.
LSTPAT30  CALL      CLPATMIS
.
          MOVE      SP70,WARDBED
          MOVE      SP70,DOCFNAME
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
          IF        OVRCD=0
.           CALL      DOCNAM00
            MOVE      DTITL,FMTTITLE            * I CAR 13038
            MOVE      DGNAME,FMTGNAME
            MOVE      DSNAME,FMTSNAME
            CALL      DOCNM000                  * end I CAR 13038
          ENDIF
.
LSTPAT40  MOVE      SP70,STATDESC
          MATCH     SP8,WBPTSTAT                  * get status description
          IF        !@EQUAL
            PACK      KEY5,CATWV,WBPTSTAT
            CALL      RDCODE1
            IF        OVRCD<>1
              MOVE      TDESC,STATDESC
            ENDIF
          ENDIF
.
          MOVE      SP70,PRIVFLAG
.
          CLEAR     PATLINK
          APPEND    LINKPRG,PATLINK
          APPEND    ".pbl?reportno=",PATLINK
          APPEND    LINKREP,PATLINK
          APPEND    "&template=",PATLINK
          APPEND    LINKTEM,PATLINK
          APPEND    "&urnumber=",PATLINK
          APPEND    WBPTURNO,PATLINK
          APPEND    "&admissno=",PATLINK
          APPEND    WBPTVNUM,PATLINK
          RESET     PATLINK
.
          CLEAR     HTMLLINK
          APPEND    "javascript:UpdateItem('",HTMLLINK
          APPEND    WBPTLNUM,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    WBPTURNO,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
          WRITE     HTMLFILE;"t.addRow(#"",WBPTURNO,"#",":
                             "#"",WBPTVNUM,"#",":
                             "#"",PCEASE,PCASE:
                                  PTMXSIN1,PTMXSIN2,PTMXSIN3,PTMXSIN4:
                                  PRIVFLAG,"#",":
                             "#"",PATFNAME,"#",":
                             "#"",WARDBED,"#",":        * Replaced AWARD to WARDBED
                             "#"",ABED,"#",":
                             "#"",STATDESC,"#",":
                             "#"",WBPTCOM1,"#",":
                             "#"",WBPTRDAT,"#",":
                             "#"",HTMLLINK,"#",":
                             "#"",DISPSEX,"#",":
                             "#"";
          CALL      WRTAGE00
          WRITE     HTMLFILE;"#",":
                             "#"",DOCFNAME,"#",":
                             "#"",CONDESC,SP1,LPCONDD,"#",":
                             "#"",WEXTN,"#");"
.
          REP       "+ ",ADMISSNO
          REP       "+ ",URNUMBER
.
          GOTO      LSTPAT10
.
LSTPAT99  RETURN
+
.------------------------------------------------------------
. Write Patient Age in Y,M,W or D
.------------------------------------------------------------
WRTAGE00  MATCH     "y",PSAGETYP
          GOTO      WRTAGE10 IF NOT EQUAL
          WRITE     HTMLFILE;PSAGEYRS," yrs";
          GOTO      WRTAGE99
.
WRTAGE10  MATCH     "d",PSAGETYP
          GOTO      WRTAGE20 IF NOT EQUAL
          WRITE     HTMLFILE;PSAGEDAY," days";
          GOTO      WRTAGE99
.
WRTAGE20  MATCH     "m",PSAGETYP
          GOTO      WRTAGE30 IF NOT EQUAL
          WRITE     HTMLFILE;PSAGEMNT," mths";
          GOTO      WRTAGE99
.
WRTAGE30  MATCH     "w",PSAGETYP
          GOTO      WRTAGE99 IF NOT EQUAL
          WRITE     HTMLFILE;PSAGEWKS," wks";
.
WRTAGE99  RETURN
.------------------------------------------------------------
. Format Patient Name <b>SURNAME</b>Title Given Names
.------------------------------------------------------------
PATNAM00  CLEAR     DISPNAME
...       REP       "' ",PSNAME
          REP       "#" ",PSNAME
...       REP       "' ",PGNAME
          REP       "#" ",PGNAME
          MOVE      SP70,PATFNAME
          REP       UPPLOW,PSNAME
          APPEND    "<b>",DISPNAME
          APPEND    PSNAME,DISPNAME
          APPEND    "</b>",DISPNAME
          PACK      NAME35,PTITL,SP70
          CALL      NAMSTR00
          PACK      NAME35,PGNAME,SP70
          CALL      NAMSTR00
          APPEND    SP70,DISPNAME
          RESET     DISPNAME
          MOVE      DISPNAME,PATFNAME
          STRIP     PATFNAME
          RETURN
.------------------------------------------------------------
. Format Patient Name <b>SURNAME</b>Title Given Names
.------------------------------------------------------------
PATGNAME  CLEAR     DISPNAME
...       REP       "' ",PSNAME
          REP       "#" ",PSNAME
...       REP       "' ",PGNAME
          REP       "#" ",PGNAME
          PACK      NAME35,PTITL,SP70
          CALL      NAMSTR00
          PACK      NAME35,PGNAME,SP70
          CALL      NAMSTR00
          MOVE      SP70,PATFNAME
          REP       UPPLOW,PSNAME
          APPEND    NAME35,DISPNAME
          APPEND    PSNAME,DISPNAME
          APPEND    SP70,DISPNAME
          RESET     DISPNAME
          MOVE      DISPNAME,PATFNAME
          STRIP     PATFNAME
          RETURN
.------------------------------------------------------------
. Add Patient to List
.------------------------------------------------------------
ADDPAT00  CALL      CLWEBPAT                       * clear variables
.
          MOVE      USERID,WEBPT001                * check if already on list
          PACK      WEBPT002,WEBPT002,SP70
          PACK      KEY21,WEBPT001,WEBPT002,WEBPT003
          CALL      RAWBPAT1
          COMPARE   ZERO,OVRCD
          GOTO      ADDPAT90 IF EQUAL
.
          CALL      MVPAT000
          CLOCK     TIME,CTIMEIS                    * date & time created
          MOVE      CTIMEIS,WBPTTIMC
          PACK      WBPTDATC,CCC,CYY,CMM,CDD
          REP       " 0",WBPTDATC
.
. ----  if this patient is a current admission, pack the visit number ----
.
          PACK      KEY24,WBPTURNO,SP70
          CALL      RSPTVIS2
ADDPAT20  CALL      RKPTVIS2
          BRANCH    OVRCD,ADDPAT30
.
          MATCH     WBPTURNO,PVIURNO
          GOTO      ADDPAT30 IF NOT EQUAL
.
          PACK      KEY8,DPVIBILL
          CALL      RDMISS1
          IF        OVRCD=0 & ASTAT=2
            MOVE      DPVIBILL,WBPTVNUM
            GOTO      ADDPAT40
          ELSE
            GOTO      ADDPAT20                      * read next visit
          ENDIF
.
ADDPAT30  CALL      CLPATMIS
.
ADDPAT40  PACK      KEY21,WBPTWUID,WBPTLNUM,WBPTURNO
          CALL      WRWBPAT1
          MOVE      ZERO,EXIT
          GOTO      ADDPAT99
.
ADDPAT90  MOVE      "Patient Already on list",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
ADDPAT99  RETURN
+
.------------------------------------------------------------
. Update Patient on List
.------------------------------------------------------------
UPDPAT00  CALL      CLWEBPAT
          PACK      WEBPT002,WEBPT002,SP70
          PACK      KEY21,USERID,WEBPT002,WEBPT003
          CALL      RDWBPAT1
          IF        OVRCD=0
            CALL      MVPAT000
            CALL      UPWBPAT1
            MOVE      ZERO,EXIT
          ELSE
            MOVE      "Patient Not on List",WEBTITLE
            CALL      WEBERR00
            MOVE      ONE,EXIT
          ENDIF
.
UPDPAT99  RETURN
+
.------------------------------------------------------------
. Update List Details
.------------------------------------------------------------
UPDPLD00  CALL      CLWEBPLD
          PACK      KEY13,USERID,WEBPD002
          CALL      RDWBPLD1
          IF        OVRCD=0
            CALL      MVPLD000
            CALL      UPWBPLD1
            MOVE      ZERO,EXIT
          ELSE
            MOVE      "List not on file",WEBTITLE
            CALL      WEBERR00
            MOVE      ONE,EXIT
          ENDIF
.
UPDPLD99  RETURN
+
.------------------------------------------------------------
. Delete Patient from List
.------------------------------------------------------------
DELPAT00  PACK      WEBPT002,WEBPT002,SP70
          PACK      KEY21,USERID,WEBPT002,WEBPT003
          CALL      RDWBPAT1
          IF        OVRCD=0
            CALL      DEWBPAT1
            MOVE      ZERO,EXIT
          ELSE
            MOVE      "Patient Not on List",WEBTITLE
            CALL      WEBERR00
            MOVE      ONE,EXIT
          ENDIF
.
DELPAT99  RETURN
+
.------------------------------------------------------------
. Delete List
.------------------------------------------------------------
DELPLD00  PACK      KEY21,USERID,WEBPD002,SP70
          CALL      RSWBPAT1                      * delete all patients from
DELPLD10  CALL      RKWBPAT1                      * webpataf
          COMPARE   ZERO,OVRCD
          GOTO      DELPLD20 IF NOT EQUAL
.
          MATCH     USERID,WBPTWUID                * Same User ID
          GOTO      DELPLD99 IF NOT EQUAL
.
          MATCH     WEBPD002,WBPTLNUM              * correct list number
          GOTO      DELPLD20 IF NOT EQUAL
.
          PACK      KEY21,WBPTWUID,WBPTLNUM,WBPTURNO
          CALL      DEWBPAT1                       * delete patient
          GOTO      DELPLD10
.
DELPLD20  PACK      KEY13,USERID,WEBPD002
          CALL      RDWBPLD1
          IF        OVRCD=0
            CALL      DEWBPLD1                     * delete list from webpldaf
            MOVE      ZERO,EXIT
          ELSE
            MOVE      "List not on file",WEBTITLE
            CALL      WEBERR00
            MOVE      ONE,EXIT
          ENDIF
          MOVE      SP8,WEBPD002
.
DELPLD99  RETURN
+
.------------------------------------------------------------
. Add New List
.------------------------------------------------------------
ADDPLD00  CALL      CLWEBPLD
          MOVE      USERID,WEBPD001
          PACK      KEY13,WEBPD001,WEBPD002
          CALL      RAWBPLD1
          COMPARE   ZERO,OVRCD
          GOTO      ADDPLD90 IF EQUAL 
.
          CALL      MVPLD000
          CALL      WRWBPLD1
          MOVE      ZERO,EXIT
          GOTO      ADDPLD99
.
ADDPLD90  MOVE      "Patient List Already on file",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
ADDPLD99  RETURN
+
.------------------------------------------------------------
. Output Next Page Based on CGI Parameter nextpage
.------------------------------------------------------------
NXTPAG00  MOVE      ZERO,F1
          MOVE      NEXTPAGE,F1
          BRANCH    F1,NXTPAG10,NXTPAG20,NXTPAG30
.
          CALL      WINCLS00
          GOTO      NXTPAG99
.
NXTPAG10  CALL      RDIREC00
          GOTO      NXTPAG99
.
NXTPAG20  CALL      GOBACK00      * Go Back 1 Html page
          GOTO      NXTPAG99
.
NXTPAG30  CALL      DAH20000      * Go Back 2 html pages
          GOTO      NXTPAG99
.
NXTPAG99  RETURN
.-------------------------------------------------------------------------------
.  Goes back 1 page
.-------------------------------------------------------------------------------
GOBACK00  CALL      OPENHTML
          BRANCH    EXIT,GOBACK99
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "    alert(#"",WEBTITLE,"#");":
                             "    self.close();":
                             "    opener.history.go(-1);":
                             "}":
                             "</script>"
          CALL      CLOSHTML
GOBACK99  RETURN
.---------------------------------------------------------------------
. Move in Patient List Details
.------------------------------------------------------------
MVPAT000  MATCH     Z70,WEBPT001
          IF        !@EQUAL
            MOVE       WEBPT001,WBPTWUID
          ENDIF
.
          MATCH     Z70,WEBPT002
          IF        !@EQUAL
            MOVE       WEBPT002,WBPTLNUM
          ENDIF
.
          MATCH     Z70,WEBPT003
          IF        !@EQUAL
            MOVE      WEBPT003,WBPTURNO
          ENDIF
.
          MATCH     Z70,WEBPT004
          IF        !@EQUAL
            MOVE      WEBPT004,WBPTVNUM
          ENDIF
.
          MATCH     Z70,WEBPT005
          IF        !@EQUAL
            MOVE      WEBPT005,WBPTDATC
          ENDIF
.
          MATCH     Z70,WEBPT006
          IF        !@EQUAL
            MOVE      WEBPT006,WBPTTIMC
          ENDIF
.
          MATCH     Z70,WEBPT007
          IF        !@EQUAL
            MOVE      WEBPT007,WBPTRDAT
          ENDIF
.
          MATCH     Z70,WEBPT008
          IF        !@EQUAL
            MOVE      WEBPT008,WBPTSTAT
          ENDIF
.
          MATCH     Z70,WEBPT009
          IF        !@EQUAL
            MOVE      WEBPT009,WBPTCOM1
          ENDIF
.
          MATCH     Z70,WEBPT010
          IF        !@EQUAL
            MOVE      WEBPT010,WBPTCOM2
          ENDIF
.
MVPAT999  RETURN
+
.---------------------------------------------------------------------
. Move in Web User ID List Details
.------------------------------------------------------------
MVPLD000  MATCH     Z70,WEBPD001
          IF        !@EQUAL
            MOVE       WEBPD001,WBPDWUID
          ENDIF
.
          MATCH     Z70,WEBPD002
          IF        !@EQUAL
            MOVE       WEBPD002,WBPDLNUM
          ENDIF
.
          MATCH     Z70,WEBPD003
          IF        !@EQUAL
            MOVE      WEBPD003,WBPDDESC
          ENDIF
.
          MATCH     Z70,WEBPD004
          IF        !@EQUAL
            MOVE      WEBPD004,WBPDBSPG
          ENDIF
.
MVPLD999  RETURN 
.------------------------------------------------------------
.   Set Parameters for Patient List Details
.------------------------------------------------------------
SPWEB000  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,SPWEB010,SPWEB020,SPWEB030,SPWEB040,SPWEB050:
                       SPWEB060,SPWEB070,SPWEB080,SPWEB090,SPWEB100
.
          MOVE      ONE,EXIT
          GOTO      SPWEB999

SPWEB010  MOVE      QRYDATA,WEBPT001
          GOTO      SPWEB999
.
SPWEB020  MOVE      QRYDATA,WEBPT002
          GOTO      SPWEB999
.
SPWEB030  MOVE      QRYDATA,WEBPT003
          GOTO      SPWEB999
.
SPWEB040  MOVE      QRYDATA,WEBPT004
          GOTO      SPWEB999
.
SPWEB050  UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00
          PACK      WEBPT005,CCENT,CYEAR,CMON,CDAY
          GOTO      SPWEB999
.
SPWEB060  MOVE      QRYDATA,WEBPT006
          GOTO      SPWEB999
.
SPWEB070  UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00
          PACK      WEBPT007,CCENT,CYEAR,CMON,CDAY
          GOTO      SPWEB999
.
SPWEB080  MOVE      QRYDATA,WEBPT008
          GOTO      SPWEB999
.
SPWEB090  MOVE      QRYDATA,WEBPT009
          GOTO      SPWEB999
.
SPWEB100  MOVE      QRYDATA,WEBPT010
          GOTO      SPWEB999
.
SPWEB999  RETURN 
.------------------------------------------------------------
.   Set Parameters for List Details
.------------------------------------------------------------
SPWBD000  MOVE      ZERO,EXIT 
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,SPWBD010,SPWBD020,SPWBD030,SPWBD040
.         
          MOVE      ONE,EXIT
          GOTO      SPWBD999

SPWBD010  MOVE      USERID,WEBPD001
          GOTO      SPWBD999
.
SPWBD020  MOVE      QRYDATA,WEBPD002
          GOTO      SPWBD999
.
SPWBD030  MOVE      QRYDATA,WEBPD003
          GOTO      SPWBD999
.
SPWBD040  MOVE      QRYDATA,WEBPD004
          GOTO      SPWBD999
.
SPWBD999  RETURN
.
.------------------------------------------------------------
. Patient Age
.------------------------------------------------------------
PATAGE00  CALL      IBACLOCK
          IF        PCEASE=1
            MOVE      PDECDTE,AGEDATE            * Date of Death
          ELSE
            PACK      AGEDATE,CCC,CYY,CMM,CDD    * Today's date
          ENDIF
          REP       " 0",AGEDATE
          CALL      CALCAGE
.
          MATCH     "y",PSAGETYP
          GOTO      PATAGE10 IF NOT EQUAL
          WRITE     HTMLFILE;PSAGEYRS," yrs";
          GOTO      PATAGE99
.
PATAGE10  MATCH     "d",PSAGETYP
          GOTO      PATAGE20 IF NOT EQUAL
          WRITE     HTMLFILE;PSAGEDAY," days";
          GOTO      PATAGE99
.
PATAGE20  MATCH     "m",PSAGETYP
          GOTO      PATAGE30 IF NOT EQUAL
          WRITE     HTMLFILE;PSAGEMNT," mths";
          GOTO      PATAGE99
.
PATAGE30  MATCH     "w",PSAGETYP
          GOTO      PATAGE99 IF NOT EQUAL
          WRITE     HTMLFILE;PSAGEWKS," wks";
PATAGE99  RETURN
.------------------------------------------------------------
.
          INC       IBAWEBCD
          INC       WEBDATCD
          INC       PATDO1IO/INC
          INC       PATDGWIO/INC
          INC       PATDSCIO/INC
          INC       PATICUIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATVISIO/INC
          INC       PATWR1IO/INC
          INC       PMSVX1IO/INC
          INC       WEBPLDIO/INC
          INC       WEBPATIO/INC
.
          INC       DAYOFWEK
          INC       CALCAGE
          INC       CLPATMIS
          INC       PATDOCTM
          INC       PATDOCCD            * I CAR 13038
          INC       NAMSTRCD
.

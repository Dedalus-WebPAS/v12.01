.****************************************************************************** .*    Program      : IBAHMS66                                                 *
.*    Description  : Incomplete coding by Consultant Episodes                 *
.*                                                                            *
.*    Author       : ROD   (15/02/93)                                         *
.*                                                                            *
.*    Modifications:                                                          *
.*                                                                            *
.******************************************************************************
.* V12.00.01 22/05/2025 Thanh T         TSK 0955096                           *
.*           Changed for alphanumeric visit number                            *
.******************************************************************************
.*        V10.04.01 13/06/2014 Steve Armstrong  CAR 301639                    *
.*                  Moved call to TFILENAM into INIT0000                      *
.******************************************************************************
.*        V10.03.01 24/12/2012 Patrick Adair  CAR 261630                      *
.*                  Removed port number from temp file name                   *
.******************************************************************************
.*        V10.02.01 28/03/2011  Mike Laming   CAR 240107                      *
.*                  Mods to PATECDaf & PATECOaf variables & Keys - file change*
.******************************************************************************
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.******************************************************************************
.*                   V9.03.00  28/10/2003   Steve Armstrong    CAR 44635      *
.*                             Ported from r5.06.                             *
.******************************************************************************
.*                   V5.03.01  28/09/1995 Lofty                              *
.*                             Set up ECOL & EVERT for PATHSPKY.             *
.*****************************************************************************
.*                   V5.01.02  01/03/93  Steve O'Gorman                      *
.*                             Fixed for new PATMRDFD File Layout            *
.*                   V5.01.03  03/03/93  Steve O'Gorman                      *
.*                             Allowed for Multi Hospital                    *
.*                   V5.01.04  24/03/1993  Steve Armstrong                   *
.*                             Mods for episode number in PATMROFD/PATMRDFD  *
.*                   V5.01.05  27/03/1993  Graeme Williams                   *
.*                             Recompiled for IBACONFD                       *
.*                   V5.01.06  02/07/1993  WHIRLPOOL   srf 123346            *
.*                             added EXTRa option by hospital/consultant     *
.*                             as per the Spec                               *
.*                   V5.01.07  28/09/1993  ROD    SRF 440390                 *
.*                             Fixed problem wards in wrong hospital printing*
.*                   V5.01.08  20/12/1993  ROD    SRF 440579                 *
.*                             Only check hosp if using Multi hospitals par  *
.*                   V5.01.09  05/01/1994  ROD    SRF 440593                 *
.*                             Check Multi Hosp using MOPTION=2              *
.*                   V5.01.10  25/04/1994  Paul Howells    SRF 440767        *
.*                             Report discharged patients only FCE's         *
.*                   V5.01.11  17/05/1994  Paul Howells    SRF 440887        *
.*                             Use PATCEDFD to determine if coding complete  *
.*                   V5.01.12  13/07/1994  Paul Howells    SRF 440927        *
.*                             Added Ep. No. to temp file key to fix I * D   *
.*****************************************************************************
.
          INC       STD001FD
          INC       IBACONFD/INC
.
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PATDSCFD/INC
          INC       PATECDFD/INC
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATTRNFD/INC
          INC       PATWR1FD/INC
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
. Temp file
.
TEMPFILE  IFILE     SQL, FIXED=31, KEY="U1-6,7-14,29-30"
XXADOCT   DIM       6       1
DXXADMNO  DIM       8       7
XXURNO    DIM       8      15
XXSPEC    DIM       3      23
XXWARD    DIM       3      26
XXEPNO    DIM       2      29
.         End of record    31
.
XXADMNO   DIM       8       * redefine for form field key
.
.
. LOCAL VARIABLES
. ---------------
ADMDATE   DIM       10
.
CMDLINE   DIM       80
CONS20    DIM       20
.
DESC10    DIM       10
DESC12    DIM       12
DIM18     DIM       18
DIM3      DIM       3
DIM30     DIM       30
DIM8ARRY  DIM       8[17]
DISDATE   DIM       10
DOC1      DIM       30
DOC2      DIM       30
DOCCODE   DIM       6
.
ENDCON    DIM       6
.
FDDATE    DIM       10
FNAME     DIM       8
FROMDATE  DIM       8
.
HOSCODE   DIM       4
.
PNAME25   DIM       25
.
SAVADOCT  DIM       6
SAVBED    DIM       3
SAVEPNO   FORM      2
SAVSPEC   DIM       3
SRCHMETH  FORM      1
STRTCON   DIM       6
SVERT     FORM      2
.
TDDATE    DIM       10
TODATE    DIM       8
.
. PROGRAM CONSTANTS
. -----------------
FINISH    INIT      "Finish"
FNHDR     INIT      "hms66a"
START     INIT      "Start "
.
.
PRGID     INIT      "IBAHMS66"
PRGNAM    INIT      "Incomplete Coding (Consultant Episodes)"
VERSION   INIT      "V12.01.00"
+
.****************************************************************************
.*  ML0000  :  Mainline code                                                *
.****************************************************************************
.
ML0000    CALL      INIT0000                      * initialisation
.
ML0100    CALL      MENU0000                      * get menu options
          BRANCH    EXIT,ML9999                   * Exit
.
.   MOPTION will only get to 2 if IBCNMHOS = 1
.
          BRANCH    MOPTION,ML1000,ML2000
.
ML1000    CALL      DSCR0000                      * Display the screen 
          CALL      KCON0000                    * Keyin consultant Range
          BRANCH    EXIT,ML9999
.
          CALL      KDAT0000                      * Keyin date range
          BRANCH    EXIT,ML1000                 * nothing input
.
          CALL      CONTQST                       * Ok to Continue?
          BRANCH    CEXIT,ML5000,ML1000,ML0100
.
.-------  This option is only available if we are using Multihospitals
.
ML2000    CALL      DSCR0000                      * Display the screen 
          CALL      HOSP0000                      * Keyin Hospital (if Req)
          BRANCH    EXIT,ML0100                   * nothing input
.
          CALL      KCON0000                    * Keyin consultant Range
          BRANCH    EXIT,ML9999
.
          CALL      KDAT0000                      * Keyin date range
          BRANCH    EXIT,ML2000,ML0100          * nothing input
.
          CALL      CONTQST                       * Ok to Continue?
          BRANCH    CEXIT,ML5000,ML2000,ML0100
.
ML5000    CALL      CRET0000                      * erase/create temp file
          CALL      EXTR0000                      * EXTRact data into temp file
          CALL      RPRT0000                      * Print report 
          GOTO      ML0100
.
ML9999    CALL      KILL0000                      * erase temp file
          CHAIN     PGM
+
.****************************************************************************
.*  INIT0000  :  Initialisation                                             *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD
.
.
          DISPLAY   *P55:24,"patecdaf"
          OPEN      PATECDA1,"patecdaf"
.
          DISPLAY   *P55:24,"pattranf"
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P55:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P55:24,"patdschf"
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P55:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P55:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P55:24,"patwr1af"
          OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY   *P55:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P55:24,"controlf"
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAME
.
INIT9999  RETURN
+
.****************************************************************************
.*  MENU0000  :  Main Menu                                                  *
.****************************************************************************
.
MENU0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Print Report by Consultant"
                 
          IF        IBCNMHOS = 1
            DISPLAY   *P1:6,*V2LON,"2",*HOFF," = Print Report by Hospital":
                      " and Consultant":
                      *P1:8,"Select Option : "
            MOVE      EIGHT,CVERT
          ELSE
            DISPLAY   *P1:7,"Select Option : "
            MOVE      SEVEN,CVERT
          ENDIF
.
MENU1000  KEYIN     *P17:CVERT,*DV,UNDLN1:
                    *P17:CVERT,*V2LON,MOPTION;
.
          COMPARE   ZERO,MOPTION
          GOTO      MENU9000 IF EQUAL
.
          IF        IBCNMHOS = 1
            BRANCH    MOPTION,MENU8000,MENU8000
          ELSE
            BRANCH    MOPTION,MENU8000
          ENDIF
.
          BEEP
          GOTO      MENU1000
.
MENU8000  MOVE      ZERO,EXIT
          GOTO      MENU9999
.
MENU9000  MOVE      ONE,EXIT
.
MENU9999  RETURN
+
.*********************************************************************
.*        DSCR0000                               Called by ML0000    *
.*        Display the screen                                         *
.*********************************************************************
.
DSCR0000  MOVE      ZERO,EXIT 
          DISPLAY   *P1:9,*EF
          IF        MOPTION = 1
            DISPLAY   *P1:10,"Consultant from : ",*P19:10,*EL:
                      *P1:11,"             to : ",*P19:11,*EL
            DISPLAY   *P1:13,"Discharge Date from : ",*P23:13,*EL:
                      *P1:14,"                 to : ",*P23:14,*EL
          ELSE
            DISPLAY   *P1:10,"    Hospital    : "
            DISPLAY   *P1:12,"Consultant from : ",*P19:13,*EL:
                      *P1:13,"             to : ",*P19:14,*EL
            DISPLAY   *P1:15,"Discharge Date from : ",*P23:15,*EL:
                      *P1:16,"                 to : ",*P23:16,*EL
          ENDIF
DSCR9999  RETURN
+
.*********************************************************************
.*        KCON0000                               Called by ML0000    *
.*        Keyin consultant range                                     *
.*********************************************************************
.
KCON0000  MOVE      ZERO,EXIT 
          IF        MOPTION = 2
            DISPLAY   *P19:13,*EL:
                      *P19:14,*EL
            MOVE      TEN2,EVERT
          ELSE
            DISPLAY   *P19:10,*EL:
                      *P19:11,*EL
            MOVE      TEN,EVERT
          ENDIF
.
          MOVE      SP8,CKYIDEF8
          MOVE      ZERO,CKYIMAND
          UNPACK    SP30,STRTCON,ENDCON
          PACK      DOC1,SP30,SP30
          PACK      DOC2,SP30,SP30
          MOVE      TEN9,ECOL
.
          CALL      PATDOCKY
          BRANCH    EXIT,KCON1000,KCON9000
          MOVE      DOCCODE,STRTCON
          CALL      PNAM0000              * Pack up the name
          DISPLAY   *P30:EVERT,PACFNAME
          PACK      DOC1,STRTCON,SP2,PACFNAME
          GOTO      KCON5000
KCON1000  MOVE      SP10,STRTCON
          DISPLAY   *P19:EVERT,*V2LON,START   
          MOVE      START,DOC1
          MOVE      ZERO,EXIT
.
KCON5000  IF        MOPTION = 2
            MOVE      TEN3,EVERT
          ELSE
            MOVE      TEN1,EVERT
          ENDIF
          CALL      PATDOCKY
          BRANCH    EXIT,KCON6000,KCON9000
          MOVE      DOCCODE,ENDCON 
          CALL      PNAM0000              * Pack up the name
          DISPLAY   *P30:EVERT,PACFNAME
          PACK      DOC2,ENDCON,SP2,PACFNAME
          GOTO      KCON8000
KCON6000  MOVE      "zzzzzzzzz",ENDCON 
          DISPLAY   *P19:EVERT,*V2LON,FINISH 
          MOVE      FINISH,DOC2
          MOVE      ZERO,EXIT
.
KCON8000  MATCH     STRTCON,ENDCON
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Invalid Doctor Range. ";
            CALL      HITENTER
            GOTO      KCON0000
          ENDIF
          GOTO      KCON9999
.
KCON9000  MOVE      ONE,EXIT
KCON9999  RETURN
.
PNAM0000  MOVE      DSNAME,PACSNAME
          MOVE      DGNAME,ANS
          MOVE      DTITL,PACTITLE
          MOVE      ANS,PACGNAME
          MOVE      ONE,PACFRMT
          CALL      PACNAME

PNAM9999  RETURN
+
.*********************************************************************
.*        HOSP0000                               Called by ML0000    *
.*        Get hospital code                                          *
.*********************************************************************
.
HOSP0000  IF        IBCNMHOS=1
            MOVE      "10",EVERT
            MOVE      "19",ECOL
            MOVE      SP3,CKYIDEF3
            MOVE      ZERO,CKYIMAND
            OPEN      PATHSPA1,"pathspaf"
            CALL      PATHSPKY
            CLOSE     PATHSPA1
            DISPLAY   *P25:EVERT,PTHSNAME;
          ENDIF
          RETURN
+
.**************************************************************************
.*  KDAT0000  :   Keyin a date range                                      *
.**************************************************************************
.
. keyin from date (admission date)
.
KDAT0000  UNPACK    SP10,CDAY,CMON,CYEAR
          MOVE      "23",CCOL
          IF        MOPTION = 2 
            DISPLAY   *P23:15,*EL:
                      *P23:16,*EL
            MOVE      TEN5,CVERT
          ELSE
            DISPLAY   *P23:13,*EL:
                      *P23:14,*EL
            MOVE      TEN3,CVERT
          ENDIF

          MOVE      ONE,CCENTRY
          MOVE      ZERO,CDEFDTE
          MOVE      "00000000",FROMDATE
.
KDAT1000  CALL      KEYDATE
          BRANCH    CQUEST,KDAT1000
          BRANCH    OVRCD,KDAT9000                * anything input
.
          PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY
          PACK      FDDATE,CDAY,SLASH,CMON,SLASH,CYEAR,CCENT    * display var
          REP       " 0",FDDATE 
          GOTO      KDAT5000
.
. keyin to date (admission date)
.
KDAT5000  UNPACK    SP10,CDAY,CMON,CYEAR,CCENT
          MOVE      "23",CCOL
          IF        MOPTION = 1
            MOVE      TEN4,CVERT
          ELSE
            MOVE      TEN6,CVERT
          ENDIF
          MOVE      ONE,CCENTRY
          MOVE      ZERO,CDEFDTE
          MOVE      "99999999",TODATE
.
KDAT6000  CALL      KEYDATE
          BRANCH    CQUEST,KDAT6000
          BRANCH    OVRCD,KDAT0000                * anything input?
.
          PACK      TODATE,CCENT,CYEAR,CMON,CDAY
          PACK      TDDATE,CDAY,SLASH,CMON,SLASH,CYEAR,CCENT    * display var
          REP       " 0",TDDATE 
.
. check if dates entered are valid (range)
.
          REP       " 0",FROMDATE
          REP       " 0",TODATE
.
          MATCH     FROMDATE,TODATE
          GOTO      KDAT8000 IF LESS
.
          MOVE      ZERO,EXIT                     * Valid range
          GOTO      KDAT9999
.
KDAT8000  DISPLAY   *P1:24,*EL,*B,"Invalid date range.  ";
          CALL      HITENTER
          GOTO      KDAT0000
. 
KDAT9000  MOVE      ONE,EXIT                      * nothing input
.
KDAT9999  RETURN
+
.***************************************************************************
.*  EXTR0000  :  Load data into temp file                                  *
.***************************************************************************
.
EXTR0000  DISPLAY   *P1:24,*EL,*P30:24,"Loading data... [                     ]"
.
. --- get patient using discharge date range ---
.
          PACK      KEY16,FROMDATE,SP20
          CALL      RDSDSCH2
EXTR1000  CALL      RDKDSCH2
          BRANCH    OVRCD,EXTR9000
.
          MATCH     DDATE,TODATE                  * in range?
          GOTO      EXTR9000 IF LESS              * no
.
. ******* loop over the tran file to get FCE *******
.
          MOVE      SP6,STADOCT
          MOVE      SP3,STATYPE
          MOVE      SP3,STWARD
.
          PACK      KEY30,DADMNO,SP30
          CALL      RDSTRAN2
EXTR2000  CALL      RDKTRAN2
          BRANCH    OVRCD,EXTR1000
.
          MATCH     TADMN,DADMNO                  * still same patient?
          GOTO      EXTR1000 IF NOT EQUAL
.
          DISPLAY   *P47:24,TADMN,TDATE,PTTREPNO
.
          MATCH     ANSA,TMOVE
          GOTO      EXTR2900 IF EQUAL             * ignore admission record
.
          MATCH     ANSD,TMOVE
          GOTO      EXTR4900 IF EQUAL       * discharged so FCE
.
          MATCH     TADOCT,STADOCT
          GOTO      EXTR5000 IF NOT EQUAL   * different attending doctor
.
.         save the attending doctor to see if a FCE on next change record
.
EXTR2900  MOVE      TADOCT,STADOCT
          MOVE      TATYPE,STATYPE
          MOVE      TWARD,STWARD
          MOVE      TBED,SAVBED
          MOVE      PTTREPNO,SAVEPNO
          GOTO      EXTR2000
.
EXTR4900  MOVE      TDATE,DDATE             * set the discharge date
.
. ******* have a finished consultant episode *******
.
EXTR5000  MATCH     STRTCON,STADOCT                * consultant in start range?
          GOTO      EXTR2900 IF LESS
.
          MATCH     STADOCT,ENDCON                * consultant in end range?
          GOTO      EXTR2900 IF LESS
.
. --- get hospital for this ward ---
.
          PACK      KEY6,STWARD,SAVBED
          CALL      RDWARD1
          BRANCH    OVRCD,EXTR2900
.
          IF        MOPTION=2
            MATCH     PTHSHOSP,PTWDHOSN             * same hospital?
            GOTO      EXTR2900 IF NOT EQUAL
          ENDIF
.
. make sure coding has not been completed
.
.
          MOVE      SP8,PTEDDTCD                  * assume coding not completed
          MOVE      "  1",KEY3                                        *I-240107
          PACK      KEY13,TADMN,SAVEPNO,KEY3,SP20                     *C-240107
          CALL      RDPTECD1
.
          MATCH     SP8,PTEDDTCD
          GOTO      EXTR2900 IF NOT EQUAL
.
. consultant episode DOESN'T have coding so write a record to temp file
.
EXTR6000  MOVE      TADMN,XXADMNO
          MOVE      STADOCT,XXADOCT
          MOVE      STATYPE,XXSPEC
          MOVE      STWARD,XXWARD
          MOVE      TURNO,XXURNO
          MOVE      SAVEPNO,XXEPNO
.
          PACK      KEY16,XXADMNO,XXADOCT,XXEPNO
          DISPLAY   *P48:24,XXADMNO;
          CALL      WRTEMP1
          GOTO      EXTR2900
.
EXTR9000  CALL      ANYD0000                      * any data found?
          BRANCH    EXIT,EXTR9999                 * no
          DISPLAY   *P1:24,*EL
          MOVE      ZERO,EXIT
.
EXTR9999  RETURN
+
.****************************************************************************
.* ANYD0000  :  Check if any data was found                                 *
.****************************************************************************
.
ANYD0000  PACK      KEY16,SP20
          CALL      RSTEMP1
          CALL      RKTEMP1
          BRANCH    OVRCD,ANYD9000
.
          MOVE      ZERO,EXIT                     * data found
          GOTO      ANYD9999
.
ANYD9000  DISPLAY   *P1:24,*EL,*B,"No data found.  ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
ANYD9999  RETURN
+
.****************************************************************************
.*  SXTR0000  :  SXTRact data into temp file                                *
.****************************************************************************
.
SXTR0000  DISPLAY   *P1:24,*EL,*P35:24,"Scanning... [        ]";
.
. SXTRact all patients who have been discharged
.
          MOVE      ZERO,SRCHMETH                 * discharge search method
          PACK      KEY16,FROMDATE,SP20
          CALL      RDSDSCH2
SXTR1000  CALL      RDKDSCH2
          BRANCH    OVRCD,SXTR5000                * no more records
.
          DISPLAY   *P48:24,DADMNO;
.
          MATCH     DDATE,TODATE                  * date in range?
          GOTO      SXTR5000 IF LESS
.
          IF        MOPTION = 2
            PACK      KEY30,DADMNO,DDATE,DTIME,SP30
            CALL      RDSTRAN2
            CALL      RDKTRAN2
            BRANCH    OVRCD,SXTR1000
.
            PACK      DIM30,TADMN,TDATE,TTIME,SP30
            MATCH     KEY30,DIM30                * Wrong Transfer Record
            GOTO      SXTR1000 IF NOT EQUAL
.
            MATCH     ANSD,TMOVE                 * Must be the Discharge Rec
            GOTO      SXTR1000 IF NOT EQUAL
.
            PACK      KEY6,TWARD,SP3             * Get Hosp from Ward/Bef d File
            CALL      RDWARD1
            BRANCH    OVRCD,SXTR1000
.
            MATCH     PTHSHOSP,PTWDHOSN          * Different Hospital
            GOTO      SXTR1000 IF NOT EQUAL
          ENDIF
.
          MOVE      DADMNO,XXADMNO
          CALL      CCOD0000                      * check for coding/write temp
          GOTO      SXTR1000                      * get next record
.
. now, get all current admissions
.
SXTR5000  MOVE      ONE,SRCHMETH                  * admission search method
          PACK      KEY9,TWO,SP9
          CALL      RDMISS2
SXTR6000  CALL      RDKMISS2
          BRANCH    OVRCD,SXTR9999
.
          DISPLAY   *P48:24,AADMNO;
. 
          COMPARE   TWO,ASTAT
          GOTO      SXTR9999 IF NOT EQUAL
.
          IF        MOPTION = 2
            PACK      KEY6,AWARD,SP3             * Get Hosp from Ward/Bef d File
            CALL      RDWARD1
            BRANCH    OVRCD,SXTR6000
.
            MATCH     PTHSHOSP,PTWDHOSN          * Different Hospital
            GOTO      SXTR6000 IF NOT EQUAL
          ENDIF
.
          MOVE      AADMNO,XXADMNO
          CALL      CCOD0000                      * check for coding/write temp
          GOTO      SXTR6000                      * get next record
.
SXTR9999  RETURN
+
.****************************************************************************
.*  CCOD0000  :  Check for coding and write to temp if no coding            *
.*               Requires  XXADMNO - admission no. to process               *
.****************************************************************************
.
CCOD0000  PACK      KEY30,XXADMNO,SP30
          CALL      RDSTRAN2
CCOD1000  CALL      RDKTRAN2
          BRANCH    OVRCD,CCOD9999
.
          MATCH     XXADMNO,TADMN                 * same admission no.?
          GOTO      CCOD9999 IF NOT EQUAL
.
.------     Test within the required consultant range  ------
.
          MATCH     STRTCON,TADOCT
          GOTO      CCOD1000 IF LESS
.
          MATCH     TADOCT,ENDCON
          GOTO      CCOD1000 IF LESS
.
          PACK      KEY16,TADOCT,TADMN
          CALL      RATEMP1
          COMPARE   ZERO,OVRCD                    * already on temp file?
          GOTO      CCOD1000 IF EQUAL             * Yes so ignore
.
. if searching by current admissions, make sure there is a consultant episode
.
          IF        SRCHMETH=1
            MOVE      TADOCT,SAVADOCT
CCOD3000    CALL      RDKTRAN2
            BRANCH    OVRCD,CCOD9999
            MATCH     TADMN,XXADMNO
            GOTO      CCOD9999 IF NOT EQUAL
.
            MATCH     TADOCT,SAVADOCT
            GOTO      CCOD3000 IF EQUAL
.                                                 * we have a different doctor
            MOVE      SAVADOCT,TADOCT      
            CALL      RDPTRAN2
          ENDIF
.
. make sure episode happening in correct hospital
.
          IF        MOPTION=2
            PACK      KEY6,TWARD,SP3             * Get Hosp from Ward/Bef d File
            CALL      RDWARD1
            BRANCH    OVRCD,CCOD1000
.
            MATCH     PTHSHOSP,PTWDHOSN          * Different Hospital
            GOTO      CCOD1000 IF NOT EQUAL
          ENDIF
. 
. make sure coding has not been completed
.
.
          MOVE      SP8,PTEDDTCD                  * assume coding not completed
          MOVE      "  1",KEY3                                        *I-240107
          PACK      KEY13,TADMN,PTTREPNO,KEY3,SP20                    *C-240107
          CALL      RDPTECD1
.
          MATCH     SP8,PTEDDTCD
          GOTO      CCOD1000 IF NOT EQUAL
.
. consultant episode DOESN'T have coding so write a record to temp file
.
CCOD5000  MOVE      TADMN,XXADMNO
          MOVE      TADOCT,XXADOCT
          MOVE      TATYPE,XXSPEC
          MOVE      TWARD,XXWARD
          MOVE      TURNO,XXURNO
.
          PACK      KEY16,XXADMNO,XXADOCT,XXEPNO
          DISPLAY   *P48:24,*V2LON,XXADMNO;
          CALL      WRTEMP1
          GOTO      CCOD1000
.
CCOD9999  RETURN
+
.****************************************************************************
.*  RPRT0000  :  Print report from temp file                                *
.****************************************************************************
.
RPRT0000  DISPLAY   *P1:24,*EL,*P35:24,"Printing...";
.
          CALL      IBACLOCK
          MOVE      ZERO,CPAGENO
          MOVE      ONE,CNOUNDLN
          MOVE      SP6,SAVADOCT
.
          CALL      HEAD132
.
          IF        MOPTION = 2
            PRINT     *40,"Hospital  :  ",PTHSHOSP,SP3,PTHSNAME:
                      *N
            ADD       TWO,CLNO
          ENDIF
          PRINT     *40,"Consultant      From : ",DOC1
          PRINT     *40,"                  to : ",DOC2,*N
          PRINT     *40,"Discharge Date Range : ",FDDATE,"  -  ",TDDATE,*N
          ADD       FIVE,CLNO
          CALL      PTAB0000                      * print headings
.
          PACK      KEY16,SP20
          CALL      RSTEMP1 
RPRT0500  CALL      RKTEMP1                       * get next record
          BRANCH    OVRCD,RPRT9000                * no more records
.
          DISPLAY   *P47:24,XXADMNO
.
. check if we need a new page
.
          COMPARE   "55",CLNO
          GOTO      RPRT5000 IF LESS
.
. we need a new page
.
          CALL      UND132
          CALL      HEAD132
          PRINT     *40,"Discharge Date Range : ",FDDATE,"  -  ",TDDATE,*N
          CALL      PTAB0000                      * print headings
          MOVE      TEN,CLNO
          MOVE      SP6,SAVADOCT
.
RPRT5000  CALL      GDAT0000                      * get data for printing
.
. print a line of data
.
          MATCH     XXADOCT,SAVADOCT              * print consultant?
          IF        !@EQUAL
            PRINT     *1,"| ",XXADOCT,SP1,CONS20;
          ELSE
            PRINT     *1,"| ";
          ENDIF
.
          PRINT     *31,"| ",XXURNO,*42,"| ":
                    PNAME25,*70,"| ",ADMDATE,*83,"| ",DISDATE,*96,"| ":
                    XXSPEC,SP1,DESC12,*115,"| ",XXWARD,SP1,DESC10,*132,"|"
.
          ADD       ONE,CLNO                      * increment line counter
          MOVE      XXADOCT,SAVADOCT              * save consultant
          GOTO      RPRT0500
.
RPRT9000  CALL      UND132                        * print underlines
          PRINT     *N,*1,"*** End of report ***" 
.
RPRT9999  RETURN
+
.****************************************************************************
.*  GDAT0000  :  Get data for printing                                      *
.****************************************************************************
.
GDAT0000  MOVE      "Unknown code",CONS20       * consultant name
          PACK      KEY6,XXADOCT
          CALL      RDDOCT1
          IF        OVRCD=0
            MOVE      DGNAME,PACGNAME
            MOVE      DSNAME,PACSNAME
            MOVE      DTITL,PACTITLE
            MOVE      TWO,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,CONS20
          ENDIF
.
          MOVE      "Unknown Patient",PNAME25   * patient name
          PACK      KEY8,XXURNO
          CALL      RDMAST1
          IF        OVRCD=0
            MOVE      PGNAME,PACGNAME
            MOVE      PSNAME,PACSNAME
            MOVE      PTITL,PACTITLE
            MOVE      TWO,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,PNAME25
          ENDIF
.
          MOVE      SP10,ADMDATE
          PACK      KEY8,XXADMNO
          CALL      RDPTMIS1
          IF        OVRCD=0
            UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY       * admission date
            CALL      PACDATE
            MOVE      CPCDATE,ADMDATE
          ENDIF
.
          MOVE      SP10,DISDATE
          PACK      KEY8,XXADMNO
          CALL      RDDSCH1
          IF        OVRCD=0
            UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY       * discharge date
            CALL      PACDATE
            MOVE      CPCDATE,DISDATE
          ENDIF
.
          MOVE      "Unknown Code",TDESC             * specialty desc
          MATCH     SP3,XXSPEC
          IF        !@EQUAL
            PACK      KEY5,ANSA,SP1,XXSPEC
            CALL      RDCODE1
          ENDIF
          MOVE      TDESC,DESC12
.
          MOVE      "Unknown Code",WBDESC            * ward desc
          PACK      KEY6,XXWARD,SP3
          CALL      RDWARD1
          MOVE      WBDESC,DESC10
.
GDAT9999  RETURN
+
.****************************************************************************
.*  PTAB0000  :  Print Table Heading                                        *
.****************************************************************************
.
PTAB0000  CALL      UND132
          PRINT     *1,"| Consultant",*31,"| U/R No.",*42,"| Patient Name":
                    *70,"| Adm. date",*83,"| Dis. date",*96,"| Specialty":
                    *115,"| Ward",*132,"|"
          CALL      UND132
.
PTAB9999  RETURN
+
.*************************************************************************
.*                             KILL0000             Called by:           *
.*                   erase temporary file                                *
.*************************************************************************
.
KILL0000  CLOSE     TEMPFILE
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
KILL9999  RETURN
+
.*************************************************************************
.*                             CRET0000            Called by:            *
.*                      create temporary file                            *
.*************************************************************************
.
CRET0000  CALL      KILL0000 
          CLEAR     CMDLINE 
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME,CMDLINE
          APPEND    " 31 U1-6,7-14,29-30",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPFILE,FNAME
.
CRET9999  RETURN
+
.*************************************************************************
.*  Temp I/O's                                                           *
.*************************************************************************
.
RATEMP1   MOVE      ZERO,OVRCD
          RESET     KEY3
          READ      TEMPFILE,KEY16;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTEMP1   RESET     KEY16
          READ      TEMPFILE,KEY16;;
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMPFILE;XXADOCT,DXXADMNO,XXURNO,XXSPEC,XXWARD,XXEPNO
          GOTO      OVERCOND IF OVER
          MOVE      DXXADMNO,XXADMNO
          RETURN
.
WRTEMP1   RESET     KEY16
          MOVE      XXADMNO,DXXADMNO
          WRITE     TEMPFILE,KEY16;XXADOCT,DXXADMNO,XXURNO,XXSPEC,XXWARD:
                                   XXEPNO
          RETURN
+
.*****************************************************************************
.*        I/O Includes                                                       *
.*****************************************************************************
.
          INC       STD001IO
.
          INC       PATDOCKY
          INC       PATHSPKY
          INC       TFILENAM                     * Generate Temp File Name
.
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATDSCIO/INC
          INC       PATECDIO/INC
          INC       PATHSPIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATTRNIO/INC
          INC       PATWR1IO/INC
          INC       WEBERRIO/INC                 * Web Server Error Logging

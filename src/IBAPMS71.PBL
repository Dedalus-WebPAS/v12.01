.******************************************************************************
.* System   :  PRS2                                                           *
.* Program  :  IBAPMS71                                                       *
.* Desc     :  Transfer Source Master Listing                                 *
.******************************************************************************
.* Date     :  16/05/1990                                                     *
.* Author   :  i chung                                                        *
.* Mods     :  V10.08.01 18/11/2016  Ebon Clements    TSK 0823851             *
.*                       Added all patvadaf fields to upload                  *
.*             V9.04.01  22/02/2005  Peter Vela       CAR 57547               *
.*                       Added Provider Number                                *
.*             V9.03.01  23/03/2004  Ebon Clements    CAR 18703               *
.*                       Added global update functions for active dates       *
.*                       Added upload transfer source codes from text file    *
.*             V5.01.00  21/06/1990  Glen Hobby                               *
.*                       Convert from V5.00 to V5.01                          *
.*             V5.01.01  17/01/1992  i chung          SRF 112314              *
.*                       Added "Destination Description Sequence" option      *
.*             V5.01.02  08/06/92 J.Tan    SRF 115070                         *
.*                       Added HDP Equivalent                                 *
.*             V5.03.01  23/11/1995  Greg Horvat      SRF 612434              *
.*                  Recompiled for PATVADDS - Fixed '?' on trans source dest  *
.*                                                                            *
.******************************************************************************
.
.$$$$$$
.  Transfer Source Master Listing (IBAPMS71)
.  -----------------------------------------
.
.  - Initialization
.
.       - Display screen header
.       - Open file
.            "patvadaf" (PATVADA1,PATVADA2)
.
.  - Process Options
.
.       - 0 = Exit
.       - 1 = Transfer Source Code Sequence
.       - 2 = Destination Description Sequence
.
.  - END
.$$$$$$
          INC       STD001FD
          INC       PATVADFD/INC
+
.*******************************************************************************
.*                          WORK VARIABLES DECLARATION                         *
.*******************************************************************************
AUTOEDAT  FORM      1            * Auto update end date
AUTOSDAT  FORM      1            * Auto update start date
CODE      DIM       5            * code input
COL       FORM      2            * vertical display position
CURRDATE  DIM       8            * Current Date
.
ECODE     DIM       5            * Ending Code
EDESC     DIM       30           * Ending code description
EDEST     DIM       30           * Ending Destination Description
EXPRDATE  DIM       8            * Transfer Source Expiry Date
.
FRCODE    DIM       5            * Starting code output
FRDEST    DIM       30           * Starting destination desc. output
.
.
UPLOADFL  DIM       127          * Upload File
QUESTFLG  FORM      1            * "?" routine flag (0=Off, 1=On)
ROW       FORM      2            * horizontal display position
.
SCODE     DIM       5            * Starting Code
SDESC     DIM       30           * Starting code description
SDEST     DIM       30           * Starting Destination Description
STRTDATE  DIM       8            * Transfer Source Start Date
.
TFSCDESC  DIM       30           * Transfer Source code desc. - used in PATVADDS
TOCODE    DIM       6            * Ending code output
TODEST    DIM       30           * Ending destination desc. output
.
UPDATFLG  FORM      1            * Update record or just print report
.
UPLDFILE  FILE      ASCII,FIXED=256
.
.Name      Type     Length   Start   Description
.----      ----     ------   -----   ----------- 
TXTFCODE   DIM      5        1       Transfer Source Code PTVACODE
TXTFDESC   DIM      30       6       Transfer Source Description PTVADESC
TXTFNHSC   DIM      4        36      HDP Equivalent
TXTFPROV   DIM      8        40      Hospital Provider Number
TXTFPNAM   DIM      30       48      Provider Name
TXTFPPCD   DIM      8        78      Provider Postcode
TXTFDTYP   DIM      1        86      Destination type
TXTFCNME   DIM      4        87      Coded Name (4 characters)
TXTFESTB   DIM      10       91      Establishment Number
.
                             101
+
.*******************************************************************************
.*                            CONSTANTS DECLARATION                            *
.*******************************************************************************
DRAW      INIT      "*-------------------------------------------------------":
                    "----------------------------*"                    *I-57547
FINISH    INIT      "Finish"
START     INIT      "Start"
.
Z5        INIT      "zzzzz"
Z30       INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
.
PRGID     INIT      "IBAPMS71"
PRGNAM    INIT      "Transfer Source Codes "
VERSION   INIT      "V12.01.00"
+
.*******************************************************************************
.*                                  MAINLINE                                   *
.*                              Controlling Logic                              *
.*******************************************************************************
ML0000    CALL      INIT0000                * display screen header & open files
.
ML1000    CALL      OPTN0000                * user selects option
          BRANCH    OPTION,ML2000,ML3000:   * 1=Code sequence, 2=Desc. sequence
                           ML4000,ML5000:
                           ML6000,ML7000
          GOTO      ML9999                  * 0=Exit
.
.         Option 1 - Transfer Source Code Sequence
.
ML2000    CALL      CSEQ0000                * get Transfer Source code range
          BRANCH    EXIT,ML1000             * no Transfer Source code found
          CALL      CONTQST                 * O.K. to continue ?
          BRANCH    CEXIT,ML3500,ML2000,ML1000
.                          Yes     No   Cancel
.
.         Option 2 - Destination Description Sequence
.
ML3000    CALL      DSEQ0000                * get Destination Description range
          BRANCH    EXIT,ML1000             * no Transfer Source code found
          CALL      CONTQST                 * O.K. to continue ?
          BRANCH    CEXIT,ML3500,ML3000,ML1000
.                          Yes     No   Cancel
.
.         Print Routine for both options
.
ML3500    CALL      PRNT0000                * print listing
          GOTO      ML1000
.
ML4000    CALL      EXPR0000                * Inactivate expired transfer codes
          GOTO      ML1000
.
ML5000    CALL      EDAT0000                * Add expiry dates to transfer codes
          GOTO      ML1000
.
ML6000    CALL      LOAD0000                * Load transfer codes from text file
          GOTO      ML1000
.
ML7000    CALL      ACTV0000                * Activate transfer source codes
          GOTO      ML1000
.
ML9999    CHAIN     PGM                     * return to calling menu
+
.*******************************************************************************
.*                                  INIT0000                                   *
.*        Display screen header, open file & set         called by : MAINLINE  *
.*        HEAD80 input requirement                                             *
.*******************************************************************************
INIT0000  CALL      DISPHEAD                * display screen header
.
          DISPLAY   *P50:24,"Opening patvadaf"
          OPEN      PATVADA1,"patvadaf"
          OPEN      PATVADA2,"patvadaf"
.
. ------  set up HEAD80 input requirement(s)  ----
.
          MOVE      ONE,CNOUNDLN
.
INIT9999  RETURN
+
.*******************************************************************************
.*                                  OPTN0000                                   *
.*        get user's option selection                    called by : MAINLINE  *
.*                                                                             *
.*        valid options     --->  (0) Exit                                     *
.*                                (1) Transfer Source Code Sequence            *
.*******************************************************************************
OPTN0000  CALL      MSCN0000                     * Main Screen
.
OPTN1000  KEYIN     *P17:12,*EL,*DV,UNDLN1,*P17:12,*V2LON,OPTION;
          COMPARE   ZERO,OPTION                  * exit ?
          GOTO      OPTN9999 IF EQUAL            * yes
.
          BRANCH    OPTION,OPTN9999,OPTN9999,OPTN9999,OPTN9999,OPTN9999:
                           OPTN9999
          BEEP
          GOTO      OPTN1000
.
OPTN9999  RETURN
+
.*******************************************************************************
.*                                  MSCN0000                                   *
.*                              Main menu SCreeN                               *
.*                  called by : OPTN0000, RDPA0000, RDPB0000                   *
.*******************************************************************************
MSCN0000  DISPLAY   *P1:3,*EF,*V2LON,*P1:4,ZERO,*P1:5,ONE,*P1:6,TWO:
                                     *P1:7,THREE,*P1:8,FOUR,*P1:9,FIVE:
                                     *P1:10,SIX,*HOFF:
                    *P2:4," = Exit":
                    *P2:5," = Transfer source listing code sequence":
                    *P2:6," = Transfer source listing description sequence":
                    *P2:7," = Inactivate expired transfer sources codes":
                    *P2:8," = Global expiry of all transfer source codes":
                    *P2:9," = Load transfer source codes from a text file":
                    *P2:10," = Activate transfer source codes":
                    *P1:12,"Select Option :";
.
MSCN9999 RETURN
+
.*******************************************************************************
.*                                  CSEQ0000                                   *
.*        get range of Transfer Source codes             called by : MAINLINE  *
.*******************************************************************************
CSEQ0000  DISPLAY   *P1:14,*EF,"Starting Transfer Source code :":
                        *P1:16,"Ending   Transfer Source code :";
.
          CALL      CLRV0000                   * clear work variables & set flag
.
. ------  get starting code  ------
.
          MOVE      "33",COL
          MOVE      "14",ROW
.
          CALL      CODE0000                   * input code
          MOVE      CODE,SCODE                 * set starting code
          BRANCH    EXIT,CSEQ1000,CSEQ9000     * 1=null input, 2=no recs found
.
.         valid start code
.
          MOVE      CODE,FRCODE
          MOVE      PTVADESC,SDESC             * set starting code description
.                                                for redisplay after "?" routine
          BRANCH    QUESTFLG,CSEQ1100          * 1 = "?" routine was called
          DISPLAY   *P41:14,SDESC;             * display source description
          GOTO      CSEQ2000
.
.         null start code
.
CSEQ1000  MOVE      START,FRCODE               * set starting code output
          BRANCH    QUESTFLG,CSEQ1100          * 1 = "?" routine was called
          DISPLAY   *P33:14,*EL,*V2LON,START;  * display word "Start"
          GOTO      CSEQ2000
CSEQ1100
          CALL      RDPA0000                   * redisplay screen afer "?"
.
. ------  get ending code  ------
.
CSEQ2000  MOVE      "33",COL
          MOVE      "16",ROW
          MOVE      FALSE,QUESTFLG
.
          CALL      CODE0000                   * input code
          BRANCH    EXIT,CSEQ2100              * null input
.
.         valid end code
.
          MOVE      CODE,ECODE                 * set ending code
          MOVE      CODE,TOCODE                * set ending code output
          MOVE      PTVADESC,EDESC             * set ending code description
.                                                for redisplay after "?" routine
          BRANCH    QUESTFLG,CSEQ2200          * 1 = "?" routine was called
          DISPLAY   *P41:16,EDESC;             * display end source description
          GOTO      CSEQ3000
.
.         null end code
.
CSEQ2100  MOVE      Z5,ECODE                   * set ending code
          MOVE      FINISH,TOCODE              * set ending code output
          BRANCH    QUESTFLG,CSEQ2200          * 1 = "?" routine was called
          DISPLAY   *P33:16,*EL,*V2LON,FINISH; * display word "Finish"
          GOTO      CSEQ3000
CSEQ2200
          CALL      RDPA0000                   * redisplay screen afer "?"
.
. ------  check if start code is less than or equal to end code  ------
.
CSEQ3000  MATCH     SCODE,ECODE
          GOTO      CSEQ8000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** End code must be greater than or = to Start code **  ";
          CALL      HITENTER
          GOTO      CSEQ2000
.
. ------  valid Transfer Source code range  ------
.
CSEQ8000  MOVE      FALSE,EXIT                 * set EXIT = 0
          GOTO      CSEQ9999
.
. ------  no Transfer Source records found  ------
.
CSEQ9000  MOVE      TRUE,EXIT                  * set EXIT = 1
.
CSEQ9999  RETURN
+
.*******************************************************************************
.*                                  CLRV0000                                   *
.*        clear all work variables and set flag          called by : CSEQ0000  *
.*******************************************************************************
CLRV0000  MOVE      SP5,SCODE
          MOVE      SP30,SDESC
          MOVE      SP5,ECODE
          MOVE      SP30,EDESC
          MOVE      SP5,FRCODE
          MOVE      SP6,TOCODE
.
          MOVE      FALSE,QUESTFLG               * set "?" routine flag off
.
CLRV9999  RETURN
+
.*******************************************************************************
.*                                  CODE0000                                   *
.*        get Transfer Source code                       called by : CSEQ0000  *
.*******************************************************************************
CODE0000  KEYIN     *PCOL:ROW,*EL,*DV,UNDLN5,*PCOL:ROW,*V2LON,CODE;
.
          ENDSET    CODE
          APPEND    SP5,CODE
          RESET     CODE
.
          MATCH     SP5,CODE
          GOTO      CODE8000 IF EQUAL            * null input ?
.
          DISPLAY   *PCOL:ROW,*V2LON,CODE;
          CMATCH    QUEST,CODE                   * "?" routine requested ?
          GOTO      CODE2000 IF EQUAL            * yes
.
          MOVE      CODE,KEY5
          CALL      RDPTVAD1                     * read PATVADFD record
          BRANCH    OVRCD,CODE1000               * record not found
          MOVE      FALSE,EXIT                   * set EXIT = 0
          GOTO      CODE9999
.
CODE1000  DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Transfer Source code not on file **    ";
          CALL      HITENTER
.
          BRANCH    QUESTFLG,CODE2100
          GOTO      CODE0000
.
. ------  "?" routine requested  ------
.
CODE2000  CALL      PATVADDS                     * scan PATVADFD file
          BRANCH    OVRCD,CODE9000               * no records found
.
          MOVE      "24",COL
          MOVE      "24",ROW
          MOVE      TRUE,QUESTFLG                * set "?" routine flag on
CODE2100
          DISPLAY   *P1:24,*EL,"Transfer Source code :";
          GOTO      CODE0000
.
. ------  null code input  ------
.
CODE8000  MOVE      TRUE,EXIT                    * set EXIT = 1
          GOTO      CODE9999
.
. ------  no Transfer Source records found  ------
.
CODE9000  MOVE      TWO,EXIT                     * set EXIT = 2
.
CODE9999  RETURN
+
.*******************************************************************************
.*                                  RDPA0000                                   *
.*        redisplay screen after "?" routine - Opt. 1    called by : CSEQ0000  *
.*******************************************************************************
RDPA0000  CALL      MSCN0000
          DISPLAY   *V2LON,OPTION,*HOFF:
                    *P1:14,"Starting Transfer Source code :":
                    *P1:16,"Ending   Transfer Source code :";
.
          MATCH     SP5,SCODE
          GOTO      RDPA1000 IF EQUAL
          DISPLAY   *P33:14,*V2LON,SCODE,*HOFF,*P41:14,SDESC;
          GOTO      RDPA2000
.
RDPA1000  DISPLAY   *P33:16,*EL,*V2LON,START;
.
RDPA2000  MATCH     Z5,ECODE
          GOTO      RDPA2100 IF EQUAL
          DISPLAY   *P33:16,*V2LON,ECODE,*HOFF,*P41:16,EDESC;
          GOTO      RDPA9999
.
RDPA2100  DISPLAY   *P33:16,*EL,*V2LON,FINISH;
.
RDPA9999  RETURN
+
.*******************************************************************************
.*                                  DSEQ0000                                   *
.*        destination description range                  called by : MAINLINE  *
.*******************************************************************************
DSEQ0000  DISPLAY   *P1:14,*EF,"Starting Destination Description :":
                        *P1:16,"Ending   Destination Description :";
.
          MOVE      "14",ROW
          MOVE      FALSE,QUESTFLG
.
.         Starting Destination Description
.
DSEQ1000  MOVE      SP30,SDEST
          KEYIN     *P36:ROW,*EL,*DV,UNDLN30,*P36:ROW,*V2LON,SDEST;
.
          ENDSET    SDEST
          APPEND    SP30,SDEST
          RESET     SDEST
.
          MATCH     SP30,SDEST
          GOTO      DSEQ2000 IF NOT EQUAL
          MOVE      START,FRDEST
.
          BRANCH    QUESTFLG,DSEQ3100
          DISPLAY   *P36:14,*EL,*V2LON,START;
          GOTO      DSEQ4000
.
DSEQ2000  CMATCH    QUEST,SDEST
          GOTO      DSEQ3000 IF NOT EQUAL
          CALL      PATVADDS
          BRANCH    OVRCD,DSEQ9000
          MOVE      "24",ROW
          MOVE      TRUE,QUESTFLG
          DISPLAY   *P1:24,*EL,"Starting Destination Description :";
          GOTO      DSEQ1000
.
DSEQ3000  MOVE      SDEST,FRDEST
          BRANCH    QUESTFLG,DSEQ3100
          DISPLAY   *P36:14,*EL,*V2LON,SDEST;
          GOTO      DSEQ4000
.
DSEQ3100  CALL      RDPB0000
.
.         Ending Destination Description
.
DSEQ4000  MOVE      "16",ROW
          MOVE      FALSE,QUESTFLG
.
DSEQ4100  MOVE      SP30,EDEST
          KEYIN     *P36:ROW,*EL,*DV,UNDLN30,*P36:ROW,*V2LON,EDEST;
          RESET     EDEST
          GOTO      DSEQ5000 IF NOT EOS
.
DSEQ4200  MOVE      Z30,EDEST
          MOVE      FINISH,TODEST
          COMPARE   FALSE,QUESTFLG
          GOTO      DSEQ4300 IF EQUAL
          CALL      RDPB0000
.
DSEQ4300  DISPLAY   *P36:16,*EL,*V2LON,FINISH;
          GOTO      DSEQ9999
.
DSEQ5000  CMATCH    SP1,EDEST
          GOTO      DSEQ4200 IF EQUAL
.
          CMATCH    QUEST,EDEST
          GOTO      DSEQ6000 IF NOT EQUAL
          CALL      PATVADDS
          BRANCH    OVRCD,DSEQ9000
          MOVE      "24",ROW
          MOVE      TRUE,QUESTFLG
.
DSEQ5100  DISPLAY   *P1:24,*EL,"Ending   Destination Description :";
          GOTO      DSEQ4100
.
DSEQ6000  MOVE      EDEST,TODEST
          COMPARE   FALSE,QUESTFLG
          GOTO      DSEQ6100 IF EQUAL
          CALL      RDPB0000
.
DSEQ6100  DISPLAY   *P36:16,*EL,*V2LON,EDEST;
.
          ENDSET    EDEST
          APPEND    Z30,EDEST
          RESET     EDEST
.
          MATCH     SDEST,EDEST
          GOTO      DSEQ8000 IF NOT LESS
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Ending Desc. must be > or equal Starting Desc. **   ";
          CALL      HITENTER
          BRANCH    QUESTFLG,DSEQ4000
          GOTO      DSEQ4100
.
. ------  valid Transfer Source description range  ------
.
DSEQ8000  MOVE      FALSE,EXIT                 * set EXIT = 0
          GOTO      DSEQ9999
.
. ------  no Transfer Source records found  ------
.
DSEQ9000  MOVE      TRUE,EXIT                  * set EXIT = 1
.
DSEQ9999  RETURN
+
.*******************************************************************************
.*                                  RDPB0000                                   *
.*        redisplay screen after "?" routine - Opt. 2    called by : CSEQ0000  *
.*******************************************************************************
RDPB0000  CALL      MSCN0000
          DISPLAY   *V2LON,OPTION,*HOFF:
                    *P1:14,"Starting Destination Description :":
                    *P1:16,"Ending   Destination Description :";
.
          MATCH     SP30,SDEST
          GOTO      RDPB1000 IF EQUAL
          DISPLAY   *P36:14,*V2LON,SDEST;
          GOTO      RDPB9999
.
RDPB1000  DISPLAY   *P36:14,*EL,*V2LON,START;
.
RDPB9999  RETURN
+
.*******************************************************************************
.*                                  PRNT0000                                   *
.*        print master listing                           called by : MAINLINE  *
.*******************************************************************************
PRNT0000  DISPLAY   *P1:24,*EL,*V2LON,*BLINKON:
                    "Generating Master Listing.  Please wait ... ";
.
          MOVE      ZERO,CPAGENO                 * initialize page no.
          CALL      HEAD0000                     * print headings
.
          COMPARE   TWO,OPTION                   * Option 2 ?
          GOTO      PRNT5000 IF EQUAL            * yes
.
.         Option 1
.
          MOVE      SCODE,KEY5
          MATCH     SP3,KEY5                     * null key ?
          GOTO      PRNT1000 IF EQUAL            * yes
.
          CALL      RDPTVAD1                     * read PATVADFD record
          BRANCH    OVRCD,PRNT9000               * record not found
          GOTO      PRNT2000
.
PRNT1000  CALL      RSPTVAD1                     * position read PATVADFD file
.
PRNT1100  CALL      RKPTVAD1
          BRANCH    OVRCD,PRNT9000               * end-of-file
.
PRNT2000  MATCH     PTVACODE,ECODE               * last record to be printed ?
          GOTO      PRNT9000 IF LESS             * yes
          GOTO      PRNT8000
.
.         Option 2
.
PRNT5000  PACK      KEY35,SDEST,SP5
          CALL      RSPTVAD2                     * position read PATVADFD file
.
PRNT5100  CALL      RKPTVAD2
          BRANCH    OVRCD,PRNT9000               * end-of-file
.
          MATCH     PTVADESC,EDEST               * last record to be printed ?
          GOTO      PRNT9000 IF LESS             * yes
.
.         print detail line
.
PRNT8000  COMPARE   "55",CLNO                    * page overflow ?
          CALL      HEAD0000 IF NOT LESS
.
          PRINT     *13,"|",SP6,PTVACODE,SP6,"|  ",PTVADESC,SP4,"|  ":
                    PTVANHSC,SP4,"|",SP1,PTVAPROV,SP8,"|"              *I-57547
.
          ADD       ONE,CLNO                     * increase current line no by 1
          BRANCH    OPTION,PRNT1100,PRNT5100
.
.         End of Range/End of File
.
PRNT9000  PRINT     *13,DRAW,*N,*N,*13,"***  End of Report  ***"
.
PRNT9999  RETURN
+
.*******************************************************************************
.*                                  HEAD0000                                   *
.*        routine to print master list headings          called by : PRNT0000  *
.*******************************************************************************
HEAD0000  COMPARE   ZERO,CPAGENO
          GOTO      HEAD1000 IF EQUAL
          PRINT     *13,DRAW
.
HEAD1000  CLOCK     TIME,CTIMEIS
          CALL      HEAD80
.
          BRANCH    OPTION,HEAD2000,HEAD3000,HEAD4000,HEAD5000,HEAD6000:
                           HEAD7000
.
HEAD2000  PRINT     *N,*20,"Transfer Source - From : ",*+,FRCODE," to : ",TOCODE
          ADD       TWO,CLNO
          GOTO      HEAD8000
.
HEAD3000  PRINT     *N,*20,"Destination Description - From : ",*+,FRDEST,*N:
                                              *46,"To   : ",TODEST
          ADD       THREE,CLNO
          GOTO      HEAD8000
.
HEAD4000  PRINT     *20,"Inactivate Expired Transfer Source Codes"
          IF        UPDATFLG=0
            PRINT     *20,"Print Report Only : Yes"
          ELSE
            PRINT     *20,"Print Report Only : No "
          ENDIF
.
          ADD       TWO,CLNO
          GOTO      HEAD8000
.
HEAD5000  UNPACK    EXPRDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.  
          PRINT     *20,"Global Inactive From Date for Transfer Source Codes"
.
          IF        AUTOEDAT=1
            PRINT     *20,"Expiry Date : ",CPCDATE:
                         *46,"Auto Update Expiry Date : Yes"
          ELSE
            PRINT     *20,"Expiry Date : ",CPCDATE:
                         *46,"Auto Update Expiry Date : No"
          ENDIF
.
          ADD       TWO,CLNO
          GOTO      HEAD8000
.
HEAD6000  PRINT     *20,"Load Transfer Source Codes From a Text File"
.
          ADD       ONE,CLNO
          GOTO      HEAD8000
.
HEAD7000  PRINT     *20,"Activate Transfer Source Codes"
          IF        UPDATFLG=0
            PRINT     *20,"Print Report Only : Yes"
          ELSE
            PRINT     *20,"Print Report Only : No "
          ENDIF
.
          ADD       TWO,CLNO
          GOTO      HEAD8000
.
HEAD8000  PRINT     *N,*13,DRAW:
                    *N,*13,"| Transfer Source |  Destination Description":
                           "           |  HDP Eq. | Provider Number |":
                    *N,*13,DRAW                                        *I-57547
.
          ADD       FOUR,CLNO                    * increase current line no by 4
.
HEAD9999  RETURN
.
+
.**************************************************************************
.*                               EXPR0000              Called by: ML4000  *
.* Inactivate expired transfer source codes                               *
.**************************************************************************
.
EXPR0000  CALL      UPDT0000                    * Print report only
.
          CALL      CONTQST                     * ok to continue
          BRANCH    CEXIT,EXPR5000,EXPR9999,EXPR9999    * 1=Yes, 2=No, 3=Cancel
.
EXPR5000  CALL      PEXP0000                    * Update/Print expired codes
.
EXPR9999  RETURN
.
.**************************************************************************
.*                               EDAT0000              Called by: ML5000  *
.*  Global addition of transfer source expiry dates                       *
.**************************************************************************
.
EDAT0000  CALL      KDAT0000                    * Keyin expiry date 
          BRANCH    EXIT,EDAT9999
.
          CALL      AUTO0000                    * Auto update on end date
.
          CALL      CONTQST                     * ok to continue
          BRANCH    CEXIT,EDAT5000,EDAT9999,EDAT9999    * 1=Yes, 2=No, 3=Cancel
.
EDAT5000  CALL      ADAT0000                    * Add/Print Expiry Date
.
EDAT9999  RETURN
.
.------------------------------------------------------------
. Update records or print report only
.------------------------------------------------------------
UPDT0000  DISPLAY   *P1:14,*EF
          KEYIN     *P1:14,*EL,"Print Report Only":
                    " (",*V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          MATCH     ANSY,ANS
          IF        @EQUAL
            MOVE      ZERO,UPDATFLG
            GOTO      UPDT9999
          ENDIF
.
          MATCH     ANSN,ANS
          IF        @EQUAL
            MOVE      ONE,UPDATFLG
            GOTO      UPDT9999
          ENDIF
.
          BEEP
          GOTO      UPDT0000
.
UPDT9999  RETURN
.
.------------------------------------------------------------
.  keyin expiry date
.------------------------------------------------------------
KDAT0000  DISPLAY    *P1:14,*EF,"Expiry Date :";
.
          MOVE      "15",CCOL
          MOVE      "14",CVERT
          CALL      IBACLOCK
          MOVE      CCC,CCENT
          MOVE      CYY,CYEAR
          MOVE      CMM,CMON
          MOVE      CDD,CDAY
          CALL      KEYDATE
          BRANCH    OVRCD,KDAT9000
.
          PACK      EXPRDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",EXPRDATE
.
          MOVE      ZERO,EXIT
          GOTO      KDAT9999
.
KDAT9000  MOVE      ONE,EXIT
          GOTO      KDAT9999
.
KDAT9999  RETURN
.
.------------------------------------------------------------
. Set auto update on end date 
.------------------------------------------------------------
AUTO0000  DISPLAY   *P1:16,*EF
          KEYIN     *P1:16,*EL,"Auto Update End Date":
                    " (",*V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          MATCH     ANSY,ANS
          IF        @EQUAL
            MOVE      ONE,AUTOEDAT
            GOTO      AUTO9999
          ENDIF
.
          MATCH     ANSN,ANS
          IF        @EQUAL
            MOVE      ZERO,AUTOEDAT
            GOTO      AUTO9999
          ENDIF
.
          BEEP
          GOTO      AUTO0000
.
AUTO9999  RETURN
.
.*******************************************************************************
.*                                  PEXP0000                                   *
.* Print/Expire transfer source codes                    called by : MAINLINE  *
.*******************************************************************************
PEXP0000  DISPLAY   *P1:24,*EL,*V2LON,*BLINKON:
                    "Printing Expired Transfer Source Codes.  Please wait ... ";
.
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
. 
          MOVE      ZERO,CPAGENO                 * initialize page no.
          CALL      HEAD0000                     * print headings
.
          PACK      KEY5,SP70
          CALL      RSPTVAD1                     * position read PATVADFD file
PEXP1100  CALL      RKPTVAD1
          BRANCH    OVRCD,PEXP9000               * end-of-file
.
          MATCH     "A",PTVAACTV                 * Skip if not active
          GOTO      PEXP1100 IF NOT EQUAL
.
          MATCH     SP70,PTVAEDAT                * Skip if no end date
          GOTO      PEXP1100 IF EQUAL
.      
          MATCH     PTVAEDAT,CURRDATE            * Check if record has expired
          GOTO      PEXP1100 IF LESS
.
          MATCH     "1",PTVAEUPD                 * Skip if Auto Update End Date
          GOTO      PEXP1100 IF NOT EQUAL        * is set to no
.
          IF        UPDATFLG=1
            MOVE      "I",PTVAACTV               * Set to Inactive
.
            PACK      PTVAUDAT,CCC,CYY,CMM,CDD
            REP       " 0",PTVAUDAT
            CLOCK     TIME,PTVAUTIM
            MOVE      SP70,PTVAUUID
            CALL      UPPTVAD1                   * Update Transfer Source
          ENDIF
.
.         print detail line
.
          COMPARE   "55",CLNO                    * page overflow ?
          CALL      HEAD0000 IF NOT LESS
.
          PRINT     *13,"|",SP6,PTVACODE,SP6,"|  ",PTVADESC,SP4,"|  ":
                    PTVANHSC,SP4,"|",SP1,PTVAPROV,SP8,"|"              *I-57547
.
          ADD       ONE,CLNO                     * increase current line no by 1
          GOTO      PEXP1100
.
.         End of Range/End of File
.
PEXP9000  PRINT     *13,DRAW,*N,*N,*13,"***  End of Report  ***"
.
PEXP9999  RETURN
.
.*******************************************************************************
.*                                  ADAT0000                                   *
.*  Global Inactive From Date for Transfer Source Codes  called by : MAINLINE  *
.*******************************************************************************
ADAT0000  DISPLAY   *P1:24,*EL,*V2LON,*BLINKON:
                    "Printing/Adding Inactive From Dates For Transfer":
                    " Source Codes.  Please wait ... ";
.
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          MOVE      ZERO,CPAGENO                 * initialize page no.
          CALL      HEAD0000                     * print headings
.
          PACK      KEY5,SP70
          CALL      RSPTVAD1                     * position read PATVADFD file
ADAT1100  CALL      RKPTVAD1
          BRANCH    OVRCD,ADAT9000               * end-of-file
.
          MATCH     "A",PTVAACTV                 * Skip if not active
          GOTO      ADAT1100 IF NOT EQUAL
.
          MATCH     SP70,PTVAEDAT                * Skip if no end date
          GOTO      ADAT1100 IF NOT EQUAL
.
          IF        UPDATFLG=1
            MOVE      EXPRDATE,PTVAEDAT
            MOVE      AUTOEDAT,PTVAEUPD
.
            PACK      PTVAUDAT,CCC,CYY,CMM,CDD
            REP       " 0",PTVAUDAT
            CLOCK     TIME,PTVAUTIM
            MOVE      SP70,PTVAUUID
            CALL      UPPTVAD1                   * Update Transfer Source
          ENDIF
.
.         print detail line
.
          COMPARE   "55",CLNO                    * page overflow ?
          CALL      HEAD0000 IF NOT LESS
.
          PRINT     *13,"|",SP6,PTVACODE,SP6,"|  ",PTVADESC,SP4,"|  ":
                    PTVANHSC,SP4,"|",SP1,PTVAPROV,SP8,"|"              *I-57547
.
          ADD       ONE,CLNO                     * increase current line no by 1
          GOTO      ADAT1100
.
.         End of Range/End of File
.
ADAT9000  PRINT     *13,DRAW,*N,*N,*13,"***  End of Report  ***"
.
ADAT9999  RETURN
.
.
.**************************************************************************
.*                               LOAD0000              Called by: ML6000  *
.*  Load transfer source codes from a text file                           *
.**************************************************************************
.
LOAD0000  CALL      KFIL0000                     * Keyin upload file name
          BRANCH    EXIT,LOAD9999
.
          CALL      SDAT0000                     * Keyin start dare
          BRANCH    EXIT,LOAD9999
.
          CALL      AUTS0000                     * Auto update start date
.
          CALL      CONTQST                      * ok to continue
          BRANCH    CEXIT,LOAD5000,LOAD9999,LOAD9999    * 1=Yes, 2=No, 3=Cancel
.
LOAD5000  CALL      READ0000                     * Upload from text file
.
LOAD9999  RETURN
.
.------------------------------------------------------------
.  keyin start date
.------------------------------------------------------------
SDAT0000  DISPLAY    *P1:16,*EF,"Start Date :";
.
          MOVE      "15",CCOL
          MOVE      "16",CVERT
          CALL      IBACLOCK
          MOVE      CCC,CCENT
          MOVE      CYY,CYEAR
          MOVE      CMM,CMON
          MOVE      CDD,CDAY
          CALL      KEYDATE
          BRANCH    OVRCD,SDAT9000
.
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",EXPRDATE
.
          MOVE      ZERO,EXIT
          GOTO      SDAT9999
.
SDAT9000  MOVE      ONE,EXIT
          GOTO      SDAT9999
.
SDAT9999  RETURN
.
.------------------------------------------------------------
. Set auto update on start date
.------------------------------------------------------------
AUTS0000  DISPLAY   *P1:18,*EF
          KEYIN     *P1:18,*EL,"Auto Update start Date":
                    " (",*V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          MATCH     ANSY,ANS
          IF        @EQUAL
            MOVE      ONE,AUTOSDAT
            GOTO      AUTS9999
          ENDIF
.
          MATCH     ANSN,ANS
          IF        @EQUAL
            MOVE      ZERO,AUTOSDAT
            GOTO      AUTS9999
          ENDIF
.
          BEEP
          GOTO      AUTS0000
.
AUTS9999  RETURN
.
.------------------------------------------------------------
. Keyin upload file name
.------------------------------------------------------------
KFIL0000  KEYIN     *P1:14,*EF,"Upload File Name   : ",UPLOADFL
          ENDSET    UPLOADFL
          APPEND    SP70,UPLOADFL
          APPEND    SP70,UPLOADFL
          RESET     UPLOADFL
.
          MATCH     SP70,UPLOADFL           * No upload file so exit 
          GOTO      KFIL9000 IF EQUAL
.
          CALL      PREP0000
          BRANCH    EXIT,KFIL9000 
.
          MOVE      ZERO,EXIT
          GOTO      KFIL9999
.
KFIL9000  MOVE      ONE,EXIT
          GOTO      KFIL9999
.
KFIL9999  RETURN
.
.**************************************************************************
.*                            PREP0000                  called by:KFIL0000
.**************************************************************************
PREP0000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      UPLDFILE,UPLOADFL
          TRAPCLR   IO
          BRANCH    OVRCD,PREP9000       * File does not exist
.
          MOVE      ZERO,EXIT
          GOTO      PREP9999
.
PREP9000  DISPLAY   *P1:24,*EL,"Upload file not found:",UPLOADFL;
          CALL      HITENTER
          MOVE      ONE,EXIT
          GOTO      PREP9999
.
PREP9999  RETURN
.
.*******************************************************************************
.*                                  READ0000                                   *
.*  Load trasnfer source codes from a text file                 by : LOAD0000  *
.*******************************************************************************
READ0000  DISPLAY   *P1:24,*EL,*V2LON,*BLINKON:
                    "Loading Transfer Source Codes From a Text File":
                    " Source Codes.  Please wait ... ";
.
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          MOVE      ZERO,CPAGENO                 * initialize page no.
          CALL      HEAD0000                     * print headings
.
READ1000  CALL      RDUPLOAD
          BRANCH    OVRCD,READ9000               * end-of-file
.
          PACK      KEY5,TXTFCODE,SP70
          CALL      RDPTVAD1   
          COMPARE   ZERO,OVRCD
          GOTO      READ1000 IF EQUAL
.
          MOVE      TXTFCODE,PTVACODE
          MOVE      TXTFDESC,PTVADESC
          MOVE      TXTFNHSC,PTVANHSC
          MOVE      ANSA,PTVAACTV
          MOVE      STRTDATE,PTVASDAT    
          MOVE      AUTOSDAT,PTVASUPD
          MOVE      SP70,PTVAEDAT    
          MOVE      ZERO,PTVAEUPD
.
          PACK      PTVACDAT,CCC,CYY,CMM,CDD
          REP       " 0",PTVACDAT
          CLOCK     TIME,PTVACTIM
          MOVE      SP70,PTVACUID
          MOVE      SP70,PTVAUDAT
          MOVE      SP70,PTVAUTIM
          MOVE      SP70,PTVAUUID
.
          MOVE      TXTFPROV,PTVAPROV
          MOVE      TXTFPNAM,PTVAPNAM
          MOVE      TXTFPPCD,PTVAPPCD
          MOVE      TXTFDTYP,PTVADTYP
          MOVE      TXTFCNME,PTVACNME
          MOVE      TXTFESTB,PTVAESTB
.
          MOVE      SP70,PTVASPAR
.
          PACK      KEY5,PTVACODE,SP70
          CALL      WRPTVAD1   
.
.         print detail line
.
          COMPARE   "55",CLNO                    * page overflow ?
          CALL      HEAD0000 IF NOT LESS
.
          PRINT     *13,"|",SP6,PTVACODE,SP6,"|  ",PTVADESC,SP4,"|  ":
                    PTVANHSC,SP4,"|",SP1,PTVAPROV,SP8,"|"              *I-57547
.
          ADD       ONE,CLNO                     * increase current line no by 1
          GOTO      READ1000
.
.         End of Range/End of File
.
READ9000  PRINT     *13,DRAW,*N,*N,*13,"***  End of Report  ***"
.
READ9999  RETURN
.
+
.**************************************************************************
.*                               ACTV0000              Called by: ML7000  *
.* Activate transfer source codes                                         *
.**************************************************************************
.
ACTV0000  CALL      UPDT0000                    * Print report only
.
          CALL      CONTQST                     * ok to continue
          BRANCH    CEXIT,ACTV5000,ACTV9999,ACTV9999    * 1=Yes, 2=No, 3=Cancel
.
ACTV5000  CALL      PACT0000                    * Update/Print activated codes
.
ACTV9999  RETURN
.
.
.*******************************************************************************
.*                                  PACT0000                                   *
.* Print ectivate transfer source codes                  called by : MAINLINE  *
.*******************************************************************************
PACT0000  DISPLAY   *P1:24,*EL,*V2LON,*BLINKON:
                    "Printing Activated Transfer Source Codes.  Please wait ..";
.
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          MOVE      ZERO,CPAGENO                 * initialize page no.
          CALL      HEAD0000                     * print headings
.
          PACK      KEY5,SP70
          CALL      RSPTVAD1                     * position read PATVADFD file
PACT1100  CALL      RKPTVAD1
          BRANCH    OVRCD,PACT9000               * end-of-file
.
          MATCH     "I",PTVAACTV                 * Skip if active
          GOTO      PACT1100 IF NOT EQUAL
.
          MATCH     SP70,PTVASDAT                * Skip if no start date
          GOTO      PACT1100 IF EQUAL
.
          MATCH     PTVASDAT,CURRDATE            * Check if record has expired
          GOTO      PACT1100 IF LESS
.
          MATCH     "1",PTVASUPD                 * Skip if Auto Update End Date
          GOTO      PACT1100 IF NOT EQUAL        * is set to no
.
          IF        UPDATFLG=1
            MOVE      "A",PTVAACTV               * Set to Inactive
.
            PACK      PTVAUDAT,CCC,CYY,CMM,CDD
            REP       " 0",PTVAUDAT
            CLOCK     TIME,PTVAUTIM
            MOVE      SP70,PTVAUUID
            CALL      UPPTVAD1                   * Update Transfer Source
          ENDIF
.
.         print detail line
.
          COMPARE   "55",CLNO                    * page overflow ?
          CALL      HEAD0000 IF NOT LESS
.
          PRINT     *13,"|",SP6,PTVACODE,SP6,"|  ",PTVADESC,SP4,"|  ":
                    PTVANHSC,SP4,"|",SP1,PTVAPROV,SP8,"|"              *I-57547
.
          ADD       ONE,CLNO                     * increase current line no by 1
          GOTO      PACT1100
.
.         End of Range/End of File
.
PACT9000  PRINT     *13,DRAW,*N,*N,*13,"***  End of Report  ***"
.
PACT9999  RETURN
+
.**************************************************************************
.*                                RDUPLOAD             called by:         *
.**************************************************************************
RDUPLOAD  MOVE      ZERO,OVRCD
          READ      UPLDFILE,SEQ;TXTFCODE,TXTFDESC,TXTFNHSC,TXTFPROV:
                                 TXTFPNAM,TXTFPPCD,TXTFDTYP,TXTFCNME:
                                 TXTFESTB
          GOTO      OVERCOND IF OVER
          RETURN
.
.*******************************************************************************
.
          INC       STD001IO
          INC       PATVADDS
          INC       PATVADIO/INC
+

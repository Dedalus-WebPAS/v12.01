.******************************************************************************
.* System         : Outpatients                                               *
.* Program        : IBAOUT85.PBL                                              *
.* Name           : Booking and Vacancies by Clinic Type                      *
.******************************************************************************
.* Date           : 03/06/1993    11:30:00 BST                                *
.* Author         : Justin 'The Pom' Coates                                   *
.* Function       : To print bookings and vacancies for a range of sessions,  *
.*                  broken down by visit type                                 *
.* Modifications  :                                                           *
.*        V12.00.01 16/05/2025  Ebon Clements TSK 0955096                     *
.*                  Alphanueric visit number changes                          *
.*        V10.07.01 30/10/2015  Ebon Clements CAR 268970                      *
.*                  Change to use OUTCLIFD index 1 instead of index           *
.*        V10.05.01 18/12/2014  Ebon Clements CAR 261630                      *
.*                  Removed port number from temp file name                   *
.*        V9.09.02  04/03/2008  Peter Vela    CAR 162914                      *
.*                  Recompiled for KYOUTCLI - readk of outcliaf               *
.*        V9.09.01  28/07/2007  Jill Habasque CAR 103645                      *
.*                  Recompiled for KYOUTCLI - read of pmshcpaf                *
.*        V5.08.01  19/09/2000  Steve Armstrong   srf 647263                  *
.*                  Recompiled for changes to OUTDSCLN                        *
.*        V5.08.B01 15/03/2000  Steve Armstrong     5.08 Mods                 *
.*                  Mods for changes to OUTCLIFD (effective date)             *
.******************************************************************************
.*        V5.01.02  30/06/1993    Justin Coates  Quote 440008  SRF 440150     *
.*                  add clinic id to report and temp file key                 *
.*        V5.01.03  06/10/1994    Paul Howells   SRF441099                    *
.*                  Separate sessions into AM & PM.                           *
.******************************************************************************
.
          INC       STD001FD
          INC       IBASEQFD/INC
          INC       OUTBOAFD/INC
          INC       OUTCLIFD/INC
          INC       OUTCONFD/INC            * system parameters
          INC       OUTCTYFD/INC
          INC       OUTOSFFD/INC
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PMSHCPFD/INC            * National HCP file
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
.         temp files
.
TEMPA     IFILE     SQL,FIXED=78, KEY="U1-6,7-12,13-18,19-19,20-22,77-77,23-30"
KEYA      INIT      " 78 U1-6,7-12,13-18,19-19,20-22,77-77,23-30"
.Name     Type      Length  Phys.  Start  Description
.-------  -----     ------  -----  -----  -------------------------------
XCTYPE    DIM       6          6       1  Clinic Type
XSESSID   DIM       6          6       7  Session Id
XCLINID   DIM       6          6      13  Session Id
XSDAY     DIM       1          1      19  Day
XHOSP     DIM       3          3      20  Hospital code
XSDATE    DIM       8          8      23  Date
.
DXBKNWRT  DIM       4          4      31  Booked - New routine
DXBKNWUR  DIM       4          4      35  Booked - New urgent
DXBKSTAR  DIM       4          4      39  Booked - Star
DXBKREVW  DIM       4          4      43  Booked - Review
DXBKEXTR  DIM       4          4      47  Booked - Extra booked
DXBKTOTL  DIM       5          5      51  Booked - Total
.
DXVCNWRT  DIM       4          4      56  Vacant - New routine
DXVCNWUR  DIM       4          4      60  Vacant - New urgent
DXVCSTAR  DIM       4          4      64  Vacant - Star
DXVCREVW  DIM       4          4      68  Vacant - Review
DXVCTOTL  DIM       5          5      72  Vacant - Total
DAMPMIND  DIM       1          1      77  Morning or Afternoon Session
.End of Record                        78
.
XBKNWRT   FORM      4
XBKNWUR   FORM      4
XBKSTAR   FORM      4
XBKREVW   FORM      4
XBKEXTR   FORM      4
XBKTOTL   FORM      5
XVCNWRT   FORM      4
XVCNWUR   FORM      4
XVCSTAR   FORM      4
XVCREVW   FORM      4
XVCTOTL   FORM      5
AMPMIND   FORM      1
.
.         program variables
.
CDFRCLIN  DIM       6         * code range variables
CDFRCTYP  DIM       6
CDFRDATE  DIM       8
CDFRSNID  DIM       6
CDTOCLIN  DIM       6
CDTOCTYP  DIM       6
CDTODATE  DIM       8
CDTOSNID  DIM       6
.
CFNAMEA   DIM       8
CMDLINE   DIM       80
CURRCLIN  DIM       6         * current clinic id
CURRCTYP  DIM       6         * current clinic type
CURRDATE  DIM       8         * current date
CURRDAY   DIM       1         * current day 
CURRHOSP  DIM       3         * current hospital
CURRAMPM  FORM      1         * current morning/afternoon session
CURRSNID  DIM       6         * current session id
.
DDAY      DIM       9
DIM14     DIM       14
DIM40     DIM       40
ERRCNT    FORM      6
HOUR      DIM       2
PRCLUNLN  FORM      1         * print underline before session id 1=yes
.
PRFRCLIN  DIM       6         * print range variables
PRFRCTYP  DIM       6
PRFRDATE  DIM       10
PRFRSNID  DIM       6
PRTOCLIN  DIM       6
PRTOCTYP  DIM       6
PRTODATE  DIM       10
PRTOSNID  DIM       6
.
SLOTTYPE  FORM      1         * slot type
.                                 1 = booked
.                                 2 = vacant 
.
TEMPDAY   DIM       3
TOVRCD    FORM      1
VISTYPE   FORM      1         * visit type of slot 
.                                 1 = new routine 
.                                 2 = new urgent 
.                                 3 = star
.                                 4 = review
.                                 5 = extra booked
.
.         program constants
.
CATCV     INIT      "CV"
FINISH    INIT      "Finish"
ISBUILD   INIT      "isbuild "
ISERASE   INIT      "iserase "
MIDDAY    INIT      "12"
START     INIT      "Start "
Z6        INIT      "zzzzzz"
MON       INIT      "Mon"
TUE       INIT      "Tue"
WED       INIT      "Wed"
THU       INIT      "Thu"
FRI       INIT      "Fri"
SAT       INIT      "Sat"
SUN       INIT      "Sun"
AM        INIT      " - AM"
PM        INIT      " - PM"
Z20       INIT      "zzzzzzzzzzzzzzzzzzzz"
.
PRGID     INIT      "IBAOUT85"
PRGNAM    INIT      "Bookings and Vacancies by Clinic Type"
VERSION   INIT      "V12.01.00"
.
.*********************************************************************
.         mainline code
.*********************************************************************
ML0000    CALL      INIT0000                * initilisation
.
ML1000    CALL      OPTN0000                * run report
          BRANCH    EXIT,ML9999             * exit
.
ML2000    CALL      INST0000                * enter search ranges
          BRANCH    EXIT,ML1000             * exit
.
          CALL      CHGE0000                * enter search ranges
          BRANCH    EXIT,ML1000             * exit
.
          CALL      GDAT0000                * get the data
          CALL      REPT0000                * print the report
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.*********************************************************************
.         initilisation
.*********************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P58:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
          DISPLAY   *P64:24,"pmshcpaf";
          OPEN      PMSHCPA1,"pmshcpaf"
.
          PACK      CFNAME,UID,FILCTYA1
          DISPLAY   *P58:24,CFNAME
          OPEN      OUTCTYA1,CFNAME
          OPEN      OUTCTYA2,CFNAME
.
          PACK      CFNAME,UID,FILCLIA1
          DISPLAY   *P58:24,CFNAME
          OPEN      OUTCLIA1,CFNAME
.
          PACK      CFNAME,UID,FILBOKA4
          DISPLAY   *P58:24,CFNAME
          OPEN      OUTBOKA1,CFNAME
.
          DISPLAY   *P58:24,"outosfaf"
          OPEN      OUTOSFA3,"outosfaf"
.
          DISPLAY   *P58:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
          CALL      TFILENAM * Get temp file name in create
          MOVE      TFILNAME,CFNAMEA
.
          MOVE      ONE,CCANLDTE            * blank cancel default
          MOVE      ONE,CDEFDTE             * enter once for default
          MOVE      ZERO,CHIGHLT            * use highlight
          MOVE      ONE,CNEWDS
.
INIT9999  RETURN
+
.*********************************************************************
.         run report ??
.*********************************************************************
OPTN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Run report":
                    *P1:7,"Select option : "
OPTN1000  KEYIN     *P17:7,*V2LON,MOPTION
          MOVE      ONE,EXIT
          COMPARE   ZERO,MOPTION
          GOTO      OPTN9999 IF EQUAL
          MOVE      ZERO,EXIT
          COMPARE   ONE,MOPTION
          GOTO      OPTN9999 IF EQUAL
          BEEP
          GOTO      OPTN1000
OPTN9999  RETURN
+
.*********************************************************************
.         enter the ranges
.*********************************************************************
INST0000  CALL      DSCR0000                * display screens
          MOVE      ONE,CCITEM              * clinic types
          CALL      CTYP0000
          BRANCH    EXIT,INST9999
.
INST4000  MOVE      THREE,CCITEM            * session id's
          CALL      SNID0000
          BRANCH    EXIT,INST9999
.
INST5000  MOVE      FIVE,CCITEM             * clinic id's
          CALL      CLIN0000
          BRANCH    EXIT,INST9999
.
          MOVE      SEVEN,CCITEM            * dates
          CALL      DATE0000
          BRANCH    EXIT,INST5000
.
INST9999  RETURN
+
.*********************************************************************
.         modify the ranges
.*********************************************************************
CHGE0000  CALL      MAINQST
          COMPARE   ZERO,CCITEM
          GOTO      CHGE9000 IF EQUAL
          GOTO      CHGE9100 IF LESS
.
          BRANCH    CCITEM,CHGE1000,CHGE1000,CHGE3000,CHGE3000,CHGE5000:
                           CHGE5000,CHGE7000,CHGE7000
          BEEP
          GOTO      CHGE0000
.
CHGE1000  CALL      CTYP0000                * enter clinic types
          GOTO      CHGE0000
CHGE3000  CALL      SNID0000                * enter session ids
          GOTO      CHGE0000
CHGE5000  CALL      CLIN0000                * enter clinic ids
          GOTO      CHGE0000
CHGE7000  CALL      DATE0000                * enter dates
          GOTO      CHGE0000
.
CHGE9000  MOVE      ZERO,EXIT
          GOTO      CHGE9999
CHGE9100  MOVE      ONE,EXIT
CHGE9999  RETURN
+
.*********************************************************************
.         get the data for the report
.*********************************************************************
GDAT0000  DISPLAY   *P1:24,*EL,"Obtaining information..."
          CLOCK     TIME,CTIMEIS
          CALL      CTPA0000                * create temp file
.
. ******* loop over the opened session file *******
.
          PACK      KEY34,CDFRDATE,SP3,IDDD,CDFRSNID,SP20
          CALL      RSOTOSF3
.
GDAT1000  CALL      RKOTOSF3
          BRANCH    OVRCD,GDAT9999          * end of file
          MATCH     OTOSDATE,CDTODATE
          GOTO      GDAT9999 IF LESS        * after  end   date
.
          DISPLAY   *P30:24,OTOSDATE,OTOSSESS,OTOSCTYP,SP3
          MATCH     IDDD,OTOSSITE
          GOTO      GDAT1000 IF NOT EQUAL   * different site
.
          MATCH     CDFRCTYP,OTOSCTYP
          GOTO      GDAT1000 IF LESS        * before start clinic type
          MATCH     OTOSCTYP,CDTOCTYP
          GOTO      GDAT1000 IF LESS        * after end clinic type
.
          MATCH     CDFRSNID,OTOSSESS
          GOTO      GDAT1000 IF LESS        * before start session id
          MATCH     OTOSSESS,CDTOSNID
          GOTO      GDAT1000 IF LESS        * after  end   session id
.
          MATCH     CDFRCLIN,OTOSCLID
          GOTO      GDAT1000 IF LESS        * before start clinic id
          MATCH     OTOSCLID,CDTOCLIN
          GOTO      GDAT1000 IF LESS        * after  end   clinic id
.
. ******* now looop over outbokaf for the above session ******
.
          PACK      KEY28,OTOSSITE,OTOSCLID,OTOSDATE,OTOSTIME,SP3
          CALL      RDSBOKA1
.
GDAT2000  CALL      RDKBOKA1
          BRANCH    OVRCD,GDAT1000          * end of file
          PACK      KEY25,OBASITE,OBACLIN,OBADATE,OBASTRT
          MATCH     KEY25,KEY28
          GOTO      GDAT1000 IF NOT EQUAL   * finished session
.
          DISPLAY   *P50:24,*V2LON,OBASLOT
          CALL      PTMP0000                * process temp file
          GOTO      GDAT2000
.
GDAT9999  RETURN
+
.*********************************************************************
.         print the report
.*********************************************************************
REPT0000  DISPLAY   *P1:24,*EL,"Printing the report..."
          MOVE      "******",CURRCTYP
          MOVE      "******",CURRSNID
          MOVE      "******",CURRCLIN
          MOVE      "*",CURRDAY 
          MOVE      "***",CURRHOSP
          MOVE      ONE,CURRAMPM
          MOVE      "********",CURRDATE
          MOVE      ONE,PRCLUNLN
          MOVE      ZERO,ERRCNT
          MOVE      "70",CLNO
          MOVE      ZERO,CPAGENO
          MOVE      SP30,KEY31
          CALL      RSTEMPA
.
REPT1000  CALL      RKTEMPA
          BRANCH    OVRCD,REPT9000
.
          CALL      PLIN0000
          GOTO      REPT1000
.
REPT9000  IF        CPAGENO = ZERO
            CALL      HEAD132
          ELSE
            CALL      UND132
          ENDIF
          PRINT     *N,"*** End of Report ***",*N
          CALL      DTPA0000
.
REPT9999  RETURN
+
.*********************************************************************
.         display the screen
.*********************************************************************
DSCR0000  DISPLAY   *P1:9,*EF:
                    *P1:10,*V2LON," 1",*HOFF,". From Clinic Type : ":
                    *P1:11,*V2LON," 2",*HOFF,". To   Clinic Type : ":
                    *P1:13,*V2LON," 3",*HOFF,". From Session Id  : ":
                    *P1:14,*V2LON," 4",*HOFF,". To   Session Id  : ":
                    *P1:16,*V2LON," 5",*HOFF,". From Clinic Id   : ":
                    *P1:17,*V2LON," 6",*HOFF,". To   Clinic Id   : ":
                    *P1:19,*V2LON," 7",*HOFF,". From Date        : ":
                    *P1:20,*V2LON," 8",*HOFF,". To   Date        : "
DSCR9999  RETURN
+
.*********************************************************************
.                   CTYP0000                    Called by : xxxx0000 
.         keyin clinic type ranges
.*********************************************************************
CTYP0000  COMPARE   TWO,CCITEM
          GOTO      CTYP2000 IF EQUAL       * change to
.
.         enter the from code
.
          MOVE      ZERO,CKYIMAND           * mandatory ??
          MOVE      SP6,CKYIDEF6            * default value
          MOVE      "10",EVERT
          MOVE      "24",ECOL
          DISPLAY   *P24:10,*EL,*P24:11,*EL;
          CALL      KYOUTCTY
          COMPARE   TWO,EXIT
          GOTO      CTYP9100 IF EQUAL       * exitchar entered
.
          MOVE      OCTCTYP,CDFRCTYP        * from code
          MOVE      OCTCTYP,PRFRCTYP        * from print code
          MATCH     SP6,OCTCTYP
          IF        @EQUAL
            DISPLAY   *PECOL:EVERT,*V2LON,START
            MOVE      START,PRFRCTYP
          ELSE
            DISPLAY   *PECOL:EVERT,*V2LON,OCTCTYP,*HOFF,SP3,OCTDESC
          ENDIF
.
.         enter the to code
.
CTYP2000  MOVE      ZERO,CKYIMAND           * mandatory ??
          MOVE      CDFRCTYP,CKYIDEF6       * default value
          MOVE      "11",EVERT
          MOVE      "24",ECOL
          DISPLAY   *PECOL:EVERT,*EL;
          CALL      KYOUTCTY
          COMPARE   TWO,EXIT
          GOTO      CTYP9100 IF EQUAL       * exitchar entered
.
          MOVE      OCTCTYP,CDTOCTYP        * set the value
          MOVE      OCTCTYP,PRTOCTYP        * to   print code
          MATCH     SP6,OCTCTYP
          IF        @EQUAL
            DISPLAY   *PECOL:EVERT,*V2LON,FINISH
            MOVE      Z6,CDTOCTYP
            MOVE      FINISH,PRTOCTYP
          ELSE
            DISPLAY   *PECOL:EVERT,*V2LON,OCTCTYP,*HOFF,SP3,OCTDESC
          ENDIF
.
          MOVE      ZERO,EXIT
          MATCH     CDFRCTYP,CDTOCTYP
          GOTO      CTYP9999 IF NOT LESS    * end >= start so OK
.
          DISPLAY   *P1:24,*B,"End value must be after Start value.  ",*EL;
          CALL      HITENTER
          GOTO      CTYP0000
.
CTYP9100  MOVE      ONE,EXIT
CTYP9999  RETURN
+
.*********************************************************************
.                   SNID0000                    Called by : xxxx0000 
.         keyin session id ranges
.*********************************************************************
SNID0000  COMPARE   FOUR,CCITEM
          GOTO      SNID2000 IF EQUAL       * change to
.
.         enter the from code
.
          MOVE      ZERO,CKYIMAND           * mandatory ??
          MOVE      SP6,CKYIDEF6            * default value
          MOVE      "13",EVERT
          MOVE      "24",ECOL
          DISPLAY   *P24:13,*EL,*P24:14,*EL;
          CALL      KYOUTCLI
          COMPARE   TWO,EXIT
          GOTO      SNID9100 IF EQUAL       * exitchar entered
.
          PACK      OCACLIN,OCACLIN,SP6
          MOVE      OCACLIN,CDFRSNID        * from code
          MOVE      OCACLIN,PRFRSNID        * from print code
          MATCH     SP6,OCACLIN
          IF        @EQUAL
            DISPLAY   *PECOL:EVERT,*V2LON,START
            MOVE      START,PRFRSNID
          ELSE
            DISPLAY   *PECOL:EVERT,*V2LON,OCACLIN,*HOFF,SP3,OCADESC
          ENDIF
.
.         enter the to code
.
SNID2000  MOVE      ZERO,CKYIMAND           * mandatory ??
          MOVE      CDFRSNID,CKYIDEF6       * default value
          MOVE      "14",EVERT
          MOVE      "24",ECOL
          DISPLAY   *PECOL:EVERT,*EL;
          CALL      KYOUTCLI
          COMPARE   TWO,EXIT
          GOTO      SNID9100 IF EQUAL       * exitchar entered
.
          PACK      OCACLIN,OCACLIN,SP6
          MOVE      OCACLIN,CDTOSNID        * set the value
          MOVE      OCACLIN,PRTOSNID        * to print code
          MATCH     SP6,OCACLIN
          IF        @EQUAL
            DISPLAY   *PECOL:EVERT,*V2LON,FINISH
            MOVE      Z6,CDTOSNID
            MOVE      FINISH,PRTOSNID
          ELSE
            DISPLAY   *PECOL:EVERT,*V2LON,OCACLIN,*HOFF,SP3,OCADESC
          ENDIF
.
          MOVE      ZERO,EXIT
          MATCH     CDFRSNID,CDTOSNID
          GOTO      SNID9999 IF NOT LESS    * end >= start so OK
.
          DISPLAY   *P1:24,*B,"End value must be after Start value.  ",*EL;
          CALL      HITENTER
          GOTO      SNID0000
.
SNID9100  MOVE      ONE,EXIT
SNID9999  RETURN
+
.*********************************************************************
.*                  DATE0000                    Called by : ML2000   *
.*        Enter the date ranges                                      *
.*********************************************************************
DATE0000  COMPARE   EIGHT,CCITEM
          GOTO      DATE2000 IF EQUAL
.
.         enter starting date
.
DATE1000  MOVE      "19",CVERT              * row
          MOVE      "24",CCOL               * column
          UNPACK    SP6,CYEAR,CMON,CDAY     * no defaults
          MOVE      CCC,CCENT
          DISPLAY   *P24:19,*EL,*P24:20,*EL
.
          CALL      KEYDATE
          BRANCH    OVRCD,DATE9100          * nothing entered
          BRANCH    CQUEST,DATE1000
.
          PACK      CDFRDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",CDFRDATE           * set up start date
          CALL      PACDATE
          MOVE      CPCDATE,PRFRDATE        * formatted start date
.
.         enter ending date (default to starting date)
.
DATE2000  MOVE      "20",CVERT              * row
          MOVE      "24",CCOL               * column
          UNPACK    CDFRDATE,CCENT,CYEAR,CMON,CDAY
.
          CALL      KEYDATE
          BRANCH    OVRCD,DATE0000          * nothing entered
          BRANCH    CQUEST,DATE2000
.
          PACK      CDTODATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",CDTODATE           * set up end date
          CALL      PACDATE
          MOVE      CPCDATE,PRTODATE        * formatted end date
.
          MATCH     CDFRDATE,CDTODATE
          GOTO      DATE3000 IF NOT LESS    * date is <= start date
.
          DISPLAY   *P1:24,*B,"Ending date must be after Starting date.  ",*EL;
          CALL      HITENTER 
          GOTO      DATE2000
.
.         check end date is before current date
.
DATE3000  MOVE      ZERO,EXIT
          GOTO      DATE9999
DATE9100  MOVE      ONE,EXIT
DATE9999  RETURN
+
.*********************************************************************
.         process the temp file
.*********************************************************************
PTMP0000  CALL      CLTV0000                * clear temp count vars
          MOVE      OTOSCTYP,XCTYPE         * clinic type
          MOVE      OTOSSESS,XSESSID        * session id
          MOVE      OTOSCLID,XCLINID        * clinic id
          MOVE      OBADAY,XSDAY            * day
          MOVE      OTOSHOSP,XHOSP          * hospital
          MOVE      OTOSDATE,XSDATE         * date
.
. afternoon or morning session
.
          UNPACK    OTOSTIME,HOUR
          MATCH     MIDDAY,HOUR
          IF        @LESS
            MOVE      ONE,AMPMIND           * morning
          ELSE
            MOVE      TWO,AMPMIND           * afternoon
          ENDIF
.
          PACK      KEY31,XCTYPE,XSESSID,XCLINID,XSDAY,XHOSP,AMPMIND,XSDATE
          CALL      RDTEMPA
          MOVE      OVRCD,TOVRCD            * save ovrcd value
.
. ******* see what type of slot it is : BOOKED - VACANT *******
.
          MATCH     ZEROVISN,OBAOUTNO
          IF        @EQUAL
            MOVE      TWO,SLOTTYPE            * set as vacant
          ELSE
            MOVE      ONE,SLOTTYPE            * set as booked
          ENDIF
.
.         see what type of visit (vistype)
.          1 = new routine, 2 = new urgent, 3 = star, 4 = review, 5 = extra bkd
.
          MOVE      ZERO,VISTYPE            * clear visit type
          UNPACK    SP3,TCINDC1,TCINDC2,TCINDC3
          PACK      KEY5,CATCV,OBAVISIT
          CALL      RDCODE1
.
          REP       "N1R4",TCINDC1
          MOVE      TCINDC1,VISTYPE         * set visit type
.
.         if a new visit type, see if routine or urgent
.
          IF        VISTYPE = ONE
            MATCH     "1",TCINDC2
            IF        @EQUAL
              MOVE      TWO,VISTYPE           * set as urgent new
            ENDIF
          ENDIF
.
.         see if a star patient
.
          MATCH     ANSS,TCINDC2
          IF        @EQUAL
            MOVE      THREE,VISTYPE           * set as star
          ENDIF
.
.         override if extra booked and slot is not vacant
.
          MATCH     ANSE,TCINDC3            * extra visit type
          IF        @EQUAL & SLOTTYPE = ONE
            MOVE      FIVE,VISTYPE            * set as extra booked
          ENDIF
.
.         keep a count of invalid records
.
          IF        VISTYPE = ZERO | SLOTTYPE = ZERO
            ADD       ONE,ERRCNT
          ENDIF
.
. ******* work out the correct value to update *******
.
          IF        SLOTTYPE = ONE
            LOAD      FORM4,VISTYPE,XBKNWRT,XBKNWUR,XBKSTAR,XBKREVW,XBKEXTR
            ADD       ONE,FORM4
            STORE     FORM4,VISTYPE,XBKNWRT,XBKNWUR,XBKSTAR,XBKREVW,XBKEXTR
            ADD       ONE,XBKTOTL
          ENDIF
.
          IF        SLOTTYPE = TWO
            LOAD      FORM4,VISTYPE,XVCNWRT,XVCNWUR,XVCSTAR,XVCREVW
            ADD       ONE,FORM4
            STORE     FORM4,VISTYPE,XVCNWRT,XVCNWUR,XVCSTAR,XVCREVW
            ADD       ONE,XVCTOTL
          ENDIF
.
. ******* post to temp file *******
.
          IF        TOVRCD = ONE
            CALL      WRTEMPA
          ELSE
            CALL      UPTEMPA
          ENDIF
.
PTMP9999  RETURN
+
.*********************************************************************
.         clear the temp file variables
.*********************************************************************
CLTV0000  MOVE      ZERO,XBKNWRT
          MOVE      ZERO,XBKNWUR
          MOVE      ZERO,XBKSTAR
          MOVE      ZERO,XBKREVW
          MOVE      ZERO,XBKEXTR
          MOVE      ZERO,XBKTOTL
          MOVE      ZERO,XVCNWRT
          MOVE      ZERO,XVCNWUR
          MOVE      ZERO,XVCSTAR
          MOVE      ZERO,XVCREVW
          MOVE      ZERO,XVCTOTL
CLTV9999  RETURN
+
.*********************************************************************
.         get the print information
.*********************************************************************
GPIN0000  MOVE      XSDAY,FORM1
          LOAD      TEMPDAY,FORM1,MON,TUE,WED,THU,FRI,SAT,SUN
          IF        AMPMIND = 1
            PACK      DDAY,TEMPDAY,AM
          ELSE
            PACK      DDAY,TEMPDAY,PM
          ENDIF
          UNPACK    XSDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PACK      KEY20,IDDD,XSESSID,XSDATE
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     IDDD,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     XSESSID,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          IF        OVRCD = 1
            MOVE      "Unknown clinic",OCADESC
            PACK      OCADESC,OCADESC,SP30
          ENDIF
          MOVE      OCADESC,KEY33
          BRANCH    OVRCD,GPIN9999
.
          MATCH     SP6,OCADOCT
          GOTO      GPIN9500 IF EQUAL       * no doctor code
.
          MOVE      "Invalid Doctor Code",KEY33
          MOVE      OCADOCT,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD,GPIN9999
          MOVE      DSNAME,PACSNAME
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,KEY33
.
.         add the clinic code
.
GPIN9500  RESET     KEY33,22
          APPEND    SP1,KEY33
          APPEND    XCLINID,KEY33
          APPEND    SP30,KEY33
          RESET     KEY33
.
GPIN9999  RETURN
+
.*********************************************************************
.         print a line of the report
.*********************************************************************
PLIN0000  DISPLAY   *P41:24,XCTYPE,XSESSID,XCLINID,XSDAY,XHOSP,XSDATE
          MATCH     CURRCTYP,XCTYPE           * different clinic type ??
          IF        !@EQUAL
            MOVE      "70",CLNO               * set for a new page
          ENDIF
.
          MATCH     CURRSNID,XSESSID          * diffent session id ??
          IF        !@EQUAL
            MOVE      "70",CLNO               * set for a new page
          ENDIF
.
          MATCH     CURRCLIN,XCLINID          * diffent clinic id ??
          IF        !@EQUAL
            MOVE      "70",CLNO               * set for a new page
          ENDIF
.
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          CALL      GPIN0000                * get the print info
.
. ******* see if to print all the little bits *******
.
          MATCH     CURRSNID,XSESSID
          GOTO      PLIN1000 IF NOT EQUAL   * diff. session id
          MATCH     CURRCLIN,XCLINID
          GOTO      PLIN1000 IF NOT EQUAL   * diff. clinic id
          MATCH     CURRDAY,XSDAY
          GOTO      PLIN1000 IF NOT EQUAL   * diff. day
          MATCH     CURRHOSP,XHOSP
          GOTO      PLIN1000 IF NOT EQUAL   * diff. hospital
          COMPARE   CURRAMPM,AMPMIND
          GOTO      PLIN1000 IF NOT EQUAL   * diff. AM/PM Session
          MATCH     CURRDATE,XSDATE
          GOTO      PLIN1500 IF NOT EQUAL   * diff. date
          GOTO      PLIN8000
.
.         print the whole first line details
.
PLIN1000  PERFORM   PRCLUNLN,UND132
          PRINT     *1,"| ",KEY33," ",DDAY,"  ",XHOSP,SP1:
                    "|",SP6,"|",SP6,"|",SP6:
                    "|",SP6,"|",SP7,"|",SP7:
                    "|",SP6,"|",SP6,"|",SP6:
                    "|",SP6,"|",SP7,"|"
          ADD       ONE,CLNO
          PRINT     *1,"|  (",XSESSID,")";
          GOTO      PLIN2000
.
PLIN1500  PRINT     *1,"|";
PLIN2000  PRINT     *37,CPDATE;
.
PLIN8000  PRINT     *52,"| ",XBKNWRT," | ",XBKNWUR," | ",XBKSTAR:
                        " | ",XBKREVW," | ",XBKEXTR,"  | ",XBKTOTL:
                        " | ",XVCNWRT," | ",XVCNWUR," | ",XVCSTAR:
                        " | ",XVCREVW," | ",XVCTOTL," |"
          ADD       ONE,CLNO
          MOVE      XCTYPE,CURRCTYP
          MOVE      XSESSID,CURRSNID
          MOVE      XCLINID,CURRCLIN
          MOVE      XSDAY,CURRDAY 
          MOVE      XHOSP,CURRHOSP
          MOVE      AMPMIND,CURRAMPM
          MOVE      XSDATE,CURRDATE
          MOVE      ONE,PRCLUNLN
.        
PLIN9999  RETURN
+
.*********************************************************************
.         print new page
.*********************************************************************
HEAD0000  CALL      HEAD132
          PRINT     *40,"Clinic Type : ",PRFRCTYP,"     to ",PRTOCTYP
          PRINT     *40,"Session Id  : ",PRFRSNID,"     to ",PRTOSNID
          PRINT     *40,"Clinic  Id  : ",PRFRCLIN,"     to ",PRTOCLIN
          PRINT     *40,"Dates       : ",PRFRDATE," to ",PRTODATE
          MOVE      SEVEN,CLNO
          CALL      TBLE0000
HEAD9999  RETURN
+
.*********************************************************************
.         print table heading
.*********************************************************************
TBLE0000  MOVE      "Invalid Code",OCTDESC
          PACK      KEY12,IDDD,XCTYPE
          CALL      RDCTYA1
          PRINT     *N,"Clinic Type : ",XCTYPE,SP2,OCTDESC
.
          CALL      UND132
          PRINT     *1,"|",*52,"|":
                    " ------------ B O O K I N G S ------------ |":
                    " ------- V A C A N C I E S ------- |"
          PRINT     *1,"|",*52,"|":
                    " --- New --- | Star | Rev  | Over  | TOTAL |":
                    " --- New --- | Star | Rev  | TOTAL |"
          PRINT     *1,"| Session                Clinic Id  Day/Date   Hos |":
                    " Rout | Urg. |      |      | Book  |       |":
                    " Rout | Urg. |      |      |       |"
          CALL      UND132
          ADD       THREE,CLNO
          MOVE      ZERO,PRCLUNLN
          MOVE      "******",CURRSNID       * print detail line again
.
TBLE9999  RETURN
+
.*********************************************************************
.         Create a temp file
.*********************************************************************
CTPA0000  CALL      DTPA0000                * delete temp file
          CLEAR     CMDLINE                 * clear command line
          PACK      CMDLINE,ISBUILD,CFNAMEA,KEYA,SP30      * set command
          RESET     CMDLINE                 * reset FP
          EXECUTE   CMDLINE,TASKID          * build the temp file
          OPEN      TEMPA,CFNAMEA           * open the temp file
CTPA9999  RETURN
+
.*********************************************************************
.         Delete temp file A
.*********************************************************************
DTPA0000  CLOSE     TEMPA                   * close temp file
          CLEAR     CMDLINE                 * clear command line
          PACK      CMDLINE,ISERASE,CFNAMEA * set command line for erase
          RESET     CMDLINE                 * reset FP
          EXECUTE   CMDLINE,TASKID          * delete the temp file
DTPA9999  RETURN
+
.*********************************************************************
.         temp file I/O
.*********************************************************************
RSTEMPA   READ      TEMPA,KEY31;;
          RETURN
.
RDTEMPA   MOVE      ZERO,OVRCD
          READ      TEMPA,KEY31;XCTYPE,XSESSID,XCLINID,XSDAY,XHOSP,XSDATE:
                                DXBKNWRT,DXBKNWUR,DXBKSTAR,DXBKREVW:
                                DXBKEXTR,DXBKTOTL:
                                DXVCNWRT,DXVCNWUR,DXVCSTAR,DXVCREVW,DXVCTOTL:
                                DAMPMIND
          GOTO      OVERCOND IF OVER
          MOVE      DXBKNWRT,XBKNWRT
          MOVE      DXBKNWUR,XBKNWUR
          MOVE      DXBKSTAR,XBKSTAR
          MOVE      DXBKREVW,XBKREVW
          MOVE      DXBKEXTR,XBKEXTR
          MOVE      DXBKTOTL,XBKTOTL
          MOVE      DXVCNWRT,XVCNWRT
          MOVE      DXVCNWUR,XVCNWUR
          MOVE      DXVCSTAR,XVCSTAR
          MOVE      DXVCREVW,XVCREVW
          MOVE      DXVCTOTL,XVCTOTL
          MOVE      DAMPMIND,AMPMIND
          RETURN
.
RKTEMPA   MOVE      ZERO,OVRCD
          READKS    TEMPA;XCTYPE,XSESSID,XCLINID,XSDAY,XHOSP,XSDATE:
                          DXBKNWRT,DXBKNWUR,DXBKSTAR,DXBKREVW:
                          DXBKEXTR,DXBKTOTL:
                          DXVCNWRT,DXVCNWUR,DXVCSTAR,DXVCREVW,DXVCTOTL:
                          DAMPMIND
          GOTO      OVERCOND IF OVER
          MOVE      DXBKNWRT,XBKNWRT
          MOVE      DXBKNWUR,XBKNWUR
          MOVE      DXBKSTAR,XBKSTAR
          MOVE      DXBKREVW,XBKREVW
          MOVE      DXBKEXTR,XBKEXTR
          MOVE      DXBKTOTL,XBKTOTL
          MOVE      DXVCNWRT,XVCNWRT
          MOVE      DXVCNWUR,XVCNWUR
          MOVE      DXVCSTAR,XVCSTAR
          MOVE      DXVCREVW,XVCREVW
          MOVE      DXVCTOTL,XVCTOTL
          MOVE      DAMPMIND,AMPMIND
          RETURN
.
UPTEMPA   MOVE      XBKNWRT,DXBKNWRT
          MOVE      XBKNWUR,DXBKNWUR
          MOVE      XBKSTAR,DXBKSTAR
          MOVE      XBKREVW,DXBKREVW
          MOVE      XBKEXTR,DXBKEXTR
          MOVE      XBKTOTL,DXBKTOTL
          MOVE      XVCNWRT,DXVCNWRT
          MOVE      XVCNWUR,DXVCNWUR
          MOVE      XVCSTAR,DXVCSTAR
          MOVE      XVCREVW,DXVCREVW
          MOVE      XVCTOTL,DXVCTOTL
          MOVE      AMPMIND,DAMPMIND
          UPDATE    TEMPA;XCTYPE,XSESSID,XCLINID,XSDAY,XHOSP,XSDATE:
                          DXBKNWRT,DXBKNWUR,DXBKSTAR,DXBKREVW:
                          DXBKEXTR,DXBKTOTL:
                          DXVCNWRT,DXVCNWUR,DXVCSTAR,DXVCREVW,DXVCTOTL:
                          DAMPMIND
          RETURN
.
WRTEMPA   MOVE      XBKNWRT,DXBKNWRT
          MOVE      XBKNWUR,DXBKNWUR
          MOVE      XBKSTAR,DXBKSTAR
          MOVE      XBKREVW,DXBKREVW
          MOVE      XBKEXTR,DXBKEXTR
          MOVE      XBKTOTL,DXBKTOTL
          MOVE      XVCNWRT,DXVCNWRT
          MOVE      XVCNWUR,DXVCNWUR
          MOVE      XVCSTAR,DXVCSTAR
          MOVE      XVCREVW,DXVCREVW
          MOVE      XVCTOTL,DXVCTOTL
          MOVE      AMPMIND,DAMPMIND
          WRITE     TEMPA,KEY31;XCTYPE,XSESSID,XCLINID,XSDAY,XHOSP,XSDATE:
                                DXBKNWRT,DXBKNWUR,DXBKSTAR,DXBKREVW:
                                DXBKEXTR,DXBKTOTL:
                                DXVCNWRT,DXVCNWUR,DXVCSTAR,DXVCREVW,DXVCTOTL:
                                DAMPMIND
          RETURN
+
          INC       STD001IO
          INC       KYOUTCLI
          INC       KYOUTCTY
          INC       IBASEQIO/INC
          INC       OUTDSCLN
          INC       OUTDSCTY
          INC       OUTBOAIO/INC
          INC       OUTCLIIO/INC
          INC       OUTCTYIO/INC
          INC       OUTOSFIO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PMSHCPIO/INC            * National HCP file
          INC       TFILENAM
          INC       WEBERRIO/INC

.............................................................................
.*********************************************************************
.                   CLIN0000
.         keyin clinic id ranges
.*********************************************************************
CLIN0000  COMPARE   FOUR,CCITEM
          GOTO      CLIN2000 IF EQUAL       * change to
.
.         enter the from code
.
          MOVE      ZERO,CKYIMAND           * mandatory ??
          MOVE      SP6,CKYIDEF6            * default value
          MOVE      "16",EVERT
          MOVE      "24",ECOL
          DISPLAY   *P24:16,*EL,*P24:17,*EL;
          CALL      KYOUTCLI
          COMPARE   TWO,EXIT
          GOTO      CLIN9100 IF EQUAL       * exitchar entered
.
          PACK      OCACLIN,OCACLIN,SP6
          MOVE      OCACLIN,CDFRCLIN        * from code
          MOVE      OCACLIN,PRFRCLIN        * from print code
          MATCH     SP6,OCACLIN
          IF        @EQUAL
            DISPLAY   *PECOL:EVERT,*V2LON,START
            MOVE      START,PRFRCLIN
          ELSE
            DISPLAY   *PECOL:EVERT,*V2LON,OCACLIN,*HOFF,SP3,OCADESC
          ENDIF
.
.         enter the to code
.
CLIN2000  MOVE      ZERO,CKYIMAND           * mandatory ??
          MOVE      CDFRCLIN,CKYIDEF6       * default value
          MOVE      "17",EVERT
          MOVE      "24",ECOL
          DISPLAY   *PECOL:EVERT,*EL;
          CALL      KYOUTCLI
          COMPARE   TWO,EXIT
          GOTO      CLIN9100 IF EQUAL       * exitchar entered
.
          PACK      OCACLIN,OCACLIN,SP6
          MOVE      OCACLIN,CDTOCLIN        * set the value
          MOVE      OCACLIN,PRTOCLIN        * to print code
          MATCH     SP6,OCACLIN
          IF        @EQUAL
            DISPLAY   *PECOL:EVERT,*V2LON,FINISH
            MOVE      Z6,CDTOCLIN
            MOVE      FINISH,PRTOCLIN
          ELSE
            DISPLAY   *PECOL:EVERT,*V2LON,OCACLIN,*HOFF,SP3,OCADESC
          ENDIF
.
          MOVE      ZERO,EXIT
          MATCH     CDFRCLIN,CDTOCLIN
          GOTO      CLIN9999 IF NOT LESS    * end >= start so OK
.
          DISPLAY   *P1:24,*B,"End value must be after Start value.  ",*EL;
          CALL      HITENTER
          GOTO      CLIN0000
.
CLIN9100  MOVE      ONE,EXIT
CLIN9999  RETURN
+

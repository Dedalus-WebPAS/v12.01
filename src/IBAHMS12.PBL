.******************************************************************************
.* System   : Patient Management System                                       *
.* Program  : IBAHMS12                                                        *
.* Desc     : 3B Certificate Expiry Date Maintenance                          *
.******************************************************************************
.* Date     :  16/09/1991                                                     *
.* Author   :  Graeme Williams                                                *
.* Mods     :                                                                 *
.******************************************************************************
.* V12.00.02 15/07/2025 Bella Turco     TSK 0961623                           *
.*           Packed AADMNO with SP70 instead of ZEROVISN in KADM0000          *
.* V12.00.01 22/05/2025 Thanh T         TSK 0955096                           *
.*           Changed for alphanumeric visit number                            *
.******************************************************************************
.*          V10.06.01 12/03/2015  Steve Armstrong  CAR 314108                 *
.*                    Removed definition of PTCGDATE as PATCGPFD is no longer *
.*                    in use.                                                 *
.******************************************************************************
.*          V10.03.01 30/06/2013  Davin             CAR 283202                *
.*                    Recompiled for changes to hl7 messaging                 *
.*          V10.02.01 07/07/2011  Steve Armstrong  CAR 240722                 *
.*                    Added RDPMPX21 prior to calling PMIHEAD                 *
.******************************************************************************
.*          V10.00.01 19/03/2010  Steve Armstrong   CAR 135505                *
.*                    Added HL7TRGID & HL7INCLD setting for all broadcast     *
.*                    messages                                                *
.******************************************************************************
.*          V9.02.01  10/02/2003  Lina Vo                                     *
.*                    Remove open of outpreaf - not used.                     *
.*          V9.02.00  22/07/2002  Dean Cramer                                 *
.*                    Port from 5.10 and make necessary changes for compile.  *
.*          V5.10.B01 26/03/2001  Glenn Saunders                              *
.*                    Remove access to PTCNSNDX and associated processing.    *
.*                    Remove all references to PATSUR file (old surname file).*
.*          V5.08.01  21/08/2000  Caleb Sharman                               *
.*                    Changed Health Fund variables to be 8 chars             *
.*          V5.07.01  02/08/1999  Caleb Sharman                               *
.*                    Recompiled for DG CLI files                             *
.*          V5.05.01  15/10/1997  Steve Armstrong                             *
.*                    Recompiled for DGCLIUPM                                 *
.******************************************************************************
.*          V5.04.01  14/01/1997  Steve Armstrong                             *
.*                    Mods for Datagate API's                                 *
.******************************************************************************
.*             V5.01.002 08/04/92 J.Tan     SRF 113924                *
.*             Fixed I*C patward index four                           *
.*             V5.01.003 22/04/93 DIG                                 *
.*                       Re-Compiled for changes to PATALERT.         *
.*             V5.01.004 25/08/93  ROD                                *
.*                       Re-Compiled for NZHIS                        *
.*             V5.01.005 13.01.94  S.Barcham                          *
.*                       Add the patnidaf file                        *
.*             V5.01.006 25/05/1994 Ian Rutt                          *
.*                       Fixed Global Recompile Bugs                  * 
.**********************************************************************
.
.$$$$$
.    3B Certificate Expiry Date Maintenance ( IBAHMS12 )
.    ---------------------------------------------------
.
.    - Initialisation
.
.    - Display Header
.            - Open Files
.               PATCODE1
.               CONTROLF
.
.    - Processing Options
.
.            - 0 = Exit
.            - 1 = Insert
.
.                - Clear Input Variables
.                - Display Screen Format
.                - Input Key Details
.                  (Claim code, health fund & table, and admission type
.                   "?" options available)
.                     - Read PATHCPA1, check if already exists
.                     - If Not, Continue...
.                - Accept Field Values 
.                             - Priority
.                             - Medical ?
.                - Select Item, Post or Cancel
.                - If Post then Write Data to PATHCPA1
.
.            - 2 = Change
.
.                - Clear Input Variables
.                - Display Screen Format
.                - Input Key Details
.                  (Claim code, health fund & table, and admission type
.                   "?" options available)
.                     - Read PATHCPA1, check if exists
.                     - If Exists, Continue...
.                - Accept Field Values 
.                             - Priority
.                             - Medical ?
.                - Select Item, Post or Cancel
.                - If Post then Update Data to PATHCPA1
.
.    - END
.$$$$$
.
          INC       STD001FD
          INC       NHIMASFD/INC                 * Include for NZHIS vars
          INC       NZHISIFD/INC                 * Include for NZHIS vars
          INC       PATCALAG/INC
          INC       PATCONFD/INC
          INC       PATCOMM/INC
          INC       PATDHEAD/INC
          INC       BOKRC1FD/INC           * bookings file
          INC       OUTPREFD/INC           * outpatient pre-attendance file
          INC       PATALRFD/INC           * patient alerts file
          INC       PATCODFD/INC           * patient codes file
          INC       PATDO1FD/INC           * doctors file
          INC       PATDSCFD/INC           * discharge file
          INC       PATGSRFD/INC           * Surname / Given Name File
          INC       PATMA1FD/INC           * patient master file
          INC       PATMI1FD/INC           * admissions file
          INC       PATNIDFD/INC           * national id number fil
          INC       PATNOBFD/INC           * no-bed ward file
          INC       PATPR1FD/INC           * 0 U/R PMI file
          INC       PATVISFD/INC           * visit file
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC           * visit extention file
          INC       PATWR1FD/INC           * ward/bed file
+
.**********************************************************************
.*                           CONSTANTS                                *
.**********************************************************************
.
DOPT1     INIT      "By Admission No. "
DOPT2     INIT      "By U/R Number "
.
.
Z30       INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
+
.**********************************************************************
.*                       GLOBAL VARIABLES                             *
.**********************************************************************
.
BJDAY     FORM      3
CJDAY     FORM      3
DISPOPTN  DIM       16
FRSTGNAM  DIM       40
FULLGNAM  DIM       80
NMPNUMB   DIM       20
SCNDGNAM  DIM       40
.
PRGID     INIT      "IBAHMS12"
PRGNAM    INIT      "3B Certificate Expiry Maintenance"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                           MAINLINE                                 *
.**********************************************************************
.
ML0000    CALL      INIT0000               * initialization and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    COPTION,ML1000,ML2000
          GOTO      ML9999                 * Exit selected
.
. ---- By admission number ----
.
ML1000    CALL      CLR0000                * clear input variables
          CALL      SCR0000                * display screen format
          CALL      KADM0000               * input admission number
          BRANCH    EXIT,ML0100            * nothing input
.
          CALL      DISP0000               * display details
          CALL      SEL0000                * select field to modify
.                                          * or ok to post
          BRANCH    EXIT,ML1000
          CALL      UPD0000                * update record
          GOTO      ML1000
.
. ---- By U/R number ----
.
ML2000    CALL      CLR0000                * clear input variables
          CALL      SCR0000                * display screen format 
          CALL      KUR0000                * input U/R number
          BRANCH    EXIT,ML0100 
.
          CALL      DISP0000               * display details
          CALL      SEL0000                * select field to modify
.                                          * or ok to post
          BRANCH    EXIT,ML2000
          CALL      UPD0000                * update record
          GOTO      ML2000
.
ML9999    CHAIN     PGM                    * chain back to program
+
.**********************************************************************
.*                             INIT0000                               *
.*                      Initialization  Routine                       *
.**********************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,TEN9;*75,CCNOTES
          READ      CONTROLF,FIFTY9;*132,CALRDESC,*231,PTCNRGNS
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patpramf";
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patnobef";
          OPEN      PATNOBE1,"patnobef"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATWR1A3,"patwr1af"
          OPEN      PATWR1A4,"patwr1af"
.
INIT2000  DISPLAY   *P64:24,"patgsrnf";
          OPEN      PATGSRN2,"patgsrnf"
          OPEN      PATGSRN3,"patgsrnf"
          OPEN      PATGSRN4,"patgsrnf"
.
INIT4000  DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA1,"patvisaf"
          OPEN      PATVISA2,"patvisaf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          MOVE      ONE,CNEWDS               * new style "?" options
.
INIT9999  RETURN
.
+
.**********************************************************************
.*                             CLR0000                                *
.*                 Routine to Clear Local Variables                   *
.**********************************************************************
.
CLR0000   
          RETURN
+
.****************************************************************************
.*                                OPTN0000                                  *
.*                        get user options or exit                          *
.*                                                                          *
.*    valid options     --->  (0) Exit                                      *
.*                            (1) Add                                       *
.*                            (2) Modify                                    *
.*                            (3) Delete                                    *
.****************************************************************************
.
. ---- display option screen ----
.
OPTN0000  HLINE     *G37,2,57,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = By Admission number":
                    *P1:6,*V2LON,TWO,*HOFF," = By U/R number"
.
OPTN1000  KEYIN     *P1:8,"Select Option : ",*EL:
                    *P17:8,*DV,UNDLN2:
                    *P17:8,*V2LON,COPTION:
                    *P17:8,*DV,COPTION
.
.         Validate selection
.
          BRANCH    COPTION,OPTN8000,OPTN8000
.
          COMPARE   ZERO,COPTION
          GOTO      OPTN9999 IF EQUAL              * exit      
.
          BEEP
          GOTO      OPTN1000
.
OPTN8000  LOAD      DISPOPTN,COPTION,DOPT1,DOPT2
          DISPLAY   *P57:2,*V2LON,"- ",DISPOPTN
.
OPTN9999  RETURN
+
.**********************************************************************
.*                            SCR0000                                 *
.*                     Main Data Screen Display                       *
.**********************************************************************
.
SCR0000   BRANCH    COPTION,SCR1000,SCR2000
.
SCR1000   DISPLAY   *P1:3,*EF:
                    *P1:4,"Admission No :"
          GOTO      SCR9999
.
SCR2000   DISPLAY   *P1:3,*EF:
                    *P1:4,"U/R Number   :"
.
SCR9999   RETURN
.
REDISPS   CALL      SCR0000                  * label for KURN redisplays
          RETURN
.**********************************************************************
.*                           KADM0000                                 *
.*                      Input admission number                        *
.**********************************************************************
KADM0000  KEYIN     *P16:4,*EL,*DV,UNDLN8:
                    *P16:4,*V2LON,AADMNO
.
          PACK      AADMNO,AADMNO,SP70
          MATCH     SP70,AADMNO              * was anything input ?
          GOTO      KADM9900 IF EQUAL        * no.
.
          DISPLAY   *P16:4,*V2LON,AADMNO     * clear underlines
.
          MOVE      AADMNO,KEY8              * key to admission file
          CALL      RDMISS1                  * valid admission record ?
          BRANCH    OVRCD,KADM9000           * no.
.
          MOVE      AADMNO,KEY8              * key to admission file
          CALL      RDVISA1                  * valid visit record ?
          BRANCH    OVRCD,KADM9000           * no.
.
.         Check that this is a current admission
.
          BRANCH    ASTAT,KADM9100,KADM1000,KADM9200,KADM1000,KADM9300
.
.         Get the PMI details
.
KADM1000  MOVE      AURNO,KEY8               * key to master file
          CALL      RLPTMAS1                 * get the PMI details
          BRANCH    OVRCD,KADM9400,KADM9500  * missing PMI details
.
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          BRANCH    OVRCD,KADM9400
.
          MOVE      ZERO,EXIT                * we have a valid patient
          GOTO      KADM9999
.
.         Error messages
.
KADM9000  DISPLAY   *P1:24,*EL,*B,"Invalid admission number. ";
          CALL      HITENTER
          GOTO      KADM0000
.
KADM9100  DISPLAY   *P1:24,*EL,*B,"Patient is a pre-admission. ";
          CALL      HITENTER
          GOTO      KADM0000
.
KADM9200  DISPLAY   *P1:24,*EL,*B,"Patient has been discharged. ";
          CALL      HITENTER
          GOTO      KADM0000
.
KADM9300  DISPLAY   *P1:24,*EL,*B,"Admission has been cancelled. ";
          CALL      HITENTER
          GOTO      KADM0000
.
KADM9400  DISPLAY   *P1:24,*EL,*B,"Missing PMI details. Please contact IBA. ";
          CALL      HITENTER
          GOTO      KADM0000
.
KADM9500  DISPLAY   *P1:24,*EL,*B,"Patient U/R number locked on port ",PPORT:
                    ". ";
          CALL      HITENTER
          GOTO      KADM0000
.
KADM9900  MOVE      ONE,EXIT                 * nothing input
.
KADM9999  RETURN
.**********************************************************************
.*                           KUR0000                                 *
.*                      Input admission number                        *
.**********************************************************************
KUR0000   MOVE      TEN6,CCOL                * column for keyin
          MOVE      FOUR,CVERT               * row for keyin
          MOVE      TWO,SRCHOPT              * display address in surn. search
          MOVE      ZERO,SRCHSYS             * display all 0 U/R numbers
          CALL      KURN                     * keyin the U/R number
          BRANCH    EXIT,KUR9999             * nothing input
          BRANCH    OVRCD,KUR9000            * invalid U/R number
.
.         We have the U/R number. Now check if this U/R number is current
.         inpatient.
.
          PACK      KEY24,PURNO,Z30          * position in visit file
          CALL      RDSVISA2                 * position in visit file
KUR1000   CALL      RDPVISA2                 * get the next record
          BRANCH    OVRCD,KUR9100            * not a current inpatient
.
          MATCH     PVIURNO,PURNO            * still correct patient ?
          GOTO      KUR9100 IF NOT EQUAL     * no, not a current inpatient
.
          COMPARE   THREE,PVITYPE            * inpatient visit ?
          GOTO      KUR1000 IF NOT EQUAL     * no, ignore record
.
          MOVE      PVIBILL,KEY8             * key to admission file
          CALL      RDMISS1                  * check for an admission record
          BRANCH    OVRCD,KUR1000            * no record, try again.
.
.         Check if this admission is a current inpatient. Ignore it if not.
.
          BRANCH    ASTAT,KUR1000,KUR8000,KUR1000,KUR8000,KUR1000
.
.         Lock U/R number
.
KUR8000   MOVE      PURNO,KEY8
          CALL      RLPTMAS1                 * read and lock U/R number
          BRANCH    OVRCD,KUR9000,KUR9200
.
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          BRANCH    OVRCD,KUR9000
.
          MOVE      ZERO,EXIT                * we have a valid patient
          GOTO      KUR9999
.
.         Error messages
.
KUR9000   DISPLAY   *P1:24,*EL,*B,"Invalid U/R number. ";
          CALL      HITENTER
          GOTO      KUR0000
.
KUR9100   DISPLAY   *P1:24,*EL,*B,"This patient is not a current inpatient. ";
          CALL      HITENTER
          GOTO      KUR0000
.
KUR9200   DISPLAY   *P1:24,*EL,*B,"Patient U/R number locked on port ",PPORT:
                    ". ";
          CALL      HITENTER
          GOTO      KADM0000
.
KUR9999   RETURN
.
.**********************************************************************
.*                           EXPY0000                                 *
.*                      Input expiry date                             *
.**********************************************************************
EXPY0000  MOVE      EIGHT,CVERT              * row for keyin
          MOVE      "33",CCOL                * column for keyin
          UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
.
          CALL      KEYDATE
          BRANCH    OVRCD,EXPY9000
.
          PACK      PTMX3BEX,CCENT,CYEAR,CMON,CDAY
          REP       " 0",PTMX3BEX
.
.         Date must be after the admission date
.
          MATCH     PVIDATE,PTMX3BEX
          GOTO      EXPY9999 IF NOT LESS
.
          UNPACK    PVIDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          DISPLAY   *P1:24,*EL,*B,"Date must not be before admission (":
                    *V2LON,CPCDATE,*HOFF,"). ";
          CALL      HITENTER
          GOTO      EXPY0000
.
.         Nothing input
.
EXPY9000  DISPLAY   *P32:8,*EL;
.
EXPY9999  RETURN
+
.**************************************************************************
.*                                   SEL0000                              *
.*                         select field to modify or post                 *
.**************************************************************************
.
SEL0000   CALL      MAINQST
          COMPARE   ZERO,CCITEM              * ok to post
          GOTO      SEL8000 IF EQUAL
          COMPARE   "-1",CCITEM              * cancel option
          GOTO      SEL9000 IF EQUAL
          BRANCH    CCITEM,SEL1000
          BEEP
          GOTO      SEL0000
.
SEL1000   CALL      EXPY0000                 * keyin expiry date
          GOTO      SEL0000
.
.         (P)ost selected
.
SEL8000   MOVE      ZERO,EXIT                * set exit
          GOTO      SEL9999
.
.         (C)ancel selected
.
SEL9000   MOVE      PURNO,KEY8
          CALL      UUPTMAS1                 * unlock master file record
.
          MOVE      ONE,EXIT                 * set exit
.
SEL9999   RETURN
+
.**************************************************************************
.*                               DISP0000                                 *
.*                   Display Data from the parameter file                 *
.**************************************************************************
.
DISP0000  CALL      PATHEAD                  * display the standard heading
          DISPLAY   *P1:8,*V2LON,ONE,*HOFF,". 3B Certificate Expiry Date : "
.
          UNPACK    PTMX3BEX,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P33:8,*V2LON,CPCDATE
.
DISP9999  RETURN
.**************************************************************************
.*                               KCLR0000                                 *
.*                   Clear keyin fields from the screen                   *
.**************************************************************************
.
KCLR0000  DISPLAY   *P33:8,*EF
.
KCLR9999  RETURN
+
.**************************************************************************
.*                              UPD0000                                   *
.*                   Update Data in the parameter file                    *
.**************************************************************************
UPD0000   DISPLAY   *P1:24,*EL,"** Posting **";
.
          CALL      UPMAST1                  * update and unlock file
.
          CALL      PMIGTNID                 * get national id for dgate write
          MOVE      NMPNUMB,PTNINMPI
          MOVE      ONE,HL7TRGID
          MOVE      SP8,HL7INCLD
.....          PROC      DGCLIUPM                 * write PMI changes to Datagate
.
          DISPLAY   *P1:24,*EL;
.
UPD9999  RETURN
+
.********************************************************************** 
.*              I/O INCLUDES FOR FILES AND GENERAL ROUTINES           *
.**********************************************************************
.
GETSVAR  RETURN                              * dummy routine for PATCOMN1
.
         INC       STD001IO
         INC       CALCAGE
         INC       PATCOMN1
         INC       PATHEAD
         INC       PMIHEAD
         INC       PATALERT
         INC       PATSNDX
         INC       PATSNX2
         INC       BOKRC1IO/INC            * bookings file
         INC       PATALRIO/INC            * patient alerts file
         INC       PATCODIO/INC            * patient i/o for codes
         INC       PATDO1IO/INC            * doctors file
         INC       PATGSRIO/INC            * Patient Surname / Given Name
         INC       PATMA1IO/INC            * PMI file
         INC       PATMI1IO/INC            * admissions file
         INC       PATNIDIO/INC            * national id number file
         INC       PATNOBIO/INC            * no-bed ward file
         INC       PATPR1IO/INC            * 0 U/R PMI file
         INC       PATVISIO/INC            * visit file
         INC       PMSPX2IO/INC
         INC       PMSVX1IO/INC            * visit extention file
         INC       PATWR1IO/INC            * ward/bed file
         INC       OUTPREIO/INC            * outpatient pre-attendance file
         INC       NZCOMPIO/INC
         INC       NHIMASIO/INC
.....         INC       DGCLIUPM
         INC       PMIGTNID
AGECHK
CLPATMAS  RETURN

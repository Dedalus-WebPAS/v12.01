.***************************************************************************
.* System    :   Patient                                                   *
.* Program   :   IBAINP28                                                  *
.* Desc      :   EDI Resubmissions                                         *
.***************************************************************************
.* Date      :   21/07/1994                                                *
.* Author    :   Rob Leonard                                               *
.* Function  :   Allow hospitals to resubmit data to the National system   *
.*               via the EDI extraction program.                           * 
.* Mods      :                                                             *
.*                                                                         *
.* V10.12.01 05/07/2018  Don Nguyen       TSK 0859859                      *
.*           Added District Health Board to Options 2 & 3 (ML2000) - Reset *
.*           by Date Range.                                                *
.***************************************************************************
.* V10.02.01 07/07/2011  Steve Armstrong  CAR 240722                       *
.*           Added RDPMPX21 prior to calling PMIHEAD                       *
.***************************************************************************
.* V10.01.01 03/02/2011 Jill Parkinson CAR 233088                          *
.*           Added pmsvx1af                                                *
.***************************************************************************
.* V10.00.02 12/04/2010  Steve Armstrong    CAR 219111                     *
.*           Recompiled for changes to PATUNAFD.                           *
.*           Mods for change from KEY17 to KEY25 in PATUNAFD.              *
.* V10.00.01 31/03/2010  Mike Laming  CAR 217575                           *
.*           Recompiled for changes to MEHDIAFD                            *
.***************************************************************************
.* V9.08.01  20/04/2007  Mike Laming   CAR 138935                          *
.*           Change Extract flag to new field MHDLSENT                     *
.***************************************************************************
.* V9.07.01  30/08/2006  Mike Laming   CAR 109741                          *
.*           NZ Legal Status - to use "mehdlsaf" (not "mehlegaf")          *
.***************************************************************************
.* V9.03.01  29/10/2003 Sandra Barcham   43783                             *
.*           Recompiled for NZHISIIO and NZHISIUP                          *
.***************************************************************************
.* V9.02.00  29/10/2002  Lina Vo                                           *
.*           Ported to V9                                                  *
.*           Removed association to PATSURFD that was made                 *
.*           redundant in r5.10                                            *
.***************************************************************************
.*        V5.08.01  21/08/2000  Caleb Sharman                              *
.*                  Changed Health Fund variables to be 8 chars            *
.*        V5.07.01  19/10/1999  Steve Downing  SRF 646137                  *
.*                  Added PTCNDCQS - Single column for doctor code search  *
.*       V5.07.B01 17/11/1998  Davin                                       *
.*                 Mods for 507                                            *
.*        V5.05.01 22/09/1997  Steve Armstrong                             *
.*                 Mods for AXIS V to be a free text description.          *
.*                 (MEHRVDIA)                                              *
.***************************************************************************
.*        V5.04.04  02/09/1997  Steve Armstrong  MOH 505 mods              *
.*                  Mods for ACC flag changes                              *
.*        V5.04.03  14/06/1997  Steve Armstrong                            *
.*                  Fixed bugs in global recompile                         *
.*               V5.04.02  07/11/1996  Howellsy                            *
.*                         Added Review Time & Legal Status Time.          *
.*               V5.04.01  26/08/96  Howellsy    SRF 641984                *
.*                         Option 1 - Meh check for astat=2 & 4 not 1 & 4  *  
.***************************************************************************
.*               V5.01.02  4/10/94  Whirlpool                              *
.*                         Changed to reset new sent flag on operation     *  
.*                         changes                                         *
.* Mods      :   V5.03.01  19/07/1995  Whirlpool                           *
.*                         added code to ask if we are re submitting  or   *  
.*                         re setting the data.                            *  
.*                         re submitting sets aelig to 3 or 2              *  
.*                         re setting sets the aelig to 0                  *  
.*               V5.03.02  25/09/1995  ROD            SRF 640755           *
.*                         changed COUNT from Form2 to Form5               *  
.*               V5.03.03  16/01/1995  Whirlpool      srf 613301           *
.*                         added reset of patunaaf sent flag to RESET by   *  
.*                         date range (New menu option)                    *  
.*               V5.03.04  30/01/1995  Whirlpool      srf 641199           *
.*                         fixed display of item as COUNT was made a form5 *  
.*               V5.03.05  06/02/1996  MATT                                *
.*                         Allowed for resubmit of non-disch MEH patients  *  
.***************************************************************************
.
.====================================================
.  FD INCLUDES 
.====================================================
.
          INC       STD001FD
.
          INC       BOKRC1FD/INC            * booking file
          INC       EOCAFCFD/INC            * ACC Flag changes file
          INC       NZHISIFD/INC            * NZHIS variables
          INC       OUTPREFD/INC            * OP pre-attendance file
          INC       MEHCONFD/INC            * Mental Health Control File
          INC       MEHDIAFD/INC            * Mental Health Diagnosis File
          INC       MEHDLSFD/INC            * Mental Health Legal Status Detail
...       INC       MEHLEGFD/INC            * Mental Health Legal Status History
          INC       MEHVI1FD/INC            * Mental Health Visit File
          INC       PATALRFD/INC            * alerts
          INC       PATCALAG/INC
          INC       PATCODFD/INC            * codes file
          INC       PATCOMM/INC             * variables for surname search
          INC       PATCONFD/INC            * control file
          INC       PATDHEAD/INC            * vars for PMIHEAD
          INC       PATDNRFD/INC            * donor file
          INC       PATDO1FD/INC
          INC       PATDSCFD/INC
          INC       PATGSRFD/INC            * surname/given name file
          INC       PATMA1FD/INC            * patient master file
          INC       PATMI1FD/INC            * admission file
          INC       PMSVX1FD/INC            * admission file
          INC       PATNADFD/INC            * need assessment details
          INC       NHIMASFD/INC            * translation file
          INC       PATNIDFD/INC            * national UR file
          INC       PATODCFD/INC
          INC       PATPR1FD/INC            * pre-admission file
          INC       PATPRVFD/INC
          INC       PATSDNFD/INC            * save donor details
          INC       PATSMWFD/INC            * save patient med. warnings
          INC       PATUNAFD/INC
          INC       PATVISFD/INC
          INC       PMSPX2FD/INC
          INC       SCRPSTFD/INC
          INC       PATHSPFD/INC            * Hospital file
.
.         program variables
.
ADDESC    DIM       14
ADM1      DIM       8
ADM2      DIM       8
ADM3      DIM       8
ADM4      DIM       8
ADM5      DIM       8
ADM6      DIM       8
ADM7      DIM       8
ADM8      DIM       8
ADM9      DIM       8
ADM10     DIM       8
ADM11     DIM       8
ADM12     DIM       8
ADMDATE   DIM       10
ATTDOC    DIM       15
BJDAY     FORM      3
CCFRDATE  DIM       8
CCTODATE  DIM       8
CHG       FORM      1
CJDAY     FORM      3
CMDLINE   DIM       80        * for nzhisiio
CODDATE   DIM       10
CODE      DIM       3
CODEA     INIT      "A "
COUNT     FORM      5
ITEM      FORM      2         * Item number on screen
DHBPREFX  DIM       3        * HDP Default of 'District Health Board' (Cat dh)
DIM3      DIM       3
DIM8      DIM       8
DISDATE   DIM       10
DOCCODE   DIM       6
FIRST     FORM      1
FORM3     FORM      3
FRDATE    DIM       8
FRSTGNAM  DIM       40
FULLGNAM  DIM       80
MHPRFLAG  FORM      1
MODE1     DIM       1         * for nzhisiio
MOREREC   FORM      1
NMPNUMB   DIM       20
OPT       DIM       2
RFLAG     FORM      1         * 0 = reset 1 = resubmit
SCNDGNAM  DIM       40
TODATE    DIM       8
TODAY     DIM       8
.
.         program constants 
.
CATdh     INIT      "dh"
Z15       INIT      "zzzzzzzzzzzzzz"
ZEDS      INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"                  *I-109741
.
++
PRGID     INIT      "IBAINP28"
PRGNAM    INIT      "Edi Re-Submissions"
VERSION   INIT      "V12.01.00"
++
.
.***********************************************************************
.*        MAINLINE
.***********************************************************************
ML0000    CALL      INIT0000                 * initialization & open files
.
.
ML0100    CALL      MENU0000                 * select option
          BRANCH    OPTION,ML1000:           * Resubmit by patient
                           ML2000:           * Resubmit by date range
                           ML2000            * Reset unadmit by date range
          GOTO      ML9999                   * exit selected
. 
.----------  Resubmit by Patient ----------  
.
ML1000    CALL      RSPT0000                 * Resubmit by patient
          BRANCH    EXIT,ML0100,ML1000       * No input, EXITCHAR
          GOTO      ML0100
.
.----------  Resubmit by Date Range -------
.
ML2000    CALL      RSDT0000                 * Resubmit by date range
          BRANCH    EXIT,ML0100,ML2000       * No input, EXITCHAR
          IF        OPTION = 2
            CALL      RCOD0000               * Resubmit Coding changes
            CALL      RAFC0000               * Resubmit Coding changes
            CALL      RSMH0000
          ENDIF
.                                             
          GOTO      ML0100
.
ML9999    CHAIN     PGM
+
.****************************************************************************
.* INIT0000  : Initialization & open files             
.* Called by : ML0000                     
.****************************************************************************
INIT0000  CALL      DISPHEAD                 * display heading
          MOVE      ONE,CNEWDS
          PACK      TODAY,CCC,CYY,CMM,CDD
          REP       " 0",TODAY
.
          DISPLAY   *P50:24,*EL,"Opening";
.
          DISPLAY   *P58:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P58:24,"controlf"
          OPEN      CONTROLF,"controlf" 
.
          DISPLAY   *P58:24,"bokrc1af"
          OPEN      BOKRC1A6,"bokrc1af"
.
          DISPLAY   *P58:24,"patdschf"
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P58:24,"patodcaf"
          OPEN      PATODCA1,"patodcaf"
          OPEN      PATODCA2,"patodcaf"
.
          DISPLAY   *P58:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P58:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P58:24,"pmspx2af"
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P58:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A4,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P58:24,"patpramf"
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
.
          DISPLAY   *P58:24,"patpraxf"
.
          DISPLAY   *P58:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P58:24,"patvisaf"
          OPEN      PATVISA1,"patvisaf"
          OPEN      PATVISA2,"patvisaf"
.
          DISPLAY   *P58:24,"patunaaf"
          OPEN      PATUNAA2,"patunaaf"
.
          READ      CONTROLF,TEN9;*75,CCNOTES
          READ      CONTROLF,TWENTY;*91,PTCNHSEM
          READ      CONTROLF,TWENTY1;*247,PTCNDCQS
          READ      CONTROLF,FIFTY9;*132,CALRDESC,*231,PTCNRGNS
          READ      CONTROLF,SIXTY9;*69,MHCNUSE
          READ      CONTROLF,SEVENTY9;*120,PTCNUEOC,*121,PTCNUACC
.
. made PATSURFD redundant in r5.10
.         IF        PTCNSNDX = 0   
.           DISPLAY   *P58:24,"patsurnf"
.           OPEN      PATSURN1,"patsurnf"     * surname only
.           OPEN      PATSURN2,"patsurnf"
.         ELSE 
          DISPLAY   *P58:24,"patgsrnf"
          OPEN      PATGSRN1,"patgsrnf"     * surname & given name
          OPEN      PATGSRN2,"patgsrnf"
          OPEN      PATGSRN3,"patgsrnf"
          OPEN      PATGSRN4,"patgsrnf"
.         ENDIF
.
          IF        PTCNUEOC = 1 & PTCNUACC = 1
            DISPLAY   *P58:24,"eocafcaf"
            OPEN      EOCAFCA1,"eocafcaf"
            OPEN      EOCAFCA2,"eocafcaf"
          ENDIF
.
          IF        MHCNUSE=1
            DISPLAY   *P58:24,"mehvi1af"
            OPEN      MEHVI1A1,"mehvi1af"
            OPEN      MEHVI1A2,"mehvi1af"
.
....        DISPLAY   *P58:24,"mehlegaf"
....        OPEN      MEHLEGA1,"mehlegaf"
.
            DISPLAY   *P58:24,"mehdlsaf"                              *I-109741
            OPEN      MEHDLSA1,"mehdlsaf"                             *I-109741
.
            DISPLAY   *P58:24,"mehdiaaf"
            OPEN      MEHDIAA1,"mehdiaaf"
          ENDIF
.
          DISPLAY   *P58:24,"pathspaf"
          OPEN      PATHSPA1,"pathspaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*        MENU0000 : Select Option
.***************************************************************************
MENU0000  HLINE     *G37,2,49,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Reset/Resubmit by Patient":
                    *P1:6,*V2LON,TWO,*HOFF," = Reset by Date Range":
                    *P1:7,*V2LON,THREE,*HOFF," = Reset Unadmitted Patients":
                                           " by Date Range";
          DISPLAY   *P1:NINE,"Select Option : "
. 
MENU1000  KEYIN     *P17:NINE,*V2LON,OPTION
.
          COMPARE   ZERO,OPTION
          GOTO      MENU9999 IF EQUAL
.
          BRANCH    OPTION,MENU2000,MENU3000,MENU4000
.
MENU1500  BEEP
          GOTO      MENU1000
.
MENU2000  DISPLAY   *P49:2,*V2LON," - by Patient "
          GOTO      MENU9999
.
MENU3000  DISPLAY   *P49:2,*V2LON," - by Date Range "
          GOTO      MENU9999
.
MENU4000  DISPLAY   *P49:2,*V2LON," - Unadmitted by Date Range "
          GOTO      MENU9999
.
MENU9999  RETURN
+
.****************************************************************************
.*        RSPT0000 : Resubmit by Patient
.****************************************************************************
RSPT0000  DISPLAY   *P1:3,*EF
          MOVE      ZERO,EXIT
          MOVE      TEN8,CCOL
          MOVE      FOUR,CVERT
.
          DISPLAY   *P5:4,"U/R Number : "
          CALL      KURN                         * get U/R
          BRANCH    EXIT,RSPT9000
          IF        OVRCD=1
            DISPLAY   *P1:24,*EL,*B,"Invalid U/R. ";
            CALL      HITENTER
            GOTO      RSPT0000        
          ENDIF
.
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          IF        OVRCD=1
            DISPLAY   *P1:24,*EL,*B,"Invalid U/R. ";
            CALL      HITENTER
            GOTO      RSPT0000        
          ENDIF
.
          CALL      PMIHEAD
.
RSPT1000  CALL      DSPA0000                     * get submitted admission
          BRANCH    EXIT,RSPT0000                * no records or exit chosen
.
RSPT2000  KEYIN     *P1:24,*EL,"Correct Admission (",*V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS
          REP       UPPLOW,ANS
.
          MATCH     ANSN,ANS
          GOTO      RSPT1000 IF EQUAL
.
          MATCH     ANSY,ANS
          GOTO      RSPT3000 IF EQUAL
.
          BEEP
          GOTO      RSPT2000
.
.-------- We have an admission to re-submitted --------
.
RSPT3000  PACK      PTODDATE,CCC,CYY,CMM,CDD
          REP       " 0",PTODDATE
          MOVE      DIM8,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,RSPT1000
.
          CALL      RSSQ0000                * re-set or re-submit
.
          IF        RFLAG = 1
            KEYIN     *P1:24,*EL,"Is this a Warning Resubmittance (",*V2LON:
                      "Y",*HOFF,"/",*V2LON,"N",*HOFF,") ? ",*V2LON,OPT
            REP       UPPLOW,OPT
.
            IF        AELIG>1
              CALL      REMH0000
            ENDIF
.
            MATCH     ANSY,OPT
            IF        @EQUAL
              MOVE      TWO,AELIG
              GOTO      RSPT4000
            ENDIF
.
            MATCH     ANSN,OPT
            IF        @EQUAL
              MOVE      THREE,AELIG
              GOTO      RSPT4000
            ENDIF
.
            BEEP
            GOTO      RSPT3000
          ELSE
            IF        AELIG>1
              CALL      REMH0000
            ENDIF
            MOVE      ZERO,AELIG
          ENDIF
.
RSPT4000  CALL      UPMISS1
.
          IF        RFLAG = 1
            DISPLAY   *P1:24,*B,"Patient can now be re-submitted. ";
          ELSE
            DISPLAY   *P1:24,*B,"Patient can now be re-sent. ";
          ENDIF
          CALL      HITENTER
          GOTO      RSPT1000
.
RSPT9000  MOVE      ONE,EXIT
.
RSPT9999  RETURN
+
.****************************************************************************
.*        RSSQ0000 : Resubmit/Re-set Question
.****************************************************************************
.
RSSQ0000    MOVE      ZERO,RFLAG
            KEYIN     *P1:24,*EL,"(",*V2LON,*DV,ANSR,*HOFF,")eset or Re(":
                      *V2LON,*DV,ANSS,*HOFF,")ubmit ? ",*V2LON,ANS
            REP       UPPLOW,ANS
.
            MATCH     ANSR,ANS
            IF        @EQUAL
              MOVE      ZERO,RFLAG
              GOTO      RSSQ9999
            ENDIF
.
            MATCH     ANSS,ANS
            IF        @EQUAL
              MOVE      ONE,RFLAG
              GOTO      RSSQ9999
            ENDIF
.
            BEEP
            GOTO      RSSQ0000
.
RSSQ9999  RETURN
+
.****************************************************************************
.*        RSDT0000 : Resubmit by Date Range
.****************************************************************************
RSDT0000  MOVE      ZERO,EXIT
          MOVE      ZERO,COUNT
.
          MOVE      SP4,DHBPREFX
          MOVE      ZERO,EXIT
          DISPLAY   *P1:11,*EF,"District Health Board : ",SP30
          MOVE      "25",ECOL
          MOVE      "11",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP70,CKYIDEF3
          PACK      CODE,CATdh
          CALL      PATCODKY
          BRANCH    EXIT,RSDT9000,RSDT9000
.
          PACK      DHBPREFX,THCSCOD,SP70
          STRIP     TDESC
          DISPLAY   *P30:EVERT,"(",*V2LON,DHBPREFX,*HOFF,*P35:EVERT,"-":
                    *P37:EVERT,*+,TDESC,*-,")"
.
          DISPLAY   *P1:13,"Starting Date : ",*P3:14,"Ending Date : "
          CALL      KDAT0000
          BRANCH    EXIT,RSDT9000
.
          CALL      CONTQST
          BRANCH    CEXIT,RSDT1000,RSDT0000,RSDT9000
.
.-------- We have an admission date range to re-submitted --------
.
RSDT1000  DISPLAY   *P1:24,*EL,*P30:24,"Admission : "
          IF        OPTION = 3
            CALL      RUNA0000             * reset patunaaf for date range
            GOTO      RSDT9999       
          ENDIF  
.
.------  Doing option 2 so reset normal patients -------
          BRANCH    PTCNHSEM,RSDT2000
.
.-------- Resubmit by date of coding -----------
.
          PACK      KEY16,CCFRDATE,SP10
          CALL      RDSMISS4
RSDT1500  CALL      RDKMISS4
          BRANCH    OVRCD,RSDT8000
.
          REP       " 0",ACODDTE
          MATCH     CCFRDATE,ACODDTE
          GOTO      RSDT1500 IF LESS
.
          MATCH     ACODDTE,CCTODATE
          GOTO      RSDT8000 IF LESS
.
          COMPARE   ONE,AELIG
          GOTO      RSDT1500 IF NOT EQUAL
.
          PACK      KEY8,AADMNO,SP20
          CALL      RDVISA1
          BRANCH    OVRCD,RSDT1500
.
          CALL      RDPMVX11
          BRANCH    OVRCD,RSDT1500
.
          CALL      CHKDHB00                * Check District Health Board
          BRANCH    EXIT,RSDT1500           * Skip if not matching hospital DHB?
.
          ADD       ONE,COUNT
          MOVE      ZERO,AELIG
          CALL      UPMISS4                      * update patmi1af
          DISPLAY   *P43:24,*V2LON,AADMNO
.
          GOTO      RSDT1500
.
.-------- Resubmit by date of discharge -----------
.
RSDT2000  PACK      KEY16,FRDATE,SP20
          CALL      RDSDSCH2
RSDT3000  CALL      RDKDSCH2
          BRANCH    OVRCD,RSDT8000
.
          REP       " 0",DDATE
          MATCH     FRDATE,DDATE
          GOTO      RSDT3000 IF LESS
.
          MATCH     DDATE,TODATE
          GOTO      RSDT8000 IF LESS
.
          PACK      KEY8,DADMNO
          CALL      RDMISS1 
          BRANCH    OVRCD,RSDT3000
.
          COMPARE   AELIG,ONE
          GOTO      RSDT3000 IF NOT EQUAL 
.
          PACK      KEY8,AADMNO,SP20
          CALL      RDVISA1
          BRANCH    OVRCD,RSDT3000
.
          CALL      RDPMVX11
          BRANCH    OVRCD,RSDT3000
.
          CALL      CHKDHB00                * Check District Health Board
          BRANCH    EXIT,RSDT3000           * Skip if not matching hospital DHB?
.
          ADD       ONE,COUNT
          MOVE      ZERO,AELIG
          CALL      UPMISS1                      * update patmi1af
          DISPLAY   *P40:24,*V2LON,AADMNO
          GOTO      RSDT3000
.
RSDT8000  DISPLAY  *P1:24,*EL,"Resubmitted ",*V2LON,COUNT,*HOFF," Admissions. ";
          CALL      HITENTER
          GOTO      RSDT9999
.
RSDT9000  MOVE      ONE,EXIT
.
RSDT9999  RETURN
. 
.****************************************************************************
.*                               RCOD0000                                   *
.*                      Resubmit coding for a date range                    *
.****************************************************************************
.
RCOD0000  DISPLAY   *P1:24,*EL,"Resubmitting coding for admission : "
.
.         Loop through the operation changes file, 
          MOVE      ZERO,COUNT
          PACK      KEY16,CCFRDATE,SP10
          CALL      RDSODCA2
RCOD1000  CALL      RDKODCA2
          BRANCH    OVRCD,RCOD8000
.
          MATCH     PTODDATE,CCTODATE
          GOTO      RCOD8000 IF LESS
.
          MATCH     "0",PTODSENT
          GOTO      RCOD1000 IF EQUAL       * already flagged
.
          PACK      KEY8,PTODADMN,SP20
          CALL      RDVISA1
          BRANCH    OVRCD,RCOD1000
.
          CALL      RDPMVX11
          BRANCH    OVRCD,RCOD1000
.
          CALL      CHKDHB00                * Check District Health Board
          BRANCH    EXIT,RCOD1000           * Skip if not matching hospital DHB?
.
          PACK      KEY8,PTODADMN
          CALL      RDODCA1
          BRANCH    OVRCD,RCOD1000
.
          MOVE      ZERO,PTODSENT
          CALL      UPODCA1 
          DISPLAY   *P37:24,*EL,*V2LON,PTODADMN
          ADD       ONE,COUNT
          GOTO      RCOD1000

RCOD8000  DISPLAY  *P1:24,*EL,"Resubmitted ",*V2LON,COUNT,*HOFF," Coding Changes. ";
          CALL      HITENTER
          GOTO      RSDT9999
.
RCOD9999  RETURN
+
.****************************************************************************
.*                               RAFC0000                                   *
.*                Resubmit ACC Flag changes for a date range                *
.****************************************************************************
.
RAFC0000  IF        PTCNUEOC <> 1 | PTCNUACC <> 1
            GOTO      RAFC9999
          ENDIF
.
          DISPLAY   *P1:24,*EL,"Resubmitting coding for admission : "
.
.         Loop through the ACC Flag changes file 
.
          MOVE      ZERO,COUNT
          PACK      KEY16,CCFRDATE,SP10
          CALL      RSECAFC2
RAFC1000  CALL      RKECAFC2
          BRANCH    OVRCD,RAFC8000
.
          MATCH     ECAFDATE,CCTODATE
          GOTO      RAFC8000 IF LESS
.
          MATCH     "0",ECAFSENT
          GOTO      RAFC1000 IF EQUAL       * already flagged
.
          PACK      KEY8,ECAFADMN,SP20
          CALL      RDVISA1
          BRANCH    OVRCD,RAFC1000
.
          CALL      RDPMVX11
          BRANCH    OVRCD,RAFC1000
.
          CALL      CHKDHB00                * Check District Health Board
          BRANCH    EXIT,RAFC1000           * Skip if not matching hospital DHB?
.
          PACK      KEY8,ECAFADMN
          CALL      RDECAFC1
          BRANCH    OVRCD,RAFC1000
.
          MOVE      ZERO,ECAFSENT
          CALL      UPECAFC1 
          DISPLAY   *P37:24,*EL,*V2LON,ECAFADMN
          ADD       ONE,COUNT
          GOTO      RAFC1000

RAFC8000  DISPLAY  *P1:24,*EL,"Resubmitted ",*V2LON,COUNT,*HOFF," ACC Flag ":
                    "Changes. ";
          CALL      HITENTER
          GOTO      RSDT9999
.
RAFC9999  RETURN
+
.****************************************************************************
.*                               RUNA0000                                   *
.*                      Resubmit unadmitted patient by date range           *
.****************************************************************************
.
RUNA0000  DISPLAY   *P1:24,*EL,"Resubmitting coding for admission : "
.
.         Loop through the operation changes file, 
.
          MOVE      ZERO,COUNT
          PACK      KEY25,ONE,CCFRDATE,SP30
          CALL      RDSUNAA2
RUNA1000  CALL      RDKUNAA2
          BRANCH    OVRCD,RUNA8000
.
          MATCH     PTUNDATE,CCTODATE
          GOTO      RUNA8000 IF LESS
.
          COMPARE   ZERO,PTUNSENT
          GOTO      RUNA1000 IF EQUAL       * already flagged
.
          PACK      KEY8,PTUNADMN,SP20
          CALL      RDVISA1
          BRANCH    OVRCD,RUNA1000
.
          CALL      RDPMVX11
          BRANCH    OVRCD,RUNA1000
.
          CALL      CHKDHB00                * Check District Health Board
          BRANCH    EXIT,RUNA1000           * Skip if not matching hospital DHB?
.
          MOVE      ZERO,PTUNSENT
          CALL      UPUNAA2 
          DISPLAY   *P35:24,*EL,*V2LON,PTUNADMN
          ADD       ONE,COUNT
          GOTO      RUNA1000

RUNA8000  DISPLAY  *P1:24,*EL,*V2LON,COUNT,*HOFF," Unadmitted ":
                              " Patients Reset. ";
          CALL      HITENTER
          GOTO      RSDT9999
.
RUNA9999  RETURN
.
.****************************************************************************
.*        RSMH0000 : 
.*        Re-Submit Mental Health Details for a date range.
.****************************************************************************
RSMH0000  COMPARE   ZERO,MHCNUSE
          GOTO      RSMH9999 IF EQUAL
          MOVE      ZERO,COUNT
          PACK      KEY9,ONE,SP20
          CALL      RSMHVIS2
RSMH1000  CALL      RKMHVIS2
          BRANCH    OVRCD,RSMH9000
.
          COMPARE   SIX,MHVISTAT
          GOTO      RSMH9000 IF NOT LESS
.
.
.-------  Check the diagnosis file  -------
.         
...       PACK      KEY16,MHVIADMN,SP10
...       CALL      RSMHDIA1
...       CALL      RKMHDIA1
...       BRANCH    OVRCD,RSMH1000         * No provisional diagnosis
...       COMPARE   MHVIADMN,MHDIADMN
...       GOTO      RSMH1000 IF NOT EQUAL  * Not same admission
.
.
.-------  Check the legal status history file  -------
.         
. ******************************************* start of changes        *C-109741
....      PACK      KEY21,MHVIADMN,CCFRDATE,SP20
....      CALL      RSMHLEG1
....2000  CALL      RKMHLEG1
....      BRANCH    OVRCD,RSMH1000
.
....      COMPARE   MHVIADMN,MHLEADMN          * Check Admission Number
....      GOTO      RSMH1000 IF NOT EQUAL
.
....      MATCH     MHLEDATE,CCTODATE          * Check Change Date
....      GOTO      RSMH1000 IF LESS
.
....      MATCH     ANSY,MHLESENT
....      IF        @EQUAL
....        ADD       ONE,COUNT
....        MOVE      ANSN,MHLESENT
....        CALL      UPMHLEG1
....      ENDIF
.
....      GOTO      RSMH2000
.
.
          PACK      KEY8,MHVIADMN,SP20
          CALL      RDVISA1
          BRANCH    OVRCD,RSMH1000
.
          CALL      RDPMVX11
          BRANCH    OVRCD,RSMH1000
.
          CALL      CHKDHB00                * Check District Health Board
          BRANCH    EXIT,RSMH1000           * Skip if not matching hospital DHB?
.
          PACK      KEY32,MHVIURNO,ZEDS,SP30
          CALL      RSMHDLS1
RSMH2000  CALL      RPMHDLS1
          BRANCH    OVRCD,RSMH1000
.
          MATCH     MHDLURNO,MHVIURNO
          GOTO      RSMH1000 IF NOT EQUAL
.
          MATCH     CCFRDATE,MHDLSDAT
          GOTO      RSMH1000 IF LESS
.
          CMATCH    "Y",MHDLSENT                                      *C-138935
          IF        @EQUAL
            ADD       ONE,COUNT
            MOVE      SP1,MHDLSENT                                    *C-138935
            CALL      UPMHDLS1
          ENDIF
.
          GOTO      RSMH2000
.
RSMH9000  DISPLAY  *P1:24,*EL,*V2LON,COUNT,*HOFF," Mental Health Legal Status ":
                              " Records Reset. ";
          CALL     HITENTER
.
RSMH9999  RETURN
.
.****************************************************************************
.*        REMH0000 :
.*        Re-Submit Mental Health Details for a selected admission.
.****************************************************************************
....0000  PACK      KEY21,AADMNO,SP20
....      CALL      RSMHLEG1
....1000  CALL      RKMHLEG1
....      BRANCH    OVRCD,REMH9999
....
....      COMPARE   AADMNO,MHLEADMN          * Check Admission Number
....      GOTO      REMH9999 IF NOT EQUAL
....
....      MOVE      ANSN,MHLESENT
....      CALL      UPMHLEG1
....      GOTO      REMH1000
.
.
REMH0000  COMPARE   ZERO,MHCNUSE
          GOTO      REMH9999 IF EQUAL
          MOVE      ZERO,COUNT
          PACK      KEY9,ONE,SP20
.
          PACK      KEY32,AURNO,ZEDS,SP30
          CALL      RSMHDLS1
REMH2000  CALL      RPMHDLS1
          BRANCH    OVRCD,REMH9999
.
          MATCH     AURNO,MHDLURNO
          GOTO      REMH9999 IF NOT EQUAL
.
          MATCH     ADATE,MHDLSDAT
          GOTO      REMH9999 IF LESS
.
          CMATCH    "Y",MHDLSENT                                      *C-138935
          IF        @EQUAL
            ADD       ONE,COUNT
            MOVE      SP1,MHDLSENT                                    *C-138935
            CALL      UPMHDLS1
          ENDIF
.
          GOTO      REMH2000
. *******************************************   end of changes        *C-109741
.
REMH9999  RETURN
.
.****************************************************************************
.*        DSPA0000 : Display submitted admissions
.*                   Returns   EXIT = 1    No records found or exit selected
.*                             EXIT = 0    Valid record selected
.*                                         DIM8 = Admission No. 
.****************************************************************************
DSPA0000  MOVE      ZERO,EXIT
          MOVE      ZERO,FIRST
          MOVE      NINE,CVERT
          MOVE      ZERO,ITEM
.
          DISPLAY   *P1:8,*EF,*V2LON,"SUBMITTED ADMISSIONS",*ULON,*P1:9:
                    "Item Adm No. Adm Date    Dis Date    Admissn Type  ":
                    "Attending Doct  "
.
          IF        PTCNHSEM=0
            DISPLAY   *P69:9,*V2LON,*ULON,"Coding Date"
          ENDIF
.
          PACK      KEY24,PURNO,Z15
          CALL      RDSVISA2
.
DSPA1000  MOVE      ZERO,MOREREC
          CALL      RDPVISA2
          BRANCH    OVRCD,DSPA4000
.
          MATCH     PURNO,PVIURNO
          GOTO      DSPA4000 IF NOT EQUAL
.
          PACK      KEY8,PVIBILL
          CALL      RDMISS1
          BRANCH    OVRCD,DSPA1000
.
          MOVE      ZERO,MHPRFLAG
          IF        MHCNUSE=1 & AELIG=3 & (ASTAT=2 | ASTAT=4)
            MOVE      SP10,DISDATE
            MOVE      SP10,CODDATE
            MOVE      ONE,MHPRFLAG
            GOTO      DSPA1500
          ENDIF        
.
          COMPARE   ONE,AELIG                    * Submitted admission ?
          GOTO      DSPA1000 IF NOT EQUAL         
.
DSPA1500  UNPACK    SP10,DDATE
          MOVE      AADMNO,KEY8
          CALL      RDDSCH1
.
          MOVE      SP20,ADDESC                  * get admission type
          MATCH     SP3,ATYPE
          GOTO      DSPA2000 IF EQUAL
          PACK      KEY5,CODEA,ATYPE
          CALL      RDCODE1
          BRANCH    OVRCD,DSPA2000
          MOVE      TDESC,ADDESC
.
DSPA2000  MOVE      SP20,ATTDOC                  * get attending doctor
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD,DSPA3000
.
          MOVE      DSNAME,PACSNAME
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,ATTDOC
.
DSPA3000  UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY  * format admission and
          CALL      PACDATE
          MOVE      CPCDATE,ADMDATE
.
          BRANCH    MHPRFLAG,DSPA3500            * No Discharge or coding date
.
          UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DISDATE
.
          UNPACK    ACODDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,CODDATE
.
DSPA3500  ADD       ONE,CVERT
.
          COMPARE   "22",CVERT                   * Screen full ?
          GOTO      DSPA7000 IF LESS
          MOVE      ONE,MOREREC
.
DSPA4000  IF        ITEM=0
            DISPLAY   *P1:24,"No Submitted Admissions exist for U/R. ";
            CALL      HITENTER
            GOTO      DSPA9000
          ENDIF
.
          CALL      DISQ0000                     * line 24 keyin
.
          TYPE      OPT
          IF        @EQUAL
            MOVE      OPT,FORM2
            IF        ITEM<FORM2 | FORM2<1
              GOTO      DSPA4500                 * invalid item selected
            ENDIF
            LOAD      DIM8,FORM2,ADM1,ADM2,ADM3,ADM4,ADM5,ADM6,ADM7,ADM8:
                                 ADM9,ADM10,ADM11,ADM12
            ADD       "9",FORM2
            DISPLAY   *P4:FORM2,*HON,DIM8
            GOTO      DSPA9999                   * valid item selected
          ENDIF
.
          MATCH     ANSE,OPT                     * Exit ?
          GOTO      DSPA9000 IF EQUAL
.
          IF        FIRST=1
            MATCH     ANSF,OPT                   * First ?
            GOTO      DSPA0000 IF EQUAL
          ENDIF
.
          IF        MOREREC=1
            MATCH     ANSN,OPT                   * Next ?
            GOTO      DSPA5000 IF EQUAL
          ENDIF
.
DSPA4500  BEEP
          GOTO      DSPA4000
.
DSPA5000  DISPLAY   *P1:10,*EF
          MOVE      ONE,FIRST
          MOVE      ZERO,ITEM           
          MOVE      TEN,CVERT
.
DSPA7000  ADD       ONE,ITEM
          STORE     AADMNO,ITEM,ADM1,ADM2,ADM3,ADM4,ADM5,ADM6,ADM7,ADM8:
                                 ADM9,ADM10,ADM11,ADM12
.
          DISPLAY   *P1:CVERT,*V2LON,ITEM,".",*HOFF,*P4:CVERT,AADMNO:
                    "  ",ADMDATE,"  ",DISDATE,"  ",ADDESC,*P52:CVERT,ATTDOC
.
          IF        PTCNHSEM=0
            DISPLAY   *P69:CVERT,CODDATE         * display coding date
          ENDIF
.
          GOTO      DSPA1000                     * get next visit
.
DSPA9000  MOVE      ONE,EXIT
.
DSPA9999  RETURN
+
.*********************************************************************
.*        KDAT0000 : Enter the date ranges
.*********************************************************************
KDAT0000  MOVE      TEN3,CVERT
          MOVE      TEN7,CCOL
          UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          DISPLAY   *P17:13,"__/__/____"
.
KDAT1000  CALL      KEYDATE                      * start Date
          BRANCH    CQUEST,KDAT0000
          BRANCH    OVRCD,KDAT9000               * nothing entered
.
          PACK      FRDATE,CCENT,CYEAR,CMON,CDAY
          PACK      CCFRDATE,CCENT,CYEAR,CMON,CDAY
.
          MATCH     CCFRDATE,TODAY
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Date cannot be greater than today.  ";
            CALL      HITENTER
            GOTO      KDAT1000
          ENDIF
.
KDAT2000  MOVE      TEN4,CVERT
          MOVE      TEN7,CCOL
          UNPACK    CCFRDATE,CCENT,CYEAR,CMON,CDAY
.
          CALL      KEYDATE                      * end Date
          BRANCH    CQUEST,KDAT2000
          BRANCH    OVRCD,KDAT0000               * nothing entered
.
          PACK      TODATE,CCENT,CYEAR,CMON,CDAY
          PACK      CCTODATE,CCENT,CYEAR,CMON,CDAY
.
          MATCH     CCTODATE,TODAY
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Date cannot be greater than today.  ";
            CALL      HITENTER
            GOTO      KDAT2000
          ENDIF
.
          MOVE      ZERO,EXIT
          MATCH     CCFRDATE,CCTODATE
          GOTO      KDAT9999 IF NOT LESS         * date is <= start date
.
          DISPLAY   *P1:24,*B,"Ending date must be after Starting date.  ",*EL;
          CALL      HITENTER 
          GOTO      KDAT0000
.
KDAT9000  MOVE      ONE,EXIT
.
KDAT9999  RETURN
+
.*********************************************************************
.*        DISQ0000 : Keyin option to line 24 question
.*********************************************************************
DISQ0000  MOVE      "16",CCOL
          DISPLAY   *P1:24,*EL,"Select Item, ";
.
          IF        FIRST=1
            DISPLAY   "(",*V2LON,"F",*HOFF,")irst, ";
            ADD       "8",CCOL
          ENDIF
.
          IF        MOREREC=1        
            DISPLAY   "(",*V2LON,"N",*HOFF,")ext, ";
            ADD       "7",CCOL
          ENDIF
.
          DISPLAY   "(",*V2LON,"E",*HOFF,")xit ? ";
          KEYIN     *V2LON,OPT
          REP       UPPLOW,OPT
.
DISQ9999  RETURN
+
.******************************************************************************
.                            CHKDHB00
.         Validates that 'District Health Board' of the patient's visit
.         hospital (PMVXMHOS) matches the HDP Default entered (DHBPREFX).
.******************************************************************************
CHKDHB00  MOVE      ZERO,EXIT
          MATCH     SP70,PMVXMHOS           * Hospital for visit filled in?
          GOTO      CHKDHB91 IF EQUAL       * no; invalid
.
          PACK      KEY3,PMVXMHOS,SP70
          CALL      RDPTHSP1
          BRANCH    OVRCD,CHKDHB91
.
          MATCH     SP70,PTHSHDHB           * District Health Board setup?
          GOTO      CHKDHB91 IF EQUAL       * no; invalid
.
          PACK      KEY5,CATdh,PTHSHDHB,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,CHKDHB91
.
          MATCH     DHBPREFX,THCSCOD        * matching HDP Default?
          GOTO      CHKDHB91 IF NOT EQUAL   * no; invalid
.
          GOTO      CHKDHB99
.
CHKDHB91  MOVE      ONE,EXIT                * doesn't match
CHKDHB99  RETURN
+
******************************************************************************
.
.
OPEN0000
CLOS0000
GETSVAR   RETURN
.
.
.===================
. I/O INCLUDES
.===================
.
          INC       STD001IO/PBL
          INC       AGECHK
          INC       BOKRC1IO/INC            * booking file
          INC       CLPATMAS                * clear master file details
          INC       EOCAFCIO/INC
          INC       NZCHGADD                * change address
          INC       NZHISIIO                * NZHIS IO
          INC       NZIBSRCH                * NZHIS IO
          INC       OUTPREIO/INC            * OP pre-attendance file
          INC       MEHDIAIO/INC            * Mental Health Diagnosis IO
          INC       MEHDLSIO/INC            * Mental Health Legal Status Detail
...       INC       MEHLEGIO/INC            * Mental Health Legal Status History
          INC       MEHVI1IO/INC            * Mental Health Visit IO
          INC       PATALERT                * alerts
          INC       PATALRIO/INC            * alerts
          INC       PATCOMN1/PBL            * holds KURN
          INC       PATDNRIO/INC            * donor file
          INC       PATCODKY/PBL
          INC       PATDOCKY/PBL
          INC       PATCODIO/INC            * codes file
          INC       PATDO1IO/INC
          INC       PATDSCIO/INC
          INC       PATGSRIO/INC            * surname/given name file
          INC       PATMA1IO/INC            * patient master file
          INC       PATMI1IO/INC            * admission file
          INC       PMSVX1IO/INC            * admission file
          INC       PATPR1IO/INC            * pre-admission file
          INC       PATVISIO/INC
          INC       PATNADIO/INC
          INC       NHIMASIO/INC            * translation file
          INC       PATNIDIO/INC            * national UR file
          INC       PATODCIO/INC
          INC       PATPRVDS/PBL
          INC       PATPRVIO/INC
          INC       PATSDNIO/INC            * save donor details
          INC       PATSMWIO/INC            * save medical alerts
          INC       PATSNDX/PBL             * ? for surnames only
          INC       PATSNX2/PBL             * ? for surnames/given names
          INC       PATUNAIO/INC
          INC       PMIHEAD
          INC       PMSPX2IO/INC
          INC       PATHSPIO/INC            * Hospital file
          INC       CALCAGE
.

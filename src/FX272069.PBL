.***************************************************************************
.* System    :   Booking System                                            *
.* Program   :   FX272069                                                  *
.***************************************************************************
.* Date      :   16/10/2012                                                *
.* Author    :   Sandra Barcham                                            *
.* Mods      :                                                             *
.***************************************************************************
.
          INC       STD001FD
.
.
. FILE DESCRIPTION INCLUDES
. -------------------------
.
          INC       BOKRC1FD/INC
          INC       BOKRX1FD/INC
.
COUNTER   FORM      5
.
PRGID     INIT      "FX272069"
PRGNAM    INIT      "Fix BKRXCNDT in bokrx1af"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select FROM and TO
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
.
          CALL      PROC0000               * process records for update
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                     * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"bokrx1af";
          OPEN      BOKRX1A1,"bokrx1af"          * booking extension file
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"bokrc1af";
          OPEN      BOKRC1A1,"bokrc1af"          * booking extension file
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Valid option selected                  *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Update bkrxcndt in bokrx1af"
.
OPTN1000  KEYIN     *P1:7,*EL,"Select option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION
          GOTO      OPTN9500 IF EQUAL                 * exit
.
          BRANCH    COPTION,OPTN2000
.
          BEEP
          GOTO      OPTN1000
.
OPTN2000  CALL      CONTQST                          * Ok to continue ?
          BRANCH    CEXIT,OPTN9000,OPTN1000,OPTN9500 * Yes, no, cancel
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.****************************************************************************
.*                                PROC0000            Called by: ML0000     *
.*              read through bokrx1af                                       *
.****************************************************************************
PROC0000  MOVE      ZERO,COUNTER
          DISPLAY   *P1:24,*EL,"Processing BOK Number: ";
          MOVE      SP10,KEY8
          CALL      RSBKRX11                     * position at start of file
PROC1000  CALL      RKBKRX11                     * read next record
          BRANCH    OVRCD,PROC9000               * end of file
.
          DISPLAY   *P30:24,*EL,*V2LON,BKRXBNUM;
.
          PACK       KEY8,BKRXBNUM,SP70
          CALL       RDBKREC1
          BRANCH     OVRCD,PROC1000
.
          COMPARE   ZERO,BKRESTAT
          GOTO      PROC1000 IF NOT EQUAL
.
          MATCH     BKREADAT,BKRXCNDT
          GOTO      PROC1000 IF NOT LESS
.
          MOVE      SP70,BKRXCNDT
          CALL      UPBKRX11
          ADD       ONE,COUNTER
          GOTO      PROC1000
.
PROC9000  DISPLAY   *P1:20,*EL,COUNTER," bokrx1af records updated.  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       BOKRX1IO/INC
          INC       BOKRC1IO/INC

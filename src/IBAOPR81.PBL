.******************************************************************************
.* System         : Theatre / Operating Rooms                                 *
.* Program        : IBAOPR81.PBL                                              *
.* Description    : Sessions Master Listing                                   *
.******************************************************************************
.* Author         : Paul Duncan                                               *
.* Date           : 12/01/1990                                                *
.* Function       : Produce a master listing for the session file             *
.* Modifications  :                                                           *
.*       V11.03.01  20/03/2023  PJ Le Febour   Task 0909393a                  *
.*                  Amended KEY19 to KEY22 for theatre index changes          *
.*       V11.00.03  13/01/2020  J.Tan          Task 0901291                   *
.*                  Mod PRDK0000 to include session on DATETO                 *
.*       V11.00.03  13/01/2020  J.Tan          Task 0901291                   *
.*                  Mod PRDK0000 to include session on DATETO                 *
.*       V11.00.02  10/12/2020  J.Tan          Task 0900609                   *
.*                  Fixed reading session file                                *
.*       V11.00.01  19/10/2020  J.Tan          Task 0898591                   *
.*                  Fixed I*D - added Theatre to index TEMPFIL2               *
.*       V10.14.01  15/03/2019  Jill Parkinson Task 0863524                   *
.*                  Changed to use KEY21 for reading oprsemaf                 *
.*       V10.04.01  07/07/2014  J.Tan     CAR 261630                          *
.*                  Removed port number from temp file name                   *
.*        V9.11.01  03/02/2009  Jill Habasque CAR 175966                      *
.*                  Added hospital ID                                         *
.*        V5.07.01  27/09/1999  Jill Habasque                                 *
.*                  Added Active/Inactive to the report                       *
.*        V5.07.B01 18/01/1999  Nicole Harrington 507 rebound 089             *
.*                  Mods to include century on to and from dates              *
.******************************************************************************
.*        V5.04.02  26/06/1997  Howellsy         505                          *
.*                  Use PATDOCKY instead of PATDOCDS                          *
.******************************************************************************
.*        V5.03.02  29/11/1995    Justin Coates  SRF 441491                   *
.*                  add the actual frequency variables                        *
.*        V5.03.01  24/11/1995    Justin Coates  SRF 441491                   *
.*                  make sure option 3 (session masters) prints all details   *
.******************************************************************************
.*        V5.01.01  10/10/1990 Justin Coates                                  *
.*                  Changed to handle the new FD's                            *
.*        V5.01.02  22/05/91  Steve O'Gorman                                  *
.*                  Fixed Errors found by the New Compiler                    *
.*        V5.01.03  25/07/91 ROD                                              *
.*                  Recompiled for OPRSESFD                                   *
.*        V5.01.04  02/06/1992    Justin Coates                               *
.*                  Added Session Masters (OPRSEMFD) Master Listing           *
.*                  and fixed up the non-use of opcnodsc and opcncdsc         *
.*        V5.01.05  26/05/1995  Matt Surridge                                 *
.*                  Fixed bugs for global recompile.                          *
.******************************************************************************
.
.         FD's needed
.
          INC       STD001FD
          INC       IBASEQFD/INC            * Sequential Numbers File
          INC       TFILEVAR/INC            * Generate Temp File Name
          INC       WEBERRFD/INC            * Web Server Error Logging
.
          INC       OPRCLIFD/INC
          INC       OPRCONFD/INC
          INC       OPROPRFD/INC
          INC       OPRSESFD/INC
          INC       OPRSEMFD/INC
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PATHSPFD/INC
.
.************************************************************************
.*                           CONSTANTS                                  *
.************************************************************************ 
BY        INIT      "- by "
DMONTH    INIT      "Monthly"
DYEARLY   INIT      "Yearly "
ACTIV     INIT      "(Active)  "
INACTIV   INIT      "(Inactive)"
SESSMAS   INIT      "Session Masters"
SP50      INIT      "                                        "
ST        INIT      "ST"
.
.************************************************************************
.*                           VARIABLES                                  *
.************************************************************************
CLINIC    DIM       6
CLOPT     FORM      1
CLTH      DIM       6
CLTHHEAD  DIM       15
CMDLINE   DIM       70
DATEFR    DIM       8
DATETO    DIM       8
DIM7      DIM       7 
DIM3      DIM       3
DOCCODE   DIM       6
IBCNMHOS  FORM      1
HOSCODE   DIM       3
LINE      FORM      2
NAME24    DIM       24
NPAG      FORM      1
OPRTEMP1  DIM       8
OPSTAT    DIM       10
PRDT      DIM       10
PRDTFR    DIM       10
PRDTTO    DIM       10
PRSTAT    DIM       9
PTCNDCQS  FORM      1
RECPR     FORM      6
STAT0     DIM       9
STAT1     DIM       9
STAT2     DIM       9
.
TEMPFIL1  IFILE     SQL, FIXED=117, KEY="u1-6,7-14,15-19,20-25"
TEMPFIL2  IFILE     SQL, FIXED=117, KEY="u20-25,7-14,15-19,1-6"
.Name     Type      Length  Phys  Start  Description
.-------  -----     ------  ----  -----  --------------------------------------
.OPSETHET DIM       6          6      1  Theatre
.OPSEDATE DIM       8          8      7  Session Date
.OPSETIME DIM       5          5     15  Session Time
.OPSECLIN DIM       6          6     20  Clinic/Surgeon
.OPSEDURA DIM       4          4     26  Duration
.OPSEENDT DIM       5          5     30  End Time
.OPSESTAT DIM       1          1     35  Session Status
.OPSETYPE DIM       3          3     36  Session Type
.OPSMBRKT FORM      3          3     39  Time for Breaks
.OPSMPREP FORM      2          2     42  Preparation Time
.OPSMTPER DIM       3          3     44  Time Period
.OPSMANAE DIM       6          6     47  Anesthetist - Usual
.OPSMXDOC DIM       6          6     53  Anesthetist - Extra
.OPSMMRFQ DIM       5          5     59  Monthly Frequency
.OPSMYRFQ DIM       53         53    64  Yearly  Frequency
.End of Record                      117
THEATRE   DIM       6
THOPT     FORM      1
.
PRGID     INIT      "IBAOPR81"
PRGNAM    INIT      "Session Master Listing"
VERSION   INIT      "V12.01.00"
.
.*********************************************************************
.*                  MAIN LINE                                        *
.*        Mainline Code / Controlling Logic                          *
.*********************************************************************
ML0000    CALL      INIT0000            
.
ML1000    CALL      SCRN0000                * Display main screen
          CALL      SLCT0000                * Get selection
          BRANCH    EXIT,ML9999             * exit selected
          BRANCH    MOPTION,ML2000,ML3000,ML3000
.
. ------- Theatre Option -------
.
ML2000    CALL      THEA0000
          GOTO      ML1000
.
. ------- Clinic Option -------
.
ML3000    CALL      CLIN0000
          GOTO      ML1000
.
ML9999    CHAIN     PGM
.
.*********************************************************************
.*                  INIT0000                                         *
.*            Display head, open file and read control file          *
.*********************************************************************
INIT0000  CALL      DISPHEAD
          MOVE      ONE,CNEWDS
.
          DISPLAY   *P50:24,"opening oprsesaf"
          OPEN      OPRSESA1,"oprsesaf"
          OPEN      OPRSESA7,"oprsesaf"
          DISPLAY   *P50:24,"opening oprsemaf"
          OPEN      OPRSEMA1,"oprsemaf"
.
          DISPLAY   *P50:24,"Opening patcodes"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P50:24,"Opening oprcliaf"
          OPEN      OPRCLIA1,"oprcliaf"
          OPEN      OPRCLIA2,"oprcliaf"
.
          DISPLAY   *P50:24,"Opening opropraf"
          OPEN      OPROPRA1,"opropraf"
          OPEN      OPROPRA2,"opropraf"
.
          DISPLAY   *P50:24,"Opening patdo1af"
          OPEN      PATDO1A2,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P50:24,"Opening controlf"
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TWENTY1;*247,PTCNDCQS
          READ      CONTROLF,FORTY;*81,OPCNODSC,*96,OPCNCDSC,*111,OPCNCLSU
          STRIP     OPCNODSC
          STRIP     OPCNCDSC
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,OPRTEMP1
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          IF        IBCNMHOS = 1
            OPEN      PATHSPA1,"pathspaf"
.
            DISPLAY   *P1:5,*EF,"Hospital Id : ";
            MOVE      TEN5,ECOL
            MOVE      FIVE,EVERT
            MOVE      SP3,CKYIDEF3
            MOVE      ONE,CKYIMAND               * set for mandatory
            CALL      PATHSPKY                   * get hospital
            COMPARE   TWO,EXIT                   * exit selected
            GOTO      INIT9000 IF EQUAL
.
            MOVE      KEY3,HOSCODE
            DISPLAY   *P22:5,*EL,PTHSNAME;
          ENDIF
.
          IF        IBCNMHOS = 1
            DISPLAY   *P64:24,"pathspaf";
            OPEN      PATHSPA1,"pathspaf"
          ENDIF
.
INIT9000  MOVE      ONE,EXIT
INIT9999  RETURN
.
.************************************************************************
.*                  SCRN0000                                            *
.*        Display main options screen                                   *
.************************************************************************
SCRN0000  HLINE     *G37,2,51,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = By ",OPCNODSC:
                    *P1:6,*V2LON,TWO,*HOFF," = By ",OPCNCDSC:
                    *P1:7,*V2LON,THREE,*HOFF," = Session Masters by ",OPCNCDSC:
                    *P1:9,"Select option : "
.
SCRN9999  RETURN
.
.************************************************************************
.*                  REDC0000                                            *
.*        Display main options screen                                   *
.************************************************************************
REDC0000  CALL      SCRN0000
          DISPLAY   *P17:9,*V2LON,MOPTION:
                    *P1:11,*EF,*V2LON,ZERO,*HOFF," = Return":
                    *P1:12,*V2LON,ONE,*HOFF," = All ",*+,OPCNCDSC,"s":
                    *P1:13,*V2LON,TWO,*HOFF," = By Single ",OPCNCDSC:
                    *P1:15,"Option : ",*V2LON,CLOPT
REDC9999  RETURN
.
.************************************************************************
.*                  REDD0000                                            *
.*        Display main options screen for theatre                       *
.************************************************************************
REDD0000  CALL      SCRN0000
          DISPLAY   *P17:9,*V2LON,MOPTION:
                    *P1:11,*EF,*V2LON,ZERO,*HOFF," = Return":
                    *P1:12,*V2LON,ONE,*HOFF," = All ",OPCNODSC,"s":
                    *P1:13,*V2LON,TWO,*HOFF," = By Single ",OPCNODSC:
                    *P1:15,"Option : ",*V2LON,CLOPT
REDD9999  RETURN
.
.************************************************************************
.*                  SLCT0000                                            *
.*        Get main selection                                            *
.************************************************************************
SLCT0000  KEYIN     *P17:9,*DV,UNDLN1:
                    *P17:9,*V2LON,MOPTION
.
          COMPARE   ZERO,MOPTION
          GOTO      SLCT9000 IF EQUAL
.
          BRANCH    MOPTION,SLCT8000,SLCT8000,SLCT8000
          BEEP
          GOTO      SLCT0000
.
SLCT8000  MOVE      ZERO,EXIT               * don't exit
          LOAD      KEY15,MOPTION,OPCNODSC,OPCNCDSC,SESSMAS
          PACK      CPHDROPT,BY,KEY15,SP10
          GOTO      SLCT9999
.
SLCT9000  MOVE      ONE,EXIT                * chose exit
.
SLCT9999  RETURN
+
.************************************************************************
.*                  THEA0000                                            *
.*        Produce a session master listing by theatre in date order     *
.************************************************************************
THEA0000  HLINE     *G37,2,51,80
          DISPLAY   *P1:11,*EF,*V2LON,ZERO,*HOFF," = Return":
                    *P1:12,*V2LON,ONE,*HOFF," = All ",*+,OPCNODSC,"s":
                    *P1:13,*V2LON,TWO,*HOFF," = By Single ",OPCNODSC
.
THEA1000  KEYIN     *P1:15,*EF,"Option : _":
                    *P10:15,*V2LON,THOPT
.
          BRANCH    THOPT,THEA2000,THEA3000
          COMPARE   ZERO,THOPT
          GOTO      THEA9995 IF EQUAL
.
          BEEP
          GOTO      THEA1000
.
.*** All Theatres Option ***
.
THEA2000  CALL      ALTH0000
          GOTO      THEA0000
.
.*** By Single Theatre Option ***
.
THEA3000  CALL      SITH0000
          GOTO      THEA0000      
. 
THEA9995  CALL      KILL0000
.
THEA9999  RETURN
+
.************************************************************************
.*                  ALTH0000                                            *
.*        List theatre session bookings for all theatres                *
.************************************************************************
ALTH0000  DISPLAY   *P51:2,*V2LON,"- All ",*+,OPCNODSC,"s "
          MOVE      TEN8,LINE
          CALL      DTRN0000                * Get date range
          BRANCH    EXIT,ALTH0000,ALTH9999
.
          CALL      PRDS0000                * positional read
.
ALTH2000  CALL      PRDK0000                * next read
          BRANCH    OVRCD,ALTH8000
.
          PACK      KEY25,OPSETHET,OPSEDATE,OPSETIME,OPSECLIN,SP70
          CALL      RAOPTP1
          IF        OVRCD=1
            CALL      WROPTP1               * write to temp file
          ELSE
            CALL      UPOPTP1
          ENDIF
          GOTO      ALTH2000                * go back for next record
.
ALTH8000  CALL      TOUT0000                * print out tempfile
.
ALTH9999  RETURN 
+
.************************************************************************
.*                  SITH0000                                            *
.*        List theatre session bookings for one theatres                *
.************************************************************************
SITH0000  DISPLAY   *P1:4,*EF:
                    *P51:2,*V2LON,"- By ",*+,OPCNODSC,SP1,*HOFF:
                    *P4:4,"Enter the ",*+,OPCNODSC," code you wish to print : "
          MOVELPTR  OPCNODSC,CCOL
          ADD       "40",CCOL
.
          KEYIN     *PCCOL:4,*EF,*DV,UNDLN6:
                    *PCCOL:4,*V2LON,THEATRE:
                    *PCCOL:4,*DV,THEATRE
.
          ENDSET    THEATRE
          APPEND    SP6,THEATRE
          RESET     THEATRE
.
          MATCH     SP6,THEATRE             * nothing entered?
          GOTO      SITH9500 IF EQUAL
.
          MATCH     QUEST,THEATRE 
          GOTO      SITH0100 IF NOT EQUAL   * ? not entered
.
          CALL      TQST0000
          BRANCH    EXIT,SITH9500
          GOTO      SITH0500
.
SITH0100  PACK      KEY6,THEATRE            * validate to file
          CALL      RAOPOPR1
          BRANCH    OVRCD,SITH9000
.
SITH0500  MOVE      SIX,LINE
          CALL      DTRN0000                * get date range
          BRANCH    EXIT,SITH0500,SITH0000  * go back if no date entered
.
          CALL      PRDS0000                * positional read
.
SITH2000  CALL      PRDK0000                * newt read
          BRANCH    OVRCD,SITH8000
.
          MATCH     OPSETHET,THEATRE        * got right theatre code?
          GOTO      SITH2000 IF NOT EQUAL   * get next record if not right 
.                                           
          PACK      KEY25,OPSETHET,OPSEDATE,OPSETIME,OPSECLIN,SP70
          CALL      RAOPTP1
          IF        OVRCD=1
            CALL      WROPTP1               * write to file in theatre then date
          ELSE
            CALL      UPOPTP1
          ENDIF
          GOTO      SITH2000                * go back for next record
.
SITH8000  CALL      TOUT0000                * print out tempfile
          GOTO      SITH9500
.
SITH9000  DISPLAY   *P1:24,*B,"Invalid Code.  ",*EL;
          CALL      HITENTER
          GOTO      SITH0000
.
SITH9500  CALL      SCRN0000
          DISPLAY   *P17:9,*V2LON,MOPTION
.
SITH9999  RETURN 
.
.*************************************************************************
.*                  CQST0000                     called by :  SITH0000   *
.*        Handle question mark on theatre                                *
.*************************************************************************
CQST0000  BRANCH    OPCNCLSU,CQST0400
          CALL      OPRCLIDS
          GOTO      CQST0500
.
CQST0400  MOVE      SP10,CLINIC 
          CALL      PATDOCDS
          BRANCH    EXIT,CQST9000
          MOVE      DOCCODE,CLINIC
          GOTO      CQST1500
.
CQST0500  DISPLAY   *P1:24,*EL,*+,OPCNCDSC," : "
          MOVELPTR  OPCNCDSC,CCOL
          ADD       FOUR,CCOL
.
CQST1000  KEYIN     *PCCOL:24,*DV,UNDLN6:
                    *PCCOL:24,*V2LON,CLINIC:
                    *PCCOL:24,*DV,CLINIC
          ENDSET    CLINIC
          APPEND    SP10,CLINIC
          RESET     CLINIC
.
          MATCH     SP6,CLINIC
          GOTO      CQST9000 IF EQUAL
.
          MATCH     QUEST,CLINIC
          GOTO      CQST1000 IF EQUAL
.
          BRANCH    OPCNCLSU,CQST1500
.
          PACK      KEY6,THEATRE
          CALL      RAOPCLI1
          BRANCH    OVRCD,CQST2000
. 
CQST1500  PACK      KEY6,CLINIC
          CALL      RDDOCT1
          BRANCH    OVRCD,CQST2000
. 
          MOVE      FALSE,EXIT
          DISPLAY   *P1:4,*EF:
                    *P51:2,*V2LON,"- By ",*+,OPCNCDSC,SP1,*HOFF:
                    *P4:4,"Enter the ",*+,OPCNCDSC:
                    " code you wish to print : ":
                    *V2LON,CLINIC
          GOTO      CQST9999
.
CQST2000  DISPLAY   *P1:24,*B,*+,OPCNCDSC," code not on file.  ",*EL;
          CALL      HITENTER
          GOTO      CQST0500
.
CQST9000  MOVE      TRUE,EXIT
.
CQST9999  RETURN
+
.************************************************************************
.*                  RDTH0000                                            *
.*        Display main options screen                                   *
.************************************************************************
RDTH0000  HLINE     *G37,2,51,80
          DISPLAY   *P1:3,*EF:
                    *P51:2,*V2LON,"- By ",*+,OPCNODSC,SP1,*HOFF:
                    *P4:4,"Enter the ",*+,OPCNODSC," code you wish to print":
                    " : ",*V2LON,THEATRE
RDTH9999  RETURN
+
.*************************************************************************
.*                  TQST0000                       called by :  SITH0000 *
.*        Handle question mark on theatre                                *
.*************************************************************************
TQST0000  CALL      OPROPRDS
.
TQST0500  DISPLAY   *P1:24,*+,OPCNODSC," : "
          MOVELPTR  OPCNODSC,CCOL
          ADD       FOUR,CCOL
.
TQST1000  KEYIN     *PCCOL:24,*DV,UNDLN6:
                    *PCCOL:24,*V2LON,THEATRE:
                    *PCCOL:24,*DV,THEATRE
          ENDSET    THEATRE
          APPEND    SP10,THEATRE
          RESET     THEATRE
.
          MATCH     SP6,THEATRE
          GOTO      TQST9000 IF EQUAL
.
          MATCH     QUEST,THEATRE
          GOTO      TQST1000 IF EQUAL
.
          PACK      KEY6,THEATRE
          CALL      RAOPOPR1
          BRANCH    OVRCD,TQST2000
. 
          MOVE      FALSE,EXIT
          CALL      RDTH0000
          GOTO      TQST9999
.
TQST2000  DISPLAY   *P1:24,*B,*+,OPCNODSC," code not on file.  ",*EL;
          CALL      HITENTER
          GOTO      TQST0500
.
TQST9000  MOVE      TRUE,EXIT
.
TQST9999  RETURN
+
.************************************************************************
.*                              DTRN0000                                *
.*                  Get date range to be printed                        *
.************************************************************************
DTRN0000  UNPACK    SP20,PRDTFR,PRDTTO
          COMPARE   THREE,MOPTION
          GOTO      DTRN7000 IF EQUAL       * no date range required
.
          DISPLAY   *P1:LINE,*EF,"From Date  :"
          ADD       ONE,LINE
          DISPLAY   *P1:LINE,"To Date    :"
          SUB       ONE,LINE
          MOVE      TEN4,CCOL
          MOVE      LINE,CVERT
          UNPACK    SP20,CCENT,CDAY,CMON,CYEAR   * clear variables
          MOVE      ZERO,CHIGHLT                 * highlight on
          MOVE      CCC,CCENT                    * default century
.
          CALL      KEYDATE                      * get a date
          BRANCH    CQUEST,DTRN9000              * "?" input not allowed
          BRANCH    OVRCD,DTRN9000
.
          CALL      PACDATE
          MOVE      CPCDATE,PRDTFR               * set up print date from
          PACK      DATEFR,CCENT,CYEAR,CMON,CDAY       * set up date from
.
          ADD       ONE,LINE
.
DTRN1600  MOVE      TEN4,CCOL
          MOVE      LINE,CVERT
          MOVE      ZERO,CHIGHLT            * highlight on
          UNPACK    SP20,CCENT,CDAY,CMON,CYEAR * clear variables
          MOVE      CCC,CCENT               * default century
.
          CALL      KEYDATE
          BRANCH    CQUEST,DTRN1600         * "?" input not allowed
          BRANCH    OVRCD,DTRN1600          * nothing entered
.
          SUB       ONE,LINE
          CALL      PACDATE
          MOVE      CPCDATE,PRDTTO          * set up print date to
          PACK      DATETO,CCENT,CYEAR,CMON,CDAY       * set up date to
.
          MATCH     DATEFR,DATETO           * valid range?
          GOTO      DTRN2500 IF LESS
          GOTO      DTRN3000
.
DTRN2500  DISPLAY   *P1:24,*B,"Invalid date range.  ",*EL;
          CALL      HITENTER
          GOTO      DTRN0000
.
DTRN3000  CALL      CONTQST
          BRANCH    CEXIT,DTRN7000,DTRN8000,DTRN9000
.
DTRN7000  MOVE      FALSE,EXIT
          GOTO      DTRN9999
.
DTRN8000  MOVE      ONE,EXIT
          GOTO      DTRN9999
.
DTRN9000  MOVE      TWO,EXIT
.
DTRN9999  RETURN
.
.************************************************************************
.*                              TOUT0000                                *
.*                 Produce a printout by theatre                        *
.************************************************************************
TOUT0000  PACK      KEY25,SP30  
          CALL      RSOPTP1                 * Position at start of file
.
          MOVE      ZERO,RECPR              * initialize records printed to zero
          CLOCK     TIME,CTIMEIS 
          MOVE      ZERO,CPAGENO
          MOVE      "55",CLNO
          UNPACK    SP50,OPSETHET,OPRMDESC  * clear variables
          MOVE      ONE,NPAG                * newpage to no
          DISPLAY   *P1:24,*EL,"Printing code : "
.
TOUT1000  CALL      RKOPTP1                 * read next record
          BRANCH    OVRCD,TOUT9000
.
          MATCH     THEATRE,OPSETHET        * new theatre = new page
          CALL      NWPG0000 IF NOT EQUAL
.
          CALL      PRNT0000                * print a record
          ADD       ONE,RECPR               * add one to records printed
          MOVE      OPSETHET,THEATRE
          GOTO      TOUT1000 
.
TOUT9000  IF        MOPTION <> 3
            CALL      UND132
          ENDIF
          PRINT     *N,"Records Printed : ",RECPR
          PRINT     *N,"*** End of Report ***"
.
TOUT9999  RETURN
.
.************************************************************************
.*                              NWPG0000                                *
.*               Finish last page and begin new page                    *
.************************************************************************
NWPG0000  BRANCH    NPAG,NWPG9000
          IF        MOPTION <> 3
            CALL      UND132
          ENDIF
          PRINT     *N,"Records Printed : ",RECPR
          CALL      HEAD0000
          MOVE      ZERO,RECPR              * clear records printed
.
NWPG9000  MOVE      ZERO,NPAG               * new page next time? = yes
.
NWPG9999  RETURN
.
.************************************************************************
.*                              CLIN0000                                *
.*           Produce a listing of sessions by clinic                    *
.************************************************************************
CLIN0000  HLINE     *G37,2,51,80
          DISPLAY   *P1:11,*EF,*V2LON,ZERO,*HOFF," = Return":
                    *P1:12,*V2LON,ONE,*HOFF," = All ",*+,OPCNCDSC,"s":
                    *P1:13,*V2LON,TWO,*HOFF," = By Single ",OPCNCDSC
.
CLIN1000  KEYIN     *P1:15,"Option : _":
                    *P10:15,*V2LON,CLOPT
.
          BRANCH    CLOPT,CLIN2000,CLIN3000
          COMPARE   ZERO,CLOPT              * nothing entered?
          GOTO      CLIN9995 IF EQUAL
.
          BEEP
          GOTO      CLIN1000
.
.**** All clinics Option ***
.
CLIN2000  CALL      ALCL0000
          GOTO      CLIN0000
.
.*** By Single Clinic ***
.
CLIN3000  CALL      SICL0000
          GOTO      CLIN0000
. 
CLIN9995  CALL      KILL0000
.
CLIN9999  RETURN
.
.************************************************************************
.*                              ALCL0000                                *
.*                List theatre session bookings for all clinics         *
.************************************************************************
ALCL0000  DISPLAY   *P51:2,*V2LON,"- All ",*+,OPCNCDSC,"s "
          MOVE      TEN8,LINE
          CALL      DTRN0000
          BRANCH    EXIT,ALCL0000,ALCL9999
.
          CALL      PRDS0000                * positional read
.
ALCL2000  CALL      PRDK0000                * next read
          BRANCH    OVRCD,ALCL8000
.
          PACK      KEY25,OPSECLIN,OPSEDATE,OPSETIME,OPSETHET,SP70
          CALL      RAOPTP2
          IF        OVRCD=1
            CALL      WROPTP2               * write to tempfile in clinic order
          ELSE
            CALL      UPOPTP2
          ENDIF
          GOTO      ALCL2000                * go back and get next record
.
ALCL8000  CALL      COUT0000                * printout tempfile
.
ALCL9999  RETURN 
.
.************************************************************************
.*                              SICL0000  
.*                List theatre session bookings for one theatres        *
.************************************************************************
SICL0000  DISPLAY   *P1:4,*EF:
                    *P51:2,*V2LON,"- By ",*+,OPCNCDSC,SP1,*HOFF:
                    *P4:4,"Enter the ",*+,OPCNCDSC," code you wish to print : " 
          MOVELPTR  OPCNCDSC,CCOL
          ADD       "40",CCOL
.
          KEYIN     *PCCOL:4,*EF,*DV,UNDLN6:
                    *PCCOL:4,*V2LON,CLINIC:
                    *PCCOL:4,*DV,CLINIC
.
          ENDSET    CLINIC
          APPEND    SP6,CLINIC
          RESET     CLINIC
.
          MATCH     SP6,CLINIC              * nothing entered?
          GOTO      SICL9500 IF EQUAL
.
          MATCH     QUEST,CLINIC              * nothing entered?
          GOTO      SICL0400 IF NOT EQUAL
.
          CALL      CQST0000
          BRANCH    EXIT,SICL9500
          GOTO      SICL0600
.
SICL0400  PACK      KEY6,CLINIC             * validate against file
          BRANCH    OPCNCLSU,SICL0500
.
          CALL      RAOPCLI1
          BRANCH    OVRCD,SICL9000
          GOTO      SICL0600
.
SICL0500  CALL      RDDOCT1
          BRANCH    OVRCD,SICL9000
.
SICL0600  MOVE      SIX,LINE
          CALL      DTRN0000               * get date range
          BRANCH    EXIT,SICL0600,SICL0000 * no date entered?
.
          CALL      PRDS0000                * positional read
.
SICL2000  CALL      PRDK0000                * next read
          BRANCH    OVRCD,SICL8000
.
          MATCH     OPSECLIN,CLINIC        * right clinic code?
          GOTO      SICL2000 IF NOT EQUAL  * get next record if not
.
          PACK      KEY25,OPSECLIN,OPSEDATE,OPSETIME,OPSETHET,SP70
          CALL      RAOPTP2
          IF        OVRCD=1
            CALL      WROPTP2              * write to tempfile in clinic order
          ELSE
            CALL      UPOPTP2
          ENDIF
          GOTO      SICL2000
.
SICL8000  CALL      COUT0000               * print out tempfile
          GOTO      SICL9500
.
SICL9000  DISPLAY   *P1:24,*B,"Invalid Code.  ",*EL;
          CALL      HITENTER
          GOTO      SICL0000
.
SICL9500  CALL      SCRN0000
          DISPLAY   *P17:9,*V2LON,MOPTION
.
SICL9999  RETURN 
+
.************************************************************************
.*                              COUT0000                                *
.*                 Produce a printout by clinic                         *
.************************************************************************
COUT0000  PACK      KEY25,SP30              * goto start of file
          CALL      RSOPTP2
.
          UNPACK    SP50,OPSECLIN,OPCLDESC  * clear variables
          MOVE      ONE,NPAG                * new page = no
          MOVE      ZERO,CPAGENO
          MOVE      ZERO,RECPR              * no records printed
          CLOCK     TIME,CTIMEIS
          MOVE      "55",CLNO
          DISPLAY   *P1:24,*EL,"Printing code : "
.
COUT1000  CALL      RKOPTP2                 * read next record
          BRANCH    OVRCD,COUT9000
.
          MATCH     CLINIC,OPSECLIN         * different clinic = new page
          CALL      NWPG0000 IF NOT EQUAL
.
          CALL      PRNT0000                * printout one record
          ADD       ONE,RECPR               * add one record to records printed
          UNPACK    SP50,NAME24,OPCLDESC    * clear variables
          MOVE      OPSECLIN,CLINIC         * update clinic
          GOTO      COUT1000 
.
COUT9000  IF        MOPTION <> 3
            CALL      UND132
          ENDIF
          PRINT     *N,"Records Printed : ",RECPR
          PRINT     *N,"*** End of Report ***"
.
COUT9999  RETURN
.
.*********************************************************************
.*                  PRDS0000                    Called by : xxxx0000 *
.*        Perform the required RDS on either oprsesaf or oprsemaf    *
.*********************************************************************
PRDS0000  DISPLAY   *P1:24,"Processing data - ",*V2LON,"Please Wait",*EL;
          CALL      CREA0000
          BRANCH    MOPTION,PRDS1000,PRDS1000,PRDS3000
          GOTO      PRDS9999
.
PRDS1000  PACK      KEY22,DATEFR,SP70
          CALL      RSOPSES7                * positional read session file
          GOTO      PRDS9999
.
PRDS3000  PACK      KEY21,SP70
          CALL      RSOPSEM1                * positional read master file
.
PRDS9999  RETURN
+
.*********************************************************************
.*                  PRDK0000                    Called by : xxxx0000 *
.*        Perform the required RDK on either oprsesaf or oprsemaf    *
.*        Returns : OVRCD = 1     finished for date range            *
.*********************************************************************
PRDK0000  BRANCH    MOPTION,PRDK1000,PRDK1000,PRDK3000
          GOTO      PRDK9999
.
PRDK1000  CALL      RKOPSES7                * next read session file
          BRANCH    OVRCD,PRDK9999          * end of file
.
          IF        IBCNMHOS = 1
            MATCH     OPSEHOSP,HOSCODE
            GOTO      PRDK1000 IF NOT EQUAL
          ENDIF
.
          MATCH     DATETO,OPSEDATE
          GOTO      PRDK9999 IF EQUAL
          GOTO      PRDK9999 IF LESS        * haven't read too far
.
          MOVE      ONE,OVRCD               * read too far
          GOTO      PRDK9999
.
.         set up the ses file variables
.
PRDK3000  CALL      RKOPSEM1                * next read master file
          BRANCH    OVRCD,PRDK9999          * end of file
.         Filtering records based on Hospital Id
.
          IF        IBCNMHOS = 1
            MATCH     OPSMHOSP,HOSCODE
            GOTO      PRDK3000 IF NOT EQUAL
          ENDIF
.
          MOVE      OPSMTHET,OPSETHET       * theatre
          PACK      OPSEDATE,OPSMDAYI,OPSMFTYP,SP8   * day/freq indicator
          MOVE      OPSMTIME,OPSETIME       * start time
          MOVE      OPSMCLIN,OPSECLIN       * clinic
          MOVE      OPSMDURA,OPSEDURA       * duration
          MOVE      OPSMENDT,OPSEENDT       * end time
          MOVE      OPSMSTAT,OPSESTAT       * status
          MOVE      OPSMTYPE,OPSETYPE       * session type
.
PRDK9999  RETURN
+
.*********************************************************************
.                   HEAD0000
.         Print the header for the printouts
.*********************************************************************
HEAD0000  MOVE      ONE,CNOUNDLN
          CALL      HEAD132
          BRANCH    MOPTION,HEAD1000,HEAD2000,HEAD2000
.
.         operating room/theatre option
.
HEAD1000  MOVE      OPCNCDSC,CLTHHEAD        * clinic
          MOVE      "Invalid Code",OPRMDESC
          PACK      KEY6,OPSETHET
          CALL      RDOPOPR1
          MOVE      ACTIV,OPSTAT
          COMPARE   ONE,OPRMSTAT
          IF        @EQUAL
            MOVE       INACTIV,OPSTAT
          ENDIF
          PRINT     *N,*30,"From :  ",PRDTFR,*54,"To :  ",PRDTTO:
                    *N,*30,*+,OPCNODSC," : ",OPSETHET,*58,OPRMDESC,OPSTAT,*N
          IF          IBCNMHOS =1
            PRINT     *N,*1,"Hospital : ",PTHSNAME
          ENDIF
.
          GOTO      HEAD5000
.
.         surgeon/clinic option
.
HEAD2000  MOVE      OPCNODSC,CLTHHEAD       * set as op. room
          PACK      KEY6,OPSECLIN
          CALL      FNAM0000                * get the description
.
          COMPARE   THREE,MOPTION
          GOTO      HEAD3000 IF EQUAL       * for session masters
.
          PRINT     *30,"From :  ",PRDTFR,*54,"To :  ",PRDTTO
.
HEAD3000  PRINT     *30,*+,OPCNCDSC," : ",KEY6,SP1,NAME24,*N
          IF          IBCNMHOS =1
            PRINT     *N,*1,"Hospital : ",PTHSNAME
          ENDIF
.
.
.         print the table heading
.
HEAD5000  CALL      UND132
          PRINT     "| Session Date | Start | Durn | End    | Status      |":
                    " Session Type         | ",CLTHHEAD,*111,"| Comments":
                    *132,"|"
          CALL      UND132
          MOVE      SP30,NAME24
.
HEAD9999  RETURN
.
.*********************************************************************
.                   PRNT0000
.         Print a line of output
.*********************************************************************
PRNT0000  COMPARE   THREE,MOPTION
          GOTO      PRNT2000 IF EQUAL       * session master option
.
          UNPACK    OPSEDATE,CCENT,CYEAR,CMON,CDAY * set up each print date
          CALL      PACDATE
          MOVE      CPCDATE,PRDT            * print date
          MOVE      "Cancelled",STAT0
          MOVE      "Booked   ",STAT1
          MOVE      "Extra    ",STAT2
          MOVE      SP20,DIM7 
          GOTO      PRNT3000
.
.         format the day of the session
.
PRNT2000  MOVE      OPSEDATE,TCODE
          UNPACK    TCODE,ANS,DIM1
          MOVE      ANS,FORM1               * day indicator
          LOAD      PRDT,FORM1,DMON,DTUE,DWED,DTHU,DFRI,DSAT,DSUN
          MOVE      "Active   ",STAT0
          MOVE      "Inactive ",STAT1
          MOVE      "         ",STAT2
          MOVE      DIM1,FORM1              * frequency indicator
          MOVE      FORM1,OPSMFTYP          * frequency indicator
          MOVE      DMONTH,DIM7 
          LOAD      DIM7,FORM1,DYEARLY      * set the frequency
.
PRNT3000  PACK      KEY5,ST,OPSETYPE        * read in code desc for type of sess
          CALL      RDCODE1
.
          COMPARE   "50",CLNO
          CALL      HEAD0000 IF NOT LESS
.
. if doing by theatre, want to display clinic  in the report and conversely
. if doing by clinic,  want to display theatre in the report 
.
          MOVE      SP30,NAME24
          BRANCH    MOPTION,PRNT4000,PRNT5000,PRNT5000
          GOTO      PRNT9999
.        
. theatre option
.
PRNT4000  MOVE      OPSECLIN,CLTH           * clinic code into print variable
          MOVE      OPSECLIN,KEY6           * set the key
          CALL      FNAM0000                * get description
          GOTO      PRNT6000
.
. clinic option
.
PRNT5000  MATCH     OPSETHET,SP6
          GOTO      PRNT6000 IF EQUAL
.
          MOVE      OPSETHET,CLTH           * theatre code into print variable
          PACK      KEY6,CLTH
          CALL      RDOPOPR1
          MOVE      OPRMDESC,NAME24
.
PRNT6000  MOVE      STAT0,PRSTAT
          LOAD      PRSTAT,OPSESTAT,STAT1,STAT2       * session status
          DISPLAY   *P17:24,OPSETHET
.
          MOVE      SP5,KEY5
          IF        OPSMFTYP = ZERO
            MOVE      OPSMMRFQ,KEY5
          ENDIF
          PRINT     "| ",PRDT:
                    *16,"| ",OPSETIME," | ",OPSEDURA," | ",OPSEENDT:
                    "  | ",PRSTAT,"   | ",TDESC:
                    *77,"| ",CLTH,SP1,NAME24,*111,"| ",DIM7,SP1,KEY5:
                    *132,"|"
          ADD       ONE,CLNO
          MOVE      SP30,NAME24
          MOVE      SP30,PRSTAT
          UNPACK    SP30,TDESC,CLTH
          COMPARE   THREE,MOPTION
          GOTO      PRNT9999 IF NOT EQUAL
.
. for option three, print the extra details
.
          PRINT     "|",*16,"| Breaks : ",OPSMBRKT:
                    " | Preparation Time: ",OPSMPREP:
                    " | Time Period : ",OPSMTPER:
                    "    | Anaesthetist's - Usual : ",OPSMANAE:
                    " | Extra : ",OPSMXDOC,"     |"
          ADD       ONE,CLNO
.
          IF        OPSMFTYP = ONE
            PRINT     "|",*16,"|",*31,"|",*54,"| Frequency : ",OPSMYRFQ,*132,"|"
            ADD       ONE,CLNO
          ENDIF
          CALL      UND132
.
PRNT9999  RETURN
.
.*********************************************************************
.*                  FNAM0000                    Called by :          *
.*        Format Clinic/Surgeon name                                 *
.*        Para's  : KEY6          the code                           *
.*********************************************************************
FNAM0000  MOVE      "Invalid Code",NAME24
          BRANCH    OPCNCLSU,FNAM5000       * using doctor codes
.
.         validate using clinic file
.
          CALL      RDOPCLI1                * read clinic file
          BRANCH    OVRCD,FNAM9999          * invalid code
          MOVE      OPCLDESC,NAME24         * use clinic description
.
          MATCH     SP6,OPCLDOCT
          GOTO      FNAM9999 IF EQUAL       * no doctor code to use
          MOVE      OPCLDOCT,KEY6           * use the doctor code
.
.         validate using doctors file
.
FNAM5000  CALL      RDDOCT1                 * read docs file
          BRANCH    OVRCD,FNAM9999          * invalid code
.
          MOVE      DSNAME,PACSNAME         * surname
          MOVE      DGNAME,PACGNAME         * given name
          MOVE      DTITL,PACTITLE          * title
          MOVE      TWO,PACFRMT             * SURNAME, TITLE GIVEN
          CALL      PACNAME                 * format the name
          MOVE      PACFNAME,NAME24         * set up doctors name
.
FNAM9999  RETURN
.
.**********************************************************************
.*                  CREA0000                                          *
.*        Create the Indexed Temp File                                *
.**********************************************************************
CREA0000  CALL      KILL0000                       * kill existing temp file
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    OPRTEMP1,CMDLINE
          APPEND    " 117 u1-6,7-14,15-19,20-25 u20-25,7-14,15-19,1-6",CMDLINE
          APPEND    SP30,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPFIL1,OPRTEMP1
          OPEN      TEMPFIL2,OPRTEMP1
.
CREA9999  RETURN
+
.**********************************************************************
.*                  KILL0000                                          *
.*        Kill the Tempfile if it Already Exists                      *
.**********************************************************************
KILL0000  CLEAR     CMDLINE
          CLOSE     TEMPFIL1
          CLOSE     TEMPFIL2
          APPEND    "iserase ",CMDLINE
          APPEND    OPRTEMP1,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
KILL9999  RETURN
+
.************************************************************************
.*                  I/O                                                 *
.*        I/O routines for temp file                                    *
.************************************************************************
.
.------ I/O Routines for temp file ------
.
RAOPTP1   MOVE      ZERO,OVRCD
          RESET     KEY25
          READ      TEMPFIL1,KEY25;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSOPTP1   MOVE      ZERO,OVRCD
          RESET     KEY25
          READ      TEMPFIL1,KEY25;;
          RETURN
.
RDOPTP1   MOVE      ZERO,OVRCD
          RESET     KEY25
          READ      TEMPFIL1,KEY25;OPSETHET,OPSEDATE,OPSETIME,OPSECLIN,OPSEDURA:
                                   OPSEENDT,OPSESTAT,OPSETYPE,OPSMBRKT,OPSMPREP:
                                   OPSMTPER,OPSMANAE,OPSMXDOC,OPSMMRFQ,OPSMYRFQ
          GOTO      OVERCOND IF OVER
          RETURN
.
RKOPTP1   MOVE      ZERO,OVRCD
          RESET     KEY25
          READKS    TEMPFIL1;OPSETHET,OPSEDATE,OPSETIME,OPSECLIN,OPSEDURA:
                             OPSEENDT,OPSESTAT,OPSETYPE,OPSMBRKT,OPSMPREP:
                             OPSMTPER,OPSMANAE,OPSMXDOC,OPSMMRFQ,OPSMYRFQ
          GOTO      OVERCOND IF OVER
          RETURN
.
RPOPTP1   MOVE      ZERO,OVRCD
          RESET     KEY25
          READKP    TEMPFIL1;OPSETHET,OPSEDATE,OPSETIME,OPSECLIN,OPSEDURA:
                             OPSEENDT,OPSESTAT,OPSETYPE,OPSMBRKT,OPSMPREP:
                             OPSMTPER,OPSMANAE,OPSMXDOC,OPSMMRFQ,OPSMYRFQ
          GOTO      OVERCOND IF OVER
          RETURN
.
WROPTP1   MOVE      ZERO,OVRCD
          RESET     KEY25
          WRITE     TEMPFIL1,KEY25;OPSETHET,OPSEDATE,OPSETIME,OPSECLIN,OPSEDURA:
                                   OPSEENDT,OPSESTAT,OPSETYPE,OPSMBRKT,OPSMPREP:
                                   OPSMTPER,OPSMANAE,OPSMXDOC,OPSMMRFQ,OPSMYRFQ
          RETURN
.
UPOPTP1   UPDATE    TEMPFIL1;OPSETHET,OPSEDATE,OPSETIME,OPSECLIN,OPSEDURA:  
                             OPSEENDT,OPSESTAT,OPSETYPE,OPSMBRKT,OPSMPREP:
                             OPSMTPER,OPSMANAE,OPSMXDOC,OPSMMRFQ,OPSMYRFQ
          RETURN
.
DEOPTP1   RESET     KEY25
          DELETE    TEMPFIL1,KEY25
          RETURN
.
RAOPTP2   MOVE      ZERO,OVRCD
          RESET     KEY25
          READ      TEMPFIL1,KEY25;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSOPTP2   MOVE      ZERO,OVRCD
          RESET     KEY25
          READ      TEMPFIL2,KEY25;;
          RETURN
.
RDOPTP2   MOVE      ZERO,OVRCD
          RESET     KEY25
          READ      TEMPFIL2,KEY25;OPSETHET,OPSEDATE,OPSETIME,OPSECLIN,OPSEDURA:
                             OPSEENDT,OPSESTAT,OPSETYPE,OPSMBRKT,OPSMPREP:
                             OPSMTPER,OPSMANAE,OPSMXDOC,OPSMMRFQ,OPSMYRFQ
          GOTO      OVERCOND IF OVER
          RETURN
.
RKOPTP2   MOVE      ZERO,OVRCD
          RESET     KEY25
          READKS    TEMPFIL2;OPSETHET,OPSEDATE,OPSETIME,OPSECLIN,OPSEDURA:
                             OPSEENDT,OPSESTAT,OPSETYPE,OPSMBRKT,OPSMPREP:
                             OPSMTPER,OPSMANAE,OPSMXDOC,OPSMMRFQ,OPSMYRFQ
          GOTO      OVERCOND IF OVER
          RETURN
.
RPOPTP2   MOVE      ZERO,OVRCD
          RESET     KEY25
          READKP    TEMPFIL2;OPSETHET,OPSEDATE,OPSETIME,OPSECLIN,OPSEDURA:
                             OPSEENDT,OPSESTAT,OPSETYPE,OPSMBRKT,OPSMPREP:
                             OPSMTPER,OPSMANAE,OPSMXDOC,OPSMMRFQ,OPSMYRFQ
          GOTO      OVERCOND IF OVER
          RETURN
.
WROPTP2   MOVE      ZERO,OVRCD
          RESET     KEY25
          WRITE     TEMPFIL2,KEY25;OPSETHET,OPSEDATE,OPSETIME,OPSECLIN,OPSEDURA:
                             OPSEENDT,OPSESTAT,OPSETYPE,OPSMBRKT,OPSMPREP:
                             OPSMTPER,OPSMANAE,OPSMXDOC,OPSMMRFQ,OPSMYRFQ
          RETURN
.
UPOPTP2   UPDATE    TEMPFIL2;OPSETHET,OPSEDATE,OPSETIME,OPSECLIN,OPSEDURA:  
                             OPSEENDT,OPSESTAT,OPSETYPE,OPSMBRKT,OPSMPREP:
                             OPSMTPER,OPSMANAE,OPSMXDOC,OPSMMRFQ,OPSMYRFQ
          RETURN
.
DEOPTP2   RESET     KEY25
          DELETE    TEMPFIL2,KEY25
          RETURN
.
.************************************************************************
.
          INC       STD001IO
          INC       TFILENAM                * Generate Temp File Name
          INC       IBASEQIO/INC            * Sequential Numbers File
          INC       WEBERRIO/INC            * Web Server Error Logging
.
          INC       OPRCLIDS
          INC       OPRCLIIO/INC
          INC       OPROPRDS
          INC       OPROPRIO/INC
          INC       OPRSESIO/INC
          INC       OPRSEMIO/INC
          INC       PATDOCDS
          INC       PATDOCKY
          INC       PATHSPKY
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATHSPIO/INC
................................................................................

.******************************************************************************
.*                                                                            *
.*               P R O G R A M  S U M M A R Y                                 *
.*               -------------  -------------                                 *
.*                                                                            *
.*      Program Name  :   IBARCP75                                            *
.*                                                                            *
.*      Function      :   Register File Maintenance                           *
.*                                                                            *
.*   Modifications:                                                           *
.******************************************************************************
.*  V9.09.01  16/05/2007  J.Tan         CAR 140282                            *
.*            Mods to parameter 'Using FMS' for Other                         *
.*  V5.09.B01 13.12.2000 Charles Handaya                                      *
.*            Added New System Code 92 - GST Sundry Cash Sales                *
.*            RCPREGFD, RCPREGIO changes                                      *
.*            Added GST Code Keyin on the Screen (Requires :  IBAGSTFD        *
.*                                                   IBAGSTIO,IBAGSTKY        *
.*            (part of BAS Statement Modification)                            *
.******************************************************************************
.*          V5.07.01  15/09/1999  Charles Handaya                             *
.*                    Added New Register - Nursing Home                       *
.*         V5.07.B01  05/11/1998  Davin                                       *
.*                    Mods for 507 & removed old debtors                      *
.******************************************************************************
. *         V5.01.00  Matt Surridge Mt Alvernia            *
. *                   PORTED FORM Release 6                *
. *         V5.01.01  11/01/94 Matt Surridge Mt Alvernia   *
. *                   Added IBA System Code 93 ( Patient   *
. *                   Billing/Deposits )                   *
. *         V5.01.02  13/01/94 Matt Surridge Mt Alvernia   *
. *                   Added Deposit type checks and extra  *
. *                   Keyin of Ledger No if GL Interface   *
. *                   is off and RCCNUPBR is on.           *
. *         V5.01.03  31/01/94 Matt Surridge Mt Alvernia   *
. *                   Removed display of chq acc descript. *
. *                   when bank rec is off.                *
. *         V5.01.04  24/02/94  Ian Rutt                   *
. *                   Standards Upgrade                    * 
. *         V5.01.05  08/02/94  Ian Rutt                   *
. *                   Added record locking, added new      *
. *                   keyin (RCPREGKY) for register code   * 
. *         V5.01.06  02/06/94  ROD                        *
. *                   Access invoices across multi-database*
. *         V5.02.01  22/12/94  Sandra Barcham             *
. *                   Update REGCHQCD & REGGENBK           *
. *                   srf 133710                           *
. *         V5.03.01  11.07.95 B.G.Ackland                 *
. *                   Added New Sundry A/R System          *
.*        V5.03.02  04/01/1996  Greg Horvat                                   *
.*                  Removed the menu program, security key, IBA screen        *
.*                  password & data directory read on the control file        * 
. *         V5.03.03  01/07/96  Howellsy  SRF 641697       *
. *                   Fixed I*C on sundry debtors ledger   *
.*                                                                            *
.******************************************************************************
          INC       FMSSTDDF
          INC       RCP001
          INC       RCPREGFD/INC
          INC       RCPCONFD/INC
          INC       PATCODFD/INC
          INC       FMSAMAFD/INC
          INC       FMSRESFD/INC
          INC       FMSDISFD/INC
          INC       FMSCHQFD/INC
          INC       FMSCAFFD/INC
          INC       FMSCSAFD/INC
          INC       FMSLMAFD/INC
          INC       PATCONFD/INC
.
          INC       IBAGKIFD/INC
          INC       IBAGSTFD/INC
.
FLAGDES   FORM      1
ACCNO     DIM       12
CANCEL    FORM      "0"
CHEQACC   DIM       15
CHEQDESC  DIM       30
CHG       FORM      1
CODERG    INIT      "RG"
CODEDP    INIT      "DP"
COL       FORM      2
DISSNO    DIM       5
EXFLAG    FORM      1
FLAG      DIM       1
FLAGQUES  FORM      1
FIBACD    FORM      2
FIELD     FORM      1
IBA       FORM      1
IBACD     DIM       2
DIM2A     DIM       2
DIM2B     DIM       2
DIM12A    DIM       12
DIM12B    DIM       12
LEDNO     DIM       2
MODE      DIM       1
MODE1     DIM       1
RESPCD    DIM       4
SLEDNO    FORM      2
SPACES    INIT      "                                       "
SRDIM20   DIM       20
SRDIM20A  DIM       20
SUNDT     DIM       2
DIM25A    DIM       25
DIM25B    DIM       25
DIM25C    DIM       25
DIM25D    DIM       25
SP12      INIT      "            "
SP14      INIT      "              "
VERT      FORM      2
.
Z20       INIT      "ZZZZZZZZZZZZZZZZZZZZ"
.
PRGID     INIT      "IBARCP75"
PRGNAM    INIT      "Register Maintenance"
VERSION   INIT      "V12.01.00"
.
          CALL      DISPHEAD
          MOVE      ONE,CNEWDS
          MOVE      ZERO,CKYIMAND
.
          DISPLAY   *P56:24,"Opening rcpregaf";
          OPEN      RCPREGA1,"rcpregaf"
.
          DISPLAY   *P64:24,"rcpregaf";
          OPEN      RCPREGA2,"rcpregaf"
.
          DISPLAY   *P64:24,"fmschqaf";
          OPEN      FMSCHQA1,"fmschqaf"
.
          DISPLAY   *P64:24,"ibagkiaf";
          OPEN      IBAGKIA1,"ibagkiaf"
.
          DISPLAY   *P64:24,"ibagkiaf";
          OPEN      IBAGKIA2,"ibagkiaf"
.
          DISPLAY   *P64:24,"ibagstaf";
          OPEN      IBAGSTA1,"ibagstaf"
.
          DISPLAY   *P64:24,"ibagstaf";
          OPEN      IBAGSTA2,"ibagstaf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,THIRTY6;*43,HSECLEV,*85,HSBD,HSYS1:
                    HSYS2,HSYS3,HSYS4,HSYS5,HSYS6,HSYS7,HSYS8,HSYS9,HSYS10:
                    HSYS11,HSYS12,HSYS13,HSYS14,HSYS15,HSYS16,HSYS17,HSYS18:
                    HSYS19,HSYS20,HFORMAT,*176,RCCNUPBR,*193,HRECPT
.
          READ      CONTROLF,TEN9;*212,CFEETYP
.
. ---- open general ledger files ----
.
          IF        HSYS1=0 & RCCNUPBR=0 
            GOTO      NEXT1 
          ENDIF
.          COMPARE   ONE,HSYS1
.          GOTO      NEXT1 IF NOT EQUAL
.
          DISPLAY   *P64:24,"fmslmaaf";
          OPEN      FMSLMAA1,"fmslmaaf"
.
          DISPLAY   *P64:24,"fmsamaaf";
          OPEN      FMSAMAA1,"fmsamaaf"
.
          DISPLAY   *P64:24,"fmsresaf";
          OPEN      FMSRESA1,"fmsresaf"
.
          DISPLAY   *P64:24,"fmsdisaf";
          OPEN      FMSDISA1,"fmsdisaf"
.
          DISPLAY   *P64:24,"fmscafaf";
          OPEN      FMSCAFA1,"fmscafaf"
.
          DISPLAY   *P64:24,"fmscsaaf";
          OPEN      FMSCSAA1,"fmscsaaf"
.
          PACK      DIM2,SP1,EXITCHAR
.
NEXT1
.
. ---- open patient management ----
.
NEXT2     COMPARE   "1",HSYS2
          GOTO      MENU IF NOT EQUAL
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
. ---- display menu ----
.
MENU      CALL      ULREGA1
          CALL      SELOPTN 
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
REPEAT    BRANCH    OPTION OF INSERT,CHANGE,DELETE
.
.        Add a Register
.
INSERT 
          CALL      DISP1                    * display input screen
          CALL      CLEARVAR                 * clear variables
.
          CALL      KEYCODE                  * key register code
          BRANCH    EXFLAG TO MENU
.
          CALL      KEYDESC                  * key description
          CALL      KEYGLED                  * key GL income account/chq ac
          BRANCH    CANCEL TO MENU
.
          CALL      KEYDISS                  * key GL dissection code
          CALL      KEYRESP                  * key GL responsibility code
          CALL      KEYGRP                   * key register group
          CALL      KEYIBA                   * key IBA system code
..          BRANCH    EXIT,INSERT
          CALL      KEYGST00                 * key GST code
.
. ---- ok to post ----
.
          CALL      SELECT
.
          BRANCH    EXIT,INSPOSTY,INSERT
.
. ---- write record ----
.
INSPOSTY  MOVE      REGCODE,KEY3
          CALL      WRREGA1
          IF        OVRCD = 2
            CALL      RECL0000
          ENDIF
          GOTO      INSERT
.
.        Change a Register
.
CHANGE  
          CALL      DISP1                          * display screen
          CALL      CLEARVAR                       * clear variables
.
          CALL      KEYCDEA                        * input register code
          BRANCH    EXFLAG TO MENU
.
          CALL      DISPVAR                        * display current details
.
          CALL      SELECT                         * select item to modify
.
          BRANCH    EXIT,CHGPOSTY,CHANGE
.
. ---- update details ----
.
CHGPOSTY  CALL      UPREGA1
          GOTO      CHANGE
.
.        Delete a Register
.
DELETE   
          CALL      DISP1                           * display screen
          CALL      CLEARVAR                        * clear variables
.
          CALL      KEYCDEA                         * input register code
          BRANCH    EXFLAG TO MENU
.
          CALL      DISPVAR                         * display current details
.
. ---- ok to delete ? ----
.
DELETE1   DISPLAY   *P1:22,*V2LON,"WARNING ",*HOFF,"- There should ":
                           "be no outstanding money for this register";
.          KEYIN     *P1:24,*EL,"Are You Sure You Want To Delete This Register":
.                           " (",*V2LON,*DV,ANSY,*HOFF,*DV,SLASH,*V2LON,*DV:
.                           ANSN,*HOFF,") ? ":
.                    *V2LON,ANS;
.          REP       UPPLOW,ANS
.
          CALL       DELQST
          BRANCH     CEXIT,DELCSH,DELETE,MENU
.          CMATCH    ANSY,ANS
.          GOTO      DELCSH IF EQUAL
.
.          CMATCH    ANSN,ANS
.          GOTO      DELETE IF EQUAL
.
.          BEEP
.          GOTO      DELETE1
.
. ---- delete selected record ----
.
DELCSH    MOVE      REGCODE,KEY3
          CALL      DEREGA1
          GOTO      DELETE
.
.      screen for input
.
DISP1     DISPLAY   *P1:4,*EF:
                    *P1:4,"   Register Code                :":
                    *P1:6,*V2LON,"1",*HOFF,". Description                  :";
.
          COMPARE   ZERO,HSYS1
          IF        @EQUAL & RCCNUPBR=0
             DISPLAY   *P1:7,*V2LON,"2",*HOFF,". Cheque Account               :"
          ELSE
             IF        HSYS1=1 | HSYS1=2
               DISPLAY   *P1:7,*V2LON,"2",*HOFF:
                         ". Default Ledger/Income Account:"
             ELSE
               DISPLAY   *P1:7,*V2LON,"2",*HOFF:
                         ". Default Ledger/Cheque Account:"
            ENDIF
          ENDIF
.
          DISPLAY   *P1:8,*V2LON,"3",*HOFF,". Dissection                   :":
                    *P1:9,*V2LON,"4",*HOFF,". Responsibility               :":
                    *P1:10,*V2LON,"5",*HOFF,". Group Code                   :":
                    *P1:11,*V2LON,"6",*HOFF,". I.B.A. System Code           :":
                    *P1:12,*V2LON,"7",*HOFF,". GST Code                     :";
.
          COMPARE   ZERO,HSYS1
          RETURN    IF EQUAL
.
          DISPLAY   *P1:14,*V2LON," ",*HOFF,"  Bank Ledger/Account          :":
                    *P1:15,*V2LON," ",*HOFF,"  Cheque Account               :":
                    *P1:16,*V2LON," ",*HOFF,"  Debtors Control Ledger/Acct. :":
                    *P1:17,*V2LON," ",*HOFF,"  GST Control Account          :";
.
          RETURN
.
. ---- clear variables for input ----
.
CLEARVAR  MOVE      SP3,REGCODE
          MOVE      SP35,REGDESC
          MOVE      SP15,REGCHQCD
          MOVE      SP2,LEDNO
          MOVE      SP12,ACCNO
          MOVE      SP14,REGGENLD
          MOVE      SP14,REGGENBK
          MOVE      SP10,REGDISS
          MOVE      SP10,REGRESP
          MOVE      SP10,DISSNO
          MOVE      SP10,RESPCD
          MOVE      SP3,REGGROUP
          MOVE      ZERO,REGIBACD
          MOVE      SP10,REGGST
          MOVE      ZERO,REGSUNDT
          MOVE      SP70,REGGENDB   * G/L Debtors Control A/C
          MOVE      SP70,REGGENGS   * G/L GST Control A/C
          MOVE      ZERO,EXFLAG
          MOVE      SPACES,FMCHDES
          MOVE      SPACES,TDESC
          MOVE      SPACES,IBADESC
          MOVE      ZERO,CANCEL
.
          RETURN
.----------------------------------------
. ---- display current details ----
.----------------------------------------
DISPVAR   IF        REGIBACD > 9
             GOTO      DISPV010
          ENDIF
          ADD       ONE,REGIBACD
          LOAD      IBADESC,REGIBACD,IBA0,IBA1,IBA2,IBA3,IBA4,IBA5,IBA6
          SUB       ONE,REGIBACD
          GOTO      DISPV020
.
DISPV010  CALL      IBADES00
.
DISPV020  MOVE      SPACES,FMCHDES
          MOVE      SPACES,FMCHCHQ
          IF        HSYS1=1 | HSYS1=2
             CALL      GETC0000
             MOVE      SP70,DIM25A
             PACK      KEY14,FMAMLEDG,FMAMACCT,SP20
             CALL      RDFMAM1
             BRANCH    OVRCD,DISPV100
             MOVE      FMAMDESC,DIM25A
          ELSE
             MOVE      SP30,CHEQDESC
             PACK      KEY15,REGCHQCD
             CALL      RDFMCH1
             BRANCH    OVRCD,DISPV100
             MOVE      FMCHDES,CHEQDESC
          ENDIF
.
DISPV100  DISPLAY   *P35:4,*EL,*V2LON,REGCODE:
                    *P35:6,*EL,*V2LON,REGDESC
          COMPARE   ZERO,HSYS1
          IF        @EQUAL & RCCNUPBR=0
             DISPLAY   *P35:7,*EL,*V2LON,REGCHQCD,*HOFF,*P51:7,CHEQDESC
             GOTO      DISPV120
          ELSE
             IF        HSYS1=1 | HSYS1=2
               DISPLAY   *P35:7,*EL,*V2LON,FMAMLEDG,SLASH,FMAMACCT,*P51:7:
                         *HOFF,DIM25A
             ELSE     
               MOVE      REGGENLD,DIM2A
               DISPLAY   *P35:7,*EL,*V2LON,DIM2A,SLASH,REGCHQCD,*P51:7
               GOTO      DISPV120
             ENDIF
          ENDIF
.
          MOVE      REGDISS,KEY5
          CALL      RDFMDS1
          DISPLAY   *P35:8,*EL,*V2LON,REGDISS,*P51:8,*HOFF,FMDSDESC
          MOVE      REGRESP,KEY4
          CALL      RDFMRS1
          MOVE      FMRSDESC,DIM25D
          DISPLAY   *P35:9,*EL,*V2LON,REGRESP,*P51:9,*HOFF,DIM25D
.
DISPV120  PACK      TDESC,SP20,SP20
.
          COMPARE   "1",HSYS2
          GOTO      DISPV140 IF NOT EQUAL
.
          MATCH     SP3,REGGROUP            * blank group code ?
          GOTO      DISPV150 IF EQUAL       * yes
.
          PACK      KEY5 WITH CODERG,REGGROUP
          CALL      RDCODE1
          BRANCH    OVRCD,DISPV150
DISPV140  DISPLAY   *P35:10,*EL,*V2LON,REGGROUP,*P51:10,*HOFF,TDESC
DISPV150  DISPLAY   *P35:11,*EL,*V2LON,REGIBACD,*P51:11,*HOFF,IBADESC
.
          PACK      IBGSDESC,SP70
          PACK      KEY6,REGGST,SP70
          CALL      RDIBGS1
          IF        OVRCD=0 & IBGSACTV=1
            MOVE      "Inactive GST Code             ",IBGSDESC
          ENDIF
          DISPLAY   *P35:12,*EL,*V2LON,REGGST,*P51:12,*HOFF,IBGSDESC
.
          COMPARE   ZERO,REGSUNDT
          GOTO      DISPV200 IF EQUAL
          MOVE      REGSUNDT,SLEDNO
.
          COMPARE   ZERO,SLEDNO
          GOTO      DISPV200 IF EQUAL
.
DISPV200  COMPARE   ZERO,HSYS1
          GOTO      DISPV999 IF EQUAL
.
          MOVE      SP70,DIM25A
          PACK      KEY14,FMAMLEDG,FMCSBNK,SP20
          CALL      RDFMAM1
          BRANCH    OVRCD,DISPV300
.
          MOVE      FMAMDESC,DIM25A
          DISPLAY   *P35:14,*EL,*V2LON,FMLALEDG,"/",FMCSBNK,*P51:14,*HOFF,DIM25A
.
DISPV300  MOVE      FMCHDES,DIM25A
          DISPLAY   *P35:15,*EL,*V2LON,FMCHCHQ,*P51:15,*HOFF,DIM25A
.
          MOVE      SP70,DIM25A
          PACK      KEY14,FMAMLEDG,FMCSDEB,SP20
          CALL      RDFMAM1
          BRANCH    OVRCD,DISPV400
          MOVE      FMAMDESC,DIM25A
.
          DISPLAY   *P35:16,*EL,*V2LON,FMLALEDG,"/",FMCSDEB,*P51:16,*HOFF,DIM25A
.
DISPV400  MATCH     SP70,REGGST
          GOTO      DISPV999 IF EQUAL
.
          MOVE      SP70,DIM25A
          PACK      KEY14,FMAMLEDG,FMCSAGST,SP20
          CALL      RDFMAM1
          BRANCH    OVRCD,DISPV999
          MOVE      FMAMDESC,DIM25A
.
          DISPLAY   *P35:17,*EL,*V2LON,FMLALEDG,"/",FMCSAGST:
                    *P51:17,*HOFF,DIM25A
.
DISPV999  RETURN
.----------------------------------------
. Get Control Account
.----------------------------------------
GETC0000  UNPACK    REGGENLD,FMAMLEDG,FMAMACCT
          PACK      KEY2,FMAMLEDG
          CALL      RDFMLA1
          MOVE      FMLABNK,FMCSBNK
          MOVE      FMLADEB,FMCSDEB
          MOVE      FMLAAGST,FMCSAGST
          PACK      KEY14,REGGENLD
          CALL      RDFMCS1
          BRANCH    OVRCD,GETC9000
          MATCH     SP70,FMCSBNK
          GOTO      GETC0010 IF NOT EQUAL
          MOVE      FMLABNK,FMCSBNK
.
GETC0010  MATCH     SP70,FMCSDEB
          GOTO      GETC0020 IF NOT EQUAL
          MOVE      FMLADEB,FMCSDEB
.
GETC0020  MATCH     SP70,FMCSAGST
          GOTO      GETC9500 IF NOT EQUAL
          MOVE      FMLAAGST,FMCSAGST
          GOTO      GETC9500
.
GETC9000  MOVE      FMLABNK,FMCSBNK
          MOVE      FMLADEB,FMCSDEB
          MOVE      FMLAAGST,FMCSAGST
.
GETC9500  PACK      KEY14,FMLALEDG,FMCSBNK
          CALL      RDFMCA1
          BRANCH    OVRCD,GETC9999
          MOVE      FMCACHQ,KEY15
          CALL      RDFMCH1
          MOVE      FMCACHQ,REGCHQCD
          MOVE      FMCSBNK,REGGENBK
GETC9999  RETURN
.
. ---- input register code & validate ----
.
KEYCODE   CALL      ULREGA1
          DISPLAY   *P35:4,*EL;
          MOVE      "35",ECOL
          MOVE      "4",EVERT
          MOVE      ONE,CKYIMODE
          CALL      RCPREGKY
          BRANCH    EXIT,KEYCODE8,KEYCODE8
.
          MOVE      REGCODE,KEY3
          CALL      RLREGA1
          COMPARE   TWO,OVRCD
          GOTO      KEYCODE6 IF EQUAL
          GOTO      KEYCODE9
.
KEYCODE6  CALL      RECL0000
.
KEYCODE8  MOVE      ONE,EXFLAG
.
KEYCODE9  RETURN
.
. ---- input register code & validate ----
.
KEYCDEA   CALL      ULREGA1
          DISPLAY   *P35:4,*EL;
          MOVE      "35",ECOL
          MOVE      "4",EVERT 
          MOVE      ZERO,CKYIMODE
          CALL      RCPREGKY
          BRANCH    EXIT,KEYCDEA8,KEYCDEA8
.
          MOVE      REGCODE,KEY3
          CALL      RLREGA1
          COMPARE   TWO,OVRCD
          GOTO      KEYCDEA6 IF EQUAL
          GOTO      KEYCDEA9
.
KEYCDEA6  CALL      RECL0000
. 
KEYCDEA8  MOVE      ONE,EXFLAG
.
KEYCDEA9  RETURN
.
. ---- input description ----
.
KEYDESC   MOVE      UNDLN,REGDESC
          DISPLAY   *P35:6,*EL,REGDESC
          MOVE      SP35,REGDESC
.
          KEYIN     *P35:6,*V2LON,REGDESC,*P35:6,*DV,REGDESC
.
KEYDESC9  RETURN
.
. ---- input GL income account & validate ----
.
KEYGLED   MOVE      ZERO,CANCEL
          BRANCH    HSYS1,KEYGLED1
. 
          COMPARE   ZERO,RCCNUPBR
          IF        @EQUAL
             MOVE      "35",CCOL 
             CALL      KEYCHEQ
             GOTO      KEYGLEDX 
          ELSE
             MOVE      "38",CCOL 
             CALL      KEYLCHQ 
             GOTO      KEYGLEDX 
          ENDIF
.
KEYGLED1  MOVE      UNDLN,LEDNO
          DISPLAY   *P35:7,*EL,LEDNO;
          MOVE      SP2,LEDNO
          MOVE      SP12,ACCNO
.
          KEYIN     *P35:7,*V2LON,*JR,*ZF,LEDNO,*P35:7,*DV,LEDNO
          DISPLAY   *P55:7,*EL
.
          ENDSET    LEDNO
          APPEND    SP2,LEDNO
          RESET     LEDNO
.
          MATCH     SP2,LEDNO
          GOTO      KEYGLED IF EQUAL
.
          MATCH     "0\",LEDNO
          GOTO      KEYGLEDA IF EQUAL
.
          MOVE      UNDLN,ACCNO
          DISPLAY   *P37:7,"/",ACCNO
          MOVE      SP12,ACCNO
.
          KEYIN     *P38:7,*V2LON,ACCNO,*P38:7,*DV,ACCNO
.
          MATCH     SP20,LEDNO
          GOTO      KEYGLED IF EQUAL
.
          MATCH     EXITCHAR,LEDNO
          GOTO      KEYGLEDA IF EQUAL
.
          PACK      KEY14 WITH LEDNO,ACCNO,SP20
          CALL      RDFMAM1
          BRANCH    OVRCD,KEYGLED6
.
          MATCH     "6",FMAMTYPE
          GOTO      KEYGLED5 IF NOT LESS
.
          MOVE      FMAMDESC,DIM25B
          DISPLAY   *P51:7,DIM25B
          GOTO      KEYGLED7
.
KEYGLED5  DISPLAY   *P1:24,*EL,*B,*V2LON,LEDNO,SLASH,ACCNO,*HOFF:
                    "Has Invalid Account Type. ";
          CALL      HITENTER
          GOTO      KEYGLED
.
KEYGLED6  DISPLAY   *P1:24,*EL,*B,*V2LON,LEDNO,SLASH,ACCNO,*HOFF:
                    "Not On Account Master File. ";
          CALL      HITENTER
          GOTO      KEYGLED
.
KEYGLED7  PACK      REGGENLD WITH LEDNO,ACCNO
          CALL      GETC0000
          MOVE      SP70,DIM25A
          PACK      KEY14,FMAMLEDG,FMCSBNK,SP20
          CALL      RDFMAM1
          BRANCH    OVRCD,KEYGLED5
.
          MOVE      FMAMDESC,DIM25A
          DISPLAY   *P35:14,*EL,*V2LON,FMLALEDG,"/",FMCSBNK,*P51:14,*HOFF,DIM25A
.
          MOVE      FMCHDES,DIM25A
          DISPLAY   *P35:15,*EL,*V2LON,FMCHCHQ,*P51:15,*HOFF,DIM25A
.
          MOVE      SP70,DIM25A
          PACK      KEY14,FMAMLEDG,FMCSDEB,SP20
          CALL      RDFMAM1
          BRANCH    OVRCD,KEYGLED8
          MOVE      FMAMDESC,DIM25A
.
          DISPLAY   *P35:16,*EL,*V2LON,FMLALEDG,"/",FMCSDEB,*P51:16,*HOFF,DIM25A
.
KEYGLED8  MATCH     SP70,REGGST
          GOTO      KEYGLEDX IF EQUAL
.
          MOVE      SP70,DIM25A
          PACK      KEY14,FMAMLEDG,FMCSAGST,SP20
          CALL      RDFMAM1
          BRANCH    OVRCD,KEYGLEDX
          MOVE      FMAMDESC,DIM25A
.
          DISPLAY   *P35:17,*EL,*V2LON,FMLALEDG,"/",FMCSAGST:
                    *P51:17,*HOFF,DIM25A
.
          GOTO      KEYGLEDX
.
KEYGLEDA  MOVE      ONE,CANCEL
.
KEYGLEDX  RETURN
.
. ------ Keyin for Ledger Cheque Account
.
KEYLCHQ   MOVE      UNDLN,LEDNO
          DISPLAY   *P35:7,*EL,LEDNO
          MOVE      SP2,LEDNO
.
          KEYIN     *P35:7,*V2LON,*JR,*ZF,LEDNO,*P35:7,*DV,LEDNO
          DISPLAY   *P55:7,*EL
.
          ENDSET    LEDNO
          APPEND    SP2,LEDNO
          RESET     LEDNO
.
          MATCH     SP2,LEDNO
          IF        @EQUAL
            DISPLAY   *P1:24,*EL,*B,"Field is Mandatory. ";
            CALL      HITENTER
            GOTO      KEYLCHQ
          ENDIF
.
          MATCH     "0\",LEDNO
          GOTO      KEYLCHQ1 IF NOT EQUAL
          MOVE      ONE,CANCEL
          GOTO      KEYLCHQ9
.
KEYLCHQ1  DISPLAY   *P37:7,"/"
          CALL      KEYCHEQ
          BRANCH    CANCEL,KEYLCHQ9,KEYLCHQ
.
          OPEN      FMSCAFA2,"fmscafaf"
.
          PACK      KEY29,CHEQACC,LEDNO,Z20
          CALL      RSFMCA2
          CALL      RPFMCA2
.
          MATCH     FMCACHQ,CHEQACC
          GOTO      KEYLCHQ3 IF EQUAL
.
          DISPLAY   *P1:24,*EL,*B,*V2LON,LEDNO,SLASH,CHEQACC,*HOFF:
                    "Not On Control Account File. ";
          CALL      HITENTER
          GOTO      KEYLCHQ
.
KEYLCHQ3  PACK      KEY14 WITH LEDNO,FMCAACC,SP20
          CALL      RDFMAM1
          BRANCH    OVRCD,KEYLCHQ7
.
          DISPLAY   *P51:7,FMAMDESC
          MOVE      LEDNO,REGGENLD
          GOTO      KEYLCHQ9
.
KEYLCHQ7  DISPLAY   *P1:24,*EL,*B,*V2LON,LEDNO,SLASH,FMCAACC,*HOFF:
                    "Not On Account Master File. ";
          CALL      HITENTER
          GOTO      KEYLCHQ
.
KEYLCHQ9  RETURN
.
. ---- input cheque a/c code (no G/L interface) ---
.
KEYCHEQ   MOVE      ZERO,CANCEL
          DISPLAY   *PCCOL:7,*EL,*P55:7,*EL;
          KEYIN     *PCCOL:7,*DV,UNDLN15:
                    *PCCOL:7,*V2LON,CHEQACC:
                    *PCCOL:7,*DV,CHEQACC
          DISPLAY   *P55:7,*EL
.
          ENDSET    CHEQACC                 * pad with blanks
          APPEND    SP15,CHEQACC
          RESET     CHEQACC
.
          MATCH     EXITCHAR,CHEQACC
          GOTO      KEYCHEQ8 IF EQUAL       * Exit chars entered
.
          MATCH     SP15,CHEQACC            * anything input ?
          GOTO      KEYCHEQ7 IF NOT EQUAL
.
          COMPARE   ZERO,RCCNUPBR
          GOTO      KEYCHEQ IF EQUAL
.
          MOVE      TWO,CANCEL
          GOTO      KEYCHEQX
.
KEYCHEQ7  PACK      KEY15,CHEQACC
          CALL      RDFMCH1                 * cheque account on file ?
          BRANCH    OVRCD,KEYCHEQ9          * no
.
          MOVE      CHEQACC,REGCHQCD
          MOVE      FMCHDES,CHEQDESC
          IF        RCCNUPBR=0
            DISPLAY   *P51:7,CHEQDESC
          ENDIF
          GOTO      KEYCHEQX
.
KEYCHEQ8  MOVE      ONE,CANCEL
          GOTO      KEYCHEQX
.
KEYCHEQ9  DISPLAY   *P1:24,*EL,*B,"Cheque account not on file.  ";
          CALL      HITENTER
          GOTO      KEYCHEQ
.
KEYCHEQX  RETURN
.
. ---- input dissection code ----
.
KEYDISS   COMPARE   ZERO,HSYS1
          RETURN    IF EQUAL
.
          MOVE      UNDLN,DISSNO
          DISPLAY   *P35:8,*EL,DISSNO;
          KEYIN     *P35:8,*V2LON,*JR,DISSNO;
          DISPLAY   *P35:8,*V2LON,DISSNO;
          PACK      KEY5,DISSNO,SP10
          CALL      RDFMDS1
          BRANCH    OVRCD,KEYDISS
          DISPLAY   *P51:8,FMDSDESC;
          MOVE      DISSNO,REGDISS
          RETURN
.
. ---- input responsibility code ----
.
KEYRESP   COMPARE   ZERO,HSYS1
          RETURN    IF EQUAL
.
          MOVE      UNDLN,RESPCD
          DISPLAY   *P35:9,*EL,RESPCD;
          KEYIN     *P35:9,*V2LON,RESPCD;
          DISPLAY   *P35:9,*V2LON,RESPCD;
          PACK      KEY4,RESPCD,SP10
          CALL      RDFMRS1
          BRANCH    OVRCD,KEYRESP
          MOVE      FMRSDESC,DIM25D
          DISPLAY   *P51:9,DIM25D;
          MOVE      RESPCD,REGRESP
          RETURN
.
. ---- input register group & validate ----

KEYGRP    MOVE      UNDLN,REGGROUP
          DISPLAY   *P35:10,*EL,REGGROUP
          MOVE      SP3,REGGROUP
          MOVE      SP20,TDESC
.
          KEYIN     *P35:10,*V2LON,REGGROUP,*P35:10,*DV,REGGROUP
.
. ---- is patient management running ----
.
          COMPARE   "1",HSYS2
          GOTO      KEYGRP9 IF NOT EQUAL
.
          CMATCH    "?",REGGROUP
          GOTO      KEYGRP1 IF NOT EQUAL
.
KYGRPQ    MOVE      CODERG,CODE
          CALL      PATCODDS
.
KYGRPQK   MOVE      SP3,REGGROUP
          MOVE      SP20,TDESC
          DISPLAY   *P1:24,*EL,"Group Code : ",UNDLN3;
          KEYIN     *P14:24,*V2LON,REGGROUP;
          DISPLAY   *P14:24,*V2LON,REGGROUP;
          ENDSET    REGGROUP
          APPEND    SP3,REGGROUP
          RESET     REGGROUP
.
          CMATCH    "?",REGGROUP
          GOTO      KYGRPQ IF EQUAL
.
          ENDSET    REGGROUP
          APPEND    SP3,REGGROUP
          RESET     REGGROUP
.
          MATCH     SP3,REGGROUP
          GOTO      KYGRPQC IF NOT EQUAL
.
          CALL      DISP1
          CALL      DISPVAR
.
          GOTO      KEYGRP9
.
KYGRPQC   PACK      KEY5 WITH CODERG,REGGROUP
          CALL      RDCODE1
          COMPARE   ZERO,OVRCD
          GOTO      KEYGRPC IF EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"Invalid Selection. ";
          CALL      HITENTER
          GOTO      KYGRPQK
.
KEYGRPC   CALL      DISP1
          CALL      DISPVAR
.
          GOTO      KEYGRP4
.
KEYGRP1   ENDSET    REGGROUP
          APPEND    SP3,REGGROUP
          RESET     REGGROUP
.
          MATCH     SP3,REGGROUP
          GOTO      KEYGRP9 IF EQUAL
.
          PACK      KEY5 WITH CODERG,REGGROUP
          CALL      RDCODE1
          BRANCH    OVRCD TO KEYGRP8
.
KEYGRP4   DISPLAY   *P51:10,TDESC
.
          GOTO      KEYGRP9
.
KEYGRP8   DISPLAY   *P1:24,*B,*EL,"Invalid Selection. ";
          CALL      HITENTER
          GOTO      KEYGRP
.
KEYGRP9   RETURN
.
. ---- input iba system code & validate ----
.
KEYIBA    MOVE      FALSE,EXIT
          MOVE      FALSE,FLAGQUES
          MOVE      UNDLN,IBACD
          DISPLAY   *P35:11,*EL,IBACD
          MOVE      ZERO,IBACD
.
          KEYIN     *P35:11,*V2LON,*JR,IBACD;
          DISPLAY   *P35:11,*V2LON,IBACD;
.
          PACK      DIM2,SP1,EXITCHAR
          MATCH     DIM2,IBACD             * Check for Exit Char
          GOTO      KEYIBA8 IF EQUAL
.
          MATCH     SP2,IBACD              * Check for Null Entry
          GOTO      KEYIBAM IF EQUAL
.
          MATCH     " 0",IBACD             * Check for 0 Entry
          GOTO      KEYIBA1 IF NOT EQUAL
.
KEYIBAM   DISPLAY   *P1:24,*EL,*B,"System Code Mandatory. ";
          CALL      HITENTER
          GOTO      KEYIBA
.
KEYIBA1   MATCH     " ?",IBACD
          GOTO      KEYIBA2 IF NOT EQUAL
          MOVE      TRUE,FLAGQUES
          GOTO      DSPIBA
.
KEYIBAE   DISPLAY   *P1:24,*EL,*B,"Invalid Selection. ";
          CALL      HITENTER
          BRANCH    FLAGQUES,KEYIBAQK
          GOTO      KEYIBA
.
KEYIBA2   TYPE      IBACD
          GOTO      KEYIBAE IF NOT EQUAL
.
          MOVE      IBACD,REGIBACD
          COMPARE   "92",REGIBACD
          GOTO      KEYIBA2A IF EQUAL
          COMPARE   "93",REGIBACD
          GOTO      SYSTEM1 IF EQUAL
          COMPARE   "94",REGIBACD
          GOTO      KEYIBA2A IF EQUAL
          COMPARE   "95",REGIBACD
          GOTO      KEYIBA2A IF EQUAL
          COMPARE   "96",REGIBACD
          GOTO      SYSTEM2 IF EQUAL
          COMPARE   "97",REGIBACD
          GOTO      SYSTEM1 IF EQUAL
          COMPARE   "98",REGIBACD
          GOTO      KEYIBA2A IF EQUAL
          COMPARE   "99",REGIBACD
          GOTO      KEYIBA2A IF EQUAL
          BRANCH    REGIBACD,SYSTEM1,SYSTEM2,KEYIBAE,SYSTEM4,SYSTEM5:
                             SYSTEM6
.
          GOTO      KEYIBAE
.
SYSTEM1   COMPARE   ONE,HSYS2
          GOTO      KEYIBAE IF NOT EQUAL
.
          BRANCH    REGIBACD,KEYIBA2A
.
          COMPARE   SIX,CFEETYP
          GOTO      KEYIBA2A IF NOT EQUAL
.
          PACK      KEY5,CODEDP,REGCODE
          CALL      RDCODE1
          IF        OVRCD=TRUE
            DISPLAY   *P1:24,*B,*EL:
                      "Deposit Registers Must Be Deposit Types. ";
            CALL      HITENTER
            BRANCH    FLAGQUES,KEYIBAQK
            GOTO      KEYIBA 
          ENDIF
.
          GOTO      KEYIBA2A
.
SYSTEM2   COMPARE   ONE,HSYS3
          GOTO      KEYIBAE IF NOT EQUAL
          GOTO      KEYIBA2A
.
SYSTEM4   COMPARE   ONE,HSYS5
          GOTO      KEYIBAE IF NOT EQUAL
          GOTO      KEYIBA2A
.
SYSTEM5   COMPARE   ONE,HSYS6
          GOTO      KEYIBAE IF NOT EQUAL
          GOTO      KEYIBA2A
.
SYSTEM6   COMPARE   ONE,HSYS7
          GOTO      KEYIBAE IF NOT EQUAL
          GOTO      KEYIBA2A
.
KEYIBA2A  MOVE      IBACD,REGIBACD
          MOVE      REGIBACD,FIBACD
          ADD       ONE,FIBACD
          LOAD      IBADESC,FIBACD,IBA0,IBA1,IBA2,IBA3,IBA4,IBA5:
                                   IBA6
.
          COMPARE   "99",REGIBACD
          GOTO      KEYIBA3 IF EQUAL
.
          COMPARE   "98",REGIBACD
          GOTO      KEYIBA3A IF EQUAL
.
          COMPARE   "97",REGIBACD
          GOTO      KEYIBA3B IF EQUAL
.
          COMPARE   "96",REGIBACD
          GOTO      KEYIBA3C IF EQUAL
.
          COMPARE   "95",REGIBACD
          GOTO      KEYIBA3A IF EQUAL
.
          COMPARE   "94",REGIBACD
          GOTO      KEYIBA3A IF EQUAL
.
          COMPARE   "93",REGIBACD
          GOTO      KEYIBA3B IF EQUAL
.
          COMPARE   "92",REGIBACD
          GOTO      KEYIBA3A IF EQUAL
.
          COMPARE   SEVEN,REGIBACD
          GOTO      KEYIBA4 IF LESS
.
          GOTO      KEYIBAE
.
KEYIBA3   COMPARE   ZERO,FLAGQUES
          GOTO      KEYIBA31 IF EQUAL
.
          CALL      KEYIBAQR
.
KEYIBA31  MOVE      "Suspense",IBADESC
          DISPLAY   *P51:11,IBADESC
          DISPLAY   *P1:13,*EL;
          GOTO      KEYIBA9
.
KEYIBA3A  COMPARE   ZERO,FLAGQUES
          GOTO      KEYIBAA1 IF EQUAL
.
          CALL      KEYIBAQR
.
KEYIBAA1  CALL      IBADES00
          DISPLAY   *P51:11,IBADESC
          DISPLAY   *P35:13,*EL;
          GOTO      KEYIBA9
.
KEYIBA3B  COMPARE   ZERO,FLAGQUES
          GOTO      KEYIBAB1 IF EQUAL
.
          CALL      KEYIBAQR
.
KEYIBAB1  
          IF        REGIBACD=97
            MOVE      "Cash Deposits (Pat.)",IBADESC
          ELSE
            MOVE      "Patient Billing/Deposit",IBADESC
          ENDIF
          DISPLAY   *P51:11,IBADESC
          DISPLAY   *P35:13,*EL;
          GOTO      KEYIBA9
.
KEYIBA3C  COMPARE   ZERO,FLAGQUES
          GOTO      KEYIBAC1 IF EQUAL
.
          CALL      KEYIBAQR
.
KEYIBAC1  MOVE      "Cash Deposits (Pri.)",IBADESC
          DISPLAY   *P51:11,IBADESC
          DISPLAY   *P35:13,*EL;
          GOTO      KEYIBA9
.
KEYIBA4   COMPARE   ZERO,FLAGQUES
          GOTO      KEYIBA5 IF EQUAL
.
          CALL      KEYIBAQR
.
KEYIBA5   DISPLAY   *P51:11,IBADESC
          MOVE      ZERO,REGSUNDT
          DISPLAY   *P35:13,*EL;
          GOTO      KEYIBA9
.
KEYIBA8   MOVE      TRUE,EXIT
.
KEYIBA9   RETURN
.
. ---- input GST code & validate ----
.
KEYGST00  MOVE      " 35",CCOL
          MOVE      "12",CVERT
          PACK      IBGSCODE,SP70,SP70
          CLEAR     IBGSCODE
          CALL      KIBGSA00
          BRANCH    EXIT,KEYGST70,KEYGST70
.
          GOTO      KEYGST80
.
KEYGST70  UNPACK    SP70,IBGSCODE,IBGSDESC
.
KEYGST80  MOVE      IBGSCODE,REGGST
          DISPLAY   *P35:12,*EL,*V2LON,REGGST,*HOFF:
                    *P51:12,IBGSDESC:
                    *P35:17,*EL;
.
          MATCH     SP70,REGGST
          GOTO      KEYGST99 IF EQUAL
.
          IF        OPTION=1
            PACK      REGGENLD,LEDNO,ACCNO
          ENDIF
.
          CALL      GETC0000
          MOVE      SP70,DIM25A
          PACK      KEY14,FMAMLEDG,FMCSAGST,SP20
          CALL      RDFMAM1
          BRANCH    OVRCD,KEYGST99
.
          MOVE      FMAMDESC,DIM25A
          DISPLAY   *P35:17,*EL,*V2LON,FMLALEDG,"/",FMCSAGST:
                    *P51:17,*HOFF,DIM25A
.
          GOTO      KEYGST99
.
KEYGST99  RETURN
.
. ---- select field ? ----
.
SELECT    CALL      MAINQST                  * select an item, Post, Cancel
.
          MOVE      ONE,EXIT
.
          COMPARE   ZERO,CCITEM
          GOTO      SELECT9 IF EQUAL
.
          MOVE      TWO,EXIT
.
          COMPARE   "-1",CCITEM
          RETURN    IF EQUAL
.
SELECT1   BRANCH    CCITEM TO CHGDESC,CHGGENLD,CHGDISS,CHGRESP,CHGGROUP:
                              CHGIBACD,CHGGSTCD
          BEEP
          GOTO      SELECT
.
CHGDESC   CALL      KEYDESC                    * description
          GOTO      SELECT
.
CHGGENLD  CALL      KEYGLED                    * GL income account
          COMPARE   ZERO,CANCEL
          GOTO      SELECT IF EQUAL
          MOVE      TWO,EXIT
          RETURN
.
CHGDISS   CALL      KEYDISS                    * GL dissection
          GOTO      SELECT
.
CHGRESP   CALL      KEYRESP                    * GL responsibility
          GOTO      SELECT
.
CHGGROUP  CALL      KEYGRP                     * group
          GOTO      SELECT
.
CHGIBACD  CALL      KEYIBA                     * system code
          IF        EXIT=TRUE
            MOVE      TWO,EXIT
            GOTO      SELECT9
          ENDIF
          GOTO      SELECT
.
CHGGSTCD  CALL      KEYGST00                   * GST code
          GOTO      SELECT
.
SELECT9   RETURN
.
. ---- help for iba system codes ----
.
DSPIBA    DISPLAY   *P1:4,*EF:
                    *P30:4,*V2LON,*ULON,"System Codes for Registers",*HOFF:
                    *P1:5,*V2LON," 1",*HOFF,"  Patient Billing":
                    *P1:6,*V2LON," 2",*HOFF,"  Private Practice":
                    *P1:7,*V2LON," 3",*HOFF,"  Sundry Debtors":
                    *P1:8,*V2LON," 4",*HOFF,"  Medical Services":
                    *P1:9,*V2LON," 5",*HOFF,"  Sundry Accounts Rec.":
                    *P1:10,*V2LON," 6",*HOFF,"  Nursing Home":
                    *P1:11,*V2LON,"92",*HOFF,". GST Sundry Cash Sales":
                    *P1:12,*V2LON,"93",*HOFF,"  Patient Billing/Deposits":
                    *P1:13,*V2LON,"94",*HOFF,"  Sundry Debtor Rec.  ":
                    *P1:14,*V2LON,"95",*HOFF,"  Sundry Cash Sales   ":
                    *P1:15,*V2LON,"96",*HOFF,"  Cash Deposits (Pri.)":
                    *P1:16,*V2LON,"97",*HOFF,"  Cash Deposits (Pat.)":
                    *P1:17,*V2LON,"98",*HOFF,"  Internal Receipts":
                    *P1:18,*V2LON,"99",*HOFF,"  Suspense"
.
KEYIBAQK  DISPLAY   *P1:24,*EL,"I.B.A. System Code : ",UNDLN2;
          KEYIN     *P22:24,*V2LON,*JR,IBACD;
          DISPLAY   *P22:24,*V2LON,IBACD;
.
          MATCH     DIM2,IBACD
          GOTO      KEYIBA8 IF EQUAL
.
          MATCH     SP2,IBACD
          GOTO      KEYIBAQM IF EQUAL
.
          MATCH     " ?",IBACD
          GOTO      DSPIBA IF EQUAL
.
          MATCH     " 0",IBACD
          GOTO      KEYIBA2 IF NOT EQUAL
.
KEYIBAQM  DISPLAY   *P1:24,*B,*EL,"System Code Mandatory. ";
          CALL      HITENTER
          KEYIN     ANS;
          DISPLAY   *P1:24,*EL;
.
          GOTO      KEYIBAQK
.
KEYIBAQR  BRANCH    OPTION TO DSPIBA1,DSPIBA2
.
. ---- redisplay screens ----
.
DSPIBA1  
          CALL      DISP1
          CALL      DISPVAR
          GOTO      DSPIBA3
.
DSPIBA2  
          CALL      DISP1
          CALL      DISPVAR
.
DSPIBA3   RETURN
.
ENDIT     CHAIN     PGM
.
IBADES00  MOVE      ZERO,FLAGDES
          COMPARE   "99",REGIBACD
          GOTO      IBADES10 IF EQUAL
          COMPARE   "98",REGIBACD
          GOTO      IBADES20 IF EQUAL
          COMPARE   "97",REGIBACD
          GOTO      IBADES30 IF EQUAL
          COMPARE   "96",REGIBACD
          GOTO      IBADES40 IF EQUAL
          COMPARE   "95",REGIBACD      * Cash Sales Receipts
          GOTO      IBADES50 IF EQUAL
          COMPARE   "94",REGIBACD      * Debtors Cash Receipt
          GOTO      IBADES60 IF EQUAL
          COMPARE   "93",REGIBACD     
          GOTO      IBADES70 IF EQUAL
          COMPARE   "92",REGIBACD      * GST Sundry Cash Sales
          GOTO      IBADES80 IF EQUAL
          MOVE      ONE,FLAGDES
          GOTO      IBADES99
IBADES10  MOVE      "Suspense",IBADESC
          GOTO      IBADES99
IBADES20  MOVE      "Internal Receipts",IBADESC
          GOTO      IBADES99
IBADES30  MOVE      "Cash Deposits (Pat.)",IBADESC
          GOTO      IBADES99
IBADES40  MOVE      "Cash Deposits (Pri.)",IBADESC
          GOTO      IBADES99
IBADES50  MOVE      "Sundry Cash Sales",IBADESC
          GOTO      IBADES99
IBADES60  MOVE      "Sundry Debtor Rec.",IBADESC
          GOTO      IBADES99
IBADES70  MOVE      "Patient Billing/Deposits",IBADESC
          GOTO      IBADES99
IBADES80  MOVE      "GST Sundry Cash Sales",IBADESC
          GOTO      IBADES99
.
IBADES99  RETURN
.
.
RECL0000  COMPARE   ONE,OPTION
          GOTO      RECL5000 IF EQUAL
.
          CALL      RECLOCK
          GOTO      RECL9999 
.
RECL5000  CALL      REXISTS
RECL9999  RETURN
+
          INC       RCPREGIO/INC
          INC       PATCODIO/INC
          INC       FMSCHQIO/INC
          INC       FMSAMAIO/INC
          INC       FMSRESIO/INC
          INC       FMSDISIO/INC
          INC       FMSCAFIO/INC
          INC       FMSCSAIO/INC
          INC       FMSLMAIO/INC
.
          INC       IBAGKIIO/INC
          INC       IBAGKIKY
          INC       IBAGSTIO/INC
          INC       IBAGSTKY
.
          INC       PATCODDS
          INC       RCPREGKY 
          INC       FMSSTDCD
.

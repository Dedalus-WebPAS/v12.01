.----------------------------------------------------------------------------
. Program   :   FIXNMDSC                                                  
. Desc      :   Update NMDS Codes for selected clinics from text file   
.----------------------------------------------------------------------------
. Function  :   This program will open a text file called fixnmdsc.txt,   
.               it contains the site, clinic and new NMDS code.                
.               It reads outbokaf table for the selected site and clinic for
.               status (1-Booked, 4-Attended and 5-No Attendance), it then
.               reads outbb1af table for outpatient number and updates
.               OTBBNMDS with the nominated input NMDS code.                    
.----------------------------------------------------------------------------
. Modifications  :                                 
.----------------------------------------------------------------------------
. V11.04.01 11/12/2023  Thanh T         TSK 0934695
.           Changed DATET000 to allow the user to enter the From date when
.           the From date is not less than To date
. V11.03.00 06/11/2023  Thanh T         TSK 0934695
.           Created program                       
.----------------------------------------------------------------------------
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
.
TEMPFILE  FILE     ASCII, FIXED=16
.
.NAME     TYPE   LENGTH   PHYSICAL  STRT    DESCRIPTION
.----     ----   ------   --------  -----   -----------
XXXXSITE  DIM       6         6      1      Site ID
XXXXCLIN  DIM       6         6      7      Clinic ID
XXXXTCOD  DIM       3         3      13     NMDS Code (category NM)
.
. End of Record                      16
.
. LOCAL VARIABLE DEFINITION
. -------------------------
RECCOUNT  FORM      5
FROMDATE  DIM       10
TODATE    DIM       10
STRTDATE  DIM       8
ENDDATE   DIM       8
FNAMEI    DIM       500
PTHNAME   DIM       500
FORM3     FORM      3
SAVE500   DIM       500
SAVE500X  DIM       500
.
CTXTNAM   INIT      "fixnmdsc.txt"
.
.----------------------------------------------------------------------------
PRGID     INIT      "FIXNMDSC"
PRGNAM    INIT      "Fix NMDS Codes"
VERSION   INIT      "V12.01.00"
.----------------------------------------------------------------------------
+
.**************************************************************************
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
ML0000    CALL      INIT0000               * initialisation and open files
.
ML1000    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
.
ML2000    CALL      DATEF000               * keyin from date
          BRANCH    EXIT,ML2000
.
ML2100    CALL      DATET000               * keyin to date
          BRANCH    EXIT,ML2100,ML2000
.
ML2200    CALL      KASC0000               * Keyin full path
          BRANCH    EXIT,ML1000
.
ML3000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML5000:          * yes
                          ML1000:          * no
                          ML1000           * cancel
. 
ML5000    CALL      PROC0000               * process
          IF        COPTION = ONE
            GOTO      ML1000
          ENDIF  
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      OUTBOKA1,"outbokaf"
          OPEN      OUTBB1A1,"outbb1af"
.
INIT9999  RETURN
+
.****************************************************************************
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Count bookings ONLY":
                    *P1:6,*V2LON,TWO,*HOFF,". Run NMDS Code UPDATE"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000,OPTN9000    * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.         Keyin from date
.**************************************************************************
DATEF000  MOVE      ZERO,EXIT
.
          DISPLAY   *P1:10,*EF,"Bookings From Date  : "
          MOVE      "10",CVERT
          MOVE      "23",CCOL
          CALL      IBACLOCK
          MOVE      CCC,CCENT
          MOVE      CYY,CYEAR
          MOVE      CMM,CMON
          MOVE      CDD,CDAY
.
          CALL      KEYDATE                 * Keyin the from date
          BRANCH    OVRCD,DATE8000
.
          PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",FROMDATE
.
          CALL      PACDATE                 * Pack the from date
          MOVE      CPCDATE,STRTDATE
.
          GOTO      DATEF999
.
DATE8000  DISPLAY   *P1:23,*EF,*V2LON,"Invalid Booking From Date";
          CALL      HITENTER
          MOVE      ONE,EXIT
          GOTO      DATEF999
.
DATEF999  RETURN
+
.**************************************************************************
.         Keyin to date
.**************************************************************************
DATET000  MOVE      ZERO,EXIT
.
          DISPLAY   *P1:11,*EL,"Bookings To Date    : "
          MOVE      "11",CVERT
          MOVE      "23",CCOL
          MOVE      CCC,CCENT
          MOVE      CYY,CYEAR
          MOVE      CMM,CMON
          MOVE      CDD,CDAY
.
          CALL      KEYDATE                 * Keyin the to date
          BRANCH    OVRCD,DATET800
.
          PACK      TODATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE
.
          CALL      PACDATE                 * Pack the to date
          MOVE      CPCDATE,ENDDATE
.
DATET700  MATCH     TODATE,FROMDATE
          GOTO      DATET820 IF NOT LESS
.
          GOTO      DATET999
.
DATET800  DISPLAY   *P1:23,*EF,*V2LON,"Invalid Booking To Date";
          CALL      HITENTER
          MOVE      ONE,EXIT
          GOTO      DATET999
.
DATET820  DISPLAY   *P1:23,*EF,*V2LON,"FROMDATE should be less than TODATE ";
          CALL      HITENTER
          MOVE      TWO,EXIT
.
DATET999  RETURN
+
.-----------------------------------------------------------------------------
.         Keyin input full path
.-----------------------------------------------------------------------------
KASC0000  MOVE      ZERO,EXIT
          PACK      FNAMEI,SP70,SP70,SP70,SP70,SP70,SP70,SP70,SP70
          PACK      PTHNAME,SP70,SP70,SP70,SP70,SP70,SP70,SP70,SP70
.
          DISPLAY   *P23:13,*EF,"where fixnmdsc.txt resides"
.
          DISPLAY   *P1:16,*V2LON,"NOTE : Text file name fixnmdsc.txt":
                    " - Fixed length":
                    *P1:17,"Site(6),Clinic id(6),New NMDS Code(3)."
.
          KEYIN     *P1:14,"Full Path/Directory : ",*V2LON,PTHNAME
.
          MATCH     SP70,PTHNAME
          GOTO      KASC9000 IF EQUAL
          GOTO      KASC9000 IF EOS
.
          CALL      APPSLH00
.
          PACK      FNAMEI,PTHNAME,CTXTNAM,SP70
          STRIP     FNAMEI
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      TEMPFILE,FNAMEI
          TRAPCLR   IO
          BRANCH    OVRCD,KASC8000
.
          MOVE      ZERO,EXIT
          GOTO      KASC9999
.
KASC8000  DISPLAY   *P1:24,*EL,*B,"fixnmdsc.txt file not found.  ";
          CALL      HITENTER
          GOTO      KASC0000
.
KASC9000  MOVE      ONE,EXIT
.
KASC9999  RETURN
.
.----------------------------------------------------------------------------
. Append a forward slash at the end of the PTHNAME if the last character is 
. not a forward slash 
.----------------------------------------------------------------------------
APPSLH00  STRIP     PTHNAME
          MOVE      PTHNAME,SAVE500
          MOVELPTR  SAVE500,FORM3
          RESET     SAVE500
          MOVE      SAVE500,SAVE500X
.
. Get last character of the PTHNAME to see if it a slash "/", if it is not,
. append a slash "/" at the end
.
          MOVE      ZERO,F3
          WHILE     F3 < FORM3
            BUMP      SAVE500X
            ADD       ONE,F3
          DO
.
          MATCH     SLASH,SAVE500X
          IF        !@EQUAL
            PACK      PTHNAME,PTHNAME,SLASH
          ENDIF
.
APPSLH    RETURN 
.
.**************************************************************************
.*        Processing                                                      *
.**************************************************************************
PROC0000  MOVE      ZERO,RECCOUNT
.
PROC1000  READ      TEMPFILE,SEQ;XXXXSITE,XXXXCLIN,XXXXTCOD
          GOTO      PROC9000 IF OVER
.
          PACK      KEY28,XXXXSITE,XXXXCLIN,FROMDATE,SP70
          CALL      RDSBOKA1
PROC2000  CALL      RDKBOKA1
          BRANCH    OVRCD,PROC1000
.
          MATCH     XXXXSITE,OBASITE        * Correct site
          GOTO      PROC1000 IF NOT EQUAL
.
          MATCH     XXXXCLIN,OBACLIN        * Correct clinic
          GOTO      PROC1000 IF NOT EQUAL
.
          MATCH     OBADATE,TODATE          * Correct date range
          GOTO      PROC1000 IF LESS
.
          COMPARE   ONE,OBASTAT             * Booked 
          GOTO      PROC2500 IF EQUAL
.
          COMPARE   FOUR,OBASTAT            * Attended 
          GOTO      PROC2500 IF EQUAL
.
          COMPARE   FIVE,OBASTAT            * Non attendance
          GOTO      PROC2500 IF EQUAL
.
          GOTO      PROC2000
.
PROC2500  PACK      KEY8,OBAOUTNO,SP70
          CALL      RDBOKB1
          BRANCH    OVRCD,PROC2000
.
          ADD       ONE,RECCOUNT
.
          IF        COPTION=2
            DISPLAY   *P1:22,*EF,OBAOUTNO;
            MOVE      XXXXTCOD,OTBBNMDS     * NMDS Code (category NM)
            CALL      UPBOKB1
          ENDIF
          GOTO      PROC2000
.
PROC9000  IF        COPTION=1
            DISPLAY   *P1:22,*EF,"Records to be updated : ",RECCOUNT,*N;
          ELSE
            DISPLAY   *P1:22,*EF,"Records Updated : ",RECCOUNT,*N;
          ENDIF
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       OUTBOAIO/INC
          INC       OUTBB1IO/INC

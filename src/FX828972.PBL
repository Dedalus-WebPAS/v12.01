.***************************************************************************
.* System    :   PMI                                                       *
.* Program   :   FX828972                                                  *
.* Desc      :   Set PMI indicator for Scanned Medical Record - PMPXSN17   *
.***************************************************************************
.* Date      :   07/10/2002                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   This program will check for an admission/discharge or     *
.*               attended OP visit for a date range. If one exists it      *
.*               will set the PMI scanned medical record flag PTMXSN17     *
.*               to yes.                                                   *
.* Modifications  :                                                        *
.*        V10.14.01 09/07/2019  Ebon Clements TSK 0828972                  *
.*                  Ignore cancelled and pre admitted IP visits PROC0000   *
.*        V10.14.00 12/02/2019  Ebon Clements TSK 0828972                  *
.*                  Created program                                        *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       IBACONFD/INC
          INC       OUTSITFD/INC
          INC       PATCONFD/INC
          INC       PATDSCFD/INC
          INC       PATMI1FD/INC
          INC       PATHSPFD/INC 
          INC       PATVISFD/INC 
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
CURRDATE  DIM       8                       * Current date
HOSPCODE  DIM       3                       * Hospital filter
LOCNCODE  DIM       1                       * Location filter
LOOPCNTR  FORM      8                       * Loop counter
OUTPSITE  DIM       6                       * OP Site filter
STRTDATE  DIM       8                       * Start Date
.
PRGID     INIT      "FX828972"
PRGNAM    INIT      "Set PMI Scanned Medical Record Indicator"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
.
ML1000    CALL      KHOS0000               * Keyin hospital
          BRANCH    EXIT,ML0100
.
          CALL      KLOC0000               * Keyin Location Code
          BRANCH    EXIT,ML0100
.
          CALL      KSIT0000               * Keyin OP Site Code
          BRANCH    EXIT,ML0100
.
          CALL      DATE0000               * Keyin start date
          BRANCH    EXIT,ML0100
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P50:24,"Opening outsitaf"
          OPEN      OUTSITA1,"outsitaf"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA1,"patvisaf"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.******************************************************************************
.*                                  KHOS0000                                  *
.*                      Keyin Hospital ID                                     *
.******************************************************************************
KHOS0000  MOVE       ZERO,EXIT
          MOVE       SP70,PTHSHOSP
          MOVE       SP70,HOSPCODE
.
          COMPARE    ONE,IBCNMHOS
          GOTO       KHOS9999 IF NOT EQUAL
.
          MOVE       SP70,PTHSHOSP
          DISPLAY    *P1:10,*EL,"Hospital Id   : ";
          MOVE       TEN8,ECOL
          MOVE       TEN,EVERT
          MOVE       SP3,CKYIDEF3
          MOVE       ONE,CKYIMAND           * Mandatory
          OPEN       PATHSPA1,"pathspaf"
.
          CALL       PATHSPKY
          BRANCH     EXIT,KHOS9000,KHOS9000
.
KHOS3000  DISPLAY    *P22:10,*V2LON,PTHSNAME;
          PACK       HOSPCODE,PTHSHOSP,SP70
.
          MOVE       ZERO,EXIT
          GOTO       KHOS9999
.
KHOS9000  MOVE       ONE,EXIT
.
KHOS9999  RETURN
+
.******************************************************************************
.*                                  KLOC0000                                  *
.*                      Keyin Location Code                                   *
.******************************************************************************
KLOC0000  MOVE       SP70,LOCNCODE
          MOVE       ZERO,EXIT
.
          COMPARE    ONE,IBCNMHOS
          GOTO       KLOC9999 IF EQUAL
.
          KEYIN     *P1:11,*EL,"Location 1st Character : ":
                    *P26:11,*V2LON,LOCNCODE
.
          PACK       LOCNCODE,LOCNCODE,SP70
.
          MATCH      EXITCHAR,LOCNCODE
          GOTO       KLOC9000 IF EQUAL
.
          MATCH      SP70,LOCNCODE
          IF         @EQUAL
            DISPLAY    *P28:11,*V2LON,"All Locations"
          ENDIF
.
          MOVE       ZERO,EXIT
          GOTO       KLOC9999
.
KLOC9000  MOVE       ONE,EXIT
.
KLOC9999  RETURN
+
.******************************************************************************
.*                                  KSIT0000                                  *
.*                      Keyin OP Site Code                                    *
.******************************************************************************
KSIT0000  MOVE       SP70,OUTPSITE
          MOVE       ZERO,EXIT
.
          COMPARE    ONE,IBCNMHOS
          GOTO       KSIT9999 IF EQUAL
.
          KEYIN     *P1:12,*EL,"OP Site Code  :":
                    *P17:12,*V2LON,OUTPSITE
.
          PACK       OUTPSITE,OUTPSITE,SP70
.
          MATCH      EXITCHAR,OUTPSITE
          GOTO       KSIT9000 IF EQUAL
.
          MATCH      SP70,OUTPSITE
          IF         @EQUAL
            DISPLAY    *P24:12,*V2LON,"All OP Sites"
          ELSE
            PACK       KEY6,OUTPSITE,SP70
            CALL       RDSITA1
            IF         OVRCD=1
              DISPLAY    *P24:12,*V2LON,"Invalid OP Site Code"
              CALL       HITENTER
              GOTO       KSIT0000
            ENDIF
            DISPLAY    *P24:12,*V2LON,OSTDESC
          ENDIF
.
          MOVE       ZERO,EXIT
          GOTO       KSIT9999
.
KSIT9000  MOVE       ONE,EXIT
.
KSIT9999  RETURN
+
+
.******************************************************************************
.*                                  DATE0000              Called by: ML0000   *
.*                            Keyin The Date Range                            *
.******************************************************************************
DATE0000  MOVE      SP70,STRTDATE
          MOVE      ZERO,EXIT
.
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE           * get current date
.
          MOVE      "18",CCOL
          DISPLAY   *P1:13,"Start Date    : ",*EL;
.
          MOVE      ONE,CCANLDTE            * Blank cancel default
.
. ----- Keyin start date -----
.
DATE1000  MOVE      "13",CVERT
          MOVE      CCC,CCENT
          UNPACK    SP6,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin date
          BRANCH    OVRCD,DATE9900          * Nothing entered
.
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDATE
.
          MATCH     STRTDATE,CURRDATE
          IF        @LESS
            DISPLAY   *P1:24,*B,"Start date must be in the past.  ",*EL;
            CALL      HITENTER
            GOTO      DATE1000
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      DATE9999
.
DATE9900  MOVE      ONE,EXIT
.
DATE9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
PROC0000  MOVE      ZERO,LOOPCNTR           * Loop counter
.
          PACK      KEY8,SP70
          CALL      RSPTVIS1
PROC1000  CALL      RKPTVIS1                * Loop visit file
          BRANCH    OVRCD,PROC9999
.
          ADD       ONE,LOOPCNTR
.
          IF        LOOPCNTR=1000
            MOVE     ZERO,LOOPCNTR
            DISPLAY  *P15:20,*V2LON,"Processing Visit Number: ",PVIBILL
          ENDIF
.
          IF        PVITYPE <> 2 & PVITYPE <> 3
            GOTO      PROC1000              * Only process OP and IP visit types
          ENDIF
.
          MATCH     STRTDATE,PVIDATE        * Skip visit if date is less than
          GOTO      PROC1000 IF LESS        * the start date
.
          PACK      KEY8,PVIBILL,SP70
          CALL      RDPMVX11                * Read visit extn file
          BRANCH    OVRCD,PROC1000
.
          IF        PVITYPE = 3
            PACK      KEY8,PVIBILL,SP70
            CALL      RDMISS1                      * Read Admission
            BRANCH    OVRCD,PROC1000
.
            COMPARE   ONE,ASTAT                    * Skip pre admissions
            GOTO      PROC1000 IF EQUAL
.
            COMPARE   FIVE,ASTAT                   * Skip cancelled
            GOTO      PROC1000 IF EQUAL
          ENDIF
.
          IF        IBCNMHOS = 1
            MATCH     PMVXMHOS,HOSPCODE          * Hospital filter
            GOTO      PROC1000 IF NOT EQUAL    
          ENDIF
.
          IF        PVITYPE = 2 & IBCNMHOS <> 1
            MATCH     SP70,OUTPSITE
            IF        !@EQUAL
              MATCH     PVISITE,OUTPSITE         * OP site filter
              GOTO      PROC1000 IF NOT EQUAL    
            ENDIF
          ENDIF
.
          IF        PVITYPE = 3 & IBCNMHOS <> 1
            CALL      WARD0000                   * Location filter
            BRANCH    EXIT,PROC1000
          ENDIF
.
          CALL      UMAS0000                * Update PMI SMR flag
.
          GOTO      PROC1000
.
PROC9999  RETURN
+
.**************************************************************************
.*                               UMAS0000             Called by: PROC0000 *
.*                                                                        *
.**************************************************************************
UMAS0000  PACK      KEY8,PVIURNO,SP70
          CALL      RDPMPX21                * Read PMI extension file 2
          BRANCH    OVRCD,UMAS9999
.
          MOVE      ONE,PMPXSN17            * Set Scanned Medical Record Exists
.                                           * to yes
          CALL      UPPMPX21
.
UMAS9999  RETURN
+
.**************************************************************************
.*                               WARD0000             Called by: PROC0000 *
.*                                                                        *
.**************************************************************************
WARD0000  MATCH     SP70,LOCNCODE                * All locations
          GOTO      WARD8000 IF EQUAL
.
          PACK      KEY8,PVIBILL,SP70
          CALL      RDMISS1                      * Read Admission
          BRANCH    OVRCD,WARD9000
.
          IF        ASTAT <> 3
            MATCH     LOCNCODE,PTMIXWRD          * Expected Ward
            IF        @EQUAL
              GOTO      WARD8000 
            ELSE
              GOTO      WARD9000
            ENDIF
          ENDIF
.
          PACK      KEY8,PVIBILL,SP70
          CALL      RDDSCH1                      * Read Discharge
          BRANCH    OVRCD,WARD9000
.
          MATCH     LOCNCODE,PTDSDWRD            * Discharge ward
          GOTO      WARD8000 IF EQUAL
.
          GOTO      WARD9000
.
WARD8000  MOVE      ZERO,EXIT
          GOTO      WARD9999
.
WARD9000  MOVE      ONE,EXIT
          GOTO      WARD9999
WARD9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       PATHSPKY
.
          INC       OUTSITIO/INC 
          INC       PATDSCIO/INC
          INC       PATHSPIO/INC
          INC       PATMI1IO/INC
          INC       PATVISIO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC

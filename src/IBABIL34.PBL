. ******************************************************************************
. *              P R O G R A M  S U M M A R Y                                  *
. *              -------------  -------------                                  *
. *                                                                            *
. *     PROGRAM NAME:     IBABIL34                                             *
. *                                                                            *
. *     FUNCTION:         DEPOSIT REPORT (HELD/APPLIED/REFUNDED)               *
. *                                                                            *
. *     DATE WRITTEN:     08/12/87                                             *
. *                                                                            *
. *     AUTHOR:           J.TAN                                                *
. *                                                                            *
. *     MODIFICATIONS:                                                         *
. *                                                                            *
. ******************************************************************************
. *     V11.03.01 12/01/2022  J.Tan            TSK 0309320                     *
. *               Recompiled for RCPCIDFD changes                              *
. ******************************************************************************
. *     V11.02.01 12/01/2022  J.Tan            TSK 0309320                     *
. *               Mod for Transferred Deposit                                  *
. *     V10.14.01 22/03/2019  J.Tan            TSK 0857392                     *
. *               Changed RCP Transaction count from DIM3 to DIM5              *
. *     V10.13.01 05/12/2018  Don Nguyen     TSK 0838558                       *
. *               Deleted temp file (PSTROL) after processing                  *
. ******************************************************************************
. *     V10.11.02 27/11/2017  Thanh Tieu     TSK 0821710                       *
. *               Recompiled as PATMASTD changed                               *
. *     V10.11.01 13/09/2017  Thanh T.       TSK 0821710                       *
. *               Audit Amission/outpatient visit comments                     *
. ******************************************************************************
. *     V10.06.01 04/08/2015  Jill Parkinson CAR 317663                        *
. *               Removed V10.05.03 change. Checking cash drawer and pmvxmhos  *
. *               caused some deposits to never appear                         *
. *     V10.05.05 02/02/2015  Ania P         CAR 261630                        *
. *               Removed use of PORT for temp file naming                     *
. *     V10.05.04 05/12/2014  J.Tan          CAR 309342                        *
. *               Fixed NOT to Check Hospital ID for ALL hospital              *
. *     V10.05.03 22/10/2014  J.Tan          CAR 302018                        *
. *               Mods checking Hospital ID of Cash Drawer for Multi Hospital  *
. *     V10.05.02 09/10/2014  Peter Vela     CAR 304592                        *
. *               Added Discharged Patients Only option to Deposits Held /     *
. *               Inpatients                                                   *
. *     V10.05.01 21/07/2014 J.Tan           CAR 291099                        *
. *               Mods printing Billing Comments                               *
. *     V10.00.01 08/06/2010  Mike Laming    CAR 220802                        *
. *               Mod to change "End Date" in the future to be current date    *
. *     V9.12.01  08/10/2009  J.Tan          CAR 191624                        *
. *               Fixed the spelling for Applied deposit                       *
. *     V9.10.01  05/05/2008  J.Tan          CAR 162313                        *
. *               Added Deposit status to comdepaf                             *
. *               Added Options to print Deposit applied and refunded          *
. *     V9.07.01  12/09/2006  Jill Habasque CAR 109852                         *
. *               Mods to print alternate ID if PTCNDAUR=1                     *
. *     V9.04.01  J.Tan          CAR 52835                                     *
. *               Mods for multi hospital                                      *
. *     V9.03.02  Lina VO        CAR 50266                                     *
. *               Fixed count & display from 'Number of patients' to 'Number   *
. *               of deposits'.                                                *
. *     V9.03.01  Lina VO        CAR 50034                                     *
. *               Changed program to use new receipting table comdepaf.        *
. *               Use new Report Type for system and change report layout.     *
. *     V9.02.02  Dean Cramer    SRF 34746                                     *
. *               Fix where only portion of Receipt Number printed.            *
. *     V9.02.01  Steve Downing  SRF 11982                                     *
. *               Removed check for CSHDEP = "D" to balance with BIL19         *
. ******************************************************************************
. *       V5.08.01  28/08/2000  Caleb Sharman                                  *
. *                 Changed Health Fund variables to be 8 chars                *
. *                   V5.07.B02 25/01/1999  N Harrington                       *
. *                             507 rebound 148                                *
. *                   V5.07.B01 11/11/98  Jim Stathopoulos                     *
. *                             507 Changes                                    *
. ******************************************************************************
. *                       W2.01 19/04/88 G.Williams (I.B.A)                    *
. *                       Allow for 0 U/R numbers                              *
. *                       Add Totals for the $amt & #pats                      *
. *                       W2.02 06/02/89 K.Peak     (I.B.A)                    *
. *                       Added receipt number  SRF 4007                       *
. *                                                                            *
. *                       V5.01 15/05/89 D. Di.Paola                           *
. *                             Alter report to allow "8"                      *
. *                             character Adm. No.                             * 
. *                             Put in STD001FD.                               *
. *                       V5.02 27/05/89 J.Tan                                 *
. *                             Fixed Mother Adm. to form8                     *
. *                             (PATMI1FD).     SRF 100750                     *
. *                       V5.03 06/06/89 J.Tan                                 *
. *                             Mods Locking master file                       *
. *                             (PATIOMAS)     SRF 100809                      *
. *                                                                            *
. *                    V5.01.01 06/07/89 S. O'Gorman                           *
. *                             Converted for N.Z.                             *
. *                   V5.01.121 27/11/90 Glen Hobby                            *
. *                            Recompiled for changes to                       *
. *                            PATMX1FD                                        *
. *                                                                            *
. ******************************************************************************
.
          INC       STD001FD   
          INC       COMCODFD/INC
          INC       COMDEPFD/INC
          INC       EMRVISFD/INC
          INC       IBACONFD/INC
          INC       IBAPASFD/INC
          INC       IBASECFD/INC
          INC       PATCONFD/INC
          INC       OUTSITFD/INC
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
          INC       PATCODFD/INC
          INC       PMSUCHFD/INC
          INC       PMSUCDFD/INC
          INC       PATMASTD/INC
          INC       PATDO1FD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATDSCFD/INC
          INC       PATPR1FD/INC
          INC       PATHSPFD/INC
          INC       PMSVX1FD/INC
          INC       PATSELDT/INC
.
          INC       PRICMNFD/INC
          INC       RCPDTEFD/INC
          INC       RCPBNKFD/INC
          INC       RCPCIDFD/INC
          INC       VISMDTFD/INC
          INC       VISMTXFD/INC
          INC       IBASEQFD/INC  
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
.         VARIABLES DEFINITIONS
.
FNAMEI    DIM       8
TEMPFIL   IFILE     SQL, FIXED=238, KEY="u1-2,237-237,3-22,23-30,31-46"
.
. ----- Tempfile consists of following vars -----
.
. Name          Type      Length     Physical     Start   Description
. ----          ----      ------     --------     -----   -----------
DCMDESYS        DIM        2          2             1     visit Type 
.PSNAME         DIM       20         20             3     patient surname
.CMDEURNO       DIM        8          8            23     ur number
.CMDEADMN       DIM        8          8            31     admission Number
DUNIQUE         DIM        8          8            39     unique number
.PGNAME         DIM       25         25            47     patient givenname
.PADD1          DIM       35         35            72     address 1
.PADD2          DIM       35         35           107     address 2
.PSUBRB         DIM       35         35           142     suburb
.PPOST          DIM        8          8           177     postcode
.CMDERECN       DIM       12         12           185     receipt number
.CMDEDTYP       DIM        3          3           197     deposit Type
.CMDETDAT       DIM        8          8           200     receipt date 
.RCDTAMNT       FORM      12.2        8           208     deposit amount
.PBDEBT         FORM       1          2           216     bad debt
CLAIMCOD        DIM        3          3           218     claim type
DOCTATTN        DIM        6          6           221     attending doctor
CMTFLG          FORM       1          2           227     comment flag
.CMDEREFD       DIM        8          8           229     invoice/refunded date
.CMDETRSF       DIM        1          1           237     Deposit Transfered
.
.                                       ----- EOR 238 -----
.
CMDLINE         DIM       50
CODE            DIM       2
CODEDATE        DIM       8
COMMTFLG        FORM      1
COMS            DIM       3
CURRDATE        DIM       8
DESCEMR         INIT      "Emergency Deposits"
DESCINP         INIT      "Inpatient Deposits"
DESCOUT         INIT      "Outpatient Deposits"
DESCPRI         INIT      "Private Practice Deposits"
DESCSYST        DIM       25                 
DIM18           DIM       18
DIM3            DIM       3
DIM23           DIM       23
DIM40           DIM       40
DIM6            DIM       6
DIM8B           DIM       8
DIM9            DIM       9
DIM10B          DIM       10
DIM11           DIM       11
DISPATON        DIM       1
DNAME           DIM       22
DP              INIT      "DP"
ENDDTE8         DIM       8                       * End date (ccyymmdd)
FORM3           FORM      3
GTOTAMT         FORM      12.2
KEYNSTAT        DIM       1
OPCND           FORM      1
PNAME           DIM       20
SAVEDATE        DIM       10
SAVEDESC        DIM       13
STRTDTE8        DIM       8                       * Start date (ccyymmdd)
SAVSYST         FORM      2
SAVURNO         DIM       8
STCLM           DIM       3
TIMEIS          DIM       5
TOTAMT          FORM     12.2
TOTPAT          FORM      6
UNIQUE          FORM      8
VERT            FORM      2
.
SLSH      INIT      "/"
HELD      INIT      "- Held   "
APPLIED   INIT      "- Applied"
REFUNDED  INIT      "- Refunded "
.
.-----------------------------------------------------------------------
PRGID     INIT      "IBABIL34"
PRGNAM    INIT      "Deposit Report"
VERSION   INIT      "V12.01.00"
.-----------------------------------------------------------------------
+
.*********************************************************************
.         ML1000 OF PROGRAM
.*********************************************************************
ML0000    CALL      INIT0000
.
ML1000    CALL      OPTN0000
          BRANCH    EXIT,ML9000
.
          BRANCH    OPTION,ML2000,ML2000,ML2000
          GOTO      ML9000
.
ML2000    CALL      REPOPT00
          BRANCH    EXIT,ML1000
.
          IF        IBCNMHOS=1
            CALL      KHOSID00       * Keyin hospital id
            BRANCH    EXIT,ML1000
          ENDIF
.
          CALL      DISPAT00
.
          CALL      CNTCHK00
          BRANCH    EXIT,ML9000
          GOTO      ML1000
.
ML9000    CHAIN     PGM
          STOP
+
.*********************************************************************
.         initilisation
.*********************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening comdepaf";
          OPEN      COMDEPA5,"comdepaf"
          OPEN      COMDEPA7,"comdepaf"
.
          DISPLAY   *P64:24,"comcodaf";
          OPEN      COMCODA1,"comcodaf"
.
          DISPLAY   *P64:24,"emrvisaf";
          OPEN      EMRVISA1,"emrvisaf"
.
          DISPLAY   *P64:24,"outsitaf";
          OPEN      OUTSITA1,"outsitaf"
.
          DISPLAY   *P64:24,"outbb1af";
          OPEN      OUTBB1A1,"outbb1af"
.
          DISPLAY   *P64:24,"pmsuchaf";
          OPEN      PMSUCHA1,"pmsuchaf"
          DISPLAY   *P64:24,"pmsucdaf";
          OPEN      PMSUCDA1,"pmsucdaf"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patpramf";
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"rcpdteaf";
          OPEN      RCPDTEA1,"rcpdteaf"
.
          OPEN      RCPBNKA1,"rcpbnkaf"
          OPEN      RCPCIDA1,"rcpcidaf"
.
          DISPLAY   *P64:24,"vismdtaf";
          OPEN      VISMDTA1,"vismdtaf"
          DISPLAY   *P64:24,"vismtxaf";
          OPEN      VISMTXA1,"vismtxaf"
.
          DISPLAY   *P64:24,"pricmntf";
          OPEN       PRICMNT1,"pricmntf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS,*85,IBCNUGST
.
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
INIT9999  RETURN
+
.*********************************************************************
.         enter the option
.*********************************************************************
OPTN0000  MOVE      ZERO,OPTION
          MOVE      ZERO,COPTION
.
          DISPLAY   *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Deposit Held":
                    *P1:6,*V2LON,TWO,*HOFF," = Deposit Applied":
                    *P1:7,*V2LON,THREE,*HOFF," = Deposit Refunded"
.
          KEYIN     *P1:9,"Option : ",*EL,*V2LON,COPTION
          COMPARE   ZERO,COPTION
          GOTO      OPTN9999 IF EQUAL
          BRANCH    COPTION,OPTN0800,OPTN0800,OPTN0800
          BEEP
          GOTO      OPTN0000
.
OPTN0800  CALL      DATE0000                * Keyin the date range
          BRANCH    EXIT,OPTN9999
.
          DISPLAY   *P1:14,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:15,*V2LON,ONE,*HOFF," = Surname Sequence":
                   *P1:16,*V2LON,TWO,*HOFF," = Surname Sequence (Short Report)":
                    *P1:17,*V2LON,THREE,*HOFF," = Total Only"
.
OPTN1000  KEYIN     *P1:19,"Option : ",*EL,*V2LON,OPTION
.
          MOVE      ONE,EXIT
          COMPARE   ZERO,OPTION
          GOTO      OPTN9999 IF EQUAL
.
          MOVE      ZERO,EXIT
          BRANCH    OPTION,OPTN9999,OPTN9999,OPTN9999
          BEEP
          GOTO      OPTN1000
.
OPTN9999  RETURN
+
.******************************************************************************
.*                                  DATE0000                                  *
.*                        Keyin The Date Range                                *
.******************************************************************************
DATE0000  MOVE      "21",CCOL
          MOVE      "11",CVERT
          DISPLAY   *P1:CVERT,*EF,"Starting Date     : ";
          ADD       ONE,CVERT
          DISPLAY   *P1:CVERT,"Ending Date       : ";
.
          MOVE      ONE,CCANLDTE            * Blank cancel default
          MOVE      ZERO,CHIGHLT            * Use highlight
.
. ----- Enter starting date -----
.
DATE1000  SUB       ONE,CVERT
DATE2000  MOVE      CCC,CCENT
          UNPACK    SP6,CYEAR,CMON,CDAY
.
          MOVE      SP70,STRTDTE8
          BRANCH    COPTION,DATE2800        * Deposit held no 'From date'
.
          CALL      KEYDATE                 * Keyin date
          BRANCH    OVRCD,DATE9500
          BRANCH    CQUEST,DATE2000
.
          PACK      STRTDTE8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDTE8
.
          MATCH     STRTDTE8,CURRDATE
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"Start date must not be after the current ":
                      "date.  ";
            CALL      HITENTER
            GOTO      DATE2000
          ENDIF
.
. ----- Enter ending date -----
.
DATE2800  ADD       ONE,CVERT
DATE3000  UNPACK    STRTDTE8,CCENT,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin date
          BRANCH    OVRCD,DATE0000
          BRANCH    CQUEST,DATE3000
.
          PACK      ENDDTE8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDTE8
.
          MATCH     ENDDTE8,CURRDATE
          IF        @LESS
            MOVE      CURRDATE,ENDDTE8   
            UNPACK    CURRDATE,CCENT,CYEAR,CMON,CDAY  
            PACK      KEY10,CDAY,SLSH,CMON,SLSH,CCENT,CYEAR 
            REP       " 0",KEY10  
            DISPLAY   *P1:24,*B,*EL,"End date must not be after the current ":
                      "date.  ";
          ENDIF
.
DATE5000  MATCH     STRTDTE8,ENDDTE8
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"End date must be after the start date.  ";
            CALL      HITENTER
            GOTO      DATE3000
          ENDIF
.
DATE9000  MOVE      ZERO,EXIT
          GOTO      DATE9999
.
DATE9500  MOVE      ONE,EXIT
DATE9999  RETURN
+
.*********************************************************************
.         enter the report option
.*********************************************************************
REPOPT00  MOVE      SP3,STCLM
          MOVE      ZERO,FSTSYS
          MOVE      NINE,FEDSYS                   * Default system range 0-9
          DISPLAY   *P1:21,*EF:                    * Clear screen
                    *P1:21,"Enter Report Type  : ",UNDLN3
.
          PACK      CMCDDESC,SP70
          MOVE      "21",EVERT
          MOVE      TWENTY2,ECOL
          MOVE      ZERO,CKYIMAND                 * mandatory
          MOVE      CURRDATE,CODEDATE
          REP       " 0",CODEDATE
          MOVE      "RP",CODE
          CALL      COMCODKY
.
          MOVE      CMCDACOD,STCLM
.
          MATCH     SP3,STCLM
          GOTO      REPOPT99 IF EQUAL
.
          DISPLAY   *P40:21,*V2LON,CMCDDESC
.
          MOVE      ZERO,F1
          MOVE      CMCDSI01,F1
          IF        F1>0
            MOVE      F1,FSTSYS
            MOVE      F1,FEDSYS
          ENDIF
.
REPOPT99  RETURN
.*********************************************************************
.       Process Report
.*********************************************************************
KHOSID00  DISPLAY    *P1:22,*EL,"Hospital Id : ";
          MOVE       TEN5,ECOL
          MOVE       "22",EVERT
          MOVE       SP3,CKYIDEF3
          MOVE       ZERO,CKYIMAND           * Not Mandatory
          OPEN       PATHSPA1,"pathspaf"
.
          CALL       PATHSPKY
          BRANCH     EXIT,KHOSID80,KHOSID90
          MOVE       ZERO,EXIT
          GOTO       KHOSID99
.
KHOSID80  MOVE       "All Hospitals",PTHSNAME
          MOVE       ZERO,EXIT
          GOTO       KHOSID99
.
KHOSID90  MOVE       ONE,EXIT
KHOSID99  RETURN
+
.*********************************************************************
.       Process Report
.*********************************************************************
CNTCHK00  DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** Initialising Deposit File **"
          CALL      INDZD000
          BRANCH    OPCND,CNTCHK90
.
          MOVE      ZERO,CPAGENO
          MOVE      ONE,UNIQUE
          MOVE      SP70,KEYNSTAT
.
          DISPLAY   *P50:24,"U/R Number: ";
.
.
          MOVE      ZERO,FORM1
          LOAD      FORM1,COPTION,ZERO,ONE,TWO
          MOVE      FORM1,KEYNSTAT
.
          IF        COPTION=1
            PACK      KEY26,KEYNSTAT,SP70
            CALL      RSCMDEP5
          ELSE
            PACK      KEY26,KEYNSTAT,STRTDTE8,SP70
            CALL      RSCMDEP7
          ENDIF
.
CNTCHK10  IF        COPTION=1
            CALL      RKCMDEP5
          ELSE
            CALL      RKCMDEP7
          ENDIF
          BRANCH    OVRCD,CNTCHK80
.
          MATCH     KEYNSTAT,CMDESTAT
          GOTO      CNTCHK80 IF NOT EQUAL
.
          IF        COPTION=1
            MATCH     CMDETDAT,ENDDTE8        * Deposit held - check trans.date
            GOTO      CNTCHK80 IF LESS
          ELSE
            MATCH     CMDEREFD,ENDDTE8        * Deposit Applied/Refund -
            GOTO      CNTCHK80 IF LESS        * check invoice/refund date
          ENDIF
.
          COMPARE   FSTSYS,CMDESYST
          GOTO      CNTCHK10 IF LESS
.
          COMPARE   CMDESYST,FEDSYS
          GOTO      CNTCHK10 IF LESS
.
          CALL      INTREC00
          PACK      KEY17,CMDERECN,CMDETCNT,SP70
          CALL      RDRCDTE1
          BRANCH    OVRCD,CNTCHK10
.
.         Check Hospital id of Cash drawer for Multi hospital
.
          IF          IBCNMHOS=1
            PACK        KEY12,CMDERECN
            CALL        RDRCBNK1
            BRANCH      OVRCD,CNTCHK10
.
....        MATCH       SP10,PTHSHOSP    * CAR 317663
....        IF          !@EQUAL
....          MATCH       SP70,RCBNCDRW
....          IF          !@EQUAL
....            PACK        KEY6,RCBNCDRW
....            CALL        RDCIDA1
....            IF          OVRCD=0
....              MATCH       RCCIHOSP,PTHSHOSP
....              GOTO        CNTCHK10 IF NOT EQUAL
....            ENDIF
....          ENDIF
....        ENDIF
          ENDIF
.
          DISPLAY   *P65:24,*V2LON,CMDEURNO;
          MOVE      CMDEURNO,KEY8
          CALL      RDMAST1                  * patient master
          BRANCH    OVRCD,CNTCHK10
.
          BRANCH    CMDESYST,CNTCHK20:       * Emergency
                             CNTCHK20:       * Outpatient
                             CNTCHK20:       * Inpatient
                             CNTCHK30        * Private Practice
.
CNTCHK20  MATCH     SP8,CMDEADMN
          GOTO      CNTCHK25 IF EQUAL
.
          IF          IBCNMHOS=1
            MATCH       SP10,PTHSHOSP
            IF          !@EQUAL
              PACK        KEY8,CMDEADMN,SP8
              CALL        RDPMVX11
              BRANCH      OVRCD,CNTCHK10
              MATCH       PMVXMHOS,PTHSHOSP
              GOTO        CNTCHK10 IF NOT EQUAL
            ENDIF
          ENDIF
.
.       Reading Billing Comments if visit number exists
.
          CALL      RDBC0000            * Billing Comments
          GOTO      CNTCHK50
.
.       Reading UR Comments if visit number does not exists
.
CNTCHK25  PACK      KEY17,CMDEURNO,URCMTTYP,Z70      * Key for start of comment
          CALL      RSPMUCH1
CNTCHK26  CALL      RPPMUCH1
          BRANCH    OVRCD,CNTCHK27
.
          MATCH     CMDEURNO,PMUHURNO
          GOTO      CNTCHK27 IF NOT EQUAL
.
          MATCH     URCMTTYP,PMUHCTYP
          GOTO      CNTCHK27 IF NOT EQUAL
.
          MATCH     "1",PMUHDELT          * is comment deleted
          GOTO      CNTCHK26 IF EQUAL
.
. UR Comment found
.
          MOVE      ZERO,CMTFLG
          GOTO      CNTCHK50
.
. No UR comment found
.
CNTCHK27  MOVE      ONE,CMTFLG
          GOTO      CNTCHK50
.
.       Reading Private Practice Comments 
.
CNTCHK30  MOVE      ONE,FORM2           * UR scan flag
          MOVE      ONE,FORM3           * First line
          PACK      KEY13,CMDEURNO,FORM2,FORM3,SP70
          CALL      RDPRCM1
          MOVE      OVRCD,CMTFLG
          GOTO      CNTCHK50
.
CNTCHK50  BRANCH    CMDESYST,CNTCHK52:       * Emergency
                             CNTCHK54:       * Outpatient
                             CNTCHK56:       * Inpatient
                             CNTCHK70        * Private Practice
.
CNTCHK52  MATCH     SP8,CMDEADMN
          GOTO      CNTCHK70 IF EQUAL 
          MOVE      CMDEADMN,KEY8
          CALL      RDEMVIS1
          BRANCH    OVRCD,CNTCHK70
          PACK      DOCTATTN,EMVIDOCT,SP10
          PACK      CLAIMCOD,EMVICOMP,SP10
          GOTO      CNTCHK70
.
CNTCHK54  MATCH     SP8,CMDEADMN
          GOTO      CNTCHK70 IF EQUAL 
          MOVE      CMDEADMN,KEY8
          CALL      RDBOKB1
          BRANCH    OVRCD,CNTCHK70
          PACK      DOCTATTN,OBADOCT,SP10
          PACK      CLAIMCOD,OBACOMP,SP10
          GOTO      CNTCHK70
.
CNTCHK56  MATCH     SP8,CMDEADMN
          GOTO      CNTCHK70 IF EQUAL 
          MOVE      CMDEADMN,KEY8
          CALL      RDMISS1
.         BRANCH    OVRCD,CNTCHK70
          IF        OVRCD=ONE
            MATCH     "Y",DISPATON
            IF        @EQUAL
              MOVE      CMDEADMN,KEY8
              CALL      RDDSCH1
              BRANCH    OVRCD,CNTCHK10
            ENDIF
            GOTO      CNTCHK70
          ELSE
            MATCH     "Y",DISPATON
            IF        @EQUAL
              MOVE      CMDEADMN,KEY8
              CALL      RDDSCH1
              BRANCH    OVRCD,CNTCHK10
            ENDIF
          ENDIF
.
          PACK      DOCTATTN,ADOCTA,SP10
          PACK      CLAIMCOD,ACLAIM,SP10
          GOTO      CNTCHK70
.
CNTCHK70  MOVE      UNIQUE,DUNIQUE
          MOVE      CMDESYST,DCMDESYS
          REP       " 0",CMDETRSF
          PACK      KEY47,DCMDESYS,CMDETRSF,PSNAME,CMDEURNO,CMDEADMN,DUNIQUE
          CALL      WRTEMP1
          ADD       ONE,UNIQUE
          GOTO      CNTCHK10
.
CNTCHK80  CALL      PRTREP00         * Print Report
.
          CALL      KILLID00
          GOTO      CNTCHK99
.
CNTCHK90  MOVE      ONE,EXIT
CNTCHK99  RETURN
+
.*********************************************************************
.      Initialize record
.*********************************************************************
INTREC00  MOVE      SP30,PADD1
          MOVE      SP30,PADD2
          MOVE      SP20,PSUBRB
          MOVE      SP6,PPOST
          MOVE      SP30,PSNAME
          MOVE      SP30,PGNAME
          MOVE      SP6,CLAIMCOD
          MOVE      SP6,DOCTATTN
          MOVE      ZERO,RCDTAMNT
          MOVE      ONE,CMTFLG
.
INTREC99  RETURN
+
.*********************************************************************
.      Create an indexed file based on port
.*********************************************************************
INDZD000  CALL      TFILENAM
          MOVE      TFILNAME,FNAMEI
.
......... CHECK IF PREVIOUSLY EXISTS..IF SO DELETED IT 
.
          CALL      KILLID00
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          APPEND    " 238 U1-2,237-237,3-22,23-30,31-46",CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          DISPLAY   *P1:10,*EF;
.
          MOVE      ZERO,OPCND
          TRAP      NOINDZ00 IF IO
          OPEN      TEMPFIL,FNAMEI
          TRAPCLR   IO
.
INDZD999  RETURN
+
.*********************************************************************
.       No File found
.*********************************************************************
NOINDZ00  MOVE      ONE,OPCND
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Temporary Index File ":
                    FNAMEI," Not Found **"
          TRAPCLR   IO
.
NOINDZ99  RETURN
+
.*********************************************************************
.       Deleting an indexed file based on port
.*********************************************************************
KILLID00  CLOSE     TEMPFIL
          DISPLAY   *P1:24,*EL,*P20:24,*V2LON,"** Deleting Indexed File **"
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
KILLID99  RETURN
+
.*********************************************************************
.                 HEADINGS FOR OUTPUT
.*********************************************************************
HEAD0000  MOVE      ONE,CNOUNDLN
          CALL      IBACLOCK    
          MOVE      SP70,CPHDROPT
          LOAD      CPHDROPT,COPTION,HELD,APPLIED,REFUNDED
          CALL      HEAD132
.
          MOVE      TEN1,CLNO
          BRANCH    COPTION,HEAD0200         * Deposit held
.
          UNPACK    STRTDTE8,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format the start date
          PRINT     *40,"Start Date        : ",CPCDATE
          ADD       ONE,CLNO
.
HEAD0200  UNPACK    ENDDTE8,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format the end date
          PRINT     *40,"End Date          : ",CPCDATE,*N
.
          IF        IBCNMHOS =1
            PRINT     *40,"Hospital : ",PTHSHOSP,"    ",PTHSNAME
            ADD       ONE,CLNO
          ENDIF
.
          MOVE      SP70,SAVEDESC
          IF        COPTION=1
            MOVE      "Deposit Date ",SAVEDESC
          ELSE
            IF        COPTION=2
              MOVE      "Applied Date ",SAVEDESC
            ELSE
              MOVE      "Refunded Date",SAVEDESC
            ENDIF
          ENDIF
.
          BRANCH    OPTION OF HEAD1000,HEAD2000,HEAD3000
.
HEAD1000  PRINT     *N,*40,"SURNAME SEQUENCE - ",DESCSYST;
.
          CALL      DPATHD00
.
          PRINT     *N:
                    *N,*3,"U/R",*12,"Adm.",*69,"Receipt",*79,"Receipt":
                    *88,"Bad",*93,"Deposit Type":
                    *N,*3,"No.",*12,"No.",*19,"Patient Name",*40,"Address":
                    *70,"Number",*80,"Date",*88,"Debt":
                    *93,SAVEDESC:
                    *121,"Amount":
                    *N,*1,"--------",*10,"--------",*19,"--------------------":
                    *40,"-----------------------",*64,"------------":
                    *77,"----------",*88,"----",*93,"-----------------":
                    *111,"----------------"
.
          GOTO      HEAD9999
.
HEAD2000  MOVE      SP70,KEY10
          IF        COPTION=1
            MOVE      "Deposit ",KEY8
            MOVE      "  Date",KEY6
          ELSE
            IF        COPTION=2
              MOVE      " Applied",KEY8
              MOVE      "  Date",KEY6
            ELSE
              MOVE      "Refunded",KEY8
              MOVE      "  Date",KEY6
            ENDIF
          ENDIF
.
          PRINT     *N,*40,"SURNAME SEQUENCE - ",DESCSYST,"(SHORT REPORT)";
.
          CALL      DPATHD00
.
          PRINT     *N:
                    *N,*3,"U/R",*12,"Adm.",*46,"Receipt",*56,"Receipt":
                    *65,KEY8:
                    *110,"Attending":
                    *120,"Bad",*125,"Claim":
                    *N,*3,"No.",*12,"No.",*19,"Patient Name",*47,"Number":
                    *57,"Date":
                    *65,KEY6:
                    *75,"Deposit Type":
                    *103,"Amount",*112,"Doctor",*120,"Debt",*125,"Type":
                    *130,"Cmt":
                    *N,*1,"--------",*10,"--------":
                    *19,"---------------------",*41,"------------":
                    *54,"---------",*64,"----------":
                    *75,"-----------------",*93,"----------------":
                    *110,"---------",*120,"---",*125,"----",*130,"---"
.
          GOTO      HEAD9999
.
HEAD3000  PRINT     *N,*40,DESCSYST;
          CALL      DPATHD00
.
          PRINT     *N;
          CALL      UND132
          GOTO      HEAD9999
.
HEAD9999  RETURN
.
.
.*********************************************************************
.              PRINT REPORT
.*********************************************************************
PRTREP00  DISPLAY    *P1:24,*EL,*P10:24,*V2LON:
                     "** Generating Report **"
          DISPLAY    *P1:24,*EL,*P20:24,"Printing Deposit :"
.
          CALL      INTREC00
          MOVE      ZERO,TOTAMT
          MOVE      ZERO,GTOTAMT
          MOVE      ZERO,TOTPAT
          MOVE      SP10,SAVURNO
          MOVE      ZERO,SAVSYST
          MOVE      "60",CLNO
          MOVE      SP70,KEY47
          CALL      RDSTEMP1
PRTREP10  CALL      RDKTEMP1
          BRANCH    OVRCD,PRTREP60
.
          MOVE      DCMDESYS,CMDESYST
          MOVE      DUNIQUE,UNIQUE
.
          DISPLAY   *P50:24,*V2LON,CMDEURNO;
.
          COMPARE   CMDESYST,SAVSYST
          IF        !@EQUAL
            IF        SAVSYST<>0
              CALL      PRTST000       * Print System Total
              ADD       TOTAMT,GTOTAMT
              MOVE      ZERO,TOTAMT
              MOVE      ZERO,TOTPAT
            ENDIF
            MOVE      CMDESYST,SAVSYST
            LOAD      DESCSYST,CMDESYST,DESCEMR,DESCOUT,DESCINP,DESCPRI
            CALL      HEAD0000
          ELSE
            COMPARE   "55",CLNO
            CALL      HEAD0000 IF NOT LESS
          ENDIF
.
          ADD       ONE,TOTPAT
          ADD       RCDTAMNT,TOTAMT
          MOVE      PGNAME,ANS
          PACK      PNAME,ANS,DOT,PSNAME
.
          MOVE      DNO,DIM3
          LOAD      DIM3,PBDEBT,DYES
.
          UNPACK    CMDETDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PACK      DIM8B WITH CPDATE
          PACK      DIM10B WITH CPCDATE
.
          MOVE      SP30,TDESC
          MOVE      SP30,DIM18
          PACK      KEY5,DP,CMDEDTYP
          CALL      RDCODE1
          IF        OVRCD=0
            MOVE      TDESC,DIM18
          ELSE
            MOVE      CMDEDTYP,DIM18
          ENDIF
.
          MOVE      SP70,SAVEDATE               * Deposit/Applied/Refunded date
          IF        COPTION=1
            UNPACK    CMDETDAT,CCENT,CYEAR,CMON,CDAY
          ELSE
            UNPACK    CMDEREFD,CCENT,CYEAR,CMON,CDAY
          ENDIF
          CALL      PACDATE
          MOVE      CPCDATE,SAVEDATE
.
          BRANCH    OPTION,PRTREP40,PRTREP50,PRTREP10
.
.      Full Report
.
PRTREP40  CLEAR     DIM40
          MATCH     SP70,PADD2
          IF        !@EQUAL
            STRIP     PADD2
            APPEND    PADD2,DIM40
            APPEND    SP2,DIM40
          ENDIF
          STRIP     PSUBRB
          APPEND    PSUBRB,DIM40
          APPEND    SP2,DIM40
          APPEND    PPOST,DIM40
          RESET     DIM40
.
          MOVE      PADD1,DIM23
.
          OPEN         CONTROLF,"controlf"
          READ         CONTROLF,HUND10;*249,PTCNDAUR
          PACK         DIM9,CMDEURNO,SP70
          MATCH        "1",PTCNDAUR
          IF           @EQUAL
            MOVE         CMDEURNO,PURNO
            PACK         KEY20,SP70
            PROC         GETALIDS
            MATCH        SP70,KEY20
            IF           !@EQUAL
              UNPACK       KEY20,DIM11,DIM9
            ENDIF
          ENDIF
.
          MOVE      SP70,KEY1
          MATCH     "1",CMDETRSF
          IF        @EQUAL
            MOVE      "*",KEY1                 * Flag for Transferred deposit
          ENDIF
.
          PRINT     *1,DIM9,*10,CMDEADMN,KEY1,*19,PNAME,*40,DIM23:
                    *64,CMDERECN,*77,DIM10B,*89,DIM3,*93,DIM18:
                    *111,"$",RCDTAMNT:
                    *N,*40,DIM40,*93,SAVEDATE
          ADD       TWO,CLNO
.
          MATCH     SP10,CLAIMCOD
          IF        !@EQUAL
            PACK      KEY5,ANSC,ANSL,CLAIMCOD
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      CLAIMCOD,TDESC
            ENDIF
            PRINT     *19,"Claim Code: ",TDESC
            ADD       ONE,CLNO
          ENDIF
.
          MATCH     SP10,DOCTATTN
          IF        !@EQUAL
            MOVE      SP30,DNAME
            MOVE      DOCTATTN,KEY6
            CALL      RDDOCT1
            IF        OVRCD=0
              MOVE      DGNAME,ANS
              PACK      DNAME,ANS,DOT,DSNAME
            ELSE
              MOVE      DOCTATTN,DNAME
            ENDIF
            PRINT     *19,"Attending Doctor: ",DNAME
            ADD       ONE,CLNO
          ENDIF
.
          CALL      PRTCMT00    * Print Comments
          PRINT     *1
          ADD       ONE,CLNO
          GOTO      PRTREP10
.
.         Print statement for short report
.
PRTREP50  MOVE      DYES,COMS
          LOAD      COMS,CMTFLG,DNO
.
          OPEN         CONTROLF,"controlf"
          READ         CONTROLF,HUND10;*249,PTCNDAUR
          PACK         DIM9,CMDEURNO,SP70
          MATCH        "1",PTCNDAUR
          IF           @EQUAL
            MOVE         CMDEURNO,PURNO
            PACK         KEY20,SP70
            PROC         GETALIDS
            MATCH        SP70,KEY20
            IF           !@EQUAL
              UNPACK       KEY20,DIM11,DIM9
            ENDIF
          ENDIF
.
          MOVE      SP70,KEY1
          MATCH     "1",CMDETRSF
          IF        @EQUAL
            MOVE      "*",KEY1                 * Flag for Transferred deposit
          ENDIF
.
         PRINT     *1,DIM9,*10,CMDEADMN,KEY1,*19,PNAME,*40,CMDERECN,*53,DIM10B:
                    *64,SAVEDATE:
                    *75,DIM18,*93,"$",RCDTAMNT:
                    *112,DOCTATTN,*120,DIM3,*125,CLAIMCOD,*130,COMS
.
          ADD       ONE,CLNO
          GOTO      PRTREP10
.
.        Print end of report
.
PRTREP60  COMPARE   ZERO,CPAGENO
          GOTO      PRTREP84 IF EQUAL
.
          CALL      PRTST000       * Print System Total
          ADD       TOTAMT,GTOTAMT
.
.       Print Final Total only if Consolidated/All Report
.
          IF        FSTSYS<>FEDSYS
            BRANCH    OPTION,PRTREP80,PRTREP82,PRTREP80
          ENDIF
          GOTO      PRTREP90
.
PRTREP80  IF        COPTION=1
            PRINT     *N,*78,"Total of All Deposits Held : ";
          ELSE
            IF        COPTION=2
              PRINT     *N,*75,"Total of All Deposits Applied : ";
            ELSE
              PRINT     *N,*74,"Total of All Deposits Refunded : ";
            ENDIF
          ENDIF
.
          PRINT     *111,"$",GTOTAMT,*N:
                    *N,*N,"  ***  END OF REPORT  ***"
          GOTO      PRTREP90
.
PRTREP82  IF        COPTION=1
            PRINT     *N,*60,"Total of All Deposits Held : ";
          ELSE
            IF        COPTION=2
              PRINT     *N,*57,"Total of All Deposits Applied : ";
            ELSE
              PRINT     *N,*56,"Total of All Deposits Refunded : ";
            ENDIF
          ENDIF
.
          PRINT     *93,"$",GTOTAMT,*N:
                    *N,*N,"  ***  END OF REPORT  ***"
          GOTO      PRTREP90
.
PRTREP84  DISPLAY   *P1:24,*EL,*P10:24,*B,*V2LON,"No Patient Selected"
          GOTO      PRTREP90
.
PRTREP90  DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** Report Generation Completed **"
.
PRTREP99  RETURN
+
.*********************************************************************
.      Display System Totals
.*********************************************************************
PRTST000  BRANCH    OPTION,PRTST100,PRTST200,PRTST100
          GOTO      PRTST999
.
PRTST100  IF        COPTION=1
            PRINT     *N,*1,"Number of Deposits Held : ",TOTPAT;
          ELSE
            IF        COPTION=2
              PRINT     *N,*1,"Number of Deposits Applied : ",TOTPAT;
            ELSE
              PRINT     *N,*1,"Number of Deposits Refunded : ",TOTPAT;
            ENDIF
          ENDIF
.
          PRINT     *73,DESCSYST," Total : ",*111,"$",TOTAMT
          ADD        ONE,CLNO
          CALL       UND132
          GOTO       PRTST999
.
PRTST200  IF        COPTION=1
            PRINT     *N,*1,"Number of Deposits Held : ",TOTPAT;
          ELSE
            IF        COPTION=2
              PRINT     *N,*1,"Number of Deposits Applied: ",TOTPAT;
            ELSE
              PRINT     *N,*1,"Number of Deposits Refunded : ",TOTPAT;
            ENDIF
          ENDIF
.
          PRINT     *55,DESCSYST," Total : ",*93,"$",TOTAMT
          ADD        ONE,CLNO
          CALL       UND132
          GOTO       PRTST999
.
PRTST999  RETURN
.*********************************************************************
.      Display Comments
.*********************************************************************
PRTCMT00  BRANCH    CMTFLG,PRTCMT99
          PRINT     *19,"Comment: ";
          MOVE      ZERO,COMMTFLG
.
PRTCMT10  MATCH     SP8,CMDEADMN
          GOTO      PRTCMT20 IF EQUAL
.
.       Reading Billing Comments if visit number exists
.
          MOVE      "003",KEY3            * Billing Comments
          PACK      KEY17,CMDEADMN,KEY3,Z70
          CALL      RSVSMDT1
PRTCMT15  CALL      RPVSMDT1
          BRANCH    OVRCD,PRTCMT90          * end of file
.
          MATCH     VSMDVISN,CMDEADMN       * same Admission Number?
          GOTO      PRTCMT90 IF NOT EQUAL
.
          MATCH     KEY3,VSMDTYPE           * same comment type?
          GOTO      PRTCMT90 IF NOT EQUAL
.
          MATCH     "1",VSMDDELT            * comment deleted?
          GOTO      PRTCMT15 IF EQUAL
.
          PACK      KEY20,VSMDVISN,VSMDTYPE,VSMDNOTE,SP70
          CALL      RSVSMTX1
PRTCMT18  CALL      RKVSMTX1
          BRANCH    OVRCD,PRTCMT15
.
          MATCH     VSMDVISN,VSMTVISN       * same Admission Number?
          GOTO      PRTCMT15 IF NOT EQUAL
.
          MATCH     VSMDTYPE,VSMTTYPE
          GOTO      PRTCMT15 IF NOT EQUAL
.
          MATCH     VSMDNOTE,VSMTNOTE
          GOTO      PRTCMT15 IF NOT EQUAL
.
          PRINT     *28,VSMTCMNT
          ADD       ONE,CLNO
          MOVE      ONE,COMMTFLG
          GOTO      PRTCMT18
.
.       Reading UR Comments if visit number does not exists
.
PRTCMT20  PACK      KEY17,CMDEURNO,URCMTTYP,Z70
          CALL      RSPMUCH1
PRTCMT21  CALL      RPPMUCH1
          BRANCH    OVRCD,PRTCMT90
.
          MATCH     CMDEURNO,PMUHURNO
          GOTO      PRTCMT90 IF NOT EQUAL
.
          MATCH     URCMTTYP,PMUHCTYP
          GOTO      PRTCMT90 IF NOT EQUAL
.
          MATCH     "1",PMUHDELT            * is comment deleted
          GOTO      PRTCMT21 IF EQUAL
.
          PACK      KEY20,PMUHURNO,PMUHCTYP,PMUHCNUM,SP70
          CALL      RSPMUCD1
PRTCMT22  CALL      RKPMUCD1
          BRANCH    OVRCD,PRTCMT21
.
          MATCH     PMUHURNO,PMUCURNO
          GOTO      PRTCMT21 IF NOT EQUAL
.
          MATCH     PMUHCTYP,PMUCCTTY
          GOTO      PRTCMT21 IF NOT EQUAL
.
          MATCH     PMUHCNUM,PMUCCNUM
          GOTO      PRTCMT21 IF NOT EQUAL
.
          MATCH     SP70,PMUCCOMM
          GOTO      PRTCMT22 IF EQUAL
.
          PRINT     *28,PMUCCOMM
          ADD       ONE,CLNO
          MOVE      ONE,COMMTFLG
          GOTO      PRTCMT22
.
.       Reading Private Practice Comments
.
PRTCMT30  MOVE      ONE,FORM2           * UR scan flag
          PACK      KEY13,CMDEURNO,FORM2,FORM3,SP70
          CALL      RSPRCM1
PRTCMT35  CALL      RKPRCM1
          BRANCH    OVRCD,PRTCMT90
.
          MATCH     CMDEURNO,PRCMDEBT
          GOTO      PRTCMT90 IF NOT EQUAL
          COMPARE   FORM2,PRCMSCAN
          GOTO      PRTCMT90 IF NOT EQUAL
          MATCH     SP70,PRCMLINE
          GOTO      PRTCMT35 IF EQUAL
.
          PRINT     *28,PRCMLINE
          ADD       ONE,CLNO
          MOVE      ONE,COMMTFLG
          GOTO      PRTCMT35
.
PRTCMT90  COMPARE   ONE,COMMTFLG           * comments printed
          GOTO      PRTCMT99 IF EQUAL
.
          PRINT     *28                    * No comments found
          ADD       ONE,CLNO
.
PRTCMT99  RETURN
+
.*********************************************************************
.       Find End
.*********************************************************************
FINDEN00  RESET       DIM40,40
FINDEN10  CMATCH      SP1,DIM40
          RETURN      IF NOT EQUAL
          BUMP        DIM40,-1
          GOTO        FINDEN10 IF NOT EOS
.
          RESET       DIM40
FINDEN99  RETURN
+
.******************************************************************************
.*        RDBC0000 - Read Billing Comments                                    *
.******************************************************************************
RDBC0000  MOVE      "003",KEY3            * Billing Comments
          PACK      KEY17,CMDEADMN,KEY3,Z70
          CALL      RSVSMDT1
RDBC1000  CALL      RPVSMDT1
          BRANCH    OVRCD,RDBC9000          * end of file
.
          MATCH     VSMDVISN,CMDEADMN       * same Admission Number?
          GOTO      RDBC9000 IF NOT EQUAL
.
          MATCH     KEY3,VSMDTYPE           * same comment type?
          GOTO      RDBC9000 IF NOT EQUAL
.
          MATCH     "1",VSMDDELT            * comment deleted?
          GOTO      RDBC1000 IF EQUAL
.
          PACK      KEY20,VSMDVISN,VSMDTYPE,VSMDNOTE,SP70
          CALL      RSVSMTX1
          CALL      RKVSMTX1
          BRANCH    OVRCD,RDBC9000
.
          MATCH     VSMDVISN,VSMTVISN       * same Admission Number?
          GOTO      RDBC9000 IF NOT EQUAL
.
          MATCH     VSMDTYPE,VSMTTYPE
          GOTO      RDBC9000 IF NOT EQUAL
.
          MATCH     VSMDNOTE,VSMTNOTE
          GOTO      RDBC9000 IF NOT EQUAL
.
          MOVE      ZERO,CMTFLG
          GOTO      RDBC9999
.
RDBC9000  MOVE      ONE,CMTFLG
RDBC9999  RETURN
+
.*********************************************************************
.       Keyin Discharged Patients Only
.*********************************************************************
DISPAT00  MOVE        SP70,DISPATON
.
          COMPARE     ONE,COPTION
          GOTO        DISPAT99 IF NOT EQUAL
.
          COMPARE     THREE,FSTSYS
          GOTO        DISPAT99 IF NOT EQUAL
.
DISPAT10  KEYIN     *P1:23,"Discharged Patient Only Y/N: ",*EL,*V2LON,DISPATON
.
          MATCH     "Y",DISPATON
          GOTO      DISPAT99 IF EQUAL
.
          MATCH     "N",DISPATON
          GOTO      DISPAT99 IF EQUAL
.
          BEEP
.
          GOTO      DISPAT10
.
DISPAT99  RETURN
+
.*********************************************************************
.       Discharged Patients Only Header
.*********************************************************************
DPATHD00  MATCH     "Y",DISPATON
          GOTO      DPATHD99 IF NOT EQUAL
.
          PRINT     " - Discharged Patients Only";
.
DPATHD99  RETURN
.*****************************************************************************
.*  GETALTID
.*  Procedure to get an alternate ID
.*  Exit=0  No alternate ID
.*       1  Alternate ID's exist
.*****************************************************************************
          DEFPROC   GETALIDS
.
          INC       PMSAIDFD/INC
.
          MOVE      ZERO,EXIT
          PACK      KEY20,SP70
          OPEN      PMSAIDA1,"pmsaidaf"
.
          PACK      KEY31,PURNO,SP70
          CALL      RSPMAID1           * Read alternate id Table
GETALI10  CALL      RKPMAID1
          BRANCH    OVRCD,GETALI99
.
          MATCH     PURNO,PMAIURNO     * Check same UR
          GOTO      GETALI99 IF NOT EQUAL
.
          MATCH     "1",PMAIDISU
          GOTO      GETALI10 IF NOT EQUAL
.
          PACK      KEY20,PMAIALID,SP70
          MOVE      ONE,EXIT
          GOTO      GETALI99
.
          INC       PMSAIDIO/INC
          INC       IBAOVRIO/INC
.
GETALI99  CLOSE     PMSAIDA1
.
          ENDPROC
+
.*********************************************************************
RDSTEMP1  RESET     KEY47
          READ      TEMPFIL,KEY47;;
          RETURN
.
RDKTEMP1  MOVE      ZERO,OVRCD
          READKS    TEMPFIL;DCMDESYS,PSNAME,CMDEURNO,CMDEADMN,DUNIQUE:
                             PGNAME,PADD1,PADD2,PSUBRB,PPOST:
                             CMDERECN,CMDEDTYP,CMDETDAT,RCDTAMNT:
                             PBDEBT,CLAIMCOD,DOCTATTN,CMTFLG,CMDEREFD:
                             CMDETRSF
          GOTO      OVERCOND IF OVER
          RETURN

WRTEMP1   RESET     KEY47
          MOVE      ZERO,OVRCD
          READ      TEMPFIL,KEY47;ANS
          GOTO      OVERCOND IF NOT OVER
          WRITE     TEMPFIL,KEY47;DCMDESYS,PSNAME,CMDEURNO,CMDEADMN,DUNIQUE:
                                   PGNAME,PADD1,PADD2,PSUBRB,PPOST:
                                   CMDERECN,CMDEDTYP,CMDETDAT,RCDTAMNT:
                                   PBDEBT,CLAIMCOD,DOCTATTN,CMTFLG,CMDEREFD:
                                   CMDETRSF
          RETURN
.
.*********************************************************************
          INC       STD001IO
          INC       COMCODKY
          INC       PATHSPKY
.
          INC       COMCODIO/INC
          INC       COMDEPIO/INC
          INC       EMRVISIO/INC
          INC       IBAPASIO/INC
          INC       IBASECIO/INC
          INC       OUTSITIO/INC
          INC       OUTBB1IO/INC
          INC       PMSUCHIO/INC
          INC       PMSUCDIO/INC
          INC       PATCODIO/INC
          INC       PATDSCIO/INC
          INC       PATDO1IO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATPR1IO/INC
          INC       PATHSPIO/INC
          INC       PMSVX1IO/INC
.
          INC       PRICMNIO/INC
          INC       RCPDTEIO/INC
          INC       RCPBNKIO/INC
          INC       RCPCIDIO/INC
          INC       VISMDTIO/INC
          INC       VISMTXIO/INC
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC

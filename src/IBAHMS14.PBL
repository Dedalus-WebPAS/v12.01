.**********************************************************************
.* System   : HOSPITAL MANAGEMENT SYSTEM                              *
.* Program  : IBAHMS14                                                *
.* Desc     : PRS2 ACCOUNT STATUS MASTER LISTING                      *
.**********************************************************************
.* Date     : 29/11/91                                                *
.* Author   : DIG                                                     *
.* Comment  :                                                         *
.* Mods     : V5.01.02  17/06/94  ROD                                 *
.*                      Add Previous Year Account Status              *
.**********************************************************************
          INC       STD001FD
          INC       PATACCFD/INC
          INC       PATCODFD/INC
+
.**********************************************************************
.*                             CONSTANTS                              *
.**********************************************************************
CATA      INIT      "A "
CATCL     INIT      "CL"
.
UNDLN115  INIT     "*-------------------------------------------------------":
                   "----------------------------------------------------------*"
+
.**********************************************************************
.*                       GLOBAL VARIABLES                             *
.**********************************************************************
ATYPE1    DIM       3
ATYPE2    DIM       3
.
CLAIM1    DIM       3
CLAIM2    DIM       3
CODE      DIM       2
.
DESCATY1  DIM       20
DESCATY2  DIM       20
DESCCLM1  DIM       20
DESCCLM2  DIM       20
.
FINISH1   DIM       6
FINISH2   DIM       6
.
PRNTATYP  DIM       3
PRNTCLAM  DIM       3
.
SAVATYPE  DIM       3
SAVCLAIM  DIM       3
START1    DIM       6
START2    DIM       6
.
TEMPADSC  DIM       20
TEMPCDSC  DIM       20
.
PRGID     INIT      "IBAHMS14"
PRGNAM    INIT      "Prs2 Account Status Master Listing"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                           MAINLINE                                 *
.*                       Controlling Logic                            *
.**********************************************************************
ML0000    CALL      INIT0000                * initialization
.                                             open files
.                                             display header
.
.------ select an option ------
.
ML1000    CALL      OPTN0000                * main option screen
          BRANCH    EXIT,ML9999
.
.------ enter the ranges to print for ------
.
ML2000    CALL      CRNG0000                * enter claim code range
          BRANCH    EXIT,ML1000
.
          CALL      ARNG0000                * enter admission type range
          BRANCH    EXIT,ML1000
.
          CALL      CONT0000                * see if O.K. to continue
          BRANCH    EXIT,ML3000,ML2000,ML1000
.
.------ print the master listing ------
.
ML3000    CALL      PRNT0000                * print the master listing
.
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.**********************************************************************
.*                           INIT0000                                 *
.*               Open files and display header                        *
.**********************************************************************
INIT0000  CALL      DISPHEAD                * display screen header
.
          DISPLAY   *P50:24,"Opening ":
                    *P60:24,"pataccaf";
          OPEN      PATACCA1,"pataccaf"
.
          DISPLAY   *P60:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          MOVE      ONE,CNEWDS
.
INIT9999  RETURN
+
.**********************************************************************
.*                           OPTN0000                                 *
.*                   Display the main option screen                   *
.**********************************************************************
OPTN0000  MOVE      FALSE,EXIT
.
          CALL      SCR0000                 * display screen format
.
.------ keyin the option ------
.
OPTN1000  MOVE      ZERO,OPTION
          DISPLAY   *P1:7,*EL,"Please Select : ";
          KEYIN     *V2LON,OPTION;
.
          COMPARE   ZERO,OPTION             * see if exit was chosen
          GOTO      OPTN9000 IF EQUAL
.
          BRANCH    OPTION,OPTN9999
.
          BEEP                              * invalid option chosen
.
          GOTO      OPTN1000
.
.------ Exit option chosen ------
.
OPTN9000  MOVE      TRUE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                                 SCR0000                                *
.*                   Display the main option screen format                *
.**************************************************************************
SCR0000   DISPLAY   *P1:4,*EF,*V2LON,ZERO:
                    *P1:5,ONE:
                    *P2:4,*HOFF," = Exit":
                    *P2:5," = Generate Master Listing";
.
SCR9999   RETURN
+
.**************************************************************************
.*                            CRNG0000                                    *
.*               Input range of claim codes for printing                  *
.**************************************************************************
CRNG0000  MOVE      SP3,CLAIM1
          MOVE      SP3,CLAIM2
          MOVE      SP20,DESCCLM1
          MOVE      SP20,DESCCLM2
          MOVE      "Start ",START1
          MOVE      "Finish",FINISH1
          DISPLAY   *P1:10,*EF,"Claim Code From     : ",UNDLN3:
                    *P1:11,"Claim Code To       : ";
          KEYIN     *P23:10,*V2LON,CLAIM1;
          DISPLAY   *P23:10,*V2LON,CLAIM1;
.
          ENDSET    CLAIM1
          APPEND    SP3,CLAIM1
          RESET     CLAIM1
.
          MATCH     SP3,CLAIM1              * test for spaces
          GOTO      CRNG1000 IF EQUAL
.
          MATCH     QUEST,CLAIM1            * test for ?
          GOTO      CRNG0500 IF NOT EQUAL
.
.------ ? option on claim codes ------
.
CRNG0200  MOVE      CATCL,CODE
          CALL      PATCODDS                * display all claim codes
          BRANCH    OVRCD,CRNG9900
.
.------ keyin claim code after ? option ------
.
CRNG0250  MOVE      SP3,CLAIM1
          MOVE      SP20,DESCCLM1
          DISPLAY   *P1:24,*EL,"Claim Code From : ":
                    *P19:24,UNDLN3;
          KEYIN     *P19:24,*V2LON,CLAIM1;
          DISPLAY   *P19:24,*V2LON,CLAIM1;
.
          ENDSET    CLAIM1
          APPEND    SP3,CLAIM1
          RESET     CLAIM1
.
          MATCH     QUEST,CLAIM1            * test for ?
          GOTO      CRNG0200 IF EQUAL
.
          MATCH     SP3,CLAIM1              * test for spaces
          GOTO      CRNG0400 IF NOT EQUAL
.
          CALL      SCR0000                 * re-display screen format
.
          DISPLAY   *P1:7,*EL,"Please Select : ":
                    *P17:7,*V2LON,OPTION,*HOFF:
                    *P1:10,*EF,"Claim Code From     : ":
                    *P1:11,"Claim Code To       : ";
.
          GOTO      CRNG1000
.
.------ claim code has been entered ------
.
CRNG0400  PACK      KEY5,CATCL,CLAIM1
          MOVE      SP20,DESCCLM1
.
          CALL      RDCODE1                 * read patient codes file
          COMPARE   ZERO,OVRCD
          GOTO      CRNG0450 IF EQUAL
.
.------ claim code not on file after ? option keyin ------
.
          DISPLAY   *P1:24,*EL,*B,"Claim Code not on file - Hit <":
                           *V2LON,"ENTER",*HOFF,"> to Continue";
          KEYIN     ANS;
          DISPLAY   *P1:24,*EL;
.
          GOTO      CRNG0250
.
.------ claim code on file after ? option keyin ------
.
CRNG0450  CALL      SCR0000                 * re-display screen format
.
          DISPLAY   *P1:7,*EL,"Please Select : ":
                    *P17:7,*V2LON,OPTION,*HOFF:
                    *P1:10,*EF,"Claim Code From     : ":
                    *P1:11,"Claim Code To       : ";
.
          GOTO      CRNG0600
.
.------ claim code has been entered ------
.
CRNG0500  PACK      KEY5,CATCL,CLAIM1
          MOVE      SP20,DESCCLM1
.
          CALL      RDCODE1                 * read patient codes file 
          COMPARE   ZERO,OVRCD
          GOTO      CRNG0600 IF EQUAL
.
.------ claim code not on file ------
.
          DISPLAY   *P1:24,*EL,*B,"Claim Code not on file - Hit <":
                           *V2LON,"ENTER",*HOFF,"> to Continue";
          KEYIN     ANS;
          DISPLAY   *P1:24,*EL;
.
          GOTO      CRNG0000
.
.------ claim code on file ------
.
CRNG0600  PACK      START1,CLAIM1,SP6
          MOVE      TDESC,DESCCLM1
.
.------ display relevant details on main screen ------
.
CRNG1000  DISPLAY   *P23:10,*V2LON,START1:
                    *P35:10,*HOFF,DESCCLM1;
.
.------ keyin second claim code ------
.
CRNG2000  MOVE      SP3,CLAIM2
          MOVE      SP20,DESCCLM2
          DISPLAY   *P23:11,*EL,UNDLN3;
          KEYIN     *P23:11,*V2LON,CLAIM2;
          DISPLAY   *P23:11,*V2LON,CLAIM2;
.
          ENDSET    CLAIM2
          APPEND    SP3,CLAIM2
          RESET     CLAIM2
.
          MATCH     SP3,CLAIM2              * test for spaces
          GOTO      CRNG3000 IF EQUAL
.
          MATCH     QUEST,CLAIM2            * test for ?
          GOTO      CRNG2500 IF NOT EQUAL
.
.------ ? option on claim codes ------
.
CRNG2100  MOVE      CATCL,CODE
          CALL      PATCODDS
          BRANCH    OVRCD,CRNG9900
.
.------ keyin second claim code after ? option ------
.
CRNG2300  MOVE      SP20,DESCCLM2
          MOVE      SP3,CLAIM2
          DISPLAY   *P1:24,*EL,"Claim Code To : ":
                    *P17:24,UNDLN3;
          KEYIN     *P17:24,*V2LON,CLAIM2;
          DISPLAY   *P17:24,*V2LON,CLAIM2 ;
.
          ENDSET    CLAIM2
          APPEND    SP3,CLAIM2
          RESET     CLAIM2
.
          MATCH     QUEST,CLAIM2            * test for ?
          GOTO      CRNG2100 IF EQUAL
.
          MATCH     SP3,CLAIM2              * test for spaces
          GOTO      CRNG2400 IF NOT EQUAL
.
          CALL      SCR0000                 * re-display screen format
.
          DISPLAY   *P1:7,*EL,"Please Select : ":
                    *P17:7,*V2LON,OPTION,*HOFF:
                    *P1:10,*EF,"Claim Code From     : ":
                    *P1:11,"Claim Code To       : ":
                    *P23:10,*V2LON,START1:           
                    *P35:10,*HOFF,DESCCLM1;
.
          GOTO      CRNG3000
.
.------ second claim code has been entered after ? option ------
.
CRNG2400  PACK      KEY5,CATCL,CLAIM2
          MOVE      SP20,DESCCLM2
.
          CALL      RDCODE1                 * read patient codes file
          COMPARE   ZERO,OVRCD              * test if on file
          GOTO      CRNG2410 IF EQUAL
.
.------ claim code not on file after ? option keyin ------
.
          DISPLAY   *P1:24,*EL,*B,"Claim Code not on file - Hit <":
                           *V2LON,"ENTER",*HOFF,"> to Continue";
          KEYIN     ANS;
          DISPLAY   *P1:24,*EL;
.
          GOTO      CRNG2300
.
.------ claim code on file after ? option ------
.
CRNG2410  CALL      SCR0000                 * re-display screen format
.
          DISPLAY   *P1:7,*EL,"Please Select : ":
                    *P17:7,*V2LON,OPTION,*HOFF:
                    *P1:10,*EF,"Claim Code From     : ":
                    *P1:11,"Claim Code To       : ":
                    *P23:10,*V2LON,START1:  
                    *P35:10,*HOFF,DESCCLM1;
.
          GOTO      CRNG2800
.
.------ second department code has been entered ------
.
CRNG2500  PACK      KEY5,CATCL,CLAIM2
          MOVE      SP20,DESCCLM2
.
          CALL      RDCODE1                 * read patient codes file
          COMPARE   ZERO,OVRCD
          GOTO      CRNG2800 IF EQUAL
.
.------ claim code not on file ------
.
          DISPLAY   *P1:24,*EL,*B,"Claim Code not on file - Hit <":
                           *V2LON,"ENTER",*HOFF,"> to Continue";
          KEYIN     ANS;
          DISPLAY   *P1:24,*EL;
.
          GOTO      CRNG2000
.
.------ pack end code range variable ------
.
CRNG2800  PACK      FINISH1,CLAIM2,SP6
          MOVE      TDESC,DESCCLM2
.
.------ display relevant details ------
.
CRNG3000  DISPLAY   *P23:11,*V2LON,FINISH1:
                    *P35:11,*HOFF,DESCCLM2;
.
.------ pack up print variables with relevant details ------
.
CRNG4100  MATCH     SP3,CLAIM2
          GOTO      CRNG4200 IF NOT EQUAL
.
          MOVE      "ZZZ",CLAIM2
          MOVE      FALSE,EXIT
.
          GOTO      CRNG9999
.
.------ make sure claim codes entered are a valid range ------
.
CRNG4200  MATCH     CLAIM1,CLAIM2
          GOTO      CRNG9000 IF LESS
.
          MOVE      FALSE,EXIT
.
          GOTO      CRNG9999
.
.------ invalid claim code range entered ------
.
CRNG9000  DISPLAY   *P1:24,*EL,*B,"Invalid Claim code range - Hit <":
                           *V2LON,"ENTER",*HOFF,"> to Continue";
          KEYIN     ANS;
          DISPLAY   *P1:24,*EL;
.
          GOTO      CRNG0000
.
.------ No codes on file for a ? option ------
.
CRNG9900  MOVE      TRUE,EXIT
.
CRNG9999  RETURN
+
.**************************************************************************
.*                            ARNG0000                                    *
.*               Input range of admission types for printing              *
.**************************************************************************
ARNG0000  MOVE      SP3,ATYPE1
          MOVE      SP3,ATYPE2
          MOVE      SP20,DESCATY1
          MOVE      SP20,DESCATY2
          MOVE      "Start ",START2
          MOVE      "Finish",FINISH2
          DISPLAY   *P1:13,*EF,"Admission Type From : ",UNDLN3:
                    *P1:14,"Admission Type To   : ";
          KEYIN     *P23:13,*V2LON,ATYPE1;
          DISPLAY   *P23:13,*V2LON,ATYPE1;
.
          ENDSET    ATYPE1
          APPEND    SP3,ATYPE1
          RESET     ATYPE1
.
          MATCH     SP3,ATYPE1              * test for spaces
          GOTO      ARNG1000 IF EQUAL
.
          MATCH     QUEST,ATYPE1            * test for ?
          GOTO      ARNG0500 IF NOT EQUAL
.
.------ ? option on Admission Type ------
.
ARNG0200  MOVE      CATA,CODE
          CALL      PATCODDS                * display all Admission Types
          BRANCH    OVRCD,ARNG9900
.
.------ keyin Admission Types after ? option ------
.
ARNG0250  MOVE      SP3,ATYPE1
          MOVE      SP20,DESCATY1
          DISPLAY   *P1:24,*EL,"Admission Type From : ":
                    *P23:24,UNDLN3;
          KEYIN     *P23:24,*V2LON,ATYPE1;
          DISPLAY   *P23:24,*V2LON,ATYPE1;
.
          ENDSET    ATYPE1
          APPEND    SP3,ATYPE1
          RESET     ATYPE1
.
          MATCH     QUEST,ATYPE1            * test for ?
          GOTO      ARNG0200 IF EQUAL
.
          MATCH     SP3,ATYPE1              * test for spaces
          GOTO      ARNG0400 IF NOT EQUAL
.
          CALL      SCR0000                 * re-display screen format
.
          DISPLAY   *P1:7,*EL,"Please Select : ":
                    *P17:7,*V2LON,OPTION,*HOFF:
                    *P1:10,*EF,"Claim Code From     : ":
                    *P1:11,"Claim Code To       : ":
                    *P23:10,*V2LON,START1:     
                    *P35:10,*HOFF,DESCCLM1:
                    *P23:11,*V2LON,FINISH1:
                    *P35:11,*HOFF,DESCCLM2:
                    *P1:13,*EF,"Admission Type From : ":
                    *P1:14,"Admission Type To   : ";
.
          GOTO      ARNG1000
.
.------ Admission Type has been entered ------
.
ARNG0400  PACK      KEY5,CATA,ATYPE1
          MOVE      SP20,DESCATY1
.
          CALL      RDCODE1                 * read patient codes file
          COMPARE   ZERO,OVRCD
          GOTO      ARNG0450 IF EQUAL
.
.------ Admission Type not on file after ? option keyin ------
.
          DISPLAY   *P1:24,*EL,*B,"Admission Type not on file - Hit <":
                           *V2LON,"ENTER",*HOFF,"> to Continue";
          KEYIN     ANS;
          DISPLAY   *P1:24,*EL;
.
          GOTO      ARNG0250
.
.------ Admission Type on file after ? option keyin ------
.
ARNG0450  CALL      SCR0000                 * re-display screen format
.
          DISPLAY   *P1:7,*EL,"Please Select : ":
                    *P17:7,*V2LON,OPTION,*HOFF:
                    *P1:10,*EF,"Claim Code From     : ":
                    *P1:11,"Claim Code To       : ":
                    *P23:10,*V2LON,START1:     
                    *P35:10,*HOFF,DESCCLM1:
                    *P23:11,*V2LON,FINISH1:
                    *P35:11,*HOFF,DESCCLM2:
                    *P1:13,*EF,"Admission Type From : ":
                    *P1:14,"Admission Type To   : ";
.
          GOTO      ARNG0600
.
.------ Admission Type has been entered ------
.
ARNG0500  PACK      KEY5,CATA,ATYPE1
          MOVE      SP20,DESCATY1
.
          CALL      RDCODE1                 * read patient codes file 
          COMPARE   ZERO,OVRCD
          GOTO      ARNG0600 IF EQUAL
.
.------ Admission Type not on file ------
.
          DISPLAY   *P1:24,*EL,*B,"Admission Type not on file - Hit <":
                           *V2LON,"ENTER",*HOFF,"> to Continue";
          KEYIN     ANS;
          DISPLAY   *P1:24,*EL;
.
          GOTO      ARNG0000
.
.------ Admission Type on file ------
.
ARNG0600  PACK      START2,ATYPE1,SP6
          MOVE      TDESC,DESCATY1
.
.------ display relevant details on main screen ------
.
ARNG1000  DISPLAY   *P23:13,*V2LON,START2:
                    *P35:13,*HOFF,DESCATY1;
.
.------ keyin second Admission Type ------
.
ARNG2000  MOVE      SP3,ATYPE2
          MOVE      SP20,DESCATY2
          DISPLAY   *P23:14,*EL,UNDLN3;
          KEYIN     *P23:14,*V2LON,ATYPE2;
          DISPLAY   *P23:14,*V2LON,ATYPE2;
.
          ENDSET    ATYPE2
          APPEND    SP3,ATYPE2
          RESET     ATYPE2
.
          MATCH     SP3,ATYPE2              * test for spaces
          GOTO      ARNG3000 IF EQUAL
.
          MATCH     QUEST,ATYPE2            * test for ?
          GOTO      ARNG2500 IF NOT EQUAL
.
.------ ? option on Admission Type ------
.
ARNG2100  MOVE      CATA,CODE
          CALL      PATCODDS
          BRANCH    OVRCD,ARNG9900
.
.------ keyin second Admission Type after ? option ------
.
ARNG2300  MOVE      SP20,DESCATY2
          MOVE      SP3,ATYPE2
          DISPLAY   *P1:24,*EL,"Admission Type To : ":
                    *P21:24,UNDLN3;
          KEYIN     *P21:24,*V2LON,ATYPE2;
          DISPLAY   *P21:24,*V2LON,ATYPE2 ;
.
          ENDSET    ATYPE2
          APPEND    SP3,ATYPE2
          RESET     ATYPE2
.
          MATCH     QUEST,ATYPE2            * test for ?
          GOTO      ARNG2100 IF EQUAL
.
          MATCH     SP3,ATYPE2              * test for spaces
          GOTO      ARNG2400 IF NOT EQUAL
.
          CALL      SCR0000                 * re-display screen format
.
          DISPLAY   *P1:7,*EL,"Please Select : ":
                    *P17:7,*V2LON,OPTION,*HOFF:
                    *P1:10,*EF,"Claim Code From     : ":
                    *P1:11,"Claim Code To       : ":
                    *P23:10,*V2LON,START1:     
                    *P35:10,*HOFF,DESCCLM1:
                    *P23:11,*V2LON,FINISH1:
                    *P35:11,*HOFF,DESCCLM2:
                    *P1:13,*EF,"Admission Type From : ":
                    *P1:14,"Admission Type To   : ":
                    *P23:13,*V2LON,START2:  
                    *P35:13,*HOFF,DESCATY1;
.
          GOTO      ARNG3000
.
.------ second Admission Type has been entered after ? option ------
.
ARNG2400  PACK      KEY5,CATA,ATYPE2
          MOVE      SP20,DESCATY2
.
          CALL      RDCODE1                 * read patient codes file
          COMPARE   ZERO,OVRCD              * test if on file
          GOTO      ARNG2410 IF EQUAL
.
.------ Admission Type not on file after ? option keyin ------
.
          DISPLAY   *P1:24,*EL,*B,"Admission Type not on file - Hit <":
                           *V2LON,"ENTER",*HOFF,"> to Continue";
          KEYIN     ANS;
          DISPLAY   *P1:24,*EL;
.
          GOTO      ARNG2300
.
.------ Admission Type on file after ? option ------
.
ARNG2410  CALL      SCR0000                 * re-display screen format
.
          DISPLAY   *P1:7,*EL,"Please Select : ":
                    *P17:7,*V2LON,OPTION,*HOFF:
                    *P1:10,*EF,"Claim Code From     : ":
                    *P1:11,"Claim Code To       : ":
                    *P23:10,*V2LON,START1:     
                    *P35:10,*HOFF,DESCCLM1:
                    *P23:11,*V2LON,FINISH1:
                    *P35:11,*HOFF,DESCCLM2:
                    *P1:13,*EF,"Admission Type From : ":
                    *P1:14,"Admission Type To   : ":
                    *P23:13,*V2LON,START2:  
                    *P35:13,*HOFF,DESCATY1;
.
          GOTO      ARNG2800
.
.------ second admission Type has been entered ------
.
ARNG2500  PACK      KEY5,CATA,ATYPE2
          MOVE      SP20,DESCATY2
.
          CALL      RDCODE1                 * read patient codes file
.
          COMPARE   ZERO,OVRCD
          GOTO      ARNG2800 IF EQUAL
.
.------ Admission Type not on file ------
.
          DISPLAY   *P1:24,*EL,*B,"Admission Type not on file - Hit <":
                           *V2LON,"ENTER",*HOFF,"> to Continue";
          KEYIN     ANS;
          DISPLAY   *P1:24,*EL;
.
          GOTO      ARNG2000
.
.------ pack end code range variable ------
.
ARNG2800  PACK      FINISH2,ATYPE2,SP6
          MOVE      TDESC,DESCATY2
.
.------ display relevant details ------
.
ARNG3000  DISPLAY   *P23:14,*V2LON,FINISH2:
                    *P35:14,*HOFF,DESCATY2;
.
.------ pack up print variables with relevant details ------
.
ARNG4100  MATCH     SP3,ATYPE2
          GOTO      ARNG4200 IF NOT EQUAL
.
          MOVE      "ZZZ",ATYPE2
          MOVE      FALSE,EXIT
.
          GOTO      ARNG9999
.
.------ make sure Admission Types entered are a valid range ------
.
ARNG4200  MATCH     ATYPE1,ATYPE2
          GOTO      ARNG9000 IF LESS
.
          MOVE      FALSE,EXIT
.
          GOTO      ARNG9999
.
.------ invalid Admission Type range entered ------
.
ARNG9000  DISPLAY   *P1:24,*EL,*B,"Invalid Admission Type range - Hit <":
                           *V2LON,"ENTER",*HOFF,"> to Continue";
          KEYIN     ANS;
          DISPLAY   *P1:24,*EL;
.
          GOTO      ARNG0000
.
.------ No codes on file for a ? option ------
.
ARNG9900  MOVE      TRUE,EXIT
.
ARNG9999  RETURN
+
.**************************************************************************
.*                              CONT0000                                  *
.*                         check if OK to Continue                        *
.*         1 = Yes                                                        *
.*         2 = No                                                         *
.*         3 = Cancel                                                     *
.**************************************************************************
CONT0000  MOVE      FALSE,EXIT
.
          CALL      CONTQST                 * see if O.K. to continue
.
          MOVE      ANS,EXIT
.
CONT9999  RETURN
+
.**************************************************************************
.*                                 PRNT0000                               *
.*                       Print the master listing                         *
.**************************************************************************
PRNT0000  DISPLAY   *P1:24,*EL,*V2LON,*BLINKON,"Printing":
                           *HOFF," - Please Wait";
          MOVE      ONE,CNOUNDLN
          MOVE      ZERO,CPAGENO
          CLOCK     TIME,CTIMEIS
          MOVE      SP3,SAVCLAIM
          MOVE      SP3,PRNTCLAM
          MOVE      SP3,SAVATYPE
          MOVE      SP3,PRNTATYP
.
          CALL      HEAD0000                * print the page header
.
          PACK      KEY9,CLAIM1,ATYPE1,SP3
          CALL      RSPTACC1                * position on the account status
.                                             file
.------ read through the account status file ------
.
PRNT1000  CALL      RKPTACC1
          BRANCH    OVRCD,PRNT9000
.
          MATCH     PTASCLAM,CLAIM2         * see if claim code is in range
          GOTO      PRNT9000 IF LESS
.
          MATCH     ATYPE1,PTASATYP         * see if the admission type is
          GOTO      PRNT1000 IF LESS          in range
.
          MATCH     PTASATYP,ATYPE2         * see if the admission type is
          GOTO      PRNT1000 IF LESS          in range
.
          MATCH     SP3,SAVCLAIM            * see if we have the first record
          GOTO      PRNT2000 IF EQUAL
.
          COMPARE   "60",CLNO               * see if we need to paginate
          GOTO      PRNT1700 IF LESS
.
          CALL      HEAD0000                * print the page header
.
          GOTO      PRNT2000
.
.------ see if we have the same claim code ------
.
PRNT1700  MATCH     SAVCLAIM,PTASCLAM
          GOTO      PRNT1800 IF EQUAL
.
          PRINT     *N,UNDLN115;
          ADD       ONE,CLNO
.
          GOTO      PRNT2000
.
.------ see if we have the same admission type ------
.
PRNT1800  MATCH     SAVATYPE,PTASATYP
          GOTO      PRNT2100 IF EQUAL
.
          CALL      SET5000
.
          GOTO      PRNT2100
.
.------ set up the print variables ------
.
PRNT2000  CALL      SET0000    
.
.------ print the current records details ------
.
PRNT2100  CALL      REC0000                 * print a record
.
          MOVE      PTASCLAM,SAVCLAIM
          MOVE      PTASATYP,SAVATYPE
          MOVE      SP20,TEMPCDSC
          MOVE      SP20,TEMPADSC
          MOVE      SP3,PRNTCLAM
          MOVE      SP3,PRNTATYP
.
          GOTO      PRNT1000
.
.------ we have reached the end of the file ------
.
PRNT9000  PRINT     *N,UNDLN115:
                    *N,*N,"**** End of Report ****";
.
PRNT9999  RETURN
+
.**************************************************************************
.*                                  HEAD0000                              *
.*                          Print the new page header                     *
.**************************************************************************
HEAD0000  COMPARE   ZERO,CPAGENO            * see if we are on the first page
          GOTO      HEAD1000 IF EQUAL
.
          PRINT     *N,UNDLN115;
.
.------ print the page header ------
.
HEAD1000  CALL      HEAD132                 * print the page header
.
          PRINT     *N,*40,"Claim Code From     : ",START1,*70,DESCCLM1:
                    *N,*40,"Claim Code To       : ",FINISH1,*70,DESCCLM2:
                    *N,*N,*40,"Admission Type From : ",START2,*70,DESCATY1:
                    *N,*40,"Admission Type To   : ",FINISH2,*70,DESCATY2:
                    *N,*N,UNDLN115:
                    *N,"|",*29,"|",*57,"|",*81,"| Previous Year",*98,"| ":
                    "Current Year",*115,"|":
                    *N,"| Claim Code",*29,"|",*31,"Admission Type",*57,"|":
                    *59,"Highest day in Period",*81,"|":
                    *83,"Account Status",*98,"| Account Status",*115,"|":
                    *N,UNDLN115;
          MOVE      TEN3,CLNO
.
HEAD9999  RETURN
+
.**************************************************************************
.*                                  SET0000                               *
.*                      Routine to set up the values to print             *
.**************************************************************************
SET0000   MOVE      SP20,TDESC
          PACK      KEY5,CATCL,PTASCLAM
          CALL      RDCODE1                 * read the patient codes file
          MOVE      TDESC,TEMPCDSC
          MOVE      PTASCLAM,PRNTCLAM
.
SET5000   MOVE      SP20,TDESC
          PACK      KEY5,CATA,PTASATYP
          CALL      RDCODE1                 * read the patient codes file
          MOVE      TDESC,TEMPADSC
          MOVE      PTASATYP,PRNTATYP
.
SET9999   RETURN
+
.**************************************************************************
.*                                  REC0000                               *
.*                      Routine to print a record                         *
.**************************************************************************
REC0000   PRINT     *N,*1,"| ",PRNTCLAM,*8,TEMPCDSC,*29,"|",*31,PRNTATYP:
                       *36,TEMPADSC,*57,"|",*68,PTASHDAY,*81,"|",*88,PTASPRAC:
                       *98,"|",*105,PTASACCS,*115,"|";
          ADD       ONE,CLNO
.
REC9999   RETURN
+ 
          INC       STD001IO 
          INC       PATCODKY
.
          INC       PATACCIO/INC
          INC       PATCODIO/INC

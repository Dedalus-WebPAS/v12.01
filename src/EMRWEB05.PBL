.----------------------------------------------------------------------
. Program       : EMRWEB05 
.
. Function      : Emergency Table Maintenance 
.
. Modifications : 
.----------------------------------------------------------------------
. V11.04.01 01/08/2024   Nikitha B    TSK 0949431
.           Modiefied table heading from "IBA Descroption" to 
.           "webPAS description" at FLHED000.
. V11.03.04 09/11/2023   Sunny        TSK 0937587
.           Added ELOC1500/RESWRD00 - FHIR Location Resource for 
.           EmergencyLocation 
. V11.03.03 06/07/2023  Peter Vela    TSK 0927262
.           Added Location include  FHRLOCIN functionality
. V11.03.02 20.06.2023  DA Sarkies    TSK 0927738
.           Moved the counter for the emergency sites into the fhir check
. V11.03.01 09/01/2023  Bella Turco   TSK 0921957
.           Continued adding PTCNNRTD & NOMORE00 to maintenance lists
. V11.02.02 16/08/2022 Alina Bhari      TSK 0923131
.           Recompile for EMRICDPR - Emergency diagnosis maintenance keyword
.           search field by 70 character                                   
. V11.02.01 19/07/2022 Bella Turco      TSK 0921957
.           Added NOMORE00 - Display "No more records" at end of list
.           Added PTCNNRTD - Displays number of rows set by parameter value
. V11.01.01 20.09.2021  DA Sarkies      TSK 0905241
.           Moved the alert in VALCODE90 to the opener so that the alert
.           isn't hidden from the user
. V11.00.02 23/03/2020  Thanh T         TSK 0880961
.           Added VALCOD40,EMRD1400
. V11.00.01 28/02/2020  Thanh T         TSK 0880961
.           Changed SPTEDA10 to return "No" when EMICACTV=1, otherwise,
.           return "Yes"
. V10.14.03 07/03/2019  Don Nguyen      TSK 0821139
.           Added 'Emergency Telehealth Service' to Location Maintenance
.           and History output.
. V10.14.02 27/02/2019  Don Nguyen      TSK 0821139
.           Added Emergency Location option output for
.           'Emergency Telehealth Service' (ELOC0000).
. V10.14.01 12/02/2019  Ebon Clements   TSK 0862664
.           Added Methamphetamine Related to diagnosis maintenance -  EMRDIA00
. V10.13.02 31/10/2018  J.Tan           TSK 0864701
.           Added Ward Bed Audit
. V10.13.01 20/08/2018  Ebon Clements   TSK 0859742
.           Added payee provider number to site file
. V10.12.06 09/07/2018  Ebon Clements  TSK 0858400
.           Fixed diagnosis multiple keyword match - SPTEDC00
. V10.12.05 04/07/2018  Ania P         TSK 0858778
.           Added inactive Diagnosis check to SPTEDA20
. V10.12.04 19/06/2018  Ania P         TSK 0858778
.           Removed inactive Diagnosis codes from SPTEDA00
. V10.12.03 14/05/2018  J.Tan          TSK 0855395
.           Mod DCTAB020 to check for blank keyword(KEYWRD01)
. V10.12.02 10/04/2017  J.Tan          TSK 0855074
.           Mod Soundex key to check for blank keyword
. V10.12.01 06/02/2018  Ebon Clements  TSK 0848319
.           Added created/updated/inactivated dates to ED location maintenance
. V10.11.07 14/11/2017  Don Nguyen     TSK 0816814
.           Added code to manage SNOMED Coding System Mappings; 
.           VMTTAB00 & DELCSM00.
. V10.11.06 22/09/2017  Ebon Clements  TSK 0843369
.           Added HERO id to location maintenance
. V10.11.05 28.08.2017  B.G.Ackland    TSK 0835482 
.           FHIR Output Change to Patient Reference ID to strip spaces
.           FHIR Change Extension Naming
. V10.11.04 17.08.2017  B.G.Ackland    TSK 0835482 
.           FHIR Output Include physcialType in Location Resource for ClinicalAide
. V10.11.03 09/08/2017  Tracey Nguyen TSK 0299206
.           1) Recompiled for EMRDKIPR
.           2) Added PTCSXOFF to determine if Soundex should be used in Doctor 
.              searches - in DCTAB000
.           3) Added GENSNDVR(Variables) and GENSNDEX to generate Soundex key 
.           4) Added CDOC000,DOCARRAY,DOCAREND to check if a doctor has been
.              displayed in searches
. V10.11.02 22.07.2017  B.G.Ackland    TSK 0835482
.           FHIR Output for Emergency Site Locations
. V10.11.01 18/07/2017  Richa Phogat   TSK 0820753
.           Added EMSIT013/EMSTACTV
. V10.09.01 23/02/2017  Don Nguyen     TSK 0822998
.           Added VALEPD00 - Remote script to validate Procedure/Diagnosis codes
. V10.08.02 29/07/2016  Jill Parkinson TSK 0823542
.           Corrected output of location room type in ELOC0800
. V10.08.01 15.06.2016  Jill Parkinson TSK 0814124
.           Changed to use emic9cod instead of emicvemd      
. V10.07.01 25.01.2015  Ania P         CAR 809392
.           Fixed emergency location description in LOCROW00
.           Added LOCSIT40 & LOCSIT50 
. V10.05.01 20.02.2015  Ebon Clements  CAR 304779
.           Added next patient location type for configurable map
. V10.04.02 31.03.2014  B.G.Ackland  CAR 299363
.           Replace META Tag Redirect with Javascript in Common RDIREC00
.           Routine
. V10.04.01 04/03/2014  Ania P         CAR 275582
.           Added EMSIT011 & EMSIT012
.----------------------------------------------------------------------
. V10.03.05 16/07/2013  Jill Parkinson CAR 287923
.           Added include of PATCONFD
. V10.03.04 30/01/2013  Patrick Adair  CAR 280147
.           Replace apostrophe in Doctor's Name with %60 for javascript
.           and in KEYWORD for Next and Previous Buttons
. V10.03.03 25/11/2012  Ebon Clements  CAR 266929
.           Fixed error message of diagnosis code - VALCOD90
. V10.03.02 11/07/2012  Peter Vela     CAR 265655
.           Added EMLOSSIN - EMLOC007 Short Stay Unit Indicator
. V10.03.01 06/07/2012  Peter Vela     CAR 261649
.           Added EMR Template Mapping
. V10.02.01 0704/2011  Jill Parkinson  CAR 239574
.           Removed open of ibaprntf
. V9.12.01  23/02/2010  Peter Vela     CAR 216692
.           Only display a Diagnosis in the search results once
. V9.08.03  14/04/2007  Peter Vela     CAR 112999
.           Added validation for existing patients in emr loc @ EMRLOC000
. V9.08.02  31/01/2007  Peter Vela     CAR 127930
.           Recompiled for EMRSITFD
. V9.08.01  24/01/2007  Peter Vela     CAR 127932
.           Added Default Request and Default Reason Location
. V9.07.01  20/07/2006  Peter Vela     CAR 112999
.           Added active indicator maintenance for EMRLOCAF.EMLOACTV
. V9.05.01  09/03/2006  Ebon Clements  CAR 78533
.           Mods for PATCODTM - Hospital specific category codes
. V9.05.B01 30/01/2006  Ebon Clements  CAR 93186
.           Mods for REDIRECT length change. Recompiled for IBAWEBDF
. V9.04.02  10/08.2005 Ebon Clements   CAR 57816
.           Added EMMAN009 - Mandatory for transaction eg. discharge
. V9.04.01  26/08/2004 Jill Habasque   CAR 52930
.           Fixed output of EMSTHSNO  
. V9.03.04  31/10/2003 Ebon Clements   CAR 44623
.           Fixed size of EMMAN008 cgi
. V9.03.03  28/10/2003 Ebon Clements   CAR 44623
.           Changed mandatory fields to use category EZ for responsibility
. V9.03.02  25/08/2003 Ebon Clements   CAR 42813
.           Recompiled for EMRSITFD changes that were missing in V9 that
.           had been made in 5.09 - EMSTDLOC
. V9.03.01  12/05/2003  Lina Vo        CAR 38501
.           Added emergency doctor Maintenance (EMRDOCFD).
.----------------------------------------------------------------------
. V9.02.07  28/02/2003  Lina Vo       SRF 34675
.           Added extra parameter LINKTYPE for SPTEDA00 to build diagnosis table
.           to link by HDP code(VEMD) or Diagnosis code(EMR).   
.           Added extra validation in VALCOD00 for validation by HDP Code(VEMD)
. V9.02.06  19/12/2002  Jill Habasque SRF 34675
.           Changed to use the VEMD code when selecting a diagnosis
. V9.02.05  05/04/2002  Pat Dirito  
.           Edited out ICD10 Code from Emrgency Diagnosis Table SPTEDA00
.           Recompiled for EMRICDPR 
. V9.02.04  27/03/2002  Bronko Sosic
.           Table changes for EMRICDFD                  
. V9.02.03  07/03/2002  Bronko Sosic
.           Cleaned up some code                       
. V9.02.02  26/11/2001  Bronko Sosic
.           Removed EMRDOCFD                           
. V9.02.01  22/11/2001  Bronko Sosic
.           Changed update in main1700                 
. V9.00.01  02/08/2001  Ashwin - HPS
.           Added New Fields for Responsible Staff and 
.           Room Type
. V5.08.15  26/09/2000  Pat Dirito    
.           Got rid of option 10-VEMD not needed anymore.
. V5.08.15  16/08/2000  Caleb Sharman
.           Changed Health Fund variables to be 8 chars
. V5.08.B01 12/04/2000  Nicole Harrington
.           Ported to 5.08, fixed compile errors
.------------------------------------------------------
. Modifications : V5.07.12 11/04/2000  Nicole Harrington
.          Added VEMD Paper Based Report Variable - PBREPORT
. V5.07.11 27/03/2000  Bronko Sosic
.          Added startkey for GOTO button
. V5.07.10 10/03/2000  Nicole Harrington
.          Added VISS extract variables
. V5.07.09 18/01/2000  Nicole Harrington
.          Change VEMD0000 to write PASSCODE to config file
. V5.07.08 12/01/2000  Nicole Harrington
.          Recompiled for IBAWEBCD - template name in WEBTITLE
. V5.07.07 29/12/1999  Nicole Harrington SRF No: 636713
.          Fixed update of diagnosis
. V5.07.06 09/11/1999  Nicole Harrington
.          Added output of printer select list
. V5.07.05 01/11/1999  Nicole Harrington
.          Fixed emergency diagnosis search
. V5.07.04 28/10/1999  Nicole Harrington
.          Recompiled for WEBDATCD/DAYOFWEK
. V5.07.03 27/10/1999  Nicole Harrington
.          Added option 10 for VEMD Extract (WEBEXT01)
. V5.07.02 29/09/1999  Nicole Harrington
.          Changed to use HTML classes
. V5.07.01 02.09.1999 B.G.Ackland      
.          Change Doctor Lenght to 6
. V5.07.00 20/07/1999 Nicole Harrington
.          Ported from 6.06 - 5.07 and mods to mand. fields.
.----------------------------------------------------------------------
. V6.06.01 22.06.1999 Katie Nelson 
.          Re-Compiled for EMRVISTM
. V6.06.00 03.05.1999 Pat Dirito 
.          Created Program
.          19/05/1999 Katie Nelson
.          Changed Option 1 to Emergency Site Maintenance
.          Added Emergency Location Maintenance Option 5.
.
.----------------------------------------------------------------------
          INC       IBAWEBDF    
          INC       WEBDATDF
          INC       WEBRSHDF
.  
          INC       PATCONFD/INC
          INC       ALLTMPFD/INC
          INC       EMRDOCFD/INC
          INC       EMRDKIFD/INC
          iNC       EMRGRCFD/INC
          iNC       EMRICDFD/INC
          INC       EMRUSEFD/INC
          INC       EMRSITFD/INC
          INC       EMRLOCFD/INC
          INC       EMRLHIFD/INC
          INC       EMRPROFD/INC
          INC       EMRUTYFD/INC    
          INC       EMRWFMFD/INC
          INC       EMRVISFD/INC
          INC       GENSNDVR/INC    * Soundex key vars for GENSNDEX  *C-0299206
          INC       PATDO1FD/INC
          INC       EMRKEDFD/INC
          INC       MRTMOVFD/INC
          INC       MRTLOCFD/INC
          INC       PMSHCPFD/INC
          INC       PMSUHLFD/INC
          INC       PMSVMTFD/INC
          INC       PMSVMTTD/INC
          INC       PMSVSCFD/INC
          INC       PMSVSCTD/INC
          INC       PMSVSTFD/INC
          INC       PMSVSTTD/INC
          INC       PATHSPFD/INC
.
ACTVFLAG  DIM       3
ACTVONLY  DIM       1
.
ALTMP001  DIM       3     * Department Code (alldepaf)
ALTMP002  DIM       8     * Map from Server
ALTMP003  DIM       2     * Map from Report
ALTMP004  DIM       3     * Map from Template
ALTMP005  DIM       3     * Map to Template
ALTMP006  DIM       30    * Description
.
COPYDEPT  DIM       3     * Copy Department
.
DEPARTMN  DIM       3     * Department code
DEPTCODE  DIM       3     * Department code
DIAARRAY  DIM       9[200]
DIAAREND  FORM      3
DOCARRAY  DIM       10[200]  * Doctors displayed in search results *C-0299206
DOCAREND  FORM      3        * Number of doctors in DOCARRAY[]     *C-0299206
DOCTCODE  DIM       6
DIM3      DIM       3
DIM30     DIM       30
.
EMSIT001  DIM       3       * Emergency Site Code
EMSIT002  DIM       25      * Emergency Description
EMSIT003  FORM      6       * Emergency Hours per week
EMSIT004  DIM       3       * Emergency Department Type
EMSIT005  DIM       3       * Emergency Service Point Code
EMSIT006  DIM       10      * Emergency Service Provider Number
EMSIT007  DIM       3       * Emergency Hospital Number
EMSIT008  DIM       3       * Emergency Default location
EMSIT009  DIM       4       * Emergency Default Request Location
EMSIT010  DIM       4       * Emergency Default Request Reason
EMSIT011  DIM       6       * Emergency Discharge Fax Template
EMSIT012  DIM       6       * Emergency Discharge Fax Printer
EMSIT013  DIM       1       * Active Indicator
EMSIT014  DIM       8       * Payee Provider Number
.
EMUPT001  DIM       5       * Update Type
EMUPT002  DIM       35      * Description
EMUPT003  DIM       2       * Security Level
EMUPT004  DIM       1       * Security ID required
EMUPT005  DIM       1       * Password Required
.
EMDOC001  DIM       6      * Emergency Doctor Code
EMDOC002  DIM       25      * Emergency Dotor Display Name
EMDOC003  DIM       3       * Emergency Doctor Display Initials
EMDOC004  DIM       1       * Emergency Doctor Display Active
.
EMMAN001  DIM       5       * Field Number
EMMAN002  DIM       35      * IBA Description
EMMAN003  DIM       35      * Site Description
EMMAN004  DIM       35      * Screen Description
EMMAN005  DIM       1       * Mandatory 0=No 1=Yes
EMMAN006  DIM       1       * Injury Dependent Data 0=No 1=Yes
EMMAN007  FORM      3       * Extract Field Number 
EMMAN008  DIM       3       * Responsibility    (HPS01I)
EMMAN009  DIM       1       * Mandatory for transaction 
.
EMLOC001  DIM       3       * Location Code
EMLOC002  DIM       25      * Description
EMLOC003  DIM       3       * Site Code 
EMLOC004  FORM      4       * Max Number of Patients
EMLOC005  DIM       1       * Room Type         (HPS01I)
EMLOC006  DIM       1       * Active Indicator
EMLOC007  DIM       1       * Short Stay Unit Indicator
.
EMLHI001  DIM       8       * Effective Date
EMLHI002  DIM       8       * Effective Time
.
EMRPR001  DIM       9       * Emergency Procedure Code
EMRPR002  DIM       70      * Emergency Procedure Description
EMRPR003  DIM       6       * Emergency Procedure HDP Equivalent Code
.
EMRDI001  DIM       9       * Emergency Diagnosis Code
EMRDI002  DIM       70      * Emergency Diagnosis Description
EMRDI003  DIM       6       * Emergency Diagnosis ICD10 Code
EMRDI004  DIM       1       * Emergency Diagnosis Code Active Flag 
EMRDI005  DIM       9       * Emergency Diagnosis VEMD Code 
EMRDI006  DIM       70      * Emergency Diagnosis Key Word 
EMRDI007  DIM       1       * Methamphetamine Related
.
EMREG001  DIM       2       * Body Region Code
EMREG002  DIM       20      * Body Region Description
EMREG003  DIM       1       * Foreign Body Flag " "=N/A "F"=Foreign "N"=Not
.
CURRDATE  DIM       8
FILTACTV  DIM       1       * Active Status Filter
FHIRBUND  FORM      1
FHRLOCIN  FORM      1      * _include=Location:partOf
TABLECOD  DIM       1       * diagnosis table/procedure table
ENDNAME   DIM       70                                              * CAR 280147
HEROLOCI  DIM       20
JAVFNAME  DIM       70                                              * CAR 280147
STRTNAME  DIM       70                                              * CAR 280147
.
ACTVDESC  DIM       3
ROOMDESC  DIM       25
SHACTIVE  DIM       1    * Save the current status
SHSITECD  DIM       3    * save site code
SHRMTYPE  DIM       1    * save room type
SHNUMPAT  FORM      4    * number of patient
.
KEY3A     DIM       3
.
LINKTYPE  DIM       1       * link type 1 by HDP code (VEMD),
.                                       2 by Diagnosis code (EMR)
.
LNK2PRT   FORM      1       * Link to parent??
.
MANDA     FORM      5       * Sets input right justified  
.
NEXTPAGE  DIM       1
PAGEFULL  FORM      1       * Page full indicator                   *C-0299206
PASSYN    DIM       3       * Password "Yes" or "No"
STARTKEY  DIM       10      * Starting Key
.
SAVKEY9   DIM       9
SAVKEY10  DIM       10
SAVKEY16  DIM       16
SAVKEY24  DIM       24
SAVKEY25  DIM       25
SAVKEY30  DIM       30
SNDXDONE  FORM      1       * Soundex shown indicator               *C-0299206
.
UPDTTYPE  DIM       1       * Update Type
.
VALDCODE  DIM       9       * validation code
VALDNAME  DIM       70      * validation name
VALDTYPE  FORM      1       * validation type
Z1        INIT      "~"                                             *C-0299206
.
.---------------------------------------------------------------------------
PRGID     INIT      "EMRWEB05"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "Emergency Table Maintenance"
.---------------------------------------------------------------------------
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200,MAIN1300,MAIN1400,MAIN1500:
                             MAIN1600,MAIN1700,MAIN1800,MAIN1900,MAIN2000:
                             MAIN2100,MAIN2200
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "Invalid Option",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  CALL      EMRSIT00        * Emergency Site Maintenance  
          BRANCH    EXIT,MAIN1000
          MOVE      "Update Complete",WEBTITLE
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1200  CALL      EMRUPD00        * Emergency Update Type Maintenance  
          BRANCH    EXIT,MAIN1000
          MOVE      "Update Complete",WEBTITLE
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1300  CALL      EMRDOC00        * Emergency Doctor Maintenance
          BRANCH    EXIT,MAIN1000
          MOVE      "Update Complete",WEBTITLE
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1400  CALL      EMRMAN00        * Emergency Mandatory Fields Maintenance  
          BRANCH    EXIT,MAIN1000
          MOVE      "Update Complete",WEBTITLE
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1500  CALL      EMRLOC00        * Emergency Location Maintenance  
          BRANCH    EXIT,MAIN1000
          MOVE      "Update Complete",WEBTITLE
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1600  CALL      EMRPRC00        * Emergency Procedure Table Maintenance  
          BRANCH    EXIT,MAIN1000
          MOVE      "Update Complete",WEBTITLE
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1700  CALL      EMRDIA00        * Emergency Diagnosis Table Maintenance  
          BRANCH    EXIT,MAIN1000
          MOVE      "Update Complete",WEBTITLE
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1800  CALL      VALCOD00                * Validate EMR Proc & Diag codes
          GOTO      MAIN1000
.
MAIN1900  CALL      EMRREG00                * Body Region Maintenance
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN2000
          GOTO      MAIN1000
.
MAIN2100  CALL      EMRTEM00                * EMR Template Mapping
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN2200  CALL      VALEPD00    * Validate EMR Proc & Diag codes (Remote Script)
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
.------------------------------------------------------------
. Output Next Page Based on CGI Parameter nextpage
.------------------------------------------------------------
NXTPAG00  MOVE      ZERO,F1
          MOVE      NEXTPAGE,F1
          BRANCH    F1,NXTPAG10,NXTPAG20,NXTPAG30,NXTPAG40
.
          CALL      WINCLS00
          GOTO      NXTPAG99
.
NXTPAG10  CALL      RDIREC00
          GOTO      NXTPAG99
.
NXTPAG20  CALL      GOBACK00
          GOTO      NXTPAG99
.
NXTPAG30  CALL      GOBACK00
          GOTO      NXTPAG99
.
NXTPAG40  CALL      GOBACK00
          GOTO      NXTPAG99
.
NXTPAG99  RETURN
.-------------------------------------------------------------------------------
.  Goes back x number of pages
.-------------------------------------------------------------------------------
GOBACK00  CALL      OPENHTML
          BRANCH    EXIT,GOBACK99
          MOVE      NEXTPAGE,F1
          SUB       TWO,F1
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "    alert(#"",WEBTITLE,"#");":
.                            "    self.close();":
                             "    window.history.go(-",F1,");":
                             "}":
                             "</script>"
          CALL      CLOSHTML
GOBACK99  RETURN
.------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------
INIT0000  OPEN      ALLTMPA1,"alltmpaf"
          OPEN      EMRICDA1,"emricdaf"
          OPEN      EMRDOCA1,"emrdocaf"
          OPEN      EMRDKIA1,"emrdkiaf"
          OPEN      EMRDKIA2,"emrdkiaf"
          OPEN      EMRGRCA1,"emrgrcaf"
          OPEN      EMRICDA3,"emricdaf"
          OPEN      EMRICDA4,"emricdaf"
          OPEN      EMRSITA1,"emrsitaf"
          OPEN      EMRLOCA1,"emrlocaf"
          OPEN      EMRLHIA1,"emrlhiaf"
..          OPEN      EMRUSEA1,"emruseaf"  
          OPEN      EMRPROA1,"emrproaf"
          OPEN      EMRUTYA1,"emrutyaf"
          OPEN      EMRWFMA1,"emrwfmaf"
          OPEN      EMRVISA7,"emrvisaf"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
          OPEN      PATDO1A1,"patdo1af"
          OPEN      EMRKEDA1,"emrkedaf"
          OPEN      EMRKEDA2,"emrkedaf"
          OPEN      WEBRSHA1,"webrshaf"
          OPEN      PATHSPA1,"pathspaf"
          OPEN      PMSUHLA1,"pmsuhlaf"
.
          OPEN      IBAPDFA1,"ibapdfaf"
          OPEN      IHAPASS1,"ihapassf"
          OPEN      IBACODE1,"ibacodef"
          OPEN      IBATMHA1,"ibatmhaf"
          OPEN      IBASPOL1,"ibaspolf"
          OPEN      IBAPRTA1,"ibaprtaf"
.
          OPEN      MRTMOVA1,"mrtmovaf"
          OPEN      MRTLOCA1,"mrtlocaf"
          OPEN      MRTLOCA3,"mrtlocaf"
.
          OPEN      PMSHCPA1,"pmshcpaf" 
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,HUND19;*248,PTCSXOFF                   *C-0299206
          READ      CONTROLF,EIGHTY;*250,PTCNHDPS     * state parameter
          READ      CONTROLF,HUND17;*216,PTCNDSID
          READ      CONTROLF,HUND25;*238,PTCNNRTD
.
          IF        IBCNMHOS=1
            OPEN      MLTCODA1,"mltcodaf"
            OPEN      MLTCODA2,"mltcodaf"
          ENDIF
.
          MOVE      ZERO,F2
          MOVE      PTCNNRTD,F2
          IF        F2 = 0
            MOVE      "10",PTCNNRTD
          ENDIF
.
          OPEN      PMSVMTA1,"pmsvmtaf"
          OPEN      PMSVSTA1,"pmsvstaf"
.
INIT9999  RETURN
+
.------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          MOVE      SP70,TEMPLATE
          MOVE      SP70,STARTKEY
          MOVE      SP70,KEYWORDS
          MOVE      SP70,VALDCODE
          MOVE      SP70,VALDNAME 
          MOVE      ZERO,VALDTYPE 
          MOVE      SP70,EMSIT001  
          MOVE      SP70,EMSIT002 
          MOVE      SP70,EMSIT003 
          MOVE      SP70,EMSIT004 
          MOVE      SP70,EMSIT005 
          MOVE      SP70,EMSIT006 
          MOVE      SP70,EMSIT007 
          MOVE      SP70,EMSIT008 
          MOVE      SP70,EMSIT009
          MOVE      SP70,EMSIT010
          MOVE      SP70,EMSIT011
          MOVE      SP70,EMSIT012
          MOVE      SP70,EMSIT013
          MOVE      SP70,EMSIT014
          MOVE      SP70,EMUPT001 
          MOVE      SP70,EMUPT002 
          MOVE      SP70,EMUPT003 
          MOVE      SP70,EMUPT004 
          MOVE      SP70,EMUPT005 
          MOVE      SP70,EMDOC001 
          MOVE      SP70,EMDOC002 
          MOVE      SP70,EMDOC003  
          MOVE      SP70,EMDOC004  
          MOVE      SP70,EMMAN001  
          MOVE      SP70,EMMAN002 
          MOVE      SP70,EMMAN003 
          MOVE      SP70,EMMAN004 
          MOVE      SP70,EMMAN005 
          MOVE      SP70,EMMAN006 
          MOVE      ZERO,EMMAN007 
          MOVE      SP70,EMMAN008                      * HPS01I
          MOVE      ZERO,EMMAN009
          MOVE      SP70,EMLOC001  
          MOVE      SP70,EMLOC002 
          MOVE      SP70,EMLOC003 
          MOVE      ZERO,EMLOC004 
          MOVE      SP70,EMLOC005                      * HPS01I
          MOVE      SP70,EMLOC006 
          MOVE      SP70,EMLOC007
          MOVE      SP70,EMLHI001
          MOVE      SP70,EMLHI002
.
          MOVE      SP70,EMRPR001 
          MOVE      SP70,EMRPR002 
          MOVE      SP70,EMRPR003 
          MOVE      SP70,EMRDI001 
          MOVE      SP70,EMRDI002 
          MOVE      SP70,EMRDI003 
          MOVE      SP70,EMRDI004 
          MOVE      SP70,EMRDI005
          MOVE      SP70,EMRDI006
          MOVE      SP70,EMRDI007
          MOVE      SP70,NEXTPAGE  
          PACK      REDIRECT,SP70,SP70,SP70,SP70
          MOVE      SP70,UPDTTYPE
          MOVE      ZERO,NORECORD
          MOVE      ZERO,STARTNUM
          MOVE      SP70,EMREG001
          MOVE      SP70,EMREG002
          MOVE      SP70,EMREG003
          MOVE      SP70,DEPTCODE
          MOVE      Z70,TABLECOD
          MOVE      SP70,ALTMP001
          MOVE      SP70,ALTMP002
          MOVE      SP70,ALTMP003
          MOVE      SP70,ALTMP004
          MOVE      SP70,ALTMP005
          MOVE      SP70,ALTMP006
          MOVE      SP70,COPYDEPT
          MOVE      SP70,ACTVONLY
          MOVE      Z70,HEROLOCI
          MOVE      SP70,FILTVSID
          MOVE      SP70,FILTVSCD
          MOVE      SP70,FILTVSPT
.
.         CALL      CPVSC000
.         CALL      CPVST000
.
          MOVE      ZERO,FHRLOCIN
          MOVE      ZERO,FHIRBUND
.
          RETURN
.------------------------------------------------------------
. Set Parameters
.------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "keywords=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,KEYWORDS
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "norecord=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NORECORD
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startkey=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTKEY
            PACK      STARTKEY,STARTKEY,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startnum=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTNUM
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "valdcode=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,VALDCODE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "valdname=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,VALDNAME
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "valdtype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,VALDTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updttype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDTTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nextpage=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NEXTPAGE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "redirect=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REDIRECT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "emsit",QRYNAME
          IF        @EQUAL
            CALL      SETEM000
            COMPARE   ZERO,EXIT
            GOTO      SETPAR99 IF EQUAL
          ENDIF
.
          MATCH     "emupt",QRYNAME
          IF        @EQUAL
            CALL      SETUP000
            COMPARE   ZERO,EXIT
            GOTO      SETPAR99 IF EQUAL
          ENDIF
.
          MATCH     "emdoc",QRYNAME
          IF        @EQUAL
            CALL      STDOC000
            COMPARE   ZERO,EXIT
            GOTO      SETPAR99 IF EQUAL
          ENDIF
.
          MATCH     "emman",QRYNAME
          IF        @EQUAL
            CALL      STMAN000
            COMPARE   ZERO,EXIT
            GOTO      SETPAR99 IF EQUAL
          ENDIF
.
          MATCH     "emreg",QRYNAME
          IF        @EQUAL
            CALL      STREG000
            COMPARE   ZERO,EXIT
            GOTO      SETPAR99 IF EQUAL
          ENDIF
.
          MATCH     "emloc",QRYNAME
          IF        @EQUAL
            CALL      STLOC000
            COMPARE   ZERO,EXIT
            GOTO      SETPAR99 IF EQUAL
          ENDIF
.
          MATCH     "emrpr",QRYNAME
          IF        @EQUAL
            CALL      STPRC000
            COMPARE   ZERO,EXIT
            GOTO      SETPAR99 IF EQUAL
          ENDIF
.
          MATCH     "tablecod=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TABLECOD
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "emrdi",QRYNAME
          IF        @EQUAL
            CALL      STDIA000
            COMPARE   ZERO,EXIT
            GOTO      SETPAR99 IF EQUAL
          ENDIF
.
.         MATCH     "ptvsc",QRYNAME
.         IF        @EQUAL
.           CALL      STVSC000
.           COMPARE   ZERO,EXIT
.           GOTO      SETPAR99 IF EQUAL
.         ENDIF
.
.         MATCH     "ptvst",QRYNAME
.         IF        @EQUAL
.           CALL      STVST000
.           COMPARE   ZERO,EXIT             
.           GOTO      SETPAR99 IF EQUAL
.         ENDIF
.
          MATCH     "deptcode=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,DEPTCODE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "altmp",QRYNAME
          IF        @EQUAL
            CALL      STTEM000
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "copydept=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,COPYDEPT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "actvonly=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ACTVONLY
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "heroloci=",QRYNAME
          IF        @EQUAL
            PACK      HEROLOCI,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "emlhi001=",QRYNAME
          IF        @EQUAL
            MATCH     SP70,QRYDATA
            GOTO      SETPAR99 IF EQUAL
            GOTO      SETPAR99 IF EOS
.
            UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
            CALL      SETMTH00            * Set CMON from MTHNAM3
            PACK      EMLHI001,CCENT,CYEAR,CMON,CDAY
            REP       " 0",EMLHI001
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "emlhi002=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,EMLHI002
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "fhrlocin=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FHRLOCIN
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "fhirbund=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FHIRBUND
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
.-------------------------------------------------------------------------------
.   Set Parameters for Emergency User Security Table 
.-------------------------------------------------------------------------------
SETEM000  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,SETEM001,SETEM002,SETEM003,SETEM004,SETEM005:
                       SETEM006,SETEM007,SETEM008,SETEM009,SETEM010:
                       SETEM011,SETEM012,SETEM013,SETEM014
.
          MOVE      ONE,EXIT
          GOTO      SETEM999
.
SETEM001  MOVE      QRYDATA,EMSIT001
          GOTO      SETEM999
SETEM002  MOVE      QRYDATA,EMSIT002
          GOTO      SETEM999
SETEM003  MOVE      QRYDATA,EMSIT003
          GOTO      SETEM999
SETEM004  MOVE      QRYDATA,EMSIT004
          GOTO      SETEM999
SETEM005  MOVE      QRYDATA,EMSIT005
          GOTO      SETEM999
SETEM006  MOVE      QRYDATA,EMSIT006
          GOTO      SETEM999
SETEM007  MOVE      QRYDATA,EMSIT007
          GOTO      SETEM999
SETEM008  MOVE      QRYDATA,EMSIT008
          GOTO      SETEM999
SETEM009  MOVE      QRYDATA,EMSIT009
          GOTO      SETEM999
SETEM010  MOVE      QRYDATA,EMSIT010
          GOTO      SETEM999
SETEM011  MOVE      QRYDATA,EMSIT011
          GOTO      SETEM999
SETEM012  MOVE      QRYDATA,EMSIT012
          GOTO      SETEM999
SETEM013  MOVE      QRYDATA,EMSIT013
          GOTO      SETEM999
SETEM014  MOVE      QRYDATA,EMSIT014
          GOTO      SETEM999
.
SETEM999  RETURN
.-------------------------------------------------------------------------------
.   Set Parameters for Emergency Update Type Table 
.-------------------------------------------------------------------------------
SETUP000  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,SETUP001,SETUP002,SETUP003,SETUP004,SETUP005
.
          MOVE      ONE,EXIT
          GOTO      SETUP999
.
SETUP001  MOVE      QRYDATA,EMUPT001
          GOTO      SETUP900
SETUP002  MOVE      QRYDATA,EMUPT002
          GOTO      SETUP900
SETUP003  MOVE      QRYDATA,EMUPT003
          GOTO      SETUP900
SETUP004  MOVE      QRYDATA,EMUPT004
          GOTO      SETUP900
SETUP005  MOVE      QRYDATA,EMUPT005
          GOTO      SETUP900
.
SETUP900  GOTO      SETUP999
.
SETUP999  RETURN
.-------------------------------------------------------------------------------
.   Set Parameters for Emergency Doctor Codes Table 
.-------------------------------------------------------------------------------
STDOC000  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,STDOC001,STDOC002,STDOC003,STDOC004
.
          MOVE      ONE,EXIT
          GOTO      STDOC999
.
STDOC001  MOVE      QRYDATA,EMDOC001
          GOTO      STDOC900
STDOC002  MOVE      QRYDATA,EMDOC002
          GOTO      STDOC900
STDOC003  MOVE      QRYDATA,EMDOC003
          GOTO      STDOC900
STDOC004  MOVE      QRYDATA,EMDOC004
          GOTO      STDOC900
.
STDOC900  GOTO      STDOC999
.
STDOC999  RETURN
.-------------------------------------------------------------------------------
.   Set Parameters for Emergency Doctor Codes Table 
.-------------------------------------------------------------------------------
STMAN000  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,STMAN001,STMAN002,STMAN003,STMAN004,STMAN005:
                       STMAN006,STMAN007,STMAN008,STMAN009           
.
          MOVE      ONE,EXIT
          GOTO      STMAN999
.
STMAN001  RESET     QRYDATA
          MATCH     SP70,QRYDATA   * Checks Qrydata
          IF        !@EQUAL 
            MOVE      QRYDATA,EMMAN001
            MOVE      EMMAN001,MANDA    * Make it a numeric
            MOVE      MANDA,EMMAN001
          ELSE 
            MOVE      SP70,EMMAN001 
          ENDIF
          GOTO      STMAN900
STMAN002  MOVE      QRYDATA,EMMAN002
          GOTO      STMAN900
STMAN003  MOVE      QRYDATA,EMMAN003
          GOTO      STMAN900
STMAN004  MOVE      QRYDATA,EMMAN004
          GOTO      STMAN900
STMAN005  MOVE      QRYDATA,EMMAN005
          GOTO      STMAN900
STMAN006  MOVE      QRYDATA,EMMAN006
          GOTO      STMAN900
STMAN007  MOVE      QRYDATA,EMMAN007
          GOTO      STMAN900
STMAN008  MOVE      QRYDATA,EMMAN008           * HPS01I
          GOTO      STMAN900                   * HPS01I
STMAN009  MOVE      QRYDATA,EMMAN009 
          GOTO      STMAN900        
.
STMAN900  GOTO      STMAN999
.
STMAN999  RETURN
.-------------------------------------------------------------------------------
.   Set Parameters for Emergency Location Table 
.-------------------------------------------------------------------------------
STLOC000  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,STLOC001,STLOC002,STLOC003,STLOC004,STLOC005,STLOC006:
                       STLOC007
.
          MOVE      ONE,EXIT
          GOTO      STLOC999
.
STLOC001  MOVE      QRYDATA,EMLOC001
          GOTO      STLOC999
STLOC002  MOVE      QRYDATA,EMLOC002
          GOTO      STLOC999
STLOC003  MOVE      QRYDATA,EMLOC003
          GOTO      STLOC999
STLOC004  MOVE      QRYDATA,EMLOC004
          GOTO      STLOC999
STLOC005  MOVE      QRYDATA,EMLOC005                    * HPS01I
          GOTO      STLOC999                            * HPS01I
STLOC006  MOVE      QRYDATA,EMLOC006
          GOTO      STLOC999
STLOC007  MOVE      QRYDATA,EMLOC007
          GOTO      STLOC999
.
STLOC999  RETURN
.-------------------------------------------------------------------------------
.   Set Parameters for Emergency Procedure Table 
.-------------------------------------------------------------------------------
STPRC000  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,STPRC001,STPRC002,STPRC003
.
          MOVE      ONE,EXIT
          GOTO      STPRC999
.
STPRC001  MOVE      QRYDATA,EMRPR001
          GOTO      STPRC999
.
STPRC002  MOVE      QRYDATA,EMRPR002
          GOTO      STPRC999
.
STPRC003  MOVE      QRYDATA,EMRPR003
          GOTO      STPRC999
.
STPRC999  RETURN
.-------------------------------------------------------------------------------
.   Set Parameters for Emergency Diagnosis Table 
.-------------------------------------------------------------------------------
STDIA000  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,STDIA001,STDIA002,STDIA003,STDIA004,STDIA005,STDIA006:
                       STDIA007
.
          MOVE      ONE,EXIT
          GOTO      STDIA999
.
STDIA001  MOVE      QRYDATA,EMRDI001
          GOTO      STDIA999
.
STDIA002  MOVE      QRYDATA,EMRDI002
          GOTO      STDIA999
.
STDIA003  MOVE      QRYDATA,EMRDI003
          GOTO      STDIA999
.
STDIA004  MOVE      QRYDATA,EMRDI004
          GOTO      STDIA999
.
STDIA005  MOVE      QRYDATA,EMRDI005
          GOTO      STDIA999
.
STDIA006  MOVE      QRYDATA,EMRDI006
          GOTO      STDIA999
.
STDIA007  MOVE      QRYDATA,EMRDI007
          GOTO      STDIA999
.
STDIA999  RETURN
.------------------------------------------------------------
. Emergency Security ID Maintenance  
.------------------------------------------------------------
EMRSIT00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      EMRSIT40 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      EMRSIT10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      EMRSIT20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      EMRSIT30 IF EQUAL
.
          GOTO      EMRSIT93
.
EMRSIT10  RESET     EMSIT001 
          PACK      KEY3,EMSIT001,SP70
          CALL      RDEMSIT1
          COMPARE   ZERO,OVRCD
          GOTO      EMRSIT92 IF EQUAL
.
          CALL      CHKSIT00      * Checks mandatory fields
          BRANCH    EXIT,EMRSIT99 
.
          CALL      SITSAV00      * Moves input variables to file variables
          PACK      KEY3,EMSIT001,SP70
          CALL      WREMSIT1      * Writes away the data
          MOVE      ZERO,EXIT
          GOTO      EMRSIT99
.
EMRSIT20  PACK      KEY3,EMSIT001,SP70
          CALL      RDEMSIT1
          BRANCH    OVRCD,EMRSIT91
.
          CALL      CHKSIT00      * Checks mandatory fields
          BRANCH    EXIT,EMRSIT99 
.
          CALL      RLEMSIT1       * Locks the record
          CALL      SITSAV00       * Moves input variables to file variables
          CALL      UPEMSIT1       * Writes away the data
          CALL      UUEMSIT1       * Unlocks the record
          MOVE      ZERO,EXIT
          GOTO      EMRSIT99
.
EMRSIT30  PACK      KEY3,EMSIT001,SP70
          CALL      RDEMSIT1
          BRANCH    OVRCD,EMRSIT91
          CALL      DEEMSIT1       * deletes away the data
          MOVE      ZERO,EXIT
          GOTO      EMRSIT99
.
EMRSIT40  PACK      KEY3,EMSIT001,SP70 
          CALL      RDEMSIT1
          IF        OVRCD=1
            CALL      CLRSIT00     * Clear all the file variables
          ENDIF
.         
          PACK      WEBTITLE,SP70,SP70
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,EMRSIT99
.
          CALL      WEBBOD00   * Process Web Body
.
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      EMRSIT99
.
EMRSIT91  MOVE      "Emergency Site record does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EMRSIT99
.
EMRSIT92  MOVE      "Emergency Site record exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EMRSIT99
.
EMRSIT93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EMRSIT99
.
EMRSIT99  RETURN
.-----------------------------------------------------------
. Initialize's file data
.-----------------------------------------------------------
CLRSIT00  MOVE      SP70,EMSTSCOD
          MOVE      SP70,EMSTDESC
          MOVE      SP70,EMSTHRPW
          MOVE      SP70,EMSTDPTY
          MOVE      SP70,EMSTSPCO
          MOVE      SP70,EMSTSPNO
          MOVE      SP70,EMSTHSNO
          MOVE      SP70,EMSTDRLC
          MOVE      SP70,EMSTDRES
          MOVE      SP70,EMSTEDST
          MOVE      SP70,EMSTEDFP
          MOVE      ZERO,EMSTACTV
          MOVE      SP70,EMSTSPAR
.
CLRSIT99  RETURN
.------------------------------------------------------------
. Moves cgi variables into file variables
.------------------------------------------------------------
SITSAV00  MOVE      EMSIT001,EMSTSCOD 
          MOVE      EMSIT002,EMSTDESC
          MOVE      EMSIT003,EMSTHRPW
          MOVE      EMSIT004,EMSTDPTY
          MOVE      EMSIT005,EMSTSPCO
          MOVE      EMSIT006,EMSTSPNO
          MOVE      EMSIT007,EMSTHSNO
          MOVE      EMSIT008,EMSTDLOC
          MOVE      EMSIT009,EMSTDRLC
          MOVE      EMSIT010,EMSTDRES
          MOVE      EMSIT011,EMSTEDST
          MOVE      EMSIT012,EMSTEDFP
          MOVE      EMSIT013,EMSTACTV
          MOVE      EMSIT014,EMSTPPRV
.
SITSAV99  RETURN
.------------------------------------------------------------
. * Checks mandatory fields
.------------------------------------------------------------
CHKSIT00  PACK      EMSIT001,EMSIT001,SP70
          MATCH     SP70,EMSIT001 
          GOTO      CHKSIT90 IF EQUAL
.
          PACK      EMSIT002,EMSIT002,SP70
          MATCH     SP70,EMSIT002 
          GOTO      CHKSIT91 IF EQUAL
.
          GOTO      CHKSIT99
.
CHKSIT90  MOVE      "Emergency Site Code is mandatory!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKSIT99
.
CHKSIT91  MOVE      "Emergency Site Description is mandatory!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKSIT99
.
CHKSIT99  RETURN
.------------------------------------------------------------
. Emergency Update Type Maintenance   
.------------------------------------------------------------
EMRUPD00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      EMRUPD40 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      EMRUPD10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      EMRUPD20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      EMRUPD30 IF EQUAL
.
          GOTO      EMRUPD93
.
EMRUPD10  RESET     EMUPT001 
          PACK      KEY5,EMUPT001,SP70
          CALL      RDEMUTY1
          COMPARE   ZERO,OVRCD
          GOTO      EMRUPD92 IF EQUAL
          CALL      CHKUPD00      * Checks mandatory fields
          BRANCH    EXIT,EMRUPD99 
          CALL      UPDSAV00      * Moves input variables to file variables
          PACK      KEY5,EMUPT001,SP70
          CALL      WREMUTY1      * Writes away the data
          MOVE      ZERO,EXIT
          GOTO      EMRUPD99
.
EMRUPD20  PACK      KEY5,EMUPT001,SP70
          CALL      RDEMUTY1
          BRANCH    OVRCD,EMRUPD91
          CALL      CHKUPD00      * Checks mandatory fields
          BRANCH    EXIT,EMRUPD99 
          CALL      RLEMUTY1      * Locks the record
          CALL      UPDSAV00      * Moves input variables to file variables
          CALL      UPEMUTY1      * Writes away the data
          CALL      UUEMUTY1      * Unlocks the record
          MOVE      ZERO,EXIT
          GOTO      EMRUPD99
.
EMRUPD30  PACK      KEY5,EMUPT001,SP70
          CALL      RDEMUTY1
          BRANCH    OVRCD,EMRUPD91
          CALL      DEEMUTY1       * Writes away the data
          MOVE      ZERO,EXIT
          GOTO      EMRUPD99
.
EMRUPD40  PACK      KEY5,EMUPT001,SP70 
          CALL      RDEMUTY1
          IF        OVRCD=1
            CALL      CLRUPD00     * Clear all the file variables
          ENDIF
.
          PACK      WEBTITLE,SP70,SP70
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,EMRUPD99
.
          CALL      WEBBOD00   * Process Web Body
.
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      EMRUPD99
.
EMRUPD91  MOVE      "Emergency Update Type record does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EMRUPD99
.
EMRUPD92  MOVE      "Emergency Update Type record exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EMRUPD99
.
EMRUPD93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EMRUPD99
.
EMRUPD99  RETURN
.-----------------------------------------------------------
. Initialize's file data
.-----------------------------------------------------------
CLRUPD00  MOVE      SP70,EMUTUTY
          MOVE      SP70,EMUTDES
          MOVE      SP70,EMUTSLV
          MOVE      SP70,EMUTSID
          MOVE      SP70,EMUTPWD
          MOVE      SP70,EMUTSPA
.
CLRUPD99  RETURN
.------------------------------------------------------------
. Moves cgi variables into file variables
.------------------------------------------------------------
UPDSAV00  MOVE      EMUPT001,KEY5 
          SQUEEZE   KEY5
          MOVE      KEY5,EMUTUTY
          MOVE      EMUPT002,EMUTDES
          MOVE      EMUPT003,EMUTSLV
.
          RESET     EMUPT004
          MATCH     SP70,EMUPT004
          IF        @EQUAL
            MOVE      "0",EMUTSID
          ELSE
            MOVE      EMUPT004,EMUTSID
          ENDIF
.
          RESET     EMUPT005
          MATCH     SP70,EMUPT005
          IF        @EQUAL
            MOVE      "0",EMUTPWD
          ELSE
            MOVE      EMUPT005,EMUTPWD
          ENDIF

          MOVE      SP70,EMUTSPA
.
UPDSAV99  RETURN
.------------------------------------------------------------
. * Checks mandatory fields
.------------------------------------------------------------
CHKUPD00  RESET     EMUPT001
          MATCH     SP70,EMUPT001 * Update Type not set report an error
          GOTO      CHKUPD90 IF EQUAL
.
          RESET     EMUPT002
          MATCH     SP70,EMUPT002
          GOTO      CHKUPD91 IF EQUAL
.
          GOTO      CHKUPD99
.
CHKUPD90  MOVE      "Emergency Update Code is mandatory!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKUPD99
.
CHKUPD91  MOVE      "Emergency Update Type is mandatory!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKUPD99
.
CHKUPD99  RETURN
.------------------------------------------------------------
. Emergency Doctor Maintenance   
.------------------------------------------------------------
EMRDOC00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      EMRDOC40 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      EMRDOC10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      EMRDOC20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      EMRDOC30 IF EQUAL
.
          GOTO      EMRDOC93
.
EMRDOC10  RESET     EMDOC001
          PACK      KEY10,EMDOC001,SP70
          CALL      RDEMDO1
          COMPARE   ZERO,OVRCD
          GOTO      EMRDOC92 IF EQUAL
          CALL      CHKDOC00      * Checks mandatory fields
          BRANCH    EXIT,EMRDOC99
          CALL      DOCSAV00      * Moves input variables to file variables
          PACK      KEY10,EMDOC001,SP70
          CALL      WREMDO1      * Writes away the data
          PROC      EMRDKIUP     * Updates keyword index
          MOVE      ZERO,EXIT
          GOTO      EMRDOC99
.
EMRDOC20  PACK      KEY10,EMDOC001,SP70
          CALL      RDEMDO1
          BRANCH    OVRCD,EMRDOC91
          CALL      CHKDOC00      * Checks mandatory fields
          BRANCH    EXIT,EMRDOC99
          CALL      RLEMDO1      * Locks the record
          CALL      DOCSAV00      * Moves input variables to file variables
          CALL      UPEMDO1      * Writes away the data
          CALL      UUEMDO1      * Unlocks the record
          PROC      EMRDKIUP     * Updates keyword index
          MOVE      ZERO,EXIT
          GOTO      EMRDOC99
.
EMRDOC30  PACK      KEY10,EMDOC001,SP70
          CALL      RDEMDO1
          BRANCH    OVRCD,EMRDOC91
          CALL      DEEMDO1       * Writes away the data
          PROC      EMRDKIUP     * Updates keyword index
          MOVE      ZERO,EXIT
          GOTO      EMRDOC99
.
EMRDOC40  PACK      KEY10,EMDOC001,SP70
          CALL      RDEMDO1
          IF        OVRCD=1
            CALL      CLRDOC00     * Clear all the file variables
          ENDIF
 
          PACK      WEBTITLE,SP70,SP70
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,EMRDOC99
.
          CALL      WEBBOD00   * Process Web Body
.
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      EMRDOC99
.
EMRDOC91  MOVE      "Emergency Doctor record does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EMRDOC99
.
EMRDOC92  MOVE      "Emergency Doctor record exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EMRDOC99
.
EMRDOC93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EMRDOC99
.
EMRDOC99  RETURN
.-----------------------------------------------------------
. Initialize's file data
.-----------------------------------------------------------
CLRDOC00  MOVE      SP70,EMDODOC
          MOVE      SP70,EMDODNM
          MOVE      SP70,EMDODIN
          MOVE      SP70,EMDOACT
          MOVE      SP70,EMDOSPA
.
CLRDOC99  RETURN
.------------------------------------------------------------
. Moves cgi variables into file variables
.------------------------------------------------------------
DOCSAV00  MOVE      EMDOC001,KEY10
          SQUEEZE   KEY10
          PACK      EMDODOC,KEY10,SP70
          MOVE      EMDOC002,EMDODNM
          MOVE      EMDOC003,EMDODIN
.
          RESET     EMDOC004
          MATCH     SP70,EMDOC004
          IF        @EQUAL
            MOVE      "0",EMDOACT
          ELSE
            MOVE      EMDOC004,EMDOACT
          ENDIF
.
          MOVE      SP70,EMDOSPA
.
DOCSAV99  RETURN
.------------------------------------------------------------
. * Checks mandatory fields
.------------------------------------------------------------
CHKDOC00  RESET     EMDOC001
          MATCH     SP70,EMDOC001
          IF        !@EQUAL
            PACK      KEY6,EMDOC001,SP70
            CALL      RDDOCT1
            BRANCH    OVRCD,CHKDOC90
          ELSE
            GOTO      CHKDOC91   * Update Type not set report an error
          ENDIF
.
          RESET     EMDOC002
          MATCH     SP70,EMDOC002
          GOTO      CHKDOC92 IF EQUAL
.
          GOTO      CHKDOC99
.
CHKDOC90  MOVE      "This Doctor does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EMRDOC99
.
CHKDOC91  MOVE      "Emergency Doctor Code is mandatory!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKDOC99
.
CHKDOC92  MOVE      "Emergency Doctor Name is mandatory!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKDOC99
.
CHKDOC99  RETURN
.------------------------------------------------------------
. Emergency Mandatory Field Maintenance   
.------------------------------------------------------------
EMRMAN00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      EMRMAN40 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      EMRMAN10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      EMRMAN20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      EMRMAN30 IF EQUAL
.
          GOTO      EMRMAN93
.
EMRMAN10  CALL      CHKMAN00      * Checks mandatory fields
          BRANCH    EXIT,EMRMAN99 
          RESET     EMMAN001 
          PACK      KEY5,EMMAN001,SP70
          CALL      RDEMWFM1
          COMPARE   ZERO,OVRCD
          GOTO      EMRMAN92 IF EQUAL
          CALL      MANSAV00      * Moves input variables to file variables
          PACK      KEY5,EMMAN001,SP70
          CALL      WREMWFM1      * Writes away the data
          MOVE      ZERO,EXIT
          GOTO      EMRMAN99
.
EMRMAN20  PACK      KEY5,EMMAN001,SP70
          CALL      RDEMWFM1
          BRANCH    OVRCD,EMRMAN91
          CALL      CHKMAN00      * Checks mandatory fields
          BRANCH    EXIT,EMRMAN99 
          CALL      RLEMWFM1      * Locks the record
          CALL      MANSAV00      * Moves input variables to file variables
          CALL      UPEMWFM1      * Writes away the data
          CALL      UUEMWFM1      * Unlocks the record
          MOVE      ZERO,EXIT
          GOTO      EMRMAN99
.
EMRMAN30  PACK      KEY5,EMMAN001,SP70
          CALL      RDEMWFM1
          BRANCH    OVRCD,EMRMAN91
          CALL      DEEMWFM1      * Writes away the data
          MOVE      ZERO,EXIT
          GOTO      EMRMAN99
.
EMRMAN40  PACK      KEY5,EMMAN001,SP70     
          CALL      RDEMWFM1
          IF        OVRCD=1
            CALL      CLRMAN00    * Clear all the file variables
          ENDIF
.
          PACK      WEBTITLE,SP70,SP70
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,EMRMAN99
.
          CALL      WEBBOD00   * Process Web Body
.
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      EMRMAN99
.
EMRMAN91  MOVE      "Mandatory record does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EMRMAN99
.
EMRMAN92  MOVE      "Mandatory record exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EMRMAN99
.
EMRMAN93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EMRMAN99
.
EMRMAN99  RETURN
.-----------------------------------------------------------
. Initialize's file data
.-----------------------------------------------------------
CLRMAN00  MOVE      SP70,EMWFFLD
          MOVE      SP70,EMWFIDE
          MOVE      SP70,EMWFDES
          MOVE      SP70,EMWFSDE
          MOVE      SP70,EMWFMAN
          MOVE      SP70,EMWFIDP
          MOVE      SP70,EMWFEXF
          MOVE      SP70,EMWFRES                   * HPS01I
          MOVE      SP70,EMWFSPA
.
CLRMAN99  RETURN
.------------------------------------------------------------
. Moves cgi variables into file variables
.------------------------------------------------------------
MANSAV00  MOVE      EMMAN001,MANDA
          MOVE      MANDA,KEY5 
          MOVE      KEY5,EMWFFLD          
          MOVE      EMMAN002,EMWFIDE
          MOVE      EMMAN003,EMWFDES
          MOVE      EMMAN004,EMWFSDE
.
          RESET     EMMAN005
          MATCH     SP70,EMMAN005
          IF        @EQUAL
            MOVE      "0",EMWFMAN
          ELSE
            MOVE      EMMAN005,EMWFMAN
          ENDIF
.
          RESET     EMMAN006
          MATCH     SP70,EMMAN006
          IF        @EQUAL
            MOVE      "0",EMWFIDP
          ELSE
            MOVE      EMMAN006,EMWFIDP
          ENDIF
.
          MOVE      EMMAN007,EMWFEXF
          MOVE      EMMAN008,EMWFRES                  * HPS01I
          MOVE      EMMAN009,EMWFMND
.
          MOVE      SP70,EMWFSPA
.
MANSAV99  RETURN
.------------------------------------------------------------
. * Checks mandatory fields
.------------------------------------------------------------
CHKMAN00  RESET     EMMAN001
          MATCH     SP70,EMMAN001 
          GOTO      CHKMAN99 IF NOT EQUAL
.
          MOVE      "Emergency Feild Code is mandatory!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKMAN99
.
CHKMAN99  RETURN
.------------------------------------------------------------
. Process Fields from Template Body
.------------------------------------------------------------
PROFLD00  BRANCH    REPORTNO,PROFLD10,PROFLD20,PROFLD30,PROFLD40,PROFLD50:
                             PROFLD60,PROFLD70,PROFLD99,PROFLD90,PROFLD99:
                             PROFL110
          GOTO      PROFLD99
.
. Emergency Site Maintenance
.
PROFLD10  CALL      ESIT0000     
          GOTO      PROFLD99
.
. Emergency Update Type Maintenance
.
PROFLD20  CALL      EUTY0000      * Emergency Update Types
          GOTO      PROFLD99
.
. Emergency Doctors Maintenance 
.
PROFLD30  BRANCH    FLDSETNO,PROFLD31,PROFLD32 
          GOTO      PROFLD99
.
PROFLD31  CALL      EDOC0000      * Emergency Doctors
          GOTO      PROFLD99
.
PROFLD32  CALL      DOCF0000      * Doctor Table Details
          GOTO      PROFLD99
.
PROFLD40  CALL      MAND0000      * Mandatory Field Maintenance Details
          GOTO      PROFLD99
.
. Emergency Location Maintenance
.
PROFLD50  CALL      ELOC0000     
          GOTO      PROFLD99
.
. Emergency Procedure Table Maintenance
.
PROFLD60  CALL      EMRP0000      * Emergency Procedure Maintenance Details
          GOTO      PROFLD99
.
. Emergency Diagnosis Table Maintenance
.
PROFLD70  CALL      EMRD0000      * Emergency Diagnosis Maintenance Details
          GOTO      PROFLD99
.
. Body Region Maintenance
.
PROFLD90  CALL      BREG0000
          GOTO      PROFLD99
.
. Emergency Template Mapping Maintenance
.
PROFL110  BRANCH    FLDSETNO,PROFL111
          GOTO      PROFLD99
.
PROFL111  CALL      ALTP0000
          GOTO      PROFLD99
.
PROFLD99  RETURN
.------------------------------------------------------------
. Emergency Site  
.------------------------------------------------------------
ESIT0000  BRANCH    FLDITMNO,ESIT0100,ESIT0200,ESIT0300,ESIT0400,ESIT0500:
                             ESIT0600,ESIT0700,ESIT0800,ESIT0900,ESIT1000:
                             ESIT1100,ESIT1200,ESIT1300,ESIT0014,ESIT1500:
                             ESIT1600,ESIT1700,ESIT1800,ESIT1900,ESIT2000
          GOTO      ESIT9999
.
ESIT0100  WRITE     HTMLFILE;EMSTSCOD;
          GOTO      ESIT9999
.
ESIT0200  WRITE     HTMLFILE;EMSTDESC;
          GOTO      ESIT9999
.
ESIT0300  WRITE     HTMLFILE;EMSTHRPW;
          GOTO      ESIT9999
.
ESIT0400  MOVE      "wj",CATEGORY          
          MOVE      EMSTDPTY,CODE
          CALL      CODITM00
          GOTO      ESIT9999
.
ESIT0500  MOVE      "wk",CATEGORY          
          MOVE      EMSTSPCO,CODE
          CALL      CODITM00
          GOTO      ESIT9999
.
ESIT0600  WRITE     HTMLFILE;EMSTSPNO;
          GOTO      ESIT9999               
.
ESIT0700  CALL      HSEL0000
          GOTO      ESIT9999
.
ESIT0800  CALL      EMTAB000      * Table of Emergency Sites
          GOTO      ESIT9999
.
ESIT0900  CALL      NEXTEM00      * Link to Next Group 
          GOTO      ESIT9999
.
ESIT1000  CALL      PREEMR00      * Link to Last Group   
          GOTO      ESIT9999
.
ESIT1100  CALL      EMSTLC00      * Default location code
          GOTO      ESIT9999
.
ESIT1200  CALL      RCRQ0000      * Default Request Location
          GOTO      ESIT9999
.
ESIT1300  CALL      MOVREA00      * Default Reason
          GOTO      ESIT9999
.
ESIT0014  PACK      KEY16,EMSIT001,SP70
          CALL      RSALTMP1
          CALL      RKALTMP1
          BRANCH    OVRCD,ESIT9999
.
          MATCH     EMSIT001,ALTMDEPT
          GOTO      ESIT9999 IF NOT EQUAL
.
          WRITE     HTMLFILE;ONE;           * Templates exist for department
          GOTO      ESIT9999
.
ESIT1500  WRITE     HTMLFILE;EMSTEDST;
          GOTO      ESIT9999
.
ESIT1600  WRITE     HTMLFILE;EMSTEDFP;
          GOTO      ESIT9999
.
ESIT1700  CALL      SELPRT00
          GOTO      ESIT9999
.
ESIT1800  WRITE     HTMLFILE;EMSTACTV;
          GOTO      ESIT9999
.
ESIT1900  IF        FHIRBUND=0
            CALL      FHRLOC00
          ELSE
             CLEAR     KEY20
             APPEND    "EmergencySite-",KEY20
             APPEND    EMSTSCOD,KEY20
             APPEND    SP70,KEY20
             RESET     KEY20
             WRITE     HTMLFILE;*+,"{ #"fullUrl#" : #"%BASEURL/Location/",KEY20,"#", ":
                                  " #"resource#" : ";
.
            CALL      FHRLOC00
.
            WRITE     HTMLFILE;*+," } "
          ENDIF
.
          IF       FHRLOCIN=1
.
            MATCH     SP70,EMSTHSNO
            IF        !@EQUAL & !@EOS
.
              PACK      KEY3,EMSTHSNO,SP70
              CALL      RDPTHSP1
              IF        OVRCD=0
.
                WRITE     HTMLFILE;*+,",{ #"fullUrl#" : #"%BASEURL/Location/Hospital-",EMSTHSNO,"#", ":
                                      " #"resource#" : ";
.
                CALL      RESLOC00
.
                WRITE     HTMLFILE;*+," } "
.
              ENDIF
.
            ENDIF
.
          ENDIF
.
          GOTO      ESIT9999
.
ESIT2000  WRITE     HTMLFILE;EMSTPPRV;
          GOTO      ESIT9999
.
ESIT9999  RETURN
.------------------------------------------------------------
. Output Site Location Resource
.------------------------------------------------------------
FHRLOC00  CLEAR     KEY20
          APPEND    "EmergencySite-",KEY20
          APPEND    EMSTSCOD,KEY20
          APPEND    SP70,KEY20
          RESET     KEY20
          WRITE     HTMLFILE;*+," { #"resourceType#": #"Location#",":
                    "   #"id#": #"",KEY20,"#",":
                    " #"extension#": [":
                    " { #"url#":#"%BASEURL/extension/webPASService#"":
                        " ,#"valueString#":#"",PRGID,SP1,PRGNAM,SP1,VERSION,"#"},":
                    " { #"url#":#"%BASEURL/extension/webPASTemplate#"":
                        " ,#"valueString#":#"%TEMPLATEHOME/",WBTPFIL," %TEMPLATEVERSION#"}":
                    " ],":
                    " #"identifier#": {#"value#": #"",KEY20,"#"},":
                    " #"name#": #"",EMSTDESC,"#",":
                    " #"physicalType#": {":
                    "  #"coding#": [ {":
                    "   #"system#": #"http://hl7.org/fhir/location-physical-type#",":
                    "   #"code#": #"es#",":
                    "   #"display#": #"Emergency Site#"  } ] },":
                    " #"partOf#": { ":
                    "  #"reference#": #"Location/Hospital-",EMSTHSNO,"#"":
                    " } } ";
          RETURN
.------------------------------------------------------------------------------
. FHIR Location Resource for EmergencyLocation.
.------------------------------------------------------------------------------
RESWRD00  STRIP     EMLODESC
          STRIP     DIM3
          CLEAR     KEY25
          APPEND    "EmergencyLocation-",KEY25
          APPEND    EMLOLOCA,KEY25
          APPEND    SP70,KEY25
          RESET     KEY25
          STRIP     WBTPFIL
          WRITE     HTMLFILE;*+," { #"resourceType#": #"Location#",":
                    " #"id#": #"",KEY25,"#",":
                    " #"extension#": [":
                    " { #"url#":#"%BASEURL/extension/webPASService#"":
                        " ,#"valueString#":#"",PRGID,SP1,PRGNAM,SP1,VERSION,"#"},":
                    " { #"url#":#"%BASEURL/extension/webPASTemplate#"":
                        " ,#"valueString#":#"%TEMPLATEHOME/",WBTPFIL," %TEMPLATEVERSION#"}":
                    " ],":
                    " #"identifier#": { #"value#": #"",KEY25,"#"},":
                    " #"name#": #"",EMLODESC,"#",":
                    " #"physicalType#": {":
                    "  #"coding#": [ {":
                    "   #"system#": #"http://hl7.org/fhir/location-physical-type#",":
                    "   #"code#": #"ro#",":
                    "   #"display#": #"Room#"  } ] } ";
.          MATCH     SP70,WEXTN
.          IF        !@EQUAL
.             WRITE     HTMLFILE;*+," #"telecom#": [":
.                       "{ #"system#": #"phone#",":
.                       "  #"value#": #"",WEXTN,"#" } ],";
.          ENDIF
.
          MATCH     SP70,DIM3
          IF        !@EQUAL & !@EOS
            WRITE     HTMLFILE;*+,",#"partOf#": { ":
                      "  #"reference#": #"Location/Hospital-",DIM3,"#"":
                      " } } ";
          ELSE
            WRITE     HTMLFILE;*+,"} ";
          ENDIF
.
          RETURN
+
.------------------------------------------------------------
. Hospital Selection
.------------------------------------------------------------
HSEL0000  PACK      KEY3,SP70
          CALL      RSPTHSP1
HSEL0100  CALL      RKPTHSP1
          BRANCH    OVRCD,HSEL9999
.
          MOVE      PTHSNAME,KEY50
          PACK      KEY5,QUOTE,PTHSHOSP,QUOTE
          MATCH     EMSTHSNO,PTHSHOSP
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=",KEY5," selected>":
                               KEY50,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=",KEY5,">",KEY50,"</option>"
          ENDIF
          GOTO      HSEL0100
.
HSEL9999  RETURN
.----------------------------------------------------------------------------
.         EMSTLC00  Locn based on site code. 0=Code, 1=Desc, 2=Selection List
.----------------------------------------------------------------------------
EMSTLC00  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,EMSTLC10,EMSTLC20
          WRITE     HTMLFILE;EMSTDLOC;
          GOTO      EMSTLC99
.
EMSTLC10  PACK      KEY3,EMSTDLOC,SP10
          CALL      RDEMLOC1
          BRANCH    OVRCD,EMSTLC99
          WRITE     HTMLFILE;EMLODESC;
          GOTO      EMSTLC99
.
EMSTLC20  PACK      KEY3,SP70
          CALL      RSEMLOC1
.
EMSTLC22  CALL      RKEMLOC1
          BRANCH    OVRCD,EMSTLC99
.
          MATCH     EMLOSCOD,EMSTSCOD           * only list recs for THIS site
          GOTO      EMSTLC22 IF NOT EQUAL
.
          PACK      KEY5,QUOTE,EMLOLOCA,QUOTE
          MATCH     EMSTDLOC,EMLOLOCA
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=",KEY5," selected>":
                               EMLODESC,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=",KEY5,">",EMLODESC,"</option>"
          ENDIF
          GOTO      EMSTLC22
.
EMSTLC99  RETURN
.------------------------------------------------------------
. Emergency Update Type
.------------------------------------------------------------
EUTY0000  BRANCH    FLDITMNO,EUTY0100,EUTY0200,EUTY0300,EUTY0400,EUTY0500:
                             EUTY0600,EUTY0700,EUTY0800
          GOTO      EUTY9999
.
EUTY0100  WRITE     HTMLFILE;EMUTUTY;
          GOTO      EUTY9999
.
EUTY0200  MATCH     SP70,EMUTDES
          GOTO      EUTY9999 IF EQUAL
          WRITE     HTMLFILE;EMUTDES;
          GOTO      EUTY9999
.
EUTY0300  WRITE     HTMLFILE;EMUTSLV;
          GOTO      EUTY9999
.
EUTY0400  WRITE     HTMLFILE;EMUTSID;
          GOTO      EUTY9999
.
EUTY0500  WRITE     HTMLFILE;EMUTPWD;   
          GOTO      EUTY9999
.
EUTY0600  CALL      UPTAB000      * Table of Emergency Update Types 
          GOTO      EUTY9999
.
EUTY0700  CALL      NEXTEM00      * Link to Next Group 
          GOTO      EUTY9999
.
EUTY0800  CALL      PREEMR00      * Link to Last Group   
          GOTO      EUTY9999
.
EUTY9999  RETURN
.------------------------------------------------------------
. Emergency Doctors
.------------------------------------------------------------
EDOC0000  BRANCH    FLDITMNO,EDOC0100,EDOC0200,EDOC0300,EDOC0400,EDOC0500:
                             EDOC0600,EDOC0700,EDOC0800
          GOTO      EDOC9999
.
EDOC0100  MOVE      TWO,LNK2PRT                       
          CALL      DCTAB000      * Table of Emergency Doctors for Maintenance
          GOTO      EDOC9999
.
EDOC0200  CALL      NEXTDG00      * Link to Next Group
          GOTO      EDOC9999
.
EDOC0300  CALL      PREVDG00      * Link to Last Group
          GOTO      EDOC9999
.
EDOC0400  WRITE     HTMLFILE;EMDODOC;
          GOTO      EDOC9999
.
EDOC0500  MATCH     SP70,EMDODNM
          GOTO      EDOC9999 IF EQUAL
          WRITE     HTMLFILE;EMDODNM;
          GOTO      EDOC9999
.
EDOC0600  MATCH     SP70,EMDODIN
          GOTO      EDOC9999 IF EQUAL
          WRITE     HTMLFILE;EMDODIN;
          GOTO      EDOC9999
.
EDOC0700  WRITE     HTMLFILE;EMDOACT;
          GOTO      EDOC9999
.
EDOC0800  MOVE      ONE,LNK2PRT  
          CALL      DCTAB000      * Table of Emergency Doctors for Search
          GOTO      EDOC9999
.
EDOC9999  RETURN
.------------------------------------------------------------
. Emergency Field Maintenance
.------------------------------------------------------------
MAND0000  BRANCH    FLDITMNO,MAND0100,MAND0200,MAND0300,MAND0400,MAND0500:
                             MAND0600,MAND0700,MAND0800,MAND0900,MAND1000:
                             MAND1100,MAND1200 
          GOTO      MAND9999
.
MAND0100  WRITE     HTMLFILE;EMWFFLD;  * mandatory no need to check for it
          GOTO      MAND9999
.
MAND0200  MATCH     SP70,EMWFIDE
          GOTO      MAND9999 IF EQUAL
          WRITE     HTMLFILE;EMWFIDE;
          GOTO      MAND9999
.
MAND0300  MATCH     SP70,EMWFDES
          GOTO      MAND9999 IF EQUAL
          WRITE     HTMLFILE;EMWFDES;
          GOTO      MAND9999
.
MAND0400  MATCH     SP70,EMWFSDE
          GOTO      MAND9999 IF EQUAL
          WRITE     HTMLFILE;EMWFSDE;
          GOTO      MAND9999
.
MAND0500  MATCH     SP70,EMWFMAN
          GOTO      MAND9999 IF EQUAL
          WRITE     HTMLFILE;EMWFMAN;
          GOTO      MAND9999
.
MAND0600  MATCH     SP70,EMWFIDP  
          GOTO      MAND9999 IF EQUAL
          WRITE     HTMLFILE;EMWFIDP;
          GOTO      MAND9999
.
MAND0700  CALL      FLTAB000      * Table of Emergency Field Maintenance 
          GOTO      MAND9999
.
MAND0800  CALL      NEXTEM00      * Link to Next Group 
          GOTO      MAND9999
.
MAND0900  CALL      PREEMR00      * Link to Last Group   
          GOTO      MAND9999
.
MAND1000  MATCH     SP70,EMWFEXF
          GOTO      MAND9999 IF EQUAL
          WRITE     HTMLFILE;EMWFEXF;
          GOTO      MAND9999
.
MAND1100  MOVE      "EZ",CATEGORY
          MOVE      EMWFRES,CODE                                               
          CALL      CODITM00
          GOTO      MAND9999
.
MAND1200  MATCH     SP70,EMWFMND       * Mandatory for transaction
          GOTO      MAND9999 IF EQUAL
          WRITE     HTMLFILE;EMWFMND;
          GOTO      MAND9999
.
MAND9999  RETURN
.------------------------------------------------------------
. Link to Next set of Emergency Maintenance Notes 
.------------------------------------------------------------
NEXTEM00  BRANCH    FHIRTYPE,NEXTEM10
          MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
.
          WRITE     HTMLFILE;"emrweb05.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2;
          GOTO      NEXTEM99
.
NEXTEM10  IF        DISNUMB=NORECORD
            ASSIGN    STARTNUM+NORECORD,F6
            MOVE      F6,KEY6
            SQUEEZE   KEY6
            WRITE     HTMLFILE;*+,"nextpage=&startnum=",KEY6
          ENDIF
.
NEXTEM99  RETURN
.-------------------------------------------------------------------------------
. Link to previous set of Emergency Maintenance Notes 
.-------------------------------------------------------------------------------
PREEMR00  BRANCH    FHIRTYPE,PREEMR10
          MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3
          IF        F3<0
            MOVE    ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
.
          WRITE     HTMLFILE;"emrweb05.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2;
          GOTO      PREEMR99
.
PREEMR10  ASSIGN    STARTNUM-NORECORD,F6
          IF        F6>=0
            MOVE      F6,KEY6
            SQUEEZE   KEY6
            WRITE     HTMLFILE;*+,"prevpage=&startnum=",KEY6
          ENDIF
.
PREEMR99  RETURN
.------------------------------------------------------------
. Link to Next set of Emergency Diagnosis Notes
.------------------------------------------------------------
NEXTDG00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
.
          REP       "'~",KEYWORDS                                   * Car 280147
.
          WRITE     HTMLFILE;"emrweb05.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&keywords=",KEYWORDS:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2;
.
NEXTDG99  RETURN
.-------------------------------------------------------------------------------
. Link to previous set of Emergency Diagnosis Notes
.-------------------------------------------------------------------------------
PREVDG00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3
          IF        F3<0
            MOVE    ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
.
          REP       "'~",KEYWORDS                                   * Car 280147
.
          WRITE     HTMLFILE;"emrweb05.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&keywords=",KEYWORDS:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2;
.
PREVDG99  RETURN
.------------------------------------------------------------
. Emergency Site's Table 
.------------------------------------------------------------
EMTAB000  IF        FHIRTYPE=0
            UNPACK    DIM127,D1,LINKPRG,DIM127
            UNPACK    DIM127,D1,LINKREP,DIM127
            UNPACK    DIM127,D1,LINKTEM,DIM127
            CALL      EMHED000      * Output Table Heading
            IF        NORECORD=0
              MOVE      PTCNNRTD,NORECORD * Set Default No Records to Display
            ENDIF
          ENDIF
.
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY3,STARTKEY,SP70
          CALL      RDEMSIT1
          IF        OVRCD=0
            CALL      RPEMSIT1
          ENDIF
EMTAB100  CALL      RKEMSIT1
          BRANCH    OVRCD,EMTAB900
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      EMTAB100
          ENDIF
.
          IF        FHIRTYPE=0
            CALL      EROWA000      * Display Emergency Security ID Row
          ELSE
            CALL      FHRRW000      * Display Emergency Security ID FHIR
          ENDIF
.
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB    
          GOTO      EMTAB100 IF NOT EQUAL
          GOTO      EMTAB999
.
EMTAB900  IF        FHIRTYPE=0
            CALL      NOMORE000        * Display No More Records Message
           ENDIF
.
EMTAB999  RETURN
.------------------------------------------------------------
. Site Maintenance (TABLE HEADING) 
.------------------------------------------------------------
EMHED000  WRITE     HTMLFILE;"<tr>":
                               "<td class=HeadingCell width=20%>Site Code</td>":
                               "<td class=HeadingCell width=60%>":
                               "Description</td>":
                               "<td class=HeadingCell width=20%>Active</td>":
                             "</tr>"
.
EMHED999  RETURN
.------------------------------------------------------------
.  Output FHIR Location Bundle
.------------------------------------------------------------
FHRRW000  IF        DISNUMB>0
            WRITE     HTMLFILE;",";
          ENDIF
          CLEAR     KEY20
          APPEND    "EmergencySite-",KEY20
          APPEND    EMSTSCOD,KEY20
          APPEND    SP70,KEY20
          RESET     KEY20
          WRITE     HTMLFILE;*+,"{ #"fullUrl#" : #"%BASEURL/Location/",KEY20,"#", ":
                                " #"resource#" : ";
.
          CALL      FHRLOC00
.
          WRITE     HTMLFILE;*+," } "
.
          IF       FHRLOCIN=1
.
            MATCH     SP70,EMSTHSNO
            IF        !@EQUAL & !@EOS
.
              PACK      KEY3,EMSTHSNO,SP70
              CALL      RDPTHSP1
              IF        OVRCD=0
.
                WRITE     HTMLFILE;*+,",{ #"fullUrl#" : #"%BASEURL/Location/Hospital-",EMSTHSNO,"#", ":
                                      " #"resource#" : ";
.
                CALL      RESLOC00
.
                WRITE     HTMLFILE;*+," } "
.
              ENDIF
.
            ENDIF
.
          ENDIF
.
FHRRW999  RETURN
.------------------------------------------------------------
. Site Maintenance (TABLE ROW) 
.------------------------------------------------------------
EROWA000  MATCH     "1",EMSTACTV
          IF        @EQUAL
            MOVE      "No",KEY3
          ELSE
            MOVE      "Yes",KEY3
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td nowrap><a href='javascript:ShowDetail01(#"":
                             LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&emsit001=",EMSTSCOD,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             EMSTSCOD,"</td>":
                             "<td>",EMSTDESC,"</td>":
                             "<td>",KEY3,"</td>":
                             "</tr>"
.
EROWA999 RETURN
.------------------------------------------------------------
. Emergency Update Type Table 
.------------------------------------------------------------
UPTAB000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          CALL      UPHED000      * Output Table Heading
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY5,STARTKEY,SP70
          CALL      RDEMUTY1
          IF        OVRCD=0
            CALL      RPEMUTY1
          ENDIF
UPTAB100  CALL      RKEMUTY1
          BRANCH    OVRCD,UPTAB900
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      UPTAB100
          ENDIF
.
          CALL      UROWA000      * Display Emergency Update Types Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB    
          GOTO      UPTAB100 IF NOT EQUAL
          GOTO      UPTAB999
.
UPTAB900  CALL      NOMORE00      * Display No More Records Message
.
UPTAB999  RETURN
.------------------------------------------------------------
. Update Type (TABLE HEADING) 
.------------------------------------------------------------
UPHED000  WRITE     HTMLFILE;"<tr>":
                               "<td class=HeadingCell width=20%>Code</td>":
                               "<td class=HeadingCell width=40%>":
                               "Update Type</td>":
                               "<td class=HeadingCell width=10%>Level</td>":
                               "<td class=HeadingCell width=10%>ID</td>":
                               "<td class=HeadingCell width=10%>Password</td>":
                             "</tr>"
.
UPHED999  RETURN
.------------------------------------------------------------
. Update Type (TABLE ROW) 
.------------------------------------------------------------
UROWA000  MATCH     SP70,EMUTDES
          IF        @EQUAL
            MOVE      "&nbsp;",EMUTDES
          ENDIF
.         
          MOVE      EMUTSLV,KEY6     * Moved to key6 so to fit blank
          MATCH     SP70,EMUTSLV
          IF        @EQUAL
            MOVE      "&nbsp;",KEY6
          ENDIF
.
          MOVE      "Yes",KEY3
          MATCH     "0",EMUTSID
          IF        @EQUAL
            MOVE      "No ",KEY3
          ENDIF
.
          MOVE      "Yes",PASSYN
          MATCH     "0",EMUTPWD
          IF        @EQUAL
            MOVE      "No ",PASSYN
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td nowrap><a href='javascript:ShowDetail02(#"":
                             LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&emupt001=",EMUTUTY,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>": 
                             EMUTUTY,"</td>":
                             "<td>",EMUTDES,"</td>":
                             "<td>",KEY6,"</td>":               
                             "<td>",KEY3,"</td>":
                             "<td>",PASSYN,"</td>":
                             "</tr>"
.
UROWA999 RETURN
.------------------------------------------------------------
. Emergency Doctors Table
.------------------------------------------------------------
DCTAB000  CALL      DCHED000      * Output Table Heading
.
          CALL      GETWRD00       * This routine Splits the KEYWORDS field
.                                  * into KEYWRD01 to KEYWRD10
          IF        LNK2PRT<>1
            UNPACK    DIM127,D1,LINKPRG,DIM127
            UNPACK    DIM127,D1,LINKREP,DIM127
            UNPACK    DIM127,D1,LINKTEM,DIM127
          ENDIF
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
          MOVE      ZERO,DOCAREND         * Num of Dr in DOCARRAY[]  *C-0299206
          MOVE      ZERO,PAGEFULL         * Initialize pagefull ind  *C-0299206
          MOVE      ZERO,SNDXDONE         * Soundex shown = No       *C-0299206
.
          STRIP     KEYWRD01
          PACK      KEY25,KEYWRD01,SP70   * Keyword for direct match *C-0299206
          GOTO      DCTAB050                                         *C-0299206
.
.         *C-0299206 - Generate Soundex key for keyword
.
DCTAB020  PACK      KEY15,KEYWRD01,SP70
          CMATCH    SP1,KEY15
          GOTO      DCTAB999 IF EQUAL         * blank keyword search
.
          MOVE      KEYWRD01,SXKEYIN          * Keyword in to convert to Soundex
          CALL      GSNDX000                  * Generate Soundex key routine
          PACK      KEYWRD01,Z1,SXKEYOUT,SP70 * Add "~" to indicate Soundex key
          STRIP     KEYWRD01
          PACK      KEY25,KEYWRD01,SP70
.
DCTAB050  CALL      RSEMDK2                                          *C-0299206
DCTAB100  CALL      RKEMDK2
          BRANCH    OVRCD,DCTAB999
.
          PACK      KEY10,EMDKDOC,SP70
          CALL      RDEMDO1
          BRANCH    OVRCD,DCTAB100
.
          CALL      EMRDKB00      * check record
          BRANCH    OVRCD,DCTAB100,DCTAB999
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      DCTAB100
          ENDIF
.
          IF        LNK2PRT=1                
            CALL      DCROWB00
            GOTO      DCTAB800
          ELSE
            IF        LNK2PRT=2
              CALL      DCROWA00      * Display Emergency Doctors Row
              GOTO      DCTAB800
            ENDIF
          ENDIF
.
          GOTO      DCTAB999          * not valid LNK2PRT
.
DCTAB800  ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      DCTAB100 IF NOT EQUAL
.
          MOVE      ONE,PAGEFULL             * Display page is full *C-0299206
.
.         *C-0299206
.         If display page is not full and direct match has finished and show
.         Soundex in searches is turned on then loop back to generate the
.         soundex key for the keyword and read the table again to output the
.         results
.
DCTAB999  IF        PAGEFULL=0
            MATCH     "0",PTCSXOFF           * Show Soundex in searches?
            IF        @EQUAL
              IF        SNDXDONE=0
                MOVE      ONE,SNDXDONE       * Soundex done = yes
                GOTO      DCTAB020           * Yes, generate Soundex key
              ENDIF
            ENDIF
            CALL      NOMORE00      * Display No More Records Message
          ENDIF
.
          RETURN
.------------------------------------------------------------
. Emergency Doctors (TABLE HEADING)
.------------------------------------------------------------
DCHED000  WRITE     HTMLFILE;"<tr align=center>":
                               "<td class=HeadingCell colspan=5>":
                               "You Searched for ":
                               KEYWORDS,"</td>":
                             "</tr>":
                             "<tr>":
                               "<td class=HeadingCell>Doctor Code</td>":
                               "<td class=HeadingCell width=40%>":
                               "Doctors Name</td>":
                               "<td class=HeadingCell width=20%>":
                               "Display Initials</td>":
                               "<td class=HeadingCell width=20%>Active</td>":
                             "</tr>"
.
DCHED999  RETURN
.------------------------------------------------------------
. Emergency Doctors (TABLE ROW) Maintenance
.------------------------------------------------------------
DCROWA00  MATCH     SP70,EMDODNM
          IF        @EQUAL
            MOVE      "&nbsp;",EMDODNM
          ELSE
            CALL      JAVEMD00                                      * Car 280147
          ENDIF
.
          MOVE      EMDODIN,KEY6   * Moved to key6 so to fit blank
          MATCH     SP70,EMDODIN
          IF        @EQUAL
            MOVE      "&nbsp;",KEY6
          ENDIF
.
          MOVE      "Yes",KEY3
          MATCH     "0",EMDOACT
          IF        @EQUAL
            MOVE      "No ",KEY3
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td nowrap><a href='javascript:ShowDetail03(#" ":
                             LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&emdoc001=",EMDODOC,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             EMDODOC,"</td>":
                             "<td>",EMDODNM,"</td>":
                             "<td>",KEY6,"</td>":
                             "<td>",KEY3,"</td>":
                             "</tr>"
.
DCROWA99 RETURN
.------------------------------------------------------------
. Emergency Doctors (TABLE ROW) Search
.------------------------------------------------------------
DCROWB00  MATCH     SP70,EMDODNM
          IF        @EQUAL
            MOVE      "&nbsp;",EMDODNM
          ELSE
            CALL      JAVEMD00                                      * Car 280147
          ENDIF
.
          MOVE      EMDODIN,KEY6   * Moved to key6 so to fit blank
          MATCH     SP70,EMDODIN
          IF        @EQUAL
            MOVE      "&nbsp;",KEY6
          ENDIF
.
          MOVE      "Yes",KEY3
          MATCH     "0",EMDOACT
          IF        @EQUAL
            MOVE      "No ",KEY3
          ENDIF
.
          WRITE     HTMLFILE;"<tr valign=top>":
                              "<td nowrap><a href=#"":
                              "javascript:updateParent('",EMDODNM:
                              "','",EMDODOC,"')#">":
                             EMDODOC,"</td>":
                             "<td>",EMDODNM,"</td>":
                             "<td>",KEY6,"</td>":
                             "<td>",KEY3,"</td>":
                             "</tr>"
.
DCROWB99 RETURN
.
.-------------------------------------------------------------------------------
. Re-format Emergency Doctor Name - change any apostrophe to %60 to prevent
. script errors in subsequent processing                              CAR 280147
.-------------------------------------------------------------------------------
JAVEMD00  MOVE      SP70,JAVFNAME
.
          MOVE      EMDODNM,JAVFNAME         * Emergency Doctor name
          SCAN      "'",JAVFNAME             * find any apostrophes
          GOTO      JAVEMD99 IF NOT EQUAL
.
          MOVE      JAVFNAME,ENDNAME
          REP       "' ",ENDNAME             * remove the apostrophe
.
          LENSET    JAVFNAME
          RESET     JAVFNAME                 * get all chars before apostrophe
          MOVE      JAVFNAME,STRTNAME
.
          REP       "'%",STRTNAME
          STRIP     STRTNAME
          PACK      JAVFNAME,STRTNAME,SIXTY,ENDNAME,SP70    * formatted name
.
JAVEMD99  RETURN
.
.------------------------------------------------------------
. Check Record
.------------------------------------------------------------
EMRDKB00  MATCH     KEYWRD01,EMDKKWD
          GOTO      EMRDKB92 IF NOT EQUAL
          IF        KEYWRDNO>1
            PACK      SAVKEY25,EMDKKWD,EMDKDOC
            CALL      EMRDKC00
            BRANCH    EXIT,EMRDKB91
            MOVE      SAVKEY25,KEY25
            CALL      RDEMDK2
          ENDIF
.
EMRDKB90  CALL      CDOC0000         * Check if doctor has been      *C-0299206
          BRANCH    EXIT,EMRDKB91    * displayed before              *C-0299206
.
          MOVE      ZERO,OVRCD
          GOTO      EMRDKB99
.
EMRDKB91  MOVE      ONE,OVRCD
          GOTO      EMRDKB99
.
EMRDKB92  MOVE      TWO,OVRCD
.
EMRDKB99  RETURN
.------------------------------------------------------------
. Check for Matching Words
.------------------------------------------------------------
EMRDKC00  MOVE      ONE,F2
          MOVE      ZERO,EXIT
          PACK      SAVKEY10,EMDKDOC,SP70
.
EMRDKC10  COMPARE   KEYWRDNO,F2           * Loop through each Key Word in Search
          GOTO      EMRDKC99 IF EQUAL     * Exit OK
          ADD       ONE,F2
          LOAD      KEYWRDXX,F2,KEYWRD01,KEYWRD02,KEYWRD03,KEYWRD04,KEYWRD05:
                             KEYWRD06,KEYWRD07,KEYWRD08,KEYWRD09,KEYWRD10
          PACK      KEY25,EMDKDOC,KEYWRDXX
          CALL      RDEMDK1               * Check for Exact Match
          COMPARE   ZERO,OVRCD            * Ok so Continue Loop
          GOTO      EMRDKC10 IF EQUAL
.
          CALL      RKEMDK1               * Not Exact so Read Next Record
          BRANCH    OVRCD,EMRDKC90        * No Match So Exit No Match
          PACK      KEY6,EMDKDOC,SP70
          MATCH     SAVKEY10,KEY10        * Match Key Fields
          GOTO      EMRDKC90 IF NOT EQUAL * No Match So Exit
          STRIP     KEYWRDXX
          MATCH     EMDKKWD,KEYWRDXX      * Check Key Word
          GOTO      EMRDKC90 IF NOT EQUAL * No Match So Exit
          GOTO      EMRDKC10              * Check Next Search Word
.
EMRDKC90  MOVE      ONE,EXIT
.
EMRDKC99  RETURN
.
.------------------------------------------------------------
. *C-0299206
. Check if this doctor has already been output in the search
.------------------------------------------------------------
CDOC0000  MOVE       ZERO,F3
          MOVE       ZERO,EXIT
.
CDOC1000  ADD        ONE,F3
          IF         F3>DOCAREND
            IF         F3>200
              GOTO       CDOC9999
            ENDIF
            MOVE       F3,DOCAREND
            MOVE       EMDODOC,DOCARRAY[F3]
            GOTO       CDOC9999
          ENDIF
.
          MATCH      EMDODOC,DOCARRAY[F3]
          GOTO       CDOC1000 IF NOT EQUAL
.
          MOVE       ONE,EXIT
.
CDOC9999  RETURN
.------------------------------------------------------------
. Mandatory Fields Table 
.------------------------------------------------------------
FLTAB000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          CALL      FLHED000      * Output Table Heading
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY5,STARTKEY,SP70
          CALL      RDEMWFM1
          IF        OVRCD=0
            CALL      RPEMWFM1
          ENDIF
FLTAB100  CALL      RKEMWFM1
          BRANCH    OVRCD,FLTAB900
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      FLTAB100
          ENDIF
.
          CALL      FLROWA00      * Display Mandatory Fields Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB    
          GOTO      FLTAB100 IF NOT EQUAL
          GOTO      FLTAB999
.
FLTAB900  CALL      NOMORE00      * Display No More Records Message
.
FLTAB999  RETURN
.------------------------------------------------------------
. Mandatory Fields (TABLE HEADING) 
.------------------------------------------------------------
FLHED000  WRITE     HTMLFILE;"<tr>":
                               "<td class=HeadingCell width=7%>Code</td>":
                               "<td class=HeadingCell width=20%>":
                               "webPAS Description</td>":
                               "<td class=HeadingCell width=20%>":
                               "Site Description</td>":
                               "<td class=HeadingCell width=20%>":
                               "Screen Description</td>":
                               "<td class=HeadingCell width=9%>":
                               "Mandatory for Transaction</td>":
                               "<td class=HeadingCell width=7%>Mandatory</td>":
                               "<td class=HeadingCell width=9%>":
                               "Responsibility</td>":
                               "<td class=HeadingCell width=7%>":
                               "Injury Data</td>":
                             "</tr>"
.
FLHED999  RETURN
.------------------------------------------------------------
. Mandatory Fields (TABLE ROW) 
.------------------------------------------------------------
FLROWA00  MATCH     SP70,EMWFIDE
          IF        @EQUAL
            MOVE      "&nbsp;",EMWFIDE
          ENDIF
.        
          MATCH     SP70,EMWFDES
          IF        @EQUAL
            MOVE      "&nbsp;",EMWFDES
          ENDIF
.        
          MATCH     SP70,EMWFSDE
          IF        @EQUAL
            MOVE      "&nbsp;",EMWFSDE
          ENDIF
.
          MOVE      "Yes",KEY3
          MATCH     "0",EMWFMAN
          IF        @EQUAL
            MOVE      "No ",KEY3
          ENDIF
.
          MOVE      "EZ",CATEGORY
          MOVE      EMWFRES,CODE
          CALL      GETCOD00
.
          MATCH     SP70,TCINDC1
          IF        @EQUAL
            MOVE      "&nbsp;",KEY6
          ELSE
            MATCH     "1",TCINDC1
            IF          @EQUAL
              MOVE      "Clerk",KEY6
            ELSE
              MATCH     "2",TCINDC1
              IF          @EQUAL
                MOVE      "Doctor",KEY6
              ELSE
                MATCH     "3",TCINDC1
                IF          @EQUAL
                  MOVE      "Nurse",KEY6
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
          MOVE      "None",KEY9
          MATCH     "1",EMWFMND
          IF        @EQUAL
            MOVE      "Discharge",KEY9
          ENDIF
.
          MOVE      "Yes",KEY4
          MATCH     "0",EMWFIDP
          IF        @EQUAL
            MOVE      "No ",KEY4
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td nowrap><a href='javascript:ShowDetail04(#"":
                             LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&emman001=",EMWFFLD,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>": 
                             EMWFFLD,"</td>":
                             "<td>",EMWFIDE,"</td>":
                             "<td>",EMWFDES,"</td>":
                             "<td>",EMWFSDE,"</td>":
                             "<td>",KEY9,"</td>":
                             "<td>",KEY3,"</td>":               
                             "<td>",KEY6,"</td>":
                             "<td>",KEY4,"</td>":
                             "</tr>"
.
FLROWA99 RETURN
.------------------------------------------------------------
. Emergency Location Maintenance  
.------------------------------------------------------------
EMRLOC00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      EMRLOC40 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      EMRLOC10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      EMRLOC20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      EMRLOC30 IF EQUAL
.
          GOTO      EMRLOC93
.
EMRLOC10  RESET     EMLOC001 
          PACK      KEY3,EMLOC001,SP70
          CALL      RDEMLOC1
          COMPARE   ZERO,OVRCD
          GOTO      EMRLOC92 IF EQUAL
.
          CALL      CHKLOC00      * Checks mandatory fields
          BRANCH    EXIT,EMRLOC99 
.
          CALL      CLEMRLHI               * Clear Location history fields
.
          MOVE      EMLOC001,EMLOLOCA      * Location Code
          MOVE      EMLOC003,EMLOSCOD      * Site Code 
          MOVE      EMLOC004,EMLOMAXP      * Max Number of Patients
          MOVE      EMLOC005,EMLOTYPE      * Room Type         (HPS01I)
          MOVE      EMLOC006,EMLOACTV      * Active Indicator
          MOVE      EMLOC007,EMLOSSIN      * Short Stay Unit Indicator
.
          CALL      WLHI0000       * Write data to Location history file
          BRANCH    EXIT,EMRLOC99
.
          CALL      LOCSAV00      * Moves input variables to file variables
.
          PACK      EMLOCDAT,CCC,CYY,CMM,CDD
          REP       " 0",EMLOCDAT             *  Date Record Created (CCYYMMDD)
          MOVE      CTIMEIS,EMLOCTIM          *  Time Record Created (HH:MM:SS)
          MOVE      USERID,EMLOCUID           *  WEB User ID Created 
.
          MOVE      SP70,EMLOUDAT             *  Update Date (CCYYMMDD)
          MOVE      SP70,EMLOUTIM             *  Update Time (HH:MM:SS)
          MOVE      SP70,EMLOUUID             *  WEB User ID Updated 
.
          MATCH     "1",EMLOACTV
          IF        @EQUAL
            MOVE      EMLOCDAT,EMLOIDAT       *  Inactive Date (CCYYMMDD)
            MOVE      EMLOCTIM,EMLOITIM       *  Inactive Time (HH:MM:SS)
            MOVE      EMLOCUID,EMLOIUID       *  WEB User ID Inactivate
          ELSE
            MOVE      SP70,EMLOIDAT           *  Inactive Date (CCYYMMDD)
            MOVE      SP70,EMLOITIM           *  Inactive Time (HH:MM:SS)
            MOVE      SP70,EMLOIUID           *  WEB User ID Inactivate 
          ENDIF
.
          PACK      KEY3,EMLOC001,SP70
          CALL      WREMLOC1      * Writes away the data
          CALL      HERA0000      * Add Hero location record
          MOVE      ZERO,EXIT
          GOTO      EMRLOC99
.
EMRLOC20  PACK      KEY3,EMLOC001,SP70
          CALL      RDEMLOC1
          BRANCH    OVRCD,EMRLOC91
.
          MATCH     "1",EMLOC006
          IF        @EQUAL
            PACK      KEY11,EMLOC001,SP70
            CALL      RSEMVIS7
EMRLOC25    CALL      RKEMVIS7
            IF        OVRCD=0
              MATCH     EMLOC001,EMVILOCN
              GOTO      EMRLOC27 IF NOT EQUAL
.
              COMPARE   ONE,EMVISTAT
              GOTO      EMRLOC25 IF NOT EQUAL
.
              MATCH     EMVISITE,WBSEESC
              GOTO      EMRLOC25 IF NOT EQUAL
.
              GOTO      EMRLOC94
.
            ENDIF  
          ENDIF
.
EMRLOC27  CALL      CHKLOC00      * Checks mandatory fields
          BRANCH    EXIT,EMRLOC99 
.
          CALL      RLEMLOC1       * Locks the record
.
          CALL      WLHI0000       * Write data to Location history file
          BRANCH    EXIT,EMRLOC99
.
          CALL      LOCSAV00       * Moves input variables to file variables
.
          PACK      EMLOUDAT,CCC,CYY,CMM,CDD
          REP       " 0",EMLOUDAT                *  Update Date (CCYYMMDD)
          MOVE      CTIMEIS,EMLOUTIM             *  Update Time (HH:MM:SS)
          MOVE      USERID,EMLOUUID              *  WEB User ID Updated
.
          MATCH     "1",EMLOACTV
          IF        @EQUAL
            MATCH     SP70,EMLOIDAT
            IF        @EQUAL
              MOVE      EMLOUDAT,EMLOIDAT        *  Inactive Date (CCYYMMDD)
              MOVE      EMLOUTIM,EMLOITIM        *  Inactive Time (HH:MM:SS)
              MOVE      EMLOUUID,EMLOIUID        *  WEB User ID Inactivate
            ENDIF
          ELSE
            MOVE      SP70,EMLOIDAT              *  Inactive Date (CCYYMMDD)
            MOVE      SP70,EMLOITIM              *  Inactive Time (HH:MM:SS)
            MOVE      SP70,EMLOIUID              *  WEB User ID Inactivate 
          ENDIF
.
          CALL      UPEMLOC1       * Writes away the data
          CALL      UUEMLOC1       * Unlocks the record
          CALL      HERU0000       * Update Hero location record
          MOVE      ZERO,EXIT
          GOTO      EMRLOC99
.
EMRLOC30  PACK      KEY3,EMLOC001,SP70
          CALL      RDEMLOC1
          BRANCH    OVRCD,EMRLOC91
.
          CALL      WLHI0000       * Write data to Location history file
          BRANCH    EXIT,EMRLOC99
.
          CALL      DEEMLOC1       * deletes away the data
.
          PACK      KEY7,TWO,EMLOLOCA,EMLOSCOD,SP70
          CALL      DEPMUHL1      * Delete HERO location id
.
          MOVE      ZERO,EXIT
          GOTO      EMRLOC99
.
EMRLOC40  PACK      KEY3,EMLOC001,SP70 
          CALL      RDEMLOC1
          IF        OVRCD=1
            CALL      CLRLOC00     * Clear all the file variables
          ENDIF
.
          PACK      KEY7,TWO,EMLOLOCA,EMLOSCOD,SP70
          CALL      RDPMUHL1
          IF        OVRCD=1
            UNPACK    SP70,PMULTYPE,PMULWDCD,PMULBDCD,PMULUHID
          ENDIF
.
          PACK      WEBTITLE,SP70,SP70
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,EMRLOC99
.
          CALL      WEBBOD00   * Process Web Body
.
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      EMRLOC99
.
EMRLOC91  MOVE      "Emergency Location record does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EMRLOC99
.
EMRLOC92  MOVE      "Emergency Location record exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EMRLOC99
.
EMRLOC93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EMRLOC99
.
EMRLOC94  MOVE      "Patient exist at this location. Cannot be made inactive.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EMRLOC99
.
EMRLOC99  RETURN
+
.------------------------------------------------------------------------------
. Write data to Location History file
.------------------------------------------------------------------------------
WLHI0000  PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE            * Current date
          MATCH     EMLHI001,CURRDATE
          GOTO      WLHI9000 IF LESS
.
          MOVE      EMLHI001,EMLHEFDT      * Effective Date
          MOVE      EMLHI002,EMLHEFTM      * Effective Time
          MOVE      ZERO,F1                * set flag to calculate previous date
          UNPACK    EMLHI001,CCENT,CYEAR,CMON,CDAY
          CALL      CDAT0000               * Calculate the previous Date
          MOVE      KEY8,EMLHEFDT
          REP       " 0",EMLHEFDT
.
          MOVE      EMLOC001,EMLHLOCA      * Location Code
          MOVE      EMLOSCOD,EMLHSCOD      * Site Code
          MOVE      EMLOMAXP,EMLHMAXP      * Maximum Number of patients
          MOVE      EMLOTYPE,EMLHOTYP      * Room Type
          MOVE      EMLOACTV,EMLHACTV      * Active
          MOVE      EMLOSSIN,EMLHSSIN      * Short stay indicator
.
          MATCH     "D",UPDTTYPE
          IF        @EQUAL
            MOVE      "1",EMLHDELE         * Deleted
          ENDIF
.
          PACK      KEY22,EMLHLOCA,EMLHSCOD,EMLHEFDT,EMLHEFTM
          CALL      RAEMLHI1
          IF        OVRCD=1
            MOVE      CURRDATE,EMLHCDAT      * System date
            CLOCK     TIME,EMLHCTIM          * System time
            MOVE      USERID,EMLHCUID        * web user id
            CALL      WREMLHI1
          ELSE
            CALL      UPEMLHI1
          ENDIF
          MOVE      ZERO,EXIT
          GOTO      WLHI9999
.
WLHI9000  MOVE      "Cannot Use A Future Date!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      WLHI9999
.
WLHI9999  RETURN
+
.------------------------------------------------------------------------------
. Add HERO location id record for ED location to PMSUHLFD
.------------------------------------------------------------------------------
HERA0000  MATCH     Z70,HEROLOCI
          GOTO      HERA9999 IF EQUAL
.
          MATCH     SP70,HEROLOCI
          GOTO      HERA9999 IF EQUAL
.
          PACK      KEY7,TWO,EMLOLOCA,EMLOSCOD,SP70
          CALL      RDPMUHL1
          IF        OVRCD=1
            MOVE      TWO,PMULTYPE
            PACK      PMULWDCD,EMLOLOCA,SP70
            PACK      PMULBDCD,EMLOSCOD,SP70
            PACK      PMULUHID,HEROLOCI,SP70
            PACK      PMULSPAR,SP70
            CALL      WRPMUHL1
          ENDIF
.
HERA9999  RETURN
.
.------------------------------------------------------------------------------
. Update HERO location id record for ED location to PMSUHLFD
.------------------------------------------------------------------------------
HERU0000  MATCH     Z70,HEROLOCI
          GOTO      HERU9999 IF EQUAL
.
          MATCH     SP70,HEROLOCI
          IF        @EQUAL
            PACK      KEY7,TWO,EMLOLOCA,EMLOSCOD,SP70
            CALL      DEPMUHL1
            GOTO      HERU9999
          ENDIF
.
          PACK      KEY7,TWO,EMLOLOCA,EMLOSCOD,SP70
          CALL      RDPMUHL1
          IF        OVRCD=1
            MOVE      TWO,PMULTYPE
            PACK      PMULWDCD,EMLOLOCA,SP70
            PACK      PMULBDCD,EMLOSCOD,SP70
            PACK      PMULUHID,HEROLOCI,SP70
            PACK      PMULSPAR,SP70
            CALL      WRPMUHL1
          ELSE
            PACK      PMULUHID,HEROLOCI,SP70
            CALL      UPPMUHL1
          ENDIF
.
HERU9999  RETURN

.------------------------------------------------------------
. * Checks mandatory fields
.------------------------------------------------------------
CHKLOC00  PACK      EMLOC001,EMLOC001,SP70
          MATCH     SP70,EMLOC001 
          GOTO      CHKLOC90 IF EQUAL
.
          PACK      EMLOC002,EMLOC002,SP70
          MATCH     SP70,EMLOC002 
          GOTO      CHKLOC91 IF EQUAL
.
          GOTO      CHKLOC99
.
CHKLOC90  MOVE      "Emergency Location Code is mandatory!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKLOC99
.
CHKLOC91  MOVE      "Emergency Location Description is mandatory!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKLOC99
.
CHKLOC99  RETURN
.------------------------------------------------------------
. Moves cgi variables into file variables
.------------------------------------------------------------
LOCSAV00  MOVE      EMLOC001,EMLOLOCA 
          MOVE      EMLOC002,EMLODESC
          MOVE      EMLOC003,EMLOSCOD
          MOVE      EMLOC004,EMLOMAXP
          MOVE      EMLOC005,EMLOTYPE                     * HPS01I
          MOVE      EMLOC006,EMLOACTV
          MOVE      EMLOC007,EMLOSSIN
.
LOCSAV99  RETURN
.------------------------------------------------------------
. Emergency Location  
.------------------------------------------------------------
ELOC0000  BRANCH    FLDITMNO,ELOC0100,ELOC0200,ELOC0300,ELOC0400,ELOC0500:
                             ELOC0600,ELOC0700,ELOC0800,ELOC0900,ELOC1000:
                             ELOC1100,ELOC1200,ELOC1300,ELOC1400,ELOC1500
          GOTO      ELOC9999
.
ELOC0100  WRITE     HTMLFILE;EMLOLOCA;
          GOTO      ELOC9999
.
ELOC0200  WRITE     HTMLFILE;EMLODESC;
          GOTO      ELOC9999
.
ELOC0300  CALL      LOCSIT00                * location Site code 
          GOTO      ELOC9999
.
ELOC0400  WRITE     HTMLFILE;EMLOMAXP;
          GOTO      ELOC9999
.
ELOC0500  CALL      LOCTAB00      * Table of Emergency locations
          GOTO      ELOC9999
.
ELOC0600  CALL      NEXTEM00      * Link to Next Group 
          GOTO      ELOC9999
.
ELOC0700  CALL      PREEMR00      * Link to Last Group   
          GOTO      ELOC9999
.
. HPS Code Start Here
ELOC0800  MATCH     SP70,EMLOTYPE
          IF        @EQUAL
            WRITE    HTMLFILE;"<option value=#" #" selected>":
                              "Emergency Map Room</option>"
          ELSE
            WRITE    HTMLFILE;"<option value=#" #">Emergency Map Room</option>"
          ENDIF
.
          MATCH     "O",EMLOTYPE
          IF        @EQUAL
            WRITE    HTMLFILE;"<option value=#"O#" selected>":
                                "Other Department</option>"
          ELSE
            WRITE    HTMLFILE;"<option value=#"O#">Other Department</option>"
          ENDIF
.
          MATCH     "W",EMLOTYPE
          IF        @EQUAL
            WRITE    HTMLFILE;"<option value=#"W#" selected>Waiting Room</option>"
          ELSE
            WRITE    HTMLFILE;"<option value=#"W#">Waiting Room</option>"
          ENDIF
.
          MATCH     "N",EMLOTYPE
          IF        @EQUAL
            WRITE    HTMLFILE;"<option value=#"N#" selected>Next Patient</option>"
          ELSE
            WRITE    HTMLFILE;"<option value=#"N#">Next Patient</option>"
          ENDIF
.
          MATCH     "E",EMLOTYPE
          IF        @EQUAL
            WRITE    HTMLFILE;"<option value=#"E#" selected>Emergency Telehealth Service</option>"
          ELSE
            WRITE    HTMLFILE;"<option value=#"E#">Emergency Telehealth Service</option>"
          ENDIF
.
          GOTO      ELOC9999  
. HPS Code Ends Here
.
ELOC0900  WRITE     HTMLFILE;EMLOACTV;
          GOTO      ELOC9999
.
ELOC1000  WRITE     HTMLFILE;EMLOSSIN;
          GOTO      ELOC9999
.
ELOC1100  WRITE     HTMLFILE;HEROLOCI;
          GOTO      ELOC9999
.
ELOC1200  WRITE     HTMLFILE;PMULUHID;
          GOTO      ELOC9999
.
ELOC1300  CALL      HLOC0000             * Display Location history
          GOTO      ELOC9999
.
ELOC1400  CALL      CHIS0000             * Check if any history record
          GOTO      ELOC9999
.
ELOC1500  IF        FHIRBUND=0
            CALL      RESWRD00
          ELSE
            CLEAR     KEY25
            APPEND    "EmergencyLocation-",KEY25
            APPEND    EMLOLOCA,KEY25
            APPEND    SP70,KEY25
            RESET     KEY25
.
            MATCH    SP70,EMLOLOCA
            IF       !@EQUAL & !@EOS
              PACK      KEY3,EMLOLOCA,SP70
              CALL      RDEMLOC1
              IF        OVRCD=0
                MOVE      SP70,DIM3
                PACK      KEY3,EMLOSCOD,SP70
                CALL      RDEMSIT1
                IF        OVRCD=0
                  MOVE      EMSTHSNO,DIM3
                ENDIF
.
                WRITE     HTMLFILE;*+,"{ #"fullUrl#" : #"%BASEURL/Location/",KEY25,"#", ":
                                  " #"resource#" : ";
                CALL      RESWRD00
                WRITE     HTMLFILE;*+," } "
              ENDIF
            ENDIF
          ENDIF
.
          GOTO      ELOC9999
.
ELOC9999  RETURN
.-----------------------------------------------------------
. Initialize's file data
.-----------------------------------------------------------
CLRLOC00  MOVE      SP70,EMLOLOCA
          MOVE      SP70,EMLODESC
          MOVE      SP70,EMLOSCOD
          MOVE      ZERO,EMLOMAXP
          MOVE      SP70,EMLOTYPE                    * HPS01I
          MOVE      SP70,EMLOACTV
          MOVE      SP70,EMLOCDAT
          MOVE      SP70,EMLOCTIM
          MOVE      SP70,EMLOCUID
          MOVE      SP70,EMLOUDAT
          MOVE      SP70,EMLOUTIM
          MOVE      SP70,EMLOUUID
          MOVE      SP70,EMLOIDAT
          MOVE      SP70,EMLOITIM
          MOVE      SP70,EMLOIUID
          MOVE      SP70,EMLOSPAR
.
CLRLOC99  RETURN
.------------------------------------------------------------
. Emergency Location's Table 
.------------------------------------------------------------
LOCTAB00  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          CALL      LOCHED00      * Output Table Heading
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY3,STARTKEY,SP70
          CALL      RSEMLOC1
          IF        OVRCD=0
            CALL      RPEMLOC1
          ENDIF
LOCTAB10  CALL      RKEMLOC1
          BRANCH    OVRCD,LOCTAB90
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      LOCTAB10
          ENDIF
.
          CALL      LOCROW00      * Display Emergency Security ID Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB    
          GOTO      LOCTAB10 IF NOT EQUAL
          GOTO      LOCTAB99
.
LOCTAB90  CALL      NOMORE00      * Dipslay No More Records Message
.
LOCTAB99  RETURN
.------------------------------------------------------------
. Location Maintenance (TABLE HEADING) 
.------------------------------------------------------------
LOCHED00  WRITE     HTMLFILE;"<tr>":
                               "<td class=HeadingCell width=20%>":
                               "Location Code</td>":
                               "<td class=HeadingCell width=30%>":
                               "Description</td>":
                               "<td class=HeadingCell>Site </td>":
                               "<td class=HeadingCell>Room Type</td>":
                               "<td class=HeadingCell>Max. No. Patients</td>":
                               "<td class=HeadingCell>Active</td>":
                               "<td class=HeadingCell>Short Stay Unit</td>":
                             "</tr>"
.
LOCHED99  RETURN
.------------------------------------------------------------
. Location Maintenance (TABLE ROW) 
.------------------------------------------------------------
LOCROW00  PACK      KEY3,EMLOSCOD,SP70
          CALL      RDEMSIT1
          IF        OVRCD=1
            MOVE      SP70,EMSTDESC
          ENDIF
.
. HPS Code Start Here
          MATCH     "O",EMLOTYPE
          IF          @EQUAL
            MOVE      "Other Department",KEY20
          ELSE
            MATCH     "W",EMLOTYPE
            IF        @EQUAL
              MOVE      "Waiting Room",KEY20
            ELSE
              MATCH     "N",EMLOTYPE
              IF        @EQUAL
                MOVE      "Next Patient",KEY20
              ELSE
                MATCH     "E",EMLOTYPE
                IF        @EQUAL
                  MOVE      "Emergency Telehealth Service",KEY20
                ELSE
                  MOVE      "Emergency Map Room",KEY20
                ENDIF
              ENDIF
            ENDIF
          ENDIF
. HPS Code Ends Here
.
          MATCH     "1",EMLOACTV
          IF        @EQUAL
            MOVE      "No",KEY3
          ELSE
            MOVE      "Yes",KEY3
          ENDIF
.
          MATCH     "1",EMLOSSIN
          IF        @EQUAL
            MOVE      "Yes",KEY3A
          ELSE
            MOVE      "No",KEY3A
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td nowrap><a href='javascript:ShowDetail05(#"":
                             LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&emloc001=",EMLOLOCA,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             EMLOLOCA,"</td>":
                             "<td>",EMLODESC,"</td>":
                             "<td>",EMSTDESC,"</td>":
                             "<td>",KEY20,"</td>":
                             "<td>",EMLOMAXP,"</td>":
                             "<td>",KEY3,"</td>":
                             "<td>",KEY3A,"</td>":
                             "</tr>"
.
LOCROW99  RETURN
.------------------------------------------------------------
.  Location Site code 
.  Parameter : 0=Description, 1=Code, 2=Selection List
.------------------------------------------------------------
LOCSIT00  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,LOCSIT10,LOCSIT20,LOCSIT40
.
          PACK      KEY3,EMLOSCOD,SP70
          CALL      RDEMSIT1
          IF        OVRCD=1
            MOVE      SP70,EMSTDESC
          ENDIF
.
          WRITE     HTMLFILE;EMSTDESC;
          GOTO      LOCSIT99
.
LOCSIT10  WRITE     HTMLFILE;EMLOSCOD;
          GOTO      LOCSIT99
.
LOCSIT20  PACK      KEY3,SP70
          CALL      RSEMSIT1
LOCSIT30  CALL      RKEMSIT1
          BRANCH    OVRCD,LOCSIT99
          MATCH     SP70,EMSTSCOD
          GOTO      LOCSIT30 IF EQUAL
          PACK      KEY10,QUOTE,EMSTSCOD,QUOTE
          MATCH     EMLOSCOD,EMSTSCOD
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=",KEY10," selected>":
                               EMSTDESC,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=",KEY10,">",EMSTDESC,"</option>"
          ENDIF
          GOTO      LOCSIT30
.
LOCSIT40  PACK      KEY3,SP70
          CALL      RSEMSIT1
LOCSIT50  CALL      RKEMSIT1
          BRANCH    OVRCD,LOCSIT99
          MATCH     SP70,EMSTSCOD
          GOTO      LOCSIT50 IF EQUAL
          PACK      KEY10,QUOTE,EMSTSCOD,QUOTE
          MATCH     WBSEESC,EMSTSCOD
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=",KEY10," selected>":
                               EMSTDESC,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=",KEY10,">",EMSTDESC,"</option>"
          ENDIF
          GOTO      LOCSIT50
.
LOCSIT99  RETURN
.------------------------------------------------------------
. Emergency Procedure Table Maintenance  
.------------------------------------------------------------
HLOC0000  PACK      KEY3,EMLOC001,SP70
          CALL      RDEMLOC1
          BRANCH    OVRCD,HLOC9999
.
          MOVE      EMLOACTV,SHACTIVE      * Save the current status
          MOVE      EMLOSCOD,SHSITECD      * save site code
          MOVE      EMLOTYPE,SHRMTYPE      * save room type
          MOVE      EMLOMAXP,SHNUMPAT      * number of patient
.
          PACK      KEY22,EMLOC001,Z70
          CALL      RSEMLHI1
HLOC1000  CALL      RPEMLHI1
          BRANCH    OVRCD,HLOC9999
.
          MATCH     EMLHLOCA,EMLOC001            * check location
          GOTO      HLOC9999 IF NOT EQUAL
.
          MATCH     SP8,EMLHEFDT
          GOTO      HLOC9999 IF EQUAL
.
          MOVE      ONE,F1                * set flag to calculate next date
          UNPACK    EMLHEFDT,CCENT,CYEAR,CMON,CDAY
          CALL      CDAT0000                 * Calculate the next Date
          UNPACK    KEY8,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
.
          MATCH     "1",SHACTIVE
          IF        @EQUAL
            MOVE      "No",ACTVDESC
          ELSE
            MOVE      "Yes",ACTVDESC              * active
          ENDIF
.
          PACK      KEY3,SHSITECD,SP70
          CALL      RDEMSIT1
          IF        OVRCD=1
            MOVE      SP70,EMSTDESC               * site code
          ENDIF
.
          MOVE      "Emergency Map Room",ROOMDESC
          MATCH     "O",SHRMTYPE
          IF        @EQUAL
            MOVE      "Other Department",ROOMDESC
          ELSE
            MATCH     "W",SHRMTYPE
            IF        @EQUAL
              MOVE      "Waiting Room",ROOMDESC
            ELSE
              MATCH     "N",SHRMTYPE
              IF        @EQUAL
                MOVE      "Next Patient",ROOMDESC
              ELSE
                MATCH     "E",SHRMTYPE
                IF        @EQUAL
                  MOVE      "Emergency Telehealth Service",ROOMDESC
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td>",CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR:
                               "</td><td>",ACTVDESC:
                               "</td><td>",EMSTDESC:
                               "</td><td>",ROOMDESC:
                               "</td><td>",SHNUMPAT,"</td></tr>"
.
          MOVE      EMLHACTV,SHACTIVE      * Save the current status
          MOVE      EMLHSCOD,SHSITECD      * save site code
          MOVE      EMLHOTYP,SHRMTYPE      * save room type
          MOVE      EMLHMAXP,SHNUMPAT      * save number of patient
.
          GOTO      HLOC1000
.
HLOC9999  RETURN
+
.------------------------------------------------------------------------
.          CHIS0000 - Check if there is any history record
.------------------------------------------------------------------------
CHIS0000  MOVE      ZERO,F1
          PACK      KEY22,EMLOC001,SP70
          CALL      RSEMLHI1
CHIS1000  CALL      RKEMLHI1
          BRANCH    OVRCD,CHIS9000
          MATCH     EMLHLOCA,EMLOC001      * Same location ?
          GOTO      CHIS9000 IF NOT EQUAL
.
          MOVE      ONE,F1
CHIS9000  WRITE     HTMLFILE;F1;
CHIS9999  RETURN
+
.------------------------------------------------------------------------
.          CDAT0000 - Calculate the next day
.------------------------------------------------------------------------
CDAT0000  MOVE      CDAY,DD              * These variables are
          MOVE      CMON,MM              * used by DMYCON
          MOVE      CYEAR,YY
          MOVE      CCENT,CC
          CALL      DMYCON               * Convert to Julian format
.
          IF        F1=1
            ADD       ONE,JULDAY         * next day
          ELSE
            SUB       ONE,JULDAY         * previous day
          ENDIF
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON               * Convert back to "normal" format
.
          MOVE      DD,CDAY
          MOVE      MM,CMON
          MOVE      YY,CYEAR
          MOVE      CC,CCENT
          PACK      KEY8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",KEY8
.
CDAT9999  RETURN
+
.------------------------------------------------------------
. Emergency Procedure Table Maintenance  
.------------------------------------------------------------
EMRPRC00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      EMRPRC40 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      EMRPRC10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      EMRPRC20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      EMRPRC30 IF EQUAL
.
          GOTO      EMRPRC93
.
EMRPRC10  CALL      ADDPRC00                * Add Procedure Code 
          GOTO      EMRPRC99
.
EMRPRC20  CALL      UPDPRC00                * Update Procedure Code 
          GOTO      EMRPRC99
.
EMRPRC30  PACK      KEY3,EMRPR001,SP70
          CALL      RDEMPRO1
          BRANCH    OVRCD,EMRPRC91
.
          CALL      DEEMPRO1                * deletes away the data
.
          MOVE      "emrproaf",MAPTBNAM
          MOVE      EMRPR001,MAPTBIND
          CALL      DELCSM00                * Delete Coding System Mappings
.
          MOVE      ZERO,EXIT
          GOTO      EMRPRC99
.
EMRPRC40  PACK      KEY9,EMRPR001,SP70 
          CALL      RDEMPRO1
          IF        OVRCD=1
            MOVE      SP70,EMPRCODE
            MOVE      SP70,EMPRDESC
            MOVE      SP70,EMPRHDPE
          ENDIF
.         
          PACK      WEBTITLE,SP70,SP70
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,EMRPRC99
.
          CALL      WEBBOD00   * Process Web Body
.
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      EMRPRC99
.
EMRPRC91  MOVE      "Emergency Procedure code does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EMRPRC99
.
EMRPRC93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EMRPRC99
.
EMRPRC99  RETURN
.------------------------------------------------------------
.  Add Procedure Code
.------------------------------------------------------------
ADDPRC00  RESET     EMRPR001               
          PACK      KEY9,EMRPR001,SP70
          CALL      RDEMPRO1 
          COMPARE   ZERO,OVRCD
          GOTO      ADDPRC92 IF EQUAL
.
          CALL      CHKPRC00      * Checks mandatory fields
          BRANCH    EXIT,ADDPRC99 
.
          MOVE      EMRPR001,EMPRCODE 
          MOVE      EMRPR002,EMPRDESC
          MOVE      EMRPR003,EMPRHDPE
.
          PACK      KEY9,EMPRCODE
          CALL      WREMPRO1      * Writes away the data
          MOVE      ZERO,EXIT
          GOTO      ADDPRC99
.
ADDPRC92  MOVE      "Emergency Procedure Code Already Exists.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ADDPRC99
.
ADDPRC99  RETURN
.------------------------------------------------------------
. Update Procedure Code
.------------------------------------------------------------
UPDPRC00  PACK      KEY9,EMRPR001,SP70
          CALL      RDEMPRO1
          BRANCH    OVRCD,UPDPRC91
.
          CALL      CHKPRC00      * Checks mandatory fields
          BRANCH    EXIT,UPDPRC99 
.
          CALL      RLEMPRO1       * Locks the record
.
          MOVE      EMRPR001,EMPRCODE 
          MOVE      EMRPR002,EMPRDESC
          MOVE      EMRPR003,EMPRHDPE
.
          CALL      UPEMPRO1       * Writes away the data
          CALL      ULEMPRO1       * Unlocks the record
.
          MOVE      ZERO,EXIT
          GOTO      UPDPRC99
.
UPDPRC91  MOVE      "Emergency Procedure code does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      UPDPRC99
.
UPDPRC99  RETURN
.------------------------------------------------------------
. Checks mandatory fields
.------------------------------------------------------------
CHKPRC00  PACK      EMRPR001,EMRPR001,SP70
          MATCH     SP70,EMRPR001 
          GOTO      CHKPRC90 IF EQUAL
.
          GOTO      CHKPRC99
.
CHKPRC90  MOVE      "Emergency Procedure Code is mandatory!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKPRC99
.
CHKPRC99  RETURN
.------------------------------------------------------------
. Emergency Procedure Maintenance Details
.------------------------------------------------------------
EMRP0000  BRANCH    FLDITMNO,EMRP0100,EMRP0200,EMRP0300,EMRP0400,EMRP0500:
                             EMRP0600,EMRP0700,EMRP0800
          GOTO      EMRP9999
.
EMRP0100  CALL      NEXTEM00      * Link to Next Group 
          GOTO      EMRP9999
.
EMRP0200  CALL      PREEMR00      * Link to Prev Group   
          GOTO      EMRP9999
.
EMRP0300  MOVE      ZERO,LNK2PRT 
          CALL      PRCTAB00      * Table of Emr Proc Codes with show details
          GOTO      EMRP9999
.
EMRP0400  WRITE     HTMLFILE;EMPRCODE;
          GOTO      EMRP9999
.
EMRP0500  WRITE     HTMLFILE;EMPRDESC;
          GOTO      EMRP9999
.
EMRP0600  WRITE     HTMLFILE;EMPRHDPE;
          GOTO      EMRP9999
.
EMRP0700  MOVE      ONE,LNK2PRT 
          CALL      PRCTAB00      * Table of Emr Proc Codes with update parent
          GOTO      EMRP9999
.
EMRP0800  MOVE      EMPRCODE,MAPTBIND
          CALL      VMTTAB00      * Clinical Coding System Mappings Table
          GOTO      EMRP9999
.
EMRP9999  RETURN
+
.------------------------------------------------------------
.  Table of Emergency Procedure Codes
.------------------------------------------------------------
PRCTAB00  IF        LNK2PRT<>1 
            UNPACK    DIM127,D1,LINKPRG,DIM127
            UNPACK    DIM127,D1,LINKREP,DIM127
            UNPACK    DIM127,D1,LINKTEM,DIM127
          ENDIF
.
          CALL      PRCHED00      * Output Table Heading
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY9,STARTKEY,SP70
          CALL      RSEMPRO1
          IF        OVRCD=0
            CALL      RPEMPRO1
          ENDIF
PRCTAB10  CALL      RKEMPRO1
          BRANCH    OVRCD,PRCTAB90
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      PRCTAB10
          ENDIF
.
          CALL      PRCROW00      * Display Emergency Security ID Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB    
          GOTO      PRCTAB10 IF NOT EQUAL
          GOTO      PRCTAB99
.
PRCTAB90  CALL      NOMORE00      * Display No More Records Message
.
PRCTAB99  RETURN
.------------------------------------------------------------
. Procedure Code Maintenance (TABLE HEADING) 
.------------------------------------------------------------
PRCHED00  WRITE     HTMLFILE;"<tr>":
                               "<td class=HeadingCell width=20%>":
                               "Procedure Code</td>":
                               "<td class=HeadingCell width=50%>":
                               "Description</td>":
                               "<td class=HeadingCell>HDP Equivalent </td>":
                             "</tr>"
.
PRCHED99  RETURN
.------------------------------------------------------------
. Procedure Maintenance (TABLE ROW) 
.------------------------------------------------------------
PRCROW00  PACK      KEY9,EMPRCODE,SP70
          CALL      RDEMPRO1
.
          BRANCH    LNK2PRT,PRCROW10
.
          WRITE     HTMLFILE;"<tr><td nowrap><a href='javascript:ShowDetail06(#"":
                             LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&emrpr001=",EMPRCODE,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             EMPRCODE,"</td>":
                             "<td>",EMPRDESC,"</td>":
                             "<td>",EMPRHDPE,"</td>":
                             "</tr>"
          GOTO      PRCROW99
.
PRCROW10 WRITE     HTMLFILE;"<tr><td nowrap>":
                            "<a href='javascript:updateParent(#"",EMPRDESC:
                            "#",#"",EMPRCODE,"#")'>":
                            "<img src=../images/MaintenanceIcon.gif hspace=4":
                            " border=0 align=absmiddle></a>":
                            EMPRCODE,"</td>":
                            "<td>",EMPRDESC,"</td>":
                            "<td>",EMPRHDPE,"</td>":
                            "</tr>"
.
PRCROW99  RETURN
.------------------------------------------------------------
. Emergency Diagnosis Table Maintenance  
.------------------------------------------------------------
EMRDIA00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      EMRDIA40 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      EMRDIA10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      EMRDIA20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      EMRDIA30 IF EQUAL
.
          GOTO      EMRDIA93
.
EMRDIA10  CALL      ADDDIA00                * Add Diagnosis Code 
          BRANCH    EXIT,EMRDIA99
          PROC      EMRICDUP                * Update Key Word Index
          GOTO      EMRDIA99
.
EMRDIA20  CALL      UPDDIA00                * Update Diagnosis Code 
          BRANCH    EXIT,EMRDIA99
          PROC      EMRICDUP                * Update Key Word Index
          GOTO      EMRDIA99
.
EMRDIA30  PACK      KEY9,EMRDI001,SP70
          CALL      RDEMICD1
          BRANCH    OVRCD,EMRDIA91
.
          CALL      DEEMICD1                * deletes away the data
.
          PROC      EMRICDUP                * Update Key Word Index
.
          MOVE      "emricdaf",MAPTBNAM
          MOVE      EMRDI001,MAPTBIND
          CALL      DELCSM00                * Delete Coding System Mappings
.
          MOVE      ZERO,EXIT
          GOTO      EMRDIA99
.
EMRDIA40  PACK      KEY9,EMRDI001,SP70
          CALL      RDEMICD1
          IF        OVRCD=1
            MOVE      SP70,EMIC9COD
            MOVE      SP70,EMICDESC
            MOVE      SP70,EMIC10CD
            MOVE      SP70,EMICACTV
            MOVE      SP70,EMICVEMD
            MOVE      SP70,EMICKEYW
            MOVE      SP70,EMICMETH
            MOVE      SP70,EMICSPAR
          ENDIF
.
          PACK      WEBTITLE,SP70,SP70
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,EMRDIA99
.
          CALL      WEBBOD00   * Process Web Body
.
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      EMRDIA99
.
EMRDIA91  MOVE      "Emergency Diagnosis code does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EMRDIA99
.
EMRDIA93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EMRDIA99
.
EMRDIA99  RETURN
.------------------------------------------------------------
. Emergency Diagnosis Maintenance Details
.------------------------------------------------------------
EMRD0000  BRANCH    FLDITMNO,EMRD0100,EMRD0200,EMRD0300,EMRD0400,EMRD0500:
                             EMRD0600,EMRD0700,EMRD0800,EMRD0900,EMRD1000:
                             EMRD1100,EMRD1200,EMRD1300,EMRD1400
          GOTO      EMRD9999
.
EMRD0100  CALL      NEXTDG00      * Link to Next Group 
          GOTO      EMRD9999
.
EMRD0200  CALL      PREVDG00      * Link to Prev Group   
          GOTO      EMRD9999
.
EMRD0300  MOVE      ZERO,LNK2PRT 
          CALL      SPTEDA00      * width link to display code details 
          GOTO      EMRD9999
.
EMRD0400  WRITE     HTMLFILE;EMIC9COD;
          GOTO      EMRD9999
.
EMRD0500  WRITE     HTMLFILE;EMICDESC;
          GOTO      EMRD9999
.
EMRD0600  WRITE     HTMLFILE;EMIC10CD;
          GOTO      EMRD9999
.
EMRD0700  WRITE     HTMLFILE;EMICACTV;
          GOTO      EMRD9999
.
EMRD0800  MOVE      ONE,LNK2PRT 
          CALL      SPTEDA00      * With link to update parent
          GOTO      EMRD9999
.
EMRD0900  CALL      SELPRT00
          GOTO      EMRD9999
.
EMRD1000  WRITE     HTMLFILE;EMICVEMD;
          GOTO      EMRD9999
.
EMRD1100  WRITE     HTMLFILE;EMICKEYW;
          GOTO      EMRD9999
.
EMRD1200  MOVE      EMIC9COD,MAPTBIND
          CALL      VMTTAB00      * Clinical Coding System Mappings Table
          GOTO      EMRD9999
.
EMRD1300  WRITE     HTMLFILE;EMICMETH;           * Methamphetamine Related
          GOTO      EMRD9999
.
EMRD1400  WRITE     HTMLFILE;ACTVONLY;
          GOTO      EMRD9999
.
EMRD9999  RETURN
+
.------------------------------------------------------------
.  Key Word Search of Emergency Diagnosis Codes
.------------------------------------------------------------
.------------------------------------------------------------
.  Key Word Search of Emergency Diagnosis Codes
.------------------------------------------------------------
SPTEDA00  WRITE     HTMLFILE;"<tr align=center>":
                               "<td class=HeadingCell colspan=5>":
                               "You Searched for ":
                               KEYWORDS,"</td>":
                             "</tr>":
                             "<tr>":
                               "<td class=HeadingCell>Diagnosis Code</td>":
                               "<td class=HeadingCell>Description</td>";
          IF        LNK2PRT<>1
            WRITE     HTMLFILE;"<td class=HeadingCell>Active</td>";
          ENDIF
.
          WRITE     HTMLFILE;"<td class=HeadingCell>HDP Code</td>":
                             "</tr>"
.
          CALL      GETWRD00       * This routine Splits the KEYWORDS field
.                                  * into KEYWRD01 to KEYWRD10
.
          IF        LNK2PRT<>1
            UNPACK    DIM127,D1,LINKPRG,DIM127
            UNPACK    DIM127,D1,LINKREP,DIM127
            UNPACK    DIM127,D1,LINKTEM,DIM127
          ELSE
            UNPACK    DIM127,D1,LINKTYPE,DIM127
          ENDIF
.
..     Insert HTML Table Heading Here
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
          MOVE      ZERO,DIAAREND
.
          STRIP     KEYWRD01
          PACK      KEY24,KEYWRD01,SP70
          CALL      RSEMKED2
SPTEDA10  CALL      RKEMKED2
          BRANCH    OVRCD,SPTEDA90
.
          PACK      KEY9,EMEDITM
          CALL      RDEMICD1
          BRANCH    OVRCD,SPTEDA10
.
          CALL      SPTEDB00
          BRANCH    OVRCD,SPTEDA10,SPTEDA90
.
          IF        LNK2PRT=1
            MATCH     "1",ACTVONLY
            IF        @EQUAL
              COMPARE   ZERO,EMICACTV
              GOTO      SPTEDA10 IF NOT EQUAL
            ELSE
              COMPARE   ONE,EMICACTV
              GOTO      SPTEDA10 IF EQUAL
            ENDIF` 
          ENDIF
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      SPTEDA10
          ENDIF
.
..  Insert Table Row Details in Here.
..  This includes reading any other information needed from alternate files.
.
          IF        LNK2PRT<>1
            COMPARE   ONE,EMICACTV
            IF        @EQUAL
              MOVE      "No",ACTVFLAG
            ELSE
              MOVE      "Yes",ACTVFLAG
            ENDIF
          ENDIF
.
          MATCH     SP70,EMICVEMD
          IF        @EQUAL
            MOVE      "&nbsp",EMICVEMD
          ENDIF
.
          BRANCH    LNK2PRT,SPTEDA20
.
          WRITE     HTMLFILE;"<tr><td nowrap><a href='javascript:ShowDetail(#"":
                             LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&emrdi001=",EMIC9COD,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             EMIC9COD,"</td>":
                             "<td>",EMICDESC,"</td>":
                             "<td>",ACTVFLAG,"</td>":
                             "<td>",EMICVEMD,"</td>":
                             "</tr>"
          GOTO      SPTEDA30
.
SPTEDA20  MATCH     "1",LINKTYPE
          IF        @EQUAL
            WRITE     HTMLFILE;"<tr><td nowrap>":
                             "<a href='javascript:updateParent(#"",EMICDESC:
                             "#",#"",EMIC9COD,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             EMIC9COD,"</td>":
                             "<td>",EMICDESC,"</td>":
                             "<td>",EMICVEMD,"</td>":
                             "</tr>"
          ELSE
            WRITE     HTMLFILE;"<tr><td nowrap>":
                             "<a href='javascript:updateParent(#"",EMICDESC:
                             "#",#"",EMIC9COD,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             EMIC9COD,"</td>":
                             "<td>",EMICDESC,"</td>":
                             "<td>",EMICVEMD,"</td>":
                             "</tr>"
          ENDIF
.
SPTEDA30  ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      SPTEDA10 IF NOT EQUAL
          GOTO      SPTEDA99
.
SPTEDA90  WRITE     HTMLFILE;"</table>"
          CALL      NOMORE00       * Display No More Records Message
.
SPTEDA99  RETURN
.------------------------------------------------------------
. Check Record
.------------------------------------------------------------
SPTEDB00  MATCH     KEYWRD01,EMEDKWD
          GOTO      SPTEDB92 IF NOT EQUAL
          IF        KEYWRDNO>1
            PACK      SAVKEY24,EMEDKWD,EMEDITM
            CALL      SPTEDC00
            BRANCH    EXIT,SPTEDB91
            MOVE      SAVKEY24,KEY24
            CALL      RDEMKED2
          ENDIF
.
. Insert Other Check for Valid Record Here i.e. Suspended or In Active
.   eg    COMPARE    ONE,INACTIVE
.         GOTO       SPTEDB91 IF EQUAL
.
          GOTO      SPTEDB90
.
SPTEDB90  CALL      CDIA0000
          BRANCH    EXIT,SPTEDB91
.
          MOVE      ZERO,OVRCD
          GOTO      SPTEDB99
SPTEDB91  MOVE      ONE,OVRCD
          GOTO      SPTEDB99
SPTEDB92  MOVE      TWO,OVRCD
SPTEDB99  RETURN
.------------------------------------------------------------
. Check for Matching Words
.------------------------------------------------------------
SPTEDC00  MOVE      ONE,F2
          MOVE      ZERO,EXIT
          PACK      SAVKEY9,EMEDITM,SP70
.
SPTEDC10  COMPARE   KEYWRDNO,F2           * Loop through each Key Word in Search
          GOTO      SPTEDC99 IF EQUAL     * Exit OK
          ADD       ONE,F2
          LOAD      KEYWRDXX,F2,KEYWRD01,KEYWRD02,KEYWRD03,KEYWRD04,KEYWRD05:
                             KEYWRD06,KEYWRD07,KEYWRD08,KEYWRD09,KEYWRD10
          PACK      KEY24,EMEDITM,KEYWRDXX
          CALL      RDEMKED1              * Check for Exact Match
          COMPARE   ZERO,OVRCD            * Ok so Continue Loop
          GOTO      SPTEDC10 IF EQUAL
.
          CALL      RKEMKED1              * Not Exact so Read Next Record
          BRANCH    OVRCD,SPTEDC90        * No Match So Exit No Match
          PACK      KEY9,EMEDITM,SP70
          MATCH     SAVKEY9,KEY9        * Match Key Fields
          GOTO      SPTEDC90 IF NOT EQUAL * No Match So Exit
          STRIP     KEYWRDXX       
          MATCH     EMEDKWD,KEYWRDXX         * Check Key Word
          GOTO      SPTEDC90 IF NOT EQUAL * No Match So Exit
          GOTO      SPTEDC10              * Check Next Search Word
.
SPTEDC90  MOVE      ONE,EXIT
SPTEDC99  RETURN
.------------------------------------------------------------
.DIATAB00  UNPACK    DIM127,D1,LINKPRG,DIM127
.          UNPACK    DIM127,D1,LINKREP,DIM127
.          UNPACK    DIM127,D1,LINKTEM,DIM127
..
.          CALL      DIAHED00      * Output Table Heading
..
.          IF        NORECORD=0
.            MOVE      "13",NORECORD       * Set Default No Records to Display
.          ENDIF
.          MOVE      ZERO,RECNUMB
.          MOVE      ZERO,DISNUMB
..
.          PACK      KEY9,SP70
.          CALL      RSEMICD1
.DIATAB10  CALL      RKEMICD1
.          BRANCH    OVRCD,DIATAB99
..
.          ADD       ONE,RECNUMB
.          IF        STARTNUM>=RECNUMB
.            GOTO      DIATAB10
.          ENDIF
..
..          CALL      DIAROW00      * Display Emergency Security ID Row
.          ADD       ONE,DISNUMB
.          COMPARE   NORECORD,DISNUMB    
.          GOTO      DIATAB10 IF NOT EQUAL
..
.DIATAB99  RETURN
..------------------------------------------------------------
.. Diagnosis Code Maintenance (TABLE HEADING) 
..------------------------------------------------------------
.DIAHED00  WRITE     HTMLFILE;"<tr>":
.                               "<td class=HeadingCell width=20%>":
.                               "Diagnosis Code</td>":
.                               "<td class=HeadingCell width=40%>":
.                               "Description</td>":
.                               "<td>ICD10 Code </td>":
.                               "<td> Active </td>":
.                             "</tr>"
..
.DIAHED99  RETURN
..------------------------------------------------------------
.. Diagnosis Maintenance (TABLE ROW) 
..------------------------------------------------------------
.DIAROW00  PACK      KEY9,EMIC9COD,SP70
.          CALL      RDEMICD1
..
.          WRITE     HTMLFILE;"<tr><td nowrap><a href='javascript:ShowDetail(#"":
.                             LINKPRG,"#.pbl":
.                             "?reportno=",LINKREP:
.                             "&template=",LINKTEM:
.                             "&emrdi001=",EMIC9COD,"#")'>":
.                             "<img src=../images/MaintenanceIcon.gif hspace=4":
.                             " border=0 align=absmiddle></a>":
..                             EMIC9COD,"</td>":
.                             "<td>",EMICDESC,"</td>":
.                             "<td>",EMIC10CD,"</td>":
.                             "<td>",EMICACTV,"</td>":
.                             "</tr>"
...
.DIAROW99  RETURN
.------------------------------------------------------------
.  Add Diagnosis Code
.------------------------------------------------------------
ADDDIA00  RESET     EMRDI001               
          PACK      KEY9,EMRDI001,SP70
          CALL      RDEMICD1 
          COMPARE   ZERO,OVRCD
          GOTO      ADDDIA92 IF EQUAL
.
          RESET     EMRDI003               
          PACK      KEY9,EMRDI003,SP70
          CALL      RDEMICD3 
          COMPARE   ZERO,OVRCD
          GOTO      ADDDIA93 IF EQUAL
.
          CALL      CHKDIA00      * Checks mandatory fields
          BRANCH    EXIT,ADDDIA99 
.
          MOVE      EMRDI001,EMIC9COD 
          MOVE      EMRDI002,EMICDESC
          MOVE      EMRDI003,EMIC10CD
          MATCH     SP70,EMRDI004
          IF        @EQUAL
            MOVE      ONE,EMRDI004
          ENDIF
          MOVE      EMRDI004,EMICACTV
          MOVE      EMRDI005,EMICVEMD
          MOVE      EMRDI006,EMICKEYW
          MOVE      EMRDI007,EMICMETH
.
          PACK      KEY9,EMIC9COD
          CALL      WREMICD1      * Writes away the data
.
          MOVE      ZERO,EXIT
          GOTO      ADDDIA99
.
ADDDIA92  MOVE      "Emergency Diagnosis Code Already Exists.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ADDDIA99
.
ADDDIA93  MOVE      "Emergency ICD10 Diagnosis Code Already Exists.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ADDDIA99
.
ADDDIA99  RETURN
.------------------------------------------------------------
. Update Diagnosis Code
.------------------------------------------------------------
UPDDIA00  PACK      KEY9,EMRDI001,SP70
          CALL      RDEMICD1
          BRANCH    OVRCD,UPDDIA91
.
..          RESET     EMRDI003               * commented out 29/12/1999
..          PACK      KEY9,EMRDI003,SP70     * by NMH - Dont think this is 
..          CALL      RDEMICD3               * necessary for update
..          IF        OVRCD = 0 
..            GOTO      UPDDIA92 
..          ENDIF
.
          CALL      CHKDIA00      * Checks mandatory fields
          BRANCH    EXIT,UPDDIA99 
.
          PACK      KEY9,EMRDI001,SP70
          CALL      RLEMICD1       * Locks the record
.
          MOVE      EMRDI001,EMIC9COD 
          MOVE      EMRDI002,EMICDESC
          MOVE      EMRDI003,EMIC10CD
          MATCH     SP70,EMRDI004
          IF        @EQUAL
            MOVE      ONE,EMRDI004
          ENDIF
          MOVE      EMRDI004,EMICACTV
          MOVE      EMRDI005,EMICVEMD
          MOVE      EMRDI006,EMICKEYW
          MOVE      EMRDI007,EMICMETH
.
          CALL      UPEMICD1       * Writes away the data
          CALL      UUEMICD1       * Unlocks the record
          MOVE      ZERO,EXIT
          GOTO      UPDDIA99
.
UPDDIA91  MOVE      "Emergency Diagnosis code does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      UPDDIA99
.
UPDDIA92  MOVE      "Emergency ICD10 Diagnosis Code Already Exists.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      UPDDIA99
.
UPDDIA99  RETURN
.------------------------------------------------------------
. Checks mandatory fields
.------------------------------------------------------------
CHKDIA00  PACK      EMRDI001,EMRDI001,SP70
          MATCH     SP70,EMRDI001 
          GOTO      CHKDIA90 IF EQUAL
.
          GOTO      CHKDIA99
.
CHKDIA90  MOVE      "Emergency Diagnosis Code is mandatory!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKDIA99
.
CHKDIA99  RETURN
.------------------------------------------------------------
.  Validate Emergency Procedure & Diagnosis Codes
.------------------------------------------------------------
VALCOD00  MOVE      SP70,VALDNAME
          MATCH     SP70,VALDCODE
          GOTO      VALCOD99 IF EQUAL
.
          MOVE      "Invalid Emergency Code Entered",VALDNAME
          BRANCH    VALDTYPE,VALCOD10,VALCOD20,VALCOD30,VALCOD40
          MOVE      "Invalid Emergency Validation Type",VALDNAME
          GOTO      VALCOD90
.
VALCOD10  PACK      KEY9,VALDCODE
          CALL      RDEMPRO1
          IF        OVRCD=0
            MOVE      EMPRDESC,VALDNAME
          ENDIF
          GOTO      VALCOD90
.
.  By Diagnosis Code (EMR)
.
VALCOD20  PACK      KEY9,VALDCODE,SP70
          CALL      RDEMICD1
          IF        OVRCD=0
            MOVE      EMICDESC,VALDNAME
          ENDIF
          GOTO      VALCOD90
.
.  By HDP Code (VEMD)
.
VALCOD30  PACK      KEY9,VALDCODE,SP70
          CALL      RDEMICD1
          BRANCH    OVRCD,VALCOD90
.
          PACK      VALDCODE,VALDCODE,SP70
          MOVE      EMICDESC,VALDNAME

          GOTO      VALCOD90
.
.  By Diagnosis Code (EMR) - Active Only
.
VALCOD40  PACK      KEY9,VALDCODE,SP70
          CALL      RDEMICD1
          IF        OVRCD=0
            COMPARE   ONE,EMICACTV
            GOTO      VALCOD90 IF EQUAL 
            MOVE      EMICDESC,VALDNAME
          ENDIF
          GOTO      VALCOD90
.
VALCOD90  CALL      OPENHTML
          BRANCH    EXIT,VALCOD99
          WRITE     HTMLFILE;"<h3 align=center>Checking Database</h3>":
                             "<script type=#"text/javascript#">":
                             "{ opener.ValidateName.value=#"",VALDNAME,"#";":
                             "  opener.ValidateCode.value=#"",VALDCODE,"#";"
.
          MATCH     "Invalid",VALDNAME
          IF        @EQUAL
            WRITE     HTMLFILE;" opener.ValidateCode.value=#"","#";"
            WRITE     HTMLFILE;" opener.ValidateName.value=#"","#";"
            WRITE     HTMLFILE;" opener."
            WRITE     HTMLFILE;"alert(#"Invalid Diagnosis Code Entered#");";
          ENDIF
.
          WRITE     HTMLFILE;"  close(); } </script>"
          CALL      CLOSHTML
.
VALCOD99  RETURN
.------------------------------------------------------------
.         EMRREG00  Body Region Maintenance
.------------------------------------------------------------
EMRREG00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      EMRREG40 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      EMRREG10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      EMRREG20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      EMRREG30 IF EQUAL
.
          GOTO      EMRUPD93
.
EMRREG10  RESET     EMREG001
          PACK      KEY2,EMREG001,SP70
          CALL      RDEMGRC1
          COMPARE   ZERO,OVRCD
          GOTO      EMRREG92 IF EQUAL
          CALL      CHKREG00      * Checks mandatory fields 
          BRANCH    EXIT,EMRREG99
          CALL      REGSAV00      * Moves input variables to file vars 
          BRANCH    EXIT,EMRREG99
          PACK      KEY2,EMREG001,SP70
          CALL      WREMGRC1      * Writes away the data
          MOVE      ZERO,EXIT
          GOTO      EMRREG99
.
EMRREG20  PACK      KEY2,EMREG001,SP70
          CALL      RDEMGRC1
          BRANCH    OVRCD,EMRREG91
          CALL      CHKREG00      * Checks mandatory fields 
          BRANCH    EXIT,EMRREG99
          CALL      RLEMGRC1      * Locks the record 
          CALL      REGSAV00      * Moves input variables to file vars 
          CALL      UPEMGRC1      * Writes away the data
          CALL      UUEMGRC1      * Unlocks the record 
          MOVE      ZERO,EXIT
          GOTO      EMRREG99
.
EMRREG30  PACK      KEY2,EMREG001,SP70
          CALL      RDEMGRC1
          BRANCH    OVRCD,EMRREG91
          CALL      DEEMGRC1       * Writes away the data
          MOVE      ZERO,EXIT
          GOTO      EMRREG99
.
EMRREG40  PACK      KEY2,EMREG001,SP70
          CALL      RDEMGRC1
          IF        OVRCD=1
            CALL      CLRREG00     * Clear all the file variables 
          ENDIF
.
          PACK      WEBTITLE,SP70,SP70
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,EMRREG99
.
          CALL      WEBBOD00   * Process Web Body
.
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      EMRREG99
.
EMRREG91  MOVE      "Body Region record does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EMRREG99
.
EMRREG92  MOVE      "Body Region record exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EMRREG99
.
EMRREG93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EMRREG99
.
EMRREG99  RETURN
.------------------------------------------------------------
.         Check mandatory body region fields
.------------------------------------------------------------
CHKREG00  RESET     EMREG001
          MATCH     SP70,EMREG001 * Update Type not set report an error
          GOTO      CHKREG90 IF EQUAL
.
          GOTO      CHKREG99
.
CHKREG90  MOVE      "Body Region Code is mandatory!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKREG99
.
CHKREG99  RETURN
.------------------------------------------------------------
. Moves cgi variables into file variables
.------------------------------------------------------------
REGSAV00  MOVE      EMREG001,EMRCCODE
          MOVE      EMREG002,EMRCDESC
          MOVE      SP70,EMUTSPA
          MOVE      ZERO,EXIT
.
          RESET     EMREG003
          MOVE      EMREG003,F1
          COMPARE   ZERO,F1
          IF        @EQUAL
            MOVE      " ",EMRCFLAG
            GOTO      REGSAV99
          ENDIF
.         
          COMPARE   TWO,F1
          IF        @EQUAL
            MOVE      ANSN,EMRCFLAG
            GOTO      REGSAV99
          ENDIF
.
          COMPARE   ONE,F1
          IF        @EQUAL
            MOVE      ANSF,EMRCFLAG
            GOTO      REGSAV99
          ENDIF
.
          MOVE      "Invalid Foreign Body Flag",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
.
REGSAV99  RETURN
.-----------------------------------------------------------
. Initialize's file data
.-----------------------------------------------------------
CLRREG00  MOVE      SP70,EMRCCODE
          MOVE      SP70,EMRCDESC
          MOVE      SP70,EMRCFLAG
          MOVE      SP70,EMRCSPAR
.
CLRREG99  RETURN
.-------------------------------------------------------------------------------
.   Set Parameters for Body Region Table
.-------------------------------------------------------------------------------
STREG000  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,STREG001,STREG002,STREG003
.
          MOVE      ONE,EXIT
          GOTO      STREG999
.
STREG001  MOVE      QRYDATA,EMREG001
          GOTO      STREG999
STREG002  MOVE      QRYDATA,EMREG002
          GOTO      STREG999
STREG003  MOVE      QRYDATA,EMREG003
          GOTO      STREG999
.
STREG999  RETURN
.------------------------------------------------------------
. Body Region
.------------------------------------------------------------
BREG0000  BRANCH    FLDITMNO,BREG0100,BREG0200,BREG0300,BREG0400,BREG0500
          GOTO      BREG9999
.
BREG0100  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,BREG0110,BREG0120      * 0=description,1=code,2=select
.
          PACK      KEY2,EMRCCODE,SP70
          CALL      RDEMGRC1
          BRANCH    OVRCD,BREG9999
.
          WRITE     HTMLFILE;EMRCDESC;
          GOTO      BREG9999
.
BREG0110  MATCH     SP70,EMRCCODE
          GOTO      BREG9999 IF EQUAL
          WRITE     HTMLFILE;EMRCCODE;
          GOTO      BREG9999
.
BREG0120  CALL      REGN0000
          GOTO      BREG9999
.
.
BREG0200  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,BREG0210
.
          WRITE     HTMLFILE;EMRCFLAG;
          GOTO      BREG9999
.
BREG0210  MATCH     ANSN,EMRCFLAG
          IF        @EQUAL
            WRITE     HTMLFILE;TWO;
            GOTO      BREG9999
          ENDIF
.
          MATCH     ANSF,EMRCFLAG
          IF        @EQUAL
            WRITE     HTMLFILE;ONE;
          ELSE
            WRITE     HTMLFILE;ZERO;
          ENDIF
          GOTO      BREG9999
.
BREG0300  CALL      BRTAB000      * Table of Body Regions   
          GOTO      BREG9999
.
BREG0400  CALL      NEXTEM00      * Link to Next Group
          GOTO      BREG9999
.
BREG0500  CALL      PREEMR00      * Link to Last Group
          GOTO      BREG9999
.
BREG9999  RETURN
.
.------------------------------------------------------------
.         REGN0000  Display  body region code selection list
.------------------------------------------------------------
REGN0000  PACK      KEY2,SP70
          CALL      RSEMGRC1
REGN0010  CALL      RKEMGRC1
          BRANCH    OVRCD,REGN9999
          WRITE     HTMLFILE;"<option value=#"",EMRCCODE,"#"> ":
                             EMRCCODE,": ",EMRCDESC,"</option>"
          GOTO      REGN0010
.
REGN9999  RETURN
.------------------------------------------------------------
.         BRTAB000  Body Region Table
.------------------------------------------------------------
BRTAB000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          CALL      BRHED000      * Output Table Heading
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY2,STARTKEY,SP70
          CALL      RDEMGRC1
          IF        OVRCD=0
            CALL      RPEMGRC1
          ENDIF
BRTAB100  CALL      RKEMGRC1
          BRANCH    OVRCD,BRTAB900
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      BRTAB100
          ENDIF
.
          CALL      BROWA000      * Display Emergency Security ID Row
.
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      BRTAB100 IF NOT EQUAL
          GOTO      BRTAB999
.
BRTAB900  CALL      NOMORE00      * Display No More Records Message
.
BRTAB999  RETURN
.------------------------------------------------------------
. Body Region (TABLE HEADING)
.------------------------------------------------------------
BRHED000  WRITE     HTMLFILE;"<tr>":
                               "<td class=HeadingCell width=20%>":
                               "Body Region Code</td>":
                               "<td class=HeadingCell width=50%>":
                               "Description</td>":
                               "<td class=HeadingCell width=30%>":
                               "Foreign Body</td>":
                             "</tr>"
.
BRHED999  RETURN
.------------------------------------------------------------
.------------------------------------------------------------
BROWA000  MATCH     SP70,EMRCDESC
          IF        @EQUAL
            MOVE      "&nbsp;",EMRCDESC
          ENDIF
.
          MATCH     ANSF,EMRCFLAG
          IF        @EQUAL
            MOVE      "Yes",KEY3
            GOTO      BROWA1000
          ENDIF
.
          MATCH     ANSN,EMRCFLAG
          IF        @EQUAL
            MOVE      "No",KEY3
            GOTO      BROWA1000
          ENDIF
.
          MOVE      "N/A",KEY3
.
BROWA100  WRITE     HTMLFILE;"<tr><td nowrap><a href='javascript:ShowDetail09(#"":
                             LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&emreg001=",EMRCCODE,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             EMRCCODE,"</td>":
                             "<td>",EMRCDESC,"</td>":
                             "<td>",KEY3,"</td>":
                             "</tr>"
.
BROWA999 RETURN
.------------------------------------------------------------
. Default Request Location
.------------------------------------------------------------
RCRQ0000  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
.
          BRANCH    F1,RCRQ0350
.
          PACK      KEY4,SP70
          CALL      RDMRLOC1
RCRQ0310  CALL      RKMRLOC1
          BRANCH    OVRCD,RCRQ9999
          COMPARE   MRLOSTAT,ONE
          IF        @EQUAL
            GOTO      RCRQ0310
          ENDIF
          PACK      KEY15,QUOTE,MRLOCODE,SP5,QUOTE
          MATCH     MRLOCODE,EMSTDRLC
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=",KEY15," selected>":
                               MRLODESC,"</option>"
          ELSE
            IF        IBCNMHOS=1
              MATCH     WBSEHOSP,MRLOHOSP
              GOTO      RCRQ0310 IF NOT EQUAL
            ENDIF
            WRITE     HTMLFILE;"<option value=",KEY15,">",MRLODESC,"</option>"
          ENDIF
          GOTO      RCRQ0310
.
RCRQ0350  PACK      KEY34,SP70
          CALL      RDMRLOC3
RCRQ0360  CALL      RKMRLOC3
          BRANCH    OVRCD,RCRQ9999
          COMPARE   MRLOSTAT,ONE
          IF        @EQUAL
            GOTO      RCRQ0360
          ENDIF
          PACK      KEY15,QUOTE,MRLOCODE,QUOTE
          MATCH     MRLOCODE,EMSTDRLC
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=",KEY15,"selected>":
                               MRLODESC,"</option>"
          ELSE
            IF        IBCNMHOS=1
              MATCH     WBSEHOSP,MRLOHOSP
              GOTO      RCRQ0360 IF NOT EQUAL
            ENDIF
            WRITE     HTMLFILE;"<option value=",KEY15,">",MRLODESC,"</option>"
          ENDIF
          GOTO      RCRQ0360
.
RCRQ9999  RETURN
.------------------------------------------------------------------
.            Selection List of Movment Reasons
. Parameters .1=Bulk Request Reasons
.------------------------------------------------------------------
MOVREA00  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1   * Bulk Reasons Flag
.
          PACK      KEY4,SP70               * Selection List of Resions
          CALL      RSMRMOV1
MOVREA10  CALL      RKMRMOV1
          BRANCH    OVRCD,MOVREA99
.
          COMPARE   ONE,MRMOSTAT            * Active?
          GOTO      MOVREA10 IF EQUAL
.
          IF        F1 = ONE
            COMPARE   TWO,MRMOUSAG
            GOTO      MOVREA10 IF NOT EQUAL
          ENDIF
.
          PACK      KEY30,QUOTE,MRMOREAS,SP5,QUOTE
          MATCH     MRMOREAS,EMSTDRES
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=",KEY30,"selected>":
                               MRMODESC,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=",KEY30,">",MRMODESC,"</option>"
          ENDIF
          GOTO      MOVREA10
.
MOVREA99  RETURN
.------------------------------------------------------------
. Check if this diagnosis has already been output in the search
.------------------------------------------------------------
CDIA0000  MOVE       ZERO,F3
          MOVE       ZERO,EXIT
.
CDIA1000  ADD        ONE,F3
          IF         F3>DIAAREND
            IF         F3>200
              GOTO       CDIA9999
            ENDIF
            MOVE       F3,DIAAREND
            MOVE       EMEDITM,DIAARRAY[F3]
            GOTO       CDIA9999
          ENDIF
.
          MATCH      EMEDITM,DIAARRAY[F3]
          GOTO       CDIA1000 IF NOT EQUAL
.
          MOVE       ONE,EXIT
.
CDIA9999  RETURN
.------------------------------------------------------------
.   Template Services
.------------------------------------------------------------
EMRTEM00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      EMRTEM50 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      EMRTEM10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      EMRTEM20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      EMRTEM30 IF EQUAL
.
          MATCH     "C",UPDTTYPE    * Copy formaction
          GOTO      EMRTEM40 IF EQUAL
.
          GOTO      EMRTEM93
.
.         ************ ADD FORMACTION ***********
.
EMRTEM10  PACK      KEY3,DEPTCODE,SP70
          CALL      RDEMSIT1                      * Read department table
          BRANCH    OVRCD,EMRTEM94
          MOVE      EMSTSCOD,DEPARTMN             * Save Department Variable
.
          PACK      KEY16,DEPARTMN,ALTMP002,ALTMP003,ALTMP004,SP70
          CALL      RAALTMP1
          COMPARE   ZERO,OVRCD
          GOTO      EMRTEM92 IF EQUAL
          CALL      SAVTEM00                     * Move cgi to file variables

          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          MOVE      KEY8,ALTMCDAT                * System date
          CLOCK     TIME,ALTMCTIM                * System time (Var chgd HPS)
          MOVE      USERID,ALTMCUID              * web user id
          MOVE      SP70,ALTMUDAT
          MOVE      SP70,ALTMUTIM
          MOVE      SP70,ALTMUUID
          MOVE      ONE,ALTMTYRC                 * alldepaf record type
          PACK      KEY16,DEPARTMN,ALTMP002,ALTMP003,ALTMP004,SP70
          CALL      WRALTMP1                     * Writes the data
.
          MOVE      ZERO,EXIT
          GOTO      EMRTEM99
.
.         ************ UPDATE FORMACTION **********
.
EMRTEM20  PACK      KEY3,DEPTCODE,SP70
          CALL      RDEMSIT1                      * Read department table
          BRANCH    OVRCD,EMRTEM94
          MOVE      EMSTSCOD,DEPARTMN             * Save Department Variable
.
          PACK      KEY16,DEPARTMN,ALTMP002,ALTMP003,ALTMP004,SP70
          CALL      RDALTMP1
          BRANCH    OVRCD,EMRTEM91
          CALL      SAVTEM00                     * Moves cgi to file variables
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          MOVE      KEY8,ALTMUDAT                * System date
          CLOCK     TIME,ALTMUTIM                * System time (Var chgd HPS)
          MOVE      USERID,ALTMUUID              * web user id
          PACK      KEY16,DEPARTMN,ALTMP002,ALTMP003,ALTMP004,SP70
          CALL      UPALTMP1                     * Updates the data
.
          MOVE      ZERO,EXIT
          GOTO      EMRTEM99
.
.         ************ DELETE FORMACTION **********
.
EMRTEM30  PACK      KEY3,DEPTCODE,SP70
          CALL      RDEMSIT1                      * Read department table
          BRANCH    OVRCD,EMRTEM94
          MOVE      EMSTSCOD,DEPARTMN             * Save Department Variable
.
          PACK      KEY16,DEPARTMN,ALTMP002,ALTMP003,ALTMP004,SP70
          CALL      RDALTMP1
          BRANCH    OVRCD,EMRTEM91
          CALL      DEALTMP1                     * Deletes the data
          MOVE      ZERO,EXIT
          GOTO      EMRTEM99
.
.         ************ COPY FORMACTION **********
.
EMRTEM40  CALL      CTEM0000                      * Copy diagnosis
.
          MOVE      ZERO,EXIT
          GOTO      EMRTEM99
.
.
.         ************ DISPLAY FORMACTION **********
.
EMRTEM50  PACK      KEY3,DEPTCODE,SP70
          CALL      RDEMSIT1                      * Read department table
          BRANCH    OVRCD,EMRTEM94
          MOVE      EMSTSCOD,DEPARTMN             * Save Department Variable
.
          PACK      KEY16,DEPARTMN,ALTMP002,ALTMP003,ALTMP004,SP70
.          PACK      KEY16,DEPARTMN,SP70
          CALL      RDALTMP1                      * Read service
          IF        OVRCD=1
            CALL      CLRTEM00                    * Clear all the file variables
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Template Services Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,EMRTEM99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      EMRTEM99
.
EMRTEM91  MOVE      "Map from Server/Report/Template Does Not Exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EMRTEM99
.
EMRTEM92  MOVE      "Template already mapped to some other Template.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EMRTEM99
.
EMRTEM93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EMRTEM99
.
EMRTEM94  MOVE      "Invalid Department Code",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EMRTEM99
.
EMRTEM99  RETURN
.
.------------------------------------------------------------
. Copy template details to this department
.------------------------------------------------------------
CTEM0000  PACK      KEY16,COPYDEPT,SP70
          CALL      RSALTMP1
CTEM1000  CALL      RKALTMP1
          BRANCH    OVRCD,CTEM9999
.
          MATCH     COPYDEPT,ALTMDEPT
          GOTO      CTEM9999 IF NOT EQUAL
.
          PACK      SAVKEY16,ALTMDEPT,ALTMMPSV,ALTMMPRP,ALTMMPTP,SP70
.
          MOVE      DEPTCODE,ALTMDEPT
.
          PACK      KEY16,ALTMDEPT,ALTMMPSV,ALTMMPRP,ALTMMPTP,SP70
          CALL      RAALTMP1
          IF        OVRCD=1
            CALL      WRALTMP1
          ENDIF
.
          MOVE      SAVKEY16,KEY16
          CALL      RDALTMP1
.
          GOTO      CTEM1000
.
CTEM9999  RETURN
.
.-----------------------------------------------------------
. move cgi variables into file variables Template
.-----------------------------------------------------------
SAVTEM00  MOVE      DEPTCODE,ALTMDEPT
.
          MATCH     SP70,ALTMP002
          IF        !@EQUAL
            MOVE      ALTMP002,ALTMMPSV
          ENDIF
.
          MATCH     SP70,ALTMP003
          IF        !@EQUAL
            MOVE      ALTMP003,ALTMMPRP
          ENDIF
.
          MATCH     SP70,ALTMP004
          IF        !@EQUAL
            MOVE      ALTMP004,ALTMMPTP
          ENDIF
.
          MATCH     SP70,ALTMP005
          IF        !@EQUAL
            MOVE      ALTMP005,ALTMMPTT
          ENDIF
.
          MOVE      ALTMP006,ALTMDESC
.
SAVTEM99  RETURN
.
.-----------------------------------------------------------
. Initialize's file data  (Template Detail)
.-----------------------------------------------------------
CLRTEM00  MOVE      SP70,ALTMDEPT
          MOVE      SP70,ALTMMPSV
          MOVE      SP70,ALTMMPRP
          MOVE      SP70,ALTMMPTP
          MOVE      SP70,ALTMMPTT
          MOVE      SP70,ALTMDESC
          MOVE      SP70,ALTMCDAT
          MOVE      SP70,ALTMCTIM
          MOVE      SP70,ALTMCUID
          MOVE      SP70,ALTMUDAT
          MOVE      SP70,ALTMUTIM
          MOVE      SP70,ALTMUUID
          MOVE      SP70,ALTMTYRC
.
CLRTEM99  RETURN
.
.--------------------------------------------------------------------
.  Set Parameters for Option 6: Allied Health Template Maintenance
.--------------------------------------------------------------------
STTEM000  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,STTEM001,STTEM002,STTEM003,STTEM004,STTEM005:
                       STTEM006
.
          MOVE      ONE,EXIT
          GOTO      STTEM999
.
STTEM001  MOVE      QRYDATA,ALTMP001
          PACK      ALTMP001,ALTMP001,SP70
          GOTO      STTEM999
.
STTEM002  MOVE      QRYDATA,ALTMP002
          PACK      ALTMP002,ALTMP002,SP70
          GOTO      STTEM999
.
STTEM003  MOVE      QRYDATA,ALTMP003
          PACK      ALTMP003,ALTMP003,SP70
          GOTO      STTEM999
.
STTEM004  MOVE      QRYDATA,ALTMP004
          PACK      ALTMP004,ALTMP004,SP70
          GOTO      STTEM999
.
STTEM005  MOVE      QRYDATA,ALTMP005
          GOTO      STTEM999
.
STTEM006  MOVE      QRYDATA,ALTMP006
          GOTO      STTEM999
.
STTEM999  RETURN
.
.------------------------------------------------------------
. Emr Template Mapping - Option 11 Template Details
.------------------------------------------------------------
ALTP0000  BRANCH    FLDITMNO,ALTP0001,ALTP0002,ALTP0003,ALTP0004:
                             ALTP0005,ALTP0006,ALTP0007,ALTP0008:
                             ALTP0009,ALTP0010,ALTP0011,ALTP0012:
                             ALTP0013,ALTP0014,ALTP0015,ALTP0016:
                             ALTP0017
.
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      ALTP9999
.
ALTP0001  MOVE      "CG",CATEGORY           * Department Code
          MOVE      DEPARTMN,CODE
          CALL      CODITM00
          GOTO      ALTP9999
.
ALTP0002  WRITE     HTMLFILE;ALTMMPSV;
          GOTO      ALTP9999
.
ALTP0003  WRITE     HTMLFILE;ALTMMPRP;
          GOTO      ALTP9999
.
ALTP0004  WRITE     HTMLFILE;ALTMMPTP;
          GOTO      ALTP9999
.
ALTP0005  WRITE     HTMLFILE;ALTMMPTT;
          GOTO      ALTP9999
.
ALTP0006  WRITE     HTMLFILE;ALTMDESC;
          GOTO      ALTP9999
.
ALTP0007  WRITE     HTMLFILE;ALTMCDAT;
          GOTO      ALTP9999
.
ALTP0008  WRITE     HTMLFILE;ALTMCTIM;
          GOTO      ALTP9999
.
ALTP0009  WRITE     HTMLFILE;ALTMCUID;
          GOTO      ALTP9999
.
ALTP0010  WRITE     HTMLFILE;ALTMUDAT;
          GOTO      ALTP9999
.
ALTP0011  WRITE     HTMLFILE;ALTMUTIM;
          GOTO      ALTP9999
.
ALTP0012  WRITE     HTMLFILE;ALTMUUID;
          GOTO      ALTP9999
.
ALTP0013  CALL      TMTAB000
          GOTO      ALTP9999
.
ALTP0014  CALL      NEXTGT00
          GOTO      ALTP9999
.
ALTP0015  CALL      PREVGT00
          GOTO      ALTP9999
.
ALTP0016  PACK      KEY3,DEPARTMN,SP70
          CALL      RDEMSIT1
          BRANCH    OVRCD,ALTP9999
.
          WRITE     HTMLFILE;EMSTDESC;
          GOTO      ALTP9999
.
ALTP0017  CALL      LOCSI200
          GOTO      ALTP9999
.
ALTP9999  RETURN
.------------------------------------------------------------
. Table of Emergency Template Maintenance : Report 11
.------------------------------------------------------------
TMTAB000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
.          PACK      KEY16,DEPTCODE,ALTMP002,ALTMP003,ALTMP004,SP70
          PACK      KEY16,DEPTCODE,SP70     * HPS(BLR) Added
          CALL      RSALTMP1
          CALL      RPALTMP1
.
TMTAB100  CALL      RKALTMP1
          BRANCH    OVRCD,TMTAB900
.
          MATCH     DEPTCODE,ALTMDEPT
.          GOTO      TMTAB100 IF NOT EQUAL
          GOTO      TMTAB900 IF NOT EQUAL
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      TMTAB100
          ENDIF
.
          MATCH     "1",ALTMTYRC            * Check if emr record type
          GOTO      TMTAB100 IF NOT EQUAL
.
          CALL      TMROW000                * Display Template Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      TMTAB100 IF NOT EQUAL
          GOTO      TMTAB999
.
TMTAB900  CALL      NOMORE00      * Display No More Records Message
.
TMTAB999  RETURN
.------------------------------------------------------------
.  Allied Health Template (TABLE ROW)
.------------------------------------------------------------
TMROW000  MATCH     SP70,ALTMDESC
          IF        @EQUAL
            MOVE      "&nbsp;",ALTMDESC
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td nowrap><a href='javascript:ShowDetail":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&deptcode=",ALTMDEPT:
                             "&altmp002=",ALTMMPSV:
                             "&altmp003=",ALTMMPRP:
                             "&altmp004=",ALTMMPTP,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             ALTMMPSV,"</td>":
                             "<td>",ALTMMPRP,"</td>":
                             "<td align=center>",ALTMMPTP,"</td>":
                             "<td align=center>",ALTMMPTT,"</td>":
                             "<td align=center>",ALTMDESC,"</td>":
                             "</tr>"
.
TMROW999 RETURN
.
.-----------------------------------------------------------
. Link to Next Group option 11
.-----------------------------------------------------------
NEXTGT00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F2
.
.          REP       " +",ALTMDEPT          * HPS(BLR) Comment
          REP       " +",DEPTCODE           * HPS(BLR) Added
          REP       " +",ALTMMPSV
          REP       " +",ALTMMPRP
          REP       " +",ALTMMPTP
.
          WRITE     HTMLFILE;"emrweb05.pbl":
                                 "?reportno=",F2:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
.                                 "&deptcode=",ALTMDEPT:    * HPS(BLR) Comment
                                 "&deptcode=",DEPTCODE:     * HPS(BLR) Added
                                 "&altmp002=",ALTMMPSV:
                                 "&altmp003=",ALTMMPRP:
                                 "&altmp004=",ALTMMPTP;
.
.          REP       "+ ",ALTMDEPT          * HPS(BLR) Comment
          REP       "+ ",DEPTCODE           * HPS(BLR) Added
          REP       "+ ",ALTMMPSV
          REP       "+ ",ALTMMPRP
          REP       "+ ",ALTMMPTP
.
NEXTGT99  RETURN
.
.-------------------------------------------------------------
. Link to Prev Group option 11
.-------------------------------------------------------------
PREVGT00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3
          IF        F3<0
            MOVE      ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F2
.
.          REP       " +",ALTMDEPT          * HPS(BLR) Comment
          REP       " +",DEPTCODE           * HPS(BLR) Added
          REP       " +",ALTMMPSV
          REP       " +",ALTMMPRP
          REP       " +",ALTMMPTP
.
          WRITE     HTMLFILE;"emrweb05.pbl":
                                 "?reportno=",F2:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
.                                 "&deptcode=",ALTMDEPT:
                                 "&deptcode=",DEPTCODE:
                                 "&altmp002=",ALTMMPSV:
                                 "&altmp003=",ALTMMPRP:
                                 "&altmp004=",ALTMMPTP;
.
.          REP       "+ ",ALTMDEPT          * HPS(BLR) Comment
          REP       "+ ",DEPTCODE           * HPS(BLR) Added
          REP       "+ ",ALTMMPSV
          REP       "+ ",ALTMMPRP
          REP       "+ ",ALTMMPTP
.
PREVGT99  RETURN
.------------------------------------------------------------
. NOMORE00 - Display "No more records" at end of list
.------------------------------------------------------------
NOMORE00  WRITE     HTMLFILE;"<tr>":
                             "<td class=RedCell colspan=20 align=center>":
                             "No more records</td>"
.
NOMORE99  RETURN
.

.------------------------------------------------------------
.  Location Site code
.  Parameter : 0=Description, 1=Code, 2=Selection List
.------------------------------------------------------------
LOCSI200  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,LOCSI210,LOCSI220
.
          PACK      KEY3,DEPTCODE,SP70
          CALL      RDEMSIT1
          IF        OVRCD=1
            MOVE      SP70,EMSTDESC
          ENDIF
.
          WRITE     HTMLFILE;EMSTDESC;
          GOTO      LOCSI299
.
LOCSI210  WRITE     HTMLFILE;DEPTCODE;
          GOTO      LOCSI299
.
LOCSI220  PACK      KEY3,SP70
          CALL      RSEMSIT1
LOCSI230  CALL      RKEMSIT1
          BRANCH    OVRCD,LOCSI299
          MATCH     SP70,EMSTSCOD
          GOTO      LOCSI230 IF EQUAL
          PACK      KEY10,QUOTE,EMSTSCOD,QUOTE
          MATCH     DEPTCODE,EMSTSCOD
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=",KEY10," selected>":
                               EMSTDESC,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=",KEY10,">",EMSTDESC,"</option>"
          ENDIF
          GOTO      LOCSI230
.
LOCSI299  RETURN
+
.------------------------------------------------------------------------------
.         Validate Emergency Procedure & Diagnosis Codes (Remote Script)
.------------------------------------------------------------------------------
VALEPD00  CALL      XMLHED00
          BRANCH    EXIT,VALEPD99
.
          MOVE      SP70,VALDNAME
.
          MATCH     SP70,VALDCODE
          GOTO      VALEPD99 IF EQUAL
.
.                            Proc     Diag     HDP VEMD
          BRANCH    VALDTYPE,VALEPD10,VALEPD20,VALEPD30
          GOTO      VALEPD91
.
.
.  By Procedure Code (EMR)
.
VALEPD10  PACK      KEY9,VALDCODE,SP70
          CALL      RDEMPRO1
          BRANCH    OVRCD,VALEPD92
.
          MOVE      EMPRDESC,VALDNAME
          GOTO      VALEPD90
.
.  By Diagnosis Code (EMR)
.
VALEPD20  PACK      KEY9,VALDCODE,SP70
          CALL      RDEMICD1
          BRANCH    OVRCD,VALEPD93
.
          COMPARE   "0",EMICACTV
          IF        !@EQUAL
            COMPARE   "1",EMICACTV
            IF        @EQUAL
              GOTO      VALEPD95
            ELSE
              GOTO      VALEPD96
            ENDIF
          ENDIF      
.
          MOVE      EMICDESC,VALDNAME
          GOTO      VALEPD90
.
.  By Diagnosis Code - no longer by EMICVEMD as per VALCOD30 (TSK 0814124)
.
VALEPD30  PACK      KEY9,VALDCODE,SP70
          CALL      RDEMICD1
          BRANCH    OVRCD,VALEPD93
.
          PACK      VALDCODE,VALDCODE,SP70
          MOVE      EMICDESC,VALDNAME
          GOTO      VALEPD90
.
VALEPD90  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>":
                             VALDNAME,"|",VALDCODE:
                             "</RETURN_VALUE>"
          GOTO      VALEPD98
.
VALEPD91  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=ERROR>":
                             "Invalid Validation Type":
                             "</RETURN_VALUE>"
          GOTO      VALEPD98
.
VALEPD92  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=ERROR>":
                             "Invalid Procedure Code Entered":
                             "</RETURN_VALUE>"
          GOTO      VALEPD98
.
VALEPD93  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=ERROR>":
                             "Invalid Diagnosis Code Entered":
                             "</RETURN_VALUE>"
          GOTO      VALEPD98
.
VALEPD94  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=ERROR>":
                             "Invalid Diagnosis Code (VEMD) Entered":
                             "</RETURN_VALUE>"
          GOTO      VALEPD98
.
VALEPD95  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=ERROR>":
                             VALDCODE," - Diagnosis Code Inactive.":
                             "</RETURN_VALUE>"
          GOTO      VALEPD98
.
VALEPD96  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=ERROR>":
                             VALDCODE," - Diagnosis Code Active - exclude":
                             " from ACC (NZ only)": 
                             "</RETURN_VALUE>"
          GOTO      VALEPD98
.
VALEPD98  CALL      XMLEND00
.
VALEPD99  RETURN
+
RESLOC00  STRIP     PTHSNAME
          STRIP     PTHSHOSP
          CLEAR     KEY16
          APPEND    "Hospital-",KEY16
          APPEND    PTHSHOSP,KEY16
          APPEND    SP70,KEY16
          RESET     KEY16
          STRIP     KEY16
          STRIP     WBTPFIL  *  Template
          WRITE     HTMLFILE;*+," { #"resourceType#": #"Location#",":
                    "   #"id#": #"",KEY16,"#",":
                    " #"extension#": [":
                    " { #"url#":#"%BASEURL/extension/webPASService#"":
                        " ,#"valueString#":#"",PRGID,SP1,PRGNAM,SP1,VERSION,"#"},":
                    " { #"url#":#"%BASEURL/extension/webPASTemplate#"":
                        " ,#"valueString#":#"%TEMPLATEHOME/",WBTPFIL," %TEMPLATEVERSION#"}":
                    " ],":
                    " #"identifier#": {":
                    "   #"value#": #"",KEY16,"#"":
                    " },";
          MATCH     SP70,PTHSTELE
          IF        !@EQUAL
             WRITE     HTMLFILE;*+," #"telecom#": [":
                       "{ #"system#": #"phone#",":
                       "  #"value#": #"",PTHSTELE,"#" }";
             MATCH    SP70,PTHSFAXN
             IF       @EQUAL
               WRITE    HTMLFILE;*+,"],";
             ELSE
               WRITE    HTMLFILE;*+,",";
             ENDIF
          ENDIF
          MATCH     SP70,PTHSFAXN
          IF        !@EQUAL
             MATCH     SP70,PTHSTELE
             IF        @EQUAL
               WRITE     HTMLFILE;*+," #"telecom#": [";
             ENDIF
             WRITE     HTMLFILE;*+,"{ #"system#": #"fax#",":
                       "  #"value#": #"",PTHSFAXN,"#" } ],";
          ENDIF
          WRITE     HTMLFILE;*+," #"name#": #"",PTHSNAME,"#", ":
                    " #"physicalType#": {":
                    "  #"coding#": [ {":
                    "   #"system#": #"http://hl7.org/fhir/location-physical-type#",":
                    "   #"code#": #"si#",":
                    "   #"display#": #"Site#"  } ] }":
                    "  } ";
          RETURN
+
.------------------------------------------------------------
.
          INC       IBAWEBCD
. 
          INC       ALLTMPIO/INC
          INC       EMRDOCIO/INC
          INC       EMRDKIIO/INC
          INC       EMRGRCIO/INC
          INC       EMRICDIO/INC
          INC       EMRUSEIO/INC
          INC       EMRSITIO/INC
          INC       EMRLOCIO/INC
          INC       EMRLHIIO/INC
          INC       EMRPROIO/INC
          INC       EMRUTYIO/INC   
          INC       EMRWFMIO/INC
          INC       EMRVISIO/INC
          INC       PATDO1IO/INC
          INC       EMRKEDIO/INC
          INC       MRTMOVIO/INC
          INC       MRTLOCIO/INC
          INC       PMSHCPIO/INC
          INC       PMSUHLIO/INC 
          INC       PMSVMTIO/INC
          INC       PMSVSCIO/INC
          INC       PMSVSTIO/INC
          INC       PATHSPIO/INC
.
          INC       CLEMRLHI
          INC       EMRICDPR                * Update Key Word Index
          INC       EMRDKIPR                * Updates keyword index
          INC       GENSNDEX          * Generate Soundex key routine *C-0299206
          INC       PATDOCTM
          INC       PMSVSTTM
          INC       PMSVMTTM
          INC       NAMSTRCD
          INC       DAYOFWEK
          INC       WEBDATCD
          INC       WEBRSHCD

 

.******************************************************************************
.* System         : Outpatients                                               *
.* Program        : IBAOUT30.PBL                                              *
.* Name           : Patients Attended But Not Invoiced                        *
.******************************************************************************
.* Date           : 02/08/1996                                                *
.* Author         : Junior                                                    *
.* Function       : Print the patients attended but not invoiced.             *
.* Modifications  :                                                           *
.*        V12.00.01 23/05/2025  Nikitha B      TSK 0955096                    *
.*                  Alphanumeric changes.                                     *
.*        V9.03.01  23/10/2003  J.Tan     CAR   44172                         *
.*                  Mods for Misc.Theatre item file (pmsmtiaf)                *
.*        V9.02.01  28/04/2003  Dean Cramer    CAR 38200                      *
.*                  Allign heading to data on report                          *
.*        V5.08.01  29/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.04.01  08/04/1997  Greg Horvat      SRF 619527                   *
.*                  Changed to print attended patients not invoiced correctly *
.*        V5.04.00  16/11/1996  Greg Horvat      SRF 617218                   *
.*                  Ported from IBAOUT30, V5.02                               *
.*                                                                            *
.******************************************************************************
+
          INC       STD001FD
.
          INC       OUTBOAFD/INC            * Clinic session booking file A
          INC       OUTBB1FD/INC            * Clinic session booking file B
          INC       OUTDTRFD/INC            * DTR fileion file
          INC       PMSMTIFD/INC            * Misc.Theatre item file
          INC       PATCODFD/INC            * Codes file
          INC       PATINVFD/INC            * Invoice file
          INC       PATMA1FD/INC            * Master file 
          INC       PATVISFD/INC            * Visit file
.
. ----- Program variables -----
.
ENDDTE8   DIM       8                       * End date (ccyymmdd)
GRNDTOTL  FORM      11.2                    * Grand total
INVTOTAL  FORM      10.2                    * Invoice total
PATNAME   DIM       40                      * Patient name
RECCOUNT  FORM      4                       * Record counter
STRTDTE8  DIM       8                       * Start date (ccyymmdd)
.
. ----- Program constants -----
.
PRGID     INIT      "IBAOUT30"
PRGNAM    INIT      "Patients Attended But Not Invoiced"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                  ML0000                                    *
.*                                Main Module                                 *
.******************************************************************************
ML0000    CALL      INIT0000                * Initialise variables & open files
ML1000    CALL      OPTN0000                * Select option
          BRANCH    EXIT,ML9999 
.
ML2000    CALL      DATE0000                * Keyin date range
          BRANCH    EXIT,ML1000
.
          CALL      CONTQST                 * Ok to continue (Y/N/C) ?
          BRANCH    CEXIT,ML3000,ML2000,ML1000
.
ML3000    CALL      PROC0000                * Process the files
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                  INIT0000              Called by: ML0000   *
.*                      Initialise Variables & Open Files                     *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen heading
.
          PACK      CFNAME,UID,FILBB1A2
          DISPLAY   *P56:24,"Opening ",CFNAME;
          CALL      OPOTBB12
.
          PACK      CFNAME,UID,FILDTRO2
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTDTRO1,CFNAME
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patinvrf";
          OPEN      PATINVR3,"patinvrf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA1,"patvisaf"
.
          DISPLAY   *P64:24,"pmsmtiaf";
          OPEN      PMSMTIA1,"pmsmtiaf"
.
          MOVE      ONE,CNOUNDLN            * No report underline
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000              Called by: ML0000   *
.*                                Select Option                               *
.******************************************************************************
OPTN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Print Report":
                    *P1:7,"Select Option : ";
.
OPTN1000  MOVE      ZERO,OPTION
          KEYIN     *P17:7,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN9000
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                  DATE0000              Called by: ML0000   *
.*                            Keyin The Date Range                            *
.******************************************************************************
DATE0000  DISPLAY   *P1:9,*EF,"Start date : ":
                    *P1:10,*EF,"End date   : ";
.
DATE1000  MOVE      SP8,STRTDTE8
          UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          MOVE      "9",CVERT
          MOVE      "14",CCOL
          CALL      KEYDATE                 * Keyin the start date
          BRANCH    OVRCD,DATE9500
.
          PACK      STRTDTE8,CCENT,CYEAR,CMON,CDAY 
          REP       " 0",STRTDTE8
.
          MOVE      SP8,ENDDTE8
          MOVE      "10",CVERT
          MOVE      "14",CCOL
          CALL      KEYDATE                 * Keyin the end date
          BRANCH    OVRCD,DATE1000
.
          PACK      ENDDTE8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDTE8
.
          MATCH     STRTDTE8,ENDDTE8
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"End date cannot be before the start ":
                      "date.  ";
            CALL      HITENTER
            GOTO      DATE0000
          ENDIF
.
DATE9000  MOVE      ZERO,EXIT
          GOTO      DATE9999
.
DATE9500  MOVE      ONE,EXIT
DATE9999  RETURN
+
.******************************************************************************
.*                                  PROC0000              Called by: ML0000   *
.*                              Process The Files                             *
.******************************************************************************
PROC0000  MOVE      ZERO,CPAGENO            * Reset page no
          CALL      HEAD0000                * Print the report heading
.
          DISPLAY   *P1:24,*EL,"Scanning : ",*P35:24,"Printing : ";
          MOVE      ZERO,GRNDTOTL           * Clear the grand total
          MOVE      ZERO,RECCOUNT           * Clear the record counter
.
          PACK      KEY16,STRTDTE8,SP8
          CALL      RDSBOKB2                * Position on & read the clinic
PROC1000  CALL      RDKBOKB2                  session B file
          BRANCH    OVRCD,PROC5000 
.
          MATCH     OBADATE,ENDDTE8
          GOTO      PROC5000 IF LESS        * End date < clinic session date
.
          UNPACK    OBADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format the clinic date
          DISPLAY   *P12:24,*V2LON,OBAOUTNO,SP2,CPCDATE;
.
          PACK      KEY8,OBAOUTNO
          CALL      RDVISA1                 * Read the visit file
          BRANCH    OVRCD,PROC1000 
.
          PACK      KEY16,OBAOUTNO,SP20
          CALL      RDSINV3                 * Position on & read the invoice
          CALL      RDKINV3                   file
          BRANCH    OVRCD,PROC2000
.
          MATCH     OBAOUTNO,PINVADM
          GOTO      PROC1000 IF EQUAL       * Same outpatient no, get next
.
PROC2000  PACK      KEY8,PVIURNO
          CALL      RDMAST1                 * Read the master file
          BRANCH    OVRCD,PROC1000
.
          PACK      KEY5,ANSC,ANSL,OBACOMP
          CALL      RDCODE1                 * Read the codes file
          BRANCH    OVRCD,PROC1000
.
          MOVE      ZERO,INVTOTAL           * Clear the invoice total
          PACK      KEY14,OBAOUTNO,SP20
          CALL      RSPMMTI1                * Position on & read the DTR file
PROC3000  CALL      RKPMMTI1
          BRANCH    OVRCD,PROC4000
.
          MOVE      OBAOUTNO,DOBAOUTN
          MATCH     PMMIVISN,DOBAOUTN
          GOTO      PROC4000 IF NOT EQUAL   * Different outpatient no
.
          ADD       PMMIAMTT,INVTOTAL       * Increment the invoice total
          GOTO      PROC3000
.
PROC4000  CALL      PRNT0000                * Print invoice details
          ADD       INVTOTAL,GRNDTOTL       * Increment the grand total
          ADD       ONE,RECCOUNT            * record counter
          GOTO      PROC1000
. 
PROC5000  PRINT     *N:
                    *10,"Number of records : ",RECCOUNT:
                    *92,"Grand total : $",GRNDTOTL,*N,*N:
                    *5,"Patients who have been invoiced & have additional ":
                    "charges waiting to be invoiced are not included on this ":
                    "report.",*N,*N:
                    *1,"*** End of Report ***"
          DISPLAY   *P1:24,*EL,"Report Completed.  ";
          CALL      HITENTER
.
PROC9000  MOVE      ZERO,EXIT
PROC9999  RETURN
+
.******************************************************************************
.*                                  PRNT0000              Called by: PROC0000 *
.*                            Print Invoice Details                           *
.******************************************************************************
PRNT0000  UNPACK    OBADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format the clinic date
.
          PACK      PATNAME,SP30,SP30
          MOVE      PTITL,PACTITLE
          MOVE      PGNAME,PACGNAME
          MOVE      PSNAME,PACSNAME
          MOVE      TWO,PACFRMT
          CALL      PACNAME                 * Format the patient name
          MOVE      PACFNAME,PATNAME        * Patient name
.
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS    * Print the report heading
.
          PRINT     *N;
          PRINT     *1,OBAOUTNO,*14,PVIURNO,*25,PATNAME,*68,CPCDATE:
                    *81,OBACOMP,*85,TDESC,*108,INVTOTAL
          ADD       TWO,CLNO
          DISPLAY   *P46:24,*V2LON,OBAOUTNO;
.
PRNT9000  MOVE      ZERO,EXIT
PRNT9999  RETURN
+
.******************************************************************************
.*                                  HEAD0000              Called by: PROC0000 *
.*                          Print The Report Heading               & PRNT0000 *
.******************************************************************************
HEAD0000  CLOCK     TIME,CTIMEIS
          CALL      HEAD132                 * Print the report heading
.
          PRINT     *N;
          UNPACK    STRTDTE8,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format the start date
          PRINT     *40,"Start Date  : ",CPCDATE,*N;
.
          UNPACK    ENDDTE8,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format the end date
          PRINT     *40,"End Date    : ",CPCDATE,*N
.
          PRINT     *3,"O/P No",*16,"U/R No",*25,"Patient Name",*70,"Date":
                    *81,"Compensation Eligibilty",*111,"Amount ($)"
          CALL      UND132
          PRINT     *N;
          ADD       "6",CLNO
.
HEAD9000  MOVE      ZERO,EXIT
HEAD9999  RETURN
+
.==============================================================================
.
          INC       STD001IO
.
          INC       OUTBB1IO/INC            * Clinic session booking file B
          INC       OUTDTRIO/INC            * DTR file
          INC       PMSMTIIO/INC            * Misc.Theatre item file
          INC       PATCODIO/INC            * Codes file
          INC       PATINVIO/INC            * Invoice file
          INC       PATMA1IO/INC            * Master file 
          INC       PATVISIO/INC            * Visit file
+
.


. ***************************************************************************** . *              P R O G R A M  S U M M A R Y                                 *
. *              -------------  -------------                                 *
. *                                                                           *
. *     PROGRAM NAME:     IBAADT11                                            *
. *                                                                           *
. *     FUNCTION:         Ward-Bed Audit Report                               *
. *                                                                           *
. *     DATE WRITTEN:     07/11/2018                                          *
. *                                                                           *
. *     AUTHOR:           J.Tan      (Task 0864701)                           *
. *                                                                           *
. *     MODIFICATIONS:                                                        *
. *             V10.14.01 18/02/2019 J.Tan    TSK 0864701                     *
. *                       Fixed print heading                                 *
. *****************************************************************************
.
          INC       STD001FD   
.
          INC       PATHSPFD/INC
          INC       PATWR1FD/INC
          INC       PATWBHFD/INC
          INC       PATCODFD/INC
          INC       EMRLHIFD/INC
          INC       EMRHISFD/INC
          INC       EMRLOCFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
          INC       IBASEQFD/INC
.
.
.         VARIABLES DEFINITIONS
.
TMBTYFL1  IFILE     SQL, FIXED=22, KEY="U1-3,4-4,5-7,8-15"
TMBTYFL2  IFILE     SQL, FIXED=22, KEY="U1-3,8-15,4-4,5-7"
.
.Name     Type   Length   Physical   Start   Description
.----     ----   ------   --------   -----   -----------
TMBHSPID  DIM       3        3         1     Hospital ID
TMBRTYPE  DIM       1        1         4     Record type 1=Bed Type,2=Room Type
.                                                        3=Ward Only,4=Location
TMBBTYPE  DIM       3        3         5     Bed Type Cat BT
.                                            Room Type Cat RT
.                                            Blank for Ward Only
.                                            Room type of Location
TMBRDATE  DIM       8        8         8     Date
TMFACTBD  DIM       6        6         16    No of Active Beds
. End of Record                        22
.
TMBACTBD  FORM      6                        No of Active Beds
.
TMGTYFL1  IFILE     SQL, FIXED=8, KEY="U1-3,4-4,5-7"
.Name     Type   Length   Physical   Start   Description
.----     ----   ------   --------   -----   -----------
TMGHSPID  DIM       3        3         1     Hospital ID
TMGRTYPE  DIM       1        1         4     Record type 1=Bed Type,2=Room Type
.                                                        3=Ward Only,4=Location
TMGBTYPE  DIM       3        3         5     Bed Type Cat BT
.                                            Room Type Cat RT
.                                            Blank for Ward Only
.                                            Room type of Location
. End of Record                        8
.
.
SWACTIVE  FORM      1
ACTVFLAG  FORM      1
COUNTER   FORM      5
DIM15     DIM       15
WDATE     DIM       8
FNAMEA    DIM       8
FNAMEB    DIM       8
CMDLINE   DIM       80
IBCNMHOS  FORM      1
MINACTBD  FORM      6     * Minimum Active beds
MAXACTBD  FORM      6     * Maximum Active beds
RECFLAG   FORM      1
.
DSPSTDTE  DIM       10
DSPEDDTE  DIM       10
TOTALSTR  FORM      6
TOTALEND  FORM      6
TOTALMIN  FORM      6
TOTALMAX  FORM      6
.
SAVKEY7   DIM       7
SAVRTYPE  DIM       1
SAVHSPID  DIM       3
CURRDATE  DIM       8
STRACTBD  FORM      6
ENDACTBD  FORM      6
STRDATE   DIM       8
ENDDATE   DIM       8
ACTDATE   DIM       8
PTCNBEML  DIM      80     * Email Address for Ward Bed Audit
.
.*********************************************************************
PRGID     INIT      "IBAADT11"
PRGNAM    INIT      "Ward-Bed Audit Report"
VERSION   INIT      "V12.01.00"
.*********************************************************************
+
.*********************************************************************
.         ML1000 OF PROGRAM
.*********************************************************************
ML0000    CALL      INIT0000
.
ML1000    CALL      OPTN0000
          BRANCH    EXIT,ML9000
.
ML1500    CALL      DATE0000                * Keyin end date
          BRANCH    EXIT,ML1000
.
          CALL      PRWR0000               * Process Ward/bed 
.
          CALL      PRNT0000               * Print report
.
          CALL      EXCUS000               * Execute remote scripts
          GOTO      ML1000
.
ML9000    CHAIN     PGM
          STOP
+
.*********************************************************************
.         initilisation
.*********************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening emrlocaf";
          OPEN      EMRLOCA1,"emrlocaf"
.
          DISPLAY   *P64:24,"emrhisaf";
          OPEN      EMRHISA1,"emrhisaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
.
          OPEN      PATHSPA1,"pathspaf"
          OPEN      PATWBHA1,"patwbhaf"
          OPEN      EMRLHIA1,"emrlhiaf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,HUND28;*23,PTCNBEML
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
INIT9999  RETURN
+
.*********************************************************************
.         enter the option
.*********************************************************************
OPTN0000  MOVE      ZERO,OPTION
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Ward/Bed Audit":
                    *P1:7,"Select Option : ";
OPTN1000  KEYIN     *P17:7,*V2LON,*DV,UNDLN2,*P17:7,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN9000
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                  DATE0000              Called by: ML0000   *
.*                            Keyin End Date                                  *
.******************************************************************************
DATE0000  MOVE      "17",CCOL
          DISPLAY   *P1:9,*EF,"Start  Date : ":
                    *P1:10,*EF,"Ending Date : "
.
          MOVE      ONE,CCANLDTE            * Blank cancel default
.
. ----- Keyin end date -----
.
DATE1000  MOVE      "10",CVERT
          MOVE      CCC,CCENT
          UNPACK    SP6,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin date
          BRANCH    OVRCD,DATE9500          * Nothing entered
          BRANCH    CQUEST,DATE1000
.
          PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDATE
          CALL      PACDATE                 * Format the end date
          MOVE      CPCDATE,DSPEDDTE
.
          DISPLAY   *PCCOL:CVERT,*EL,*V2LON,DSPEDDTE;
.
          MATCH     ENDDATE,CURRDATE
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"End date must not be in the future.  ";
            CALL      HITENTER
            GOTO      DATE1000
          ENDIF
.
.         Start date is the first date of the month
.
          MOVE      "01",CDAY
          PACK      STRDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRDATE
          CALL      PACDATE                 * Format the start date
          MOVE      CPCDATE,DSPSTDTE
.
          DISPLAY   *PCCOL:9,*EL,*V2LON,DSPSTDTE;
.
          MATCH     STRDATE,ENDDATE
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"End date must after the start date.  ";
            CALL      HITENTER
            GOTO      DATE1000
          ENDIF
          MOVE      ZERO,EXIT
          GOTO      DATE9999
.
DATE9500  MOVE      ONE,EXIT
DATE9999  RETURN
+
.*********************************************************************
.       Process Ward/bed
.*********************************************************************
PRWR0000  DISPLAY    *P1:24,*EL,*P10:24,*V2LON:
                     "** Generating Report **"
          DISPLAY   *P50:24,"Ward : ";
.
          CALL      CTMP0000                * Create Temp file
          CALL      ACTB0000                * Process Bed activity
.
PRWR9999  RETURN
+
.*********************************************************************
.       Process Ward/Bed activity
.*********************************************************************
ACTB0000  MOVE      STRDATE,WDATE            * Start date
.
ACTB1000  MATCH     WDATE,ENDDATE
          GOTO      ACTB9999 IF LESS
.
          CALL      WRBD0000                * Check Ward/bed activity
          CALL      LOCA0000                * Check Location activity
.
          UNPACK    WDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      "1",F2
          CALL      CALD0000                 * Calculate Next day
          PACK      WDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",WDATE
.
          GOTO      ACTB1000
.
ACTB9999  RETURN
+
.*********************************************************************
.       Process ward/bed activity for the selected date
.*********************************************************************
WRBD0000  MOVE      SP70,KEY6
          CALL      RDSWARD1
WRBD1000  CALL      RDKWARD1
          BRANCH    OVRCD,WRBD9999
.
          MOVE      ZERO,ACTVFLAG             * default to Inactive
          CALL      WBHS0000                  * Ward/Bed History
.
          IF        WACTIVE<>1
            ADD       ONE,ACTVFLAG             * active flag
          ENDIF
.
          MATCH     SP70,WBED
          GOTO      WRBD4000 IF NOT EQUAL
.
          IF        WINPUT=1 | WINPUT=2
            MOVE      PTWDHOSN,TMBHSPID    * Hospital ID
            MOVE      WDATE,TMBRDATE
            GOTO      WRBD4100             * Ward Only
          ENDIF
          GOTO      WRBD1000
.
WRBD4000  MOVE      PTWDHOSN,TMBHSPID      * Hospital ID
          MOVE      WDATE,TMBRDATE
.
          MOVE      "1",TMBRTYPE
          MOVE      WEFRBT,TMBBTYPE        * Bed type
          CALL      WRUP0000               * Write/Update tempfile
.
          MOVE      "2",TMBRTYPE
          MOVE      WRTYPE,TMBBTYPE        * Room type
          CALL      WRUP0000               * Write/Update tempfile
.
          GOTO      WRBD1000
.
WRBD4100  MOVE      "3",TMBRTYPE           * Ward Only
          MOVE      SP70,TMBBTYPE
          CALL      WRUP0000               * Write/Update tempfile
.
          GOTO      WRBD1000
.
WRBD9999  RETURN
+
.*********************************************************************
.         Check ward bed history
.*********************************************************************
WBHS0000  MOVE      WACTIVE,SWACTIVE        * Save the current status
.
.         Check if Ward/Bed was created prior the date
.
          PACK      KEY14,WWARD,WBED,SP70
          CALL      RSPTWBH1
          CALL      RKPTWBH1
          BRANCH    OVRCD,WBHS2000
.
          MATCH     WWARD,WBHWARD
          GOTO      WBHS9999 IF NOT EQUAL
          MATCH     WBED,WBHBED
          GOTO      WBHS9999 IF NOT EQUAL
.
          MATCH     SP8,WBHDATE
          GOTO      WBHS9999 IF EQUAL
.
          UNPACK    WBHDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      "1",F2
          CALL      CALD0000                 * Calculate Next day
          PACK      KEY8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",KEY8
.
          MATCH     KEY8,WDATE
          GOTO      WBHS2000 IF NOT LESS
.
.         Ward/Bed was created prior the selected date
.
          MOVE      ONE,WACTIVE             * Ward/Bed Inactive
          GOTO      WBHS9999
.
WBHS2000  PACK      KEY14,WWARD,WBED,Z70
          CALL      RSPTWBH1
WBHS4000  CALL      RPPTWBH1
          BRANCH    OVRCD,WBHS9000
.
          MATCH     WWARD,WBHWARD
          GOTO      WBHS9000 IF NOT EQUAL
          MATCH     WBED,WBHBED
          GOTO      WBHS9000 IF NOT EQUAL
.
          MATCH     SP8,WBHDATE
          GOTO      WBHS9000 IF EQUAL
.
          UNPACK    WBHDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      "1",F2
          CALL      CALD0000                 * Calculate Next day
          PACK      KEY8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",KEY8
.
          MATCH     KEY8,WDATE
          IF        @LESS
            MOVE      WBHACTV,SWACTIVE       * Active flag from history
            GOTO      WBHS4000
          ENDIF
.
WBHS9000  MOVE      SWACTIVE,WACTIVE          * Active flag from history
WBHS9999  RETURN
+
.*********************************************************************
.         Process Emergency Location
.*********************************************************************
LOCA0000  MOVE      SP70,KEY3
          CALL      RSEMLOC1
LOCA1000  CALL      RKEMLOC1
          BRANCH    OVRCD,LOCA9999
.
          MATCH     "O",EMLOTYPE              * Room type
          GOTO      LOCA1200 IF EQUAL         * Other
          MATCH     "W",EMLOTYPE
          GOTO      LOCA1200 IF EQUAL         * Waiting roon
          MATCH     SP70,EMLOTYPE
          GOTO      LOCA1000 IF NOT EQUAL
.
LOCA1200  MOVE      ZERO,ACTVFLAG             * default to Inactive
          CALL      LHIS0000                  * Location History
.
          MATCH     "1",EMLOACTV
          IF        !@EQUAL
            ADD       ONE,ACTVFLAG             * active flag
          ENDIF
.
          MOVE      "~~~",TMBHSPID         * Hospital id
          MOVE      "4",TMBRTYPE
          PACK      TMBBTYPE,EMLOTYPE,SP70 * Room type
          MOVE      WDATE,TMBRDATE
          CALL      WRUP0000               * Write/Update tempfile
.
          GOTO      LOCA1000
.
LOCA9999  RETURN
+
.*********************************************************************
.         Check Location history
.*********************************************************************
LHIS0000  MOVE      EMLOACTV,SWACTIVE        * Save the current status
.
.         Check if Location was created prior the date
.
          PACK      KEY22,EMLOLOCA,SP70
          CALL      RSEMLHI1
          CALL      RKEMLHI1
          BRANCH    OVRCD,LHIS2000
.
          MATCH     EMLOLOCA,EMLHLOCA        * Same location ?
          GOTO      LHIS9999 IF NOT EQUAL
.
          MATCH     "1",EMLHDELE             * Deleted ?
          IF        @EQUAL
            MOVE      "1",EMLHACTV           * Make it inactive
          ENDIF
.
          MATCH     SP8,EMLHEFDT
          GOTO      LHIS9999 IF EQUAL
.
          UNPACK    EMLHEFDT,CCENT,CYEAR,CMON,CDAY
          MOVE      "1",F2
          CALL      CALD0000                 * Calculate Next day
          PACK      KEY8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",KEY8
.
          MATCH     KEY8,WDATE
          GOTO      LHIS2000 IF NOT LESS
.
.         Location was created prior the selected date
.
          MOVE      ONE,EMLOACTV            * Location Inactive
          GOTO      LHIS9999
.
LHIS2000  PACK      KEY22,EMLOLOCA,Z70
          CALL      RSEMLHI1
LHIS4000  CALL      RPEMLHI1
          BRANCH    OVRCD,LHIS9000
.
          MATCH     EMLOLOCA,EMLHLOCA        * Same location ?
          GOTO      LHIS9000 IF NOT EQUAL
          MATCH     SP8,EMLHEFDT
          GOTO      LHIS9000 IF EQUAL
.
          MATCH     "1",EMLHDELE             * Deleted ?
          IF        @EQUAL
            MOVE      "1",EMLHACTV           * Make it inactive
          ENDIF
.
          UNPACK    EMLHEFDT,CCENT,CYEAR,CMON,CDAY
          MOVE      "1",F2
          CALL      CALD0000                 * Calculate Next day
          PACK      KEY8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",KEY8
.
          MATCH     KEY8,WDATE
          IF        @LESS
            MOVE      EMLHACTV,SWACTIVE       * Active flag from history
            GOTO      LHIS4000
          ENDIF
.
LHIS9000  MOVE      SWACTIVE,EMLOACTV         * Active flag from history
LHIS9999  RETURN
+
.*********************************************************************
.         Calculate next or previous date
.*********************************************************************
CALD0000  MOVE      CDAY,DD              * These variables are
          MOVE      CMON,MM              * used by DMYCON
          MOVE      CYEAR,YY
          MOVE      CCENT,CC
          CALL      DMYCON               * Convert to Julian format
.
          ADD       F2,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON               * Convert back to "normal" format
.
          MOVE      DD,CDAY
          MOVE      MM,CMON
          MOVE      YY,CYEAR
          MOVE      CC,CCENT
.
CALD9999  RETURN
+
.*********************************************************************
.         Write/Update tempfile
.*********************************************************************
WRUP0000  PACK      KEY15,TMBHSPID,TMBRTYPE,TMBBTYPE,TMBRDATE
          CALL      RDTMBT1
          IF        OVRCD=1
            UNPACK    KEY15,TMBHSPID,TMBRTYPE,TMBBTYPE,TMBRDATE
            MOVE      ACTVFLAG,TMBACTBD       * No of active bed
            CALL      WRTMBT1
          ELSE
            ADD       ACTVFLAG,TMBACTBD
            CALL      UPTMBT1
          ENDIF
.
          PACK      KEY7,TMBHSPID,TMBRTYPE,TMBBTYPE
          CALL      RDTMGT1
          COMPARE   ZERO,OVRCD
          GOTO      WRUP9999 IF EQUAL       * already exists
.
          UNPACK    KEY7,TMGHSPID,TMGRTYPE,TMGBTYPE
          CALL      WRTMGT1
.
WRUP9999  RETURN
+
.*********************************************************************
.       Print Report
.*********************************************************************
PRNT0000  MOVE      "60",CLNO
          MOVE      ZERO,RECFLAG
          MOVE      SP70,SAVRTYPE
          MOVE      Z70,SAVHSPID
          MOVE      ZERO,TOTALSTR
          MOVE      ZERO,TOTALEND
          MOVE      ZERO,TOTALMIN
          MOVE      ZERO,TOTALMAX
          PACK      KEY7,SP70
          CALL      RSTMGT1
PRNT1000  CALL      RKTMGT1
          BRANCH    OVRCD,PRNT8000
.
          MATCH     "~~~",TMGHSPID         * Hospital id
          GOTO      PRNT2200 IF EQUAL      * Location
.
          MATCH     Z70,SAVHSPID
          IF        !@EQUAL
            MATCH     SAVHSPID,TMGHSPID      * Check hospital id
            GOTO      PRNT2000 IF EQUAL
          ENDIF
.
          IF        RECFLAG=0
            MOVE      ONE,RECFLAG
          ELSE
            CALL      PRTT0000             * Print total
          ENDIF
          MOVE      TMGHSPID,SAVHSPID      * hospital id
          CALL      HEAD0000
          MOVE      TMGRTYPE,SAVRTYPE
          GOTO      PRNT2400
.
PRNT2000  MATCH     SAVRTYPE,TMGRTYPE      * Check Record type
          GOTO      PRNT2400 IF EQUAL
.
PRNT2200  MATCH     "4",SAVRTYPE
          GOTO      PRNT2400 IF EQUAL      * Printing Emergency
.
          CALL      PRTT0000               * Print total
          MOVE      TMGRTYPE,SAVRTYPE        * record type
          MATCH     "4",TMGRTYPE
          GOTO      PRNT2300 IF EQUAL
.
          COMPARE   "40",CLNO
          GOTO      PRNT2300 IF NOT LESS
.
          CALL      RHED0000
          GOTO      PRNT2400
.
PRNT2300  CALL      HEAD0000
.
PRNT2400  COMPARE   "50",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PACK      SAVKEY7,TMGHSPID,TMGRTYPE,TMGBTYPE,SP70
          CALL      NBED0000                       * get number of active bed
          CALL      MNMX0000                       * get Min and Max
.
          MATCH     "4",TMGRTYPE
          GOTO     PRNT3000 IF EQUAL               * Location Emergency
.
          MATCH    "1",TMGRTYPE
          IF       @EQUAL
            PACK     KEY5,ANSB,ANST,TMGBTYPE       * Bed Type
            CALL     RDCODE1
          ELSE
            MATCH    "2",TMGRTYPE
            IF       @EQUAL
              PACK     KEY5,ANSR,ANST,TMGBTYPE     * Room type
              CALL     RDCODE1
            ELSE
              MOVE    "Ward Only",TDESC
            ENDIF
          ENDIF
.
          GOTO      PRNT6000
.
.         Emergency
.
PRNT3000  MOVE      SP70,TDESC
.
          MATCH     "O",TMGBTYPE      * Room type
          IF        @EQUAL
            MOVE      "Other Department",TDESC
            GOTO      PRNT6000
          ENDIF
.
          MATCH     "W",TMGBTYPE
          IF        @EQUAL
            MOVE      "Waiting Room",TDESC
            GOTO      PRNT6000
          ENDIF
.
          MATCH     SP70,TMGBTYPE
          IF        @EQUAL
            MOVE      "Emergency Map Room",TDESC
            GOTO      PRNT6000
          ENDIF
          GOTO     PRNT1000
.
PRNT6000  PRINT     *1,"| ",TDESC,*25,"| ",STRACTBD:
                    *45,"| ",ENDACTBD,*65,"| ",MINACTBD:
                    *85,"| ",MAXACTBD,*132,"|"
.
          ADD       STRACTBD,TOTALSTR
          ADD       ENDACTBD,TOTALEND
          ADD       MINACTBD,TOTALMIN
          ADD       MAXACTBD,TOTALMAX
          ADD       ONE,CLNO
          GOTO      PRNT1000
.
.         Print end of report
.
PRNT8000  CALL      PRTT0000                   * Print total
.
          PRINT     *N,*N,"  ***  END OF REPORT  ***"
.
          DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** Report Generation Completed **"
          CALL      KILL0000                * Kill tempfile
.
PRNT9999  RETURN
+
.*********************************************************************
.                 Get Max and Min activity beds for each group
.*********************************************************************
NBED0000  MOVE      ZERO,MINACTBD
          MOVE      ZERO,MAXACTBD
          MOVE      ZERO,STRACTBD
          MOVE      ZERO,ENDACTBD
.
.         Get the Total at Start
.
          PACK      KEY15,SAVKEY7,SP70
          CALL      RSTMBT1                        * get the max and min
          CALL      RKTMBT1
          BRANCH    OVRCD,NBED9999
.
          PACK      KEY7,TMBHSPID,TMBRTYPE,TMBBTYPE,SP70
          MATCH     SAVKEY7,KEY7
          IF        @EQUAL
            MOVE      TMBACTBD,STRACTBD              * Total at Start
          ENDIF
.
.         Get the Total at End
.
          PACK      KEY15,SAVKEY7,Z70
          CALL      RSTMBT1                        * get the max and min
          CALL      RPTMBT1
          BRANCH    OVRCD,NBED9999
.
          PACK      KEY7,TMBHSPID,TMBRTYPE,TMBBTYPE,SP70
          MATCH     SAVKEY7,KEY7
          IF        @EQUAL
            MOVE      TMBACTBD,ENDACTBD              * Total at End
          ENDIF
.
NBED9999  RETURN
+
.*********************************************************************
.         Get the Max and Min
.*********************************************************************
MNMX0000  MOVE      ZERO,COUNTER
          PACK      KEY15,SAVKEY7,SP70
          CALL      RSTMBT1
MNMX1000  CALL      RKTMBT1
          BRANCH    OVRCD,MNMX9999
.
          PACK      KEY7,TMBHSPID,TMBRTYPE,TMBBTYPE,SP70
          MATCH     SAVKEY7,KEY7
          GOTO      MNMX9999 IF NOT EQUAL
.
          IF        COUNTER=0
            MOVE      TMBACTBD,MINACTBD
            MOVE      TMBACTBD,MAXACTBD
            MOVE      ONE,COUNTER
          ENDIF
.
          IF        TMBACTBD<MINACTBD
            MOVE      TMBACTBD,MINACTBD
          ELSE
            IF        TMBACTBD>MAXACTBD
              MOVE      TMBACTBD,MAXACTBD
            ENDIF
          ENDIF
          GOTO      MNMX1000
.
MNMX9999  RETURN
+
.*********************************************************************
.                 Print total
.*********************************************************************
PRTT0000  MATCH     "3",SAVRTYPE
          GOTO      PRTT1000 IF EQUAL      * Ward Only (No Total)
          COMPARE   ZERO,RECFLAG
          GOTO      PRTT2000 IF EQUAL       * no record found
.
          CALL      UND132
          PRINT     *1,"| Total",*25,"| ",TOTALSTR:
                    *45,"| ",TOTALEND,*65,"| ",TOTALMIN:
                    *85,"| ",TOTALMAX,*132,"|"
.
PRTT1000  CALL      UND132
          PRINT     *N
.
PRTT2000  MOVE      ZERO,TOTALSTR
          MOVE      ZERO,TOTALEND
          MOVE      ZERO,TOTALMIN
          MOVE      ZERO,TOTALMAX
.
PRTT9999  RETURN
+
.*********************************************************************
.                 HEADINGS FOR OUTPUT
.*********************************************************************
HEAD0000  MOVE      ZERO,CNOUNDLN
          CALL      IBACLOCK
          CALL      HEAD132
.
          PRINT     *40,"Date From : ",DSPSTDTE," To ",DSPEDDTE
.
          MATCH     "4",TMGRTYPE
          IF        !@EQUAL
            PRINT     *40,"Inpatients",*N
            IF        IBCNMHOS=1
              PRINT     *40,"Hospital ";
              MATCH     SP70,SAVHSPID
              IF        !@EQUAL
                PACK      KEY3,SAVHSPID
                CALL      RDPTHSP1
                IF        OVRCD=0
                  PRINT     *50,PTHSNAME
                  GOTO      HEAD1000
                ENDIF
              ENDIF
              PRINT     *50," "
            ENDIF
          ELSE
            PRINT     *40,"Emergency",*N
          ENDIF
.
HEAD1000  MOVE      EIGHT,CLNO
          CALL      RHED0000
.
HEAD9999  RETURN
+
.*********************************************************************
.                 HEADINGS FOR OUTPUT
.*********************************************************************
RHED0000  CALL      UND132
.
          MATCH     "1",TMGRTYPE
          IF        @EQUAL
            PRINT     *1,"| Bed Category (Cat BT)";
          ELSE
            MATCH     "2",TMGRTYPE
            IF        @EQUAL
              PRINT     *1,"| Room Category (Cat RT)";
            ELSE
              MATCH     "3",TMGRTYPE
              IF        @EQUAL
                PRINT     *1,"| Ward Only";
              ELSE
                PRINT     *1,"| Location Type";
              ENDIF
            ENDIF
          ENDIF
.
          PRINT     *25,"| Total at Start":
                    *45,"| Total at End",*65,"| Minimum":
                    *85,"| Maximum",*132,"|"
.
          CALL      UND132
          ADD       THREE,CLNO
.
RHED9999  RETURN
+
.*********************************************************************
.         Create a tempfile
.*********************************************************************
CTMP0000  CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAMEA
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAMEB
.
          CALL      KILL0000
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMEA,CMDLINE
          APPEND    " 22 U1-3,4-4,5-7,8-15 U1-3,8-15,4-4,5-7",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TMBTYFL1,FNAMEA
          OPEN      TMBTYFL2,FNAMEA
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMEB,CMDLINE
          APPEND    " 8 U1-3,4-4,5-7",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TMGTYFL1,FNAMEB
.
          CALL      CLER0000                * empty it if Oracle
.
CTMP9999  RETURN
+
.*********************************************************************
.         delete tempfile
.*********************************************************************
KILL0000  CLOSE     TMBTYFL1
          CLOSE     TMBTYFL2
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMEA,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CLOSE     TMGTYFL1
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMEB,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
KILL9999  RETURN
+
.******************************************************************************
.*                             CLER0000                 Called by:            *
.*               close and erase the temporary files                          *
.******************************************************************************
CLER0000  PACK      KEY15,SP70
          CALL      RSTMBT1
          CALL      RKTMBT1
          BRANCH    OVRCD,CLER2000
.
          PACK      KEY15,TMBHSPID,TMBRTYPE,TMBBTYPE,TMBRDATE
          CALL      DETMBT1
          GOTO      CLER0000
.
CLER2000  PACK      KEY7,SP70
          CALL      RSTMGT1
          CALL      RKTMGT1
          BRANCH    OVRCD,CLER9999
.
          PACK      KEY7,TMGHSPID,TMGRTYPE,TMGBTYPE
          CALL      DETMGT1
          GOTO      CLER0000
.
CLER9999  RETURN
+
.*********************************************************************
.         Execute remote scripts
.*********************************************************************
EXCUS000  CLEAR     CMDLINE                    * Run us1 sending spool file
          APPEND    "ibaadt11.us1 ",CMDLINE    * name
          APPEND    SPLFILE,CMDLINE
          APPEND    ".prt ",CMDLINE
          APPEND    PTCNBEML,CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
EXCUS999  RETURN
+
.*********************************************************************
.         I/O for tempfile
.*********************************************************************
RSTMBT1   RESET     KEY15
          READ      TMBTYFL1,KEY15;;
          RETURN
.
RKTMBT1   MOVE      ZERO,OVRCD
          READKS    TMBTYFL1;TMBHSPID,TMBRTYPE,TMBBTYPE,TMBRDATE,TMFACTBD
          GOTO      OVERCOND IF OVER
          MOVE      TMFACTBD,TMBACTBD
          RETURN
.
RPTMBT1   MOVE      ZERO,OVRCD
          READKP    TMBTYFL1;TMBHSPID,TMBRTYPE,TMBBTYPE,TMBRDATE,TMFACTBD
          GOTO      OVERCOND IF OVER
          MOVE      TMFACTBD,TMBACTBD
          RETURN
.
RDTMBT1   MOVE      ZERO,OVRCD
          RESET     KEY15
          READ      TMBTYFL1,KEY15;TMBHSPID,TMBRTYPE,TMBBTYPE,TMBRDATE,TMFACTBD
          GOTO      OVERCOND IF OVER
          MOVE      TMFACTBD,TMBACTBD
          RETURN
.
WRTMBT1   MOVE      TMBACTBD,TMFACTBD
          RESET     KEY15
          WRITE     TMBTYFL1,KEY15;TMBHSPID,TMBRTYPE,TMBBTYPE,TMBRDATE,TMFACTBD
          RETURN
.
UPTMBT1   MOVE      TMBACTBD,TMFACTBD
          UPDATE    TMBTYFL1;TMBHSPID,TMBRTYPE,TMBBTYPE,TMBRDATE,TMFACTBD
          RETURN

DETMBT1   RESET     KEY15
          DELETE    TMBTYFL1,KEY15
          RETURN
.
RSTMGT1   RESET     KEY7
          READ      TMGTYFL1,KEY7;;
          RETURN
.
RKTMGT1   MOVE      ZERO,OVRCD
          READKS    TMGTYFL1;TMGHSPID,TMGRTYPE,TMGBTYPE
          GOTO      OVERCOND IF OVER
          RETURN
.
RPTMGT1   MOVE      ZERO,OVRCD
          READKP    TMGTYFL1;TMBHSPID,TMGRTYPE,TMGBTYPE
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTMGT1   MOVE      ZERO,OVRCD
          RESET     KEY7
          READ      TMGTYFL1,KEY7;TMGHSPID,TMGRTYPE,TMGBTYPE
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTMGT1   RESET     KEY7
          WRITE     TMGTYFL1,KEY7;TMGHSPID,TMGRTYPE,TMGBTYPE
          RETURN
.
UPTMGT1   UPDATE    TMGTYFL1;TMGHSPID,TMGRTYPE,TMGBTYPE
          RETURN

DETMGT1   RESET     KEY7
          DELETE    TMGTYFL1,KEY7
          RETURN
+
.*********************************************************************
.
          INC       STD001IO
          INC       TFILENAM
.
          INC       IBASEQIO/INC
          INC       WEBERRIO/INC
          INC       PATHSPIO/INC
          INC       PATWR1IO/INC
          INC       PATWBHIO/INC
          INC       PATCODIO/INC
          INC       EMRLHIIO/INC
          INC       EMRHISIO/INC
          INC       EMRLOCIO/INC
.
.

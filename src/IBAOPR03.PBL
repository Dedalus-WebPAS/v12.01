+
.**********************************************************************
.* System   : Operating Room System                                   *
.* Program  : IBAOPR03                                                *
.* Desc     : Clinic File Maintenance                                 *
.**********************************************************************
.* Date     :  14/12/89                                 
.* Author   :  K.Peak                                    
.* Mods.    :  Paul Duncan (6/3/90)                        
.*                         - Doctor type know mandatory     
.*                         - altered change and delete options
.*                         - fixed re-disp after doc type "?"
.*   V5.01.01  21/01/91  Rod Knowles                    
.*             Converted back to version 5.01            
.*   V5.01.02  24/09/91  J.Tan      SRF 110752            
.*             Fixed displaying surgeon/clinic description 
.*   V5.01.03  24/05/1995  Matt Surridge          
.*             Fixed bugs for global recompile.    
.*   V5.04.01  26/06/1997  Howellsy         505
.*             Use PATDOCKY instead of PATDOCDS
.*   V5.07.01  10.10.1999  B.G.Ackland         
.*             Enable Doctor Code & Description
.**********************************************************************
.
.$$$$$
.    Operating Room System Clinic File Maintenance ( IBAOPR03 )
.    ---------------------------------------------------------
.
.    - Initialisation
.
.    - Display Header
.            - Open Files
.               OPRCLIA1
.               PATCODE1
.               PATDO1A1
.               PATDO1A2
.               CONTROLF
.
.    - Processing Options
.
.            - 0 = Exit
.            - 1 = Add
.
.                - Clear Input Variables
.                - Display Screen Format
.                - Input Clinic/Doctor Code
.                     - Read OPRCLIA1, check if already exists
.                     - If Not, Continue...
.                - Accept Field Values 
.                             - Description
.                             - Doctor Code  
.                             - Doctor Type
.                - Select Item, Post or Cancel
.                - If Post then Write Data to OPRCLIA1
.
.            - 2 = Modify
.
.                - Clear Input Variables
.                - Display Screen Format
.                - Input Clinic/Doctor Code
.                     - Read OPRCLIA1, check if exists
.                     - If Exists, Continue...
.                - Accept Field Values 
.                             - Description    
.                             - Doctor Code  
.                             - Doctor Type
.                - Select Item, Post or Cancel
.                - If Post then Write Data to OPRCLIA1
.
.            - 3 = Delete
.
.                - Clear Input Variables
.                - Display Screen Format
.                - Input Clinic/Doctor Code
.                     - Read OPRCLIA1, check if exists
.                     - If Exists, Continue...
.                - Display Details 
.                             - Description    
.                             - Doctor Code  
.                             - Doctor Type
.                - O.K. to Delete Yes or No ?
.                - If Yes then Delete Record from OPRCLIA1 File
.
.    - END
.$$$$$
.
          INC       STD001FD
          INC       OPRCLIFD/INC           * Clinic File
          INC       OPRCONFD/INC           * Operating Room Control file
          INC       PATCODFD/INC           * patient codes file
          INC       PATDO1FD/INC           * Doctor file
+
.**********************************************************************
.*                       GLOBAL VARIABLES                             *
.**********************************************************************
.
CODE      DIM       5
CODEDT    INIT      "YG"            * doctor type (surgeon)
CODEYG    INIT      "YG"            * doctor type (clinic)
.
DOCCODE   DIM       6
DOCDESC   DIM       22              * doctor description
DIM3      DIM       3               * used in patdsdoc
.
ITEM      FORM      2
.
.
MODE      DIM       1
.
PTCNDCQS  FORM      1
RANGE     FORM      1
.
TYPDESC   DIM       20              * doctor type description
.
UVERT1    FORM      2
VAR       FORM      2
.
PRGNAM    DIM       50
PRGID     INIT      "IBAOPR03"
PRGNAM1   INIT      " Maintenance"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                           MAINLINE                                 *
.**********************************************************************
.
          CALL      INIT0000               * initialization and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000,ML2000,ML3000
.
. ---- add a clinic/doctor code ----
.
ML1000    CALL       CLR0000                * clear input variables
          MOVE       "I",MODE               * Insert mode
          CALL       SCR0000                * display screen format
          CALL       PROC0000               * input clinic/doctor code
          BRANCH     EXIT,ML0100 
          CALL       ACPT0000               * accept initial values
          CALL       SEL0000                * select field to modify
.                                           * or ok to post
          BRANCH     EXIT,ML1000
          CALL       WRT0000                * write to file
          GOTO       ML1000
.
. ---- modify a clinic/doctor code ----
.
ML2000    CALL       CLR0000                * clear input variables
          MOVE       "C",MODE               * Change mode
          CALL       SCR0000                * display screen format 
          CALL       ENTR0000               * enter clinic/doctor code
          BRANCH     EXIT,ML0100
          CALL       DISP0000               * display details
          CALL       SEL0000                * select field to modify
.                                           * or ok to post
          BRANCH     EXIT,ML2000
          CALL       UPD0000                * update record
          GOTO       ML2000
.
. ---- delete a clinic/doctor code ----
.
ML3000    CALL       CLR0000                * clear input variables
          CALL       SCR0000                * display screen format 
          CALL       INPT0000               * enter clinic/doctor code
          BRANCH     EXIT,ML0100
          CALL       DISP0000               * display detail 
          CALL       CONT0000               * ok to delete ?
          BRANCH     EXIT,ML3000
          CALL       DEL0000                * delete this record
          GOTO       ML3000
.
ML9999    CHAIN      PGM                    * chain back to program
+
.**********************************************************************
.*                             INIT0000                               *
.*                      Initialization  Routine                       *
.**********************************************************************
.
INIT0000  DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,TWENTY1;*247,PTCNDCQS
          READ      CONTROLF,FORTY;*96,OPCNCDSC,*111,OPCNCLSU
.
          RESET     OPCNCDSC,15
INIT1000  CMATCH    SP1,OPCNCDSC
          GOTO      INIT2000 IF NOT EQUAL
          BUMP      OPCNCDSC BY -1
          GOTO      INIT2000 IF EOS
          GOTO      INIT1000
INIT2000  MOVEFPTR  OPCNCDSC,VAR
          RESET     OPCNCDSC,VAR
          LENSET    OPCNCDSC
          RESET     OPCNCDSC
.
          PACK      PRGNAM,OPCNCDSC,PRGNAM1
.
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening oprcliaf";
          OPEN      OPRCLIA1,"oprcliaf"
.
          DISPLAY   *P64:24,"oprcliaf";
          OPEN      OPRCLIA2,"oprcliaf"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
INIT9999  RETURN
+
.**********************************************************************
.*                             CLR0000                                *
.*                 Routine to Clear Local Variables                   *
.**********************************************************************
.
CLR0000
.
. ----- Clear the Clinic File Variables -----
.
          MOVE      SP6,OPCLCLIN
          PACK      OPCLDESC,SP20,SP20
          MOVE      SP6,OPCLDOCT
          MOVE      SP3,OPCLCAT 
.
. ----- Clear the Doctor File Variables -----
.
          MOVE      SP6,DCODE
          MOVE      SP9,DTITL
          PACK      DGNAME,SP20,SP20
          PACK      DSNAME,SP20,SP20
          MOVE      SP3,DRTYPE 
.
          MOVE      SP1,MODE
          RETURN
+
.****************************************************************************
.*                                OPTN0000                                  *
.*                        get user options or exit                          *
.*                                                                          *
.*    valid options     --->  (0) Exit                                      *
.*                            (1) Add                                       *
.*                            (2) Modify                                    *
.*                            (3) Delete                                    *
.****************************************************************************
.
. ---- display option screen ----
.
OPTN0000    MOVE      FALSE,EXIT
.
            HLINE     *G37,2,68,80
.
            CALL      SELOPTN                        * select option
            COMPARE   ZERO,COPTION
            GOTO      OPTN9000 IF EQUAL              * exit      
            COMPARE   FOUR,COPTION
            GOTO      OPTN0000 IF NOT LESS           * invalid option, re-enter
            GOTO      OPTN9999
.
OPTN9000    MOVE      TRUE,EXIT                      * set EXIT = 1 
.
OPTN9999    RETURN
+
.**********************************************************************
.*                            SCRN0000                                *
.*                     Main Data Screen Display                       *
.**********************************************************************
.
SCR0000   BRANCH    OPCNCLSU,SCR1000               * 0= Clinic, 1=Surgeon
.
          DISPLAY   *P1:3,*EF:
                    *P5:4,*+,OPCNCDSC,*-," Code",*P25:4," : ":
                    *P2:6,*V2LON,ONE,*HOFF,". Description          : ":
                    *P2:7,*V2LON,TWO,*HOFF,". Doctor Code          : ":
                    *P2:8,*V2LON,THREE,*HOFF,". Doctor Type          : ";
          GOTO      SCR9999
.
SCR1000   DISPLAY   *P1:3,*EF:
                    *P5:4,*+,OPCNCDSC,*-," Code",*P25:4," : ":
                    *P2:6,*V2LON,ONE,*HOFF,". Title                : ":
                    *P2:7,*V2LON,TWO,*HOFF,". Surname              : ":
                    *P2:8,*V2LON,THREE,*HOFF,". Given Name(s)        : ":
                    *P2:9,*V2LON,FOUR,*HOFF,". Doctor Type          : ";
.
SCR9999   RETURN
+
.**********************************************************************
.*                            PROC0000                                *
.*                 Routine to Keyin Clinic/Doctor Codes               *
.**********************************************************************
.
PROC0000  MOVE      FALSE,EXIT
.
          DISPLAY   *P28:4,*EL,UNDLN6;
          KEYIN     *P28:4,*V2LON,OPCLCLIN;
.
          ENDSET    OPCLCLIN
          APPEND    SP6,OPCLCLIN
          RESET     OPCLCLIN
.
          DISPLAY   *P28:4,*V2LON,OPCLCLIN;
.
          MATCH     QUEST,OPCLCLIN
          GOTO      PROC0000 IF EQUAL
.
          MATCH     SP6,OPCLCLIN
          GOTO      PROC9000 IF EQUAL
.
          BRANCH    OPCNCLSU,PROC1000              * 0= Clinic, 1=Surgeon
.
          PACK      KEY6,OPCLCLIN
          CALL      RDOPCLI1
          BRANCH    OVRCD,PROC9999
          GOTO      PROC2000
.
PROC1000  PACK      KEY6,OPCLCLIN
          CALL      RDDOCT1 
          BRANCH    OVRCD,PROC9999
.
PROC2000  DISPLAY   *P1:24,*B,"Record Already Exists - ",*EL;
          CALL      HITENTER
          GOTO      PROC0000
.
PROC9000  MOVE      TRUE,EXIT
PROC9999  RETURN
+
.**********************************************************************
.*                           ENTR00000                                *
.*                  Read Clinic/Doctor File for Record                *
.**********************************************************************
.
ENTR0000  MOVE      FALSE,EXIT
.
          DISPLAY   *P28:4,*EL,UNDLN6;
          KEYIN     *P28:4,*V2LON,OPCLCLIN;
          ENDSET    OPCLCLIN
          APPEND    SP6,OPCLCLIN
          RESET     OPCLCLIN
          DISPLAY   *P28:4,*V2LON,OPCLCLIN;
.
          MATCH     SP6,OPCLCLIN
          GOTO      ENTR9500 IF EQUAL
.
          BRANCH    OPCNCLSU,ENTR1000              * 0= Clinic, 1=Surgeon
.
          PACK      KEY6,OPCLCLIN
          CALL      RDOPCLI1
          BRANCH    OVRCD,ENTR9000
          GOTO      ENTR9999
.
ENTR1000  MOVE      OPCLCLIN,DCODE
          MOVE      DCODE,KEY6
          CALL      RDDOCT1 
          BRANCH    OVRCD,ENTR9000
          GOTO      ENTR9999
.
ENTR9000  DISPLAY   *P1:24,*B,"Record Not on File - ",*EL;
          CALL      HITENTER
          GOTO      ENTR0000
.
ENTR9500  MOVE      TRUE,EXIT                    * nothing entered
ENTR9999  RETURN
.
.**********************************************************************
.*                           INPT00000                                *
.*                  Read Clinic/Doctor File for Record                *
.**********************************************************************
.
INPT0000  MOVE      FALSE,EXIT
.
          DISPLAY   *P28:4,*EL,UNDLN6;
          KEYIN     *P28:4,*V2LON,OPCLCLIN;
          ENDSET    OPCLCLIN
          APPEND    SP6,OPCLCLIN
          RESET     OPCLCLIN
          DISPLAY   *P28:4,*V2LON,OPCLCLIN;
.
          MATCH     SP6,OPCLCLIN
          GOTO      INPT9500 IF EQUAL
.
          BRANCH    OPCNCLSU,INPT1000              * 0= Clinic, 1=Surgeon
.
          PACK      KEY6,OPCLCLIN
          CALL      RDOPCLI1
          BRANCH    OVRCD,INPT9000
          GOTO      INPT9999
.
INPT1000  PACK      KEY6,OPCLCLIN
          CALL      RDDOCT1 
          BRANCH    OVRCD,INPT9000
          GOTO      INPT9999
.
.------- Clinic code Not On File ----
.
INPT9000  DISPLAY   *P1:24,*B,"Record Not on File - ",*EL;
          CALL      HITENTER
          GOTO      INPT0000
.
INPT9500  MOVE      TRUE,EXIT                    * nothing entered
INPT9999  RETURN
+
.**********************************************************************
.*                           ACPT00000                                *
.*                      Accept Initial Values                         *
.**********************************************************************
.
ACPT0000  BRANCH    OPCNCLSU,ACPT1000              * 0= Clinic, 1=Surgeon
.
          CALL      DESC0000                    * keyin description    
          CALL      DCOD0000                    * keyin doctor code 
          CALL      CTYP0000                    * keyin doctor type 
          GOTO      ACPT9999
.
ACPT1000  CALL      TITL0000                    * keyin doctor title    
          CALL      SNAM0000                    * keyin doctor surname
          CALL      GNAM0000                    * keyin doctor given name(s)
          CALL      DTYP0000                    * keyin doctor type 
.
ACPT9999  RETURN
+
.**************************************************************************
.*                                   SEL0000                              *
.*                         select field to modify or post                 *
.**************************************************************************
.
SEL0000    BRANCH    OPCNCLSU,SEL4000               * 0= Clinic, 1=Surgeon
.
           MOVE      "C",MODE
           MOVE      FALSE,EXIT
           CALL      MAINQST
           COMPARE   ZERO,CCITEM                * ok to post
           GOTO      SEL9999 IF EQUAL
           COMPARE   "-1",CCITEM                * cancel option
           GOTO      SEL9000 IF EQUAL
           BRANCH    CCITEM,SEL1000,SEL2000,SEL3000
.
           BEEP
           GOTO      SEL0000
.
SEL1000    CALL      DESC0000                        * keyin description
           GOTO      SEL0000
.
SEL2000    CALL      DCOD0000                        * keyin doctor code  
           GOTO      SEL0000
.
SEL3000    CALL      CTYP0000                        * keyin doctor type 
           GOTO      SEL0000
.
SEL4000    MOVE      FALSE,EXIT
           CALL      MAINQST
           COMPARE   ZERO,CCITEM                * ok to post
           GOTO      SEL9999 IF EQUAL
           COMPARE   "-1",CCITEM                * cancel option
           GOTO      SEL9000 IF EQUAL
           BRANCH    CCITEM,SEL5000,SEL6000,SEL7000,SEL8000
.
           BEEP
           GOTO      SEL0000
.
SEL5000    CALL      TITL0000                        * keyin doctor title
           GOTO      SEL0000
.
SEL6000    CALL      SNAM0000                        * keyin doctor surname
           GOTO      SEL0000
.
SEL7000    CALL      GNAM0000                        * keyin doctor given name
           GOTO      SEL0000
.
SEL8000    CALL      DTYP0000                        * keyin doctor type 
           GOTO      SEL0000
.
SEL9000    MOVE      TRUE,EXIT
SEL9999    RETURN
+
.**************************************************************************
.*                               DISP0000                                 *
.*                   Display Data from the Clinic File                    *
.**************************************************************************
.
DISP0000  BRANCH    OPCNCLSU,DISP3000              * 0= Clinic, 1=Surgeon
          MOVE      SP30,DOCDESC
          MOVE      OPCLDOCT,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD,DISP1000
          MOVE      SP1,ANS
          MOVE      DGNAME,ANS
          PACK      DOCDESC,ANS,DOT,DSNAME         * doctor description
.
DISP1000  
.
          MOVE      SP20,TYPDESC
          PACK      KEY5,CODEYG,OPCLCAT
          CALL      RDCODE1
          BRANCH    OVRCD,DISP2000
          MOVE      TDESC,TYPDESC                  * doctor type description
.
DISP2000  DISPLAY   *P28:6,*V2LON,OPCLDESC:
                    *P28:7,OPCLDOCT,*HOFF,*P36:7,DOCDESC:
                    *P28:8,*V2LON,OPCLCAT,*HOFF,*P36:8,TYPDESC;
          GOTO      DISP9999
.
.         Surgeon Code
.
DISP3000  MOVE      SP20,TYPDESC
          PACK      KEY5,CODEDT,DRTYPE
          CALL      RDCODE1
          BRANCH    OVRCD,DISP4000
          MOVE      TDESC,TYPDESC                  * doctor type description
.
DISP4000  DISPLAY   *P28:6,*V2LON,DTITL:
                    *P28:7,DSNAME:
                    *P28:8,DGNAME:
                    *P28:9,*V2LON,DRTYPE,*HOFF,*P36:9,TYPDESC;
.
DISP9999  RETURN
+
.**************************************************************************
.*                              DESC0000                                  *
.*                       Keyin Clinic Description                         *
.**************************************************************************
.
DESC0000 
.          MATCH     SP6,OPCLDOCT
.          GOTO      DESC2000 IF NOT EQUAL
.
          DISPLAY   *P28:6,*EL,UNDLN30
          KEYIN     *P28:6,*V2LON,OPCLDESC;
          ENDSET    OPCLDESC
          APPEND    SP30,OPCLDESC
          RESET     OPCLDESC
.
          MATCH     SP30,OPCLDESC
          GOTO      DESC1000 IF EQUAL
.
          DISPLAY   *P28:6,*V2LON,OPCLDESC;
          GOTO      DESC9999
.
DESC1000  DISPLAY   *P28:6,*EL;
          CMATCH    "I",MODE
          GOTO      DESC9999 IF EQUAL
          DISPLAY   *P1:24,*B,"Description is Mandatory - ",*EL;
          CALL      HITENTER
          GOTO      DESC0000
.
DESC2000  CMATCH    "I",MODE
          GOTO      DESC9999 IF EQUAL
          DISPLAY   *P1:24,*EL,*B,"- Doctor Code already entered - ",*EL;
          CALL      HITENTER
.
DESC9999  RETURN
+
.**************************************************************************
.*                              DCOD0000                                  *
.*                         Keyin Doctor Code                              *
.**************************************************************************
.
DCOD0000  MOVE      SP30,DOCDESC
..          MATCH     SP30,OPCLDESC
..          GOTO      DCOD9000 IF NOT EQUAL
.
          MOVE      SP6,OPCLDOCT
          MOVE      "28",ECOL
          MOVE      "07",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          CALL      PATDOCKY
          BRANCH    EXIT,DCOD9999,DCOD9999
.
          MOVE      DCODE,OPCLDOCT
.
          DISPLAY   *P28:7,*V2LON,OPCLDOCT;
. 
DCOD1000  MOVE      SP30,DOCDESC
          MOVE      OPCLDOCT,KEY6
          MOVE      SP1,ANS
          MOVE      DGNAME,ANS
          PACK      DOCDESC,ANS,DOT,DSNAME
          DISPLAY   *P36:7,DOCDESC;                * doctor description
          GOTO      DCOD9999
.
DCOD9000  CMATCH    "I",MODE
          GOTO      DCOD9999 IF EQUAL
          DISPLAY   *P1:24,*EL,*B,"Clinic Description already entered - ",*EL;
          CALL      HITENTER
.
DCOD9999  RETURN
.
.**************************************************************************
.*                              CTYP0000                                  *
.*                          Keyin Doctor Type                             *
.**************************************************************************
.
CTYP0000  MOVE      SP20,TYPDESC
          MOVE      SP3,OPCLCAT
.
          DISPLAY   *P28:8,*EL,UNDLN3;
          KEYIN     *P28:8,*V2LON,OPCLCAT;
          DISPLAY   *P28:8,*V2LON,OPCLCAT;
.
          RESET     OPCLCAT
          GOTO      CTYP0000 IF EOS
.
          MATCH     QUEST,OPCLCAT
          GOTO      CTYP1000 IF NOT EQUAL
          MOVE      SP3,OPCLCAT
          MOVE      CODEYG,CODE
          CALL      PATCODDS
          CALL      RDSP0000
          GOTO      CTYP0000
.
CTYP1000  MOVE      SP20,TYPDESC
          PACK      KEY5,CODEYG,OPCLCAT
          CALL      RDCODE1
          BRANCH    OVRCD,CTYP8000
          MOVE      TDESC,TYPDESC
          DISPLAY   *P36:8,TYPDESC;                * doctor type description
          GOTO      CTYP9999
.
CTYP8000  DISPLAY   *P1:24,*B,"Code Does Not Exist - ",*EL;
          CALL      HITENTER
          GOTO      CTYP0000
.
CTYP9999  RETURN
+
.**************************************************************************
.*                              TITL0000                                  *
.*                       Keyin Surgeon Title                              *
.**************************************************************************
.
TITL0000  DISPLAY   *P28:6,*EL,UNDLN9
          KEYIN     *P28:6,*V2LON,DTITL;
          DISPLAY   *P28:6,*V2LON,DTITL;
.
          RESET     DTITL
          RETURN
+
.**************************************************************************
.*                              SNAM0000                                  *
.*                       Keyin Surgeon Surname                            *
.**************************************************************************
.
SNAM0000  PACK      DSNAME,SP20,SP20
.
          DISPLAY   *P28:7,*EL,UNDLN20
          KEYIN     *P28:7,*V2LON,DSNAME;
          DISPLAY   *P28:7,*V2LON,DSNAME;
.
          RESET     DSNAME
          MATCH     SP20,DSNAME
          GOTO      SNAM9999 IF NOT EQUAL
.
          DISPLAY   *P1:24,*B,"Surname is Mandatory - ",*EL;
          CALL      HITENTER
          GOTO      SNAM0000
.
SNAM9999  RETURN
+
.**************************************************************************
.*                              GNAM0000                                  *
.*                       Keyin Surgeon Given Name(s)                      *
.**************************************************************************
.
GNAM0000  DISPLAY   *P28:8,*EL,UNDLN20,UNDLN5
          KEYIN     *P28:8,*V2LON,DGNAME;
          DISPLAY   *P28:8,*V2LON,DGNAME;
.
          ENDSET    DGNAME
          APPEND    SP30,DGNAME
          RESET     DGNAME
.
GNAM9999  RETURN
+
.**************************************************************************
.*                              DTYP0000                                  *
.*                          Keyin Doctor Type                             *
.**************************************************************************
.
DTYP0000  MOVE      SP20,TYPDESC
          MOVE      SP3,DRTYPE
.
          DISPLAY   *P28:9,*EL,UNDLN3;
          KEYIN     *P28:9,*V2LON,DRTYPE;
          DISPLAY   *P28:9,*V2LON,DRTYPE;
.
          RESET     DRTYPE
          GOTO      DTYP0000 IF EOS
.
          MATCH     QUEST,DRTYPE
          GOTO      DTYP1000 IF NOT EQUAL
          MOVE      SP3,DRTYPE
          MOVE      CODEDT,CODE
          CALL      PATCODDS
          CALL      RDSP0000
          GOTO      DTYP0000
.
DTYP1000  MOVE      SP20,TYPDESC
          PACK      KEY5,CODEDT,DRTYPE
          CALL      RDCODE1
          BRANCH    OVRCD,DTYP8000
          MOVE      TDESC,TYPDESC
          DISPLAY   *P36:9,TYPDESC;                * doctor type description
          GOTO      DTYP9999
.
DTYP8000  DISPLAY   *P1:24,*B,"Code Does Not Exist - ",*EL;
          CALL      HITENTER
          GOTO      DTYP0000
.
DTYP9999  RETURN
+
.**************************************************************************
.*                             RDSP0000                                   *
.*                  Redisplay the Screen After a "?"                      *
.**************************************************************************
.
RDSP0000  BRANCH    OPCNCLSU,RDSP1000              * 0= Clinic, 1=Surgeon
          CALL      SCR0000
.
          DISPLAY   *P28:4,*V2LON,OPCLCLIN:
                    *P28:6,OPCLDESC:
                    *P28:7,OPCLDOCT,*HOFF,*P36:7,DOCDESC:
                    *P28:8,*V2LON,OPCLCAT,*HOFF,*P36:8,TYPDESC;
          GOTO      RDSP9999
.
RDSP1000  CALL      SCR0000
          DISPLAY   *P28:4,*V2LON,OPCLCLIN:
                    *P28:6,DTITL:
                    *P28:7,DSNAME:
                    *P28:8,DGNAME:
                    *P28:9,*V2LON,DRTYPE,*HOFF,*P36:9,TYPDESC;
.
RDSP9999  RETURN
+
.**************************************************************************
.*                              WRT0000                                   *
.*                   Write Data to the Clinic File                        *
.**************************************************************************
.
WRT0000   BRANCH    OPCNCLSU,WRT1000               * 0= Clinic, 1=Surgeon
.
          DISPLAY   *P1:24,"Writing to Clinic File - Please Wait ",*P1:24,*EL;
          CALL      HITENTER
.
          PACK      KEY6,OPCLCLIN
          CALL      WROPCLI1
          GOTO      WRT9999
.
WRT1000   DISPLAY   *P1:24,"Writing to Doctor File - Please Wait ",*P1:24,*EL;
          CALL      HITENTER
.
          PACK      KEY6,OPCLCLIN
          CALL      WRDOCT1
.
WRT9999   RETURN
+
.**************************************************************************
.*                              UPD0000                                   *
.*                   Update Data in the Clinic File                       *
.**************************************************************************
.
UPD0000   BRANCH    OPCNCLSU,UPD1000               * 0= Clinic, 1=Surgeon
.
          DISPLAY   *P1:24,"Updating Clinic File - Please Wait ",*P1:24,*EL;
          CALL      HITENTER
.
          PACK      KEY6,OPCLCLIN
          CALL      RAOPCLI1 
          CALL      UPOPCLI1
          GOTO      UPD9999
.
UPD1000   DISPLAY   *P1:24,"Updating Doctor File - Please Wait ",*P1:24,*EL;
          CALL      HITENTER
.
          PACK      KEY6,DCODE
          CALL      UPDOCT1
.
UPD9999   RETURN
+
.**************************************************************************
.*                              DEL0000                                   *
.*               Delete a Record from the Clinic File                     *
.**************************************************************************
.
DEL0000   BRANCH    OPCNCLSU,DEL1000               * 0= Clinic, 1=Surgeon
.
          DISPLAY   *P1:24,"Deleting Clinic File Record - Please Wait ";
          CALL      HITENTER
.
          MOVE      OPCLCLIN,KEY6
          CALL      DEOPCLI1
          GOTO      DEL9999
.
DEL1000   DISPLAY   *P1:24,"Deleting Doctor File Record - Please Wait ";
          CALL      HITENTER
.
          MOVE      OPCLCLIN,KEY6
          CALL      DEDOCT1 
.
DEL9999   RETURN
+
.**************************************************************************
.*                             CONT0000                                   *
.*                      O.K. to Continue for Delete                       *
.**************************************************************************
.
CONT0000  MOVE      FALSE,EXIT
.
          DISPLAY   *P1:24,"O.K. to Delete Record (",*V2LON,"Y",*HOFF:
                           "/",*V2LON,"N",*HOFF,") ";
          KEYIN     *V2LON,ANS;
.
          CMATCH    ANSY,ANS
          GOTO      CONT9999 IF EQUAL
.
          CMATCH    ANSN,ANS
          GOTO      CONT9000 IF EQUAL
.
          BEEP
          GOTO      CONT0000
.
CONT9000  MOVE      TRUE,EXIT
CONT9999  RETURN
+
.********************************************************************** 
.*              I/O INCLUDES FOR FILES AND GENERAL ROUTINES           *
.**********************************************************************
.
         INC       STD001IO
         INC       PATCODKY                * patient codes file routine
         INC       PATDOCKY                * doctor codes file routine
         INC       PATCODIO/INC            * patient i/o for codes
         INC       PATDO1IO/INC            * doctor i/o 
         INC       OPRCLIIO/INC            * clinic i/o
.

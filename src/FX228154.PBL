.***************************************************************************
.* System    :   Patient Management                                        *
.* Program   :   FX228154                                                  *
.* Desc      :   Fixit for Program for Reassign Debtors                    *
.***************************************************************************
.* Date      :   17/08/2010                                                *
.* Author    :   J.Tan                                                     *
.* Function  :   This program will write all outstanding invoices to       *
.*               Un-reconciled invoice file                                *
.* Modifications  :                                                        *
.* V10.01.01     10/03/2011 Jill Parkinson CAR 233088                      *
.*               Added pmsvx1af                                            *
.*        V10.00.00 17/08/2010  J.Tan      CAR 228154                      *
.*                  Created program                                        *
.***************************************************************************
.
.
          INC       STD001FD
.
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATUREFD/INC
          INC       PATINVFD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
RECNUM    FORM      8
FORM12P2  FORM      12.2
.
PRGID     INIT      "FX228154"
PRGNAM    INIT      "Fixit for Un-reconciled Invoices"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000       * proceed with fixit
.
MAIN1000  CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN2000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN2000  CALL      PROC0000               * process files with coded fields
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: MAIN0000  *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patinvrf";
          OPEN      PATINVR4,"patinvrf"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patureaf";
          OPEN      PATUREA1,"patureaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: MAIN0000  *
.*                        get user options or exit                          *
.*    Returns:  EXIT    = FALSE (0)  Run fixit                              *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*****************************************************************************
PROC0000  DISPLAY   *P1:24,*EF,"Processing:";
          CALL      IBACLOCK
          CALL      HEAD0000
.
          MOVE      ZERO,RECNUM
          MOVE      "3",KEY1                     * Inpatient only
          PACK      KEY32,KEY1,SP70
          CALL      RDSINV4
PROC1000  CALL      RDKINV4
          BRANCH    OVRCD,PROC9000
.
          MATCH     KEY1,DPINVSYS
          GOTO      PROC9000 IF NOT EQUAL
.
          MATCH     "99999999",PINVBLCD
          GOTO      PROC1000 IF NOT EQUAL        * invoice balanced
.
          MOVE      PINVADM,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,PROC1000
.
          DISPLAY   *P13:24,*V2LON,PINVNO;
.
          MOVE      PINVAMT,FORM12P2
          ADD       PINVPATA,FORM12P2
          ADD       PINVHFDA,FORM12P2
          ADD       PINVOTHA,FORM12P2
          ADD       PINVJOUR,FORM12P2
          ADD       PTINCRTT,FORM12P2
          ADD       PTINGSTJ,FORM12P2
.
          COMPARE   ZERO,FORM12P2            * invoice balanced ?
          GOTO      PROC6000 IF NOT EQUAL    * no.
.
          MOVE      PINVNO,KEY8
          CALL      RDPTURE1
          IF        OVRCD=0
            CALL      DEPTURE1               * remove record from Un-reconcilled
          ENDIF
          GOTO      PROC1000
.
PROC6000  MOVE      PINVNO,KEY8
          CALL      RDPTURE1
          COMPARE   ZERO,OVRCD
          GOTO      PROC1000 IF EQUAL        * already exist
.
          MOVE      FORM12P2,PTUROSTD
          MOVE      PINVNO,PTURINVN
          MOVE      PINVADM,PTURVISN
          MOVE      "0",PTURSTAT             * New
          MOVE      PINVSYS,PTURSYST
          MOVE      ACLAIM,PTURCLMN
          MOVE      AFUNDH,PTURHFND
          MOVE      AFNDTB,PTURHTAB
.
          MOVE      PINVAMT,PTURINVT
          MOVE      PINVPATA,PTURPAYM
          ADD       PINVHFDA,PTURPAYM
          ADD       PINVOTHA,PTURPAYM
.
          MOVE      PTINCRTT,PTURJRNL
          ADD       PINVJOUR,PTURJRNL
          ADD       PTINGSTJ,PTURJRNL
.
          PACK      PTURCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PTURCDAT
          MOVE      CTIMEIS,PTURCTIM
          MOVE      SP70,PTURCUID        * WEB user id
.
          PACK      PTURUDAT,CCC,CYY,CMM,CDD
          REP       " 0",PTURUDAT
          MOVE      CTIMEIS,PTURUTIM
          MOVE      SP70,PTURUUID        * WEB user id
.
          PACK      PTURSPAR,SP70,SP70
          CALL      WRPTURE1
.
          COMPARE   SIXTY,CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PRINT     *1,"|",*3,PINVNO,*20,"|",*22,PTUROSTD
          ADD       ONE,CLNO
.
          ADD       ONE,RECNUM
          GOTO      PROC1000
.
PROC9000  DISPLAY   *P1:23,*EL,"Fixit complete.  Number of Records found: ":
                    RECNUM
          CALL      HITENTER
.
PROC9999  RETURN
+
.*****************************************************************************
.*                               HEAD0000              Called by: MAIN0000   *
.*****************************************************************************
HEAD0000  CALL      HEAD132
          CALL      LINE0000
          PRINT     *1,"|",*3,"Invoice Number":
                    *20,"|",*22,"Outstanding Amount"
          CALL      LINE0000
          MOVE      SEVEN,CLNO
HEAD9999  RETURN
+
.*****************************************************************************
.
LINE0000  PRINT     "*-----------------------------------------------":
                    "------------------------------------------------":
                    "-----------------------------------*"
.
LINE9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PATUREIO/INC
          INC       PATINVIO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC

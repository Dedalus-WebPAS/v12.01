.----------------------------------------------------------------------
. Program       : PATWEB74
.
. Function      : Patient Report Server 1
.
. Modifications : 
. V10.03.01 16/07/2013  Jill Parkinson CAR 287923
.           Added include of PATCONFD
.                  V10.02.01 04/04/2011  Jill Parkinson CAR 239574
.                            Removed open of ibaprntf
.                  V9.05.01  09/03/2006  Ebon Clements CAR 78533
.                           Mods for PATCODTM - Hospital specific category codes
.                  V5.07.01 20.12.1999  Katie Nelson
.                           Recompiled for IBAWEBDF/IBAWEBCD                   
.                  V5.07.00 27/10/1999  Katie Nelson
.                           Port to 5.07                                       
.----------------------------------------------------------------------
.                  V6.06.01 04/05/1999  Katie Nelson
.                           Removed update default printer file - is now called
.                           from the CLSPRINT routine in WEBPRTCD.
.                  V6.04.00 15.07.1998 B.G.Ackland
.                          New Program
.
.----------------------------------------------------------------------
          INC       IBAWEBDF    
          INC       WEBRSHDF    
          INC       PATCONFD/INC
.
STARTCAT  DIM       2
ENDNGCAT  DIM       2
.
PRGID     INIT      "PATWEB74"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "Patient Report Scheduler"
.
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "INVALID OPTION",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  CALL      REPFRM00   * Output Report Form
          GOTO      MAIN1000
.
MAIN1200  CALL      SCHREP00   * Schedule Report
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
.------------------------------------------------------------
. Output Report Form
.------------------------------------------------------------
REPFRM00  MOVE      "Schedule Report",WEBTITLE
          CALL      WEBHED00
          BRANCH    EXIT,REPFRM99
.
          CALL      WEBBOD00
.
          CALL      WEBEND00
.
REPFRM99  RETURN
.------------------------------------------------------------
.  Process fields from template body
.------------------------------------------------------------
PROFLD00  CALL      REPT0000
PROFLD99  RETURN
.------------------------------------------------------------
.  Report Form Field
.------------------------------------------------------------
REPT0000  BRANCH    FLDITMNO,REPT0100,REPT0200,REPT0300,REPT0400
          GOTO      REPT9999
.
REPT0100  CALL      SELPRT00
          GOTO      REPT9999
.
REPT0200  UNPACK    DIM127,D1,KEY2,DIM127 
          MOVE      KEY2,CATEGORY
          MOVE      SP70,CODE
          CALL      CODSEL00
          GOTO      REPT9999
.
REPT0300  CALL      CATSEL00
          GOTO      REPT9999
.
REPT0400  CALL      IBACLOCK
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          UNPACK    KEY8,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      REPT9999
.
REPT9999  RETURN
.------------------------------------------------------------
. Add Selection Options to Form
.------------------------------------------------------------
CATSEL00  PACK      KEY5,SP70
          CALL      RDSCODE1
CATSEL10  CALL      RDKCODE1
          BRANCH    OVRCD,CATSEL99
          MATCH     SP70,ACODE
          IF        !@EQUAL
            PACK      KEY5,TCODE,Z70
            CALL      RDSCODE1
            GOTO      CATSEL10
          ENDIF
          PACK      KEY4,QUOTE,TCODE,QUOTE
          WRITE     HTMLFILE;"<option value=",KEY4,">":
                             TDESC,"(",TCODE,")</option>"
          GOTO      CATSEL10
CATSEL99  RETURN
.------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------
INIT0000  OPEN      IBASPOL1,"ibaspolf"
          OPEN      IBAPRTA1,"ibaprtaf"        * For Printer Selection
          OPEN      IBACODE1,"ibacodef"        * For Printer Selection
          OPEN      IHAPASS1,"ihapassf"        * For Printer Selection
          OPEN      IBAPDFA1,"ibapdfaf"        * For default printer selection
          OPEN      WEBRSHA1,"webrshaf"
          OPEN      WEBRSHA2,"webrshaf" 
.
          OPEN      PATCODE1,"patcodes"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          IF        IBCNMHOS=1
            OPEN      MLTCODA1,"mltcodaf"
            OPEN      MLTCODA2,"mltcodaf"
          ENDIF
          RETURN
.------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          MOVE      SP70,TEMPLATE
          MOVE      SP70,SELPRINT
          MOVE      ZERO,NOCOPIES
.
          CALL      CLRRSH00
.
          RETURN
.------------------------------------------------------------
. Set Parameters
.------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "selprint=",QRYNAME
          IF        @EQUAL
            SQUEEZE   QRYDATA
            MOVE      QRYDATA,SELPRINT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nocopies=",QRYNAME
          IF        @EQUAL
            SQUEEZE   QRYDATA
            MOVE      QRYDATA,NOCOPIES
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "schd",QRYNAME
          IF        @EQUAL
            CALL      SETRSH00
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startcat=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTCAT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "endngcat=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ENDNGCAT
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
.
          INC       IBAWEBCD    
          INC       WEBRSHCD
.
.------------------------------------------------------------
. Schedule a Report for Execution
.------------------------------------------------------------
SCHREP00  MOVE      "WRPPAT51",SCHDPROG
          MOVE      "Code File Listing",SCHDNAME
          MATCH     STARTCAT,ENDNGCAT
          GOTO      SCHREP10 IF NOT LESS
          MOVE      "Invalid Category Range",WEBTITLE
          CALL      WEBERR00
          GOTO      SCHREP99
.
SCHREP10  CALL      NEWRSH00                 * Schedule Report
.
.  Write Parameters for Report
.
          WRITE     PARAFILE,SEQ;"selprint=",SELPRINT
          WRITE     PARAFILE,SEQ;"nocopies=",NOCOPIES
          WRITE     PARAFILE,SEQ;"startcat=",STARTCAT
          WRITE     PARAFILE,SEQ;"endngcat=",ENDNGCAT
.
          CLOSE     PARAFILE
.
          CALL      UUWBRS1                  * Unlock Record
.
          MOVE      "Report Scheduled",WEBTITLE
          CALL      WEBERR00
.
SCHREP99  RETURN
.

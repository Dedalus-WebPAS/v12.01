.******************************************************************************
.* System         : Outpatients                                               *
.* Program        : IBAOUT84.PBL       (Korner Report KHM09A)                 *
.* Name           : Consultant, GP Clinic Activity                            *
.******************************************************************************
.* Date           : 17/06/1993         15:06:47 BST                           *
.* Author         : Justin Coates                                             *
.* Function       : To report on Clinic activity                              *
.* Modifications  :                                                           *
.******************************************************************************
.*        V11.02.01 09/02/2022  Thanh T          TSK 0905641                  *
.*                  Recompiled as OUTMA1FD changes                            *
.*        V10.07.01 04/11/2015  Ebon Clements  CAR 268970                     *
.*                  Change to use OUTCLIFD index 1 instead of index 2         *
.*        V10.05.01 18/12/2014  Ebon Clements  CAR 261630                     *
.*                  Removed port number from temp file name                   *
.*        V10.03.01 17/10/2012  Jill Parkinson CAR 273375                     *
.*                  Recompiled for DATECONV - first day of year calculation   *
.*        V5.08.B01 15/03/2000  Steve Armstrong    5.08 Mods                  *
.*                  Mods for changes to OUTCLIFD (effective date)             *
.******************************************************************************
.*        V5.04.01  20/12/1996  Howellsy          EOC & ACC                   *
.*                  Recompiled for OUTRF1FD                                   *
.*        V5.01.02  21/07/1993    Lofty the Pom       SRF 440219              *
.*                  print by hospital, not OP site.  get correct specialty    *
.*        V5.01.03  06/08/1993    Lofty the Pom                               *
.*                  use contracting default unit for A&E section              *
.*        V5.01.04  19/08/1994    Paul Howells        SRF 440953              *
.*                  Calculate a session as to the hour.                       *
.*----------------------------------------------------------------------------*
.*        V5.03.01  12/04/1995    Max White           SRF 441212              *
.*                    Check OTCNULET parameter to determine if referral       *
.*                  details are held in outbokcf or outrf1af.                 *
.*                    Also fixed SRF 440953, an individual session is where   *
.*                  session id, clinic type, session date & session hour      *
.*                  are the same.                                             *
.******************************************************************************
.
.$$$$$
. columns in the report are :
.  1 - number of sessions held      - from OUTOSFFD
.  2 - number of sessions cancelled - from OUTCSLFD
.  3 - number of NEW visit type who attended             - within clinics
.  4 - number of NEW visit type who were DNA
.  5 - number of REVIEW/other visit type who attended
   6 - number of REVIEW/other visit type who were DNA
.  7 - number of NEW visit types who attended an OUTSIDE clinic session
.  8 - number of REVIEW/other visit types who attended an OUTSIDE clinic session
.  9 - number of Referral letters
. 10 - number of Private patients (Cat CL indicator 1 = P)
.
. Note : Columns 7 & 8 are not calculated as these were defined as clinics
.        without Clinic Master records, which the IBA system does not have.
.        (they are one-off sessions I would think, kind of emergency ones or
.         ones to cope with extra patients)
.
. For each unit, summarizes the A&E details
.   Number of First attenders
.   Number of Follow up attenders
.   Total number of attenders
.
.$$$$$
.
          INC       STD001FD
          INC       AAEDE1FD/INC
          INC       IBASEQFD/INC
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
          INC       OUTBOCFD/INC
          INC       OUTCONFD/INC
          INC       OUTCLIFD/INC
          INC       OUTCSLFD/INC
          INC       OUTCTYFD/INC
          INC       OUTMA1FD/INC
          INC       OUTOSFFD/INC
          INC       OUTRF1FD/INC
          INC       OUTSITFD/INC
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PATHSPFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
.         temp file
.
TEMPA     IFILE     SQL, FIXED=131, KEY="U1-5,6-6,7-9,10-15,16-21"
KEYA      INIT      " 131 U1-5,6-6,7-9,10-15,16-21"
.Name     Type      Length  Phys.  Start  Description
.-------  ----      ------  -----  -----  -------------------------------
XUNIT     DIM       5           5      1  Unit Code 
XTYPE     DIM       1           1      6  Type of record (1=OP, 2=A&E)
XHOSP     DIM       3           3      7  Hospital code
XSESS     DIM       6           6     10  Session Id
XCTYPE    DIM       6           6     16  Clinic Type          
.
DXCSHELD  DIM       6           6     22  Clinic sessions held
DXCSCANC  DIM       6           6     28  Clinic sessions cancelled
DXWCRASN  DIM       6           6     34  WCS - RA - Seen
DXWCRADN  DIM       6           6     40  WCS - RA - D.N.A
DXWCCASN  DIM       6           6     46  WCS - CA - Seen
DXWCCADN  DIM       6           6     52  WCS - CA - D.N.A
DXOCRASN  DIM       6           6     58  OCS - RA - Seen
DXOCCASN  DIM       6           6     64  OCS - CA - Seen
DXGPREFL  DIM       6           6     70  GP written referral request
DXPPATTD  DIM       6           6     76  Private patient attendances
XPRFIX    DIM       3           3     82  Site file prefix
DXFSTATT  DIM       6           6     85  First attenders
DXFLLATT  DIM       6           6     91  Follow up attenders
XCTYPD    DIM       28         28     97  Clinic Type Description          
XSITE     DIM       6          6      125 Op Site Code
.
.end of record                       131
.
TEMPB     IFILE     SQL, FIXED=32, KEY="U1-3,4-9,10-15,16-21,22-29,30-31"
KEYB      INIT      " 32 U1-3,4-9,10-15,16-21,22-29,30-31"
.
.Name     Type      Length   Phys.  Start  Description
.-------  ----      ------   -----  -----  -------------------------------
XBHOSP     DIM       3           3      1  Hospital code
XBSITE     DIM       6           6      4  Outpatients Site Code 
XBSESS     DIM       6           6     10  Session Id
XBCTYPE    DIM       6           6     16  Clinic Type          
XBSDATE    DIM       8           8     22  Session Date
XBSHR      DIM       2           2     30  Session Hour
.                                      32
.
XCSHELD   FORM      6         * Clinic sessions held
XCSCANC   FORM      6         * Clinic sessions cancelled
XWCRASN   FORM      6         * Within Clinic Sess. - Referral Attend. - Seen
XWCRADN   FORM      6         * WCS - RA - D.N.A
XWCCASN   FORM      6         * WCS - Consultant iniated Attendance - Seen
XWCCADN   FORM      6         * WCS - CA - D.N.A
XOCRASN   FORM      6         * Outside Clinic Sesssion - RA - Seen
XOCCASN   FORM      6         * OCS - CA - Seen
XGPREFL   FORM      6         * GP written referral request
XPPATTD   FORM      6         * Private patient attendances
XFSTATT   FORM      6         * First attenders
XFLLATT   FORM      6         * Follow up attenders
.
TEMPC     IFILE     SQL, FIXED=4, KEY="U1-3"
KEYS      INIT      " 4 U1-3"
.Name     Type      Length  Phys.  Start  Description
.-------  ----      ------  -----  -----  -------------------------------
.OSTFILE  DIM       3           3      1  Site file prefix
.end of record                         4
.
.         program variables
.
CDFRDATE  DIM       8         * from date
CDTODATE  DIM       8         * to   date
CFNAMEA   DIM       8
CFNAMEB   DIM       8
CFNAMEC   DIM       8
CMDLINE   DIM       80
COCNDOFT  DIM       5         * provider/unit for A&E
DIM4      DIM       4
DIM5      DIM       5         * THCSCOD value
FORM6     FORM      6
FORM8     FORM      8
HELDCANC  FORM      1         * 1=update held, 2=update cancelled
HELDNOR   FORM      1         * 1=update held/cancelled, 2=update normal
PRFRDATE  DIM       10        * from date print value
PRTODATE  DIM       10        * to   date print value
PRUNDLN   FORM      1
REFCONS   FORM      1         * 1=referral(NEW), 2=consultant(REVIEW)
SEENDNA   FORM      1         * 1=seen, 2=DNA
TOVRCD    FORM      1         * temp file ovrcd
.
.         program constants
.
CATA      INIT      "A "
CATCL     INIT      "CL"
CATCV     INIT      "CV"
CATEP     INIT      "EP"
ISBUILD   INIT      "isbuild "
ISERASE   INIT      "iserase "
OPT1      INIT      "by Specialty     "
OPT2      INIT      "by Hosp and Spec. "
OPT3      INIT      "by Cons. and Spec."
Z3        INIT      "zzz"
Z5        INIT      "zzzzz"
Z6        INIT      "zzzzzz"
ZU3       INIT      "ZZZ"
ZU6       INIT      "ZZZZZZ"
.
PRGID     INIT      "IBAOUT84"
PRGNAM    INIT      "Consultant, GP Clinic Activity (KHM09A)"
VERSION   INIT      "V12.01.00"
.
.*********************************************************************
.         mainline code
.*********************************************************************
ML0000    CALL      INIT0000
.
ML1000    CALL      OPTN0000                * run report ???
          BRANCH    EXIT,ML9999             * no
.
          CALL      RNGE0000                * enter search ranges
          BRANCH    EXIT,ML1000             * nothing entered
.
          CALL      GDAT0000                * get the data
          CALL      REPT0000                * print the report
          GOTO      ML1000
.
ML9999    CHAIN     PGM 
+
.*********************************************************************
.         initilisation
.*********************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P58:24,"controlf"
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,SEVENTY4;*118,COCNDOFT
          READ      CONTROLF,THIRTY1;*174,OTCNULET
.
          DISPLAY   *P58:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
          DISPLAY   *P58:24,"outosfaf"
          OPEN      OUTOSFA3,"outosfaf"
.
          IF        OTCNULET = 1
            DISPLAY   *P58:24,"outrf1af"
            OPEN      OUTRF1A1,"outrf1af"
          ENDIF
.
          DISPLAY   *P58:24,"outsitaf"
          OPEN      OUTSITA1,"outsitaf"
          DISPLAY   *P58:24,"pathspaf"
          OPEN      PATHSPA1,"pathspaf"
          DISPLAY   *P58:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
          DISPLAY   *P58:24,"aaede1af"
          OPEN      AAEDE1A3,"aaede1af"
.
          MOVE      ONE,CNOUNDLN
.
          CALL      TFILENAM * Get temp file name in create
          MOVE      TFILNAME,CFNAMEA
.
          CALL      TFILENAM * Get temp file name in create
          MOVE      TFILNAME,CFNAMEB
.
          CALL      TFILENAM * Get temp file name in create
          MOVE      TFILNAME,CFNAMEC
.
          CALL      SITE0000                * get all prefixes
.
INIT9999  RETURN
+
.*********************************************************************
.         run the option ??
.*********************************************************************
OPTN0000  HLINE     *G37,2,60,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = by Specialty":
                    *P1:6,*V2LON,"2",*HOFF," = by Hospital and Specialty":
                    *P1:7,*V2LON,"3",*HOFF," = by Session Id and Specialty":
                    *P1:9,"Select option : ";
.
OPTN1000  KEYIN     *P17:9,*V2LON,MOPTION
          MOVE      ONE,EXIT
          COMPARE   ZERO,MOPTION
          GOTO      OPTN9900 IF EQUAL
          MOVE      ZERO,EXIT
          BRANCH    MOPTION,OPTN9000,OPTN9000,OPTN9000
          BEEP
          GOTO      OPTN1000
.
OPTN9000  LOAD      CPHDROPT,MOPTION,OPT1,OPT2,OPT3
          STRIP     CPHDROPT
          DISPLAY   *P60:2,*V2LON,"- ",*+,CPHDROPT,SP1
          GOTO      OPTN9999
.
OPTN9900  CALL      DTPC0000                * delete site file
.
OPTN9999  RETURN
+
.*********************************************************************
.         enter the ranges
.*********************************************************************
RNGE0000  DISPLAY   *P1:10,*EF:
                    *P1:11,"From Date       : ":
                    *P1:12,"To   Date       : "
.
          CALL      KDAT0000                * enter month range
          BRANCH    EXIT,RNGE9999           * exit
.
          CALL      CONTQST
          BRANCH    CEXIT,RNGE9000,RNGE0000,RNGE9100
.
RNGE9000  MOVE      ZERO,EXIT
          GOTO      RNGE9999
RNGE9100  MOVE      ONE,EXIT
RNGE9999  RETURN
+
.*********************************************************************
.         get the data for the report
.*********************************************************************
GDAT0000  DISPLAY   *P1:24,*EL,"Obtaining information.."
          CLOCK     TIME,CTIMEIS            * clock processing start time
          CALL      CTPA0000                * create temp file
          CALL      CTPB0000                * create temp file
.
. ******************************************
. ******* loop over site prefix file *******
. ******************************************
.
          MOVE      SP3,KEY3
          READ      TEMPC,KEY3;;
.
GDAT1000  READKS    TEMPC;OSTFILE
          GOTO      GDAT7000 IF OVER        * end of file
.
. ******* loop over the opened session file *******
.
          PACK      KEY34,CDFRDATE,SP30
          CALL      RSOTOSF3
.
GDAT2000  CALL      RKOTOSF3
          BRANCH    OVRCD,GDAT5000          * end of file
          MATCH     OTOSDATE,CDTODATE
          GOTO      GDAT5000 IF LESS        * after  end   date
.
.         read the master file to get the clinic type 
.
          CLOSE     OUTMA1A1
          PACK      CFNAME,OSTFILE,FILMA1A1
          CALL      OPOTMA11
.
          UNPACK    OTOSDATE,CCENT,CYEAR,CMON,CDAY
          CALL      DATECONV
.
          PACK      KEY18,OTOSSITE,OTOSCLID,WEEKDAY,OTOSTIME
          CALL      RDMASA1
          BRANCH    OVRCD,GDAT2000          * no master details
.
. ******* update the number of held sessions *******
.
          DISPLAY   *P30:24,OTOSDATE,OTOSCLID,OTOSCTYP,SP3
          MOVE      ONE,HELDNOR             * call update/cancelled routine
          MOVE      ONE,HELDCANC            * update held
.
          UNPACK    OTOSTIME,XBSHR
          PACK      KEY31,OTOSHOSP,OTOSSITE,OTOSSESS,OTOSCTYP,OTOSDATE,XBSHR
          CALL      RDTEMPB
          IF        OVRCD = 1
            UNPACK    KEY31,XBHOSP,XBSITE,XBSESS,XBCTYPE,XBSDATE,XBSHR
            CALL      WRTEMPB
          ELSE
            GOTO      GDAT2200              * same session as before
          ENDIF
.
          CALL      UPOP0000                * update temp file
.
GDAT2200  CLOSE     OUTBOKA1
          PACK      CFNAME,OSTFILE,FILBOKA1
          OPEN      OUTBOKA1,CFNAME
.
          CLOSE     OUTBB1A1
          PACK      CFNAME,OSTFILE,FILBB1A1
          CALL      OPOTBB11
.
          IF        OTCNULET <> 1
            CLOSE     OUTBOKC1
            PACK      CFNAME,OSTFILE,FILBOKC1
            OPEN      OUTBOKC1,CFNAME
          ENDIF
.
. ******* now loop over outbokaf for the above session ******
.
          PACK      KEY28,OTOSSITE,OTOSCLID,OTOSDATE,OTOSTIME,SP3
          CALL      RDSBOKA1
.
GDAT2500  CALL      RDKBOKA1
          BRANCH    OVRCD,GDAT2000          * end of file
          PACK      KEY25,OBASITE,OBACLIN,OBADATE,OBASTRT
          MATCH     KEY25,KEY28
          GOTO      GDAT2000 IF NOT EQUAL   * finished session
.
          COMPARE   FOUR,OBASTAT
          GOTO      GDAT2500 IF LESS        * not attended or DNA so ignore
.
          DISPLAY   *P50:24,*V2LON,OBASLOT
          MOVE      TWO,HELDNOR             * calculate normal values
          CALL      UPOP0000                * update temp file
          GOTO      GDAT2500
.
. ***************************************************
. ******* update number of cancelled sessions *******
. ***************************************************
.
GDAT5000  DISPLAY   *P1:24,*EL,"Obtaining information...."
          CALL      CTPB0000                * create temp file
          CLOSE     OUTCSLA2
          PACK      CFNAME,OSTFILE,FILCSLA2
          OPEN      OUTCSLA2,CFNAME
.
          CLOSE     OUTMA1A1
          PACK      CFNAME,OSTFILE,FILMA1A1
          CALL      OPOTMA11
.
          MOVE      ONE,HELDNOR             * call update/cancelled routine
          MOVE      TWO,HELDCANC            * update cancelled
          PACK      KEY27,SP20,SP20
          CALL      RSOTCSL2
.
GDAT5100  CALL      RKOTCSL2
          BRANCH    OVRCD,GDAT1000
.
          MATCH     CDFRDATE,OTCSDATE
          GOTO      GDAT5100 IF LESS        * before start date
          MATCH     OTCSDATE,CDTODATE
          GOTO      GDAT5100 IF LESS        * after  end date
.
          UNPACK    OTCSDATE,CCENT,CYEAR,CMON,CDAY
          CALL      DATECONV
.
          PACK      KEY18,OTCSSITE,OTCSCLIN,WEEKDAY,OTCSSTRT
          CALL      RDMASA1
          BRANCH    OVRCD,GDAT5100          * no master details
.
          UNPACK    OTCSSTRT,XBSHR
          PACK      KEY31,OTMAHOSP,OTCSSITE,OTMACLSE,OTCSCTYP,OTCSDATE,XBSHR
          CALL      RDTEMPB
          IF        OVRCD = 1
            UNPACK    KEY31,XBHOSP,XBSITE,XBSESS,XBCTYPE,XBSDATE,XBSHR
            CALL      WRTEMPB
          ELSE
            GOTO      GDAT5100              * same session as before
          ENDIF
.
          MOVE      OTMAHOSP,OTOSHOSP       * set hospital code
          MOVE      OMATYP,OTOSCTYP         * set clinic type
          MOVE      OTCSSITE,OTOSSITE       * set site
          MOVE      OTMACLSE,OTOSSESS       * set session id
.
          DISPLAY   *P30:24,OTCSDATE,OTCSCLIN,OTOSCTYP,SP3
          CALL      UPOP0000                * update temp file
          GOTO      GDAT5100
.
. **************************************************
. ******* update the number of A&E attenders *******
. **************************************************
.
GDAT7000  DISPLAY   *P1:24,*EL,"Obtaining information......"
          PACK      KEY21,CDFRDATE,SP30
          CALL      RDSDETA3
.
GDAT7100  CALL      RDKDETA3
          BRANCH    OVRCD,GDAT9999          * end of file
          MATCH     ADADATE,CDTODATE
          GOTO      GDAT9999 IF LESS        * after end date
.
          CALL      UPAE0000                * update A&E
          GOTO      GDAT7100
.
GDAT9999  RETURN
+
.*********************************************************************
.         produce the report
.*********************************************************************
REPT0000  DISPLAY   *P1:24,*EL,"Printing report - ",*V2LON,*BLINKON:
                    "Please wait";
          UNPACK    SP20,XUNIT,XHOSP,XSESS
          MOVE      ONE,XTYPE
          MOVE      ZERO,CPAGENO
          MOVE      "70",CLNO
          MOVE      SP30,KEY21
          CALL      RSTEMPA
.
REPT1000  CALL      RKTEMPA
          BRANCH    OVRCD,REPT9000
.
          MOVE      XTYPE,FORM1
          PERFORM   FORM1,PLIN0000,AAED0000
          GOTO      REPT1000
.
REPT9000  IF        CPAGENO = ZERO
            CALL      HEAD0000
          ENDIF
          PRINT     *N,"*** End of Report ***",*N
          CALL      DTPA0000
          CALL      DTPB0000
.
REPT9999  RETURN
+
.*********************************************************************
.         Enter the date ranges
.*********************************************************************
KDAT0000  DISPLAY   *P19:11,*EL,*P19:12,*EL
.
.         enter starting date
.
KDAT1000  MOVE      "11",CVERT              * row
          MOVE      "19",CCOL               * column
          UNPACK    SP6,CYEAR,CMON,CDAY     * no defaults
          MOVE      CCC,CCENT
.
          CALL      KEYDATE
          BRANCH    OVRCD,KDAT9100          * nothing entered
          BRANCH    CQUEST,KDAT1000
.
          PACK      CDFRDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",CDFRDATE           * set up start date
          CALL      PACDATE
          MOVE      CPCDATE,PRFRDATE        * formatted start date
.
.         enter ending date (default to starting date)
.
KDAT2000  MOVE      "12",CVERT              * row
          MOVE      "19",CCOL               * column
          UNPACK    CDFRDATE,CCENT,CYEAR,CMON,CDAY
.
          CALL      KEYDATE
          BRANCH    OVRCD,KDAT1000          * nothing entered
          BRANCH    CQUEST,KDAT2000
.
          PACK      CDTODATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",CDTODATE           * set up end date
          CALL      PACDATE
          MOVE      CPCDATE,PRTODATE        * formatted end date
.
          MOVE      ZERO,EXIT
          MATCH     CDFRDATE,CDTODATE
          GOTO      KDAT9999 IF NOT LESS    * date is <= start date
.
          DISPLAY   *P1:24,*B,"Ending date must be after Starting date.  ",*EL;
          CALL      HITENTER 
          GOTO      KDAT0000
.
KDAT9100  MOVE      ONE,EXIT
KDAT9999  RETURN
+
.*********************************************************************
.         get all site prefixes
.*********************************************************************
SITE0000  CALL      CTPC0000
          MOVE      SP6,KEY6
          CALL      RDSSITA1
.
SITE1000  CALL      RDKSITA1
          BRANCH    OVRCD,SITE9999
.
          MOVE      OSTFILE,KEY3
          READ      TEMPC,KEY3;ANS
          GOTO      SITE1000 IF NOT OVER    * already on file
.
          WRITE     TEMPC,KEY3;OSTFILE
          GOTO      SITE1000
.
SITE9999  RETURN
+
.*********************************************************************
.         get the variables for the temp file key
.         Para's  : OTOSHOSP      hospital code
.                   OTOSCTYP      clinic type                 
.                   OTOSSESS      session id
.*********************************************************************
UPOP0000  MOVE      OTOSHOSP,KEY3           * get the unit code
          CALL      RDPTHSP1
          BRANCH    OVRCD,UPOP9999          * invalid hospital code
.
.         get specialty
.
...          PACK      KEY5,CATA,OMACIND       * read using clinic type
...          CALL      RDCODE1
...          BRANCH    OVRCD,UPOP9999          * invalid clinic type
.
...          UNPACK    THCSCOD,KEY3,KEY1
...          MATCH     SP1,KEY1
...          IF        @EQUAL
...            PACK      DIM5,THCSCOD,SP5
...          ELSE
...            PACK      DIM5,KEY3,ZERO,KEY1
...          ENDIF
.
.         get clinic type description
.
          CLOSE     OUTCTYA1
          PACK      CFNAME,OSTFILE,FILCTYA1
          OPEN      OUTCTYA1,CFNAME
.
          MOVE      SP30,XCTYPD
          PACK      KEY12,OTOSSITE,OTOSCTYP,SP20
          CALL      RDCTYA1
          IF        OVRCD = 0
            MOVE      OCTDESC,XCTYPD
          ENDIF 
.
.         by unit and clinic type
.
          IF        MOPTION = ONE
            PACK      KEY21,PTHSUNIT,ONE,ZU3,ZU6,OTOSCTYP
            PERFORM   HELDNOR,PHOC0000,PNOR0000
            PACK      KEY21,PTHSUNIT,ONE,ZU3,ZU6,Z6
            PERFORM   HELDNOR,PHOC0000,PNOR0000
            PACK      KEY21,Z5,ONE,ZU3,ZU6,Z6
            PERFORM   HELDNOR,PHOC0000,PNOR0000
          ENDIF
.
.         by unit and site and clinic type
.
          IF        MOPTION = TWO
            PACK      KEY21,PTHSUNIT,ONE,OTOSHOSP,ZU6,OTOSCTYP
            PERFORM   HELDNOR,PHOC0000,PNOR0000
            PACK      KEY21,PTHSUNIT,ONE,OTOSHOSP,ZU6,Z6
            PERFORM   HELDNOR,PHOC0000,PNOR0000
            PACK      KEY21,PTHSUNIT,ONE,Z3,ZU6,Z6
            PERFORM   HELDNOR,PHOC0000,PNOR0000
            PACK      KEY21,Z5,ONE,Z3,ZU6,Z6
            PERFORM   HELDNOR,PHOC0000,PNOR0000
          ENDIF
.
.         by unit and session id and clinic type
.
          IF        MOPTION = THREE
            PACK      KEY21,PTHSUNIT,ONE,ZU3,OTOSSESS,OTOSCTYP
            PERFORM   HELDNOR,PHOC0000,PNOR0000
            PACK      KEY21,PTHSUNIT,ONE,ZU3,OTOSSESS,Z6
            PERFORM   HELDNOR,PHOC0000,PNOR0000
            PACK      KEY21,PTHSUNIT,ONE,ZU3,Z6,Z6
            PERFORM   HELDNOR,PHOC0000,PNOR0000
            PACK      KEY21,Z5,ONE,ZU3,Z6,Z6
            PERFORM   HELDNOR,PHOC0000,PNOR0000
          ENDIF
.
UPOP9999  RETURN
+
.*********************************************************************
.         update temp file for A&E
.*********************************************************************
UPAE0000  MOVE      SP3,OSTFILE
          CALL      CLTV0000                * clear temp file
          PACK      KEY21,COCNDOFT,TWO,Z3,Z6,Z6
          UNPACK    KEY21,XUNIT,XTYPE,XHOSP,XSESS,XCTYPE
          CALL      RDTEMPA
          MOVE      OVRCD,TOVRCD            * save ovrcd
.
          PACK      KEY5,CATEP,ADAPREV
          CALL      RDCODE1
          BRANCH    OVRCD,UPAE9999          * invalid code
.
          MATCH     "1",TCINDC1
          IF        @EQUAL
            ADD       ONE,XFSTATT             * add to first attenders
          ELSE   
            ADD       ONE,XFLLATT             * add to follow up attenders
          ENDIF
.
          IF        TOVRCD = ONE
            CALL      WRTEMPA
          ELSE
            CALL      UPTEMPA
          ENDIF
.
UPAE9999  RETURN
+
.*********************************************************************
.         update temp file values
.*********************************************************************
PHOC0000  CALL      CLTV0000                * clear temp file vars
          UNPACK    KEY21,XUNIT,XTYPE,XHOSP,XSESS,XCTYPE
          CALL      RDTEMPA
          MOVE      OVRCD,TOVRCD            * save ovrcd
.
          IF        HELDCANC = ONE
            ADD       ONE,XCSHELD             * update number held
          ELSE
            ADD       ONE,XCSCANC             * update number cancelled
          ENDIF
.
          IF        TOVRCD = ONE
            CALL      WRTEMPA
          ELSE
            CALL      UPTEMPA
          ENDIF
.
PHOC9999  RETURN
+
.*********************************************************************
.         update temp file values
.*********************************************************************
PNOR0000  CALL      CLTV0000                * clear temp file vars
          UNPACK    KEY21,XUNIT,XTYPE,XHOSP,XSESS,XCTYPE
          CALL      RDTEMPA
          MOVE      OVRCD,TOVRCD            * save ovrcd value
.
          MOVE      OBAOUTNO,KEY8
          CALL      RDBOKB1
          BRANCH    OVRCD,PNOR9999          * no booking details
.
          MOVE      OBASITE,XSITE           * OP Site Code
.
          PACK      KEY5,CATCV,OBAVISIT
          CALL      RDCODE1
          BRANCH    OVRCD,PNOR9999          * invalid visit type
.
. ------- determine if new (referral attendances) or review (consultant iniatit)
.
          MOVE      TWO,REFCONS             * default to review
          MATCH     ANSN,TCINDC1
          IF        @EQUAL
            MOVE      ONE,REFCONS             * set as referral
          ENDIF
.
. ------- determine if Seen or DNA or none -------
.
          MOVE      ZERO,SEENDNA
          LOAD      SEENDNA,OBASTAT,ZERO,ZERO,ZERO,ONE,TWO
.
. ------- see if a referral -------
.
          MOVE      OBAOUTNO,KEY8
          IF        OTCNULET = 1
            CALL      RDOTRFL1              * read outrf1af for referral
            BRANCH    OVRCD,PNOR1000        * not a referral
          ELSE
            CALL      RDOTBOC1              * read outbokcf for referral
            BRANCH    OVRCD,PNOR1000        * not a referral
          ENDIF
.
          ADD       ONE,XGPREFL             * set as a referral
.
. ------- see if a private patient -------
.
PNOR1000  PACK      KEY5,CATCL,OBACOMP
          CALL      RDCODE1
          BRANCH    OVRCD,PNOR9999
          MATCH     ANSP,TCINDC1
          IF        @EQUAL & OBASTAT = FOUR
            ADD       ONE,XPPATTD             * a private patient
          ENDIF
.
. ------- now update the main values -------
.
          IF        REFCONS = ONE
            LOAD      FORM6,SEENDNA,XWCRASN,XWCRADN   * Referral Attendance
            ADD       ONE,FORM6
            STORE     FORM6,SEENDNA,XWCRASN,XWCRADN
          ELSE
            LOAD      FORM6,SEENDNA,XWCCASN,XWCCADN   * Consulant Attendance
            ADD       ONE,FORM6
            STORE     FORM6,SEENDNA,XWCCASN,XWCCADN
          ENDIF
.
          IF        TOVRCD = ONE
            CALL      WRTEMPA
          ELSE
            CALL      UPTEMPA
          ENDIF
.
PNOR9999  RETURN
+
.*********************************************************************
.         print the A&E details
.*********************************************************************
AAED0000  MATCH     SP5,XUNIT
          GOTO      AAED9999 IF EQUAL       * no unit to print
          MATCH     Z5,XUNIT
          GOTO      AAED9999 IF EQUAL       * no unit to print
.
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS    * print new page
.
          ASSIGN    (XFSTATT+XFLLATT),FORM8
          PRINT     *40,"Unit : ",XUNIT
          PRINT     *N,"Part 2 : Accident & Emergency Services activity"
          PRINT     *1,"*-----------------------------------------------------*"
          PRINT     *1,"| First Attendance | Follow Up Attendance |    Total  |"
          PRINT     *1,"*-----------------------------------------------------*"
          PRINT     *1,"|        ",XFSTATT,"    |        ",XFLLATT:
                       "        | ",FORM8,"  |"
          PRINT     *1,"*-----------------------------------------------------*"
          MOVE      "70",CLNO
.
AAED9999  RETURN
+
.*********************************************************************
.         clear the temp file variables
.*********************************************************************
CLTV0000  MOVE      ZERO,XCSHELD
          MOVE      ZERO,XCSCANC
          MOVE      ZERO,XWCRASN
          MOVE      ZERO,XWCRADN
          MOVE      ZERO,XWCCASN
          MOVE      ZERO,XWCCADN
          MOVE      ZERO,XOCRASN
          MOVE      ZERO,XOCCASN
          MOVE      ZERO,XGPREFL
          MOVE      ZERO,XPPATTD
          MOVE      OSTFILE,XPRFIX
          MOVE      ZERO,XFSTATT
          MOVE      ZERO,XFLLATT
          MOVE      SP70,XSITE
.
CLTV9999  RETURN
+
.*********************************************************************
.         print a line of the report
.*********************************************************************
PLIN0000  MOVE      ZERO,PRUNDLN
          MATCH     Z5,XUNIT
          GOTO      PLIN9999 IF EQUAL       * Grand Total
          MATCH     Z3,XHOSP
          GOTO      PLIN5000 IF EQUAL       * Unit total
          MATCH     Z6,XSESS
          GOTO      PLIN5000 IF EQUAL       * Hospital total
          MATCH     Z6,XCTYPE
          GOTO      PLIN4000 IF EQUAL       * Unit/Hospital/Session Id total
.
. ------- normal line -------
.
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS    * print new page
.
          GOTO      PLIN9000
.
. ------- site/session id total -------
.
PLIN4000  IF        MOPTION = ONE
            MOVE      "Total for Unit      ",XCTYPD
          ENDIF
          IF        MOPTION = TWO
            MOVE      "Total for Hospital  ",XCTYPD
          ENDIF
          IF        MOPTION = THREE
            MOVE      "Total for Session Id",XCTYPD
          ENDIF
          MOVE      ONE,PRUNDLN             * print underline after details
          MOVE      "70",CLNO               * set for new page
          CALL      UNLN0000                * print an underline
          GOTO      PLIN9000
.
. ------- unit total -------
.
PLIN5000  MOVE      "Total for Unit",XCTYPD
          MOVE      ONE,PRUNDLN             * print underline after details
          MOVE      "70",CLNO               * set for new page
          GOTO      PLIN9000
.
. ------- grand total -------
.
PLIN6000  MOVE      "Grand Total",XCTYPD
          MOVE      ONE,PRUNDLN             * print underline after details
          GOTO      PLIN9000
.
. ------- print the line -------
.
PLIN9000  PRINT     *1,"| ",XCTYPD," | ":
                    XCSHELD," | ",XCSCANC," | ",XWCRASN," | ",XWCRADN," | ":
                    XWCCASN," | ",XWCCADN," | ",XOCRASN," | ",XOCCASN," | ":
                    SP2,XGPREFL," | ",XPPATTD," |"
          ADD       ONE,CLNO
          PERFORM   PRUNDLN,UNLN0000
          MOVE      SP5,XUNIT               * clear the unit code
.
PLIN9999  RETURN
+
.*********************************************************************
.         print page heading
.*********************************************************************
HEAD0000  CALL      HEAD132
          PRINT     *40,"Date : ",PRFRDATE," to ",PRTODATE
          ADD       ONE,CLNO
          MATCH     "2",XTYPE
          GOTO      HEAD9999 IF EQUAL       * dont do for A&E section
.
          PRINT     *40,"Unit : ",XUNIT
          PRINT     *N,"Part 1 : Consultant/GP Outpatient clinical activity"
          IF        MOPTION = TWO
            MOVE      "Invalid Code",PTHSNAME
            MOVE      XHOSP,KEY3
            CALL      RDPTHSPA1
            PRINT     *N,"Hospital : ",XHOSP,SP5,PTHSNAME
          ENDIF
          IF        MOPTION = THREE
            CALL      GSID0000
            PRINT     *N,"Session Id : ",XSESS,SP5,KEY30
          ENDIF
          CALL      UNLN0000
          PRINT     *1,"|                              |                 |":
                    "    WITHIN   CLINIC   SESSIONS     | OUTSIDE SESSION |":
                    "          |        |"
          PRINT     *1,"|                              |                 |":
                    "-----------------------------------|-----------------|":
                    "          |        |"
          PRINT     *1,"|                              |  Clinic Sess'ns |":
                    "  Referral Att.  |  Consultant Att | Referr | Consul |":
                    "   G.P.   |  Priv. |"
          PRINT     *1,"|                              |-----------------|":
                    "-----------------|-----------------|--------|--------|":
                    " Written  |  Pat's |"
          PRINT     *1,"| Clinic Type                  |  Held  | Canc.  |":
                    "  Seen  | D.N.A. |  Seen  | D.N.A. |  Seen  |  Seen  |":
                    " Referral |  Seen  |"
          CALL      UNLN0000
          MOVE      TEN,CLNO
.
HEAD9999  RETURN
+
.*********************************************************************
.         print an underline
.*********************************************************************
UNLN0000  PRINT     *1,"*-------------------------------------":
                       "--------------------------------------":
                       "-----------------------------------------------*"
          ADD       ONE,CLNO
UNLN9999  RETURN
+
.*********************************************************************
.         get the session id description
.*********************************************************************
GSID0000  MOVE      SP30,KEY30
          PACK      CFNAME,XPRFIX,FILCLIA1
          OPEN      OUTCLIA1,CFNAME
.
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
.
          PACK      KEY20,XSITE,XSESS,KEY8
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     XSITE,OCASITE
               IF        !@EQUAL
                MOVE      ONE,OVRCD
              ENDIF
.
              MATCH     XSESS,OCACLIN
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ENDIF
            ENDIF
          ENDIF
          CLOSE     OUTCLIA1
          IF        OVRCD = 1
            MOVE      "Unknown clinic",OCADESC
            PACK      OCADESC,OCADESC,SP30
          ENDIF
          BRANCH    OVRCD,GSID9999
.
          MOVE      OCADESC,KEY30
          MATCH     SP6,OCADOCT
          GOTO      GSID9999 IF EQUAL       * no doctor code
.
          MOVE      OCADOCT,KEY6
GSID5000  CALL      RDDOCT1
          BRANCH    OVRCD,GSID9999          * invalid code
.
          MOVE      DSNAME,PACSNAME
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,KEY30
.
GSID9999  RETURN
+
.*********************************************************************
.         Create a temp file
.*********************************************************************
CTPA0000  CALL      DTPA0000                * delete temp file
          CLEAR     CMDLINE                 * clear command line
          PACK      CMDLINE,ISBUILD,CFNAMEA,KEYA,SP30      * set command
          RESET     CMDLINE                 * reset FP
          EXECUTE   CMDLINE,TASKID          * build the temp file
          OPEN      TEMPA,CFNAMEA           * open the temp file
CTPA9999  RETURN
.*********************************************************************
.         Create a temp file
.*********************************************************************
CTPB0000  CALL      DTPB0000                * delete temp file
          CLEAR     CMDLINE                 * clear command line
          PACK      CMDLINE,ISBUILD,CFNAMEB,KEYB,SP30      * set command
          RESET     CMDLINE                 * reset FP
          EXECUTE   CMDLINE,TASKID          * build the temp file
          OPEN      TEMPB,CFNAMEB           * open the temp file
CTPB9999  RETURN
+
.*********************************************************************
.         Create a temp file
.*********************************************************************
CTPC0000  CLEAR     CMDLINE                 * clear command line
          PACK      CMDLINE,ISBUILD,CFNAMEC,KEYS,SP30      * set command
          RESET     CMDLINE                 * reset FP
          EXECUTE   CMDLINE,TASKID          * build the temp file
          OPEN      TEMPC,CFNAMEC           * open the temp file
CTPC9999  RETURN
+
.*********************************************************************
.         Delete temp file 
.*********************************************************************
DTPB0000  CLOSE     TEMPB                   * close temp file
          CLEAR     CMDLINE                 * clear command line
          PACK      CMDLINE,ISERASE,CFNAMEB * set command line for erase
          RESET     CMDLINE                 * reset FP
          EXECUTE   CMDLINE,TASKID          * delete the temp file
DTPB9999  RETURN
+
.*********************************************************************
.         Delete temp file 
.*********************************************************************
DTPA0000  CLOSE     TEMPA                   * close temp file
          CLEAR     CMDLINE                 * clear command line
          PACK      CMDLINE,ISERASE,CFNAMEA * set command line for erase
          RESET     CMDLINE                 * reset FP
          EXECUTE   CMDLINE,TASKID          * delete the temp file
DTPA9999  RETURN
+
.*********************************************************************
.         Delete temp file 
.*********************************************************************
DTPC0000  CLOSE     TEMPC                   * close temp file
          CLEAR     CMDLINE                 * clear command line
          PACK      CMDLINE,ISERASE,CFNAMEC * set command line for erase
          RESET     CMDLINE                 * reset FP
          EXECUTE   CMDLINE,TASKID          * delete the temp file
DTPC9999  RETURN
+
.*********************************************************************
.         temp file I/O
.*********************************************************************
RSTEMPA   READ      TEMPA,KEY21;;
          RETURN
+
RDTEMPA   MOVE      ZERO,OVRCD
          READ      TEMPA,KEY21;XUNIT,XTYPE,XHOSP,XSESS,XCTYPE:
                                DXCSHELD,DXCSCANC,DXWCRASN,DXWCRADN,DXWCCASN:
                                DXWCCADN,DXOCRASN,DXOCCASN,DXGPREFL,DXPPATTD:
                                XPRFIX,XFSTATT,XFLLATT,XCTYPD,XSITE
          GOTO      OVERCOND IF OVER
          MOVE      DXCSHELD,XCSHELD
          MOVE      DXCSCANC,XCSCANC
          MOVE      DXWCRASN,XWCRASN
          MOVE      DXWCRADN,XWCRADN
          MOVE      DXWCCASN,XWCCASN
          MOVE      DXWCCADN,XWCCADN
          MOVE      DXOCRASN,XOCRASN
          MOVE      DXOCCASN,XOCCASN
          MOVE      DXGPREFL,XGPREFL
          MOVE      DXPPATTD,XPPATTD
          MOVE      DXFSTATT,XFSTATT
          MOVE      DXFLLATT,XFLLATT
          RETURN
+
RKTEMPA   MOVE      ZERO,OVRCD
          READKS    TEMPA;XUNIT,XTYPE,XHOSP,XSESS,XCTYPE:
                          DXCSHELD,DXCSCANC,DXWCRASN,DXWCRADN,DXWCCASN:
                          DXWCCADN,DXOCRASN,DXOCCASN,DXGPREFL,DXPPATTD:
                          XPRFIX,XFSTATT,XFLLATT,XCTYPD,XSITE
          GOTO      OVERCOND IF OVER
          MOVE      DXCSHELD,XCSHELD
          MOVE      DXCSCANC,XCSCANC
          MOVE      DXWCRASN,XWCRASN
          MOVE      DXWCRADN,XWCRADN
          MOVE      DXWCCASN,XWCCASN
          MOVE      DXWCCADN,XWCCADN
          MOVE      DXOCRASN,XOCRASN
          MOVE      DXOCCASN,XOCCASN
          MOVE      DXGPREFL,XGPREFL
          MOVE      DXPPATTD,XPPATTD
          MOVE      DXFSTATT,XFSTATT
          MOVE      DXFLLATT,XFLLATT
          RETURN
+
WRTEMPA   MOVE      XCSHELD,DXCSHELD
          MOVE      XCSCANC,DXCSCANC
          MOVE      XWCRASN,DXWCRASN
          MOVE      XWCRADN,DXWCRADN
          MOVE      XWCCASN,DXWCCASN
          MOVE      XWCCADN,DXWCCADN
          MOVE      XOCRASN,DXOCRASN
          MOVE      XOCCASN,DXOCCASN
          MOVE      XGPREFL,DXGPREFL
          MOVE      XPPATTD,DXPPATTD
          MOVE      XFSTATT,DXFSTATT
          MOVE      XFLLATT,DXFLLATT
          WRITE     TEMPA,KEY21;XUNIT,XTYPE,XHOSP,XSESS,XCTYPE:
                                DXCSHELD,DXCSCANC,DXWCRASN,DXWCRADN,DXWCCASN:
                                DXWCCADN,DXOCRASN,DXOCCASN,DXGPREFL,DXPPATTD:
                                XPRFIX,XFSTATT,XFLLATT,XCTYPD,XSITE
          RETURN
+
UPTEMPA   MOVE      XCSHELD,DXCSHELD
          MOVE      XCSCANC,DXCSCANC
          MOVE      XWCRASN,DXWCRASN
          MOVE      XWCRADN,DXWCRADN
          MOVE      XWCCASN,DXWCCASN
          MOVE      XWCCADN,DXWCCADN
          MOVE      XOCRASN,DXOCRASN
          MOVE      XOCCASN,DXOCCASN
          MOVE      XGPREFL,DXGPREFL
          MOVE      XPPATTD,DXPPATTD
          MOVE      XFSTATT,DXFSTATT
          MOVE      XFLLATT,DXFLLATT
          UPDATE    TEMPA;XUNIT,XTYPE,XHOSP,XSESS,XCTYPE:
                          DXCSHELD,DXCSCANC,DXWCRASN,DXWCRADN,DXWCCASN:
                          DXWCCADN,DXOCRASN,DXOCCASN,DXGPREFL,DXPPATTD:
                          XPRFIX,XFSTATT,XFLLATT,XCTYPD,XSITE
          RETURN
+
RDTEMPB   MOVE      ZERO,OVRCD
          READ      TEMPB,KEY31;XBHOSP,XBSITE,XBSESS,XBCTYPE,XBSDATE,XBSHR
          GOTO      OVERCOND IF OVER
          RETURN
+
WRTEMPB   WRITE     TEMPB,KEY31;XBHOSP,XBSITE,XBSESS,XBCTYPE,XBSDATE,XBSHR
          RETURN
+
          INC       STD001IO
          INC       AAEDE1IO/INC
          INC       DATECONV
          INC       IBASEQIO/INC
          INC       OUTCSLIO/INC
          INC       OUTBOAIO/INC
          INC       OUTBB1IO/INC
          INC       OUTBOCIO/INC
          INC       OUTCLIIO/INC
          INC       OUTMA1IO/INC
          INC       OUTSITIO/INC
          INC       OUTOSFIO/INC
          INC       OUTCTYIO/INC
          INC       OUTRF1IO/INC
          INC       PATHSPIO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       TFILENAM
          INC       WEBERRIO/INC

............................................................................

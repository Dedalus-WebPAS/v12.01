.-------------------------------------------------------
. V9.03.01  10.09.2003 Steve Downing (VF.11.01 CAR 9375)
.           Recompiled for SINREBFD & SINREBIO
.-------------------------------------------------------
. V9.02.00  12.07.2002   G.Saunders   Port to v9 release
.           Port program from vf.09 to v9.02
.-------------------------------------------------------
. VF.01.01  18.07.2000 Charles Handaya
.           Recompiled for SINBACIO change
.           Related to Quote 9817
.----------------------------------------------------------------------
. VF.00.01  29.12.1999 Charles Handaya
.           Recompiled for Fixing Audit Date (CCYYMMDD)
. VF.00.00  15.01.1999 Sandra Barcham
.           Year 2000 modifications
.----------------------------------------------------------------------
          INC       IBAWEBDF    
          INC       PATCONFD/INC
          INC       SINCONFD/INC
          INC       SINCCAFD/INC
          INC       SINCCBFD/INC
          INC       SINCIAFD/INC
          INC       SINCSEFD/INC
          INC       SINPOAFD/INC
          INC       SINPOCFD/INC
          INC       SINPERFD/INC
          INC       SINBACFD/INC
          INC       SINREAFD/INC
          INC       SINREBFD/INC
.
BGRED     INIT      "#"##FF0000#""
BGGREEN   INIT      "#"##00FF00#""
YTDBGC    DIM       9
CURBGC    DIM       9
NEXTLINK  DIM       127
LASTLINK  DIM       127
MAXVALUE  FORM      10
STEPVAL   FORM      10
INDEX2    FORM      2
REPTYEAR   DIM      6 
.
YTDBUD    FORM      12.2
YTDAMT    FORM      12.2
VARCUR    FORM      12.2
VARYTD    FORM      12.2
PVRCUR    FORM      12.1
PVRYTD    FORM      12.1
PERNAME   DIM       10
PERNAME3  DIM       3
.
COSTCENT  DIM       5
F10P2     FORM      10.2
.                         * Report Parameters
STRDAT    DIM       8  
ENDDAT    DIM       8
STRDOC    DIM       6
ENDDOC    DIM       6
.
REPOPTS   DIM       50
DATRAN    DIM       16
.
COUNT     FORM      4                      * length read FIFO
JAVASCRP  FILE      ASCII, FIXED=250
.
PARANUMB  FORM      4
.
PRGID     INIT      "SINWEB01"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "Supply & Inventory Information Server"
.
          CALL      WEBINT00    * Initialise Fifo Etc.
          CALL      INIT0000    * Open Files
.
MAIN1000  CALL      WEBRED00    * Read Fifo WEBPID and USERID
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200,MAIN1300,MAIN1400
.
          MOVE      "INVALID OPTION",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  CALL      EXPORD00   * Expected Orders
          GOTO      MAIN1000
.
MAIN1200  CALL      BACORD00   * Back Order Summary
          GOTO      MAIN1000
.
MAIN1300  CALL      STKUSE00   * Stock Usage
          GOTO      MAIN1000
.
MAIN1400  CALL      STKREQ00   * Stock Requisitions
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
.
PROFLD00  RETURN
.------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------
INIT0000  OPEN      JAVASCRP,"barchart/js"
          OPEN      CONTROLF,"controlf"
          OPEN      SINCIAA1,"sinciaaf"
          OPEN      SINCCAA1,"sinccaaf"
          OPEN      SINCCBA1,"sinccbaf"
          OPEN      SINCSEA1,"sincseaf"
          OPEN      SINPOAA1,"sinpoaaf"
          OPEN      SINPOCA5,"sinpocaf"
          OPEN      SINBACA1,"sinbacaf"
          OPEN      SINPERA1,"sinperaf"
          OPEN      SINREAA4,"sinreaaf"
          OPEN      SINREBA1,"sinrebaf"
          MOVE      "1",MSGLEN
          READ      CONTROLF,TWENTY4;HAPER1,HAPER2,HAPER3,HAPER4:
                                     HAPER5,HAPER6,HAPER7,HAPER8:
                                     HAPER9,HAPER10,HAPER11,HAPER12
          RETURN
.------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          MOVE      "000",TEMPLATE
          MOVE      SP70,COSTCENT
          MOVE      SP70,REPTYEAR
          RETURN
.------------------------------------------------------------
. Set Parameters
.------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "costcent=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,COSTCENT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "reptyear=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPTYEAR
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
.------------------------------------------------------------
. List Available Cost Centre
.------------------------------------------------------------
GETCST00  MOVE      ZERO,EXIT
          MATCH     SP70,COSTCENT
          GOTO      GETCST99 IF NOT EQUAL
          MOVE      ZERO,COUNT
          PACK      KEY9,PASSCODE,SP70
          CALL      RSSICS1
GETCST10  CALL      RKSICS1
          BRANCH    OVRCD,GETCST90
          MATCH     PASSCODE,SICSUID
          GOTO      GETCST90 IF NOT EQUAL
          ADD       ONE,COUNT
          MOVE      SICSCST,COSTCENT
          COMPARE   TWO,COUNT
          GOTO      GETCST10 IF NOT EQUAL
.
          CALL      LSTCST00
          MOVE      ONE,EXIT
          GOTO      GETCST99
.
GETCST90  COMPARE   ZERO,COUNT
          GOTO      GETCST95 IF EQUAL
          MOVE      ZERO,EXIT
          GOTO      GETCST99
.
GETCST95  MOVE      "INVALID SECURITY **",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      GETCST99
GETCST99  RETURN
.------------------------------------------------------------
. List Selections for Cost Centre
.------------------------------------------------------------
LSTCST00  MOVE      "Cost Centre Selection Table",WEBTITLE
          CALL      WEBHED00
          WRITE     HTMLFILE;"<table border=1><tr bgcolor=#FFFF00>":
                             "<td align=center>Select Cost Centre</td><tr>"
          PACK      KEY9,PASSCODE,SP70
          CALL      RSSICS1
LSTCST10  CALL      RKSICS1
          BRANCH    OVRCD,GETCST90
          MATCH     PASSCODE,SICSUID
          GOTO      LSTCST90 IF NOT EQUAL
          MOVE      TEMPLATE,F3
          MOVE      F3,KEY3
          REP       " 0",KEY3
          CLEAR     HTMLLINK
          APPEND    "sinweb01.pbl?reportno=",HTMLLINK
          MOVE      REPORTNO,F1
          APPEND    F1,HTMLLINK
          APPEND    "&template=",HTMLLINK
          APPEND    KEY3,HTMLLINK
          APPEND    "&costcent=",HTMLLINK
          MOVE      SICSCST,KEY5
          REP       " +",KEY5
          APPEND    KEY5,HTMLLINK
          RESET     HTMLLINK
          MOVE      SICSCST,KEY5
          CALL      RDSICA1
          WRITE     HTMLFILE;"<tr><td>":
                             "<a href=",HTMLLINK,">":
                             SICADESC,"</a></td></tr>"
          GOTO      LSTCST10
.
LSTCST90  WRITE     HTMLFILE;"</table>"
          CALL      WEBEND00
.
LSTCST99  RETURN
.------------------------------------------------------------
. List Expected Order for a Cost Centre
.------------------------------------------------------------
EXPORD00  CALL      GETCST00
          BRANCH    EXIT,EXPORD99
.
          PACK      KEY9,PASSCODE,COSTCENT,SP70
          CALL      RDSICS1
          BRANCH    OVRCD,EXPORD95
.
          MOVE      COSTCENT,KEY5
          CALL      RDSICA1
          CLEAR     WEBTITLE
          APPEND    "Outstanding Orders for ",WEBTITLE
          APPEND    SICADESC,WEBTITLE
          RESET     WEBTITLE
          CALL      WEBHED00
.
          WRITE     HTMLFILE;"<table border=1 width=#"100%#">"
          WRITE     HTMLFILE;"<tr bgcolor=#FFFF00>":
                              "<td align=center colspan=5><b>":
                              "Orders Outstanding for ":
                              SICADESC,"</b></td></tr>":
                               "<tr bgcolor=#FFFF00>":
                               "<td align=center><b>Expected Delivery</b></td>":
                               "<td><b>Description</b></td>":
                               "<td align=right><b>Order No</b></td>":
                               "<td align=right><b>Quantity</b></td>":
                               "<td align=right><b>Expected Cost</b></td>":
                               "</tr>"
.
          PACK      KEY23,COSTCENT,ONE,SP70
          CALL      RSSIPC5 
EXPORD10  CALL      RKSIPC5 
          BRANCH    OVRCD,EXPORD90
          MATCH     SIPCCST,COSTCENT
          GOTO      EXPORD90 IF NOT EQUAL
          MATCH     SP70,SIPCEDD
          GOTO      EXPORD10 IF EQUAL
          MATCH     "00000000",SIPCEDD
          GOTO      EXPORD10 IF EQUAL
.
          PACK      KEY7,SIPCPON
          CALL      RDSIPA1
          BRANCH    OVRCD,EXPORD10
.
          MATCH     SP70,SIPADCM
          GOTO      EXPORD20 IF EQUAL
          MATCH     "00000000",SIPADCM
          GOTO      EXPORD20 IF EQUAL
          GOTO      EXPORD10
.        
EXPORD20  UNPACK    SIPCEDD,CCENT,CYEAR,CMON,CDAY
          ASSIGN    SIPCOQT*SIPCECT,F10P2
          MOVE      F10P2,KEY13
          IF        SIPCECT=-1
            MOVE      "       T.B.A.",KEY13
          ENDIF
          WRITE     HTMLFILE;"<tr><td align=center>":
                                 CDAY,"/",CMON,"/",CCENT,CYEAR,"</td>":
                                 "<td>",SIPCPD,"</td>":
                                 "<td align=right>",SIPCPON,"</td>":
                                 "<td align=right>",SIPCOQT,"</td>":
                                 "<td align=right>",KEY13,"</td>":
                                 "</tr>"
          GOTO      EXPORD10
.
EXPORD90  WRITE     HTMLFILE;"</table>"
          CALL      WEBEND00
          GOTO      EXPORD99
.
EXPORD95  MOVE      "INVALID SECURITY",WEBTITLE
          CALL      WEBERR00
          GOTO      EXPORD99
.
EXPORD99  RETURN
.------------------------------------------------------------
. List Back Orders for a Cost Centre
.------------------------------------------------------------
BACORD00  CALL      GETCST00
          BRANCH    EXIT,BACORD99
.
          PACK      KEY9,PASSCODE,COSTCENT,SP70
          CALL      RDSICS1
          BRANCH    OVRCD,BACORD95
.
          MOVE      COSTCENT,KEY5
          CALL      RDSICA1
          CLEAR     WEBTITLE
          APPEND    "Backorders for ",WEBTITLE
          APPEND    SICADESC,WEBTITLE
          RESET     WEBTITLE
          CALL      WEBHED00
.
          WRITE     HTMLFILE;"<table border=1 width=#"100%#">"
          WRITE     HTMLFILE;"<tr bgcolor=#FFFF00>":
                               "<td align=center colspan=6><B>":
                               SICADESC,"</b></td></tr>":
                               "<tr bgcolor=#FFFF00>":
                               "<td><b>Catalog</b></td>":
                               "<td><b>Cat No</b></td>":
                               "<td><b>Date</b></td>":
                               "<td><b>Req No</b></td>":
                               "<td align=right><b>Quantity</b></td>":
                               "<td align=right><b>Cost</b></td>":
                               "</tr>"
.
          PACK      KEY25,COSTCENT,SP70
          CALL      RSSIBA1 
BACORD10  CALL      RKSIBA1 
          BRANCH    OVRCD,BACORD90
          MATCH     SIBACST,COSTCENT
          GOTO      BACORD90 IF NOT EQUAL
          MOVE      SIBACAT,KEY7
          CALL      RDSIIA1
          BRANCH    OVRCD,BACORD10
          UNPACK    SIBADAT,CCENT,CYEAR,CMON,CDAY
          ASSIGN    SIIAAVC*SIBAQTY,F10P2
          WRITE     HTMLFILE;"<tr><td>",SIIADES,"</td>":
                                 "<td>",SIIACAT,"</td>":
                                 "<td align=center>":
                                 CDAY,"/",CMON,"/",CCENT,CYEAR,"</td>":
                                 "<td>",SIBAREQ,"</td>":
                                 "<td align=right>",SIBAQTY,"</td>":
                                 "<td align=right>",F10P2,"</td>":
                                 "</a></tr>"
          GOTO      BACORD10
.
BACORD90  WRITE     HTMLFILE;"</table>"
          CALL      WEBEND00
          GOTO      BACORD99
.
BACORD95  MOVE      "INVALID SECURITY",WEBTITLE
          CALL      WEBERR00
          GOTO      BACORD99
.
BACORD99  RETURN
.------------------------------------------------------------
. Add Javascript for Bar Chart
.------------------------------------------------------------
JSBC0000  OPEN      JAVASCRP,"barchart/js"
.
JSBC1000  READ      JAVASCRP,SEQ;LINE127
          GOTO      JSBC9999 IF OVER
          WRITE     HTMLFILE;LINE127
          MATCH     "var chart = new Chart",LINE127   * Insert Chart Details
          GOTO      JSBC2000 IF EQUAL
          MATCH     "var data = new Array()",LINE127  * Insert Chart Data
          GOTO      JSBC3000 IF EQUAL
          GOTO      JSBC1000
.
JSBC2000  CALL      STKHED00
          GOTO      JSBC1000
.
JSBC3000  CALL      STKDAT00
          GOTO      JSBC1000
.
JSBC9999  RETURN
.------------------------------------------------------------
. Stock Usage 
.------------------------------------------------------------
STKUSE00  CALL      GETCST00
          BRANCH    EXIT,STKUSE99
.
          PACK      KEY9,PASSCODE,COSTCENT,SP70
          CALL      RDSICS1
          BRANCH    OVRCD,STKUSE95
.
          MOVE      COSTCENT,KEY5
          CALL      RDSICA1
          CLEAR     WEBTITLE
          APPEND    "Stock Usage for ",WEBTITLE
          APPEND    SICADESC,WEBTITLE
          RESET     WEBTITLE
          CALL      WEBHED00
          MATCH     SP70,REPTYEAR
          IF        @EQUAL
            PACK      KEY11,SICACODE,Z70
            CALL      RSSICB1
            CALL      RPSICB1
            MOVE      SICBDAT,REPTYEAR
          ENDIF
.
          CALL      JSBC0000
.
          MOVE      TEMPLATE,F3
          MOVE      F3,KEY3
          REP       " 0",KEY3
          CLEAR     NEXTLINK
          APPEND    "/cgi-bin/sinweb01.pbl?reportno=3&template=",NEXTLINK
          APPEND    KEY3,NEXTLINK
          APPEND    "&costcent=",NEXTLINK
          MOVE      COSTCENT,KEY5
          REP       " +",KEY5
          APPEND    KEY5,NEXTLINK
          APPEND    "&reptyear=",NEXTLINK
          MOVE      REPTYEAR,F4
          ADD       ONE,F4
          MOVE      F4,KEY4
          REP       " 0",KEY4
          APPEND    KEY4,NEXTLINK
          RESET     NEXTLINK
.
          CLEAR     LASTLINK
          APPEND    "/cgi-bin/sinweb01.pbl?reportno=3&template=",LASTLINK
          APPEND    KEY3,LASTLINK
          APPEND    "&costcent=",LASTLINK
          MOVE      COSTCENT,KEY5
          REP       " +",KEY5                                                  
          APPEND    KEY5,LASTLINK
          APPEND    "&reptyear=",LASTLINK
          MOVE      REPTYEAR,F4
          SUB       ONE,F4
          MOVE      F4,KEY4
          REP       " 0",KEY4
          APPEND    KEY4,LASTLINK
          RESET     LASTLINK                                                   
.
          MOVE      REPTYEAR,KEY4
          MOVE      KEY4,F4
          WRITE     HTMLFILE;"<table border=1 width=#"100%#" >"
          WRITE     HTMLFILE;"<tr bgcolor=#FFFF00>":
                               "<td align=center":
                               " bordercolordark=#FFFFFF ":
                               " bordercolorlight=#000000>":
                               "<a href=",LASTLINK,">":
                               "Previous Year":
                               "</a></td>":
                               "<td align=center colspan=7><B>":
                               "Cost Centre ",SICADESC:
                               " Financial Year ",KEY4,"</b></td>":
                               "<td align=center":
                               " bordercolordark=#FFFFFF ":
                               " bordercolorlight=#000000>":
                               "<a href=",NEXTLINK,">":
                               "Next Year":
                               "</a></td>":
                               "</tr>":
                               "<tr bgcolor=#FFFF00>":
                                 "<td align=right><B>Actual</b></td>":
                                 "<td align=right><b>Budget</b></td>":
                                 "<TD ALIGN=RIGHT><B>Variance</b></td>":
                                 "<TD ALIGN=RIGHT><B>%Var</b></td>":
                                 "<TD><B>Period</b></td>":
                                 "<TD ALIGN=RIGHT><B>YTD Actual</b></td>":
                                 "<td align=right><b>YTD Budget</b></td>":
                                 "<td align=right><b>Variance</b></td>":
                                 "<td align=right><b>%Var</b></td>":
                                 "</tr>"
.
          MOVE      ZERO,YTDAMT
          MOVE      ZERO,YTDBUD
.
          MOVE      ZERO,F2
STKUSE10  ADD       ONE,F2
          IF        F2>12
            GOTO      STKUSE90
          ENDIF
          MOVE      REPTYEAR,KEY4
          PACK      SICBDAT,KEY4,F2
          REP       " 0",SICBDAT
          PACK      KEY11,SICACODE,SICBDAT,SP70
          CALL      RDSICB1
          IF        OVRCD=1
            MOVE      ZERO,SICBBUD
            MOVE      ZERO,SICBAMT
          ENDIF
.
          LOAD      PERNAME,F2,HAPER1,HAPER2,HAPER3,HAPER4:
                               HAPER5,HAPER6,HAPER7,HAPER8:
                               HAPER9,HAPER10,HAPER11,HAPER12
          ASSIGN    SICBAMT-SICBBUD,VARCUR
          ASSIGN    (VARCUR/SICBBUD)*100.0,PVRCUR
          ADD       SICBBUD,YTDBUD
          ADD       SICBAMT,YTDAMT
          ASSIGN    YTDAMT-YTDBUD,VARYTD
          ASSIGN    (VARYTD/YTDBUD)*100.0,PVRYTD
          MULT      SEQ,PVRYTD
          MULT      SEQ,VARYTD
          MOVE      BGRED,YTDBGC
          IF        VARYTD>=0
            MOVE      BGGREEN,YTDBGC
          ENDIF
          MULT      SEQ,PVRCUR
          MULT      SEQ,VARCUR
          MOVE      BGRED,CURBGC
          IF        VARCUR>=0
            MOVE      BGGREEN,CURBGC
          ENDIF
          MOVE      TEMPLATE,F3
          MOVE      F3,KEY3
          REP       " 0",KEY3
          CLEAR     HTMLLINK
          APPEND    "/cgi-bin/sinweb01.pbl?reportno=4&template=",HTMLLINK
          APPEND    "&costcent=",HTMLLINK
          APPEND    KEY3,HTMLLINK
          MOVE      COSTCENT,KEY5
          REP       " +",KEY5
          APPEND    KEY5,HTMLLINK
          APPEND    "&reptyear=",HTMLLINK
          APPEND    SICBDAT,HTMLLINK
          RESET     HTMLLINK
          SQUEEZE   HTMLLINK
          WRITE     HTMLFILE;"<tr>":
                                 "<td align=right>",SICBAMT,"</td>":
                                 "<td align=right>",SICBBUD,"</td>":
                                 "<td align=right bgcolor=",CURBGC:
                                 ">",VARCUR,"</td>":
                                 "<td align=right bgcolor=",CURBGC:
                                 ">",PVRCUR,"</td>":
                                 "<td><a href=",HTMLLINK,">",PERNAME,"</td>":
                                 "<td align=right>",YTDAMT,"</td>":
                                 "<td align=right>",YTDBUD,"</td>":
                                 "<td align=right bgcolor=",YTDBGC:
                                 ">",VARYTD,"</td>":
                                 "<td align=right bgcolor=",YTDBGC:
                                 ">",PVRYTD,"</td>":
                                 "</tr>"
          GOTO      STKUSE10
.
STKUSE90  WRITE     HTMLFILE;"</table>"
          CALL      WEBEND00
          GOTO      STKUSE99
.
STKUSE95  MOVE      "INVALID SECURITY",WEBTITLE
          CALL      WEBERR00
          GOTO      STKUSE99

STKUSE99  RETURN
.------------------------------------------------------------
. Graph Stock Usage Heading Details
.------------------------------------------------------------
STKHED00  WRITE     HTMLFILE;"( #"#","
          WRITE     HTMLFILE;"  #"Stock Usage<BR>in Dollars#","
          WRITE     HTMLFILE;"  #"Stock Usage for ",SICADESC,"#","
.
          ASSIGN    MAXVALUE/10,STEPVAL
          WRITE     HTMLFILE;STEPVAL,","
          WRITE     HTMLFILE;"  20,"
          WRITE     HTMLFILE;"  20,"
          WRITE     HTMLFILE;"  5,"
          WRITE     HTMLFILE;"  0,"
          WRITE     HTMLFILE;"  2 "
          WRITE     HTMLFILE;");"
          RETURN 
.
.------------------------------------------------------------
. Graph Stock Usage Data Details
.------------------------------------------------------------
STKDAT00  MOVE      ZERO,MAXVALUE
          MOVE      "-1",INDEX2
          MOVE      ZERO,F2
STKDAT10  ADD       ONE,F2
          ADD       ONE,INDEX2
          IF        F2>12
            GOTO      STKDAT99
          ENDIF
          MOVE      REPTYEAR,KEY4
          PACK      SICBDAT,KEY4,F2
          REP       " 0",SICBDAT
          PACK      KEY11,SICACODE,SICBDAT,SP70
          CALL      RDSICB1
          IF        OVRCD=1
            MOVE      ZERO,SICBBUD
            MOVE      ZERO,SICBAMT
          ENDIF
          IF        SICBBUD>MAXVALUE
            MOVE      SICBBUD,MAXVALUE
          ENDIF
          IF        SICBAMT>MAXVALUE
            MOVE      SICBAMT,MAXVALUE
          ENDIF
.
          LOAD      PERNAME3,F2,HAPER1,HAPER2,HAPER3,HAPER4:
                               HAPER5,HAPER6,HAPER7,HAPER8:
                               HAPER9,HAPER10,HAPER11,HAPER12
          CLEAR     HTMLLINK
          APPEND    "/cgi-bin/sinweb01.pbl?reportno=4&costcent=",HTMLLINK
          STRIP     COSTCENT
          APPEND    COSTCENT,HTMLLINK
          APPEND    "&reptyear=",HTMLLINK
          APPEND    SICBDAT,HTMLLINK
          RESET     HTMLLINK
          SQUEEZE   HTMLLINK
          WRITE     HTMLFILE;"data[",INDEX2,"] = new Object(":
                                 "#"",PERNAME3,"#",",SICBAMT:
                                 ",#"/images/bar1.gif#",#"",HTMLLINK:
                                 "#");"
          ADD       ONE,INDEX2
          WRITE     HTMLFILE;"data[",INDEX2,"] = new Object(":
                                 "#"",SP1,"#",",SICBBUD:
                                 ",#"/images/bar2.gif#",#"",HTMLLINK:
                                 "#");"
          GOTO      STKDAT10
.
STKDAT99  RETURN
.
.------------------------------------------------------------
. List Back Orders for a Cost Centre
.------------------------------------------------------------
STKREQ00  MOVE      COSTCENT,KEY5
          CALL      RDSICA1
          CLEAR     WEBTITLE
          APPEND    "Stock Requisition for ",WEBTITLE
          APPEND    SICADESC,WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00
.
          MOVE      REPTYEAR,KEY6
          CALL      RDSIPE1
.
          WRITE     HTMLFILE;"<table border=1 width=#"100%#">"
          WRITE     HTMLFILE;"<tr bgcolor=#FFFF00>":
                               "<td align=center colspan=6><B>":
                               SICADESC,"</b></td></tr>":
                               "<tr bgcolor=#FFFF00>":
                                 "<td><b>Catalog</B></TD>":
                                 "<td><b>Cat No</B></TD>":
                                 "<td><b>Date</B></TD>": 
                                 "<td><b>Req No</B></TD>":
                                 "<td align=right><b>Quantity</b></td>":
                                 "<td align=right><b>Cost</b></td>":
                                 "</tr>"
.
          PACK      KEY21,SICACODE,SIPESDT,SP70
          CALL      RSSIRA4                  
STKREQ10  CALL      RKSIRA4
          BRANCH    OVRCD,STKREQ99
          MATCH     SIRACST,SICACODE
          GOTO      STKREQ99 IF NOT EQUAL
          MATCH     SIRADAT,SIPEEDT
          GOTO      STKREQ99 IF LESS
.
          PACK      KEY20,SIRATYP,SIRAREQ,SP70
          CALL      RSSIRB1
STKREQ20  CALL      RKSIRB1
          BRANCH    OVRCD,STKREQ99
          MATCH     SIRATYP,SIRBTYP 
          GOTO      STKREQ10 IF NOT EQUAL
          MATCH     SIRAREQ,SIRBREQ 
          GOTO      STKREQ10 IF NOT EQUAL
          MOVE      SIRBCAT,KEY7
          CALL      RDSIIA1
          BRANCH    OVRCD,STKREQ10
          UNPACK    SIRADAT,CCENT,CYEAR,CMON,CDAY
          WRITE     HTMLFILE;"<TR><TD>",SIIADES,"</TD>":
                                 "<TD>",SIIACAT,"</TD>":
                                 "<TD ALIGN=CENTER>":
                                 CDAY,"/",CMON,"/",CCENT,CYEAR,"</TD>":
                                 "<TD>",SIRATYP,SIRAREQ,"</TD>":
                                 "<TD ALIGN=RIGHT>",SIRBIQT,"</TD>":
                                 "<TD ALIGN=RIGHT>",SIRBAMT,"</TD>":
                                 "</A></TR>"
          GOTO      STKREQ20
.
STKREQ90  WRITE     HTMLFILE;"</TABLE>"
          CALL      WEBEND00
          GOTO      STKREQ99
.
STKREQ95  MOVE      "INVALID SECURITY",WEBTITLE
          CALL      WEBERR00
          GOTO      STKREQ99
.
STKREQ99  RETURN
.
.
          INC       SINCCAIO/INC
          INC       SINCCBIO/INC
          INC       SINCSEIO/INC
          INC       SINCIAIO/INC
          INC       SINPOAIO/INC
          INC       SINPOCIO/INC
          INC       SINPERIO/INC
          INC       SINBACIO/INC
          INC       SINREAIO/INC
          INC       SINREBIO/INC
          INC       IBAWEBCD    

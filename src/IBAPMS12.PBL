.*****************************************************************************
.* System    :   Patient Management                                          *
.* Program   :   IBAPMS12                                                    *
.* Desc      :   Lower Case HCP Codes                                        *
.*****************************************************************************
.* Date      :   30/08/2016                                                  *
.* Author    :   Peter Vela                                                  *
.* Function  :   This program will report records on the HCP table with any  *
.*               lower case characters in the HCP code field                 *
.* Mods      :                                                               *
.*               V10.08.02 07/11/2016  Peter Vela        TSK 0307252         *
.*                         Added Active/Inactive column                      *
.*               V10.08.01 30/08/2016  Peter Vela        TSK 0307252         *
.*                         Created program                                   *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PMSHCPFD/INC                 * HCP Table 
.
. LOCAL VARIABLE DEFINITION
. -------------------------
ACTVINDC  DIM       10
DIM10     DIM       10
.
. PROGRAM CONSTANTS
. -----------------
REPLOWCS  INIT      "a~b~c~d~e~f~g~h~i~j~k~l~m~n~o~p~q~r~s~t~u~v~w~x~y~z~"
PIPE      INIT      "|"
.
PRGID     INIT      "IBAPMS12"
PRGNAM    INIT      "Lower Case HCP Codes"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
          CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
.
          PERFORM   COPTION,PRIN0000       * print report
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"pmshcpaf";
          OPEN      PMSHCPA1,"pmshcpaf"
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run report                              *
.*                        TRUE  (1)  Exit option selected                    *
.*              COPTION  1 = Standard Report selected                        *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Report" 
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ":
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run report
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               PRIN0000              Called by: MAIN0000   *
.*                  Process the records for printing                         *
.*****************************************************************************
.
PRIN0000  MOVE      ZERO,CPAGENO
          CALL      IBACLOCK
.
          CALL      HEAD0000
.
          PACK      KEY10,SP70
          CALL      RSPMHCP1
PRIN0500  CALL      RKPMHCP1
          BRANCH    OVRCD,PRIN9500               * eof - finished
.
          MOVE      SP70,DIM10
          MOVE      PMHCHCPC,DIM10
          REP       REPLOWCS,DIM10
.
          SCAN      "~",DIM10
          GOTO      PRIN0500 IF NOT EQUAL          
.
.         A valid record has been found, so print it
.
          COMPARE   FIFTY7,CLNO                  * page full ?
          CALL      HEAD0000 IF NOT LESS         * yes
.
          MOVE      PMHCTITL,PACTITLE
          MOVE      PMHCGNAM,PACGNAME
          MOVE      PMHCSNAM,PACSNAME
          MOVE      TWO,PACFRMT
          CALL      PACNAME 
.
          MOVE      "Active    ",ACTVINDC
          MATCH     "1",PMHCSTTS
          IF        @EQUAL
            MOVE      "Inactive  ",ACTVINDC
          ENDIF
.
          CALL      DISP0000                     * print record details
          GOTO      PRIN0500                     * get next record
.
PRIN9500  CALL      LINE0000
          PRINT     *N,*N,*1,"*** End of Report ***"
          DISPLAY   *P1:23,*EF:
                    *P1:24,"Report printed.  ";
          CALL      HITENTER
.
PRIN9999  RETURN
+
.*****************************************************************************
.*                            DISP0000                 Called by: PRIN0000   *
.*                  Valid record so print it                                 *
.*****************************************************************************
.
DISP0000  PRINT     *1,PIPE,*3,PMHCHCPC,*14,PIPE,*16,PACFNAME:
                     *110,PIPE,*112,ACTVINDC,*132,PIPE
.
          ADD       ONE,CLNO                     * increment line count
.
DISP9999  RETURN
+
.*****************************************************************************
.*                            HEAD0000                 Called by: PRIN0000   *
.*                       Print page heading                                  *
.*****************************************************************************
.
HEAD0000  CALL      HEAD132                      * display page header
.
          CALL      LINE0000
.
          PRINT     *1,PIPE,*3,"HCP Code",*14,PIPE,*16,"HCP Name":
                    *110,PIPE,*112,"Status",*132,PIPE
.
          CALL      LINE0000                     * draw line across page
.
          MOVE      TEN2,CLNO                  * initialise line count
.
HEAD9999  RETURN
+
.*****************************************************************************
.*                            LINE0000                 Called by: HEAD0000   *
.*                      Draw line across page                     PRIN0000   *
.*****************************************************************************
.
LINE0000  PRINT     "*-----------------------------------------------":
                    "------------------------------------------------":
                    "-----------------------------------*"
.
LINE9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PMSHCPIO/INC
+

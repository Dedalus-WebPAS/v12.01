.*****************************************************************************
.* System    :   Patient Management System                                   *
.* Program   :   CONVICDD                                                    *
.* Desc      :   Data Migration Program to upload ICD 10 disease data from   *
.*               a third party legacy system into webPAS.                    *
.*****************************************************************************
.* Date      :   23/05/2013                                                  *
.* Author    :   Steve Armstrong                                             *
.* Function  :   This program will loop through the convicdd.txt file and    *
.*               for each valid record found, it will write a new patd13af   *
.*               record for the patient.                                     *
.* Mods      :                                                               *
.*        V11.05.01  24/03/2025  Davin           TSK 0956210                 *
.*                   Changes to write to ICD10 Edition 13 table (patd13af)   *
.*                   Field 36 must be 1 to indicate valid icd10ed13 code     *
.*                   Field 37 should be 0,1,2 for Cluster Allocation Required*
.*        V11.02.01  31/03/2022  Davin           TSK 0917793                 *
.*                   Changes to write to ICD10 Edition 12 table (patd12af)   *
.*                   Field 35 must be 1 to indicate valid icd10ed12 code     *
.*        V10.14.01  14/03/2019  Don Nguyen      TSK 0869412                 *
.*                   Changes to write to ICD10 Edition 11 table (patd11af)   *
.*        V10.10.01  17/03/2017  Davin         TSK 0833093     HDP 2017/2018 *
.*                   Changes to write to ICD10v10 table (pat10dxf)           *
.*        V10.06.01  01/04/2015  Davin             CAR 311669                *
.*                   Changes to write to ICD10v9 table (pat10d9f)            *
.*        V10.04.01  17/03/2014  Steve Armstrong   CAR 296128                *
.*                   Added display of warning message if error records found.*
.*****************************************************************************
.*        V10.03.00  23/05/2013  Steve Armstrong   CAR 284420                *
.*                   Created program                                         *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATICDFD/INC
.
.         ICD Disease file layout - convicdd.txt
.
ICDD9UPL  FILE      HL7, FIXED=4000         * Pipe delimited upload file
.                                             4096 is maximum
.
.         Fields must be pipe delimited and in the following sequence.
.         Field lengths are irrelevant other than the fact that where
.         a field is longer than allowed, the extra data will be ignored.
.
ICDDCODE  DIM       9      1      * ICD10 disease code       (patd13af.dpcode)
ICDDDESC  DIM      70     10      * ICD10 disease description(patd13af.ddesc)
ICDDFLAG  DIM       1     80      * Validity of code         (patd13af.dflag)
.                                     P - Valid/posting record
.                                     H - Not valid/header record
ICDDAGEG  DIM       1     81      * Age group edit           (patd13af.dagegp)
.                                       - No restraint
.                                     1 - Reject if within month range
.                                     2 - Warn if within month range
.                                     3 - Reject if within year range
.                                     4 - Warn if within year range
.                                     5 - Reject if outside year range
.                                     6 - Warn if outside year range
.                                     7 - Reject if outside month rang
.                                     8 - Warn if outside month range
.                                     9 - Reject if within day range
.                                     A - Warn if within day range
.                                     B - Reject if outside day range
.                                     C - Warn if outside day range
ICDDLOWL  DIM       3     82      * Age low limit            (patd13af.ptxdall1)
ICDDHIGH  DIM       3     85      * Age high limit           (patd13af.ptxdahl1)
ICDD2AGP  DIM       1     88      * 2nd age group edit       (patd13af.pt0d2agp)
.                                       - No restraint
.                                     1 - Reject if within month range
.                                     2 - Warn if within month range
.                                     3 - Reject if within year range
.                                     4 - Warn if within year range
.                                     5 - Reject if outside year range
.                                     6 - Warn if outside year range
.                                     7 - Reject if outside month rang
.                                     8 - Warn if outside month range
.                                     9 - Reject if within day range
.                                     A - Warn if within day range
.                                     B - Reject if outside day range
.                                     C - Warn if outside day range
ICDD2ALL  DIM       3     89      * 2nd age low limit        (patd13af.ptxdall2)
ICDD2AHL  DIM       3     92      * 2nd age high limit       (patd13af.ptxdahl2)
ICDDSEXE  DIM       1     95      * Sex edit                 (patd13af.dsex)
.                                       - No restraint
.                                     1 - Reject if male
.                                     2 - Warn if male
.                                     3 - Reject if female
.                                     4 - Warn if female
.                                     5 - Reject if female /
.                                         No Explanation Code
.                                     6 - Reject if male/No Expl Code
ICDDADTP  DIM       1     96      * Asterisk/dagger type     (patd13af.pt0dadtp)
.                                       - Not an asterisk/dagger code
.                                     A - Asterisk code
.                                     D - Dagger code
ICDDPRFX  DIM       2     97      * Diagnosis Code Prefix    (patd13af.pto2prfx)
.                                       - No restraint
.                                     1 - Notify if A
.                                     2 - Notify if C
.                                     3 - Notify if P
.                                     4 - Notify if A or C
.                                     5 - Notify if P or C
.                                     6 - Warn if A
.                                     7 - Warn if C
.                                     8 - Warn if P
.                                     9 - Warn if A or C
.                                    10 - Warn if P or C
.                                    11 - Notify if C, Warn if P
.                                    12 - Warn if A,Notify if C
.                                    13 - Notify if A, Warn if C
ICDDSAC2  DIM       7     99      * Suggested asterisk code  (patd13af.pt0dsac2)
ICDDDAGR  DIM       1    106      * Dagger & asterisk edit   (patd13af.ddagger)
.                                       - No restraint
.                                     1 - Reject if no following
.                                         asterisk code
.                                     2 - Warn if no following
.                                         asterisk code
.                                     3 - Reject if no preceding
.                                         dagger code
ICDDAREA  DIM       1    107      * Area edit                (patd13af.darea)
.                                       - No restraint
.                                     1 - Reject if code found
.                                     2 - Warn if code found
.                                     3 - Notify Infectious Disease
.                                     4 - Notify Rare Infectious
.                                         Disease
ICDDCPRA  DIM       1    108      * Code practices edit      (patd13af.pt0dcpra)
.                                       - No restraint
.                                     1 - Reject unacceptable 1st code
.                                     2 - Warn questionable principal
.                                         diagnosis (V3.1)
.                                     3 - Warn questionable principal
.                                         diagnosis/procedure (V3.1)
.                                     4 - Warn dont code CMV for <
.                                         24 hours
ICDDACRQ  DIM       1    109      * Additional code requirements edit 
                                        - No restraint       (patd13af.pt0dacrq)
.                                     1 - Reject this code requires an
.                                         external code
.                                     2 - External cause code
.                                     3 - Warn cancer code requires a
.                                         morphology code to follow
.                                     4 - Morphology code
.                                     5 - Code may be followed by a
.                                         morphology code
.                                     6 - Reject external cause code,
.                                         needs a place code
.                                     7 - [not in use]
.                                     8 - Reject external cause code,
.                                         needs activity & place codes
.                                     P - Place code
.                                     A - Activity code
.                                     W - Warning, this code requires
.                                         an external cause code
ICDDMI9C  DIM       9    110      * Mapped ICD9 disease code (patd13af.pt0dmi9c)
ICDDCMFT  DIM       2    119      * Complex mappings format  (patd13af.pt0dcmft)
.                                     0 - No complex mappings
.                                     1 - Obstetrics (ICD)
.                                     2 - Head injuries (ICD)
.                                     3 - Tendon injuries (ICD)
.                                     4 - Pacemakers (ICO)
.                                     5 - Social induction (ICD & ICO)
.                                     6 - Medical augment (ICO & ICD)
.                                     7 - 2-3 Maps (ICO)
.                                     8 - 2-3 Maps (ICD)
ICDDV1CD  DIM       1    121      * Valid ICD10 version 1 code
                                      0 - No, 1 - Yes        (patd13af.pt0dv1cd)
ICDDV2CD  DIM       1    122      * Valid ICD10 version 2 code
                                      0 - No, 1 - Yes        (patd13af.pt0dv2cd)
ICDDV1MP  DIM       9    123      * ICD10 version 1 map code (patd13af.pt0dv1mp)
.                                     (only if a valid ICD10 version 2 code)
ICDDV3CD  DIM       1    132      * Valid ICD10 version 3 code
                                      0 - No, 1 - Yes        (patd13af.pt0dv3cd)
ICDDV2MP  DIM       9    133      * ICD10 version 2 map code (patd13af.pt0dv2mp)
.                                     (only if a valid ICD10 version 3 code)
ICDDV4CD  DIM       1    142      * Valid ICD10 version 4 code
                                      0 - No, 1 - Yes        (patd13af.pt0dv4cd)
ICDDV5CD  DIM       1    143      * Valid ICD10 version 5 code
                                      0 - No, 1 - Yes        (patd13af.pt0dv5cd)
ICDDV6CD  DIM       1    144      * Valid ICD10 version 6 code
                                      0 - No, 1 - Yes        (patd13af.pt0dv6cd)
ICDDDSC2  DIM       200  145      * 200 char Description     (patd13af.pt0ddsc2)
ICDDV7CD  DIM       1    345      * Valid ICD10 version 7 code
                                      0 - No, 1 - Yes        (patd13af.pt0dv7cd)
ICDDV8CD  DIM       1    346      * Valid ICD10 version 8 code
                                      0 - No, 1 - Yes        (patd13af.pt0dv8cd)
ICDDV9CD  DIM       1    347      * Valid ICD10 version 9 code
                                      0 - No, 1 - Yes        (patd13af.pt0dv9cd)
ICDDVXCD  DIM       1    348      * Valid ICD10 version 10 code
                                      0 - No, 1 - Yes        (patd13af.pt0dvxcd)
ICDDMETH  DIM       1    349      * Methamphetamine Related  (patd13af.ptodmeth)
.                                     blank or 0 - No, 1 - Yes
ICDDVX1C  DIM       1    350      * Valid ICD10 version 11 code
                                      0 - No, 1 - Yes        (patd13af.pt0dvx1c)
ICDDVX2C  DIM       1    351      * Valid ICD10 version 12 code
                                      0 - No, 1 - Yes        (patd13af.pt0dvx2c)
ICDDVX3C  DIM       1    352      * Valid ICD10 version 13 code
                                      0 - No, 1 - Yes        (patd13af.pt0dvx3c)
ICDDCARQ  DIM       1    353      * Cluster Allocation Reqd  (patd13af.pt0dcarq)
.                                     blank or 0 - No, 1 - Yes, 2 - Maybe
. End of Record          354
. 37 fields
.
.
. LOCAL VARIABLE DEFINITION
. -------------------------
ADDCOUNT  FORM      8             * added record count
.
CURRDATE  DIM       8             * current date
.
DUPCOUNT  FORM      8             * duplicate record count  >>>(may not be used)
.
ERORDESC  DIM       70
ERORFLAG  FORM      1             * Field Validation Error Flag
.                                     0 = No Errors on Field Validations
.                                     1 = Errors on Field Validations
.
FORM10    FORM      10
.
MANCOUNT  FORM      8             * mandatory error record count
.
NUMCOUNT  FORM      8             * numeric error record count
.
RECCOUNT  FORM      8             * record read counter
.
TMPFIELD  DIM       8             * Field name
TMPSTRNG  DIM       8             * Temporary string (max field length)
.
VCHKFLAG  FORM      1             * Validation flag
.                                     0 = writing data to database
.                                     1 = only validating import data file
.
.
.
. PROGRAM CONSTANTS
. -----------------
PIPE      INIT      "|"
.
.
.
PRGID     INIT      "CONVICDD"
PRGNAM    INIT      "ICD10 V13 Disease Upload"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000       * proceed with upload
.
MAIN1000  CALL      OPEN0000               * open upload file
          BRANCH    EXIT,MAIN0100          * file not found
.
          CALL      ASKQ0000               * validation run only ?
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN2000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN2000  CALL      UPLD0000               * process upload
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patd13af";
          OPEN      PATD13A1,"patd13af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          MOVE      ONE,CNOUNDLN
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run data upload                         *
.*                        TRUE  (1)  Exit selected                           *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Data Upload"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run data upload
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               OPEN0000              Called by: MAIN0000   *
.*                  Open upload file (convicdd.txt)                          *
.*****************************************************************************
.
OPEN0000  MOVE      ZERO,OVRCD
.
          TRAP      OVERCOND IF IO
          OPEN      ICDD9UPL,"convicdd"
          TRAPCLR   IO
          BRANCH    OVRCD,OPEN9100
.
          MOVE      ZERO,EXIT
          GOTO      OPEN9999
.
OPEN9100  DISPLAY   *P1:24,*EL,*B,"Upload file - convicdd.txt - not found.  ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
OPEN9999  RETURN
+
.*****************************************************************************
.*                               ASKQ0000              Called by: MAIN0000   *
.*                  Ask if running validation only                           *
.* Returns: VCHKFLAG  - validation flag                                      *
.*                       0 = normal mode                                     *
.*                       1 = validation only mode                            *
.*****************************************************************************
.
ASKQ0000  MOVE      ANSY,ANS
          KEYIN     *P1:10,*EL,"Validation only (",*V2LON,*DV,ANSY,*HOFF:
                    *DV,SLASH,*V2LON,*DV,ANSN,*HOFF,") ?":
                    *P25:10,*V2LON,*RV,ANS
.
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
.
          MATCH     ANSY,ANS                     * Y input ?
          IF        @EQUAL
            DISPLAY   *P25:10,*V2LON,DYES,*HOFF:
                      *P35:10,"(No data will be uploaded)"
            MOVE      ONE,VCHKFLAG               * yes
            GOTO      ASKQ9999
          ENDIF
.
          MATCH     ANSN,ANS                     * N input ?
          IF        @EQUAL
            DISPLAY   *P25:10,*V2LON,DNO,*HOFF:
                      *P35:10,"(Data will be uploaded)"
            MOVE      ZERO,VCHKFLAG              * yes
            GOTO      ASKQ9999
          ENDIF
.
          GOTO      ASKQ0000                     * invalid input
.
ASKQ9999  RETURN
+
.******************************************************************************
.*                                  UPLD0000              Called by: MAIN0000 *
.*                  Process the PMI upload file records                       *
.******************************************************************************
.
UPLD0000  CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          DISPLAY   *P1:13,"Processing ",*V2LON,"convicdd.txt",*HOFF:
                    *P1:15,"Started    : ",*V2LON,CTIMEIS,*HOFF:
                    *P1:17,"ICD Code   : ":
                    *P1:18,"Records    : "
.
          MOVE      ZERO,RECCOUNT                * initialise records read
          MOVE      ZERO,ADDCOUNT                * initialise added record count
          MOVE      ZERO,DUPCOUNT                * initialise dup. record count
          MOVE      ZERO,MANCOUNT                * init. mand error record count
          MOVE      ZERO,NUMCOUNT                * init. num error record count
.
          CALL      HEAD0000                     * Print the report header
.
.         Loop through upload file records
.
UPLD1000  READ      ICDD9UPL,SEQ;ICDDCODE,ICDDDESC,ICDDFLAG,ICDDAGEG,ICDDLOWL:
                                 ICDDHIGH,ICDD2AGP,ICDD2ALL,ICDD2AHL,ICDDSEXE:
                                 ICDDADTP,ICDDPRFX,ICDDSAC2,ICDDDAGR,ICDDAREA:
                                 ICDDCPRA,ICDDACRQ,ICDDMI9C,ICDDCMFT,ICDDV1CD:
                                 ICDDV2CD,ICDDV1MP,ICDDV3CD,ICDDV2MP,ICDDV4CD:
                                 ICDDV5CD,ICDDV6CD,ICDDDSC2,ICDDV7CD,ICDDV8CD:
                                 ICDDV9CD,ICDDVXCD,ICDDMETH,ICDDVX1C,ICDDVX2C:
                                 ICDDVX3C,ICDDCARQ
          GOTO      UPLD9000 IF OVER
.
          ADD       ONE,RECCOUNT                 * increment records read
          IF        (RECCOUNT%1000) = 0 | RECCOUNT = 1
            DISPLAY   *P14:17,*V2LON,ICDDCODE:
                      *P14:18,RECCOUNT;
          ENDIF
.
          CALL      POUT0000                     * pad out fields with spaces
.
          CALL      CLER0000                     * clear database variables
.
          CALL      MAND0000                     * check for Mandatory fields
          IF        EXIT = 1 & VCHKFLAG = 0
            GOTO      UPLD1000                   * errors on validation
          ENDIF
.
          CALL      VNUM0000                     * validate numeric fields
          IF        EXIT = 1 & VCHKFLAG = 0
            GOTO      UPLD1000                   * errors on validation
          ENDIF
.
          CALL      SETV0000                     * set up file variables
.
          BRANCH    VCHKFLAG,UPLD1000            * error checking only
.
          MOVE      ICDDCODE,KEY9
          CALL      RAPD13A1                     * record on file ?
          IF        OVRCD = 1
            CALL      WRPD13A1                   * no - write new record
            ADD       ONE,ADDCOUNT               * increment added record count
          ELSE
            MOVE      "Disease record already exists ",ERORDESC
            CALL      PERR0000                   * print error line
            ADD       ONE,DUPCOUNT               * increment dupl. record count
          ENDIF
.
          GOTO      UPLD1000                     * get next record
.
.         Upload completed
.
UPLD9000  ASSIGN    (DUPCOUNT+MANCOUNT+NUMCOUNT),FORM10
          IF        FORM10 > 0
            CALL      LINE0000                   * print bottom line if errors
          ENDIF
.
          COMPARE   CLNO,FIFTY8                  * room for stats ?
          CALL      HEAD0000 IF LESS             * no - new page req'd.
.
          CLOCK     TIME,CTIMEIS
          DISPLAY   *P14:18,*V2LON,RECCOUNT,*HOFF:
                    *P1:21,"Finished   : ",*V2LON,CTIMEIS
.
.         Warn the user if there were any records with errors
.
          IF        FORM10 > 0
            DISPLAY   *P1:22,*V2LON,*BLINKON,"Warning:  Records found with ":
                      "errors.  Refer to error report."
          ENDIF
.
          DISPLAY   *P1:24,*EL,*B,"Upload complete.  ";
          CALL      HITENTER
.
          PRINT     *N:
                    *1,"Total records read                    - ",RECCOUNT,*N:
                    *1,"Records added                         - ",ADDCOUNT,*N:
                    *1,"Duplicate Records detected            - ",DUPCOUNT,*N:
                    *1,"Records with invalid numbers detected - ",NUMCOUNT,*N:
                    *1,"Records with blank mandatory fields   - ",MANCOUNT:
                    *N,*N,*1,"*** End of Report ***"
.
UPLD9999  RETURN
+
.******************************************************************************
.*                                  CLER0000              Called by: UPLD0000 *
.*                    Clear all the database fields                           *
.******************************************************************************
.
CLER0000  MOVE      SP9,DPCODE
          MOVE      SP70,DDESC
          MOVE      SP1,DFLAG
          MOVE      SP1,DAGEGP
          MOVE      ZERO,PTXDALL1
          MOVE      ZERO,PTXDAHL1
          MOVE      SP1,PT0D2AGP
          MOVE      ZERO,PTXDALL2
          MOVE      ZERO,PTXDAHL2
          MOVE      SP1,DSEX
          MOVE      SP1,PT0DADTP
          MOVE      SP2,PT0DPRFX
          MOVE      SP7,PT0DSAC2
          MOVE      SP1,DDAGGER
          MOVE      SP1,DAREA
          MOVE      SP1,PT0DCPRA
          MOVE      SP1,PT0DACRQ
          MOVE      SP9,PT0DMI9C
          MOVE      ZERO,PT0DCMFT
          MOVE      ZERO,PT0DV1CD
          MOVE      ZERO,PT0DV2CD
          MOVE      SP9,PT0DV1MP
          MOVE      ZERO,PT0DV3CD
          MOVE      SP9,PT0DV2MP
          MOVE      ZERO,PT0DV4CD
          MOVE      ZERO,PT0DV5CD
          MOVE      ZERO,PT0DV6CD
          PACK      PT0DDSC2,SP70,SP70,SP70
          MOVE      ZERO,PT0DV7CD
          MOVE      ZERO,PT0DV8CD
          MOVE      ZERO,PT0DV9CD
          MOVE      ZERO,PT0DVXCD
          MOVE      SP1,PTODMETH
          MOVE      ZERO,PT0DVX1C
          MOVE      ZERO,PT0DVX2C
          MOVE      ZERO,PT0DVX3C
          MOVE      SP1,PT0DCARQ
          MOVE      SP70,PTDSPR13
.
CLER9999  RETURN
+
.****************************************************************************
.*                             MAND0000                Called by: UPLD0000  *
.*                 Check for standard Mandatory fields                      *
.* Returns: EXIT   0 = all mandatory fields are populated                   *
.*                 1 = one or more mandatory fields are blank               *
.*          MANCOUNT - updated count of records with mandatory fields       *
.*                     missing                                              *
.****************************************************************************
.
MAND0000  MOVE      ZERO,ERORFLAG                * init. validation error flag
.
          MOVE      ICDDCODE,TMPSTRNG
          MOVE      "ICDDCODE",TMPFIELD          * disease code
          CALL      CHKM0000
.
          MOVE      ICDDDESC,TMPSTRNG
          MOVE      "ICDDDESC",TMPFIELD          * disease description
          CALL      CHKM0000
.
          MOVE      ICDDFLAG,TMPSTRNG
          MOVE      "ICDDFLAG",TMPFIELD          * validity of code
          CALL      CHKM0000
.
          BRANCH    ERORFLAG,MAND9100            * errors on mand. fields
.
          MOVE      ZERO,EXIT                    * no errors on mand. fields
          GOTO      MAND9999
.
MAND9100  ADD       ONE,MANCOUNT                 * incr. mand error record count
          MOVE      ONE,EXIT
.
MAND9999  RETURN
+
.*****************************************************************************
.*                            CHKM0000             Called by: MAND0000       *
.*                    Check if a mandatory field is blank                    *
.* Requires: TMPSTRNG - field to be checked                                  *
.*           TMPFIELD - 8 character field name                               *
.* Returns:  ERORFLAG = 1   field is blank (error)                           *
.*****************************************************************************
.
CHKM0000  MATCH     TMPSTRNG,SP8                 * blank field ?
          GOTO      CHKM9999 IF NOT EQUAL        * no - finished
.
          MOVE      "Mandatory Field blank ",ERORDESC
          ENDSET    ERORDESC
          APPEND    LBRACK,ERORDESC
          APPEND    TMPFIELD,ERORDESC
          APPEND    RBRACK,ERORDESC
          APPEND    SP70,ERORDESC
          RESET     ERORDESC
          CALL      PERR0000                     * print error line
.
          MOVE      ONE,ERORFLAG                 * set error flag
.
CHKM9999  RETURN
+
.******************************************************************************
.*                                  HEAD0000              Called by: UPLD0000 *
.*                           Print The Report Header                 PERR0000 *
.******************************************************************************
.
HEAD0000  MOVE      "- Error Report",CPHDROPT
          CALL      HEAD132                 * Print the report header
.
          PRINT     *1,"< convicdd.txt >"
.
          CALL      LINE0000                     * draw line across page
.
          PRINT     *1,PIPE,*3,"Code",*13,PIPE,*15,"Error Description",*132,PIPE
.
          CALL      LINE0000                     * draw line across page
.
          MOVE      SEVEN,CLNO                   * initialise line count
.
HEAD9999  RETURN
+
.****************************************************************************
.*                            LINE0000                 Called by: HEAD0000  *
.*                      Draw line across page                               *
.****************************************************************************
.
LINE0000  PRINT     "*-----------------------------------------------":
                    "------------------------------------------------":
                    "-----------------------------------*"
.
LINE9999  RETURN
+
.******************************************************************************
.*                                  SETV0000              Called by: UPLD0000 *
.*                       Load the record variables                            *
.******************************************************************************
.
SETV0000  MOVE      ICDDCODE,DPCODE
          MOVE      ICDDDESC,DDESC
          MOVE      ICDDFLAG,DFLAG
          MOVE      ICDDAGEG,DAGEGP
          MOVE      ICDDLOWL,PTXDALL1
          MOVE      ICDDHIGH,PTXDAHL1
          MOVE      ICDD2AGP,PT0D2AGP
          MOVE      ICDD2ALL,PTXDALL2
          MOVE      ICDD2AHL,PTXDAHL2
          MOVE      ICDDSEXE,DSEX
          MOVE      ICDDADTP,PT0DADTP
          MOVE      ICDDPRFX,PT0DPRFX
          MOVE      ICDDSAC2,PT0DSAC2
          MOVE      ICDDDAGR,DDAGGER
          MOVE      ICDDAREA,DAREA
          MOVE      ICDDCPRA,PT0DCPRA
          MOVE      ICDDACRQ,PT0DACRQ
          MOVE      ICDDMI9C,PT0DMI9C
          MOVE      ICDDCMFT,PT0DCMFT
          MOVE      ICDDV1CD,PT0DV1CD
          MOVE      ICDDV2CD,PT0DV2CD
          MOVE      ICDDV1MP,PT0DV1MP
          MOVE      ICDDV3CD,PT0DV3CD
          MOVE      ICDDV2MP,PT0DV2MP
          MOVE      ICDDV4CD,PT0DV4CD
          MOVE      ICDDV5CD,PT0DV5CD
          MOVE      ICDDV6CD,PT0DV6CD
          MOVE      ICDDDSC2,PT0DDSC2
          MOVE      ICDDV7CD,PT0DV7CD
          MOVE      ICDDV8CD,PT0DV8CD
          MOVE      ICDDV9CD,PT0DV9CD
          MOVE      ICDDVXCD,PT0DVXCD
          MOVE      ICDDMETH,PTODMETH
          MOVE      ICDDVX1C,PT0DVX1C
          MOVE      ICDDVX2C,PT0DVX2C
          MOVE      ICDDVX3C,PT0DVX3C
          MOVE      ICDDCARQ,PT0DCARQ
.
SETV9999  RETURN
+
.*****************************************************************************
.*                            POUT0000             Called by: UPLD0000       *
.*            Pad out all the record fields with spaces given that we        *
.*            are dealing with pipe delimited records                        *
.*****************************************************************************
.
POUT0000  PACK      ICDDCODE,ICDDCODE,SP9
          PACK      ICDDDESC,ICDDDESC,SP70
          PACK      ICDDFLAG,ICDDFLAG,SP1
          PACK      ICDDAGEG,ICDDAGEG,SP1
          PACK      ICDDLOWL,ICDDLOWL,SP3
          RJUSTIFY  ICDDLOWL
          REP       " 0",ICDDLOWL
          PACK      ICDDHIGH,ICDDHIGH,SP3
          RJUSTIFY  ICDDHIGH
          REP       " 0",ICDDHIGH
          PACK      ICDD2AGP,ICDD2AGP,SP1
          PACK      ICDD2ALL,ICDD2ALL,SP3
          RJUSTIFY  ICDD2ALL
          REP       " 0",ICDD2ALL
          PACK      ICDD2AHL,ICDD2AHL,SP3
          RJUSTIFY  ICDD2AHL
          REP       " 0",ICDD2AHL
          PACK      ICDDSEXE,ICDDSEXE,SP1
          PACK      ICDDADTP,ICDDADTP,SP1
          PACK      ICDDPRFX,ICDDPRFX,SP2
          PACK      ICDDSAC2,ICDDSAC2,SP7
          PACK      ICDDDAGR,ICDDDAGR,SP1
          PACK      ICDDAREA,ICDDAREA,SP1
          PACK      ICDDCPRA,ICDDCPRA,SP1
          PACK      ICDDACRQ,ICDDACRQ,SP1
          PACK      ICDDMI9C,ICDDMI9C,SP9
.
          PACK      ICDDCMFT,ICDDCMFT,SP2
          RJUSTIFY  ICDDCMFT
          REP       " 0",ICDDCMFT
.
          PACK      ICDDV1CD,ICDDV1CD,SP1
          REP       " 0",ICDDV1CD
.
          PACK      ICDDV2CD,ICDDV2CD,SP1
          REP       " 0",ICDDV2CD
.
          PACK      ICDDV1MP,ICDDV1MP,SP9
.
          PACK      ICDDV3CD,ICDDV3CD,SP1
          REP       " 0",ICDDV3CD
.
          PACK      ICDDV2MP,ICDDV2MP,SP9
.
          PACK      ICDDV4CD,ICDDV4CD,SP1
          REP       " 0",ICDDV4CD
. 
          PACK      ICDDV5CD,ICDDV5CD,SP1
          REP       " 0",ICDDV5CD
.
          PACK      ICDDV6CD,ICDDV6CD,SP1
          REP       " 0",ICDDV6CD
.
          PACK      ICDDDSC2,ICDDDSC2,SP70,SP70,SP70
.
          PACK      ICDDV7CD,ICDDV7CD,SP1
          REP       " 0",ICDDV7CD
.
          PACK      ICDDV8CD,ICDDV8CD,SP1
          REP       " 0",ICDDV8CD
.
          PACK      ICDDV9CD,ICDDV9CD,SP1
          REP       " 0",ICDDV9CD
.
          PACK      ICDDVXCD,ICDDVXCD,SP1
          REP       " 0",ICDDVXCD
.
          PACK      ICDDMETH,ICDDMETH,SP1
.
          PACK      ICDDVX1C,ICDDVX1C,SP1
          REP       " 0",ICDDVX1C
.
          PACK      ICDDVX2C,ICDDVX2C,SP1
          REP       " 0",ICDDVX2C
.
          PACK      ICDDVX3C,ICDDVX3C,SP1
          REP       " 0",ICDDVX3C
.
          PACK      ICDDCARQ,ICDDCARQ,SP1
          REP       " 0",ICDDCARQ
.
POUT9999  RETURN
+
.*****************************************************************************
.*                            VNUM0000             Called by: UPLD0000       *
.*                    Validate numeric fields                                *
.* Returns:  EXIT - 0 = No numeric validation errors                         *
.*                  1 = Numeric validation errors                            *
.*          NUMCOUNT - updated count of records with numeric field errors    *
.*****************************************************************************
.
VNUM0000  MOVE      ZERO,ERORFLAG                * init. validation error flag
.
.         Age low limit numeric field
.
          TYPE      ICDDLOWL                     * age low limit numeric ?
          GOTO      VNUM0050 IF NOT EQUAL        * no - error
          GOTO      VNUM0100                     * yes - valid value
.
VNUM0050  MOVE      "ICDDLOWL",TMPFIELD
          CALL      NERR0000
.
.         Age high limit numeric field
.
VNUM0100  TYPE      ICDDHIGH                     * age high limit numeric ?
          GOTO      VNUM0150 IF NOT EQUAL        * no - error
          GOTO      VNUM0200                     * yes - valid value
.
VNUM0150  MOVE      "ICDDHIGH",TMPFIELD
          CALL      NERR0000
.
.         2nd age low limit numeric field
.
VNUM0200  TYPE      ICDD2ALL                     * 2nd age low limit numeric ?
          GOTO      VNUM0250 IF NOT EQUAL        * no - error
          GOTO      VNUM0300                     * yes - valid value
.
VNUM0250  MOVE      "ICDD2ALL",TMPFIELD
          CALL      NERR0000
.
.         2nd age high limit numeric field
.
VNUM0300  TYPE      ICDD2AHL                     * 2nd age high limit numeric ?
          GOTO      VNUM0350 IF NOT EQUAL        * no - error
          GOTO      VNUM0400                     * yes - valid value
.
VNUM0350  MOVE      "ICDD2AHL",TMPFIELD
          CALL      NERR0000
.
.         Valid ICD Version 1 code numeric field
.
VNUM0400  TYPE      ICDDV1CD                     * ICD version 1 code numeric ?
          GOTO      VNUM0450 IF NOT EQUAL        * no - error
.
          MOVE      ZERO,FORM1
          MOVE      ICDDV1CD,FORM1
          COMPARE   FORM1,ONE                    * value  0 or 1 ?
          GOTO      VNUM0450 IF LESS             * no - error
          GOTO      VNUM0500                     * yes - valid value
.
VNUM0450  MOVE      "ICDDV1CD",TMPFIELD
          CALL      NERR0000
.
.         Valid ICD Version 2 code numeric field
.
VNUM0500  TYPE      ICDDV2CD                     * ICD version 2 code numeric ?
          GOTO      VNUM0550 IF NOT EQUAL        * no - error
.
          MOVE      ZERO,FORM1
          MOVE      ICDDV2CD,FORM1
          COMPARE   FORM1,ONE                    * value  0 or 1 ?
          GOTO      VNUM0550 IF LESS             * no - error
          GOTO      VNUM0600                     * yes - valid value
.
VNUM0550  MOVE      "ICDDV2CD",TMPFIELD
          CALL      NERR0000
.
.         Valid ICD Version 3 code numeric field
.
VNUM0600  TYPE      ICDDV3CD                     * ICD version 3 code numeric ?
          GOTO      VNUM0650 IF NOT EQUAL        * no - error
.
          MOVE      ZERO,FORM1
          MOVE      ICDDV3CD,FORM1
          COMPARE   FORM1,ONE                    * value  0 or 1 ?
          GOTO      VNUM0650 IF LESS             * no - error
          GOTO      VNUM0700                     * yes - valid value
.
VNUM0650  MOVE      "ICDDV3CD",TMPFIELD
          CALL      NERR0000
.
.         Valid ICD Version 4 code numeric field
.
VNUM0700  TYPE      ICDDV4CD                     * ICD version 4 code numeric ?
          GOTO      VNUM0750 IF NOT EQUAL        * no - error
.
          MOVE      ZERO,FORM1
          MOVE      ICDDV4CD,FORM1
          COMPARE   FORM1,ONE                    * value  0 or 1 ?
          GOTO      VNUM0750 IF LESS             * no - error
          GOTO      VNUM0800                     * yes - valid value
.
VNUM0750  MOVE      "ICDDV4CD",TMPFIELD
          CALL      NERR0000
.
.         Valid ICD Version 5 code numeric field
.
VNUM0800  TYPE      ICDDV5CD                     * ICD version 5 code numeric ?
          GOTO      VNUM0850 IF NOT EQUAL        * no - error
.
          MOVE      ZERO,FORM1
          MOVE      ICDDV5CD,FORM1
          COMPARE   FORM1,ONE                    * value  0 or 1 ?
          GOTO      VNUM0850 IF LESS             * no - error
          GOTO      VNUM0900                     * yes - valid value
.
VNUM0850  MOVE      "ICDDV5CD",TMPFIELD
          CALL      NERR0000
.
.         Valid ICD Version 6 code numeric field
.
VNUM0900  TYPE      ICDDV6CD                     * ICD version 6 code numeric ?
          GOTO      VNUM0950 IF NOT EQUAL        * no - error
.
          MOVE      ZERO,FORM1
          MOVE      ICDDV6CD,FORM1
          COMPARE   FORM1,ONE                    * value  0 or 1 ?
          GOTO      VNUM0950 IF LESS             * no - error
          GOTO      VNUM1000                     * yes - valid value
.
VNUM0950  MOVE      "ICDDV6CD",TMPFIELD
          CALL      NERR0000
.
.         Valid ICD Version 7 code numeric field
.
VNUM1000  TYPE      ICDDV7CD                     * ICD version 7 code numeric ?
          GOTO      VNUM1050 IF NOT EQUAL        * no - error
.
          MOVE      ZERO,FORM1
          MOVE      ICDDV7CD,FORM1
          COMPARE   FORM1,ONE                    * value  0 or 1 ?
          GOTO      VNUM1050 IF LESS             * no - error
          GOTO      VNUM1100                     * yes - valid value
.
VNUM1050  MOVE      "ICDDV7CD",TMPFIELD
          CALL      NERR0000
.
.         Valid ICD Version 8 code numeric field
.
VNUM1100  TYPE      ICDDV8CD                     * ICD version 8 code numeric ?
          GOTO      VNUM1150 IF NOT EQUAL        * no - error
.
          MOVE      ZERO,FORM1
          MOVE      ICDDV8CD,FORM1
          COMPARE   FORM1,ONE                    * value  0 or 1 ?
          GOTO      VNUM1150 IF LESS             * no - error
          GOTO      VNUM1160                     * yes - valid value
.
VNUM1150  MOVE      "ICDDV8CD",TMPFIELD
          CALL      NERR0000
.
.         Valid ICD Version 9 code numeric field
.
VNUM1160  TYPE      ICDDV9CD                     * ICD version 9 code numeric ?
          GOTO      VNUM1170 IF NOT EQUAL        * no - error
.
          MOVE      ZERO,FORM1
          MOVE      ICDDV9CD,FORM1
          COMPARE   FORM1,ONE                    * value  0 or 1 ?
          GOTO      VNUM1170 IF LESS             * no - error
          GOTO      VNUM1180                     * yes - valid value
.
VNUM1170  MOVE      "ICDDV9CD",TMPFIELD
          CALL      NERR0000
.
.         Valid ICD Version 10 code numeric field
.
VNUM1180  TYPE      ICDDVXCD                     * ICD version 10 code numeric ?
          GOTO      VNUM1190 IF NOT EQUAL        * no - error
.
          MOVE      ZERO,FORM1
          MOVE      ICDDVXCD,FORM1
          COMPARE   FORM1,ONE                    * value  0 or 1 ?
          GOTO      VNUM1190 IF LESS             * no - error
          GOTO      VNUM1200                     * yes - valid value
.
VNUM1190  MOVE      "ICDDVXCD",TMPFIELD
          CALL      NERR0000
.
.         Valid ICD Version 11 code numeric field
.
VNUM1200  TYPE      ICDDVX1C                     * ICD version 11 code numeric ?
          GOTO      VNUM1290 IF NOT EQUAL        * no - error
.
          MOVE      ZERO,FORM1
          MOVE      ICDDVX1C,FORM1
          COMPARE   FORM1,ONE                    * value  0 or 1 ?
          GOTO      VNUM1290 IF LESS             * no - error
          GOTO      VNUM1300                     * yes - valid value
.
VNUM1290  MOVE      "ICDDVX1C",TMPFIELD
          CALL      NERR0000
.
.         Valid ICD Version 12 code numeric field
.
VNUM1300  TYPE      ICDDVX2C                     * ICD version 12 code numeric ?
          GOTO      VNUM1390 IF NOT EQUAL        * no - error
.
          MOVE      ZERO,FORM1
          MOVE      ICDDVX2C,FORM1
          COMPARE   FORM1,ONE                    * value  0 or 1 ?
          GOTO      VNUM1390 IF LESS             * no - error
          GOTO      VNUM1900                     * yes - valid value
.
VNUM1390  MOVE      "ICDDVX2C",TMPFIELD
          CALL      NERR0000
.
.         Valid ICD Version 13 code numeric field
.
VNUM1400  TYPE      ICDDVX3C                     * ICD version 13 code numeric ?
          GOTO      VNUM1490 IF NOT EQUAL        * no - error
.
          MOVE      ZERO,FORM1
          MOVE      ICDDVX3C,FORM1
          COMPARE   FORM1,ONE                    * value  0 or 1 ?
          GOTO      VNUM1490 IF LESS             * no - error
          GOTO      VNUM1900                     * yes - valid value
.
VNUM1490  MOVE      "ICDDVX3C",TMPFIELD
          CALL      NERR0000
.
.         Complex mapping format numeric field
.
VNUM1900  TYPE      ICDDCMFT                     * complex map. format numeric ?
          GOTO      VNUM1950 IF NOT EQUAL        * no - error
          GOTO      VNUM9000                     * yes - valid value
.
VNUM1950  MOVE      "ICDDCMFT",TMPFIELD
          CALL      NERR0000
.
VNUM9000  BRANCH    ERORFLAG,VNUM9100            * errors on number validation
.
          MOVE      ZERO,EXIT                    * no errors on no. validation
          GOTO      VNUM9999
.
VNUM9100  ADD       ONE,NUMCOUNT                 * incr. num. error record count
          MOVE      ONE,EXIT
.
VNUM9999  RETURN
+
.*****************************************************************************
.*                            NERR0000             Called by: VNUM0000       *
.*                    Process number error                                   *
.* Requires: TMPFIELD - field name                                           *
.*****************************************************************************
.
NERR0000  MOVE      "Invalid numeric field ",ERORDESC
          ENDSET    ERORDESC
          APPEND    LBRACK,ERORDESC
          APPEND    TMPFIELD,ERORDESC
          APPEND    RBRACK,ERORDESC
          APPEND    SP70,ERORDESC
          RESET     ERORDESC
          CALL      PERR0000                     * print error line
.
          MOVE      ONE,ERORFLAG                 * set number error flag
.
NERR9999  RETURN
+
.*****************************************************************************
.*                              PERR0000           Called by: UPLD0000       *
.*                   Print an error detail line               CHKM0000       *
.* Requires: Valid read on PMI records (for name fields)      DERR0000       *
.*           ERORDESC - error description                     TERR0000       *
.*           CLNO - current page line count                   VURN0000       *
.* Returns:  CLNO - updated page line count                   NERR0000       *
.*                                                            VCOD0000       *
.*****************************************************************************
.
PERR0000  COMPARE   CLNO,SIXTY3                  * page full ?
          IF        @LESS
            CALL      LINE0000                   * yes - print bottom line
            CALL      HEAD0000                   * print header
          ENDIF
.
          PRINT     *1,PIPE,*3,ICDDCODE,*13,PIPE,*15,ERORDESC,*132,PIPE
          ADD       ONE,CLNO                     * increment page line count
.
PERR9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PATD13IO/INC

.******************************************************************************
.* System         :  Patient Billing                                          *
.* Program        :  IBAADT49                                                 *
.* Description    :  Western Australia Health Department Transmission         *
.******************************************************************************
.* Date           :  06/12/1996                                               *
.* Author         :  Jeni Tan                                                 *
.* Function       :  This program will extract all patients who discharged in *
.*                   the selected date range                                  *
.* Modifications  :                                                           *
.******************************************************************************
.*        V12.00.04 07/08/2025  Nikitha B     TSK 0964837                     *
.*                  Reinstated DOYR2024 check around XSEPTP at SDSC5000       *
.*        V12.00.03 26/06/2025  Nikitha B     TSK 0963143                     *
.*                  Removed DOYR2024 check around XSEPTP in SETF5200 and      *
.*                  SDSC5000                                                  *
.*        V12.00.02 26/06/2025  Nikitha B     TSK 0962676                     *
.*                  Removed DOYR2025 check around XGENDER in GEND0000.        *
.*                  Replaced WORK3 to WORK4 strcuture for PreJuly2025 extract *
.*                  Added missing XGENDER field in WORK4 file at WREC1000     *
.*        V12.00.01 19/05/2025 Nikitha B     TSK 0955096                      *
.*                  Alphanumeric changes                                      *
.*        V11.05.02 08/04/2025  PJ Le Febour  TSK 0958382                     *
.*                  change DOYR2024 variable to DOYR2025 JULY2024 to JULY2025 *
.*                         WORK4 to WORK5                                     *
.*        V11.05.01 24/03/2025  Davin         TSK 0956210                     *
.*                  Add ICD10 Ed.13 - Go-live Date PTCNGDX3 (Sect.130 Pos.86) *
.*        V11.04.04 05/09/2024  Thannh T      TSK 0951330                     *
.*                  Changed SDSC5000 to use YF2 variable instead of F2 to fix *
.*                  issue with F2 losing it s value                           *
.*        V11.04.03 04/07/2024  Thanh T       TSK 0948804                     *
.*                  Changed SDSC0000 to extract mode of separation based on   *
.*                  JULY2024 and not DOYR2024                                 *
.*        V11.04.02 17/06/2024  Thannh T       TSK 0947969                    *
.*                  Changed to create an extract file size 518 for Statutory  *
.*                  Changes 2024-25 and 517 for Statutory Changes 2023-2024   * 
.*        V11.04.01 22/03/2024  Thannh T       TSK 0940239                    *
.*                  WA HMDS Statutory Changes 2024-25                         *
.*        V11.03.03 31/10/2023  Alina Bhari    TSK 0936998                    *
.*                  Corrected the overnight stay for qualified new born baby  *
.*                  -LEND9000                                                 *
.*        V11.03.02 07/08/2023  Davin          TSK 0932953                    *
.*                  Fixed Days of qualified baby got transferred on same date *
.*        V11.03.01 13/06/2023  Davin          TSK 0933580                    *
.*                  Added PTCNED90/PTCNE100/PTCNE110 for DRG 9.0/10.0/11.0    *
.******************************************************************************
.*        V11.02.01 31/03/2022  Davin          TSK 0917793                    *
.*                  Add ICD10 Ed.12 - Go-live Date PTCNGDX2 (Sect.128 Pos.231)*
.*        V11.01.01 28/07/2021  Jill Parkinson Task 0909504                   *
.*                  Changed to use ddate for validating vet affairs card      *
.*        V11.00.02 18/08/2020  J.Tan          Task 0895379                   *
.*                  Added mapping to XVETRN                                   *
.*        V11.00.01 10/03/2020  Jill Parkinson Task 0879163                   *
.*                  Added include SEXDSCIO                                    *
.*        V10.15.03 16/01/2020  Ken Bell       TSK 0868737                    *
.*                  Should be 569 not Block 589 for included MV Value         *
.*        V10.15.02 22/11/2019  Ken Bell       TSK 0868737                    *
.*                  Block Number could be left fill zero/right space fill     *
.*        V10.15.01 18/11/2019  Ken Bell       TSK 0868737                    *
.*                  Check Operation Block 589 else no Mechanical Ventilation  *
.*        V10.14.01 14/03/2019  Don Nguyen     TSK 0869412                    *
.*                  Added read of ICD10 Ed.11 Go-live Date parameter (PTCNGDX1)*
.*        V10.13.02 17/10/2018  Don Nguyen     TSK 0838558                    *
.*                  Deleted temp files (WORK & WORK3) on program exit         *
.*                  01/11/2018  Thanh T        TSK 0865413                    *
.*                  Changed LANG0000 to get preferred language only when      *
.*                  Interpreter is Yes                                        *
.*        V10.13.01 01/08/2018  Jill Parkinson TSK 0859565                    *
.*                  Added client status=0 to send contracted hospital         *
.******************************************************************************
.*        V10.12.01 06/03/2018  J.Tan          TSK 0852417                    *
.*                  Mod to use PMCDCNUM for DVA number instead of VCLMNO      *
.*        V10.11.02 21/12/2017  Don Nguyen     TSK 0847269                    *
.*                  Modified EOCL0000 to keep reading the discharges until we *
.*                  get the original linked admission (for Statistical        *
.*                  Discharges); using CHKSTA00 (CHKADMNO) and SAVADMNO.      *
.*        V10.11.01 17/08/2017  Ebon Clements  TSK 0837262                    *
.*                  Fixed EOC link admission number population - EOCL0000     *
.*        V10.10.04 19/06/2017  Jill Parkinson Tsk 0840534                    *
.*                  Corrected country for overseas addresses                  *
.*        V10.10.03 30/05/2017  Davin          TSK 0836775                    *
.*                  Added PTCNED80 to extract drg version 8.0 if required     *
.*        V10.10.02 23/03/2017  Richa Phogat   TSK 0832066                    *
.*                  Changed DVA Card colour to be collected from              *
.*                  PMCDDVAC.PMSPX2FD instead of PMPXDVAC.PMIMA1FD            *
.*        V10.10.01 09/03/2017 Jill Parkinson TSK 0831952                     *
.*                  Changed to determine XSTATE the same as IBAADT50          *
.*        V10.09.02 17/02/2017 Ania P           TSK 0828227                   *
.*                  Added check for MHDLACTV                                  *
.*        V10.09.01 28/12/2016 Jill Parkinson   TSK 0831312                   *
.*                  Changed payment classification to allow 33 and 34         *
.*        V10.08.03 10/05/2016 J.Tan            TSK 0324456                   *
.*                  Fixed up the heading                                      *
.*        V10.08.02 05/05/2016 J.Tan            TSK 0324456                   *
.*                  Added 'Discharge Only' option                             *
.*        V10.08.01 04/04/2016 Peter Vela       TSK 0816405                   *
.*                  Only extract mental health legal status from mehdlsaf     *
.*                  if care type indicator 5 is P                             *
.*        V10.07.03 03/03/2016 Peter Vela       TSK 0814845                   *
.*                  Added functionality to get mental health legal status     *
.*                  from mehdlsaf if PMVXMHLS is spaces                       *
.*        V10.07.02 01/03/2016 Don Nguyen       TSK 0813506                   *
.*                  Squeezed out any space from PTMXMCCD (Medicare Code)      *
.*        V10.07.01 25/02/2016 Steve Armstrong  CAR 0814297                   *
.*                  Allow value of 32 for the combination of Cat A HDP value  *
.*                  5 or 6 and Cat CC HDP value 32.                           *
.******************************************************************************
.*        V10.06.02 16/06/2015 Sandra Barcham  CAR 318072                     *
.*                  Allow cat LA indicator 12 to exclude language             *
.*        V10.06.01 25/03/2015  Don Nguyen     CAR 312700                     *
.*                  Changes for 01 July 2015 (DOYR2015)                       *
.*        V10.05.01 16/07/2014  Davin          CAR 303652                     *
.*                  Get drg from patpgraf based on discharge date grouper     *
.*                  version. Get mdc from patdgwaf. (drgv0000)                *
.*        V10.04.02 15/06/2014  Jill Parkinson CAR 301630                     *
.*                  Moved TFILENAM to INIT0000 and KILL000 on exit            *
.*        V10.04.01 11/03/2014  Ania P         CAR 297942                     *
.*                  2014 Changes: Added Medicare & Medicare Person ID         *
.*        V10.03.06 17/10/2013  Davin          CAR 284205                     *
.*                  Use HOS93 parameters for disease code prefixes (wrdis000) *
.*        V10.03.05 22/05/2013  Don Nguyen     CAR 284462                     *
.*                  For DOYR2013 clear ICU hours if separation date is before *
.*                  the extract start date.                                   *
.*        V10.03.04 03/05/2013  Don Nguyen     CAR 284462                     *
.*                  Changes for 01 July 2013 (DOYR2013)                       *
.*        V10.03.03 19/12/2012  Patrick Adair  CAR 261630                     *
.*                  Removed port number from temp file name                   *
.*        V10.03.02 05/11/2012  Mike Laming    CAR 269788                     *
.*                  Remove "PACK XCTHOS,SP8" from WA HDP Contract at ADMF1000 *
.*        V10.03.01 18/11/2011  Mike Laming    CAR 240184                     *
.*                  Changes to IBAPOSTF Post Code table - added State to Keys *
.*        V10.02.02 28/10/2011  Mike Laming    CAR 250267                     *
.*                  Mod to XVENHR Pack statement at SETF5010                  *
.*        V10.02.01 28/03/2011  Mike Laming    CAR 240107                     *
.*                  Mods to PATECDaf & PATECOaf variables & Keys - file change*
.*        V10.01.03 17/02/2011  Mike Laming    CAR 233977                     *
.*                  Mods for National Medical Registration No. - text file chng
.*        V10.01.02 24/01/2011 J.Tan           CAR 233200                     *
.*                  Replaced PTCNHCPB with PTCNHCCC                           *
.*        V10.01.01 23/11/2010 Sandra Barcham  CAR 234202                     *
.*                  Fix XADTYP to not allow 5 as per July 2010 specification  *
.*        V10.00.03 20/07/2010 Sandra Barcham  CAR 224065                     *
.*                  Fix version V10.00.02 to stop looping                     *
.*        V10.00.02 02/07/2010 Sandra Barcham  CAR 224065                     *
.*                  Exclude any postcode with OS in state                     *
.*        V10.00.01 20/05/2010 Jill Parkinson CAR 221983                      *
.*                  Mods to use ptcnbage                                      *
.*        V9.12.03  28/01/2010 Jill Parkinson CAR 214324                      *
.*                  Mods to call calcage instread of using psage              *
.*        V9.12.02  29/09/2009  Jill Parkinson CAR 202994                     *
.*                  Mods to use PTCNSORL and PTCNEMPL                         *
.*        V9.12.01  12/06/2009  Jill Habasque CAR 198706                      *
.*                  July 2009 mods                                            *
.*        V9.10.02  19/08/2008 Sandra Barcham CAR 176480                      *
.*                  When reading patecoaf use discarge date not pteodtcd      *
.*                  When reading patecdaf use discarge date not pteddtcd      *
.*        V9.10.01  22/04/2008 Jill Habasque  CAR 166203                      *
.*                  July 2008 changes                                         *
.*        V9.09.05  22/01/2008 Sandra Barcham     155554                      *
.*                  If patient has a health fund but Cat CL THSCOD=23         *
.*                  XINSRN should be a 2                                      *
.*        V9.09.04  29/10/2007  Ebon Clements CAR 153446                      *
.*                  Fixed read of post code file                              *
.*        V9.09.03  29/08/2007  Ebon Clements CAR 148552                      *
.*                  Don't report dva details of claim code is not DVA         *
.*                  Added discharge from leave and leave days equal zero error*
.*                  Added EOCL0000 to populate EOC link adm if statistical adm*
.*        V9.09.02  02/08/2007  Jill Habasque CAR 145971                      *
.*                  Fixed IF statement around PTCNWAUC                        *
.*        V9.09.01  31/07/2007  Jill Habasque CAR 145971                      *
.*                  Removed check for bed type when calculating qualified days*
.*        V9.08.07  15/05/2007  Ian Farnell  CAR 135077                       *
.*                  changed PTEDOPER to PTEDUSID at WREC0000                 *
.*        V9.08.06  20/04/2007 Jill Habasque  CAR 139025                      *
.*                  Mods to use PTVANHSC if populated                         *
.*        V9.08.05  26/02/2007  Mike Laming  CAR 132806                       *
.*                  Move XLOCTN code to DSCR0000 (from GADM0000)              *
.*        V9.08.04  12/01/2007  Mike Laming  CAR 128389                       *
.*                  Correct ICD10 READ at ECDA3000 & ECOA3000                 *
.*        V9.08.03  22/12/2006  Ian Farnell  CAR 128389                       *
.*                  increased valid range for XCLNST at CLST0000 to 0 to 9    *
.*        V9.08.02  23/11/2006  Steve Armstrong  CAR 125110                   *
.*                  Removed extract code related to discharges prior to       *
.*                  01/07/2006.                                               *
.*                  Mods to XLANG & XINTRP.                                   *
.*                  Mods to only send 1,2 or 3 for XPSEX.                     *
.*                  Mods to populate XGRPVR with the default grouper version, *
.*                  and only when DRG is found.                               *
.*                  Mods to get DRG based on default grouper version.         *
.*                  Mods to get Contract code from patasfaf for XCTRES.       *
.*        V9.08.01  26/10/2006  Jill Habasque    CAR 117379                   *
.*                  Added MVEXT000                                            *
.******************************************************************************
.*        V9.07.04  30/08/2006  Ebon Clements    CAR 117379                   *
.*                  Changed aboriginality to use PMPXABRG                     *
.*                  Changed Employment Stat to use PMVXEMPL                   *
.*                  Changed LOS to use PMVXIDUS                               *
.*                  Changed mental H. Legal Status to use PMVXMHLS            *
.*                  Changed DVA card colour to use PMPXDVAC                   *
.*        V9.07.03  16/08/2006  Steve Armstrong  CAR 114471                   *
.*                  Fixed TEMP file definition (AADMNO is FORM - not DIM)     *
.*        V9.07.02  01/08/2006  Mike Laming   CAR 114471                      *
.*                  Remove XHCODE from IVAR0000 (don't clear it)              *
.*                  Change SP9 at Pos.38 to SP7 (caters for change in XLANG)  *
.*        V9.07.01  13/06/2006  Mike Laming   CAR 111862                      *
.*                  Changes to extract file as per v6.14                      *
.*        V9.07.00  07/06/2006  Mike Laming   CAR 111820                      *
.*                  Ported from V6.14                                         *
.******************************************************************************
.*        V6.14.02  29/05/2006  Mike Laming   CAR 101974    2006 WA HDP       *
.*                  Fix Mental Health Status XMHLGL to come from Indic.5      *
.*        V6.14.01  29/05/2006  Mike Laming   CAR 105249    2006 HCP          *
.*                  Change Size & Position of PTCNUREA, & PTCNUVTH            *
.*        V6.13.01  27/04/2006  Mike Laming   CAR 101974   WA HDMS 2006       *
.*                  Change XLANG from DIM 2 to DIM4                           *
.******************************************************************************
.
          INC       STD001FD
.
          INC       PATCALAG/INC
          INC       IBAPOSFD/INC
          INC       MEHDLSFD/INC
          INC       OPRDEAFD/INC 
          INC       OPRDEBFD/INC
          INC       PATASFFD/INC
          INC       PATCMPFD/INC            * Complex mapping file
          INC       PATCODFD/INC            * Codes file
          INC       PATCONFD/INC            * Control file
          INC       PATDADFD/INC
          INC       PATVADFD/INC
          INC       PATDO1FD/INC
          INC       PATDSCFD/INC            * Discharge file
          INC       PATECDFD/INC            * Patient episode disease file
          INC       PATECOFD/INC            * Patient episode operation file
          INC       PATICDFD/INC
          INC       PATICOFD/INC
          INC       PATICUFD/INC
          INC       PATDGWFD/INC
          INC       PATFN1FD/INC
          INC       PATLINFD/INC
          INC       PATMA1FD/INC            * Patient master file
          INC       PMSPX2FD/INC            * PMI extension
          INC       PATMI1FD/INC            * Patient admission file
          INC       PATVISFD/INC            * Patient visit record
          INC       PMSVX1FD/INC            * Visit extension file
          INC       PATPGRFD/INC
          INC       PATTRNFD/INC            * Transfer file
          INC       PATWR1FD/INC
          INC       PATWVEFD/INC
          INC       PMSCCDFD/INC
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
TEMP      IFILE     SQL, FIXED=84, KEY="D1-8"
.Name      Type      Length   Physical   Start   Description
.----      ----      ------   --------   -----   -----------------
.PURNO     DIM       8           8         1     U/R Number
.AADMNO    FORM      8           5         9     Admission Number
ERRDESC    DIM       70         70        14     Error Description
.
.End of Record                            84
.
WORK      FILE      ASCII, FIXED=267        * acknowledgement file
.
...WORK3     FILE      ASCII, FIXED=490     * discharged on or after 01/07/2006
...WORK3     FILE      ASCII, FIXED=501     * Changes for Dec 2010
...WORK3     FILE      ASCII, FIXED=506        * Changes for Jul 2013
WORK5     FILE      ASCII, FIXED=518        * Changes for Jul 2025
WORK4     FILE      ASCII, FIXED=518        * Changes for Jul 2024
WORK3     FILE      ASCII, FIXED=517        * Changes for Jul 2023
.
.Name     Type   Length    Start    Description
.----     ----   ------    -----    -----------
XEVNTYP   DIM       4         1     Event Type
XMORTYP   DIM       1         5     Morbidity Record Type
XEOCLNK   DIM      12         6     Episode of Care Link Field
XDAYQNB   FORM      3        18     Days of qual newborn care
XNUMLEV   DIM       2        21     Number of leave periods
XACCOCC   FORM      1        23     Accomodation Occupied
XLANG     DIM       4        24     Language
XUPDFLG   DIM       1        28     Update Flag
XSORLOC   DIM       2        29     Source of Referral - location
XSORCLI   DIM       2        31     Source of Referral - clinical/professional
XSORTRA   DIM       2        33     Source of Referral - transport
XHIHCD    FORM      3        35     Hospital in home care days
.SP7      DIM       7        38     Blank                            *C-114471
XHCODE    DIM       4        45     Establishment/Hospital Code
XADMNO    DIM      12        49     Admission/Account Number
XPURNO    DIM      10        61     Client Identifier/UR Number
XMUMSID   DIM      10        71     Mothers Identifier
XADATE    DIM       8        81     Admission Date
XATIME    DIM       4        89     Admission Time
XDDATE    DIM       8        93     Separation Date
XDTIME    DIM       4       101     Separation Time
XPSNAME   DIM      50       105     Surname
XPGNAME   DIM      30       155     First Forename
XSFNAME   DIM      30       185     Second Forename
XPADD1    DIM      50       215     Residential address
XSUBRB    DIM      30       265     Suburb
XPCODE    DIM       6       295     Postcode
XSTATE    DIM       1       301     State
.SP6      DIM       6       302     Blank
XDOBDT    DIM       8       308     Date of Birth
XPSEX     DIM       1       316     Gender/Sex
XABORN    DIM       1       317     Indigenous status
XCNTBR    DIM       4       318     Country of Birth
XMSTAT    DIM       1       322     Marital Status
XEMPST    DIM       2       323     Employment Status
XINTRP    DIM       1       325     Interpreter Service
XOCCPC    DIM       4       326     Occupation Code
XSREFR    DIM       2       330     Source of referral
XINLOS    DIM       1       332     Intended LOS
XCTHOS    DIM       4       333     Admitted From
XLOCTN    DIM      20       337     Location ATIO
...XCLINC    DIM       6       357     Clinician Responsible for Care *C-233977
XNMRNO    DIM      13       357     Practitioner Registration No.  (NMR No.)
XSPLTY    DIM       3       370     Speciality on Admit (Doct Type of Att.doc)
XSPCDS    DIM       3       373     Speciality on Disch.
XADTYP    DIM       1       376     Admission Status
XWEIGH    DIM       4       377     Admission Weight
XLEVDY    DIM       4       381     Total days of leave
XPSYCH    DIM       4       385     Total days of Psychiatric Care
XMHLGL    DIM       1       389     Mental Health Legal Status
XPAYCL    DIM       2       390     Payment Classification (CL)
XVETRN    DIM       1       392     Veteran Entitlement
XINSRN    DIM       1       393     Insurance Status
XICUDY    DIM       4       394     Days in I.C.U.
XVENHR    DIM       5       398     Ventilator hours
XREADM    DIM       1       403     Readmission Status
XURETN    DIM       1       404     Unplanned Return to Theatre
XEPSCR    DIM       2       405     Type of Episode of care
...XCLNST    DIM       2       407     Client status    *0958382
XCLNST    DIM       2       407     Patient Type
XCTRES    DIM       4       409     Contracting Establishmentn-0 FORD
XSEPTP    DIM       2       413     Separation Mode
XDSCTO    DIM       4       415     Transferred to/transfer source code
...XDSATT    DIM       6       412     Clinician on discharge         *C-233977
XDSATT    DIM      13       419     Clinician on discharge (NMR No.)
XGRPVR    DIM      10       432     Group Version
XDIACG    DIM       3       442     Major Diagnostic Category
XDIAGR    DIM       4       445     Diagnosis Related Group
XCODID    DIM      20       449     Code Identifier
XDVAFN    DIM      12       469     DVA File Number
XDVAAN    DIM      12       481     DVA Authorisation Number - no longer in use
XDVAAD    DIM       8       493     DVA Authorisation Date   - no longer in use
XICUHR    DIM       5       501     Hours in ICU
XMEDINO   DIM      10       506     Medicare Number
XMEDIPID  DIM       1       516     Medicare Number - Person ID
XGENDER   DIM       1       517     Gender
.
.End of Record              518
.
.
.Diagnosis/External Cause/Place of occurence/Activity/Morphology codes
.
.Name     Type   Length    Start    Description
.----     ----   ------    -----    -----------
.XEVNTYP   DIM       4         1     Event Type
.XADMNO    DIM      12         5     Admission/Account Number
.XPURNO    DIM      10        17     Client Identifier/UR Number
SP18       DIM      18        27     Blank
.XHCODE    DIM       4        45     Establishment/Hospital Code
XPRIOR     DIM       3        49     Priority
XICDCOD    DIM      10        52     ICD Code
XPLCOCC    DIM      10        62     Place of occurence
XACTIV     DIM      10        72     Activity
XCOFICD    DIM       1        82     Condition onset flag for ICD
XCOFPOO    DIM       1        83     Condition onset flag for Place of Occurence
XCOFACT    DIM       1        84     Condition onset flag for Activity
.End of Record                85
.
.Procedure Codes
.Name     Type   Length    Start    Description
.----     ----   ------    -----    -----------
.XEVNTYP   DIM       4         1     Event Type
.XADMNO    DIM      12         5     Admission/Account Number
.XPURNO    DIM      10        17     Client Identifier/UR Number
.SP18      DIM      18        27     Blank
.XHCODE    DIM       4        45     Establishment/Hospital Code
.XPRIOR    DIM       3        49     Priority
XPROCCOD   DIM      10        52     Procedure Code
...XCLINPR    DIM       6        62     Clinician for procedure       *C-233977
XCLINPR    DIM      13        62     Clinician for procedure  (NMR No.)
XDATPROC   DIM       8        75     Date of procedure
.SP10      DIM      10        83     Blank
.End of Record                93                           * was 86   *C-233977
.
.Check Line
.Name     Type   Length    Start    Description
.----     ----   ------    -----    -----------
.XEVNTYP   DIM       4         1     Event Type
.XADMNO    DIM      12         5     Admission/Account Number
.XPURNO    DIM      10        17     Client Identifier/UR Number
.SP18      DIM      18        27     Blank
.XHCODE    DIM       4        45     Establishment/Hospital Code
XDIAGCNT   DIM       4        49     Diagnosis Count
XEXCACNT   DIM       4        53     External Cause count
XMORPCNT   DIM       4        57     Morphology Codes count
XPROCCNT   DIM       4        61     Procedure count
.End of Record                65


. ----- Program Variables -----
.
ACTIVITY  DIM       2
ADMTYPE   DIM       3
BEDTYPE   DIM       3
BJDAY     FORM      3                       * For CALCAGE
CJDAY     FORM      3                       * For CALCAGE
CODEFLG   FORM      1
COUNT     FORM      2
COUNTA    FORM      2
COUNTD    FORM      4
COUNTEX   FORM      2
COUNTE    FORM      2
COUNTP    FORM      2
COUNTO    FORM      2
COUNTM    FORM      2
CALDAYS   FORM      6
CDYSDAYS  FORM      6
CDYSFDTE  DIM       8
CDYSTDTE  DIM       8
CMDLINE   DIM       60
CMPCNT    FORM      1
MHCODE    DIM       2
CATA      INIT      "A "
CATBT     INIT      "BT"
CATRT     INIT      "RT"
CODECC    INIT      "CC"
CODECL    INIT      "CL"
CODEDD    INIT      "DD"
CODEGi    INIT      "Gi"
CODEP     INIT      "P "
CODEP1    INIT      "P1"
CODEP2    INIT      "P2"
CODEP3    INIT      "P3"
CODEP4    INIT      "P4"
CODEP5    INIT      "P5"
CODEP6    INIT      "P6"
CODEPO    INIT      "PO"
CODEU1    INIT      "U1"
CODEU2    INIT      "U2"
CODEU3    INIT      "U3"
CODEU4    INIT      "U4"
CODEU5    INIT      "U5"
CODEU6    INIT      "U6"
CODEU7    INIT      "U7"
CODEU8    INIT      "U8"
CODEU9    INIT      "U9"
CODEV1    INIT      "V1"
CODEV2    INIT      "V2"
CODEVA    INIT      "VA"
CODEVB    INIT      "VB"
CURDATE   DIM       8
CDYSYEAR  FORM      2
DISCHDAT  DIM       8
DIM1A     DIM       1
DIM1B     DIM       1
DIM1C     DIM       1
DIM2A     DIM       2
DIM3      DIM       3
DIM4      DIM       4
DIM5      DIM       5
DIM6      DIM       6
DIM8      DIM       8
DIM9      DIM       9
DIM9A     DIM       9
DIM10     DIM       10
DIM17     DIM       17
DIM17A    DIM       17
DOCNAME   DIM       31
DOYR2025  FORM      1
DRGVERS   DIM       3
DRG041    INIT      "041"
DRG042    INIT      "042"
DRG050    INIT      "050"
DRG051    INIT      "051"
DRG052    INIT      "052"
DRG060    INIT      "060"
DRG062    INIT      "062"
DRG070    INIT      "070"
DRG080    INIT      "080"
DRG090    INIT      "090"
DRG100    INIT      "100"
DRG110    INIT      "110"
ENDDATE   DIM       8
FNAMEA    DIM       20
FNAMEB    DIM       24
FNAMES    DIM       8
FORM3     FORM      3
FORM6     FORM      6
FORM8     FORM      8
FORM9     FORM      9
JULY2024  INIT      "20240701"          
JULY2025  INIT      "20250701"          
.
NUMLEV    FORM      1
NEWFILE   FORM      1 
RECNUM    FORM      6
PATNAME   DIM       31
PSAGECON  DIM       3
PSAGETYP  DIM       1
POSTCODE  DIM       4
SCODFLG   FORM      1
SAVDATE   DIM       10
SEX       DIM       8
SAVKEY10  DIM       10
SENDFLAG  FORM      1             * send flag for activity codes
.                                       1 = No
.                                       0 = Yes
DSCHFLAG  FORM      1             * discharged only flag
.                                       1 = No
.                                       0 = Yes
CHKADMNO  DIM       8
ICUMECFL  DIM       1             * ICUMEC Required Flag
SAVADMNO  DIM       8
SKEY8     DIM       8
STRTDATE  DIM       8
SP12      INIT      "            "
SP50      INIT      "                                                     "
TIMEIS    DIM       8
WDATE1    DIM       8
WDATE2    DIM       8
WRDBED    DIM       6
WORKHITH  FORM      4
WORKICLS  FORM      4
WORKLDAY  FORM      4
WORKPSYC  FORM      4
WORKQUAL  FORM      4
XF2       FORM      2
YF2       FORM      2
.
.
. ----- Program Constants -----
.
OYEAR     FORM      "365"
ZED30     INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
.
PRGID     INIT      "IBAADT49"
PRGNAM    INIT      "WA Health Department Transmissions"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                  ML0000                                    *
.*                                Main Module                                 *
.******************************************************************************
ML0000    CALL      INIT0000                * Initialize variables & open files
ML1000    CALL      DSCD0000                * Keyin discharge date
          BRANCH    EXIT,ML9999
.
          CALL      CDID0000                * sending coder id ?
          CALL      ACTV0000                * ask user if activity is sent
          CALL      DSCH0000                * ask Discharged Only
          CALL      CONTQST                 * Ok to Continue (Y/N/C) ?
          BRANCH    CEXIT,ML7500,ML1000,ML1000
.                         Yes    No     Cancel
.
ML7500    CALL      EXTC0000                * Extract data for selected period
          CALL      ERRS0000                * print error message if any
          CALL      ENDP0000                * end of processing, copy files.
.
          CALL      MVEXT000
          GOTO      ML1000
.
ML9999    CALL      KILL0000
          CHAIN     PGM
+
.******************************************************************************
.*                                 INIT0000               Called by: ML0000   *
.*                     Initialize Variables & Open Files                      *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen header
.
          SCAN      "PreJulyTest",REPTNAME
          IF        @EQUAL
            MOVE      "20250101",JULY2025    
          ENDIF
          RESET     REPTNAME
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"ibapostf";
          OPEN      IBAPOST1,"ibapostf"
.
          DISPLAY   *P64:24,"oprdetaf";
          OPEN      OPRDETA2,"oprdetaf"
.
          DISPLAY   *P64:24,"oprdetbf";
          OPEN      OPRDETB1,"oprdetbf"
.
          DISPLAY   *P64:24,"patasfaf";
          OPEN      PATASFA1,"patasfaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
.
          DISPLAY   *P64:24,"patdadaf";
          OPEN      PATDADA1,"patdadaf"
.
          DISPLAY   *P64:24,"patdgwaf";
          OPEN      PATDGWA1,"patdgwaf"
.
          DISPLAY   *P64:24,"patvadaf";
          OPEN      PATVADA1,"patvadaf"
.
          DISPLAY   *P64:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH3,"patdschf"
          OPEN      PATDSCH2,"patdschf"
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"patecdaf";
          OPEN      PATECDA1,"patecdaf"
.
          DISPLAY   *P64:24,"patecdaf";
          OPEN      PATECDA2,"patecdaf"
.
          DISPLAY   *P64:24,"patecoaf";
          OPEN      PATECOA1,"patecoaf"
.
          DISPLAY   *P64:24,"patecoaf";
          OPEN      PATECOA2,"patecoaf"
.
          DISPLAY   *P64:24,"paticddf"
          CALL      OPICD1           
.
          DISPLAY   *P64:24,"paticdof"
          CALL      OPICO1
.
          DISPLAY   *P64:24,"paticuaf";
          OPEN      PATICUA1,"paticuaf"
.
          DISPLAY   *P64:24,"patlinkf";
          OPEN      PATLINK1,"patlinkf"
.
          DISPLAY   *P64:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
.         
          DISPLAY   *P64:24,"patvisaf"
          OPEN      PATVISA1,"patvisaf"
.
          DISPLAY   *P64:24,"pmsvx1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patpgraf";
          OPEN      PATPGRA1,"patpgraf"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY   *P64:24,"mehdlsaf";
          OPEN      MEHDLSA1,"mehdlsaf"
.
          DISPLAY   *P64:24,"pmsccdaf";
          OPEN      PMSCCDA1,"pmsccdaf"
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAMES
.
          DISPLAY   *P64:24,"patwvetf";
          OPEN      PATWVET1,"patwvetf"
.
          READ      CONTROLF,TEN;*37,CMOPRT,*54,CFILENO,*235,CMDISP,CMDISS:
                                 *237,CMDISC,*238,CMDISA
          READ      CONTROLF,TWENTY1;*214,PTCNUREA                    * ?????
          READ      CONTROLF,TWENTY1;*220,PTNSWHDP
          READ      CONTROLF,FIFTY9;*220,PTCNUADT
          READ      CONTROLF,EIGHTY8;*59,PTCNI10D
          READ      CONTROLF,HUND03;*245,PTCNWAUC
          READ      CONTROLF,HUND05;*158,PTCNDGPV,*185,PTCNELPS
          READ      CONTROLF,TWENTY1;*46,PTCNBAGE
          READ      CONTROLF,HUND10;*228,PTCNHCCC
          READ      CONTROLF,HUND19;*183,PTCNED41,*191,PTCNED42,*199,PTCNED50:
                                    *207,PTCNED51,*215,PTCNED52,*223,PTCNED60:
                                    *231,PTCNED62,*239,PTCNED70       * 303652
.
          READ      CONTROLF,NINTY8;*149,PTCNGDV2
          READ      CONTROLF,HUND09;*151,PTCNGDV3
          READ      CONTROLF,HUND05;*217,PTCNGDV4,*227,PTCNGDV5
          READ      CONTROLF,HUND12;*239,PTCNGDV6
          READ      CONTROLF,HUND10;*61,PTCNGDV7,*69,PTCNGDV8,*77,PTCNGDV9:
                                    *85,PTCNGDVX
          READ      CONTROLF,HUND17;*220,PTCNGDX1
          READ      CONTROLF,HUND20;*232,PTCNED80
          READ      CONTROLF,HUND23;*232,PTCNED90
          READ      CONTROLF,HUND24;*224,PTCNE100
          READ      CONTROLF,HUND28;*231,PTCNGDX2,*239,PTCNE110       *I-0917793
          READ      CONTROLF,HUND30;*86,PTCNGDX3,*94,PTCNE120         *I-0956210
.
          PACK      CURDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURDATE
.
INIT9999  RETURN
+
.******************************************************************************
.*                                 DSCD0000               Called by: ML0000   *
.*                         Keyin The Discharge Date                           *
.******************************************************************************
DSCD0000  MOVE      ZERO,EXIT
          MOVE      ZERO,DOYR2025
          CALL      CRET0000
.
. ----- Enter Starting Date -----
.
DSCD1000  DISPLAY   *P1:5,*EF,"Starting Discharge Date : ":
                    *P1:6,*EL,"Ending   Discharge Date : ";
          MOVE      "5",CVERT
          MOVE      "27",CCOL
          MOVE      CCC,CCENT
          UNPACK    SP6,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin the starting date
          BRANCH    OVRCD,DSCD9000
.
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDATE
          CALL      PACDATE                 * Pack the starting date
.
.          MATCH    "20150701",STRTDATE
.          IF       !@LESS
.            MOVE     ONE,DOYR2015
.          ENDIF
          MATCH     JULY2025,STRTDATE      
          IF        !@LESS
            MOVE      ONE,DOYR2025
          ENDIF
.
.
. ----- Check If Starting Date Is Greater Than Today's Date -----
.
          MATCH     STRTDATE,CURDATE
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Keyed in date is greater than today's ":
                      "date. ";
            CALL      HITENTER
            GOTO      DSCD1000
          ENDIF
.
. ----- Enter Ending Date -----
.
DSCD2000  MOVE      "6",CVERT
          MOVE      "27",CCOL
          CALL      KEYDATE                 * Keyin the ending date
          BRANCH    OVRCD,DSCD1000
.
          PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDATE
          CALL      PACDATE                 * Pack the starting date
.
. ----- Check If Ending Date Is Greater Tha Today's Date -----
.
          MATCH     ENDDATE,CURDATE
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Keyed in date is greater than today's ":
                      "date. ";
            CALL      HITENTER
            GOTO      DSCD2000
          ENDIF
.
. ----- Check That The Ending Date Is Greater Than The Starting Date -----
.
          MATCH     STRTDATE,ENDDATE
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Ending date must be greater than the ":
                      "starting date. ";
            CALL      HITENTER
            GOTO      DSCD2000
          ENDIF
.
. ----- Check If Discharge Date Range Exists -----
.
          PACK      KEY16,STRTDATE,SP20
          CALL      RDSDSCH2                * Position on discharge file
          CALL      RDKDSCH2                * Read the discharge file
          BRANCH    OVRCD,DSCD3000
.
          MATCH     DDATE,ENDDATE
          GOTO      DSCD9999 IF NOT LESS    * Patients found in date range, exit
.
DSCD3000  DISPLAY   *P1:24,*EL,*B,"No discharged patients found in the ":
                    "date range. ";
          CALL      HITENTER
          GOTO      DSCD1000
.
DSCD9000  MOVE      ONE,EXIT
DSCD9999  RETURN
+
.******************************************************************************
.*                                 CDID0000               Called by: ML0000   *
.*                        Question of Sending coder ID                        *
.******************************************************************************
CDID0000  MOVE      ZERO,SCODFLG              * default to not sending ID
.
          DISPLAY   *P1:8,*EF,"Do you want to send coder ID":
                               " (",*V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ?";
CDID1000  KEYIN     *P40:8,*DV,UNDLN1,*P40:8,*V2LON,ANS;
          ENDSET    ANS
          APPEND    SP1,ANS
          RESET     ANS
.
          MATCH     SP1,ANS
          GOTO      CDID1100 IF EQUAL
          REP       UPPLOW,ANS
          DISPLAY   *P40:8,*V2LON,ANS;
.
          CMATCH    "Y",ANS
          GOTO      CDID2000 IF EQUAL
          CMATCH    "N",ANS
          GOTO      CDID9999 IF EQUAL
CDID1100  BEEP
          GOTO      CDID1000
.
CDID2000  MOVE      ONE,SCODFLG             * Send coder id
CDID9999  RETURN
+
.******************************************************************************
.*                                 ACTV0000               Called by: ML0000   *
.*                Ask if user is sending activity codes                       *
.* Returns: SENDFLAG  0 = Yes, send activity codes                            *
.*                    1 = No, don't send activity codes                       *
.******************************************************************************
.
ACTV0000  KEYIN     *P1:10,*EL,"Sending Activity codes (",*V2LON,*DV,ANSY,*HOFF:
                    "/",*V2LON,*DV,ANSN,*HOFF,") :":
                    *P32:10,*V2LON,ANS
.
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
.
          MATCH     ANSY,ANS                     * yes - entered ?
          IF        @EQUAL
            MOVE      ZERO,SENDFLAG              * yes
            GOTO      ACTV9999
          ENDIF
.
          MATCH     ANSN,ANS                     * no - entered ?
          IF        @EQUAL
            MOVE      ONE,SENDFLAG               * yes
            GOTO      ACTV9999
          ENDIF
.
          BEEP                                   * invalid entry
          GOTO      ACTV0000
.
ACTV9999  RETURN
+
.******************************************************************************
.*                                 DSCH0000               Called by: ML0000   *
.*                Ask for Discharged Only                                     *
.******************************************************************************
.
DSCH0000  MOVE      ZERO,DSCHFLAG              * No
          KEYIN     *P1:11,*EL,"Discharged Only (",*V2LON,*DV,ANSY,*HOFF:
                    "/",*V2LON,*DV,ANSN,*HOFF,") :":
                    *P32:11,*V2LON,ANS
.
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
.
          MATCH     ANSY,ANS                     * yes - entered ?
          IF        @EQUAL
            MOVE      ONE,DSCHFLAG               * yes
            GOTO      DSCH9999
          ENDIF
          MATCH     ANSN,ANS
          GOTO      DSCH9999 IF EQUAL
.
          BEEP                                   * invalid entry
          GOTO      DSCH0000
.
DSCH9999  RETURN
+
.******************************************************************************
.*                                 SADM0000               Called by: ML0000   *
.*                        Question of generating an admission                 *
.******************************************************************************
SADM0000  DISPLAY   *P1:10,*EF,"Do you want to generate Data for an admission":
                               " (",*V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ?";
SADM1000  KEYIN     *P55:10,*DV,UNDLN1,*P55:10,*V2LON,ANS;
          ENDSET    ANS
          APPEND    SP1,ANS
          RESET     ANS
.
          MATCH     SP1,ANS
          GOTO      SADM1100 IF EQUAL
          REP       UPPLOW,ANS
          DISPLAY   *P55:10,*V2LON,ANS;
.
          CMATCH    "Y",ANS
          GOTO      SADM1150 IF EQUAL 
          CMATCH    "N",ANS
          GOTO      SADM9999 IF EQUAL
SADM1100  BEEP
          GOTO      SADM0000
.
SADM1150  DISPLAY   *P1:11,*EL,"Please enter the type of update :";
          DISPLAY   *P1:24,*V2LON,"Y",*HOFF,"=Updated, ",*V2LON,"D":
                    *HOFF,"=Deleted, ",*V2LON,"M",*HOFF,"=Merged, ":
                    *V2LON,"C",*HOFF,"=Confirmed ",*V2LON,"V":
                    *HOFF,"=Validation";
          KEYIN     *P35:11,ANS;
          REP       UPPLOW,ANS
          MOVE      ANS,DIM1
          REP       "Y1D1M1C1V1",DIM1
          ENDSET    ANS
          APPEND    SP1,ANS
          RESET     ANS
          MATCH     SP1,ANS
          GOTO      SADM1000 IF EQUAL
          MATCH     "1",DIM1
          IF        @EQUAL
            MOVE      ANS,XUPDFLG
          ELSE
            GOTO      SADM1150
          ENDIF
.
SADM2000  KEYIN     *P1:14,*EF,"Admission No. : ",*V2LON,AADMNO;
          MATCH     ZEROVISN,AADMNO
          GOTO      SADM2100 IF NOT EQUAL
          DISPLAY   *P1:14,*EL;
          GOTO      SADM1150
.
SADM2100  PACK      KEY8,AADMNO
          CALL      RDMISS1
          BRANCH    OVRCD,SADM2200
.
          PACK      KEY8,AADMNO
          CALL      RDVISA1                 * Read the visit file
          BRANCH    OVRCD,SADM2200
.
          PACK      KEY8,AADMNO
          CALL      RDPMVX11                * Read the visit extn file
          BRANCH    OVRCD,SADM2200
.
          COMPARE   FIVE,ASTAT          * Cancelled ?
          GOTO      SADM2150 IF NOT EQUAL
          DISPLAY   *P1:24,*EL,*B,*V2LON,"** Patient is cancelled.**    ";
          CALL      HITENTER
          GOTO      SADM2000
.
SADM2150  COMPARE   THREE,ASTAT         * Discharged ?
          GOTO      SADM3000 IF EQUAL
          DISPLAY   *P1:24,*EL,*B,*V2LON,"** Patient is not discharged.**    ";
          CALL      HITENTER
          GOTO      SADM2000
.
SADM2200  DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid admission number.**    ";
          CALL      HITENTER
          GOTO      SADM2000
.
.         check if master data exist for this Admission Number
.
SADM3000  MOVE      AURNO,KEY8
          CALL      RDMAST1
          COMPARE   ZERO,OVRCD
          IF        @EQUAL
            CALL      RDPMPX21
            COMPARE   ZERO,OVRCD
            GOTO      SADM4000 IF EQUAL
          ENDIF
.
          DISPLAY   *P1:24,*EL,*B,*V2LON,"** Missing master record.**    ";
          CALL      HITENTER
          GOTO      SADM2000
.
.         display basic details for this patient and ask if patient is correct
.
SADM4000  DISPLAY   *P1:13,*EF,*P1:14,"Admission No. : ":
                    *P26:14,"Name :",*P65:14,"Sex :":
                    *P1:15,"U/R Number    :",*P26:15,"Born : ",*P44:15,"Age :":
                    *P54:15,"Admission Date : ":
                    *P1:16,"Ward : ",*P16:16,"Doct : ",*P54:16,"Discharge":
                           " Date :";
.
          CALL      GDET0000                * get patient's details
          BRANCH    EXIT,SADM2000
          CALL      CALCAGE
.
          DISPLAY   *P17:14,*V2LON,AADMNO,*P33:14,PATNAME,*P71:14,SEX:
                    *P17:15,AURNO,*P33:15,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR:
                    *P50:15,PSAGEYRS,*P71:15,SAVDATE:
                    *P8:16,WRDBED,*P23:16,DOCNAME,*P71:16,CPCDATE;
.
.         patient who discharged within the date range must have been processed
.
          MATCH     STRTDATE,DDATE
          GOTO      SADM5000 IF LESS
.
          MATCH     DDATE,ENDDATE
          GOTO      SADM5000 IF LESS 
.
          DISPLAY   *P1:19,*EL,*B,*V2LON,"** Patient has been processed. **  ";
          CALL      HITENTER
          GOTO      SADM2000
.
SADM5000  DISPLAY   *P1:24,*EL,"Correct Patient (",*V2LON,"Y",*HOFF,"/",*V2LON:
                               "N",*HOFF,") ?";
.
SADM7000  KEYIN     *P25:24,*DV,UNDLN1,*P25:24,*V2LON,ANS;
          ENDSET    ANS
          APPEND    SP1,ANS
          RESET     ANS
.
          MATCH     SP1,ANS
          GOTO      SADM7200 IF EQUAL
          REP       UPPLOW,ANS
          DISPLAY   *P25:24,*V2LON,ANS;
.
          MATCH     ANSY,ANS
          GOTO      SADM8000 IF EQUAL
          MATCH     ANSN,ANS
          GOTO      SADM2000 IF EQUAL
.
SADM7200  BEEP
          GOTO      SADM7000
.
SADM8000  MOVE      ZERO,COUNTO
          MOVE      ZERO,COUNTM
          MOVE      ZERO,COUNTEX
          MOVE      ZERO,COUNTD
          CALL      SETF0000                * Set up fields
          CALL      WREC0000                * Write Record
          CALL      ECDA0000                * Get dis & ops after
          CALL      ECOA0000                * writing summary details.
          CALL      WRCHK
          GOTO      SADM2000                * Next admission
.
SADM9999  RETURN
+
.******************************************************************************
.*                                  GDET0000                                  *
.*        get patient's details                called by : GAEP0000, SADM0000 *
.******************************************************************************
GDET0000  MOVE      ZERO,EXIT
.
. ------  get patient's name  ------
.
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,PATNAME
.
. ------  get patient's sex  ------
.
.* ****************************************** Start of Changes         *I-49016
.                                           * original code for sex desc. has
.                                           * been deleted & replaced by :-
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          MOVE      DSEXDESC,SEX
.* ******************************************   End of Changes         *I-49016
.
. ------  get patient's date of birth  ------
.
GDET1200  UNPACK    PBDATE,XCENT,XYEAR,XMON,XDAY
.
          MATCH     SP3,AWARD
          GOTO      GDET2100 IF NOT EQUAL
.
          MOVE      "N/A",AWARD
          MOVE      SP3,ABED
.
GDET2100  PACK      WRDBED,AWARD,ABED            * for screen display
.
. ------  get attending doctor  ------
.
          PACK      PACFNAME,SP30,SP30
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD,GDET3000
.
          LENSET    DGNAME
          MOVE      DSNAME,PACSNAME
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,DOCNAME
.
. ------  get Admission Date  ------
.
GDET3000  UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,SAVDATE
.
. ------  get Discharge Date  ------
.
          MOVE      SP8,CPCDATE
          PACK      KEY8,AADMNO
          CALL      RDDSCH1
          BRANCH    OVRCD,GDET8000
.
          UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.                                               * I CAR 37921
          MOVE      ZERO,NEWFILE
          MATCH     "20090701",DDATE
          IF        !@LESS
            MOVE      ONE,NEWFILE
          ENDIF
.                                               * end I CAR 37921
          GOTO      GDET9000
.
GDET8000  DISPLAY   *P1:24,*EL,*B,*V2LON,"** Missing discharged record.**    ";
          CALL      HITENTER
          MOVE      ONE,EXIT
          GOTO      GDET9999
.
GDET9000  MOVE      ZERO,EXIT
GDET9999  RETURN
+
.******************************************************************************
.*                                 EXTC0000               Called by: ML0000   *
.*                        Load The Admission Details                          *
.******************************************************************************
EXTC0000  MOVE      ZERO,EXIT
          MOVE      ZERO,CODEFLG            * Default to coded
          MOVE      SP1,XUPDFLG             * set update flag to "No"
          MOVE      ZERO,RECNUM
          DISPLAY   *P1:24,*EL,"Scanning : ",*P25:24,"Loading : ";
          CALL      CREA0000                * Open the tempfile
          CALL      VHCD0000                * validating hospital code
.
          PACK      KEY16,STRTDATE,SP20
          CALL      RDSDSCH2                * Position on the discharge file
EXTC1000  CALL      RDKDSCH2                * Read the discharge file
          BRANCH    OVRCD,EXTC8000
.
          MATCH     DDATE,ENDDATE
          GOTO      EXTC8000 IF LESS        * Out of date range, exit
.
          DISPLAY   *P12:24,*V2LON,DADMNO;
          PACK      KEY8,DURNO
          CALL      RDMAST1                 * Read the master file
          BRANCH    OVRCD,EXTC1000
.
          PACK      KEY8,DURNO
          CALL      RDPMPX21                * Read Extn File 2
          BRANCH    OVRCD,EXTC1000
.
          PACK      KEY8,DADMNO
          CALL      RDMISS1                 * Read the admission file
          BRANCH    OVRCD,EXTC1000
.
          PACK      KEY8,DADMNO
          CALL      RDVISA1                 * Read the visit file
          BRANCH    OVRCD,EXTC1000
.
          PACK      KEY8,DADMNO
          CALL      RDPMVX11                * Read the visit extn file
          BRANCH    OVRCD,EXTC1000
.
          PACK      KEY5,CATA,ATYPE
          CALL      RDCODE1
          BRANCH    OVRCD,EXTC7000
.
          MATCH     "X",TCINDC11            * srf 638331
          GOTO      EXTC1000 IF EQUAL
.
          BRANCH    DSCHFLAG,EXTC7000       * Discharged only
.
          CALL      CHCO0000                * check if patient is coded
          BRANCH    CODEFLG,EXTC1000
.
EXTC7000  MATCH     "5",DASTAT
          GOTO      EXTC1000 IF EQUAL       * no cancelled records
.
          MOVE      ZERO,COUNTO
          MOVE      ZERO,COUNTM
          MOVE      ZERO,COUNTEX
          MOVE      ZERO,COUNTD
          CALL      SETF0000                * Set up fields
          CALL      WREC0000                * Write Record
          BRANCH    DSCHFLAG,EXTC1000       * Discharged Only
          CALL      ECDA0000                * Get dis & ops after
          CALL      ECOA0000                * writing summary details.
          CALL      WRCHK    
          GOTO      EXTC1000
.
EXTC8000  CALL      SADM0000                * process an admission ?
.
          MOVE      SP10,DIM6
          MOVE      RECNUM,DIM6
          REP       " 0",DIM6
          WRITE     WORK,SEQ;DIM6
.
          DISPLAY   *P1:24,*EL,*V2LON,*B,"Transmission completed. ";
          CALL      HITENTER
.
EXTC9999  RETURN
+
.******************************************************************************
.*                                  CHCO0000              Called by: PROC0000 *
.*                           Check If Patients Coded                          *
.******************************************************************************
CHCO0000  MOVE      ZERO,CODEFLG            * Default to coded
          PACK      KEY13,AADMNO,SP20                                 *C-240107
          CALL      RSPTECD1
          CALL      RKPTECD1
          BRANCH    OVRCD,CHCO1000
.
          MATCH     AADMNO,PTEDADMN
          GOTO      CHCO9000 IF EQUAL
.
CHCO1000  PACK      KEY13,AADMNO,SP20                                 *C-240107
          CALL      RSPTECO1
CHCO2000  CALL      RKPTECO1
          BRANCH    OVRCD,CHCO3000
.
          MATCH     AADMNO,PTEOADMN
          GOTO      CHCO3000 IF NOT EQUAL
.
          MATCH     ANSX,PTEOTYPE
          GOTO      CHCO2000 IF EQUAL
          GOTO      CHCO9000
.
CHCO3000  MOVE      ONE,CODEFLG             * Not coded
.
CHCO9000  MOVE      ZERO,EXIT
CHCO9999  RETURN
+
.******************************************************************************
.*                                 IVAR0000               Called by: EXTC0000 *
.*                        Initialisation variables                            *
.******************************************************************************
IVAR0000  MOVE      ZERO,EXIT
.                                           * do not clear XUPDFLG or XHCODE
.
          UNPACK    SP70,XPROCCOD,XCLINPR,XDATPROC
          UNPACK    SP70,XPRIOR,XICDCOD,XPLCOCC,XACTIV
          UNPACK    SP70,XCOFICD,XCOFPOO,XCOFACT
.
          MOVE      ZERO,XDAYQNB
          MOVE      ZERO,XACCOCC
          MOVE      ZERO,XHIHCD
          UNPACK    SP50,XEVNTYP,XMORTYP,XEOCLNK,XNUMLEV,XLANG
          UNPACK    SP50,XSORLOC,XSORCLI,XSORTRA,XADMNO               *C-114471
          UNPACK    SP50,XPURNO,XMUMSID,XADATE,XATIME,XDDATE,XDTIME
          UNPACK    SP50,XPSNAME
          UNPACK    SP50,XPGNAME
          UNPACK    SP50,XSFNAME
          UNPACK    SP50,XPADD1
          UNPACK    SP50,XSUBRB
          UNPACK    SP50,XPCODE,XSTATE,XDOBDT,XPSEX,XABORN
          UNPACK    SP50,XCNTBR,XMSTAT,XEMPST,XINTRP,XOCCPC,XSREFR
          UNPACK    SP70,XINLOS,XCTHOS,XLOCTN,XNMRNO,XSPLTY,XSPCDS
          UNPACK    SP50,XADTYP,XWEIGH,XLEVDY,XPSYCH,XMHLGL
          UNPACK    SP50,XPAYCL,XVETRN,XINSRN,XICUDY,XVENHR,XREADM
          UNPACK    SP50,XURETN,XEPSCR,XCLNST,XCTRES,XSEPTP,XDSCTO
          UNPACK    SP50,XDSATT,XGRPVR,XDIACG,XDIAGR,XCODID,XDVAFN
          UNPACK    SP50,XDVAAN,XDVAAD,XICUHR
          MOVE      SP70,XMEDINO
          UNPACK    SP70,XMEDIPID,XGENDER
.
          MOVE      ZERO,NUMLEV
          MOVE      SP10,ACTIVITY
          MOVE      ONE,F2
.
IVAR9999  RETURN
+
.******************************************************************************
.*                                 SETF0000               Called by: EXTC0000 *
.*                        Set up fields                                       *
.******************************************************************************
SETF0000  CALL      IVAR0000                     * initialise variables
.
          MOVE      AADMNO,DIM8VISN
          PACK      XADMNO,SP4,DIM8VISN              * Admission Number
          REP       " 0",XADMNO
          MOVE      AADMNO,SKEY8
.
....      IF        AURNO = 0
          MATCH     "       0",AURNO
          IF        @EQUAL
            MOVE      "Missing U/R number ",ERRDESC
            CALL      WRTEMP 
          ELSE
            MOVE      AURNO,DIM8
            PACK      XPURNO,SP2,DIM8              * U/R Number
            REP       " 0",XPURNO
          ENDIF
.
          MATCH     ADATE,PBDATE                 * check if it's a baby
          IF        @EQUAL
            MOVE      ZERO,FORM8
            PACK      KEY16,PURNO,SP8
            CALL      RDSLINK1
SETF0500    CALL      RDKLINK1
            IF        OVRCD<>1
              PACK      XPURNO,SP2,PURNO
              REP       " 0",XPURNO
              MOVE      PURNO,DIM8
              MATCH     LINKFUR,DIM8
              IF        @EQUAL
                PACK      KEY5,ANSL,ANSU,LINKREA,SP8
                CALL      RDCODE1
                MATCH     TCINDC1,ANSM
                IF        @EQUAL
                  MOVE      LINKTUR,FORM8  
                ENDIF
                GOTO      SETF0500
              ENDIF
            ENDIF
.
            COMPARE   ZERO,FORM8  
            IF        @EQUAL
              MOVE      "Missing Mother ID for A baby ",ERRDESC
              CALL      WRTEMP 
            ELSE
              MOVE      FORM8,DIM8
              PACK      XMUMSID,SP2,DIM8           * Mother's admission number
              REP       " 0",XMUMSID
            ENDIF
          ENDIF
.
          PACK      XADATE,SP20
          MATCH     PBDATE,ADATE
          IF        @LESS
            MOVE      "Adm.Date must be >= DOB",ERRDESC
            CALL      WRTEMP
          ELSE
            MATCH     ADATE,DDATE
            IF        @LESS
              MOVE    "Disc.Date must be >= Adm.Date",ERRDESC
              CALL      WRTEMP
            ELSE
              UNPACK    ADATE,XCENT,XYEAR,XMON,XDAY
              PACK      XADATE,XDAY,XMON,XCENT,XYEAR
              REP       " 0",XADATE                  * adm.date
            ENDIF
          ENDIF
.
          UNPACK    ATIME,DIM2,ANS,DIM2A
          PACK      XATIME,DIM2,DIM2A
          REP       " 0",XATIME                  * adm.time
          MOVE      ZERO,FORM4
          MOVE      XATIME,FORM4
          IF        FORM4 >= 2400 
            MOVE      "Adm.Time is >= 2400 Hrs",ERRDESC
            CALL      WRTEMP
          ENDIF
.
          MATCH     SP8,PMVXEOCL
          IF        @EQUAL|@EOS
            MOVE      AADMNO,PMVXEOCL
          ENDIF
.
          SQUEEZE   PMVXEOCL
          MOVE      PMVXEOCL,XEOCLNK            * EOC link admission number
.
          UNPACK    SP30,XSORLOC,XSORCLI,XSORTRA
          READ      CONTROLF,HUND14;*218,PTCNSORL
          MATCH     "1",PTCNSORL
          IF        @EQUAL
            PACK      KEY5,ANSS,SP1,ASRCE,SP70     * location SOR
            CALL      RDCODE1
            IF        OVRCD<>1
              MOVE      THCSCOD,XSORLOC
            ENDIF
          ELSE
            PACK      KEY5,ANSS,ANSG,PMVXSLOC     * location SOR
            CALL      RDCODE1
            IF        OVRCD<>1
              MOVE      THCSCOD,XSORLOC
            ENDIF
          ENDIF
.
          PACK      KEY5,ANSS,ANSH,PMVXSCLI     * clinical SOR
          CALL      RDCODE1
          IF        OVRCD<>1
            MOVE      THCSCOD,XSORCLI
          ENDIF
.
          PACK      KEY5,ANSS,ANSI,PMVXSTRA     * transport SOR
          CALL      RDCODE1
          IF        OVRCD<>1
            MOVE      THCSCOD,XSORTRA
          ENDIF
.
          CALL      SDSC0000                     * setup discharged details
.
          PACK      XPSNAME,PSNAME,SP20,SP20
          MATCH     SP20,PSNAME
          IF        @EQUAL
            MOVE      "Surname is UNKNOWN ",ERRDESC
            CALL      WRTEMP
          ENDIF
          CALL      GSNM0000                * Get the given/second name
.
          PACK      XPADD1,PADD1,SP20,SP20
          PACK      XPCODE,PPOST,SP10
          PACK      XSUBRB,SP30,SP30
.
          PACK      POSTCODE,PPOST,SP70
.
          PACK      KEY56,PPOST,PADD2,SP10,PADD4,SP70                 *C-240184
          CALL      RDIBPOS1                * Read a postcode file record
          BRANCH    OVRCD,SETF1000
.
          MOVE      IBPOSUBR,XSUBRB         * Suburb
          GOTO      SETF4000
.
SETF1000  PACK      KEY56,PPOST,PSUBRB,SP10,PADD4,SP70                *C-240184
          CALL      RDIBPOS1                * Read a postcode file record
          BRANCH    OVRCD,SETF2000
.
          MOVE      IBPOSUBR,XSUBRB         * Suburb
          GOTO      SETF4000
.
SETF2000  PACK      KEY56,PPOST,SP70                                  *C-240184
          CALL      RSIBPOS1                * Position & read a postcode file
SETF2100  CALL      RKIBPOS1                  record
          BRANCH    OVRCD,SETF3000
.
          MATCH     PPOST,IBPOPCOD
          GOTO      SETF3000 IF NOT EQUAL   * Different postcode, error
.
          MATCH     "OS ",IBPOSTAT
          GOTO      SETF2100 IF EQUAL
.
          MOVE      IBPOSUBR,XSUBRB         * Suburb
          GOTO      SETF4000
.
SETF3000  MOVE      "Invalid Post code ",ERRDESC
          CALL      WRTEMP
.
.
SETF4000  PACK      XSTATE,SP10
          MATCH     "8888",XPCODE
          IF        @EQUAL
            MOVE      PSUBRB,XSUBRB         * Suburb
            MOVE      "0",XSTATE
            GOTO      SETF4100
          ENDIF
.
          MATCH     "NSW",IBPOSTAT
          IF        @EQUAL
            MOVE      "1",XSTATE
            GOTO      SETF4100
          ENDIF
          MATCH     "VIC",IBPOSTAT
          IF        @EQUAL
            MOVE      "2",XSTATE
            GOTO      SETF4100
          ENDIF
.
          MATCH     "QLD",IBPOSTAT
          IF        @EQUAL
            MOVE      "3",XSTATE
            GOTO      SETF4100
          ENDIF
.
          MATCH     "SA ",IBPOSTAT
          IF        @EQUAL
            MOVE      "4",XSTATE
            GOTO      SETF4100
          ENDIF
.
          MATCH     "WA ",IBPOSTAT
          IF        @EQUAL
            MOVE      "5",XSTATE
            GOTO      SETF4100
          ENDIF
.
          MATCH     "TAS",IBPOSTAT
          IF        @EQUAL
            MOVE      "6",XSTATE
            GOTO      SETF4100
          ENDIF
.
          MATCH     "NT ",IBPOSTAT
          IF        @EQUAL
            MOVE      "7",XSTATE
            GOTO      SETF4100
          ENDIF
.
          MATCH     "ACT",IBPOSTAT
          IF        @EQUAL
            MOVE      "8",XSTATE
            GOTO      SETF4100
          ENDIF
.
          MOVE      "0",XSTATE
.
SETF4100  UNPACK    PBDATE,XCENT,XYEAR,XMON,XDAY
          MATCH     SP8,PBDATE
          IF        @EQUAL
            MOVE      "Missing Date of Birth ",ERRDESC
            CALL      WRTEMP
          ELSE
            PACK      XDOBDT,XDAY,XMON,XCENT,XYEAR   * dob
          ENDIF
.
.                                           * original code for sex desc. has
.                                           * been deleted & replaced by :-
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          IF        DSEXNO > 2
            MOVE      THREE,XPSEX
          ELSE
            MOVE      DSEXNO,XPSEX
          ENDIF
.
          CALL      INDG0000                     * Indigenous status
.
          PACK      XCNTBR,SP10                  * Country of birth
          PACK      KEY5,ANSC,SP1,PCONT
          CALL      RDCODE1
          IF        OVRCD = 0
            UNPACK    THCSCOD,XCNTBR
          ENDIF
.
          MATCH     SP4,XCNTBR
          IF        @EQUAL
            MOVE      "Country of Birth code is blank ",ERRDESC
            CALL      WRTEMP
          ELSE
            TYPE      XCNTBR
            IF        !@EQUAL
              MOVE      "Country of Birth Code is in not in Numeric ",ERRDESC
              CALL      WRTEMP
            ENDIF
          ENDIF
.
          PACK      XMSTAT,SP10                  * Marital status
          PACK      KEY5,ANSM,SP1,PMSTAT
          CALL      RDCODE1
          IF        OVRCD = 0
            PACK      XMSTAT,THCSCOD,SP20        * Marital status
          ENDIF
          MATCH     SP1,XMSTAT   
          IF        @EQUAL
            MOVE      "Marital status is blank ",ERRDESC
            CALL      WRTEMP
          ELSE
            MOVE      ZERO,FORM1
            MOVE      XMSTAT,FORM1
            IF        FORM1 < 1 | FORM1 > 6
              MOVE     "Marital status must be between 1 to 6 ",ERRDESC
              CALL      WRTEMP
            ENDIF
            CALL      CALCAGE
            IF        PSAGEYRS < 14
              MATCH     "1",XMSTAT
              IF        !@EQUAL
                MOVE      "Age < 14, marital status shoud be 1 (Single)",ERRDESC
                CALL      WRTEMP
              ENDIF
            ENDIF
          ENDIF
.
          CALL      EMPL0000                * Get occupation/employment stat
.
          CALL      LANG0000                * routine from IBAPMS49
.
SETF4500  MOVE      SP10,XSREFR               * Source of referral
.
SETF5000  CALL      ILOS0000                  * intended LOS
.
          CALL      CLST0000                  * Client status
.
          CALL      ADMF0000                  * contracting/adm.from/trans.to
.
          CALL      ADST0000                  * admission status
.
          CALL      WGHT0000                  * birth weight
.
          CALL      PYCL0000                  * Payment classification
          CALL      VETR0000                  * Veteran
.
          CALL      INST0000                  * Insurance status
.
          CALL      GEND0000                  * Gender
.
SETF5010  MOVE      SP10,XVENHR               * Ventilator in hours
          CALL      CHBL0000                  * Check Block Number
          MATCH     "Y",ICUMECFL
          GOTO      SETF5020 IF NOT EQUAL
.
          PACK      KEY8,AADMNO
          CALL      RDPTICU1
          IF        OVRCD = 0
            COMPARE   ZERO,PTICUMEC
            IF        !@EQUAL
              MOVE      PTICUMEC,XVENHR       * in hours              *C-250267
              REP       " 0",XVENHR
            ENDIF
          ENDIF
.
.         Re-admission Status                    **********
SETF5020  PACK      XREADM,SP20
          MOVE      SP10,DIM3
.
          PACK      KEY5,ANSK,ANSO,PMVXUREA
          CALL      RDCODE1                 * Cat."KO"
          IF        OVRCD<>1
            MATCH     SP1,TCINDC1
            IF        @EQUAL
              UNPACK    THCSCOD,DIM1,XREADM      * then use HDP 2 pos
            ELSE
              UNPACK    TCINDC1,XREADM           * use Indicator 1
            ENDIF
          ENDIF
          REP       "Y1N2",XREADM
.
          CALL      UPLN0000                  * Unplanned return
.
          CALL      EPSD0000                  * Episode care
.
          PACK      ACTIVITY,SP10
          PACK      KEY5,CODEPO,APLOCCR
          CALL      RDCODE1
          IF        OVRCD = 0
            PACK      ACTIVITY,THCSCOD        * Episode of care
          ENDIF
.
          CALL      CLOS0000                  * loop through trans file
.
          MATCH     "70",XSEPTP
          IF        @EQUAL
            MATCH     "0000",XLEVDY
            IF        @EQUAL
              CLEAR     ERRDESC
              APPEND    "Separation mode discharge from leave & ",ERRDESC
              APPEND    "leave days = 0",ERRDESC
              RESET     ERRDESC
              CALL      WRTEMP
            ENDIF
          ENDIF
.
          MOVE      PMEDI,XMEDINO
          SQUEEZE   PTMXMCCD
          MOVE      PTMXMCCD,XMEDIPID
.
          MATCH     SP70,XMEDINO
          IF        !@EQUAL
            MATCH     SP70,XMEDIPID
            IF        @EQUAL
              CLEAR     ERRDESC
              APPEND    "Medicare Number Person ID is missing.",ERRDESC
              RESET     ERRDESC
              CALL      WRTEMP
            ENDIF
          ENDIF
.
SETF9999  RETURN
+
.******************************************************************************
.*                                 VHCD0000               Called by: SETF0000 *
.*                        Validating hospital file no / establishment         *
.******************************************************************************
VHCD0000  UNPACK    CFILENO,DIM4
          MOVE      DIM4,XHCODE
          RESET     DIM4
VHCD1000  CMATCH    SP1,DIM4
          GOTO      VHCD9000 IF EQUAL
          MOVE      SP1,DIM1
          MOVE      DIM4,DIM1
          TYPE      DIM1
          GOTO      VHCD9000 IF NOT EQUAL
          BUMP      DIM4,1
          GOTO      VHCD1000 IF NOT EOS
          GOTO      VHCD9999
.
VHCD9000  MOVE      "Hospital code must be a 4 character number ",ERRDESC
          MOVE      SP10,SKEY8
          CALL      WRTEMP
VHCD9999  RETURN
+
.******************************************************************************
.*                                 INDG0000               Called by: SETF0000 *
.*                        Get the indigenous status                           *
.******************************************************************************
INDG0000  PACK      XABORN,SP10                  * Aboriginal
.
          MATCH     SP3,PMPXABRG
          IF        !@EQUAL
            PACK      KEY5,CODEVA,PMPXABRG,SP70
            CALL      RDCODE1
            IF        OVRCD = 0
              UNPACK    THCSCOD,XABORN           * Aboriginal
            ENDIF
          ENDIF
.
          MATCH     SP1,XABORN
          IF        @EQUAL
            MOVE      "Indigenous Status is blank ",ERRDESC
            CALL      WRTEMP
          ENDIF
.
INDG9999  RETURN
+
.******************************************************************************
.*                                 EMPL0000               Called by: SETF0000 *
.*                        Get employment status/Occupation                    *
.******************************************************************************
EMPL0000  MOVE      SP10,XOCCPC                  * occupation not required
.
          READ      CONTROLF,EIGHTY8;*237,PTCNEMPL
          MOVE      SP10,ACODE
          MOVE      ZERO,FORM1
          UNPACK    SP2,DIM1,DIM1A
          UNPACK    PTCNEMPL,DIM1,DIM1A
          MOVE      DIM1A,FORM1
          MATCH     ANSU,DIM1
          IF        @EQUAL
            LOAD      ACODE,FORM1,AUSR1,AUSR2,AUSR3,AUSR4,AUSR5,AUSR6,AUSR7
          ENDIF
.
          MATCH     ANSP,DIM1
          IF        @EQUAL
            LOAD      ACODE,FORM1,PUSR1,PUSR2,PUSR3,PUSR4,PUSR5,PUSR6
          ENDIF
.
          PACK      KEY5,PTCNEMPL,ACODE,SP70
          CALL      RDCODE1
          IF        OVRCD = 0
            UNPACK    THCSCOD,DIM1,DIM2        * Employment status
            PACK      XEMPST,DIM1,SP2
          ENDIF
.
          MATCH     SP3,PMVXEMPL
          IF        !@EQUAL
            PACK      KEY5,ANSK,ANSD,PMVXEMPL,SP70
            CALL      RDCODE1
            IF        OVRCD = 0
              UNPACK    THCSCOD,DIM1,DIM2        * Employment status
              PACK      XEMPST,DIM1,SP2
            ENDIF
          ENDIF
.
EMPL9999  RETURN
+
.******************************************************************************
.*                                 CLST0000               Called by: SETF0000 *
.*                        Client status                                       *
.******************************************************************************
CLST0000  PACK      XCLNST,SP10
          MOVE      SP1,DIM1
          PACK      KEY5,ANSA,SP1,ATYPE
          CALL      RDCODE1
          IF        OVRCD = 0
            UNPACK    THCSCOD,DIM1,DIM4
            PACK      XCLNST,DIM1,SP1         * client status
          ENDIF
.
          MATCH     SP2,XCLNST
          IF        !@EQUAL
            UNPACK    SP2,DIM1,DIM1A
            MOVE      ZERO,FORM1
            UNPACK    XCLNST,DIM1,DIM1A
            MATCH     SP1,DIM1A
            IF        @EQUAL
              MOVE      DIM1,FORM1
              IF        FORM1 = 0
                GOTO      CLST9999
              ENDIF
              IF        FORM1 = 9
                GOTO      CLST9999
              ENDIF
....          IF        FORM1 > 0 & FORM1 < 8                           D-61767
              IF        FORM1 > 0 & FORM1 < 9
                GOTO      CLST9999
              ENDIF
            ENDIF
          ENDIF
.
CLST9000  MOVE        SP1,ANS
....      MOVE        "Client status must be b/w 0 & 7 ",ERRDESC        D-61767
          MOVE        "Patient Type must be b/w 0 & 9 ",ERRDESC
          CALL        WRTEMP
.
CLST9999  RETURN
+
.******************************************************************************
.*                                 ILOS0000               Called by: SETF0000 *
.*                        Intended LOS                                        *
.******************************************************************************
ILOS0000  PACK      XINLOS,SP10                  * Intended LOS
          MOVE      SP1,TCINDC1
.
          MATCH     SP3,PMVXIDUS
          IF        !@EQUAL
            PACK      KEY5,ANSV,ANSI,PMVXIDUS,SP70
            CALL      RDCODE1
            IF        OVRCD = 0
              UNPACK    TCINDC1,XINLOS           * Intended LOS
            ENDIF
          ENDIF
.
          MATCH     "1",XINLOS
          IF        @EQUAL
            MOVE      "2",XINLOS
            GOTO      ILOS9999
          ENDIF
.
          MATCH     "2",XINLOS
          IF        @EQUAL
            MOVE      "1",XINLOS
            GOTO      ILOS9999
          ENDIF
.
          MATCH     SP1,XINLOS
          IF        @EQUAL
            MOVE      "Intended LOS is blank ",ERRDESC
            CALL      WRTEMP
          ELSE
            MOVE      ZERO,FORM1
            MOVE      XINLOS,FORM1
            IF        FORM1 <> 1 & FORM1 <> 2
              MOVE      "Intended LOS must be 1 or 2 ",ERRDESC
              CALL      WRTEMP
            ENDIF
          ENDIF
ILOS9999  RETURN
+
.******************************************************************************
.*                                 ADMF0000               Called by: SETF0000 *
.*                        Admitting from/transfer to                          *
.******************************************************************************
ADMF0000  MOVE      SP10,XCTRES              * contracting
          MOVE      SP10,XCTHOS              * admitting from
          MOVE      SP10,XDSCTO              * transfer to
.
.                         * Using Admission/Discharge Transfer Dest file (1=yes)
          IF        PTCNUADT = 1
            PACK      KEY8,AADMNO
            CALL      RDPTDAD1
            BRANCH    OVRCD,ADMF5000 
.
            PACK      KEY5,PTDAADTS,SP70
            CALL      RDPTVAD1
            IF        OVRCD=1
              MOVE      PTDAADTS,XCTHOS
            ELSE
              MATCH     SP70,PTVANHSC
              IF        !@EQUAL
                MOVE      PTVANHSC,XCTHOS      * Valid transfer source
              ELSE
                MOVE      PTVACODE,XCTHOS      * Valid transfer source
              ENDIF
            ENDIF
.                                       * WA HDP Using Contracting ? (1=Yes)
ADMF1000    MATCH     "1",PTCNWAUC
            IF        @EQUAL
              MATCH     "5 ",XCLNST            * check if client status = 5
              IF        @EQUAL
                PACK      KEY25,AADMNO,ANSA,SP30
                CALL      RDPTASF1
                IF        OVRCD = 0
                  PACK      XCTRES,PTAFCSID    * contracting
....              PACK      XCTHOS,SP8                                *D-269788
                ENDIF
              ENDIF
              MATCH     "0 ",XCLNST            * check if client status = 0
              IF        @EQUAL
                PACK      KEY25,AADMNO,ANSA,SP30
                CALL      RDPTASF1
                IF        OVRCD = 0
                  PACK      XCTRES,PTAFCSID    * contracting
                ENDIF
              ENDIF
            ENDIF
            PACK      KEY5,PTDADCTS,SP70
            CALL      RDPTVAD1
            IF        OVRCD=1
              MOVE      PTDADCTS,XDSCTO
            ELSE
              MATCH     SP70,PTVANHSC
              IF        !@EQUAL
                MOVE      PTVANHSC,XDSCTO      * Valid transfer source
              ELSE
                MOVE      PTVACODE,XDSCTO      * Valid transfer source
              ENDIF
            ENDIF
.
ADMF5000    MATCH     SP4,XCTHOS
            IF        @EQUAL
              MOVE      "0900",XCTHOS
            ENDIF
.
            MATCH     SP4,XDSCTO
            IF        @EQUAL
              PACK      KEY8,AADMNO
              CALL      RDDSCH1
.                                               * I CAR 37921
              MOVE      ZERO,NEWFILE
              MATCH     "20090701",DDATE
              IF        !@LESS
                MOVE      ONE,NEWFILE
              ENDIF
.                                  
              PACK      KEY5,SP10
....          IF        PTDTWHDP = 1         * this is not in V9 - use PTNSWHDP
              IF        PTNSWHDP = 0
                MATCH     SP3,DDEST
                IF        !@EQUAL
                  PACK      KEY5,ANSD,ANSD,DDEST
                ENDIF
              ELSE
                MATCH     SP3,DSTAT
                IF        !@EQUAL
                  PACK      KEY5,ANSD,SP1,DSTAT
                ENDIF
              ENDIF
              CALL      RDCODE1
              MATCH     SP4,THCSCOD
              IF        @EQUAL
                MOVE      "0900",XDSCTO
              ELSE
                MOVE      THCSCOD,XDSCTO
              ENDIF
            ENDIF
.
          ENDIF
.
          MATCH     "0944",XCTHOS           * Reclassified This Hospital
          IF        @EQUAL
            MOVE      AADMNO,SAVADMNO       * Save our admission first
            CALL      EOCL0000
          ENDIF
.
          GOTO      ADMF9999 
.
ADMF9000  MOVE      "Admitted from is blank ",ERRDESC
          CALL      WRTEMP
.
ADMF9999  RETURN
+
.******************************************************************************
.*                                 EOCL0000               Called by: ADMF0000 *
.*                        Get episode of care link if statistical admission   *
.******************************************************************************
EOCL0000  PACK      KEY24,AURNO,ADATE,Z70
          CALL      RDSDSCH3                * Higher admission number might not
EOCL1000  CALL      RDPDSCH3                * be for a later date/time
          BRANCH    OVRCD,EOCL9000
.
          MATCH     AURNO,DURNO             * Discharge for correct U/R
          GOTO      EOCL9000 IF NOT EQUAL
.
          MATCH     ADATE,DDATE             * Admission and Discharge
          GOTO      EOCL9000 IF NOT EQUAL   * date equal
.
          MATCH     AADMNO,DADMNO           * Ignore the admissions
          GOTO      EOCL1000 IF EQUAL       * own discharge
.
          MATCH     DTIME,ATIME             * Find first discharge time prior
          GOTO      EOCL1000 IF LESS        * to admission time
.
.         We've got our previous discharge/admission; now check if it's a
.         Statistical Admission?
          MOVE      DDADMNO,CHKADMNO
          CALL      CHKSTA00                * Is it a Statistical Admission?
          IF        EXIT=1
            PACK      KEY8,DADMNO,SP10
            CALL      RDMISS1               * Read the admission file
            BRANCH    OVRCD,EOCL2000
.
            GOTO      EOCL0000              * Get previous discharge/admission
          ENDIF
.
EOCL2000  SQUEEZE   DDADMNO
          MOVE      DDADMNO,XEOCLNK
.
EOCL9000  PACK      KEY8,SAVADMNO,SP70      * Read original admission
          CALL      RDMISS1
          CALL      RDDSCH1
.
EOCL9999  RETURN
+
.******************************************************************************
.*                  CHKSTA00                              Called by: EOCL0000 *
.*        Check if the admission (CHKADMNO) is a Statistical Admission.       *
.*                                                                            *
.*        Return:                                                             *
.*          EXIT = 0 (NO)                                                     *
.*               = 1 (YES)                                                    *
.******************************************************************************
CHKSTA00  MOVE      ZERO,EXIT
          MOVE      SP10,DIM4
.
          PACK      KEY8,CHKADMNO,SP10
          CALL      RDPTDAD1                * read Transfer Destination file
          BRANCH    OVRCD,CHKSTA99
.
          PACK      KEY5,PTDAADTS,SP70      * use Transfer Source Code
          CALL      RDPTVAD1                * read Transfer Source Code file
          IF        OVRCD=1
.           No matching Transfer Source Code record; so we just use the
.           'Admission Transfer Source' code from the Transfer Destination file
            MOVE      PTDAADTS,DIM4
          ELSE
            MATCH     SP70,PTVANHSC
            IF        !@EQUAL
              MOVE      PTVANHSC,DIM4       * HDP Equivalent
            ELSE
              MOVE      PTVACODE,DIM4       * Transfer Destination Code
            ENDIF
          ENDIF
.
          MATCH     "0944",DIM4
          IF        @EQUAL
            MOVE      ONE,EXIT              * Is a Statistical Admission
          ENDIF
CHKSTA99  RETURN
+
.******************************************************************************
.*                                 ADST0000               Called by: SETF0000 *
.*                        Admission status                                    *
.******************************************************************************
ADST0000  PACK      XADTYP,SP10
          MOVE      SP3,DIM3
          LOAD      KEY2,PTCNELPS,CODEU1,CODEU2,CODEU3,CODEU4,CODEU5:
                                  CODEU6,CODEU7,CODEU8,CODEU9,CODEV1:
                                  CODEV2,CODECC,CODEP,CODEVB
.
          LOAD      KEY3,PTCNELPS,AUSR1,AUSR2,AUSR3,AUSR4,AUSR5:
                                  AUSR6,AUSR7,DIM3,DIM3,DIM3:
                                  DIM3,ACARE,ACLSS,PMVXCADM
          MATCH     SP3,KEY3
          IF        !@EQUAL
            PACK      KEY5,KEY2,KEY3
            CALL      RDCODE1
            IF        OVRCD = 0
              UNPACK    THCSCOD,XADTYP             * admission status
            ENDIF
          ENDIF
.         MATCH     "6",XADTYP
.         IF        @EQUAL
.           MOVE      "5",XADTYP
.         ENDIF
          MOVE      ZERO,FORM1
          MOVE      XADTYP,FORM1
          IF        NEWFILE=1
            IF        FORM1 < 3 | FORM1 > 7
              MOVE      "Admission Status must be 3,4,6 or 7",ERRDESC
              CALL      WRTEMP
            ENDIF
          ELSE
            IF        FORM1 < 3 | FORM1 > 5
              MOVE      "Admission Status must be 3,4 or 5",ERRDESC
              CALL      WRTEMP
            ENDIF
          ENDIF
.
ADST9999  RETURN
+
.******************************************************************************
.*                                 WGHT0000               Called by: SETF0000 *
.*                        birth weight                                        *
.******************************************************************************
WGHT0000  MOVE      SP10,XWEIGH
          MOVE      SP10,DIM4
          MOVE      ZERO,FORM4
          UNPACK    ABWGHT,DIM2,DIM4
          MATCH     "   0",DIM4
          IF        @EQUAL
            MOVE      SP4,DIM4
          ENDIF
.
          MATCH     SP4,DIM4
          IF        !@EQUAL
            MOVE      DIM4,FORM4               * Right justified
            MOVE      FORM4,DIM4
            REP       " 0",DIM4
            PACK      XWEIGH,DIM4,SP10
          ENDIF
.
          PACK      AGEDATE,ADATE
          CALL      CALCAGE                 * Calculate age
          IF        PSAGEDAY < PTCNBAGE
            MATCH     SP4,XWEIGH          * it's a baby, must have weight
            IF        @EQUAL
              MOVE      "Baby must have weight ",ERRDESC
              CALL      WRTEMP
            ENDIF
          ELSE
            MOVE      XWEIGH,FORM6
            IF        FORM6 > 9000
              MOVE      SP70,XWEIGH
              CLEAR     ERRDESC
              APPEND    "Warning Birth Weight > 9000g, ",ERRDESC
              APPEND    SP70,ERRDESC
              RESET     ERRDESC
              CALL      WRTEMP
            ENDIF
          ENDIF
.
WGHT9999  RETURN
+
.******************************************************************************
.*                                 PYCL0000               Called by: SETF0000 *
.*                        get payment classification                          *
.******************************************************************************
PYCL0000  PACK      XPAYCL,SP10
          PACK      KEY5,CODECL,ACLAIM
          CALL      RDCODE1
          IF        OVRCD = 0
            UNPACK    THCSCOD,XPAYCL             * payment class.
          ENDIF
.
PYCL1000  MATCH     SP2,XPAYCL                   * I CAR 37921
          IF        !@EQUAL
            STRIP     XPAYCL
            MOVE      XPAYCL,XF2 
            SETLPTR   XPAYCL
            IF        XF2=21|XF2=22|XF2=23|XF2=24|XF2=25|XF2=26|XF2=27|XF2=28|XF2=29|XF2=30|XF2=31|XF2=32|XF2=33|XF2=34
              GOTO      PYCL9999
            ENDIF
          ENDIF
.
          MOVE      "Payment classification must be b/w 21 to 34 ",ERRDESC
          CALL      WRTEMP
.                                                * end I CAR 37921
PYCL9999  RETURN
+
.******************************************************************************
.*                                 VETR0000               Called by: SETF0000 *
.*                        get Veteran card                                    *
.******************************************************************************
VETR0000  PACK      XVETRN,SP10                  * Veteran
          MOVE      SP10,ACODE
          MOVE      ZERO,FORM1
          PACK      XDVAFN,SP70        * claim number
          PACK      XDVAAN,SP70        * authorisation number
          PACK      XDVAAD,SP70        * authorisation date
.
          MATCH     "27",XPAYCL                * DVA
          GOTO      VETR9999 IF NOT EQUAL
.
.         Get DVA Number using concession card details
.
          MOVE      THREE,DIM1                  * set for DVA card type
          CALL      GDVACR00                    * get DVA card details
          PACK      XDVAFN,PMCDCNUM             * claim number
.
          MATCH     SP3,PMCDDVAC
          IF        !@EQUAL 
            PACK      KEY5,ANSD,ANSX,PMCDDVAC,SP70
            CALL      RDCODE1
            IF        OVRCD = 0
              PACK      XVETRN,THCSCOD
              REP       "G1",XVETRN
              REP       "W2",XVETRN
            ENDIF
          ENDIF
.                                                       * C CAR 37921
          MATCH     "27",XPAYCL
          IF        @EQUAL
            MATCH     SP1,XVETRN
            IF        @EQUAL
            MOVE      "Payment Class is 27,VA Colour must not be blank",ERRDESC
              CALL      WRTEMP
            ENDIF
          ELSE
            GOTO      VETR9999     * not DVA patient
          ENDIF
.                                                   * end C CAR 37921
          PACK        KEY8,AADMNO
          CALL        RDWVET1
          IF          OVRCD=0
.           PACK        XDVAFN,VCLMNO      * claim number
....                                                        * start   *D-233977
....        PACK        XDVAAN,PTWVAUTH    * authorisation number
....        UNPACK      PTWVDATE,CCENT,CYEAR,CMON,CDAY    * authorisation date
....        PACK        XDVAAD,CDAY,CMON,CCENT,CYEAR * authorisation date
....        REP         " 0",XDVAAD
....        MATCH       "00000000",XDVAAD
....        IF          @EQUAL
....          MOVE        SP8,XDVAAD
....        ENDIF                                           * end     *D-233977
          ENDIF
VETR9999  RETURN
+
.******************************************************************************
.*                               GDVACR00                                     *
.*                        get Veteran card                                    *
.******************************************************************************
GDVACR00  MOVE      "ct",DIM2
          PACK      KEY19,PURNO,SP70
          CALL      RSPMCCD1
GDVACR20  CALL      RKPMCCD1
          BRANCH    OVRCD,GDVACR80
.
          MATCH     PURNO,PMCDURNO                 * same U/R still ?
          GOTO      GDVACR80 IF NOT EQUAL          * no - nothing found
.
.         Check if the card type is DVA or Pension (Cat ct, Indicator 1)
.
          PACK      KEY5,DIM2,PMCDCTYP
          CALL      RDCODE1                        * code on file ?
          BRANCH    OVRCD,GDVACR20                 * no - ignore record
.
          MATCH     DIM1,TCINDC1                   * same concession card type?
          GOTO      GDVACR20 IF NOT EQUAL          * no - ignore record
.
.         A concession card of the same type has been found, so check
.         if there is an expiry date
.
          MATCH     SP8,PMCDEXDT                 * expiry date blank ?
          GOTO      GDVACR99 IF EQUAL            * yes - assume valid
.
.         There is an expiry date for the card, so check that the
.         card is current
.
          PACK      DIM8,DDATE
          MATCH     SP70,DDATE
          IF        @EQUAL
            CALL      IBACLOCK
            PACK      DIM8,CCC,CYY,CMM,CDD
            REP       " 0",DIM8
          ENDIF
.
          MATCH     PMCDEXDT,DIM8               * card valid for date ?
          GOTO      GDVACR99 IF LESS             * no - nothing found
.
          GOTO      GDVACR20
.
GDVACR80  MOVE      SP70,PMCDCNUM
          MOVE      SP70,PMCDCTYP
          MOVE      SP70,PMCDEXDT
          MOVE      SP70,PMCDDVAC
          GOTO      GDVACR99
.
GDVACR99  RETURN
.
.******************************************************************************
.*                                 INST0000               Called by: SETF0000 *
.*                        Insurance status                                    *
.******************************************************************************
INST0000  MOVE      "2",XINSRN                * not insured
          MATCH     SP6,AFUNDH
          IF        !@EQUAL
            MOVE      "1",XINSRN              * insured
            MATCH     "23",XPAYCL
            IF        @EQUAL
              MOVE      "2",XINSRN              * insured
            ENDIF
          ENDIF
.                                             * C CAR 37921
          MATCH     "22",XPAYCL
          IF        @EQUAL
            MATCH     "1",XINSRN
            IF        !@EQUAL
              MOVE    "Payment Class.=22, Insurance status must be 1 ",ERRDESC
              CALL      WRTEMP
            ENDIF
          ENDIF
.                                             * end C CAR 37921
INST9999  RETURN
.
.******************************************************************************
.                   Gender
.******************************************************************************
GEND0000  PACK      XGENDER,SP70                 * Gender
.
          MATCH     SP3,PMPXUCC4
          IF        !@EQUAL
            PACK      KEY5,CODEGi,PMPXUCC4,SP70
            CALL      RDCODE1
            IF        OVRCD = 0
              UNPACK    THCSCOD,XGENDER          * Gender
            ENDIF
          ENDIF
.
          MOVE      ZERO,FORM1
          MOVE      SP1,DIM1
          UNPACK    XGENDER,DIM1
          MATCH     SP1,DIM1
          IF        !@EQUAL
            MOVE      DIM1,FORM1
            IF        FORM1=0 | FORM1=6 | FORM1=7 | FORM1=8
              GOTO      GEND2000
            ENDIF
            GOTO      GEND9999
          ENDIF
.
GEND2000  MOVE      "Gender code must be b/w 1-5 or 9 ",ERRDESC
          CALL      WRTEMP
.
GEND9999  RETURN
+
.******************************************************************************
.*                                 UPLN0000               Called by: SETF0000 *
.*                        Unplanned return to theatre                         *
.******************************************************************************
UPLN0000  PACK      XURETN,SP10
          MOVE      SP10,DIM3
.
          PACK      KEY5,ANSK,ANSB,PMVXUVTH
          CALL      RDCODE1                 * Cat."KB"
          IF        OVRCD<>1
            UNPACK    THCSCOD,XURETN        * unplanned readmission alternate
          ENDIF
.
          MATCH     SP1,XURETN
          GOTO      UPLN9999 IF EQUAL
.
          MOVE      ZERO,FORM1
          MOVE      XURETN,FORM1
          IF        FORM1 <> 1 & FORM1 <> 2
            MOVE      "Unplanned return to theatre must be 1 or 2 ",ERRDESC
            CALL      WRTEMP
          ENDIF
UPLN9999  RETURN
+
.******************************************************************************
.*                                 EPSD0000               Called by: SETF0000 *
.*                   type episode of care                                     *
.******************************************************************************
EPSD0000  PACK      XEPSCR,SP10
          MOVE      ZERO,FORM2
          PACK      KEY5,ANSC,ANSC,ACARE
          CALL      RDCODE1
          IF        OVRCD = 0
            PACK      XEPSCR,THCSCOD          * Episode of care
          ENDIF
.
          MATCH     SP2,XEPSCR
          IF        @EQUAL
            MOVE      "Episode of care is blank ",ERRDESC
            CALL      WRTEMP
            GOTO      EPSD9999
          ELSE
            UNPACK    XEPSCR,DIM1,DIM1A
            MATCH     SP1,DIM1A
            IF        @EQUAL
              MOVE      DIM1,FORM2
            ELSE
              MOVE      XEPSCR,FORM2
            ENDIF
.
.            COMPARE   ONE,DOYR2015
.            GOTO      EPSD1000 IF EQUAL
.
.            IF        FORM2 > 20 & FORM2 < 32
.              GOTO      EPSD5000
.            ENDIF
.
EPSD1000    IF        FORM2 > 20 & FORM2 < 33
              GOTO      EPSD5000
            ENDIF
          ENDIF
.
.          IF        DOYR2015=1
.            MOVE        "Episode of care must be b/w 21 to 32 ",ERRDESC
.          ELSE
.            MOVE        "Episode of care must be b/w 21 to 31 ",ERRDESC
.          ENDIF
.
          MOVE      "Episode of care must be b/w 21 to 32 ",ERRDESC
          CALL      WRTEMP
.
          GOTO      EPSD9999
.
.
EPSD5000  MOVE      SP1,ANS                 * check Client Status combinations
.
          MATCH     "0",XCLNST
          IF        @EQUAL
            IF  FORM2=21 | FORM2=22 | FORM2=23 | FORM2=24 | FORM2=25 | FORM2=29
              GOTO      EPSD9999
            ENDIF
            GOTO      EPSD6000
          ENDIF
.
          MATCH     "1",XCLNST
          IF        @EQUAL
            IF        FORM2=26
              GOTO      EPSD9999
            ENDIF
            GOTO      EPSD6000
          ENDIF
.
          MATCH     "2",XCLNST
          IF        @EQUAL
            IF        FORM2=26
              GOTO      EPSD9999
            ENDIF
            GOTO      EPSD6000
          ENDIF
.
          MATCH     "3",XCLNST
          IF        @EQUAL
            IF        FORM2=28
              GOTO      EPSD9999
            ENDIF
            GOTO      EPSD6000
          ENDIF
.
          MATCH     "4",XCLNST
          IF        @EQUAL
            IF        FORM2=25
              GOTO      EPSD9999
            ENDIF
            GOTO      EPSD6000
          ENDIF
.
          MATCH     "5",XCLNST
          IF        @EQUAL
            IF  FORM2=21 | FORM2=22 | FORM2=23 | FORM2=24 | FORM2=25 | FORM2=29 | FORM2=32
              GOTO      EPSD9999
            ENDIF
            GOTO      EPSD6000
          ENDIF
.
          MATCH     "6",XCLNST
          IF        @EQUAL
            IF  FORM2=21 | FORM2=22 | FORM2=23 | FORM2=24 | FORM2=25 | FORM2=29 | FORM2=32
              GOTO      EPSD9999
            ENDIF
            GOTO      EPSD6000
          ENDIF
.
          MATCH     "7",XCLNST
          IF        @EQUAL
            IF        FORM2=27
              GOTO      EPSD9999
            ENDIF
            GOTO      EPSD6000
          ENDIF
.
          MATCH     "8",XCLNST
          IF        @EQUAL
            IF        FORM2=30 | FORM2=31
              GOTO      EPSD9999
            ENDIF
            GOTO      EPSD6000
          ENDIF
.
.
EPSD6000  MOVE      "Patient Type/Care Type combination invalid ",ERRDESC
          CALL      WRTEMP
.
EPSD9999  RETURN
+
.******************************************************************************
.*                                 ERRS0000               Called by: SETF0000 *
.*                        print error messages                                *
.******************************************************************************
ERRS0000  COMPARE   ZERO,RECNUM
          GOTO      ERRS9999 IF EQUAL
.
          MOVE      "60",CLNO
          MOVE      ZERO,CPAGENO
          CALL      HEAD0000
.
          PACK      KEY8,SP10
          CALL      RSTEMP
ERRS1000  CALL      RKTEMP
          BRANCH    OVRCD,ERRS9000
.
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PRINT     *1,"|",*3,PURNO,*12,"|",*14,AADMNO:
                    *23,"|",*25,ERRDESC,*132,"|"
          ADD       ONE,CLNO
          GOTO      ERRS1000
.
ERRS9000  CALL      UND132
ERRS9999  RETURN
+
.******************************************************************************
.*                                 HEAD0000               Called by: SETF0000 *
.*                        Setup Heading                                       *
.******************************************************************************
HEAD0000  IF        DSCHFLAG=1
            MOVE      "Discharged Only",CPHDROP1
          ELSE
            PACK      CPHDROP1,SP70,SP70
          ENDIF
.
          CALL      IBACLOCK
          CALL      HEAD132
.
          CALL      UND132 
          PRINT     *1,"| U/R No",*12,"| Adm. No":
                    *23,"| Description",*132,"|"
          CALL      UND132
          ADD       TWO,CLNO
HEAD9999  RETURN
+
.******************************************************************************
.*                                 SDSC0000               Called by: SETF0000 *
.*                        Setup discharged details                            *
.******************************************************************************
SDSC0000  MOVE      AURNO,DIM8
          CALL      RDDSCH1
          BRANCH    OVRCD,SDSC9000
.
          MOVE      DDATE,DISCHDAT
.                                               * I CAR 37921
          MOVE      ZERO,NEWFILE
          MATCH     "20090701",DDATE
          IF        !@LESS
            MOVE      ONE,NEWFILE
          ENDIF
.                                               * end I CAR 37921
          MATCH     PBDATE,DDATE
          IF        @LESS
            MOVE      "Disc.Date must be >= DOB",ERRDESC
            CALL      WRTEMP
          ELSE
            MATCH     ADATE,DDATE
            IF        @LESS
              MOVE    "Disc.Date must be >= Adm.Date",ERRDESC
              CALL      WRTEMP
            ELSE
              UNPACK    DDATE,XCENT,XYEAR,XMON,XDAY        * Discharge date
              PACK      XDDATE,XDAY,XMON,XCENT,XYEAR
              REP       " 0",XDDATE
            ENDIF
          ENDIF
.
          UNPACK    DTIME,DIM2,ANS,DIM2A
          PACK      XDTIME,DIM2,DIM2A
          REP       " 0",XDTIME
          MOVE      ZERO,FORM4
          MOVE      XDTIME,FORM4
          IF        FORM4 >= 2400 
            MOVE      "Disc.Time is >= 2400 Hrs",ERRDESC
            CALL      WRTEMP
          ENDIF
.
          CALL      DRGV0000
.
          MATCH     SP3,PMVXMHLS                * Mental Health Legal Status
          IF        !@EQUAL
            PACK      KEY5,ANSL,ANSS,PMVXMHLS,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              MOVE      TCINDC5,XMHLGL          * Legal status        *I-101974
            ENDIF
          ELSE
.
            MATCH     SP70,ACARE
            GOTO      SDSC4600 IF EQUAL
.
            PACK      KEY5,ANSC,ANSC,ACARE,SP70
            CALL      RDCODE1
            BRANCH    OVRCD,SDSC4600
.
            MATCH     ANSP,TCINDC5
            GOTO      SDSC4600 IF NOT EQUAL
.
            PACK      KEY32,AURNO,Z70
            CALL      RSMHDLS1
SDSC4000    CALL      RPMHDLS1
            BRANCH    OVRCD,SDSC4600
.
            MATCH     AURNO,MHDLURNO
            GOTO      SDSC4600 IF NOT EQUAL
.
            MATCH     "0",MHDLACTV
            GOTO      SDSC4000 IF NOT EQUAL
            MATCH     SP70,MHDLEDAT
            GOTO      SDSC4500 IF EQUAL
            GOTO      SDSC4500 IF EOS
.
            MATCH     DDATE,MHDLEDAT
            GOTO      SDSC4600 IF LESS
.
SDSC4500    MATCH     SP70,MHDLSCOD
            GOTO      SDSC4600 IF EQUAL
            GOTO      SDSC4600 IF EOS
.
            PACK      KEY5,ANSL,ANSS,MHDLSCOD,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              MOVE      TCINDC5,XMHLGL
            ENDIF
.
          ENDIF
.
SDSC4600  PACK      KEY5,SP10
          IF        PTNSWHDP = 1
            MATCH     SP3,DSTAT
            IF        !@EQUAL
              PACK      KEY5,ANSD,SP1,DSTAT
            ENDIF
          ELSE
            MATCH     SP3,DDEST
            IF        !@EQUAL
              PACK      KEY5,ANSD,ANSD,DDEST
            ENDIF
          ENDIF
.
          MATCH     JULY2024,DDATE
          IF        !@LESS
            GOTO      SDSC5000
          ENDIF
.
SDSC4800  CALL      RDCODE1
          IF        OVRCD<>1
            UNPACK    TCINDC5,XSEPTP           * Discharge to
          ENDIF
          UNPACK    SP2,DIM1,DIM1A
          MOVE      ZERO,FORM1
          UNPACK    XSEPTP,DIM1
          MATCH     SP1,DIM1
          IF        !@EQUAL
            MOVE      DIM1,FORM1
            COMPARE   ZERO,FORM1
            GOTO      SDSC9999 IF NOT EQUAL
          ENDIF
          MOVE      "Mode of separation must be b/w 1 to 9 ",ERRDESC
          CALL      WRTEMP
          GOTO      SDSC9999
.
SDSC5000  CALL      RDCODE1
          IF        OVRCD = 0
            UNPACK    PTCDUDF3,XSEPTP
          ENDIF
.
          MOVE      ZERO,YF2
          MOVE      XSEPTP,YF2
.
          IF        YF2 = ZERO
            GOTO      SDSC7000
          ENDIF
.
          IF        YF2 < 10 | YF2 > 90
            GOTO      SDSC7000
          ENDIF
.
          GOTO      SDSC9999
.
SDSC7000  MOVE      "Mode of Separation must be b/w 10 to 90",ERRDESC
          CALL      WRTEMP
          GOTO      SDSC9999
.
SDSC9000  MOVE      "Missing Discharged record",ERRDESC
          CALL      WRTEMP
SDSC9999  RETURN
+
.******************************************************************************
.*                                DRGV0000                Called by: PROC0000 *
.*      Get the drg from patpgraf based on the discharge date grouper version *
.******************************************************************************
.
DRGV0000  MATCH     PTCNE110,DISCHDAT       * disc.date > DRG 11.0 effect.date?
          IF        !@LESS
            MOVE      DRG110,DRGVERS        * Use 11.0 DRG version
            GOTO      DRGV1000
          ENDIF
.
          MATCH     PTCNE100,DISCHDAT       * disc.date > DRG 10.0 effect.date?
          IF        !@LESS
            MOVE      DRG100,DRGVERS        * Use 10.0 DRG version
            GOTO      DRGV1000
          ENDIF
.
          MATCH     PTCNED90,DISCHDAT       * disc.date > DRG 9.0 effect.date?
          IF        !@LESS
            MOVE      DRG090,DRGVERS        * Use 9.0 DRG version
            GOTO      DRGV1000
          ENDIF
.
          MATCH     PTCNED80,DISCHDAT       * disc.date > DRG 8.0 effect.date?
          IF        !@LESS
            MOVE      DRG080,DRGVERS        * Use 8.0 DRG version
            GOTO      DRGV1000
          ENDIF
.
          MATCH     PTCNED70,DISCHDAT       * disc.date > DRG 7.0 effect.date?
          IF        !@LESS
            MOVE      DRG070,DRGVERS        * Use 7.0 DRG version
            GOTO      DRGV1000
          ENDIF
.
          MATCH     PTCNED62,DISCHDAT       * disc.date > DRG 6.2 effect.date?
          IF        !@LESS
            MOVE      DRG062,DRGVERS        * Using 6.2 DRG version
            GOTO      DRGV1000
          ENDIF
.
          MATCH     PTCNED60,DISCHDAT       * disc.date > DRG 6.0 effect.date?
          IF        !@LESS
            MOVE      DRG060,DRGVERS        * Using 6.0 DRG version
            GOTO      DRGV1000
          ENDIF
.
          MATCH     PTCNED52,DISCHDAT       * disc.date > DRG 5.2 effect.date?
          IF        !@LESS
            MOVE      DRG052,DRGVERS        * Using 5.2 DRG version
            GOTO      DRGV1000
          ENDIF
.
          MATCH     PTCNED51,DISCHDAT       * disc.date > DRG 5.1 effect.date?
          IF        !@LESS
            MOVE      DRG051,DRGVERS        * Using 5.1 DRG version
            GOTO      DRGV1000
          ENDIF
.
          MATCH     PTCNED50,DISCHDAT       * disc.date > DRG 5.0 effect.date?
          IF        !@LESS
            MOVE      DRG050,DRGVERS        * Using 5.0 DRG version
            GOTO      DRGV1000
          ENDIF
.
          MATCH     PTCNED42,DISCHDAT       * disc.date > DRG 4.2 effect.date?
          IF        !@LESS
            MOVE      DRG042,DRGVERS        * Using 4.2 DRG version
            GOTO      DRGV1000
          ENDIF
.
          MATCH     PTCNED41,DISCHDAT       * disc.date > DRG 4.1 effect.date?
          IF        !@LESS
            MOVE      DRG041,DRGVERS        * Using 4.1 DRG version
            GOTO      DRGV1000
          ENDIF
.
          GOTO      DRGV9000
.
DRGV1000  PACK      KEY13,AADMNO,SP1,ONE,DRGVERS
          CALL      RDPTPGR1                * DRG found for grouper vers ?
          BRANCH    OVRCD,DRGV9000          * no - finished
.
          MATCH     SP4,PTPGNDRG            * blank DRG ?
          GOTO      DRGV9000 IF EQUAL       * yes - finished
.
          MOVE      PTPGNDRG,XDIAGR         * load DRG
.
          UNPACK    DRGVERS,DIM1,D1,ANS     * break up into 0,5,2
          MATCH     "1",DIM1
          IF        @EQUAL
            PACK      XGRPVR,DIM1,D1,DOT,ANS,SP10  * set DRG Version as "10.0"
          ELSE
            PACK      XGRPVR,D1,DOT,ANS,SP10       * set DRG Version as "5.2"
          ENDIF
.
          PACK      KEY7,PTPGNDRG,DRGVERS
          CALL      RDPTDGW1
          BRANCH    OVRCD,DRGV9000
.
          MOVE      PTDWMDCC,XDIACG         * load MDC
.
DRGV9000  MOVE      ZERO,EXIT
.
DRGV9999  RETURN
+
.******************************************************************************
.*                                 GSNM0000               Called by: EXTC0000 *
.*                        Get the second name                                 *
.******************************************************************************
GSNM0000  PACK      XPGNAME,PGNAME,SP20,SP20  * first forename 
          RESET     XPGNAME
GSNM1000  BUMP      XPGNAME
          GOTO      GSNM2000 IF EOS
          MATCH     SP1,XPGNAME
          GOTO      GSNM1000 IF NOT EQUAL
          APPEND    SP20,XPGNAME
          APPEND    SP20,XPGNAME
          RESET     XPGNAME
.
GSNM2000  PACK      XSFNAME,SP20,SP20       * second name
          RESET     PGNAME
GSNM3000  BUMP      PGNAME
          GOTO      GSNM9999 IF EOS
          MATCH     SP1,PGNAME
          GOTO      GSNM3000 IF NOT EQUAL
          BUMP      PGNAME
          PACK      XSFNAME,PGNAME,SP20,SP20
GSNM9999  RETURN
+
.****************************************************************************** .*                                 ECDA0000               Called by: EXTC0000 * .*                        Generate Diseases detail                            *
.******************************************************************************
ECDA0000  MOVE      ZERO,COUNT
          MOVE      ZERO,COUNTA
          MOVE      ZERO,COUNTE
          MOVE      ZERO,COUNTP
          PACK      KEY16,AADMNO,SP20                                 (C-240107
          CALL      RSPTECD2                * Position on & read a patient
ECDA1000  CALL      RKPTECD2                  episode disease file record
          BRANCH    OVRCD,ECDA9999
.
          MATCH     AADMNO,PTEDADMN
          GOTO      ECDA9999 IF NOT EQUAL
.
          MOVE      ZERO,CMPCNT             * Complex map counter - no
.
ECDA3000  MATCH     PTCNI10D,DDATE
          IF        @LESS
            PACK      KEY9,PTEDCODE
            CALL      RD10T9D1              * Read an ICD9 disease file record
            BRANCH    OVRCD,ECDA1000
.
            MOVE      DICD10CD,PTEDCODE     * Mapped ICD10 disease code
          ENDIF
.
          PACK      KEY9,PTEDCODE
          MOVE      DDATE,ICDRDDTE          * send Coding Date
          CALL      RDPTICD1                * read ICD10 Vn - selected by routin
          BRANCH    OVRCD,ECDA1000
.
ECDA4000  MOVE      PTEDCODE,XICDCOD
          MOVE      PTEDOSET,XCOFICD
          CALL      WRDIS000                * write a disease record
          GOTO      ECDA1000
.
ECDA5000  COMPARE   ZERO,CMPCNT
          GOTO      ECDA1000 IF EQUAL       * Not using complex mapping
.
          ADD       ONE,CMPCNT
          COMPARE   FOUR,CMPCNT
          GOTO      ECDA1000 IF NOT LESS    * Complex map counter >=4
.
          MOVE      SP9,PTEDCODE
          LOAD      PTEDCODE,CMPCNT,PTCP1MCD,PTCP2MCD,PTCP3MCD
          MATCH     SP9,PTEDCODE
          GOTO      ECDA1000 IF EQUAL       * Blank code
          GOTO      ECDA2000
.
ECDA9999  RETURN
+
.*******************************************************************************
.*         RMD000 - Remove Dot, place of occurrence and activity fields        *
.*******************************************************************************
.
RMD000    REP       "E ",KEY9
          RESET     KEY9
          CLEAR     KEY7
.
RMD100    CMATCH    SP1,KEY9
          GOTO      RMD200 IF EQUAL
.
          CMATCH    DOT,KEY9
          GOTO      RMD200 IF EQUAL
.
          CMATCH    "-",KEY9
          GOTO      RMD200 IF EQUAL
.
          CMATCH    SLASH,KEY9              * Ignore slashes
          GOTO      RMD200 IF EQUAL
          MOVE      KEY9,ANS
          APPEND    ANS,KEY7
.
RMD200    BUMP      KEY9
          GOTO      RMD100 IF NOT EOS
.
RMD300    RESET     KEY9
          APPEND    SP10,KEY7
          RESET     KEY7
.
RMD999  RETURN
.
.******************************************************************************
.*                                 CHBL0000               Called by: EXTC0000 *
.*                        Check operation for Block Number                    *
.******************************************************************************
.
CHBL0000  MOVE      "N",ICUMECFL            * Y/N Flag - Mechanical Ventilation
          MOVE      ZERO,COUNT
          PACK      KEY16,AADMNO,SP2
          CALL      RSPTECO2                * Position on & read a patient
CHBL1000  CALL      RKPTECO2                  episode operation file record
          BRANCH    OVRCD,CHBL9999
.
          MATCH     AADMNO,PTEOADMN
          GOTO      CHBL9999 IF NOT EQUAL
.
          MATCH     ANSO,PTEOTYPE
          GOTO      CHBL1000 IF NOT EQUAL   * Ignore "X" "I" & "N"
.
          MATCH     PTCNEDRG,PTEOCODE
          GOTO      CHBL1000 IF EQUAL       * Exclude dummy code
.
          MOVE      ZERO,CMPCNT             * Complex map counter - no
          PACK      KEY9,PTEOCODE
          MATCH     "19990701",DDATE
          GOTO      CHBL3000 IF NOT LESS    * do coding in ICD10-M
.
.         Sending codes in ICD9
.
          MATCH     PTCNI10D,PTEODTCD
          IF        !@LESS
            MOVE      PTEOCODE,PTCPCOD
            CALL      PATM10T9              * Map ICD10 to ICD9 codes
            MOVE      PTCP1MCD,PTEOCODE
            MOVE      ONE,CMPCNT            * Complex map counter - yes
          ENDIF
.
CHBL2000  PACK      KEY9,PTEOCODE
          CALL      RD10T9O1                * Read an ICD9 operation file record
          BRANCH    OVRCD,CHBL1000
          GOTO      CHBL1000
.
.         Sending codes in ICD10
.
CHBL3000  MATCH     PTCNI10D,DDATE
          IF        @LESS
            PACK      KEY9,PTEOCODE
            CALL      RD10T9O1              * Read an ICD9 oper file record
            BRANCH    OVRCD,CHBL1000
.
            MOVE      OICD10CD,PTEOCODE     * Mapped ICD10 oper code
          ENDIF
.
          PACK      KEY9,PTEOCODE
          MOVE      DDATE,ICDRDDTE          * send Coding Date
          CALL      RDPTICO1                * read ICD10 Vn - selected by routin
          BRANCH    OVRCD,CHBL1000
.
.         Require ICU Mechanical Ventilation Hours for Block 569
.
          MATCH     "0569",PT0OBLKN
          GOTO      CHBL9000 IF EQUAL
          MATCH     " 569",PT0OBLKN
          GOTO      CHBL9000 IF EQUAL
          MATCH     "569 ",PT0OBLKN
          GOTO      CHBL9000 IF EQUAL
.
          GOTO      CHBL1000                * Next Code
.
CHBL9000  MOVE      "Y",ICUMECFL
.
CHBL9999  RETURN
+
.******************************************************************************
.*                                 ECOA0000               Called by: EXTC0000 *
.*                        Generate operations detail                          *
.******************************************************************************
ECOA0000  MOVE      ZERO,COUNT
          PACK      KEY16,AADMNO,SP2                                  *C-240107
          CALL      RSPTECO2                * Position on & read a patient
ECOA1000  CALL      RKPTECO2                  episode operation file record
          BRANCH    OVRCD,ECOA9999
.
          MATCH     AADMNO,PTEOADMN
          GOTO      ECOA9999 IF NOT EQUAL
.
          MATCH     ANSO,PTEOTYPE                                      *I-48888
          GOTO      ECOA1000 IF NOT EQUAL   * Ignore "X" "I" & "N"     *I-48888
.
          MATCH     PTCNEDRG,PTEOCODE
          GOTO      ECOA1000 IF EQUAL       * Exclude dummy code
.
          MOVE      ZERO,CMPCNT             * Complex map counter - no
          PACK      KEY9,PTEOCODE
          MATCH     "19990701",DDATE
          GOTO      ECOA3000 IF NOT LESS    * do coding in ICD10-M
.
.         Sending codes in ICD9
.
          MATCH     PTCNI10D,PTEODTCD
          IF        !@LESS
            MOVE      PTEOCODE,PTCPCOD
            CALL      PATM10T9              * Map ICD10 to ICD9 codes
            MOVE      PTCP1MCD,PTEOCODE
            MOVE      ONE,CMPCNT            * Complex map counter - yes
          ENDIF
.
ECOA2000  PACK      KEY9,PTEOCODE
          CALL      RD10T9O1                * Read an ICD9 operation file record
          BRANCH    OVRCD,ECOA5000
          GOTO      ECOA4000
.
.         Sending codes in ICD10
.
ECOA3000  MATCH     PTCNI10D,DDATE
          IF        @LESS
            PACK      KEY9,PTEOCODE
            CALL      RD10T9O1              * Read an ICD9 oper file record
            BRANCH    OVRCD,ECOA1000
.
            MOVE      OICD10CD,PTEOCODE     * Mapped ICD10 oper code
          ENDIF
.
          PACK      KEY9,PTEOCODE
....      CALL      RDPT10O1                * Read an ICD10 oper file record
          MOVE      DDATE,ICDRDDTE          * send Coding Date
          CALL      RDPTICO1                * read ICD10 Vn - selected by routin
          BRANCH    OVRCD,ECOA1000
.
ECOA4000  MOVE      PTEOCODE,XPROCCOD       
.
          MOVE      PTEOCODE,DIM9
          CALL      RMDT0000                 * Remove any dots
          PACK      DIM10,DIM9A,SP10
.
          ADD       ONE,COUNT
          REP       " 0",PTEODATE
          UNPACK    PTEODATE,XCENT,XYEAR,XMON,XDAY
          PACK      PTEODATE,XDAY,XMON,XCENT,XYEAR
.
          MOVE      PTEODATE,XDATPROC
.
          PACK      KEY6,TADOCT             * Attending Doc.
          CALL      RDDOCT1
          IF        OVRCD = 0
            MATCH     SP15,PTDOREGN                        * start    *C-233977
            IF        !@EQUAL
              MOVE      PTDOREGN,KEY15      * National Registration No.
            ELSE
              MOVE      PTDOWHDP,KEY15      * WA HDP Doctor code
            ENDIF
            SQUEEZE   KEY15
            PACK      XCLINPR,KEY15,SP15    * Aust Health Prac. Reg. No.
          ENDIF
.
          CALL      WRPROC                  * write an operation record
          GOTO      ECOA1000
.
ECOA5000  COMPARE   ZERO,CMPCNT
          GOTO      ECOA1000 IF EQUAL       * Not using complex mapping
.
          ADD       ONE,CMPCNT
          COMPARE   FOUR,CMPCNT
          GOTO      ECOA1000 IF NOT LESS    * Complex map counter >=4
.
          MOVE      SP9,PTEOCODE
          LOAD      PTEOCODE,CMPCNT,PTCP1MCD,PTCP2MCD,PTCP3MCD
          MATCH     SP9,PTEOCODE
          GOTO      ECOA1000 IF EQUAL       * Blank code
          GOTO      ECOA2000
.
ECOA9999  RETURN
+
.******************************************************************************
.*                                  RMDT0000              Called by: EXTC0000 *
.*          Move DIM9 to DIM9A (Left Justify), eliminating any dots           *
.******************************************************************************
RMDT0000  MOVE      DIM9,DIM9A
          RESET     DIM9A
          REP       ". - / ",DIM9A
          SQUEEZE   DIM9A
          PACK      DIM9A,DIM9A,SP9
.
RMDT9000  MOVE      ZERO,EXIT
RMDT9999  RETURN
+
.******************************************************************************
.*                                 WREC0000               Called by: EXTC0000 *
.*                        Write a record                                      *
.******************************************************************************
WREC0000  DISPLAY   *P40:24,*V2LON,DADMNO;
          ADD       ONE,RECNUM
          MOVE      "SUM",XEVNTYP
          MOVE      "I",XMORTYP
          MOVE      NUMLEV,XNUMLEV
          REP       "G1",XVETRN
          REP       "W2",XVETRN
.
          PACK      KEY16,AADMNO,SP20                                 *C-240107
          CALL      RSPTECD2                * Position on & read a patient
          CALL      RKPTECD2                  episode disease file record
          BRANCH    OVRCD,WREC1000
.
          MATCH     AADMNO,PTEDADMN
          GOTO      WREC1000 IF NOT EQUAL
.
          COMPARE   ONE,SCODFLG             * Check if to send coder id ?
          GOTO      WREC1000 IF NOT EQUAL   * no.
.
.         PACK      XCODID,PTEDOPER,SP20    * user name
          PACK      XCODID,PTEDUSID,SP20    * user name
.
WREC1000  MOVE      ZERO,EXIT 
          IF        DOYR2025 = 1
            WRITE     WORK5,SEQ;XEVNTYP,XMORTYP,XEOCLNK,XDAYQNB,XNUMLEV:
                              XACCOCC,XLANG,XUPDFLG,XSORLOC,XSORCLI:
                              XSORTRA,XHIHCD,SP7,XHCODE,XADMNO:
                              XPURNO,XMUMSID,XADATE,XATIME,XDDATE:
                              XDTIME,XPSNAME,XPGNAME,XSFNAME,XPADD1,XSUBRB:
                              XPCODE,XSTATE,SP6,XDOBDT,XPSEX,XABORN,XCNTBR:
                              XMSTAT,XEMPST,XINTRP,XOCCPC,XSREFR,XINLOS,XCTHOS:
                              XLOCTN,XNMRNO,XSPLTY,XSPCDS,XADTYP,XWEIGH,XLEVDY:
                              XPSYCH,XMHLGL,XPAYCL,XVETRN,XINSRN,XICUDY,XVENHR:
                              XREADM,XURETN,XEPSCR,XCLNST,XCTRES,XSEPTP,XDSCTO:
                              XDSATT,XGRPVR,XDIACG,XDIAGR,XCODID,XDVAFN,XDVAAN:
                              XDVAAD,XICUHR,XMEDINO,XMEDIPID,XGENDER
          ELSE
            WRITE     WORK4,SEQ;XEVNTYP,XMORTYP,XEOCLNK,XDAYQNB,XNUMLEV:
                              XACCOCC,XLANG,XUPDFLG,XSORLOC,XSORCLI:
                              XSORTRA,XHIHCD,SP7,XHCODE,XADMNO:
                              XPURNO,XMUMSID,XADATE,XATIME,XDDATE:
                              XDTIME,XPSNAME,XPGNAME,XSFNAME,XPADD1,XSUBRB:
                              XPCODE,XSTATE,SP6,XDOBDT,XPSEX,XABORN,XCNTBR:
                              XMSTAT,XEMPST,XINTRP,XOCCPC,XSREFR,XINLOS,XCTHOS:
                              XLOCTN,XNMRNO,XSPLTY,XSPCDS,XADTYP,XWEIGH,XLEVDY:
                              XPSYCH,XMHLGL,XPAYCL,XVETRN,XINSRN,XICUDY,XVENHR:
                              XREADM,XURETN,XEPSCR,XCLNST,XCTRES,XSEPTP,XDSCTO:
                              XDSATT,XGRPVR,XDIACG,XDIAGR,XCODID,XDVAFN,XDVAAN:
                              XDVAAD,XICUHR,XMEDINO,XMEDIPID,XGENDER
          ENDIF
.
WREC9999  RETURN
+
.******************************************************************************
.*                                 WRDIS000               Called by: EXTC0000 *
.*                        Write a disease code record                         *
.******************************************************************************
.
WRDIS000  MATCH     "2",PT0DACRQ          * External cause code?
          GOTO      WRDIS05 IF EQUAL
.
          MATCH     "6",PT0DACRQ          * External cause code?
          GOTO      WRDIS05 IF EQUAL
.
          MATCH     "7",PT0DACRQ          * External cause code?
          GOTO      WRDIS05 IF EQUAL
.
          MATCH     "8",PT0DACRQ          * External cause code?
          GOTO      WRDIS10 IF NOT EQUAL
.
WRDIS05   MOVE      "EC  ",XEVNTYP
          ADD       ONE,COUNTEX
.
          CALL      RKPTECD2              * get place of occurence code
          BRANCH    OVRCD,WRDIS90
          MOVE      PTEDCODE,XPLCOCC
          MOVE      PTEDOSET,XCOFPOO
          CALL      RKPTECD2              * get activity code
          BRANCH    OVRCD,WRDIS90
          MOVE      PTEDCODE,XACTIV
          MOVE      PTEDOSET,XCOFACT
          GOTO      WRDIS90
.
WRDIS10   MATCH     "4",PT0DACRQ          * Morphology code?
          IF        @EQUAL
            MOVE      "MO  ",XEVNTYP
            ADD       ONE,COUNTM
            GOTO      WRDIS90
          ENDIF
.
          MATCH     CMDISP,PTEDTYPE       * Primary Disease?
          IF        @EQUAL
            MOVE      "PD  ",XEVNTYP 
            ADD       ONE,COUNTD
            GOTO      WRDIS90
          ENDIF
.
          MATCH     CMDISC,PTEDTYPE       * Complication
          IF        @EQUAL
            MOVE      "CM  ",XEVNTYP
            ADD       ONE,COUNTD
            GOTO      WRDIS90
          ENDIF
.
          MATCH     CMDISA,PTEDTYPE       * Co diagnosis
          IF        @EQUAL
            MATCH     SP70,PT0DADTP
            IF        !@EQUAL
              MOVE      "CA  ",XEVNTYP
              ADD       ONE,COUNTD
              GOTO      WRDIS90
            ENDIF
          ENDIF
.
          MOVE      "OC  ",XEVNTYP         * associated
          ADD       ONE,COUNTD
.
WRDIS90   PACK      XPRIOR,SP1,F2
          ADD       ONE,F2
.
WRDIS95   MOVE      ZERO,EXIT
          IF        DOYR2025 = 1
            WRITE     WORK5,SEQ;XEVNTYP,XADMNO,XPURNO,SP18,XHCODE,XPRIOR:
                              XICDCOD,XPLCOCC,XACTIV,XCOFICD,XCOFPOO:
                              XCOFACT
          ELSE
            WRITE     WORK4,SEQ;XEVNTYP,XADMNO,XPURNO,SP18,XHCODE,XPRIOR:
                              XICDCOD,XPLCOCC,XACTIV,XCOFICD,XCOFPOO:
                              XCOFACT
          ENDIF
          UNPACK    SP50,XICDCOD,XPLCOCC,XACTIV
          UNPACK    SP70,XCOFICD,XCOFPOO,XCOFACT
.
WRDIS99   RETURN
+
.******************************************************************************
.*                                 WRPROC                 Called by: EXTC0000 *
.*                        Write a procedure code record                       *
.******************************************************************************
.
WRPROC    IF        COUNTO=0
            MOVE      "PP",XEVNTYP        * principal procedure
          ELSE
            MOVE      "OP",XEVNTYP        * additional procedure
          ENDIF
. 
....      PACK      XPRIOR,SP1,PTEOCNT    * priority                  *D-240107
          PACK      XPRIOR,PTEOCNT        * priority                  *I-240107
          IF        DOYR2025 = 1
            WRITE     WORK5,SEQ;XEVNTYP,XADMNO,XPURNO,SP18,XHCODE,XPRIOR:
                              XPROCCOD,XCLINPR,XDATPROC,SP10
          ELSE
            WRITE     WORK4,SEQ;XEVNTYP,XADMNO,XPURNO,SP18,XHCODE,XPRIOR:
                              XPROCCOD,XCLINPR,XDATPROC,SP10
          ENDIF
          ADD       ONE,COUNTO
WRPROC99  RETURN
+
.******************************************************************************
.*                                 WRCHK                  Called by: EXTC0000 *
.*                        Write the check line (totals)                       *
.******************************************************************************
.
WRCHK     MOVE      "CHK",XEVNTYP
          MOVE      COUNTD,XDIAGCNT
          MOVE      COUNTEX,XEXCACNT
          MOVE      COUNTM,XMORPCNT
          MOVE      COUNTO,XPROCCNT
.
          IF        DOYR2025 = 1
            WRITE     WORK5,SEQ;XEVNTYP,XADMNO,XPURNO,SP18,XHCODE,XDIAGCNT:
                              XEXCACNT,XMORPCNT,XPROCCNT
          ELSE
            WRITE     WORK4,SEQ;XEVNTYP,XADMNO,XPURNO,SP18,XHCODE,XDIAGCNT:
                              XEXCACNT,XMORPCNT,XPROCCNT
          ENDIF
.
WRCHK99   RETURN
+
.******************************************************************************
.*                                 CREA0000               Called by: EXTC0000 *
.*                        Create a text file                                  *
.******************************************************************************
CREA0000  CLOSE     WORK,DELETE
          IF        DOYR2025 = 1
            CLOSE     WORK5,DELETE
          ELSE
            CLOSE     WORK4,DELETE
          ENDIF
.
          PREP      WORK,"file1a"
          IF        DOYR2025 = 1
            PREP      WORK5,"file3"
          ELSE
            PREP      WORK4,"file3"
          ENDIF
.
CREA9999  RETURN
.
+
.******************************************************************************
.*                                  CLOS0000              Called by: GDAT0000 *
.*                     Calculate The LOS For This Patient                     *
.******************************************************************************
CLOS0000  MOVE      ZERO,WORKHITH           * Initialise HITH care days
          MOVE      ZERO,WORKICLS           * Initialise ICU LOS
          MOVE      ZERO,WORKLDAY           * Initialise leave days
          MOVE      ZERO,WORKPSYC           * Initialise psychiatric days
          MOVE      ZERO,WORKQUAL           * Initialise psychiatric days
.
. ----- Daycase -----
.
          MATCH     ADATE,DDATE
          IF        @EQUAL
            PACK      KEY30,AADMNO,SP30
            CALL      RDSTRAN2              * Position on & read the transfer
            CALL      RDKTRAN2                file
            IF        OVRCD = 0 
              MATCH     AADMNO,TADMN
              IF        @EQUAL
                CALL      GADM0000            * Get admitting details 
              ENDIF
            ENDIF
.
            PACK      KEY30,AADMNO,ZED30
            CALL      RDSTRAN2              * Position on & read the transfer
            CALL      RDPTRAN2                file
            IF        OVRCD=0
              MATCH     AADMNO,TADMN
              IF        @EQUAL
                MOVE      TATYPE,ADMTYPE
                MOVE      TRBTYP,BEDTYPE
                CALL      CHKC0000            * Check a stay in a ICU
                IF        EXIT=1
                  MOVE      ONE,WORKICLS
                  MOVE      ZERO,EXIT
                ENDIF
                CALL      CPSY0000            * Check for psychiatric care
                IF        EXIT=1
                  MOVE      ONE,WORKPSYC
                  MOVE      ZERO,EXIT
                ENDIF
                CALL      CQNB0000            * Check for qualified care
                IF        EXIT=1
                  MOVE      ONE,WORKQUAL
                  PACK      XCLNST,ONE,SP70
                  MOVE      ZERO,EXIT
                ENDIF
                CALL      CHIH0000            * Check for hospital in the home
                IF        EXIT=1
                  MOVE      ONE,WORKHITH
                  MOVE      ZERO,EXIT
                ENDIF
              ENDIF
            ENDIF
            GOTO      CLOS9000
          ENDIF
.
. ----- Non Daycase -----
.
          PACK      KEY30,AADMNO,SP20
          CALL      RDSTRAN2                * Position on & read the transfer
CLOS1000  CALL      RDKTRAN2                  file
          IF        OVRCD=1 
            GOTO      CLOS7000              * Error
          ENDIF
          MATCH     AADMNO,TADMN
          GOTO      CLOS7000 IF NOT EQUAL   * Error
.
          CMATCH    ANSA,TMOVE
          GOTO      CLOS2000 IF EQUAL       * Admission record
.
          CMATCH    ANSL,TMOVE
          GOTO      CLOS3000 IF EQUAL       * Leave record
.
          CMATCH    ANSR,TMOVE
          GOTO      CLOS4000 IF EQUAL       * Return from leave record
.
          CMATCH    ANSC,TMOVE
          GOTO      CLOS5000 IF EQUAL       * Change record
.
          CMATCH    ANSD,TMOVE
          GOTO      CLOS6000 IF EQUAL       * Discharge record
.
          GOTO      CLOS5000                * Assume change record
.
. ----- Admission Record -----
.
CLOS2000  CALL      LSAV0000                * Save details
          CALL      GADM0000                * Get admitting details
          GOTO      CLOS1000
.
. ----- Leave Record -----
.
CLOS3000  CALL      LEND0000                * Process end of a period
          GOTO      CLOS1000
.
. ----- Return Record -----
.
CLOS4000  CALL      LDAY0000                * Calculate the leave days
          CALL      LSAV0000                * Save details
          GOTO      CLOS1000
.
. ----- Change Record -----
.
CLOS5000  CALL      LEND0000                * Process end of a period
          CALL      LSAV0000                * Save new details
          GOTO      CLOS1000
.
. ----- Discharge Record -----
.
CLOS6000  CALL      LEND0000                * Process end of a period
          GOTO      CLOS9000
.
. ----- Error -----
.
CLOS7000  MOVE      DDATE,TDATE             * Assume discharge date
          CALL      LEND0000                * Process end of a period
          GOTO      CLOS9999
.
. ----- Finished -----
.
CLOS9000  MOVE      SP10,XICUDY           * ICU days; no longer sent
          MOVE      SP10,XICUHR           * ICU hours
.
.         MATCH     "20140701",DDATE      * Separations before date?
.         GOTO      CLOS9010 IF LESS      * yes; clear ICU hours
.
          PACK      KEY8,AADMNO
          CALL      RDPTICU1
          IF        OVRCD=0
            COMPARE   ZERO,PTICUINT
            IF        !@EQUAL
              MOVE      PTICUINT,XICUHR   * ICU hours
              REP       " 0",XICUHR
            ENDIF
          ENDIF
.
CLOS9010  MOVE      WORKLDAY,XLEVDY         * Total leave days
          REPLACE   " 0",XLEVDY             * zero fill
.
          MOVE      WORKQUAL,XDAYQNB        * qualified days (form 3)
          MOVE      WORKHITH,XHIHCD         * hospital in the home care days
          MOVE      WORKPSYC,XPSYCH         * Psychiatric days
          COMPARE   ZERO,WORKPSYC
          IF          @EQUAL
            MOVE        SP8,XPSYCH
          ENDIF
          CALL      DSCR0000                * Get discharged details
.
CLOS9999  RETURN
+
.******************************************************************************
.*                                  LSAV0000              Called by: CLOS0000 *
.*         Save Start Of Period Details For Length Of Stay Calculations       *
.******************************************************************************
LSAV0000  PACK      CDYSFDTE,TDATE
          MOVE      TATYPE,STATYPE
          MOVE      TRBTYP,STRBTYP
LSAV9999  RETURN
+
.******************************************************************************
.*                                  LEND0000              Called by: CLOS0000 *
.*              Process End Of Period Details For Length Of Stay              *
.******************************************************************************
LEND0000  PACK      CDYSTDTE,TDATE
          MATCH     CDYSTDTE,CDYSFDTE
          GOTO      LEND9000 IF NOT LESS
.
          CALL      CALCDAYS                * Calculate diff between dates
          SUB       ONE,CDYSDAYS            * We dont want inclusive dates
          MOVE      STATYPE,ADMTYPE
          MOVE      STRBTYP,BEDTYPE
          CALL      CHKC0000                * Check if stay in a ICU
          IF        EXIT=1
            ADD       CDYSDAYS,WORKICLS
            MOVE      ZERO,EXIT
          ENDIF
.
          CALL      CPSY0000                * Check for psychiatric care
          IF        EXIT=1
            ADD       CDYSDAYS,WORKPSYC
            MOVE      ZERO,EXIT
          ENDIF
.
          CALL      CQNB0000                * Check for qualified new born
          IF        EXIT=1
            IF        CDYSDAYS=0
              MOVE      ONE,CDYSDAYS
            ENDIF
            ADD       CDYSDAYS,WORKQUAL
            PACK      XCLNST,ONE,SP70
            MOVE      ZERO,EXIT
          ENDIF
.
          CALL      CHIH0000                * Check for hospital in the home
          IF        EXIT=1
            ADD       CDYSDAYS,WORKHITH
            MOVE      ZERO,EXIT
          ENDIF
.
LEND9000  MATCH     CDYSTDTE,CDYSFDTE
          GOTO      LEND9200 IF NOT EQUAL
.
.         New born transferred on the same day will be counted as qualified care
.
          MATCH     ADATE,DDATE
          GOTO      LEND9200 IF NOT EQUAL
.
          MOVE      STATYPE,ADMTYPE
          CALL      CQNB0000                * Check for qualified care
          IF        EXIT=1
            MOVE      ONE,WORKQUAL
            MOVE      ZERO,EXIT
          ENDIF
.
LEND9200  MOVE      TDATE,STDATE            * Save transfer date for leave days
LEND9999  RETURN
+
.******************************************************************************
.*                                  LDAY0000              Called by: CLOS0000 *
.*                           Calculate The Leave Days                         *
.******************************************************************************
LDAY0000  PACK      CDYSFDTE,STDATE
          PACK      CDYSTDTE,TDATE
          MATCH     CDYSTDTE,CDYSFDTE
          GOTO      LDAY9999 IF NOT LESS
.
          CALL      CALCDAYS                * Calculate diff between dates
          SUB       ONE,CDYSDAYS            * We dont want inclusive dates
          ADD       CDYSDAYS,WORKLDAY       * Total leave days
          ADD       ONE,NUMLEV
LDAY9999  RETURN
+
.******************************************************************************
.*                                  GADM0000                                  *
.*                        Get admitting details (doctor and ward)             *
.******************************************************************************
GADM0000  MOVE      ZERO,EXIT                                         *C-132806
.                                           * following code moved to DSCR0000
....      PACK      XLOCTN,TWARD,SP10       * location
....      MATCH     SP6,XLOCTN
....      IF        @EQUAL
....        MOVE      "Location is blank ",ERRDESC
....        CALL      WRTEMP
....      ENDIF
.
          PACK      KEY6,TADOCT             * attending doctor
          CALL      RDDOCT1
          IF        OVRCD = 0
            MATCH     SP15,PTDOREGN                        * start    *C-233977
            IF        !@EQUAL
              MOVE      PTDOREGN,KEY15      * National Registration No.
            ELSE
              MOVE      PTDOWHDP,KEY15      * WA HDP Doctor code
            ENDIF
            SQUEEZE   KEY15
            PACK      XNMRNO,KEY15,SP15     * Aust Health Prac. Reg. No.
            MATCH     SP6,XNMRNO
            IF        @EQUAL
              MOVE      "Invalid National Regist. No. on Admission",ERRDESC
              CALL      WRTEMP                             * end      *C-233977
            ENDIF
.
            PACK      KEY5,ANSD,ANST,DRTYPE
            CALL      RDCODE1
            IF        OVRCD = 0
              PACK      XSPLTY,THCSCOD,SP10      * Doctor type
            ENDIF
            MATCH     SP3,XSPLTY
            IF        @EQUAL
              MOVE      "Invalid speciality on admission ",ERRDESC
              CALL      WRTEMP
            ENDIF
          ELSE
            MOVE      "Invalid admission attending doctor code ",ERRDESC
            CALL      WRTEMP
          ENDIF
.
GADM9999  RETURN
+
.******************************************************************************
.*                           DSCR0000                                         *
.*                   Get Discharged details                                   *
.******************************************************************************
DSCR0000  MOVE      ZERO,EXIT
.                                                                     *I-132806
          PACK      XLOCTN,TWARD,SP10       * location
          MATCH     SP6,XLOCTN
          IF        @EQUAL
            MOVE      "Location is blank ",ERRDESC
            CALL      WRTEMP
          ENDIF
.
          MOVE      TWO,XACCOCC
          PACK      KEY6,TWARD,TBED
          CALL      RDWARD1
          IF        OVRCD<>1
            PACK      KEY5,CATRT,WRTYPE
            CALL      RDCODE1           * accomodation occupied
            IF        OVRCD<>1
              MOVE      TCINDC1,XACCOCC
            ENDIF
            COMPARE    "1",XACCOCC
            IF         !@EQUAL
              COMPARE    "2",XACCOCC
              IF         !@EQUAL
                MOVE       "Invalid accomodation occupied",ERRDESC
                CALL       WRTEMP
              ENDIF
            ENDIF
          ENDIF
.
          PACK      KEY6,TADOCT             * Attending Doc.
          CALL      RDDOCT1
          IF        OVRCD = 0
            MATCH     SP15,PTDOREGN                        * start    *C-233977
            IF        !@EQUAL
              MOVE      PTDOREGN,KEY15      * National Registration No.
            ELSE
              MOVE      PTDOWHDP,KEY15      * WA HDP Doctor code
            ENDIF
            SQUEEZE   KEY15
            PACK      XDSATT,KEY15,SP15     * Aust Health Prac. Reg. No.
            MATCH     SP6,XDSATT
            IF        @EQUAL
              MOVE      "Invalid National Regist. No. on separation",ERRDESC
              CALL      WRTEMP                             * end      *C-233977
            ENDIF
.
            PACK      KEY5,ANSD,ANST,DRTYPE
            CALL      RDCODE1
            IF        OVRCD = 0
              PACK      XSPCDS,THCSCOD,SP10
            ENDIF
            MATCH     SP3,XSPCDS
            IF        @EQUAL
              MOVE      "Invalid Speciality on separation ",ERRDESC
              CALL      WRTEMP
            ENDIF
          ELSE
            MOVE      "Invalid discharged attending doctor code ",ERRDESC
            CALL      WRTEMP
          ENDIF
.
DSCR9999  RETURN
+
.******************************************************************************
.*                                  CHKC0000                                  *
.*                   Check For A Stay In A Critical Care Unit                 *
.******************************************************************************
CHKC0000  MOVE      ZERO,EXIT               * Default normal/HDU bed stay
          IF        PTCNHCCC=1
            MATCH     SP3,BEDTYPE
            GOTO      CHKC9999 IF EQUAL     * Dont allow blank codes
.
            PACK      KEY5,ANSB,ANST,BEDTYPE   * Using bed type for CCU/ICU
            CALL      RDCODE1
            BRANCH    OVRCD,CHKC9999
.
            MATCH     ANSI,TCINDC1
            GOTO      CHKC9999 IF NOT EQUAL * Not an CCU/ICU stay
          ELSE
            MATCH     SP3,ADMTYPE
            GOTO      CHKC9999 IF EQUAL     * Dont allow blank codes
.
            PACK      KEY5,ANSA,SP1,ADMTYPE    * Using admin type for CCU/ICU
            CALL      RDCODE1
            BRANCH    OVRCD,CHKC9999
.
            COMPARE   ONE,TCFORM6
            GOTO      CHKC9999 IF NOT EQUAL * Not an CCU/ICU stay
          ENDIF
          MOVE      ONE,EXIT                * CCU/ICU stay (not HDU)
CHKC9999  RETURN
+
.******************************************************************************
.*                                  CPSY0000                                  *
.*                   Check For Psychiatric care                               *
.******************************************************************************
CPSY0000  MOVE      ZERO,EXIT               * Default normal/HDU bed stay
          MATCH     SP3,ADMTYPE
          GOTO      CPSY9999 IF EQUAL     * Dont allow blank codes
.
          PACK      KEY5,ANSA,SP1,ADMTYPE    * Using admin type for CCU/ICU
          CALL      RDCODE1
          BRANCH    OVRCD,CPSY9999
.
          MATCH     ANSP,TCINDC3
          GOTO      CPSY9999 IF NOT EQUAL   * Not psychiatric care
          MOVE      ONE,EXIT
CPSY9999  RETURN
+
.******************************************************************************
.*                                  CQNB0000                                  *
.*                   Check For Qualified new born                             *
.******************************************************************************
CQNB0000  MOVE      ZERO,EXIT
          MATCH     SP3,ADMTYPE
          GOTO      CQNB9999 IF EQUAL     * Dont allow blank codes
.
          PACK      KEY5,ANSA,SP1,ADMTYPE    * Using admin type for qualified
          CALL      RDCODE1
          BRANCH    OVRCD,CQNB9999
.
          MATCH     "1",THCSCOD
          GOTO      CQNB9999 IF NOT EQUAL   * Not qualified
.
......    MATCH     SP3,BEDTYPE
......    GOTO      CQNB9999 IF EQUAL     * Dont allow blank codes
.
......    PACK      KEY5,ANSB,ANST,BEDTYPE   * Using bed type for CCU/ICU
......    CALL      RDCODE1
......    BRANCH    OVRCD,CQNB9999
.
......    MATCH     ANSQ,TCINDC5
......    GOTO      CQNB9999 IF NOT EQUAL * Not a qualified stay
.
          MOVE      ONE,EXIT
CQNB9999  RETURN
+
.******************************************************************************
.*                                  CHIH0000                                  *
.*                       Check For Hospital In The Home                       *
.******************************************************************************
CHIH0000  MATCH     SP3,BEDTYPE
          GOTO      CHIH9999 IF EQUAL        * Dont allow blank codes
.
          PACK      KEY5,ANSB,ANST,BEDTYPE   * Using bed type
          CALL      RDCODE1
          BRANCH    OVRCD,CHIH9999
.
          MATCH     ANSH,TCINDC1
          GOTO      CHIH9999 IF NOT EQUAL    * Not hospital in the home
.
          MOVE      ONE,EXIT
CHIH9999  RETURN
+
.******************************************************************************
.* Get Interpreter and Preferred Language Required. Only get language when    *
.* Interpreter is Yes.                                                        *
.******************************************************************************
.
LANG0000  MATCH     "0",PMVXINTR                 * interpreter req'd ?
          IF        @EQUAL
            MOVE      TWO,XINTRP                 * no
            GOTO      LANG9999
          ENDIF
.
          MATCH     "1",PMVXINTR                 * interpreter req'd ?
          IF        @EQUAL
            MOVE      ONE,XINTRP                 * yes
            GOTO      LANG2000
          ENDIF
.
          MOVE      "Invalid interpreter required ",ERRDESC
          CALL      WRTEMP
.

LANG2000  MATCH     SP3,PMVXLNG1                 * pref. language blank ?
          GOTO      LANG9999 IF EQUAL
.
          PACK      KEY5,ANSL,ANSA,PMVXLNG1,SP70
          CALL      RDCODE1
          IF        OVRCD=0
            MATCH     ANSX,TCINDC12
            GOTO      LANG9999 IF EQUAL
            TYPE      THCSCOD                    * numeric HDP Equiv.?
            IF        @EQUAL
              MOVE      THCSCOD,DIM4             * yes
              SQUEEZE   DIM4
              MOVELPTR  DIM4,FORM1               * 4 digits in length ?
              IF        FORM1 = 4
                MOVE      THCSCOD,XLANG          * yes - load preferred language
                GOTO      LANG9999
              ENDIF
            ENDIF
          ENDIF
.
.         Invalid preferred language
.
          MOVE      "Invalid preferred language ",ERRDESC
          CALL      WRTEMP
.
LANG9999  RETURN
.
.******************************************************************************
.*                                  RJUS0000                                  *
.*                   Move field to be right justified                         *
.******************************************************************************
RJUS0000  MOVE      ZERO,FORM4
          MOVE      SP10,DIM4
          RESET     THCSCOD,4
          GOTO      RJUS2500
.
RJUS2000  BUMP      THCSCOD,-1
          GOTO      RJUS9999 IF EOS
.
RJUS2500  CMATCH    SP1,THCSCOD
          GOTO      RJUS2000 IF EQUAL
.
          MOVEFPTR  THCSCOD,FORM2
.
          SETLPTR   THCSCOD,FORM2
.
RJUS3000  RESET     THCSCOD
          MOVE      THCSCOD,FORM4
          MOVE      FORM4,DIM4
          RESET     DIM4
.
RJUS9999  RETURN
+
.*************************************************************************
.*                             CRET0000            Called by:            *
.*                      create temporary file                            *
.*************************************************************************
CRET0000  CALL      KILL0000 
          CLEAR     CMDLINE 
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMES,CMDLINE
          APPEND    " 84 D1-8",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMP,FNAMES
.
CRET9999  RETURN
+
.******************************************************************************
.*                                 ENDP0000                                   *
.*                        End of processing                                   *
.******************************************************************************
ENDP0000  CALL      IBACLOCK
          UNPACK    CFILENO,DIM4
          CLOCK     TIME,TIMEIS
          UNPACK    TIMEIS,XHOUR,ANS,XMIN
          MOVE      SP30,FNAMEA
          MOVE      SP30,FNAMEB
          PACK      DIM8,CCC,CYY,CMM,CDD
          REP       " 0",DIM8
.
          MOVE      "WAHCS.txt",FNAMEA
.
          PACK      CMDLINE,SP20,SP20,SP20
          CLEAR     CMDLINE
          APPEND    "mv `ldat file3.txt` ",CMDLINE
          APPEND    FNAMEA,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          MOVE      ".ack",DIM4
          PACK      FNAMEB,FNAMEA,DIM4
.
          PACK      CMDLINE,SP20,SP20,SP20
          CLEAR     CMDLINE 
          APPEND    "mv `ldat file1a.txt` ",CMDLINE
          APPEND    FNAMEB,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CALL      KILL0000
ENDP9999  RETURN
+
.*************************************************************************
.*                             KILL0000             Called by:           *
.*                   erase temporary file                                *
.*************************************************************************
KILL0000  CLOSE     TEMP
          PACK      CMDLINE,SP20,SP20,SP20
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMES,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PREP      WORK,"file1a"
          CLOSE     WORK,DELETE
.
          IF        DOYR2025 = 1
            PREP      WORK5,"file3"
            CLOSE     WORK5,DELETE
          ELSE
            PREP      WORK4,"file3"
            CLOSE     WORK4,DELETE
          ENDIF
.
KILL9999  RETURN
+
.------------------------------------------------------------
. Move Extract to Web Directory
.------------------------------------------------------------
MVEXT000  CLEAR     CMDLINE
          APPEND    "ibaadt49.us1 ",CMDLINE
          APPEND    FNAMEA,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          RETURN
.
.==============================================================================
RSTEMP    RESET     KEY8
          READ      TEMP,KEY8;;
          RETURN
.
RKTEMP    MOVE      ZERO,OVRCD
          READKS    TEMP;PURNO,AADMNO,ERRDESC
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP    RESET     SKEY8
          WRITE     TEMP,SKEY8;PURNO,AADMNO,ERRDESC
          RETURN
+
.==============================================================================
          INC       STD001IO
.
          INC       CALCDAYS
          INC       CALCAGE
          INC       PATM10T9                * Map ICD10 to ICD9 codes
          INC       TFILENAM                     * Generate Temp File Name
          INC       SEXDSCIO
.
          INC       IBAPOSIO/INC
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       MEHDLSIO/INC
          INC       OPRDEAIO/INC 
          INC       OPRDEBIO/INC
          INC       PATASFIO/INC
          INC       PATCMPIO/INC            * Complex mapping file
          INC       PATCODIO/INC            * Codes file
          INC       PATDADIO/INC
          INC       PATVADIO/INC
          INC       PATDO1IO/INC
          INC       PATDSCIO/INC            * Discharge file
          INC       PATECDIO/INC            * Patient episode disease file
          INC       PATECOIO/INC            * Patient episode operation file
          INC       PATICDIO/INC
          INC       PATICOIO/INC
          INC       PATICUIO/INC
          INC       PATDGWIO/INC
          INC       PATFN1IO/INC
          INC       PATLINIO/INC
          INC       PATMA1IO/INC            * Patient master file
          INC       PMSPX2IO/INC
          INC       PATMI1IO/INC            * Patient admission file
          INC       PATVISIO/INC            * Patient visit record
          INC       PMSVX1IO/INC            * Visit extension file
          INC       PATPGRIO/INC
          INC       PATTRNIO/INC            * Transfer file
          INC       PATWR1IO/INC
          INC       PATWVEIO/INC
          INC       PMSCCDIO/INC
          INC       WEBERRIO/INC                 * Web Server Error Logging
.
+
.


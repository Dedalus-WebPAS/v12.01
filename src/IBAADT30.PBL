.***************************************************************************
.* System    :   Patient Management System                                 *
.* Program   :   IBAADT30                                                  *
.* Desc      :   DRG/Casemix Theatre Fee Listing                           *
.***************************************************************************
.* Date      :   31/07/96                                                  *
.* Author    :   J.Tan                                                     *
.* Mods.     :                                                             *
.*        V10.01.01 15/02/2011 Ebon Clements  CAR 233048                   *
.*                  Removed new file option                                *
.*        V10.00.01 21/07/2010 J.Tan  CAR 225702                           *
.*                  Added range of Contract ID                             *
.*        V9.04.01  03/08/05 J.Tan    CAR 62750                            *
.*                  Removed redefined amount variables                     *
.*        V9.03.01  30/05/2003  Dean Cramer    CAR 38200                   *
.*                  Fixed print overlaps for web                           *
.*        V5.09.B02 09/01/2001  Jill Habasque                              *
.*                  Fixed header display to have listing (not mainentance) *
.*        V5.09.B01 20/12/2000  Ebon Clements                              *
.*                  Remove episode type and added new field for daycase    *
.*        V5.08.03  06/12/2000  Caleb Sharman                              *
.*                  Mods to use keyin routine for health fund entries      *
.*        V5.08.02  16/08/2000  Caleb Sharman                              *
.*                  Changed Health Fund variables to be 8 chars            *
.*        V5.08.01  06/06/2000  Jill Habasque                              *
.*                  Changed from key10 to key14 for the health fund file   *
.***************************************************************************
          INC       STD001FD
          INC       PATCTFFD/INC  
          INC       PATCODFD/INC
          INC       PATFN1FD/INC
+
.**********************************************************************
.*                       GLOBAL VARIABLES                             *
.**********************************************************************
.
CFEETYP   FORM      1                     * used for PATFNDKY
CODE      DIM       2
DIM8      DIM       8 
DIM15     DIM       15
DIM15B    DIM       15
ENDCONTR  DIM       6                       * Contract ID
FRMCONTR  DIM       6                       * Contract ID
ECOL1     FORM      2
FRCLM     DIM       5
FORM8P2   FORM      8.2
TOCLM     DIM       5
FRHF      DIM       6
TOHF      DIM       6
STRCLM    DIM       3
ENDCLM    DIM       3
STRHF     DIM       6
ENDHF     DIM       6
SAVCONTR  DIM       6
ZERO4     INIT      "0000"
ZERO8     INIT      "0000    "
.
PRGNAM    INIT      "Casemix Code Theatre fee file Listing" 
PRGID     INIT      "IBAADT30"
VERSION   INIT      "V12.01.00"
.**********************************************************************
.*                           MAINLINE                                 *
.*                       Controlling Logic                            *
.**********************************************************************
ML0000    CALL      INIT0000            * initialization
.                                       * open files
.                                       * display header
.
ML1000    CALL      OPTN0000            * select option
          BRANCH    EXIT,ML9999
.
ML2000    CALL      OPFL0000            * Open theatre fee file
          BRANCH    EXIT,ML1000
.
          CALL      CNID0000            * Select Contract ID
.
          CALL      KCLM0000            * Keyin range of claim code
          BRANCH    EXIT,ML1000
          CALL      KFUN0000            * Keyin range of health fund
.
ML2400    CALL      CONTQST             * OK to continue ?
          BRANCH    CEXIT,ML3000,ML1000,ML9999
          BEEP
          GOTO      ML2400
.
ML3000    CALL      PRIN0000            * print all details
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.**********************************************************************
.*                           INIT0000                                 *
.*               Open files and display header                        *
.**********************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P50:24,"Opening ";
.
          DISPLAY   *P60:24,"patfn1af";
          OPEN      PATFN1A1,"patfn1af"
.
          DISPLAY   *P60:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
INIT9999  RETURN
+
.**********************************************************************
.*                           OPTN0000                                 *
.*                   Display the main option screen                   *
.**********************************************************************
OPTN0000  MOVE       FALSE,EXIT
          DISPLAY    *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                     *P1:5,*V2LON,ONE,*HOFF:
                     " = Code Sequence";
.
OPTN1000  KEYIN      *P1:7,*EL,"Please Select : ",*V2LON,OPTION;
          COMPARE    ZERO,OPTION
          GOTO       OPTN9000 IF EQUAL
.
          BRANCH     OPTION,OPTN9999
          BEEP
          GOTO       OPTN1000
.
OPTN9000  MOVE       TRUE,EXIT
.
OPTN9999  RETURN
+
.**********************************************************************
.*                             OPFL0000                               *
.*                Open casemix theatre fee file                       *
.**********************************************************************
OPFL0000  OPEN      PATCTFA1,"patctfaf"                           
OPFL9999  RETURN
+
.**********************************************************************
.*                             KCLM0000                               *
.*                keyin range of claim codes                          *
.**********************************************************************
KCLM0000  MOVE       ZERO,EXIT
          UNPACK     SP10,FRCLM,TOCLM
          MOVE       SP20,STRCLM
          MOVE       SP20,ENDCLM
.
          DISPLAY    *P1:13,*EF,"From Claim Code   :":
                     *P1:14,"To   Claim Code   :";
.
          MOVE      "13",EVERT
          MOVE      "21",ECOL               * keyin position
          MOVE      "30",ECOL1              * Display description position
          MOVE      SP3,ACODE
          MOVE      "CL",CODE
          CALL      KCOD0000                * keyin code
          BRANCH    EXIT,KCLM1000,KCLM9000  * Nothing,exit entered
          MOVE      ACODE,STRCLM  
          MOVE      STRCLM,FRCLM
          GOTO      KCLM5000
.
KCLM1000  DISPLAY   *PECOL:EVERT,*V2LON,"Start",*HOFF;
          MOVE      "START",FRCLM
          MOVE      ZERO,EXIT
.
KCLM5000  MOVE      "14",EVERT
          MOVE      SP3,ACODE
          CALL      KCOD0000                * keyin code
          BRANCH    EXIT,KCLM6000,KCLM9000  * Nothing,exit entered
          MOVE      ACODE,ENDCLM  
          MOVE      ENDCLM,TOCLM
          GOTO      KCLM7000
.
KCLM6000  DISPLAY   *PECOL:EVERT,*V2LON,"End",*HOFF;
          MOVE      "END  ",TOCLM
          MOVE      ZERO,EXIT
          GOTO      KCLM9999
.
KCLM7000  MATCH     STRCLM,ENDCLM
          GOTO      KFUN9999 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Ending Claim < Starting Claim **";
          CALL      HITENTER
          GOTO      KCLM0000
.
KCLM9000  MOVE      ONE,EXIT
KCLM9999  RETURN
+
.******************************************************************************
.*                                 KCOD0000              Called By : ML0000   *
.*                         Keyin codes                                        *
.******************************************************************************
KCOD0000  CALL      KEYINCOD
          BRANCH    EXIT,KCOD1000:          * Nothing entered
                         KCOD2000           * Exitchar entered
.
KCOD1000  DISPLAY   *PECOL1:EVERT,*V2LON,TDESC;
KCOD2000  RETURN
+
.**********************************************************************
.*                             KFUN0000                               *
.*                Keyin range of health funds                         *
.**********************************************************************
KFUN0000  MOVE      SP6,STRHF
          DISPLAY   *P24:17,*EL:
                    *P1:16,*EL,"Starting Health Fund : ";
.
          MOVE      TEN6,EVERT
          MOVE      TWENTY4,ECOL
          MOVE      ZERO,CKYIMAND
          CALL      PATFNDKY
.
          MOVE      HCODE,STRHF
.
          MATCH     SP6,STRHF
          IF        @EQUAL
            DISPLAY   *P24:16,*V2LON,"Start"
            MOVE      "START",FRHF
          ELSE
            DISPLAY   *P24:16,*V2LON,STRHF,SP5,HFNAME
            MOVE      STRHF,FRHF
          ENDIF
.
          MOVE      SP6,ENDHF
          DISPLAY   *P1:17,*EL,"Ending   Health Fund : ";
.
          MOVE      TEN7,EVERT
          MOVE      TWENTY4,ECOL
          MOVE      ZERO,CKYIMAND
          CALL      PATFNDKY
.
          MOVE      HCODE,ENDHF
          RESET     ENDHF
          MATCH     SP6,ENDHF
          GOTO      KFUN1000 IF NOT EQUAL
          MOVE      "ZZZZZZ",ENDHF
.
KFUN1000  ENDSET    ENDHF
          APPEND    SP6,ENDHF
          RESET     ENDHF
.
          MATCH     "ZZZZZZ",ENDHF
          IF        @EQUAL
            DISPLAY   *P24:17,*V2LON,"Finish"
            MOVE      "END",TOHF
          ELSE
             DISPLAY   *P24:17,*V2LON,ENDHF,SP5,HFNAME
             MOVE      ENDHF,TOHF
          ENDIF
.
          MATCH     STRHF,ENDHF
          GOTO      KFUN9999 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Ending H.F. < Starting H.F. **";
          CALL      HITENTER
          GOTO      KFUN0000
.
KFUN9999  RETURN
+
.******************************************************************************
.*                                 CNID0000              Called By : ML0000   *
.*                          Select Contract ID                                *
.******************************************************************************
.
CNID0000  MOVE      SP6,FRMCONTR
          KEYIN     *P1:9,*EL,"Starting Contract ID : ",*V2LON,FRMCONTR
          ENDSET    FRMCONTR
          APPEND    SP70,FRMCONTR
          RESET     FRMCONTR
.
          MATCH     SP70,FRMCONTR
          IF        @EQUAL
            DISPLAY   *P23:9,*EL,"Start";
            GOTO      CNID2000
          ENDIF
.
.         PACK      KEY33,FRMCONTR,SP70
.         CALL      RSPTCMX2
.         CALL      RKPTCMX2
.         BRANCH    OVRCD,CNID9000
.
.         MATCH     FRMCONTR,PTCMCNID
.         GOTO      CNID9000 IF NOT EQUAL
.
CNID2000  MOVE      SP6,ENDCONTR
          KEYIN     *P1:11,*EL,"Ending Contract ID : ",*V2LON,ENDCONTR
          ENDSET    ENDCONTR
          APPEND    SP70,ENDCONTR
          RESET     ENDCONTR
.
          MATCH     SP70,ENDCONTR
          IF        @EQUAL
            DISPLAY   *P23:11,*EL,"End";
            MOVE      "ZZZZZZ",ENDCONTR
            GOTO      CNID9999
          ENDIF
.
.         PACK      KEY33,ENDCONTR,SP70
.         CALL      RSPTCMX2
.         CALL      RKPTCMX2
.         BRANCH    OVRCD,CNID9000
.
.         MATCH     ENDCONTR,PTCMCNID
.         GOTO      CNID9000 IF NOT EQUAL
.
          GOTO      CNID9999
.
CNID9000  DISPLAY   *P1:24,*EL,*B,"Invalid Contract ID. ";
          CALL      HITENTER
          GOTO      CNID0000
.
CNID9999  RETURN
.
.**********************************************************************
.*                             PRIN0000                               *
.*                Read file and print all details                     *
.**********************************************************************
PRIN0000  MOVE      ZERO,CPAGENO
          CLOCK     TIME,CTIMEIS
          MOVE      "60",CLNO
          MOVE      SP70,SAVCONTR
.
          DISPLAY   *P1:24,"Printing Episode/Claim Code : ";
.
          MATCH     SP70,FRMCONTR
          IF        @EQUAL
            PACK      KEY33,SP70
            GOTO      PRIN0600
          ENDIF
.
          PACK      KEY33,FRMCONTR,SP70
PRIN0600  CALL      RSPTCTF1                      * position to start of file
PRIN1000  CALL      RKPTCTF1
          BRANCH    OVRCD,PRIN8000
.
          DISPLAY   *P35:24,*EL,*V2LON,PTCTCNID,*P42:24,"- ",PTCTCLMN;
.
          MATCH      SP70,ENDCONTR
          IF         !@EQUAL
            MATCH      PTCTCNID,ENDCONTR
            GOTO       PRIN8000 IF LESS
          ENDIF
.
          MATCH     SP3,STRCLM
          IF        !@EQUAL
            MATCH     STRCLM,PTCTCLMN             * within selected range?
            GOTO      PRIN1000 IF LESS            * no.
          ENDIF
.
          MATCH     SP3,ENDCLM                    * check if end claim entered?
          IF        !@EQUAL
            MATCH     PTCTCLMN,ENDCLM             * within selected range?
            GOTO      PRIN1000 IF LESS            * no.
          ENDIF
.
          MATCH     SP6,STRHF                     * check if hf entered ?
          IF        !@EQUAL
            MATCH     STRHF,PTCTHFND
            GOTO      PRIN1000 IF LESS
          ENDIF
.
          MATCH     SP6,ENDHF
          IF        !@EQUAL
            MATCH     PTCTHFND,ENDHF
            GOTO      PRIN1000 IF LESS
          ENDIF
.
          MATCH      PTCTCNID,SAVCONTR
          GOTO       PRIN2000 IF EQUAL
.
          MATCH      SP70,SAVCONTR
          IF         !@EQUAL
            CALL       UND132
          ENDIF
          MOVE       PTCTCNID,SAVCONTR
          CALL       HEAD0000
.
PRIN2000  CALL      PREC0000                       * print a Record  
          GOTO      PRIN1000
.
PRIN8000  COMPARE   ZERO,CPAGENO
          GOTO      PRIN9000 IF EQUAL
.
          CALL      UND132
          PRINT     *N,"*** END OF REPORT  ***";
          GOTO      PRIN9999
.
PRIN9000  DISPLAY   *P1:24,*EL,*B,"No Data Available. ";
          CALL      HITENTER
.
PRIN9999  RETURN
+
.**********************************************************************
.*                                HEAD0000                            *
.*                          Print page header                         *
.**********************************************************************
HEAD0000  CALL      HEAD132
.
          PRINT     *40,"Contract from    :";
.
          MATCH     SP6,FRMCONTR
          IF        !@EQUAL
            PRINT     *58,FRMCONTR;
          ELSE
            PRINT     *58,"START";
          ENDIF
.
          PRINT     *65," to : ";
.
          MATCH     "ZZZZZZ",ENDCONTR
          IF        !@EQUAL
            PRINT     *71,ENDCONTR,*N;
          ELSE
            PRINT     *71,"END",*N;
          ENDIF
.
          PRINT     *40,"Claim Code from  :",FRCLM:
                           *65," to : ",TOCLM,*N:
                    *40,"Health Fund from :",FRHF:
                           *65," to : ",TOHF,*N;
.
          PRINT     *3,"Contract ID      : ",SAVCONTR,*N;
.
          CALL      UND132
          PRINT     *1,"|  Claim Code":
                    *24,"|  Health Fund":
                    *53,"|  Table Type":
                    *67,"| DRG/    ":
                    *77,"|Banding":
                    *86,"|Seq.":
                    *91,"|     Rebate",*106,"|     Patient":
                    *124,"|"," Day   |":
                    *N,*1,"|",*24,"|":
                    *53,"|",*67,"| MBS",*77,"|",*86,"|":
                    *91,"|     Portion",*106,"|     Portion",*124,"|":
                    " Case  |"
.
          ADD       FOUR,CLNO
          CALL      UND132
.
HEAD9999  RETURN
+
.**********************************************************************
.*                                PREC0000                             *
.*                   Routine to print a PRECord                        *
.**********************************************************************
PREC0000  COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          MOVE      ANSN,DIM1
          IF        PTCTDCAS = 1
            MOVE      ANSY,DIM1
          ENDIF
.
          MOVE      SP20,DIM15
          MOVE      "CL",TCODE
          PACK      KEY5,TCODE,PTCTCLMN
          CALL      RDCODE1
          PACK      DIM15,TDESC
          PRINT     *1,"| ",PTCTCLMN," -",DIM15,*24,"|";
.
          MOVE      SP20,DIM15B
          MATCH     SP6,PTCTHFND
          GOTO      PREC2000 IF EQUAL
.
          PACK      KEY14,PTCTHFND,ZERO8,SP10
          CALL      RDFUND1
          PACK      DIM15B,HFNAME
          PRINT     *27,PTCTHFND," -",DIM15B;
          PACK      DIM8,PTCTCASM,SP10
.
PREC2000  MOVE      PTCTDREB,FORM8P2
          PRINT     *53,"|  ",PTCTTABT:
                    *67,"| ",DIM8:      
                    *77,"| ",PTCTMBSB:
                    *86,"| ",PTCTITMS:
                    *91,"| ",FORM8P2;
.
          MOVE      PTCTDPAT,FORM8P2
          PRINT     *106,"| ",FORM8P2,*124,"|":
                    *126,DIM1,*131," |"
                    
          ADD       ONE,CLNO
.
PREC9999   RETURN
+
.
          INC       STD001IO
          INC       PATDSCOD
          INC       KEYINCOD
          INC       PATFNDKY
          INC       PATCTFIO/INC
          INC       PATFN1IO/INC
          INC       PATCODIO/INC

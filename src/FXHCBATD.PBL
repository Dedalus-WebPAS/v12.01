.***************************************************************************
.* System    :   Outpatients                                               *
.* Program   :   FXHCBATD                                                  *
.* Desc      :   Fix the date funds received field in hicbtcaf table       *
.***************************************************************************
.* Date      :   14/12/2005                                                *
.* Author    :   Steve Armstrong                                           *
.* Function  :   This program will accept input of a batch number and      *
.*               a date, then proceed to update the hicbtcaf.hcbtfndr      *
.*               field.                                                    *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       HICBTCFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
BATCHNUM  DIM       5
CURRDATE  DIM       8             * ccyymmdd
PYMTDATE  DIM       8             * ccyymmdd
.
PRGID     INIT      "FXHCBATD"
PRGNAM    INIT      "Fix Batch EFT Payment Date"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      GBAT0000               * get batch number
          BRANCH    EXIT,ML9999            * nothing entered
.
          CALL      GDAT0000               * get payment date
          BRANCH    EXIT,ML0100            * nothing entered
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML1000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML1000    CALL      UPDD0000               * update payment date
          GOTO      ML0100
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"hicbtcaf";
          OPEN      HICBTCA1,"hicbtcaf"
.
INIT9999  RETURN
+
.**************************************************************************
.*                               GBAT0000              Called by: ML0000  *
.*                     Get the batch number                               *
.* Returns:   BATCHNUM - valid batch number                               *
.*            EXIT  0 = Ok to continue                                    *
.*                  1 = Nothing entered                                   *
.**************************************************************************
.
GBAT0000  KEYIN     *P1:4,*EF,"Batch Number: ":
                    *P15:4,*V2LON,BATCHNUM
.
          PACK      BATCHNUM,BATCHNUM,SP5
          MATCH     SP5,BATCHNUM                 * anything entered ?
          GOTO      GBAT9100 IF EQUAL            * no - finished
.
          PACK      KEY33,BATCHNUM,SP30,SP10
          CALL      RSHCBTC1                     * position in file
          CALL      RKHCBTC1                     * read next record
          BRANCH    OVRCD,GBAT8000
.
          MATCH     BATCHNUM,HCBTBTCH            * same batch still ?
          GOTO      GBAT8000 IF NOT EQUAL        * no - invalid batch
.
          GOTO      GBAT9000                     * valid batch
.
GBAT8000  DISPLAY   *P1:24,*EL,*B,"Batch not on file.  ";
          CALL      HITENTER
          GOTO      GBAT0000
.
GBAT9000  MOVE      ZERO,EXIT
          GOTO      GBAT9999
.
GBAT9100  MOVE      ONE,EXIT
.
GBAT9999  RETURN
+
.**************************************************************************
.*                               GDAT0000              Called by: ML0000  *
.*                     Get the EFT payment date                           *
.* Returns:  PYMTDATE - EFT payment date (ccyymmdd)                       *
.*           EXIT    0 = Valid date entered, ok to continue               *
.*                   1 = nothing entered                                  *
.**************************************************************************
.
GDAT0000  CALL      IBACLOCK                     * get current date
          MOVE      CCC,CCENT
          MOVE      CYY,CYEAR
          MOVE      CMM,CMON
          MOVE      CDD,CDAY
          PACK      CURRDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",CURRDATE
.
          MOVE      ONE,CCENTRY                  * set keydate parameters
          MOVE      ONE,CCANLDTE
          MOVE      ONE,CDEFDTE
          MOVE      ZERO,CHIGHLT
.
          DISPLAY   *P1:6,"Existing payment date:"
          MATCH     SP8,HCBTFNDR                 * EFT payment date blank ?
          IF        !@EQUAL
            UNPACK    HCBTFNDR,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            DISPLAY   *P24:6,*V2LON,CPCDATE
          ENDIF
.
          DISPLAY   *P1:8,"New payment date:"
          MOVE      EIGHT,CVERT                  * set row
          MOVE      TWENTY4,CCOL                 * set column
          CALL      KEYDATE                      * keyin date
          BRANCH    OVRCD,GDAT9100               * nothing entered
.
          PACK      PYMTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",PYMTDATE
.
          MATCH     PYMTDATE,CURRDATE
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Date cannot be in the future. ";
            CALL      HITENTER
            GOTO      GDAT0000
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      GDAT9999
.
GDAT9100  MOVE      ONE,EXIT
.
GDAT9999  RETURN
+
.**************************************************************************
.*                               UPDD0000              Called by: ML0000  *
.*                     Update the batch payment date                      *
.**************************************************************************
.
UPDD0000  MOVE      PYMTDATE,HCBTFNDR
          CALL      UPHCBTC1
.
UPDD9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       HICBTCIO/INC

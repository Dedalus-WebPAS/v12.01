.***************************************************************************
.* System    :   Outpatients                                               *
.* Program   :   FX                                                        *
.* Desc      :                                                             *
.***************************************************************************
.* Date      :   07/10/2002                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   This program will print the trnsport/porter report        *
.* Modifications  :                                                        *
.*        V9.12.00  28/09/2009  Ebon Clements CAR 202769                   *
.*                  Created program                                        *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       VISMDTFD/INC
          INC       PMSBRQFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
RECCNT    FORM      8
TOTCNT    FORM      8
.
PRGID     INIT      "FX085933"
PRGNAM    INIT      "Find/Update blank vsmdtime  "
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000,ML1000  * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      PMSBRQA1,"pmsbrqaf"
          OPEN      VISMDTA1,"vismdtaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
		    *P1:5,*V2LON,ONE,*HOFF,". Run - Display only":
                    *P1:6,*V2LON,TWO,*HOFF,". Run - Update"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000,OPTN9000    * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  MOVE      ZERO,RECCNT
          MOVE      ZERO,TOTCNT
          PACK      KEY17,SP70
          CALL      RSVSMDT1
PROC1000  CALL      RKVSMDT1
          BRANCH    OVRCD,PROC9999
.
          ADD       ONE,TOTCNT
          MATCH     "009",VSMDTYPE
          GOTO      PROC1000 IF NOT EQUAL
.
          IF        TOTCNT%100=1
                      DISPLAY   *P1:24,"Records processed ",*V2LON,TOTCNT;
          ENDIF
.
          MATCH     SP70,VSMDTIME
          GOTO      PROC1000 IF NOT EQUAL
.
          PACK      KEY24,VSMDVISN,SP70
          CALL      RSPMBRQ1
          CALL      RKPMBRQ1
          BRANCH    OVRCD,PROC1000
.
          MATCH     PMBRVISN,VSMDVISN
          GOTO      PROC1000 IF NOT EQUAL
.
          ADD       ONE,RECCNT
          PRINT     *1,"Visit Number ",VSMDVISN," request time ",PMBRREQT 
.
          IF        COPTION=2
            PACK      VSMDTIME,PMBRREQT
            CALL      UPVSMDT1
          ENDIF
          GOTO      PROC1000
.
PROC9999  PRINT     *1,"Total to be updated ",RECCNT
          RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       PMSBRQIO/INC
          INC       VISMDTIO/INC
.
          INC       STD001IO

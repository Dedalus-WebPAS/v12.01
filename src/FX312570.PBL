.******************************************************************************
.*    Program      : FX312570                                                 *
.*    Description  : Fix program for DRG versions;                            *
.*                   Health Fund, Claim Code & State Default.                 *
.*                                                                            *
.*    Author       : Don Nguyen                                               *
.*    Date         : 26/08/2016                                               *
.*                                                                            *
.*    Modifications:                                                          *
.*        V10.08.00  26/08/2016  Don Nguyen      TSK 0312570                  *
.*                   Created program                                          *
.******************************************************************************
.
          INC       STD001FD
.
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATFN1FD/INC
          INC       PMSEDGFD/INC
          INC       WEBGRPVA/INC       * DRG vars
.
FINDFILE  FILE      ASCII, FIXED=256
.
. LOCAL VARIABLES
. ---------------
CODE      DIM       2
DISPRCTP  DIM       20
DRGVERST  DIM       3
.
NEWPATH   DIM       60
OLDPATH   DIM       60
RECNUM    FORM      8
RECNDISP  DIM       8
RECRTYPE  FORM      1   * Record Type (1=Health Fund, 2=Claim Code, 3=State Def)
UPDTCNTR  FORM      8
UPDTDISP  DIM       8
USINGORC  DIM       1
.
VCHKFLAG  FORM      1             * Validation flag
.                                     0 = writing data to database
.                                     1 = only validating import data file
.
.
. PROGRAM CONSTANTS
. -----------------
CATCL     INIT      "CL"
RECTYPHF  INIT      "Health Fund"
RECTYPCL  INIT      "Claim Code"
RECTYPSD  INIT      "State Default"
.
PRGNAM    INIT      "FIXIT PROGRAM FOR DRG VERSION"
PRGID     INIT      "FX312570"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                           MAIN0000                                         *
.*                  Program Mainline                                          *
.******************************************************************************
MAIN0000  CALL      INIT0000           * init and open files
          BRANCH    EXIT,MAIN9999      * init failure
.
MAIN0100  CALL      OPTN0000           * get option
          BRANCH    COPTION,MAIN1000,MAIN1000,MAIN1000
          GOTO      MAIN9999           * exit selected
.
MAIN1000  CALL      ASKQ0000           * validation run only ?
.
          CALL      CONTQST            * Ok to continue ?
          BRANCH    CEXIT,MAIN5000:    * yes
                          MAIN0100:    * no
                          MAIN9999     * cancel
.
.         Running fixit
.
MAIN5000  PERFORM   COPTION,RUNHF000,RUNCL000,RUNST000
.
MAIN9999  CHAIN     PGM
+
.******************************************************************************
.*                           INIT0000                 Called by: MAIN0000     *
.*        Display heading and check that the files needed exists & open them  *
.******************************************************************************
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
          MOVE      SP70,RECNDISP
          MOVE      ZERO,UPDTCNTR
          MOVE      SP70,UPDTDISP
          MOVE      SP70,DISPRCTP
.
          OPEN      CONTROLF,"controlf"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATFN1A1,"patfn1af"
          OPEN      PMSEDGA1,"pmsedgaf"
.
INIT9999  RETURN
+
.******************************************************************************
.*                           OPTN0000                 Called by: MAIN0000     *
.*                  Get user options or exit                                  *
.*    Returns:  EXIT    = FALSE (0)  Ok to proceed                            *
.*                        TRUE  (1)  Exit option selected                     *
.*              COPTION - option selected                                     *
.******************************************************************************
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run for Health Fund":
                    *P1:6,*V2LON,TWO,*HOFF,". Run for Claim Code":
                    *P1:7,*V2LON,THREE,*HOFF,". Run for State Default"
.
OPTN0500  KEYIN     *P1:9,*EL,"Select Option : ":
                    *P17:9,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000,OPTN9000,OPTN9000
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               ASKQ0000              Called by: MAIN0000   *
.*                  Ask if running validation only                           *
.* Returns: VCHKFLAG  - validation flag                                      *
.*                       0 = normal mode                                     *
.*                       1 = validation only mode                            *
.*****************************************************************************
ASKQ0000  MOVE      ANSY,ANS
          KEYIN     *P1:12,*EL,"Validation only (",*V2LON,*DV,ANSY,*HOFF:
                    *DV,SLASH,*V2LON,*DV,ANSN,*HOFF,") ?":
                    *P25:12,*V2LON,*RV,ANS
.
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
.
          MATCH     ANSY,ANS                     * Y input ?
          IF        @EQUAL
            DISPLAY   *P25:12,*V2LON,DYES,*HOFF:
                      *P35:12,"(No data will be uploaded)"
            MOVE      ONE,VCHKFLAG               * yes
            GOTO      ASKQ9999
          ENDIF
.
          MATCH     ANSN,ANS                     * N input ?
          IF        @EQUAL
            DISPLAY   *P25:12,*V2LON,DNO,*HOFF:
                      *P35:12,"(Data will be uploaded)"
            MOVE      ZERO,VCHKFLAG              * yes
            GOTO      ASKQ9999
          ENDIF
.
          GOTO      ASKQ0000                     * invalid input
.
ASKQ9999  RETURN
+
.******************************************************************************
.*                           COLHED00                     Called by: HEAD0000 *
.*                                                                            *
.*        Print column header row                                             *
.******************************************************************************
COLHED00  LOAD      DISPRCTP,RECRTYPE,RECTYPHF,RECTYPCL,RECTYPSD
.
          CALL      UND132
          PRINT     *1,"| DRG":
                    *10,"| ",DISPRCTP:
                    *132,"|"
          ADD       ONE,CLNO
          CALL      UND132
.
COLHED99  RETURN
+
.******************************************************************************
.*                           HEAD0000                                         *
.*                                                                            *
.*        Print Report standard page heading                                  *
.******************************************************************************
HEAD0000  CALL      HEAD132                      * Standard heading
          PRINT     *N
          ADD       ONE,CLNO
.
          IF        VCHKFLAG=0
            CALL      COLHED00
          ENDIF
.
HEAD9999  RETURN
+
.******************************************************************************
.*                           RUNHF000                 Called by: MAIN0000     *
.*        Loop thru patfn1af table and migrate Grouper Version (PTHFGRPV)     *
.*        values (if non-blank) to the new table (pmsedgaf) with a default    *
.*        Effective Date value of '19010101'.                                 *
.******************************************************************************
RUNHF000  CALL      IBACLOCK
          MOVE      ZERO,CPAGENO
.
          DISPLAY   *P1:24,*EL,*P10:24,"Printing...";
          MOVE      ZERO,UPDTCNTR
          MOVE      ONE,RECRTYPE       * Health Fund record type
.
          CALL      HEAD0000
.
          PACK      KEY14,SP70
          CALL      RDSFUND1
RUNHF010  CALL      RDKFUND1
          BRANCH    OVRCD,RUNHF900
.
          MATCH     SP70,PTHFGRPV
          GOTO      RUNHF010 IF EQUAL
.
          ADD       ONE,UPDTCNTR
          BRANCH    VCHKFLAG,RUNHF010  * Validation Only? get next record
.
          CALL      CLPMSEDG           * Clear file vars
.
          PACK      PMEGRTYP,RECRTYPE
          PACK      PMEGKEYV,SP3,HCODE,SP70
          PACK      PMEGDRGV,PTHFGRPV
.
          PACK      KEY34,PMEGRTYP,PMEGKEYV,PMEGDRGV,SP70
          CALL      RAPMEDG1
          BRANCH    OVRCD,RUNHF020
.
          GOTO      RUNHF030
.
.         Add new record...
.
RUNHF020  MOVE      "19010101",PMEGFDAT
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          CALL      WRPMEDG1
          TRAPCLR   IO
          BRANCH    OVRCD,RUNHF010
.
          GOTO      RUNHF040
.
.
.         Update existing record...
.
RUNHF030  CALL      RDPMEDG1
          BRANCH    OVRCD,RUNHF010
.
          MOVE      PTHFGRPV,PMEGDRGV
          MOVE      "19010101",PMEGFDAT
          CALL      UPPMEDG1
.
RUNHF040  MOVE      PTHFGRPV,DRGVERS
          CALL      DRGVER00
.
          PRINT     *1,"| ",DRGVERST:
                    *10,"| ",HFNAME:
                    *132,"|"
          CALL      UND132
.
          COMPARE   "60",CLNO
          IF        !@LESS
            CALL      HEAD0000
          ENDIF
.
          GOTO      RUNHF010
.
RUNHF900  IF        VCHKFLAG=1
            CALL      PRTSUM00         * Validation only; print summary line
          ENDIF
.
          IF        CPAGENO > 0
            PRINT     *N,*N,"*** End of Report ***"
          ENDIF
.
RUNHF999  RETURN
+
.******************************************************************************
.*                           RUNCL000                 Called by: MAIN0000     *
.*        Loop thru patcodes table (Cat CL) and migrate the Grouper Version   *
.*        (PTCDGRPV) values (if non-blank) to the new table (pmsedgaf), with  *
.*        a default Effective Date value of '19010101'.                       *
.******************************************************************************
RUNCL000  CALL      IBACLOCK
          MOVE      ZERO,CPAGENO
.
          DISPLAY   *P1:24,*EL,*P10:24,"Printing...";
          MOVE      ZERO,UPDTCNTR
          MOVE      TWO,RECRTYPE       * Claim Code record type
.
          CALL      HEAD0000
.
          PACK      KEY5,CATCL,SP3
          CALL      RDSCODE1
RUNCL010  CALL      RDKCODE1
          BRANCH    OVRCD,RUNCL900
.
          MATCH     CATCL,TCODE
          GOTO      RUNCL900 IF NOT EQUAL
.
          MATCH     SP70,PTCDGRPV
          GOTO      RUNCL010 IF EQUAL
.
          ADD       ONE,UPDTCNTR
          BRANCH    VCHKFLAG,RUNCL010  * Validation Only? get next record
.
          CALL      CLPMSEDG           * Clear file vars
.
          PACK      PMEGRTYP,RECRTYPE
          PACK      PMEGKEYV,SP3,ACODE,SP70
          PACK      PMEGDRGV,PTCDGRPV
.
          PACK      KEY34,PMEGRTYP,PMEGKEYV,PMEGDRGV,SP70
          CALL      RAPMEDG1
          BRANCH    OVRCD,RUNCL020
.
          GOTO      RUNCL030
.
.         Add new record...
.
RUNCL020  MOVE      "19010101",PMEGFDAT
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          CALL      WRPMEDG1
          TRAPCLR   IO
          BRANCH    OVRCD,RUNCL010
.
          GOTO      RUNCL040
.
.
.         Update existing record...
.
RUNCL030  CALL      RDPMEDG1
          BRANCH    OVRCD,RUNCL010
.
          MOVE      PTCDGRPV,PMEGDRGV
          MOVE      "19010101",PMEGFDAT
          CALL      UPPMEDG1
.
RUNCL040  MOVE      PTCDGRPV,DRGVERS
          CALL      DRGVER00
.
          PRINT     *1,"| ",DRGVERST:
                    *10,"| ",PTCDLDES:
                    *132,"|"
          CALL      UND132
.
          COMPARE   "60",CLNO
          IF        !@LESS
            CALL      HEAD0000
          ENDIF
.
          GOTO      RUNCL010         
.
RUNCL900  IF        VCHKFLAG=1
            CALL      PRTSUM00         * Validation only; print summary line
          ENDIF
.
          IF        CPAGENO > 0
            PRINT     *N,*N,"*** End of Report ***"
          ENDIF
.
RUNCL999  RETURN
+
.******************************************************************************
.*                           RUNST000                 Called by: MAIN0000     *
.*        Get the State Default Grouper Version value in patconaf (PTCNDGPV)  *
.*        and write this to the new table (pmsedgaf) with a default           *
.*        Effective Date value of '19010101'.                                 *
.******************************************************************************
RUNST000  CALL      IBACLOCK
          MOVE      ZERO,CPAGENO
          MOVE      ZERO,UPDTCNTR
          MOVE      THREE,RECRTYPE     * State Default record type
.
          READ      CONTROLF,HUND05;*158,PTCNDGPV
.
          CALL      HEAD0000
.
          MATCH     SP70,PTCNDGPV      * No Default Grouper Version
          GOTO      RUNST900 IF EQUAL
.
          ADD       ONE,UPDTCNTR
          BRANCH    VCHKFLAG,RUNST900  * Validation Only? finish up
.
          CALL      CLPMSEDG           * Clear file vars
.
          PACK      PMEGRTYP,RECRTYPE
          PACK      PMEGKEYV,SP70
          PACK      PMEGDRGV,PTCNDGPV
.
          PACK      KEY34,PMEGRTYP,PMEGKEYV,PMEGDRGV,SP70
          CALL      RAPMEDG1
          BRANCH    OVRCD,RUNST020
.
          GOTO      RUNST030
.
RUNST020  MOVE      "19010101",PMEGFDAT
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          CALL      WRPMEDG1
          TRAPCLR   IO
          BRANCH    OVRCD,RUNST900
.
          GOTO      RUNST040
.
.
.         Update existing record...
.
RUNST030  CALL      RDPMEDG1
          BRANCH    OVRCD,RUNST900
.
          MOVE      PTCNDGPV,PMEGDRGV
          MOVE      "19010101",PMEGFDAT
          CALL      UPPMEDG1
.
RUNST040  MOVE      PTCNDGPV,DRGVERS
          CALL      DRGVER00
.
          PRINT     *1,"| ",DRGVERST:
                    *10,"| (Current State)":
                    *132,"|"
          CALL      UND132
.
RUNST900  IF        VCHKFLAG=1
            CALL      PRTSUM00         * Validation only; print summary line
          ENDIF
.
          IF        CPAGENO > 0
            PRINT     *N,*N,"*** End of Report ***"
          ENDIF
.
RUNST999  RETURN
+
.******************************************************************************
.*                           PRTSUM00                                         *
.*        Print a summary line for the total number of records to be updated  *
.******************************************************************************
PRTSUM00  MOVE      SP70,DISPRCTP
          LOAD      DISPRCTP,RECRTYPE,RECTYPHF,RECTYPCL,RECTYPSD
          PRINT     *1,"Total number of ",*+,DISPRCTP:
                    *-," records to be updated: ",UPDTCNTR
.
PRTSUM99  RETURN
+
.------------------------------------------------------------------------------
.         Gets the matching DRG Version number from DRG Version Code (DRGVERS)
.
.         Parameters:
.           DRGVERS - DRG Version Code
.
.         Return:
.           DRGVERST - DRG Version number
.------------------------------------------------------------------------------
DRGVER00  MOVE      "N/A",DRGVERST
.
          MATCH     DRG041,DRGVERS
          IF        @EQUAL
            MOVE      "4.1",DRGVERST
            GOTO      DRGVER99
          ENDIF
.
          MATCH     DRG042,DRGVERS
          IF        @EQUAL
            MOVE      "4.2",DRGVERST
            GOTO      DRGVER99
          ENDIF
.
          MATCH     DRG050,DRGVERS
          IF        @EQUAL
            MOVE      "5.0",DRGVERST
            GOTO      DRGVER99
          ENDIF
.
          MATCH     DRG051,DRGVERS
          IF        @EQUAL
            MOVE      "5.1",DRGVERST
            GOTO      DRGVER99
          ENDIF
.
          MATCH     DRG052,DRGVERS
          IF        @EQUAL
            MOVE      "5.2",DRGVERST
            GOTO      DRGVER99
          ENDIF
.
          MATCH     DRG060,DRGVERS
          IF        @EQUAL
            MOVE      "6.0",DRGVERST
            GOTO      DRGVER99
          ENDIF
.
          MATCH     DRG062,DRGVERS
          IF        @EQUAL
            MOVE      "6.2",DRGVERST
            GOTO      DRGVER99
          ENDIF
.
          MATCH     DRG070,DRGVERS
          IF        @EQUAL
            MOVE      "7.0",DRGVERST
            GOTO      DRGVER99
          ENDIF
.
          MATCH     DRG080,DRGVERS
          IF        @EQUAL
            MOVE      "8.0",DRGVERST
            GOTO      DRGVER99
          ENDIF
.
DRGVER99  RETURN
+
.------------------------------------------------------------------------------
.
          INC       STD001IO
          INC       CLPMSEDG
.
          INC       PATCODIO/INC
          INC       PATFN1IO/INC
          INC       PMSEDGIO/INC

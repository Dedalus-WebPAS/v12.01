.---------------------------------------------------------------------e
. Program       : COMWEB10
.
. Function      : eReferral Web server functions
.
. Modifications :
.----------------------------------------------------------------------
. V12.01.01 29/01/2026  Thanh T        TSK 0930214
.           Recompiled for UPEREFVS changes
. V12.00.01 14/05/2025  J.Tan          TSK 0955096
.           Added alpanumeric visit number generation
. V11.04.01 08.01.2024  DA Sarkies       TSK 0941573
.           Added code that switches " from '
. V11.03.03 08.01.2024  DA Sarkies       TSK 0941573
.           Added code that switches " from '
. V11.03.02 18/07/2023  Alina Bhari      TSK 0934060
.           Properly linked casek in theatre booking list of eReferral list 
.           - BKRW3000, BKRW4000                                            
. V11.03.01 06.12.2022  DA Sarkies       TSK 0922635
.           Added tag to display eReferral reference number. Added slots on
.           on the request list to Ref number and Ref source. Added DOCD0000
.           and DOCS0000 to display and print documents attached to eReferral
. V11.02.02 10/02/2022  Thanh T          TSK 0889899
.           Recompiled as WATTR1FD changes
. V11.02.01 07/02/2022  Thanh T          TSK 0896905
.           Recompiled for ALLDEPFD changes
. V11.00.03 09/12/2020  Thanh T          TSK 0872840
.           Added PMSCCDFD/PMSCCDIO as PMSPX2TM changes
. V11.00.02 23/06/2020 Ebon Clements  TSK 0891376
.           Changed get eReferral properties remote script to return
.           a max of 600 characters of the value - ERPROP00
. V11.00.01 04/06/2020 Don Nguyen     TSK 0881876
.           Removed 'Street Number' from list row output (EROW0000) since it's
.           already part of the 'Street Name' data loaded (ibacom10.us1).
. V10.13.01 30/07/2018  Ebon Clements   TSK 0815359
.           Don't include IP AH departments - VALDP000
. V10.12.07 02/07/2018  Ebon Clements TSK 0845295
.           Populate WL procedure code and count for bookings - VISIT000
. V10.12.06 21/06/2018  Ebon Clements TSK 0845295
.           Increased size of LINKDOC
. V10.12.05 07/06/2018  Ebon Clements TSK 0845295
.           Fixed referral reason for admission display - EROW0000 - PROCDESC
. V10.12.04 31/01/2018  Ebon Clements TSK 0845295
.           Fixed hospital ereferral list key pack - EHOS0000
. V10.12.03 31/01/2018  Ebon Clements TSK 0845295
.           Fixed eReferral tag display for cat codes - ERTG0000
. V10.12.02 30/01/2018  Ebon Clements TSK 0845295
.           Added eReferral document functionality
. V10.12.01 25/01/2018  Ebon Clements TSK 0845295
.           Added validate referral department remote script
. V10.12.00 09/01/2018  Ebon Clements TSK 0845295
.           Created program
.----------------------------------------------------------------------
          INC       IBAWEBDF    
          INC       WEBDATDF
.
          INC       ALLCONFD/INC
          INC       ALLDEPFD/INC
          INC       ALLDIAFD/INC
          INC       ALLREFFD/INC
          INC       ALLPRRFD/INC
          INC       BOKDIAFD/INC
          INC       BOKRC1FD/INC
          INC       COMERDFD/INC
          INC       COMERHFD/INC
          INC       PATDO1FD/INC   * Doctor File
          INC       PATMI1FD/INC
          INC       PATMA1FD/INC
          INC       PATMISTD/INC
          INC       PATMASTD/INC
          INC       PATVISFD/INC   * Patients Visits File
          INC       PMSVX1FD/INC   * Visit Extension File
          INC       PATCONFD/INC
          INC       PATALRFD/INC
          INC       OBSECOFD/INC
          INC       OBSPCTFD/INC
          INC       OBSPCOFD/INC
          INC       OPRDEAFD/INC
          INC       OUTSITFD/INC
          INC       PATCOMFD/INC
          INC       PATPR1FD/INC
          INC       PMSPX2FD/INC
          INC       PATFN1FD/INC
          INC       PATHSPFD/INC
          INC       PATWR1FD/INC
          INC       MLTSECFD/INC
          INC       MRTMASFD/INC
          INC       NHIMASFD/INC
          INC       NHIETHFD/INC
          INC       PATCALAG/INC
          INC       PMSPX2TD/INC
          INC       PATRE1FD/INC
          INC       PATDGWFD/INC
          INC       PATCMNFD/INC
          INC       PATITMFD/INC
          INC       PATDADFD/INC
          INC       PATVADFD/INC
          INC       PATCMCFD/INC
          INC       PATASFFD/INC
          INC       PMSHCGTD/INC
          INC       PMSHCGFD/INC
          INC       PMSHCPFD/INC
          INC       PMSHCPTD/INC
          INC       PMSUCHFD/INC
          INC       PMSUCDFD/INC
          INC       PMSCCDFD/INC            * Concession Card Details
.
          INC       APSMASFD/INC
          INC       PATIN1FD/INC
          INC       PATDSCFD/INC
          INC       VISMDTFD/INC
          INC       VISMTXFD/INC
          INC       WATPROFD/INC
          INC       WATTR1FD/INC
          INC       WATTX1FD/INC
.----------------------------------------------------------------------
ADOCNAME  DIM       50
EREFRLID  DIM       20
ADMITVMO  DIM       50
ADMINDTE  DIM       8
BKDESC    DIM       20
BOOKICON  DIM       50      * Display Booking Icon in Table
BOOKLINK  DIM       100     * Url to Booking Template
BKTYPE    DIM       3
CALCDAT1  DATE      8
CALCDAT2  DATE      8
CASEKEYS  DIM       19
CLAIMCOD  DIM       3
CLAMDESC  DIM       20
CMDLINE   DIM       200
CORSP001  DIM       100
CORSP003  DIM       3
CURRDATE  DIM       8       * Current Date
DEPTCODE  DIM       3
DOCMLINK  DIM       200
DOCEXIST  FORM      1
DOCTCODE  DIM       8
DIM4      DIM       4
DIM8      DIM       8
DIM10     DIM       10
DIM20     DIM       20
DIM40     DIM       40
DIM600    DIM       600
DISPCOMP  DIM       20
DISPDATE  DIM       11
DISPDIAG  DIM       80
DISPEAID  DIM       20
DISPECCD  DIM       8
DISPPROB  DIM       80
DISPPRTY  DIM       20
DISPPCCD  DIM       8
DISPSTAT  DIM       20
HISDATE   DIM       11
HOSPFLAG  DIM       3
HTMLLNK2  DIM       227
NEXTPAGE  DIM       1       * Nextpage parameter CGI parameter
NUMDAYS   FORM      3
NWFILENM  DIM       100
REMOTENO  FORM      2
ONLINEID  DIM       20
OLDFILEN  DIM       100
PROCDESC  DIM       95
PRIORCLR  DIM       20
POSTCODE  DIM       6
STATCODE  DIM       2
STORURNO  DIM       8
STORVISN  DIM       8
STORCASK  DIM       19
TODAY     DIM       8
UPDTTYPE  DIM       1       * Update Type
RSTRVISN  DIM       8
STARTKEY  DIM       10      * Starting Key
STATDESC  DIM       25
STREETNM  DIM       20
STREETNO  DIM       6
SUBURB    DIM       20
FORM8     FORM      8 
FORM3     FORM      3 
FIRSTFLG  DIM       1
LINKDOC   DIM       227
FILTBTYP  DIM       3
FILTCLAM  DIM       3
FILTDOCT  DIM       10
FILTFLAG  DIM       2
FILTHOSP  DIM       1
FLDRLINK  DIM       100
PRIOFLAG  DIM       20
PSAGETYP  DIM       3
SHOWFLAG  FORM      1
MBSDESC   DIM       70
PSAGECON  DIM       3
UNITDESC  DIM       20
VARCOUNT  FORM      2
VARIABLE  DIM       8
COUNT     FORM      3
DOCREF    DIM       200
DIM3      DIM       3
XMLSRC    DIM       10
.
CATBK     INIT      "BK"
.
DASH      INIT      "-"
.
GREEN     INIT      "Green"
GREY      INIT      "Grey"
RED       INIT      "Red"
YELLOW    INIT      "Yellow"
.
RFFRVARS  INIT      "RFFR" 
RFFR0005  INIT      "RFFR0005"         * AdmissionDate
RFFR0007  INIT      "RFFR0007"         * PatientTitlePASCode
RFFR0009  INIT      "RFFR0009"         * PatientSurname
RFFR0010  INIT      "RFFR0010"         * PatientGivenName
RFFR0012  INIT      "RFFR0012"         * PatientDOB
RFFR0014  INIT      "RFFR0014"         * PatientSex
RFFR0020  INIT      "RFFR0020"         * StreetN 
RFFR0021  INIT      "RFFR0021"         * StreetName
RFFR0022  INIT      "RFFR0022"         * Suburb
RFFR0024  INIT      "RFFR0024"         * Postcode
RFFR0100  INIT      "RFFR0100"         * ClaimTypePASCode
RFFR0101  INIT      "RFFR0101"         * AdmittingDoctorPASCode
RFFR0108  INIT      "RFFR0108"         * PresentingProblem
RFFR0119  INIT      "RFFR0119"         * AdmissionTypePASCode
RFFR0138  INIT      "RFFR0138"         * XML Source
.
CNVQUOTE  INIT      "#"'"              * Replace Quotes
.
STATUS1   INIT      "Pending"
STATUS2   INIT      "Processed"
STATUS3   INIT      "Processing"
STATUS4   INIT      "Processed Cancelled"
STATUS5   INIT      "Ignore"
STATUS6   INIT      "Cancelled"
STATUS7   INIT      "Added as Booked"
STATUS8   INIT      "Added as Referral"
STATUS9   INIT      "Added as Waiting List"
.
TOLOCASE  INIT      "AaBbCcDdEeFfGgHhIiJjKkLlMmNnOoPpQqRrSsTtUuVvWwXxYyZz"
.
.----------------------------------------------------------------------
PRGID     INIT      "COMWEB10"
VERSION   INIT      "V12.01.01"
PRGNAM    INIT      "eReferral Web Server"
.----------------------------------------------------------------------
.
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200,MAIN1300,MAIN1400
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "Invalid Option.",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  CALL      EREF0000           * eReferral list (Hospital)
          GOTO      MAIN1000
.
MAIN1200  CALL      PREF0000           * Process eReferral
          GOTO      MAIN1000
.
MAIN1300  CALL      REMOTE00           * Remote scripts
          GOTO      MAIN1000
.
MAIN1400  CALL      EVRF0000           * eReferral event list for a U/R
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
.------------------------------------------------------------
. Output Next Page Based on CGI Parameter nextpage
.------------------------------------------------------------
NXTPAG00  MOVE      ZERO,F1
          MOVE      NEXTPAGE,F1
          BRANCH    F1,NXTPAG10,NXTPAG20
.
          CALL      WINCLS00
          GOTO      NXTPAG99
.
NXTPAG10  CALL      RDIREC00
          GOTO      NXTPAG99
.
NXTPAG20  CALL      PREDIR00      * Redirect Parent
          GOTO      NXTPAG99
.
NXTPAG99  RETURN
.------------------------------------------------------------------------------
.  Redirect Parent URL
.------------------------------------------------------------------------------
PREDIR00  CALL      OPENHTML
          BRANCH    EXIT,PREDIR90
.
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             " parent.location.href=#"",REDIRECT,"#";":
                             "}":
                             "</script>"
          CALL      CLOSHTML
          GOTO      PREDIR99
.
PREDIR90  DISPLAY   "*** Fifo Not Found ***"
.
PREDIR99  RETURN
.
.------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------
INIT0000  CALL      INTMAS00     * Open routine for PATMASTM 
.
          OPEN      CONTROLF,"controlf"
.
          OPEN      ALLDEPA1,"alldepaf"
          OPEN      ALLDIAA1,"alldiaaf"
          OPEN      ALLPRRA1,"allprraf"
          OPEN      ALLREFA2,"allrefaf"
          OPEN      BOKRC1A1,"bokrc1af"
          OPEN      BOKRC1A6,"bokrc1af"
          OPEN      BOKDIAA1,"bokdiaaf"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
          OPEN      COMERDA1,"comerdaf"
          OPEN      COMERHA1,"comerhaf"
          OPEN      COMERHA7,"comerhaf"
          OPEN      MLTSECA1,"mltsecaf"
          OPEN      OBSECOA1,"obsecoaf"
          OPEN      OBSPCTA1,"obspctaf"
          OPEN      OBSPCOA1,"obspcoaf"
          OPEN      OPRDETA2,"oprdetaf"
          OPEN      PATVISA2,"patvisaf"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      PATRE1A1,"patre1af"
          OPEN      PATDGWA1,"patdgwaf"
          OPEN      PATCMNT1,"patcmntf"
          OPEN      PATITEM1,"patitemn"
          OPEN      PATASFA1,"patasfaf"
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATHSPA1,"pathspaf"
          OPEN      PATMI1A8,"patmi1af"
          OPEN      PMSHCPA1,"pmshcpaf"
          OPEN      PMSCCDA1,"pmsccdaf"
.
          OPEN      VISMDTA1,"vismdtaf"
          OPEN      VISMTXA1,"vismtxaf"
          OPEN      WATPROA1,"watproaf"
          OPEN      WATTR1A1,"wattr1af"
          OPEN      WATTX1A1,"wattx1af"
.
INIT9999  RETURN
.------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          MOVE      SP70,TEMPLATE
          MOVE      SP70,NEXTPAGE
          MOVE      SP70,REDIRECT
          MOVE      SP70,STARTKEY
          MOVE      SP70,UPDTTYPE
          MOVE      SP70,URNUMBER
          MOVE      SP70,ADMISSNO
          MOVE      ZERO,REMOTENO
          MOVE      SP70,VIEWTYPE
          MOVE      ZERO,SHOWFLAG
          MOVE      SP70,FILTFLAG
          MOVE      SP70,HOSPFLAG
          MOVE      SP70,PRIOFLAG
          MOVE      SP70,FRSTDATE
          MOVE      SP70,LASTDATE
          MOVE      SP70,VYEARMTH
          MOVE      SP70,FILTBTYP
          MOVE      SP70,FILTCLAM
          MOVE      SP70,FILTDOCT
          MOVE      SP70,EREFRLID
          MOVE      SP70,STATCODE
          MOVE      SP70,CASEKEYS
          MOVE      SP70,DEPTCODE
.
CLRPAR99  RETURN
.------------------------------------------------------------
. Set Parameters
.------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updttype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDTTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "urnumber=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,URNUMBER
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "admissno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ADMISSNO
            GOTO      SETPAR99
          ENDIF
.  
          MATCH     "nextpage=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NEXTPAGE
            GOTO      SETPAR99
          ENDIF 
.
          MATCH     "redirect=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REDIRECT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "remoteno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REMOTENO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "viewtype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,VIEWTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtflag=",QRYNAME
          IF        @EQUAL
            PACK      FILTFLAG,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtclam=",QRYNAME
          IF        @EQUAL
            PACK      FILTCLAM,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtdoct=",QRYNAME
          IF        @EQUAL
            PACK      FILTDOCT,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtbtyp=",QRYNAME
          IF        @EQUAL
            PACK      FILTBTYP,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "hospflag=",QRYNAME
          IF        @EQUAL
            PACK      HOSPFLAG,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "prioflag=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,PRIOFLAG
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "showflag=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SHOWFLAG
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "lastdate=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,LASTDATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "vyearmth=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,VYEARMTH
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "frstdate=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FRSTDATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "erefrlid=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,EREFRLID
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "statcode=",QRYNAME
          IF        @EQUAL
            MOVE      ZERO,F2               * Right justify 2 character status
            SQUEEZE   QRYDATA
            MOVE      QRYDATA,F2
            MOVE      F2,STATCODE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "casekeys=",QRYNAME
          IF        @EQUAL
            PACK      CASEKEYS,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "deptcode=",QRYNAME
          IF        @EQUAL
            PACK      DEPTCODE,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
.------------------------------------------------------------
. Hospital level eReferral list
.------------------------------------------------------------
EREF0000  CALL      CURPER00
          CALL      CALCDT00   * Calculate Next and Last Period Dates
.
          CLEAR     WEBTITLE
          MOVE      "eReferrals - Online Request List",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,PREF9999
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
EREF9999  RETURN
.------------------------------------------------------------
. Process eReferral
.------------------------------------------------------------
PREF0000  CLEAR     WEBTITLE
          MOVE      "Process Online Request for Treatment",WEBTITLE
          RESET     WEBTITLE
.
          PACK      KEY20,EREFRLID,SP70
          CALL      RDCMERH1                * Read eReferral header
          BRANCH    OVRCD,PREF9000

          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,PREF9999
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          GOTO      PREF9999
.
PREF9000  MOVE      "Invalid eReferral ID.",WEBTITLE
          CALL      WEBERR00
          GOTO      PREF9999
.
PREF9999  RETURN
.
.------------------------------------------------------------
. eReferral event list for a U/R
.------------------------------------------------------------
EVRF0000  MATCH     SP70,UPDTTYPE
          GOTO      EVRF7000 IF EQUAL
.
          MATCH     "P",UPDTTYPE
          GOTO      EVRF1000 IF EQUAL
.
          GOTO      EVRF9000
.
EVRF1000
          MOVE      ZERO,EXIT
          GOTO      EVRF9999
.
EVRF7000  PACK      KEY8,URNUMBER,SP70
          CALL      RDMAST1                 * Read PMI
          BRANCH    OVRCD,EVRF9200
.
          PACK      KEY8,URNUMBER,SP70
          CALL      RDPMPX21                * Read PMI
          BRANCH    OVRCD,EVRF9200
.
          PACK      KEY20,EREFRLID,SP70
          CALL      RDCMERH1                * Read eReferral header
          BRANCH    OVRCD,EVRF9100
.
          CLEAR     WEBTITLE
          MOVE      "eReferral - Existing Event List",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,PREF9999
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      EVRF9999
.
EVRF9000  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EVRF9999
.
EVRF9100  MOVE      "Invalid eReferral ID.",WEBTITLE
          CALL      WEBERR00
          GOTO      EVRF9999
.
EVRF9200  MOVE      "Invalid UR Number.",WEBTITLE
          CALL      WEBERR00
          GOTO      EVRF9999
.
EVRF9999  RETURN

.
.******************************************************************************
.                    REMOTE SCRIPTING FUNCTION
.******************************************************************************
REMOTE00  CALL      XMLHED00
          BRANCH    EXIT,REMOTE99
.
          BRANCH    REMOTENO,REMOTE10,REMOTE20,REMOTE30,REMOTE40,REMOTE50:
                             REMOTE60
          GOTO      REMOTE95
.
REMOTE10  CALL      SETS0000  * set eReferral status
          GOTO      REMOTE95
.
REMOTE20  CALL      ULNKU000  * Unlink URNO
          GOTO      REMOTE95
.
REMOTE30  CALL      ULNKV000  * Unlink Visit
          GOTO      REMOTE95
.
REMOTE40  CALL      ERPROP00  * get eReferral properties
          GOTO      REMOTE95
.
REMOTE50  CALL      VISIT000  * Link Visit to UR
          GOTO      REMOTE95
.
REMOTE60  CALL      VALDP000  * Validate A/H department
          GOTO      REMOTE95
.
REMOTE95  CALL      XMLEND00
.
REMOTE99  RETURN
.
.------------------------------------------------------------
. get eReferral Properties
.------------------------------------------------------------
ERPROP00  WRITE     HTMLFILE;"[";
          REP       "+ ",EREFRLID
          PACK      KEY20,EREFRLID,SP70
          CALL      RDCMERH1
          IF        OVRCD<>1
            MOVE      ANSY,FIRSTFLG
            PACK      KEY28,CMRHUNIQ,SP70
            CALL      RACMERD1
ERPROP10    CALL      RKCMERD1
            BRANCH    OVRCD,ERPROP90
.
            MATCH     CMRHUNIQ,CMRDUNIQ
            GOTO      ERPROP90 IF NOT EQUAL
.
            MATCH     SP70,CMRDWBID
            GOTO      ERPROP10 IF EQUAL
.
            MOVE      CMRDVALU,DIM600
            STRIP     DIM600
            REP       TOLOCASE,CMRDWBID
            MATCH     ANSY,FIRSTFLAG
            IF        @EQUAL
              MOVE      ANSN,FIRSTFLG
              WRITE     HTMLFILE;"{#"name#":#"",CMRDWBID,"#",#"value#":#"",DIM600,"#"}";
            ELSE
              WRITE     HTMLFILE;",{#"name#":#"",CMRDWBID,"#",#"value#":#"",DIM600,"#"}";
            ENDIF
            GOTO      ERPROP10
.
          ENDIF
.
ERPROP90  WRITE     HTMLFILE;"]";
.
ERPROP99  RETURN
.
.------------------------------------------------------------
. Set eReferral status
.------------------------------------------------------------
SETS0000  REP       "+ ",EREFRLID
          PACK      KEY20,EREFRLID,SP70
          CALL      RDCMERH1
          BRANCH    OVRCD,SETS9000
.
          MOVE      STATCODE,CMRHSTAT
.
          MATCH     URNUMBER,SP70
          GOTO      SETS0100 IF EQUAL
.
          MATCH     "UNK",URNUMBER
          IF        @EQUAL
            MOVE      SP70,CMRHURNO
            GOTO      SETS0100
          ENDIF
.
          MATCH     URNUMBER,CMRHURNO
          GOTO      SETS0100 IF EQUAL
.
          MOVE      URNUMBER,CMRHURNO
.
SETS0100  MATCH     ADMISSNO,SP70
          IF        !@EQUAL
            MATCH     "UNK",URNUMBER
            IF        @EQUAL
              MOVE      SP70,CMRHVISN
            ELSE
              MOVE      ADMISSNO,CMRHVISN
            ENDIF
          ENDIF
.
          CALL      UPCMERH1
.
          WRITE     HTMLFILE;"true";
.
          GOTO      SETS9999
.
SETS9000  WRITE     HTMLFILE;"false";
.
SETS9999  RETURN
.------------------------------------------------------------
. Process Fields 
.------------------------------------------------------------
PROFLD00  BRANCH    REPORTNO,PROFLD10,PROFLD20,PROFLD99,PROFLD40
.
          GOTO      PROFLD99 
.
PROFLD10  BRANCH    FLDSETNO,PROFLD11
          GOTO      PROFLD99
.
PROFLD11  CALL      RTAG0000      * eReferral tags
          GOTO      PROFLD99
.
PROFLD20  BRANCH    FLDSETNO,PROFLD21,PROFLD22,PROFLD23,PROFLD24
          GOTO      PROFLD99
.
PROFLD21  CALL      ERTG0000      * eReferral details 
          GOTO      PROFLD99
.
PROFLD22  CALL      RTAG0000      * eReferral tags 
          GOTO      PROFLD99
.
PROFLD23  CALL      CMRH0000      * eReferral header file tags
          GOTO      PROFLD99
.
PROFLD24  CALL      TABL0000      * eReferral lists
          GOTO      PROFLD99
.
PROFLD40  BRANCH    FLDSETNO,PROFLD41,PROFLD42,PROFLD43,PROFLD44,PROFLD45
          GOTO      PROFLD99
.
PROFLD41  CALL      MASF0000      * Master File Info
          GOTO      PROFLD99
.
PROFLD42  CALL      RTAG0000      * eReferral tags
          GOTO      PROFLD99
.
PROFLD43  CALL      ERTG0000      * eReferral details 
          GOTO      PROFLD99
.
PROFLD44  CALL      CMRH0000      * eReferral header file tags
          GOTO      PROFLD99
.
PROFLD45  CALL      TABL0000      * eReferral lists
          GOTO      PROFLD99
.
PROFLD99  RETURN
.
.-------------------------------------------------------------
. Lists
.-------------------------------------------------------------
TABL0000  BRANCH    FLDITMNO,TABL0010,TABL0020,TABL0030,TABL0040,TABL0050:
                             TABL0060
.
          GOTO      TABL9999
.
TABL0010  CALL      REFL0000    * Refeferral list
          GOTO      TABL9999
.
TABL0020  CALL      WATL0000    * Waiting list
          GOTO      TABL9999
.
TABL0030  CALL      BOKL0000    * Booking list
          GOTO      TABL9999
.
TABL0040  CALL      PADL0000    * Pre Admission/Admission list
          GOTO      TABL9999
.
TABL0050  CALL      DOCD0000    * Document List
          GOTO      TABL9999
.
TABL0060  CALL      DOCS0000    * Document List
          GOTO      TABL9999
.
TABL9999  RETURN
.
.-------------------------------------------------------------
. eReferral by date range tags
.-------------------------------------------------------------
RTAG0000  BRANCH    FLDITMNO,RTAG0100,RTAG0200,RTAG0300,RTAG0400,RTAG0500:
                             RTAG0600,RTAG0700,RTAG0800,RTAG0900,RTAG1000:
                             RTAG1100,RTAG1200,RTAG1300
          GOTO      RTAG9999
.
RTAG0100  CALL      NEXT0000
          GOTO      RTAG9999
.
RTAG0200  CALL      PREV0000
          GOTO      RTAG9999
.
RTAG0300  WRITE     HTMLFILE;CMRHEREF;       *eReferral Reference
          GOTO      RTAG9999
.
RTAG0400 
          GOTO      RTAG9999
.
RTAG0500 
          GOTO      RTAG9999
.
RTAG0600 
          GOTO      RTAG9999
.
RTAG0700  CALL      SELV0000
          GOTO      RTAG9999
.
RTAG0800  WRITE     HTMLFILE;VIEWTYPE;
          GOTO      RTAG9999
.
RTAG0900  CALL      HOSSEL00
          GOTO      RTAG9999
.
RTAG1000  CALL      EHOS0000
          GOTO      RTAG9999
.
RTAG1100 
          GOTO      RTAG9999
.
RTAG1200  WRITE     HTMLFILE;EREFRLID;      *eReferral ID
          GOTO      RTAG9999
.
.         Tag to display existance of documents
.
RTAG1300  PACK      KEY24,CMRHEREF,SP70
          CALL      RSOBECO1               * Does a document exist
          CALL      RKOBECO1
          IF        OVRCD = 1      
            WRITE     HTMLFILE;"N";        * No Record
          ELSE
            MATCH     CMRHEREF,OBECEAID    * Document found
            IF        @EQUAL
              WRITE     HTMLFILE;"Y";
            ELSE
              WRITE     HTMLFILE;"N";      * No document found
            ENDIF
          ENDIF
          GOTO     RTAG9999
.
RTAG9999  RETURN
.
.-------------------------------------------------------------
. Hospital level eReferral list
.-------------------------------------------------------------
EHOS0000  IF        SHOWFLAG=0
            MATCH     SP70,HOSPFLAG
            IF        @EQUAL
              MOVE      WBSEHOSP,HOSPFLAG   * Default to users hosp
            ENDIF
          ENDIF
.
          PACK      KEY33,FRSTDATE,SP70
          CALL      RSCMERH7
EHOS1000  CALL      RKCMERH7
          BRANCH    OVRCD,EHOS9999
.
          MATCH     CMRHDATE,LASTDATE
          GOTO      EHOS9999 IF LESS
.
EHOS4000  MATCH     SP70,HOSPFLAG
          IF        !@EQUAL
            MATCH     HOSPFLAG,CMRHHOSP
            GOTO      EHOS1000 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,FILTFLAG
          IF        !@EQUAL
            MATCH     FILTFLAG,CMRHSTAT
            GOTO      EHOS1000 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,FILTBTYP
          IF        !@EQUAL
            MATCH     FILTBTYP,CMRHBTYP
            GOTO      EHOS1000 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,FILTDOCT
          IF        !@EQUAL
            PACK      KEY28,CMRHUNIQ,RFFR0101,SP70
            CALL      RDCMERD1
            BRANCH    OVRCD,EHOS1000
.
            MATCH     FILTDOCT,CMRDVALU
            GOTO      EHOS1000 IF NOT EQUAL
          ENDIF
.
          MOVE      SP70,CLAIMCOD
          PACK      KEY28,CMRHUNIQ,RFFR0100,SP70      * ClaimTypePASCode
          CALL      RDCMERD1
          IF        OVRCD<>1
            MOVE      CMRDVALU,CLAIMCOD
          ENDIF
.
          MATCH     SP70,FILTCLAM
          IF        !@EQUAL
            MATCH     FILTCLAM,CLAIMCOD
            GOTO      EHOS1000 IF NOT EQUAL
          ENDIF
.
          CALL      EROW0000                     * Display row
          GOTO      EHOS1000
.
EHOS9999  RETURN
.
.------------------------------------------------------------
. Hospital level eReferral list row
.------------------------------------------------------------
EROW0000  MOVE      SP70,PGNAME
          MOVE      SP70,PSEX
          MOVE      SP70,PTITL
          MOVE      SP70,PSNAME
          MOVE      SP70,PTMASNAM
          MOVE      SP70,STREETNO
          MOVE      SP70,PBDATE
          MOVE      SP70,STREETNM
          MOVE      SP70,SUBURB
          MOVE      SP70,POSTCODE
          MOVE      SP70,BKTYPE
          MOVE      SP70,ADMINDTE
          MOVE      SP70,ADMITVMO
          MOVE      SP70,STATDESC
          MOVE      SP70,PROCDESC
          MOVE      SP70,XMLSRC
.
          MATCH     CMRHVISN,SP70
          GOTO      EROW0500 IF NOT EQUAL
.
          MOVE      CMRHDATE,FIRSTDAT
          MOVE      CMRHTIME,D5
          REP       ": ",D5
          SQUEEZE   D5
          PACK      FIRSTIME,D5,SP70
       
          CALL      IBACLOCK
          PACK      LASTDATE,CCC,CYY,CMM,CDD
          REP       " 0",LASTDATE
          MOVE      CTIMEIS,D5
          REP       ": ",D5
          SQUEEZE   D5
          PACK      LASTTIME,D5,SP70
.
          CALL      TIMEDIFF
.
          IF        CALCTIME <=4320
            MOVE      YELLOW,PRIORCLR       * <= 2 days waiting
            GOTO      EROW0600
          ENDIF
.
          IF        CALCTIME >= 4321
            MOVE      RED,PRIORCLR          * >= 3 days waiting     
            GOTO      EROW0600
          ENDIF
.
          GOTO      EROW0600
.
EROW0500  MOVE      GREEN,PRIORCLR
.
EROW0600  PACK      VARIABLE,SP70,SP70
          MOVE      ONE,VARCOUNT
.
          MATCH     SP70,PRIOFLAG
          GOTO      EROW1000 IF EQUAL
.
          MATCH     PRIOFLAG,PRIORCLR
          GOTO      EROW9999 IF NOT EQUAL
.
EROW1000  LOAD      VARIABLE,VARCOUNT,RFFR0009:      * 1 surname
                                      RFFR0007:      * 2 title
                                      RFFR0010:      * 3 given name
                                      RFFR0014:      * 4 gender
                                      RFFR0012:      * 5 dob
                                      RFFR0020:      * 6 Street No
                                      RFFR0021:      * 7 Street Name
                                      RFFR0022:      * 8 Suburb
                                      RFFR0024:      * 9 post code
                                      RFFR0119:      * 10 admission type
                                      RFFR0005:      * 11 admssion date
                                      RFFR0101:      * 13 admitting doctor
                                      RFFR0108:      * 14 presenting problem
                                      RFFR0138       * 15 XML Source
.
          PACK      KEY28,CMRHUNIQ,VARIABLE,SP70
          CALL      RDCMERD1
          IF        OVRCD <> 1
            BRANCH    VARCOUNT,EROW1010:          * 1
                               EROW1020:          * 2
                               EROW1030:          * 3
                               EROW1040:          * 4
                               EROW1050:          * 5
                               EROW1060:          * 6
                               EROW1070:          * 7
                               EROW1080:          * 8
                               EROW1090:          * 9
                               EROW1100:          * 10
                               EROW1110:          * 11
                               EROW1120:          * 12
                               EROW1130:          * 13
                               EROW1140           * 14
          ENDIF
.
          GOTO      EROW2000
.
EROW1010  MOVE      CMRDVALU,PSNAME
          MOVE      CMRDVALU,PTMASNAM
          GOTO      EROW2000
.
EROW1020  MOVE      CMRDVALU,PTITL
          GOTO      EROW2000
.
EROW1030  MOVE      CMRDVALU,PGNAME
          GOTO      EROW2000
.
EROW1040  MOVE      CMRDVALU,PSEX
          GOTO      EROW2000
.
EROW1050  MOVE      CMRDVALU,PBDATE
          GOTO      EROW2000
.
EROW1060  MOVE      CMRDVALU,STREETNO
          GOTO      EROW2000
.
EROW1070  MOVE      CMRDVALU,STREETNM
          REP       CNVQUOTE,STREETNM
          GOTO      EROW2000
.
EROW1080  MOVE      CMRDVALU,SUBURB
          GOTO      EROW2000
.
EROW1090  MOVE      CMRDVALU,POSTCODE
          GOTO      EROW2000
.
EROW1100  MOVE      CMRDVALU,BKTYPE
          GOTO      EROW2000
.
EROW1110  MOVE      CMRDVALU,ADMINDTE
          GOTO      EROW2000
.
EROW1120  PACK      KEY10,CMRDVALU,SP70
          CALL      RDPMHCP1
          IF        OVRCD<>1
            MOVE      PMHCTITL,FMTTITLE
            MOVE      PMHCGNAM,FMTGNAME
            MOVE      PMHCSNAM,FMTSNAME
            CALL      DOCNM000
            MOVE      DOCFNAME,ADMITVMO
          ENDIF
          GOTO      EROW2000
.
EROW1130  MOVE      CMRDVALU,PROCDESC
          GOTO      EROW2000
.
EROW1140  MOVE      CMRDVALU,XMLSRC
          GOTO      EROW2000
.
EROW2000  ADD       ONE,VARCOUNT
.
          COMPARE   "15",VARCOUNT
          GOTO      EROW1000 IF LESS
.
          CALL      IBACLOCK
          IF        PCEASE=1
            MOVE      PDECDTE,AGEDATE            * Date of Death
          ELSE
            PACK      AGEDATE,CCC,CYY,CMM,CDD    * Today's date
          ENDIF
          REP       " 0",AGEDATE
.
          CALL      CALCAGE
          MOVE      CMRHURNO,PURNO
          MATCH     SP70,PURNO
          IF        !@EQUAL
            PACK      KEY8,PURNO,SP70
            CALL      RDMAST1
            CALL      RDPMPX21
          ENDIF
          CALL      PATLN000
.
          MOVE      SP70,BKDESC
          MATCH     BKTYPE,SP70
          GOTO      EROW2050 IF EQUAL
.
          PACK      KEY5,CATBK,BKTYPE,SP70
          CALL      RDCODE1
          IF        OVRCD<>1
            MOVE      TDESC,BKDESC
          ENDIF
.
EROW2050  MOVE      ZERO,FORM2
          SQUEEZE   CMRHSTAT
          MOVE      CMRHSTAT,FORM2
          LOAD      STATDESC,FORM2,STATUS1:     * pending
                                  STATUS2:     * processed
                                  STATUS3:     * processing
                                  STATUS4:     * process cancelled
                                  STATUS5:     * ignore
                                  STATUS6:     * cancelled
                                  STATUS7:     * added as booked
                                  STATUS8:     * added as referral
                                  STATUS9      * added as waiting list
.
          STRIP     PRIORCLR
.
          MOVE      SP70,HISDATE
          MOVE      ZERO,DOCEXIST
          PACK      LINKDOC,SP70
          PACK      DOCMLINK,SP70,SP70,SP70
.
          MATCH     CMRHLHDT,SP70
          GOTO      EROW3000 IF NOT EQUAL
.
          MATCH     CMRHVISN,SP70
          IF        !@EQUAL
            MOVE      ONE,DOCEXIST
            GOTO      EROW4000
          ENDIF
.
          GOTO        EROW5000
.
EROW3000  MATCH     CMRHLHRD,SP70
          GOTO      EROW3100 IF EQUAL
.
          UNPACK    CMRHLHRD,CCENT,CYEAR,DIM2,CDAY
          MOVE      DIM2,F2
          REP       " 0",CCENT
          REP       " 0",CYEAR
          REP       " 0",CDAY
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      HISDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
EROW3100  MOVE      ONE,DOCEXIST
.
          MATCH     CMRHVISN,SP70
          GOTO      EROW4000 IF NOT EQUAL
.
          PACK      LINKDOC,SP70
          PACK      KEY24,CMRHEREF,Z70,SP70
          CALL      RSOBECO1
          CALL      RPOBECO1
.
          MATCH     CMRHEREF,OBECEAID
          IF        @EQUAL
            PACK      KEY4,OBECSLID
            CALL      RDOBPT1
            STRIP     OBPTURLP
            APPEND    "javascript:openDocument('",LINKDOC
            REP       " +",CMRHEREF
            APPEND    CMRHEREF,LINKDOC
            APPEND    DASH,LINKDOC
            REP       " 0",OBECCPID
            APPEND    OBECCPID,LINKDOC
            APPEND    OBECFEXT,LINKDOC
            APPEND    "','",LINKDOC
            APPEND    OBPTURLP,LINKDOC
            APPEND    "');",LINKDOC
            REP       "+ ",CMRHEREF
            RESET     LINKDOC
          ENDIF
.
          GOTO        EROW5000
.
EROW4000  PACK      LINKDOC,SP70
          APPEND    "javascript:printDocument('",LINKDOC
          APPEND    CMRHURNO,LINKDOC
          APPEND    "','",LINKDOC
          APPEND    CMRHVISN,LINKDOC
          APPEND    "','",LINKDOC
          APPEND    CMRHUNIQ,LINKDOC
          APPEND    "');",LINKDOC
          RESET     LINKDOC
.
EROW5000  COMPARE   ONE,DOCEXIST                 * No clinical documents
          GOTO      EROW6000 IF NOT EQUAL
.
          CLEAR     DOCMLINK
          APPEND    "<a href=\#"",DOCMLINK
          APPEND    LINKDOC,DOCMLINK
          APPEND    "\#"><img src=../images/historyreceived1.gif ",DOCMLINK
          APPEND    "class=Icon></a>",DOCMLINK
          RESET     DOCMLINK
.
EROW6000  MOVE      CMRHUNIQ,ONLINEID
          REP       " +",ONLINEID
.
          COMPARE   SEVEN,FORM2
          IF        @EQUAL
            PACK      BOOKICON,SP70
            PACK      BOOKLINK,SP70,SP70
.....       CLEAR     BOOKICON
.....       APPEND    "<img src=../images/AppointmentIcon.gif /> ",BOOKICON
.....       RESET     BOOKICON
.....       CLEAR     BOOKLINK
.....       APPEND    "javascript:LinkToBook('",BOOKLINK
.....       APPEND    ONLINEID,BOOKLINK
.....       APPEND    "','",BOOKLINK
.....       APPEND    CMRHURNO,BOOKLINK
.....       APPEND    "','",BOOKLINK
.....       APPEND    CMRHVISN,BOOKLINK
.....       APPEND    "')",BOOKLINK
.....       RESET     BOOKLINK
          ENDIF
.
          CLEAR     FLDRLINK
          APPEND    "javascript:LinkTo('",FLDRLINK
          APPEND    ONLINEID,FLDRLINK
          APPEND    "','",FLDRLINK
          APPEND    CMRHURNO,FLDRLINK
          APPEND    "','",FLDRLINK
          APPEND    CMRHVISN,FLDRLINK
          APPEND    "')",FLDRLINK
          RESET     FLDRLINK
. 
          MOVE      SP70,CLAMDESC
          MATCH     SP70,CLAIMCOD
          IF        !@EQUAL
            PACK      KEY5,ANSC,ANSL,CLAIMCOD,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              MOVE      TDESC,CLAMDESC      * Claim type description
            ENDIF
          ENDIF
.
          MATCH     SP70,CMRHPCOD
          IF        !@EQUAL
            PACK      KEY9,CMRHPCOD,SP70
            CALL      RDPROA1
            IF        OVRCD<>1
              PACK      PROCDESC,WPCODE,SP1,DASH,SP1,WPDESC,SP70
            ENDIF
          ENDIF
.
          MOVE      SP70,DISPDATE
          MATCH     SP70,CMRHDATE
          IF        !@EQUAL
            UNPACK    CMRHDATE,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      DISPDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          ENDIF
.
          WRITE     HTMLFILE;"t.addRow(#"",FLDRLINK,"#",":
                             "#"",PRIORCLR,"#",":          * priority colour
                             "#"",ZERO,"#",":              * folder colour
                             "#"",FORMATNM:                * fmtd name/addr
                                 "<br /><span style=#'padding-left:30px;#'>":
                                 STREETNM,SUBURB,POSTCODE,"#",":
                             "#"",BKDESC,"#",":            * booking type
                             "#"",CMRHDATE,"#",":          * received date
                             "#"",HISDATE,"#",":           * history received
                             "#"",CMRHEDAT,"#",":          * expect admiss dt
                             "#"",ADMITVMO,"#",":          * admitting dr
                             "#"",CMRHURNO,"#",":          * urno
                             "#"",CMRHVISN,"#",":          * admission no
                             "#"","#",":                   * reg received dt
                             "#"",STATDESC,"#",";           * status
.
          MATCH     CMRHLHDT,SP70
          IF        @EQUAL
            WRITE     HTMLFILE;"#"",ZERO,"#",":            * no history recvd
                               "#"",SP1,"#",";
          ELSE
            WRITE     HTMLFILE;"#"",DOCEXIST,"#",":        * history recvd
                               "#"",LINKDOC,"#",";
          ENDIF
.
          COMPARE   SEVEN,FORM2
          IF        @EQUAL
            WRITE     HTMLFILE;"#"",BOOKLINK,"#",":        * booked
                               "#"",BOOKICON,"#",";
          ELSE
            WRITE     HTMLFILE;"#"",SP1,"#",":             * not booked
                               "#"",SP1,"#",";
          ENDIF
.
          WRITE     HTMLFILE;"#"",SP1,"#",":               * 17
                             "#"",CLAMDESC,"#",":          * 18
                             "#"",PROCDESC,"#",";          * 19
.
          WRITE     HTMLFILE;"#"",DISPDATE,"&nbsp;",DOCMLINK,"#",":   * 20
                             "#"",CMRHEREF,"#",":          * 21
                             "#"",XMLSRC,"#",":            * 22
                             "#"#");"
.
EROW9999  RETURN
.
.------------------------------------------------------------
. Hospital Selection
.------------------------------------------------------------
HOSSEL00  PACK      KEY3,SP70
          CALL      RSPTHSP1
HOSSEL10  CALL      RKPTHSP1
          BRANCH    OVRCD,HOSSEL99
.
          PACK      KEY13,WBSEUID,SP70                * All hospital access
          CALL      RDMLSEC1
          IF        OVRCD=1
            PACK      KEY13,WBSEUID,PTHSHOSP,SP70     * This hospital access
            CALL      RDMLSEC1
            BRANCH    OVRCD,HOSSEL10
          ENDIF
.
          MOVE      PTHSNAME,KEY50
          PACK      KEY5,QUOTE,PTHSHOSP,QUOTE
          MATCH     FILTHOSP,PTHSHOSP
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=",KEY5," selected>":
                               KEY50,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=",KEY5,">",KEY50,"</option>"
          ENDIF
          GOTO      HOSSEL10
.
HOSSEL99  RETURN
.
.------------------------------------------------------------
. Previous Day, Week, Month
.------------------------------------------------------------
PREV0000  MOVE      TEMPLATE,F3
          MOVE      F3,KEY3
          REP       " 0",KEY3
.
          MOVE      REPORTNO,F2
          MOVE      F2,KEY2
          REP       " 0",KEY2
.
          WRITE     HTMLFILE;"comweb10.pbl":
                             "?reportno=",KEY2:
                             "&template=",KEY3:
                             "&frstdate=",PREFDATE:
                             "&lastdate=",PRELDATE:
                             "&viewtype=",VIEWTYPE;
PREV9999  RETURN
.
.-------------------------------------------------------------
. Next Day, Week, Month
.-------------------------------------------------------------
NEXT0000  MOVE      TEMPLATE,F3
          MOVE      F3,KEY3
          REP       " 0",KEY3
.
          MOVE      REPORTNO,F2
          MOVE      F2,KEY2
          REP       " 0",KEY2
.
          WRITE     HTMLFILE;"comweb10.pbl":
                             "?reportno=",KEY2:
                             "&template=",KEY3:
                             "&frstdate=",NXTFDATE:
                             "&viewtype=",VIEWTYPE:
                             "&lastdate=",NXTLDATE;
NEXT9999  RETURN
.
.-------------------------------------------------------------
. Patient eReferral Details
.-------------------------------------------------------------
ERTG0000  MOVE      ZERO,DIM1
          UNPACK    DIM127,DIM1,DIM127
.
          MATCH     ".",DIM1
          GOTO      ERTG2000 IF EQUAL
.
ERTG1000  PACK      DIM4,FLDITMNO,DIM1
          MOVE      DIM4,FORM4
          PACK      VARIABLE,RFFRVARS,FORM4,SP70
          REP       " 0.0",VARIABLE
          UNPACK    DIM127,DIM1,DIM1,DIM127
.
          GOTO      ERTG3000
.
ERTG2000  UNPACK    DIM127,DIM1,DIM127
          MOVE      FLDITMNO,FORM4
          PACK      VARIABLE,RFFRVARS,FORM4,SP70
          REP       " 0.0",VARIABLE
          GOTO      ERTG3000
.
ERTG3000  MATCH     "E",DIM1
          IF        @EQUAL
            UNPACK    DIM127,D1,DIM2,DIM127
          ENDIF
.
          PACK      KEY28,CMRHUNIQ,VARIABLE,SP70
          CALL      RDCMERD1
          BRANCH    OVRCD,ERTG9000
.
          MATCH     SP70,DIM1
          IF        @EQUAL
            MOVE      CMRDVALU,DIM10
            WRITE     HTMLFILE;DIM10;
            GOTO      ERTG9999
          ENDIF
.
          MATCH     "A",DIM1
          IF        @EQUAL
            MOVE      CMRDVALU,DIM20
            WRITE     HTMLFILE;DIM20;
            GOTO      ERTG9999
          ENDIF
.
          MATCH     "B",DIM1
          IF        @EQUAL
            MOVE      CMRDVALU,DIM40
            WRITE     HTMLFILE;DIM40;
            GOTO      ERTG9999
          ENDIF
.
          MATCH     "D",DIM1
          IF        @EQUAL
            UNPACK    CMRDVALU,CCENT,CYEAR,DIM2,CDAY
            MOVE     DIM2,F2
            REP       " 0",CCENT
            REP       " 0",CYEAR
            REP       " 0",CDAY
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG:
                                 SEP,OCT,NOV,DEC
            WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          ENDIF
.
          MATCH     "E",DIM1
          IF        @EQUAL
            MOVE      CMRDVALU,DIM20
.
            PACK      KEY5,DIM2,CMRDVALU,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              MOVE      TDESC,DIM20
            ENDIF
            WRITE     HTMLFILE;DIM20;
          ENDIF
.
          MATCH     "Y",DIM1
          IF        @EQUAL
            MATCH     "1",CMRDVALU,
            IF        @EQUAL
              WRITE     HTMLFILE;"Yes";
            ELSE
              WRITE     HTMLFILE;"No";
            ENDIF
            GOTO      ERTG9999
          ENDIF
.
          GOTO      ERTG9999
.
ERTG9000  WRITE     HTMLFILE;" ";
.
ERTG9999  RETURN
.
.-------------------------------------------------------------
. eReferral header fields
.-------------------------------------------------------------
CMRH0000  BRANCH    FLDITMNO,CMRH0010,CMRH0020,CMRH0030,CMRH0040,CMRH0050:
                             CMRH0060,CMRH0070
          WRITE     HTMLFILE;"Invalid Tag"
.
CMRH0010  WRITE     HTMLFILE;CMRHUNIQ;
          GOTO      CMRH9999
.
CMRH0020  WRITE     HTMLFILE;CMRHURNO;
          GOTO      CMRH9999
.
CMRH0030  WRITE     HTMLFILE;CMRHVISN;
          GOTO      CMRH9999
.
CMRH0040  
...       PACK      KEY28,EREFRLID,Admission reason name
...       CALL      RDPMEDB1
...       IF        OVRCD=1
...         PACK      PMDBVALU,SP70,SP70,SP70,SP70,SP70,SP70,SP70,SP70,SP70
...       ENDIF
.
...       WRITE     HTMLFILE;PMDBVALU;
          GOTO      CMRH9999
.
CMRH0050  MATCH     SP70,CMRHADOC
          GOTO      CMRH9999 IF EQUAL
.
          MOVE      SP70,DOCFNAME
          MOVE      SP70,ADOCNAME
          MOVE      CMRHADOC,KEY6
          CALL      RDDOCT1
          IF        OVRCD=0
            MOVE      DTITL,FMTTITLE
            MOVE      DGNAME,FMTGNAME
            MOVE      DSNAME,FMTSNAME
            CALL      DOCNM000
            PACK      ADOCNAME,DOCFNAME
          ENDIF
.
          WRITE     HTMLFILE;DOCFNAME;
          GOTO      CMRH9999
.
CMRH0060  MATCH     SP70,CMRHEDAT
          GOTO      CMRH9999 IF EQUAL
.
          UNPACK    CMRHEDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      CMRH9999
.
CMRH0070  WRITE     HTMLFILE;EREFRLID;
          GOTO      CMRH9999
.
CMRH9999  RETURN
.
.-------------------------------------------------------------------------------
. Clear URNO and Visit Number on comerhaf and reset documents to eReferral
.-------------------------------------------------------------------------------
ULNKU000  REP       "+ ",EREFRLID
          PACK      KEY20,EREFRLID,SP70
          CALL      RDCMERH1
          BRANCH    OVRCD,ULNKU900
.
          MOVE      " 1",CMRHSTAT
          PACK      STORURNO,CMRHURNO,SP70
          PACK      STORVISN,CMRHVISN,SP70
          PACK      CMRHURNO,SP70
          PACK      CMRHVISN,SP70
          PACK      CMRHPCOD,SP70
          PACK      CMRHPCNT,SP70
.
          CALL      UPCMERH1
.
          CALL      UNDC0000
.
          PACK      KEY20,STORURNO,SP70
          CALL      RSOBPC1
          CALL      RKOBPC1
          BRANCH    OVRCD,ULNKU800
.
          MATCH     OBPCURNO,STORURNO
          GOTO      ULNKU800 IF NOT EQUAL
.
          GOTO      ULNKU850
.
ULNKU800  PACK      KEY8,STORURNO           * validate UR
          CALL      RDMAST1
          BRANCH    OVRCD,ULNKU850
.
          MATCH     "1",PTMXSIN4
          IF        @EQUAL
            PACK      KEY8,STORURNO         * validate UR
            CALL      RLPTMAS1
            BRANCH    OVRCD,ULNKU850,ULNKU850
            MOVE      "0",PTMXSIN4
            CALL      UPMAST1
          ENDIF
.
ULNKU850  WRITE     HTMLFILE;"true";
.
          GOTO      ULNKU999
.
ULNKU900  WRITE     HTMLFILE;"false";
.
ULNKU999  RETURN
.
.------------------------------------------------------------------------------
. Rename eReferral Documents from EREFRLID to URNO, copy details to OBSPCOAF
. and update URNO on OBSECOAF
.------------------------------------------------------------------------------
UNDC0000  PACK      KEY24,CMRHEREF,SP70
          CALL      RSOBECO1
UNDC1000  CALL      RKOBECO1
          BRANCH    OVRCD,UNDC9999
.
          MATCH     CMRHEREF,OBECEAID
          GOTO      UNDC9999 IF NOT EQUAL
.
          MATCH     OBECURNO,STORURNO
          GOTO      UNDC1000 IF NOT EQUAL
.
          PACK      KEY20,STORURNO,STORVISN,Z70
          CALL      RSOBPC1
UNDC2000  CALL      RPOBPC1
          BRANCH    OVRCD,UNDC1000
.
          CALL      IBACLOCK
          PACK      DIM8,CCC,CYY,CMM,CDD
          REP       " 0",DIM8
          PACK      TODAY,DIM8,SP70
          CLOCK     TIME,CTIMEIS
.
          MATCH     OBECURNO,OBPCURNO
          GOTO      UNDC1000 IF NOT EQUAL
.
          MATCH     OBECEAID,OBPCEAID
          GOTO      UNDC2000 IF NOT EQUAL
.
          PACK      KEY20,OBPCURNO,OBPCCVIS,OBPCCPID
          CALL      DEOBPC1
.
          PACK      KEY24,CMRHEREF,OBECCPID,SP70
          MOVE      SP70,OBECURNO
          CALL      UPOBECO1
.
          MOVE      STORVISN,RSTRVISN
          MOVE      CMRHEREF,DISPEAID
          MOVE      OBPCCPID,DISPPCCD
          MOVE      OBECCPID,DISPECCD
          REP       " 0",RSTRVISN
          REP       " 0",DISPEAID
          REP       " 0",DISPPCCD
          REP       " 0",DISPECCD
.
          PACK      KEY4,OBECSLID,SP70
          CALL      RDOBPT1
          STRIP     OBPTSDIR
.
          PACK      NWFILENM,OBPTSDIR,DISPEAID,DASH,DISPECCD,SP70
          PACK      OLDFILEN,OBPTSDIR,RSTRVISN,DASH,DISPPCCD,SP70
          STRIP     OLDFILEN
          STRIP     NWFILENM
          CALL      MVDC0000
.
          GOTO      UNDC2000
.
UNDC9999  RETURN
.
.------------------------------------------------------------
.         call us1 script to move file to new directory
.         will new file name
.------------------------------------------------------------
MVDC0000  CLEAR     CMDLINE
          APPEND    "comweb11pdf.us1 ",CMDLINE
          APPEND    OLDFILEN,CMDLINE
          APPEND    ".pdf",CMDLINE             * old filename
          APPEND    " ",CMDLINE
          APPEND    NWFILENM,CMDLINE           * new filename
          APPEND    ".pdf",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          MATCH     "0       ",TASKID
MVDC9999  RETURN
.
.------------------------------------------------------------
. Clear Visit Number on pmsehbaf
.------------------------------------------------------------
ULNKV000  REP       "+ ",EREFRLID
          PACK      KEY20,EREFRLID,SP70
          CALL      RDCMERH1
          BRANCH    OVRCD,ULNKV900
.
          MOVE      " 3",CMRHSTAT
          PACK      STORURNO,CMRHURNO,SP70
          PACK      STORVISN,CMRHVISN,SP70
          PACK      CMRHVISN,SP70
          PACK      CMRHPCOD,SP70
          PACK      CMRHPCNT,SP70
.
          CALL      UPCMERH1
.
          CALL      UNDC0000
.
          PACK      KEY20,STORURNO,SP70
          CALL      RSOBPC1
          CALL      RKOBPC1
          BRANCH    OVRCD,ULNKV800
.
          MATCH     OBPCURNO,STORURNO
          GOTO      ULNKV800 IF NOT EQUAL
.
          GOTO      ULNKV850
.
ULNKV800  PACK      KEY8,STORURNO           * validate UR
          CALL      RDMAST1
          BRANCH    OVRCD,ULNKV850
.
          MATCH     "1",PTMXSIN4
          IF        @EQUAL
            PACK      KEY8,STORURNO         * validate UR
            CALL      RLPTMAS1
            BRANCH    OVRCD,ULNKV850,ULNKV850
            MOVE      "0",PTMXSIN4
            CALL      UPMAST1
            CALL      UUPTMAS1
          ENDIF
.
ULNKV850  WRITE     HTMLFILE;"true";
.
          GOTO      ULNKV999
.
ULNKV900  WRITE     HTMLFILE;"false";
.
ULNKV999  RETURN
.
.-------------------------------------------------------------
. Waiting/Active referral list by U/R
.-------------------------------------------------------------
REFL0000  PACK      KEY16,URNUMBER,SP70
          CALL      RSALREF2
REFL1000  CALL      RKALREF2
          BRANCH    OVRCD,REFL9999
.
          MATCH     ALREURNO,URNUMBER
          GOTO      REFL9999 IF NOT EQUAL
.
          MATCH     "0",ALRESTAT
          IF        !@EQUAL
            MATCH     "1",ALRESTAT
            GOTO      REFL1000 IF NOT EQUAL
          ENDIF
.
          CALL      RFRW0000
.
          GOTO      REFL1000
.
REFL9999  RETURN
.-------------------------------------------------------------
. Referral list row
.-------------------------------------------------------------
RFRW0000  MOVE      SP70,DISPSTAT
.
          MATCH     "0",ALRESTAT
          IF        @EQUAL
            MOVE      "RF - Waiting",DISPSTAT
          ENDIF
.
          MATCH     "1",ALRESTAT
          IF        @EQUAL
            MOVE      "RF - Active",DISPSTAT
          ENDIF
.
          MOVE      SP70,UNITDESC
          MATCH     SP70,ALREDEPT
          IF        !@EQUAL
            PACK      KEY5,ANSC,ANSG,ALREDEPT,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      ALREDEPT,TDESC
            ENDIF
            MOVE      TDESC,UNITDESC
          ENDIF
.
          PACK      KEY10,ALREHCP,SP70
          CALL      RDPMHCP1
          IF        OVRCD=0
            MOVE      PMHCTITL,FMTTITLE
            MOVE      PMHCGNAM,FMTGNAME
            MOVE      PMHCSNAM,FMTSNAME
            CALL      DOCNM000
          ELSE
            MOVE    SP70,DOCFNAME
          ENDIF
.
          MOVE      SP70,DISPPROB
          PACK      KEY12,ALREDEPT,ALREPRO1,SP70
          CALL      RDALPRR1
          IF        OVRCD<>1
            MOVE      ALPRDESC,DISPPROB
          ENDIF
.
          MOVE      SP70,DISPPRTY
          MATCH     SP70,ALREPRTY
          IF        !@EQUAL
            PACK      KEY5,ANSP,ANSR,ALREPRTY,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      ALREPRTY,TDESC
            ENDIF
            MOVE      TDESC,DISPPRTY
          ENDIF
.
          MOVE      SP70,DISPDIAG
          PACK      KEY12,ALREDEPT,ALREDIA1,SP70
          CALL      RDALDIA1
          IF        OVRCD<>1
            MOVE      ALDIDESC,DISPDIAG
          ENDIF
.
          MOVE      SP70,DISPCOMP
          MATCH     SP70,ALRECOMP
          IF        !@EQUAL
            PACK      KEY5,ANSC,ANSL,ALRECOMP,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      ALRECOMP,TDESC
            ENDIF
            MOVE      TDESC,DISPCOMP
          ENDIF
.
          CLEAR     HTMLLINK
          APPEND    "javascript:linkVisit('",HTMLLINK
          APPEND    "REF",HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    ALREVISN,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    ALREURNO,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    SP1,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    ALREDEPT,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
          CLEAR     HTMLLNK2
          APPEND    "javascript:linkVisitUR('",HTMLLNK2
          APPEND    ALREVISN,HTMLLNK2
          APPEND    "','",HTMLLNK2
          APPEND    ALREURNO,HTMLLNK2
          APPEND    "','",HTMLLNK2
          APPEND    SP1,HTMLLNK2
          APPEND    "')",HTMLLNK2
          RESET     HTMLLNK2
.
          WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":
                             "#"",ALRERDAT,SP8,"#",":
                             "#"",DISPSTAT,"#",":
                             "#"",UNITDESC,"#",":
                             "#"",DOCFNAME,"#",":
                             "#"",DISPPROB,"#",":
                             "#"",DISPPRTY,"#",":
                             "#"",DISPDIAG,"#",":
                             "#"",DISPCOMP,"#",":
                             "#"",ALREVISN,"#",":
                             "#"",HTMLLNK2,"#"":
                             ")"
.
RFRW9999  RETURN
.
.-------------------------------------------------------------
. Unscheduled Waiting List
.-------------------------------------------------------------
WATL0000  PACK      KEY19,URNUMBER,SP70
          CALL      RDSTREA1
WATL1000  CALL      RDKTREA1
          BRANCH    OVRCD,WATL9999
.
          MATCH     WMURNO,URNUMBER
          GOTO      WATL9999 IF NOT EQUAL
.
          COMPARE   ONE,WMSTAT
          GOTO      WATL1000 IF NOT EQUAL
.
          CALL      WTRW0000
.
          GOTO      WATL1000
.
WATL9999  RETURN
.
.-------------------------------------------------------------
. Waiting list row
.-------------------------------------------------------------
WTRW0000  PACK      KEY19,WMURNO,WMCODE,WMCNT,SP70
          CALL      RDWTTX11 
          BRANCH    OVRCD,WTRW9999
.
          MOVE      SP70,DISPSTAT
.
          COMPARE   ONE,WMSTAT
          IF        @EQUAL
            MOVE      "WL - Unscheduled",DISPSTAT
          ENDIF
.
          MOVE      SP70,UNITDESC
          MATCH     SP70,WMUNIT
          IF        !@EQUAL
            PACK      KEY5,ANSW,ANSU,WMUNIT,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      WMUNIT,TDESC
            ENDIF
            MOVE      TDESC,UNITDESC
          ENDIF
.
          PACK      KEY6,WMDOCTOR,SP70
          CALL      RDDOCT1
          IF        OVRCD=0
            MOVE      DTITL,FMTTITLE
            MOVE      DGNAME,FMTGNAME
            MOVE      DSNAME,FMTSNAME
            CALL      DOCNM000
          ELSE
            MOVE    SP70,DOCFNAME
          ENDIF
.
          MOVE      SP70,DISPPROB
          PACK      KEY9,WMCODE,SP70
          CALL      RDPROA1
          IF        OVRCD<>1
            MOVE      WPDESC,DISPPROB
          ENDIF
.
          MOVE      SP70,DISPPRTY
          MATCH     SP70,WMPTY
          IF        !@EQUAL
            PACK      KEY5,ANST,ANSP,WMPTY,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      WMPTY,TDESC
            ENDIF
            MOVE      TDESC,DISPPRTY
          ENDIF
.
          MOVE      WMREASON,DISPDIAG
.
          MOVE      SP70,DISPCOMP
          MATCH     SP70,WTTXCTYP
          IF        !@EQUAL
            PACK      KEY5,ANSC,ANSL,WTTXCTYP,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      WTTXCTYP,TDESC
            ENDIF
            MOVE      TDESC,DISPCOMP
          ENDIF
.
          CLEAR     HTMLLINK
          APPEND    "javascript:linkVisit('",HTMLLINK
          APPEND    "WAT",HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    WMBOOK,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    WMURNO,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    WMURNO,HTMLLINK
          APPEND    WMCODE,HTMLLINK
          APPEND    WMCNT,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    SP1,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
          CLEAR     HTMLLNK2
          APPEND    "javascript:linkVisitUR('",HTMLLNK2
          APPEND    WMBOOK,HTMLLNK2
          APPEND    "','",HTMLLNK2
          APPEND    WMURNO,HTMLLNK2
          APPEND    "','",HTMLLNK2
          APPEND    WMURNO,HTMLLNK2
          APPEND    WMCODE,HTMLLNK2
          APPEND    WMCNT,HTMLLNK2
          APPEND    "')",HTMLLNK2
          RESET     HTMLLNK2
.
.
          WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":
                             "#"",WMDATE1,SP8,"#",":
                             "#"",DISPSTAT,"#",":
                             "#"",UNITDESC,"#",":
                             "#"",DOCFNAME,"#",":
                             "#"",DISPPROB,"#",":
                             "#"",DISPPRTY,"#",":
                             "#"",DISPDIAG,"#",":
                             "#"",DISPCOMP,"#",":
                             "#"",WMBOOK,"#",":
                             "#"",HTMLLNK2,"#"":
                             ")"
.
WTRW9999  RETURN
.
.-------------------------------------------------------------
. Booking List
.-------------------------------------------------------------
BOKL0000  PACK      KEY16,URNUMBER,SP70
          CALL      RSBKREC6
BOKL1000  CALL      RKBKREC6
          BRANCH    OVRCD,BOKL9999
.
          MATCH     BKREURNO,URNUMBER
          GOTO      BOKL9999 IF NOT EQUAL
.
          COMPARE   ZERO,BKRESTAT
          GOTO      BOKL1000 IF NOT EQUAL
.
          CALL      BKRW0000
.
          GOTO      BOKL1000
.
BOKL9999  RETURN
.
.-------------------------------------------------------------
. Booking list row
.-------------------------------------------------------------
BKRW0000  MOVE      SP70,DISPSTAT
.
          MOVE      "Booking",DISPSTAT
.
          MOVE      SP70,UNITDESC
          MATCH     SP70,BKREDEPT
          IF        !@EQUAL
            PACK      KEY5,ANSA,ANSC,BKREDEPT,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      BKREDEPT,TDESC
            ENDIF
            MOVE      TDESC,UNITDESC
          ENDIF
.
          PACK      KEY6,BKREADOC,SP70
          CALL      RDDOCT1
          IF        OVRCD=0
            MOVE      DTITL,FMTTITLE
            MOVE      DGNAME,FMTGNAME
            MOVE      DSNAME,FMTSNAME
            CALL      DOCNM000
          ELSE
            MOVE    SP70,DOCFNAME
          ENDIF
.
          MOVE      SP70,DISPPROB
.
          MOVE      SP70,DISPPRTY
.
          MOVE      SP70,DISPDIAG
          PACK      KEY18,BKREBOOK,SP70
          CALL      RSBKDIA1
          CALL      RKBKDIA1
          BRANCH    OVRCD,BKRW3000
.
          MATCH     BKREBOOK,BKDIBOOK
          GOTO      BKRW3000 IF NOT EQUAL
.
          MOVE      BKDIDES1,DISPDIAG
.
BKRW3000  MOVE      SP70,DISPCOMP
          MATCH     SP70,BKRECLMT
          IF        !@EQUAL
            PACK      KEY5,ANSC,ANSL,BKRECLMT,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      BKRECLMT,TDESC
            ENDIF
            MOVE      TDESC,DISPCOMP
          ENDIF
.
          CLEAR     HTMLLINK
          APPEND    "javascript:linkVisit('",HTMLLINK
.
          MOVE      SP70,KEY26              * Theatre case key
.
          PACK      KEY31,BKREBOOK,SP30
          CALL      RSOPDEA2
BKRW4000  CALL      RKOPDEA2                * get first booking
          BRANCH    OVRCD,BKRW5000          * no bookings
.
          MATCH     DBKREBOO,OPDAADMN
          GOTO      BKRW5000 IF NOT EQUAL   * no bookings for this visit
.
          COMPARE   THREE,OPDASTAT
          GOTO      BKRW4000 IF EQUAL
.
          PACK      KEY26,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN,OPDASTAT:
                    OPDACASE,SP70
.
BKRW5000  MATCH     "THE",BKRETYPE
          IF        @EQUAL
            APPEND    "THE",HTMLLINK
          ELSE
            APPEND    "BOK",HTMLLINK
          ENDIF
.
          APPEND    "','",HTMLLINK
          APPEND    BKREBOOK,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    BKREURNO,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    KEY26,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    SP1,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
          CLEAR     HTMLLNK2
          APPEND    "javascript:linkVisitUR('",HTMLLNK2
          APPEND    BKREBOOK,HTMLLNK2
          APPEND    "','",HTMLLNK2
          APPEND    BKREURNO,HTMLLNK2
          APPEND    "','",HTMLLNK2
          APPEND    SP1,HTMLLNK2
          APPEND    "')",HTMLLNK2
          RESET     HTMLLNK2

.
          WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":
                             "#"",BKREEDAT,SP8,"#",":
                             "#"",DISPSTAT,"#",":
                             "#"",UNITDESC,"#",":
                             "#"",DOCFNAME,"#",":
                             "#"",DISPPROB,"#",":
                             "#"",DISPPRTY,"#",":
                             "#"",DISPDIAG,"#",":
                             "#"",DISPCOMP,"#",":
                             "#"",BKREBOOK,"#",":
                             "#"",HTMLLNK2,"#"":
                             ")"
.
BKRW9999  RETURN
.
.-------------------------------------------------------------
. Pre admission/admission List
.-------------------------------------------------------------
PADL0000  PACK      KEY17,URNUMBER,SP70
          CALL      RSPTMI18
PADL1000  CALL      RKPTMI18 
          BRANCH    OVRCD,PADL9999
.
          MATCH     AURNO,URNUMBER
          GOTO      PADL9999 IF NOT EQUAL
.
          COMPARE   FIVE,ASTAT              * Skip cancalled IP visits
          GOTO      PADL1000 IF EQUAL
.
          CALL      PARW0000
.
          GOTO      PADL1000
.
PADL9999  RETURN
.
.-------------------------------------------------------------
. Document List for unprocessed eAdmissions
.-------------------------------------------------------------
.
.         Search for documents
.
DOCD0000  MOVE      ZERO,COUNT
          PACK      KEY24,CMRHEREF,SP70
          CALL      RSOBECO1
DOCD1000  CALL      RKOBECO1
          BRANCH    OVRCD,DOCD9999
.
          MATCH     OBECEAID,CMRHEREF
          GOTO      DOCD9999 IF NOT EQUAL
.
.         Found Document and loads details
.
          ADD       ONE,COUNT
          MOVE      OBECCPID,DIM4
          REP       " 0",DIM4
          PACK      KEY4,OBECSLID,SP70
          CALL      RDOBPT1
.
.         Processes document details
.
          STRIP     OBPTURLP
          MOVE      OBECEAID,DIM20
          REP       " 0",DIM20
          PACK      DOCREF,OBPTURLP,DIM20,DASH,DIM4,OBECFEXT
          STRIP     DOCREF
.
.         Processes date in human readable form
.
          UNPACK    OBECINDT,CCENT,CYEAR,DIM2,CDAY
          MOVE      DIM2,F2
          REP       " 0",CCENT
          REP       " 0",CYEAR
          REP       " 0",CDAY
.
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
.
          REP       " +",OBECEAID
          REP       " +",OBECCPID
          MOVE      COUNT,DIM3
          REP       " 0",DIM3
.
.         Displays document details as list
.
          WRITE     HTMLFILE;"<tr id=rowid",DIM3,">":
                             "<td><a href='",*+,DOCREF,*-,"' target=_open>":
                             "<img align=absmiddle hspace=4 ":
                             "src=#"../images/ClinicalNote.gif#" /></a>":
                             CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,"</td><td>":
                             TDESC,"<input type=hidden id=docid",DIM3:
                             " value=#"",OBECURNO,OBPCCVIS,OBPCCPID,"#" /></td>"
.
          REP      "+ ",OBECEAID
          REP      "+ ",OBECCPID
.
          UNPACK   OBECINDT,CCENT,CYEAR,DIM2,CDAY
          MOVE     DIM2,F2
          REP      " 0",CCENT
          REP      " 0",CYEAR
          REP      " 0",CDAY
          LOAD     MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
.
          GOTO     DOCD1000
.
DOCD9999  RETURN
.
.-------------------------------------------------------------
.         Document history list for processed eAdmissions
.-------------------------------------------------------------
.
.         Checks for documents attached to visit
.
DOCS0000  MOVE     ZERO,COUNT
          PACK     KEY20,URNUMBER,ADMISSNO,SP70
          CALL     RSOBPC1
DOCS1000  CALL     RKOBPC1
          BRANCH   OVRCD,DOCS9999
.
          MATCH    URNUMBER,OBPCURNO
          GOTO     DOCS9999 IF NOT EQUAL
.
          MATCH    ADMISSNO,OBPCCVIS
          GOTO     DOCS1000 IF NOT EQUAL
.
.         Gets Details of Document
.
DOCS2000  ADD      ONE,COUNT
          MOVE     OBPCCPID,DIM4
          REP      " 0",DIM4
          PACK     KEY4,OBPCSLID,SP70
          CALL     RDOBPT1
.
          STRIP    OBPTURLP
          MOVE     OBPCCVIS,DIM8
          REP      " 0",DIM8
          PACK     DOCREF,OBPTURLP,DIM8,DASH,DIM4,OBPCFEXT
.
.         Sets the date in readable form
. 
          STRIP    DOCREF
          UNPACK   OBPCINDT,CCENT,CYEAR,DIM2,CDAY
          MOVE     DIM2,F2
          REP      " 0",CCENT
          REP      " 0",CYEAR
          REP      " 0",CDAY
.
          LOAD     MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
.
          REP      " +",OBPCURNO
          REP      " +",OBPCCVIS
          REP      " +",OBPCCPID
          MOVE     COUNT,DIM3
          REP      " 0",DIM3
.
.         Places details of document to front end list
.
          WRITE    HTMLFILE;"<tr id=rowid",DIM3,">":
                            "<td><a href='",*+,DOCREF,*-,"' target=_open>":
                            "<img align=absmiddle hspace=4 ":
                            "src=#"../images/ClinicalNote.gif#" /></a>":
                            CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,"</td>":
                            "<td><a href=#"javascript:ViewDoc('cliweb08.pbl?":
                            "reportno=01&template=003&detailky=":
                            OBPCURNO,OBPCCVIS,OBPCCPID,"');#"":
                            "><img align=absmiddle hspace=4":
                          " src=#"../images/historyrecieved1.gif#"></a>",TDESC:
                            "<input type=hidden id=docid",DIM3:
                            " value=#"",OBPCURNO,OBPCCVIS,OBPCCPID,"#" /></td>"
.
          REP      "+ ",OBPCURNO
          REP      "+ ",OBPCCVIS
          REP      "+ ",OBPCCPID
.
.         Sets print options for the document
.
          PACK     KEY10,OBPCINUS,SP70
          CALL     RDWBSE1
          IF       OVRCD<>1
            WRITE    HTMLFILE;"<td>",WBSENAM,"</td>":
                              "<td><select onchange='changePrinter(this);' ":
                              "id='prtid",DIM3,"'>"
          ELSE
            WRITE    HTMLFILE;"<td>&nbsp;</td>":
                              "<td><select onchange='changePrinter(this);' ":
                              "id='prtid",DIM3,"'>"
          ENDIF
          CALL       SELPRT00
          WRITE      HTMLFILE;"</select></td>"
.
.         Checkbox for assigning document to print queue
.
          WRITE      HTMLFILE;"<td><input type='checkbox' ":
                              "onclick=#"if (this.checked) { add(this); }":
                              " else { remove(this); }#"  ":
                              "id='prchk",DIM3,"' value=#"0#" /></td></tr>"
.
          GOTO       DOCS1000
.
DOCS9999  RETURN
.
.-------------------------------------------------------------
. Pre admission/admission list row
.-------------------------------------------------------------
PARW0000  MOVE      SP70,DISPSTAT
.
          IF        ASTAT=1
            MOVE      "Pre Admission",DISPSTAT
          ENDIF
.
          IF        ASTAT=2
            MOVE      "Current Admission",DISPSTAT
          ENDIF
.
          IF        ASTAT=3
            MOVE      "Discharged",DISPSTAT
          ENDIF
.
          IF        ASTAT=4
            MOVE      "On Leave",DISPSTAT
          ENDIF
.
          MOVE      SP70,UNITDESC
          MATCH     SP70,ACLSSFT
          IF        !@EQUAL
            PACK      KEY5,ANSA,ANSC,ACLSSFT,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      ACLSSFT,TDESC
            ENDIF
            MOVE      TDESC,UNITDESC
          ENDIF
.
          PACK      KEY6,ADOCTA,SP70
          CALL      RDDOCT1
          IF        OVRCD=0
            MOVE      DTITL,FMTTITLE
            MOVE      DGNAME,FMTGNAME
            MOVE      DSNAME,FMTSNAME
            CALL      DOCNM000
          ELSE
            MOVE    SP70,DOCFNAME
          ENDIF
.
          MOVE      SP70,DISPPROB
.
          MOVE      SP70,DISPPRTY
.
          MOVE      ADIAG1,DISPDIAG
.
          MOVE      SP70,DISPCOMP
          MATCH     SP70,ACLAIM
          IF        !@EQUAL
            PACK      KEY5,ANSC,ANSL,ACLAIM,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      ACLAIM,TDESC
            ENDIF
            MOVE      TDESC,DISPCOMP
          ENDIF
.
          CLEAR     HTMLLINK
          APPEND    "javascript:linkVisit('",HTMLLINK
          APPEND    "IP",HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    AADMNO,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    AURNO,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    SP1,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    SP1,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
          CLEAR     HTMLLNK2
          APPEND    "javascript:linkVisitUR('",HTMLLNK2
          APPEND    AADMNO,HTMLLNK2
          APPEND    "','",HTMLLNK2
          APPEND    AURNO,HTMLLNK2
          APPEND    "','",HTMLLNK2
          APPEND    SP1,HTMLLNK2
          APPEND    "')",HTMLLNK2
          RESET     HTMLLNK2
.
          WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":
                             "#"",ADATE,ATIME,"#",":
                             "#"",DISPSTAT,"#",":
                             "#"",UNITDESC,"#",":
                             "#"",DOCFNAME,"#",":
                             "#"",DISPPROB,"#",":
                             "#"",DISPPRTY,"#",":
                             "#"",DISPDIAG,"#",":
                             "#"",DISPCOMP,"#",":
                             "#"",AADMNO,"#",":
                             "#"",HTMLLNK2,"#"":
                             ")"
.
PARW9999  RETURN
.
.------------------------------------------------------------
. Update Visit Number/WL procedure on comerhaf.cmrhvisn
.------------------------------------------------------------
VISIT000  PACK      STORURNO,URNUMBER,SP70
          PACK      STORVISN,ADMISSNO,SP70
          PACK      STORCASK,CASEKEYS,SP70
.
          REP       "+ ",EREFRLID
          PACK      KEY20,EREFRLID,SP70
          CALL      RDCMERH1
          BRANCH    OVRCD,VISIT900
.
          MATCH     CMRHURNO,STORURNO
          GOTO      VISIT910 IF NOT EQUAL
.
          MOVE      ZERO,F1
.
          MATCH     SP70,CASEKEYS
          IF        !@EQUAL
            MOVE      " 9",CMRHSTAT         * Added as W/L
            GOTO    VISIT300
          ENDIF
          
          PACK      KEY8,ADMISSNO,SP70
          CALL      RDBKREC1
          BRANCH    OVRCD,VISIT200
.
          MOVE      " 7",CMRHSTAT           * Added as Booked
          PACK      CASEKEYS,BKREURNO,BKREPROC,BKRECNT,SP70
.
          GOTO      VISIT300 
.
VISIT200  MOVE      " 8",CMRHSTAT         * Added as Referral
.
VISIT300  MOVE      CMRHSTAT,KEY2
          MOVE      ADMISSNO,KEY8
          MOVE      CASEKEYS,KEY19
          PROC      UPEREFVS               * Update eReferral Details
.
VISIT900  WRITE     HTMLFILE;"false";
          GOTO      VISIT999
.
VISIT910  DISPLAY   "*** UR Numbers are not same ***"
.
VISIT999  RETURN
.
.------------------------------------------------------------
. Validate allied health department
.------------------------------------------------------------
VALDP000  PACK     KEY3,DEPTCODE,SP70
          CALL     RDALDEP1            * Read department
          BRANCH   OVRCD,VALDP900
.
          PACK     KEY5,ANSC,ANSG,DEPTCODE,SP70
          CALL     RDCODE1
          BRANCH   OVRCD,VALDP900
.
          MATCH    ANSI,TCINDC3        * IP Department
          GOTO     VALDP900 IF EQUAL
.
          MATCH    "0",ALDEREFE        * Using referrals
          GOTO     VALDP9000 IF EQUAL
.
          WRITE    HTMLFILE;"true";    * Valid referral department
          GOTO     VALDP999
.
VALDP900  WRITE    HTMLFILE;"false;"   * Invalid referral department
          GOTO     VALDP999
.
VALDP999  RETURN
.
.------------------------------------------------------------
.
          INC       IBAWEBCD
          INC       WEBDATCD
.
          INC       ALLDEPIO/INC
          INC       ALLDIAIO/INC
          INC       ALLPRRIO/INC
          INC       ALLREFIO/INC
          INC       BOKDIAIO/INC
          INC       BOKRC1IO/INC
          INC       COMERDIO/INC
          INC       COMERHIO/INC
          INC       OBSECOIO/INC
          INC       OBSPCTIO/INC
          INC       OBSPCOIO/INC
          INC       PATDO1IO/INC   * Doctor File
          INC       PATMI1IO/INC
          INC       PATMA1IO/INC
          INC       PATVISIO/INC   * Patients Visits File
          INC       PMSVX1IO/INC   * Visit Extension File
          INC       PATALRIO/INC
          INC       OUTSITIO/INC
          INC       OPRDEAIO/INC
          INC       PATCOMIO/INC
          INC       PATPR1IO/INC
          INC       PMSPX2IO/INC
          INC       PATFN1IO/INC
          INC       PATWR1IO/INC
          INC       MLTSECIO/INC
          INC       MRTMASIO/INC
          INC       NHIMASIO/INC
          INC       NHIETHIO/INC
          INC       PATRE1IO/INC
          INC       PATDGWIO/INC
          INC       PATCMNIO/INC
          INC       PATITMIO/INC
          INC       PATDADIO/INC
          INC       PATVADIO/INC
          INC       PATCMCIO/INC
          INC       PATASFIO/INC
          INC       PMSHCGIO/INC
          INC       PMSHCPIO/INC
          INC       PMSUCHIO/INC
          INC       PMSCCDIO/INC            * Concession Card Details
.
          INC       PATHSPIO/INC
          INC       PMSUCDIO/INC
          INC       APSMASIO/INC
          INC       PATIN1IO/INC
          INC       PATDSCIO/INC
          INC       VISMDTIO/INC
          INC       VISMTXIO/INC
          INC       WATPROIO/INC
          INC       WATTR1IO/INC
          INC       WATTX1IO/INC
.
          INC       CLNHIMAS                   * Clear nhimasaf file variables
          INC       CLPMSPX2
          INC       PATMASTM
          INC       PATDOCTM
          INC       PATNAMCD 
          INC       PMSHCGTM
          INC       PMSHCPTM
          INC       PMSPX2TM
          INC       PATMSXTM
          INC       PATMISTM
          INC       PATDOCCD
          INC       CALCAGE
          INC       NAMSTRCD
          INC       DAYOFWEK
          INC       UPEREFVS


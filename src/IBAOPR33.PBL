.***************************************************************************
.* System    :   Theatre                                                   *
.* Program   :   IBAOPR33.PBL                                              *
.* Desc      :   Theatre Recovery Closed Report                            *
.***************************************************************************
.* Date      :   25/05/2016                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   This program will print the theatre recovery closed report*
.* Modifications  :                                                        *
.* ------------------------------------------------------------------------*
.* V11.03.02 19/04/2023  Thanh T        TSK 0909393                        *
.*           Changed LOAD1000,PROC1000 to use RDOPSES1 instead of RDOPSES7.*
.*           Changed LOAD0000 to use RSOPDEA6 instead of RSOPDEA1.         *   
.* V11.03.01 20/03/2023  PJ Le Febour   TSK 0909393                        *
.*           amended KEY19 to KEY22 for theatre index changes              *
.* V11.01.01 27/04/2021  Thanh T        TSK 0895165                        *
.*           Recompiled for OPRARDFD changes                               *
.* ------------------------------------------------------------------------*
.* V10.12.01 05/05/2018  Ken Bell      TSK 0851092                         *
.*           Add Hospital Code for Recovery Closed Table if Multi-Hospital *
.* ------------------------------------------------------------------------*
.* V10.08.05 29/07/2016  Ebon Clements TSK 0308881                         *
.*           Only test recovery review if patient in recovery - REVW0000   *
.* V10.08.04 15/07/2016  Ebon Clements TSK 0308881                         *
.*           Added recovery bay total                                      *
.* V10.08.03 14/07/2016  Ebon Clements TSK 0308881                         *
.*           Fixed report totals                                           *
.* V10.08.02 28/06/2016  Ebon Clements TSK 0308881                         *
.*           Fixed print of heading closure reason                         *
.* V10.08.01 21/06/2016  Ebon Clements TSK 0308881                         *
.*           Fixed print of heading if no locations are closed             *
.* V10.08.00 25/05/2016  Ebon Clements TSK 0308881                         *
.*           Created program                                               *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       IBASEQFD/INC            * Sequential Numbers File
          INC       TFILEVAR/INC            * Generate Temp File Name
          INC       WEBERRFD/INC            * Web Server Error Logging
          INC       BOKRC1FD/INC
          INC       BOKRX1FD/INC
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PATMA1FD/INC 
          INC       PATMI1FD/INC
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
          INC       OPRARDFD/INC
          INC       OPRCLIFD/INC
          INC       OPRCONFD/INC
          INC       OPRDEAFD/INC
          INC       OPROPRFD/INC
          INC       OPRRCIFD/INC
          INC       OPRSESFD/INC
          INC       OPRSRGFD/INC
          INC       WEBCONFD/INC
          INC       PATHSPFD/INC
.
.         Temporary File Definition
.         -------------------------
.
.         Filename : xxxtmpxx.dat          (xx = port id)
.
OPRTEMP1  IFILE SQL, FIXED=14, KEY="UC1-3,4,13"
.
.NAME     TYPE    LENGTH   PHYSICAL   START   DESCRIPTION
.----     ----    ------   --------   -----   -----------
XXXXLOCN  DIM       3        3          1     Surgery location cat YD
XXXXUNIQ  DIM       10       10         4     Unique id for theatre booking
.
.End of Record                          14
.
.         File Variables
.
ERASE     INIT      "iserase "
ISBUILD   INIT      "isbuild "
TEMPFILE  DIM       8
UKEY      INIT      " 14 UC1-3,4,13"   * set up for record length & relevant key
.
. LOCAL VARIABLE DEFINITION
. -------------------------
ACUITYDS  DIM       20
ARRVDELY  DIM       20
CALCTIME  FORM      6
CATEGORY  DIM       2
CATCR     INIT      "Cr"
CATOS     INIT      "Os"
CATOU     INIT      "Ou"
CATOC     INIT      "oc"
CATOD     INIT      "od"
CATYDDES  DIM       20  
CDYSFDTE  DIM       8
CDYSDAYS  FORM      6
CDYSTDTE  DIM       8
CDYSYEAR  FORM      2
CODE      DIM       2
CLINCODE  DIM       6
CLOSDATE  DIM       10
CLOSTIME  DIM       8
CMDLINE   DIM       227
CURRDATE  DIM       8
CURSTATS  DIM       21
CURRLOCN  DIM       3
DEPTDELY  DIM       20
DIM3A     DIM       3
DIM15     DIM       15
DIM32     DIM       32
DISPNAME  DIM       60
DURATIME  FORM      8
DOCFNAME  DIM       50
DOWNCASE  INIT      "AaBbCcDdEeFfGgHhIiJjKkLlMmNnOoPpQqRrSsTtUuVvWwXxYyZz"
EXPORTFL  FORM      1
FIRSTDAT  DIM       8
FIRSTIME  DIM       8
FORM3     FORM      3
LASTDATE  DIM       8
LASTTIME  DIM       8
FMTGNAME  DIM       35
FMTTITLE  DIM       10
FMTSNAME  DIM       35
FIRSTHR   DIM       2
FIRSTMIN  DIM       2
LASTHOUR  DIM       2
LASTMIN   DIM       2
HOUR      DIM       2
HOUR1     FORM      6
HOUR2     FORM      6
MINUTE1   FORM      3
MINUTE2   FORM      3
KIHOSPID  DIM       3
KIHSNAME  DIM       35
IBCNMHOS  FORM      1
LOCNDESC  DIM       20
MIN       DIM       2
NAME35    DIM       35
NORECORD  FORM      1
PATSNAME  DIM       25
RECOVBAY  DIM       20
REVWDATE  DIM       10
REVWREQD  DIM       3
REVWTIME  DIM       8
CRESDESC  DIM       20
STAPRFIX  INIT      "RIP - "
SURFNAME  DIM       40
SURGCODE  DIM       6
THETDESC  DIM       25
TOTLFLAG  FORM      1
TOTLHOLD  FORM      3
TOTLANAE  FORM      3
TOTLTHEA  FORM      3
TOTLRECF  FORM      3
TOTLRECB  FORM      3
TOTLRECD  FORM      3
TOTLPACU  FORM      3
.
WORKDATE  DATE      8
WORKTIME  DIM       8
.
PRGID     INIT      "IBAOPR33"
PRGNAM    INIT      "Theatre Recovery Closed Report"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000       * proceed with clean up
.
MAIN1000  CALL      CRTX0000               * Create Excel export file
          BRANCH    EXIT,MAIN0100          
.
          CALL      HQST0000               * Hospital for Multi-Hosp
          BRANCH    EXIT,MAIN0100          * cancel
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN4000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN4000  CALL      CREA0000               * create temp file
          CALL      LOAD0000               * load the temp file
          CALL      PROC0000               * process report
          CALL      KILL0000               * remove temp file
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      BOKRC1A1,"bokrc1af"
          OPEN      BOKRX1A1,"bokrx1af"
          OPEN      OPRDETA1,"oprdetaf"
          OPEN      OPRDETA6,"oprdetaf"
          OPEN      OPRDETA3,"oprdetaf"
          OPEN      OPRARDA1,"oprardaf"
          OPEN      OPRCLIA1,"oprcliaf"
          OPEN      OPRSESA1,"oprsesaf"
          OPEN      OPRSESA7,"oprsesaf"
          OPEN      OPRSRGA1,"oprsrgaf"
          OPEN      OPROPRA1,"opropraf"
          OPEN      OPRRCIA1,"oprrciaf"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATHSPA1,"pathspaf"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af" 
          OPEN      PMSPX2A1,"pmspx2af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,FORTY;*111,OPCNCLSU
          READ      CONTROLF,SIXTY8;*1,OPCNDPR1,OPCNDPR2,OPCNDPR3,OPCNDPR4:
                             OPCNDPR5
          READ      CONTROLF,HUND13;*38,OPCNCRRM
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,TEMPFILE
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.****************************************************************************
.*                                CRTX0000             Called by: ML2000    *
.* Create export file for excel                                             *
.****************************************************************************
CRTX0000  MOVE      ZERO,EXPORTFL
.
          DISPLAY   *P1:9,"Create Export file for Excel Y/N : "
          KEYIN     *P36:9,*V2LON,*RV,DIM1:
                    *P36:9,*DV,DIM1
CRTX1000  REP       UPPLOW,DIM1
          DISPLAY   *P36:9,*V2LON,DIM1
.
          MATCH     "Y",DIM1
          IF        @EQUAL
            MOVE      ONE,EXPORTFL
            GOTO      CRTX9000
          ENDIF
.
          MATCH     "N",DIM1
          GOTO      CRTX9000 IF EQUAL
.
          GOTO      CRTX9100
.
CRTX9000  MOVE      ZERO,EXIT
          GOTO      CRTX9999
.
CRTX9100  MOVE      ONE,EXIT
          GOTO      CRTX9999
.
CRTX9999  RETURN
+
.***************************************************************************
.*                               HQST0000              Called by ML2000    *
.* Query prompt for hospital Id (only for Multi Hospital clients           *
.***************************************************************************
HQST0000  MOVE      FALSE,EXIT
.
          UNPACK    SP30,KIHOSPID,KIHSNAME
          IF        IBCNMHOS = 1
            MOVE      "11",CVERT
            MOVE      "18",CCOL
            CALL      KYHOSPID                * keyin Hospital Id.
            BRANCH    EXIT,HQST9500
.
            PACK      KIHOSPID,PTHSHOSP,SP10  * Hospital Id.
            PACK      KIHSNAME,PTHSNAME,SP20  * Hospital Name
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      HQST9999
.
HQST9500  MOVE      ONE,EXIT
.
HQST9999  RETURN
+
.**************************************************************************
.*                               LOAD0000              Called by: MAIN0000*
.*                                                                        *
.**************************************************************************
LOAD0000  MOVE      ZERO,TOTLHOLD
          MOVE      ZERO,TOTLANAE
          MOVE      ZERO,TOTLTHEA
          MOVE      ZERO,TOTLRECF
          MOVE      ZERO,TOTLRECB
          MOVE      ZERO,TOTLRECD
          MOVE      ZERO,TOTLPACU
.
          CALL      IBACLOCK                     * load current date
.
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          PACK      KEY26,CURRDATE,SP70
          CALL      RSOPDEA6
LOAD1000  CALL      RKOPDEA6
          BRANCH    OVRCD,LOAD9999
.
          MATCH     OPDADATE,CURRDATE
          GOTO      LOAD9999 IF LESS
.
          PACK      KEY22,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN,OPDASTAT:
                          OPDACASE,SP70
          CALL      RDOPSES1
          BRANCH    OVRCD,LOAD1000
.
          COMPARE   THREE,OPDASTAT
          GOTO      LOAD1000 IF EQUAL
.
          PACK      KEY22,OPSEDGSI,Z70      * Recovery closed for this location
          CALL      RSOPRCI1
LOAD1100  CALL      RPOPRCI1
          BRANCH    OVRCD,LOAD1000
.
          MATCH     OPSEDGSI,OPRCLOCN          * Correct location
          GOTO      LOAD1000 IF NOT EQUAL
.
          IF        IBCNMHOS=1
            MATCH     KIHOSPID,OPRCHOSP
            GOTO      LOAD1100 IF NOT EQUAL
          ELSE
            MATCH     SP70,OPRCHOSP
            GOTO      LOAD1100 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,OPRCEDAT              * Is there an end date
          GOTO      LOAD1000 IF NOT EQUAL      * Skip if location not closed 
.
          PACK      KEY10,OPDAUNIQ,SP70
          CALL      RDOPARD1
          BRANCH    OVRCD,LOAD1000
.
          MATCH     SP70,OPARTETC           * Exit theatre complex time
          GOTO      LOAD1000 IF NOT EQUAL
.
          PACK      KEY11,OPDAUNIQ,SP70
          CALL      RSOPSRG1
          CALL      RKOPSRG1
          BRANCH    OVRCD,LOAD2000
.
          MATCH     OPSGUNID,OPDAUNIQ
          GOTO      LOAD2000 IF NOT EQUAL
.
          MATCH     "1",OPSGUY05
          GOTO      LOAD5000 IF EQUAL
.
          MATCH     SP70,OPSGTIDR
          GOTO      LOAD5000 IF NOT EQUAL
.
          GOTO      LOAD3000
.
LOAD2000  CALL      CLOPRSRG
.
LOAD3000  MOVE      ZERO,TOTLFLAG           * Count totals
          CALL      PATSTS00                * Get status and totals for people
.                                           * not printed on the report
          GOTO      LOAD1000
.
LOAD5000  MOVE      ZERO,TOTLFLAG           * Count totals
          CALL      PATSTS00                * Get status and totals for people
.                                           * printed on the report
          MOVE      OPSEDGSI,XXXXLOCN       * location
          MOVE      OPDAUNIQ,XXXXUNIQ       * unique id
.
          PACK      KEY13,XXXXLOCN,XXXXUNIQ,SP70
          CALL      RDTEMP1 
          IF        OVRCD=1
            CALL      WRTEMP1               * Write booking to temp file
          ENDIF
.
          GOTO      LOAD1000
.
LOAD9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: MAIN0000*
.*                                                                        *
.**************************************************************************
PROC0000  MOVE      ZERO,CPAGENO
          MOVE      SP70,CURRLOCN
          MOVE      ONE,NORECORD
.
          PACK      KEY5,ANSY,ANSD,SP70
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE      "Location",TDESC
          ENDIF
          MOVE      TDESC,CATYDDES
          STRIP     CATYDDES
.
          CALL      IBACLOCK                     * load current date
.
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          IF        EXPORTFL=1
            PRINT    *1,"<table border=1>"
          ENDIF
.
          PACK      KEY13,SP70
          CALL      RSTEMP1
PROC1000  CALL      RKTEMP1                      * Loop temp file and print
          BRANCH    OVRCD,PROC9000
.
          MOVE      ZERO,NORECORD                  * records printed
.
          MATCH     XXXXLOCN,CURRLOCN
          IF        !@EQUAL
            MOVE      XXXXLOCN,CURRLOCN
            CALL      HEAD0000                     * print heading
          ENDIF
.
          PACK      KEY10,XXXXUNIQ,SP70
          CALL      RDOPDEA3
          BRANCH    OVRCD,PROC1000
.
          COMPARE   THREE,OPDASTAT
          GOTO      PROC1000 IF EQUAL
.
          PACK      KEY10,OPDAUNIQ,SP70
          CALL      RDOPARD1
          BRANCH    OVRCD,PROC1000
.
          MOVE      ONE,TOTLFLAG
          CALL      PATSTS00                * Get status and totals
.
          PACK      KEY22,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN,OPDASTAT:
                          OPDACASE,SP70
          CALL      RDOPSES1
          BRANCH    OVRCD,PROC1000
.
          PACK      KEY11,OPDAUNIQ,SP70
          CALL      RSOPSRG1
          CALL      RKOPSRG1
          BRANCH    OVRCD,PROC1000
.
          MATCH     OPSGUNID,OPDAUNIQ
          GOTO      PROC1000 IF NOT EQUAL
.
          MATCH     "1",OPSGUY05
          GOTO      PROC2000 IF EQUAL
.
          MATCH     SP70,OPSGTIDR
          GOTO      PROC2000 IF NOT EQUAL
.
          GOTO      PROC1000
.
PROC2000  MATCH     SP70,OPARTETC           * Exit theatre complex time
          GOTO      PROC1000 IF NOT EQUAL
.
          MATCH     SP70,OPDATHET
          GOTO      PROC3000 IF EQUAL
.
          PACK      KEY6,OPDATHET,SP70
          CALL      RDOPOPR1
          IF        OVRCD=1
            MOVE      "INVALID",OPRMDESC
          ENDIF
          MOVE       OPRMDESC,THETDESC
.
PROC3000  PACK      KEY8,OPDAADMN,SP70
          CALL      RDBKREC1
          BRANCH    OVRCD,PROC1000
.
          PACK      KEY8,OPDAADMN,SP70
          CALL      RDBKRX11
          BRANCH    OVRCD,PROC1000
.
          PACK      KEY8,OPDAADMN,SP70
          CALL      RDMISS1
          BRANCH    OVRCD,PROC1000
.
          PACK      KEY8,BKREURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,PROC1000
.
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          BRANCH    OVRCD,PROC1000
.
          MOVE      TWO,PACFRMT
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          CALL      PACNAME
          MOVE      PACFNAME,PATSNAME
.
          MOVE      SP70,ARRVDELY
          MATCH     SP70,OPARRLAT
          IF        !@EQUAL
            PACK      KEY5,CATOS,OPARRLAT,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              MOVE     TDESC,ARRVDELY
            ENDIF
          ENDIF
.
          MOVE      SP70,DEPTDELY
          MATCH     SP70,OPARRDCD
          IF        !@EQUAL
            PACK      KEY5,CATOU,OPARRDCD,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              MOVE     TDESC,DEPTDELY
            ENDIF
          ENDIF
.
          MOVE      SP70,RECOVBAY
          MATCH     SP70,OPARUC03
          IF        !@EQUAL
            PACK      KEY5,CATOC,OPARUC03,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              MOVE     TDESC,RECOVBAY
            ENDIF
          ENDIF
.
          MOVE      SP70,ACUITYDS
          MATCH     SP70,OPARUC04
          IF        !@EQUAL
            PACK      KEY5,CATOD,OPARUC04,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              MOVE     TDESC,ACUITYDS
            ENDIF
          ENDIF
.
          MOVE      SP70,CLOSDATE
          MOVE      SP70,CLOSTIME
          MATCH     SP70,OPARUT02
          IF        !@EQUAL
            UNPACK    OPARUT02,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            MOVE      CPCDATE,REVWDATE           * formatted review date
            MOVE      OPARUT03,REVWTIME          * review time
          ENDIF
.
          CALL      SESDET00                * Get session details
.
          CALL      REVW0000                * Check if recovery review required
.
          CALL      PRNT0000                * Print patint line
.
          GOTO      PROC1000
.
PROC9000  IF        NORECORD=1
            CALL      HEAD0000                     * print heading
          ENDIF
.
          CALL      ENDD0000                * Print end of report
.
          IF        EXPORTFL=1
            PRINT    *1,"</table>"
          ENDIF
.
PROC9999  RETURN
.
.******************************************************************************
. Check if recovery review required and return class name for cell highlight
.******************************************************************************
REVW0000  MOVE      SP70,REVWREQD
.
          PACK      DIM32,OPARTETC,OPARTICU,OPARTEXP,OPARTIDP,SP70
.
          MATCH     SP70,DIM32                   * Has patient left theatre
          GOTO      REVW9999 IF NOT EQUAL
.
          MATCH     SP70,OPARTIRF                * Exit if not in recovery
          GOTO      REVW9999 IF EQUAL
.
          MOVE      OPSEDATE,WORKDATE            * Date into recovery
          MATCH     "1",OPSGUY02
          IF        @EQUAL
            ADD       ONE,WORKDATE               * Add 1 day if overnight
          ENDIF
          MOVE      OPARTIRF,WORKTIME            * Time into recovery
.
          MATCH     SP70,OPARUT02
          IF        !@EQUAL
            MOVE      OPARUT02,WORKDATE          * User last review date
            MOVE      OPARUT03,WORKTIME
          ENDIF
.
          MOVE      WORKDATE,FIRSTDAT
          REP       " 0",FIRSTDAT
          UNPACK    WORKTIME,HOUR,D1,MIN
          PACK      FIRSTIME,HOUR,MIN
          REP       " 0",FIRSTIME
.
          CALL      IBACLOCK                    * Current date and time
          CLOCK     TIME,CTIMEIS
.
          PACK      LASTDATE,CCC,CYY,CMM,CDD
          REP       " 0",LASTDATE
          UNPACK    CTIMEIS,HOUR,D1,MIN
          PACK      LASTTIME,HOUR,MIN
          REP       " 0",LASTTIME
.
          CALL      TIMEDIFF
          MOVE      CALCTIME,DURATIME
.
          SQUEEZE   OPCNCRRM
          MOVE      ZERO,FORM3
          MOVE      OPCNCRRM,FORM3
          IF        DURATIME > FORM3 & FORM3 > ZERO
            MOVE      "Yes",REVWREQD
          ENDIF
.
REVW9999  RETURN
+
.**************************************************************************
.*                               PRNT0000              Called by: PROC0000*
.*                                                                        *
.**************************************************************************
PRNT0000  IF        EXPORTFL=1
            CALL      EXLR0000                   * html row
            GOTO      PRNT9999
          ENDIF
.
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          MOVE      PURNO,D8
          LJUSTIFY  D8
          PRINT     *1,PATSNAME,*27,D8,*36,THETDESC:
                    *62,SURFNAME,*104,OPDAEXPD
.
          PRINT     *1,"Arrival Delay   : ",ARRVDELY:
                    *72,"Arrival Time",*90,":",*93,OPARATIM:
                    *102,"Recovery Time (Front)",*123,":",*125,OPARTIRF
          ADD       ONE,CLNO
.
          PRINT     *1,"Departure Delay : ",DEPTDELY:
                    *72,"Anaesthetic Prep",*90,":",*93,OPARANAP:
                    *102,"Recovery Time (Back)",*123,":",*125,OPARTIRB
          ADD       ONE,CLNO
.
          PRINT     *1,"Status : ",CURSTATS:
                    *36,"Bay : ",RECOVBAY:
                    *72,"Anaesthetic Start",*90,":",*93,OPARANAS:
                    *102,"Recovery Time (D/P)",*123,":",*125,OPARTIRD
          ADD       ONE,CLNO
.
          PRINT     *1,"Acuity          : ",ACUITYDS:
                    *72,"Surgery Start",*90,":",*93,OPSGTISS:
                    *102,"Ready to Depart Time",*123,":",*125,OPARTIDP
          ADD       ONE,CLNO
.
          PRINT     *1,"Review Required : ",REVWREQD:
                    *72,"Surgery End",*90,":",*93,OPSGTISE:
                    *102,"Time Exit Theatre",*123,":",*125,OPARTETC
          ADD       ONE,CLNO
.
          PRINT     *1,"Clinical Review Date : ",REVWDATE,SP1,REVWTIME:
                    *72,"Time went to ICU",*90,":",*93,OPARTICU:
                    *102,"Time Patient Died",*123,":",*125,OPARTEXP
          ADD       ONE,CLNO
.
          CALL      UND132
.
PRNT9999  RETURN
+
.**************************************************************************
.*                               HEAD0000              Called by: PRNT0000*
.*                                                                        *
.*************************************************************************
HEAD0000  MOVE      SP70,LOCNDESC
.
          MATCH     SP70,CURRLOCN           * Current location
          GOTO      HEAD0500 IF EQUAL
.
          PACK      KEY5,ANSY,ANSD,CURRLOCN,SP70
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE      "Location",TDESC
          ENDIF
          MOVE      TDESC,LOCNDESC
.
HEAD0500  MOVE      SP70,CRESDESC
          MOVE      SP70,CLOSDATE
          MOVE      SP70,CLOSTIME
.
          PACK      KEY22,CURRLOCN,Z70      * Recovery closed for this location
          CALL      RSOPRCI1
HEAD0510  CALL      RPOPRCI1
          BRANCH    OVRCD,HEAD1000
.
          MATCH     OPSEDGSI,OPRCLOCN          * Correct location
          GOTO      HEAD1000 IF NOT EQUAL
.
          IF        IBCNMHOS=1
            MATCH     KIHOSPID,OPRCHOSP
            GOTO      HEAD0510 IF NOT EQUAL
          ELSE
            MATCH     SP70,OPRCHOSP
            GOTO      HEAD0510 IF NOT EQUAL
          ENDIF

          MATCH     SP70,OPRCEDAT              * Is there an end date
          GOTO      HEAD1000 IF NOT EQUAL      * Skip if location not closed
.
          MATCH     SP70,OPRCREAS              * Blank closure reason
          GOTO      HEAD1000 IF EQUAL
.
          UNPACK    OPRCSDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,CLOSDATE           * formatted close date
          MOVE      OPRCSTIM,CLOSTIME          * Close time
.
          PACK      KEY5,CATCR,OPRCREAS,SP70
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE      ACODE,TDESC              * Closure reason
          ENDIF
          MOVE      TDESC,CRESDESC
.
HEAD1000  IF        EXPORTFL=1
            CALL      EXLH0000                 * html heading
            GOTO      HEAD9999
          ENDIF
.
          CALL      HEAD132
          PRINT     *10,*+,CATYDDES," : ",LOCNDESC:
                    *64,"Closure Reason : ",CRESDESC:
                    " Closed at : ",CLOSDATE,SP1,CLOSTIME
.
          ADD       ONE,CLNO
          CALL      UND132
          PRINT     *1,"Patient",*27,"U/R",*36,"Theatre":
                    *62,"Surgeon",*104,"Expected Duration"
          ADD       ONE,CLNO
          CALL      UND132
.
HEAD9999  RETURN
+
.**************************************************************************
.*                               EXLH0000              Called by: PROC0000*
.*                                                                        *
.**************************************************************************
EXLH0000  PRINT     *1,"<tr><td colspan=2>",CATYDDES,"</td>":
                    "<td colspan=2 align=left>",LOCNDESC,"</td>":
                    "<td colspan=2>Closure Reason</td>":
                    "<td colspan=2 align=left>",CRESDESC,"</td>":
                    "<td colspan=2>Closed at</td>":
                    "<td colspan=12 align=left>",CLOSDATE,SP1,OPRCSTIM,"</td>":
                    "</td></tr>"
.
          PRINT     *1,"<tr><td>Patient</td>":
                    "<td>U/R</td>":
                    "<td>Theatre</td>":
                    "<td>Surgeon</td>":
                    "<td>Expected Duration</td>":
                    "<td>Status</td>":
                    "<td>Arrival Delay</td>":
                    "<td>Departure Delay</td>":
                    "<td>Arrival Time</td>":
                    "<td>Anaesthetic Prep</td>":
                    "<td>Anaesthetic Start</td>":
                    "<td>Surgery Start</td>"
.
          PRINT     *1,"<td>Surgery End</td>":
                    "<td>Time Went To ICU</td>":
                    "<td>Recovery Time (Front)</td>":
                    "<td>Recovery Time (Back)</td>":
                    "<td>Recovery Time (D/P)</td>":
                    "<td>Ready To Depart Time</td>":
                    "<td>Time Exit Theatre</td>":
                    "<td>Time Patient Died</td>"
.
          PRINT     *1,"<td>Acuity</td>":
                    "<td>Bay</td>":
                    "<td>Clinical Review Required</td>":
                    "<td>Clinical Review Date/Time</td></tr>" 
.
EXLH9999  RETURN
+
.**************************************************************************
.*                               EXLR0000              Called by: PROC0000*
.*                                                                        *
.**************************************************************************
EXLR0000  PRINT    *1,"<tr><td>",PATSNAME,"</td>":
                    "<td>",PURNO,"</td>":
                    "<td>",THETDESC,"</td>":
                    "<td>",SURFNAME,"</td>":
                    "<td>",OPDAEXPD,"</td>":
                    "<td>",CURSTATS,"</td>":
                    "<td>",ARRVDELY,"</td>":
                    "<td>",DEPTDELY,"</td>":
                    "<td>",OPARATIM,"</td>":
                    "<td>",OPARANAP,"</td>":
                    "<td>",OPARANAS,"</td>":
                    "<td>",OPSGTISS,"</td>"
.
          PRINT     *1,"<td>",OPSGTISE,"</td>":
                    "<td>",OPARTICU,"</td>":
                    "<td>",OPARTIRF,"</td>":
                    "<td>",OPARTIRB,"</td>":
                    "<td>",OPARTIRD,"</td>":
                    "<td>",OPARTIDP,"</td>":
                    "<td>",OPARTETC,"</td>":
                    "<td>",OPARTEXP,"</td>"
.
          PRINT     *1,"<td>",ACUITYDS,"</td>":
                    "<td>",RECOVBAY,"</td>":
                    "<td>",REVWREQD,"</td>":
                    "<td>",REVWDATE,SP1,REVWTIME,"</td>":
                    "</tr>"
.
EXLR9999  RETURN
+
.**************************************************************************
.*                               EXLT0000              Called by: PROC0000*
.*                                                                        *
.**************************************************************************
EXLT0000  PRINT     *1,"<tr>":
                    "<td colspan=2>Number of Patients in Holding</td>":
                    "<td>",TOTLHOLD,"</td></tr>"
.
          PRINT     *1,"<tr>":
                    "<td colspan=2>Number of Patients in Anaesthetic</td>":
                    "<td>",TOTLANAE,"</td></tr>"
.
          PRINT     *1,"<tr>":
                    "<td colspan=2>Number of Patients in Theatre</td>":
                    "<td>",TOTLTHEA,"</td></tr>"
.
          PRINT     *1,"<tr>":
                    "<td colspan=2>Number of Patients in Recovery Front</td>":
                    "<td>",TOTLRECF,"</td></tr>"

          PRINT     *1,"<tr>":
                    "<td colspan=2>Number of Patients in Recovery Back</td>":
                    "<td>",TOTLRECB,"</td></tr>"
.
          PRINT     *1,"<tr>":
                    "<td colspan=2>Number of Patients in Recovery Day</td>":
                    "<td>",TOTLRECD,"</td></tr>"
.
          PRINT     *1,"<tr>":
                    "<td colspan=2>Number of PACU Bays in Use</td>":
                    "<td>",TOTLPACU,"</td></tr>"
.
EXLT9999  RETURN
+
.**************************************************************************
. Get Details for Session                              Called B: PROC0000 *
.**************************************************************************
SESDET00  MOVE      OPSECLIN,CLINCODE
.
          COMPARE   ZERO,OPCNCLSU
          GOTO      SESDET10 IF NOT EQUAL
.
          PACK      KEY6,OPSECLIN,SP70
          CALL      RDOPCLI1
          IF        OVRCD=0
            MATCH     SP70,OPCLDOCT
            IF        @EQUAL | @EOS
              MOVE      OPCLDESC,SURFNAME
            ELSE
              PACK      KEY6,OPCLDOCT,SP70
              CALL      RDDOCT1
              IF        OVRCD=0
                MOVE      DTITL,FMTTITLE
                MOVE      DGNAME,FMTGNAME
                MOVE      DSNAME,FMTSNAME
                CALL      DOCNM000
                MOVE      DOCFNAME,SURFNAME
                MOVE      OPCLDOCT,SURGCODE
              ELSE
                MOVE      "&nbsp;",SURFNAME
              ENDIF
            ENDIF
          ELSE
            MOVE      "&nbsp;",SURFNAME
          ENDIF
          GOTO      SESDET99
.
SESDET10  MOVE      OPSECLIN,KEY6
          CALL      RDDOCT1
          IF        OVRCD=0
            MOVE      DTITL,FMTTITLE
            MOVE      DGNAME,FMTGNAME
            MOVE      DSNAME,FMTSNAME
            CALL      DOCNM000
            MOVE      DOCFNAME,SURFNAME
            MOVE      OPSECLIN,SURGCODE
          ELSE
            MOVE      "&nbsp;",SURFNAME
          ENDIF
.
SESDET99  RETURN
+
.**************************************************************************
. Current Status of Patient + totals                   Called By : PRNT0000
.**************************************************************************
PATSTS00  MOVE      SP70,CURSTATS
          COMPARE   THREE,OPDASTAT
          GOTO      PATSTS05 IF NOT EQUAL
.
          MOVE      "SC",CATEGORY
          MOVE      OPDACANC,CODE
          CALL      GETCOD00
          MATCH     ANST,TCINDC3
          IF        @EQUAL
            MOVE      "Transferred",CURSTATS
          ELSE
            MOVE      "Cancelled",CURSTATS
          ENDIF
          GOTO      PATSTS90
.
PATSTS05  MATCH     SP70,OPARATIM
          IF        !@EQUAL
            MOVE      "Holding",CURSTATS
          ENDIF
.
          MATCH     SP70,OPARANAS
          IF        !@EQUAL
            MOVE      "Anaes",CURSTATS
          ENDIF
.
          MATCH     SP70,OPSGTIPS
          IF        !@EQUAL
            MOVE      "Theatre",CURSTATS
          ENDIF
.
          MATCH     SP70,OPSGTISS
          IF        !@EQUAL
            MOVE      "Theatre",CURSTATS
          ENDIF
.
          MATCH     SP70,OPARTIRF
          IF        !@EQUAL
            MOVE      "Recovery-Front",CURSTATS
          ENDIF
.
          MATCH     SP70,OPARTIRB
          IF        !@EQUAL
            MOVE      "Recovery-Back",CURSTATS
          ENDIF
.
          MATCH     SP70,OPARTIRD
          IF        !@EQUAL
            MOVE      "Recovery-Day",CURSTATS
          ENDIF
.
          MATCH     SP70,OPARTIDP
          IF        !@EQUAL
            MOVE      "Ready To Depart",CURSTATS
          ENDIF
.
          MATCH     SP70,OPARTETC
          IF        !@EQUAL
            MOVE      "Exit",CURSTATS
          ENDIF
.
          MATCH     SP70,OPARTICU
          IF        !@EQUAL
            MOVE      "ICU",CURSTATS
          ENDIF
.
          MATCH     SP70,OPARTEXP
          IF        !@EQUAL
            MATCH     SP70,CURSTATS              * Is current status blank?
            IF        @EQUAL
              MOVE      "Deceased",CURSTATS      * Yes, set status to "Deceased"
            ELSE
              MOVE      CURSTATS,DIM15           * No, save current status
              PACK      CURSTATS,STAPRFIX,DIM15  * Build new status with prefix
            ENDIF
          ENDIF
.
PATSTS90  COMPARE   ONE,TOTLFLAG                 * Printing so don't re count
          GOTO      PATSTS99 IF EQUAL            * totals
.
          MATCH     "Holding",CURSTATS
          IF        @EQUAL
            ADD       ONE,TOTLHOLD
          ENDIF
.
          MATCH     "Anaes",CURSTATS
          IF        @EQUAL
            ADD       ONE,TOTLANAE
          ENDIF
.
          MATCH     "Theatre",CURSTATS
          IF        @EQUAL
            ADD       ONE,TOTLTHEA
          ENDIF
.
          MATCH     "Recovery-Front",CURSTATS
          IF        @EQUAL
            ADD       ONE,TOTLRECF
.
            MATCH     SP70,OPARUC03
            IF        !@EQUAL
              ADD       ONE,TOTLPACU
            ENDIF
          ENDIF
.
          MATCH     "Recovery-Back",CURSTATS
          IF        @EQUAL
            ADD       ONE,TOTLRECB
.
            MATCH     SP70,OPARUC03
            IF        !@EQUAL
              ADD       ONE,TOTLPACU
            ENDIF
          ENDIF
.
          MATCH     "Recovery-Day",CURSTATS
          IF        @EQUAL
            ADD       ONE,TOTLRECD
.
            MATCH     SP70,OPARUC03
            IF        !@EQUAL
              ADD       ONE,TOTLPACU
            ENDIF
          ENDIF
.
PATSTS99  RETURN
+
.**************************************************************************
.*                               ENDD0000              Called by: PROC0000*
.*                                                                        *
.**************************************************************************
ENDD0000  IF        EXPORTFL=1
            CALL      EXLT0000                   * html totals
            GOTO      PRNT9999
          ENDIF
.
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PRINT     *1,"Number of Patients in Holding        : ",TOTLHOLD:
                    *N,"Number of Patients in Anaesthetic    : ",TOTLANAE:
                    *N,"Number of Patients in Theatre        : ",TOTLTHEA:
                    *N,"Number of Patients in Recovery Front : ",TOTLRECF:
                    *N,"Number of Patients in Recovery Back  : ",TOTLRECB:
                    *N,"Number of Patients in Recovery Day   : ",TOTLRECD:
                    *N,"Number of Patients in PACU Bays      : ",TOTLPACU
.
          CALL      UND132
.
          PRINT     *N,"*** End of Report ***",*N
.
ENDD9999  RETURN
+
.**************************************************************************
. Get Code Description                                  Called By: PATSTS00
.**************************************************************************
GETCOD00  MOVE      SP70,TDESC
          MOVE      SP70,ACODE
          MOVE      SP70,THCSCOD
          MOVE      SP70,PTCDLDES
          MOVE      ZERO,TCFORM6
          UNPACK    SP70,TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5:
                         TCINDC6,TCINDC7,TCINDC8,TCINDC9,TCINDC10:
                         TCINDC11,TCINDC12,TCINDC13,TCINDC14,TCINDC15:
                         TCINDC16,TCINDC17,TCINDC18,TCINDC19,TCINDC20:
                         TCINDC21,PTCOACTV
          MATCH     SP70,CODE
          GOTO      GETCOD99 IF EQUAL
.
          PACK      KEY5,CATEGORY,CODE
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE      SP70,ACODE
            MOVE      SP70,TDESC
            MOVE      SP70,PTCDLDES
            UNPACK    SP70,TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5:
                           TCINDC6,TCINDC7,TCINDC8,TCINDC9,TCINDC10:
                           TCINDC11,TCINDC12,TCINDC13,TCINDC14,TCINDC15:
                           TCINDC16,TCINDC17,TCINDC18,TCINDC19,TCINDC20:
                           TCINDC21,PTCOACTV
          ENDIF
.
GETCOD99  RETURN
+
.**************************************************************************
.*                              CREA0000               Called by:         *
.*             create a new temporary file                                *
.**************************************************************************
.
CREA0000  CALL      KILL0000                     * remove existing file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,TEMPFILE,UKEY
          EXECUTE   CMDLINE,TASKID               * create temporary index file
.
          OPEN      OPRTEMP1,TEMPFILE            * open temp index file
.
CREA9999  RETURN
+
.****************************************************************************
.*                              KILL0000               Called by:           *
.*               close and erase the temporary file                         *
.****************************************************************************
.
KILL0000  CLOSE     OPRTEMP1                     * close file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ERASE,TEMPFILE       * set file erase command
          EXECUTE   CMDLINE,TASKID               * erase temp file
.
KILL9999 RETURN
+
.****************************************************************************
.*               Temp file IO routines                                      *
.****************************************************************************
RSTEMP1   READ      OPRTEMP1,KEY13;;
          RETURN
.
RDTEMP1   MOVE      ZERO,OVRCD
          READ      OPRTEMP1,KEY13;XXXXLOCN,XXXXUNIQ
          GOTO      OVERCOND IF OVER
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    OPRTEMP1;XXXXLOCN,XXXXUNIQ
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   WRITE     OPRTEMP1,KEY13;XXXXLOCN,XXXXUNIQ
          RETURN
.
+
.******************************************************************************
.*                                 KYHOSPID                                   *
.*                            Key in Hospital Id.                             *
.******************************************************************************
KYHOSPID  MOVE      ZERO,EXIT
          DISPLAY   *P1:CVERT,"Hospital Id.  : ";
KYHOS100  MOVE      SP6,DIM3A
          KEYIN     *PCCOL:CVERT,*V2LON,DIM3A;
.
.
          PACK      KEY3,DIM3A,SP10
KYHOS200  MATCH     SP3,KEY3
          GOTO      KYHOS300 IF EQUAL
.
          MATCH     QUEST,KEY3
          IF        @EQUAL
            CALL      GETSCR00
            DISPLAY   *P1:4,*EF,*P35:4,"Hospital Details";
            MOVE      "6",EVERT
            PACK      KEY3,SP70
            CALL      RSPTHSP1
KYHOS210    CALL      RKPTHSP1
            BRANCH    OVRCD,KYHOS290
            DISPLAY   *P1:EVERT,PTHSHOSP,SP4,PTHSNAME;
            ADD       ONE,EVERT
            IF        ECOL < 22
              GOTO      KYHOS210
            ENDIF
.
KYHOS290    DISPLAY   *P1:24,*EL,"Hospital ID : ",UNDLN3;
            KEYIN     *P15:24,*V2LON,DIM3A;
            PACK      KEY3,DIM3A,SP10
            CALL      PUTSCR00
          ENDIF
.
KYHOS300  UNPACK    SP70,PTHSHOSP,PTHSNAME,PTHSAPPR
          MOVE      "All Hospitals",PTHSNAME
          CALL      RDPTHSP1
          DISPLAY   *PCCOL:CVERT,*V2LON,KEY3,SP4,PTHSNAME
          GOTO      KYHOS999
.
KYHOS950  MOVE      ONE,EXIT
.
KYHOS999  RETURN
+
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       NAMSTRCD
          INC       PATDOCCD
          INC       TFILENAM                * Generate Temp File Name
          INC       TIMEDIFF
          INC       CALCDAYS
          INC       CLOPRSRG
.
          INC       BOKRC1IO/INC
          INC       BOKRX1IO/INC
          INC       IBASEQIO/INC
          INC       OPRARDIO/INC
          INC       OPRCLIIO/INC
          INC       OPRDEAIO/INC
          INC       OPROPRIO/INC
          INC       OPRRCIIO/INC
          INC       OPRSESIO/INC
          INC       OPRSRGIO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATHSPIO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
          INC       WEBERRIO/INC 

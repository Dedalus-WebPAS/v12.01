.***************************************************************************
.* System    :   Emergency                                                 *
.***************************************************************************
.* Program   :   IBAEMR26                                                  *
.* Desc      :   Detailed Emergency Revenue by Doctor                      *
.***************************************************************************
.* Date      :   01/10/2010                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   This program will print the detailed emergency revenue    *
.*           :   by doctor report                                          *
.* Modifications  :                                                        *
.***************************************************************************
.* V12.00.01 28/05/2025 Thanh T         TSK 0955096                        *
.*           Changed for alphanumeric visit number                         *
.***************************************************************************
.*        V10.07.01 05/01/2016 Ebon Clements  TSK 0808871                  *
.*                  Changed procedures (ATRECTYP=5) to test                *
.*                  cat PG ATMISGRP ind 7 = "D" to include in revenue      *
.*                  Previously all procedures where included               *
.*        V10.03.04 18/02/2013 Ania P         CAR 281021                   *
.*                  Removed redundant modification of constants.           *
.*        V10.03.03 22/01/2013 Patrick Adair  CAR 261630                   *
.*                  Removed port number from temp file name                *
.*        V10.03.02 06/09/2012  J.Tan         CAR 272449                   *
.*                  Mods checking range of service doctors in NDAT0000     *
.*        V10.03.01 15/02/2012  Ebon Clements CAR 259996                   *
.*                  Fixed journal to date test and transaction date that   *
.*                  prints for credit notes                                *
.*        V10.02.02 30/06/2011  Saroeun Soeur CAR 238757                   *
.*                  removed read invoice on journals NDAT0000              *
.*                  fix heading for COPTION = 6                            *
.*        V10.02.01 16/06/2011  Saroeun Soeur CAR 238757                   *
.*                  removed journal                                        *
.*        V10.01.03 03/03/2011  Ebon Clements CAR 238757                   *
.*                  Added option to run by transaction date                *
.*        V10.01.02 07/01/2011  Ebon Clements CAR 236321                   *
.*                  Added option to run by invoice or arrival date         *
.*        V10.01.01 25/10/2010  Ebon Clements CAR 232365                   *
.*                  Changed date range to test visit date not invoice date *
.*                  Added arrival date/time and total per day              *
.*        V10.00.03 15/10/2010  Ebon Clements CAR 230580                   *
.*                  Added credit notes and journals                        *
.*        V10.00.02 13/10/2010  Ebon Clements CAR 230580                   *
.*                  Fixed triage cat filter and doctors totals             *
.*        V10.00.01 12/10/2010  Ebon Clements CAR 230580                   *
.*                  Added misc charge group cat FI test                    *
.*        V10.00.00 01/10/2010  Ebon Clements CAR 230580                   *
.*                  Created program                                        *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       AAEDTRFD/INC
          INC       EMRVISFD/INC
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       PMSFCIFD/INC
          INC       PMSHCPFD/INC
          INC       PATVISFD/INC
          INC       PATCODFD/INC
          INC       PATDO1FD/INC          * doctor file pat
          INC       PATJRNFD/INC
          INC       PATMA1FD/INC          * doctor file pat
          INC       PATINVFD/INC
          INC       PMSCNOFD/INC
          INC       PMSEVTFD/INC
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
.         Temporary File Definition
.         -------------------------
.
REPTEMP1  IFILE   SQL, FIXED=76, KEY="U1-10,11-18,19-23,24-31,32-39,40-48,49-54,55-56"
.
.Name     Type     Size    Physical  Start  Desc
.----     ----     ----    --------  -----  ---------------
.                                                       
TEMPDOCT  DIM       10     10        1      Doctor Code
TEMPVDAT  DIM       8      8         11     Visit Date          * Z70 for total 
TEMPVTIM  DIM       5      5         19     Visit Time          * Z70 for total
TEMPVISN  DIM       8      8         24     Visit/AAE Number    * Z70 for total
TEMPINVN  DIM       8      8         32     Invoice Number      * Z70 for total
TEMPITMN  DIM       9      9         40     Item Number         * Z70 for total
TEMPTRNO  DIM       6      6         49     Trans Number        * Z70 for total
TEMPUNIQ  DIM       2      2         55     Unique Number       * Z70 for total
TEMPIAMT  FORM      12.2   8         57     Item Amount
TEMPSRVS  FORM      2      2         65     No. of services
TEMPIDAT  DIM       8      8         67     Invoice Date
TEMPSRCE  DIM       1      1         75     Item Source  
.                                           "C"redit nots
.                                           "J"ournal
.                                           "I"nvoice
. End of record                      76
.
ERASE     INIT      "iserase "
ISBUILD   INIT      "isbuild "
TEMPFILE  DIM       8
UKEY      INIT      "  76 U1-10,11-18,19-23,24-31,32-39,40-48,49-54,55-56 "
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
CMDLINE   DIM       127      
DIM10     DIM       10
DISPCAT1  DIM       1
DISPCAT2  DIM       1
DISPCAT3  DIM       1
DISPCAT4  DIM       1
DISPCAT5  DIM       1
DISPCAT6  DIM       1
DISPCAT9  DIM       1
PATFNAME  DIM       16
SAVEVISN  DIM       8
SERVDOCT  DIM       10
SAVKEY56  DIM       56
SUBTOTAL  FORM      12.2
DOCFNAME  DIM       20        
DOCCODE   DIM       6
FROMDATE  DIM       8        
FDOCCODE  DIM       10
LASTDOCT  DIM       10
PRNTDCOD  FORM      3
TDOCCODE  DIM       10
TODATE    DIM       8        
TOTALAMT  FORM      12.2     
TRIAGCAT  DIM       7
.
PRGID     INIT      "IBAEMR26"
PRGNAM    INIT      "Detailed Emergency Revenue by Doctor"
VERSION   INIT      "V12.01.00"
.
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
.
          CALL      KDAT0000               * keyin the date range
          CALL      KDOC0000               * keyin the doctor range
          CALL      KTRI0000               * Keyin triage category
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      CREA0000               * create report tempfile
          CALL      CVAR0000               * clear vars
.
          IF        COPTION=1 | COPTION=2
            CALL      VDAT0000             * load tempfile by visit date
          ENDIF
.
          IF        COPTION=3 | COPTION=4
            CALL      IDAT0000             * load tempfile by invoice date
          ENDIF
.
          IF        COPTION=5 | COPTION=6
            CALL      TDAT0000             * load tempfile by transaction date
            CALL      RDAT0000             * load tempfile C/Note trans. date
            CALL      NDAT0000             * load tempfile Journal trans. date
          ENDIF
.
          CALL      PRNT0000               * process
          CALL      KILL0000               * delete report tempfile
.
ML9999    CHAIN     PGM                    * chain back to program
.
.****************************************************************************
.*                                INIT0000                                  *
.*                             initialisation                               *
.****************************************************************************
INIT0000  CALL      DISPHEAD
          CALL      IBACLOCK
.
          DISPLAY   *P56:24,"Opening aaedtref";
          OPEN      AAEDTRE1,"aaedtref"
          OPEN      AAEDTRE4,"aaedtref"
.
          DISPLAY   *P64:24,"pmscnoaf";
          OPEN      PMSCNOA1,"pmscnoaf"
          OPEN      PMSCNOA3,"pmscnoaf"
          OPEN      PMSCNOA4,"pmscnoaf"
.
          DISPLAY   *P64:24,"pmshcpaf";
          OPEN      PMSHCPA1,"pmshcpaf"
.
          DISPLAY   *P64:24,"pmshcpaf";
          OPEN      PMSHCPA2,"pmshcpaf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA1,"patvisaf"
.
          DISPLAY   *P64:24,"patinvrf";
          OPEN      PATINVR1,"patinvrf"
          OPEN      PATINVR2,"patinvrf"
          OPEN      PATINVR3,"patinvrf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"pmsfciaf";
          OPEN      PMSFCIA3,"pmsfciaf"
.
          DISPLAY   *P64:24,"patjrnlf";
          OPEN      PATJRNL1,"patjrnlf"
          OPEN      PATJRNL3,"patjrnlf"
.
          DISPLAY   *P64:24,"emrvisaf";
          OPEN      EMRVISA1,"emrvisaf"
          OPEN      EMRVISA4,"emrvisaf"
.
          DISPLAY   *P64:24,"pmsevtaf";
          OPEN      PMSEVTA1,"pmsevtaf"
          OPEN      PMSEVTA4,"pmsevtaf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
INIT9999  RETURN
.
.****************************************************************************
.*                                OPTN0000                                  *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Emergency Department":
                                            " - By Visit Date":
                    *P1:6,*V2LON,TWO,*HOFF,". ED Ward Events":
                                            " - By Visit Date":
                    *P1:7,*V2LON,THREE,*HOFF,". Emergency Department":
                                            " - By Invoice Date":
                    *P1:8,*V2LON,FOUR,*HOFF,". ED Ward Events":
                                            " - By Invoice Date":
                    *P1:9,*V2LON,FIVE,*HOFF,". Emergency Department":
                                            " - By Transaction Date":
                    *P1:10,*V2LON,SIX,*HOFF,". ED Ward Events":
                                            " - By Transaction Date"
.
OPTN0500  KEYIN     *P1:11,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:11,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000,OPTN9000,OPTN9000,OPTN9000:
                            OPTN9000,OPTN9000
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
.
.********************************************************************
. Keyin the date range
.********************************************************************
KDAT0000  DISPLAY   *P1:13,*EF:
                    *P1:13,"From  Date  :":
                    *P1:14,"To    Date  :"
.
KDAT1000  CALL      IBACLOCK 
          MOVE      CCC,CCENT
          MOVE      CYY,CYEAR
          MOVE      CMM,CMON
          MOVE      CDD,CDAY 
.
          MOVE      TEN5,CCOL
          MOVE      TEN3,CVERT
.
          MOVE      "40",ECOL
          MOVE      CVERT,EVERT
          MOVE      CCC,CCENT
.
          CALL      KEYDATE
          BRANCH    OVRCD OF KDAT0000
.
          PACK      FROMDATE WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",FROMDATE
.
          MOVE      TEN5,CCOL
          MOVE      TEN4,CVERT
          MOVE      "40",ECOL
          MOVE      CVERT,EVERT
.
          CALL      KEYDATE
          BRANCH    OVRCD OF KDAT1000
.
          PACK      TODATE WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE
.
          MATCH     FROMDATE,TODATE
          GOTO      KDAT8000 IF LESS
.
          GOTO      KDAT9999
.
KDAT8000  DISPLAY   *P1:24,*B,*V2LON:
                    "** FROM Date Greater Than TO Date ** ";
          CALL      HITENTER
          DISPLAY   *P1:24,*EL;
          GOTO      KDAT0000
.
KDAT9999  RETURN
.
.****************************************************************************
. Keyin the doctor range
.****************************************************************************
KDOC0000  DISPLAY   *P1:16,*EL,"From Doctor Code : "
          MOVE      SP70,FDOCCODE 
          MOVE      "20",ECOL
          MOVE      "16",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP70,CKYIDEF8
          MOVE      ZERO,CKYINACT
          CALL      PMSHCPKY
          BRANCH    EXIT,KDOC1000,KDOC0000  
          MOVE      PMHCHCPC,FDOCCODE
          GOTO      KDOC2000
.
KDOC1000  MOVE      SP70,FDOCCODE
          DISPLAY   *P20:16,*EL,"Start"          
.
KDOC2000  DISPLAY   *P1:17,*EL,"To Doctor Code : "
          MOVE      SP70,TDOCCODE
          MOVE      "18",ECOL
          MOVE      "17",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP70,CKYIDEF8 
          MOVE      ZERO,CKYINACT
          CALL      PMSHCPKY
          BRANCH    EXIT,KDOC3000,KDOC0000
          MOVE      PMHCHCPC,TDOCCODE
          GOTO      KDOC4000
.
KDOC3000  MOVE      Z70,TDOCCODE
          DISPLAY   *P18:17,*EL,"Finish"  
.
KDOC4000  MATCH     FDOCCODE,TDOCCODE
          GOTO      KDOC5000 IF LESS
          GOTO      KDOC9999
.
KDOC5000  DISPLAY   *P1:24,*B,*V2LON:
                    "** From Doctor Code Greater than To Doctor Code. **";
          CALL      HITENTER
          DISPLAY   *P1:24,*EL;
          GOTO      KDOC0000
.
KDOC9999  RETURN
.
.********************************************************************
. Keyin the triage category filter 
. Each character of TRIAGCAT indicates if triage category
. 1,2,3,4,5,6 or 9 should be included.
.********************************************************************
KTRI0000  IF        COPTION <> 1 & COPTION <> 3 &  COPTION <> 5 
            GOTO      KTRI9999 
          ENDIF
.
          MOVE      SP70,TRIAGCAT
          KEYIN     *P1:18,*EL,"Triage Category : ",*DV,UNDLN1:
                    *P19:18,*V2LON,TRIAGCAT
.
          PACK      TRIAGCAT,TRIAGCAT,SP70
.
          MATCH     SP70,TRIAGCAT
          IF        @EQUAL
            MOVE      "1234569",TRIAGCAT
            DISPLAY   *P19:18,*V2LON,TRIAGCAT
          ENDIF
.        
          REP       " 0",TRIAGCAT
.
KTRI9999  RETURN
. 
.
.********************************************************************
. Load emr revenue data to temp file by visit date
.********************************************************************
VDAT0000  DISPLAY   *P1:24,*EL:
                    *P10:24,"Visit No: ":
                    *P40:24,"Scanning: "
.
          IF        COPTION=1
            PACK      KEY24,FROMDATE,SP70             * ED Visits
            CALL      RSEMVIS4
          ELSE
            PACK      KEY16,FROMDATE,SP70             * ED Events
            CALL      RSPMEVT4
          ENDIF
VDAT1000  IF        COPTION=1
            CALL      RKEMVIS4
            BRANCH    OVRCD,VDAT9999
.
            MATCH     EMVIDATE,TODATE
            GOTO      VDAT9999 IF LESS
.
            PACK      KEY8,EMVIADMN,SP70
            CALL      RDVISA1
            BRANCH    OVRCD,VDAT1000
.
            MATCH     "1234569",TRIAGCAT              * All triage cat
            GOTO      VDAT2000 IF EQUAL
.
            MATCH     SP70,EMVITRIG                   * No triage cat
            GOTO      VDAT1000 IF EQUAL
.
            PACK      KEY5,ANSA,ANSA,EMVITRIG,SP70
            CALL      RDCODE1
            BRANCH    OVRCD,VDAT1000
.
            SCAN      TCINDC1,TRIAGCAT
            RESET     TRIAGCAT                       * Check triage cat
            GOTO      VDAT1000 IF NOT EQUAL
          ELSE
            CALL      RKPMEVT4
            BRANCH    OVRCD,VDAT9999
.
            MATCH     PMEVADTE,TODATE
            GOTO      VDAT9999 IF LESS
.
            PACK      KEY8,PMEVVISN,SP70
            CALL      RDVISA1
            BRANCH    OVRCD,VDAT1000
.
            PACK      KEY5,ANSE,ANSV,PMEVEVNT,SP70
            CALL      RDCODE1
            BRANCH    OVRCD,VDAT1000
.
            MATCH     ANSE,TCINDC1
            GOTO      VDAT10000 IF NOT EQUAL
.
          ENDIF
.
VDAT2000  PACK      KEY16,PVIBILL,SP70
          CALL      RSPTINV3
VDAT2100  CALL      RKPTINV3                     * Get all invoices for this 
          BRANCH    OVRCD,VDAT1000               * visit number
.
          MATCH     PVIBILL,PINVADM
          GOTO      VDAT1000 IF NOT EQUAL            
.           
          MATCH     "1",PTINCRST                 * Inv fully credited
          GOTO      VDAT2100 IF EQUAL
.
          MOVE      PINVAMT,TEMPIAMT
          CALL      SDOC0000                     * Get serv.doct for invoice
.
          PACK      KEY23,PINVNO,SP70
          CALL      RDSDTRE4
VDAT3000  CALL      RDKDTRE4
          BRANCH    OVRCD,VDAT2100
.
          MATCH     PINVNO,ATINVNO
          GOTO      VDAT2100 IF NOT EQUAL
.
          MATCH     "1",ATDTCRST
          GOTO      VDAT3000 IF EQUAL            * Fully credited
.
          MATCH     "2",ATDTCRST
          GOTO      VDAT3000 IF EQUAL            * Item credited
.
          COMPARE   THREE,ATRECTYP               * Check Journals
          GOTO      VDAT4000 IF NOT EQUAL
.
.         Journals
.
          MATCH     SP70,SERVDOCT
          GOTO      VDAT3000 IF EQUAL
.
          MATCH     FDOCCODE,SERVDOCT
          GOTO      VDAT3000 IF LESS
.
          MATCH     SERVDOCT,TDOCCODE
          GOTO      VDAT3000 IF LESS
.
          MOVE      "J ",KEY2
          PACK      KEY5,KEY2,ATTYPE,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,VDAT3000
.
          MATCH     "I",TCINDC3
          GOTO      VDAT3000 IF NOT EQUAL
.
          PACK      TEMPDOCT,SERVDOCT,SP70
          PACK      TEMPITMN,ATTYPE,SP70         * Journal code
          GOTO      VDAT7000
.
VDAT4000  MATCH     SP70,ATDTEDAD
          GOTO      VDAT3000 IF EQUAL
          GOTO      VDAT3000 IF EOS
.
          MATCH     FDOCCODE,ATDTEDAD
          GOTO      VDAT3000 IF LESS
.
          MATCH     ATDTEDAD,TDOCCODE
          GOTO      VDAT3000 IF LESS
.
          PACK      TEMPDOCT,ATDTEDAD,SP70
          PACK      TEMPITMN,ATITEMNO,SP70
.
          COMPARE   FIVE,ATRECTYP                * Include All Procedures
          GOTO      VDAT6000 IF EQUAL
.
          COMPARE   ONE,ATRECTYP                 * Check Misc Charges Filter
          GOTO      VDAT3000 IF NOT EQUAL
.
.         Misc.Item
.
          MATCH     SP70,PINVCLM                 * Check if Work, Vet or TAC
          GOTO      VDAT3000 IF EQUAL
.
          PACK      KEY5,ANSC,ANSL,PINVCLM,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,VDAT3000
.
          PACK      KEY5,TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5,SP70
.
          RESET     KEY5
          SCAN      ANSW,KEY5                    * Work
          GOTO      VDAT5000 IF EQUAL
.
          RESET     KEY5
          SCAN      ANSM,KEY5                    * TAC
          GOTO      VDAT5000 IF EQUAL
.
          RESET     KEY5
          SCAN      ANSV,KEY5                    * Vet
          GOTO      VDAT5000 IF EQUAL
.
          GOTO      VDAT3000
.
VDAT5000  MATCH     SP70,ATMISGRP                * Misc Charge Group
          GOTO      VDAT3000 IF EQUAL
.
          PACK      KEY5,ANSF,ANSI,ATMISGRP,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,VDAT3000
.
          MATCH     ANSD,TCINDC7                 * Include in Doctors Revenue
          GOTO      VDAT3000 IF NOT EQUAL
.
          GOTO      VDAT7000
.
VDAT6000  MATCH     SP70,ATMISGRP                * Misc Charge Group
          GOTO      VDAT3000 IF EQUAL
.
          PACK      KEY5,ANSP,ANSG,ATMISGRP,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,VDAT3000
.
          MATCH     ANSD,TCINDC7                 * Include in Doctors Revenue
          GOTO      VDAT3000 IF NOT EQUAL
.
VDAT7000  DISPLAY   *P24:24,*V2LON,PINVNO;
.
          IF        COPTION=1
            MOVE      EMVIDATE,TEMPVDAT
            MOVE      EMVITIME,TEMPVTIM
          ELSE
            MOVE      PMEVADTE,TEMPVDAT
            MOVE      PMEVATIM,TEMPVTIM
          ENDIF
.
          PACK      TEMPVISN,DATNUMB,SP70
          PACK      TEMPINVN,ATINVNO,SP70
          PACK      TEMPTRNO,ATTRANS,SP70
          MOVE      " 1",TEMPUNIQ
          MOVE      ATPATAMT,TEMPIAMT
          MOVE      ATSERVS,TEMPSRVS
          PACK      TEMPIDAT,PINVDTE,SP70        * Write Inv Line Item
          MOVE      "I",TEMPSRCE
.
VDAT9000  PACK      KEY56,TEMPDOCT,TEMPVDAT,TEMPVTIM,TEMPVISN,TEMPINVN:    
                          TEMPITMN,TEMPTRNO,TEMPUNIQ,SP70
          CALL      RATEMP1
          IF        OVRCD=1
            CALL      WRTEMP1
          ELSE
            MOVE      ZERO,F2
            MOVE      TEMPUNIQ,F2
            ADD       ONE,F2
            MOVE      F2,TEMPUNIQ
            GOTO      VDAT9000
          ENDIF
.
          CALL      TOTA0000                 * Write invoice total
          CALL      TOTB0000                 * Write doctor daily total
          CALL      TOTC0000                 * Write doctor total
.
          GOTO      VDAT3000
.
VDAT9999  RETURN
.
.********************************************************************
. Load emr revenue data to temp file by invoice date
.********************************************************************
IDAT0000  DISPLAY   *P1:24,*EL:
                    *P10:24,"Visit No: ":
                    *P40:24,"Scanning: "
.
          PACK      KEY16,FROMDATE,SP70
          CALL      RSPTINV2
IDAT1000  CALL      RKPTINV2                     * Get all invoices for       
          BRANCH    OVRCD,IDAT9999               * the date range
.
          MATCH     PINVDTE,TODATE
          GOTO      IDAT9999 IF LESS
.
          MATCH     "1",PTINCRST                 * Inv fully credited
          GOTO      IDAT1000 IF EQUAL
.
          IF        COPTION=3
            PACK      KEY8,PINVADM,SP70
            CALL      RDEMVIS1
            BRANCH    OVRCD,IDAT1000
.
            PACK      KEY8,EMVIADMN,SP70
            CALL      RDVISA1
            BRANCH    OVRCD,IDAT1000
.
            MATCH     "1234569",TRIAGCAT              * All triage cat
            GOTO      IDAT2000 IF EQUAL
.
            MATCH     SP70,EMVITRIG                   * No triage cat
            GOTO      IDAT1000 IF EQUAL
.
            PACK      KEY5,ANSA,ANSA,EMVITRIG,SP70
            CALL      RDCODE1
            BRANCH    OVRCD,IDAT1000
.
            SCAN      TCINDC1,TRIAGCAT
            RESET     TRIAGCAT                       * Check triage cat
            GOTO      IDAT1000 IF NOT EQUAL
          ELSE
            PACK      KEY8,PINVADM,SP70
            CALL      RDPMEVT1
            BRANCH    OVRCD,IDAT1000
.
            PACK      KEY8,PMEVVISN,SP70
            CALL      RDVISA1
            BRANCH    OVRCD,IDAT1000
.
            PACK      KEY5,ANSE,ANSV,PMEVEVNT,SP70
            CALL      RDCODE1
            BRANCH    OVRCD,IDAT1000
.
            MATCH     ANSE,TCINDC1
            GOTO      IDAT1000 IF NOT EQUAL
          ENDIF
.
IDAT2000  MOVE      PINVAMT,TEMPIAMT
          CALL      SDOC0000                     * Get serv.doct for invoice
.
          PACK      KEY23,PINVNO,SP70
          CALL      RDSDTRE4
IDAT3000  CALL      RDKDTRE4
          BRANCH    OVRCD,IDAT1000
.
          MATCH     PINVNO,ATINVNO
          GOTO      IDAT1000 IF NOT EQUAL
.
          MATCH     "1",ATDTCRST
          GOTO      IDAT3000 IF EQUAL            * Fully credited
.
          MATCH     "2",ATDTCRST
          GOTO      IDAT3000 IF EQUAL            * Item credited
.
          COMPARE   THREE,ATRECTYP               * Check Journals
          GOTO      IDAT4000 IF NOT EQUAL
.
.         Journals
.
          MATCH     SP70,SERVDOCT
          GOTO      IDAT3000 IF EQUAL
.
          MATCH     FDOCCODE,SERVDOCT
          GOTO      IDAT3000 IF LESS
.
          MATCH     SERVDOCT,TDOCCODE
          GOTO      IDAT3000 IF LESS
.
          MOVE      "J ",KEY2
          PACK      KEY5,KEY2,ATTYPE,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,IDAT3000
.
          MATCH     "I",TCINDC3
          GOTO      IDAT3000 IF NOT EQUAL
.
          PACK      TEMPDOCT,SERVDOCT,SP70
          PACK      TEMPITMN,ATTYPE,SP70         * Journal code
          GOTO      IDAT7000
.
IDAT4000  MATCH     SP70,ATDTEDAD
          GOTO      IDAT3000 IF EQUAL
          GOTO      IDAT3000 IF EOS
.
          MATCH     FDOCCODE,ATDTEDAD
          GOTO      IDAT3000 IF LESS
.
          MATCH     ATDTEDAD,TDOCCODE
          GOTO      IDAT3000 IF LESS
.
          PACK      TEMPDOCT,ATDTEDAD,SP70
          PACK      TEMPITMN,ATITEMNO,SP70
.
          COMPARE   FIVE,ATRECTYP                * Include All Procedures
          GOTO      IDAT6000 IF EQUAL
.
          COMPARE   ONE,ATRECTYP                 * Check Misc Charges Filter
          GOTO      IDAT3000 IF NOT EQUAL
.
.         Misc.Item
.
          MATCH     SP70,PINVCLM                 * Check if Work, Vet or TAC
          GOTO      IDAT3000 IF EQUAL
.
          PACK      KEY5,ANSC,ANSL,PINVCLM,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,IDAT3000
.
          PACK      KEY5,TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5,SP70
.
          RESET     KEY5
          SCAN      ANSW,KEY5                    * Work
          GOTO      IDAT5000 IF EQUAL
.
          RESET     KEY5
          SCAN      ANSM,KEY5                    * TAC
          GOTO      IDAT5000 IF EQUAL
.
          RESET     KEY5
          SCAN      ANSV,KEY5                    * Vet
          GOTO      IDAT5000 IF EQUAL
.
          GOTO      IDAT3000
.
IDAT5000  MATCH     SP70,ATMISGRP                * Misc Charge Group
          GOTO      IDAT3000 IF EQUAL
.
          PACK      KEY5,ANSF,ANSI,ATMISGRP,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,IDAT3000
.
          MATCH     ANSD,TCINDC7                 * Include in Doctors Revenue
          GOTO      IDAT3000 IF NOT EQUAL
.
          GOTO      IDAT7000
.
IDAT6000  MATCH     SP70,ATMISGRP                * Misc Charge Group
          GOTO      IDAT3000 IF EQUAL
.
          PACK      KEY5,ANSP,ANSG,ATMISGRP,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,IDAT3000
.
          MATCH     ANSD,TCINDC7                 * Include in Doctors Revenue
          GOTO      IDAT3000 IF NOT EQUAL
.
IDAT7000  DISPLAY   *P24:24,*V2LON,PINVNO;
.
          IF        COPTION=3
            MOVE      EMVIDATE,TEMPVDAT
            MOVE      EMVITIME,TEMPVTIM
          ELSE
            MOVE      PMEVADTE,TEMPVDAT
            MOVE      PMEVATIM,TEMPVTIM
          ENDIF
.
          PACK      TEMPVISN,DATNUMB,SP70
          PACK      TEMPINVN,ATINVNO,SP70
          PACK      TEMPTRNO,ATTRANS,SP70
          MOVE      " 1",TEMPUNIQ
          MOVE      ATPATAMT,TEMPIAMT
          MOVE      ATSERVS,TEMPSRVS
          PACK      TEMPIDAT,PINVDTE,SP70        * Write Inv Line Item
          MOVE      "I",TEMPSRCE
.
IDAT9000  PACK      KEY56,TEMPDOCT,TEMPVDAT,TEMPVTIM,TEMPVISN,TEMPINVN:    
                          TEMPITMN,TEMPTRNO,TEMPUNIQ,SP70
          CALL      RATEMP1
          IF        OVRCD=1
            CALL      WRTEMP1
          ELSE
            MOVE      ZERO,F2
            MOVE      TEMPUNIQ,F2
            ADD       ONE,F2
            MOVE      F2,TEMPUNIQ
            GOTO      IDAT9000
          ENDIF
.
          CALL      TOTA0000                 * Write invoice total
          CALL      TOTB0000                 * Write doctor daily total
          CALL      TOTC0000                 * Write doctor total
.
          GOTO      IDAT3000
.
IDAT9999  RETURN
.
.**************************************************************************
.         Get service doctor for this invoice
.**************************************************************************
SDOC0000  MOVE      SP70,SERVDOCT
.
          PACK      KEY23,PINVNO,SP70
          CALL      RDSDTRE4
SDOC1000  CALL      RDKDTRE4
          BRANCH    OVRCD,SDOC9999
.
          MATCH     PINVNO,ATINVNO
          GOTO      SDOC9999 IF NOT EQUAL
.
          BRANCH    ATRECTYP,SDOC2000,SDOC1000,SDOC1000:
                             SDOC1000,SDOC2000
          GOTO      SDOC1000
.
SDOC2000  MATCH     SP70,ATDTEDAD
          GOTO      SDOC1000 IF EQUAL
          MOVE      ATDTEDAD,SERVDOCT
.
SDOC9999 RETURN
+
.**************************************************************************
. Print the report
.**************************************************************************
PRNT0000  CALL      HEAD0000                     * Print page heading
          MOVE      SP70,SAVEVISN
.
          PACK      KEY56,SP70
          CALL      RSTEMP1
PRNT1000  CALL      RKTEMP1
          BRANCH    OVRCD,PRNT9000
.
          CALL      DOCD0000                * Doctors daily total
          BRANCH    EXIT,PRNT1000
.
          CALL      DOCT0000                * Doctors total
          BRANCH    EXIT,PRNT1000
.
          PACK      KEY10,TEMPDOCT,SP70
          CALL      RDPMHCP1
          IF        OVRCD=0
            MOVE      PMHCGNAM,DIM1
            PACK      DOCFNAME,DIM1,DOT,PMHCSNAM
            RESET     DOCFNAME
          ELSE
            MOVE      TEMPDOCT,DOCFNAME
          ENDIF
.
          MOVE      SP70,PATFNAME
          PACK      KEY8,TEMPVISN,SP70
          CALL      RDPTVIS1
          IF        OVRCD=0
            PACK       KEY8,PVIURNO,SP70
            CALL       RDMAST1
            IF         OVRCD=1
              MOVE       "Invalid Patient",PATFNAME 
            ENDIF
            MOVE       PGNAME,DIM1
            PACK       PATFNAME,DIM1,SP1,PSNAME
          ELSE
            MOVE    "Invalid Patient",PATFNAME 
          ENDIF
.
          CALL      PLIN0000                     * Print report line
          GOTO      PRNT1000
.
PRNT9000  CALL      GTOT0000                     * Print grand totals
.
PRNT9999  RETURN
.
.***************************************************************************
. Print report line
.***************************************************************************
PLIN0000  COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          MATCH     SP70,SAVEVISN           * Print blank line between each Inv
          IF        !@EQUAL
            MATCH     TEMPVISN,SAVEVISN
            IF        !@EQUAL
              PRINT     *1,"|",*22,"|",*39,"|",*56,"|",*65,"|",*74,"|",*85,"|":
                        *87,"|",*97,"|" ,*100,"|",*116,"|",*132,"|"
              ADD       ONE,CLNO
            ENDIF
          ENDIF
.
          MOVE      TEMPVISN,SAVEVISN
.
          PRINT     *1,"|";
.
          MATCH     TEMPDOCT,LASTDOCT
          IF        @EQUAL
            IF        PRNTDCOD=0
              STRIP     TEMPDOCT
              PRINT     *+,"(",TEMPDOCT,")";
              RESET     TEMPDOCT
              PACK      TEMPDOCT,TEMPDOCT,SP70
              MOVE      ONE,PRNTDCOD
            ENDIF
            GOTO      PLIN1000
          ENDIF
.
          PRINT     DOCFNAME;
          MOVE      TEMPDOCT,LASTDOCT
          MOVE      ZERO,PRNTDCOD
.
PLIN1000  UNPACK    TEMPVDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PRINT     *22,"|",CPCDATE,SP1,TEMPVTIM:
                    *39,"|",PATFNAME:
                    *56,"|",TEMPVISN:
                    *65,"|",TEMPINVN;
.
          UNPACK    TEMPIDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PRINT     *74,"|",CPCDATE,*85,"|",TEMPSRCE,*87,"|",TEMPITMN:
                    *97,"|",TEMPSRVS,*100,"|",TEMPIAMT;
.
          PACK      SAVKEY56,TEMPDOCT,TEMPVDAT,TEMPVTIM,TEMPVISN,TEMPINVN:    
                             TEMPITMN,TEMPTRNO,TEMPUNIQ,SP70
          CALL      RKTEMP1
          BRANCH    OVRCD,PLIN5000               * Check for sub total
.
          MATCH     Z70,TEMPITMN
          GOTO      PLIN5000 IF NOT EQUAL
.
          PRINT     *116,"|",TEMPIAMT,*132,"|"
          ADD       TEMPIAMT,TOTALAMT
.
          GOTO      PLIN7000
.
PLIN5000  PRINT     *116,"|",*132,"|"
.
          PACK      KEY56,SAVKEY56
          CALL      RDTEMP1
.
PLIN7000  ADD       ONE,CLNO
.
PLIN9999  RETURN
.
.*****************************************************************************
.         Printing of grand totals
.*****************************************************************************
GTOT0000 
          PRINT     *1,"| Grand Total";
          PRINT     *116,"|",TOTALAMT,*132,"|",*N;
          ADD       ONE,CLNO
          CALL      UND132
.
          PRINT      *N,"** END OF REPORT **";
.
GTOT9999  RETURN
.
.
.***************************************************************************
. Print a new page 
.***************************************************************************
HEAD0000  CALL      HEAD132
.
          MOVE      FDOCCODE,DIM10
          MATCH     SP70,FDOCCODE
          IF        @EQUAL
            MOVE      "Start",DIM10
          ENDIF 
          PRINT     *40,"From Doctor : ",DIM10," to ";
.
          MOVE      TDOCCODE,DIM10
          MATCH     Z70,TDOCCODE
          IF        @EQUAL
            MOVE      "Finish",DIM10
          ENDIF
          PRINT      DIM10
.
          UNPACK    FROMDATE INTO CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *40,"Date From  : ",CPCDATE," to ";
.
          UNPACK    TODATE INTO CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     CPCDATE
.
          IF        COPTION=1 | COPTION=3 | COPTION=5
            IF        COPTION=1
              PRINT     *40,"Event Type  : Emergency Department":
                            " - By Visit Date"
            ELSE
              IF        COPTION=5
                PRINT     *40,"Event Type  : Emergency Department":
                              " - By Transaction Date"
              ELSE
                PRINT     *40,"Event Type  : Emergency Department":
                              " - By Invoice Date"
              ENDIF
            ENDIF
            ADD       ONE,CLNO
            UNPACK    TRIAGCAT,DISPCAT1,DISPCAT2,DISPCAT3,DISPCAT4:
                               DISPCAT5,DISPCAT6,DISPCAT9

            PRINT     *40,"Triage Category : ";
.
            MATCH     "1",DISPCAT1
            IF        @EQUAL
              PRINT     DISPCAT1;
            ENDIF
.
            MATCH     "2",DISPCAT2
            IF        @EQUAL
              PRINT     " ",DISPCAT2;
            ENDIF
.
            MATCH     "3",DISPCAT3
            IF        @EQUAL
              PRINT     " ",DISPCAT3;
            ENDIF
.
            MATCH     "4",DISPCAT4
            IF        @EQUAL
              PRINT     " ",DISPCAT4;
            ENDIF
.
            MATCH     "5",DISPCAT5
            IF        @EQUAL
              PRINT     " ",DISPCAT5;
            ENDIF
.
            MATCH     "6",DISPCAT6
            IF        @EQUAL
              PRINT     " ",DISPCAT6;
            ENDIF
.
            MATCH     "9",DISPCAT9
            IF        @EQUAL
              PRINT     " ",DISPCAT9
            ELSE
              PRINT     SP1
            ENDIF
.
            ADD       ONE,CLNO
          ELSE
            IF        COPTION=2
              PRINT     *40,"Event Type  : ED Ward Events - By Visit Date"
            ELSE
              IF        COPTION=6
                PRINT     *40,"Event Type  : ED Ward Events - ":
                              "By Transaction Date"
              ELSE
                PRINT     *40,"Event Type  : ED Ward Events - By Invoice Date"
              ENDIF
            ENDIF
            ADD       ONE,CLNO
          ENDIF
.
          CALL      UND132
          IF        COPTION=5 | COPTION=6
            PRINT       *1,"|Doctor Name":
                        *22,"|Arrival":
                        *39,"|Patient Name":
                        *56,"|Vis Num":
                        *65,"|Invoice ":
                        *74,"|Trn Date":
                        *85,"|T":
                        *87,"|Itm Num":
                        *97,"|No":
                        *100,"|     Amount",*116,"|   Sub Total  ",*132,"|"
          ELSE
            PRINT       *1,"|Doctor Name":
                        *22,"|Arrival":
                        *39,"|Patient Name":
                        *56,"|Vis Num":
                        *65,"|Invoice ":
                        *74,"|Inv Date":
                        *85,"|T":
                        *87,"|Itm Num":
                        *97,"|No":
                        *100,"|     Amount",*116,"|   Sub Total  ",*132,"|"
          ENDIF
.
          CALL      UND132
          MOVE      TEN,CLNO
.
HEAD9999  RETURN
.
.****************************************************************************
.* Print doctor daily total                                                 *
.****************************************************************************
DOCD0000  MOVE      ZERO,EXIT
.
          MATCH     Z70,TEMPVDAT            * Exit if doctor total record
          GOTO      DOCD9999 IF EQUAL
.
          MATCH     Z70,TEMPVTIM            * Daily total record
          GOTO      DOCD9999 IF NOT EQUAL     
.
          UNPACK    TEMPVDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
. 
          PRINT     *1,"|";
.
          MATCH     TEMPDOCT,LASTDOCT
          IF        @EQUAL
            IF        PRNTDCOD=0
              STRIP     TEMPDOCT
              PRINT     *+,"(",TEMPDOCT,")";
              RESET     TEMPDOCT
              PACK      TEMPDOCT,TEMPDOCT,SP70
              MOVE      ONE,PRNTDCOD
            ENDIF
          ENDIF
.
          PRINT     *22,"-------------------------------------":
                        "-------------------------------------":
                        "------------------------------------":
                    *132,"|"
.
          PRINT     *1,"|",*22,"|",CPCDATE:
                    *117,TEMPIAMT,*132,"|"
.
          PRINT     *1,"|":
                    *22,"-------------------------------------":
                        "-------------------------------------":
                        "------------------------------------":
                    *132,"|"
          ADD       THREE,CLNO
          MOVE      SP70,SAVEVISN
.
          MOVE      ONE,EXIT
.     
DOCD9999  RETURN
.
.****************************************************************************
.* Print doctor total                                                       *
.****************************************************************************
DOCT0000  MOVE      ZERO,EXIT
.
          MATCH     Z70,TEMPVDAT            * Exit if doctor total record
          GOTO      DOCD9999 IF NOT EQUAL
.
          CALL      UND132
          PRINT     *1,"| Doctor Total ";
          PRINT     *117,TEMPIAMT,*132,"|"
          ADD       ONE,CLNO
          CALL      UND132
          MOVE      SP70,SAVEVISN
.
          PRINT     *N
          ADD       TWO,CLNO
          CALL      UND132
.
          MOVE      ONE,EXIT
.
DOCT9999  RETURN
.
.****************************************************************************
.* Close and erase the temporary file                                       *
.****************************************************************************
KILL0000  CLOSE     REPTEMP1                     * close file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ERASE,TEMPFILE       * set file erase command
          EXECUTE   CMDLINE,TASKID               * erase temp file
.
KILL9999  RETURN 
.
.*********************************************************************
.* Create a new temporary file                                       *
.*********************************************************************
CREA0000  CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,TEMPFILE
.
          CALL      KILL0000                     * remove existing file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,TEMPFILE,UKEY
          EXECUTE   CMDLINE,TASKID               * create temporary index file
.
          OPEN      REPTEMP1,TEMPFILE            * open temp index file
.
CREA9999  RETURN
.
.*********************************************************************
.* Clear vars                                                        *
.*********************************************************************
CVAR0000  MOVE      SP6,LASTDOCT
          MOVE      ZERO,TOTALAMT
.
CVAR9999  RETURN
.
.********************************************************************
. Load emr revenue data to temp file - By transaction date
. This is the date the transaction happened. This is driven from
. the invoice file as an invoice can't be back dated.
.********************************************************************
TDAT0000  DISPLAY   *P1:24,*EL:
                    *P10:24,"Visit No: ":
                    *P40:24,"Scanning: "
.
          PACK      KEY16,FROMDATE,SP70
          CALL      RSPTINV2
TDAT1000  CALL      RKPTINV2                     * Get all invoices for
          BRANCH    OVRCD,TDAT9999               * the date range
.
          MATCH     PINVDTE,TODATE
          GOTO      TDAT9999 IF LESS
.
          IF        COPTION=5
            PACK      KEY8,PINVADM,SP70
            CALL      RDEMVIS1
            BRANCH    OVRCD,TDAT1000
.
            PACK      KEY8,EMVIADMN,SP70
            CALL      RDVISA1
            BRANCH    OVRCD,TDAT1000
.
            MATCH     "1234569",TRIAGCAT              * All triage cat
            GOTO      TDAT2000 IF EQUAL
.
            MATCH     SP70,EMVITRIG                   * No triage cat
            GOTO      TDAT1000 IF EQUAL
.
            PACK      KEY5,ANSA,ANSA,EMVITRIG,SP70
            CALL      RDCODE1
            BRANCH    OVRCD,TDAT1000
.
            SCAN      TCINDC1,TRIAGCAT
            RESET     TRIAGCAT                       * Check triage cat
            GOTO      TDAT1000 IF NOT EQUAL
          ELSE
            PACK      KEY8,PINVADM,SP70
            CALL      RDPMEVT1
            BRANCH    OVRCD,TDAT1000
.
            PACK      KEY8,PMEVVISN,SP70
            CALL      RDVISA1
            BRANCH    OVRCD,TDAT1000
.
            PACK      KEY5,ANSE,ANSV,PMEVEVNT,SP70
            CALL      RDCODE1
            BRANCH    OVRCD,TDAT1000
.
            MATCH     ANSE,TCINDC1
            GOTO      TDAT1000 IF NOT EQUAL
          ENDIF
.
TDAT2000  MOVE      PINVAMT,TEMPIAMT
          CALL      SDOC0000                     * Get serv.doct for invoice
.
          PACK      KEY23,PINVNO,SP70
          CALL      RDSDTRE4
TDAT3000  CALL      RDKDTRE4
          BRANCH    OVRCD,TDAT1000
.
          MATCH     PINVNO,ATINVNO
          GOTO      TDAT1000 IF NOT EQUAL
.
          COMPARE   THREE,ATRECTYP               * Check Journals
          GOTO      TDAT3000 IF EQUAL
.
TDAT4000  MATCH     SP70,ATDTEDAD
          GOTO      TDAT3000 IF EQUAL
          GOTO      TDAT3000 IF EOS
.
          MATCH     FDOCCODE,ATDTEDAD
          GOTO      TDAT3000 IF LESS
.
          MATCH     ATDTEDAD,TDOCCODE
          GOTO      TDAT3000 IF LESS
.
          PACK      TEMPDOCT,ATDTEDAD,SP70
          PACK      TEMPITMN,ATITEMNO,SP70
.
          COMPARE   FIVE,ATRECTYP                * Include All Procedures
          GOTO      TDAT6000 IF EQUAL
.
          COMPARE   ONE,ATRECTYP                 * Check Misc Charges Filter
          GOTO      TDAT3000 IF NOT EQUAL
.
.         Misc.Item
.
          MATCH     SP70,PINVCLM                 * Check if Work, Vet or TAC
          GOTO      TDAT3000 IF EQUAL
.
          PACK      KEY5,ANSC,ANSL,PINVCLM,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,TDAT3000
.
          PACK      KEY5,TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5,SP70
.
          RESET     KEY5
          SCAN      ANSW,KEY5                    * Work
          GOTO      TDAT5000 IF EQUAL
.
          RESET     KEY5
          SCAN      ANSM,KEY5                    * TAC
          GOTO      TDAT5000 IF EQUAL
.
          RESET     KEY5
          SCAN      ANSV,KEY5                    * Vet
          GOTO      TDAT5000 IF EQUAL
.
          GOTO      TDAT3000
.
TDAT5000  MATCH     SP70,ATMISGRP                * Misc Charge Group
          GOTO      TDAT3000 IF EQUAL
.
          PACK      KEY5,ANSF,ANSI,ATMISGRP,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,TDAT3000
.
          MATCH     ANSD,TCINDC7                 * Include in Doctors Revenue
          GOTO      TDAT3000 IF NOT EQUAL
.
          GOTO      TDAT7000
.
TDAT6000  MATCH     SP70,ATMISGRP                * Misc Charge Group
          GOTO      TDAT3000 IF EQUAL
.
          PACK      KEY5,ANSP,ANSG,ATMISGRP,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,TDAT3000
.
          MATCH     ANSD,TCINDC7                 * Include in Doctors Revenue
          GOTO      TDAT3000 IF NOT EQUAL
.
TDAT7000  DISPLAY   *P24:24,*V2LON,PINVNO;
.
          IF        COPTION=5
            MOVE      EMVIDATE,TEMPVDAT
            MOVE      EMVITIME,TEMPVTIM
          ELSE
            MOVE      PMEVADTE,TEMPVDAT
            MOVE      PMEVATIM,TEMPVTIM
          ENDIF
.
          PACK      TEMPVISN,DATNUMB,SP70
          PACK      TEMPINVN,ATINVNO,SP70
          PACK      TEMPTRNO,ATTRANS,SP70
          MOVE      " 1",TEMPUNIQ
          MOVE      ATPATAMT,TEMPIAMT
          MOVE      ATSERVS,TEMPSRVS
          PACK      TEMPIDAT,PINVDTE,SP70        * Write Inv Line Item
          MOVE      "I",TEMPSRCE
.
TDAT9000  PACK      KEY56,TEMPDOCT,TEMPVDAT,TEMPVTIM,TEMPVISN,TEMPINVN:
                          TEMPITMN,TEMPTRNO,TEMPUNIQ,SP70
          CALL      RATEMP1
          IF        OVRCD=1
            CALL      WRTEMP1
          ELSE
            MOVE      ZERO,F2
            MOVE      TEMPUNIQ,F2
            ADD       ONE,F2
            MOVE      F2,TEMPUNIQ
            GOTO      TDAT9000
          ENDIF
.
          CALL      TOTA0000                 * Write invoice total
          CALL      TOTB0000                 * Write doctor daily total
          CALL      TOTC0000                 * Write doctor total
.
          GOTO      TDAT3000
.
TDAT9999  RETURN
.
.********************************************************************
. Load emr revenue data to temp file - By transaction date
. This is the date the transaction happened. This is driven from
. the credit note as a credit note can't be back dated.
.********************************************************************
RDAT0000  DISPLAY   *P1:24,*EL:
                    *P10:24,"Visit No: ":
                    *P40:24,"Scanning: "
.
          PACK      KEY18,FROMDATE,SP70
          CALL      RSPMCNO4
RDAT1000  CALL      RKPMCNO4
          BRANCH    OVRCD,RDAT9999
.
          MATCH     PMCNDATE,TODATE
          GOTO      RDAT9999 IF LESS
.
          MATCH     " 1",PMCNSYST                * Emergency Credit notes
          GOTO      RDAT1000 IF NOT EQUAL
.
          PACK      KEY8,PMCNINVN,SP70
          CALL      RDPTINV1                     * Read Invoice
          BRANCH    OVRCD,RDAT1000
.
RDAT1200  IF        COPTION=5
            PACK      KEY8,PINVADM,SP70
            CALL      RDEMVIS1
            BRANCH    OVRCD,RDAT1000
.
            PACK      KEY8,EMVIADMN,SP70
            CALL      RDVISA1
            BRANCH    OVRCD,RDAT1000
.
            MATCH     "1234569",TRIAGCAT              * All triage cat
            GOTO      RDAT2000 IF EQUAL
.
            MATCH     SP70,EMVITRIG                   * No triage cat
            GOTO      RDAT1000 IF EQUAL
.
            PACK      KEY5,ANSA,ANSA,EMVITRIG,SP70
            CALL      RDCODE1
            BRANCH    OVRCD,RDAT1000
.
            SCAN      TCINDC1,TRIAGCAT
            RESET     TRIAGCAT                       * Check triage cat
            GOTO      RDAT1000 IF NOT EQUAL
          ELSE
            PACK      KEY8,PINVADM,SP70
            CALL      RDPMEVT1
            BRANCH    OVRCD,RDAT1000
.
            PACK      KEY8,PMEVVISN,SP70
            CALL      RDVISA1
            BRANCH    OVRCD,RDAT1000
.
            PACK      KEY5,ANSE,ANSV,PMEVEVNT,SP70
            CALL      RDCODE1
            BRANCH    OVRCD,RDAT1000
.
            MATCH     ANSE,TCINDC1
            GOTO      RDAT1000 IF NOT EQUAL
          ENDIF
.
RDAT2000  MOVE      PINVAMT,TEMPIAMT
          CALL      SDOC0000                     * Get serv.doct for invoice
.
          PACK      KEY31,PMCNCRNO,PMCNVISN,PMCNINVN,SP70
          CALL      RSPMFCI3
RDAT3000  CALL      RKPMFCI3
          BRANCH    OVRCD,RDAT1000
.
          MATCH     PMFCCRNO,PMCNCRNO
          GOTO      RDAT1000 IF NOT EQUAL
.
          MATCH     PMFCVISN,PMCNVISN
          GOTO      RDAT1000 IF NOT EQUAL
.
          MATCH     PMFCINVN,PMCNINVN
          GOTO      RDAT1000 IF NOT EQUAL
.
          PACK      KEY22,PMFCVISN,PMFCINVN,PMFCTRAN,SP70
          CALL      RDDTRE1
          BRANCH    OVRCD,RDAT3000
.
          MOVE      PMFCCAMT,ATPATAMT
.
RDAT4000  MATCH     SP70,ATDTEDAD
          GOTO      RDAT3000 IF EQUAL
          GOTO      RDAT3000 IF EOS
.
          MATCH     FDOCCODE,ATDTEDAD
          GOTO      RDAT3000 IF LESS
.
          MATCH     ATDTEDAD,TDOCCODE
          GOTO      RDAT3000 IF LESS
.
          PACK      TEMPDOCT,ATDTEDAD,SP70
          PACK      TEMPITMN,ATITEMNO,SP70
.
          COMPARE   FIVE,ATRECTYP                * Include All Procedures
          GOTO      RDAT6000 IF EQUAL
.
          COMPARE   ONE,ATRECTYP                 * Check Misc Charges Filter
          GOTO      RDAT3000 IF NOT EQUAL
.
.         Misc.Item
.
          MATCH     SP70,PINVCLM                 * Check if Work, Vet or TAC
          GOTO      RDAT3000 IF EQUAL
.
          PACK      KEY5,ANSC,ANSL,PINVCLM,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,RDAT3000
.
          PACK      KEY5,TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5,SP70
.
          RESET     KEY5
          SCAN      ANSW,KEY5                    * Work
          GOTO      RDAT5000 IF EQUAL
.
          RESET     KEY5
          SCAN      ANSM,KEY5                    * TAC
          GOTO      RDAT5000 IF EQUAL
.
          RESET     KEY5
          SCAN      ANSV,KEY5                    * Vet
          GOTO      RDAT5000 IF EQUAL
.
          GOTO      RDAT3000
.
RDAT5000  MATCH     SP70,ATMISGRP                * Misc Charge Group
          GOTO      RDAT3000 IF EQUAL
.
          PACK      KEY5,ANSF,ANSI,ATMISGRP,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,RDAT3000
.
          MATCH     ANSD,TCINDC7                 * Include in Doctors Revenue
          GOTO      RDAT3000 IF NOT EQUAL
.
          GOTO      RDAT7000
.
RDAT6000  MATCH     SP70,ATMISGRP                * Misc Charge Group
          GOTO      RDAT3000 IF EQUAL
.
          PACK      KEY5,ANSP,ANSG,ATMISGRP,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,RDAT3000
.
          MATCH     ANSD,TCINDC7                 * Include in Doctors Revenue
          GOTO      RDAT3000 IF NOT EQUAL
.
RDAT7000  DISPLAY   *P24:24,*V2LON,PINVNO;
.
          IF        COPTION=5
            MOVE      EMVIDATE,TEMPVDAT
            MOVE      EMVITIME,TEMPVTIM
          ELSE
            MOVE      PMEVADTE,TEMPVDAT
            MOVE      PMEVATIM,TEMPVTIM
          ENDIF
.
          PACK      TEMPVISN,DATNUMB,SP70
          PACK      TEMPINVN,ATINVNO,SP70
          PACK      TEMPTRNO,ATTRANS,SP70
          MOVE      " 1",TEMPUNIQ
          MOVE      ATPATAMT,TEMPIAMT
          MOVE      ATSERVS,TEMPSRVS
.
          IF        COPTION=5 | COPTION=6
            PACK      TEMPIDAT,PMCNDATE,SP70
          ELSE
            PACK      TEMPIDAT,PINVDTE,SP70        * Write Inv Line Item
          ENDIF
.
          MOVE      "C",TEMPSRCE
.
RDAT9000  PACK      KEY56,TEMPDOCT,TEMPVDAT,TEMPVTIM,TEMPVISN,TEMPINVN:
                          TEMPITMN,TEMPTRNO,TEMPUNIQ,SP70
          CALL      RATEMP1
          IF        OVRCD=1
            CALL      WRTEMP1
          ELSE
            MOVE      ZERO,F2
            MOVE      TEMPUNIQ,F2
            ADD       ONE,F2
            MOVE      F2,TEMPUNIQ
            GOTO      RDAT9000
          ENDIF
.
          CALL      TOTA0000                 * Write invoice total
          CALL      TOTB0000                 * Write doctor daily total
          CALL      TOTC0000                 * Write doctor total
.
          GOTO      RDAT3000
.
RDAT9999  RETURN
.
.********************************************************************
. Load emr revenue data to temp file - By transaction date
. This is the date the transaction happened. This is driven from
. the journal as a journal can't be back dated.
.********************************************************************
NDAT0000  DISPLAY   *P1:24,*EL:
                    *P10:24,"Visit No: ":
                    *P40:24,"Scanning: "
.
          PACK      KEY5,ANSJ,SP1,SP70
          CALL      RDSCODE1
NDAT1000  CALL      RDKCODE1
          BRANCH    OVRCD,NDAT9999
.
          MATCH     ANSJ,TCODE                   * Type journal
          GOTO      NDAT9999 IF NOT EQUAL
.
          MATCH     ANSI,TCINDC3                 * Include in doctors rev
          GOTO      NDAT1000 IF NOT EQUAL
.
          MOVE      ACODE,KEY2
          PACK      KEY24,KEY2,FROMDATE,SP70
          CALL      RDSJRNL1                     * Read journals using type
NDAT2000  CALL      RDKJRNL1                     * and fromdate
          BRANCH    OVRCD,NDAT1000
.
          MATCH     JCODE,KEY2                   * Correct journal type
          GOTO      NDAT1000 IF NOT EQUAL
.
          MATCH     JDATE,TODATE                 * Skip if outside date range
          GOTO      NDAT1000 IF LESS
.
.          PACK      KEY8,JINVN,SP70
.          CALL      RDPTINV1                     * Read Invoice
.          BRANCH    OVRCD,NDAT2000
.
.          MATCH     FROMDATE,PINVDTE             * Include this credit note
.          GOTO      NDAT3000 IF LESS             * if inv not in date range
.
.          MATCH     PINVDTE,TODATE               * Include this credit note
.          GOTO      NDAT3000 IF LESS             * if inv not in date range
.
.          GOTO      NDAT2000
.
NDAT3000  IF        COPTION=5
            PACK      KEY8,JADMN,SP70
            CALL      RDEMVIS1
            BRANCH    OVRCD,NDAT2000
.
            PACK      KEY8,EMVIADMN,SP70
            CALL      RDVISA1
            BRANCH    OVRCD,NDAT2000
.
            MATCH     "1234569",TRIAGCAT              * All triage cat
            GOTO      NDAT4000 IF EQUAL
.
            MATCH     SP70,EMVITRIG                   * No triage cat
            GOTO      NDAT2000 IF EQUAL
.
            PACK      KEY5,ANSA,ANSA,EMVITRIG,SP70
            CALL      RDCODE1
            BRANCH    OVRCD,NDAT2000
.
            SCAN      TCINDC1,TRIAGCAT
            RESET     TRIAGCAT                       * Check triage cat
            GOTO      NDAT2000 IF NOT EQUAL
          ELSE
            PACK      KEY8,JADMN,SP70
            CALL      RDPMEVT1
            BRANCH    OVRCD,NDAT2000
.
            PACK      KEY8,PMEVVISN,SP70
            CALL      RDVISA1
            BRANCH    OVRCD,NDAT2000
.
            PACK      KEY5,ANSE,ANSV,PMEVEVNT,SP70
            CALL      RDCODE1
            BRANCH    OVRCD,NDAT2000
.
            MATCH     ANSE,TCINDC1
            GOTO      NDAT2000 IF NOT EQUAL
          ENDIF
.
NDAT4000  MOVE      PINVAMT,TEMPIAMT
          MOVE      JINVN,PINVNO
          CALL      SDOC0000                     * Get serv.doct for invoice
.
          PACK      KEY22,JADMN,JINVN,JTRNS,SP70
          CALL      RDDTRE1
          BRANCH    OVRCD,NDAT2000
.
          COMPARE   THREE,ATRECTYP               * type Journal
          GOTO      NDAT2000 IF NOT EQUAL
.
          MATCH     SP70,SERVDOCT
          GOTO      NDAT2000 IF EQUAL
          GOTO      NDAT2000 IF EOS
.
          MATCH     FDOCCODE,SERVDOCT
          GOTO      NDAT2000 IF LESS
.
          MATCH     SERVDOCT,TDOCCODE
          GOTO      NDAT2000 IF LESS
.
          DISPLAY   *P24:24,*V2LON,PINVNO;
.
          PACK      TEMPDOCT,SERVDOCT,SP70
          PACK      TEMPITMN,ATTYPE,SP70         * Journal code
.
.
          IF        COPTION=5
            MOVE      EMVIDATE,TEMPVDAT
            MOVE      EMVITIME,TEMPVTIM
          ELSE
            MOVE      PMEVADTE,TEMPVDAT
            MOVE      PMEVATIM,TEMPVTIM
          ENDIF
.
          PACK      TEMPVISN,DATNUMB,SP70
          PACK      TEMPINVN,ATINVNO,SP70
          PACK      TEMPTRNO,ATTRANS,SP70
          MOVE      " 1",TEMPUNIQ
          MOVE      ATPATAMT,TEMPIAMT
          MOVE      ATSERVS,TEMPSRVS
.
          IF        COPTION=5 | COPTION=6
            PACK      TEMPIDAT,ATDDATE,SP70
          ELSE
            PACK      TEMPIDAT,PINVDTE,SP70        * Write Inv Line Item
          ENDIF
.
          MOVE      "J",TEMPSRCE
.
NDAT9000  PACK      KEY56,TEMPDOCT,TEMPVDAT,TEMPVTIM,TEMPVISN,TEMPINVN:
                          TEMPITMN,TEMPTRNO,TEMPUNIQ,SP70
          CALL      RATEMP1
          IF        OVRCD=1
            CALL      WRTEMP1
          ELSE
            MOVE      ZERO,F2
            MOVE      TEMPUNIQ,F2
            ADD       ONE,F2
            MOVE      F2,TEMPUNIQ
            GOTO      NDAT9000
          ENDIF
.
          CALL      TOTA0000                 * Write invoice total
          CALL      TOTB0000                 * Write doctor daily total
          CALL      TOTC0000                 * Write doctor total
.
          GOTO      NDAT2000
.
NDAT9999  RETURN
.
.****************************************************************************
. Write invoice total to temp file
.****************************************************************************
TOTA0000  PACK      KEY56,TEMPDOCT,TEMPVDAT,TEMPVTIM,TEMPVISN,TEMPINVN,Z70
          CALL      RDTEMP1
          IF        OVRCD=1
            PACK      TEMPITMN,Z70
            PACK      TEMPTRNO,Z70
            PACK      TEMPUNIQ,Z70
            MOVE      ZERO,TEMPSRVS
            MOVE      SP70,TEMPSRCE
            CALL      WRTEMP1
          ELSE
            ADD       ATPATAMT,TEMPIAMT
            CALL      UPTEMP1
          ENDIF
.
TOTA9999  RETURN
.
.****************************************************************************
. Write doctors daily total to temp file
.****************************************************************************
TOTB0000  PACK      KEY56,TEMPDOCT,TEMPVDAT,Z70
          CALL      RDTEMP1
          IF        OVRCD=1
            PACK      TEMPVTIM,Z70
            PACK      TEMPVISN,Z70
            PACK      TEMPINVN,Z70
            PACK      TEMPITMN,Z70
            PACK      TEMPTRNO,Z70
            PACK      TEMPUNIQ,Z70
            MOVE      ZERO,TEMPSRVS
            MOVE      SP70,TEMPSRCE
            CALL      WRTEMP1
          ELSE
            ADD       ATPATAMT,TEMPIAMT
            CALL      UPTEMP1
          ENDIF
.
TOTB9999  RETURN
.
.****************************************************************************
. Write doctors total to temp file
.****************************************************************************
TOTC0000  PACK      KEY56,TEMPDOCT,Z70 
          CALL      RDTEMP1
          IF        OVRCD=1
            PACK      TEMPVDAT,Z70
            PACK      TEMPVTIM,Z70
            PACK      TEMPINVN,Z70
            PACK      TEMPITMN,Z70
            PACK      TEMPTRNO,Z70
            PACK      TEMPUNIQ,Z70
            MOVE      ZERO,TEMPSRVS
            MOVE      SP70,TEMPSRCE
            CALL      WRTEMP1
          ELSE
            ADD       ATPATAMT,TEMPIAMT
            CALL      UPTEMP1
          ENDIF
.
TOTC9999  RETURN
.
.*        IO ROUTINES FOR TEMPORARY FILE                                    *
.****************************************************************************
RATEMP1   MOVE      ZERO,OVRCD
          READ      REPTEMP1,KEY56;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTEMP1   READ      REPTEMP1,KEY56;;
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    REPTEMP1;TEMPDOCT,TEMPVDAT,TEMPVTIM,TEMPVISN,TEMPINVN:
                             TEMPITMN,TEMPTRNO,TEMPUNIQ,TEMPIAMT,TEMPSRVS:
                             TEMPIDAT,TEMPSRCE
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   WRITE     REPTEMP1,KEY56;TEMPDOCT,TEMPVDAT,TEMPVTIM,TEMPVISN,TEMPINVN:
                             TEMPITMN,TEMPTRNO,TEMPUNIQ,TEMPIAMT,TEMPSRVS:
                             TEMPIDAT,TEMPSRCE
          RETURN
.
RDTEMP1   MOVE      ZERO,OVRCD
          READ      REPTEMP1,KEY56;TEMPDOCT,TEMPVDAT,TEMPVTIM,TEMPVISN,TEMPINVN:
                             TEMPITMN,TEMPTRNO,TEMPUNIQ,TEMPIAMT,TEMPSRVS:
                             TEMPIDAT,TEMPSRCE
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTEMP1   UPDATE    REPTEMP1;TEMPDOCT,TEMPVDAT,TEMPVTIM,TEMPVISN,TEMPINVN:
                             TEMPITMN,TEMPTRNO,TEMPUNIQ,TEMPIAMT,TEMPSRVS:
                             TEMPIDAT,TEMPSRCE
          RETURN
.
. **************************************************************************
.         I/O Includes                                                     *
.***************************************************************************
.
          INC       STD001IO
          INC       PMSHCPKY
          INC       TFILENAM                     * Generate Temp File Name
.
          INC       AAEDTRIO/INC
          INC       EMRVISIO/INC
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       PMSFCIIO/INC
          INC       PMSHCPIO/INC
          INC       PATVISIO/INC
          INC       PATCODIO/INC
          INC       PATJRNIO/INC
          INC       PATMA1IO/INC
          INC       PATINVIO/INC
          INC       PMSCNOIO/INC
          INC       PMSEVTIO/INC
          INC       WEBERRIO/INC                 * Web Server Error Logging
.

.***************************************************************************
.* System    :   Web Security                                              *
.* Program   :   IBASEC01                                                  *
.* Desc      :   Command line add, update and copy web user id             *
.***************************************************************************
.* Date      :   12/06/2008                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   This program will add, update and copy web users from     *
.*           :   a command line prompt and maintain passwords              *
.* Mods      :                                                             *
.*           :  V10.06.01  25/03/2015  Ebon Clements       CAR 310322      *
.*           :             Added WEBLOGNM - Web Long Login Name to add     *
.*           :             user, copy user and activate/inactivate user    *
.*           :  V10.04.01  Peter McMullen   CAR 300759                     *
.*           :             Default WBSELOGN from WBSEUID                   *
.*               V9.10.00  Ebon Clements    CAR 168863                     *
.*           :             Created program                                 *
.***************************************************************************
.
          INC       STD002FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       MLTSECFD/INC
          INC       WEBCONFD/INC
          INC       WEBSECFD/INC
          INC       WEBSTEFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
COMM127   DIM       127
COPUSRID  DIM       10     * User ID
ERRORMES  DIM       127
.
NEWPASSW  DIM       10     * New password
PASSWORD  DIM       10     * Password
SECPASSC  DIM       4
UPDTTYPE  DIM       1
USERNAME  DIM       80
VERPASSW  DIM       10     * Verify password
WEBUSRID  DIM       10     * User ID
WEBUNAME  DIM       30     * User Name
WEBULURL  DIM       40     * Login URL
WEBPASWD  DIM       10     * Password
WEBNEWPS  DIM       10     * New web Password
WEBLOGNM  DIM       80     * New web long login name
.
NINETY1   FORM      "91"
QUOTE     INIT      "#""
REPUNIQ   INIT      "yzxywxvwuvtustrsqrpqopnomnlmkljkijhighfgefdecdbcab"
ZEND      INIT      "z~~~"
.
PRGID     INIT      "IBASEC01"
PRGNAM    INIT      "Web User ID Maintenance"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
          BRANCH    EXIT,ML9999
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000,ML2000: * proceed with clean up
                    ML3000,ML4000,ML5000
.
          GOTO      ML9999
.
ML1000    CALL      ADDW0000               * Add user id
          GOTO      ML9999
.
ML2000    CALL      COPY0000               * Copy user id
          GOTO      ML9999
.
ML3000    CALL      UPDT0000               * Update user name
          GOTO      ML9999
.
ML4000    CALL      ACTV0000               * Activate/Inactivate user id
          GOTO      ML9999
.
ML5000    CALL      PASW0000               * Change password   
          GOTO      ML9999
.
ML9999    STOP                             * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      SETX0000
.
          CALL      DISPHEAD                  * display heading
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,NINETY1;*19,CWEBPCMD,*59,CWEBPFIL
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          MATCH     SP70,CWEBPCMD
          IF        @EQUAL
            MOVE      "Remote Password Command.",ERRORMES
            CALL      PERR0000
            GOTO      INIT9999
          ENDIF
.
          MATCH     "00000000",CWEBPCMD
          IF        @EQUAL
            MOVE      "Remote Password Command.",ERRORMES
            CALL      PERR0000
            GOTO      INIT9999
          ENDIF
.
          MATCH     SP70,CWEBPFIL
          IF        @EQUAL
            MOVE      "Remote Password File.",ERRORMES
            CALL      PERR0000
            GOTO      INIT9999
          ENDIF
.
          MATCH     "00000000",CWEBPFIL
          IF        @EQUAL
            MOVE      "Remote Password File.",ERRORMES
            CALL      PERR0000
            GOTO      INIT9999
          ENDIF
.
          OPEN      WEBSECA1,"websecaf"
          OPEN      WEBSECA7,"websecaf"
          OPEN      WEBSTEA1,"websteaf"
          OPEN      IHAPASS1,"ihapassf"
.
          IF        IBCNMHOS=1
            OPEN      MLTSECA1,"mltsecaf"
          ENDIF
.
          MOVE      ZERO,EXIT
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Add User":
                    *P1:6,*V2LON,TWO,*HOFF,". Copy User":
                    *P1:7,*V2LON,THREE,*HOFF,". Change User Name":
                    *P1:8,*V2LON,FOUR,*HOFF,". Activate/Inactivate User":
                    *P1:9,*V2LON,FIVE,*HOFF,". Change Password"
.
OPTN0500  KEYIN     *P1:11,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:11,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000,OPTN9000:   * run clean-up
                    OPTN9000,OPTN9000,OPTN9000
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               ADDW0000                                 *
.*                                                                        *
.**************************************************************************
ADDW0000  CALL      KYID0000               * Keyin user id
          BRANCH    EXIT,ADDW9999
.
          CALL      KYNM0000               * Keyin user name
          BRANCH    EXIT,ADDW9999
.
          CALL      KYUL0000               * Keyin login url
          BRANCH    EXIT,ADDW9999
.
          CALL      KYPS0000               * Keyin password
          BRANCH    EXIT,ADDW9999
.
          CALL      KYLN0000               * Keyin login name
          BRANCH    EXIT,ADDW9999
.
          CALL      ADDU0000               * Add user
.
ADDW9999  RETURN
+
.**************************************************************************
.*                               COPY0000                                 *
.*                                                                        *
.**************************************************************************
COPY0000  CALL      KYID0000               * Keyin user id
          BRANCH    EXIT,COPY9999
.
          CALL      KYNM0000               * Keyin user name
          BRANCH    EXIT,COPY9999
.
          CALL      KYCP0000               * Keyin copy from user id
          BRANCH    EXIT,COPY9999
.
          CALL      KYPS0000               * Keyin password
          BRANCH    EXIT,COPY9999
.
          CALL      KYLN0000               * Keyin login name
          BRANCH    EXIT,COPY9999
.
          CALL      COPU0000               * Copy user
.
COPY9999  RETURN
+
.**************************************************************************
.*                               KYID0000                                 *
.*                                                                        *
.**************************************************************************
KYID0000  KEYIN     *P1:13,*EL,"Web User ID : ",*V2LON,WEBUSRID
.
          PACK      WEBUSRID,WEBUSRID,SP70
          MATCH     SP70,WEBUSRID
          GOTO      KYID9000 IF EQUAL
.
          PACK      KEY10,WEBUSRID,SP70
          CALL      RDWBSE1
          COMPARE   ZERO,OVRCD
          GOTO      KYID9100 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KYID9999
.
KYID9000  MOVE      "Security ID is a Mandatory Field.",ERRORMES
          CALL      PERR0000
          GOTO      KYID9999
.
KYID9100  MOVE      "Security ID already on file.",ERRORMES
          CALL      PERR0000
          GOTO      KYID9999
.
KYID9999  RETURN
+
.**************************************************************************
.*                               KYLN0000                                 *
.*                                                                        *
.**************************************************************************
KYLN0000  KEYIN     *P1:17,*EL,"Web Login Name : ",*V2LON,WEBLOGNM
.
          PACK      WEBLOGNM,WEBLOGNM,SP70
          MATCH     SP70,WEBLOGNM
          GOTO      KYLN0000 IF EQUAL
.
          PACK      KEY80,WEBLOGNM,SP70
          CALL      RDWBSE7
          COMPARE   ZERO,OVRCD
          GOTO      KYLN9100 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KYLN9999
.
KYLN9000  MOVE      "Login Name is a Mandatory Field.",ERRORMES
          CALL      PERR0000
          GOTO      KYLN9999
.
KYLN9100  MOVE      "Login Name already on file.",ERRORMES
          CALL      PERR0000
          GOTO      KYLN9999
.
KYLN9999  RETURN
+
.**************************************************************************
.*                               KULN0000                                 *
.*                                                                        *
.**************************************************************************
KULN0000  KEYIN     *P1:15,*EL,"New Web Login Name : ",*V2LON,WEBLOGNM
.
          PACK      WEBLOGNM,WEBLOGNM,SP70
          MATCH     SP70,WEBLOGNM
          GOTO      KULN0000 IF EQUAL
.
          MATCH     WBSELOGN,WEBLOGNM       * Check if login name has changed
          GOTO      KULN8000 IF EQUAL
.
          PACK      KEY80,WEBLOGNM,SP70     * Error if new login name is
          CALL      RAWBSE7                 * already on file
          COMPARE   ZERO,OVRCD
          GOTO      KULN9100 IF EQUAL
.
KULN8000  MOVE      ZERO,EXIT
          GOTO      KULN9999
.
KULN9000  MOVE      "New Login Name is a Mandatory Field.",ERRORMES
          CALL      PERR0000
          GOTO      KULN9999
.
KULN9100  MOVE      "New Login Name already on file.",ERRORMES
          CALL      PERR0000
          GOTO      KULN9999
.
KULN9999  RETURN
+
.**************************************************************************
.*                               KYCP0000                                 *
.*                                                                        *
.**************************************************************************
KYCP0000  KEYIN     *P1:15,*EL,"Copy Web User ID : ",*V2LON,COPUSRID
.
          PACK      COPUSRID,COPUSRID,SP70
          MATCH     SP70,COPUSRID
          GOTO      KYCP9000 IF EQUAL
.
          PACK      KEY10,COPUSRID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,KYCP9100    
.         
          MOVE      ZERO,EXIT
          GOTO      KYCP9999
.         
KYCP9000  MOVE      "Copy Security ID is a Mandatory Field.",ERRORMES
          CALL      PERR0000
          GOTO      KYCP9999
.         
KYCP9100  MOVE      "Invalid Copy From Security ID.",ERRORMES
          CALL      PERR0000
          GOTO      KYCP9999
.
KYCP9999  RETURN
+
.**************************************************************************
.*                               KYNM0000                                 *
.*                                                                        *
.**************************************************************************
KYNM0000  KEYIN     *P1:14,*EL,"Web User Name : ",*V2LON,WEBUNAME
.
          PACK      WEBUNAME,WEBUNAME,SP70
          MATCH     SP70,WEBUNAME
          GOTO      KYNM9000 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KYNM9999
.
KYNM9000  MOVE      "Security ID Name is a Mandatory Field.",ERRORMES
          CALL      PERR0000
          GOTO      KYNM9999
.
KYNM9999  RETURN
+
.**************************************************************************
.*                               KYUL0000                                 *
.*                                                                        *
.**************************************************************************
KYUL0000  KEYIN     *P1:15,*EL,"Login URL : ",*V2LON,WEBULURL
.
          PACK      WEBULURL,WEBULURL,SP70
          MATCH     SP70,WEBULURL
          GOTO      KYUL9000 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KYUL9999
.
KYUL9000  MOVE      "Login ULR is a Mandatory Field.",ERRORMES
          CALL      PERR0000
          GOTO      KYUL9999
.
KYUL9999  RETURN
+
.**************************************************************************
.*                               KYPS0000                                 *
.*                                                                        *
.**************************************************************************
KYPS0000  KEYIN     *P1:16,*EL,"Password : ",*EOFF,*V2LON,WEBPASWD
.
          PACK      WEBPASWD,WEBPASWD,SP70
          MATCH     SP70,WEBPASWD
          GOTO      KYPS9000 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KYPS9999
.
KYPS9000  MOVE      "Password is a Mandatory Field.",ERRORMES
          CALL      PERR0000
          GOTO      KYPS9999
.
KYPS9999  RETURN
+
.**************************************************************************
.*                               KYNP0000                                 *
.*                                                                        *
.**************************************************************************
KYNP0000  KEYIN     *P1:17,*EL,"New Password : ",*EOFF,*V2LON,WEBNEWPS
.
          PACK      WEBNEWPS,WEBNEWPS,SP70
          MATCH     SP70,WEBNEWPS
          GOTO      KYNP9000 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KYNP9999
.
KYNP9000  MOVE      "New Password is a Mandatory Field.",ERRORMES
          CALL      PERR0000
          GOTO      KYNP9999
.
KYNP9999  RETURN
+
.**************************************************************************
.*                               ADDU0000                                 *
.*                                                                        *
.**************************************************************************
ADDU0000  MOVE      WEBUSRID,WBSEUID
          MOVE      WEBLOGNM,WBSELOGN
          MOVE      WEBUNAME,WBSENAM
          MOVE      SP70,WBSEDOC 
.
          CALL      GNID0000                     * Generate new passcode
          BRANCH    EXIT,ADDU9999
.
          MOVE      SECPASSC,WBSEPCD             
          CALL      ALLP0000                     * Save new passcode in ihapassf
.
          MOVE      SP70,WBSEWRD 
          MOVE      SP70,WBSESIT 
          MOVE      SP70,WBSECLI 
          MOVE      SP70,WBSEOCG 
          MOVE      WEBULURL,WBSEDIR 
          MOVE      SP70,WBSEESC 
          MOVE      SP70,WBSEEUC 
          MOVE      ONE,WBSEACT 
          MOVE      SP70,WBSEEXP 
          MOVE      SP70,WBSEPSE 
          MOVE      SP70,WBSECNT 
          MOVE      SP70,WBSEAUT 
          MOVE      SP70,WBSEAGE 
          MOVE      SP70,WBSEDFM 
          MOVE      SP70,WBSEGPCD
          MOVE      SP70,WBSEGPPC
          MOVE      SP70,WBSEGPCN
          MOVE      SP70,WBSEMESA
          MOVE      SP70,WBSELACD
          MOVE      SP70,WBSELACT
          MOVE      SP70,WBSEDEPT
          MOVE      SP70,WBSECXA 
          MOVE      SP70,WBSEDALL
          MOVE      SP70,WBSEDTYP
          MOVE      SP70,WBSEHLOC
          MOVE      SP70,WBSEVWSL
          MOVE      SP70,WBSEADSL
          MOVE      SP70,WBSEDGSI
          MOVE      SP70,WBSEFPCD
          MOVE      SP70,WBSECASH
          MOVE      SP70,WBSEMRTS
          MOVE      SP70,WBSEHOSP
          MOVE      SP70,WBSELTYP
          MOVE      SP70,WBSEFDEP
          MOVE      SP70,WBSENURS
.
          CALL      IBACLOCK
          PACK      WBSECDAT,CCC,CYY,CMM,CDD
          REP       " 0",WBSECDAT
          MOVE      CTIMEIS,WBSECTIM
          MOVE      SP70,WBSECUID
          MOVE      SP70,WBSEUUID
          MOVE      SP70,WBSEUDAT
          MOVE      SP70,WBSEUTIM
.         
          PACK      KEY10,WBSEUID,SP70
          CALL      WRWBSE1
.
          MOVE      "A",UPDTTYPE
          MOVE      WBSELOGN,USERNAME
          MOVE      WEBPASWD,NEWPASSW
          MOVE      WEBPASWD,VERPASSW
          CALL      PASS0000
.
          MOVE      ZERO,EXIT
          GOTO      ADDU9999
.
ADDU9000  MOVE      "CONTACT IBA - passcode allocation full.",ERRORMES
          CALL      PERR0000
          GOTO      ADDU9999
.
ADDU9999  RETURN
+
.**************************************************************************
.*                               COPU0000                                 *
.*                                                                        *
.**************************************************************************
COPU0000  PACK      KEY10,COPUSRID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,COPU9999     
.
          MOVE      WEBUSRID,WBSEUID
          MOVE      WEBLOGNM,WBSELOGN
          MOVE      WEBUNAME,WBSENAM
.
          CALL      GNID0000                     * Generate new passcode
          BRANCH    EXIT,COPU9999
.         
          MOVE      SECPASSC,WBSEPCD
          CALL      ALLP0000                     * Save new passcode in ihapassf
.
          MOVE      SP70,WBSECUID
          CALL      IBACLOCK
          PACK      WBSECDAT,CCC,CYY,CMM,CDD
          REP       " 0",WBSECDAT
          MOVE      CTIMEIS,WBSECTIM
          MOVE      SP70,WBSEUUID
          MOVE      SP70,WBSEUDAT
          MOVE      SP70,WBSEUTIM
.
          PACK      KEY10,WBSEUID,SP70
          CALL      WRWBSE1
.
          CALL      CPLEV000              * copy security levels
          CALL      CPHOS000              * copy hospital access
.
          MOVE      "A",UPDTTYPE
          MOVE      WBSELOGN,USERNAME
          MOVE      WEBPASWD,NEWPASSW
          MOVE      WEBPASWD,VERPASSW
          CALL      PASS0000
.
          MOVE      ZERO,EXIT
          GOTO      COPU9999
.
COPU9000  MOVE      "CONTACT IBA - passcode allocation full.",ERRORMES
          CALL      PERR0000
          GOTO      COPU9999
.
COPU9999  RETURN
+
.------------------------------------------------------------
.  Copy Levels from COPUSRID to WEBUSRID
.------------------------------------------------------------
CPLEV000  PACK      COPUSRID,COPUSRID,SP70
          PACK      KEY18,COPUSRID,SP70
          CALL      RSWBST1
CPLEV100  CALL      RKWBST1
          BRANCH    OVRCD,CPLEV999
          MATCH     COPUSRID,WBSTUID
          GOTO      CPLEV999 IF NOT EQUAL
.
          MOVE      WEBUSRID,WBSTUID
          PACK      KEY18,WBSTUID,WBSTPRG
          CALL      RAWBST1
          BRANCH    OVRCD,CPLEV200
          GOTO      CPLEV100
.
CPLEV200  CALL      WRWBST1
          PACK      KEY18,COPUSRID,WBSTPRG
          CALL      RDWBST1
          GOTO      CPLEV100
.
CPLEV999  RETURN
+
.------------------------------------------------------------
.  Copy Hospital Access from COPUSRID to WEBUSRID
.------------------------------------------------------------
CPHOS000  COMPARE   ONE,IBCNMHOS
          GOTO      CPHOS999 IF NOT EQUAL
.
          PACK      COPUSRID,COPUSRID,SP70
          PACK      WEBUSRID,WEBUSRID,SP70
          PACK      KEY13,COPUSRID,SP70
          CALL      RDMLSEC1
          BRANCH    OVRCD,CPHOS050
          GOTO      CPHOS200
.
CPHOS050  PACK      KEY13,COPUSRID,SP70
          CALL      RSMLSEC1
CPHOS100  CALL      RKMLSEC1
          BRANCH    OVRCD,CPHOS999
          MATCH     COPUSRID,MLSCUSID
          GOTO      CPHOS999 IF NOT EQUAL
.
CPHOS200  MOVE      WEBUSRID,MLSCUSID
          PACK      KEY13,MLSCUSID,MLSCHOSP,SP70
          CALL      RAMLSEC1
          BRANCH    OVRCD,CPHOS300
          GOTO      CPHOS100
.
CPHOS300  CALL      WRMLSEC1
          PACK      KEY13,COPUSRID,MLSCHOSP,SP70
          CALL      RDMLSEC1
          GOTO      CPHOS100
.
CPHOS999  RETURN
+
.-------------------------------------------------
. Allocate a passcode and write record to ihapasaf
.-------------------------------------------------
ALLP0000  MOVE    SECPASSC,SECODE
          MOVE    SP70,SECNUMB
          MOVE    WBSENAM,SECUSER
          MOVE    SP70,SECACCT
          MOVE    SP70,SECNUMB1
          MOVE    SP70,SECNUMB2
          MOVE    ZERO,SECDIRCT
          MOVE    SP70,SECMENU 
          MOVE    SP70,SECPRINT
          MOVE    SP70,SECDEPT 
          MOVE    SP70,SECMNNUM
          MOVE    SP70,SECSPARE
.
          PACK    KEY4,SECODE
          CALL    WRPASS1
.
ALLP9999  RETURN
+
.**********************************************************************
. Get next bank rec unique id
.**********************************************************************
GNID0000  MOVE      ZERO,EXIT
          MOVE      SP70,SECPASSC
          PACK      KEY4,ZEND
          CALL      RDSPASS1
          CALL      RDPPASS1
          BRANCH    OVRCD,GNID1150
.
          UNPACK    SECODE,ANS,KEY3
          MATCH     "a",ANS
          GOTO      GNID1150 IF LESS
          MOVE      ZERO,F3
          MOVE      KEY3,F3
.
GNID1100  COMPARE   "999",F3          * at end of group ?
          GOTO      GNID1110 IF NOT EQUAL
.
          MATCH     "z",ANS
          GOTO      GNID9000 IF EQUAL
.
          REP       REPUNIQ,ANS
          MOVE      ZERO,F3
.
GNID1110  ADD       ONE,F3
          MOVE      F3,D3
          REP       " 0",D3
          PACK      KEY4,ANS,D3,SP70
          CALL      RDPASS1
          BRANCH    OVRCD,GNID9500
          GOTO      GNID1100
.
.---- no records !! ----
.
GNID1150  MOVE      ONE,F3
          MOVE      "a",ANS
          MOVE      F3,D3
          REP       " 0",D3
          PACK      KEY4,ANS,D3,SP70
          GOTO      GNID9500
.
GNID9000  MOVE      ONE,EXIT          * Passcode allocation full
          GOTO      GNID9999
.
GNID9500  MOVE      KEY4,SECPASSC
GNID9999  RETURN
+
.------------------------------------------------------------
.         PASS0000  User Password Maintenance
.------------------------------------------------------------
PASS0000  MATCH     "A",UPDTTYPE
          GOTO      PASS3000 IF EQUAL
.
          MATCH     "U",UPDTTYPE
          GOTO      PASS1000 IF EQUAL
.
          GOTO      PASS9999
.
PASS1000  MATCH     PASSWORD,NEWPASSW
          GOTO      PASS9400 IF EQUAL
.
          MOVE      SP70,TASKID               * password verification
          CLEAR     COMM127
          STRIP     CWEBPCMD
          APPEND    CWEBPCMD,COMM127
          APPEND    " -t ",COMM127
          STRIP     CWEBPFIL
          APPEND    CWEBPFIL,COMM127
          APPEND    SP1,COMM127
          APPEND    QUOTE,COMM127
          STRIP     USERNAME
          APPEND    USERNAME,COMM127
          APPEND    QUOTE,COMM127
          APPEND    SP1,COMM127
          APPEND    QUOTE,COMM127
          STRIP     PASSWORD
          APPEND    PASSWORD,COMM127
          APPEND    QUOTE,COMM127
          RESET     COMM127
          EXECUTE   COMM127,TASKID
          MATCH     "0",TASKID
          GOTO      PASS9200 IF NOT EQUAL
.
          MOVE      SP70,TASKID                * password updating
          CLEAR     COMM127
          STRIP     CWEBPCMD
          APPEND    CWEBPCMD,COMM127
          APPEND    SP1,COMM127
          STRIP     CWEBPFIL
          APPEND    CWEBPFIL,COMM127
          APPEND    SP1,COMM127
          APPEND    QUOTE,COMM127
          STRIP     USERNAME
          APPEND    USERNAME,COMM127
          APPEND    QUOTE,COMM127
          APPEND    SP1,COMM127
          APPEND    QUOTE,COMM127
          STRIP     NEWPASSW
          APPEND    NEWPASSW,COMM127
          APPEND    QUOTE,COMM127
          APPEND    SP1,COMM127
          APPEND    QUOTE,COMM127
          STRIP     VERPASSW
          APPEND    VERPASSW,COMM127
          APPEND    QUOTE,COMM127
          RESET     COMM127
          EXECUTE   COMM127,TASKID
          MATCH     "0",TASKID
          GOTO      PASS9100 IF NOT EQUAL
.
          GOTO      PASS9999
.
PASS3000  MOVE      SP70,TASKID                * password updating
          CLEAR     COMM127
          STRIP     CWEBPCMD
          APPEND    CWEBPCMD,COMM127
          APPEND    SP1,COMM127
          STRIP     CWEBPFIL
          APPEND    CWEBPFIL,COMM127
          APPEND    SP1,COMM127
          APPEND    QUOTE,COMM127
          STRIP     USERNAME
          APPEND    USERNAME,COMM127
          APPEND    QUOTE,COMM127
          APPEND    SP1,COMM127
          APPEND    QUOTE,COMM127
          STRIP     NEWPASSW
          APPEND    NEWPASSW,COMM127
          APPEND    QUOTE,COMM127
          APPEND    SP1,COMM127
          APPEND    QUOTE,COMM127
          STRIP     VERPASSW
          APPEND    VERPASSW,COMM127
          APPEND    QUOTE,COMM127
          RESET     COMM127
          EXECUTE   COMM127,TASKID
          MATCH     "0",TASKID
          GOTO      PASS9300 IF NOT EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      PASS9999
.
PASS9100  CLEAR     ERRORMES
          APPEND    "Password change unsuccessful. Error: ",ERRORMES
          APPEND    TASKID,ERRORMES
          RESET     ERRORMES
          CALL      PERR0000
          GOTO      PASS9999
.
PASS9200  MOVE      "Password invalid. Error: ",ERRORMES
          CALL      PERR0000
          GOTO      PASS9999
.
PASS9300  CLEAR     ERRORMES
          APPEND    "Password addition unsuccessful. Error:",ERRORMES
          APPEND    TASKID,ERRORMES
          RESET     ERRORMES
          CALL      PERR0000
          GOTO      PASS9999
.
PASS9400  MOVE      "ERROR: Password Must Be Changed",ERRORMES
          CALL      PERR0000
          GOTO      PASS9999
.
PASS9999  RETURN
+
.**************************************************************************
.*                               UPDT0000                                 *
.*                                                                        *
.**************************************************************************
UPDT0000  CALL      KUID0000               * Keyin update user id
          BRANCH    EXIT,UPDT9999
.
          CALL      KYNM0000               * Keyin user name
          BRANCH    EXIT,UPDT9999
.
          MOVE      WEBUNAME,WBSENAM
.
          CALL      IBACLOCK
          PACK      WBSEUDAT,CCC,CYY,CMM,CDD
          REP       " 0",WBSEUDAT
          MOVE      CTIMEIS,WBSEUTIM
.
          CALL      UPWBSE1
.
UPDT9999  RETURN
+
.**************************************************************************
.*                               ACTV0000                                 *
.*                                                                        *
.**************************************************************************
ACTV0000  CALL      KUID0000               * Keyin update user id
          BRANCH    EXIT,ACTV9999
.
          CALL      KEYA0000               * Keyin activate or inactivate
          BRANCH    EXIT,ACTV9999
.
          CALL      KULN0000               * Keyin update login name
          BRANCH    EXIT,ACTV9999
.
          MOVE      WEBLOGNM,WBSELOGN
.
          MATCH     "I",UPDTTYPE
          IF        @EQUAL
            MOVE      ZERO,WBSEACT           * Inactive
          ENDIF
.
          MATCH     "A",UPDTTYPE
          IF        @EQUAL
            MOVE      ONE,WBSEACT            * Active
          ENDIF
.
          CALL      IBACLOCK
          PACK      WBSEUDAT,CCC,CYY,CMM,CDD
          REP       " 0",WBSEUDAT
          MOVE      CTIMEIS,WBSEUTIM
.
          CALL      UPWBSE1
.
ACTV9999  RETURN
+
.**************************************************************************
.*                               KEYA0000                                 *
.*                                                                        *
.**************************************************************************
KEYA0000  KEYIN     *P1:14,*EL,"(A)ctivate or (I)nactivate : ",*V2LON,UPDTTYPE
.
          PACK      UPDTTYPE,UPDTTYPE,SP70
.
          MATCH     ANSA,UPDTTYPE                * Activate
          GOTO      KEYA9000 IF EQUAL
.
          MATCH     ANSI,UPDTTYPE                * Inactivate
          GOTO      KEYA9000 IF EQUAL
.
          MOVE      "Invalid update type.",ERRORMES
          CALL      PERR0000
          GOTO      KEYA9999
.
KEYA9000  MOVE      ZERO,EXIT
          GOTO      KEYA9999
.
KEYA9999  RETURN
+
.**************************************************************************
.*                               KUID0000                                 *
.*                                                                        *
.**************************************************************************
KUID0000  KEYIN     *P1:13,*EL,"Update Web User ID : ",*V2LON,WEBUSRID
.
          PACK      WEBUSRID,WEBUSRID,SP70
          MATCH     SP70,WEBUSRID
          GOTO      KUID9000 IF EQUAL
.
          PACK      KEY10,WEBUSRID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,KUID9100
.
          MOVE      ZERO,EXIT
          GOTO      KUID9999
.
KUID9000  MOVE      "Security ID is a Mandatory Field.",ERRORMES
          CALL      PERR0000
          GOTO      KUID9999
.
KUID9100  MOVE      "Invalid Security ID.",ERRORMES
          CALL      PERR0000 
          GOTO      KUID9999
.
KUID9999  RETURN
+
.**************************************************************************
.*                               PASW0000                                 *
.*                                                                        *
.**************************************************************************
PASW0000  CALL      KUID0000               * Keyin update user id
          BRANCH    EXIT,PASW9999
.
          CALL      KYPS0000               * Keyin password
          BRANCH    EXIT,PASW9999
.
          CALL      KYNP0000               * Keyin new password
          BRANCH    EXIT,PASW9999
.
          MOVE      "U",UPDTTYPE
          MOVE      WEBPASWD,PASSWORD      * Verify original password
.
          MOVE      WBSELOGN,USERNAME  
          MOVE      WEBNEWPS,NEWPASSW      * New passord
          MOVE      WEBNEWPS,VERPASSW
          CALL      PASS0000
.
PASW9999  RETURN
+
.**************************************************************************
.*                               PERR0000                                 *
.*                                                                        *
.**************************************************************************
PERR0000  MOVE      ONE,EXIT
          DISPLAY   *P1:24,*EL,*B,*V2LON,ERRORMES
.
          CLEAR     COMM127 
          APPEND    "ibasec01.us1 ",COMM127
          APPEND    "'",COMM127
          APPEND    ERRORMES,COMM127
          APPEND    "'",COMM127
          RESET     COMM127
.
          EXECUTE   COMM127,TASKID
.
PERR9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD002IO
.
          INC       MLTSECIO/INC
          INC       WEBSECIO/INC
          INC       WEBSTEIO/INC

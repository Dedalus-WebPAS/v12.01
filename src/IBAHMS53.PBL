.******************************************************************************
.*               P R O G R A M  S U M M A R Y                                 *
.*               -------------  -------------                                 *
.*                                                                            *
.*     PROGRAM NAME:      IBAHMS53                                            *
.*                                                                            *
.*     FUNCTION:          3B CERTIFICATE PENDING                              *
.*                                                                            *
.*     DATE WRITTEN:      13/06/86                                            *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.*                                                                            *
.******************************************************************************
.*        V12.00.01 18.08.2025  Ebon Clements  TSK 0964196                    *
.*                  Corrected 4 characters day calculation                    *
.*                  Corrected temp file length                                *
.*        V11.05.02 05.12.2024  DA Sarkies     TSK 0925848                    *
.*                  Increased size of ADYHOSP variables to 4 chars            *
.*        V11.05.01 03.12.2024  DA Sarkies     TSK 0925848                    *
.*                  Increased size of LOS days to 4 chars                     *
.******************************************************************************
.*        V11.03.01 24/11/2022  J.Tan          TSK 0925848                    *
.*                  Mod to exclude daycases                                   *
.*        V11.01.01 24/11/2021  Ebon Clements  TSK 0913885                    *
.*                  Include on leave patients on the report - DISPDA          *
.*                  V10.13.01 stopped them printing                           *
.******************************************************************************
.*        V10.13.02 05/12/2018  Don Nguyen     TSK 0838558                    *
.*                  Deleted temp file (PSTROL) after processing               *
.*        V10.13.01 11/09/2018  Thanh T        TSK 0861120                    *
.*                  Fixed issue with report returning patients which is not   *
.*                  current                                                   *
.*        V10.12.01 04/06/2018  Ebon Clements  TSK 0858122                    *
.*                  Fixed temp file length after form to dim change           *
.******************************************************************************
.*        V10.11.01 25/10/2017  J.Tan          TSK 0842350                    *
.*                  Mod FORM fields to DIM in tempfile                        *
.*        V10.10.01 20/04/2017  Ania P         CAR 260631                     *
.*                  Remove use of PORT                                        *
.*        V10.08.01 06/09/2016  J.Tan          TSK 0813787                    *
.*                  Added Adm.time to Tempfile file                           *
.*        V10.04.02 23/04/2014  Jill Parkinson CAR 292687                     *
.*                  Added value of 2 for PT3BHOSP - only use previous days as *
.*                  entered on the admission screen                           *
.*        V10.04.01 15/04/2014  Sandra Barcham   CAR 300109                   *
.*                  Changed Daynum from form 3 to form 5                      *
.*        V10.03.04 20/09/2013  Patrick Adair    CAR 290995                   *
.*                  Do not show 3B expiry date if it is less than visit date  *
.*        V10.03.03 21/12/2012  J.Tan            CAR 272547                   *
.*                  Fixed 3B expiry date                                      *
.*        V10.03.02 17/12/2012  J.Tan            CAR 272547                   *
.*                  Mod to check patceraf file for 3B expiry date             *
.*        V10.03.01 15/12/2011  Jill Parkinson   CAR 256858                   *
.*                  Mod to use ADYHOSP if PTMI3BDY is blank                   *
.*        V10.01.02 03/03/2011  Jill Parkinson   CAR 233269                   *
.*                  Mod to use PTMI3BDY                                       *
.*        V10.01.01 07/01/2011  Mike Laming      CAR 235222                   *
.*                  Added new test for multi Hospital at VIST020              *
.******************************************************************************
.*        V9.12.01  14/12/2009  Ebon Clements    CAR 210584                   *
.*                  Added claim code/health fund column                       *
.*        V9.11.02  27/02/2009  J.Tan            CAR 188906                   *
.*                  Fixed pat.with same visitdate for previous n current visit*
.*        V9.11.01  03/12/2008  J.Tan            CAR 184490                   *
.*                  Mods to save days of hospitalisation of previous visits   *
.*        V9.04.01  02/09/2004  Ebon Clements    CAR 53144                    *
.*                  Added Multi-Hospital processing                           *
.*        V9.02.02  28/11/2001  Steve Downing  SRF 13374                      *
.*                  Fixed check on C3BDAYS, fixed bug in length of total days *
.*        V9.02.01  09/10/2001  Steve Downing  SRF 13374                      *
.*                  Fixed check on C3BDAYS, to allow for stay at other hosp.  *
.******************************************************************************
.*        V5.09.B01 12/02/2001  Greg Horvat                                   *
.*                  Replaced the call to CALC with DAYSEP                     *
.*        V5.08.01  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.07.B03 27/01/1999  Nicole Harrington 507 rebound                 *
.*                  Mods to print century                                     *
.*        V5.07.B02 10/12/1998  Jim Stathopoulos                              *
.*                  CDATE Changes                                             *
.*        V5.07.B01 16/11/1998  Jim Stathopoulos                              *
.*                  507 Changes                                               *
.******************************************************************************
.*        V5.04.01  02/05/1997  Steve Armstrong   SRF 642857                  *
.*                  Changed temp file name "pstidx" to standard format        *
.******************************************************************************
. *                       V5.01.001 16/09/91 Graeme Williams               *
. *                                 Adapted from IBAPAT96, version 5.01.123*
. *                                 SRF 108797, Quote 7027                 *
. *                                                                        *
. *    AUTHOR:            PAUL LO                                          *
. *                                                                        *
. **************************************************************************
.
          INC       STD001FD   
.
          INC       PATCODFD/INC
          INC       IBACONFD/INC
          INC       PATMI1FD/INC
          INC       PATMA1FD/INC
          INC       PATDSCFD/INC
          INC       PATVISFD/INC
          INC       PATHSPFD/INC
          INC       PMSVX1FD/INC
          INC       PATCERFD/INC
          INC       IBASEQFD/INC 
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.         
.               Temporary index file for report
.
TEMPFILE  IFILE   SQL, FIXED=149, KEY="U57-64,1-8,9-14,15-22"
PSTROL    FILE    ASCII, FIXED=256
.
. NAME     TYPE   LENGTH   PHYSICAL   START LOC.   DESCRIPTION
. ----     ----   ------   --------   ----------   -----------
DIDXDATE   DIM      8         8           1        (99999999-VISIT DATE)
DIDXTIME   DIM      6         6           9        (999999-Admission Time)
DUNIQUE    DIM      8         8           15       UNIQUE FIELD
SDAYHOSP   DIM      8         8           23       NUMBER OF DAYS IN HOSPITAL
.ADATE     DIM      8         8           31       ADMISSION DATE
.AWARD     DIM      3         3           39       WARD
.ABED      DIM      3         3           42       BED
.ATYPE     DIM      3         3           45       ADMISSION TYPE
.PSAGE     FORM     3         3           48       AGE
.ADOCTA    DIM      6         6           51       ATTENDING DOCTOR
.PVIURNO   DIM      8         8           57       U/R NUMBER
.DPVIBILL  DIM      8         8           65       BILLING NUMBER
.PVIDATE   DIM      8         8           73       VISIT DATE
.PSNAME    DIM      20        20          81       SURNAME
.PGNAME    DIM      25        25         101       GIVEN NAME
SSAV3BDY   DIM      4         4          126       NO OF DAYS OF HOSPITALIZATION
.PTMX3BEX  DIM      8         8          130       3B Expiry date
.ACLAIM    DIM      3         3          138       CLAIM CODE
.AFUNDH    DIM      6         6          141       HEALTH FUND
.DASTAT    DIM      1         1          147       ADMISSION STATUS    
TSPARE     DIM      1         1          148       SPARE VARIABLE
.EOR                                     149
.
.     redifine form fields from key
.
IDXDATE    FORM     8
IDXTIME    FORM     6
UNIQUE     FORM     8
DAYHOSP    FORM     8
SAV3BDY    FORM     4
.
.               VARIABLES DEFINITION
.
.
WDD             DIM     2
WMM             DIM     2
WYY             DIM     2
WCC             DIM     2
.
CMDLINE         DIM     50
CDAYS           FORM    3               /* CUT-OFF DAYS            */
CURDAYS         FORM    4               /* CURRENT STAY IN HOSPITAL*/
C3BDAYS         FORM    3
CURDATE         DIM     8
.
DIM20           DIM     20
DIM29           DIM     29
DAYNUM          FORM    5
.
FIVE9           FORM    "59"
FORM6           FORM    6
FORM8           FORM    8
FNAMEI          DIM     8
FNAMER          DIM     8
FROMDATE        DIM     8
TODATE          DIM     8
.
JDAYS           FORM    3
JYEAR           FORM    2
.
LYR             FORM    1
LSTDAYS         FORM    4
.
OPCND           FORM    1
PT3BHOSP        FORM    1
RECFLG          FORM    1
.
SAFUNDH         DIM     6
SDASTAT         DIM     1
SACLAIM         DIM     3 
SADATES         DIM     8
SPSNAME         DIM     20
SPGNAME         DIM     25
SPTMX3BX        DIM     8
SAWARD          DIM     3
SABED           DIM     3
SATYPE          DIM     3
SPSAGE          FORM    3
SADOCTA         DIM     6
SADYHOSP        FORM    4
STATUS          FORM    1
SAVURNO         DIM     8
SAVDATE         DIM     8
STOPFLG         FORM    1
SP24            INIT    "                        "
.
XSEC            DIM     2
.
TOTDAY          FORM    4               /* TOTAL DAYS IN HOSIPITAL */
WKC3BDYS        FORM    4
.
.---------------------------------------------------------------------
VERSION   INIT      "V12.01.00"
PRGID     INIT      "IBAHMS53"
PRGNAM    INIT      "3b Certificate Pending"
.---------------------------------------------------------------------
.
.                  OPEN FILES
.
          DISPLAY   *ES;
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patmi1af";
          OPEN      PATMI1A2,"patmi1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA1,"patvisaf" 
          OPEN      PATVISA2,"patvisaf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          OPEN      PATCERA1,"patceraf"
.
          READ      CONTROLF,TWENTY;*77,PT3BHOSP
          READ      CONTROLF,FIVE9;*2,C3BDAYS
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          UNPACK    CDATE WITH XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
          PACK      CURDATE WITH XCENT,XYEAR,XMON,XDAY
          REP       " 0",CURDATE
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMEI
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMER
.
..................  START OF PROGRAM 
..................  ASK FOR CUT-OFF DAYS
.
START   MOVE       ZERO  TO  CPAGENO
.
        KEYIN     *P1:24,*EL,*P1:5,*EF,"Keyin number of cut-off days : ":
                  *V2LON,CDAYS    
.
        COMPARE   ZERO TO CDAYS
        GOTO      ENDIT IF EQUAL
        GOTO      ENDIT IF LESS
.
        CALL        KHOS0000                     * Enter Hospital
        BRANCH      EXIT,START
.
.                 CONFIRMATION
.
REKEY   KEYIN       *P1:24,*EL,"Ok to Continue (":
                    *V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
        REP         UPPLOW,ANS
.
        MATCH       "N" TO ANS
        GOTO        START  IF EQUAL
.
        MATCH       "Y" TO ANS
        GOTO        ADSTAT IF EQUAL
.
        BEEP
        GOTO        REKEY 
.
ADSTAT 
        DISPLAY     *P1:24,*EL,*P10:24,*V2LON:
                    "** Creating Temporary file **",*W2,*P10:24,*EL;
.
        CALL        INDZD
        BRANCH      OPCND OF ENDIT
.
        DISPLAY     *P1:24,*EL,*P20:24,"Admission Number : ":
                    *P60:24,"Scanning";
        MOVE        ZERO,CLNO
        MOVE        ZERO,UNIQUE
.
.               READ CURRENT ADMISSION PATIENTS
.
        PACK        KEY9 WITH TWO,SP8                                      
        CALL        RDSMISS2
        CALL        BROWSE2
.
.               READ ON LEAVE PATIENTS
.
        PACK        KEY9 WITH FOUR,SP8                                      
        CALL        RDSMISS2
        CALL        BROWSE4
.
.       Looping temporary file, - to workout the total days in hospital
.
        CALL        LOOP000
.
        COMPARE     ZERO,CPAGENO
        GOTO        NOREC IF EQUAL
.
        PRINT       *N,"  ***  END OF REPORT  ***"
        DISPLAY     *P10:24,*EL,*V2LON:
                    "** Report Generation Completed **",*W3;
        CALL        KILLIDZ
        GOTO        START
.
.                 NO PATIENT RECORDS SELECTED
.
NOREC   DISPLAY     *P1:24,*EL,*P20:24,*B,*V2LON:
                    "**  No Admissions on File  **",*W3
        CALL        KILLIDZ
        GOTO        START
.
NOMAST  DISPLAY     *P1:24,*EL,*P20:24,*B,*V2LON:
                    "U/R No.: ",AURNO," Missing Master record **",*W3
        RETURN
.
NODSCH  DISPLAY     *P1:24,*EL,*P20:24,*B,*V2LON:
                    "Adm. Number: ",PVIBILL,"  Missing Discharge record ",*W3
        RETURN
.
.              READ CURRENT ADMISSION PATIENTS UNTIL END
.
BROWSE2 CALL        RDKMISS2
        COMPARE     ONE,OVRCD
        RETURN      IF EQUAL
.
        COMPARE     TWO,ASTAT
        RETURN      IF NOT EQUAL
        CALL        VIST000
        GOTO        BROWSE2
.
.              READ ON LEAVE PATIENTS UNTIL END
.
BROWSE4 CALL        RDKMISS2
        COMPARE     ONE,OVRCD
        RETURN      IF EQUAL
.
        COMPARE     FOUR,ASTAT
        RETURN      IF NOT EQUAL
        CALL        VIST000
        GOTO        BROWSE4
.
.       Check the visits of this patient
.
VIST000 DISPLAY     *P72:24,AADMNO;
.
        MOVE        SP8,PTMX3BEX
        MOVE        AURNO,KEY8
        CALL        RDMAST1
        COMPARE     ZERO,OVRCD
        IF          !@EQUAL
          CALL        NOMAST                * Display error message
          GOTO        VIST999
        ENDIF
.
        CALL        GCER0000                * Get 3B Expiry date
.
.       Check that this current Admission is for selected Hospital    *I-235222
.
VIST020   COMPARE   ONE,IBCNMHOS
          IF        IBCNMHOS = 1
            MATCH     SP10,PTHSHOSP         * All Hospitals ?
            IF        !@EQUAL
.                                           * get the patients Hospital
              PACK        KEY8,AADMNO,SP10
              CALL        RDVISA1           * read Visit for current Admission
              BRANCH      OVRCD,VIST999
.
              COMPARE     THREE,PVITYPE     * (over-kill?)
              GOTO        VIST999 IF NOT EQUAL
.
              PACK        KEY8,PVIBILL,SP70
              CALL        RDPMVX11
              BRANCH      OVRCD,VIST999
.
              MATCH       PMVXMHOS,PTHSHOSP    * Correct hospital
              GOTO        VIST999 IF NOT EQUAL
            ENDIF
          ENDIF                                             * end     *I-235222
.
.       The patient must be still admitted or on leave
.
VIST050 MOVE        PURNO,PVIURNO
        PACK        KEY24 WITH PVIURNO,Z70
        CALL        RDSVISA2
VIST100 CALL        RDPVISA2
        BRANCH      OVRCD OF VIST999
.
        MATCH       PURNO,PVIURNO
        GOTO        VIST999 IF NOT EQUAL
.
.       Inpatient only
.
        COMPARE     THREE,PVITYPE
        GOTO        VIST100 IF NOT EQUAL
.
        COMPARE     ONE,IBCNMHOS
        GOTO        VIST110 IF NOT EQUAL
.   
        PACK        KEY8,PVIBILL,SP70
        CALL        RDPMVX11
        BRANCH      OVRCD,VIST100
.   
        MATCH       SP10,PTHSHOSP        * All Hospitals
        GOTO        VIST110 IF EQUAL
.   
        MATCH       PMVXMHOS,PTHSHOSP    * Correct hospital
        GOTO        VIST100 IF NOT EQUAL
.
VIST110 MOVE        PVIBILL,KEY8
        CALL        RDMISS1
        BRANCH      OVRCD OF VIST100
.
.       Get the number of days in hospital for this visit
.
        BRANCH      ASTAT OF VIST100,VIST200,VIST120,VIST200
        GOTO        VIST100
.
.       Get the discharge date for this visit
.
VIST120 MOVE        PVIBILL,KEY8
        CALL        RDDSCH1
        BRANCH      OVRCD OF VIST100
.
        MATCH       DDATE,ADATE
        GOTO        VIST100 IF EQUAL        * Exclude Daycases
.
        MOVE        DDATE,TODATE
        GOTO        VIST220
.
.       Use current date as TODATE for current visit
.
VIST200 MOVE        CURDATE,TODATE
.
VIST220 REP         " 0",TODATE
        UNPACK      PVIDATE WITH XCENT,XYEAR,XMON,XDAY
        PACK        FROMDATE WITH XCENT,XYEAR,XMON,XDAY
        REP         " 0",FROMDATE
        UNPACK      ATIME,XHOUR,D1,XMIN,D1,XSEC
        PACK        DIDXTIME,XHOUR,XMIN,XSEC,SP70
        REP         " 0",DIDXTIME         *  Visit Time
.
.       Calculate the number of days diffrence b/w TODATE and FROMDATE
.
        MOVE        ZERO,DAYNUM
        DAYSEP      FROMDATE,TODATE,DAYNUM
        MOVE        DAYNUM,DAYHOSP
.
.       Reverse the visit date order (effectively making key decresing order)
.
        MOVE        FROMDATE,SAVDATE
.
        MOVE        SAVDATE,FORM8
        MOVE        "99999999",IDXDATE
        SUB         FORM8,IDXDATE
.
        MOVE        ZERO,FORM6
        TYPE        DIDXTIME
        IF          @EQUAL
          MOVE        DIDXTIME,FORM6
        ENDIF
        MOVE        "999999",IDXTIME
        SUB         FORM6,IDXTIME
        MOVE        IDXTIME,DIDXTIME       * move back to DIM field
.
        ADD         ONE,UNIQUE
        PACK        KEY30 WITH PVIURNO,IDXDATE,DIDXTIME,UNIQUE
        CALL        WRTEMP
        GOTO        VIST100
.
VIST999 RETURN
.
+
.       Looping the Temp file
.
LOOP000 MOVE        ZERO,SAVURNO
        MOVE        ZERO,RECFLG
        MOVE        ZERO,STOPFLG
        MOVE        ZERO,TOTDAY
        MOVE        SP8,SAVDATE
.
        MOVE        SP24,KEY30
        CALL        RDSTEMP
LOOP100 CALL        RDKTEMP
        BRANCH      OVRCD OF LOOP600
.
        MATCH       SAVURNO,PVIURNO
        GOTO        LOOP200 IF EQUAL
.
.       A new patient
.
        DISPLAY     *P40:24,*V2LON,PVIBILL;
.
        COMPARE     ZERO,RECFLG
        GOTO        LOOP140 IF EQUAL
.
.       Print the previous patient
.
        CALL        DISPDA
        MOVE        ZERO,TOTDAY
        MOVE        ZERO,CURDAYS
        MOVE        ZERO,RECFLG
.
.       The first one must be the Current visit
.
LOOP140 MOVE        PVIURNO,SAVURNO
        MOVE        DAYHOSP,TOTDAY
.
        COMPARE     TWO,PT3BHOSP
        IF          @EQUAL
          ADD          SAV3BDY,TOTDAY ***only using days entered on adm page ***
          GOTO         LOOP150
        ENDIF
.
        COMPARE     ONE,PT3BHOSP         * Check parameter if days of hosp.
        GOTO        LOOP150 IF NOT EQUAL * to be included to total no. of days
        ADD         SAV3BDY,TOTDAY    * Add number of days of hospitalization
.
LOOP150 MOVE        DAYHOSP,CURDAYS
.
        MOVE        ONE,RECFLG
        MOVE        ZERO,STOPFLG
        UNPACK      PVIDATE WITH XCENT,XYEAR,XMON,XDAY
        PACK        SAVDATE WITH XCENT,XYEAR,XMON,XDAY
        REP         " 0",SAVDATE
.
        CALL        SAVARS
        GOTO        LOOP100
.
.
.       Workout how long ago was the last visit
.
LOOP200 BRANCH      STOPFLG OF LOOP100
        IF          PT3BHOSP=2
          GOTO        LOOP300
        ENDIF
.
        UNPACK      PVIDATE WITH XCENT,XYEAR,XMON,XDAY
        PACK        FROMDATE WITH XCENT,XYEAR,XMON,XDAY
.
        MOVE        PVIBILL,KEY8
        CALL        RDDSCH1
        BRANCH      OVRCD OF LOOP210
        MOVE        DDATE,FROMDATE
.
LOOP210 REP         " 0",FROMDATE           * Compare discharged date of
        MOVE        SAVDATE,TODATE          * previous visit to this admission
        MOVE        ZERO,DAYNUM
        DAYSEP      FROMDATE,TODATE,DAYNUM
.
.       Check with the maximum days of breaks
.
        MOVE        ZERO,WKC3BDYS
        MOVE        C3BDAYS,WKC3BDYS
        ADD         SADYHOSP,WKC3BDYS
        COMPARE     DAYNUM,WKC3BDYS
        GOTO        LOOP300 IF LESS
.
.       Accumulate number of days in hospital from the previous visits
.
        ADD         DAYHOSP,TOTDAY
.
        COMPARE     TWO,PT3BHOSP
        IF          @EQUAL
          MOVE        SAV3BDY,TOTDAY
        ENDIF
.
        COMPARE     ONE,PT3BHOSP         * Check parameter if days of hosp.
        GOTO        LOOP220 IF NOT EQUAL * to be included to total no. of days
        ADD         SAV3BDY,TOTDAY    * Add number of days of hospitalization
.
LOOP220 UNPACK      PVIDATE WITH XCENT,XYEAR,XMON,XDAY
        PACK        SAVDATE WITH XCENT,XYEAR,XMON,XDAY
        REP         " 0",SAVDATE
.
        MOVE        ZERO,F4
        MOVE        SAV3BDY,SADYHOSP
        GOTO        LOOP100
.
LOOP300 MOVE        ONE,STOPFLG     * Flag to stop looking for previous visits
        GOTO        LOOP100
.
.       Check the last record
.
LOOP600 COMPARE     ZERO,RECFLG
        GOTO        LOOP999 IF EQUAL
        CALL        DISPDA
.
LOOP999 RETURN
.
.*******************************************************************************
.       Get 3B Expiry date
.*******************************************************************************
GCER0000  MOVE      ZERO,FORM1
          MOVE      SP70,KEY8
          PACK      KEY19,AURNO,SP70
          CALL      RSPTCER1
GCER1000  CALL      RKPTCER1
          BRANCH    OVRCD,GCER9000
.
          MATCH     AURNO,PTCEURNO
          GOTO      GCER9000 IF NOT EQUAL
.
          MOVE      "cr",KEY2
          PACK      KEY5,KEY2,PTCETYPE
          CALL      RDCODE1
          BRANCH    OVRCD,GCER1000
          COMPARE   THREE,TCFORM6
          GOTO      GCER1000 IF NOT EQUAL       * Not 3B Certificate
.
          MOVE      ONE,FORM1
          MATCH     SP70,PTCETDAT
          GOTO      GCER1000 IF EQUAL
.
          MATCH     ADATE,PTCETDAT             *  expiry date < visit date
          GOTO      GCER1000 IF LESS
.
          MATCH     SP70,KEY8
          IF        @EQUAL
            MOVE      PTCETDAT,KEY8          * Save todate
          ELSE
            MATCH     KEY8,PTCETDAT
            GOTO      GCER1000 IF LESS
            MOVE      PTCETDAT,KEY8
          ENDIF
          GOTO      GCER1000
.
GCER9000  COMPARE   ZERO,FORM1
          GOTO      GCER9999 IF EQUAL        * No record found in patceraf
          MATCH     SP70,KEY8
          GOTO      GCER9999 IF EQUAL
.
          MOVE      KEY8,PTMX3BEX            * 3B Expiry date
GCER9999  RETURN
+
.*******************************************************************************
.       Save the variables
.*******************************************************************************
SAVARS  MOVE        PSNAME,SPSNAME
        MOVE        PGNAME,SPGNAME
        MOVE        PTMX3BEX,SPTMX3BX
        MOVE        PVIBILL,SPVIBILL
        MOVE        PVIURNO,SPVIURNO
        MOVE        AWARD,SAWARD
        MOVE        ABED,SABED
        MOVE        PVIURNO,SPVIURNO
        MOVE        ATYPE,SATYPE
        MOVE        PSAGE,SPSAGE
        MOVE        ADOCTA,SADOCTA
        MOVE        ADATE,SADATES
        MOVE        SAV3BDY,SADYHOSP
        MOVE        ZERO,F4
..      MOVE        PTMI3BDY,F4
..      COMPARE     ZERO,F4
..      IF          !@EQUAL
..        MOVE        PTMI3BDY,SADYHOSP
..      ELSE
..        MOVE        ADYHOSP,SADYHOSP
..      ENDIF
        MOVE        ACLAIM,SACLAIM
        MOVE        AFUNDH,SAFUNDH
        MOVE        DASTAT,SDASTAT
        RETURN
.
.     PRINT ACTUAL DATA 
.
DISPDA  MATCH       "2",SDASTAT        * Current
        IF          !@EQUAL
          MATCH       "4",SDASTAT      * On Leave
          RETURN      IF NOT EQUAL                
        ENDIF
.
        COMPARE     CDAYS,TOTDAY
        RETURN      IF LESS
.
        COMPARE     ZERO,CLNO
        CALL        HEAD IF EQUAL   
        COMPARE     "55",CLNO
        CALL        HEAD IF NOT LESS
.
        MOVE        SPSNAME,DIM29
        RESET       DIM29,20
DSPNM   CMATCH      SP1,DIM29
        GOTO        APNAM IF NOT EQUAL
        BUMP        DIM29,-1
        GOTO        DSPNM IF NOT EOS
.
APNAM   APPEND      ", ",DIM29
        APPEND      SPGNAME,DIM29
        APPEND      SP30,DIM29
        RESET       DIM29
.
        MOVE        SPVIBILL,AADMNO
        MOVE        SPVIURNO,AURNO
.
.       Total number of days from previous visits
.
        MOVE        TOTDAY,LSTDAYS
        SUB         CURDAYS,LSTDAYS
        UNPACK      SADATES INTO WCC,WYY,WMM,WDD
.
        UNPACK      SPTMX3BX,CCENT,CYEAR,CMON,CDAY
        CALL        PACDATE
.
        PRINT       *1,"| ",AADMNO,*12,"|",WDD,"/",WMM,"/",WCC,WYY:
                    *23,"|",SAWARD,SLASH,SABED:
                    *31,"|",AURNO,*40,"| ",DIM29,*72,"|",SACLAIM:
                    *79,"|",SATYPE:
                    *83,"|",SPSAGE,*87,"| ",SADOCTA,*95,"| ",CPCDATE:
                    *118,"| ",CURDAYS:
                    *126,"|",TOTDAY,*132,"|",*N:
                    *1,"|",*12,"|",*23,"|",*31,"|":
	            *40,"|",*72,"|",SAFUNDH,*79,"|":
                    *83,"|",*87,"| ",*95,"|":
                    *118,"| ",LSTDAYS,*126,"| ",*132,"|"
.
        CALL        PAGEND
        ADD         TWO,CLNO
        RETURN
.
.          LAST LINE ON THE PAGE
.
PAGEND  PRINT     "*---------------------------------------------------":
                  "--------------------------------------------------------":
                  "-----------------------*"
        ADD         ONE,CLNO
        RETURN
.
.                 HEADINGS FOR OUTPUT
.
HEAD      MOVE      ONE,CNOUNDLN
          CALL      IBACLOCK
          CALL      HEAD132
.
          IF        IBCNMHOS =1
            PRINT     *40,"Hospital : ",PTHSNAME
          ENDIF
.
          PRINT     *40,"Number Of Cut-Off Days : ",CDAYS,*N
.
          CALL      PAGEND
.
          PRINT     *1,"|",*12,"|",*23,"|",*31,"|":
                    *40,"|",*72,"|",*79,"|",*83,"|",*87,"|":
                    *95,"|":
                    *118,"|    Days",*132,"|",*N:
                    *1,"| Adm no",*12,"| Adm Date":
                    *23,"| Ward",*31,"| U/R no":
                    *40,"| Patients Name",*72,"| CL/",*79,"|Adm":
                    *83,"|Age",*87,"|Att.":
                    *95,"| Cert. Expiry Date":
                    *118,"|Current",*126,"|",*132,"|",*N:
                    *1,"|",*12,"|",*23,"| /Bed":
                    *31,"|",*40,"|",*72,"| HF":
                    *79,"|Typ",*83,"|",*87,"|Doctor":
                    *95,"|":
                    *118,"|Last":
                    *126,"|Total",*132,"|"  
.
          CALL     PAGEND
.
          IF        IBCNMHOS =1
            MOVE      "10",CLNO
          ELSE
            MOVE      "9",CLNO
          ENDIF
          RETURN
.
.      Create an indexed file based on port
.       for accumulating Admission Type totals
.
INDZD   MOVE        ZERO,STATUS
.
.       CHECK IF PREVIOUSLY EXISTS..IF SO DELETED IT 
.
        CALL        KILLIDZ
.
        PREP        PSTROL,FNAMER
        WRITE       PSTROL,SEQ;"isbuild ",FNAMEI," 149 U57-64,1-8,9-14,15-22"
        WEOF        PSTROL,SEQ
.
        MOVE        ONE,STATUS
.
        CLEAR       CMDLINE
        APPEND      "sh ",CMDLINE
        APPEND      FNAMER,CMDLINE
        APPEND      ".txt",CMDLINE
        RESET       CMDLINE
.
        EXECUTE     CMDLINE,TASKID
.
        CLOSE       PSTROL,DELETE
.
        MOVE        ZERO,OPCND
        TRAP        NOINDZ IF IO
        OPEN	    TEMPFILE,FNAMEI
        TRAPCLR     IO
ENDC    RETURN
.
NOINDZ  MOVE        ONE,OPCND
        DISPLAY     *P1:24,*EL,*B,*V2LON:
                    "** Temporary Index File ":
                    FNAMEI," Not Found **",*W4;
        TRAPCLR     IO
        RETURN
.
.
.         Deleting an indexed file based on port
.
KILLIDZ   CLOSE     TEMPFILE
          MOVE      ZERO,STATUS
.
          DISPLAY   *P1:24,*EL,*P20:24,*V2LON:
                    "** Deleting Indexed File **",*W;
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          DISPLAY   *P1:3,*EF,*P1:10
          RETURN
.
+
.         I/O Routine for Temporary file
.
WRTEMP   RESET     KEY30
         MOVE      IDXDATE,DIDXDATE
         MOVE      UNIQUE,DUNIQUE
         MOVE      ZERO,F4
         MOVE      PTMI3BDY,F4
         COMPARE   ZERO,F4
         IF        !@EQUAL
           MOVE      PTMI3BDY,SAV3BDY
         ELSE
           MOVE      ADYHOSP,SAV3BDY
         ENDIF
.
         MOVE      DAYHOSP,SDAYHOSP
         MOVE      SAV3BDY,SSAV3BDY
         MOVE      PVIBILL,DPVIBILL
         WRITE     TEMPFILE,KEY30;DIDXDATE,DIDXTIME,DUNIQUE:
                                  SDAYHOSP,ADATE,AWARD,ABED,ATYPE:
                                  PSAGE,ADOCTA,PVIURNO,DPVIBILL,PVIDATE:
                                  PSNAME,PGNAME,SSAV3BDY,PTMX3BEX:
                                  ACLAIM,AFUNDH,DASTAT,TSPARE
         RETURN
.
RDTEMP   MOVE      ZERO,OVRCD
         RESET     KEY30
         READ      TEMPFILE,KEY30;DIDXDATE,DIDXTIME,DUNIQUE:
                                  SDAYHOSP,ADATE,AWARD,ABED,ATYPE:
                                  PSAGE,ADOCTA,PVIURNO,DPVIBILL,PVIDATE:
                                  PSNAME,PGNAME,SSAV3BDY,PTMX3BEX:
                                  ACLAIM,AFUNDH,DASTAT,TSPARE
         GOTO      OVERCOND IF OVER
         MOVE      DIDXDATE,IDXDATE
         MOVE      DUNIQUE,UNIQUE
         MOVE      SDAYHOSP,DAYHOSP
         MOVE      SSAV3BDY,SAV3BDY
         MOVE      DPVIBILL,PVIBILL
         RETURN
.
RDKTEMP  MOVE      ZERO,OVRCD
         READKS    TEMPFILE;DIDXDATE,DIDXTIME,DUNIQUE:
                            SDAYHOSP,ADATE,AWARD,ABED,ATYPE:
                            PSAGE,ADOCTA,PVIURNO,DPVIBILL,PVIDATE:
                            PSNAME,PGNAME,SSAV3BDY,PTMX3BEX:
                            ACLAIM,AFUNDH,DASTAT,TSPARE
         GOTO      OVERCOND IF OVER
         MOVE      DIDXDATE,IDXDATE
         MOVE      DUNIQUE,UNIQUE
         MOVE      SDAYHOSP,DAYHOSP
         MOVE      SSAV3BDY,SAV3BDY
         MOVE      DPVIBILL,PVIBILL
         RETURN
.
RDSTEMP  MOVE      ZERO,OVRCD
         RESET     KEY30
         READ      TEMPFILE,KEY30;;
         GOTO      OVERCOND IF OVER
         RETURN
.
DETEMP   RESET     KEY30
         DELETE    TEMPFILE,KEY24
         RETURN
.
UPTEMP   MOVE      DAYHOSP,SDAYHOSP
         MOVE      SAV3BDY,SSAV3BDY
         MOVE      PVIBILL,DPVIBILL
         UPDATE    TEMPFILE;IDXDATE,UNIQUE:
                            SDAYHOSP,ADATE,AWARD,ABED,ATYPE:
                            PSAGE,ADOCTA,PVIURNO,DPVIBILL,PVIDATE:
                            PSNAME,PGNAME,SSAV3BDY,PTMX3BEX:
                            ACLAIM,AFUNDH,DASTAT,TSPARE
         RETURN
+
.
         INC       PATCODIO/INC
         INC       PATMA1IO/INC
         INC       PATMI1IO/INC
         INC       PATDSCIO/INC
         INC       PATVISIO/INC
         INC       PATHSPIO/INC
         INC       PMSVX1IO/INC
         INC       PATCERIO/INC
         INC       PATHSPKY
         INC       STD001IO    
         INC       TFILENAM 
         INC       IBASEQIO/INC 
         INC       WEBERRIO/INC
.
.
.******************************************************************************
.*                                  KHOS0000              Called by: ML0000   *
.*                            Enter The hospital                              *
.******************************************************************************
KHOS0000  COMPARE   ONE,IBCNMHOS
          GOTO      KHOS9999 IF NOT EQUAL
.
          DISPLAY   *P1:7,*EL,"Hospital Id : ";
          MOVE      TEN5,ECOL
          MOVE      SEVEN,EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS2000,KHOS9500
          GOTO      KHOS3000
.
KHOS2000  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          MOVE      ZERO,EXIT
.
KHOS3000  CLOSE     PATHSPA1
.
          DISPLAY   *P20:7,*V2LON,PTHSNAME;
          GOTO      KHOS9999
.
KHOS9500  MOVE      ONE,EXIT
KHOS9999  RETURN
.
ENDIT    CHAIN      PGM
         STOP

.******************************************************************************
.*                                                                            *
.*                        P R O G R A M   S U M M A R Y                       *
.*                        -------------   -------------                       *
.*                                                                            *
.*     PROGRAM NAME:    IBAPAT45                                              *
.*                                                                            *
.*     FUNCTION:        INCOMPLETE CODING REPORT                              *
.*                                                                            *
.*     DATE WRITTEN:    06/07/2009                                            *
.*                                                                            *
.*     AUTHOR:          WeeLee Koo                                            *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.******************************************************************************
.* V12.00.01 28/05/2025 Thanh T         TSK 0955096                           *
.*           Changed for alphanumeric visit number                            *
.******************************************************************************
.*        V10.11.01 08/09/2017  Ebon Clements  TSK 0818097                    *
.*                  Added excluded newborn functionality -  EXNB0000          *
.*        V10.08.01 22/11/2016  Ebon Clements  TSK 0319860                    *
.*                  Ignore patecdaf ICD coding lock record - NXDISE           *
.*        V10.03.01 31/12/2012 Patrick Adair   CAR 261630                     *
.*                  Removed port number from temp file name                   *
.*        V10.02.02 08/07/2011  Ebon Clements  CAR 240724                     *
.*                  Mods for MRT hospital/location - Added hospital to        *
.*                  location key                                              *
.*        V10.02.01 28/03/2011  Mike Laming   CAR 240107                      *
.*                  Mods to PATECDaf & PATECOaf variables & Keys - file change*
.*        V9.12.02  27/08/2009  WeeLee Koo     CAR 199998                     *
.*                  Changed Terminal Digit display                            *
.*        V9.12.01  03/08/2009  WeeLee Koo     CAR 199998                     *
.*                  Change Reporting Format. Source from IBAPAT43.            *
.******************************************************************************
.
          INC       STD001FD
          INC       IBACONFD/INC
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       MRTVISFD/INC
          INC       MRTMASFD/INC
          INC       MRTLOCFD/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATDO1FD/INC
          INC       PATDSCFD/INC
          INC       PATECDFD/INC
          INC       PATECOFD/INC
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
          INC       PATTRNFD/INC
..          INC       PATWR1FD/INC
+
.
.         TEMP FILES DECLARATION
.
TEMPFL1   IFILE     SQL,   FIXED=176, KEY="U1-14"      
TEMPFL2   IFILE     SQL,   FIXED=176, KEY="U125-132,64-71" 
TEMPFL3   IFILE     SQL,   FIXED=176, KEY="U56-63,64-71"
TEMPFL4   IFILE     SQL,   FIXED=176, KEY="U164-171,64-71,1-14"
.
. ----- Tempfile consists of following vars -----
.
. Name   Type      Length     Physical     Start   Description
. ----   ----      ------     --------     -----   -----------
.DCODE    DIM       6          6             1
DUNIQUE   DIM       8          8             7
.DNAME21  DIM      21         21            15
.DTELES   DIM      20         20            36
.PURNO    DIM       8          8            56
.DDADMNO  DIM       8          8            64
.PSNAME   DIM      20         20            72
.PNAME20  DIM      20         20            92
.PADATE   DIM       8          8           112
.A5TIME   DIM       5          5           120
.PDDATE   DIM       8          8           125
.D5TIME   DIM       5          5           133
.AHFUND   DIM       6          6           138                        
MRLOCN    DIM       20         20          144
TDIGIT    DIM       8          8           164
LOSY      DIM       4          4           172
.End of Record                             176                         
.
.         WORK VARIABLES
.
DIM2A     DIM       2     * for terminal digit
DIM2B     DIM       2     * for terminal digit
DIM2C     DIM       2     * for terminal digit
DIM2D     DIM       2     * for terminal digit
ADMDATE   DIM       10    * for admission date
DSCDATE   DIM       10    * for discharge date
AHFUND    DIM       6                                                  *I-53568
A5TIME    DIM       5
D5TIME    DIM       5
BOARDFLG  FORM      1     * used to branch over boarder check
.
CMDLINE   DIM       150
COUNT     FORM      8
.
DIM3      DIM       3     * required by PATDOCDS
DIM4      DIM       4     * LOS
DIM6      DIM       6    
DIM14     DIM       14   
DNAME21   DIM       21
DOCTEMP   DIM       6
DOCCODE   DIM       6
DESCOPTA  DIM       23    * option description in heading
.
ENDATE    DIM       8
.
GNDTOTAL  FORM      6
.
LNO       FORM      2
.
MRKEY     DIM       10
MRLOC     DIM       7
NAME11    DIM       11    * doctor's name
.
PAGENO    FORM      3
PADATE    DIM       8
PDDATE    DIM       8
PFDATE    DIM       10
PTDATE    DIM       10
PNAME20   DIM       20    * for record writing
PNAME13   DIM       13    * patient name display 
.
SNAME7    DIM       7     * patient surname display
SORT      FORM      1     * Types of Sorting
STARTD    DIM       8
.
TEMPFILE  DIM       8
UNIQUE    FORM      8
URNUMB    DIM       8
.
.         CONSTANTS
.
CODEP     INIT      "P "
FIVE5     FORM      "55"
ERASE     INIT      "iserase "
ISBUILD   INIT      "isbuild "
UKEY      INIT      " 176 U1-14 U125-132,64-71 U56-63,64-71 U164-171,64-71,1-14"
.
SP12      INIT      "            "
SP23      INIT      "                       "
.
PRGID     INIT      "IBAPAT45"
PRGNAM    INIT      "Incomplete Coding Report"
VERSION   INIT      "V12.01.00"
+
.
.         Display screen header and open files
.
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P64:24,"mrtmasaf";
          OPEN      MRTMASA2,"mrtmasaf"
.
          DISPLAY   *P64:24,"mrtlocaf";
          OPEN      MRTLOCA1,"mrtlocaf"
.
          DISPLAY   *P64:24,"mrtvisaf";
          OPEN      MRTVISA1,"mrtvisaf"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patecdaf";
          OPEN      PATECDA2,"patecdaf"
.
          DISPLAY   *P64:24,"patecoaf";
          OPEN      PATECOA2,"patecoaf"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P64:24,"pattranf"
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"pmsvx1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TEN;*66,CINVLAY
          READ      CONTROLF,FIFTY9;*34,CTYPEUR
.
          MOVE      ONE,BOARDFLG
          COMPARE   EIGHT,CINVLAY
          GOTO      INIT0000 IF NOT EQUAL
          MOVE      ZERO,BOARDFLG
.
INIT0000  
.
.*******************************************************************************
.*              Select Option: (0 - Exit,                                      *
.*                              1 - Run Report)                                *
.*                                                                             *
.*******************************************************************************
.
          DISPLAY   *P1:4,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Run Report":
                    *P1:7,"Select Option :";
.
REKSTRT   KEYIN     *P17:7,*EL,*DV,UNDLN1,*P17:7,*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
          BRANCH    OPTION OF VALID
          BEEP
          GOTO      REKSTRT
.
VALID     DISPLAY   *P1:9,*EF:
                    *P1:10,"Starting Discharge Date  :":
                    *P1:11,"Ending   Discharge Date  :"
.
.         Keyin Start Discharge Date
.
KSTART    MOVE      ZERO,CHIGHLT            * 0 = highlight
          MOVE      ONE,CDEFDTE             * 1 = check day only for default 
.
          MOVE      "29",CCOL               * column for keyin
          MOVE      "10",CVERT              * row for keyin
          MOVE      "50",ECOL               * column for error
          MOVE      "10",EVERT              * row for error
.                                           * 1 = check day only for default
KSTART1   MOVE      SP2,CDAY                * set up defaults
          MOVE      SP2,CMON
          MOVE      SP2,CYEAR
          MOVE      CCC,CCENT
          CALL      KEYDATE
          BRANCH    CQUEST,KSTART1          * "?" keyed
          BRANCH    OVRCD,FINISH            * no date keyed
.
          PACK      STARTD,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STARTD
          PACK      PFDATE WITH CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          REP       " 0",PFDATE
.
.         Keyin End Discharge Date
.
KEND      MOVE      "11",CVERT              * row for keyin
          MOVE      "11",EVERT              * row for error
.
KEND1     MOVE      SP2,CDAY                * set up defaults
          DISPLAY   *P29:11,*V2LON,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR;
          MOVE      SP2,CMON
          MOVE      SP2,CYEAR
          MOVE      CCC,CCENT
          CALL      KEYDATE
          BRANCH    CQUEST,KEND1            * "?" keyed
          BRANCH    OVRCD,CURDAY            * no date keyed
          GOTO      CHKDAT2
.
CURDAY    MOVE      CDD,CDAY
          MOVE      CMM,CMON
          MOVE      CYY,CYEAR
          MOVE      CCC,CCENT
          REP       " 0",CDAY
          REP       " 0",CMON
          REP       " 0",CYEAR
          REP       " 0",CCENT
          DISPLAY   *P29:11,*V2LON,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR;
.
CHKDAT2   PACK      ENDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDATE
          PACK      PTDATE WITH CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          REP       " 0",PTDATE
.
          MATCH     STARTD,ENDATE
          GOTO      KTDOC31 IF NOT LESS
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Ending Date must be > or equal to Starting Date **  ";
          CALL      HITENTER
          GOTO      KEND
.
KTDOC31   CALL      SORT0000
          BRANCH    EXIT,VALID
          CALL      KHOS0000
          BRANCH    EXIT,VALID
          GOTO      CONFIRM
.
.******************************************************************************
.*                           SORT0000                                         *
.*                     Select Types of Sorting                                *
.******************************************************************************
.
SORT0000  DISPLAY   *P39:13,*EF:
                    *P1:14,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:15,*V2LON,ONE,*HOFF," = Discharge Date":
                    *P1:16,*V2LON,TWO,*HOFF," = Medical Record Number":
                    *P1:17,*V2LON,THREE,*HOFF," = Terminal Digit "
.
          KEYIN     *P1:19,"Sort By : ",*V2LON,SORT
.
          COMPARE   ZERO,SORT
          GOTO      SORT9000 IF EQUAL
.
          BRANCH    SORT,SORT1000:     * Discharge Date
                         SORT2000:     * Medical Record Number
                         SORT3000      * Terminal Digit
.
          BEEP
          GOTO      SORT0000
.
.         Screen display in heading
.
SORT1000  DISPLAY   *P50:2,*+,"- ",*V2LON,"By Discharge Date"
          MOVE      "Discharge Date         ",DESCOPTA
          MOVE      ZERO,EXIT
          GOTO      SORT9999
.
SORT2000  DISPLAY   *P50:2,*+,"- ",*V2LON,"By Medical Record Number"
          MOVE      "Medical Record Number  ",DESCOPTA
          MOVE      ZERO,EXIT
          GOTO      SORT9999
.
SORT3000  DISPLAY   *P50:2,*+,"- ",*V2LON,"By Terminal Digit"
          MOVE      "Terminal Digit         ",DESCOPTA
          MOVE      ZERO,EXIT
          GOTO      SORT9999
.
SORT9000  MOVE      ONE,EXIT
.
SORT9999  RETURN
.
.******************************************************************************
.*                           KHOS0000                                         *
.*                     Enter Hospital ID                                      *
.******************************************************************************
.
KHOS0000  COMPARE   ONE,IBCNMHOS
          GOTO      KHOS9999 IF NOT EQUAL
.
          DISPLAY   *P1:21,*EL,"Hospital Id : ";
          MOVE      TEN5,ECOL
          MOVE      "21",EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS2000,KHOS9500
          GOTO      KHOS3000
.
KHOS2000  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          MOVE      ZERO,EXIT
.
KHOS3000  CLOSE     PATHSPA1
.
          DISPLAY   *P20:21,*V2LON,PTHSNAME;
          GOTO      KHOS9999
.
KHOS9500  MOVE      ONE,EXIT
.
KHOS9999  RETURN
+
.******************************************************************************
.*         Confirm Date and Doctor Code ranges                                *
.*                                                                            *
.******************************************************************************
.
CONFIRM   DISPLAY   *P1:24,*EL,"Ok to Continue (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ?";
.
CONFIRM1  KEYIN     *P24:24,*DV,UNDLN1,*P24:24,*V2LON,ANS;
.
          RESET     ANS
          GOTO      CONFIRM2 IF EOS
          REP       UPPLOW,ANS
          DISPLAY   *P24:24,*V2LON,ANS;
.
          MATCH     "N",ANS
          GOTO      CLSCR IF EQUAL
          MATCH     "Y",ANS
          GOTO      PROCESS IF EQUAL
.
CONFIRM2  BEEP
          GOTO      CONFIRM1
.
CLSCR     DISPLAY   *P29:10,*EL,*P29:15,*EL,*P29:17,*EL,*P29:18,*EF;
          GOTO      KSTART
.
FINISH    DISPLAY   *P1:10,*EF;
          GOTO      REKSTRT
.
.         Start processing data for report
.
PROCESS   CALL      KILL0000
          CALL      CREA0000
.
          MOVE      ZERO,PAGENO
          MOVE      "60",LNO
          MOVE      ONE TO UNIQUE
.
NODET     DISPLAY   *P1:24,*EL,*V2LON:
                    *P20:24,"** Scanning Discharge File **",*W2:
                    *HOFF,*P1:24,*EL:
                    *P10:24,"Doctor Code :",*P40:24,"Discharge Date :"
.
.         READ DISCHARGE FILE BASE ON START & END DATE
.
          PACK      KEY16,STARTD,SP10
          CALL      RDSDSCH2
LOOP      CALL      RDKDSCH2
          BRANCH    OVRCD OF PRINTR

          REP       " 0",DDATE
          UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          MOVE      PTDSDLOS,DIM4
          MOVE      DIM4,LOSY
.
          MATCH     SP8,ENDATE
          GOTO      CHKHOS10 IF EQUAL
.
          MATCH     DDATE,ENDATE
          GOTO      PRINTR IF LESS
.
.         Multi-Hospital Processing
.
CHKHOS10  COMPARE   ONE,IBCNMHOS
          GOTO      NODISE IF NOT EQUAL
.
          MATCH     SP10,PTHSHOSP
          GOTO      NODISE IF EQUAL
.
          MOVE      DADMNO,KEY8
          CALL      RDPMVX11
          BRANCH    OVRCD,LOOP
.
          MATCH     PMVXMHOS,PTHSHOSP
          GOTO      LOOP IF NOT EQUAL
.
NODISE    PACK      KEY16,DADMNO,SP20                                 *C-240107
          CALL      RSPTECD2
NXDISE    CALL      RKPTECD2
          BRANCH    OVRCD,CHKADM
.
          MATCH     DADMNO,PTEDADMN
          GOTO      CHKADM IF NOT EQUAL
.
          COMPARE   ZERO,PTEDEPNO                * Skip coding locked record
          GOTO      NXDISE IF EQUAL
.
          GOTO      LOOP
.
CHKADM    MOVE      DADMNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD OF LOOP
.
          MATCH     AURNO,DURNO
          GOTO      LOOP IF NOT EQUAL
.
          CALL      EXNB0000               * Excluded newborn
          BRANCH    EXIT,LOOP
.
          PACK      KEY5,ANSA,SP1,ATYPE
          CALL      RDCODE1                * Cat."A "
          BRANCH    OVRCD,CHKADM04
          MATCH     "B",TCINDC3            * is this a boarder?
          GOTO      LOOP IF EQUAL          * ignore boarders
.
          BRANCH    BOARDFLG,CHKADM04
          PACK      KEY5,CODEP,ACLSS
          CALL      RDCODE1                * Cat."P "
          BRANCH    OVRCD,CHKADM04
          MATCH     "3",TCINDC1                * boarder patient ?
          GOTO      LOOP IF EQUAL              * ignore if yes
.
CHKADM04  MOVE      ADOCTA,DOCTEMP
.
NEXT13    MOVE      DOCTEMP,KEY6                 * get Doctor's code and name
          CALL      RDDOCT1
          COMPARE   ZERO,OVRCD
          GOTO      NEXT16 IF EQUAL
.
NEXT14    MOVE      "UNKN  ",DOCTEMP                          
          MOVE      SP20,DTELES                               
          BRANCH    OPTION,NEXT15
.
NEXT15    MOVE      "Unknown",DNAME21         
          GOTO      NEXT18                                    
.
NEXT16    MOVE      SP23,DNAME21
          MOVE      DGNAME,ANS
          PACK      DNAME21 WITH ANS,DOT,DSNAME
.
NEXT17    PACK      KEY8,DADMNO,SP12
          CALL      RDMRVS1
          MOVE      MRVSMKEY,MRKEY
.
          PACK      KEY10,MRKEY,SP12
          CALL      RDMRMAS2 
          PACK      MRLOC,MRMACHOS,MRMACLOC,SP70
.
          PACK      KEY7,MRLOC,SP12
          CALL      RDMRLOC1
          MOVE      MRLODESC,MRLOCN
.
NEXT18    PACK      KEY8,DURNO
          CALL      RDMAST1
.
          MOVE      PGNAME,PNAME20
.
          MOVE      ADATE,PADATE
          REP       " 0",PADATE
          MOVE      DDATE,PDDATE
          REP       " 0",PDDATE
.
          MOVE      ATIME,A5TIME
          MOVE      DTIME,D5TIME
          MOVE      AFUNDH,AHFUND
.
.         UR Number = 6 digits (healthscope)
.
          MOVE      PURNO,URNUMB
          REP       " 0",URNUMB
.
          UNPACK    URNUMB,DIM2A,DIM2B,DIM2C,DIM2D
          PACK      TDIGIT,SP2,DIM2D,DIM2B,DIM2C
.
          DISPLAY   *P24:24,*V2LON,DOCTEMP;
.
          MOVE      UNIQUE,DUNIQUE
          PACK      KEY14 WITH DOCTEMP,DUNIQUE
          CALL      WRTEMP1      
          ADD       ONE TO UNIQUE
          GOTO      LOOP
.
PRINTR    DISPLAY   *P1:24,*EL,*V2LON,*P29:24,"** Generating Report **",*W2;
.
          SUB       ONE,UNIQUE
          MOVE      UNIQUE,GNDTOTAL
          DISPLAY   *P1:23,*EF,"Total Records       : ",*V2LON,UNIQUE,*HOFF:
                    *P1:24,"Printing Record No. :",*P35:24,"Doctor Code :";
.
          COMPARE   ZERO,UNIQUE
          GOTO      NOREC IF EQUAL
.
          CALL      HEAD
          BRANCH    SORT,PRSORT1,PRSORT2,PRSORT3
.      
PRSORT1   MOVE      ONE,COUNT
          PACK      KEY16,SP30
          CALL      RSTEMP2
PRSORT1A  CALL      RKTEMP2
          BRANCH    OVRCD,ENDP
.
          MOVE      DUNIQUE,UNIQUE
          DISPLAY   *P23:24,*V2LON,COUNT,*P49:24,DCODE;
.
          CALL      PRINTD1         
          ADD       ONE,COUNT
          GOTO      PRSORT1A
.
PRSORT2   MOVE      ONE,COUNT
          PACK      KEY16,SP30
          CALL      RSTEMP3
PRSORT2A  CALL      RKTEMP3
          BRANCH    OVRCD,ENDP
.
          MOVE      DUNIQUE,UNIQUE
          DISPLAY   *P23:24,*V2LON,COUNT,*P49:24,DCODE;
.
          CALL      PRINTD1
          ADD       ONE,COUNT
          GOTO      PRSORT2A
.
PRSORT3   MOVE      ONE,COUNT
          PACK      KEY30,SP30
          CALL      RSTEMP4
PRSORT3A  CALL      RKTEMP4
          BRANCH    OVRCD,ENDP
.
          MOVE      DUNIQUE,UNIQUE
          DISPLAY   *P23:24,*V2LON,COUNT,*P49:24,DCODE;
.
          CALL      PRINTD1
          ADD       ONE,COUNT
          GOTO      PRSORT3A
.
.         OVER Condition found..print last line of -- and display message
.
ENDP      COMPARE   ZERO,UNIQUE
          GOTO      NOREC IF EQUAL
.
.          CALL      HLINE
.
          PRINT     *N,*12,"Total No. of Patients : ",GNDTOTAL:
                    *N,*N,"***  End of Report  ***"
.
          DISPLAY   *P1:23,*EF,*P24:24,*V2LON:
                    "** Report Generation Completed **",*W2;
.
          CALL      KILL0000
          GOTO      FINISH
.
NOREC     DISPLAY   *P1:23,*EF,*P28:24,*V2LON,"** No Record Selected **",*W2;
.
          MATCH     "IBARSH99",PGM
          IF        @EQUAL
            PRINT     *N,*N,"***  No Report Generated ***"
          ENDIF
.
          CALL      KILL0000
          GOTO      FINISH
.
.         PRINT ACTUAL DATA
.
PRINTD1   UNPACK    PADATE,CCENT,CYEAR,CMON,CDAY
          PACK      ADMDATE WITH CDAY,SLASH,CMON,SLASH,CCENT,CYEAR 
          REP       " 0",ADMDATE
.
          UNPACK    PDDATE,CCENT,CYEAR,CMON,CDAY
          PACK      DSCDATE WITH CDAY,SLASH,CMON,SLASH,CCENT,CYEAR 
          REP       " 0",DSCDATE
.          
          MOVE      DNAME21,NAME11
          MOVE      MRLOCN,DIM14
          MOVE      PSNAME,SNAME7
          MOVE      PNAME20,PNAME13
.
          PRINT     *1," ",DCODE:
                    *8," ",NAME11:
                    *20," ",DDADMNO:
                    *29," ",ADMDATE,SP1,A5TIME:
                    *47," ",DSCDATE,SP1,D5TIME:
                    *65," ",PURNO:
                    *74," ",SNAME7:
                    *82," ",PNAME13:
                    *96," ",DIM14:
                    *111," ",AHFUND:
                    *118," ",TDIGIT:
                    *127," ",LOSY:
                    *132," "
.
          ADD       ONE,LNO
          RETURN
.
.         HEADINGS FOR OUTPUT
.
HEAD      
.          COMPARE   ZERO,PAGENO
.          CALL      HLINE IF NOT EQUAL
.
          MOVE      ONE,CNOUNDLN
          CALL      IBACLOCK
          ADD       ONE,PAGENO
          MOVE      "8",LNO
.
          CALL      HEAD132
.
          IF        IBCNMHOS =1
            PRINT     *41,"Hospital       : ",PTHSNAME
          ENDIF
.
          PRINT     *41,"Discharge Date : From  ",PFDATE,"  To  ",PTDATE,*N:
                    *41,"Sort By        : ",DESCOPTA,*N
.
.          CALL      HLINE
          PRINT     *1,"|DrCode":
                    *8,"|DrName":
                    *20,"|Adm No":
                    *29,"|Adm Date/Time":                                    
                    *47,"|Dis Date/Time":
                    *65,"|U/R No":
                    *74,"|Surname":
                    *82,"|Given Name":
                    *96,"|MRLocation":
                    *111,"|HFund":
                    *118,"|TDigit":
                    *127,"|LOS":
                    *132,"|"
          CALL      HLINE
          RETURN
+
HLINE     PRINT      *1,"*--------------------------------------------------":
                    *52,"---------------------------------------------------":
                   *103,"-----------------------------*"
          ADD       ONE,LNO
          RETURN
+
.         Deleting an Indexed File based on PORT
.
KILL0000  CLOSE     TEMPFL1                      * close file
          CLOSE     TEMPFL2
          CLOSE     TEMPFL3
          CLOSE     TEMPFL4
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ERASE,TEMPFILE       * set file erase command
          EXECUTE   CMDLINE,TASKID               * erase temp file
.
KILL9999 RETURN
.
.         Creating Temp Indexed file used for this report
.
CREA0000  CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,TEMPFILE
.
          CALL      KILL0000                     * remove existing file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,TEMPFILE,UKEY
          EXECUTE   CMDLINE,TASKID               * create temporary index file
.
          OPEN      TEMPFL1,TEMPFILE            * open temp index 1 file
          OPEN      TEMPFL2,TEMPFILE            * open temp index 2 file
          OPEN      TEMPFL3,TEMPFILE            * open temp index 3 file
          OPEN      TEMPFL4,TEMPFILE            * open temp index 4 file
.
CREA9999  RETURN
+
ENDIT     CHAIN     PGM
          STOP
+
.******************************************************************************
.*                       I/O Routines For The Tempfile                        *
.******************************************************************************
.
RATEMP1   MOVE      ZERO,OVRCD
          RESET     KEY14
          READ      TEMPFL1,KEY14;ANS
          GOTO      OVERCOND IF OVER
          RETURN    
.         
RSTEMP1   RESET     KEY14
          READ      TEMPFL1,KEY14;;
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMPFL1;DCODE,DUNIQUE,DNAME21,DTELES,PURNO:
                            DDADMNO,PSNAME,PNAME20,PADATE,A5TIME:
                            PDDATE,D5TIME,AHFUND,MRLOCN,TDIGIT,LOSY 
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY14
          READ      TEMPFL1,KEY14;DCODE,DUNIQUE,DNAME21,DTELES,PURNO:
                            DDADMNO,PSNAME,PNAME20,PADATE,A5TIME:
                            PDDATE,D5TIME,AHFUND,MRLOCN,TDIGIT,LOSY 
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTEMP1   UPDATE    TEMPFL1;DCODE,DUNIQUE,DNAME21,DTELES,PURNO:
                            DDADMNO,PSNAME,PNAME20,PADATE,A5TIME:
                            PDDATE,D5TIME,AHFUND,MRLOCN,TDIGIT,LOSY 
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   RESET     KEY14
          WRITE     TEMPFL1,KEY14;DOCTEMP,DUNIQUE,DNAME21,DTELES,PURNO:
                                  DDADMNO,PSNAME,PNAME20,PADATE,A5TIME:
                                  PDDATE,D5TIME,AHFUND,MRLOCN,TDIGIT,LOSY 
          RETURN
.
DETEMP1   DELETE    TEMPFL1,KEY14
          RETURN
.
RATEMP2   MOVE      ZERO,OVRCD
          RESET     KEY16
          READ      TEMPFL2,KEY16;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTEMP2   RESET     KEY16
          READ      TEMPFL2,KEY16;;
          RETURN
.
RKTEMP2   MOVE      ZERO,OVRCD
          READKS    TEMPFL2;DCODE,DUNIQUE,DNAME21,DTELES,PURNO:
                            DDADMNO,PSNAME,PNAME20,PADATE,A5TIME:
                            PDDATE,D5TIME,AHFUND,MRLOCN,TDIGIT,LOSY
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTEMP2   MOVE      ZERO,OVRCD
          RESET     KEY16
          READ      TEMPFL2,KEY16;DCODE,DUNIQUE,DNAME21,DTELES,PURNO:
                                  DDADMNO,PSNAME,PNAME20,PADATE,A5TIME:
                                  PDDATE,D5TIME,AHFUND,MRLOCN,TDIGIT,LOSY
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTEMP2   UPDATE    TEMPFL2;DCODE,DUNIQUE,DNAME21,DTELES,PURNO:
                            DDADMNO,PSNAME,PNAME20,PADATE,A5TIME:
                            PDDATE,D5TIME,AHFUND,MRLOCN,TDIGIT,LOSY
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP2   RESET     KEY16
          WRITE     TEMPFL2,KEY16;DCODE,DUNIQUE,DNAME21,DTELES,PURNO:
                                  DDADMNO,PSNAME,PNAME20,PADATE,A5TIME:
                                  PDDATE,D5TIME,AHFUND,MRLOCN,TDIGIT,LOSY
          RETURN
.
DETEMP2   DELETE    TEMPFL2,KEY16
          RETURN
.
RATEMP3   MOVE      ZERO,OVRCD
          RESET     KEY16
          READ      TEMPFL3,KEY16;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTEMP3   RESET     KEY16
          READ      TEMPFL3,KEY16;;
          RETURN
.
RKTEMP3   MOVE      ZERO,OVRCD
          READKS    TEMPFL3;DCODE,DUNIQUE,DNAME21,DTELES,PURNO:
                            DDADMNO,PSNAME,PNAME20,PADATE,A5TIME:
                            PDDATE,D5TIME,AHFUND,MRLOCN,TDIGIT,LOSY
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTEMP3   MOVE      ZERO,OVRCD
          RESET     KEY16
          READ      TEMPFL3,KEY16;DCODE,DUNIQUE,DNAME21,DTELES,PURNO:
                                  DDADMNO,PSNAME,PNAME20,PADATE,A5TIME:
                                  PDDATE,D5TIME,AHFUND,MRLOCN,TDIGIT,LOSY
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTEMP3   UPDATE    TEMPFL3;DCODE,DUNIQUE,DNAME21,DTELES,PURNO:
                            DDADMNO,PSNAME,PNAME20,PADATE,A5TIME:
                            PDDATE,D5TIME,AHFUND,MRLOCN,TDIGIT,LOSY
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP3   RESET     KEY16
          WRITE     TEMPFL3,KEY16;DCODE,DUNIQUE,DNAME21,DTELES,PURNO:
                                  DDADMNO,PSNAME,PNAME20,PADATE,A5TIME:
                                  PDDATE,D5TIME,AHFUND,MRLOCN,TDIGIT,LOSY
          RETURN
.
DETEMP3   DELETE    TEMPFL3,KEY16
          RETURN
.
RATEMP4   MOVE      ZERO,OVRCD
          RESET     KEY30
          READ      TEMPFL4,KEY30;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTEMP4   RESET     KEY30
          READ      TEMPFL4,KEY30;;
          RETURN
.
RKTEMP4   MOVE      ZERO,OVRCD
          READKS    TEMPFL4;DCODE,DUNIQUE,DNAME21,DTELES,PURNO:
                            DDADMNO,PSNAME,PNAME20,PADATE,A5TIME:
                            PDDATE,D5TIME,AHFUND,MRLOCN,TDIGIT,LOSY
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTEMP4   MOVE      ZERO,OVRCD
          RESET     KEY30
          READ      TEMPFL4,KEY30;DCODE,DUNIQUE,DNAME21,DTELES,PURNO:
                                  DDADMNO,PSNAME,PNAME20,PADATE,A5TIME:
                                  PDDATE,D5TIME,AHFUND,MRLOCN,TDIGIT,LOSY
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTEMP4   UPDATE    TEMPFL4;DCODE,DUNIQUE,DNAME21,DTELES,PURNO:
                            DDADMNO,PSNAME,PNAME20,PADATE,A5TIME:
                            PDDATE,D5TIME,AHFUND,MRLOCN,TDIGIT,LOSY
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP4   RESET     KEY30
          WRITE     TEMPFL4,KEY30;DCODE,DUNIQUE,DNAME21,DTELES,PURNO:
                                  DDADMNO,PSNAME,PNAME20,PADATE,A5TIME:
                                  PDDATE,D5TIME,AHFUND,MRLOCN,TDIGIT,LOSY
          RETURN
.
DETEMP4   DELETE    TEMPFL4,KEY30
          RETURN
.
.==============================================================================
.
          INCLUDE   STD001IO
.
          INC       EXCLNEWB
          INC       PATDOCKY
          INC       PATHSPKY
          INC       TFILENAM                     * Generate Temp File Name
.
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       MRTVISIO/INC
          INC       MRTMASIO/INC
          INC       MRTLOCIO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATDSCIO/INC
          INC       PATECDIO/INC
          INC       PATECOIO/INC
          INC       PATHSPIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATTRNIO/INC
          INC       PMSVX1IO/INC
          INC       WEBERRIO/INC                 * Web Server Error Logging
+

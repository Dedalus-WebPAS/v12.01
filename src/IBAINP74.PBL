.******************************************************************************
.*  System       :  Patient Management                                        *
.*  Program      :  IBAINP74                                                  *
.*  Description  :  Midnight Census Report (adapted from IBABIL74)            *
.******************************************************************************
.*  Date         :  20/01/1994  (V6.01.00)                                    *
.*  Author       :  Gabrielle Cameron                                         *
.*  Function     :  Midnight Census report                                    *
.*                  Adapted from IBABIL74 with the following change           *
.*                  At the end of each ward specific details will be printed  *
.*                  about the :     Admissions                                *
.*                                  Discharges                                *
.*                                  Patients Transfers In                     *
.*                                  Patients Transfers Out                    *
.*                                  Patients On Leave                         *
.*                                  Patients Returned from Leave              *
.*  Modification :                                                            *
.******************************************************************************
.*  V12.00.01 23/05/2025 Bella Turco             TSK 0955096                  *
.*            Alphanumeric changes                                            *
.*  V10.12.04 29/06/2018 Thanh T.                TSK 0858275                  *
.*            Fixed problem with Midnight census report showing the patient   *
.*            that are admitted on the same date as the report date           *
.*  V10.12.03 08/06/2018 Thanh T.                TSK 0858275                  *
.*            Fixed problem with Midnight census report showing the patient   *
.*            that are admitted on the same date as the report date           *
.*  V10.12.02 16/05/2018 Tracey Nguyen           TSK 0837181                  *
.*            Modified PRTB0000 to check for blank Overflow Desc              *
.*  V10.12.01 08/05/2018 Tracey Nguyen           TSK 0837181                  *
.*            Modified PRTB0000 to identify the patients who are transferred  *
.*            to a ward/bed with an 'Overflow' reason in the transfer reason  *
.*            record to be shown on the Midnight Census Report                *
.******************************************************************************
.*        V10.08.01 24/06/2016 Jill Parkinson    TSK 0821138                  *
.*                  Removed changes made in version 10.06.01                  *
.*        V10.06.01 13/07/2015 Ania P             CAR 317108                  *
.*                  Fixed census date/time comparisons in APATxxxx            *
.*        V10.04.04 17/06/2014 Steve Armstrong    CAR 301639                  *
.*                  Moved call to TFILENAM for XXNAME1 into INIT0000.         * 
.*                  Changed SECTION1 tempfile to use TFILNAME instead of      *
.*                  port based name and added call to KILL0000 on exit of     *
.*                  program.                                                  *
.*        V10.04.03 16/05/2014 Peter Vela         CAR 298477                  *
.*                  Added Hospital Boarders                                   * 
.*        V10.04.02 08/02/2014 Ania P             CAR 293365                  *
.*                  Fixed header date.                                        *
.*        V10.04.01 18/12/2013 Peter Vela         CAR 291318                  *
.*                  Added RAs before the writes to temp file                  *
.******************************************************************************
.*        V10.03.07 06/12/2013 Ania P             CAR 293365                  *
.*                  Fixed header date.                                        *
.*        V10.03.06 07/08/2013 Ania P             CAR 289561                  *
.*                  Changed column widths                                     *
.*                  07/08/2013 Ania P             CAR 287532                  *
.*                  Added date/time in heading                                *
.*        V10.03.05 25/07/2013 Peter Vela         CAR 289011                  *
.*                  Changed Ward column to display code rather than desc      *
.*        V10.03.04 16/06/2013 Peter Vela         CAR 287136                  *
.*                  Fixed Ward Only loop in WARD0000                          *
.*        V10.03.03 28/06/2013 Peter Vela         CAR 287532                  *
.*                  Added Admission and Discharges desction back in after     *
.*                  re-model                                                  *
.*        V10.03.02 27/06/2013 Ebon Clements      CAR 287511                  *
.*                   Changes for WEXTN and NSEXTN length change from 4 to 20  *
.*        V10.03.01 28/12/2012 Patrick Adair      CAR 261630                  *
.*                  Removed port number from temp file name                   *
.*        V10.02.02 25/06/2011 Steve Armstrong    CAR 240722                  *
.*                  Removed reference to PATLOCFD.                            *
.*        V10.02.01 10/06/2011 Peter McMullen CAR 240725                      *
.*                  re-model from revised IBABIL74                            *
.******************************************************************************
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.******************************************************************************
.*        V9.11.01  05/12/2008  J.Tan         CAR 180608                      *
.*                  Mods Midnight census report to check for yesterday date   *
.*        V9.10.00  24/06/2008  Mike Laming   CAR 170927                      *
.*                  Ported from V6.16 to V9.10 (add Multi Hospital)           *
.******************************************************************************
.*        V6.14.01  24/07/2006  Peter Vela    CAR 113087                      *
.*                  Fixed duplication of patients NOBLP                       *
.*        V6.13.01  23/12/2005  Peter Vela    CAR 85121                       *
.*                  Fixed print layout for empty ward only wards              *
.*        V6.12.01  26/08/2004  Mike Laming   CAR 52990                       *
.*                  Add "Current Status" report - major changes to clean up   *
.*        V6.11.02  24/03/2004  Mike Laming   CAR 47990 rebound               *
.*                  Fix printing of "Grand Total" (after test for Option 1&2) *
.*        V6.11.01  19/03/2004  Mike Laming   CAR 47990                       *
.*                  Cleanup record counts & Descriptions at PTSM0000          *
.*        V6.07.B01 11/04/2000  Greg Horvat                                   *
.*                  Removed MATADAFD MATADBFD & MATADBIO                      *
.*                  V6.06.01  20/10/1999  Steve Downing  SRF 635334           *
.*                            Fixed W*I error for Particular Nursing Station  *
.******************************************************************************
.
.
          INC       STD001FD
.
          INC       IBASEQFD/INC             * Sequential Numbers File
          INC       PATCONFD/INC
          INC       PATDHEAD/INC
          INC       PATBRDFD/INC
          INC       PATCALAG/INC
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PATDSCFD/INC             * Patient Discharge file
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATNOBFD/INC
          INC       PATNURFD/INC
          INC       PATONLFD/INC
          INC       PATTRNFD/INC             * Patient Transfer file
          INC       PATWR1FD/INC
          INC       PATHSPFD/INC
          INC       TFILEVAR/INC             * Generate Temp File Name
          INC       WEBERRFD/INC             * Web Server Error Logging
+
.         TEMPORARY FILES
.
XXFILE1   IFILE     SQL, FIXED=46, KEY="U1-3,4-4,5-7,8-17"
XXNAME1   DIM       8                              * Temporary File name 1
XXKEY     INIT      " 46 U1-3,4-4,5-7,8-17"
.
.Name     Type   Physical    Start Loc       Description
.----     ----   --------    ---------       -----------
XXWARD    DIM       3           1            * Ward Code
DXXSTYPE  DIM       1           4            * Type of Record
.                                            * 0 = In Ward at Previous Midnight
.                                            * 1 = Admissions
.                                            * 2 = Discharges
.                                            * 3 = Transfers In
.                                            * 4 = Transfers Out
.                                            * 5 = On Leave
.                                            * 6 = Returned from Leave
XXTIME    DIM       5           5            * Time of Event
DXXADMN   DIM       8          10            * Admission Number
XXURNO    DIM       8          18            * U/R Number
XXSURN    DIM       20         26            * Surname of Patients
.                             ----
. end of record                46
.
.  Tempfile Midnight Census Report Hospital Boarders
.
SECTION1  IFILE     SQL,FIXED=22, KEY="U1-8"
.
.Name     Type      Length    Physical  Position  Description
.----     ----      ------    --------  --------  -----------
XADMN     DIM       8            8         1      Admission Number
XWRDESC   DIM       10           10        9      Ward Desc
XBED      DIM       3            3         19     Bed
.
.End of Record                             22
+
SECTIO1   INIT      "secti1"
.
. redefined DIM fields
.
XXSTYPE   FORM      1           4            * Type of Record
XXADMN    FORM      8          10            * Admission Number
.
.**********************************************************************
.*                            VARIABLES                               *
.**********************************************************************
ADIAG     DIM       50
AJDAY     FORM      3                  * JULIAN ADMISSION DAY
.
BJDAY     FORM      3
.
CDESC     DIM       20
CJDAY     FORM      3                  * JULIAN CURRENT DAY
COUNT     FORM      2
.
DATYPE    DIM       3
DHHMM     DIM       5
DNAME15   DIM       15                 * DOCTOR NAME SHORTENED
DPSKIP    DIM       3
DRLGTYP   DIM       30
DSEX      DIM       6
.
EOFNUR    DIM       1
EOFWR1    DIM       1
..
GRDPAT    FORM      4                  * grand total of patients
GRDSTBY   FORM      4                  * grand total of standby patients
GRDLVE    FORM      4                  * grand total of patients on leave
.
IBCNMHOS  FORM      1                  * Multi hospital indicator from controlf
.
LOCDESC   DIM       25
MIN1      FORM      1
.
NEWPAGE   FORM      1                                * New page printed
.                                                    0 = No
.                                                    1 = Yes
.
OVFLDESC  DIM       8                  * Overflow Description        *C-0837181
PAGENO    FORM      3
PHDAYS    FORM      3
PNAME     DIM       33                 * Formatted Patient's Name
PRNTFLG   FORM      1                              * Print Flag
.                                                    0 = No
.                                                    1 = Yes
PSKIP     FORM      1
.
SAVBED    DIM       3
SAVNDESC  DIM       30
SAVNEXTN  DIM       20
SAVNSIST  DIM       30
SAVNSTAT  DIM       3
SAVWEXTN  DIM       20
SAVSTYPE  FORM      1                              * Save Type of Record
SUMDESC   DIM       19                             * Summary Description
SAWARD    DIM       3
SBED      DIM       3
SRLGTYP   DIM       3

SWARD     DIM       3
.
TEMPFILE  DIM       8
TOTPAT    FORM      3                  * total patients in ward
TOTSTBY   FORM      3                  * total standby patients
TOTLVE    FORM      3                  * total patients on leave
TOTBRD    FORM      3                  * total boarders
.
WRDESC    DIM       32
WRSDESC   DIM       10
.
.============================================================================================================

CMDLINE   DIM       80          * Command line

.
DETLIN1   DIM       15
DIM2B     DIM       2
DIM3      DIM       3
DIM4      DIM       4
DIM5      DIM       5
DIM6      DIM       6
DIM8      DIM       8
DIM11     DIM       11
DIM12A    DIM       12
DIM12B    DIM       12
DIM16     DIM       16
DIM18     DIM       18
DIM40     DIM       40                                               *C-0837181

DMSTAT    DIM       9
DOCNAME   DIM       22         * DOCTORNAME TITLE.INITIAL.SURNAME
ADMNDESC  INIT      "Admission"
DSCHDESC  INIT      "Discharge"
ONLVDESC  INIT      "On Leave"
RTLVDESC  INIT      "Returned from Leave"
TRINDESC  INIT      "Transfer In"
TROTDESC  INIT      "Transfer Out"
.
ACTDATE   DIM       8                              * Current Date      *I-47990
WORKTIME  DIM       4                              * Current Date      *I-47990
WORKDATE  DATE      8                              * Work date         *I-47990
DATEFOR1  DIM       12                             *                   *I-47990
DATEFOR2  DIM       12                             *                   *I-47990
DATEFOR3  DIM       10                             *                   *I-47990
REPDATEC  DIM       8                              * Report Date (CCYYMMDD)

GTOTAL0   FORM      4                              * Grand Total prev midnight
GTOTAL1   FORM      4                              * Grand Total Admissions
GTOTAL2   FORM      4                              * Grand Total Discharges
GTOTAL3   FORM      4                              * Grand Total Transfers In
GTOTAL4   FORM      4                              * Grand Total Transfers Out
GTOTAL5   FORM      4                              * Grand Total On Leave
GTOTAL6   FORM      4                              * Grand Tot. Ret from leave
GTOTAL7   FORM      4                              * Grand Total current midnight
.
.
TOTAL     FORM      4                              * Total
TOTALDSC  DIM       15                             * Description with Total
.
.
WRDDESC   INIT      "Ward : "                                          *I-47990
.
WSNAME    DIM       10
WTOTAL0   FORM      4                              * Ward Total prev midnight
WTOTAL1   FORM      4                              * Ward Total Admissions
WTOTAL2   FORM      4                              * Ward Total Discharges
WTOTAL3   FORM      4                              * Ward Total Transfers In
WTOTAL4   FORM      4                              * Ward Total Transfers Out
WTOTAL5   FORM      4                              * Ward Total On Leave
WTOTAL6   FORM      4                              * Ward Tot. Ret from leave
WTOTAL7   FORM      4                              * Ward Total current midnight
.
XNAME     DIM       25
.
YSTRDAY   DIM       8                              * Previous midnight date
+
.**********************************************************************
.*                            CONSTANTS                               *
.**********************************************************************
ASATDATE  INIT      "as at "
ASATMDNT  INIT      "as at Midnight "
BRL       INIT      "("                                                *I-47990
BRR       INIT      ")"
CODEA     INIT      "A "
CODECO    INIT      "CO"                    * patient condition category
CODEM     INIT      "M "
INVUR     INIT      "Invalid UR -"
Z20       INIT      "zzzzzzzzzzzzzzzzzzzz"
Z22       INIT      "zzzzzzzzzzzzzzzzzzzzzz"
.
.----------------------------------------------------------------------
PRGID     INIT      "IBAINP74"
PRGNAM    INIT      "Midnight Census Report"
VERSION   INIT      "V12.01.00"
.----------------------------------------------------------------------
+
.**********************************************************************
.*                               ML0000                               *
.*                              Mainline                              *
.**********************************************************************
ML0000    CALL      INIT0000           * initialization
.
ML1000    CALL      CLR0000            * clear variables
          CALL      OPTN0000           * select option
          BRANCH    EXIT,ML9000
          BRANCH    OPTION,ML2000,ML3000,ML4000,ML5000
.
.         Particular Ward
.
ML2000    CALL      PWRD0000
          BRANCH    EXIT,ML1000
          GOTO      ML6000
.
.         Particular Nursing Station
.
ML3000    CALL      PNUR0000
          BRANCH    EXIT,ML1000
          GOTO      ML6000
.
.         All Nursing Station
.
ML4000    CALL      ANUR0000
          BRANCH    EXIT,ML1000
          GOTO      ML6000
.
.         All Ward
.
ML5000    CALL      AWRD0000
          BRANCH    EXIT,ML1000
.
ML6000    CALL      END0000            * end of report
          GOTO      ML1000
.
.         ....The End
.
ML9000    CALL      DTMP0000           * delete temp files
          CALL      KILL0000
          CHAIN     PGM
+
.**********************************************************************
.*                       INIT0000                                     *
.*                    Initialisation                                  *
.**********************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY    *P64:24,"patbrdaf";
          OPEN      PATBRDA1,"patbrdaf"
          OPEN      PATBRDA2,"patbrdaf"
          OPEN      PATBRDA3,"patbrdaf"
          OPEN      PATBRDA4,"patbrdaf"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patnobef";
          OPEN      PATNOBE1,"patnobef"
.
          DISPLAY   *P64:24,"patnursm";
          OPEN      PATNURS1,"patnursm"
.
          DISPLAY   *P64:24,"patonlvf";
          OPEN      PATONLV1,"patonlvf"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATWR1A3,"patwr1af"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TEN;*13,PTCNTLVE
          READ      CONTROLF,TEN3;*245,CHOSTYP
          READ      CONTROLF,TWENTY;*213,PTCNDPCN
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,HUND25;*173,PTCNBTRE,*174,PTCNBTOD     *C-0837181
          MOVE      "99",CLNO               * force new page
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,XXNAME1
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,TEMPFILE
.
          CALL      CREA0000                  * create/open temp files
.
          RETURN
+
.**********************************************************************
.*                       CLR0000                                      *
.*                    Clear Variables                                 *
.**********************************************************************
CLR0000   MOVE       ZERO,PAGENO           * page number
          PACK       WRDESC,SP30,SP30       * Ward Name
          MOVE       SP20,WRSDESC
          MOVE       ZERO,TOTPAT            * Total Patients
          MOVE       ZERO,TOTSTBY           * Total Standby Patients
          MOVE       ZERO,TOTLVE
          MOVE       ZERO,GRDPAT            * Grand Patients Total
          MOVE       ZERO,GRDSTBY           * Grand Standby Patients Total
          MOVE       ZERO,GRDLVE
          RETURN
+
.**********************************************************************
.*                           OPTN0000                                 *
.*                         Select OPtioN                              *
.**********************************************************************
OPTN0000  MOVE       FALSE,EXIT
          CALL       SCR0000
.
OPTN1000  KEYIN      *P17:10,*EL,*DV,UNDLN1,*P17:10,*V2LON,OPTION;
          COMPARE    ZERO,OPTION
          GOTO       OPTN9000 IF EQUAL
.
          BRANCH     OPTION,OPTN9999,OPTN9999,OPTN9999,OPTN9999
          BEEP
          GOTO       OPTN1000
.
OPTN9000  MOVE       TRUE,EXIT
.
OPTN9999  RETURN
+
.**********************************************************************
.*                            SCR0000                                 *
.*                         SCReeN options                             *
.**********************************************************************
SCR0000   DISPLAY    *P1:3,*EF:
                     *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                     *P1:5,*V2LON,ONE,*HOFF," = Particular Ward":
                     *P1:6,*V2LON,TWO,*HOFF," = Particular Nursing Station":
                     *P1:7,*V2LON,THREE,*HOFF," = All Nursing Stations":
                     *P1:8,*V2LON,FOUR,*HOFF," = All Wards":
                     *P1:10,"Select Option : ";
          RETURN
+
.**********************************************************************
.*                       PWRD0000                                     *
.*                    Keyin Paricular Ward                            *
.**********************************************************************
PWRD0000 MOVE       "Particular Ward",CPHDROPT
         MOVE      FALSE,EXIT
         KEYIN      *P1:13,*EF,"Enter Ward Number : ",*P40:13,"( ",*V2LON,"?":
                    *HOFF," for Scan )",*P21:13,*V2LON,SAWARD,*EL;
         RESET      SAWARD
         GOTO       PWRD9000 IF EOS
.
         ENDSET     SAWARD
         APPEND     SP3,SAWARD
         RESET      SAWARD
.
         MATCH      SP3,SAWARD
         GOTO       PWRD9000 IF EQUAL
.
         CMATCH     QUEST,SAWARD
         GOTO       PWRD1000 IF NOT EQUAL
         CALL       PATWRDDS
         CALL       SCR0000
         DISPLAY    *P17:10,*V2LON,OPTION;
         GOTO       PWRD0000
.
PWRD1000 MOVE       SAWARD,SWARD
         CALL       APAT0000                * build total census file
.
         PACK       KEY6,SWARD,SP6
         CALL       RDWARD1
         BRANCH     OVRCD,PWRD8000
.
         BRANCH     WACTIVE,PWRD8500       * Ignore non-active wards
.
         MOVE       ONE,PSKIP
         MOVE       WBDESC,WRDESC
         MOVE       WWARD,WRSDESC
         MOVE       WEXTN,SAVWEXTN
         DISPLAY    *P30:13,*EL,WRDESC;
.
PWRD2000 MOVE       "60",CLNO
         DISPLAY    *P1:24,*EL,*P20:24,"Ward :",*P40:24,"Bed :":
                    *P50:24,"Adm : ";
         CALL       WARD0000
         GOTO       PWRD9000
.
PWRD8000  DISPLAY   *P1:24,*EL,*B,*V2LON,"** Ward does not exist **    ";
          CALL      HITENTER
          GOTO      PWRD0000
.
PWRD8500  DISPLAY   *P1:24,*EL,*B,*V2LON,"** Ward is not active **    ";
          CALL      HITENTER
          GOTO      PWRD0000
.
PWRD9000  MOVE      TRUE,EXIT
.
PWRD9999  RETURN
+
.**********************************************************************
.*                             PNUR0000                               *
.*                  Keyin Paricular Nursing Station                   *
.**********************************************************************

PNUR0000  MOVE      "Particular Nurs. St.",CPHDROPT
          MOVE      FALSE,EXIT
          KEYIN     *P1:13,*EF,"Enter Station Number : ",*P40:13,"( ",*V2LON:
                    "?",*HOFF," for Scan )",*P24:13,*V2LON,SAVNSTAT,*EL;
          RESET     SAVNSTAT
          GOTO      PNUR9000 IF EOS
.
          ENDSET    SAVNSTAT
          APPEND    SP3,SAVNSTAT
          RESET     SAVNSTAT
.
          MATCH     SP3,SAVNSTAT
          GOTO      PNUR9000 IF EQUAL
.
          CMATCH    QUEST,SAVNSTAT
          GOTO      PNUR1000 IF NOT EQUAL
          CALL      PATNURDS
          DISPLAY   *P1:24,*EL;
          CALL      HITENTER
          CALL      SCR0000
          DISPLAY   *P17:10,*V2LON,OPTION;
          GOTO      PNUR0000
.
PNUR1000  CALL      SEP0000                 * separate page ?
.
          CALL      APAT0000                * build total census file
          MOVE      SP3,NSTAT
          MOVE      SP3,NWARD
          MOVE      SP3,SAWARD
.
          PACK      KEY6 WITH SAVNSTAT,SP3
          CALL      RDNURS1
          BRANCH    OVRCD,PNUR8000
          DISPLAY   *P32:13,*EL,NSDESC;
          MOVE      NSDESC,SAVNDESC         * N/Station description
          MOVE      NSIST,SAVNSIST          * Sister-in-Charge
          MOVE      NSEXTN,SAVNEXTN         * Ward extension
.
PNUR2000  MOVE      "60",CLNO
          DISPLAY   *P1:24,*EL,*P20:24,"Ward :",*P40:24,"Bed :";
.
          CALL     NURS0000
.
          GOTO     PNUR9000
.
PNUR8000  DISPLAY   *P1:24,*EL,*B,*V2LON,"** Station does not exist **    ";
          CALL      HITENTER
          GOTO      PNUR0000
.
PNUR9000  MOVE      TRUE,EXIT
.
PNUR9999  RETURN
+
.**********************************************************************
.*                            AWRD0000                                *
.*                           All Wards                                *
.**********************************************************************
AWRD0000  MOVE      "All Wards",CPHDROPT
          CALL      KHOS0000                * Check which hospital
          BRANCH    EXIT,AWRD9999
          MOVE      FALSE,EXIT
          CALL      SEP0000                 * Separate page ?
          DISPLAY   *P1:24,*EL,*P20:24,"Ward :",*P40:24,"Bed :";
.
          CALL      APAT0000                * build total census file
.
          MOVE      SP3,SAWARD
          MOVE      SP6,KEY6
          CALL      RDSWARD1
          CALL      RDKWARD1                * read first ward record
          BRANCH    OVRCD,AWRD9999
.
          MOVE      SP1,EOFWR1
AWRD2000  CALL      WARD0000
          MATCH     SP1,EOFWR1
          GOTO      AWRD2000 IF EQUAL
.         BRANCH    EXIT,AWRD2000
.
          CALL      GTOT0000                * grand totals
.
AWRD9000  MOVE      ONE,EXIT
.
AWRD9999  RETURN
+
.**********************************************************************
.*                       ANUR0000                                     *
.*                   All Nursing Stations                             *
.**********************************************************************
ANUR0000  MOVE      "All Nursing Stations",CPHDROPT
          MOVE      FALSE,EXIT
          CALL      SEP0000
.
ANUR0500  DISPLAY   *P1:24,*EL,"Station : ",*P20:24,"Ward :",*P40:24,"Bed :";
.
          CALL      APAT0000                * build total census file
.
          MOVE      SP6,KEY6
          CALL      RDSNURS1
.
ANUR1000  CALL      RDKNURS1
          BRANCH    OVRCD,ANUR9000
.
ANUR1001  CALL      NURS0000
          BRANCH    EXIT,ANUR1001
.
ANUR9000  MOVE      ONE,EXIT
.
ANUR9999  RETURN
+
.**********************************************************************
.*                             HEAD0000                               *
.*                        Heading for report                          *
.**********************************************************************
HEAD0000  CALL      HEADP000   * std page header
          CALL      HEADW000
.
HEAD9999  RETURN
+
.**********************************************************************
.*                             HEADA000                               *
.*                        Heading for report                          *
.**********************************************************************
HEADA000  CALL      HEADP000   * std page header
.
          CALL      HEADB000
.
HEADA999  RETURN
+
.**********************************************************************
.*                             HEADP000                               *
.*                        Just the top heading                        *
.**********************************************************************
HEADP000  ADD       ONE,PAGENO
          MOVE      ONE,CNOUNDLN
          CALL      IBACLOCK
          UNPACK    CTIMEIS,DIM5,DIM3
.
          MOVE      ACTDATE,WORKDATE
          MATCH     "2345",WORKTIME
          IF        @LESS
            SUB       ONE,WORKDATE
          ENDIF
          MOVE      WORKDATE,YSTRDAY                     * Previous date
          UNPACK    YSTRDAY,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                              * Format date
.
          PACK      CPHDROP1,PRGNAM,SP70                 * For HEAD132
          IF        COPTION=1
            PACK      CPHDROP2,ASATMDNT,CPCDATE
          ELSE
            PACK      CPHDROP2,ASATDATE,DIM5,SP1,DATEFOR3
          ENDIF
.
          CALL      HEAD132
.
          ADD       TWO,CLNO
          BRANCH    OPTION,HEADP999,HEADP100,HEADP100,HEADP999
          GOTO      HEAD9999
.
HEADP100  PRINT     *N,*1,"SISTER : ",SAVNSIST,*40,"STATION : ",SAVNSTAT:
                    *55,SAVNDESC,*94," Extension : ",SAVNEXTN
          ADD       ONE,CLNO
.
HEADP999  RETURN
+
.**********************************************************************
.*                       HEADW0000                                    *
.*                    Print Ward Heading                              *
.**********************************************************************
HEADW000  PRINT     *N,*40,"WARD    : ",SAWARD,*55,WRDESC:
                    *95,"Extension : ",SAVWEXTN,*N
          ADD       TWO,CLNO
          CALL      PGE0000
.
          PRINT        *1,"| Ward",*9,"|Admission",*19,"|Patient's Name":
                      *53,"|Rel|Sex|Age|Admission Diagnosis":
                     *116,"|Attending",*132,"|":
                    *N,*1,"| /Bed",*9,"| Number",*19,"| ":
                      *53,"|   |M/F|   |":
                     *116,"|Doctor",*132,"|"
          ADD       TWO,CLNO
          CALL      PGE0000
.
HEADW900 RETURN
+
.**********************************************************************
.*                             PGE0000                                *
.*                       Print line separator                         *
.**********************************************************************
PGE0000   PRINT     *1,"*-------------------------------------------":
                       "--------------------------------------------":
                       "-------------------------------------------*"
          ADD       ONE,CLNO
          RETURN
+
.*******************************************************************************
.*                             UND70                                           *
.*                        UNDer line 70 characters                             *
.*******************************************************************************
UND70     PRINT     *1,"*----------------------------------":
                       "----------------------------------*"
....      ADD       ONE,CLNO
          RETURN
+
.**********************************************************************
.*                       PRTB0000                                     *
.*                     Print Bed/Patient                              *
.**********************************************************************
PRTB0000  COMPARE   "58",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PACK      KEY8,WADMNO
          PACK      DIM3,PSAGE
          MATCH     ZEROVISN,WADMNO   
          IF        @EQUAL
            PACK      KEY8,SP30
            PACK      DIM3,SP30
            GOTO      PRTB2000                                 *C-0837181-start
          ENDIF
.
PRTB0100  CALL      RDMISS1                   
          BRANCH    OVRCD,PRTB9999
.
          COMPARE   THREE,ASTAT              * Discharge patient
          GOTO      PRTB0200 IF EQUAL        
.
          MATCH     ADATE,REPDATEC           * Admission Date <= Report Date
          GOTO      PRTB9999 IF LESS         * No    
          GOTO      PRTB0500
.
PRTB0200  CALL      RDDSCH1                  * Check discharge date
          BRANCH    OVRCD,PRTB9999
.
          MATCH     REPDATEC,DDATE           * Report Date <= Discharge Date
          GOTO      PRTB9999 IF LESS         * No
.
PRTB0500  MATCH     "1",PTCNBTRE                   * Collect Transfer reason?
          GOTO      PRTB2000 IF NOT EQUAL          
.
          MATCH     SP70,PTCNBTOD                  * Overflow desc is blank? 
          GOTO      PRTB2000 IF EQUAL | @EOS
.
.-------- If current Transfer record in pattranf has a Transfer Reason
.-------- Cat RB code with indicator1="O" then display the parameter variable
.-------- PTCNBTOD right justified in the Admission Diagnosis field on the 
.-------- report.
.
          PACK      KEY30,WADMNO,REPDATEC,Z70           * Read PATTRAN file
          CALL      RDSTRAN2                            * Positional Read
          CALL      RDPTRAN2                            * Read previous record
          BRANCH    OVRCD,PRTB2000
.
          MATCH     WADMNO,TADMN                        * Same visit?
          GOTO      PRTB2000 IF NOT EQUAL
.
          MATCH     SP70,PTTRTRTY                       * Transfer Reason set?
          GOTO      PRTB2000 IF EQUAL | @EOS
.
          PACK      KEY5,ANSR,ANSB,PTTRTRTY,SP70        * Check Category RB
          CALL      RDCODE1
          BRANCH    OVRCD,PRTB2000
.
          MATCH     ANSO,TCINDC1                        * Overflow ind on?
          GOTO      PRTB2000 IF NOT EQUAL 
.
          MOVE      PTCNBTOD,OVFLDESC
          RJUSTIFY  OVFLDESC
          MOVE      ADIAG,DIM40
.
.-------- Includes Transfer Reason Overflow Description in report --------
PRTB1000  PRINT     *1,"|",WWARD,SP1,SAVBED,*9,"| ",KEY8,*19,"|",PNAME:
                    *53,"|",PREG,*57,"|",PSEX,*61,"|",DIM3:
                    *65,"|",DIM40:
                    *106,"  ",OVFLDESC:
                    *116,"|",DNAME15,*132,"|"
          GOTO      PRTB9000
.
.-------- DO NOT include Transfer Reason Overflow Description in report -----
PRTB2000  PRINT     *1,"|",WWARD,SP1,SAVBED,*9,"| ",KEY8,*19,"|",PNAME:
                    *53,"|",PREG,*57,"|",PSEX,*61,"|",DIM3:
                    *65,"|",ADIAG:
                    *116,"|",DNAME15,*132,"|"
.          MOVE      WWARD,SWARD
.
PRTB9000  ADD       ONE,CLNO                                    *C-0837181-end
          CALL      PGE0000
          DISPLAY   *P27:24,*V2LON,WWARD,*P46:24,SAVBED;
.
          PACK      KEY8,WADMNO,SP70
          CALL      RDASECT1
          IF        OVRCD=1
            MOVE      WADMNO,XADMN
            MOVE      WRSDESC,XWRDESC
            MOVE      SAVBED,XBED
            CALL      WRSECT1
          ENDIF
.
PRTB9999  RETURN
+
.**********************************************************************
.*                       PRTS0000                                     *
.*                     Print Ward Summary                             *
.**********************************************************************
PRTS0000  COMPARE   "43",CLNO
          CALL      HEADP000 IF NOT LESS
.
          CALL      UND70
          PRINT     *1,"!Patients as at Midnight ",DATEFOR1:
                    *39,WTOTAL0,*53,TOTALDSC,*70,"!":
                    *N,*1,"! Movements during ",DATEFOR3,*70,"!":
                    *N,*1,"!           - Admissions ",*39,WTOTAL1,*70,"!":
                    *N,*1,"!           - Discharges ",*39,WTOTAL2,*70,"!":
                    *N,*1,"!           - Transfers In ",*39,WTOTAL3,*70,"!":
                    *N,*1,"!           - Transfers Out ",*39,WTOTAL4,*70,"!":
                    *N,*1,"!           - Went On Leave ",*39,WTOTAL5,*70,"!":
                    *N,*1,"!           - Return from Leave ",*39,WTOTAL6,*70,"!"
.
.....     PRINT     *1,"!Total as at Midnight ",DATEFOR2:               D-52990
          PRINT     *1,"!",DETLIN1,DATEFOR2:  * total at midnight or now
                    *39,WTOTAL7,*70,"!":
                    *N,*1,"!",*70,"!":
                    *N,*1,"!",*70,"!":
                    *N,*1,"!",*23:
                    " Verified By : ______________________________ ",*70,"!"
          ADD       "15",CLNO
          CALL      UND70
          PRINT     *N
.
PRTS999   RETURN
+
.**********************************************************************
.*                             TOT0000                                *
.*                           Print Total                              *
.**********************************************************************
TOT0000   COMPARE   "58",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PRINT     *N,*44,"Total Boarders : ",TOTBRD:
                    *67,"Total In Ward : ",TOTPAT:
                    *90,"Total on Leave : ",TOTLVE:
                    *113,"Total Standby : ",TOTSTBY
          ADD       TWO,CLNO
.
TOT9999  RETURN
+
.**********************************************************************
.*                             GTOT0000                               *
.*                        Print Grand Total                           *
.**********************************************************************
GTOT0000  CALL      HEADP000         * force new page
.
          PRINT     *N,*50,"GRAND TOTALS : ",*66,"Total in Ward : ",GRDPAT:
                                             *88,"Total on Leave : ",GRDLVE:
                                            *112,"Total Standby : ",GRDSTBY:
                    *N,*50,"------------"
.
          MOVE      "Grand Totals",TOTALDSC
          MOVE       GTOTAL0,WTOTAL0
          MOVE       GTOTAL1,WTOTAL1
          MOVE       GTOTAL2,WTOTAL2
          MOVE       GTOTAL3,WTOTAL3
          MOVE       GTOTAL4,WTOTAL4
          MOVE       GTOTAL5,WTOTAL5
          MOVE       GTOTAL6,WTOTAL6
          MOVE       GTOTAL7,WTOTAL7
          CALL       PRTS0000          * print summary
.
          MOVE       ZERO,GTOTAL0
          MOVE       ZERO,GTOTAL1
          MOVE       ZERO,GTOTAL2
          MOVE       ZERO,GTOTAL3
          MOVE       ZERO,GTOTAL4
          MOVE       ZERO,GTOTAL5
          MOVE       ZERO,GTOTAL6
          MOVE       ZERO,GTOTAL7

          RETURN
+
.**********************************************************************
.*                             END0000                                *
.*                          End of Report                             *
.**********************************************************************
END0000   COMPARE   ZERO,PAGENO
          GOTO      NDAT0000 IF NOT EQUAL
          COMPARE   "57",CLNO
          CALL      HEADP000 IF NOT LESS
.
          PRINT     *N," ***  End of Report  ***"
.
          DISPLAY   *P1:24,*EL,*P24:24,*V2LON:
                    "** Report Generation Completed **",*W2;
END9999   RETURN
+
.**********************************************************************
.*                             NDAT0000                               *
.*                       No Data to be Printed                        *
.**********************************************************************
NDAT0000  DISPLAY   *P1:24,*EL,*P25:24,*B,*V2LON:
                    "** No available data on file **",*W2;
          RETURN
+
.*********************************************************************
.*                  VOLP0000                    Called by : lots     *
.*        Check that the patient is on Short-term leave.             *
.*        Para's  : OADMNO        Admission Number                   *
.*        Returns : EXIT = 1      not a valid patient                *
.*********************************************************************
VOLP0000  COMPARE   ONE,PTCNTLVE
          GOTO      VOLP9000 IF NOT EQUAL   * not using Type of Leave
.
          PACK      KEY30,OADMNO,Z22
          CALL      RDSTRAN2
          CALL      RDPTRAN2
          BRANCH    OVRCD,VOLP9100          * no tran record
.
          MATCH     OADMNO,TADMN
          GOTO      VOLP9100 IF NOT EQUAL   * no tran record
.
          MATCH     "L",TMOVE
          GOTO      VOLP9100 IF NOT EQUAL   * not a leave record
.
          MATCH     SP3,PTTRLTYP
          GOTO      VOLP9100 IF EQUAL       * no on-leave type
.
          PACK      KEY5,ANST,ANSL,PTTRLTYP
          CALL      RDCODE1
          BRANCH    OVRCD,VOLP9100          * invalid Type of Leave
.
          MATCH     SP1,TCINDC1
          GOTO      VOLP9100 IF NOT EQUAL   * not short term leave
.
VOLP9000  MOVE      FALSE,EXIT
          GOTO      VOLP9999
.
VOLP9100  MOVE      TRUE,EXIT
.
VOLP9999  RETURN
++
.**********************************************************************
.*                       NURS0000                                     *
.*               Process One Nursing Station                          *
.* This routine assumes we are positioned on the ns master record     *
.* (NWARD = "   ") on entry.                                          *
.**********************************************************************
NURS0000 MOVE      FALSE,EXIT
         MOVE      NSTAT,SAVNSTAT
         MOVE      NSDESC,SAVNDESC         * N/Station description
         MOVE      NSIST,SAVNSIST          * Sister-in-Charge
         MOVE      NSEXTN,SAVNEXTN         * Ward extension
         MOVE      ZERO,GRDPAT             * Grand Patients Total
         MOVE      ZERO,GRDSTBY            * Grand Standby Patients Total
         MOVE      ZERO,GRDLVE
.
NURS1000 CALL      RDKNURS1
         IF        OVRCD  = 1
           MOVE      "Y",EOFNUR
         ENDIF
         BRANCH    OVRCD,NURS9000
         MATCH     NSTAT,SAVNSTAT
         GOTO      NURS9000 IF NOT EQUAL
.
         PACK      KEY6,NWARD,SP30
         CALL      RDWARD1
         BRANCH    OVRCD,NURS1000          * (should not happen)
         CALL      WARD0000
         GOTO      NURS1000
.
NURS9000 CALL      GTOT0000
         MOVE      TRUE,EXIT
         MATCH     "Y",EOFNUR
         IF        @EQUAL
           MOVE      FALSE,EXIT
         ENDIF
NURS9999 RETURN
+
.**********************************************************************
.*                       WARD0000                                     *
.*                    Process One  Ward                               *
.* This routine assumes we are positioned on the ward master record   *
.* (WBED = "   ") on entry.                                           *
.**********************************************************************
WARD0000 MOVE      FALSE,EXIT
.
         COMPARE   ZERO,WACTIVE
         GOTO      WARD0500 IF NOT EQUAL
.
         COMPARE   ONE,IBCNMHOS         * Using multi hospital
         GOTO      WARD1000 IF NOT EQUAL
.
         PACK      PTHSHOSP,PTHSHOSP,SP70
         MATCH     SP10,PTHSHOSP        * All Hospitals
         GOTO      WARD1000 IF EQUAL
.
         MATCH     PTWDHOSN,PTHSHOSP    * Correct hospital
         GOTO      WARD1000 IF EQUAL
.
. If we are processing a single ward we cannot select an inactive one, but
. process all wards will find them, and we just skip over the entire ward set.
.
WARD0500 PACK      KEY6,WWARD,Z70         * set key for end of this ward
         CALL      RDSWARD1
         CALL      RDKWARD1               * skip to next if any
         IF        OVRCD = 1
           MOVE      "Y",EOFWR1
         ENDIF
         BRANCH    OVRCD,WARD9000
         GOTO      WARD0000               * start again
.
WARD1000 MOVE      WBDESC,WRDESC
         MOVE      WWARD,WRSDESC
         MOVE      PTWDSDES,TOTALDSC
         MOVE      WEXTN,SAVWEXTN
         MOVE      WWARD,SAWARD
         MOVE      SP3,SAVBED
         DISPLAY   *P30:13,*EL,WRDESC;
.
         COMPARE   "52",CLNO              * all 6 for head, 2 for bed
         GOTO      WARD1100 IF NOT LESS
         CALL      HEADW0000
         GOTO      WARD1200
.
WARD1100 CALL      HEAD0000
.
WARD1200 STRIP     WRDESC
         ENDSET    WRDESC
         APPEND    " (continued)",WRDESC
         RESET     WRDESC
.
.  Find patients assigned to the ward, but without a bed
.
WARD2000 PACK      KEY13 WITH WWARD,SP10   * report the no-beds
         CALL      RDSNOBE1
WARD2100 CALL      RDKNOBE1
         BRANCH    OVRCD,WARD3000
.
         MATCH     WWARD,NBWARD
         GOTO      WARD3000 IF NOT EQUAL
         MOVE      NBADMNO,WADMNO
         CALL      GETP0000
         BRANCH    EXIT,WARD2100
         ADD       ONE,TOTPAT
         CALL      PRTB0000
         GOTO      WARD2100
.
WARD3000 PACK      KEY14,WWARD,SP20        * report patients on leave
         CALL      RDSONLV1
WARD3100 CALL      RDKONLV1
         BRANCH    OVRCD,WARD4000
.
         MATCH     WWARD,OWARD
         GOTO      WARD4000 IF NOT EQUAL
         CALL      VOLP0000                * is patient on short term leave >
         BRANCH    EXIT,WARD3100           * not on short term leave
.
         MOVE      OADMNO,WADMNO
         MOVE      "LVE",SAVBED
         MOVE      SP20,WBDESC
         CALL      GETP0000
         BRANCH    EXIT,WARD3100
         ADD       ONE,TOTLVE
         CALL      PRTB0000
         GOTO      WARD3100

WARD4000 BRANCH    WINPUT,WARD8000         * if ward only, straight to totals
.

WARD5000 CALL      RDKWARD1
         IF        OVRCD = 1
           MOVE      "Y",EOFWR1
         ENDIF
         BRANCH    OVRCD,WARD8000
.
         MATCH     SP3,WBED
.        GOTO      WARD8000 IF EQUAL
         IF        @EQUAL
           COMPARE   ONE,WINPUT
           IF        @EQUAL
.
             CALL      PBRD0000               * Print Hospital Boarders
             ADD       TOTPAT,GRDPAT          * add for grand patient total
             ADD       TOTSTBY,GRDSTBY        * add for grand standby total
             ADD       TOTLVE,GRDLVE
             CALL      TOT0000
             MOVE      ZERO,TOTPAT            * Total Patients
             MOVE      ZERO,TOTSTBY           * Total Standby Patients
             MOVE      ZERO,TOTLVE
.
             CALL      WSUM0000               * calc and print ward summary
.
.                                  * if new page per ward, force headings
             IF        PSKIP = 0
               MOVE      "99",CLNO
             ENDIF
             GOTO      WARD9000
.
           ELSE
             GOTO      WARD8000
           ENDIF
         ENDIF
.
         MATCH     WWARD,SAWARD
         GOTO      WARD8000 IF NOT EQUAL
.
         BRANCH    WACTIVE,WARD5000        * Non-active bed, ignore
.
         MOVE      WBED,SAVBED
.
WARD6000 CALL      GETP0000
         MATCH     WADMNO,ZEROVISN
         IF        !@EQUAL
           ADD       ONE,TOTPAT
         ENDIF
         CALL      PRTB0000
.
         MATCH     ZEROVISN,WSTBY
         GOTO      WARD5000 IF EQUAL
         MOVE      WSTBY,WADMNO
         MOVE      "STB",SAVBED

         CALL      GETP0000
         BRANCH    EXIT,WARD5000
         ADD       ONE,TOTSTBY
         CALL      PRTB0000
.
         GOTO      WARD5000
.
WARD8000 CALL      PBRD0000               * Print Hospital Boarders
         ADD       TOTPAT,GRDPAT          * add for grand patient total
         ADD       TOTSTBY,GRDSTBY        * add for grand standby total
         ADD       TOTLVE,GRDLVE
         CALL      TOT0000                * print totals
         MOVE      ZERO,TOTPAT            * Total Patients
         MOVE      ZERO,TOTSTBY           * Total Standby Patients
         MOVE      ZERO,TOTLVE
.
         CALL      WSUM0000               * calc and print ward summary
.
.                                  * if new page per ward, force headings
         IF        PSKIP = 0
           MOVE      "99",CLNO
         ENDIF
.
         COMPARE   ONE,WINPUT              * is this a no-bed ward
         GOTO      WARD9000 IF NOT EQUAL
.
                                          * skip to next ward
         PACK      KEY6,WWARD,Z70         * set key for end of this ward
         CALL      RDSWARD1
         CALL      RDKWARD1               * skip to next if any
         IF        OVRCD = 1
           MOVE      "Y",EOFWR1
         ENDIF
         GOTO      WARD9000
.
WARD9000 MOVE      FALSE,EXIT
         MATCH     "Y",EOFWR1
         IF         !@EQUAL
           MOVE       TRUE,EXIT
         ENDIF
WARD9999 RETURN
+
.*******************************************************************************
.*                             WSUM0000                                        *
.*                       Tally sumary of ward figures                          *
.*******************************************************************************
WSUM0000 MOVE      ZERO,WTOTAL0           * Clear total Previous Midnight
         MOVE      ZERO,WTOTAL1           * Clear total No. of Admissions
         MOVE      ZERO,WTOTAL2           * Clear total No. of Discharges
         MOVE      ZERO,WTOTAL3           * Clear total No. of Trans. In
         MOVE      ZERO,WTOTAL4           * Clear total No. of Trans. Out
         MOVE      ZERO,WTOTAL5           * Clear total No. of on Leave
         MOVE      ZERO,WTOTAL6           * Clear total No. of Return leave
         MOVE      ZERO,WTOTAL7           * Clear total Current Midnight
.
         MOVE      FALSE,PRNTFLG            * Not Printed
         MOVE      TRUE,NEWPAGE             * Print First heading
.
         PACK      KEY17,SAWARD,SP20
         CALL      RSXXFIL1                 * Positional Read
WSUM1000 CALL      RKXXFIL1                 * Read next record
         BRANCH    OVRCD,WSUM3000           * EOF, exit routine
.

         MATCH     SAWARD,XXWARD            * Same Ward ?
         GOTO      WSUM3000 IF NOT EQUAL    * No, exit routine
.
         BRANCH    XXSTYPE,WSUM2000,WSUM2000,WSUM2000:
                            WSUM2000,WSUM2000,WSUM2000
.
         ADD       ONE,WTOTAL0              * Add one to record type  = 0
         GOTO      WSUM1000                 * Read next record

WSUM2000 LOAD      TOTAL,XXSTYPE,WTOTAL1,WTOTAL2,WTOTAL3:
                                  WTOTAL4,WTOTAL5,WTOTAL6
         ADD       ONE,TOTAL                * Add ward record type
         STORE     TOTAL,XXSTYPE,WTOTAL1,WTOTAL2,WTOTAL3:  * Add ward total
                                  WTOTAL4,WTOTAL5,WTOTAL6
.
         COMPARE   TRUE,NEWPAGE             * New page printed
         GOTO      WSUM2500 IF EQUAL        * Yes print header
.
         COMPARE   XXSTYPE,SAVSTYPE         * Same Type of Record ?
         GOTO      WSUM2700  IF EQUAL       * Yes do not print header
.
WSUM2500 CALL      UND70
         LOAD      SUMDESC,XXSTYPE,ADMNDESC,DSCHDESC,TRINDESC:
                                   TROTDESC,ONLVDESC,RTLVDESC
         PRINT     *1,"!",*4,SUMDESC,*53,SWARD,*70,"!"
         PRINT     *1,"!",*4,"-------------------",*70,"!",*N:
                   *1,"!",*4,"Admn No":
                          *20,"U/R No":
                          *34,"Time":
                          *47,"Surname":
                          *70,"!"
         MOVE      FALSE,NEWPAGE            * New page printed
         ADD       FOUR,CLNO
.
WSUM2700 MOVE      TRUE,PRNTFLG             * Printed
.
         PRINT   *1,"!",*4,XXADMN:
                        *20,XXURNO:
                        *34,XXTIME:
                        *47,XXSURN:
                        *70,"!"
         ADD       ONE,CLNO
.
         COMPARE   "58",CLNO
         IF        !@LESS
           CALL      UND70
WSUM2800   CALL      HEAD0000
         ENDIF
.
         MOVE      XXSTYPE,SAVSTYPE         * Save type of record
.
         GOTO      WSUM1000
.
WSUM3000 COMPARE   TRUE,PRNTFLG
         CALL      UND70 IF EQUAL
.
         ADD       WTOTAL0,WTOTAL7
         ADD       WTOTAL1,WTOTAL7
         SUB       WTOTAL2,WTOTAL7
         ADD       WTOTAL3,WTOTAL7
         SUB       WTOTAL4,WTOTAL7
         SUB       WTOTAL5,WTOTAL7
         ADD       WTOTAL6,WTOTAL7
.
         ADD       WTOTAL0,GTOTAL0        * Grand total Previous Midnight
         ADD       WTOTAL1,GTOTAL1        * Grand total No. of Admissions
         ADD       WTOTAL2,GTOTAL2        * Grand total No. of Discharges
         ADD       WTOTAL3,GTOTAL3        * Grand total No. of Trans. In
         ADD       WTOTAL4,GTOTAL4        * Grand total No. of Trans. Out
         ADD       WTOTAL5,GTOTAL5        * Grand total No. of on Leave
         ADD       WTOTAL6,GTOTAL6        * Grand total No. of Return leave
         ADD       WTOTAL7,GTOTAL7        * Grand total Current Midnight
.
          CALL      PRTS0000               * Print ward summary
.
WSUM9999  RETURN
+
.**********************************************************************
.*                             GETP0000                               *
.*                       Fetch Patient data                           *
.**********************************************************************
GETP0000  PACK      PNAME,SP30,SP30
          MOVE      SP30,DNAME15
          MOVE      SP30,DATYPE
          MOVE      SP30,PREG
          MOVE      SP30,PSEX
          MOVE      SP30,PSAGE
          MOVE      SP30,ADIAG
          MOVE      FALSE,EXIT
          PACK      LOCDESC,SP30,SP30

          PACK      KEY8,WADMNO
          MOVE      ZEROVISN,WADMNO
          CALL      RDMISS1
.
.         Check if patient is currently admitted
          BRANCH    OVRCD,GETP9999
          IF        ASTAT = 2
            GOTO      GETP0500
          ENDIF
.
.         Check if patient is on leave
          IF        ASTAT = 4
            GOTO      GETP0500
          ENDIF
.
          MOVE      TRUE,EXIT
          GOTO      GETP9999
.
GETP0500  MATCH     ADATE,REPDATEC           * Admission Date <= Report Date
          IF        @LESS                   
            MOVE      TRUE,EXIT              * No, current admission
            GOTO      GETP9999
          ENDIF
.
GETP1000  MOVE      AADMNO,WADMNO
          MOVE      ADIAG1,ADIAG
.
          MOVE      AURNO,KEY8
          PACK      PSNAME,INVUR,AURNO
          UNPACK    SP30,PTITL,PGNAME
          CALL      RDMAST1
.                                           * get doctor name
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD,GETP5000
          MOVE      DGNAME,ANS
          PACK      DNAME15 WITH ANS,DOT,DSNAME
.
GETP5000  STRIP     PSNAME
          STRIP     PTITL
          STRIP     PGNAME
          PACK      PNAME,PSNAME,SP1,PTITL,SP1,PGNAME
.
GETP9000  MOVE      FALSE,EXIT
.
GETP9999  RETURN
+
.*******************************************************************************
.*                                APAT0000                                     *
.*         get All PATients for processing and store in temp file              *
.*******************************************************************************
APAT0000  MOVE      ZERO,EXIT
.
          CALL      OPTWO000               * display Midnight or Current
          BRANCH    EXIT,APAT9000
.
          CALL      SETDT000               * set date ranges & headings
.
          DISPLAY   *P1:24,*EL,*P20:24,*EL,*V2LON:
                    "** Processing Patients          **"
.
          CALL      CTMP0000                 * Create Temp file
.
.         Current Admissions
.
          PACK      KEY9,TWO,SP8             * Get all current admissions
          CALL      RDSMISS2                 * Positional Read
APAT0100  CALL      RDKMISS2                 * Read Next Record
          BRANCH    OVRCD,APAT1000           * EOF, get on leave patient
.
          COMPARE   TWO,ASTAT                * Current Admission ?
          GOTO      APAT1000 IF NOT EQUAL    * No, get on leave patient
.
          MATCH     ADATE,REPDATEC           * Admission Date <= Report Date
          GOTO      APAT0100 IF LESS         * No, current admission
.
          DISPLAY   *P43:24,AADMNO
.
          CALL      PROC0000                 * Process record
          GOTO      APAT0100                 * Get next current admission
.
.         On leave Patients
.
APAT1000  PACK      KEY9,FOUR,SP8            * Get all on leave patients
          CALL      RDSMISS2                 * Positional Read
APAT1100  CALL      RDKMISS2                 * Read Next Record
          BRANCH    OVRCD,APAT3000           * EOF, Process discharge records
.
          COMPARE   FOUR,ASTAT               * On leave patient ?
          GOTO      APAT3000 IF NOT EQUAL    * No, exit routine
.
          MATCH     ADATE,REPDATEC           * Admission Date <= Report Date
          GOTO      APAT1100 IF LESS         * No, get next on leave patient
          DISPLAY   *P43:24,AADMNO
.
          CALL      PROC0000                 * Process record
          GOTO      APAT1100                 * Get next on leave paient
.
.         Discharge Patients
.
APAT3000  PACK      KEY16,REPDATEC,SP8       * Get all discharge patients
          CALL      RDSDSCH2                 * Positional Read
APAT3100  CALL      RDKDSCH2                 * Read Next Record
          BRANCH    OVRCD,APAT9000           * EOF, exit routine
.
          MATCH     REPDATEC,DDATE            * Report Date <= Discharge Date
          GOTO      APAT3100 IF LESS         * No, get next on discharge patient
.
          PACK      KEY8,DADMNO              * Read Admission file for ADATE
          CALL      RDMISS1
          COMPARE   ZERO,OVRCD               * Admission Number exist ?
          GOTO      APAT3200 IF EQUAL        * Yes. Check ADATE
.
          GOTO      APAT3100                 * Error get next record
.
APAT3200  MATCH     ADATE,REPDATEC           * Admission Date <= Report Date
          GOTO      APAT3100 IF LESS         * No, get next discharge patient
.
          DISPLAY   *P43:24,DADMNO
.
          CALL      PROC0000                 * Process record
          GOTO      APAT3100                 * Get next on leave paient
.
.    prevent shadow of last PMI rec if first bed reported is empty
.
APAT9000  MOVE      SP30,PGNAME
          MOVE      SP30,PSNAME
          MOVE      SP30,PREG
          MOVE      SP30,PSEX
          PACK      ADIAG,SP30,SP30
.
APAT9999  RETURN
+
.*****************************************************************************
.*                              OPTWO000
.*                Second Option screen asking Midnight or Current
.*****************************************************************************
OPTWO000  MOVE      ZERO,EXIT
.
          DISPLAY   *P1:17,*V2LON,"1",*HOFF," = Midnight Census report":
                    *P1:18,*V2LON,"2",*HOFF," = Current time Census report";
.
OPTWO100  KEYIN     *P1:20,"Option : ",*EL,*V2LON,COPTION
          BRANCH    COPTION,OPTWO800,OPTWO800
          BEEP
          GOTO      OPTWO100
.
OPTWO800  MOVE      ZERO,EXIT
          GOTO      OPTWO999
.
OPTWO900  MOVE      ONE,EXIT
.
OPTWO999  RETURN
+
.*****************************************************************************
.*                              SETDT000
.*                     Set report Dates and headings
.*****************************************************************************
SETDT000  CALL      IBACLOCK
.
          UNPACK    CTIMEIS,DIM2,DIM1,DIM3
          PACK      WORKTIME,DIM2,DIM3
          REP       " 0",WORKTIME
          PACK      ACTDATE,CCC,CYY,CMM,CDD      * Current date
          REP       " 0",ACTDATE
          MOVE      ACTDATE,WORKDATE
.
          COMPARE   TWO,COPTION
          GOTO      SETDT200 IF EQUAL            * "Current Date" selected
.
SETDT100  MATCH     "2345",WORKTIME              * after 23:45 = current day
          IF        @LESS
            SUB       ONE,WORKDATE               * back-up the date range
          ENDIF
.
SETDT200  MOVE      WORKDATE,REPDATEC            * Report date
          UNPACK    REPDATEC,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                      * Format date
          PACK      DATEFOR2,BRL,CPCDATE,BRR
          MOVE      CPCDATE,DATEFOR3
          SUB       ONE,WORKDATE
          MOVE      WORKDATE,YSTRDAY             * Previous date
          UNPACK    YSTRDAY,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                      * Format date
          PACK      DATEFOR1,BRL,CPCDATE,BRR
.
SETDT500  BRANCH    COPTION,SETDT600:       * set up lines for Midnight Census
                            SETDT700        * set up lines for Current Census
.
SETDT600  MOVE      "as at Midnight ",DETLIN1
          GOTO      SETDT9999
.
SETDT700  MOVE      "as at ",DIM6
          UNPACK    CTIMEIS,DIM5,DIM3
          PACK      DETLIN1,DIM6,DIM5,SP4
.
SETDT999  RETURN
+
.*******************************************************************************
.*                                PROC0000                                     *
.*                            Process Record                                   *
.*                                                                             *
.* Parameters : Admission Record                                               *
.*******************************************************************************
PROC0000  PACK      KEY8,AURNO               * Read Master Record.
          CALL      RDMAST1
          COMPARE   ZERO,OVRCD               * Master Record exist ?
          GOTO      PROC1000 IF EQUAL        * Yes.
.
          DISPLAY   *P1:24,*EL,*P20:24,*EL,*V2LON:
                    "** Processing Patients          **"
.
          GOTO      PROC9999
.
PROC1000  CALL      TYPE0000                 * Write XXSTYPE = 0

          PACK      KEY30,AADMNO,REPDATEC,SP30 * Read PATTRAN file
          CALL      RDSTRAN2                 * Positional Read
PROC2000  CALL      RDKTRAN2                 * Read next record
          BRANCH    OVRCD,PROC9999           * EOF, exit
.
          MATCH     TADMN,AADMNO             * Same admission Number ?
          GOTO      PROC9999 IF NOT EQUAL    * No.
.
          MATCH     TDATE,REPDATEC           * Same date ?
          GOTO      PROC9999 IF NOT EQUAL    * No.
.
          MATCH     TMOVE,ANSA               * Transfer Type = A (Admission)
          CALL      TYPE1000 IF EQUAL        * Yes
.
          MATCH     TMOVE,ANSD               * Transfer Type = D (Discharge)
          CALL      TYPE2000 IF EQUAL        * Yes
.
          MATCH     TMOVE,ANSC               * Transfer Type = C (Change)
          CALL      TYPE3000 IF EQUAL        * Yes
.
          MATCH     TMOVE,ANSL               * Transfer Type = L (On leave)
          CALL      TYPE5000 IF EQUAL        * Yes
.
          MATCH     TMOVE,ANSR               * Transfer Type = R (Return leave)
          CALL      TYPE6000 IF EQUAL        * Yes
.
          MOVE      TWARD,SAWARD             * Save ward
          GOTO      PROC2000                 * Read next PATTRAN record
.
PROC9999  RETURN
+
.*******************************************************************************
.*                                TYPE0000                                     *
.*    Ensure the patient was admitted the previous night of the report date    *
.*    if so write a record to the temp file with XXSTYPE = 0                   *
.*******************************************************************************
TYPE0000  PACK      KEY30,AADMNO,REPDATEC,SP30
          CALL      RDSTRAN2
          CALL      RDPTRAN2                 * Read previous
          BRANCH    OVRCD,TYPE0999           * EOF, exit
.
          MATCH     TADMN,AADMNO             * Same admission number ?
          GOTO      TYPE0999 IF NOT EQUAL    * No, exit
.
. Beyond this point, the patient WAS in the hospital last midnight, although
. now necessarily in the current ward
.
TYPE0500  MATCH     ANSL,TMOVE               * Patient on leave ?
          GOTO      TYPE0999 IF EQUAL        * Yes, exit
.
.         Write Record to temp file
.
          MOVE      TWARD,XXWARD             * Ward
          MOVE      ZERO,XXSTYPE             * In Ward at Previous Midnight
          MOVE      "00:00",XXTIME           * Time of event
          MOVE      AADMNO,XXADMN            * Admission Number
          MOVE      AURNO,XXURNO             * U/R Number
          MOVE      PSNAME,XXSURN            * Surname
          PACK      KEY17,XXWARD,XXSTYPE,XXTIME,XXADMN,SP70
          CALL      RAXXFIL1
          IF        OVRCD=1
            CALL      WRXXFIL1                 * Write to file
          ENDIF
.
          MOVE      TWARD,SAWARD             * Save the value of the ward
.
TYPE0999  RETURN
+
.*******************************************************************************
.*                                TYPE1000                                     *
.*                   Write a record to the Temp file with XXSTYPE = 1          *
.* Parameters : Transfer Record, TMOVE = A                                     *
.*              AADMNO, AURNO, PSNAME                                          *
.*******************************************************************************
TYPE1000  MOVE      TWARD,XXWARD             * Ward
          MOVE      ONE,XXSTYPE              * Admission
          MOVE      TTIME,XXTIME             * Time of event
          MOVE      AADMNO,XXADMN            * Admission Number
          MOVE      AURNO,XXURNO             * U/R Number
          MOVE      PSNAME,XXSURN            * Surname
          PACK      KEY17,XXWARD,XXSTYPE,XXTIME,XXADMN,SP70
          CALL      RAXXFIL1
          IF        OVRCD=1
            CALL      WRXXFIL1                 * Write to file
          ENDIF
.
TYPE1999  RETURN
+
.*******************************************************************************
.*                                TYPE2000                                     *
.*                   Write a record to the Temp file with XXSTYPE = 2          *
.* Parameters : Transfer Record, TMOVE = D                                     *
.*              AADMNO, AURNO, PSNAME                                          *
.*******************************************************************************
TYPE2000  MOVE      TWARD,XXWARD             * Ward
          MOVE      TWO,XXSTYPE              * Discharge
          MOVE      TTIME,XXTIME             * Time of event
          MOVE      AADMNO,XXADMN            * Admission Number
          MOVE      AURNO,XXURNO             * U/R Number
          MOVE      PSNAME,XXSURN            * Surname
          PACK      KEY17,XXWARD,XXSTYPE,XXTIME,XXADMN,SP70
          CALL      RAXXFIL1
          IF        OVRCD=1
            CALL      WRXXFIL1                 * Write to file
          ENDIF
.
TYPE2999  RETURN
+
.*******************************************************************************
.*                                TYPE3000                                     *
.*                   Write a record to the Temp file with XXSTYPE = 3          *
.*                     "               "             "    XXSTYPE = 4          *
.* Parameters : Transfer Record, TMOVE = C                                     *
.*              AADMNO, AURNO, PSNAME                                          *
.*******************************************************************************
TYPE3000  MATCH     SAWARD,TWARD             * Same ward ?
          GOTO      TYPE3999 IF EQUAL        * Yes.
.
          MOVE      TWARD,XXWARD             * Ward
          MOVE      THREE,XXSTYPE            * Transfer in
          MOVE      TTIME,XXTIME             * Time of event
          MOVE      AADMNO,XXADMN            * Admission Number
          MOVE      AURNO,XXURNO             * U/R Number
          MOVE      PSNAME,XXSURN            * Surname
          PACK      KEY17,XXWARD,XXSTYPE,XXTIME,XXADMN,SP70
          CALL      RAXXFIL1
          IF        OVRCD=1 
            CALL      WRXXFIL1                 * Write to file
          ENDIF
.
          MOVE      SAWARD,XXWARD            * Ward
          MOVE      FOUR,XXSTYPE             * Transfer out
          MOVE      TTIME,XXTIME             * Time of event
          MOVE      AADMNO,XXADMN            * Admission Number
          MOVE      AURNO,XXURNO             * U/R Number
          MOVE      PSNAME,XXSURN            * Surname
          PACK      KEY17,XXWARD,XXSTYPE,XXTIME,XXADMN,SP70
          CALL      RAXXFIL1
          IF        OVRCD=1
            CALL      WRXXFIL1                 * Write to file
          ENDIF

TYPE3999  RETURN
+
.*******************************************************************************
.*                                TYPE5000                                     *
.*                   Write a record to the Temp file with XXSTYPE = 5          *
.* Parameters : Transfer Record, TMOVE = L                                     *
.*              AADMNO, AURNO, PSNAME                                          *
.*******************************************************************************
TYPE5000  MOVE      TWARD,XXWARD             * Ward
          MOVE      FIVE,XXSTYPE             * On Leave
          MOVE      TTIME,XXTIME             * Time of event
          MOVE      AADMNO,XXADMN            * Admission Number
          MOVE      AURNO,XXURNO             * U/R Number
          MOVE      PSNAME,XXSURN            * Surname
          PACK      KEY17,XXWARD,XXSTYPE,XXTIME,XXADMN,SP70
          CALL      RAXXFIL1
          IF        OVRCD=1
            CALL      WRXXFIL1                 * Write to file
          ENDIF
.
TYPE5999  RETURN
+
.*******************************************************************************
.*                                TYPE5000                                     *
.*                   Write a record to the Temp file with XXSTYPE = 6          *
.* Parameters : Transfer Record, TMOVE = R                                     *
.*              AADMNO, AURNO, PSNAME                                          *
.*******************************************************************************
TYPE6000  MOVE      TWARD,XXWARD             * Ward
          MOVE      SIX,XXSTYPE              * Return from Leave
          MOVE      TTIME,XXTIME             * Time of event
          MOVE      AADMNO,XXADMN            * Admission Number
          MOVE      AURNO,XXURNO             * U/R Number
          MOVE      PSNAME,XXSURN            * Surname
          PACK      KEY17,XXWARD,XXSTYPE,XXTIME,XXADMN,SP70
          CALL      RAXXFIL1
          IF        OVRCD=1
            CALL      WRXXFIL1                 * Write to file
          ENDIF
.
TYPE6999  RETURN
+
.*******************************************************************************
.*                             CTMP0000                                        *
.*                        Create TeMP file                                     *
.*******************************************************************************
CTMP0000  CALL      DTMP0000                 * Ensure Old Temp. file is removed
.
          CLEAR     CMDLINE                  * Build Command Line
          APPEND    "isbuild ",CMDLINE
          APPEND    XXNAME1,CMDLINE
          APPEND    XXKEY,CMDLINE
          RESET     CMDLINE
          STRIP     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      XXFILE1,XXNAME1          * Open Temporary file
.
CTMP9999  RETURN
+
.
.*******************************************************************************
.*                             DTMP0000                                        *
.*                        Delete TeMP file                                     *
.*******************************************************************************
DTMP0000  CLOSE     XXFILE1
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    XXNAME1,CMDLINE
          RESET     CMDLINE
          STRIP     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
DTMP9999  RETURN
.*******************************************************************************
.*                        I/O Routines for Temp file                           *
.*******************************************************************************
RAXXFIL1  MOVE      ZERO,OVRCD
          RESET     KEY17
          READ      XXFILE1,KEY17;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSXXFIL1  RESET     KEY17
          READ      XXFILE1,KEY17;;
          RETURN
.
RDXXFIL1  MOVE      ZERO,OVRCD
          RESET     KEY17
          READ      XXFILE1,KEY17;XXWARD,DXXSTYPE,XXTIME,DXXADMN,XXURNO,XXSURN
          GOTO      OVERCOND IF OVER
          MOVE      DXXSTYPE,XXSTYPE
          MOVE      DXXADMN,XXADMN
          RETURN
.
RKXXFIL1  MOVE      ZERO,OVRCD
          READKS    XXFILE1;XXWARD,DXXSTYPE,XXTIME,DXXADMN,XXURNO,XXSURN
          GOTO      OVERCOND IF OVER
          MOVE      DXXSTYPE,XXSTYPE
          MOVE      DXXADMN,XXADMN
          RETURN
.
RPXXFIL1  MOVE      ZERO,OVRCD
          READKP    XXFILE1;XXWARD,DXXSTYPE,XXTIME,DXXADMN,XXURNO,XXSURN
          GOTO      OVERCOND IF OVER
          MOVE      DXXSTYPE,XXSTYPE
          MOVE      DXXADMN,XXADMN
          RETURN
.
UPXXFIL1  MOVE      XXSTYPE,DXXSTYPE
          MOVE      XXADMN,DXXADMN
          UPDATE    XXFILE1;XXWARD,DXXSTYPE,XXTIME,DXXADMN,XXURNO,XXSURN
          RETURN
.
WRXXFIL1  RESET     KEY17
          MOVE      XXSTYPE,DXXSTYPE
          MOVE      XXADMN,DXXADMN
          WRITE     XXFILE1,KEY17;XXWARD,DXXSTYPE,XXTIME,DXXADMN,XXURNO,XXSURN
          RETURN
.
DEXXFIL1  RESET     KEY17
          DELETE    XXFILE1,KEY17
          RETURN
********************************************************************************
+
.******************************************************************************
.*                                  KHOS0000              Called by: ML0000   *
.*                            Enter The hospital                              *
.******************************************************************************
KHOS0000  COMPARE   ONE,IBCNMHOS
          GOTO      KHOS9999 IF NOT EQUAL
.
          DISPLAY   *P1:13,*EL,"Hospital Id : ";
          MOVE      TEN5,ECOL
          MOVE      TEN3,EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS2000,KHOS9500
          GOTO      KHOS3000
.
KHOS2000  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          MOVE      ZERO,EXIT
.
KHOS3000  CLOSE     PATHSPA1
.
          DISPLAY   *P20:13,*V2LON,PTHSNAME;
          GOTO      KHOS9999
.
KHOS9500  MOVE      ONE,EXIT
KHOS9999  RETURN
+
.**********************************************************************
.*                            SEP0000                                 *
.*                        Separate page ?                             *
.**********************************************************************
SEP0000   KEYIN     *P1:16,*EL,"Would you like each Ward on a seperate PAGE (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
          RESET     ANS
          GOTO      SEP1000 IF EOS
          REP       UPPLOW,ANS
.
          MATCH     "Y",ANS
          GOTO      SEP2000 IF EQUAL
          MATCH     "N",ANS
          GOTO      SEP1000 IF EQUAL
          BEEP
          GOTO      SEP0000
.
SEP1000   MOVE      ONE,PSKIP               * No page separation
          DISPLAY   *P53:16,*EL,*V2LON,"No";
          GOTO      SEP9999
.
SEP2000   MOVE      ZERO,PSKIP              * Page separation
          DISPLAY   *P53:16,*EL,*V2LON,"Yes";
.
SEP9999   RETURN
+
.**********************************************************************
.*                         CREA0000                                   *
.*                  Create Temporary Files                            *
.**********************************************************************
.
CREA0000  CALL      KILL0000
.
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    TEMPFILE,CMDLINE
          APPEND    " 22 UC1-8",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      SECTION1,TEMPFILE
.
          RETURN
.
.**********************************************************************
.*                         KILL0000                                   *
.*                  Remove Temporary Files                            *
.**********************************************************************
.
KILL0000  CLOSE     SECTION1
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    TEMPFILE,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          RETURN
+
.**********************************************************************
.*                       HEADB000                                     *
.*                    Print Boarders Heading                          *
.**********************************************************************
HEADB000  PRINT     *1,"|",*40,"HOSPITAL BOARDERS",*132,"|"
          ADD       ONE,CLNO
          CALL      PGE0000
.
          PRINT        *1,"| Ward",*9,"| U/R No",*19,"|Patient's Name":
                      *60,"|Sex|Age| Mobile Number":
                     *85,"| Phone Number",*104,"| Email",*132,"|"
          CALL      PGE0000
.
HEADB900  ADD       ONE,CLNO
          RETURN
+
.**********************************************************************
.*                         PBRD0000                                   *
.**********************************************************************
.
PBRD0000  CALL      HEADB000
          MOVE      ZERO,TOTBRD
.
          PACK      KEY8,SP70
          CALL      RDSSECT1
PBRD1000  CALL      RDKSECT1
          BRANCH    OVRCD,PBRD9000
.
          PACK      KEY26,XADMN,SP70
          CALL      RSPTBRD3
PBRD2000  CALL      RKPTBRD3
          BRANCH    OVRCD,PBRD1000
.
          MATCH     XADMN,PTBRVISN
          GOTO      PBRD1000 IF NOT EQUAL
.
          CALL      IBACLOCK
          PACK      DIM8,CCC,CYY,CMM,CDD
          REP       " 0",DIM8
.
          MATCH     SP70,PTBRSDAT
          IF        !@EQUAL & !@EOS
            MATCH     PTBRSDAT,DIM8
            GOTO      PBRD2000 IF LESS
          ENDIF
.
          MATCH     SP70,PTBREDAT
          IF        !@EQUAL & !@EOS
            MATCH     DIM8,PTBREDAT
            GOTO      PBRD2000 IF LESS
          ENDIF
.
          PACK      KEY8,XADMN,SP70
          CALL      RDMISS1
          BRANCH    OVRCD,PBRD2000
.
          PACK      KEY8,AURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,PBRD2000
.
          MOVE      PTBRSNAM,PACSNAME            * set up patients name
          MOVE      PTBRGNAM,PACGNAME
          MOVE      SP70,PACTITLE
          MOVE      TWO, PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,XNAME
.
          MOVE      SP70,PSAGEYRS
          MATCH     SP70,PTBRBDAT
          IF        !@EQUAL & !@EOS
            CALL      IBACLOCK
            PACK      AGEDATE,CCC,CYY,CMM,CDD
            MOVE      PTBRBDAT,PBDATE
            CALL      CALCAGE
          ENDIF
.
          MOVE      PTBRMPHN,DIM12A
          MOVE      PTBRHPHN,DIM12B
          MOVE      PTBREMAL,DIM18
          MOVE      XWRDESC,DIM3
.
          PRINT     *1,"|",DIM3,SP1,XBED,*9,"| ",XADMN,*19,"|",XNAME:
                    *60,"|",*62,PTBRGEND,*64,"|",PSAGEYRS,*68,"|",DIM12A:
                    *85,"|",DIM12B:
                    *104,"|",DIM18,*132,"|"
.
          ADD       ONE,TOTBRD
.
          CALL      PGE0000
          ADD       TWO,CLNO
.
          COMPARE   "58",CLNO
          CALL      HEADA000 IF NOT LESS
.
          GOTO      PBRD2000
.
PBRD9000  CALL      CREA0000
PBRD9999  RETURN
+
.**********************************************************************
. TEMP FILE I/O
.
RDSSECT1  RESET     KEY8
          READ      SECTION1,KEY8;;
          RETURN
.
RDASECT1 RESET      KEY8
         MOVE       ZERO,OVRCD
         READ       SECTION1,KEY8;ANS
         GOTO       OVERCOND IF OVER
         RETURN
.
RDKSECT1  MOVE      ZERO,OVRCD
          READKS    SECTION1;XADMN,XWRDESC,XBED
          GOTO      OVERCOND IF OVER
          RETURN
.
.
WRSECT1   WRITE     SECTION1,KEY8;XADMN,XWRDESC,XBED
          RETURN
.==============================================================================
.
          INC       STD001IO
.
          INC       PATNURDS
          INC       PATWRDDS
          INC       PATHSPKY
          INC       TFILENAM                     * Generate Temp File Name
          INC       CALCAGE
.
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       PATBRDIO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATDSCIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATNOBIO/INC
          INC       PATNURIO/INC
          INC       PATONLIO/INC
          INC       PATTRNIO/INC
          INC       PATWR1IO/INC
          INC       PATHSPIO/INC
          INC       WEBERRIO/INC                 * Web Server Error Logging
+

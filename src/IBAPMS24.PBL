. *****************************************************************************
. *              P R O G R A M  S U M M A R Y                                 *
. *              -------------  -------------                                 *
. *                                                                           *
. *     PROGRAM NAME:     IBAPMS24                                            *
. *                                                                           *
. *     FUNCTION:         AVERAGE IN-PATIENT DISTRIBUTION                     *
. *                                                                           *
. *     DATE WRITTEN:     04/04/90                                            *
. *                                                                           *
. *     AUTHOR:           DIG (Adapted from IBAHOS55 .....                    *
. *                            which also didn't work)                        *
. *                                                                           *
. *     MODIFICATIONS:                                                        *
. *                                                                           *
.******************************************************************************
.*        V12.00.01 22/05/2025  Alina Bhari   TSK 0955096                     *
.*                  Added alpanumeric visit number generation                 *
.*        V10.04.01 15/04/2014  J.Tan          CAR 261630                     *
.*                  Removed port number from temp file name                   *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V5.08.02  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*         V5.08.01 09/08/2000  Caleb Sharman  SRF 647126                     *
.*                  Mods to DAYA0000 so ENDDTE contains 8 charachters not 6   *
.*         V5.07.01 09/11/1998  Glenn Berry      5.07 changes                 *
.******************************************************************************
.
          INC       STD001FD
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
          INC       PATCOMM/INC
.
          INC       PATDAYFD/INC
          INC       PATDO1FD/INC
          INC       PATDRGFD/INC
          INC       PATTRNFD/INC     * FOR ONLEAVE DATE
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATDSCFD/INC
          INC       PATCODFD/INC
.
PATFNAME  DIM       8
.
PATMP1XX  IFILE     SQL, FIXED=9, KEY="u1-8"
.
PSTSRT    IFILE     SQL, FIXED=157, KEY="u1-9"
.
. ----- Tempfile consists of following vars -----
.
. Name          Type      Length     Physical     Start   Description
. ----          ----      ------     --------     -----   -----------
.DRTYPE         DIM        3          3             1
.STADOCT        DIM        6          6             4
.DTITL          DIM        9          9            10
.DSNAME         DIM       20         20            19
.DGNAME         DIM       25         25            39
.DAY1           FORM       3          3            64
.DAY2           FORM       3          3            67
.DAY3           FORM       3          3            70
.DAY4           FORM       3          3            73
.DAY5           FORM       3          3            76
.DAY6           FORM       3          3            79
.DAY7           FORM       3          3            82
.DAY8           FORM       3          3            85
.DAY9           FORM       3          3            88
.DAY10          FORM       3          3            91
.DAY11          FORM       3          3            94
.DAY12          FORM       3          3            97
.DAY13          FORM       3          3           100
.DAY14          FORM       3          3           103
.DAY15          FORM       3          3           106
.DAY16          FORM       3          3           109
.DAY17          FORM       3          3           112
.DAY18          FORM       3          3           115
.DAY19          FORM       3          3           118
.DAY20          FORM       3          3           121
.DAY21          FORM       3          3           124
.DAY22          FORM       3          3           127
.DAY23          FORM       3          3           130
.DAY24          FORM       3          3           133
.DAY25          FORM       3          3           136
.DAY26          FORM       3          3           139
.DAY27          FORM       3          3           142
.DAY28          FORM       3          3           145
.DAY29          FORM       3          3           148
.DAY30          FORM       3          3           151
.DAY31          FORM       3          3           154
. ----- EOR 157 -----
.
.               WORK AREA
.
CODEDT    INIT      "DT"
CPERMTH   DIM       6
CURMTH    DIM       6
CENT      DIM       2
.
.
FNAMEI    DIM       8
.
DAY1      FORM      3
DAY2      FORM      3
DAY3      FORM      3
DAY4      FORM      3
DAY5      FORM      3
DAY6      FORM      3
DAY7      FORM      3
DAY8      FORM      3
DAY9      FORM      3
DAY10     FORM      3
DAY11     FORM      3
DAY12     FORM      3
DAY13     FORM      3
DAY14     FORM      3
DAY15     FORM      3
DAY16     FORM      3
DAY17     FORM      3
DAY18     FORM      3
DAY19     FORM      3
DAY20     FORM      3
DAY21     FORM      3
DAY22     FORM      3
DAY23     FORM      3
DAY24     FORM      3
DAY25     FORM      3
DAY26     FORM      3
DAY27     FORM      3
DAY28     FORM      3
DAY29     FORM      3
DAY30     FORM      3
DAY31     FORM      3
.
TDAY1     FORM      3
TDAY2     FORM      3
TDAY3     FORM      3
TDAY4     FORM      3
TDAY5     FORM      3
TDAY6     FORM      3
TDAY7     FORM      3
TDAY8     FORM      3
TDAY9     FORM      3
TDAY10    FORM      3
TDAY11    FORM      3
TDAY12    FORM      3
TDAY13    FORM      3
TDAY14    FORM      3
TDAY15    FORM      3
TDAY16    FORM      3
TDAY17    FORM      3
TDAY18    FORM      3
TDAY19    FORM      3
TDAY20    FORM      3
TDAY21    FORM      3
TDAY22    FORM      3
TDAY23    FORM      3
TDAY24    FORM      3
TDAY25    FORM      3
TDAY26    FORM      3
TDAY27    FORM      3
TDAY28    FORM      3
TDAY29    FORM      3
TDAY30    FORM      3
TDAY31    FORM      3
.
GDAY1     FORM      3
GDAY2     FORM      3
GDAY3     FORM      3
GDAY4     FORM      3
GDAY5     FORM      3
GDAY6     FORM      3
GDAY7     FORM      3
GDAY8     FORM      3
GDAY9     FORM      3
GDAY10    FORM      3
GDAY11    FORM      3
GDAY12    FORM      3
GDAY13    FORM      3
GDAY14    FORM      3
GDAY15    FORM      3
GDAY16    FORM      3
GDAY17    FORM      3
GDAY18    FORM      3
GDAY19    FORM      3
GDAY20    FORM      3
GDAY21    FORM      3
GDAY22    FORM      3
GDAY23    FORM      3
GDAY24    FORM      3
GDAY25    FORM      3
GDAY26    FORM      3
GDAY27    FORM      3
GDAY28    FORM      3
GDAY29    FORM      3
GDAY30    FORM      3
GDAY31    FORM      3
.
DAYFLG    FORM      1
DIM8      DIM       8
DMONTH    DIM       10
DRANGE    DIM       26
DYEAR     DIM       4
FORM3     FORM      3
.
CMDLINE   DIM       30
COUNT     FORM      2
FROMDAY   FORM      2
ENDDAY    FORM      2
.
LASTDAY   DIM       8
LASTFLG   FORM      "0"
LBRAC     INIT      "("
RBRAC     INIT      ")"
LNO       FORM      2
DDESC     DIM       12
.
PREVFLG   FORM      1
WSTAT     FORM      1
.
RECFLG    FORM      1
SDAYMON   DIM       4
SDRTYPE   DIM       3
SELMTH    DIM       6
SYEAR     DIM       4
.
DTTL      DIM       4
ENDDTE    DIM       8
GNAME     DIM       15
SNAME     DIM       15
STRTDTE   DIM       8
TDAYMON   DIM       4
TO        INIT      " To "
TODAY     DIM       8
TOTDNAM   DIM       35
TYEAR     DIM       4
.
FRFORM    FORM      8
FROMDATE  DIM       8
TODATE    DIM       8
TOFORM    FORM      8
TMPADMN   DIM       8
VERT      FORM      2
.
TEN59     FORM      "59"
NDAY      FORM      3
TNDAY     FORM      3
GNDAY     FORM      3
NBDY      FORM      4
TBDY      FORM      4
GBDY      FORM      5
.
PRGID     INIT      "IBAPMS24"
PRGNAM    INIT      "Average In-patient Distribution Report"
VERSION   INIT      "V12.01.00"
.
.         START OF PROGRAM
.
          CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A2,"patmi1af"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"patdrgaf";
          OPEN      PATDRGA1,"patdrgaf"
          OPEN      PATDRGA3,"patdrgaf"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TEN59;*27,CPERMTH
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,PATFNAME
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,FNAMEI
.
          PACK      CPTDATE,CCC,CYY,CMM,CDD
          REP       " 0",CPTDATE
.
          CALL      GPERD
          BRANCH    EXIT,NOCPER
.
          PACK      CURMTH,DRGYR,DRGNUM
          GOTO      START
.
NOCPER    KEYIN     *P1:24,*V2LON,*B:
                    "** Error: Current Date not in Period File. ":
                    "Hit <ENTER> to exit ",*EOFF,ANS;
          GOTO      ENDIT
.
.        START OF PROGRAM
.
.             KEYIN TO DATE
.
START     MOVE      ZERO,NBDY
          MOVE      ZERO,TBDY
          MOVE      ZERO,GBDY
          DISPLAY   *P1:4,*EF,"Keyin ",CPERMTH,"/Year :";
.
KADAT     DISPLAY   *P33:4,*EL
          CALL      KEYMN
          BRANCH    OVRCD OF ENDIT
.
          PACK      SELMTH WITH DRGYR,DRGNUM
          REP       " 0",SELMTH
.
          MATCH     CURMTH,SELMTH
          GOTO      INVDTE0 IF NOT LESS
          GOTO      REKEY
.
INVDTE0   DISPLAY   *P1:24,*B,*EL,*V2LON,"** Date must be <":
                    " Current Date **",*W2,*P1:24,*EL;
          GOTO      KADAT
.
.        Check if dates are OK or not
.
REKEY     DISPLAY   *P1:24,*EF,CPERMTH;
          KEYIN     " ok (",*V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
          MATCH     "N" TO ANS
          GOTO      START IF EQUAL
.
          MATCH     "Y" TO ANS
          GOTO      CALDAY IF EQUAL
.
          BEEP
          GOTO      REKEY
.
.       If it is equal to the current period then the max
.       no number of days for the period would be todays date
.
CALDAY    UNPACK    DRGTDTE,CCENT,CYEAR,CMON,CDAY
          PACK      TODAY,CCC,CYY,CMM,CDD
          REP       " 0",TODAY
          PACK      LASTDAY,CCENT,CYEAR,CMON,CDAY
.
          MATCH     DRGTDTE,TODAY
          GOTO      PROCC IF NOT LESS
.
          PACK      LASTDAY,CCC,CYY,CMM,CDD
          REP       " 0",LASTDAY
.
.            PROCESS START
.
PROCC     MOVE      DRGYR,DYEAR
          UNPACK    DRGFDTE,CCENT,CYEAR,CMON,CDAY
          UNPACK    DRGTDTE,XCENT,XYEAR,XMON,XDAY
          PACK      DRANGE,LBRAC,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR,TO,XDAY:
                           SLASH,XMON,SLASH,XCENT,XYEAR,RBRAC
          RESET     DRANGE
          APPEND    SP20,SP20,DTTL
.
          DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** Initialising Ward Summary File **",*W2,*P1:10;
          CALL      CREAIDX
          CLOCK     TIME,CTIMEIS
.
.------ Create the Indexed Temp File ------
.
          DISPLAY   *P1:24,*EL,*V2LON,*BLINKON,"Creating the Indexed Temp":
                    " File",*HOFF," - Please Wait";
.
.------ Kill the Tempfile if it Already Exists ------
.
          CALL      KILLFNM
.
.------ Build new indexed temp file ------
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    PATFNAME,CMDLINE
          APPEND    " 9 u1-8",CMDLINE
          APPEND    SP30,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
          OPEN      PATMP1XX,PATFNAME
.
          CALL      ZEROTDAY
          CALL      ZEROGDAY
.
          CALL      DAYA0000       * write the patdayaf data to the temp file
.
          DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** Processing Admission Numbers **",*W;
          DISPLAY   *P1:24,*EL,*P20:24,"Admission No :":
                    *P56:24,"Scanning :";
.
.------ position on the temp file ------
.
          MOVE      SP8,KEY8
          CALL      RSTMPR1
.
.------ read through the indexed temp file and process each admission ------
.
NEXTADM   CALL      RKTMPR1
          BRANCH    OVRCD,REPORT
.
          MOVE      TMPADMN,KEY8
          CALL      RDMISS1              * read the admissions file
          BRANCH    OVRCD,NEXTADM
.
          MATCH     ADATE,ENDDTE         * make sure admission is in date range
          GOTO      NEXTADM IF LESS
.
          DISPLAY   *P68:24,AADMNO;
          MOVE      SP6,DDATE
.
          BRANCH    ASTAT,NEXTADM,CONTADM,CONTDIS,CONTADM,NEXTADM
.
CONTDIS   MOVE      AADMNO,KEY8
          CALL      RDDSCH1              * read discharge file
          BRANCH    OVRCD,NEXTADM
.
          MATCH     STRTDTE,DDATE        * make sure admission is in date range
          GOTO      NEXTADM IF LESS        
.
.------ we have a valid admission so extract details ------
.------ and write to summary file ------
.
CONTADM   DISPLAY   *P35:24,*V2LON,AADMNO;
          CALL      TRANS
          GOTO      NEXTADM
.
.**********************************************************************
.*                            DAYA0000                                *
.*     Process patdayaf Data writing to temp indexed file patmp1xx    *
.**********************************************************************
DAYA0000  UNPACK    DRGTDTE,TYEAR,TDAYMON
          UNPACK    TYEAR,CCENT,CYEAR
          PACK      ENDDTE,CCENT,CYEAR,TDAYMON
.
          UNPACK    DRGFDTE,SYEAR,SDAYMON
          UNPACK    SYEAR,CCENT,CYEAR
          PACK      STRTDTE,CCENT,CYEAR,SDAYMON
.
          DISPLAY   *P1:24,*EL,*V2LON,*BLINKON,"Initialising the Indexed Temp":
                    " File",*HOFF," - Please Wait";
.
.------ close previous day file and set up the name of the next one ------
.
DAYA1000  CLOSE     PATDAYA1
          PACK      CFNAME,FILDAYA1,SYEAR
.
          TRAP      DAYA4900 IF IO        * trap if file does not exist
          OPEN      PATDAYA1,CFNAME
          TRAPCLR   IO
.
          PACK      KEY12,SDAYMON,SP10    * position on daily patient list file
          CALL      RSPTDAY1
.
.------ read through the patient daily list file ------
.
DAYA2000  CALL      RKPTDAY1
          BRANCH    OVRCD,DAYA5000
.
          MATCH     TYEAR,SYEAR            * see if the current file is the one
          GOTO      DAYA3000 IF NOT EQUAL    for the last year in the date range
.
          MATCH     PTDYDATE,TDAYMON       * if so, make sure the month and day
          GOTO      DAYA9999 IF LESS         are still in range
.
DAYA3000
.------ Only need to write to the temporary file if the admission number -----
.------ is not already there ------
.
          MOVE      PTDYADMN,KEY8
          CALL      RDTMPR1                * read the temp file
          BRANCH    OVRCD,DAYA4000
          GOTO      DAYA2000
.
.------ write a new record to the temp file ------
.
DAYA4000  CALL      WRTMPR1
          GOTO      DAYA2000
.
.------ need to pop the return address off the stack if I/O error trapped -----
.
DAYA4900  NORETURN
.
.------ either file does not exist or we have reached the end of the ------
.------ current file ------
.
DAYA5000  MATCH     TYEAR,SYEAR             * test if we have reached the end 
          GOTO      DAYA9999 IF NOT LESS      year in the date range
.
          MOVE      SYEAR,FORM4             * if not, increment the start year
          ADD       ONE,FORM4                 to the next year and process
          MOVE      FORM4,SYEAR               the file for this year
          REP       " 0",SYEAR
          MOVE      "0101",SDAYMON
.
          GOTO      DAYA1000
.
DAYA9999  RETURN
+
.
.         Loop through the trans file
.
TRANS     MOVE      SP1,STMOVE
          MOVE      ADOCTA,STADOCT
          MOVE      ZERO,PREVFLG
          MOVE      ZERO,LASTFLG
          MOVE      ZERO,DAYFLG
.
.         Check for day case patient
.
          MATCH     ADATE,DDATE
          GOTO      NODCASE IF NOT EQUAL
.
          MOVE      ONE,DAYFLG              * Day case
.
NODCASE   UNPACK    DRGFDTE,CCENT,CYEAR,CMON,CDAY
          PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY
.
          PACK      KEY30,AADMNO,SP20,SP20
          CALL      RDSTRAN2
NXTTRN    CALL      RDKTRAN2
          BRANCH    OVRCD OF CHKLAST
.
          MATCH     TADMN,AADMNO
          GOTO      CHKLAST IF NOT EQUAL
.
          MATCH     FROMDATE,TDATE
          GOTO      KEEPREC IF LESS
.
          UNPACK    DRGTDTE,CCENT,CYEAR,CMON,CDAY
          PACK      TODATE,CCENT,CYEAR,CMON,CDAY
.
          MATCH     TDATE,TODATE
          GOTO      CHKLAST IF LESS
.
.         Check if there is any record less than the selected month
.
          COMPARE   ZERO,PREVFLG
          GOTO      NXT10 IF EQUAL
.
.         There is a record before the month, Check with the first move
.
          CMATCH    "R",TMOVE
          GOTO      NXT05 IF EQUAL
.
.
.         If TMOVE does not start with Admission or Return record,
.         than we need to update the bed days from the
.         start of month if necessary
.
          CMATCH    "C",TMOVE
          GOTO      NXT02 IF NOT EQUAL
.
          MOVE      TMOVE,STMOVE
.
          MATCH     TADOCT,STADOCT
          GOTO      NXTTRN IF EQUAL
.
NXT02     MOVE      TDATE,TODATE
          CALL      CHKREC
.
          CMATCH    "D",TMOVE
          RETURN    IF EQUAL
.
NXT05     MOVE      TADOCT,STADOCT
          MOVE      TDATE,FROMDATE
          MOVE      ZERO,PREVFLG
          MOVE      TMOVE,STMOVE
          GOTO      NXTTRN
.
.        Check the TMOVE
.
NXT10     MOVE      ZERO,PREVFLG
          CMATCH    "A",TMOVE
          GOTO      NXT20 IF NOT EQUAL
.
          MOVE      TDATE,FROMDATE
          MOVE      TADOCT,STADOCT
          MOVE      TMOVE,STMOVE
          GOTO      NXTTRN
.
NXT20     CMATCH    "R",TMOVE
          GOTO      NXT30 IF NOT EQUAL
.
          MOVE      TDATE,FROMDATE
          MOVE      TADOCT,STADOCT
          MOVE      TMOVE,STMOVE
          GOTO      NXTTRN
.
NXT30     CMATCH    "D",TMOVE
          GOTO      NXT40 IF EQUAL
.
          CMATCH    "C",TMOVE
          GOTO      NXT40 IF NOT EQUAL
.
.       Check if ADOCT has been changed
.
          MOVE      TMOVE,STMOVE
          MATCH     STADOCT,TADOCT
          GOTO      NXTTRN IF EQUAL
.
.       Update the bed days
.
NXT40     MOVE      TDATE,TODATE
          CALL      CHKREC
.
.       If it is Discharged record that means we finish
.
          CMATCH    "D",TMOVE
          RETURN    IF EQUAL
.
          MOVE      TDATE,FROMDATE
          MOVE      TADOCT,STADOCT
          MOVE      TMOVE,STMOVE
          GOTO      NXTTRN
.
.         Save the record which is less than the selected month
.
KEEPREC   MOVE      ONE,PREVFLG
          MOVE      TADOCT,STADOCT
          MOVE      TMOVE,STMOVE
          GOTO      NXTTRN
.
.         Check with the last record whether we need to update bed days
.
CHKLAST   CMATCH    "L",STMOVE
          RETURN    IF EQUAL
.
          CMATCH    "D",STMOVE
          RETURN    IF EQUAL
.
          CMATCH    " ",STMOVE
          RETURN    IF EQUAL
.
.         Update the beddays till the end of month
.
          MOVE      ONE,LASTFLG            * Flag to include the last record
          MOVE      LASTDAY,TODATE
          CALL      CHKREC
          RETURN
.
.         Update the number of bed days up to the todate
.
CHKREC    PACK      DSNAME WITH SP20,SP10
          PACK      DGNAME WITH SP20,SP10
          MOVE      ZERO,RECFLG
          MOVE      STADOCT,KEY6
          CALL      RDDOCT1
.
          MOVE      TODATE,TOFORM 
          MOVE      FROMDATE,FRFORM 
.
          UNPACK    FROMDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CDAY,FROMDAY
          MOVE      FROMDAY,COUNT
.
.         Check if the record already exist
.
          PACK      KEY9 WITH DRTYPE,STADOCT
          CALL      RDWORK
          BRANCH    OVRCD OF NEWREC
.
          BRANCH    DAYFLG OF CHKUP1     * Day case only has one bed day
.
.         Update the existing record
.
CHKUP     COMPARE   TOFORM,FRFORM
          GOTO      CHKUP2 IF NOT LESS
.
CHKUP1    LOAD      FORM3 USING COUNT OF DAY1,DAY2,DAY3,DAY4,DAY5:
                    DAY6,DAY7,DAY8,DAY9,DAY10,DAY11:
                    DAY12,DAY13,DAY14,DAY15,DAY16:
                    DAY17,DAY18,DAY19,DAY20,DAY21:
                    DAY22,DAY23,DAY24,DAY25,DAY26:
                    DAY27,DAY28,DAY29,DAY30,DAY31
.
.         Add one to the record
.
          ADD       ONE,FORM3
          STORE     FORM3 USING COUNT OF DAY1,DAY2,DAY3,DAY4,DAY5:
                    DAY6,DAY7,DAY8,DAY9,DAY10,DAY11:
                    DAY12,DAY13,DAY14,DAY15,DAY16:
                    DAY17,DAY18,DAY19,DAY20,DAY21:
                    DAY22,DAY23,DAY24,DAY25,DAY26:
                    DAY27,DAY28,DAY29,DAY30,DAY31
.
          MOVE      FRFORM,DIM8
          UNPACK    DIM8,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,CC
          MOVE      CYEAR,YY
          MOVE      CMON,MM
          MOVE      CDAY,DD
.
          CALL      DMYCON                 * convert to julian form
.
          ADD       ONE,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
.
          CALL      JULCON                 * convert back to CC,YY,MM,DD
.
          PACK      DIM8,CC,YY,MM,DD
          REP       " 0",DIM8
          MOVE      DIM8,FRFORM
.
          MOVE      DD,COUNT
          MOVE      ZERO,DAYFLG
          GOTO      CHKUP
.
.         Check if we need to update beddays for the last record
.
CHKUP2    COMPARE   ZERO,LASTFLG
          GOTO      CHKUP3 IF EQUAL
.
          MOVE      ZERO,LASTFLG
          GOTO      CHKUP1
.
CHKUP3    CALL      UPWORK
          RETURN
.
.         Write a new record
.
NEWREC    CALL      ZERODAY               *  Initialize the variables
          MOVE      FROMDAY,COUNT
.
          BRANCH    DAYFLG OF CHKWR1     * Day case only has one bed day
.
CHKWR     
          COMPARE   TOFORM,FRFORM
          GOTO      CHKWR2 IF NOT LESS
.
CHKWR1    STORE     ONE USING COUNT OF DAY1,DAY2,DAY3,DAY4,DAY5:
                    DAY6,DAY7,DAY8,DAY9,DAY10,DAY11:
                    DAY12,DAY13,DAY14,DAY15,DAY16:
                    DAY17,DAY18,DAY19,DAY20,DAY21:
                    DAY22,DAY23,DAY24,DAY25,DAY26:
                    DAY27,DAY28,DAY29,DAY30,DAY31
.
          MOVE      FRFORM,DIM8
          UNPACK    DIM8,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,CC
          MOVE      CYEAR,YY
          MOVE      CMON,MM
          MOVE      CDAY,DD
.
          CALL      DMYCON                 * convert to julian form
.
          ADD       ONE,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
.
          CALL      JULCON                 * convert back to CC,YY,MM,DD
.
          PACK      DIM8,CC,YY,MM,DD
          REP       " 0",DIM8
          MOVE      DIM8,FRFORM
          MOVE      DD,COUNT
.
          MOVE      ONE,RECFLG
          MOVE      ZERO,DAYFLG
          GOTO      CHKWR
.
CHKWR2    COMPARE   ZERO,LASTFLG
          GOTO      CHKWR3 IF EQUAL
.
.         We need to update beddays for the last record
.
          MOVE      ZERO,LASTFLG
          GOTO      CHKWR1
.
.         Make sure we are not creating a new blank record
.
CHKWR3    COMPARE   ONE,RECFLG
          CALL      WRWORK IF EQUAL
          RETURN
.
.
.     READ ACTUAL DATA
.
REPORT    MOVE      "60",LNO
          DISPLAY   *P1:10,*EF,*P20:24,*V2LON,"** Generating Report **",*W;
          MOVE      SP3,SDRTYPE
.
          DISPLAY   *P5:24,*EL,"Generating :"
.
          MOVE      SP9,KEY9
          CALL      RDSWORK
NEXTT     CALL      RDKWORK
          BRANCH    OVRCD OF ENDP
.
          DISPLAY   *P20:24,*V2LON,DRTYPE,*P28:24,STADOCT;
.
          MATCH     DRTYPE,SDRTYPE
          GOTO      SAMETYP IF EQUAL
.
.        Print the total of each doctor type
.
          MATCH     SP3,SDRTYPE
          CALL      PRTSUBT IF NOT EQUAL
.
          CALL      ZEROTDAY
          MOVE      DRTYPE,SDRTYPE
.
SAMETYP   COMPARE   "50",LNO
          CALL      HEAD IF NOT LESS
.
.
          ENDSET    DTITL
NEXT70    CMATCH    SP1,DTITL
          GOTO      NEXT71 IF NOT EQUAL
          BUMP      DTITL BY -1
          GOTO      NEXT71 IF EOS
          GOTO      NEXT70
NEXT71    LENSET    DTITL
          RESET     DTITL
          APPEND    DTITL,TOTDNAM
.
          ENDSET    DSNAME
NEXT72    CMATCH    SP1,DSNAME
          GOTO      NEXT73 IF NOT EQUAL
          BUMP      DSNAME BY -1
          GOTO      NEXT73 IF EOS
          GOTO      NEXT72
NEXT73    LENSET    DSNAME
          RESET     DSNAME
          APPEND    SP1,TOTDNAM
          APPEND    SP1,TOTDNAM
          APPEND    DSNAME,TOTDNAM
.
          ENDSET    DGNAME
NEXT74    CMATCH    SP1,DGNAME
          GOTO      NEXT75 IF NOT EQUAL
          BUMP      DGNAME BY -1
          GOTO      NEXT75 IF EOS
          GOTO      NEXT74
NEXT75    LENSET    DGNAME
          RESET     DGNAME
          APPEND    SP1,TOTDNAM
          APPEND    DGNAME,TOTDNAM
.
          CALL      DOCTOT
          CALL      PRTDETL
          CALL      ADDTOT
          GOTO      NEXTT
.
DOCTOT    MOVE      ONE,COUNT
DTOTAL    LOAD      NDAY FROM COUNT OF DAY1,DAY2,DAY3,DAY4,DAY5,DAY6,DAY7:
                    DAY8,DAY9,DAY10,DAY11,DAY12,DAY13:
                    DAY14,DAY15,DAY16,DAY17,DAY18,DAY19:
                    DAY20,DAY21,DAY22,DAY23,DAY24,DAY25:
                    DAY26,DAY27,DAY28,DAY29,DAY30,DAY31
          ADD       NDAY,NBDY
          ADD       NDAY,TBDY
          ADD       NDAY,GBDY
          ADD       ONE,COUNT
          COMPARE   "32",COUNT
          GOTO      DTOTAL IF LESS
          RETURN
.
.        Print the detail of each doctor
.
PRTDETL   PRINT     *1,"!",STADOCT,*9,TOTDNAM,*44,"!  ",NBDY:
                    *52,"! ",DAY1,SP2,DAY3,SP2,DAY5,SP2,DAY7,SP2,DAY9,SP2,DAY11:
                    SP2,DAY13,SP2,DAY15,SP2,DAY17,SP2,DAY19,SP2,DAY21,SP2:
                    DAY23,SP2,DAY25,SP2,DAY27,SP2,DAY29,SP2,DAY31,*132,"!":
                    *N,*1,"!",*44,"!",*52,"!",*56,DAY2,SP2,DAY4,SP2:
                    DAY6,SP2,DAY8,SP2,DAY10:
                    SP2,DAY12,SP2,DAY14,SP2,DAY16,SP2,DAY18,SP2,DAY20,SP2:
                    DAY22,SP2,DAY24,SP2,DAY26,SP2,DAY28,SP2,DAY30,*132,"!"
.
          MOVE      ZERO,NBDY
          PACK      TOTDNAM,SP20,SP20
          CALL      PAGELN
          ADD       THREE,LNO
          RETURN
.
.        Print the sub-totals of doctor type
.
PRTSUBT   COMPARE   "50",LNO
          CALL      HEAD IF NOT LESS
.
          MOVE      SP20,TDESC
          PACK      KEY5 WITH CODEDT,SDRTYPE
          CALL      RDCODE1
.
          MOVE      TDESC,DDESC
.
          PRINT     *1,"!",*8,"Total Doctor Type: ",SDRTYPE,SP2,DDESC,*44,"!  ":
                    TBDY,*52,"! ",TDAY1,SP2,TDAY3,SP2,TDAY5,SP2,TDAY7,SP2,TDAY9:
                    SP2,TDAY11,SP2,TDAY13,SP2,TDAY15,SP2,TDAY17,SP2,TDAY19:
                    SP2,TDAY21,SP2,TDAY23,SP2,TDAY25,SP2,TDAY27,SP2:
                    TDAY29,SP2,TDAY31,*132,"!":
                    *N,*1,"!",*44,"!",*52,"!",*56,TDAY2,SP2,TDAY4,SP2:
                    TDAY6,SP2,TDAY8,SP2,TDAY10,SP2,TDAY12,SP2:
                    TDAY14,SP2,TDAY16,SP2,TDAY18,SP2,TDAY20,SP2:
                    TDAY22,SP2,TDAY24,SP2,TDAY26,SP2,TDAY28,SP2:
                    TDAY30,*132,"!":
                    *N,*1,"*=============================":
                    "==============================":
                    "==============================":
                    "=========================================*"
.
          MOVE      ZERO,TBDY
          ADD       THREE,LNO
          CALL      ZEROTDAY
          MOVE      DRTYPE,SDRTYPE
          RETURN
.
.
.        Print the Grand total
.
PRTGNDT   COMPARE   "50",LNO
          CALL      HEAD IF NOT LESS
.
          PRINT     *1,"!",*8,"Grand  Total: ",*44,"! ",GBDY:
                    *52,"!",*54,GDAY1,SP2,GDAY3,SP2,GDAY5,SP2,GDAY7,SP2,GDAY9:
                    SP2,GDAY11,SP2,GDAY13,SP2,GDAY15,SP2,GDAY17,SP2,GDAY19:
                    SP2,GDAY21,SP2,GDAY23,SP2,GDAY25,SP2,GDAY27,SP2:
                    GDAY29,SP2,GDAY31,*132,"!":
                    *N,*1,"!",*44,"!",*52,"!",*56,GDAY2,SP2,GDAY4,SP2:
                    GDAY6,SP2,GDAY8,SP2,GDAY10,SP2,GDAY12,SP2:
                    GDAY14,SP2,GDAY16,SP2,GDAY18,SP2,GDAY20,SP2:
                    GDAY22,SP2,GDAY24,SP2,GDAY26,SP2,GDAY28,SP2:
                    GDAY30,*132,"!"
.
          CALL      PAGEND
          ADD       THREE,LNO
          RETURN
.
.                 HEADINGS FOR OUTPUT
.
HEAD      UNPACK    SELMTH,XCENT,XYEAR,XMON
.
          CALL      HEAD132
          PRINT     *N,*40,"For ",CPERMTH," : ",DRGNUM,"/",DRGYR:
                       *70,DRANGE,*N
.
          CALL      PAGEND
          PRINT     *1,"!Code",*8,"Doctors Name":
                    *44,"! Total":
                    *52,"!   1    3    5    7    9   11   13   15   17   19":
                    "   21   23   25   27   29   31",*132,"!":
                    *N,*1,"!",*44,"! B/Days",*52,"!":
                    *58,"2    4    6    8   10   12   14":
                    "   16   18   20   22   24   26   28   30",*132,"!"
.
          CALL      PAGEND
          MOVE      EIGHT,LNO
          RETURN
.
NODATA    DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "*** No average in-patient distribution ***",*W3;
.
          CLOSE     PSTSRT
          CALL      KILLIDX
          GOTO      START
.
.           OVER CONDITION FOUND..PRINT LAST LINE OF -- AND DISPLAY MESSAGE
.
ENDP      COMPARE   ZERO,CPAGENO
          GOTO      NODATA IF EQUAL
.
          DISPLAY   *P1:24,*EL,*V2LON,*P20:24:
                    "** Report Generation Completed **",*W2;
.
          CALL      PRTSUBT
          CALL      PRTGNDT
.
          CLOSE     PSTSRT
          CLOSE     PATMP1XX
.
          CALL      KILLFNM
          CALL      KILLIDX
          GOTO      START
.
PAGEND    PRINT     *1,"*-----------------------------":
                    "------------------------------":
                    "------------------------------":
                    "-----------------------------------------*"
          RETURN
.
PAGELN    PRINT     *1,"!-----------------------------":
                    "------------------------------":
                    "------------------------------":
                    "-----------------------------------------!"
          RETURN
.
.
.      CREATE AN INDEXED FILE 
.       FOR ACCUMULATING DOCTOR TYPE TOTALS
CREAIDX
.
          CALL      KILLIDX
.
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          APPEND    " 157 u1-9",CMDLINE
          APPEND    SP30,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      PSTSRT,FNAMEI
          RETURN
.
.       Deleting  AN INDEXED FILE TEMPFILE
.
KILLIDX   CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          RETURN
.
KILLFNM   CLEAR     CMDLINE
          CLOSE     PATMP1XX
          APPEND    "iserase ",CMDLINE
          APPEND    PATFNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          RETURN
.
.
.         Add to Sub-total bed days
.
ADDTOT    MOVE      ONE,COUNT
TOTLOP    LOAD      NDAY FROM COUNT OF DAY1,DAY2,DAY3,DAY4,DAY5,DAY6,DAY7:
                    DAY8,DAY9,DAY10,DAY11,DAY12,DAY13:
                    DAY14,DAY15,DAY16,DAY17,DAY18,DAY19:
                    DAY20,DAY21,DAY22,DAY23,DAY24,DAY25:
                    DAY26,DAY27,DAY28,DAY29,DAY30,DAY31
.
          LOAD      TNDAY FROM COUNT OF TDAY1,TDAY2,TDAY3,TDAY4,TDAY5,TDAY6:
                    TDAY7,TDAY8,TDAY9,TDAY10,TDAY11:
                    TDAY12,TDAY13,TDAY14,TDAY15,TDAY16:
                    TDAY17,TDAY18,TDAY19,TDAY20,TDAY21:
                    TDAY22,TDAY23,TDAY24,TDAY25,TDAY26:
                    TDAY27,TDAY28,TDAY29,TDAY30,TDAY31
.
          ADD       NDAY,TNDAY
          STORE     TNDAY USING COUNT OF TDAY1,TDAY2,TDAY3,TDAY4,TDAY5,TDAY6:
                    TDAY7,TDAY8,TDAY9,TDAY10,TDAY11:
                    TDAY12,TDAY13,TDAY14,TDAY15,TDAY16:
                    TDAY17,TDAY18,TDAY19,TDAY20,TDAY21:
                    TDAY22,TDAY23,TDAY24,TDAY25,TDAY26:
                    TDAY27,TDAY28,TDAY29,TDAY30,TDAY31
.
.         Add to Grand total bed days
.
          LOAD      GNDAY USING COUNT OF GDAY1,GDAY2,GDAY3,GDAY4,GDAY5,GDAY6:
                    GDAY7,GDAY8,GDAY9,GDAY10,GDAY11:
                    GDAY12,GDAY13,GDAY14,GDAY15,GDAY16:
                    GDAY17,GDAY18,GDAY19,GDAY20,GDAY21:
                    GDAY22,GDAY23,GDAY24,GDAY25,GDAY26:
                    GDAY27,GDAY28,GDAY29,GDAY30,GDAY31
.
          ADD       NDAY,GNDAY
          STORE     GNDAY USING COUNT OF GDAY1,GDAY2,GDAY3,GDAY4,GDAY5,GDAY6:
                    GDAY7,GDAY8,GDAY9,GDAY10,GDAY11:
                    GDAY12,GDAY13,GDAY14,GDAY15,GDAY16:
                    GDAY17,GDAY18,GDAY19,GDAY20,GDAY21:
                    GDAY22,GDAY23,GDAY24,GDAY25,GDAY26:
                    GDAY27,GDAY28,GDAY29,GDAY30,GDAY31
.
          COMPARE   "31",COUNT
          RETURN    IF EQUAL
.
          ADD       ONE,COUNT
          GOTO      TOTLOP
.
.
.        Initialize the work file
.
ZERODAY   MOVE      ONE,COUNT
.
DAYLOP    STORE     ZERO USING COUNT OF DAY1,DAY2,DAY3,DAY4,DAY5,DAY6,DAY7:
                    DAY8,DAY9,DAY10,DAY11,DAY12,DAY13:
                    DAY14,DAY15,DAY16,DAY17,DAY18,DAY19:
                    DAY20,DAY21,DAY22,DAY23,DAY24,DAY25:
                    DAY26,DAY27,DAY28,DAY29,DAY30,DAY31
          COMPARE   "31",COUNT
          RETURN    IF EQUAL
.
          ADD       ONE,COUNT
          GOTO      DAYLOP
.
.         Initialize the sub-total variables
.
ZEROTDAY  MOVE      ONE,COUNT
.
DAYLOPT   STORE     ZERO USING COUNT OF TDAY1,TDAY2,TDAY3,TDAY4,TDAY5,TDAY6:
                    TDAY7,TDAY8,TDAY9,TDAY10,TDAY11:
                    TDAY12,TDAY13,TDAY14,TDAY15,TDAY16:
                    TDAY17,TDAY18,TDAY19,TDAY20,TDAY21:
                    TDAY22,TDAY23,TDAY24,TDAY25,TDAY26:
                    TDAY27,TDAY28,TDAY29,TDAY30,TDAY31
          COMPARE   "31",COUNT
          RETURN    IF EQUAL
.
          ADD       ONE,COUNT
          GOTO      DAYLOPT
.
.
.         Initialize the grand total variables
.
ZEROGDAY  MOVE      ONE,COUNT
.
DAYLOPG   STORE     ZERO USING COUNT OF GDAY1,GDAY2,GDAY3,GDAY4,GDAY5,GDAY6:
                    GDAY7,GDAY8,GDAY9,GDAY10,GDAY11:
                    GDAY12,GDAY13,GDAY14,GDAY15,GDAY16:
                    GDAY17,GDAY18,GDAY19,GDAY20,GDAY21:
                    GDAY22,GDAY23,GDAY24,GDAY25,GDAY26:
                    GDAY27,GDAY28,GDAY29,GDAY30,GDAY31
          COMPARE   "31",COUNT
          RETURN    IF EQUAL
.
          ADD       ONE,COUNT
          GOTO      DAYLOPG
.
.         Keyin Period
.
KEYMN     MOVE      "22",CCOL
          MOVE      FOUR,CVERT
          UNPACK    SP4,CYEAR,CMON
          MOVE      CCC,CCENT
.
          MOVE      ZERO,CHIGHLT
          CALL      KEYPER
          BRANCH    OVRCD,KEYM999
.
          PACK      KEY6,ICENT,IYEAR,IMON
          REP       " 0",KEY6
          CALL      RDDRGA3
          BRANCH    OVRCD,KEYM900
          DISPLAY   *P33:CVERT,*V2LON,DRGLDSC
          GOTO      KEYM999
.
KEYM900   DISPLAY   *P50:CVERT,*V2LON,*B,*EL:
                    "** Invalid Date **",*W2,*P50:CVERT,*EL;
          GOTO      KEYMN
.
KEYM999   RETURN
.
.         FILE  I/O REQUESTS FOR WORK FILE
.
RDWORK    MOVE      ZERO,OVRCD
          RESET     KEY9
          READ      PSTSRT,KEY9;DRTYPE,STADOCT,DTITL,DSNAME,DGNAME:
                    DAY1,DAY2,DAY3,DAY4:
                    DAY5,DAY6,DAY7,DAY8,DAY9,DAY10,DAY11,DAY12:
                    DAY13,DAY14,DAY15,DAY16,DAY17,DAY18,DAY19:
                    DAY20,DAY21,DAY22,DAY23,DAY24,DAY25,DAY26:
                    DAY27,DAY28,DAY29,DAY30,DAY31
          GOTO      OVERCOND IF OVER
          RETURN
.
RDSWORK   MOVE      ZERO,OVRCD
          RESET     KEY9
          READ      PSTSRT,KEY9;;
          GOTO      OVERCOND IF OVER
          RETURN
.
RDKWORK   MOVE      ZERO,OVRCD
          READKS    PSTSRT;DRTYPE,STADOCT,DTITL,DSNAME,DGNAME:
                    DAY1,DAY2,DAY3,DAY4,DAY5:
                    DAY6,DAY7,DAY8,DAY9,DAY10,DAY11,DAY12:
                    DAY13,DAY14,DAY15,DAY16,DAY17,DAY18,DAY19:
                    DAY20,DAY21,DAY22,DAY23,DAY24,DAY25,DAY26:
                    DAY27,DAY28,DAY29,DAY30,DAY31
          GOTO      OVERCOND IF OVER
          RETURN
.
WRWORK    RESET     KEY9
          WRITE     PSTSRT,KEY9;KEY9,DTITL,DSNAME,DGNAME,DAY1,DAY2,DAY3,DAY4:
                    DAY5,DAY6,DAY7,DAY8,DAY9,DAY10,DAY11,DAY12:
                    DAY13,DAY14,DAY15,DAY16,DAY17,DAY18,DAY19:
                    DAY20,DAY21,DAY22,DAY23,DAY24,DAY25,DAY26:
                    DAY27,DAY28,DAY29,DAY30,DAY31
          RETURN
.
UPWORK    UPDATE    PSTSRT;DRTYPE,STADOCT,DTITL,DSNAME,DGNAME,DAY1,DAY2,DAY3:
                    DAY4,DAY5,DAY6,DAY7,DAY8,DAY9,DAY10,DAY11,DAY12:
                    DAY13,DAY14,DAY15,DAY16,DAY17,DAY18,DAY19:
                    DAY20,DAY21,DAY22,DAY23,DAY24,DAY25,DAY26:
                    DAY27,DAY28,DAY29,DAY30,DAY31
          RETURN
+
.
.------ I/O Routines for temp file ------
.
RATMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READ      PATMP1XX,KEY8;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READ      PATMP1XX,KEY8;;
          RETURN
.
RDTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READ      PATMP1XX,KEY8;TMPADMN
          GOTO      OVERCOND IF OVER
          RETURN
.
RKTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READKS    PATMP1XX;TMPADMN
          GOTO      OVERCOND IF OVER
          RETURN
.
RPTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READKP    PATMP1XX;TMPADMN
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          WRITE     PATMP1XX,KEY8;KEY8
          RETURN
.
UPTMPR1   UPDATE    PATMP1XX;TMPADMN
          RETURN
.
DETMPR1   RESET     KEY8
          DELETE    PATMP1XX,KEY8
          RETURN
.
.
          INC       STD001IO
          INC       TFILENAM                * Generate Temp File Name
          INC       IBASEQIO/INC            * Sequential Numbers File
          INC       WEBERRIO/INC            * Web Server Error Logging
          INC       PATCOMN3
          INCLUDE   PATDAYIO/INC
          INC       PATDSCIO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATTRNIO/INC
          INC       PATDO1IO/INC
          INCLUDE   PATDRGIO/INC
          INC       PATCODIO/INC
.
ENDIT     CHAIN     PGM
          STOP
.

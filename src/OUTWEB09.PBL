.----------------------------------------------------------------------
. Program       : OUTWEB09
.
. Function      : Outpatient Direct Processing 
.
. Modifications : 
.----------------------------------------------------------------------
. V12.01.02 05/02/2026  Zumin Yu       TSK 0972419
.           Updated PTCNMRPN to read from the new location
. V12.01.01 11/12/2025  Bella Turco    TSK 0949715
.                       Recompiled for UPDUR - added NZPMICHG
. V12.00.01 19/05/2025  J.Tan          TSK 0955096
.           Added alpanumeric visit number generation
. V11.05.02 06.03.2025  DA Sarkies     TSK 0955909
.           Added code to retrieve and store NDIS number in watespaf
. V11.05.01 21.11.2024  Ebon Clements  TSK 0953786
.           Added identifying gender to ESIS patient details watespaf
. V11.04.01 19/12/2023 Thanh T           TSK 0932545
.           Packed PMCEFTXT with spaces
. V11.02.02 01/04/2022  Thanh T        TSK 0903453
.           Added SPPXUCC4, SPPXUCC5, SPPXATF1 and SPPXATF2 for   
.           DEATHAUD changes
. V11.02.01 10/02/2022  Thanh T        TSK 0905641
.           Recompiled as OUTHEDFD/OUTMA1FD changes
. V11.01.03 20/12/2021  Thanh T        TSK 0877055                  
.           Added DEATHATD for common variables to be used in DEATHAUD*
. V11.01.02 15/09/2021  Thanh T        TSK 0889595
.           Recompiled for PMSDAUFD/DEATHAUD changes
. V11.01.01 02/07/2021  Thanh T        TSK 0905755
.           Recompiled as BEDBDPRO changes
. V11.00.04 09/12/2020  Thanh T        TSK 0872840
.           Recompiled as PMSPX2TM changes
. V11.00.03 23/06/2020  Peter Vela        TSK 0878550
.           Recompiled for DEATHAUD
. V11.00.02 10/06/2020  Peter Vela        TSK 0878550
.           Recompiled for DEATHAUD
. V11.00.01 09/04/2020  J.Tan             TSK 0868813
.           Mod to set PACFRMT to 3 for PKNAME
. V10.15.01 18/10/2019  Richa Phogat    TSK 0861257
.           Added code to get Requestor description(REQCDESC) under APMT0000,
.           ACTT0000 & HATB0000
. V10.14.01 13/06/2019  Peter Vela      Task 0871798
.           Recompiled for DEATHAUD
. V10.13.01 13/08/2018  Peter Vela      Task 0845218
.           Recompiled for DEATHAUD
. V10.12.01 11/07/2018  Peter Vela      TSK 0845218
.           Recompiled for DEATHAUD
. V10.11.04 27/12/2017  Steve Armstrong  TSK 0850165
.           Recompiled for changes to DGCLICAC.
. V10.11.03 27/11/2017  Thanh Tieu     Task 0821710
.           Recompiled as PATMASTM changed
. V10.11.02 22/11/2017  Ania P         TSK 0261630
.           Moved TFILNAME to CFNAMEDP in INIT0000
. V10.11.01 30/07/2017  Thanh T.       TSK 0821710
.           Audit admission/outpatient/UR comments. changed PATMASTM/PMSPX2TM.
.----------------------------------------------------------------------
. V10.10.01 20/06/2017  Ania P         CAR 261630
.           Recompiled for UPDUCARD.PBL
. V10.07.01 04/01/2016 Ebon Clements  TSK 0294154
.           Added Opt out of SMS to prev address file write
. V10.06.02 15/04/2015  Peter Vela     CAR 312723
.           Recompiled for DEATHAUD
. V10.06.01 12/03/2015  Steve Armstrong CAR 314108
.           Removed reference to PATCGPFD (No Longer in use)
.----------------------------------------------------------------------
. V10.05.01 11/08/2014  Don Nguyen      CAR 297774
.           Recompiled for changes to PMSBRQFD/IO
.----------------------------------------------------------------------
. V10.04.04 07/04/2014  Peter Vela      CAR 286258
.           Recompiled for PATMASTM
. V10.04.03 31.03.2014  B.G.Ackland  CAR 299363
.           Replace META Tag Redirect with Javascript in Common RDIREC00
.           Routine
. V10.04.02 24/03/2014  Steve Armstrong  CAR 298483
.           Recompiled for changes to DGCLICOB
. V10.04.01 22/01/2014  Don Nguyen       CAR 295972
.           Modified UPAD0000 - KEY16 to KEY24. Updated key PACK.
.----------------------------------------------------------------------
. V10.03.26 27/11/2013  Ebon Clements    CAR 294535
.           Renamed Date Coded to Date Closed - DVIS1000
. V10.03.25 12/09/2013  Ania P           CAR 291203
.           Replace " with ' before outputting outdcoaf fields.
. V10.03.24 10/09/2013  Steve Armstrong  CAR 291089
.           Recompiled for changes to PMSQVIFD.
. V10.03.23 06/09/2013  Peter Vela       CAR 289901
.           Populated Closed Referral High Risk Alert in HATB0000 and ACTT0000
.           if there is a linked Outpatient visit
. V10.03.22 04/09/2013  Peter Vela       CAR 289901
.           Fixed Action Type display for PMI Update and Cancelled Appointment
.           in HATB0000 and ACTT0000
.           30/08/2013  Don Nguyen       CAR 273426
.           Recompiled for changes to CLALLREF
. V10.03.21 28/08/2013  Peter Vela       CAR 289901
.           Populated Pending Reschedule (Cancelled Appointments) High Risk 
.           Alert to HATB0000 and ACTT0000
. V10.03.20 05/08/2013  Ebon Clements    CAR 283202
.           Added preferred method of contact vars to PX2U0000
. V10.03.19 26/07/2013  Steve Armstrong  CAR 287787
.           Recompiled for changes to DGCLICUA.
. V10.03.18 18/07/2013  Steve Armstrong  CAR 268961
.           Recompiled for changes to PMSQVIFD.
. V10.03.17 30/06/2013  Davin             CAR 283202
.           Recompiled for changes to hl7 messaging (pmsqptfd)
. V10.03.16 20/06/2013  Peter Vela       CAR 283202
.           Recompiled for PMSPX2FD
. V10.03.15 01/05/2013  Steve Armstrong  CAR 284376
.           Recompiled for changes to DGCLICUP.
. V10.03.14 24/04/2013  Ebon Clements     CAR 261630
.           Removed port number from temp file name
. V10.03.13 08/04/2013  Ebon Clements  CAR 282457
.           Default location filter from user id
. V10.03.12 03/04/2013  Davin          CAR 270648
.           Recompiled for changes to HL7 messaging includes
. V10.03.11 05/03/2013 Jill Parkinson    CAR 257831
.           Added write/update of pmsupdaf
. V10.03.10 05/02/2013 Ania P.           CAR 274764
.           Changed OBATIME to OPRSTIME in DVIS3200.
. V10.03.09 05/12/2012 Peter Vela        CAR 262297
.           Added Location Filter to HATB0000
. V10.03.08 23/07/2012 Patrick Adair     CAR 269159
.           Recompiled for changes to PATCOMN2
. V10.03.07 19/07/2012  Steve Armstrong   CAR 268870
.           Recompiled for changes to PATCOMN2 & UPDUR
. V10.03.06 09/07/2012  Steve Armstrong   CAR 267675
.           Recompiled for changes to PATCOMN2.
. V10.03.05 16/03/2012  Ebon Clements   CAR 262012
.           Fixed update of surname when confirm OPD demographics - UPDMAS00
. V10.03.04 16/01/2012  Sandra Barcham  CAR 256563
.           Recompiled for PATMASTM
. V10.03.03 08/12/2011 Ebon Clements    CAR 248529
.           Added webPAS unique id to contacts
. V10.03.02 01/12/2011  Mike Laming    CAR 250985
.           Added MultiDatabase to DEAPOL00 (DEATHPOL), added variables etc to
.           this for DEATHPOL.PBL
. V10.03.01 10/11/2011  Ebon Clements     CAR 248529
.           Added multiple contacts of the same type functionality
.           14/11/2011  Davin           CAR 249257
.           Added A08 triggers for prfa change - tacc0000 (hl7trgid 1,2,3)
. V10.02.10 26/10/2011  Ebon Clements    CAR 253842
.           Fixed AH referral filters on OPD action list - HATB3000
. V10.02.09 21/10/2011  Ebon Clements    CAR 252711
.           Fixed pending reschedule preferred date display - DVIS0000
. V10.02.08 11/10/2011  Mark Coupland    CAR 252429
.           Recompiled for PMSPX2TM and PATMASTM
. V10.02.07 05/10/2011 Ebon Clements   CAR 251737
.           Ignore second NOK when finding NOK cat code - GTAG0300
. V10.02.06 03/10/2011 Ebon Clements   CAR 252187
.           Fixed action type filter for patient level action list - FILTPATY
. V10.02.05 03/10/2011 Ebon Clements   CAR 251845
.           Only save postal and NOK if details entered
. V10.02.04 13/09/2011 Jill Parkinson  CAR 248652
.           Recompiled for PMSPX2TM - Added read of pmsvx1af in GETCURIP
. V10.02.03 13/09/2011  Ebon Clements            CAR 242014
.           Fixed confirm demographics update decease patient
. V10.02.02 13/09/2011  Ebon Clements            CAR 242014
.           Added HTMLLNK2 link to PMI to hosital OPD action list - HATB0000
. V10.02.01 12/09/2011  Ebon Clements            CAR 242014
.           Fixed I * C on visit file index 2
. V10.02.00 24/08/2011  Ebon Clements            CAR 242014
.           Created Web Server
.----------------------------------------------------------------------
          INC       IBAWEBDF    
          INC       WEBDATDF
.
          INC       MULTVISV/INC        * PATDPATH variables          *I-250985
          INC       PATDPTHV/INC        * PATDPATH variables          *I-250985
.
          INC       ALLCONFD/INC
          INC       ALLREFFD/INC
          INC       ALLLNKFD/INC
          INC       APSMASFD/INC
          INC       BOKRC1FD/INC
          INC       BOKRX1FD/INC
          INC       DEATHATD/INC
          INC       EMRCONFD/INC
          INC       EMRVISFD/INC
          INC       MEHCONFD/INC
          INC       MLTSECFD/INC
          INC       MRTMASFD/INC
          INC       NHIETHFD/INC
          INC       NHIMASFD/INC
          INC       NZHISIFD/INC
          INC       OUTARTFD/INC
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
          INC       OUTCANFD/INC
          INC       OUTCLIFD/INC
          INC       OUTCTYFD/INC 
          INC       OUTHEDFD/INC
          INC       OUTMA1FD/INC
          INC       OUTSESFD/INC
          INC       OPRCONFD/INC
          INC       OUTCONFD/INC
          INC       OUTDANFD/INC
          INC       OUTDANTD/INC
          INC       OUTDCOFD/INC
          INC       OUTDEMFD/INC
          INC       OUTDEMTD/INC
          INC       OUTRSHFD/INC
          INC       OUTSITFD/INC
          INC       PATAK1FD/INC
          INC       PATALRFD/INC
          INC       PATAM1FD/INC
          INC       PATCALAG/INC
.
          INC       PMSUCHFD/INC
          INC       PMSUCDFD/INC
          INC       PATMASTD/INC
          INC       PATCOMM/INC
          INC       PATCONFD/INC
          INC       PATDRGFD/INC
          INC       PATDO1FD/INC
          INC       PATDTHFD/INC
          INC       PATDSCFD/INC
          INC       PATFN1FD/INC
          INC       PATGSRFD/INC
          INC       PATHOSFD/INC                                      *I-250985
          INC       PATHSPFD/INC
          INC       PATIN1FD/INC
          INC       PATINVFD/INC
          INC       PATLOCFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATNIDFD/INC
          INC       PATONLFD/INC
          INC       PATPA1FD/INC
          INC       PATPNIFD/INC
          INC       PATPR1FD/INC
          INC       PATRE1FD/INC
          INC       PATRE1TD/INC
          INC       PATTRNFD/INC
          INC       PATURAFD/INC
          INC       PATVISFD/INC
          INC       PATWR1FD/INC
          INC       PMSBBDFD/INC
          INC       PMSBRQFD/INC
          INC       PMSCCDFD/INC
          INC       PMSCEXFD/INC
          INC       PMSCEXTD/INC
          INC       PMSCURFD/INC
          INC       PMSDAUFD/INC
          INC       PMSEPBFD/INC
          INC       PMSHCGTD/INC
          INC       PMSHCGFD/INC
          INC       PMSHCPFD/INC
          INC       PMSHCPTD/INC
          INC       PMSPX2FD/INC
          INC       PMSPX2TD/INC
          INC       PMSRELFD/INC
          INC       PMSTLEFD/INC
          INC       PMSUPDFD/INC
          INC       PMSVX1FD/INC
          INC       PRSPMIFD/INC
          INC       TFILEVAR/INC
          INC       WATCONFD/INC
          INC       WATESEFD/INC 
          INC       WATESPFD/INC
          INC       WEBDINVS/INC
.
.----------------------------------------------------------------------
ADD       DIM       2
ALLSTAT0  INIT      "Waiting"
ALLSTAT1  INIT      "Active"
ALLSTAT2  INIT      "Closed"
ALLSTAT3  INIT      "Inactive"
ALLSTAT4  INIT      "Cancelled"
ALLSTAT5  INIT      "Rejected"
AMM       DIM       2
AYY       DIM       2
ACC       DIM       2
ACTION    DIM       1
AOUTCOME  DIM       3
ATCHAR    INIT      "@"
BEDRWARD  INIT      "~~B"
CATTC     INIT      "tc"
CATCT     INIT      "ct"
CARDNUMB  DIM       20
CALDAYS   FORM      4
CBOOK     FORM      1
COMMTFLG  DIM       1
CFILEPRE  DIM       3   * Current Open File Prefix for Outpatient Files
CHAR1     DIM       1
CHAR2     DIM       1
CHAR3     DIM       1
CHAR4     DIM       1
CHARS58   DIM       4
CHAR9     DIM       1
CMDLINE   DIM       80                                                *I-250985
COMM127   DIM       127
COMMURNO  DIM       8
COMMADAT  DIM       8
COMMATIM  DIM       8
COMMTYPE  DIM       2
COMFILAF  FILE      ASCII, FIXED=128
COMFILNM  DIM       8
COMMLIN1  DIM       100
COMMLIN2  DIM       100
COMMLIN3  DIM       100
COMPLINK  DIM       127
COUNTER   FORM      2
DESCCANC  DIM       3
DESCATYP  DIM       25
DESCCLHR  DIM       3
DESCAPPD  DIM       16
DESCCLIN  DIM       30
DESCOUTC  DIM       20
DESCCOMP  DIM       3
DESCPEND  DIM       3
DIM4      DIM       4
DIM9      DIM       9
DIM11     DIM       11
DISPDAT1  DIM       11
DLCHAR    INIT      "$"
DOCTCODE  DIM       8
DETNTREC  FORM      2
DESCCUID  DIM       30   
DESCUUID  DIM       30   
DVACHAR1  INIT      "QNVTSW"
F3A       FORM      3
FILTATYP  DIM       2
FILTPATY  DIM       2
FILTADAT  DIM       1
FILTCLID  DIM       6
FILTCOMP  DIM       1
FILTCLTY  DIM       6
FILTHOSP  DIM       3
FILTLOCA  DIM       3
FILTSPEC  DIM       3
FILTVDAT  DIM       1
FRSTGNAM  DIM       40
FOLDERSF  DIM       3
FORM8     FORM      8
FULLGNAM  DIM       80
HBWAIT    FORM      1
HELPRNT   FORM      3
HOSPCODE  DIM       4                                                 *I-250985
HOSPITAL  DIM       25                                                *I-250985
HTMLLNK2  DIM       127
KEY1A     DIM       1
KEY2A     DIM       2
MHLRCODE  DIM       3
MRCNNOHS  FORM      2                                                 *I-250985
OLDCEASE  FORM      1
OTDFLAG   FORM      1
OTDWEND   FORM      4
OPERCODE  DIM       4
NXTALPHA  FORM      1
NEXTPAGE  DIM       1       
NMPNUMB   DIM       20
PREVADDR  FORM      1
PSAGECON  DIM       3
PSAGETYP  DIM       3
RETURNFL  DIM       1
SAVEALIA  FORM      1
SAVEALPR  FORM      1
SAVEPCTR  DIM       2
SAVEPHCP  DIM       10
SAVEPRAC  DIM       10
SAVPFUND  DIM       6
CODERS    INIT      "rs"
REQCDESC  DIM       20               * Requestor (Category rs)
.
ALIASSTS  DIM       1
SAVEUSR1  DIM       3
.
SAVESNAM  DIM       40
SAVEXFNM  DIM       40
SAVEXSNM  DIM       40
SAVETITL  DIM       4
SAVEPSEX  DIM       1
SAVEPDOB  DIM       8
SAVEPFGN  DIM       25
SAVEPMST  DIM       3
SAVEPREG  DIM       3
SAVEPCNT  DIM       3
SAVEPTYP  DIM       3
SAVEABRG  DIM       3
SAVEUSR7  DIM       3
SAVEMEDI  DIM       10
SAVEMCCD  DIM       2
SAVEMEDC  DIM       8
SAVECONP  DIM       1
SAVEUPRF  DIM       3
SAVEINTR  DIM       1
SAVELNG1  DIM       3
SAVEPRVI  DIM       3
SAVEFLDR  DIM       3
SAVESN16  DIM       1
SPPXUCC4  DIM       3
SPPXUCC5  DIM       3
SPPXATF1  DIM       80
SPPXATF2  DIM       80
SAVECUID  DIM       8
SAVECDAT  DIM       8
SAVECTIM  DIM       8
.
. PRS2 PMI Trigger save variables
SAVPBDAT  DIM       8             * birthdate
SAVXABRG  DIM       3             * indig. status
SAVPCONT  DIM       3             * cob
SAVXLNG1  DIM       3             * pref. language
SAVPPSEX  DIM       1             * sex
SAVPMSTA  DIM       3             * marital status
SAVPTYPE  DIM       3             * resident status
SAVXINTR  DIM       1             * interpreter requ.
SAVPADD2  DIM       35            * address line 2
SAVPSUBR  DIM       35            * suburb
SAVPPOST  DIM       8             * postcode
SAVPMEDI  DIM       10            * medicare number
SAVPMCCD  DIM       2             * medicare code
SELECHOS  DIM       3
NZPMICHG  DIM       1
.
SCNDGNAM  DIM       40
SHOWFLAG  FORM      1
SURSYST   FORM      1
SVPXINTR  DIM       1 
SVPXLNG1  DIM       3 
SPPEML    DIM       80
THETFLAG  FORM      1
UPDTTYPE  DIM       1       
URWTARAY  INIT      "234567"
URNLNGTH  FORM      2
URCHKDGT  FORM      4
URNOWGHT  FORM      1
URCHKPRD  FORM      2
USERNAME  DIM       10
VCASE     DIM       1
WARCOUNT  FORM      2
WEBWARNG  DIM       127[12]
.
.----------------------------------------------------------------------
PRGID     INIT      "OUTWEB09"
VERSION   INIT      "V12.01.02"
PRGNAM    INIT      "Outpatient Direct Processing"
.----------------------------------------------------------------------
.
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          CALL      OUTFIL00       * Check Correct Files Are Open & Valid Site
          BRANCH    EXIT,MAIN1000
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200,MAIN1300,MAIN1400
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "Invalid Option",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  CALL      OPDP0000        * Request PMI Update
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1200  CALL      APPM0000        * Appointment Messages 
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1300  CALL      PATA0000        * Patient Level Actions Update
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1400  CALL      HOSA0000        * Hospital Level Action List  
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
.------------------------------------------------------------
. Output Next Page Based on CGI Parameter nextpage
.------------------------------------------------------------
NXTPAG00  MOVE      ZERO,F1
          MOVE      NEXTPAGE,F1
          BRANCH    F1,NXTPAG10,NXTPAG20,NXTPAG30
.
          CALL      WINCLS00
          GOTO      NXTPAG99
.
NXTPAG10  CALL      RDIREC00
          GOTO      NXTPAG99
.
NXTPAG20  CALL      WEBWAR00
          GOTO      NXTPAG99
.
NXTPAG30  CALL      PREDIR00
          GOTO      NXTPAG99
.
NXTPAG99  RETURN
.
.------------------------------------------------------------------------------
.  Redirect Parent URL
.------------------------------------------------------------------------------
PREDIR00  CALL      OPENHTML
          BRANCH    EXIT,PREDIR90
.
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             " parent.location.href=#"",REDIRECT,"#";":
                             "}":
                             "</script>"
          CALL      CLOSHTML
          GOTO      PREDIR99
.
PREDIR90  DISPLAY   "*** Fifo Not Found ***"
.
PREDIR99  RETURN
.------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------
INIT0000  CALL      INTMAS00     * Open routine for PATMASTM
.
          OPEN      ALLREFA1,"allrefaf"
          OPEN      ALLLNKA1,"alllnkaf"
          OPEN      BOKRC1A1,"bokrc1af"
          OPEN      BOKRC1A6,"bokrc1af"
          OPEN      BOKRC1A6,"bokrc1af"
          OPEN      BOKRX1A1,"bokrx1af"
          OPEN      MLTSECA1,"mltsecaf"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
          OPEN      PATHSPA1,"pathspaf"
          OPEN      PATIN1A1,"patin1af"
          OPEN      PMSRELA1,"pmsrelaf"
          OPEN      PMSTLEA1,"pmstleaf"
          OPEN      PATRE1A1,"patre1af"
          OPEN      PATURAD1,"paturadf"
          OPEN      PATURAD2,"paturadf"
          OPEN      PATGSRN1,"patgsrnf"
          OPEN      PATGSRN2,"patgsrnf"
          OPEN      PATGSRN3,"patgsrnf"
          OPEN      PATGSRN4,"patgsrnf"
          OPEN      PATONLV1,"patonlvf"
          OPEN      PATPA1A1,"patpa1af"
          OPEN      PATPNIA1,"patpniaf"
          OPEN      PATVISA2,"patvisaf"
          OPEN      PATVISA3,"patvisaf"
          OPEN      PATVISA4,"patvisaf"
.
          OPEN      PMSBBDA1,"pmsbbdaf"
          OPEN      PMSBBDA2,"pmsbbdaf"
          OPEN      PMSCEXA1,"pmscexaf"
          OPEN      PMSEPBA1,"pmsepbaf"
          OPEN      PMSEPBA2,"pmsepbaf"
          OPEN      PATTRAN1,"pattranf"
          OPEN      PATTRAN2,"pattranf"
          OPEN      PMSBRQA1,"pmsbrqaf"
          OPEN      PMSBRQA2,"pmsbrqaf"
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATDSCH3,"patdschf"
          OPEN      PATDTHA1,"patdthaf"
          OPEN      PATNIDA1,"patnidaf"
          OPEN      PATINVR3,"patinvrf"
          OPEN      PATLOCA1,"patlocaf"
          OPEN      PMSCURA1,"pmscuraf"
          OPEN      PATDRGA1,"patdrgaf"
          OPEN      PATDRGA2,"patdrgaf"
          OPEN      WATESEA1,"wateseaf"
          OPEN      WATESPA1,"watespaf"
          OPEN      PRSPMIA1,"prspmiaf"
          OPEN      PMSCCDA1,"pmsccdaf"
.
          OPEN      OUTARTA1,"outartaf"
          OPEN      OUTDEMA1,"outdemaf"
          OPEN      OUTDANA1,"outdanaf"
          OPEN      OUTDANA2,"outdanaf"
          OPEN      OUTDANA3,"outdanaf"
          OPEN      OUTDANA4,"outdanaf"
          OPEN      OUTDCOA1,"outdcoaf"
.
          CALL      RDCTRL00                     * Read control file parameters
          IF        CAUDM=0
            OPEN      PATAM1A1,"patam1af"
          ENDIF
.
. Open outpatient file with site prefix
.
          MOVE      "out",CFILEPRE       * Save Current File Prefix
          CALL      OPNOUT00             * Open Outpatient Files
.
          MATCH     "1",PTCNNWCA
          IF        @EQUAL
            OPEN      PMSAUDCE,"pmsaudce"
            OPEN      PMSAUDC2,"pmsaudce"
          ENDIF
.
          IF        IBCNMHOS=1
            OPEN      MLTCODA1,"mltcodaf"
            OPEN      MLTCODA2,"mltcodaf"
          ENDIF
.
          CALL      TFILENAM
          MOVE      TFILNAME,COMFILNM
          CALL      TFILENAM
          MOVE      TFILNAME,CFNAMEDP
.
INIT9999  RETURN
.
+
.------------------------------------------------------------
. RDCTRL00 - Read control file parameters
.------------------------------------------------------------
.
RDCTRL00  READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TEN;*25,PTCNVKCA,*42,PTCNDGAT,*53,CPCONT:
                                *126,CLABPG:
                                *127,CLABPRT,*164,CSPECCO,*165,CIDENT:
                                *166,CMEMB,*167,CSECURA,*188,CCHKRAT:
                                *194,CDIETCD,*196,CGENRUR,*197,CASSOCD:
                                *198,CDIETDF,*201,CAUDA,*211,CAUDK:
                                *213,CAUDM,*217,CAUDQ,*244,CHOSPNUM
.
          READ      CONTROLF,TEN3;*143,CSIZLAB,*186,CMBSOPR,*188,CMABINS:
                                  *194,CVETINS,*216,CUSR1,*217,CUSRDS1:
                                  *229,CUSR2,*230,CUSRDS2,*242,CCLMCHN:
                                  *243,CACCLSS,*244,CHCS,*245,CHOSTYP
.
          READ      CONTROLF,TEN4;*227,CIDLAY,*229,CUSMAN1,*230,CUSMAN2:
                                  *231,CATDMAN,*248,CURDEL
.
          READ      CONTROLF,TEN5;*187,CBOOK,*189,CTHETR,*190,CKMBS,*191,CMUSR1:
                                  *192,CMUSDS1,*204,CMUSR2,*205,CMUSDS2:
                                  *217,CMUSR3,*218,CMUSDS3,*230,CMUSMAN1:
                                  *231,CMUSMAN2,*232,CMUSMAN3,*233,CUSR3:
                                  *234,CUSRDS3,*246,CUSMAN3,*249,CRDRTYP
.
          READ      CONTROLF,TEN8;*17,CUSR4,*18,CUSRDS4,*30,CUSMAN4:
                                  *31,CUSR5,*32,CUSRDS5,*44,CUSMAN5:
                                  *45,CUSR6,*46,CUSRDS6,*58,CUSMAN6:
                                  *59,CUSR7,*60,CUSRDS7,*72,CUSMAN7:
                                  *143,CLOWMBS,*148,CLOWADM,*151,CHIGHMBS:
                                  *156,CHIGHADM,*159,CTOPADM,*206,CFINAN:
                                  *225,CCHGST1,*228,CCHGST2,*246,CMAXAGE
.
          READ      CONTROLF,TEN9;*75,CCNOTES,*208,CPMIAD,*209,CSRESP:
                                  *212,CFEETYP,*214,CWRDSCN,*225,CSALIAS
.
          READ      CONTROLF,TWENTY;*50,PTCNADBF,*52,PTPCLRES,*165,PTCNDFBO:
                                    *193,PTCNDCLM
.
          READ      CONTROLF,TWENTY1;*45,PTCNDRSM,*46,PTCNBAGE,*205,PTCNUKEY:
                                     *221,CNSWLOC,*238,PTCNAGEC,*248,PTCNSOUC
.
          READ      CONTROLF,TWENTY2;*30,STCNIPPM,*31,STCNIPAD,*45,STCNFUND
.
          READ      CONTROLF,TWENTY7;*46,HBWAIT
.
          READ      CONTROLF,THIRTY;*90,HELPRNT
.
          READ      CONTROLF,THIRTY7;*137,HWDSOUR,*241,WTCNUSBC
.
          READ      CONTROLF,FORTY;*44,OPCNAUDB,*61,OPCNAUDS,*112,OPCNCODE
.
          READ      CONTROLF,FORTY2;*19,OPCNFOTM,*20,OPCNTOTM,*101,OPCNSGCL
.
          READ      CONTROLF,FIFTY9;*2,C3BDAYS:
                                    *6,CLUNTNO,*35,CTYPLAB,*141,CPIDCOD:
                                    *171,PTCNSTAU,*172,PTCNNUMM,*174,PTCNNDAT:
                                    *182,PTCNORNO,*192,PTCNURCF,*193,PTCNRQCF:
                                    *194,PTCNDPCF,*195,PTCNRLAY,*196,PTCNUCOD:
                                    *209,PTCNDAYF,*216,PTPRVALS:
                                    *220,PTCNUADT,*226,CCARECL:
                                    *233,PTCNADOC,PTCNNMPI
.
          READ      CONTROLF,SIXTY9;*43,MHCNAUDA,*69,MHCNUSE,*80,MHCNUDSA
.
          READ      CONTROLF,SEVENTY7;*201,PTCNIDHD
.
          READ      CONTROLF,SEVENTY9;*82,PTCNDSCI,*94,PTCNADTT:
                                      *95,PTCNDSTT,*138,PTCNURHA
.
          READ      CONTROLF,EIGHTY;*250,PTCNHDPS
.
          READ      CONTROLF,EIGHTY8;*234,PTCNNCFB,*235,PTCNADCF
.
          READ      CONTROLF,FIFTY9;*234,CPNMPICK
          READ      CONTROLF,TWENTY1;*55,SRCHDOBS,*138,PTCNNHII
          READ      CONTROLF,FIFTY9;*234,PTCNNMPI
          READ      CONTROLF,NINTY8;*124,PTCNHPCD,*129,PTCNDGST
          READ      CONTROLF,HUND05;*98,PTCNCMYN,PTCNNWCM,*166,PTCNCMXF:
                                    *210,PTCNLCLM
          READ      CONTROLF,HUND10;*115,PTCNUEDI,*243,PTCNCOMF,*248,PTCNEPAY
          READ      CONTROLF,HUND18;*138,PTCNNEWC,*139,PTCNAIWI,*167,PTCNNWCA:
                                    *179,PTCNH7ID
          READ      CONTROLF,HUND30;*180,PTCNMRPN
.
RDCTRL99  RETURN
.------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          MOVE      SP70,TEMPLATE
          MOVE      SP70,NEXTPAGE
          MOVE      SP70,REDIRECT
          MOVE      SP70,UPDTTYPE
          MOVE      SP70,URNUMBER
          MOVE      SP70,ADMISSNO
          MOVE      SP70,AOUTCOME
          MOVE      ZERO,PREVADDR
          MOVE      ZERO,SAVEALIA
          MOVE      ZERO,WARCOUNT
          MOVE      ZERO,SAVEALPR
          MOVE      " 0",FILTATYP
          MOVE      SP70,FILTPATY
          MOVE      SP70,RETURNFL
          MOVE      SP70,FILTHOSP
          MOVE      SP70,FILTSPEC
          MOVE      SP70,FRSTDATE
          MOVE      SP70,LASTDATE
          MOVE      SP70,VIEWTYPE
          MOVE      SP70,VYEARMTH
          MOVE      SP70,FILTCLID
          MOVE      SP70,FILTLOCA
          MOVE      SP70,FILTCLTY
          MOVE      SP70,FILTCOMP
          MOVE      SP70,FILTADAT
          MOVE      SP70,FILTVDAT
          MOVE      ZERO,SHOWFLAG
          MOVE      SP70,NZPMICHG
.
          MOVE      ONE,OTDFLAG
          MOVE      ZERO,OTDWEND
.
          CALL      CPOTDE00
          CALL      CPOTDA00
.
CLRPAR99  RETURN
.------------------------------------------------------------
. Set Parameters
.------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updttype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDTTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nextpage=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NEXTPAGE
            GOTO      SETPAR99
          ENDIF 
.
          MATCH     "redirect=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REDIRECT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "otdem",QRYNAME
          IF        @EQUAL
            CALL      SPOTDE00            
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "otdan",QRYNAME
          IF        @EQUAL
            CALL      SPOTDA00            
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "urnumber=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,URNUMBER
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "admissno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ADMISSNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "aoutcome=",QRYNAME
          IF        @EQUAL
            PACK      AOUTCOME,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "opdcommt",QRYNAME
          IF        @EQUAL
            CALL      GETCOM00
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "prevaddr=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,PREVADDR
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "savealia=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SAVEALIA
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "savealpr=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SAVEALPR
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtatyp=",QRYNAME
          IF        @EQUAL
            PACK      FILTATYP,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtpaty=",QRYNAME
          IF        @EQUAL
            PACK      FILTPATY,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "returnfl=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,RETURNFL
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filthosp=",QRYNAME
          IF        @EQUAL
            PACK      FILTHOSP,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "frstdate=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FRSTDATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "lastdate=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,LASTDATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "viewtype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,VIEWTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "vyearmth=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,VYEARMTH
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtclid=",QRYNAME
          IF        @EQUAL
            PACK      FILTCLID,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtloca=",QRYNAME
          IF        @EQUAL
            PACK      FILTLOCA,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF

.
          MATCH     "filtclty=",QRYNAME
          IF        @EQUAL
            PACK      FILTCLTY,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtspec=",QRYNAME
          IF        @EQUAL
            PACK      FILTSPEC,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtcomp=",QRYNAME
          IF        @EQUAL
            PACK      FILTCOMP,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtadat=",QRYNAME
          IF        @EQUAL
            PACK      FILTADAT,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtvdat=",QRYNAME
          IF        @EQUAL
            PACK      FILTVDAT,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "showflag=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SHOWFLAG
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
.------------------------------------------------------------
. Outpatient Direct Patient Level
.------------------------------------------------------------
OPDP0000  MATCH     SP70,UPDTTYPE
          GOTO      OPDP7000 IF EQUAL
.
          MATCH     "A",UPDTTYPE            * Add
          GOTO      OPDP1000 IF EQUAL
.
          MATCH     "U",UPDTTYPE            * Update
          GOTO      OPDP2000 IF EQUAL
.
          MATCH     "D",UPDTTYPE            * Delete
          GOTO      OPDP3000 IF EQUAL
.
          MATCH     "C",UPDTTYPE            * Confirm PMI Update
          GOTO      OPDP4000 IF EQUAL
.
          GOTO      OPDP9000
.
OPDP1000  PACK      OTDEM002,CCC,CYY,CMM,CDD     * Set action date and time   
          REP       " 0",OTDEM002                * to current
          CLOCK     TIME,OTDEM003
. 
          PACK      KEY24,OTDEM001,OTDEM002,OTDEM003,SP70
          CALL      RDOTDEM1
          COMPARE   ZERO,OVRCD
          GOTO      OPDP9300 IF EQUAL
.
          CALL      CLOUTDEM                     * Clear file vars
          CALL      MVOTDM00                     * Move cgi to file vars
.
          MATCH     SP70,OTDMDVAN
          IF        !@EQUAL
            PACK      PMCDURNO,OTDMURNO,SP70       * Check DVA card number
            PACK      PMCDCTYP,OTDMDVCT,SP70
            PACK      PMCDCNUM,OTDMDVAN,SP70
            CALL      CHKCRD00                     * check concession card
            BRANCH    EXIT,OPDP9999
          ENDIF
.
          MATCH     SP70,OTDMSFNO
          IF        !@EQUAL
            PACK      PMCDURNO,OTDMURNO,SP70       * Check safety net
            PACK      PMCDCTYP,OTDMSFCT,SP70
            PACK      PMCDCNUM,OTDMSFNO,SP70
            CALL      CHKCRD00                     * check concession card
            BRANCH    EXIT,OPDP9999
          ENDIF
.
          PACK      OTDMCDAT,CCC,CYY,CMM,CDD    
          REP       " 0",OTDMCDAT                
          CLOCK     TIME,OTDMCTIM
          MOVE      USERID,OTDMCUID
.
          PACK      KEY24,OTDMURNO,OTDMADAT,OTDMATIM,SP70
          CALL      WROTDEM1                     * Add request
.
          MOVE      OTDMURNO,COMMURNO
          MOVE      OTDMADAT,COMMADAT
          MOVE      OTDMATIM,COMMATIM
          MOVE      " 0",COMMTYPE                * PMI update comment
          CALL      SAVCOM00                     * Update Comments
.
          CALL      CLOUTDAN
          MOVE      OTDMURNO,OTDNURNO
          MOVE      OTDMADAT,OTDNADAT
          MOVE      OTDMATIM,OTDNATIM
          MOVE      " 0",OTDNATYP                * PMI Update
          MOVE      OTDMCDAT,OTDNCDAT
          MOVE      OTDMCTIM,OTDNCTIM
          MOVE      OTDMCUID,OTDNCUID
.
          PACK      KEY34,OTDNURNO,OTDNADAT,OTDNATIM,OTDNATYP,SP70
          CALL      RAOTDAN1
          IF        OVRCD=1
            CALL      WROTDAN1                    * Write actions record
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      OPDP9999
.
OPDP2000  PACK      KEY24,OTDEM001,OTDEM002,OTDEM003,SP70
          CALL      RDOTDEM1
          BRANCH    OVRCD,OPDP9400
.
          CALL      MVOTDM00                     * Move cgi to file vars
.
          MATCH     SP70,OTDMDVAN
          IF        !@EQUAL
            PACK      PMCDURNO,OTDMURNO,SP70       * Check DVA card number
            PACK      PMCDCTYP,OTDMDVCT,SP70
            PACK      PMCDCNUM,OTDMDVAN,SP70
            CALL      CHKCRD00                     * check concession card
            BRANCH    EXIT,OPDP9999
          ENDIF
.
          MATCH     SP70,OTDMSFNO
          IF        !@EQUAL
            PACK      PMCDURNO,OTDMURNO,SP70       * Check safety net
            PACK      PMCDCTYP,OTDMSFCT,SP70
            PACK      PMCDCNUM,OTDMSFNO,SP70
            CALL      CHKCRD00                     * check concession card
            BRANCH    EXIT,OPDP9999
          ENDIF
.
          PACK      OTDMUDAT,CCC,CYY,CMM,CDD
          REP       " 0",OTDMUDAT
          CLOCK     TIME,OTDMUTIM
          MOVE      USERID,OTDMUUID
.
          CALL      UPOTDEM1                     * Update request
.
          MOVE      OTDMURNO,COMMURNO
          MOVE      OTDMADAT,COMMADAT
          MOVE      OTDMATIM,COMMATIM
          MOVE      " 0",COMMTYPE                * PMI update comment
          CALL      SAVCOM00                     * Update Comments
.
          MOVE      ZERO,EXIT
          GOTO      OPDP9999
.
OPDP3000  PACK      KEY24,OTDEM001,OTDEM002,OTDEM003,SP70
          CALL      RDOTDEM1
          BRANCH    OVRCD,OPDP9400
.
          CALL      DEOTDEM1                     * Delete request
.
          MOVE      OTDMURNO,COMMURNO
          MOVE      OTDMADAT,COMMADAT
          MOVE      OTDMATIM,COMMATIM
          MOVE      " 0",COMMTYPE                * PMI update comment
          CALL      DELCOM00                     * Delete request comments
.
          PACK      KEY34,OTDEM001,OTDEM002,OTDEM003,SP1,ZERO,SP70
          CALL      DEOTDAN1
.
          MOVE      ZERO,EXIT
          GOTO      OPDP9999
.
OPDP4000  PACK      KEY24,OTDEM001,OTDEM002,OTDEM003,SP70
          CALL      RDOTDEM1
          BRANCH    OVRCD,OPDP9400
.
          PACK      KEY34,OTDEM001,OTDEM002,OTDEM003,SP1,ZERO,SP70
          CALL      RDOTDAN1
          BRANCH    OVRCD,OPDP9400
.
          CALL      UPMAD000                    * Update PMI
          BRANCH    EXIT,OPDP9999
.
          MOVE      ONE,OTDNCOMP                * Set to completed
          MOVE      AOUTCOME,OTDNOUTC           * Save outcome 
.
          PACK      OTDNCOMD,CCC,CYY,CMM,CDD    * Set completed by
          REP       " 0",OTDNCOMD               
          CLOCK     TIME,OTDNCOMT
          MOVE      USERID,OTDNCOMU
.
          PACK      OTDNUDAT,CCC,CYY,CMM,CDD    * Set updated by
          REP       " 0",OTDNUDAT
          CLOCK     TIME,OTDNUTIM
          MOVE      USERID,OTDNUUID
.
          CALL      UPOTDAN1
.
          MOVE      ZERO,EXIT
          GOTO      OPDP9999

.
OPDP7000  MATCH     SP70,WBSEOPDU                * Not an OP direct user
          GOTO      OPDP9200 IF EQUAL
.
          PACK      KEY8,URNUMBER,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,OPDP9100
.
          CALL      RDPMPX21
          BRANCH    OVRCD,OPDP9100
.
          PACK      KEY24,OTDEM001,OTDEM002,OTDEM003,SP70
          CALL      RDOTDEM1
          IF        OVRCD=1
            CALL      CLOUTDEM
          ENDIF
.
          PACK      KEY34,OTDEM001,OTDEM002,OTDEM003,SP1,ZERO,SP70
          CALL      RDOTDAN1
          IF        OVRCD=1
            CALL      CLOUTDAN
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Outpatient Direct",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,OPDP9999
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      OPDP9999
.
OPDP9000  MOVE      "Invalid form action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      OPDP9999
.
OPDP9100  MOVE      "Invalid U/R number.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      OPDP9999
.
OPDP9200  MOVE      "Invalid outpatient direct user.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      OPDP9999
.
OPDP9300  MOVE      "Request already exists for this date and time.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      OPDP9999
.
OPDP9400  MOVE      "Invalid Request record.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      OPDP9999
.
OPDP9999  CLOSE     COMFILAF,DELETE
          RETURN
.------------------------------------------------------------
. Process Fields 
.------------------------------------------------------------
PROFLD00  BRANCH    REPORTNO,PROFLD10,PROFLD20,PROFLD30,PROFLD40
.
          GOTO      PROFLD99 
.
PROFLD10  BRANCH    FLDSETNO,PROFLD11,PROFLD12,PROFLD13
          GOTO      PROFLD99
.
PROFLD11  CALL      MASF0000                     * PMI details 
          GOTO      PROFLD99
.
PROFLD12  CALL      OTDM0000                     * OP direct demographics
          GOTO      PROFLD99
.
PROFLD13  CALL      GTAG0000                     * General OP direct tags
          GOTO      PROFLD99
.
PROFLD20  BRANCH    FLDSETNO,PROFLD21,PROFLD22,PROFLD23
          GOTO      PROFLD99
.
PROFLD21  CALL      MASF0000                     * PMI details 
          GOTO      PROFLD99
.
PROFLD22  CALL      OTDN0000                     * OP direct action tags
          GOTO      PROFLD99
.
PROFLD23  CALL      GTAG0000                     * General OP direct tags
          GOTO      PROFLD99
.
PROFLD30  BRANCH    FLDSETNO,PROFLD31,PROFLD32,PROFLD33
          GOTO      PROFLD99
.
PROFLD31  CALL      MASF0000                     * PMI details
          GOTO      PROFLD99
.
PROFLD32  CALL      GTAG0000                     * General OP direct tags
          GOTO      PROFLD99
.
PROFLD33  CALL      OTDN0000                     * OP direct action tags
          GOTO      PROFLD99
.
PROFLD40  BRANCH    FLDSETNO,PROFLD41
          GOTO      PROFLD99
.
PROFLD41  CALL      GTAG0000                     * General OP direct tags
          GOTO      PROFLD99
.
PROFLD99  RETURN
.
.------------------------------------------------------------
. General OP direct tags
.------------------------------------------------------------
GTAG0000  BRANCH    FLDITMNO,GTAG0100,GTAG0200,GTAG0300,GTAG0400,GTAG0500:
                             GTAG0600,GTAG0700,GTAG0800,GTAG0900,GTAG1000:
                             GTAG1100,GTAG1200,GTAG1300,GTAG1400,GTAG1500:
                             GTAG1600,GTAG1700,GTAG1800,GTAG1900,GTAG2000:
                             GTAG2100,GTAG2200,GTAG2300,GTAG2400,GTAG2500:
                             GTAG2600,GTAG2700,GTAG2800,GTAG2900,GTAG3000:
                             GTAG3100,GTAG3200,GTAG3300
.
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      GTAG9999
.
GTAG0100  CALL      PMIT0000                * Requested PMI Update table
          GOTO      GTAG9999
.
GTAG0200  UNPACK    DIM127,D1,KEY2,DIM127
          MOVE      KEY2,COMMTYPE
          MOVE      OTDMURNO,COMMURNO       * Request comments
          MOVE      OTDMADAT,COMMADAT
          MOVE      OTDMATIM,COMMATIM
.
          CALL      DISCOM00                * Display comments
          GOTO      GTAG9999
.
GTAG0300  UNPACK    DIM127,D1,KEY1,DIM127
          UNPACK    DIM127,D1,KEY1A,DIM127
.
          PACK      KEY5,CATTC,SP70
          CALL      RDSCODE1
GTAG0350  CALL      RDKCODE1
          BRANCH    OVRCD,GTAG9999
.
          MATCH     CATTC,TCODE
          GOTO      GTAG9999 IF NOT EQUAL
.
          MATCH     KEY1,TCINDC1
          GOTO      GTAG0350 IF NOT EQUAL
.
          MATCH     "I",PTCOACTV
          GOTO      GTAG0350 IF EQUAL
.
          MATCH     "2",TCINDC3
          GOTO      GTAG0350 IF EQUAL
.
          MATCH     "0",KEY1A
          IF        @EQUAL
            WRITE     HTMLFILE;TDESC;
          ELSE
            WRITE     HTMLFILE;ACODE;
          ENDIF
          GOTO      GTAG9999
.
GTAG0400  MOVE      "FZ",CATEGORY
          MOVE      AOUTCOME,CODE
          CALL      CODITM00
          GOTO      GTAG9999
.
GTAG0500  WRITE     HTMLFILE;OTDNCOMP;
          GOTO      GTAG9999
.
GTAG0600  MATCH     SP70,OTDMSNAM
          IF        !@EQUAL
            MATCH     OTDMSNAM,PSNAME
            GOTO      GTAG0610 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,OTDMFGNM
          IF        !@EQUAL
            MATCH     OTDMFGNM,PMPXFNAM
            GOTO      GTAG0610 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,OTDMSGNM
          IF        !@EQUAL
            MATCH     OTDMSGNM,PMPXSNAM
            GOTO      GTAG0610 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,OTDMGEND
          IF        !@EQUAL
            MATCH     PSEX,OTDMGEND
            GOTO      GTAG0610 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,OTDMBDAT
          IF        !@EQUAL
            MATCH     PBDATE,OTDMBDAT
            GOTO      GTAG0610 IF NOT EQUAL
          ENDIF
.
          WRITE     HTMLFILE;ZERO;
          GOTO      GTAG9999
.
GTAG0610  WRITE     HTMLFILE;ONE;           * Ask alias update question
          GOTO      GTAG9999
.
GTAG0700  MATCH     SP70,OTDMADD1
          IF        !@EQUAL
            MATCH     PADD1,OTDMADD1
            GOTO      GTAG0710 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,OTDMADD2
          IF        !@EQUAL
            MATCH     PADD2,OTDMADD2
            GOTO      GTAG0710 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,OTDMADD3
          IF        !@EQUAL
            MATCH     PSUBRB,OTDMADD3
            GOTO      GTAG0710 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,OTDMADD4
          IF        !@EQUAL
            MATCH     "8888",OTDMPOST
            IF        @EQUAL
              PACK      KEY5,ANSC,SP1,OTDMADD4,SP70
              CALL      RDCODE1
              IF        OVRCD=1
                MOVE      OTDMADD4,TDESC
              ENDIF
              MATCH     PADD4,TDESC
              GOTO      GTAG0710 IF NOT EQUAL
            ELSE
              MATCH     PADD4,OTDMADD4
              GOTO      GTAG0710 IF NOT EQUAL
            ENDIF
          ENDIF
.
          MATCH     SP70,OTDMPOST
          IF        !@EQUAL
            MATCH     PPOST,OTDMPOST
            GOTO      GTAG0710 IF NOT EQUAL
          ENDIF
.
          WRITE     HTMLFILE;ZERO;
          GOTO      GTAG9999
.
GTAG0710  WRITE     HTMLFILE;ONE;           * Ask prev address update question
          GOTO      GTAG9999
.
GTAG0800  UNPACK    DIM127,D1,KEY1,DIM127
          UNPACK    DIM127,D1,KEY1A,DIM127
.
          PACK      KEY5,CATCT,SP70
          CALL      RDSCODE1
GTAG0850  CALL      RDKCODE1
          BRANCH    OVRCD,GTAG9999
.
          MATCH     CATCT,TCODE
          GOTO      GTAG9999 IF NOT EQUAL
.
          MATCH     KEY1,TCINDC1
          GOTO      GTAG0850 IF NOT EQUAL
.
          MATCH     "I",PTCOACTV
          GOTO      GTAG0850 IF EQUAL
.
          MATCH     "0",KEY1A
          IF        @EQUAL
            WRITE     HTMLFILE;TDESC;
          ELSE
            WRITE     HTMLFILE;ACODE;
          ENDIF
          GOTO      GTAG9999
.
GTAG0900  PROC      CHALI000                 * Check alias indicator
          GOTO      GTAG9999
.
GTAG1000  UNPACK    DIM127,D1,KEY1,DIM127
          UNPACK    DIM127,D1,KEY1A,DIM127
.
          CALL      DCRD0000                 * Display concession card
          GOTO      GTAG9999
.
GTAG1100  UNPACK    DIM127,D1,KEY1,DIM127
          UNPACK    DIM127,D1,KEY2A,DIM127
.
          CALL      DEXC0000                 * Display extra contact details
          GOTO      GTAG9999
.
GTAG1200  CALL      APMT0000                 * Appointment message table
          GOTO      GTAG9999
.
GTAG1300  UNPACK    DIM127,D1,KEY2,DIM127
          MOVE      KEY2,COMMTYPE
          MOVE      OTDNURNO,COMMURNO        * Action Comments
          MOVE      OTDNADAT,COMMADAT
          MOVE      OTDNATIM,COMMATIM
.
          CALL      DISCOM00                 * Display comments
          GOTO      GTAG9999
.
GTAG1400  WRITE     HTMLFILE;FILTATYP;
          GOTO      GTAG9999
.
GTAG1500  CALL      ACTT0000                * PMI OPD Action list
          GOTO      GTAG9999
.
GTAG1600  MOVE      OTDNURNO,COMMURNO
          MOVE      OTDNADAT,COMMADAT
          MOVE      OTDNATIM,COMMATIM
          MOVE      OTDNATYP,COMMTYPE
.
          CALL      DISCOM00                * Display comments
          GOTO      GTAG9999
.
GTAG1700  CALL      DVIS0000                * Display visit details 
          GOTO      GTAG9999
.
GTAG1800  WRITE     HTMLFILE;RETURNFL;
          GOTO      GTAG9999
.
GTAG1900  MATCH     SP70,FILTHOSP
          IF        @EQUAL
            MOVE      WBSEHOSP,FILTHOSP
          ENDIF
          MOVE      FILTHOSP,SELECHOS
          CALL      HSEL0000                * Hospital selection list
          GOTO      GTAG9999
.
GTAG2000  WRITE     HTMLFILE;VIEWTYPE;
          GOTO      GTAG9999
.
GTAG2100  CALL      SELV0000                
          GOTO      GTAG9999
.
GTAG2200  CALL      NEXT0000                
          GOTO      GTAG9999
.
GTAG2300  CALL      PREV0000                
          GOTO      GTAG9999
.
GTAG2400  WRITE     HTMLFILE;FILTCLID;
          GOTO      GTAG9999
.
GTAG2500  WRITE     HTMLFILE;FILTCLTY;
          GOTO      GTAG9999
.
GTAG2600  MOVE      "SA",CATEGORY
          MOVE      FILTSPEC,CODE
          CALL      CODITM00
          GOTO      GTAG9999
.
GTAG2700  WRITE     HTMLFILE;FILTCOMP;
          GOTO      GTAG9999
.
GTAG2800  CALL      HATB0000                * Hosital OPD Action list
          GOTO      GTAG9999
.
GTAG2900  WRITE     HTMLFILE;FILTADAT;
          GOTO      GTAG9999
.
GTAG3000  WRITE     HTMLFILE;FILTVDAT;
          GOTO      GTAG9999
.
GTAG3100  CALL      APPD0000                * Get appointment date for visit
          GOTO      GTAG9999
.
GTAG3200  WRITE     HTMLFILE;FILTPATY;
          GOTO      GTAG9999
.
GTAG3300  WRITE     HTMLFILE;FILTLOCA;
          GOTO      GTAG9999
.
GTAG9999  RETURN
.
.------------------------------------------------------------
. Display DVA and safety net concession card details
.------------------------------------------------------------
DCRD0000  PACK      KEY5,CATCT,SP70
          CALL      RDSCODE1
DCRD1050  CALL      RDKCODE1
          BRANCH    OVRCD,DCRD9999
.
          MATCH     CATCT,TCODE
          GOTO      DCRD9999 IF NOT EQUAL
.
          MATCH     KEY1,TCINDC1
          GOTO      DCRD1050 IF NOT EQUAL
.
          MATCH     "I",PTCOACTV
          GOTO      DCRD1050 IF EQUAL
.
          PACK      KEY19,PURNO,SP70
          CALL      RSPMCCD1
DCRD1060  CALL      RKPMCCD1
          BRANCH    OVRCD,DCRD9999
.
          MATCH     PURNO,PMCDURNO
          GOTO      DCRD9999 IF NOT EQUAL
.
          MATCH     ACODE,PMCDCTYP
          GOTO      DCRD1060 IF NOT EQUAL
.
          MATCH     "0",KEY1A
          IF        @EQUAL
            WRITE     HTMLFILE;PMCDCNUM;
            GOTO      DCRD9999
          ENDIF
.
          MATCH     "1",KEY1A
          IF        @EQUAL
            UNPACK    PMCDEXDT,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
            GOTO      DCRD9999
          ENDIF
.
          MATCH     "2",KEY1A
          IF        @EQUAL
            WRITE     HTMLFILE;PMCDDVAC;
            GOTO      DCRD9999
          ENDIF
.
DCRD9999  RETURN
.
.------------------------------------------------------------
. Display post code and NOK extra contact details
.------------------------------------------------------------
DEXC0000  PACK      KEY5,CATTC,SP70
          CALL      RDSCODE1
DEXC1050  CALL      RDKCODE1
          BRANCH    OVRCD,DEXC9999
.
          MATCH     CATTC,TCODE
          GOTO      DEXC9999 IF NOT EQUAL
.
          MATCH     KEY1,TCINDC1
          GOTO      DEXC1050 IF NOT EQUAL
.
          MATCH     "I",PTCOACTV
          GOTO      DEXC1050 IF EQUAL
.
          MATCH     "2",TCINDC3
          GOTO      DEXC1050 IF EQUAL
.
          PACK      KEY14,PURNO,ACODE,SP70
          CALL      RSPMCEX1
DEXC1080  CALL      RKPMCEX1
          BRANCH    OVRCD,DEXC9999
.
          MATCH     PURNO,PMCEURNO          * Correct U/R
          GOTO      DEXC9999 IF NOT EQUAL
.
          MATCH     ACODE,PMCETYPE          * Correct type
          GOTO      DEXC9999 IF NOT EQUAL
.
          MATCH     "1",PMCEACTV            * Skip if inactive
          GOTO      DEXC1080 IF EQUAL
.
          MOVE      ZERO,F2
          MOVE      KEY2A,F2
          BRANCH    F2,DEXC2000,DEXC2010,DEXC2020,DEXC2030,DEXC2040:
                       DEXC2050,DEXC2060,DEXC2070,DEXC2080,DEXC2090:
                       DEXC2100,DEXC2110,DEXC2120,DEXC2130
          GOTO      DEXC9999
.
DEXC2000  WRITE     HTMLFILE;PMCETITL;
          GOTO      DEXC9999
.
DEXC2010  WRITE     HTMLFILE;PMCESNAM;
          GOTO      DEXC9999
.
DEXC2020  WRITE     HTMLFILE;PMCEGNAM;
          GOTO      DEXC9999
.
DEXC2030  WRITE     HTMLFILE;PMCEGNM2;
          GOTO      DEXC9999
.
DEXC2040  WRITE     HTMLFILE;PMCEADD1;
          GOTO      DEXC9999
.
DEXC2050  WRITE     HTMLFILE;PMCEADD2;
          GOTO      DEXC9999
.
DEXC2060  WRITE     HTMLFILE;PMCEADD3;
          GOTO      DEXC9999
.
DEXC2070  WRITE     HTMLFILE;PMCEADD4;
          GOTO      DEXC9999
.
DEXC2080  WRITE     HTMLFILE;PMCEPOST;
          GOTO      DEXC9999
.
DEXC2090  WRITE     HTMLFILE;PMCETELP;
          GOTO      DEXC9999
.
DEXC2100  WRITE     HTMLFILE;PMCETELB;
          GOTO      DEXC9999
.
DEXC2110  WRITE     HTMLFILE;PMCETELM;
          GOTO      DEXC9999
.
DEXC2120  WRITE     HTMLFILE;PMCERELP;
          GOTO      DEXC9999
.
DEXC2130  WRITE     HTMLFILE;PMCEEMAI;
          GOTO      DEXC9999
.
DEXC9999  RETURN
.
.------------------------------------------------------------
. Patient level list requested pmi udpate
.------------------------------------------------------------
PMIT0000  PACK      KEY24,URNUMBER,SP70
          CALL      RSOTDEM1
PMIT1000  CALL      RKOTDEM1
          BRANCH    OVRCD,PMIT9999
.
          MATCH     URNUMBER,OTDMURNO
          GOTO      PMIT9999 IF NOT EQUAL
.
          PACK      COMMLIN1,SP70,SP70
          PACK      COMMLIN2,SP70,SP70
          PACK      COMMLIN3,SP70,SP70
.
          PACK      KEY29,OTDMURNO,OTDMADAT,OTDMATIM,SP1,ZERO,SP70
          CALL      RSOTDCO1
PMIT1500  CALL      RKOTDCO1
          BRANCH    OVRCD,PMIT2000
.
          MATCH     OTDMURNO,OTDOURNO
          GOTO      PMIT2000 IF NOT EQUAL
.
          MATCH     OTDMADAT,OTDOADAT
          GOTO      PMIT2000 IF NOT EQUAL
.
          MATCH     OTDMATIM,OTDOATIM
          GOTO      PMIT2000 IF NOT EQUAL
.
          MATCH     " 0",OTDOATYP
          GOTO      PMIT2000 IF NOT EQUAL
.
          MATCH     SP70,COMMLIN1
          IF        @EQUAL
            MOVE      OTDOCOMM,COMMLIN1
            GOTO      PMIT1500
          ENDIF
.
          MATCH     SP70,COMMLIN2
          IF        @EQUAL
            MOVE      OTDOCOMM,COMMLIN2
            GOTO      PMIT1500
          ENDIF
.
          MATCH     SP70,COMMLIN3
          IF        @EQUAL
            MOVE      OTDOCOMM,COMMLIN3
          ENDIF
.
PMIT2000  MOVE      OTDMCUID,DESCCUID
          PACK      KEY10,OTDMCUID,SP70
          CALL      RDWBSE1
          IF        OVRCD<>1
            MOVE      WBSENAM,DESCCUID
          ENDIF
.
          MOVE      OTDMUUID,DESCUUID
          PACK      KEY10,OTDMUUID,SP70
          CALL      RDWBSE1
          IF        OVRCD<>1
            MOVE      WBSENAM,DESCUUID
          ENDIF
.
          UNPACK    OTDMADAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DISPDAT1,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          PACK      KEY34,OTDMURNO,OTDMADAT,OTDMATIM,SP1,ZERO,SP70
          CALL      RDOTDAN1
          IF        OVRCD=1
            CALL      CLOUTDAN
          ENDIF
.
          MOVE      SP70,DESCCOMP
          MATCH     "1",OTDNCOMP
          IF        @EQUAL
            MOVE      DYES,DESCCOMP
          ENDIF
.
          MOVE      SP70,DESCOUTC
          MATCH     SP70,OTDNOUTC
          IF        !@EQUAL
            PACK      KEY5,ANSF,ANSZ,OTDNOUTC,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      OTDNOUTC,TDESC
            ENDIF
            MOVE      TDESC,DESCOUTC
          ENDIF
.
          CLEAR     HTMLLINK
          APPEND    "javascript:UpdateRequest('",HTMLLINK
          APPEND    OTDMURNO,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    DISPDAT1,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    OTDMATIM,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
          REP       "#"'",DESCCUID
          REP       "#"'",COMMLIN1
          REP       "#"'",COMMLIN2
          REP       "#"'",COMMLIN3
          REP       "#"'",DESCOUTC
.
          WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":           * 0
                             "#"",OTDMURNO,"#",":                    * 1
                             "#"",OTDMADAT,OTDMATIM,"#",":           * 2
                             "#"",OTDMUDAT,OTDMUTIM,"#",":           * 3
                             "#"",DESCUUID,"#",":                    * 4
                             "#"",OTDMCDAT,OTDMCTIM,"#",":           * 5
                             "#"",DESCCUID,"#",":                    * 6
                             "#"",COMMLIN1,COMMLIN2,COMMLIN3,"#",":  * 7
                             "#"",OTDNCOMD,OTDNCOMT,"#",":           * 8
                             "#"",DESCCOMP,"#",":                    * 9 
                             "#"",DESCOUTC,"#");"                    * 10
.
          GOTO      PMIT1000
.
PMIT9999  RETURN
.
.*******************************************************************************
. Get comments                                     
.*******************************************************************************
GETCOM00  PREP      COMFILAF,COMFILNM
          MOVE      ZERO,OTDFLAG
          MOVE      ONE,F3
          WRITE     COMFILAF,SEQ;QRYDATA
.
GETCOM10  READ      CONFFILE,SEQ;QRYNAME,QRYDATA
          GOTO      GETCOM90 IF OVER
          MATCH     SP70,QRYNAME
          GOTO      GETCOM80 IF NOT EQUAL
          ADD       ONE,F3
          WRITE     COMFILAF,SEQ;QRYDATA
          GOTO      GETCOM10
.
GETCOM80  MOVE      ONE,TEXTAREA                 * Flag for Next CGI Parameter
GETCOM90  MOVE      F3,OTDWEND
          OPEN      COMFILAF,COMFILNM
GETCOM99  RETURN
.
.*******************************************************************************
. Update comments                                
.*******************************************************************************
SAVCOM00  BRANCH    OTDFLAG,SAVCOM90
.
          PACK      KEY29,COMMURNO,COMMADAT,COMMATIM,COMMTYPE,SP70
          CALL      RSOTDCO1
SAVCOM10  CALL      RKOTDCO1
          BRANCH    OVRCD,SAVCOM20
.
          MATCH     COMMURNO,OTDOURNO
          GOTO      SAVCOM20 IF NOT EQUAL
.
          MATCH     COMMADAT,OTDOADAT
          GOTO      SAVCOM20 IF NOT EQUAL
.
          MATCH     COMMATIM,OTDOATIM
          GOTO      SAVCOM20 IF NOT EQUAL
.
          MATCH     COMMTYPE,OTDOATYP
          GOTO      SAVCOM20 IF NOT EQUAL
.
          PACK      KEY29,OTDOURNO,OTDOADAT,OTDOATIM,OTDOATYP,OTDOCLNO,SP70
          CALL      DEOTDCO1
          CALL      RSOTDCO1
          GOTO      SAVCOM10
.
SAVCOM20  COMPARE   ZERO,OTDWEND
          GOTO      SAVCOM99 IF EQUAL
.
          MOVE      ZERO,F3
SAVCOM30  READ      COMFILAF,SEQ;COMM127
          GOTO      SAVCOM90 IF OVER
.
          ADD       ONE,F3
          MOVE      COMMURNO,OTDOURNO
          MOVE      COMMADAT,OTDOADAT
          MOVE      COMMATIM,OTDOATIM
          MOVE      COMMTYPE,OTDOATYP
          MOVE      F3,OTDOCLNO
.
          PACK      KEY29,OTDOURNO,OTDOADAT,OTDOATIM,OTDOATYP,OTDOCLNO,SP70
          CALL      RDOTDCO1
          IF        OVRCD=1
            MOVE      COMM127,OTDOCOMM
            CALL      WROTDCO1
          ENDIF
.
          COMPARE   F3,OTDWEND
          GOTO      SAVCOM30 IF NOT EQUAL
.
SAVCOM90  OPEN      COMFILAF,COMFILNM  * To reset position back to the start
SAVCOM99  RETURN
.
.*******************************************************************************
. Display comments                               
.*******************************************************************************
DISCOM00  PACK      KEY29,COMMURNO,COMMADAT,COMMATIM,COMMTYPE,SP70
          CALL      RAOTDCO1
DISCOM10  CALL      RKOTDCO1
          BRANCH    OVRCD,DISCOM99
.
          MATCH     COMMURNO,OTDOURNO
          GOTO      DISCOM99 IF NOT EQUAL
.
          MATCH     COMMADAT,OTDOADAT
          GOTO      DISCOM99 IF NOT EQUAL
.
          MATCH     COMMATIM,OTDOATIM
          GOTO      DISCOM99 IF NOT EQUAL
.
          MATCH     COMMTYPE,COMMTYPE
          GOTO      DISCOM99 IF NOT EQUAL
.
          STRIP     OTDOCOMM
          MOVELPTR  OTDOCOMM,F3
          IF        F3>0
            SFORMAT   VAR,F3
          ELSE
            SFORMAT   VAR,1
          ENDIF
          MOVE      OTDOCOMM,VAR
          WRITE     HTMLFILE;VAR
          SFORMAT   VAR,127
          GOTO      DISCOM10
.
DISCOM99  RETURN
.
.*******************************************************************************
. Delete request comments
.*******************************************************************************
DELCOM00  PACK      KEY29,COMMURNO,COMMADAT,COMMATIM,COMMTYPE,SP70
          CALL      RSOTDCO1
DELCOM10  CALL      RKOTDCO1
          BRANCH    OVRCD,DELCOM99
.
          MATCH     COMMURNO,OTDOURNO
          GOTO      DELCOM99 IF NOT EQUAL
.
          MATCH     COMMADAT,OTDOADAT
          GOTO      DELCOM99 IF NOT EQUAL
.
          MATCH     COMMATIM,OTDOATIM
          GOTO      DELCOM99 IF NOT EQUAL
.
          MATCH     COMMTYPE,OTDOATYP
          GOTO      DELCOM99 IF NOT EQUAL
.
          PACK      KEY29,OTDOURNO,OTDOADAT,OTDOATIM,OTDOATYP,OTDOCLNO,SP70
          CALL      DEOTDCO1
          CALL      RSOTDCO1
          GOTO      DELCOM10
.
DELCOM99  RETURN
.
.------------------------------------------------------------
. Update PMI master file
.------------------------------------------------------------
+
.------------------------------------------------------------
. UPMAD000 - Update Master Details
. Returns:   EXIT  1 = Error
.                  2 = Ok to continue with warning
.------------------------------------------------------------
.
UPMAD000  REP       "+ ",ADMISSNO
          REP       "+ ",URNUMBER
.         REP       "+ ",REDIRECT
          MOVE      ZEROUR,PURNO
          MOVE      ZERO,NZIBVNEW           * clear NMPI sel after search flag
          CALL      CLPATMAS                * clear patmasfd vars
          CALL      CLPMSPX2                * clear mast ext2 details
          MOVE      SP20,NMPNUMB            * clear NMPI number
          CALL      ZKIN0000
.
          CALL      VALUR000                * Validate U/R Number
          BRANCH    EXIT,UPMAD996,UPMAD999
.
          CALL      RDSURN00                * Readin the surname index record
          MOVE      "0",OLDCEASE            * reset Deceased indicator
.
          MOVE      PURNO,KEY8
          CALL      RLPTMAS1                * record found ?
          BRANCH    OVRCD,UPMAD994:         * no
                          UPMAD991          * yes, but already locked
.
          PACK      KEY8,PURNO
          CALL      RDPMPX21
          BRANCH    OVRCD,UPMAD993
.
          MOVE      PMPXPEML,SPPEML         * save email for prev adress check
.
          MOVE      PCEASE,OLDCEASE
          CALL      VALADM00                * check birth date to other adms.
          BRANCH    EXIT,UPMAD996           * if age less than 90 days
.
          CALL      AMSV0000                * save master file variables
          CALL      SPRS0000                * save PRS2 PMI trigger variables
.                                           * save Interpreter/Language vars
          MOVE      PMPXINTR,SVPXINTR                                  *I-40489
          MOVE      PMPXLNG1,SVPXLNG1                                  *I-40489
          MOVE      PFUNDH,SAVPFUND         * Save hfund for expected payor
.
          CALL      UPDMAS00                * Move mast cgi param into mast vars
.
          CALL      IBACLOCK
          PACK      XDATE,CCC,CYY,CMM,CDD
          REP       " 0",XDATE
.
          MATCH     PDECDTE,XDATE
          GOTO      UPMAD995 IF LESS
.
          CALL      PX2U0000                * Move mast ext2 parm into file vars
          CALL      ALPR0000
.
          CALL      VLSEX000                * Validate sex. Can only be Indeter
          BRANCH    EXIT,UPMAD996           * if age less than 90 days
.
          IF        PREVADDR=1
            CALL      UPAD0000              * update previous address file
          ENDIF
.
          IF        PCEASE=0
            MOVE      SP8,PDECDTE           * Clear Deceased Date
            MOVE      SP10,PTMADCNO         * clear death cert.
            MOVE      ZERO,PAUTOPY          * no autopsy
          ELSE
            MATCH     "1",PTCNDEES
            IF        @EQUAL
              MATCH     SP70,ADMISSNO
              GOTO      UPMAD050 IF EQUAL
              GOTO      UPMAD050 IF EOS
.
              PACK      KEY8,ADMISSNO,SP70
              CALL      RDPTRES1
              IF        OVRCD=1
UPMAD050        CALL      CLRRES00
              ELSE
                MOVE      SP1,ANS
                MOVE      PGNAME,ANS
                PACK      PKNAME,DLCHAR,SP1,ANS,DOT,PTMASNAM,SP70
                CALL      UPPTRES1
                CALL      TACC0000                   * send HL7 message
              ENDIF
            ENDIF
          ENDIF
.
          MOVE      THREE,URSYST
          MOVE      PURNO,GSRURNO           * Ensure GSRURNO is set correctly
          CALL      UPCOMCRD                * Update Community Card
.
          CALL      ECON0000                * Update extra contact details
.
          CALL      UPDUR
          BRANCH    EXIT,UPMAD996
.
          CALL      WRUPD000  * write to demographics updated table
.
          CALL      PNMP0000                * post NMPI details if needed
.
          CALL      DEAPOL00                * Update the Death Polling Table
.
          RESET     PRGID                   * Reset PRGID after scan for WEB
.
          CALL      UPPMI000                * Update PMI Extension file
.
          CALL      UUPTMAS1
.
          CALL      PRS2T000                * write any PRS2 PMI detail changes
          CALL      WESIS000                * write any ESIS PMI detail changes
.
          CALL      BEDN0000                * Update bed board/expected patient
.
          CALL      UCCD0000                * Update concession card details
.
          CALL      ALIA0000                * Write new alias records
          MOVE      ZERO,EXIT
          GOTO      UPMAD999
.
UPMAD991  CLEAR     WEBTITLE
          APPEND    "Patient U/R ",WEBTITLE
          APPEND    URNUMBER,WEBTITLE
          APPEND    " is Locked on port ",WEBTITLE
          APPEND    PPORT,WEBTITLE
          RESET     WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      UPMAD999
.
UPMAD993  CLEAR     WEBTITLE
          APPEND    "Patient U/R ",WEBTITLE
          APPEND    URNUMBER,WEBTITLE
          APPEND    " missing extension file.",WEBTITLE
          RESET     WEBTITLE
          CALL      WEBERR00
          CALL      UUPTMAS1
          MOVE      ONE,EXIT
          GOTO      UPMAD999
.
UPMAD994  CLEAR     WEBTITLE
          APPEND    "Patient U/R ",WEBTITLE
          APPEND    URNUMBER,WEBTITLE
          APPEND    " missing from master file.",WEBTITLE
          RESET     WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      UPMAD999
.
UPMAD995  CLEAR     WEBTITLE
          APPEND    "Patient cannot ",WEBTITLE
          APPEND    " be deceased in the future.",WEBTITLE
          RESET     WEBTITLE
          CALL      WEBERR00
UPMAD996  CALL      UUPTMAS1
          MOVE      ONE,EXIT
          GOTO      UPMAD999
.
UPMAD999  RETURN
.
.------------------------------------------------------------
. WEBWAR00 - Display warnings and redirect parent 
.------------------------------------------------------------
WEBWAR00  CALL      OPENHTML
          BRANCH    EXIT,WEBWAR95
.
          WRITE     HTMLFILE;"<script type=#"text/javascript#" ":
                             "src=#"../js/Standard.js#"></script>":
                             "<script type=#"text/javascript#" ":
                             "src=#"../js/Custom.js#"></script>":
                             "<script type=#"text/javascript#"> "
.
          COMPARE   ZERO,WARCOUNT
          GOTO      WEBWAR20 IF EQUAL
.
          MOVE      ONE,COUNTER
WEBWAR10  WRITE     HTMLFILE;"    alert(#"",WEBWARNG[COUNTER],"#");",012
          ADD       ONE,COUNTER
          COMPARE   COUNTER,WARCOUNT
          GOTO      WEBWAR20 IF LESS
          GOTO      WEBWAR10
.
WEBWAR20  PACK      REDIRECT,REDIRECT,SP70
          REP       " +",REDIRECT
          WRITE     HTMLFILE;"parent.location.href=#"",REDIRECT,"#"":
                             "</script>"
          REP       "+ ",REDIRECT
          CALL      CLOSHTML
          GOTO      WEBWAR99
.
WEBWAR95  DISPLAY   "*** Fifo Not Found ***"
.
WEBWAR99  RETURN
.
+
.------------------------------------------------------------
. Validate the U/R - from routine KURN
. Returns:   EXIT  0 = Ok to continue
.                  1 = Error
.                  2 = Ok to continue with warning
.------------------------------------------------------------
.
VALUR000  MOVE      URNUMBER,PURNO          * Move to the master file variable
.
          MOVE      PURNO,KEY8
          CALL      RDMAST1                 * Validate U/R number against PMI
          COMPARE   ZERO,OVRCD
          GOTO      VALUR030 IF EQUAL       * Valid UR so Continue
.
          MOVE      ZERO,OVRCD
          COMPARE   ZERO,PTCNNMPI
          PROC      VALURNMP IF NOT EQUAL   * check if on patnmpaf
          BRANCH    OVRCD,VALUR990,VALUR990
.
. Check if this is an associated U/R number
.-------------------------------------------
VALUR030  MOVE      PURNO,KEY8
          CALL      RDPMPX21
          BRANCH    OVRCD,VALUR990
.
          COMPARE   ONE,PSTAT               * Is this an associated U/R number ?
          GOTO      VALUR900 IF NOT EQUAL   * No
.
          CALL      ASSURN00                * Yes, Get Associated UR Details
          BRANCH    EXIT,VALUR991
.
          GOTO      VALUR995
.
VALUR900  MOVE      ZERO,EXIT
          GOTO      VALUR999
.
VALUR990  CLEAR     WEBTITLE
          APPEND    "Invalid U/R Number - ",WEBTITLE
          APPEND    URNUMBER,WEBTITLE
          RESET     WEBTITLE
          CALL      WEBERR00
VALUR991  MOVE      ONE,EXIT
          GOTO      VALUR999
.
VALUR995  CLEAR     WEBTITLE
          APPEND    "This U/R Number is merged with ",WEBTITLE
          APPEND    PURNO,WEBTITLE
          RESET     WEBTITLE
          ADD       ONE,WARCOUNT
          MOVE      WEBTITLE,WEBWARNG[WARCOUNT]
          MOVE      TWO,EXIT
          GOTO      VALUR999
.
VALUR999  RETURN
.
.------------------------------------------------------------
. ASSURN00 : We have an associated U/R number.
.            Now find the correct U/R number.
. From PATCOMN1
. Returns:    EXIT  0 = Ok to continue
.                   1 = Error
.----------------------------------------------------------------------
.
ASSURN00  MOVE      PPSNAME,CPPURNO     * This is where the new U/R is hidden
.
.         We have to check this new U/R number, just in case it is also
.         associated to another U/R number. eg. A->B->C->D->...->H
.
          MOVE      CPPURNO,KEY8        * Key for new U/R number
          CALL      RDMAST1             * Read in the new PMI record
          BRANCH    OVRCD,ASSURN91      * New U/R number missing
          BRANCH    PSTAT,ASSURN00      * Another associated U/R number
.
.         We have the correct U/R number
.
          MOVE      ZERO,EXIT
          GOTO      ASSURN99            * Return the U/R
.
.         Error in the data files. Tell the user, and ask for another U/R
.
ASSURN91  CLEAR     WEBTITLE
          APPEND    "Error: U/R merged with ",WEBTITLE
          APPEND    CPPURNO,WEBTITLE
          APPEND    " which is missing. ",WEBTITLE
          RESET     WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
.
ASSURN99  RETURN
.
+
.------------------------------------------------------------
. Readin the surname index record
.------------------------------------------------------------
.
RDSURN00  MOVE      SP1,VCASE
          LOAD      VCASE,PCASE,CCNOTES
.
RDSURN20  CALL      NEWS0000                * using new soundex file
.
RDSURN99  RETURN
.
.------------------------------------------------------------
.                          NEWS0000
.         maintenance of the double soundex surname file
.------------------------------------------------------------
.
NEWS0000  MOVE      PTMASNAM,GSRSNAM
          MOVE      PMPXFNAM,GSRGNAM
          MOVE      PMPXSNAM,PTGSGNM2
          MOVE      PBDATE,GSRDOB            * Date of birth
          MOVE      PSEX,GSRSEX              * Sex
          MOVE      PURNO,GSRURNO
          MOVE      ZEROVISN,GSRBILL
          MOVE      ZERO,GSRSYS
          CALL      SOUNDEX                  * get soundex key for surname
          CALL      SOUNDX2                  * get soundex key for given names
.
          PACK      KEY115,GSRURNO,GSRBILL,GSRSNAM,GSRGNAM,PTGSGNM2,GSRDOB:
                           GSRSEX
          CALL      RDPTGSR1                 * check if its already there
          IF        OVRCD = 1
            IF        SAVEALIA=1
              CALL      WRPTGSR1
            ENDIF
          ENDIF
.
NEWS9999  RETURN
+
.-------------------------------------------------------------
. Validate sex. Can only be (I)ndeter if age less than 90 days
. Returns:    EXIT  0 = Ok to continue
.                   1 = Error
.-------------------------------------------------------------
.
VLSEX000  MATCH     "I",PSEX
          GOTO      VLSEX900 IF NOT EQUAL
.
          MATCH     SP70,PBDATE
          GOTO      VLSEX900 IF EQUAL
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          MATCH     "00",CDAY
          GOTO      VLSEX900 IF EQUAL
.
          CALL      IBACLOCK
          IF        PCEASE=1
            MOVE      PDECDTE,AGEDATE            * Date of Death
          ELSE
            PACK      AGEDATE,CCC,CYY,CMM,CDD    * Today's date
          ENDIF
          REP       " 0",AGEDATE
          CALL      CALCAGE
          COMPARE   PSAGEDAY,NINTY
          GOTO      VLSEX900 IF NOT LESS
.
          MOVE      "Invalid sex for age",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      VLSEX999
.
VLSEX900  MOVE      ZERO,EXIT
.                                                * age is greater than 90 days
VLSEX999  RETURN
.
+
.------------------------------------------------------------
. VALADM00 - Validate the date of birth if changed
. Returns:   EXIT  0 = Ok to continue
.                  1 = Error
.------------------------------------------------------------
.
VALADM00  MATCH     SP70,OTDMBDAT               * not changed
          GOTO      VALADM90 IF EQUAL
.
          MATCH     OTDMBDAT,PBDATE
          GOTO      VALADM90 IF EQUAL
.
          MATCH     PBDATE,OTDMBDAT
          GOTO      VALADM90 IF LESS
.
          PACK      KEY24,URNUMBER,SP70
          CALL      RSPTVIS2
VALADM10  CALL      RKPTVIS2
          BRANCH    OVRCD,VALADM90
.
          MATCH     URNUMBER,PVIURNO
          GOTO      VALADM90 IF NOT EQUAL
.
          COMPARE   THREE,PVITYPE
          GOTO      VALADM10 IF NOT EQUAL
.
          MATCH     "00000000",PVIDATE
          GOTO      VALADM10 IF EQUAL
.
          PACK      KEY8,PVIBILL
          CALL      RDMISS1
          BRANCH    OVRCD,VALADM10
.
          BRANCH    ASTAT,VALADM10
          MATCH     ADATE,OTDMBDAT
          GOTO      VALADM10 IF LESS
          GOTO      VALADM10 IF EQUAL
.
          MOVE      "Patient has admissions before date of birth.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      VALADM99
.
VALADM90  MOVE      ZERO,EXIT
.
VALADM99  RETURN
+
.----------------------------------------------------------------------
. UPAD0000 - Update address details save last address change for today
.----------------------------------------------------------------------
UPAD0000  PACK      PADATE,CCC,CYY,CMM,CDD
          REP       " 0",PADATE
          CLOCK     TIME,PATIME
.
          PACK      KEY24,PURNO,PADATE,PATIME
          CALL      RDPADD1                 * Read previous address file
          IF        OVRCD = 1
            MOVE      PURNO,PAURNO
            MOVE      SPADD1,PAADD1
            MOVE      SPADD2,PAADD2
            MOVE      SPSUBRB,PASUBR
            MOVE      SPADD4,PAADD4
            MOVE      SPPOST,PAPOST
            MOVE      SPTELEP,PATELEP
            MOVE      SPTELEB,PATELEB
            MOVE      SPCELL,PTPAMOBL
            MOVE      SPPEML,PTPAEMAL
            MOVE      SPOSMS,PTPAOSMS
            CALL      IBACLOCK
            PACK      PTPACDTE,CCC,CYY,CMM,CDD
            REPLACE   " 0",PTPACDTE
            CLOCK     TIME,PTPACTIM
            MOVE      USERID,PTPAWEBC
            MOVE      SP70,PTPALUPD
            MOVE      SP70,PTPALUPT
            MOVE      USERID,PTPAWEBU         * Updated by
            MOVE      SP70,PTPASPAR
            CALL      WRPADD1                 * Write to previous address file
          ELSE
            MOVE      PURNO,PAURNO
            MOVE      SPADD1,PAADD1
            MOVE      SPADD2,PAADD2
            MOVE      SPSUBRB,PASUBR
            MOVE      SPADD4,PAADD4
            MOVE      SPPOST,PAPOST
            MOVE      SPTELEP,PATELEP
            MOVE      SPTELEB,PATELEB
            MOVE      SPCELL,PTPAMOBL
            MOVE      SPPEML,PTPAEMAL
            MOVE      SPOSMS,PTPAOSMS
            CALL      IBACLOCK
            PACK      PTPALUPD,CCC,CYY,CMM,CDD
            REPLACE   " 0",PTPALUPD
            CLOCK     TIME,PTPALUPT
            MOVE      USERID,PTPAWEBU
            MOVE      SP70,PTPASPAR
            CALL      UPPADD1                 * Write to previous address file
          ENDIF
.
UPAD9999  RETURN
.
.------------------------------------------------------------
. AMSV0000 - Subroutine to save master variables - From PATPRG31
.------------------------------------------------------------
.
AMSV0000  MOVE      PMICRO,SPMICRO
          MOVE      PUSR1,SPUSR1
          MOVE      PUSR2,SPUSR2
          MOVE      PUSR3,SPUSR3
          MOVE      PUSR4,SPUSR4
          MOVE      PUSR5,SPUSR5
          MOVE      PUSR6,SPUSR6
          MOVE      PUYN1,SPUYN1
          MOVE      PUYN2,SPUYN2
          MOVE      PUYN3,SPUYN3
.
          MOVE      PURNO,SPURNO
          MOVE      PTITL,SPTITL
          MOVE      PSNAME,SPSNAME
          MOVE      PTMASNAM,SPTMASNM
          MOVE      PGNAME,SPGNAME
          MOVE      PMPXFNAM,SPMPXFNM
          MOVE      PMPXSNAM,SPMPXSNM
          MOVE      PPSNAME,SPPSNAME
          MOVE      PSMOK,SPSMOK
          MOVE      PSEX,SPSEX
          MOVE      PMSTAT,SPMSTAT
          MOVE      PBDATE,SPBDATE
          MOVE      PSAGE,SPSAGE
          MOVE      PCONT,SPCONT
          MOVE      PREG,SPREG
          MOVE      PTYPE,SPTYPE
          MOVE      POCCUP,SPOCCUP
          MOVE      PMEDI,SPMEDI
          MOVE      PPENNO,SPPENNO
          MOVE      PPNDTE,SPPNDTE
          MOVE      PREPAT,SPREPAT
          MOVE      PCHGDTE,SPCHGDTE
          MOVE      PLOCDOC,SPLOCDOC
          MOVE      PCEASE,SPCEASE
.
          MOVE      PNKNAME,SPNKNAME
          MOVE      PNKADD1,SPNKADD1
          MOVE      PNKADD2,SPNKADD2
          MOVE      PNKSUBR,SPNKSUBR
          MOVE      PNKADD4,SPNKADD4
          MOVE      PNKPOST,SPNKPOST
          MOVE      PNKTELP,SPNKTELP
          MOVE      PNKTELB,SPNKTELB
          MOVE      PNKRELP,SPNKRELP
.
          CALL      PRAV0000                * Person Responsible A/C variables
.
          MOVE      PADD1,SPADD1
          MOVE      PADD2,SPADD2
          MOVE      PSUBRB,SPSUBRB
          MOVE      PADD4,SPADD4
          MOVE      PPOST,SPPOST
          MOVE      PTELEP,SPTELEP
          MOVE      PTELEB,SPTELEB
          MOVE      PTMXCELL,SPCELL
          MOVE      PTMXDOFR,SPDOR
          MOVE      PMPXRHC1,SPREGP
          MOVE      PMPXRH1G,SPGPPC
          MOVE      PMPXR1GC,SPGPPT
          MOVE      PTMXOSMS,SPOSMS
.
          MOVE      PHIGH1,SPHIGH1
          MOVE      PHIGH2,SPHIGH2
          MOVE      PHIGH3,SPHIGH3
          MOVE      PAUTOPY,SPAUTOPY
          MOVE      PDECDTE,SPDECDTE
          MOVE      PYRRES,SPYRRES
.
.-------- Save Community Card Details ---------------------------------
.
          MOVE      PTMXCARD,SPCARD
          MOVE      PTMXCEXP,SPCEXP
          MOVE      PTMXCXMP,SPCXMP
          MOVE      PTMXFMLY,SPFMLY
.
AMSV9999  RETURN
.
.------------------------------------------------------------
. PRAV0000  : save person responsible for account variables
.------------------------------------------------------------
.
PRAV0000  MOVE      PKNAME,SPKNAME
          MOVE      PKADD1,SPKADD1
          MOVE      PKADD2,SPKADD2
          MOVE      PKSUBR,SPKSUBR
          MOVE      PKADD4,SPKADD4
          MOVE      PKPOST,SPKPOST
          MOVE      PKTELEP,SPKTELP
          MOVE      PKTELEB,SPKTELB
          MOVE      PKRELP,SPKRELP
          MOVE      PTREMOBL,SPKMOBL
.
PRAV9999  RETURN
.
.******************************************************************************
.*                                 WESIS000                                   *
.*                           Write/update watespaf                            *
.******************************************************************************
WESIS000  READ      CONTROLF,EIGHTY;*250,PTCNHDPS
.
          COMPARE   THREE,PTCNHDPS
          GOTO      WESIS999 IF NOT EQUAL   * Not in Victoria
.
          MOVE      SP70,CARDNUMB
          PACK      KEY16,PURNO,Z70
          CALL      RSWTESP1
          CALL      RPWTESP1
          BRANCH    OVRCD,WESIS999
.
          MATCH     PURNO,WTEPURNO
          GOTO      WESIS999 IF NOT EQUAL
.
          CALL      IBACLOCK
          PACK      XDATE,CCC,CYY,CMM,CDD
          REP       " 0",XDATE
.
          PACK      KEY19,URNUMBER,SP70
          CALL      RSPMCCD1
WESIS100  CALL      RKPMCCD1
          BRANCH    OVRCD,WESIS200
.
          MATCH     URNUMBER,PMCDURNO
          GOTO      WESIS200 IF NOT EQUAL
.
          MATCH     XDATE,PMCDEXDT
          IF        !@LESS
            PACK      KEY5,CATct,PMCDCTYP
            CALL      RDCODE1
            BRANCH    OVRCD,WESIS100
            MATCH     "7",TCINDC1
            IF        @EQUAL
              MOVE      PMCDCNUM,CARDNUMB
              GOTO      WESIS200
            ENDIF
          ENDIF
          GOTO      WESIS100
.
WESIS200  CALL      CHKESI00
          BRANCH    EXIT,WESIS999   * no changes
          GOTO      WESIS200
.
WESIS250  PACK      WTEPURNO,PURNO
          PACK      WTEPEDAT,SP70
          PACK      WTEPPDOB,PBDATE,SP70
          PACK      WTEPEDOB,PMPXEDOB,SP70
          PACK      WTEPPSEX,PSEX,SP70
          PACK      WTEPMEDI,PMEDI,SP70
          SQUEEZE   PTMXMCCD
          PACK      WTEPMREF,PTMXMCCD,SP70
          PACK      WTEPRESI,PTYPE,SP70
          PACK      WTEPPOST,PPOST,SP70
          PACK      WTEPSUBR,PSUBRB,SP70
          PACK      WTEPABRG,PMPXABRG,SP70
          PACK      WTEPPGEN,PMPXUCC4,SP70
          PACK      WTEPSPAR,SP70
.
          PACK      KEY16,PURNO,SP70
          CALL      RAWTESP1
          IF        OVRCD=1
            PACK      WTEPWEBC,WBSEUID,SP70
            PACK      WTEPCDAT,CCC,CYY,CMM,CDD,SP70
            REP       " 0",WTEPCDAT
            PACK      WTEPCTIM,CTIMEIS,SP70
            REP       " 0",WTEPCTIM
            CALL      WRWTESP1
          ELSE
            PACK      WTEPWEBU,WBSEUID,SP70
            PACK      WTEPUDAT,CCC,CYY,CMM,CDD,SP70
            REP       " 0",WTEPUDAT
            PACK      WTEPUTIM,CTIMEIS,SP70
            REP       " 0",WTEPUTIM
            CALL      UPWTESP1
          ENDIF
.
WESIS999  RETURN
.
.
.******************************************************************************
.*                                 CHKESI00                                   *
.*                 Check if any ESIS have changed since last sent             *
.******************************************************************************
.
CHKESI00  MOVE      ZERO,EXIT
.
          MATCH     WTEPPDOB,PBDATE
          GOTO      CHKESI90 IF NOT EQUAL
.
          MATCH     WTEPEDOB,PMPXEDOB
          GOTO      CHKESI90 IF NOT EQUAL
.
          MATCH     WTEPPSEX,PSEX
          GOTO      CHKESI90 IF NOT EQUAL
.
          MATCH     WTEPMEDI,PMEDI
          GOTO      CHKESI90 IF NOT EQUAL
.
          SQUEEZE   PTMXMCCD
          MATCH     WTEPMREF,PTMXMCCD
          GOTO      CHKESI90 IF NOT EQUAL
.
          MATCH     WTEPRESI,PTYPE
          GOTO      CHKESI90 IF NOT EQUAL
.
          MATCH     WTEPPOST,PPOST
          GOTO      CHKESI90 IF NOT EQUAL
.
          MATCH     WTEPSUBR,PSUBRB
          GOTO      CHKESI90 IF NOT EQUAL
.
          MATCH     WTEPABRG,PMPXABRG
          GOTO      CHKESI90 IF NOT EQUAL
.
          MATCH     WTEPPGEN,PMPXUCC4
          GOTO      CHKESI90 IF NOT EQUAL
.
          MOVE      ONE,EXIT                 * No Changes
.
CHKESI90  GOTO      CHKESI99
.
CHKESI99  RETURN
.
+
.******************************************************************************
.*                                 PRS2T000                                   *
.*                Check for any PRS2 PMI trigger variable changes             *
.******************************************************************************
.
PRS2T000  READ      CONTROLF,EIGHTY8;*116,CHCSDTE,*124,CHCSST,*132,CHCSED
.
          COMPARE   THREE,PTCNHDPS
          GOTO      PRS2T999 IF NOT EQUAL   * Not in Victoria
.
          PACK      KEY24,PURNO,Z70
          CALL      RDSDSCH3
PRS2T100  CALL      RDPDSCH3
          BRANCH    OVRCD,PRS2T999          * must have discharged visits
.
          MATCH     PURNO,DURNO
          GOTO      PRS2T999 IF NOT EQUAL   * same u/r?
.
          MATCH     CHCSDTE,DDATE
          GOTO      PRS2T999 IF LESS        * Ddate must be >= start HDP date
.
          CALL      CPRST000                * check PRS2 trigger variables
.
PRS2T999  RETURN
+
.******************************************************************************
.*                                 CPRST000                                   *
.*             Check if any PRS2 trigger variables have changed               *
.******************************************************************************
.
CPRST000  MATCH     SAVPBDAT,PBDATE
          IF        !@EQUAL
            MOVE      "1",P2PMUNIQ
            MOVE      PBDATE,P2PMVALU
            CALL      WPRSP000
          ENDIF
          MATCH     SAVXABRG,PMPXABRG
          IF        !@EQUAL
            MOVE      "2",P2PMUNIQ
            MOVE      PMPXABRG,P2PMVALU
            CALL      WPRSP000
          ENDIF
          MATCH     SAVPCONT,PCONT
          IF        !@EQUAL
            MOVE      "3",P2PMUNIQ
            MOVE      PCONT,P2PMVALU
            CALL      WPRSP000
          ENDIF
          MATCH     SAVXLNG1,PMPXLNG1
          IF        !@EQUAL
            MOVE      "4",P2PMUNIQ
            MOVE      PMPXLNG1,P2PMVALU
            CALL      WPRSP000
          ENDIF
          MATCH     SAVPPSEX,PSEX
          IF        !@EQUAL
            MOVE      "5",P2PMUNIQ
            MOVE      PSEX,P2PMVALU
            CALL      WPRSP000
          ENDIF
          MATCH     SAVPMSTA,PMSTAT
          IF        !@EQUAL
            MOVE      "6",P2PMUNIQ
            MOVE      PMSTAT,P2PMVALU
            CALL      WPRSP000
          ENDIF
          MATCH     SAVPTYPE,PTYPE
          IF        !@EQUAL
            MOVE      "7",P2PMUNIQ
            MOVE      PTYPE,P2PMVALU
            CALL      WPRSP000
          ENDIF
          MATCH     SAVXINTR,PMPXINTR
          IF        !@EQUAL
            MOVE      "8",P2PMUNIQ
            MOVE      PMPXINTR,P2PMVALU
            CALL      WPRSP000
          ENDIF
.
          IF        CNSWLOC=2
            MATCH     SAVPADD2,PADD2
            IF        !@EQUAL
              MOVE      "9",P2PMUNIQ
              MOVE      PADD2,P2PMVALU           * using address line 2
              CALL      WPRSP000
            ENDIF
          ELSE
            MATCH     SAVPSUBR,PSUBRB
            IF        !@EQUAL
              MOVE      "9",P2PMUNIQ
              MOVE      PSUBRB,P2PMVALU          * using suburb field
              CALL      WPRSP000
            ENDIF
          ENDIF
.
          MATCH     SAVPPOST,PPOST
          IF        !@EQUAL
            MOVE      "10",P2PMUNIQ
            MOVE      PPOST,P2PMVALU
            CALL      WPRSP000
          ENDIF
          MATCH     SAVPMEDI,PMEDI
          IF        !@EQUAL
            MOVE      "11",P2PMUNIQ
            PACK      P2PMVALU,PMEDI,SP1,PTMXMCCD
            CALL      WPRSP000
          ENDIF
          MATCH     SAVPMCCD,PTMXMCCD
          IF        !@EQUAL
            MOVE      "11",P2PMUNIQ
            PACK      P2PMVALU,PMEDI,SP1,PTMXMCCD
            CALL      WPRSP000
          ENDIF
CPRST999  RETURN
.
+
.******************************************************************************
.*                                 SPRS0000                                   *
.*                      Save PRS2 PMI trigger variables                       *
.******************************************************************************
.
SPRS0000  MOVE      PBDATE,SAVPBDAT
          MOVE      PMPXABRG,SAVXABRG
          MOVE      PCONT,SAVPCONT
          MOVE      PMPXLNG1,SAVXLNG1
          MOVE      PSEX,SAVPPSEX
          MOVE      PMSTAT,SAVPMSTA
          MOVE      PTYPE,SAVPTYPE
          MOVE      PMPXINTR,SAVXINTR
          MOVE      PADD2,SAVPADD2
          MOVE      PSUBRB,SAVPSUBR
          MOVE      PPOST,SAVPPOST
          MOVE      PMEDI,SAVPMEDI
          MOVE      PTMXMCCD,SAVPMCCD
SPRS2999  RETURN
.
.******************************************************************************
.*                                 WPRSP000                                   *
.* If any of the PRS2 trigger variables have changed, write/update prspmiaf   *
.******************************************************************************
WPRSP000  MOVE      PURNO,P2PMURNO
          PACK      P2PMSPAR,SP70
          PACK      KEY11,P2PMURNO,P2PMUNIQ,SP70
          CALL      RAP2PMI1
          IF        OVRCD=1
            CALL      WRP2PMI1
          ELSE
            CALL      UPP2PMI1
          ENDIF
WPRSP999  RETURN
.
.------------------------------------------------------------
. PNMP0000  :  Post NMPI Number
.              Parameter : PURNO   (U/R Number)
.                          PVIBILL (Visit Number)
.                          NMPNUMB (NMPI Number)
. from KEYINPMI
.------------------------------------------------------------
.
PNMP0000  COMPARE   ZERO,PTCNNMPI
          GOTO      PNMP9999 IF EQUAL            * not using link
.
          COMPARE   ONE,PTCNNHII
          GOTO      PNMP9999 IF EQUAL            * using NZ link
.
.         Check whether a nmpi number has been inputed, if one has
.         write it to the PNI/NID/NMP file, otherwise do nothing
.
          MATCH     NMPNUMB,SP20
          GOTO      PNMP5000 IF EQUAL            * no number entered
.
          MATCH     ZEROUR,PURNO
          GOTO      PNMP3000 IF NOT EQUAL
.
.         Zero U/R :: post to PATPNI file
.
          MATCH     ZEROVISN,PVIBILL
          GOTO      PNMP9999 IF EQUAL
.
.         Zero Billing Number DO NOT POST TO PNI FILE OR NMP FILE
.
          PACK      KEY10,CHOSINDX,PVIBILL   * Hospital No. and visit No.
          CALL      RDPTPNI1
          MOVE      CHOSINDX,PTPNHNUM        * Hospital No.
          MOVE      PVIBILL,PTPNADNO         * Admission No.
          MOVE      NMPNUMB,PTPNNMPI         * National Id
          IF        OVRCD = ONE
            CALL      WRPTPNI1               * Write record does not exist
          ELSE
            CALL      UPPTPNI1               * Update record exists
          ENDIF
          GOTO      PNMP9999
.
.         A valid U/R :: post to PATNMP/PATNID file
.
PNMP3000  PACK      KEY10,CHOSINDX,PURNO
          CALL      RDPTNID1
          BRANCH    OVRCD,PNMP4000
.
          MOVE      NMPNUMB,PTNINMPI         * National Id
          CALL      UPPTNID1                * Update File
          GOTO      PNMP9999
.
PNMP4000  PACK      KEY10,CHOSINDX,PVIBILL
          CALL      RDPTPNI1                  * Exist on PNI file ?
          IF        OVRCD = ONE
            MOVE      CHOSINDX,PTNIHNUM       * No. hospital number
            MOVE      PURNO,PTNILNUM          *     local U/R Number
.
            SQUEEZE   NMPNUMB                 * left justify field
            PACK      NMPNUMB,NMPNUMB,SP20    * clear rest of field
            STRIP     NMPNUMB                 * remove blanks
            MOVELPTR  NMPNUMB,FORM2           * get the actual length
.
            MOVE      SP20,PTNINMPI           * clear the actual field
            ASSIGN    20-FORM2,IMON           * get the No. blanks at start
            RESET     PTNINMPI,IMON           * reset actual field
            APPEND    NMPNUMB,PTNINMPI        * add items entered
            RESET     PTNINMPI                * this field is now Right Just.
            MOVE      SP10,PTNISPAR           *     set spare variable
            PACK      KEY10,CHOSINDX,PURNO    *     Reset Key
            CALL      WRPTNID1                *     Write NMP record
          ELSE
            MOVE      PTPNHNUM,PTNIHNUM       * Yes. copy hospital number
            MOVE      PURNO,PTNILNUM          *      copy local U/R Number
            MOVE      PTPNNMPI,PTNINMPI       *      copy nmpi U/R No.
            MOVE      SP10,PTNISPAR           *      set spare variable
            PACK      KEY10,CHOSINDX,PURNO    *      Reset Key
            CALL      WRPTNID1                *      Write NMP record
            PACK      KEY10,CHOSINDX,PVIBILL   * Hospital No. and visit No.
            CALL      DEPTPNI1                *      delete PNI record
          ENDIF
          GOTO      PNMP9999
.
.         Nothing input. Check if there was previously details, and if so,
.         delete them
.
PNMP5000  MATCH     ZEROUR,PURNO             * Zero U/R Number
          IF        @EQUAL
            PACK      KEY10,CHOSINDX,PVIBILL
            CALL      DEPTPNI1
          ENDIF
          PACK      KEY10,CHOSINDX,PURNO   * All other countries
          CALL      DEPTNID1
.
PNMP9999  RETURN
.
+
.------------------------------------------------------------
.                          ALPR0000
.------------------------------------------------------------
.
ALPR0000  COMPARE   ONE,SAVEALPR
          GOTO      ALPR9999 IF NOT EQUAL
.
          MATCH     SP70,PMPXPFSN
          IF        @EQUAL | @EOS
            MATCH     SP70,PMPXPFGN
            IF        @EQUAL | @EOS
              GOTO      ALPR9999
            ENDIF
          ENDIF
.
          PACK      GSRSNAM,PMPXPFSN,SP70
          PACK      FULLGNAM,PMPXPFGN,SP70,SP70
          CALL      SEPRNAME
          PACK      GSRGNAM,FRSTGNAM,SP70
          PACK      PTGSGNM2,SCNDGNAM,SP70
          MOVE      PBDATE,GSRDOB            * Date of birth
          MOVE      PSEX,GSRSEX              * Sex
          MOVE      PURNO,GSRURNO
          MOVE      ZEROVISN,GSRBILL
          MOVE      ZERO,GSRSYS
          CALL      SOUNDEX                  * get soundex key for surname
          CALL      SOUNDX2                  * get soundex key for given names
.
          PACK      KEY115,GSRURNO,GSRBILL,GSRSNAM,GSRGNAM,PTGSGNM2,GSRDOB:
                           GSRSEX
          CALL      RDPTGSR1                 * check if its already there
          IF        OVRCD = 1
            CALL      WRPTGSR1
          ENDIF
.
ALPR9999  RETURN
.
.------------------------------------------------------------
. UPPMI000 - Update PMI Extension details
.------------------------------------------------------------
.
UPPMI000  MOVE      URNUMBER,PMPXURNO
          MOVE      PMPXURNO,KEY8
          CALL      RAPMPX21
          IF        OVRCD=1
            CALL      WRPMPX21          * write a new record
          ELSE
            CALL      UPPMPX21          * update the record
          ENDIF
.
UPPMI999  RETURN
.
.------------------------------------------------------------
. UPDMAS00 - Update Master Details
.------------------------------------------------------------
UPDMAS00  MOVE      OTDMTITL,PTITL
          MOVE      OTDMSNAM,PSNAME
          MOVE      OTDMSNAM,PTMASNAM
          MOVE      OTDMGEND,PSEX
          MOVE      OTDMBDAT,PBDATE
          MOVE      OTDMMSTA,PMSTAT
          MOVE      OTDMMEDN,PMEDI
          MOVE      OTDMMEDR,PTMXMCCD
          MOVE      OTDMADD1,PADD1
          MOVE      OTDMADD2,PADD2
          MOVE      OTDMADD3,PSUBRB
.
          MATCH     "8888",OTDMPOST
          IF        @EQUAL
            PACK      KEY5,ANSC,SP1,OTDMADD4,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      OTDMADD4,TDESC
            ENDIF
            MOVE      TDESC,PADD4
          ELSE
            MOVE      OTDMADD4,PADD4
          ENDIF
.
          MOVE      OTDMPOST,PPOST
          MOVE      OTDMTELP,PTELEP
          MOVE      OTDMTELW,PTELEB
          MOVE      OTDMTELM,PTMXCELL
.
          MOVE      ZERO,PCEASE
.
          MATCH     SP70,OTDMDECD
          IF        !@EQUAL
            MOVE      OTDMDECD,PDECDTE
            MOVE      ONE,PCEASE
          ENDIF
.
          MATCH     SP70,OTDMUKDD
          IF        !@EQUAL
            MATCH     "1",OTDMUKDD
            IF        @EQUAL
              MOVE      ANSY,PTMAUKDD
              MOVE      ONE,PCEASE
            ELSE
              MOVE      ANSN,PTMAUKDD
            ENDIF
          ENDIF
.
UPDMAS99  RETURN
.
.------------------------------------------------------------
. PX2U0000 Update master file extension 2 vars
.------------------------------------------------------------
PX2U0000  PACK      PMPXFNAM,OTDMFGNM,SP70
          PACK      PMPXSNAM,OTDMSGNM,SP70
          STRIP     PMPXFNAM
          PACK      PGNAME,PMPXFNAM,SP1,PMPXSNAM,SP70
          RESET     PMPXFNAM
          PACK      PMPXFNAM,PMPXFNAM,SP70
          MOVE      OTDMMEDV,PMPXMEDC
          MOVE      OTDMEMAI,PMPXPEML
          MOVE      OTDMDETY,PMPXDETY
          MOVE      OTDMPMCN,PMPXPMCN
          MOVE      OTDMPMCR,PMPXPMCR
.
PX2U9999  RETURN
.
.------------------------------------------------------------
. Delete any existing DVA and safety net cards and add
. new DVA card and safety net card
.------------------------------------------------------------
UCCD0000  MATCH     SP70,OTDMDVCT           * DVA Card Type
          GOTO      UCCD5000 IF EQUAL
.
          PACK      KEY19,OTDMURNO,SP70
          CALL      RSPMCCD1
UCCD1000  CALL      RKPMCCD1                * Delete existing DVA card if found
          BRANCH    OVRCD,UCCD2000
.
          MATCH     OTDMURNO,PMCDURNO
          GOTO      UCCD2000 IF NOT EQUAL
.
          MATCH     OTDMDVCT,PMCDCTYP
          GOTO      UCCD1000 IF NOT EQUAL
.
          PACK      KEY19,PMCDURNO,PMCDEXDT,PMCDCTYP,SP70
          CALL      DEPMCCD1
.
UCCD2000  MATCH     SP70,OTDMDVED           * Blank expiry date
          GOTO      UCCD5000 IF EQUAL
.
          MATCH     SP70,OTDMDVAN           * Blank card number
          GOTO      UCCD5000 IF EQUAL
.
          MOVE      OTDMURNO,PMCDURNO       * U/R Number
          MOVE      OTDMDVCT,PMCDCTYP       * DVA Card Type (Category ct)
          MOVE      OTDMDVAN,PMCDCNUM       * Card Number
          MOVE      OTDMDVED,PMCDEXDT       * Card Expiry Date
          PACK      PMCDCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PMCDCDAT           * Date Record Created (ccyymmdd)
          CLOCK     TIME,PMCDCTIM           * Time Record Created (hh:mm:ss)
          MOVE      USERID,PMCDCUID         * WEB User ID Created (websecaf)
          MOVE      SP70,PMCDUDAT           * Date Record Updated (ccyymmdd)
          MOVE      SP70,PMCDUTIM           * Time Record Updated (hh:mm:ss)
          MOVE      SP70,PMCDUUID           * WEB User ID Updated (websecaf)
          MOVE      OTDMDVAC,PMCDDVAC       * DVA Card Colour (Cat DX)
          MOVE      SP70,PMCDSPAR           * Spare Variable

          PACK      KEY19,OTDMURNO,OTDMDVED,OTDMDVCT,SP70
          CALL      RAPMCCD1
          IF        OVRCD=1
            CALL      WRPMCCD1
          ENDIF
.
UCCD5000  MATCH     SP70,OTDMSFCT           * Safety Net Card Type
          GOTO      UCCD9999 IF EQUAL
.
          PACK      KEY19,OTDMURNO,SP70
          CALL      RSPMCCD1
UCCD6000  CALL      RKPMCCD1                * Delete safety net card if found
          BRANCH    OVRCD,UCCD7000
.
          MATCH     OTDMURNO,PMCDURNO
          GOTO      UCCD7000 IF NOT EQUAL
.
          MATCH     OTDMSFCT,PMCDCTYP
          GOTO      UCCD6000 IF NOT EQUAL
.
          PACK      KEY19,PMCDURNO,PMCDEXDT,PMCDCTYP,SP70
          CALL      DEPMCCD1
.
UCCD7000  MATCH     SP70,OTDMSFED           * Blank expiry date
          GOTO      UCCD9999 IF EQUAL
.
          MATCH     SP70,OTDMSFNO           * Blank card number
          GOTO      UCCD9999 IF EQUAL
.
          MOVE      OTDMURNO,PMCDURNO       * U/R Number
          MOVE      OTDMSFCT,PMCDCTYP       * Safety Net Card Type (Category ct)
          MOVE      OTDMSFNO,PMCDCNUM       * Card Number
          MOVE      OTDMSFED,PMCDEXDT       * Card Expiry Date
          PACK      PMCDCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PMCDCDAT           * Date Record Created (ccyymmdd)
          CLOCK     TIME,PMCDCTIM           * Time Record Created (hh:mm:ss)
          MOVE      USERID,PMCDCUID         * WEB User ID Created (websecaf)
          MOVE      SP70,PMCDUDAT           * Date Record Updated (ccyymmdd)
          MOVE      SP70,PMCDUTIM           * Time Record Updated (hh:mm:ss)
          MOVE      SP70,PMCDUUID           * WEB User ID Updated (websecaf)
          MOVE      SP70,PMCDDVAC           * DVA Card Colour (Cat DX)
          MOVE      SP70,PMCDSPAR           * Spare Variable

          PACK      KEY19,OTDMURNO,OTDMDVED,OTDMDVCT,SP70
          CALL      RAPMCCD1
          IF        OVRCD=1
            CALL      WRPMCCD1
          ENDIF
.
UCCD9999  RETURN
.
.------------------------------------------------------------
. CHKCRD00 - Concession Card validation
.------------------------------------------------------------
CHKCRD00  MOVE      ZERO,EXIT
          PACK      KEY5,CATct,PMCDCTYP,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,CHKCRD90
.
          MOVE      ZERO,FORM1
          MOVE      TCINDC1,FORM1
          BRANCH    FORM1,CHKCRD99:              * Centrelink
                          CHKCRD20:              * Safety Net
                          CHKCRD30:              * DVA
                          CHKCRD99               * Pension
.
          GOTO      CHKCRD95
.
CHKCRD20  UNPACK    PMCDCNUM,KEY2,KEY9,DIM9      * Safety Net card number
          MATCH     "SN",KEY2
          IF        !@EQUAL
            MATCH     "CN",KEY2
            GOTO      CHKCRD91 IF NOT EQUAL
          ENDIF
.
          TYPE      KEY9
          GOTO      CHKCRD91 IF NOT EQUAL
.
          MATCH     SP70,DIM9
          GOTO      CHKCRD91 IF NOT EQUAL
.
          GOTO      CHKCRD99
.
CHKCRD30  RESET     DVACHAR1                     * DVA card number
          UNPACK    PMCDCNUM,CHAR1,CHAR2,CHAR3,CHAR4,CHARS58,CHAR9,DIM11
          SCAN      CHAR1,DVACHAR1          * validate char 1
          GOTO      CHKCRD91 IF NOT EQUAL
.
          MOVE      ZERO,NXTALPHA           * following chars can be non-numeric
          TYPE      CHAR2
          IF        @EQUAL
            MOVE      ONE,NXTALPHA          * following chars must be numeric
          ENDIF
.
          TYPE      CHAR3
          IF        !@EQUAL
            COMPARE   ONE,NXTALPHA
            GOTO      CHKCRD91 IF EQUAL     * char 3 must be numeric
          ELSE
            MOVE      ONE,NXTALPHA          * following chars must be numeric
          ENDIF
.
          TYPE      CHAR4
          IF        !@EQUAL
            COMPARE   ONE,NXTALPHA
            GOTO      CHKCRD91 IF EQUAL     * char 4 must be numeric
          ENDIF
.
          TYPE      CHARS58                 * chars 5-8 must be numeric
          GOTO      CHKCRD91 IF NOT EQUAL
.
          TYPE      CHAR9                   * char 9 must be non-numeric
          GOTO      CHKCRD91 IF EQUAL
.
          MATCH     SP70,DIM11
          GOTO      CHKCRD91 IF NOT EQUAL
.
          GOTO      CHKCRD99
.
CHKCRD90  MOVE      "Invalid Concession Card Type. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKCRD99
.
CHKCRD91  CLEAR     WEBTITLE
          STRIP     TDESC
          APPEND    "Invalid ",WEBTITLE
          APPEND    TDESC,WEBTITLE
          APPEND    " Card Number. ",WEBTITLE
          CALL      WEBERR00
          RESET     TDESC
          MOVE      ONE,EXIT
          GOTO      CHKCRD99
.
CHKCRD95  MOVE      "Indicator not set up for Concession Card Type. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKCRD99
.
CHKCRD99  RETURN
.
.------------------------------------------------------------
. Delete any post code and NOK extra contacts and add 
. new post code and NOK extra contact 
.------------------------------------------------------------
ECON0000  MATCH     SP70,OTDMC1TY           * Post code extra contact type
          GOTO      ECON5000 IF EQUAL
.
ECON2000  MATCH     SP70,OTDMC1TL           
          GOTO      ECON3000 IF NOT EQUAL
.
          MATCH     SP70,OTDMC1SN           
          GOTO      ECON3000 IF NOT EQUAL
.
          MATCH     SP70,OTDMC1G1           
          GOTO      ECON3000 IF NOT EQUAL
.
          MATCH     SP70,OTDMC1G2           
          GOTO      ECON3000 IF NOT EQUAL
.
          MATCH     SP70,OTDMC1A1           
          GOTO      ECON3000 IF NOT EQUAL
.
          MATCH     SP70,OTDMC1A2           
          GOTO      ECON3000 IF NOT EQUAL
.
          MATCH     SP70,OTDMC1A3           
          GOTO      ECON3000 IF NOT EQUAL
.
          MATCH     SP70,OTDMC1A4           
          GOTO      ECON3000 IF NOT EQUAL
.
          MATCH     SP70,OTDMC1PC           
          GOTO      ECON3000 IF NOT EQUAL
.
          MATCH     SP70,OTDMC1TP           
          GOTO      ECON3000 IF NOT EQUAL
.
          MATCH     SP70,OTDMC1TB           
          GOTO      ECON3000 IF NOT EQUAL
.
          MATCH     SP70,OTDMC1TM           
          GOTO      ECON3000 IF NOT EQUAL
.
          MATCH     SP70,OTDMC1RP           
          GOTO      ECON3000 IF NOT EQUAL
.
          MATCH     SP70,OTDMC1EM           
          GOTO      ECON3000 IF NOT EQUAL
.
          GOTO      ECON4000
.
ECON3000  CALL      CONC0000                      * Calculate contact counter
.                                                 * for add if required
          PACK      KEY14,OTDMURNO,OTDMC1TY,SP70
          CALL      RSPMCEX1
ECON3100  CALL      RKPMCEX1
          BRANCH    OVRCD,ECON3500
.
          MATCH     OTDMURNO,PMCEURNO            * Same U/R
          GOTO      ECON3500 IF NOT EQUAL
.
          MATCH     OTDMC1TY,PMCETYPE            * Same Contact Type
          GOTO      ECON3500 IF NOT EQUAL
.
          MATCH     "1",PMCEACTV                 * Skip if Inactive
          GOTO      ECON3100 IF EQUAL
.
          MOVE      PMCECNTR,PMCEX003            * Store contact counter
          MOVE      TWO,AUDTTYPE                 * write before audit record
          CALL      WAPMCE00
.
ECON3500  MOVE      OTDMURNO,PMCEURNO       * U/R Number
          MOVE      OTDMC1TY,PMCETYPE       * Type of Contact (Cat tc)
          MOVE      PMCEX003,PMCECNTR       * Counter
          MOVE      OTDMC1TL,PMCETITL       * Title
          MOVE      OTDMC1SN,PMCESNAM       * Surname
          MOVE      OTDMC1G1,PMCEGNAM       * Given Name
          MOVE      OTDMC1G2,PMCEGNM2       * Second Given Name
          MOVE      OTDMC1A1,PMCEADD1       * Address Line 1
          MOVE      OTDMC1A2,PMCEADD2       * Address Line 2
          MOVE      OTDMC1A3,PMCEADD3       * Address Line 3
.
          MATCH     "8888",OTDMC1PC
          IF        @EQUAL
            PACK      KEY5,ANSC,SP1,OTDMC1A4,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      OTDMC1A4,TDESC
            ENDIF
            MOVE      TDESC,PMCEADD4        * Address Line 4
          ELSE
            MOVE      OTDMC1A4,PMCEADD4     * Address Line 4
          ENDIF
.
          MOVE      OTDMC1PC,PMCEPOST       * Post Code
          MOVE      OTDMC1TP,PMCETELP       * Telephone Private
          MOVE      OTDMC1TB,PMCETELB       * Telephone Business
          MOVE      OTDMC1TM,PMCETELM       * Telephone Mobile
          MOVE      OTDMC1RP,PMCERELP       * Relationship
          MOVE      OTDMC1EM,PMCEEMAI       * Email Address
          PACK      PMCECDAT,CCC,CYY,CMM,CDD * Date Record Created (CCYYMMDD)
          REP       " 0",PMCDCDAT           * Date Record Created (ccyymmdd)
          CLOCK     TIME,PMCECTIM           * Time Record Created (HH:MM:SS)
          MOVE      USERID,PMCECUID         * WEB User ID Created (websecaf)
          MOVE      SP70,PMCEUDAT       * Update Date (CCYYMMDD)
          MOVE      SP70,PMCEUTIM       * Update Time (HH:MM:SS)
          MOVE      SP70,PMCEUUID       * WEB User ID Updated (websecaf)
          MOVE      SP70,PMCESLET       * Send Letter (0=No,1=Yes)
          MOVE      ZERO,PMCEACTV       * Active (0=Active 1=Inactive)
          MOVE      SP70,PMCECONT       * Country of Birth (Cat C)
          PACK      PMCEFTXT,SP70,SP70,SP70,SP70
                                        * Free Text 
          MOVE      SP70,PMCESPAR       * Spare
.
          PACK      KEY14,PMCEURNO,PMCETYPE,PMCECNTR,SP70
          CALL      RAPMCEX1
          IF        OVRCD=1
            CALL      CONU0000                    * Get contact webPAS unique id
.
            CALL      WRPMCEX1
            MOVE      ONE,AUDTTYPE            * write add audit record
            CALL      WAPMCE00
          ELSE
            CALL      UPPMCEX1
            MOVE      THREE,AUDTTYPE               * write after audit record
            CALL      WAPMCE00
          ENDIF
          GOTO      ECON5000
.
ECON4000  PACK      KEY14,OTDMURNO,OTDMC1TY,SP70
          CALL      RSPMCEX1
ECON4100  CALL      RKPMCEX1
          BRANCH    OVRCD,ECON5000
.
          MATCH     OTDMURNO,PMCEURNO            * Same U/R
          GOTO      ECON5000 IF NOT EQUAL
.
          MATCH     OTDMC1TY,PMCETYPE            * Same Contact Type
          GOTO      ECON5000 IF NOT EQUAL
.
          MATCH     "1",PMCEACTV                 * Skip if Inactive
          GOTO      ECON4100 IF EQUAL
.
          PACK      KEY14,OTDMURNO,PMCETYPE,PMCECNTR,SP70
          CALL      DEPMCEX1
.
          MOVE      FOUR,AUDTTYPE           * write delete audit record
          CALL      WAPMCE00
.
ECON5000  MATCH     SP70,OTDMC2TY           * NOK extra contact type
          GOTO      ECON9999 IF EQUAL
.
ECON6000  MATCH     SP70,OTDMC2TL
          GOTO      ECON7000 IF NOT EQUAL
.
          MATCH     SP70,OTDMC2SN
          GOTO      ECON7000 IF NOT EQUAL
.
          MATCH     SP70,OTDMC2G1
          GOTO      ECON7000 IF NOT EQUAL
.
          MATCH     SP70,OTDMC2G2
          GOTO      ECON7000 IF NOT EQUAL
.
          MATCH     SP70,OTDMC2A1
          GOTO      ECON7000 IF NOT EQUAL
.
          MATCH     SP70,OTDMC2A2
          GOTO      ECON7000 IF NOT EQUAL
.
          MATCH     SP70,OTDMC2A3
          GOTO      ECON7000 IF NOT EQUAL
.
          MATCH     SP70,OTDMC2A4
          GOTO      ECON7000 IF NOT EQUAL
.
          MATCH     SP70,OTDMC2PC
          GOTO      ECON7000 IF NOT EQUAL
.
          MATCH     SP70,OTDMC2TP
          GOTO      ECON7000 IF NOT EQUAL
.
          MATCH     SP70,OTDMC2TB
          GOTO      ECON7000 IF NOT EQUAL
.
          MATCH     SP70,OTDMC2TM
          GOTO      ECON7000 IF NOT EQUAL
.
          MATCH     SP70,OTDMC2RP
          GOTO      ECON7000 IF NOT EQUAL
.
          MATCH     SP70,OTDMC2EM
          GOTO      ECON7000 IF NOT EQUAL
.
          GOTO      ECON8000
.
ECON7000  CALL      CONC0000                      * Calculate contact counter
.                                                 * for add if required
          PACK      KEY14,OTDMURNO,OTDMC2TY,SP70
          CALL      RSPMCEX1
ECON7100  CALL      RKPMCEX1
          BRANCH    OVRCD,ECON7500
.
          MATCH     OTDMURNO,PMCEURNO            * Same U/R
          GOTO      ECON7500 IF NOT EQUAL
.
          MATCH     OTDMC2TY,PMCETYPE            * Same Contact Type
          GOTO      ECON7500 IF NOT EQUAL
.
          MATCH     "1",PMCEACTV                 * Skip if Inactive
          GOTO      ECON7100 IF EQUAL
.
          MOVE      PMCECNTR,PMCEX003            * Store contact counter
          MOVE      TWO,AUDTTYPE                 * write before audit record
          CALL      WAPMCE00
.
ECON7500  MOVE      OTDMURNO,PMCEURNO       * U/R Number
          MOVE      OTDMC2TY,PMCETYPE       * Type of Contact (Cat tc)
          MOVE      OTDMC2TL,PMCETITL       * Title
          MOVE      OTDMC2SN,PMCESNAM       * Surname
          MOVE      OTDMC2G1,PMCEGNAM       * Given Name
          MOVE      OTDMC2G2,PMCEGNM2       * Second Given Name
          MOVE      OTDMC2A1,PMCEADD1       * Address Line 1
          MOVE      OTDMC2A2,PMCEADD2       * Address Line 2
          MOVE      OTDMC2A3,PMCEADD3       * Address Line 3
.
          MATCH     "8888",OTDMC2PC
          IF        @EQUAL
            PACK      KEY5,ANSC,SP1,OTDMC2A4,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      OTDMC2A4,TDESC
            ENDIF
            MOVE      TDESC,PMCEADD4        * Address Line 4
          ELSE
            MOVE      OTDMC2A4,PMCEADD4     * Address Line 4
          ENDIF
.
          MOVE      OTDMC2PC,PMCEPOST       * Post Code
          MOVE      OTDMC2TP,PMCETELP       * Telephone Private
          MOVE      OTDMC2TB,PMCETELB       * Telephone Business
          MOVE      OTDMC2TM,PMCETELM       * Telephone Mobile
          MOVE      OTDMC2RP,PMCERELP       * Relationship
          MOVE      OTDMC2EM,PMCEEMAI       * Email Address
          PACK      PMCECDAT,CCC,CYY,CMM,CDD * Date Record Created (CCYYMMDD)
          REP       " 0",PMCDCDAT           * Date Record Created (ccyymmdd)
          CLOCK     TIME,PMCECTIM           * Time Record Created (HH:MM:SS)
          MOVE      USERID,PMCECUID         * WEB User ID Created (websecaf)
          MOVE      SP70,PMCEUDAT       * Update Date (CCYYMMDD)
          MOVE      SP70,PMCEUTIM       * Update Time (HH:MM:SS)
          MOVE      SP70,PMCEUUID       * WEB User ID Updated (websecaf)
          MOVE      SP70,PMCESLET       * Send Letter (0=No,1=Yes)
          MOVE      ZERO,PMCEACTV       * Active (0=Active 1=Inactive)
          MOVE      SP70,PMCECONT       * Country of Birth (Cat C)
          PACK      PMCEFTXT,SP70,SP70,SP70,SP70
                                        * Free Text 
          MOVE      SP70,PMCESPAR       * Spare
.
          PACK      KEY14,PMCEURNO,PMCETYPE,PMCECNTR,SP70
          CALL      RAPMCEX1
          IF        OVRCD=1
            CALL      CONU0000                    * Get contact webPAS unique id
.
            CALL      WRPMCEX1
            MOVE      ONE,AUDTTYPE            * write add audit record
            CALL      WAPMCE00
          ELSE
            CALL      UPPMCEX1
            MOVE      THREE,AUDTTYPE               * write after audit record
            CALL      WAPMCE00
          ENDIF
.     
          GOTO      ECON9999
.
ECON8000  PACK      KEY14,OTDMURNO,OTDMC2TY,SP70
          CALL      RSPMCEX1
ECON8100  CALL      RKPMCEX1
          BRANCH    OVRCD,ECON9999
.
          MATCH     OTDMURNO,PMCEURNO            * Same U/R
          GOTO      ECON9999 IF NOT EQUAL
.
          MATCH     OTDMC2TY,PMCETYPE            * Same Contact Type
          GOTO      ECON9999 IF NOT EQUAL
.
          MATCH     "1",PMCEACTV                 * Skip if Inactive
          GOTO      ECON8100 IF EQUAL
.
          PACK      KEY14,OTDMURNO,PMCETYPE,PMCECNTR,SP70
          CALL      DEPMCEX1
.
          MOVE      FOUR,AUDTTYPE           * write delete audit record
          CALL      WAPMCE00
.
ECON9999  RETURN
.
.------------------------------------------------------------
. Write new alias records                               
.------------------------------------------------------------
ALIA0000  MATCH     SP70,OTDMA1SN           * No Alias 1
          GOTO      ALIA2000 IF EQUAL
.
          MOVE      OTDMA1SN,GSRSNAM
          MOVE      OTDMA1FG,GSRGNAM
          MOVE      OTDMA1SG,PTGSGNM2
          MOVE      OTDMA1DB,GSRDOB
          MOVE      OTDMA1GE,GSRSEX
          MOVE      OTDMURNO,GSRURNO
          MOVE      ZEROVISN,GSRBILL
          MOVE      ZERO,GSRSYS
          CALL      SOUNDEX                  * get soundex key for surname
          CALL      SOUNDX2                  * get soundex key for given names
.
          PACK      KEY115,GSRURNO,GSRBILL,GSRSNAM,GSRGNAM,PTGSGNM2,GSRDOB:
                           GSRSEX
          CALL      RDPTGSR1                 * check if its already there
          IF        OVRCD = 1
            CALL      WRPTGSR1
          ENDIF
.
ALIA2000  MATCH     SP70,OTDMA2SN           * No Alias 2
          GOTO      ALIA3000 IF EQUAL
.
          MOVE      OTDMA2SN,GSRSNAM
          MOVE      OTDMA2FG,GSRGNAM
          MOVE      OTDMA2SG,PTGSGNM2
          MOVE      OTDMA2DB,GSRDOB
          MOVE      OTDMA2GE,GSRSEX
          MOVE      OTDMURNO,GSRURNO
          MOVE      ZEROVISN,GSRBILL
          MOVE      ZERO,GSRSYS
          CALL      SOUNDEX                  * get soundex key for surname
          CALL      SOUNDX2                  * get soundex key for given names
.
          PACK      KEY115,GSRURNO,GSRBILL,GSRSNAM,GSRGNAM,PTGSGNM2,GSRDOB:
                           GSRSEX
          CALL      RDPTGSR1                 * check if its already there
          IF        OVRCD = 1
            CALL      WRPTGSR1
          ENDIF
.
ALIA3000  MATCH     SP70,OTDMA3SN           * No Alias 3
          GOTO      ALIA4000 IF EQUAL
.
          MOVE      OTDMA3SN,GSRSNAM
          MOVE      OTDMA3FG,GSRGNAM
          MOVE      OTDMA3SG,PTGSGNM2
          MOVE      OTDMA3DB,GSRDOB
          MOVE      OTDMA3GE,GSRSEX
          MOVE      OTDMURNO,GSRURNO
          MOVE      ZEROVISN,GSRBILL
          MOVE      ZERO,GSRSYS
          CALL      SOUNDEX                  * get soundex key for surname
          CALL      SOUNDX2                  * get soundex key for given names
.
          PACK      KEY115,GSRURNO,GSRBILL,GSRSNAM,GSRGNAM,PTGSGNM2,GSRDOB:
                           GSRSEX
          CALL      RDPTGSR1                 * check if its already there
          IF        OVRCD = 1
            CALL      WRPTGSR1
          ENDIF
.
ALIA4000  MATCH     SP70,OTDMA4SN           * No Alias 4
          GOTO      ALIA9999 IF EQUAL
.
          MOVE      OTDMA4SN,GSRSNAM
          MOVE      OTDMA4FG,GSRGNAM
          MOVE      OTDMA4SG,PTGSGNM2
          MOVE      OTDMA4DB,GSRDOB
          MOVE      OTDMA4GE,GSRSEX
          MOVE      OTDMURNO,GSRURNO
          MOVE      ZEROVISN,GSRBILL
          MOVE      ZERO,GSRSYS
          CALL      SOUNDEX                  * get soundex key for surname
          CALL      SOUNDX2                  * get soundex key for given names
.
          PACK      KEY115,GSRURNO,GSRBILL,GSRSNAM,GSRGNAM,PTGSGNM2,GSRDOB:
                           GSRSEX
          CALL      RDPTGSR1                 * check if its already there
          IF        OVRCD = 1
            CALL      WRPTGSR1
          ENDIF

.
ALIA9999  RETURN
.
.------------------------------------------------------------
. Outpatient Direct Appointment Messages
.------------------------------------------------------------
APPM0000  MATCH     SP70,UPDTTYPE
          GOTO      APPM7000 IF EQUAL
.
          MATCH     "A",UPDTTYPE            * Add
          GOTO      APPM1000 IF EQUAL
.
          MATCH     "U",UPDTTYPE            * Update
          GOTO      APPM2000 IF EQUAL
.
          MATCH     "D",UPDTTYPE            * Delete
          GOTO      APPM3000 IF EQUAL
.
          GOTO      APPM9000
.
APPM1000  PACK      OTDAN002,CCC,CYY,CMM,CDD     * Set action date and time
          REP       " 0",OTDAN002                * to current
          CLOCK     TIME,OTDAN003
          MOVE      " 1",OTDAN004
.
          PACK      KEY34,OTDAN001,OTDAN002,OTDAN003,OTDAN004,OTDAN005,SP70
          CALL      RDOTDAN1
          COMPARE   ZERO,OVRCD
          GOTO      APPM9300 IF EQUAL
.
          CALL      CLOUTDAN                     * Clear file vars
          CALL      MVOTDN00                     * Move cgi to file vars
.
          PACK      OTDNCDAT,CCC,CYY,CMM,CDD
          REP       " 0",OTDNCDAT
          CLOCK     TIME,OTDNCTIM
          MOVE      USERID,OTDNCUID
.
          MATCH     "1",OTDNCOMP
          IF        @EQUAL
            PACK      OTDNCOMD,CCC,CYY,CMM,CDD
            REP       " 0",OTDNCOMD
            CLOCK     TIME,OTDNCOMT
            MOVE      USERID,OTDNCOMU
          ENDIF
.
          PACK      KEY34,OTDNURNO,OTDNADAT,OTDNATIM,OTDNATYP,OTDNVISN,SP70
          CALL      WROTDAN1                     * Add action 
.
          MOVE      OTDNURNO,COMMURNO
          MOVE      OTDNADAT,COMMADAT
          MOVE      OTDNATIM,COMMATIM
          MOVE      " 1",COMMTYPE                * Appointment comment
          CALL      SAVCOM00                     * Update Comments
.
          MOVE      ZERO,EXIT
          GOTO      APPM9999
.
APPM2000  PACK      KEY34,OTDAN001,OTDAN002,OTDAN003,OTDAN004,OTDAN005,SP70
          CALL      RDOTDAN1
          BRANCH    OVRCD,APPM9400
.
          CALL      MVOTDN00                     * Move cgi to file vars
.
          MATCH     "1",OTDNCOMP
          IF        @EQUAL
            PACK      OTDNCOMD,CCC,CYY,CMM,CDD
            REP       " 0",OTDNCOMD
            CLOCK     TIME,OTDNCOMT
            MOVE      USERID,OTDNCOMU
          ELSE
            MOVE      SP70,OTDNCOMU
            MOVE      SP70,OTDNCOMD
            MOVE      SP70,OTDNCOMT
          ENDIF
.
          PACK      OTDNUDAT,CCC,CYY,CMM,CDD
          REP       " 0",OTDNUDAT
          CLOCK     TIME,OTDNUTIM
          MOVE      USERID,OTDNUUID
.
          CALL      UPOTDAN1                     * Update request
.
          MOVE      OTDNURNO,COMMURNO
          MOVE      OTDNADAT,COMMADAT
          MOVE      OTDNATIM,COMMATIM
          MOVE      " 1",COMMTYPE                * comment
          CALL      SAVCOM00                     * Update Comments
.
          MOVE      ZERO,EXIT
          GOTO      APPM9999
.
APPM3000  PACK      KEY34,OTDAN001,OTDAN002,OTDAN003,OTDAN004,OTDAN005,SP70
          CALL      RDOTDAN1
          BRANCH    OVRCD,APPM9400
.
          MOVE      OTDNURNO,COMMURNO
          MOVE      OTDNADAT,COMMADAT
          MOVE      OTDNATIM,COMMATIM
          MOVE      " 1",COMMTYPE                * PMI update comment
          CALL      DELCOM00                     * Delete request comments
.
          PACK      KEY34,OTDAN001,OTDAN002,OTDAN003,OTDAN004,OTDAN005,SP70
          CALL      DEOTDAN1
.
          MOVE      ZERO,EXIT
          GOTO      APPM9999
.
APPM7000  MATCH     SP70,WBSEOPDU                * Not an OP direct user
          GOTO      APPM9200 IF EQUAL
.
          PACK      KEY8,URNUMBER,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,APPM9100
.
          CALL      RDPMPX21
          BRANCH    OVRCD,APPM9100
.
          PACK      KEY34,OTDAN001,OTDAN002,OTDAN003,OTDAN004,OTDAN005,SP70
          CALL      RDOTDAN1
          IF        OVRCD=1
            CALL      CLOUTDAN
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Outpatient Direct",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,APPM9999
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      APPM9999
.
APPM9000  MOVE      "Invalid form action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      APPM9999
.
APPM9100  MOVE      "Invalid U/R number.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      APPM9999
.
APPM9200  MOVE      "Invalid outpatient direct user.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      APPM9999
.
APPM9300  MOVE      "Message already exists for this date and time.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      APPM9999
.
APPM9400  MOVE      "Invalid message record.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      APPM9999
.
APPM9999  CLOSE     COMFILAF,DELETE
          RETURN
.
.------------------------------------------------------------
. Appointment Message List 
.------------------------------------------------------------
APMT0000  PACK      KEY34,URNUMBER,ADMISSNO,SP1,ONE,SP70
          CALL      RSOTDAN2
APMT1000  CALL      RKOTDAN2
          BRANCH    OVRCD,APMT9999
.
          MATCH     URNUMBER,OTDNURNO
          GOTO      APMT9999 IF NOT EQUAL
.
          MATCH     ADMISSNO,OTDNVISN
          GOTO      APMT9999 IF NOT EQUAL
.
          MATCH     " 1",OTDNATYP                * Appointment message
          GOTO      APMT9999 IF NOT EQUAL
.
          PACK      COMMLIN1,SP70,SP70
          PACK      COMMLIN2,SP70,SP70
          PACK      COMMLIN3,SP70,SP70
.
          PACK      KEY29,OTDNURNO,OTDNADAT,OTDNATIM,SP1,ONE,SP70
          CALL      RSOTDCO1
APMT1500  CALL      RKOTDCO1
          BRANCH    OVRCD,APMT2000
.
          MATCH     OTDNURNO,OTDOURNO
          GOTO      APMT2000 IF NOT EQUAL
.
          MATCH     OTDNADAT,OTDOADAT
          GOTO      APMT2000 IF NOT EQUAL
.
          MATCH     OTDNATIM,OTDOATIM
          GOTO      APMT2000 IF NOT EQUAL
.
          MATCH     " 1",OTDOATYP
          GOTO      APMT2000 IF NOT EQUAL
.
          MATCH     SP70,COMMLIN1
          IF        @EQUAL
            MOVE      OTDOCOMM,COMMLIN1
            GOTO      APMT1500
          ENDIF
.
          MATCH     SP70,COMMLIN2
          IF        @EQUAL
            MOVE      OTDOCOMM,COMMLIN2
            GOTO      APMT1500
          ENDIF
.
          MATCH     SP70,COMMLIN3
          IF        @EQUAL
            MOVE      OTDOCOMM,COMMLIN3
          ENDIF
.
APMT2000  MOVE      OTDNCUID,DESCCUID
          PACK      KEY10,OTDNCUID,SP70
          CALL      RDWBSE1
          IF        OVRCD<>1
            MOVE      WBSENAM,DESCCUID
          ENDIF
.
          MOVE      OTDNUUID,DESCUUID
          PACK      KEY10,OTDNUUID,SP70
          CALL      RDWBSE1
          IF        OVRCD<>1
            MOVE      WBSENAM,DESCUUID
          ENDIF
.
          UNPACK    OTDNADAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DISPDAT1,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          MOVE      SP70,DESCCOMP
          MATCH     "1",OTDNCOMP
          IF        @EQUAL
            MOVE      DYES,DESCCOMP
          ENDIF
.
          MOVE      SP70,DESCOUTC
          MATCH     SP70,OTDNOUTC
          IF        !@EQUAL
            PACK      KEY5,ANSF,ANSZ,OTDNOUTC,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      OTDNOUTC,TDESC
            ENDIF
            MOVE      TDESC,DESCOUTC
          ENDIF
.
          MOVE      SP70,REQCDESC
          MATCH     SP70,OTDNREQC
          IF        !@EQUAL
            PACK      KEY5,CODERS,OTDNREQC,SP70
            CALL      RDCODE1
            IF        OVRCD=0
              MOVE      TDESC,REQCDESC   * Requestor description (Cat rs)
            ENDIF
          ENDIF
.
          CLEAR     HTMLLINK
          APPEND    "javascript:UpdateMessage('",HTMLLINK
          APPEND    OTDNURNO,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    DISPDAT1,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    OTDNATIM,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    OTDNATYP,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    OTDNVISN,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
          REP       "#"'",DESCCUID
          REP       "#"'",COMMLIN1
          REP       "#"'",COMMLIN2
          REP       "#"'",COMMLIN3
          REP       "#"'",DESCOUTC
.
          WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":           * 0
                             "#"",OTDNURNO,"#",":                    * 1
                             "#"",OTDNADAT,OTDNATIM,"#",":           * 2
                             "#"",OTDNUDAT,OTDNUTIM,"#",":           * 3
                             "#"",DESCUUID,"#",":                    * 4
                             "#"",OTDNCDAT,OTDNCTIM,"#",":           * 5
                             "#"",DESCCUID,"#",":                    * 6
                             "#"",COMMLIN1,COMMLIN2,COMMLIN3,"#",":  * 7
                             "#"",OTDNCOMD,OTDNCOMT,"#",":           * 8
                             "#"",DESCCOMP,"#",":                    * 9
                             "#"",DESCOUTC,"#",":                    * 10
                             "#"",REQCDESC,"#");"                    * 11
.
          GOTO      APMT1000
.
APMT9999  RETURN
.
.------------------------------------------------------------
. Outpatient Direct Patient level update
.------------------------------------------------------------
PATA0000  MATCH     SP70,UPDTTYPE
          GOTO      PATA7000 IF EQUAL
.
          MATCH     "U",UPDTTYPE            * Update
          GOTO      PATA2000 IF EQUAL
.
          MATCH     "D",UPDTTYPE            * Delete
          GOTO      PATA3000 IF EQUAL
.
          GOTO      PATA9000
.
PATA1000 
          MOVE      ZERO,EXIT
          GOTO      PATA9999
.
PATA2000  PACK      KEY34,OTDAN001,OTDAN002,OTDAN003,OTDAN004,OTDAN005,SP70
          CALL      RDOTDAN1
          BRANCH    OVRCD,PATA9300
.
          CALL      MVOTDN00                     * Move cgi to file vars
.
          MATCH     "1",OTDNCOMP
          IF        @EQUAL
            PACK      OTDNCOMD,CCC,CYY,CMM,CDD
            REP       " 0",OTDNCOMD
            CLOCK     TIME,OTDNCOMT
            MOVE      USERID,OTDNCOMU
          ELSE
            MOVE      SP70,OTDNCOMU
            MOVE      SP70,OTDNCOMD
            MOVE      SP70,OTDNCOMT
          ENDIF
.
          PACK      OTDNUDAT,CCC,CYY,CMM,CDD
          REP       " 0",OTDNUDAT
          CLOCK     TIME,OTDNUTIM
          MOVE      USERID,OTDNUUID
.
          CALL      UPOTDAN1                     * Update request
.
          MOVE      OTDNURNO,COMMURNO
          MOVE      OTDNADAT,COMMADAT
          MOVE      OTDNATIM,COMMATIM
          MOVE      OTDNATYP,COMMTYPE            * Comment
          CALL      SAVCOM00                     * Update Comments
.
          MOVE      ZERO,EXIT
          GOTO      PATA9999
.
PATA3000  
          MOVE      ZERO,EXIT
          GOTO      PATA9999
.
PATA7000  MATCH     SP70,WBSEOPDU                * Not an OP direct user
          GOTO      PATA9200 IF EQUAL
.
          PACK      KEY8,URNUMBER,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,PATA9100
.
          CALL      RDPMPX21
          BRANCH    OVRCD,PATA9100
.
          PACK      KEY34,OTDAN001,OTDAN002,OTDAN003,OTDAN004,OTDAN005,SP70
          CALL      RDOTDAN1
          IF        OVRCD=1
            CALL      CLOUTDAN
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Outpatient Direct",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,PATA9999
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      PATA9999
.
PATA9000  MOVE      "Invalid form action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      PATA9999
.
PATA9100  MOVE      "Invalid U/R number.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      PATA9999
.
PATA9200  MOVE      "Invalid outpatient direct user.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      PATA9999
.
PATA9300  MOVE      "Invalid message record.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      PATA9999
.
PATA9999  CLOSE     COMFILAF,DELETE
          RETURN
.
.------------------------------------------------------------
. Patient level action table
.------------------------------------------------------------
ACTT0000  PACK      KEY34,URNUMBER,SP70
          CALL      RSOTDAN1
ACTT1000  CALL      RKOTDAN1
          BRANCH    OVRCD,ACTT9999
.
          MATCH     URNUMBER,OTDNURNO
          GOTO      ACTT9999 IF NOT EQUAL
.
          MATCH     SP70,FILTPATY
          IF        !@EQUAL
            MATCH     OTDNATYP,FILTPATY
            GOTO      ACTT1000 IF NOT EQUAL
          ENDIF
.
          PACK      COMMLIN1,SP70,SP70
          PACK      COMMLIN2,SP70,SP70
          PACK      COMMLIN3,SP70,SP70
.
          PACK      KEY29,OTDNURNO,OTDNADAT,OTDNATIM,OTDNATYP,SP70
          CALL      RSOTDCO1
ACTT1500  CALL      RKOTDCO1
          BRANCH    OVRCD,ACTT2000
.
          MATCH     OTDNURNO,OTDOURNO
          GOTO      ACTT2000 IF NOT EQUAL
.
          MATCH     OTDNADAT,OTDOADAT
          GOTO      ACTT2000 IF NOT EQUAL
.
          MATCH     OTDNATIM,OTDOATIM
          GOTO      ACTT2000 IF NOT EQUAL
.
          MATCH     OTDNATYP,OTDOATYP
          GOTO      ACTT2000 IF NOT EQUAL
.
          MATCH     SP70,COMMLIN1
          IF        @EQUAL
            MOVE      OTDOCOMM,COMMLIN1
            GOTO      ACTT1500
          ENDIF
.
          MATCH     SP70,COMMLIN2
          IF        @EQUAL
            MOVE      OTDOCOMM,COMMLIN2
            GOTO      ACTT1500
          ENDIF
.
          MATCH     SP70,COMMLIN3
          IF        @EQUAL
            MOVE      OTDOCOMM,COMMLIN3
          ENDIF
.
ACTT2000  UNPACK    OTDNADAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DISPDAT1,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          MOVE      SP70,DESCCOMP
          MOVE      SP70,COMPLINK
          MATCH     "1",OTDNCOMP
          IF        @EQUAL
            MOVE      DYES,DESCCOMP
          ENDIF
.
          MOVE      SP70,DESCATYP
          MOVE      SP70,DESCPEND
.
          MATCH     " 0",OTDNATYP
          IF        @EQUAL
            MOVE      "PMI Update",DESCATYP
          ENDIF
.
          MATCH     " 1",OTDNATYP
          IF        @EQUAL
            MOVE      "Appointment Message",DESCATYP
          ENDIF
.
          MATCH     " 2",OTDNATYP
          IF        @EQUAL
            MOVE      "Reschedule Appointment",DESCATYP
          ENDIF
.
          MATCH     " 3",OTDNATYP
          IF        @EQUAL
            MOVE      "Cancel Appointment",DESCATYP
          ENDIF
.
          MATCH     " 4",OTDNATYP
          IF        @EQUAL
            MOVE      "Pending Reschedule",DESCATYP
            MOVE      DYES,DESCPEND
          ENDIF
.
          MATCH     " 5",OTDNATYP
          IF        @EQUAL
            MOVE      "Closed Referral",DESCATYP
          ENDIF
.
          MOVE       SP70,DESCCLIN 
          MOVE       SP70,DESCAPPD
          MOVE       SP70,DESCCANC
          MOVE       SP70,DESCCLHR
.
          PACK      KEY8,OTDNVISN,SP70
          CALL      RDVISA1                      * Read Visit
          IF        OVRCD=1
            CALL     CLPATVIS
          ENDIF
.
          PACK      KEY8,OTDNVISN,SP70
          CALL      RDPMVX11                     * Read Visit Extn
          IF        OVRCD=1
            CALL      CLPMSVX1
          ENDIF
.
          MATCH     "10",PTVITYPE                * AH Referral
          GOTO      ACTT3000 IF EQUAL
.
          CALL      COTFIL00                     * Open correct outpatient files
.
          PACK      KEY8,OTDNVISN,SP70
          CALL      RDOTCAN1                     * Check if cancelled
          IF        OVRCD<>1
            MOVE      DYES,DESCCANC  
            PACK      DESCAPPD,OPCNDATE,OPCNTIME,SP70
            PACK      KEY20,OPCNSITE,OPCNCLIN,OPCNDATE,SP70
            CALL      RDCLIA1
            IF        OVRCD=1
              PACK      KEY20,OPCNSITE,OPCNCLIN,OPCNDATE,Z70
              CALL      RDSCLIA1
              CALL      RDPCLIA1                   * Read Clinic Record
              BRANCH    OVRCD,ACTT4000
.
              MATCH     OPCNSITE,OCASITE
              GOTO      ACTT4000 IF NOT EQUAL
.
              MATCH     OPCNCLIN,OCACLIN
              GOTO      ACTT4000 IF NOT EQUAL
            ENDIF
.
            PACK      KEY25,OPCNSITE,OPCNCLIN,OPCNDATE,OPCNSTRT,SP70
            CALL      RDSESA1
            BRANCH    OVRCD,ACTT1000
.
            PACK      KEY18,OSESITE,OSECLIN,OSEDAY,OSESTRT,SP70
            CALL      RDMASA1
            BRANCH    OVRCD,ACTT1000
.
            PACK      KEY28,OSESITE,OSECLIN,OSEDAY,OSESTRT,OSESSHNO,OSESSDAT,SP70
            CALL      RDOTHED1
            BRANCH    OVRCD,ACTT1000
.
            MATCH     "1",OTHECLHR
            IF        @EQUAL
              MOVE      DYES,DESCCLHR              * Clinical high risk
            ENDIF
.
            MOVE      OCADESC,DESCCLIN
            GOTO      ACTT4000
          ENDIF
.
          MATCH     SP70,OTDNVISN
          GOTO      ACTT4000 IF EQUAL
.
          PACK      KEY36,OTDNVISN,SP70
          CALL      RDSBOKA6
          CALL      RDKBOKA6                     * Read OP Booking
          BRANCH    OVRCD,ACTT4000
.
          MATCH     OTDNVISN,DOBAOUTN
          GOTO      ACTT4000 IF NOT EQUAL
.
          PACK      DESCAPPD,OBADATE,OBATIME,SP70
.
          PACK      KEY20,OBASITE,OBACLIN,OBADATE,SP70
          CALL      RDCLIA1
          IF        OVRCD=1
            PACK      KEY20,OBASITE,OBACLIN,OBADATE,Z70
            CALL      RDSCLIA1                                        
            CALL      RDPCLIA1                   * Read Clinic Record
            BRANCH    OVRCD,ACTT4000
.
            MATCH     OBASITE,OCASITE
            GOTO      ACTT4000 IF NOT EQUAL
.
            MATCH     OBACLIN,OCACLIN
            GOTO      ACTT4000 IF NOT EQUAL
          ENDIF

          MOVE      OCADESC,DESCCLIN
.
          PACK      KEY8,OTDNVISN,SP70
          CALL      RDBOKB1
          BRANCH    OVRCD,ACTT4000
.       
          MATCH     "1",OTBBCLHR                
          IF        @EQUAL
            MOVE      DYES,DESCCLHR              * Clinical high risk
          ENDIF
          GOTO      ACTT4000
.
ACTT3000  PACK      KEY8,OTDNVISN,SP70         
          CALL      RDALREF1                     * Read AH Referral
          IF        OVRCD=1
            CALL      CLALLREF
          ENDIF
.           
ACTT4000  MOVE      SP70,PTHSNAME
          MATCH     SP70,PMVXMHOS
          IF        !@EQUAL
            PACK      KEY3,PMVXMHOS,SP70         * Read hospital
            CALL      RDPTHSP1
            IF        OVRCD=1
              MOVE      PMVXMHOS,PTHSNAME
            ENDIF
          ENDIF
.
          MOVE      SP70,REQCDESC
          MATCH     SP70,OTDNREQC
          IF        !@EQUAL
            PACK      KEY5,CODERS,OTDNREQC,SP70
            CALL      RDCODE1
            IF        OVRCD=0
              MOVE      TDESC,REQCDESC   * Requestor description (Cat rs)
            ENDIF
          ENDIF
.
          MOVE      SP70,DESCCOMP
          MOVE      SP70,COMPLINK
          MOVE      ZERO,COMMTFLG
.
          MATCH     "1",OTDNCOMP
          IF        @EQUAL
            MOVE      DYES,DESCCOMP
.
            MOVE      ONE,COMMTFLG
            CLEAR     COMPLINK
            APPEND    "javascript:ViewComplete('",COMPLINK
            APPEND    OTDNURNO,COMPLINK
            APPEND    "','",COMPLINK
            APPEND    DISPDAT1,COMPLINK
            APPEND    "','",COMPLINK
            APPEND    OTDNATIM,COMPLINK
            APPEND    "','",COMPLINK
            APPEND    OTDNATYP,COMPLINK
            APPEND    "','",COMPLINK
            APPEND    OTDNVISN,COMPLINK
            APPEND    "')",COMPLINK
            RESET     COMPLINK
          ENDIF
.
          MATCH     " 5",OTDNATYP
          IF        @EQUAL
            PACK      KEY16,OTDNVISN,SP70
            CALL      RSALLNK1
            CALL      RKALLNK1
            BRANCH    OVRCD,ACTT5000
.
            MATCH     OTDNVISN,ALLNVISN
            GOTO      ACTT5000 IF NOT EQUAL
.
            PACK      KEY8,ALLNLNKV,SP70
            CALL      RDBOKB1
            BRANCH    OVRCD,ACTT5000
.
            MATCH     "1",OTBBCLHR
            IF        @EQUAL
              MOVE      DYES,DESCCLHR              * Clinical high risk
            ENDIF
          ENDIF

.
ACTT5000  CLEAR     HTMLLINK
          APPEND    "javascript:UpdateAction('",HTMLLINK
          APPEND    OTDNURNO,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    DISPDAT1,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    OTDNATIM,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    OTDNATYP,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    OTDNVISN,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
          REP       "#"'",COMMLIN1
          REP       "#"'",COMMLIN2
          REP       "#"'",COMMLIN3
          REP       "#"'",DESCOUTC
          REP       "#"'",PTHSNAME
.
          WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":           * 0
                             "#"",OTDNADAT,OTDNATIM,"#",":           * 1
                             "#"",DESCATYP,"#",":                    * 2
                             "#"",COMMLIN1,COMMLIN2,COMMLIN3,"#",":  * 3
                             "#"",DESCCOMP,"#",":                    * 4
                             "#"",PTHSNAME,"#",":                    * 5
                             "#"",DESCCLIN,"#",":                    * 6
                             "#"",DESCAPPD,"#",":                    * 7
                             "#"",DESCCANC,"#",":                    * 8
                             "#"",OTDNADAT,OTDNATIM,"#",":           * 9
                             "#"",DESCCLHR,"#",":                    * 10
                             "#"",DESCCOMP,"#",":                    * 11
                             "#"",DESCPEND,"#",":                    * 12
                             "#"",COMMTFLG,"#",":                    * 13
                             "#"",COMPLINK,"#",":                    * 14
                             "#"",REQCDESC,"#");"                    * 15
.
          GOTO      ACTT1000
.
ACTT9999  RETURN
.
.*******************************************************************************
.                     Check Correct Files are Open for System
.*******************************************************************************
OUTFIL00  MOVE      WBSESIT,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD,OUTFIL90
          MATCH     OSTFILE,CFILEPRE         * Save Current File Prefix
          IF        !@EQUAL
            MOVE      OSTFILE,CFILEPRE       * Save Current File Prefix
            CALL      OPNOUT00               * Open Outpatient Files
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      OUTFIL99
.
OUTFIL90  MOVE      "Outpatient Site Invalid",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
OUTFIL99  RETURN
.
.*******************************************************************************
.                    Open Outpatient Files
.*******************************************************************************
OPNOUT00  PACK      CFNAME,CFILEPRE,FILBOKA6  * Get the name of the BOKA6 file
          CLOSE     OUTBOKA6
          OPEN      OUTBOKA6,CFNAME           * Open the file
.
          PACK      CFNAME,CFILEPRE,FILCLIA1  * Get the name of the CLIA1 file
          CLOSE     OUTCLIA1
          OPEN      OUTCLIA1,CFNAME           * Open the file
.
          PACK      CFNAME,CFILEPRE,FILCANA1  * Get the name of the CANA1 file
          CLOSE     OUTCANA1
          OPEN      OUTCANA1,CFNAME           * Open the file
.
          PACK      CFNAME,CFILEPRE,FILBB1A1  * Get the name of the BB1A1 file
          CLOSE     OUTBB1A1
          OPEN      OUTBB1A1,CFNAME           * Open the file
.
          PACK      CFNAME,CFILEPRE,FILCTYA1  * Get the name of the CTYA1 file
          CLOSE     OUTCTYA1
          OPEN      OUTCTYA1,CFNAME           * Open the file
.
          PACK      CFNAME,CFILEPRE,FILRSHA3  * Get the name of the RSHA3 file
          CLOSE     OUTRSHA3
          OPEN      OUTRSHA3,CFNAME           * Open the file
.
          PACK      CFNAME,CFILEPRE,FILHEDA1  * Get the name of the HEDA1 file
          CLOSE     OUTHEDA1
          OPEN      OUTHEDA1,CFNAME           * Open the file
.
          PACK      CFNAME,CFILEPRE,FILSESA1  * Get the name of the SESA1 file
          CLOSE     OUTSESA1
          OPEN      OUTSESA1,CFNAME           * Open the file
.
          PACK      CFNAME,CFILEPRE,FILMA1A1  * Get the name of the MA1A1 file
          CLOSE     OUTMA1A1
          OPEN      OUTMA1A1,CFNAME           * Open the file
.
OPNOUT99  RETURN
.
.*******************************************************************************
. COTFIL00 - Check that the correct outpatient site prefix files are open
.            according to the visit files site code. If the site is invalid
.            or blank just open the default site of out.                        
.*******************************************************************************
COTFIL00  MATCH     SP70,PVISITE             * Does the visit have a site
          GOTO      COTFIL50 IF NOT EQUAL
.
COTFIL10  MATCH     "out",CFILEPRE           * Is out currently open
          IF        !@EQUAL
            MOVE      "out",OSTFILE          * open default prefix of out
            MOVE      OSTFILE,CFILEPRE       * Save Current File Prefix
            CALL      OPNOUT00               * Open out Outpatient Files
          ENDIF
.
          GOTO      COTFIL99
.
COTFIL50  MOVE      PVISITE,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD,COTFIL10           * error invalid site code
.
          MATCH     OSTFILE,CFILEPRE         * Save Current File Prefix
          IF        !@EQUAL
            MOVE      OSTFILE,CFILEPRE       * Save Current File Prefix
            CALL      OPNOUT00               * Open prefixed Outpatient Files
          ENDIF
.
          GOTO      COTFIL99
.
COTFIL99  RETURN
.
.------------------------------------------------------------
. Display visit details associated with the OPD action
.------------------------------------------------------------
DVIS0000  PACK      KEY8,OTDNVISN,SP70
          PACK      KEY8,OTDNVISN,SP70
          CALL      RDVISA1                      * Read Visit
          IF        OVRCD=1
            CALL     CLPATVIS
          ENDIF
.
          MATCH     "10",PTVITYPE                * AH Referral
          GOTO      DVIS1000 IF EQUAL
.
          CALL      COTFIL00                     * Open correct outpatient files
.
          PACK      KEY8,OTDNVISN,SP70
          CALL      RDOTCAN1                     * Cancelled OP Appointment
          IF        OVRCD<>1
            GOTO      DVIS2000       
          ENDIF
.
          PACK      KEY36,OTDNVISN,SP70
          CALL      RDSBOKA6
          CALL      RDKBOKA6                     * Read OP Booking
          BRANCH    OVRCD,DVIS9999
.
          MATCH     OTDNVISN,DOBAOUTN
          GOTO      DVIS3000 IF EQUAL
.
          GOTO      DVIS9999
.
. Display AH Referral info
.
DVIS1000  PACK      KEY8,OTDNVISN,SP70
          CALL      RDALREF1                     * Read AH Referral
          BRANCH    OVRCD,DVIS9999
.
          PACK      KEY5,ANSC,ANSG,ALREDEPT,SP70
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE      ALREDEPT,TDESC
          ENDIF
.
          UNPACK    ALRERDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DISPDAT1,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          WRITE     HTMLFILE;"<tr><td colspan=4 align=left class=DataField>":
                             "Referral Details:":
                             "</td></tr>"
.
          WRITE     HTMLFILE;"<tr>":
                             "<td class=DataLabel>Department:</td>":
                             "<td class=DataField>",TDESC,"</td>":
                             "<td class=DataLabel>Referral Date:</td>":
                             "<td class=DataField>",DISPDAT1,"</td>":
                             "</tr>"
.
          UNPACK    ALREDCLO,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DISPDAT1,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          MOVE     ALLSTAT0,KEY10
          MOVE     ZERO,FORM1
          MOVE     ALRESTAT,FORM1
          LOAD     KEY10,FORM1,ALLSTAT1,ALLSTAT2,ALLSTAT3,ALLSTAT4:
                                 ALLSTAT5
.
          WRITE     HTMLFILE;"<tr>":
                             "<td class=DataLabel>Status:</td>":
                             "<td class=DataField>",KEY10,"</td>":
                             "<td class=DataLabel>Date Closed:</td>":
                             "<td class=DataField>",DISPDAT1,"</td>":
                             "</tr>"
          GOTO      DVIS9999
.
. Display Cancelled OP Appointment
.
DVIS2000  PACK      KEY20,OPCNSITE,OPCNCLIN,OPCNDATE,SP70
          CALL      RDCLIA1
          IF        OVRCD=1
            PACK      KEY20,OPCNSITE,OPCNCLIN,OPCNDATE,Z70
            CALL      RDSCLIA1
            CALL      RDPCLIA1                   * Read Clinic Record
            BRANCH    OVRCD,DVIS2100
.
            MATCH     OPCNSITE,OCASITE
            GOTO      DVIS2100 IF NOT EQUAL
.
            MATCH     OPCNCLIN,OCACLIN
            GOTO      DVIS2100 IF NOT EQUAL
          ENDIF
          MOVE      OCADESC,KEY30
.
DVIS2100  PACK      KEY12,OPCNSITE,OPCNCTYP,SP70
          CALL      RDCTYA1
          IF        OVRCD=1
            PACK      OCTDESC,OPCNCTYP,SP70
          ENDIF
.
          UNPACK    OPCNDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DISPDAT1,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          WRITE     HTMLFILE;"<tr><td colspan=4 align=left class=DataField>":
                             "Cancelled Appointment Details:":
                             "</td></tr>"
.
          WRITE     HTMLFILE;"<tr>":
                             "<td class=DataLabel>Clinic Type:</td>":
                             "<td class=DataField>",OCTDESC,"</td>":
                             "<td class=DataLabel>Appointment Date:</td>":
                             "<td class=DataField>",DISPDAT1,"</td>":
                             "</tr>"
.
          WRITE     HTMLFILE;"<tr>":
                             "<td class=DataLabel>Clinic Id:</td>":
                             "<td class=DataField>",KEY30,"</td>":
                             "<td class=DataLabel>Appointment Time:</td>":
                             "<td class=DataField>",OPCNTIME,"</td>":
                             "</tr>"
.
          WRITE     HTMLFILE;"<tr>":
                             "<td class=DataLabel>Status:</td>":
                             "<td class=DataField>Cancelled</td>":
                             "<td class=DataLabel></td>":
                             "<td class=DataField></td>":
                             "</tr>"
.
          MATCH     " 4",OTDNATYP           * Pending Reschedule
          GOTO      DVIS9999 IF NOT EQUAL
.
          PACK      KEY32,OTDNURNO,OTDNREFN,OPCNRDTE,OPCNRTIM,SP70
          CALL      RDOTART1
          BRANCH    OVRCD,DVIS9999
.
          PACK      KEY12,OTARPRSI,OTARCLTY,SP70
          CALL      RDCTYA1
          IF        OVRCD=1
            PACK      OCTDESC,OTARCLTY,SP70
          ENDIF
.
          MOVE      SP70,DISPDAT1
          MATCH     SP70,OTARPRDT
          IF        !@EQUAL
            UNPACK    OTARPRDT,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      DISPDAT1,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          ENDIF
.
          PACK      KEY20,OTARPRSI,OTARCLID,OTARPRDT,SP70
          CALL      RDCLIA1
          IF        OVRCD=1
            PACK      KEY20,OTARPRSI,OTARCLID,OPCNDATE,Z70
            CALL      RDSCLIA1
            CALL      RDPCLIA1                   * Read Clinic Record
            BRANCH    OVRCD,DVIS2200
.
            MATCH     OTARPRSI,OCASITE
            GOTO      DVIS2200 IF NOT EQUAL
.
            MATCH     OTARCLID,OCACLIN
            GOTO      DVIS2200 IF NOT EQUAL
          ENDIF
          MOVE      OCADESC,KEY30
. 
DVIS2200  WRITE     HTMLFILE;"<tr><td colspan=4><hr></td></tr>"
.
          WRITE     HTMLFILE;"<tr><td colspan=4 align=left class=DataField>":
                             "Requested Appointment Details:":
                             "</td></tr>"
.

          WRITE     HTMLFILE;"<tr>":
                             "<td class=DataLabel>Clinic Type:</td>":
                             "<td class=DataField>",OCTDESC,"</td>":
                             "<td class=DataLabel>Preferred Date:</td>":
                             "<td class=DataField>",DISPDAT1,"</td>":
                             "</tr>"
.
          WRITE     HTMLFILE;"<tr>":
                             "<td class=DataLabel>Clinic Id:</td>":
                             "<td class=DataField>",KEY30,"</td>":
                             "<td class=DataLabel></td>":
                             "<td class=DataField></td>":
                             "</tr>"

.
          GOTO      DVIS9999
.
. Display OP Appointment 
.
DVIS3000  PACK      KEY12,OBASITE,OBACTYP,SP70
          CALL      RDCTYA1
          IF        OVRCD=1
            PACK      OCTDESC,OBACTYP,SP70
          ENDIF
.
          UNPACK    OBADATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DISPDAT1,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          PACK      KEY20,OBASITE,OBACLIN,OBADATE,SP70
          CALL      RDCLIA1
          IF        OVRCD=1
            PACK      KEY20,OBASITE,OBACLIN,OBADATE,Z70
            CALL      RDSCLIA1
            CALL      RDPCLIA1                   * Read Clinic Record
            BRANCH    OVRCD,DVIS3100
.
            MATCH     OBASITE,OCASITE
            GOTO      DVIS3100 IF NOT EQUAL
.
            MATCH     OBACLIN,OCACLIN
            GOTO      DVIS3100 IF NOT EQUAL
          ENDIF
          MOVE      OCADESC,KEY30
.
DVIS3100  WRITE     HTMLFILE;"<tr><td colspan=4 align=left class=DataField>":
                             "Current Appointment Details:":
                             "</td></tr>"
.
          WRITE     HTMLFILE;"<tr>":
                             "<td class=DataLabel>Clinic Type:</td>":
                             "<td class=DataField>",OCTDESC,"</td>":
                             "<td class=DataLabel>Appointment Date:</td>":
                             "<td class=DataField>",DISPDAT1,"</td>":
                             "</tr>"
.
          WRITE     HTMLFILE;"<tr>":
                             "<td class=DataLabel>Clinic Id:</td>":
                             "<td class=DataField>",KEY30,"</td>":
                             "<td class=DataLabel>Appointment Time:</td>":
                             "<td class=DataField>",OBATIME,"</td>":
                             "</tr>"
.
          MOVE      SP70,KEY10
          IF        OBASTAT=1
            MOVE      "Booked",KEY10
          ENDIF
.
          IF        OBASTAT=4
            MOVE      "Attended",KEY10
          ENDIF
.
          IF        OBASTAT=5
            MOVE      "DNA",KEY10
          ENDIF
.
          IF        OBASTAT=6
            MOVE      "Suspended",KEY10
          ENDIF
.
          WRITE     HTMLFILE;"<tr>":
                             "<td class=DataLabel>Status:</td>":
                             "<td class=DataField>",KEY10,"</td>":
                             "<td class=DataLabel></td>":
                             "<td class=DataField></td>":
                             "</tr>"
.
          MATCH     " 2",OTDNATYP                * Display rescheduled appt
          GOTO      DVIS9999 IF NOT EQUAL
.
          WRITE     HTMLFILE;"<tr><td colspan=4><hr></td></tr>"
.
          WRITE     HTMLFILE;"<tr><td colspan=4 align=left class=DataField>":
                             "Rescheduled Appointment Details:":
                             "</td></tr>"
.
          PACK      KEY25,OBASITE,OTDNADAT,OTDNVISN,SP70
          CALL      RDSRSHA3
DVIS3150  CALL      RDKRSHA3                * Get rescheduled Appt for action 
          BRANCH    OVRCD,DVIS9999          * date and time
.
          MATCH     OBASITE,OPRSSITE
          GOTO      DVIS9999 IF NOT EQUAL
.
          MATCH     OTDNADAT,OPRSRDTE
          GOTO      DVIS9999 IF NOT EQUAL
.
          MATCH     OTDNVISN,OTDNVISN
          GOTO      DVIS9999 IF NOT EQUAL
.
          MATCH     OTDNATIM,OPRSRTIM
          GOTO      DVIS3150 IF NOT EQUAL
.
          PACK      KEY12,OPRSSITE,OPRSCTYP,SP70
          CALL      RDCTYA1
          IF        OVRCD=1
            PACK      OCTDESC,OPRSCTYP,SP70
          ENDIF
.
          UNPACK    OPRSDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DISPDAT1,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          PACK      KEY20,OPRSSITE,OPRSCLIN,OPRSDATE,SP70
          CALL      RDCLIA1
          IF        OVRCD=1
            PACK      KEY20,OPRSSITE,OPRSCLIN,OPRSDATE,Z70
            CALL      RDSCLIA1
            CALL      RDPCLIA1                   * Read Clinic Record
            BRANCH    OVRCD,DVIS3200
.
            MATCH     OPRSSITE,OCASITE
            GOTO      DVIS3200 IF NOT EQUAL
.
            MATCH     OPRSCLIN,OCACLIN
            GOTO      DVIS3200 IF NOT EQUAL
          ENDIF
          MOVE      OCADESC,KEY30
.
DVIS3200  WRITE     HTMLFILE;"<tr>":
                             "<td class=DataLabel>Clinic Type:</td>":
                             "<td class=DataField>",OCTDESC,"</td>":
                             "<td class=DataLabel>Appointment Date:</td>":
                             "<td class=DataField>",DISPDAT1,"</td>":
                             "</tr>"
.
          WRITE     HTMLFILE;"<tr>":
                             "<td class=DataLabel>Clinic Id:</td>":
                             "<td class=DataField>",KEY30,"</td>":
                             "<td class=DataLabel>Appointment Time:</td>":
                             "<td class=DataField>",OPRSTIME,"</td>":
                             "</tr>"
.
          WRITE     HTMLFILE;"<tr>":
                             "<td class=DataLabel>Status:</td>":
                             "<td class=DataField>Rescheduled</td>":
                             "<td class=DataLabel></td>":
                             "<td class=DataField></td>":
                             "</tr>"
.
          GOTO      DVIS9999

.
DVIS9999  RETURN
.
.------------------------------------------------------------
. Hospital level outpatient direct actions list
.------------------------------------------------------------
HOSA0000  MATCH     SP70,UPDTTYPE
          GOTO      HOSA7000 IF EQUAL
.
          GOTO      HOSA9000
.
HOSA7000  MATCH     SP70,WBSEOPDU                * Not an OP direct user
          GOTO      HOSA9100 IF EQUAL
.
          CALL      CURPER00   * Calculate Next and Last Period Dates
          CALL      CALCDT00   * Calculate Next and Last Period Dates
.
          MATCH     SP70,FILTADAT
          IF        @EQUAL
            MATCH     SP70,FILTVDAT
            IF        @EQUAL
              MOVE      ONE,FILTADAT             * Default Action date filter
            ENDIF
          ENDIF
.
          IF        SHOWFLAG=0
            PACK      FILTLOCA,WBSELTYP,SP70     * Default location filter
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Outpatient Direct",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,HOSA9999
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      HOSA9999
.
HOSA9000  MOVE      "Invalid form action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      HOSA9999
.
HOSA9100  MOVE      "Invalid outpatient direct user.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      PATA9999
.
HOSA9999  RETURN
.
.------------------------------------------------------------
. Hospital Selection - List of Hospitals allowed to User
.------------------------------------------------------------
HSEL0000  PACK      KEY3,SP70
          CALL      RSPTHSP1
HSEL0100  CALL      RKPTHSP1
          BRANCH    OVRCD,HSEL9999
.
          PACK      KEY13,USERID,SP70                * All hospital access
          CALL      RDMLSEC1
          IF        OVRCD=1
            PACK      KEY13,USERID,PTHSHOSP,SP70     * This hospital access
            CALL      RDMLSEC1
            BRANCH    OVRCD,HSEL0100
          ENDIF
.
          MOVE      PTHSNAME,KEY50
          PACK      KEY5,QUOTE,PTHSHOSP,QUOTE
          MATCH     SELECHOS,PTHSHOSP
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=",KEY5," selected>":
                               KEY50,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=",KEY5,">",KEY50,"</option>"
          ENDIF
          GOTO      HSEL0100
.
HSEL9999  RETURN
.
.------------------------------------------------------------
. Next Day, Week, Month
.------------------------------------------------------------
NEXT0000  MOVE      TEMPLATE,F3
          MOVE      F3,KEY3
          REP       " 0",KEY3
          MOVE      REPORTNO,F2
          MOVE      F2,KEY2
          REP       " 0",KEY2
.
          REP       " +",FILTHOSP
          REP       " +",FILTATYP
          REP       " +",FILTPATY
          REP       " +",FILTSPEC
          REP       " +",FILTCLID
          REP       " +",FILTCLTY
          REP       " +",FILTCOMP
          REP       " +",FILTADAT
          REP       " +",FILTVDAT
          REP       " +",FILTLOCA
.
          WRITE     HTMLFILE;"outweb09.pbl":
                             "?reportno=",KEY2:
                             "&template=",KEY3:
                             "&frstdate=",NXTFDATE:
                             "&lastdate=",NXTLDATE:
                             "&viewtype=",VIEWTYPE:
                             "&filthosp=",FILTHOSP:
                             "&filtatyp=",FILTATYP:
                             "&filtpaty=",FILTPATY:
                             "&filtspec=",FILTSPEC:
                             "&filtclid=",FILTCLID:
                             "&filtclty=",FILTCLTY:
                             "&filtcomp=",FILTCOMP:
                             "&filtadat=",FILTADAT:
                             "&filtvdat=",FILTVDAT:
                             "&filtloca=",FILTLOCA:
                             "&showflag=",SHOWFLAG;
.
          REP       "+ ",FILTHOSP
          REP       "+ ",FILTATYP
          REP       "+ ",FILTPATY
          REP       "+ ",FILTSPEC
          REP       "+ ",FILTCLID
          REP       "+ ",FILTCLTY
          REP       "+ ",FILTCOMP
          REP       "+ ",FILTADAT
          REP       "+ ",FILTVDAT
          REP       "+ ",FILTLOCA
.
NEXT9999  RETURN
.------------------------------------------------------------
. Previous Day, Week, Month
.------------------------------------------------------------
PREV0000  MOVE      TEMPLATE,F3
          MOVE      F3,KEY3
          REP       " 0",KEY3
          MOVE      REPORTNO,F2
          MOVE      F2,KEY2
          REP       " 0",KEY2
.
          REP       " +",FILTHOSP
          REP       " +",FILTATYP
          REP       " +",FILTPATY
          REP       " +",FILTSPEC
          REP       " +",FILTCLID
          REP       " +",FILTCLTY
          REP       " +",FILTCOMP
          REP       " +",FILTADAT
          REP       " +",FILTVDAT
          REP        " +",FILTLOCA
.
          WRITE     HTMLFILE;"outweb09.pbl":
                             "?reportno=",KEY2:
                             "&template=",KEY3:
                             "&frstdate=",PREFDATE:
                             "&lastdate=",PRELDATE:
                             "&viewtype=",VIEWTYPE:
                             "&filthosp=",FILTHOSP:
                             "&filtatyp=",FILTATYP:
                             "&filtpaty=",FILTPATY:
                             "&filtspec=",FILTSPEC:
                             "&filtclid=",FILTCLID:
                             "&filtclty=",FILTCLTY:
                             "&filtcomp=",FILTCOMP:
                             "&filtadat=",FILTADAT:
                             "&filtvdat=",FILTVDAT:
                             "&filtloca=",FILTLOCA:
                             "&showflag=",SHOWFLAG;
.
          REP       "+ ",FILTHOSP
          REP       "+ ",FILTATYP
          REP       "+ ",FILTPATY
          REP       "+ ",FILTSPEC
          REP       "+ ",FILTCLID
          REP       "+ ",FILTCLTY
          REP       "+ ",FILTCOMP
          REP       "+ ",FILTADAT
          REP       "+ ",FILTVDAT
          REP       "+ ",FILTLOCA
.
PREV9999  RETURN
.
.------------------------------------------------------------
. Hospital level action table
.------------------------------------------------------------
HATB0000  MATCH     "1",FILTADAT
          IF        @EQUAL
            PACK      KEY34,FRSTDATE,SP70        * By Action Date
          ELSE
            PACK      KEY42,FRSTDATE,SP70        * By Visit Date
          ENDIF
.
          MATCH     "1",FILTADAT
          IF        @EQUAL
            CALL      RSOTDAN3                   * By Action Date
          ELSE
            CALL      RSOTDAN4                   * By Appointment Date
          ENDIF
HATB1000  MATCH     "1",FILTADAT
          IF        @EQUAL
            CALL      RKOTDAN3                   * By Action Date
          ELSE
            CALL      RKOTDAN4                   * By Appointment Date
          ENDIF
          BRANCH    OVRCD,HATB9999
.
          MATCH     "1",FILTADAT
          IF        @EQUAL
            MATCH     OTDNADAT,LASTDATE          * Exit if after action date to
            GOTO      HATB9999 IF LESS
.
            MATCH     "1",FILTVDAT
            IF        @EQUAL
              MATCH     FRSTDATE,OTDNVDAT        * skip if before visit date to
              GOTO      HATB1000 IF LESS
.
              MATCH     OTDNVDAT,LASTDATE        * skip if after visit date to
              GOTO      HATB1000 IF LESS
            ENDIF
          ELSE
            MATCH     OTDNVDAT,LASTDATE          * Exit if after visit date to
            GOTO      HATB9999 IF LESS
          ENDIF
.
          MATCH     SP70,FILTATYP
          IF        !@EQUAL
            MATCH     OTDNATYP,FILTATYP          * Action Type Filter
            GOTO      HATB1000 IF NOT EQUAL
          ENDIF
.
          MATCH     "1",FILTCOMP
          IF        @EQUAL
            MATCH     "1",OTDNCOMP               * Action Complete
            GOTO      HATB1000 IF NOT EQUAL
          ELSE
            MATCH     "1",OTDNCOMP               * Action Complete
            GOTO      HATB1000 IF EQUAL
          ENDIF
.
          PACK      COMMLIN1,SP70,SP70
          PACK      COMMLIN2,SP70,SP70
          PACK      COMMLIN3,SP70,SP70
.
          PACK      KEY29,OTDNURNO,OTDNADAT,OTDNATIM,OTDNATYP,SP70
          CALL      RSOTDCO1
HATB1500  CALL      RKOTDCO1
          BRANCH    OVRCD,HATB2000
.
          MATCH     OTDNURNO,OTDOURNO
          GOTO      HATB2000 IF NOT EQUAL
.
          MATCH     OTDNADAT,OTDOADAT
          GOTO      HATB2000 IF NOT EQUAL
.
          MATCH     OTDNATIM,OTDOATIM
          GOTO      HATB2000 IF NOT EQUAL
.
          MATCH     OTDNATYP,OTDOATYP
          GOTO      HATB2000 IF NOT EQUAL
.
          MATCH     SP70,COMMLIN1
          IF        @EQUAL
            MOVE      OTDOCOMM,COMMLIN1
            GOTO      HATB1500
          ENDIF
.
          MATCH     SP70,COMMLIN2
          IF        @EQUAL
            MOVE      OTDOCOMM,COMMLIN2
            GOTO      HATB1500
          ENDIF
.
          MATCH     SP70,COMMLIN3
          IF        @EQUAL
            MOVE      OTDOCOMM,COMMLIN3
          ENDIF
.
HATB2000  UNPACK    OTDNADAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DISPDAT1,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          MOVE      SP70,DESCCOMP
          MOVE      SP70,COMPLINK
          MATCH     "1",OTDNCOMP
          IF        @EQUAL
            MOVE      DYES,DESCCOMP
          ENDIF
.
          MOVE      SP70,DESCATYP
          MOVE      SP70,DESCPEND
.
          MATCH     " 0",OTDNATYP
          IF        @EQUAL
            MOVE      "PMI Update",DESCATYP
          ENDIF
.
          MATCH     " 1",OTDNATYP
          IF        @EQUAL
            MOVE      "Appointment Message",DESCATYP
          ENDIF
.
          MATCH     " 2",OTDNATYP
          IF        @EQUAL
            MOVE      "Reschedule Appointment",DESCATYP
          ENDIF
.
          MATCH     " 3",OTDNATYP
          IF        @EQUAL
            MOVE      "Cancel Appointment",DESCATYP
          ENDIF
.
          MATCH     " 4",OTDNATYP
          IF        @EQUAL
            MOVE      "Pending Reschedule",DESCATYP
            MOVE      DYES,DESCPEND
          ENDIF
.
          MATCH     " 5",OTDNATYP
          IF        @EQUAL
            MOVE      "Closed Referral",DESCATYP
          ENDIF
.
          MOVE       SP70,DESCCLIN
          MOVE       SP70,DESCAPPD
          MOVE       SP70,DESCCANC
          MOVE       SP70,DESCCLHR
.
          PACK      KEY8,OTDNVISN,SP70
          CALL      RDVISA1                      * Read Visit
          IF        OVRCD=1
            CALL     CLPATVIS
          ENDIF
.
          PACK      KEY8,OTDNVISN,SP70
          CALL      RDPMVX11                     * Read Visit Extn
          IF        OVRCD=1
            CALL      CLPMSVX1
          ELSE
            MATCH     SP70,FILTHOSP
            IF        !@EQUAL
              MATCH     PMVXMHOS,FILTHOSP        * Hospital Filter
              GOTO      HATB1000 IF NOT EQUAL
            ENDIF
          ENDIF
.
          MATCH     "10",PTVITYPE                * AH Referral
          GOTO      HATB3000 IF EQUAL
.
          CALL      COTFIL00                     * Open correct outpatient files
.
          PACK      KEY8,OTDNVISN,SP70
          CALL      RDOTCAN1                     * Check if cancelled
          IF        OVRCD<>1
            MOVE      DYES,DESCCANC
            PACK      DESCAPPD,OPCNDATE,OPCNTIME,SP70
.
            CALL      CCTY0000
            BRANCH    EXIT,HATB1000
.
            MATCH     SP70,FILTCLID
            IF        !@EQUAL
              MATCH     FILTCLID,OPCNCLIN 
              GOTO      HATB1000 IF NOT EQUAL
            ENDIF
.
            MATCH     SP70,FILTCLTY
            IF        !@EQUAL
              MATCH     FILTCLTY,OPCNCTYP          * Clinic Type Filter
              GOTO      HATB1000 IF NOT EQUAL
            ENDIF
.
            MATCH     SP70,FILTHOSP
            IF        !@EQUAL
              MATCH     FILTHOSP,OTHEHOSP          * Clinic Id Filter
              GOTO      HATB1000 IF NOT EQUAL
            ENDIF
.
            MATCH     SP70,FILTLOCA
            IF        !@EQUAL
              PACK      KEY25,OPCNSITE,OPCNCLIN,OPCNDATE,OPCNSTRT,SP70
              CALL      RDSESA1
              BRANCH    OVRCD,HATB1000
.
              PACK      KEY18,OSESITE,OSECLIN,OSEDAY,OSESTRT,SP70
              CALL      RDMASA1
              BRANCH    OVRCD,HATB1000
.
              MATCH     FILTLOCA,OTMALTYP
              GOTO      HATB1000 IF NOT EQUAL
            ENDIF
.
            MOVE      OTHEHOSP,PMVXMHOS
            MOVE      OCADESC,DESCCLIN
.
            MATCH     "1",OTHECLHR
            IF        @EQUAL
              MOVE      DYES,DESCCLHR              * Clinical high risk
            ENDIF
.
            GOTO      HATB4000
          ENDIF
.
          MATCH     SP70,OTDNVISN
          GOTO      HATB4000 IF EQUAL
.
          PACK      KEY36,OTDNVISN,SP70
          CALL      RDSBOKA6
          CALL      RDKBOKA6                     * Read OP Booking
          BRANCH    OVRCD,HATB4000
.
          MATCH     OTDNVISN,DOBAOUTN
          GOTO      HATB4000 IF NOT EQUAL
.
          MATCH     SP70,FILTSPEC                * Special Arrangements Filter
          IF        !@EQUAL
            MATCH     FILTSPEC,OTBBSPCA
            GOTO      HATB1000 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,FILTCLID
          IF        !@EQUAL
            MATCH     FILTCLID,OBACLIN             * Clinic Id Filter
            GOTO      HATB1000 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,FILTCLTY
          IF        !@EQUAL
            MATCH     FILTCLTY,OBACTYP             * Clinic Type Filter
            GOTO      HATB1000 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,FILTLOCA
          IF        !@EQUAL
            PACK      KEY25,OBASITE,OBACLIN,OBADATE,OBASTRT,SP70
            CALL      RDSESA1
            BRANCH    OVRCD,HATB1000
.
            PACK      KEY18,OSESITE,OSECLIN,OSEDAY,OSESTRT,SP70
            CALL      RDMASA1
            BRANCH    OVRCD,HATB1000
.
            MATCH     FILTLOCA,OTMALTYP
            GOTO      HATB1000 IF NOT EQUAL
          ENDIF
.
          PACK      DESCAPPD,OBADATE,OBATIME,SP70
.
          PACK      KEY20,OBASITE,OBACLIN,OBADATE,SP70
          CALL      RDCLIA1
          IF        OVRCD=1
            PACK      KEY20,OBASITE,OBACLIN,OBADATE,Z70
            CALL      RDSCLIA1
            CALL      RDPCLIA1                   * Read Clinic Record
            BRANCH    OVRCD,HATB4000
.
            MATCH     OBASITE,OCASITE
            GOTO      HATB4000 IF NOT EQUAL
.
            MATCH     OBACLIN,OCACLIN
            GOTO      HATB4000 IF NOT EQUAL
          ENDIF
.
          MOVE      OCADESC,DESCCLIN
.
          PACK      KEY8,OTDNVISN,SP70
          CALL      RDBOKB1
          BRANCH    OVRCD,HATB4000
.
          MATCH     "1",OTBBCLHR
          IF        @EQUAL
            MOVE      DYES,DESCCLHR              * Clinical high risk
          ENDIF
.
          MATCH     SP70,FILTSPEC                * Special Arrangements Filter
          IF        !@EQUAL
            MATCH     FILTSPEC,OTBBSPCA
            GOTO      HATB1000 IF NOT EQUAL
          ENDIF
.
          GOTO      HATB4000
.
HATB3000  PACK      KEY8,OTDNVISN,SP70
          CALL      RDALREF1                     * Read AH Referral
          IF        OVRCD=1
            CALL      CLALLREF
          ENDIF
.
          MATCH     SP70,FILTCLID
          IF        !@EQUAL
            MATCH     FILTCLID,ALRECLID
            GOTO      HATB1000 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,FILTCLTY
          IF        !@EQUAL
            MATCH     FILTCLTY,ALRECTYP  
            GOTO      HATB1000 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,FILTLOCA
          IF        !@EQUAL
            GOTO      HATB1000
          ENDIF
.
HATB4000  MOVE      SP70,PTHSNAME
          MATCH     SP70,PMVXMHOS
          IF        !@EQUAL
            PACK      KEY3,PMVXMHOS,SP70         * Read hospital
            CALL      RDPTHSP1
            IF        OVRCD=1
              MOVE      PMVXMHOS,PTHSNAME
            ENDIF
          ENDIF
.
          MOVE      SP70,DESCCOMP
          MOVE      SP70,COMPLINK
          MOVE      ZERO,COMMTFLG
.
          PACK      KEY8,OTDNURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,HATB1000
.
          PACK      KEY8,OTDNURNO,SP70
          CALL      RDPMPX21
          BRANCH    OVRCD,HATB1000

          IF        PCEASE=1
            MOVE      PDECDTE,AGEDATE            * Date of Death
          ELSE
            PACK      AGEDATE,CCC,CYY,CMM,CDD    * Today's date
          ENDIF
          REP       " 0",AGEDATE
          CALL      CALCAGE
          CALL      PATLN000
.
          CALL      PATFLD00                * Set Patient Folder suffix
          MOVE      KEY3,FOLDERSF           * Save returned value
.
          MOVE      SP70,REQCDESC
          MATCH     SP70,OTDNREQC
          IF        !@EQUAL
            PACK      KEY5,CODERS,OTDNREQC,SP70
            CALL      RDCODE1
            IF        OVRCD=0
              MOVE      TDESC,REQCDESC   * Requestor description (Cat rs)
            ENDIF
          ENDIF
.
          MATCH     "1",OTDNCOMP
          IF        @EQUAL
            MOVE      DYES,DESCCOMP
.
            MOVE      ONE,COMMTFLG
            CLEAR     COMPLINK
            APPEND    "javascript:ViewComplete('",COMPLINK
            APPEND    OTDNURNO,COMPLINK
            APPEND    "','",COMPLINK
            APPEND    DISPDAT1,COMPLINK
            APPEND    "','",COMPLINK
            APPEND    OTDNATIM,COMPLINK
            APPEND    "','",COMPLINK
            APPEND    OTDNATYP,COMPLINK
            APPEND    "','",COMPLINK
            APPEND    OTDNVISN,COMPLINK
            APPEND    "')",COMPLINK
            RESET     COMPLINK
          ENDIF
.
          MATCH     " 5",OTDNATYP
          IF        @EQUAL
            PACK      KEY16,OTDNVISN,SP70
            CALL      RSALLNK1
            CALL      RKALLNK1
            BRANCH    OVRCD,HATB5000
.
            MATCH     OTDNVISN,ALLNVISN
            GOTO      HATB5000 IF NOT EQUAL
.
            PACK      KEY8,ALLNLNKV,SP70
            CALL      RDBOKB1
            BRANCH    OVRCD,HATB5000
. 
            MATCH     "1",OTBBCLHR
            IF        @EQUAL
              MOVE      DYES,DESCCLHR              * Clinical high risk
            ENDIF
          ENDIF
.
HATB5000  CLEAR     HTMLLINK
          APPEND    "javascript:UpdateAction('",HTMLLINK
          APPEND    OTDNURNO,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    DISPDAT1,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    OTDNATIM,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    OTDNATYP,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    OTDNVISN,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
          CLEAR     HTMLLNK2
          APPEND    "javascript:OPDActionList('",HTMLLNK2
          APPEND    OTDNURNO,HTMLLNK2
          APPEND    "','",HTMLLNK2
          APPEND    OTDNVISN,HTMLLNK2
          APPEND    "')",HTMLLNK2
          RESET     HTMLLNK2
.
          REP       "#"'",COMMLIN1
          REP       "#"'",COMMLIN2
          REP       "#"'",COMMLIN3
          REP       "#"'",PTHSNAME
          REP       "#"'",DESCCLIN
          REP       "#"'",FORMATNM
.
          WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":           * 0
                             "#"",OTDNADAT,OTDNATIM,"#",":           * 1
                             "#"",DESCATYP,"#",":                    * 2
                             "#"",COMMLIN1,COMMLIN2,COMMLIN3,"#",":  * 3
                             "#"",DESCCOMP,"#",":                    * 4
                             "#"",PTHSNAME,"#",":                    * 5
                             "#"",DESCCLIN,"#",":                    * 6
                             "#"",DESCAPPD,"#",":                    * 7
                             "#"",DESCCANC,"#",":                    * 8
                             "#"",OTDNADAT,OTDNATIM,"#",":           * 9
                             "#"",DESCCLHR,"#",":                    * 10
                             "#"",DESCCOMP,"#",":                    * 11
                             "#"",DESCPEND,"#",":                    * 12
                             "#"",COMMTFLG,"#",":                    * 13
                             "#"",COMPLINK,"#",":                    * 14
                             "#"",FORMATNM,"#",":                    * 15
                             "#"",FOLDERSF,"#",":                    * 16
                             "#"",OTDNURNO,"#",":                    * 17
                             "#"",HTMLLNK2,"#",":                    * 18
                             "#"",REQCDESC,"#");"                    * 19
.
          GOTO      HATB1000
.
HATB9999  RETURN
.
.------------------------------------------------------------
. Get clinic type description for cancelled bookings
.------------------------------------------------------------
CCTY0000  MOVE      ZERO,EXIT
          PACK      KEY25,OPCNSITE,OPCNCLIN,OPCNDATE,OPCNSTRT,SP70
          CALL      RDSESA1
          BRANCH    OVRCD,CCTY9000
.
          PACK      KEY18,OSESITE,OSECLIN,OSEDAY,OSESTRT,SP70
          CALL      RDMASA1
          BRANCH    OVRCD,CCTY9000
.
          PACK      KEY28,OSESITE,OSECLIN,OSEDAY,OSESTRT,OSESSHNO,OSESSDAT,SP70
          CALL      RDOTHED1
          BRANCH    OVRCD,CCTY9000
.
          PACK      KEY12,OTHESITE,OTHECTYP,SP70
          CALL      RDCTYA1
          BRANCH    OVRCD,CCTY9000
.
          PACK      KEY20,OPCNSITE,OPCNCLIN,OPCNDATE,SP70
          CALL      RDCLIA1
          IF        OVRCD=1
            PACK      KEY20,OPCNSITE,OPCNCLIN,OPCNDATE,Z70
            CALL      RDSCLIA1
            CALL      RDPCLIA1                   * Read Clinic Record
            BRANCH    OVRCD,CCTY9000
.
            MATCH     OPCNSITE,OCASITE
            GOTO      CCTY9000 IF NOT EQUAL
.
            MATCH     OPCNCLIN,OCACLIN
            GOTO      CCTY9000 IF NOT EQUAL
          ENDIF

          MOVE      ZERO,EXIT
          GOTO      CCTY9999
.
CCTY9000  MOVE      ONE,EXIT
          GOTO      CCTY9999
.
CCTY9999  RETURN
.
.------------------------------------------------------------
. Output appointment date for appointment message add screen
.------------------------------------------------------------
APPD0000  PACK      KEY36,ADMISSNO,SP70
          CALL      RDSBOKA6
          CALL      RDKBOKA6                     * Read OP Booking
          BRANCH    OVRCD,APPD9999
.
          MATCH     ADMISSNO,DOBAOUTN
          GOTO      APPD9999 IF NOT EQUAL
.
          UNPACK    OBADATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
.
APPD9999  RETURN
.
.*****************************************************************************
.*                              TACC0000                                     *
.*        Trigger a broadcast message - A08 (visit update)                   *
.*        Read all the relevant visit records for the visit type, so that    *
.*        the holding table data will be populated with the relevant visit   *
.*        data when sending the broadcast message.                           *
.*        message.                                                           *
.* Requires: ADMISSNO - visit number                                         *
.*           URNUMBER - U/R number                                           *
.*****************************************************************************
.
TACC0000  MOVE      URNUMBER,KEY8
          CALL      RDMAST1                    * PMI record found ?
          BRANCH    OVRCD,TACC9999             * no - don't send message
.         
          MOVE      URNUMBER,KEY8
          CALL      RDPMPX21                   * PMI extension record found ?
          BRANCH    OVRCD,TACC9999             * no - don't send message
.         
          MOVE      ADMISSNO,KEY8
          CALL      RDVISA1                    * visit record found ?
          BRANCH    OVRCD,TACC9999             * no - don't send message
.
          BRANCH    PVITYPE,TACC1000:            * Emergency
                            TACC2000:            * Outpatient
                            TACC3000             * Inpatient
.
          GOTO      TACC9999                     * visit type not relevant
.
.         Emergency visit
.
TACC1000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      EMRVISA1,"emrvisaf"          * emrvisaf opened ?
          TRAPCLR   IO
          BRANCH    OVRCD,TACC9999               * no - don't send message
.
          MOVE      PVIBILL,KEY8
          CALL      RDEMVIS1                     * Emergency visit record found?
          BRANCH    OVRCD,TACC9999               * no
.
          COMPARE   FOUR,EMVISTAT
          GOTO      TACC9999 IF EQUAL
.
          MOVE      EMVIADMN,KEY8
          CALL      RDPMVX11
          BRANCH    OVRCD,TACC9999
.
.         Set up the message for sending
.
          CALL      PMIGTNID                   * get national id for dgate write
          MOVE      NMPNUMB,PTNINMPI
          MOVE      ONE,HL7TRGID
          MOVE      SP8,HL7INCLD
          PROC      DGCLICEC                     * send update visit details
.
          GOTO      TACC9999                     * finished
.
.         Outpatient visit
.
TACC2000  MATCH     SP6,PVISITE                  * blank site ?
          GOTO      TACC9999 IF EQUAL            * yes - don't send message
.
          MOVE      PVISITE,KEY6
          CALL      RDSITA1                      * site found ?
          BRANCH    OVRCD,TACC9999               * no - don't send message
.
          MATCH     SP3,OSTFILE                  * site prefix blank ?
          GOTO      TACC9999 IF EQUAL            * yes - don't send message
.
          PACK      CFNAME,OSTFILE,FILBOKA6
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OUTBOKA6,CFNAME              * outbokaf opened ?
          TRAPCLR   IO
          BRANCH    OVRCD,TACC9999               * no - don't send message
.
          PACK      CFNAME,OSTFILE,FILBB1A1
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OUTBB1A1,CFNAME              * outbb1af opened ?
          TRAPCLR   IO
          BRANCH    OVRCD,TACC9999               * no - don't send message
.
.         Get the outbokaf record
.
          PACK      KEY36,PVIBILL,SP20,SP20
          CALL      RDSBOKA6                     * position on visit number
          CALL      RDKBOKA6                     * read next record
          BRANCH    OVRCD,TACC9999               * eof - finished
.
          MATCH     OBAOUTNO,PVIBILL             * same visit number ?
          GOTO      TACC9999 IF NOT EQUAL        * no - get next O/P record
.
.         Get the outbb1af record
.
          MOVE      PVIBILL,KEY8
          CALL      RDBOKB1                      * outbb1af record found ?
          BRANCH    OVRCD,TACC9999               * no
.
.         Set up the message for sending
.
          CALL      PMIGTNID                   * get national id for dgate write
          MOVE      NMPNUMB,PTNINMPI
          MOVE      TWO,HL7TRGID
          MOVE      SP8,HL7INCLD
          PROC      DGCLICOB                     * send update visit details
.
          GOTO      TACC9999
.
.         Inpatient visit
.
TACC3000  MOVE      PVIBILL,KEY8
          CALL      RDMISS1                      * Admission record found ?
          BRANCH    OVRCD,TACC9999               * no
.
          MOVE      PVIBILL,KEY8
          CALL      RDPMVX11                     * visit extension found ?
          BRANCH    OVRCD,TACC9999               * no
.
          IF        ASTAT = 3
            MOVE      PVIBILL,KEY8
            CALL      RDDSCH1                    * discharge record found ?
            BRANCH    OVRCD,TACC9999             * no
          ENDIF
.
.         Get the last transfer record for the admission
.
          PACK      KEY30,PVIBILL,Z70
          CALL      RDSTRAN2                     * position on admission number
          CALL      RDPTRAN2                     * read previous record
          BRANCH    OVRCD,TACC9999               * eof - finished
.
          MATCH     TADMN,PVIBILL                * same admission still ?
          GOTO      TACC9999 IF NOT EQUAL        * no - finished
.
.         Set up the message for sending
.
          CALL      PMIGTNID                   * get national id for dgate write
          MOVE      NMPNUMB,PTNINMPI
          MOVE      THREE,HL7TRGID
          MOVE      SP8,HL7INCLD
          PROC      DGCLICAC                     * send update visit details
.
TACC9999  RETURN
+
.------------------------------------------------------------
. Write an update demographics record (pmsupdaf)
.------------------------------------------------------------
WRUPD000  OPEN      PMSUPDA1,"pmsupdaf"
.
          PACK      PMUDURNO,URNUMBER,SP70
.
          CALL      IBACLOCK
          PACK      PMUDDATE,CCC,CYY,CMM,CDD
          REPLACE   " 0",PMUDDATE
          CLOCK     TIME,PMUDTIME
.
          PACK      PMUDUSER,USERID,SP70
.
          PACK      KEY8,PMUDURNO,SP70
          CALL      RDPMUPD1
          IF        OVRCD=1
            CALL      IBACLOCK
            PACK      PMUDDATC,CCC,CYY,CMM,CDD
            REPLACE   " 0",PMUDDATC
            CLOCK     TIME,PMUDTIMC
            PACK      PMUDWEBC,USERID,SP70
            CALL      WRPMUPD1
          ELSE
            CALL      IBACLOCK
            PACK      PMUDDATE,CCC,CYY,CMM,CDD
            REPLACE   " 0",PMUDDATE
            CLOCK     TIME,PMUDTIME
            CALL      UPPMUPD1
          ENDIF
.
          CLOSE     PMSUPDA1
.
WRUPD999  RETURN
.
+
.------------------------------------------------------------
.
          INC       IBAWEBCD
          INC       WEBDATCD
.
          INC       BEDBDVCD
          INC       BEDBDPRO
          INC       CALCAGE
          INC       CLALLREF
          INC       CLPMSDAU
          INC       CLNHIMAS
          INC       CLPATMAS
          INC       CLPATVIS
          INC       CLPMSPX2
          INC       CLPMSVX1
          INC       DAYOFWEK
          INC       DEATHAUD
          INC       DEATHPOL
          INC       DGCLICAC
          INC       DGCLICEC
          INC       DGCLICOB
          INC       DGCLICUP
          INC       NAMSTRCD
          INC       NHOSPDAY
          INC       OUTDANTM     
          INC       OUTDEMTM
          INC       PATCOMN2
          INC       PATCOMN3
          INC       PATDOCCD
          INC       PATDOCTM
          INC       PATDPATH                                          *I-250985
          INC       PATMASTM
          INC       PATMSXTM
          INC       PATNAMCD
          INC       PATRE1TM 
          INC       PMSCEXTM
          INC       PMSHCGTM
          INC       PMSHCPTM
          INC       PMSPX2TM
          INC       PERRESAC
          INC       SEPRNAME
          INC       TFILENAM
          INC       VALURNMP
          INC       UPDCCARD
          INC       UPDUR
.
          INC       ALLREFIO/INC
          INC       ALLLNKIO/INC
          INC       APSMASIO/INC
          INC       BOKRC1IO/INC
          INC       BOKRX1IO/INC
          INC       EMRVISIO/INC
          INC       MLTSECIO/INC
          INC       MRTMASIO/INC
          INC       NHIETHIO/INC
          INC       NHIMASIO/INC
          INC       OUTARTIO/INC
          INC       OUTBOAIO/INC
          INC       OUTBB1IO/INC
          INC       OUTCANIO/INC
          INC       OUTCLIIO/INC
          INC       OUTCTYIO/INC
          INC       OUTDANIO/INC
          INC       OUTDCOIO/INC
          INC       OUTDEMIO/INC
          INC       OUTMA1IO/INC
          INC       OUTHEDIO/INC
          INC       OUTRSHIO/INC
          INC       OUTSESIO/INC
          INC       OUTSITIO/INC
          INC       PATALRIO/INC
          INC       PATAM1IO/INC
          INC       PMSUCHIO/INC
          INC       PMSUCDIO/INC
          INC       PATDRGIO/INC
          INC       PATDO1IO/INC
          INC       PATDSCIO/INC
          INC       PATDTHIO/INC
          INC       PATFN1IO/INC
          INC       PATGSRIO/INC
          INC       PATHSPIO/INC
          INC       PATHOSIO/INC                                      *I-250985
          INC       PATIN1IO/INC
          INC       PATINVIO/INC
          INC       PATLOCIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATNIDIO/INC
          INC       PATONLIO/INC
          INC       PATPA1IO/INC
          INC       PATPNIIO/INC
          INC       PATPR1IO/INC
          INC       PATRE1IO/INC
          INC       PATTRNIO/INC
          INC       PATURAIO/INC
          INC       PATVISIO/INC
          INC       PATWR1IO/INC
.
          INC       PMSBBDIO/INC
          INC       PMSBRQIO/INC
          INC       PMSCCDIO/INC
          INC       PMSCEXIO/INC
          INC       PMSCURIO/INC
          INC       PMSDAUIO/INC
          INC       PMSEPBIO/INC
          INC       PMSHCGIO/INC
          INC       PMSHCPIO/INC
          INC       PMSPX2IO/INC
          INC       PMSRELIO/INC
          INC       PMSTLEIO/INC
          INC       PMSUPDIO/INC
          INC       PMSVX1IO/INC
          INC       PRSPMIIO/INC
          INC       WATESEIO/INC
          INC       WATESPIO/INC

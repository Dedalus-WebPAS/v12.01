.*****************************************************************************
.* System    :   HL7 Messaging                                               *
.* Program   :   FX814510                                                    *
.* Desc      :   Program to set parameters for receiving HL7 Messages        *
.*****************************************************************************
.* Date      :   10/06/2016                                                  *
.* Author    :   Steve Armstrong                                             *
.* Function  :   This program will allow the user to turn on/off all the     *
.*               HL7 parameters for receiving messages.                      *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATCONFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
DIM3      DIM       3
.
.
.
PRGID     INIT      "FX814510"
PRGNAM    INIT      "Set HL7 Receiver Message Parameters"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000       * proceed with fixit
.
MAIN1000  CALL      PROC0000               * process
          GOTO      MAIN0100
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                     * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
.         Load current parameter settings
.
          READ      CONTROLF,HUND23;*214,PTCNRA03:    * receiving A03
                                    *215,PTCNRA04:    * receiving A04
                                    *216,PTCNRA05:    * receiving A05
                                    *217,PTCNRA28:    * receiving A28
                                    *218,PTCNRA31:    * receiving A34
                                    *219,PTCNRA34:    * receiving A03
                                    *220,PTCNRI12:    * receiving I12
                                    *221,PTCNRI13:    * receiving I13
                                    *222,PTCNRI14:    * receiving I14
                                    *223,PTCNRM02:    * receiving M02
                                    *224,PTCNRP03:    * receiving P03
                                    *225,PTCNRA19     * receiving A19
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run fixit                               *
.*                        TRUE  (1)  Exit option selected                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*               Display the current parameter settings and allow            *
.*               the user to change them.                                    *
.*****************************************************************************
.
PROC0000  CALL      SCRN0000                     * display screen
          CALL      SELC0000                     * select field to modifiy
          BRANCH    EXIT,PROC9999                * cancel selected
.
          CALL      UPDT0000                     * update control file
.
PROC9999  RETURN
+
.*****************************************************************************
.*                               SCRN0000              Called by: MAIN0000   *
.*                  Display the current parameter settings                   *
.*****************************************************************************
.
SCRN0000  DISPLAY   *P1:3,*EF;
.
          MOVE      DNO,DIM3
          MATCH     "1",PTCNRA03
          IF        @EQUAL
            MOVE      DYES,DIM3
          ENDIF
          DISPLAY   *P1:6,*V2LON," 1",*HOFF,". Receive A03 (A/H Contact) ":
                    "Message (",*V2LON,ANSY,*HOFF,SLASH,*V2LON,ANSN,*HOFF,")":
                    *P68:6,": ",*V2LON,DIM3
.
          MOVE      DNO,DIM3
          MATCH     "1",PTCNRA04
          IF        @EQUAL
            MOVE      DYES,DIM3
          ENDIF
          DISPLAY   *P1:7,*V2LON," 2",*HOFF,". Receive A04 (Attend O/P ":
                    "Booking ) Message (",*V2LON,ANSY,*HOFF,SLASH,*V2LON,ANSN:
                    *HOFF,")":
                    *P68:7,": ",*V2LON,DIM3
.
          MOVE      DNO,DIM3
          MATCH     "1",PTCNRA05
          IF        @EQUAL
            MOVE      DYES,DIM3
          ENDIF
          DISPLAY   *P1:8,*V2LON," 3",*HOFF,". Receive A05 (I/P Preadmit) ":
                    "Message (",*V2LON,ANSY,*HOFF,SLASH,*V2LON,ANSN:
                    *HOFF,")":
                    *P68:8,": ",*V2LON,DIM3
.
          MOVE      DNO,DIM3
          MATCH     "1",PTCNRA19
          IF        @EQUAL
            MOVE      DYES,DIM3
          ENDIF
          DISPLAY   *P1:9,*V2LON," 4",*HOFF,". Receive A19 (Query) Message (":
                    *V2LON,ANSY,*HOFF,SLASH,*V2LON,ANSN,*HOFF,")":
                    *P68:9,": ",*V2LON,DIM3
.
          MOVE      DNO,DIM3
          MATCH     "1",PTCNRA28
          IF        @EQUAL
            MOVE      DYES,DIM3
          ENDIF
          DISPLAY   *P1:10,*V2LON," 5",*HOFF,". Receive A28 (Add Person ":
                    "Information) Message (",*V2LON,ANSY,*HOFF,SLASH,*V2LON:
                    ANSN,*HOFF,")":
                    *P68:10,": ",*V2LON,DIM3
.
          MOVE      DNO,DIM3
          MATCH     "1",PTCNRA31
          IF        @EQUAL
            MOVE      DYES,DIM3
          ENDIF
          DISPLAY   *P1:11,*V2LON," 6",*HOFF,". Receive A31 (Update Person ":
                    "Information) Message (",*V2LON,ANSY,*HOFF,SLASH,*V2LON:
                    ANSN,*HOFF,")":
                    *P68:11,": ",*V2LON,DIM3
.
          MOVE      DNO,DIM3
          MATCH     "1",PTCNRA34
          IF        @EQUAL
            MOVE      DYES,DIM3
          ENDIF
          DISPLAY   *P1:12,*V2LON," 7",*HOFF,". Receive A34 (Merge Patient ":
                    "Information) Message (",*V2LON,ANSY,*HOFF,SLASH,*V2LON:
                    ANSN,*HOFF,")":
                    *P68:12,": ",*V2LON,DIM3
.
          MOVE      DNO,DIM3
          MATCH     "1",PTCNRI12
          IF        @EQUAL
            MOVE      DYES,DIM3
          ENDIF
          DISPLAY   *P1:13,*V2LON," 8",*HOFF,". Receive I12 (Add Patient ":
                    "Referral) Message (",*V2LON,ANSY,*HOFF,SLASH,*V2LON:
                    ANSN,*HOFF,")":
                    *P68:13,": ",*V2LON,DIM3
.
          MOVE      DNO,DIM3
          MATCH     "1",PTCNRI13
          IF        @EQUAL
            MOVE      DYES,DIM3
          ENDIF
          DISPLAY   *P1:14,*V2LON," 9",*HOFF,". Receive I13 (Update Patient ":
                    "Referral) Message (",*V2LON,ANSY,*HOFF,SLASH,*V2LON:
                    ANSN,*HOFF,")":
                    *P68:14,": ",*V2LON,DIM3
.
          MOVE      DNO,DIM3
          MATCH     "1",PTCNRI14
          IF        @EQUAL
            MOVE      DYES,DIM3
          ENDIF
          DISPLAY   *P1:15,*V2LON,"10",*HOFF,". Receive I14 (Delete Patient ":
                    "Referral) Message (",*V2LON,ANSY,*HOFF,SLASH,*V2LON:
                    ANSN,*HOFF,")":
                    *P68:15,": ",*V2LON,DIM3
.
          MOVE      DNO,DIM3
          MATCH     "1",PTCNRM02
          IF        @EQUAL
            MOVE      DYES,DIM3
          ENDIF
          DISPLAY   *P1:16,*V2LON,"11",*HOFF,". Receive M02 (Staff/":
                    "Practitioner) Message (",*V2LON,ANSY,*HOFF,SLASH,*V2LON:
                    ANSN,*HOFF,")":
                    *P68:16,": ",*V2LON,DIM3
.
          MOVE      DNO,DIM3
          MATCH     "1",PTCNRP03
          IF        @EQUAL
            MOVE      DYES,DIM3
          ENDIF
          DISPLAY   *P1:17,*V2LON,"12",*HOFF,". Receive P03 (Post Detail ":
                    "Financial Transaction) Message (",*V2LON,ANSY,*HOFF:
                    SLASH,*V2LON,ANSN,*HOFF,")":
                    *P68:17,": ",*V2LON,DIM3
.
SCRN9999  RETURN
+
.*****************************************************************************
.*                               SELC0000              Called by: MAIN0000   *
.*          Select a parameter to change and alow posting of changes         *
.*****************************************************************************
.
SELC0000  CALL      MAINQST                      * Prompt user for action
.
          COMPARE   ZERO,CCITEM                  * Post selected ?
          GOTO      SELC8000 IF EQUAL            * Yes
          GOTO      SELC9000 IF LESS             * Cancel selected
.
.
SELC1000  BRANCH    CCITEM,SELC2000:         * 
                           SELC2000:
                           SELC2000:
                           SELC2000:
                           SELC2000:
                           SELC2000:
                           SELC2000:
                           SELC2000:
                           SELC2000:
                           SELC2000:
                           SELC2000:
                           SELC2000
          BEEP
          GOTO      SELC0000
.
SELC2000  ASSIGN    (CCITEM+FIVE),CVERT
SELC2100  KEYIN     *P70:CVERT,*EL,*V2LON,ANS
          PACK      ANS,ANS,SP1
.
          REP       UPPLOW,ANS
.
          MATCH     ANSY,ANS
          IF        @EQUAL
            MOVE      "1",KEY1
            MOVE      DYES,DIM3
            GOTO      SELC7000
          ENDIF
.
          MATCH     ANSN,ANS
          IF        @EQUAL
            MOVE      "0",KEY1
            MOVE      DNO,DIM3
            GOTO      SELC7000
          ENDIF
.
          BEEP
          GOTO      SELC2100
.
SELC7000  STORE     KEY1,CCITEM,PTCNRA03,PTCNRA04,PTCNRA05,PTCNRA19,PTCNRA28:
                                PTCNRA31,PTCNRA34,PTCNRI12,PTCNRI13,PTCNRI14:
                                PTCNRM02,PTCNRP03
          DISPLAY   *P70:CVERT,*V2LON,DIM3
          GOTO      SELC0000
.
.         Post was selected. Set the exit flag to tell the calling routine.
.
SELC8000  MOVE      ZERO,EXIT                    * Post selected
          GOTO      SELC9999
.
.         Cancel was selected. Set the exit flag to tell the calling routine.
.
SELC9000  MOVE      ONE,EXIT                     * Cancel selected
.
SELC9999  RETURN
+
.*****************************************************************************
.*                               UPDT0000              Called by: MAIN0000   *
.*                  Update the parameter settings                            *
.*****************************************************************************
.
UPDT0000  WRITAB    CONTROLF,HUND23;*214,PTCNRA03:
                                    *215,PTCNRA04:
                                    *216,PTCNRA05:
                                    *217,PTCNRA28:
                                    *218,PTCNRA31:
                                    *219,PTCNRA34:
                                    *220,PTCNRI12:
                                    *221,PTCNRI13:
                                    *222,PTCNRI14:
                                    *223,PTCNRM02:
                                    *224,PTCNRP03:
                                    *225,PTCNRA19
.
UPDT9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO

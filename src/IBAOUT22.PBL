.******************************************************************************
.* System    :  OUTPATIENTS                                                   *
.* Program   :  IBAOUT22                                                      *
.* Desc      :  OUTPATIENT DNA STATISTICS  (Taranaki)                         *
.******************************************************************************
.* Date      :  29/03/1994                                                    *
.* Author    :  bez  (adapted from IBAOUT41)                                  *
.******************************************************************************
. V10.04.01 14/07/2013  Ebon Clements     CAR 261630                          *
.           Removed port number from temp file name                           *
.******************************************************************************
.
.======================================== 
. FD LIST
.======================================== 
          INC       STD001FD
          INC       IBASEQFD/INC
          INC       PATCOMM/INC
          INC       OUTCTYFD/INC
          INC       OUTATTFD/INC
          INC       PATCODFD/INC
          INC       PATDRGFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
+
.
.======================================== 
. VARIABLE LIST
.======================================== 
CMDLINE   DIM       50
CODECG    INIT      "CG"         * Group Code
COUNT     FORM      2
CURPER    DIM       6            * Current period (CCYYMM)
CSTPER    DIM       6            * First period of current year (CCYYMM)
DIM4      DIM       4
FIRSTREC  FORM      1
FORM7     FORM      7
FORM7Y    FORM      7
MPERREQD  FORM      2            * Required period (calculated for every record)
MPERUSED  FORM      2            * Number of months used (calculated initially)
MPRTPOS   FORM      3            * Variable print position
NPYEAR    FORM      2            * Number of periods in a financial year
OUTTMP    DIM       8
PENDDATE  DIM       7            * End month - MM/CCYY
PSTRDATE  DIM       7            * Start month - MM/CCYY
SAVGROUP  DIM       3
SH1DESC   DIM       27
SH2DESC   DIM       22
.
.
. Temporary file layout
.----------------------------------------------------------------
OUTTMPXX  IFILE     SQL, FIXED=75, KEY="U1-9"
.Name     Type   Length    Physical   Start    Description
.----     ----   ------    --------   -----    -----------
TGROUP    DIM       3          3         1     Group Code
TCTYPE    DIM       6          6         4     Clinic Type
TCPER1    FORM      7          5        10     Totals for period 1
TCPER2    FORM      7          5        15                period 2
TCPER3    FORM      7          5        20                period 3
TCPER4    FORM      7          5        25                period 4
TCPER5    FORM      7          5        30                period 5
TCPER6    FORM      7          5        35                period 6
TCPER7    FORM      7          5        40                period 7
TCPER8    FORM      7          5        45                period 8
TCPER9    FORM      7          5        50                period 9
TCPER10   FORM      7          5        55                period 10
TCPER11   FORM      7          5        60                period 11
TCPER12   FORM      7          5        65                period 12
TCPER13   FORM      7          5        70                period 13
.
.End of Record                          75
.----------------------------------------------------------------
.
TCPERTOT  FORM      7            * period totals for Clinic Type
.
TGPER1    FORM      7            *** GROUP TOTALS ***
TGPER2    FORM      7
TGPER3    FORM      7
TGPER4    FORM      7
TGPER5    FORM      7
TGPER6    FORM      7
TGPER7    FORM      7
TGPER8    FORM      7
TGPER9    FORM      7
TGPER10   FORM      7
TGPER11   FORM      7
TGPER12   FORM      7
TGPER13   FORM      7
TGPERTOT  FORM      7
.
TZPER1    FORM      7            *** GRAND TOTALS ***
TZPER2    FORM      7
TZPER3    FORM      7
TZPER4    FORM      7
TZPER5    FORM      7
TZPER6    FORM      7
TZPER7    FORM      7
TZPER8    FORM      7
TZPER9    FORM      7
TZPER10   FORM      7
TZPER11   FORM      7
TZPER12   FORM      7
TZPER13   FORM      7
TZPERTOT  FORM      7
.
VENDDATE  DIM       6            * End month - CCYYMM
VSTRDATE  DIM       6            * Start month - CCYYMM
VSITE     DIM       6
.
PRGID     INIT      "IBAOUT22"
PRGNAM    INIT      "Outpatient Dna Statistics"
VERSION   INIT      "V12.01.00"
+
.
.**************************************************************************
.* MAINLINE     
.**************************************************************************
ML0000    CALL      INIT0000          * initialization and open files
          BRANCH    EXIT,ML9999       * Date Range file error
.
ML1000    CALL      OPTN0000          * select option
          BRANCH    EXIT,ML9999       * EXIT=1 if "0" chosen in menu
.
ML2000    CALL      DATE0000          * get date range
          CALL      CONTQST           * Ok to Continue ?
          BRANCH    CEXIT,ML3000,ML2000,ML1000
.
ML3000    CALL      PROC0000          * Process data
          BRANCH    FIRSTREC,ML4000   * If set, no records found
.
          CALL      PRNT0000          * Print data
          CALL      KILL0000          * delete temp file
          GOTO      ML1000
.
ML4000    DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** No records found for date range requested **    ";
          CALL      HITENTER
          GOTO      ML2000
.
ML9999    CHAIN     PGM               * chain back to appropriate menu
+
.
.****************************************************************************
.* INIT0000 Initialization
.****************************************************************************
INIT0000  CALL      DISPHEAD
.
          MOVE      IDDD,VSITE          * Initialize the current site
          MATCH     SP6,VSITE           * Valid site ?
          GOTO      INIT9000 IF EQUAL   * No. Initialization failure
.
          DISPLAY   *P56:24,"Opening";
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdrgaf";
          OPEN      PATDRGA1,"patdrgaf"
          OPEN      PATDRGA3,"patdrgaf"
.
          PACK      CFNAME,UID,FILCTYA1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTCTYA1,CFNAME
.
          PACK      CFNAME,UID,FILATTA1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTATTA1,CFNAME
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,OUTTMP
.
.         Work out what the current period is, and the first period in the
.         current financial year
.
          PACK      CPTDATE,CCC,CYY,CMM,CDD
          REP       " 0",CPTDATE
          CALL      GPERD                      * Find the current period
          BRANCH    EXIT,INIT8900              * No current period found
.
          PACK      CURPER,DRGCYR,DRGCNUM
.
          PACK      KEY6,DRGYR,SP2
          CALL      RDSDRGA1
          CALL      RDKDRGA1
.
          PACK      CSTPER,DRGCYR,DRGCNUM
.
.         Now count the number of periods in a financial year
.
          MOVE      ZERO,NPYEAR
          MOVE      DRGYR,DIM4
          PACK      KEY6,DRGYR,SP2
          CALL      RDSDRGA1
.
INIT8000  CALL      RDKDRGA1
          BRANCH    OVRCD,INIT8100
.
          MATCH     DRGYR,DIM4
          GOTO      INIT8100 IF NOT EQUAL
          ADD       ONE,NPYEAR
          GOTO      INIT8000
.
.         Finished initialization
.
INIT8100  MOVE      FALSE,EXIT
          GOTO      INIT9999
.
.         No current period on file
.
INIT8900  KEYIN     *P1:24,*EL,*B,*V2LON:
                    "** Error: Current date not found in Period file **    ":
                    *HOFF,"Hit <",*V2LON,"ENTER",*HOFF,"> to exit ":
                    *EOFF,ANS,*P1:24;
          MOVE      TRUE,EXIT
          GOTO      INIT9999
.
INIT9000  DISPLAY   *P1:24,*EL,*B:
                    "Incorrect call to program - Site info wrong. ";
          CALL      HITENTER
.
INIT9100  MOVE      ONE,EXIT                * Initialization failure
.
INIT9999  RETURN 
+
.
.****************************************************************************
.* OPTN0000 : get option
.****************************************************************************
OPTN0000  MOVE       ONE,EXIT
.
          DISPLAY    *P1:3,*EF:
                     *P1:4,*V2LON,ZERO,*HOFF,". Exit":
                     *P1:5,*V2LON,ONE,*HOFF,". Run Report"
.
OPTN0500  KEYIN      *P1:7,"Please Select : ",*V2LON,OPTION;
          COMPARE    ZERO,OPTION
          GOTO       OPTN9999 IF EQUAL
.
          MOVE       ZERO,EXIT
          BRANCH     OPTION,OPTN1000
.
          BEEP
          GOTO       OPTN0500
.
.-------- display header according to option chosen ----
.
OPTN1000  
.
OPTN9999  RETURN
+
.
.****************************************************************************
.* DATE0000 : Keyin Dates
.****************************************************************************
DATE0000  DISPLAY   *P1:11,*EF,"Generate Report from ";
          UNPACK    CSTPER,CCENT,CYEAR,CMON
          MOVE      ZERO,CHIGHLT
          MOVE      TWENTY2,CCOL
          MOVE      TEN1,CVERT
          CALL      KEYPER
          BRANCH    CQUEST,DATE0000
.
.         Validate the date entered
.
          PACK      KEY6,CCENT,CYEAR,CMON
          REP       " 0",KEY6
          CALL      RDDRGA3
          BRANCH    OVRCD,DATE2000
.
          PACK      PSTRDATE,DRGCNUM,SLASH,DRGCYR
          PACK      VSTRDATE,DRGYR,DRGNUM
.
.         Get the To Date
.
DATE1000  DISPLAY   *P30:11,"to ";
          UNPACK    CURPER,CCENT,CYEAR,CMON
          MOVE      "33",CCOL
          CALL      KEYPER
          BRANCH    CQUEST,DATE1000
.
.         Validate the date entered
.
          PACK      KEY6,CCENT,CYEAR,CMON
          REP       " 0",KEY6
          CALL      RDDRGA3
          BRANCH    OVRCD,DATE3000
.
          PACK      PENDDATE,DRGCNUM,SLASH,DRGCYR
          PACK      VENDDATE,DRGYR,DRGNUM
.
          MATCH     VSTRDATE,VENDDATE
          GOTO      DATE4000 IF NOT LESS
          DISPLAY   *P1:24,*EL,*V2LON,*B:
                    "** To Date must be greater than or = to From Date **  ";
          CALL      HITENTER
          GOTO      DATE1000
.
.         From Date not on file
.
DATE2000  DISPLAY   *P1:24,*EL,*V2LON,*B:
                    "** Date not found in Date Range file **    ";
          CALL      HITENTER
          GOTO      DATE0000
.
.         To Date not on file
.
DATE3000  DISPLAY   *P1:24,*EL,*V2LON,*B:
                    "** Date not found in Date Range file **    ";
          CALL      HITENTER
          GOTO      DATE1000
.
.         Calculate MPERUSED - # of periods used
.
DATE4000  MOVE      ZERO,MPERUSED
          PACK      KEY6,VSTRDATE
          CALL      RDDRGA1
          BRANCH    OVRCD,DATE4100
          GOTO      DATE4200
.
DATE4100  CALL      RDKDRGA1
          BRANCH    OVRCD,DATE5000
.
DATE4200  PACK      XDATE,DRGYR,DRGNUM
          MATCH     XDATE,VENDDATE
          GOTO      DATE5000 IF LESS
.
          ADD       ONE,MPERUSED
          GOTO      DATE4100
.
.         Validate the number of periods
.
DATE5000  COMPARE   TEN4,MPERUSED
          GOTO      DATE9999 IF LESS
.
          DISPLAY   *P1:24,*EL,*V2LON,*B,"** Maximum 13 periods allowed **    ";
          CALL      HITENTER
          GOTO      DATE0000
.
DATE9999  RETURN
+
.
.**************************************************************************
.* PROC0000 : Process Data
.**************************************************************************
PROC0000  MOVE      ONE,FIRSTREC          * 1st record not read yet
.
.         check if there is any records to be processed
.
          PACK      KEY24,IDDD,SP30
          CALL      RDSATTA1
PROC1000  CALL      RDKATTA1
          BRANCH    OVRCD,PROC9999        * End-of-file
          MATCH     OATSITE,IDDD
          GOTO      PROC9999 IF NOT EQUAL * Different site - exit
          MATCH     VSTRDATE,OATDATE
          GOTO      PROC1000 IF LESS      * Before start of date range
          MATCH     OATDATE,VENDDATE
          GOTO      PROC1000 IF LESS      * Past end of date range
.
.         yes, there are records that satisfy the Site and Dates criteria
.
          CALL      CREA0000              * Create temp file
          CALL      INTV0000              * Initialize page no & total variables
.
          DISPLAY   *P1:14,*EF,*V2LON:
                    "*** Processing Statistics ***",*HOFF:
                    *P1:16,"Group Code  :":
                    *P1:17,"Clinic Type :":
                    *P1:18,"Date        :";
.
          CALL      RDSATTA1
PROC2000  CALL      RDKATTA1
          BRANCH    OVRCD,PROC9999        * No more records
.
          MATCH     OATSITE,IDDD
          GOTO      PROC9999 IF NOT EQUAL * Different site - exit
          MATCH     VSTRDATE,OATDATE
          GOTO      PROC2000 IF LESS      * Before start of date range
          MATCH     OATDATE,VENDDATE
          GOTO      PROC2000 IF LESS      * Past end of date range
.
          UNPACK    OATDATE,CCENT,CYEAR,CMON
          DISPLAY   *P15:16,*V2LON,OATGRUP,*P15:17,OATCTYP:
                    *P15:18,CMON,SLASH,CCENT,CYEAR;
.
          MOVE      ZERO,FIRSTREC         * Got a record
.
          CALL      CALM0000              * Calculate period # for this month
.
          PACK      KEY9,OATGRUP,OATCTYP
          CALL      RDTMP1
          BRANCH    OVRCD,PROC4000
.
.         Update existing record
.
          LOAD      FORM7,MPERREQD,TCPER1,TCPER2,TCPER3,TCPER4,TCPER5:
                                   TCPER6,TCPER7,TCPER8,TCPER9,TCPER10:
                                   TCPER11,TCPER12,TCPER13
          ADD       OATNNON,FORM7
          STORE     FORM7,MPERREQD,TCPER1,TCPER2,TCPER3,TCPER4,TCPER5:
                                   TCPER6,TCPER7,TCPER8,TCPER9,TCPER10:
                                   TCPER11,TCPER12,TCPER13
          CALL      UPTMP1
          GOTO      PROC2000
.
.         Write new record
.
PROC4000  MOVE      OATGRUP,TGROUP
          MOVE      OATCTYP,TCTYPE
          MOVE      ZERO,TCPER1
          MOVE      ZERO,TCPER2
          MOVE      ZERO,TCPER3
          MOVE      ZERO,TCPER4
          MOVE      ZERO,TCPER5
          MOVE      ZERO,TCPER6
          MOVE      ZERO,TCPER7
          MOVE      ZERO,TCPER8
          MOVE      ZERO,TCPER9
          MOVE      ZERO,TCPER10
          MOVE      ZERO,TCPER11
          MOVE      ZERO,TCPER12
          MOVE      ZERO,TCPER13
.
          STORE     OATNNON,MPERREQD,TCPER1,TCPER2,TCPER3,TCPER4,TCPER5:
                                     TCPER6,TCPER7,TCPER8,TCPER9,TCPER10:
                                     TCPER11,TCPER12,TCPER13
          CALL      WRTMP1
          GOTO      PROC2000
.
PROC9999  RETURN
+
.********************************************************************** 
. CALM0000 : Calculate the period for a particular month - value in MPERREQD
.            Requires : VSTRDATE, CMON, CYEAR
.********************************************************************** 
CALM0000  UNPACK    VSTRDATE,XCENT,XYEAR,XMON
          MOVE      XMON,IMON
          MOVE      XYEAR,IYEAR
          MOVE      XCENT,ICENT
.
          MOVE      CMON,MPERREQD
.
          MATCH     XYEAR,CYEAR
          GOTO      CALM1000 IF EQUAL
          ADD       NPYEAR,MPERREQD            * Different year
.
CALM1000  SUB       IMON,MPERREQD              * Same year
          ADD       ONE,MPERREQD
.
CALM9999  RETURN
+
.
.**************************************************************************
.* PRNT0000 : Print Data
.**************************************************************************
PRNT0000  DISPLAY   *P1:14,*EF,*V2LON:
                    "*** Generating Report ***",*HOFF:
                    *P1:16,"Group Code  :":
                    *P1:17,"Clinic Type :";
.
          MOVE      "80",CLNO
          UNPACK    SP30,KEY9,SAVGROUP
          CALL      RDSTMP1
.
PRNT1000  CALL      RDKTMP1
          BRANCH    OVRCD,PRNT8000
.
          DISPLAY   *P15:16,*V2LON,TGROUP,*P15:17,TCTYPE;
.
          MATCH     SP3,SAVGROUP
          GOTO      PRNT2000 IF EQUAL
          MATCH     SAVGROUP,TGROUP
          GOTO      PRNT2000 IF EQUAL
.
          CALL      PRNX3000                * Print group totals
.
PRNT2000  MOVE      TGROUP,SAVGROUP
          CALL      PRNX1000                * Print record
          GOTO      PRNT1000
.
PRNT8000  CALL      PRNX3000                * Print last group totals
          CALL      PRNX4000                * Print grand totals
          CALL      PRNX7000                * Print end of report
.
          DISPLAY   *P1:24,*EL,*P24:24,*V2LON:
                    "** Report Generation Completed **",*W2;
.
PRNT9999  RETURN
+
.
.****************************************************************************
.*                        Printing subroutines                              *
.****************************************************************************
PRNX1000  COMPARE   "55",CLNO
          GOTO      PRNX1100 IF LESS
          CALL      PRNX2000                   * Print new page head
.
PRNX1100  MOVE      "** Unknown Clinic Type        ",OCTDESC
          PACK      KEY12,IDDD,TCTYPE
          CALL      RDCTYA1
.
          MOVE      OCTDESC,SH1DESC            * Truncate the description
          PRINT     *1,TCTYPE,*8,SH1DESC;
.
          MOVE      ZERO,TCPERTOT              * Initialize total
          MOVE      TWENTY8,MPRTPOS            * Print position
          MOVE      ZERO,COUNT
.
PRNX1200  ADD       SEVEN,MPRTPOS
          ADD       ONE,COUNT
          COMPARE   COUNT,MPERUSED
          GOTO      PRNX1500 IF LESS
          LOAD      FORM7,COUNT,TCPER1,TCPER2,TCPER3,TCPER4,TCPER5:
                                TCPER6,TCPER7,TCPER8,TCPER9,TCPER10:
                                TCPER11,TCPER12,TCPER13
          PRINT     *MPRTPOS,FORM7;
.
.         Add to group monthly figures
.
          LOAD      FORM7Y,COUNT,TGPER1,TGPER2,TGPER3,TGPER4,TGPER5:
                                 TGPER6,TGPER7,TGPER8,TGPER9,TGPER10:
                                 TGPER11,TGPER12,TGPER13
          ADD       FORM7,FORM7Y
          STORE     FORM7Y,COUNT,TGPER1,TGPER2,TGPER3,TGPER4,TGPER5:
                                 TGPER6,TGPER7,TGPER8,TGPER9,TGPER10:
                                 TGPER11,TGPER12,TGPER13
.
.         Add to clinic type total
.
          ADD       FORM7,TCPERTOT
          GOTO      PRNX1200
.
.         Finished clinic type monthly figures, now print total
.
PRNX1500  PRINT     *MPRTPOS,TCPERTOT
          ADD       ONE,CLNO
          RETURN
.
.----------------------------------------------------------------------------
.         Print New Page
.----------------------------------------------------------------------------
PRNX2000  CLOCK     TIME,CTIMEIS
.
          CALL      HEAD132
          PRINT     *40,"Period from ",PSTRDATE," to ",PENDDATE:
                    *N,*N,"Clinic Type/Description";
.
.         Loop over the period file and get the description for each column
.
          MOVE      "35",MPRTPOS               * Location to start actual print
          MOVE      VSTRDATE,KEY6
          CALL      RDDRGA1
PRNX2200  PACK      XDATE,DRGYR,DRGNUM
.
          MATCH     XDATE,VENDDATE
          GOTO      PRNX2300 IF LESS
.
          UNPACK    DRGCYR,CCENT,CYEAR
          PRINT     *MPRTPOS,SP1,DRGSDSC,SP1,CYEAR;
          ADD       SEVEN,MPRTPOS
.
          CALL      RDKDRGA1
          BRANCH    OVRCD,PRNX2300
          GOTO      PRNX2200
.
PRNX2300  PRINT     *MPRTPOS,"  Total":
                    *N,"----------------------------------";
.
          CALL      PRNX5000                   * Print rest of separator lines
          MOVE      SEVEN,CLNO
          RETURN
.
.----------------------------------------------------------------------------
.         Print group totals
.----------------------------------------------------------------------------
PRNX3000  COMPARE   "55",CLNO
          GOTO      PRNX3100 IF LESS
          CALL      PRNX2000                   * Print new page head
.
PRNX3100  MOVE      "*ERROR*Unknown Group",TDESC
          PACK      KEY5,CODECG,SAVGROUP
          CALL      RDCODE1
.
          PACK      SH2DESC,SAVGROUP,SP1,TDESC
          CALL      PRNX5000                   * Print separator lines
          PRINT     *1,"Total Group ",SH2DESC;
.
          MOVE      ZERO,TGPERTOT              * Initialize total
          MOVE      TWENTY8,MPRTPOS            * Print position
          MOVE      ZERO,COUNT
.
PRNX3200  ADD       SEVEN,MPRTPOS
          ADD       ONE,COUNT
          COMPARE   COUNT,MPERUSED
          GOTO      PRNX3500 IF LESS
          LOAD      FORM7,COUNT,TGPER1,TGPER2,TGPER3,TGPER4,TGPER5:
                                TGPER6,TGPER7,TGPER8,TGPER9,TGPER10:
                                TGPER11,TGPER12,TGPER13
          PRINT     *MPRTPOS,FORM7;
.
.         Add to grand total monthly figures
.
          LOAD      FORM7Y,COUNT,TZPER1,TZPER2,TZPER3,TZPER4,TZPER5:
                                 TZPER6,TZPER7,TZPER8,TZPER9,TZPER10:
                                 TZPER11,TZPER12,TZPER13
          ADD       FORM7,FORM7Y
          STORE     FORM7Y,COUNT,TZPER1,TZPER2,TZPER3,TZPER4,TZPER5:
                                 TZPER6,TZPER7,TZPER8,TZPER9,TZPER10:
                                 TZPER11,TZPER12,TZPER13
.
.         Add to group total
.
          ADD       FORM7,TGPERTOT
          GOTO      PRNX3200
.
.         Finished group monthly figures, now print total
.
PRNX3500  PRINT     *MPRTPOS,TGPERTOT
          CALL      PRNX5000                   * Print separator lines
          ADD       THREE,CLNO
.
          MOVE      ZERO,TGPER1
          MOVE      ZERO,TGPER2
          MOVE      ZERO,TGPER3
          MOVE      ZERO,TGPER4
          MOVE      ZERO,TGPER5
          MOVE      ZERO,TGPER6
          MOVE      ZERO,TGPER7
          MOVE      ZERO,TGPER8
          MOVE      ZERO,TGPER9
          MOVE      ZERO,TGPER10
          MOVE      ZERO,TGPER11
          MOVE      ZERO,TGPER12
          MOVE      ZERO,TGPER13
          MOVE      ZERO,TGPERTOT    
          RETURN
.
.----------------------------------------------------------------------------
.         Print grand totals
.----------------------------------------------------------------------------
PRNX4000  COMPARE   "55",CLNO
          GOTO      PRNX4100 IF LESS
          CALL      PRNX2000                   * Print new page head
.
PRNX4100  PRINT     *1,"Grand Total";
.
          MOVE      ZERO,TZPERTOT              * Initialize total
          MOVE      TWENTY8,MPRTPOS            * Print position
          MOVE      ZERO,COUNT
.
PRNX4200  ADD       SEVEN,MPRTPOS
          ADD       ONE,COUNT
          COMPARE   COUNT,MPERUSED
          GOTO      PRNX4500 IF LESS
.
          LOAD      FORM7,COUNT,TZPER1,TZPER2,TZPER3,TZPER4,TZPER5:
                                TZPER6,TZPER7,TZPER8,TZPER9,TZPER10:
                                TZPER11,TZPER12,TZPER13
          PRINT     *MPRTPOS,FORM7;
.
.         Add to group total
.
          ADD       FORM7,TZPERTOT
          GOTO      PRNX4200
.
.         Finished group monthly figures, now print total
.
PRNX4500  PRINT     *MPRTPOS,TZPERTOT
          CALL      PRNX6000                   * Print separator lines
          RETURN
.
.----------------------------------------------------------------------------
.         Print page separator type 1
.----------------------------------------------------------------------------
PRNX5000  MOVE      TWENTY8,MPRTPOS
          MOVE      ZERO,COUNT
.
PRNX5100  ADD       SEVEN,MPRTPOS
          ADD       ONE,COUNT
          COMPARE   COUNT,MPERUSED
          GOTO      PRNX5200 IF LESS
          PRINT     *MPRTPOS," ------";
          GOTO      PRNX5100
.
PRNX5200  PRINT     *MPRTPOS," ------"
          RETURN
.
.----------------------------------------------------------------------------
.         Print page separator type 2
.----------------------------------------------------------------------------
PRNX6000  MOVE      TWENTY8,MPRTPOS
          MOVE      ZERO,COUNT
.
PRNX6100  ADD       SEVEN,MPRTPOS
          ADD       ONE,COUNT
          COMPARE   COUNT,MPERUSED
          GOTO      PRNX6200 IF LESS
          PRINT     *MPRTPOS," ======";
          GOTO      PRNX6100
.
PRNX6200  PRINT     *MPRTPOS," ======"
          RETURN
.
.----------------------------------------------------------------------------
.         Print tail of report
.----------------------------------------------------------------------------
PRNX7000  PRINT     *N,*N,"***  End of Report  ***"
          RETURN
+
.
.**************************************************************************
.* CREA0000 : Create temp file 
.**************************************************************************
CREA0000  CLOSE     OUTTMPXX
          CALL      KILL0000
.
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    OUTTMP,CMDLINE
          APPEND    " 75 u1-9",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      OUTTMPXX,OUTTMP
CREA9999  RETURN
+
.
.**************************************************************************
.* KILL0000 : Remove temp file
.**************************************************************************
KILL0000  PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    OUTTMP,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
KILL9999  RETURN
+
.
.**************************************************************************
.* INTV0000 : Initialize page number and all total variables     
.**************************************************************************
INTV0000  MOVE      ZERO,CPAGENO
.
          MOVE      ZERO,TGPER1
          MOVE      ZERO,TGPER2
          MOVE      ZERO,TGPER3
          MOVE      ZERO,TGPER4
          MOVE      ZERO,TGPER5
          MOVE      ZERO,TGPER6
          MOVE      ZERO,TGPER7
          MOVE      ZERO,TGPER8
          MOVE      ZERO,TGPER9
          MOVE      ZERO,TGPER10
          MOVE      ZERO,TGPER11
          MOVE      ZERO,TGPER12
          MOVE      ZERO,TGPER13
          MOVE      ZERO,TGPERTOT
.
          MOVE      ZERO,TZPER1
          MOVE      ZERO,TZPER2
          MOVE      ZERO,TZPER3
          MOVE      ZERO,TZPER4
          MOVE      ZERO,TZPER5
          MOVE      ZERO,TZPER6
          MOVE      ZERO,TZPER7
          MOVE      ZERO,TZPER8
          MOVE      ZERO,TZPER9
          MOVE      ZERO,TZPER10
          MOVE      ZERO,TZPER11
          MOVE      ZERO,TZPER12
          MOVE      ZERO,TZPER13
          MOVE      ZERO,TZPERTOT
INVT9999  RETURN
+
.
.------------------------------------------------------------------------------
.         Temp file I/O's
.------------------------------------------------------------------------------
RDTMP1    MOVE      ZERO,OVRCD
          RESET     KEY9
          READ      OUTTMPXX,KEY9;TGROUP,TCTYPE,TCPER1,TCPER2,TCPER3,TCPER4:
                                  TCPER5,TCPER6,TCPER7,TCPER8,TCPER9,TCPER10:
                                  TCPER11,TCPER12,TCPER13
          GOTO      OVERCOND IF OVER
          RETURN
.
RDSTMP1   MOVE      ZERO,OVRCD
          RESET     KEY9
          READ      OUTTMPXX,KEY9;;
          RETURN
.
RDKTMP1   MOVE      ZERO,OVRCD
          READKS    OUTTMPXX;TGROUP,TCTYPE,TCPER1,TCPER2,TCPER3,TCPER4:
                             TCPER5,TCPER6,TCPER7,TCPER8,TCPER9,TCPER10:
                             TCPER11,TCPER12,TCPER13
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTMP1    MOVE      ZERO,OVRCD
          UPDATE    OUTTMPXX;TGROUP,TCTYPE,TCPER1,TCPER2,TCPER3,TCPER4:
                             TCPER5,TCPER6,TCPER7,TCPER8,TCPER9,TCPER10:
                             TCPER11,TCPER12,TCPER13
          RETURN
.
WRTMP1    MOVE      ZERO,OVRCD
          RESET     KEY9
          WRITE     OUTTMPXX,KEY9;KEY9,TCPER1,TCPER2,TCPER3,TCPER4:
                                  TCPER5,TCPER6,TCPER7,TCPER8,TCPER9:
                                  TCPER10,TCPER11,TCPER12,TCPER13
          GOTO      OVERCOND IF OVER
          RETURN
+
.==============================================================================
.
.==================================
. I/O INCLUDES
.==================================
           INC        STD001IO
           INC        PATCOMN3
           INC        TFILENAM
           INC        IBASEQIO/INC
           INC       OUTCTYIO/INC
           INC       OUTATTIO/INC
           INC        PATDRGIO/INC
           INC       PATCODIO/INC
           INC       WEBERRIO/INC
       
+
.

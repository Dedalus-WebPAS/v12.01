.***************************************************************************
.*   System        :  THEATRE                                              * 
.*   Program       :  IBAOPR10                                             *
.*   Description   :  Operation screen maintenance program                 *
.***************************************************************************
.*   Date          :  17/10/90                                             *
.*   Programmer    :  Glen Hobby                                           *
.*   Modifications :                                                       *
.*                    V5.08.B02  13/01/2000  Caleb Sharman  5.08 mods      *
.*                    Added a new field OPSCMAND which records if a time   *
.*                    field is mandatory or not                            *
.*                    V5.03.01  24/01/96   J.Tan    SRF 613583             *
.*                              Moved *EL to the start of line before      *
.*                              keying in fields                           *
.*                    V5.03.B1  23/06/1995  Matt                           *
.*                              Fixed Spelling mistake.                    *
.*                    V5.01.02  22/05/91  Steve O'Gorman                   *
.*                              Fixed Errors found by the New Compiler     *
.*                    V5.01.01 21/01/91 Rod Knwowles                       *
.*                             Ported from release 5                       *
.*                    V5.01 14/12/90 Graeme Williams                       *
.*                          Ported from release 5.01                       *
.*                    Paul Marshall  30/10/90                              *
.*                    Added option six (enquiry screen)                    *
.*                    Fixed minor bugs in insert and update routines       *
.*                                                                         *
.***************************************************************************
.
.$$$$$$
.  Operation screen file maintenance (IBAOPR10)
.  --------------------------------------------
.
.  Open files :
.               OPRSCRFD
.
.  Display screen layout
.  Keyin the first two codes.  These make up the key to the record.
.
.  Insert mode -
.                Keyin the rest of the variables.
.                Post away.
.
.  Modify mode -
.                Read the record on file and display the rest of the variables.
.                Make any changes
.                Post away.
.
.  Delete mode - 
.                Read the record on file and display the rest of the variables.
.                Delete the record
.
.  Print master listing - 
.                Obtain the starting and finishing values.  Loop through the
.                OPRSCRFD file over this range and print out all records found.
.
.  Copy mode - 
.                Keyin the FROM unique screen id.  Validate that this id
.                exists on file.  
.                Keyin the TO unique screen id.  Validate that this id does
.                not exist on file.
.                Copy all FROM records to the TO records.
.
.  End
.
.$$$$$$
          INC       STD001FD
.
          INC       OPRSCRFD/INC             * Operation screen
.
.***************************************************************************
.*                            GLOBAL VARIABLES                             *
.***************************************************************************
COUNTER   FORM      4                        * Record counter
FINISH    DIM       8                        * Finishing code
START     DIM       8                        * Starting code
.
PROGVARN  FORM      2
PROGDROW  FORM      2
PROGDCOL  FORM      2
PROGKCOL  FORM      2
PROGDSCL  FORM      2
PROGDESC  DIM       20
PROGDSIZ  FORM      2
PROGSIZE  FORM      2
PROGKROW  FORM      2
PROGDSRW  FORM      2
PROGKEY   DIM       8
.
XXS       DIM       127
.
.***************************************************************************
.*                              CONSTANTS                                  *
.***************************************************************************
.
ANSNO     INIT      "No"              * yes  
ANSYES    INIT      "Yes"             * no
OPR40BFL  INIT      "OPR40BFL"        * bookings
OPR43BFL  INIT      "OPR43BFL"        * bookings
OPR43US1  INIT      "OPR43US1"        * patient usage
OPR43US2  INIT      "OPR43US2"        * team usage
X30       INIT      "xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx"
.
PRGID     INIT      "IBAOPR10"               * program id
PRGNAM    INIT      "Operation Screen File Maintenance"
VERSION   INIT      "V12.01.00"
.
.***************************************************************************
.*                              MAINLINE                                   *
.*                            Controlling Logic                            *
.***************************************************************************
ML0000    CALL      INIT0000                 * display header and open files
.
ML1000    CALL      CLR0000                  * Clear variables 
          CALL      OPT0000                  * Exit or produce report 
.
          BRANCH    OPTION,ML2000,ML3000,ML4000,ML5000,ML6000,ML7000
          GOTO      ML9999
.
. ------ Input Operation Screen ------
.
ML2000    CALL      SCR0000                  * display screen layout 
ML2010    CALL      KEYA0000                 * Keyin first field
          BRANCH    EXIT,ML1000
ML2020    CALL      KEYB0000                 * Keyin second field
          BRANCH    EXIT,ML2010
          CALL      KEYR0000                 * Keyin rest of fields
          CALL      SLTM0000                 * Select field to change
          BRANCH    EXIT,ML1000              * Cancelled out
          CALL      POST0000                 * Post data away
          CALL      SCRN0000
          GOTO      ML2020
.
. ------ Modify Operation Screen -----
.
ML3000    CALL      SCR0000
ML3010    CALL      KEYA0000                 * Keyin first field
          BRANCH    EXIT,ML1000
ML3020    CALL      KEYB0000                 * Keyin second field
          BRANCH    EXIT,ML3010
          CALL      DISP0000                 * Display data
          CALL      SLTM0000                 * Select field to change
          BRANCH    EXIT,ML1000              * Cancelled out
          CALL      POST0000                 * Post data away
          CALL      SCRN0000
          GOTO      ML3020
.
. ------ Delete Operation Screen --------
.
ML4000    CALL      SCR0000
ML4010    CALL      KEYA0000                 * Keyin first field
          BRANCH    EXIT,ML1000
ML4020    CALL      KEYB0000                 * Keyin second field
          BRANCH    EXIT,ML4010
          CALL      DISP0000                 * Display data
          CALL      DELT0000                 * Delete data
          CALL      SCRN0000
          GOTO      ML4020
.
. ----- Print Master Listing -----
.
ML5000    CALL      PRNT0000                 * Do print run
          GOTO      ML1000
.
. ----- Copy -----
.
ML6000    CALL      COPY0000                 * Copy records
          GOTO      ML1000
.
ML7000    DISPLAY   *P1:4,*EF,"    Unique identification for screen : "
          CALL      KEYA0000
          CALL      SHOW0000                 * Display screen layout
          GOTO      ML1000
.
ML9999    CHAIN     PGM                      * chain back to program
.
.*************************************************************************
.*                                  INIT0000                             *
.*                 Display screen headings and open files                *
.* Called by : ML0000                                                    *
.*************************************************************************
INIT0000  CALL      DISPHEAD                 * display screen headings
.
          DISPLAY   *P50:24,*EL,"oprscraf";
          OPEN      OPRSCRA1,"oprscraf";
.
INIT9999  RETURN
.
.************************************************************************
.*                             CLR0000                                  *
.*  Clear variables                                                     *
.*  Called by : ML0000                                                  *
.************************************************************************
CLR0000   MOVE      ZERO,COUNTER
.
CLR9999   RETURN
.
.*************************************************************************
.*                                  OPT0000                              *
.*                       Select option                                   *
.* Called by: ML0000                                                     *
.*************************************************************************
OPT0000   HLINE     *G37,2,57,80
          DISPLAY   *P1:3,*EF: 
                    *P2:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P2:5,*V2LON,ONE,*HOFF:
                       " = Input Operation Screen":
                    *P2:6,*V2LON,TWO,*HOFF:
                       " = Modify Operation Screen":
                    *P2:7,*V2LON,THREE,*HOFF:
                       " = Delete Operation Screen":
                    *P2:8,*V2LON,FOUR,*HOFF:
                       " = Print Master Listing":
                    *P2:9,*V2LON,FIVE,*HOFF:
                       " = Copy Operation Screen":
                    *P2:10,*V2LON,SIX,*HOFF:
                       " = Display Screen Layout"
.
OPT1000   KEYIN     *P2:12,*EL,"Select Option : _",*P18:12,*V2LON,OPTION  
.
          BRANCH    OPTION,OPT9999,OPT9999,OPT9999,OPT9999,OPT9999,OPT9999
.
          COMPARE   ZERO,OPTION
          GOTO      OPT9999 IF EQUAL
.
          BEEP
          GOTO      OPT1000                  * Incorrect keyin
.
OPT9999   RETURN
.
.************************************************************************
.*                                 SCR0000                              *
.*                               Display screen layout                  *
.*  Called by : ML0000                                                  *
.************************************************************************
SCR0000   BRANCH    OPTION,SCR1000,SCR2000,SCR3000
.
SCR1000   DISPLAY   *P70:2," Insert "
          GOTO      SCR4000
.
SCR2000   DISPLAY   *P70:2," Modify "
          GOTO      SCR4000
.
SCR3000   DISPLAY   *P70:2," Delete "
.
SCR4000   DISPLAY   *P1:4,"    Unique identification for screen : ":
                    *P1:5,"    Field number                     : "
          DISPLAY   *P2:6,*EF,"   Variable number                  : ":
                    *P2:7,"   Display row                      : ":
                    *P2:8,"   Display column                   : ":
                    *P2:9,"   Keyin column                     : ":
                    *P2:10,"   Column code for code description : ":
                    *P2:11,"   Field description                : ":
                    *P2:12,"   Description size                 : ":
                    *P2:13,"   Field size / Code desc. size     : ":
                    *P2:14,"   Keyin row                        : ":
                    *P3:15,"  Row code for code description    : "
.
          BRANCH    OPTION,SCR4200,SCR4200,SCR4300
.
SCR4200   DISPLAY   *P1:6,*V2LON,ONE,*HOFF,".",*V2LON:
                    *P1:7,TWO,*HOFF,".",*V2LON:
                    *P1:8,THREE,*HOFF,".",*V2LON:
                    *P1:9,FOUR,*HOFF,".",*V2LON:
                    *P1:10,FIVE,*HOFF,".",*V2LON:
                    *P1:11,SIX,*HOFF,".",*V2LON:
                    *P1:12,SEVEN,*HOFF,".",*V2LON:
                    *P1:13,EIGHT,*HOFF,".",*V2LON:
                    *P1:14,NINE,*HOFF,".",*V2LON:
                    *P1:15,TEN,*HOFF,"."
          GOTO      SCR4400
.
SCR4300   DISPLAY   *P1:4,*V2LON,ONE:
                    *P1:5,TWO
.
SCR4400   BRANCH    OPTION,SCR5000,SCR6000,SCR6000
.
SCR5000  DISPLAY   *P40:4,UNDLN8:
                    *P40:5,UNDLN2
SCRN0000  DISPLAY   *P40:6,UNDLN2:
                    *P40:7,UNDLN2:
                    *P40:8,UNDLN2:
                    *P40:9,UNDLN2:
                    *P40:10,UNDLN2:
                    *P40:11,UNDLN20:
                    *P40:12,UNDLN2:
                    *P40:13,UNDLN2:
                    *P40:14,UNDLN2:
                    *P40:15,UNDLN2,*EF
          GOTO      SCR9999
.
SCR6000   
          DISPLAY   *P40:4,UNDLN8:
                    *P40:5,UNDLN2
.
SCR9999   RETURN
.
.***************************************************************************
.*                            KEYA0000                                     *
.*  Keyin first field - (first part of the key)                            *
.*  Called by : ML0000                                                     *
.***************************************************************************
KEYA0000  KEYIN     *P40:4,*EL,*DV,UNDLN8,*P40:4,*V2LON,OPSCKEY
.
          ENDSET    OPSCKEY                  * Unique id for screen
          APPEND    SP8,OPSCKEY
          RESET     OPSCKEY
.
          MATCH     SP8,OPSCKEY
          GOTO      KEYA9000 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KEYA9999
.
KEYA9000  MOVE      ONE,EXIT
.
KEYA9999  RETURN
.
.***************************************************************************
.*                       KEYB0000                       Called By : ML0000 *
.*        Keys in the field number ( 2nd part of the key )                 *
.***************************************************************************
KEYB0000 
.         Field number
.
          KEYIN     *P40:5,*EL,*DV,UNDLN2,*P40:5,*V2LON,OPSCFLD
.
.
          COMPARE   ZERO,OPSCFLD
          GOTO      KEYB9000 IF EQUAL
.
          CALL      VALD0000                 * Check if this record exists
.
.         Determine if we should display an error message
.
          BRANCH    OPTION,KEYB0100,KEYB0200,KEYB0200
.
KEYB0100  BRANCH    EXIT,KEYB8000            * Insert mode 
.
          DISPLAY   *P1:24,*EL,*B,"Record already exists.  ";
          CALL      HITENTER
          GOTO      KEYB0000                 * Keyin a new record
.
KEYB0200  COMPARE   ZERO,EXIT                * Modify/Change mode
          GOTO      KEYB8000 IF EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"Record does not exist.  ";
          CALL      HITENTER
          GOTO      KEYB0000                 * Keyin a new record
.
KEYB8000  MOVE      ZERO,EXIT
          GOTO      KEYB9999
.
KEYB9000  MOVE      ONE,EXIT
.
KEYB9999  RETURN
.
.**************************************************************************
.*                              VALD0000                                  *
.*  Check if this record exists                                           *
.*  Called by : KEYA0000                                                  *
.**************************************************************************
VALD0000  MOVE      OPSCFLD,DOPSCFLD 
.
          PACK      KEY10,OPSCKEY,DOPSCFLD
.
          CALL      RDOPSCR1
          BRANCH    OVRCD,VALD1000
.
          MOVE      ZERO,EXIT
          GOTO      VALD9999
.
VALD1000  MOVE      ONE,EXIT
.
VALD9999  RETURN
.
.**************************************************************************
.*                          KEYR0000                                      *
.*  Keyin rest of fields.                                                 *
.*  Called by : ML0000                                                    *
.**************************************************************************
KEYR0000  
.
          KEYIN     *P40:6,*EL,*DV,UNDLN2,*P40:6,*V2LON,OPSCVARN
.
          KEYIN     *P40:7,*EL,*DV,UNDLN2,*P40:7,*V2LON,OPSCDROW
.
          MOVE      OPSCDROW,OPSCKROW
          MOVE      OPSCDROW,OPSCDSRW
.
          DISPLAY   *P40:14,*V2LON,OPSCDROW
.
          DISPLAY   *P40:15,*V2LON,OPSCDROW
.
          KEYIN     *P40:8,*EL,*DV,UNDLN2,*P40:8,*V2LON,OPSCDCOL
.
          KEYIN     *P40:9,*EL,*DV,UNDLN2,*P40:9,*V2LON,OPSCKCOL
.
          KEYIN     *P40:10,*EL,*DV,UNDLN2,*P40:10,*V2LON,OPSCDSCL
.
          KEYIN     *P40:11,*EL,*DV,UNDLN20,*P40:11,*V2LON,OPSCDESC
.
          ENDSET    OPSCDESC                 * Field description
          APPEND    SP20,OPSCDESC
          RESET     OPSCDESC
.
          KEYIN     *P40:12,*EL,*DV,UNDLN2,*P40:12,*V2LON,OPSCDSIZ
.
          KEYIN     *P40:13,*EL,*DV,UNDLN2,*P40:13,*V2LON,OPSCSIZE
.
.
KEYR9999  RETURN
.
.***************************************************************************
.*                           SELT0000                                      *
.*  Select field to change                                                 *
.*  Called by : ML0000                                                     *
.***************************************************************************
SELT0000  CALL      MAINQST
.
          COMPARE   "-1",CCITEM
          GOTO      SELT9000 IF EQUAL        * Cancel
.
          COMPARE   ZERO,CCITEM              * Post
          GOTO      SELT8000 IF EQUAL
.
SELT1000  BRANCH    CCITEM,SELT2000,SELT3000,SELT4000,SELT5000,SELT6000:
                           SELT7000,SELT7100,SELT7200,SELT7300,SELT7400:
                           SELT7500,SELT7600
.
          BEEP
          GOTO      SELT0000 
.
.         Unique id for screen
.
SELT2000  KEYIN     *P40:4,*EL,*DV,UNDLN10,*P40:4,*V2LON,OPSCKEY
.
          ENDSET    OPSCKEY                  * Unique id for screen
          APPEND    SP8,OPSCKEY
          RESET     OPSCKEY
.
.         Check that the key is valid
.
          MOVE      OPSCFLD,DOPSCFLD 
          PACK      KEY10,OPSCKEY,DOPSCFLD
          BRANCH    OPTION,SELT2100,SELT2200
.
.         Insert mode, record cannot exist
.
SELT2100  CALL      RAOPSCR1
          BRANCH    OVRCD,SELT0000
.
          DISPLAY   *P1:24,*EL,*B,"Record already exists.  ";
          CALL      HITENTER
          GOTO      SELT2000 
.
.         Modify mode, record must exist
.
SELT2200  CALL      RAOPSCR1
          BRANCH    OVRCD,SELT2300
          GOTO      SELT0000
.
SELT2300  DISPLAY   *P1:24,*EL,*B,"Record does not exist.  ";
          CALL      HITENTER
          GOTO      SELT2000
.
.         Field number
.
SELT3000  KEYIN     *P40:5,*EL,*DV,UNDLN2,*P40:5,*V2LON,OPSCFLD
.
.         Check that the key is valid
.
          MOVE      OPSCFLD,DOPSCFLD 
          PACK      KEY10,OPSCKEY,DOPSCFLD
          BRANCH    OPTION,SELT3100,SELT3200
.
.         Insert mode, record cannot exist
.
SELT3100  CALL      RAOPSCR1
          BRANCH    OVRCD,SELT0000
.
          DISPLAY   *P1:24,*EL,*B,"Record already exists.  ";
          CALL      HITENTER
          GOTO      SELT3000 
.
.         Modify mode, record must exist
.
SELT3200  CALL      RAOPSCR1
          BRANCH    OVRCD,SELT3300
          GOTO      SELT0000
.
SELT3300  DISPLAY   *P1:24,*EL,*B,"Record does not exist.  ";
          CALL      HITENTER
          GOTO      SELT3000
.
SELT4000  KEYIN     *P40:6,*EL,*DV,UNDLN2,*P40:6,*V2LON,OPSCVARN
          GOTO      SELT0000
.
SELT5000  KEYIN     *P40:7,*EL,*DV,UNDLN2,*P40:7,*V2LON,OPSCDROW
          GOTO      SELT0000
.
SELT6000  KEYIN     *P40:8,*EL,*DV,UNDLN2,*P40:8,*V2LON,OPSCDCOL
          GOTO      SELT0000
.
SELT7000  KEYIN     *P40:9,*EL,*DV,UNDLN2,*P40:9,*V2LON,OPSCKCOL
          GOTO      SELT0000
.
SELT7100  KEYIN     *P40:10,*EL,*DV,UNDLN2,*P40:10,*V2LON,OPSCDSCL
          GOTO      SELT0000
.
SELT7200  KEYIN     *P40:11,*EL,*DV,UNDLN20,*P40:11,*V2LON,OPSCDESC
.
          ENDSET    OPSCDESC                 * Field description
          APPEND    SP20,OPSCDESC
          RESET     OPSCDESC
          GOTO      SELT0000
.
SELT7300  KEYIN     *P40:12,*EL,*DV,UNDLN2,*P40:12,*V2LON,OPSCDSIZ
          GOTO      SELT0000
.
SELT7400  KEYIN     *P40:13,*EL,*DV,UNDLN2,*P40:13,*V2LON,OPSCSIZE
          GOTO      SELT0000
.
SELT7500  KEYIN     *P40:14,*EL,*DV,UNDLN2,*P40:14,*V2LON,OPSCKROW
          GOTO      SELT0000
.
SELT7600  KEYIN     *P40:15,*EL,*DV,UNDLN2,*P40:15,*V2LON,OPSCDSRW
          GOTO      SELT0000
.
.
SELT8000  MOVE      ZERO,EXIT
          GOTO      SELT9999
.
SELT9000  MOVE      ONE,EXIT
.
SELT9999  RETURN
.
.***************************************************************************
.*                           SLTM0000                                      *
.*  Select field to change - modify option                                 *
.*  Called by : ML0000                                                     *
.***************************************************************************
SLTM0000  CALL      MAINQST
.
          COMPARE   "-1",CCITEM
          GOTO      SLTM9000 IF EQUAL        * Cancel
.
          COMPARE   ZERO,CCITEM              * Post
          GOTO      SLTM8000 IF EQUAL
.
SLTM1000  BRANCH    CCITEM,SLTM4000,SLTM5000,SLTM6000:
                           SLTM7000,SLTM7100,SLTM7200,SLTM7300,SLTM7400:
                           SLTM7500,SLTM7600,SLTM7700
.
          BEEP
          GOTO      SLTM0000 
.
SLTM4000  KEYIN     *P40:6,*EL,*DV,UNDLN2,*P40:6,*V2LON,OPSCVARN
          GOTO      SLTM0000
.
SLTM5000  KEYIN     *P40:7,*EL,*DV,UNDLN2,*P40:7,*V2LON,OPSCDROW
          GOTO      SLTM0000
.
SLTM6000  KEYIN     *P40:8,*EL,*DV,UNDLN2,*P40:8,*V2LON,OPSCDCOL
          GOTO      SLTM0000
.
SLTM7000  KEYIN     *P40:9,*EL,*DV,UNDLN2,*P40:9,*V2LON,OPSCKCOL
          GOTO      SLTM0000
.
SLTM7100  KEYIN     *P40:10,*EL,*DV,UNDLN2,*P40:10,*V2LON,OPSCDSCL
          GOTO      SLTM0000
.
SLTM7200  KEYIN     *P40:11,*EL,*DV,UNDLN20,*P40:11,*V2LON,OPSCDESC
.
          ENDSET    OPSCDESC                 * Field description
          APPEND    SP20,OPSCDESC
          RESET     OPSCDESC
.
          GOTO      SLTM0000
.
SLTM7300  KEYIN     *P40:12,*EL,*DV,UNDLN2,*P40:12,*V2LON,OPSCDSIZ
          GOTO      SLTM0000
.
SLTM7400  KEYIN     *P40:13,*EL,*DV,UNDLN2,*P40:13,*V2LON,OPSCSIZE
          GOTO      SLTM0000
.
SLTM7500  KEYIN     *P40:14,*EL,*DV,UNDLN2,*P40:14,*V2LON,OPSCKROW
          GOTO      SLTM0000
.
SLTM7600  KEYIN     *P40:15,*EL,*DV,UNDLN2,*P40:15,*V2LON,OPSCDSRW
          GOTO      SLTM0000
.
SLTM7700  KEYIN     *P40:16,*EL,*DV,UNDLN2,*P40:16,*V2LON,DIM1
          REP       "Y1N0",DIM1
          MOVE      DIM1,FORM1
          IF        FORM1 = ONE | FORM1 = ZERO
            IF        FORM1 = ZERO
              DISPLAY   *P40:16,ANSNO
            ELSE
              DISPLAY   *P40:16,ANSYES
            ENDIF
.
            MOVE      FORM1,OPSCMAND
          ELSE
            DISPLAY   *P1:24,"Enter Y/N ";
            GOTO      SLTM7700
          ENDIF
          GOTO      SLTM0000
.
SLTM8000  MOVE      ZERO,EXIT
          GOTO      SLTM9999
.
SLTM9000  MOVE      ONE,EXIT
.
SLTM9999  RETURN
.
.****************************************************************************
.*                            POST0000                                      *
.*  Post data away.                                                         *
.*  Called by : ML0000                                                      *
.****************************************************************************
POST0000  MOVE      OPSCFLD,DOPSCFLD 
          PACK      KEY10,OPSCKEY,DOPSCFLD
.
.         Either write a new record or update an existing record
.
          BRANCH    OPTION,POST1000,POST2000       
.
POST1000  CALL      WROPSCR1
          GOTO      POST9999
.
POST2000  CALL      MVIN0000
          CALL      RSOPSCR1
          CALL      MOUT0000
          CALL      UPOPSCR1
.
POST9999  RETURN
.
.****************************************************************************
.*                            MVIN0000                                      *
.*  Move data into temporary variables                                      *
.*  Called by : ML0000                                                      *
.****************************************************************************
MVIN0000   MOVE     OPSCVARN,PROGVARN
           MOVE     OPSCDROW,PROGDROW
           MOVE     OPSCDCOL,PROGDCOL
           MOVE     OPSCKCOL,PROGKCOL
           MOVE     OPSCDSCL,PROGDSCL
           MOVE     OPSCDESC,PROGDESC
           MOVE     OPSCDSIZ,PROGDSIZ
           MOVE     OPSCSIZE,PROGSIZE
           MOVE     OPSCKROW,PROGKROW
           MOVE     OPSCDSRW,PROGDSRW
.
MVIN9999  RETURN
.
.****************************************************************************
.*                            MOUT0000                                      *
.*  Move data into file variables                                           *
.*  Called by : ML0000                                                      *
.****************************************************************************
MOUT0000   MOVE     PROGVARN,OPSCVARN
           MOVE     PROGDROW,OPSCDROW
           MOVE     PROGDCOL,OPSCDCOL
           MOVE     PROGKCOL,OPSCKCOL
           MOVE     PROGDSCL,OPSCDSCL
           MOVE     PROGDESC,OPSCDESC
           MOVE     PROGDSIZ,OPSCDSIZ
           MOVE     PROGSIZE,OPSCSIZE
           MOVE     PROGKROW,OPSCKROW
           MOVE     PROGDSRW,OPSCDSRW
.
MOUT9999  RETURN
.
.****************************************************************************
.*                            DISP0000                                      *
.*  Display data                                                            *
.*  Called by : ML0000                                                      *
.****************************************************************************
DISP0000  DISPLAY   *P40:6,OPSCVARN:
                    *P40:7,OPSCDROW:
                    *P40:8,OPSCDCOL:
                    *P40:9,OPSCKCOL:
                    *P40:10,OPSCDSCL:
                    *P40:11,OPSCDESC:
                    *P40:12,OPSCDSIZ:
                    *P40:13,OPSCSIZE:
                    *P40:14,OPSCKROW:
                    *P40:15,OPSCDSRW
.
          MOVE      OPSCVARN,FORM2
.
          MATCH     OPSCKEY,OPR40BFL       * check if booking
          IF        @EQUAL         
            IF        FORM2 = FORTY4|FORM2 = FORTY6|FORM2 = FIFTY6
                                           * check if a time field
              GOTO       DISP5000          * display mandatory details
            ENDIF
          ENDIF
.
          MATCH     OPSCKEY,OPR43US2       * check if team usage 
          IF        @EQUAL
            IF        FORM2 > ZERO & FORM2 < FIVE
                                           * check if a time field
              GOTO      DISP5000           * display mandatory details
            ENDIF
          ENDIF
.
          MATCH     OPSCKEY,OPR43US1       * check if patient usage
          IF        @EQUAL
            IF        FORM2 > TEN4 & FORM2 < TWENTY4 
                                           * check if a time field
              GOTO      DISP5000           * display mandatory details
            ENDIF
          ENDIF
.
          MATCH     OPSCKEY,OPR43BFL       * check if a booking
          IF        @EQUAL
            IF        FORM2 = FORTY4|FORM2 = FORTY6|FORM2 = FIFTY6
                                           * check if a time field
              GOTO      DISP5000           * display mandatory details
            ENDIF
          ENDIF
.
          GOTO      DISP9999       * not a time field so skip mand. details
.
DISP5000  DISPLAY   *P1:16,*V2LON,TEN1,*HOFF,".":
                    *P5:16,"Mandatory (Y/N)                  : ";
          IF        OPSCMAND  = 1
            DISPLAY   ANSYES
          ELSE
            DISPLAY   ANSNO
          ENDIF
.
DISP9999  RETURN
.
.**************************************************************************
.*                              DELT0000                                  *
.*  Delete data                                                           *
.*  Called by : ML0000                                                    *
.**************************************************************************
DELT0000  CALL      DELQST
          COMPARE   ONE,CEXIT
          GOTO      DELT9999 IF NOT EQUAL
.
          MOVE      OPSCFLD,DOPSCFLD 
          PACK      KEY10,OPSCKEY,DOPSCFLD
          CALL      DEOPSCR1
.
DELT9999  RETURN
.
.**************************************************************************
.*                             PRNT0000                                   *
.*  Print off the master listing                                          *
.*  Called by : ML0000                                                    *
.**************************************************************************
PRNT0000  DISPLAY   *P57:2,"- Master Listing ": 
                    *P1:13,"Starting Code  : ":
                    *P1:14,"Finishing Code : "
          CALL      STRT0000                 * Get starting code
          CALL      FNSH0000                 * Get finishing code
.
          CALL      CONTQST
          COMPARE   ONE,CEXIT
          GOTO      PRNT9999 IF NOT EQUAL
.
          DISPLAY   *P1:24,*EL,"*** Printing ***    Unique id for":
                    " screen : ";
.
          CLOCK     TIME,CTIMEIS
          MOVE      ZERO,CPAGENO
          MOVE      ONE,CNOUNDLN
.
          CALL      HEAD0000                 * Print page header
.
          PACK      KEY10,START,SP10 
.
          CALL      RSOPSCR1
.
PRNT1000  CALL      RKOPSCR1
          BRANCH    OVRCD,PRNT9000
.
.         We don't need to check FINISH if it is all Z's
.
          MATCH     "ZZZZZZZZ",FINISH
          GOTO      PRNT1100 IF EQUAL        
.
          MATCH     FINISH,OPSCKEY
          GOTO      PRNT1100 IF EQUAL
          GOTO      PRNT9000 IF NOT LESS
.
PRNT1100  DISPLAY   *P44:24,*V2LON,*EL,OPSCKEY; * Show some screen action
.
          PRINT     "|",OPSCKEY,*18,"|",OPSCFLD,*28,"|",OPSCDROW:
                    *40,"|",OPSCDCOL,*53,"|",OPSCKROW,*63,"|",OPSCKCOL:
                    *74,"|",OPSCVARN,*84,"|":
                    OPSCDESC,*105,"|",OPSCDSIZ,*132,"|"
          CALL      UND132
          PRINT     "|",OPSCSIZE,*28,"|",OPSCDSRW,*53,"|",OPSCDSCL:
                    *132,"|" 
          CALL      UNDE0000
.
          ADD       ONE,COUNTER              * Increment record counter
.
          ADD       FOUR,CLNO
          COMPARE   "55",CLNO
          GOTO      PRNT1000 IF LESS
.
          CALL      HEAD0000                 * Go to a new page
          GOTO      PRNT1000
.
PRNT9000  PRINT     *N,*N,"*** End of Report ***     Number of records ":
                    "printed = ",COUNTER
.
PRNT9999  RETURN
.
.**************************************************************************
.*                             STRT0000                                   *
.*  Get starting code                                                     *
.*  Called by : PRNT0000                                                  *
.**************************************************************************
STRT0000  KEYIN     *P19:13,*EL,*DV,UNDLN8,*P19:13,*V2LON,START
.
          ENDSET    START
          APPEND    SP8,START
          RESET     START
.
          MATCH     SP8,START
          GOTO      STRT9999 IF NOT EQUAL
.
          DISPLAY   *P19:13,*V2LON,"Start"
.
STRT9999  RETURN
.
.**************************************************************************
.*                              FNSH0000                                  *
.*  Get finishing code                                                    *
.*  Called by : PRNT0000                                                  *
.**************************************************************************
FNSH0000  KEYIN     *P19:14,*EL,*DV,UNDLN8,*P19:14,*V2LON,FINISH
.
          ENDSET    FINISH
          APPEND    SP8,FINISH
          RESET     FINISH
.
          MATCH     SP8,FINISH
          GOTO      FNSH8000 IF NOT EQUAL
.
          DISPLAY   *P19:14,*V2LON,"Finish"
          MOVE      "ZZZZZZZZ",FINISH
.
.         Check that FINISH > or = to START
.
FNSH8000  MATCH     FINISH,START
          GOTO      FNSH9999 IF LESS
          GOTO      FNSH9999 IF EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"Finish value must be > or = to start ":
                    "value.  ";
          CALL      HITENTER
          GOTO      FNSH0000
.
FNSH9999  RETURN
.
.**************************************************************************
.*                           HEAD0000                                     *
.*  Print page header                                                     *
.*  Called by : PRNT0000                                                  *
.**************************************************************************
HEAD0000  CALL      HEAD132
.
          MATCH     SP9,START
          GOTO      HEAD1000 IF NOT EQUAL
.
          PRINT     *40,"From : Start to : "; 
          GOTO      HEAD1500
.
HEAD1000  PRINT     *40,"From : ",START," to : ";
. 
HEAD1500  MATCH     "ZZZZZZZZ",FINISH
          GOTO      HEAD2000 IF NOT EQUAL
.
          PRINT     "Finish" 
          GOTO      HEAD3000
.
HEAD2000  PRINT     FINISH
.
HEAD3000  CALL      UND132
          PRINT     *1,"|Unique Screen Id|Field No.|Display Row|Display":
                    " Col.|Keyin Row|Keyin Col.|Var. Num.|":
                    "Field Description   |Desc. size",*132,"|"
          CALL      UND132
          PRINT     "|Field Size/Code Desc. Size|Row Code for Code Desc. ":
                    "|Column Code for Code Desc.",*132,"|" 
.
          CALL      UND132
.
          MOVE      NINE,CLNO               * Set line number
.
HEAD9999  RETURN
.
.************************************************************************
.*                              UNDE0000                                *
.*  Print a line of equal signs                                         *
.*  Called by : PRNT0000                                                *
.************************************************************************
UNDE0000  PRINT     "*====================================================":
                    "=====================================================":
                    "=========================*"
.
UNDE9999  RETURN
.
.**************************************************************************
.*                                COPY0000                                *
.*  Copy records                                                          *
.*  Called by : ML0000                                                    *
.**************************************************************************
COPY0000  DISPLAY   *P57:2,"- Copy ":
                    *P1:13,"From Unique id for screen :":
                    *P1:14,"To Unique id for screen   :" 
          CALL      FROM0000                 * Get from value
          BRANCH    EXIT,COPY9999
          CALL      TOID0000                 * Get to id value
          BRANCH    EXIT,COPY9999
.
          CALL      CONTQST
          BRANCH    CEXIT,COPY1000,COPY0000,COPY9999
.
COPY1000  CALL      COPR0000                 * Copy the record
.
COPY9999  RETURN
.
.**************************************************************************
.*                              FROM0000                                  *
.*  Get from unique id for screen                                         *
.*  Called by : COPY0000                                                  *
.**************************************************************************
FROM0000  KEYIN     *P29:13,*EL,*DV,UNDLN8,*P29:13,*V2LON,START
.
          ENDSET    START
          APPEND    SP8,START
          RESET     START
.
          MATCH     SP8,START
          GOTO      FROM9000 IF EQUAL
.
.         Now this unique id must exist on file.
.
          PACK      KEY10,START,SP2
.
          CALL      RSOPSCR1
          CALL      RKOPSCR1
          BRANCH    OVRCD,FROM1000
.
          MATCH     OPSCKEY,START
          GOTO      FROM8000 IF EQUAL
.
FROM1000  DISPLAY   *P1:24,*EL,*B,"This Unique id does not exist.  ";
          CALL      HITENTER
          GOTO      FROM0000
.
FROM8000  MOVE      ZERO,EXIT
          GOTO      FROM9999
.
FROM9000  MOVE      ONE,EXIT
.
FROM9999  RETURN
.
.****************************************************************************
.*                               TOID0000                                   *
.*  Get to unique id for screen                                             *
.*  Called by : COPY0000                                                    *
.****************************************************************************
TOID0000  KEYIN     *P29:14,*EL,*DV,UNDLN8,*P29:14,*V2LON,FINISH
.
          ENDSET    FINISH
          APPEND    SP8,FINISH
          RESET     FINISH
.
          MATCH     SP8,FINISH
          GOTO      TOID9000 IF EQUAL
.
.         Now this to value must not exist.
.
          PACK      KEY10,FINISH,SP2
          CALL      RSOPSCR1
          CALL      RKOPSCR1
          BRANCH    OVRCD,TOID8000
.
          MATCH     OPSCKEY,FINISH
          GOTO      TOID8000 IF NOT EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"This Unique id already exists.  ";
          CALL      HITENTER
          GOTO      TOID0000
.
TOID8000  MOVE      ZERO,EXIT
          GOTO      TOID9999
.
TOID9000  MOVE      ONE,EXIT
.
TOID9999  RETURN
.
.***************************************************************************
.*                               COPR0000                                  *
.*  Copy the records                                                       *
.*  Called by : COPY0000                                                   *
.***************************************************************************
COPR0000  CALL      COPS0000                 * Display the copy screen
.
          PACK      KEY10,START,SP2
          CALL      RSOPSCR1
.
COPR1000  CALL      RKOPSCR1
          BRANCH    OVRCD,COPR9000
.
          MATCH     OPSCKEY,START
          GOTO      COPR9000 IF NOT EQUAL
.
          ADD       ONE,COUNTER               * Incremement record counter
.
          DISPLAY   *P31:19,*V2LON,OPSCFLD
.
          PACK      KEY10,FINISH,OPSCFLD
          CALL      WROPSCR1
.
          DISPLAY   *P52:19,*V2LON,OPSCFLD
.
          GOTO      COPR1000
.
COPR9000  DISPLAY   *P1:21,*B,"Finished copy.  Total records copied = ":
                    *V2LON,COUNTER:
                    *P1:24,*EL;
          CALL      HITENTER 
.
COPR9999  RETURN
.
.**************************************************************************
.*                              COPS0000                                  *
.*  Display the copy screen                                               *
.*  Called by : COPR0000                                                  *
.**************************************************************************
COPS0000  DISPLAY   *P25:16,"----- FROM -----":
                    *P46:16,"----- TO -----":
                    *P1:18,"Unique Id For Screen :":
                    *P31:18,*V2LON,START:
                    *P52:18,FINISH:
                    *P1:19,*HOFF,"Field Number         :"
.
COPS9999  RETURN
.
.***************************************************************************
.*                           SHOW0000                   Called By : ML0000 *
.***************************************************************************
SHOW0000  PACK      KEY10,OPSCKEY,SP10
.
          DISPLAY   *P10:6,"The screen will appear as below, with _ denoting ":
                    "code keyins":
                  *P10:7," and xxx denoting descriptions and free format keyins"
.
          MOVE      OPSCKEY,PROGKEY
.
          CALL       RSOPSCR1          * Positional Read
.
SHOW1000  CALL       RKOPSCR1          * Subsequent Read
          BRANCH     OVRCD,SHOW9999
.
          PACK      XXS,X30,X30,X30,X30
.
          MATCH      OPSCKEY,PROGKEY
.
          GOTO       SHOW9999 IF NOT EQUAL
.
          RESET      OPSCDESC,OPSCDSIZ
          LENSET     OPSCDESC
          ENDSET     OPSCDESC
.
          RESET      XXS,OPSCSIZE
          LENSET     XXS
          ENDSET     XXS
.
          COMPARE    OPSCDSCL,ZERO
          GOTO       SHOW2000 IF EQUAL
.
          DISPLAY   *POPSCDCOL:OPSCDROW,*V2LON,DOPSCFLD,*HOFF,". ",*+,OPSCDESC:
                    *POPSCKCOL:OPSCKROW,UNDLN1,*POPSCDSCL:OPSCDSRW,*+,XXS
.
          RESET     XXS
.
          GOTO      SHOW1000
.
SHOW2000  
          DISPLAY   *POPSCDCOL:OPSCDROW,*V2LON,DOPSCFLD,*HOFF,". ",*+,OPSCDESC:
                    *POPSCKCOL:OPSCKROW,*+,XXS
.
          RESET     XXS
.
          GOTO      SHOW1000
.
SHOW9999  DISPLAY   *P1:24,"";
          CALL      HITENTER
          RETURN
.
.----------------------------------------------------------------------------
          INC       STD001IO
.
          INC       OPRSCRIO/INC             

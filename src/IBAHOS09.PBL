.******************************************************************************
.*                                                                            *
.*                        P R O G R A M   S U M M A R Y                       *
.*                        -------------   -------------                       *
.*                                                                            *
.*     PROGRAM NAME  :  IBAHOS09                                              *
.*                                                                            *
.*     FUNCTION      :  IN-PATIENT WARD/BED REPORT                            *
.*                                                                            *
.*     DATE WRITTEN  :  26/05/1988                                            *
.*                                                                            *
.*     AUTHOR        :  KARIN PEAK                                            *
.*                                                                            *
.*     MODIFICATIONS :                                                        *
.*                                                                            *
.******************************************************************************
.*        V12.00.01 28/05/2005 PJ Le Febour AlphaNum Visitnum                 *
.*                  amended COMPARE ZERO,WADMNO to MATCH ZEROVISN,WADMNO      *
.******************************************************************************
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V9.09.00  15/01/2008 Jill Habasque CAR 145292                       *
.*                  Ported from V9.07 to V9.08                                *
.*        V9.07.00  13/07/2007 Ian Farnell                                    *
.*                  Ported from V9.06 to V9.07                                *
.******************************************************************************
.*        V9.06.00  12/07/2007 Ian Farnell                                    *
.*                  Ported from V6 to V9.06                                   *
.******************************************************************************
.*        V6.06.01  10/02/1999  Jill Habasque                                 *
.*                  Fixed DISPHEAD display                                    *
.*        V6.05.01  04/11/1998 Nicole Harrington                              *
.*                  Changed to use HEAD132                                    *
.*        V6.00.00  15/10/91 i chung           SRF 110157                     *
.*                  Increased print size of Surname & Given Names             *
.*        V6.00.01  16/10/1992    Graeme Williams                             *
.*                  Don't print age if PTCNNRHD = 1                           *
.*        V6.04.01  03/06/1997    ROD                                         *
.*                  check if inactive ward for single ward option             *
.******************************************************************************
.
          INC       STD001FD
.
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATNOBFD/INC
          INC       PATWR1FD/INC
.
. 	  Work Variables
.
IBCNMHOS  FORM    1
PSNAME14  DIM       14
PGNAME19  DIM       19
PTITL4    DIM       4
PTCNNRHD  FORM      1
.
SWARD     DIM       3
SBED      DIM       3
.
WARD      DIM       3                          * Ward input - V5.53 (SRF 106231)
.
XDAY1     DIM       2
XDAY2     DIM       2
XMON1     DIM       2
XMON2     DIM       2
XYEAR1    DIM       2
XYEAR2    DIM       2
XCENT1    DIM       2
XCENT2    DIM       2
.
PRGID     INIT      "IBAHOS09"
PRGNAM    INIT      "IN-PATIENT WARD/BED REPORT" 
VERSION   INIT      "V12.01.00"
+
.
.         Display Screen Header and Open files
.
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patnobef";
          OPEN      PATNOBE1,"patnobef"
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATWR1A3,"patwr1af"      * added for V5.53 - SRF 106213
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TWENTY;*183,PTCNNRHD
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
.         Start of Program
.
START     CALL      KHOS0000
          CALL      MSCREEN
.
RKSTRT    KEYIN     *P17:8,*EL,*DV,UNDLN1,*P17:8,*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
          BRANCH    OPTION,ALLWRDS,ONEWRD
          BEEP
          GOTO      RKSTRT
.
.         Main Menu Screen
.
MSCREEN   CALL      DISPHEAD
          DISPLAY   *P1:3,*EF:
                    *P2:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P2:5,*V2LON,ONE,*HOFF," = All Wards":
                    *P2:6,*V2LON,TWO,*HOFF," = Single Ward":
                    *P1:8,"Select Option :";
          RETURN
+
.
.         All wards on the System
.
ALLWRDS   MOVE      ZERO,CPAGENO
          MOVE      SP3,SWARD
.
          MOVE      "60",CLNO
          DISPLAY   *P53:2,"- ",*V2LON,"for all Wards",*HOFF:
                    *P1:24,*EL,*P20:24,"Ward :",*P40:24,"Bed :";
.
          MOVE      SP6,KEY6
          CALL      RDSWARD1
.
NEXTW     CALL      RDKWARD1
          BRANCH    OVRCD OF ENDP  
.
          BRANCH    WACTIVE,NXTWARD        * Ignore non-active wards
.
          IF        IBCNMHOS=1
            PACK      PTHSHOSP,PTHSHOSP,SP70
            MATCH     SP10,PTHSHOSP        * All Hospitals
            GOTO      NEXTW10 IF EQUAL
.
            MATCH     PTWDHOSN,PTHSHOSP    * Correct hospital
            GOTO      NXTWARD IF NOT EQUAL
          ENDIF
.
NEXTW10   MATCH     WWARD,SWARD            * same Ward ?
          GOTO      PATWRD IF EQUAL        * yes
          CALL      HEAD                   * no - print headings
          MOVE      WWARD,SWARD
.
          COMPARE   ONE,WINPUT             * Ward Only Input ?
          GOTO      NEXTW IF NOT EQUAL     * no
.
          PACK      KEY13 WITH WWARD,SP6,SP2
          CALL      RDSNOBE1
NOBLP     CALL      RDKNOBE1
          BRANCH    OVRCD OF NEXTW
.
          MATCH     WWARD,NBWARD
          GOTO      NEXTW IF NOT EQUAL
.
          MOVE      NBADMNO,WADMNO
          CALL      DISPDA1
          GOTO      NOBLP
.
PATWRD    MATCH     ZEROVISN,WADMNO
          GOTO      EMPDA1 IF EQUAL
          CALL      DISPDA1
          GOTO      NEXTW
.
NXTWARD   MATCH     SP3,WBED
          GOTO      NEXTW IF NOT EQUAL
          PACK      KEY6,WWARD,ANSZ,ANSZ,ANSZ
          CALL      RDSWARD1
          GOTO      NEXTW
.
NOWARD    DISPLAY   *P1:24,*EL,*P25:24,*B,*V2LON:
                    "** No available data on file **",*W2;
          GOTO      START
.
EMPDA1    CALL      EMPBED1
          GOTO      NEXTW
.
.         OVER condition found..print last line of -- and display message
.
ENDP      COMPARE   ZERO,CPAGENO
          GOTO      NOWARD IF EQUAL
          PRINT     *N,*N," ***  End of Report  ***"
.
          DISPLAY   *P1:24,*EL,*P24:24,*V2LON:
                    "** Report Generation Completed **",*W2;
          GOTO      START
.
.         Print actual data
.
DISPDA1   MOVE      WADMNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD OF EMPBED1
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF EMPBED1
.
FPRINT    MOVE      PGNAME,PGNAME19
          MOVE      PSNAME,PSNAME14
          MOVE      PTITL,PTITL4
          MOVE      SP3,ANS
.
          COMPARE   "57",CLNO
          CALL      HEAD IF NOT LESS
.
          COMPARE   ONE,PTCNNRHD
          GOTO      PRNT8000 IF EQUAL       * new header
.
          PRINT     *1,"| ",WWARD,*7,WBED:
                    *10,"| ",PSNAME14,ANS,PTITL4,ANS,PGNAME19:
                    *51,"|",PSAGE,*57,"|",*64,"|",*72,"|",*80,"|":
                    *87,"|",*94,"|",*102,"|",*111,"|",*132,"|"
          GOTO      PRNT9000
.
PRNT8000  PRINT     *1,"| ",WWARD,*7,WBED:
                    *10,"| ",PSNAME14,ANS,PTITL4,ANS,PGNAME19:
                    *51,"|",*57,"|",*64,"|",*72,"|",*80,"|":
                    *87,"|",*94,"|",*102,"|",*111,"|",*132,"|"
.
PRNT9000  ADD       ONE,CLNO
.
          CALL      PAGSEP
          CALL      PAGEND
.
          DISPLAY   *P27:24,*V2LON,WWARD,*P46:24,WBED;
          MOVE      WWARD,SWARD
          RETURN
.
.         Empty bed printout
.
EMPBED1   COMPARE   "57",CLNO
          CALL      HEAD IF NOT LESS
.
          PRINT     *1,"| ",WWARD,*7,WBED,*10,"|",*51,"|",*57,"|",*64,"|":
                    *72,"|",*80,"|",*87,"|",*94,"|",*102,"|",*111,"|",*132,"|"
.
          ADD       ONE,CLNO
          CALL      PAGSEP
.
.         Detail Line separator
.
PAGEND    PRINT     *1,"*-----------------------------------------":
                       "-------------------------------------------":
                       "----------------------------------------------*"
          ADD       ONE,CLNO
          RETURN
.
PAGSEP    PRINT    *1,"|",*10,"|":
		   *51,"|",*57,"|",*64,"|",*72,"|",*80,"|",*87,"|":
                   *94,"|",*102,"|",*111,"|",*132,"|"
.
          ADD       ONE,CLNO
          RETURN
.
.         Headings for output
.
HEAD      CALL      IBACLOCK 
          MOVE      ONE,CNOUNDLN
          CALL      HEAD132
.
          BRANCH    OPTION,HEAD1
          PRINT     *40,"(Ward ",WARD,")",*N      * added for V5.53 - SRF 106213
          GOTO      HEAD2
.
HEAD1     PRINT     *40,"(All Wards)",*N          * added for V5.53 - SRF 106213
.
HEAD2     MOVE      FIVE,CLNO 
          CALL      PAGEND
.
          IF        PTCNNRHD = ONE
            PRINT     *1,"|        |             ":
                      *51,"|     |      |       |       |      |EVEN'G|      ":
                      " |        |                    |":
                      *N,*1,"| Rm  Bed| Patient Name":
                      *51,"|DIET |B'FAST| M/T   | LUNCH | A/T  | MEAL | SUPP ":
                      " | B'FAST | Remarks            |"
          ELSE
            PRINT     *1,"|  Ward",*10,"|",*51,"|",*57,"|",*64,"|",*72,"|":
                      *80,"|",*87,"|",*94,"|",*102,"|",*111,"|",*132,"|":
                      *N,*1,"|  /Bed",*10,"| Patient Name":
                      *51,"|"," Age",*57,"| FAST",*64,"| FR.FL",*72,"| LIGHT":
                      *80,"| SOFT",*87,"| SPEC",*94,"| CL FL",*102,"| ADMISS":
                      *111,"| Anticipated Changes",*132,"|"
          ENDIF
.
          ADD       TWO,CLNO
          CALL      PAGEND
          RETURN
+
.******************************************************************************
.*        ONEWRD                               Added for V5.53 - SRF 106213   *
.*        This routine prints the report for a single ward only               *
.******************************************************************************
ONEWRD    DISPLAY   *P53:2,"- ",*V2LON,"for a single Ward",*HOFF:
                    *P1:12,*EL,"Please enter Ward :";
.
INPWRD    KEYIN     *P21:12,*EF,*DV,UNDLN3,*P21:12,*V2LON,WARD;
          ENDSET    WARD
          APPEND    SP3,WARD
          RESET     WARD
.
          MATCH     SP3,WARD
          GOTO      START IF EQUAL
          DISPLAY   *P21:12,*V2LON,WARD;
.
          CMATCH    "?",WARD                     * Wards scan ?
          GOTO      WARDOPTS IF EQUAL            * yes
.
          PACK      KEY6,WARD,SP3
          CALL      RDWARD1
          BRANCH    OVRCD,INPWRD1
          COMPARE   ZERO,WACTIVE                 * is Ward actvie ?
          GOTO      INPWRD3 IF EQUAL             * yes
.
          DISPLAY   *P1:24,*EL,*B,*V2LON,"** Ward is INACTIVE **    ";
          GOTO      INPWRD2
.
INPWRD1   DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Ward does not exist on file **    ";
.
INPWRD2   CALL      HITENTER
          GOTO      INPWRD
.
INPWRD3   DISPLAY   *P28:12,WBDESC;
.
          CALL      CONTQST                      * OK to continue ?
          BRANCH    CEXIT,PRTWRD,INPWRD,START    * 1 = Yes, 2 = No, 3 = Cancel
          GOTO      INPWRD3
.
.         Wards scan
.
WARDOPTS  CALL      PATWRDDS
          CALL      MSCREEN
          DISPLAY   *P17:8,*V2LON,OPTION;
          GOTO      ONEWRD
.
.         Print data
.
PRTWRD    DISPLAY   *P1:24,*EL,*P20:24,"Ward :",*P40:24,"Bed :";
.
          MOVE      ZERO,CPAGENO
          MOVE      "60",CLNO
.
          COMPARE   ONE,WINPUT                   * Ward Only Input ?
          GOTO      PRTWRD4 IF NOT EQUAL         * no
.
.         Print from No-Bed file
.
          PACK      KEY13 WITH WWARD,SP6,SP2
          CALL      RDSNOBE1
.
PRTWRD1   CALL      RDKNOBE1
          BRANCH    OVRCD OF ENDP                * end of file
.
          MATCH     WWARD,NBWARD                 * same Ward ?
          GOTO      ENDP IF NOT EQUAL            * no
.
          MOVE      NBADMNO,WADMNO
          CALL      DISPDA1
          GOTO      PRTWRD1
.
.         Print from Ward/Bed file
.
PRTWRD2   MATCH     ZEROVISN,WADMNO              * zero Admission Number ?
          GOTO      PRTWRD3 IF EQUAL             * yes - print blank line
          CALL      DISPDA1                      * no - print detail
          GOTO      PRTWRD4
.
PRTWRD3   CALL      EMPBED1
.
PRTWRD4   CALL      RDKWARD1                     * read next Ward/Bed record
          BRANCH    OVRCD OF ENDP                * end of file
.
          BRANCH    WACTIVE,PRTWRD4              * inactive?
.
          MATCH     WWARD,WARD                   * same Ward ?
          GOTO      PRTWRD2 IF EQUAL             * yes
          GOTO      ENDP                         * no
.
ENDIT     CHAIN     PGM
          STOP
.
.******************************************************************************
.*                                  KHOS0000              Called by: ML0000   *
.*                            Enter The hospital                              *
.******************************************************************************
KHOS0000  COMPARE   ONE,IBCNMHOS
          GOTO      KHOS9999 IF NOT EQUAL
.
          DISPLAY   *P1:13,*EL,"Hospital Id : ";
          MOVE      TEN5,ECOL
          MOVE      TEN3,EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS2000,KHOS9500
          GOTO      KHOS3000
.
KHOS2000  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          MOVE      ZERO,EXIT
.
KHOS3000  CLOSE     PATHSPA1
.
          DISPLAY   *P20:13,*V2LON,PTHSNAME;
          GOTO      KHOS9999
.
KHOS9500  MOVE      ONE,EXIT
KHOS9999  RETURN
.
+
.==============================================================================
.
          INC       STD001IO
          INC       PATWRDDS                   * added for V5.53 - SRF 106213
          INC       PATHSPKY
          INC       PATHSPIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATNOBIO/INC
          INC       PATWR1IO/INC

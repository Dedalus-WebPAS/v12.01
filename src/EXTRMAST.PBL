.*****************************************************************************
.* System    :   Patient Management System                                   *
.* Program   :   EXTRMAST                                                    *
.* Desc      :   PMI Data Extraction Program                                 *
.*****************************************************************************
.* Date      :   24/02/2014                                                  *
.* Author    :   Steve Armstrong                                             *
.* Function  :   This program will extract the PMI Master Details into a     *
.*               pipe delimited file in a format that will feed into         *
.*               CONVMAST.                                                   *
.* Mods:                                                                     *
.*        V11.04.01 29/08/2024  PJ Le Febour     TSK 0950549                 *
.*                  PROC0800 - MOVE SPACES to XDVAC PROC2000 - STRIP XDVAC   *
.*        V10.12.01 29/05/2018  Steve Armstrong  TSK 0857962                 *
.*                  Fixed infinite look on DVA card check.                   *
.*****************************************************************************
.*        V10.10.01 20/03/2017  Steve Armstrong  TSK 0832066                 *
.*                  Mods to only use Concession Card file for DVA data       *
.*                  instead of PREPAT and PMPXDVAC.                          *
.*                  Also fixed the loop through pmsccdaf to use RPPMCCD1     *
.*                  instead of RKPMCCD1 as no records would have been found  *
.*                  and added a check on whether expiry date is blank.       *
.*****************************************************************************
.*        V10.08.01 06/10/2016  Steve Armstrong  TSK 0820875                 *
.*                  Added new field XPEML                                    *
.*****************************************************************************
.*        V10.06.01 13/04/2015  Steve Armstrong  CAR 313894                  *
.*                  Added new field XIWICOD1 (pmpxiwi1)                      *
.*****************************************************************************
.*        V10.04.00 24/02/2014  Steve Armstrong  CAR 296128                  *
.*                  Created program.                                         *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       IBACONFD/INC
          INC       NHIMASFD/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATMA1FD/INC
          INC       PMSCCDFD/INC
          INC       PMSPX2FD/INC
          INC       PMSRESFD/INC
.
.
PTMASEXT  FILE      ASCII,FIXED=4096
.
.
.         Patient Master Details file layout - convmast.txt
.         (as per CONVMAST.PBL)
.
.Name     Type    Length Description
.----     ----    ------ -----------
XURNO     DIM       8    U/R number/NHI Number                  (purno)
XTIT      DIM       4    Patient title                          (ptitl)
XSURN     DIM       40   Patient surname                        (ptmasnam)
XPGNAM1   DIM       40   Patient First Name                     (pmpxfnam)
.                                                               (pgname)
XPGNAM2   DIM       40   Patient Second Name                    (pmpxsnam)
.                                                               (pgname)
XPSURN    DIM       19   Previous Surname                       (ppsname)
XADD1     DIM       35   Patient address line 1                 (padd1)
XADD2     DIM       35   Patient address line 2                 (padd2)
XADD3     DIM       35   Patient address line 3                 (psubrb)
XADD4     DIM       35   Patient address line 4                 (padd4)
XPOST     DIM       8    Patient postcode                       (ppost)
XTELP     DIM       20   Patient private phone no.              (ptelep)
XTELB     DIM       20   Patient business phone no.             (pteleb)
XCELLPH   DIM       20   Patient mobile phone No.               (ptmxcell)
XPADD1    DIM       35   Postal Address Line 1                  (pmtxadd1)
XPADD2    DIM       35   Postal Address Line 2                  (ptmxadd2)
XPADD3    DIM       35   Postal Address Line 3                  (ptmxadd3)
XPADD4    DIM       35   Postal Address Line 4                  (ptmxadd4)
XPCODE    DIM       8    Postal Address Post Code               (ptmxpost)
XSEX      DIM       1    Patients sex                           (psex)
XMSTAT    DIM       3    Patients marital status (Cat M)        (pmstat)
XDOB      DIM       8    Patients date of birth (ccyymmdd)      (pbdate)
XPMICRO   DIM       20   Microfilm Address                      (pmicro)
XCOB      DIM       3    Patients country of birth (Cat C)      (pcont)
XABORIG   DIM       3    Aboriginality (Cat VA)                 (pmpxabrg)
XREL      DIM       3    Patients religion (Cat R)              (preg)
XOCCUP    DIM       14   Patients occupation                    (poccup)
XSMOK     DIM       1    Patient smoker                         (psmok)
.                          0=Unknown  1=yes  2=no
XPBDEBT   DIM       1    Bad Debt Indicator                     (pbdebt)
.                          0=no  1=yes
XPCASE    DIM       1    Case Notes Indicator                   (pcase)
.                          0=no  1=yes
XPDIET    DIM       3    Diet Code (Cat DC)                     (pbdiet)
XDATDTH   DIM       8    Date of death (ccyymmdd)               (pdecdte)
XDCEASE   DIM       1    Deceased indicator                     (pcease)
.                          0=no  1=yes
XDETYP    DIM       3    Death Type (Cat DY)                    (pmpxdety)
XAUTOP    DIM       1    Autopsy indicator                      (pautopy)
.                          0=no  1=yes
XNOKNAME  DIM       20   NOK name                               (pnkname)
XNOKADD1  DIM       35   NOK address line 1                     (pnkadd1)
XNOKADD2  DIM       35   NOK address line 2                     (pnkadd2)
XNOKADD3  DIM       35   NOK address line 3                     (pnksubr)
XNOKADD4  DIM       35   NOK address line 4                     (pnkadd4)
XNOKPOST  DIM       8    NOK Post Code                          (pnkpost)
XNOKPPHN  DIM       20   NOK private phone no.                  (pnktelp)
XNOKBPHN  DIM       20   NOK business phone no.                 (pnktelb)
XPMPXKMO  DIM       20   NOK mobile phone No.                   (pmpxkmob)
XNOKREL   DIM       10   NOK relationship (pmsrelaf)            (pnkrelp)
XCONNAME  DIM       20   Emer.contact name                      (ptmxecnm)
XCONADD1  DIM       35   Emer.contact address line 1            (ptmxeca1)
XCONADD2  DIM       35   Emer.contact address line 2            (ptmxeca2)
XCONADD3  DIM       35   Emer.contact address line 3            (ptmxeca3)
XCONADD4  DIM       35   Emer.contact address line 4            (ptmxeca4)
XCONPOST  DIM       8    Emer.contact Post Code                 (ptmxecpc)
XCONPPHN  DIM       20   Emer.contact private phone no.         (ptmxecpp)
XCONBPHN  DIM       20   Emer.contact business phone no.        (ptmxecbp)
XPMPXCMO  DIM       20   Emer.contact mobile phone No.          (pmpxcmob)
XCONREL   DIM       10   Emer. contact relationship (pmsrelaf)  (ptmxecre)
XNEANAME  DIM       20   Nearest Rel name                       (ptmxnrnm)
XNEAADD1  DIM       35   Nearest Rel address line 1             (ptmxnra1)
XNEAADD2  DIM       35   Nearest Rel address line 2             (ptmxnra2)
XNEAADD3  DIM       35   Nearest Rel address line 3             (ptmxnra3)
XNEAADD4  DIM       35   Nearest Rel address line 4             (ptmxnra4)
XNEAPOST  DIM       8    Nearest Rel Post Code                  (ptmxnrpc)
XNEAPPHN  DIM       20   Nearest Rel private phone no.          (ptmxnrpp)
XNEABPHN  DIM       20   Nearest Rel business phone no.         (ptmxnrbp)
XPMPXRMO  DIM       20   Nearest Relative Mobile Phone No.      (pmpxrmob)
XNEAREL   DIM       10   Nearest Rel relationship (pmsrelaf)    (ptmxnrre)
.
XREGGPP   DIM       10   Registered GP Practice (pmshcgaf)      (pmpxrh1g)
XREGGP    DIM       10   Registered GP (pmshcpaf)               (pmpxrhc1)
XHFUND    DIM       6    CHE/Board Code (Health Fund-patfn1af)  (pfundh)
XHFTAB    DIM       8    Health Fund Table (patfn1af)           (pfndtb)
XFNDMM    DIM       19   Patient HF Membership number           (pfndmm)
XPFNDYY   DIM       2    Years of Fund Membership               (pfndyy)
XPFNDCG   DIM       6    Last Fund Cover Change                 (pfndcg)
XPMEDI    DIM       10   Medicare Number                        (pmedi)
XPMCCD    DIM       2    Medicare Code                          (ptmxmccd)
XMEDCEX   DIM       6    Medicare Expiry Date (ccyymm)          (pmpxmedc)
XPLDDT    DIM       8    Last Discharge Date (ccyymmdd)         (plddate)
XREPAT    DIM       10   Repat number                           (prepat)
XPENNO    DIM       15   Pension number                         (ppenno)
.                                                               (pmcdcnum)
XPNDTE    DIM       6    Pension no expiry date (ccyymm)        (ppndte)
.                                                               (pmcdexdt)
XDVAC     DIM       3    DVA Card Colour (Cat DX)               (pmpxdvac)
XCARD     DIM       20   Community Card Number                  (ptmxcard)
XEXPD     DIM       8    Comm. Card No. Exp. Date (ccyymmdd)    (ptmxcexp)
XEXMT     DIM       3    Community Card Exemption Type (Cat XM) (ptmxcxmp)
XFAMID    DIM       12   Community Card Family Id.              (ptmxfmly)
XPUSR1    DIM       3    User Defined Field 1 (Cat P1)          (pusr1)
XPUSR2    DIM       3    User Defined Field 2 (Cat P2)          (pusr2)
XPUSR3    DIM       3    User Defined Field 3 (Cat P3)          (pusr3)
XPUSR4    DIM       3    User Defined Field 4 (Cat P4)          (pusr4)
XPUSR5    DIM       3    User Defined Field 5 (Cat P5)          (pusr5)
XPUSR6    DIM       3    User Defined Field 6 (Cat P6)          (pusr6)
XPUYN1    DIM       1    User Defined Yes/No field 1            (puyn1)
XPUYN2    DIM       1    User Defined Yes/No field 2            (puyn2)
XPUYN3    DIM       1    User Defined Yes/No field 3            (puyn3)
XPUYN4    DIM       1    User Defined Yes/No field 4            (pmpxuyn4)
XPUYN5    DIM       1    User Defined Yes/No field 5            (pmpxuyn5)
.
XALTNUMB  DIM       20   Alternate ID (pmaialid) - No longer used
XNATNUMB  DIM       20   National Number (ptninmpi) - Not used for extraction
.
XPTYPE    DIM       3    Residence/Patient Type (Cat T )        (ptype)
XRSCONT   DIM       3    Country of Residence   (Cat C )        (pmrscont)
XPYRRES   DIM       2    Number of Years Residence              (pyrres)
XPXLNG1   DIM       3    Pref. Language 1 (Cat LA)              (pmpxlng1)
XPXINTR   DIM       1    Interpreter Req'd (0=No, 1=Yes)        (pmpxintr)
XPSTAT    DIM       1    Record Status (0=Valid, 1=Merged)      (pstat)
XRSTEXP   DIM       8    Temp Residence Exp. Date (ccyymmdd)    (pmrstexp)
XRSCOMM   DIM       127  Residence Comments                     (pmrscomm)
.
.         NZ NHI Fields
.
XMANMPI   DIM       7    NHI Number                             (nhmanmpi)
XMAURNO   DIM       8    Local U/R (NHI)                        (nhmaurno)
XMASURN   DIM       25   Surname                                (nhmasurn)
XMAGIV1   DIM       20   Given Name 1                           (nhmagiv1)
XMAGIV2   DIM       20   Given Name 2                           (nhmagiv2)
XMAGIV3   DIM       20   Given Name 3                           (nhmagiv3)
XMAPREI   DIM       1    Preferred Name Indicator (1-3)         (nhmaprei)
XMAADD1   DIM       35   Address Line 1                         (nhmaadd1)
XMAADD2   DIM       30   Address Line 2                         (nhmaadd2)
XMAADD3   DIM       30   Suburb                                 (nhmaadd3)
XMAADD4   DIM       30   City/Town                              (nhmaadd4)
XMAADD5   DIM       30   Country                                (nhmaadd5)
XMADOB    DIM       8    Date of Birth (ccyymmdd)               (nhmadob)
XMADDTH   DIM       8    Date of Death (ccyymmdd)               (nhmaddth)
XMADOMC   DIM       4    Domicile Code                          (nhmadomc)
XMARES    DIM       1    Residence Status (Y/N)                 (nhmares)
XMAETH    DIM       2    Ethnicity (nhiethaf)                   (nhmaeth)
XMASEX    DIM       1    Gender                                 (nhmasex)
XMADAT    DIM       8    Date Last Updated (ccyymmdd)           (nhmaadat)
XMATIM    DIM       8    Time Last Updated (hh:mm:ss)           (nhmaatim)
XMAETH2   DIM       2    2nd Ethnicity (nhiethaf)               (nhmaeth2)
XMAETH3   DIM       2    3rd Ethnicity (nhiethaf)               (nhmaeth3)
.
.         Additional Fields
.
XCHGDTE   DIM       8    date of last change (ccyymmdd)         (pchgdte)
XTMADCDT  DIM       8    Demographics Confirmed Date (ccyymmdd) (ptmadcdt)
XTMXDEAF  DIM       3    Deaf (Cat DK)                          (ptmxdeaf)
XSNTNUMB  DIM       20   Safety Net Card Number                 (pmcdcnum)
XSNTEDTE  DIM       8    Saf.Net Card Expiry Date (ccyymmdd)    (pmcdexdt)
XIWICOD1  DIM       3    IWI code 1 (Category iw)               (pmpxiwi1)
XPEML     DIM       80   Patient's Email Address                (pmpxpeml)
.
.
. LOCAL VARIABLE DEFINITION
. -------------------------
CURRDATE  DIM       8
DVAFLAG   FORM      1             * DVA Flag
.                                     0 = DVA concession card record not found
.                                     1 = DVA concession card record found
EXTCOUNT  FORM      10
PENFLAG   FORM      1             * Pension Flag
.                                     0 = Pension conc. card record not found
.                                     1 = Pension conc. card record found
RECCOUNT  FORM      10
SNNFLAG   FORM      1             * Safety Net Flag
.                                     0 = Safety Net conc. card record not found
.                                     1 = Safety Net conc. card record found
STRTDATE  DIM       8
.
.
. PROGRAM CONSTANTS
. -----------------
CATCT     INIT      "ct"
PIPE      INIT      "|"
TILDA19   INIT      "~~~~~~~~~~~~~~~~~~~"
.
PRGID     INIT      "EXTRMAST"
PRGNAM    INIT      "PMI Master Details Extraction"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000:      * Full PMI extract
                            MAIN1000       * PMI updates only
.
MAIN1000  CALL      GDAT0000               * get current & update start date
          BRANCH    EXIT,MAIN0100          * nothing entered
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN5000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN5000  CALL      CFIL0000               * create file
          BRANCH    EXIT,MAIN0100          * file exists
.
          CALL      PROC0000               * process
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"pmsccdaf";
          OPEN      PMSCCDA1,"pmsccdaf"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"pmresaff";
          OPEN      PMSRESA1,"pmsresaf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TWENTY1;*138,PTCNNHII
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          IF        PTCNNHII = 1
            DISPLAY   *P64:24,"nhimasaf";
            OPEN      NHIMASA2,"nhimasaf"
          ENDIF
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run data extraction                     *
.*                        TRUE  (1)  Exit option selected                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Initial PMI Data Extraction":
                    *P1:6,*V2LON,TWO,*HOFF,". Update PMI Data Extraction"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ":
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000:            * run extraction
                            OPTN9000             * run extraction
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               GDAT0000              Called by: MAIN0000   *
.*                Get the current date and start date for updates            *
.* Returns: EXIT  0 =  valid date input                                      *
.*                1 =  nothing input                                         *
.*          STRTDATE = starting date for updates (Option 2 only)             *
.*****************************************************************************
.
GDAT0000  CALL      IBACLOCK                     * load current date
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          BRANCH    COPTION,GDAT9000             * full load
.
GDAT1000  DISPLAY   *P1:11,*EF,"From Date :"
          MOVE      ONE,CCENTRY
          MOVE      TEN1,CVERT
          MOVE      TEN3,CCOL
          MOVE      ONE,CCANLDTE
          MOVE      THREE,CDEFDTE
.
.         Set up default to current date
.
          UNPACK    CURRDATE,CCENT,CYEAR,CMON,CDAY
.
          CALL      KEYDATE
          BRANCH    OVRCD,GDAT9100
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
.
          MATCH     STRTDATE,CURRDATE            * date in future ?
          GOTO      GDAT8000 IF LESS             * yes
          GOTO      GDAT9000                     * no
.
GDAT8000  DISPLAY   *P1:24,*EL,*B,"Date cannot be in the future.  ";
          CALL      HITENTER
          GOTO      GDAT1000
.
GDAT9000  MOVE      ZERO,EXIT
          GOTO      GDAT9999
.
GDAT9100  MOVE      ONE,EXIT
.
GDAT9999  RETURN
+
.*****************************************************************************
.*                                  CFIL0000       Called by: MAIN0000       *
.*                         Create the extract file                           *
.*****************************************************************************
.
CFIL0000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PTMASEXT,"convmast"     * extract file exists ?
          TRAPCLR   IO
          BRANCH    OVRCD,CFIL5000          * no - create it
.
CFIL1000  KEYIN     *P1:23,*B,*EF,"The extract file convmast.txt already ":
                    "exists.":
                    *P1:24,"Overwrite it (",*V2LON,*DV,ANSY,*HOFF,"/":
                    *V2LON,*DV,ANSN,*HOFF,")?":
                    *P21:24,*V2LON,ANS
.
          REP       UPPLOW,ANS
          MATCH     ANSY,ANS                * "Y" entered ?
          GOTO      CFIL2000 IF EQUAL       * yes - continue
.
          MATCH     ANSN,ANS                * "N" entered ?
          GOTO      CFIL9100 IF EQUAL       * no
.
          BEEP
          GOTO      CFIL1000
.
.         Delete the existing file
.
CFIL2000  CLOSE     PTMASEXT,DELETE
.
.         Create a new extract file
.
CFIL5000  PREP      PTMASEXT,"convmast"
.
          MOVE      ZERO,EXIT
          GOTO      CFIL9999
.
CFIL9100  MOVE      ONE,EXIT
.
CFIL9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*        Loop through the PMI master file and extract each record           *
.*****************************************************************************
.
PROC0000  MOVE      ZERO,RECCOUNT                * initialise record count
          MOVE      ZERO,EXTCOUNT                * initialise extract count
          DISPLAY   *P1:24,*EL,"Processing:";
.
          MOVE      SP8,KEY8
          CALL      RDSMAST1                     * position at start of file
PROC0500  CALL      RDKMAST1                     * read next record
          BRANCH    OVRCD,PROC9000               * eof - finished
.
          ADD       ONE,RECCOUNT                 * increment record counter
.
          DISPLAY   *P13:24,*V2LON,RECCOUNT
.
.         If we are running option 2, then only extract those records that
.         were created or last updated after the date
.
          BRANCH    COPTION,PROC0800             * option 1
.
          MATCH     SP8,PCHGDTE                  * blank change date ?
          GOTO      PROC0600 IF EQUAL            * yes - check registration date
.
          MATCH     STRTDATE,PCHGDTE             * last change before st. date ?
          GOTO      PROC0500 IF LESS             * yes - ignore record
          GOTO      PROC0800                     * no - process record
.
PROC0600  MATCH     SP8,PTMXRDAT                 * blank registration date ?
          GOTO      PROC0500 IF EQUAL            * yes
.
          MATCH     STRTDATE,PTMXRDAT            * registered before st. date ?
          GOTO      PROC0500 IF LESS             * yes - ignore record
.
PROC0800  CALL      CLPMSPX2
          MOVE      PURNO,KEY8
          CALL      RDPMPX21                     * pmspx2af record on file ?
.
          CALL      CLNHIMAS                     * clear nhi variables
          MOVE      SP8,NHMADAT                  * clear vars set in CLNHIMAS
          MOVE      SP8,NHMATIM
.
          MOVE      SP3,XDVAC                    * clear DVA 
         
          IF        PTCNNHII = 1
            MOVE      PURNO,KEY8
            CALL      RDNHMAS2                   * nhi record on file ?
          ENDIF
.
          CALL      CLPMSRES
          MOVE      PURNO,KEY8
          CALL      RDPMRES1
.
.         Only one of each of DVA, Pension and Safety Net card can be recorded,
.         so use the latest.
.
          MOVE      ZERO,DVAFLAG                 * init. conc. card flags
          MOVE      ZERO,PENFLAG
          MOVE      ZERO,SNNFLAG
.
          MOVE      SP10,XREPAT                  * load default DVA number
          MOVE      PPENNO,XPENNO                * load default pension number
          MOVE      PPNDTE,XPNDTE                * load default pen expiry date
          MOVE      SP20,XSNTNUMB
          MOVE      SP8,XSNTEDTE
.
          PACK      KEY19,PURNO,TILDA19
          CALL      RSPMCCD1                     * position after last record
PROC1000  CALL      RPPMCCD1                     * read previous record
          BRANCH    OVRCD,PROC2000               * eof - finished
.
          MATCH     PURNO,PMCDURNO               * same U/R still ?
          GOTO      PROC2000 IF NOT EQUAL        * no - finished
.
          MATCH     SP8,PMCDEXDT                 * blank expiry date ?
          IF        !@EQUAL
            MATCH     CURRDATE,PMCDEXDT          * expiry date < current date ?
            GOTO      PROC1000 IF LESS           * yes - finished
          ENDIF
.
.         Check if this is a DVA, Pension or Safety Net Card Type ?
.         Cat ct Indicator 1,  2 = Safety Net Number
.                              3 = DVA
.                              4 = Pension
.
          PACK      KEY5,CATCT,PMCDCTYP
          CALL      RDCODE1                      * code on file ?
          BRANCH    OVRCD,PROC1000               * no - ignore record
.
          MATCH     "2",TCINDC1                  * Safety Net card ?
          GOTO      PROC1100 IF NOT EQUAL        * no - check if DVA card
.
.         Check if we already have the latest card for this card type
.
          BRANCH    SNNFLAG,PROC1000             * ignore record
.
          MOVE      PMCDCNUM,XSNTNUMB            * load SN card number
          MOVE      PMCDEXDT,XSNTEDTE            * load SN expiry date
.
          MOVE      ONE,SNNFLAG                  * set Safety Net flag
          GOTO      PROC1000
.
PROC1100  MATCH     "3",TCINDC1                  * DVA card ?
          GOTO      PROC1200 IF NOT EQUAL        * no - check if Pension card
.
.         Check if we already have the latest card for this card type
.
          BRANCH    DVAFLAG,PROC1000             * ignore record
.
          MOVE      PMCDCNUM,XREPAT              * load DVA card number
          MOVE      PMCDDVAC,XDVAC               * load DVA card colour code
.
          MOVE      ONE,DVAFLAG
          GOTO      PROC1000
.
PROC1200  MATCH     "4",TCINDC1                  * Pension card ?
          GOTO      PROC1000 IF NOT EQUAL        * no - ignore record
.
.         Check if we already have the latest card for this card type
.
          BRANCH    PENFLAG,PROC1000             * ignore record
.
          MOVE      PMCDCNUM,XPENNO              * load Pension card number
          MOVE      PMCDEXDT,XPNDTE              * load Pension expiry date
.
          MOVE      ONE,PENFLAG
          GOTO      PROC1000
.
PROC2000  MOVE      PURNO,XURNO
          SQUEEZE   XURNO
          MOVE      PTITL,XTIT
          SQUEEZE   XTIT
          MOVE      PTMASNAM,XSURN
          STRIP     XSURN
          MOVE      PMPXFNAM,XPGNAM1
          STRIP     XPGNAM1
          MOVE      PMPXSNAM,XPGNAM2
          STRIP     XPGNAM2
          MOVE      PPSNAME,XPSURN
          STRIP     XPSURN
          MOVE      PADD1,XADD1
          STRIP     XADD1
          MOVE      PADD2,XADD2
          STRIP     XADD2
          MOVE      PSUBRB,XADD3
          STRIP     XADD3
          MOVE      PADD4,XADD4
          STRIP     XADD4
          MOVE      PPOST,XPOST
          SQUEEZE   XPOST
          MOVE      PTELEP,XTELP
          STRIP     XTELP
          MOVE      PTELEB,XTELB
          STRIP     XTELB
          MOVE      PTMXCELL,XCELLPH
          STRIP     XCELLPH
          MOVE      PTMXADD1,XPADD1
          STRIP     XPADD1
          MOVE      PTMXADD2,XPADD2
          STRIP     XPADD2
          MOVE      PTMXADD3,XPADD3
          STRIP     XPADD3
          MOVE      PTMXADD4,XPADD4
          STRIP     XPADD4
          MOVE      PTMXPOST,XPCODE
          SQUEEZE   XPCODE
          MOVE      PSEX,XSEX
          SQUEEZE   XSEX
          MOVE      PMSTAT,XMSTAT
          SQUEEZE   XMSTAT
          MOVE      PBDATE,XDOB
          SQUEEZE   XDOB
          MOVE      PMICRO,XPMICRO
          STRIP     XPMICRO
          MOVE      PCONT,XCOB
          SQUEEZE   XCOB
          MOVE      PMPXABRG,XABORIG
          SQUEEZE   XABORIG
          MOVE      PREG,XREL
          SQUEEZE   XREL
          MOVE      POCCUP,XOCCUP
          STRIP     XOCCUP
          MOVE      PSMOK,XSMOK
          SQUEEZE   XSMOK
          MOVE      PBDEBT,XPBDEBT
          SQUEEZE   XPBDEBT
          MOVE      PCASE,XPCASE
          SQUEEZE   XPCASE
          MOVE      PDIET,XPDIET
          SQUEEZE   XPDIET
          MOVE      PDECDTE,XDATDTH
          SQUEEZE   XDATDTH
          MOVE      PCEASE,XDCEASE
          SQUEEZE   XDCEASE
          MOVE      PMPXDETY,XDETYP
          SQUEEZE   XDETYP
          MOVE      PAUTOPY,XAUTOP
          SQUEEZE   XAUTOP
          MOVE      PNKNAME,XNOKNAME
          STRIP     XNOKNAME
          MOVE      PNKADD1,XNOKADD1
          STRIP     XNOKADD1
          MOVE      PNKADD2,XNOKADD2
          STRIP     XNOKADD2
          MOVE      PNKSUBR,XNOKADD3
          STRIP     XNOKADD3
          MOVE      PNKADD4,XNOKADD4
          STRIP     XNOKADD4
          MOVE      PNKPOST,XNOKPOST
          SQUEEZE   XNOKPOST
          MOVE      PNKTELP,XNOKPPHN
          STRIP     XNOKPPHN
          MOVE      PNKTELB,XNOKBPHN
          STRIP     XNOKBPHN
          MOVE      PMPXKMOB,XPMPXKMO
          STRIP     XPMPXKMO
          MOVE      PNKRELP,XNOKREL
          SQUEEZE   XNOKREL
          MOVE      PTMXECNM,XCONNAME
          STRIP     XCONNAME
          MOVE      PTMXECA1,XCONADD1
          STRIP     XCONADD1
          MOVE      PTMXECA2,XCONADD2
          STRIP     XCONADD2
          MOVE      PTMXECA3,XCONADD3
          STRIP     XCONADD3
          MOVE      PTMXECA4,XCONADD4
          STRIP     XCONADD4
          MOVE      PTMXECPC,XCONPOST
          SQUEEZE   XCONPOST
          MOVE      PTMXECPP,XCONPPHN
          STRIP     XCONPPHN
          MOVE      PTMXECBP,XCONBPHN
          STRIP     XCONBPHN
          MOVE      PMPXCMOB,XPMPXCMO
          STRIP     XPMPXCMO
          MOVE      PTMXECRE,XCONREL
          SQUEEZE   XCONREL
          MOVE      PTMXNRNM,XNEANAME
          STRIP     XNEANAME
          MOVE      PTMXNRA1,XNEAADD1
          STRIP     XNEAADD1
          MOVE      PTMXNRA2,XNEAADD2
          STRIP     XNEAADD2
          MOVE      PTMXNRA3,XNEAADD3
          STRIP     XNEAADD3
          MOVE      PTMXNRA4,XNEAADD4
          STRIP     XNEAADD4
          MOVE      PTMXNRPC,XNEAPOST
          SQUEEZE   XNEAPOST
          MOVE      PTMXNRPP,XNEAPPHN
          STRIP     XNEAPPHN
          MOVE      PTMXNRBP,XNEABPHN
          STRIP     XNEABPHN
          MOVE      PMPXRMOB,XPMPXRMO
          STRIP     XPMPXRMO
          MOVE      PTMXNRRE,XNEAREL
          SQUEEZE   XNEAREL
          MOVE      PMPXRH1G,XREGGPP
          SQUEEZE   XREGGPP
          MOVE      PMPXRHC1,XREGGP
          SQUEEZE   XREGGP
          MOVE      PFUNDH,XHFUND
          SQUEEZE   XHFUND
          MOVE      PFNDTB,XHFTAB
          STRIP     XHFTAB
          MOVE      PFNDMM,XFNDMM
          SQUEEZE   XFNDMM
          MOVE      PFNDYY,XPFNDYY
          SQUEEZE   XPFNDYY
          MOVE      PFNDCG,XPFNDCG
          SQUEEZE   XPFNDCG
          MOVE      PMEDI,XPMEDI
          SQUEEZE   XPMEDI
          MOVE      PTMXMCCD,XPMCCD
          SQUEEZE   XPMCCD
          MOVE      PMPXMEDC,XMEDCEX
          SQUEEZE   XMEDCEX
          MOVE      PLDDATE,XPLDDT
          SQUEEZE   XPLDDT
          STRIP     XREPAT
          STRIP     XPENNO
          SQUEEZE   XPNDTE
          MOVE      PTMXCARD,XCARD
          STRIP     XCARD
          MOVE      PTMXCEXP,XEXPD
          SQUEEZE   XEXPD
          MOVE      PTMXCXMP,XEXMT
          SQUEEZE   XEXMT
          MOVE      PTMXFMLY,XFAMID
          STRIP     XFAMID
          MOVE      PUSR1,XPUSR1
          SQUEEZE   XPUSR1
          MOVE      PUSR2,XPUSR2
          SQUEEZE   XPUSR2
          MOVE      PUSR3,XPUSR3
          SQUEEZE   XPUSR3
          MOVE      PUSR4,XPUSR4
          SQUEEZE   XPUSR4
          MOVE      PUSR5,XPUSR5
          SQUEEZE   XPUSR5
          MOVE      PUSR6,XPUSR6
          SQUEEZE   XPUSR6
          MOVE      PUYN1,XPUYN1 
          SQUEEZE   XPUYN1
          MOVE      PUYN2,XPUYN2
          SQUEEZE   XPUYN2
          MOVE      PUYN3,XPUYN3
          SQUEEZE   XPUYN3
          MOVE      PMPXUYN4,XPUYN4
          SQUEEZE   XPUYN4
          MOVE      PMPXUYN5,XPUYN5
          SQUEEZE   XPUYN5
          MOVE      SP20,XALTNUMB
          CLEAR     XALTNUMB
          MOVE      SP20,XNATNUMB
          CLEAR     XNATNUMB
          MOVE      PTYPE,XPTYPE
          SQUEEZE   XPTYPE
          MOVE      PMRSCONT,XRSCONT
          SQUEEZE   XRSCONT
          MOVE      PYRRES,XPYRRES
          SQUEEZE   XPYRRES
          MOVE      PMPXLNG1,XPXLNG1
          SQUEEZE   XPXLNG1
          MOVE      PMPXINTR,XPXINTR
          SQUEEZE   XPXINTR
          MOVE      PSTAT,XPSTAT
          SQUEEZE   XPSTAT
          MOVE      PMRSTEXP,XRSTEXP
          SQUEEZE   XRSTEXP
          MOVE      PMRSCOMM,XRSCOMM
          STRIP     XRSCOMM
          MOVE      NHMANMPI,XMANMPI
          SQUEEZE   XMANMPI
          MOVE      NHMAURNO,XMAURNO
          SQUEEZE   XMAURNO
          MOVE      NHMASURN,XMASURN
          STRIP     XMASURN
          MOVE      NHMAGIV1,XMAGIV1
          STRIP     XMAGIV1
          MOVE      NHMAGIV2,XMAGIV2
          STRIP     XMAGIV2
          MOVE      NHMAGIV3,XMAGIV3
          STRIP     XMAGIV3
          MOVE      NHMAPREI,XMAPREI
          SQUEEZE   XMAPREI
          MOVE      NHMAADD1,XMAADD1
          STRIP     XMAADD1
          MOVE      NHMAADD2,XMAADD2
          STRIP     XMAADD2
          MOVE      NHMAADD3,XMAADD3
          STRIP     XMAADD3
          MOVE      NHMAADD4,XMAADD4
          STRIP     XMAADD4
          MOVE      NHMAADD5,XMAADD5
          STRIP     XMAADD5
          MOVE      NHMADOB,XMADOB
          SQUEEZE   XMADOB
          MOVE      NHMADDTH,XMADDTH
          SQUEEZE   XMADDTH
          MOVE      NHMADOMC,XMADOMC
          SQUEEZE   XMADOMC
          MOVE      NHMARES,XMARES
          SQUEEZE   XMARES
          MOVE      NHMAETH,XMAETH
          SQUEEZE   XMAETH
          MOVE      NHMASEX,XMASEX
          SQUEEZE   XMASEX
          MOVE      NHMADAT,XMADAT
          SQUEEZE   XMADAT
          MOVE      NHMATIM,XMATIM
          SQUEEZE   XMATIM
          MOVE      NHMAETH2,XMAETH2
          SQUEEZE   XMAETH2
          MOVE      NHMAETH3,XMAETH3
          SQUEEZE   XMAETH3
          MOVE      PCHGDTE,XCHGDTE
          SQUEEZE   XCHGDTE
          MOVE      PTMADCDT,XTMADCDT
          SQUEEZE   XTMADCDT
          MOVE      PTMXDEAF,XTMXDEAF
          SQUEEZE   XTMXDEAF
          STRIP     XSNTNUMB
          SQUEEZE   XSNTEDTE
          MOVE      PMPXIWI1,XIWICOD1
          SQUEEZE   XIWICOD1
          MOVE      PMPXPEML,XPEML
          SQUEEZE   XPEML
.
          STRIP     XDVAC   
.
          WRITE     PTMASEXT,SEQ;*+,XURNO,PIPE,XTIT,PIPE,XSURN,PIPE:
                                 XPGNAM1,PIPE,XPGNAM2,PIPE,XPSURN,PIPE:
                                 XADD1,PIPE,XADD2,PIPE,XADD3,PIPE,XADD4,PIPE:
                                 XPOST,PIPE,XTELP,PIPE,XTELB,PIPE,XCELLPH,PIPE:
                                 XPADD1,PIPE,XPADD2,PIPE,XPADD3,PIPE:
                                 XPADD4,PIPE,XPCODE,PIPE,XSEX,PIPE,XMSTAT,PIPE:
                                 XDOB,PIPE,XPMICRO,PIPE,XCOB,PIPE,XABORIG,PIPE:
                                 XREL,PIPE,XOCCUP,PIPE,XSMOK,PIPE,XPBDEBT,PIPE:
                                 XPCASE,PIPE,XPDIET,PIPE,XDATDTH,PIPE:
                                 XDCEASE,PIPE,XDETYP,PIPE,XAUTOP,PIPE:
                                 XNOKNAME,PIPE,XNOKADD1,PIPE,XNOKADD2,PIPE:
                                 XNOKADD3,PIPE,XNOKADD4,PIPE,XNOKPOST,PIPE:
                                 XNOKPPHN,PIPE,XNOKBPHN,PIPE,XPMPXKMO,PIPE:
                                 XNOKREL,PIPE,XCONNAME,PIPE,XCONADD1,PIPE:
                                 XCONADD2,PIPE,XCONADD3,PIPE,XCONADD4,PIPE:
                                 XCONPOST,PIPE,XCONPPHN,PIPE,XCONBPHN,PIPE:
                                 XPMPXCMO,PIPE,XCONREL,PIPE,XNEANAME,PIPE:
                                 XNEAADD1,PIPE,XNEAADD2,PIPE,XNEAADD3,PIPE:
                                 XNEAADD4,PIPE,XNEAPOST,PIPE,XNEAPPHN,PIPE:
                                 XNEABPHN,PIPE,XPMPXRMO,PIPE,XNEAREL,PIPE:
                                 XREGGPP,PIPE,XREGGP,PIPE,XHFUND,PIPE:
                                 XHFTAB,PIPE,XFNDMM,PIPE,XPFNDYY,PIPE:
                                 XPFNDCG,PIPE,XPMEDI,PIPE,XPMCCD,PIPE:
                                 XMEDCEX,PIPE,XPLDDT,PIPE,XREPAT,PIPE:
                                 XPENNO,PIPE,XPNDTE,PIPE,XDVAC,PIPE,XCARD,PIPE:
                                 XEXPD,PIPE,XEXMT,PIPE,XFAMID,PIPE,XPUSR1,PIPE:
                                 XPUSR2,PIPE,XPUSR3,PIPE,XPUSR4,PIPE:
                                 XPUSR5,PIPE,XPUSR6,PIPE,XPUYN1,PIPE:
                                 XPUYN2,PIPE,XPUYN3,PIPE,XPUYN4,PIPE:
                                 XPUYN5,PIPE,XALTNUMB,PIPE,XNATNUMB,PIPE:
                                 XPTYPE,PIPE,XRSCONT,PIPE,XPYRRES,PIPE:
                                 XPXLNG1,PIPE,XPXINTR,PIPE,XPSTAT,PIPE:
                                 XRSTEXP,PIPE,XRSCOMM,PIPE,XMANMPI,PIPE:
                                 XMAURNO,PIPE,XMASURN,PIPE,XMAGIV1,PIPE:
                                 XMAGIV2,PIPE,XMAGIV3,PIPE,XMAPREI,PIPE:
                                 XMAADD1,PIPE,XMAADD2,PIPE,XMAADD3,PIPE:
                                 XMAADD4,PIPE,XMAADD5,PIPE,XMADOB,PIPE:
                                 XMADDTH,PIPE,XMADOMC,PIPE,XMARES,PIPE:
                                 XMAETH,PIPE,XMASEX,PIPE,XMADAT,PIPE:
                                 XMATIM,PIPE,XMAETH2,PIPE,XMAETH3,PIPE:
                                 XCHGDTE,PIPE,XTMADCDT,PIPE,XTMXDEAF,PIPE:
                                 XSNTNUMB,PIPE,XSNTEDTE,PIPE,XIWICOD1,PIPE:
                                 XPEML,*-
.
          ADD       ONE,EXTCOUNT                 * increment extract count
          GOTO      PROC0500                     * get next record
.
PROC9000  DISPLAY   *P1:23,*EF,"Extraction complete.":
                    *P1:24,*V2LON,EXTCOUNT,*HOFF," records extracted.  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       CLNHIMAS
          INC       CLPMSPX2
          INC       CLPMSRES
.
          INC       NHIMASIO/INC
          INC       PATCODIO/INC
          INC       PATMA1IO/INC
          INC       PMSCCDIO/INC
          INC       PMSPX2IO/INC
          INC       PMSRESIO/INC

.******************************************************************************
.* System   : INPATIENT                                                       *
.* Program  : IBAPMS31                                                        *
.* Desc     : Periodic Statistical Update                                     *
.******************************************************************************
.* Date     :  02/03/90                                                       *
.* Author   :  K.Peak                                                         *
.* Mods.    :                                                                 *
.*        V12.00.02 11/06/2025  Ebon Clements  TSK 0961623                    *
.*                  Alphanueric visit number changes                          *
.*        V12.00.01 30/05/2025 Nikitha B       TSK 0955096                    *
.*                  Alphanumeric changes                                      *
.*        V11.01.01 22/02/2021  J.Tan          TSK 0888639                    *
.*                  Changed patmmbs counter record to DIM3                    *
.*        V10.03.02 02/01/2013 Patrick Adair   CAR 261630                     *
.*                  Removed port number from temp file name                   *
.*        V10.03.01 18/10/2012  Jill Parkinson CAR 273375                     *
.*                  Recompiled for DATECONV - first day of year calculation   *
.*        V10.02.01 28/03/2011  Mike Laming   CAR 240107                      *
.*                  Mods to PATECDaf & PATECOaf variables & Keys - file change*
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V9.12.01  09/06/2009  Jill Habasque                                 *
.*                  Mods to fix separation bed day calculation                *
.*        V9.08.01  18/01/2007 Sandra Barcham    131128                       *
.*                  Use indicator 1 D on both category D and DD for death     *
.*        V9.02.08  02/10/2002  Jill Habasque SRF 18718                       * 
.*                  Changed to use category A, indicator 3 for boarders       *
.*        V9.02.07  04/09/2002  Jill Habasque SRF 17573                       * 
.*                  Changed to use RTIODAYS instead of NHOSPDAY               *
.*        V9.02.06  07.08.2002  Ebon Clements SRF 20366                       *
.*                  Added check for PTCNMSDF to UPDM0000,DDIS0000 and DADM0000*
.*                  to fix I * C on patmsdaf                                  *
.*        V9.02.05  18.01.2002  Dean Cramer                                   *
.*                  Add ward processing BDAD0000 for unit statistics          *
.*        V9.02.04  28.12.2001  Dean Cramer                                   *
.*                  Make Dept for PATMSD pattranf.tdept not tatype            *
.*                  Change the category for Dept check to AC from A           *
.*                  Change so accepts date in current period                  *
.*        V9.02.03  24.10.2001  B.G.Ackland                                   *
.*                  Replace pi with Record IO Locks                           *
.*        V9.02.02  10/10/2001  Dean Cramer                                   *
.*                  Modify for web access.                                    *
.*        V9.02.01  24/08/2001  Dean Cramer                                   *
.*                  Reinstate pre change version until change spec resolved   *
.*        V5.09.B03 12/02/2001  Greg Horvat                                   *
.*                  Replaced the call to CALC with DAYSEP                     *
.*        V5.09.B02 30/01/2001  Tonii                                         *
.*                  Mods to include Unknown and Indeterminate as valid gender *
.*        V5.09.B01 18/01/2001  Ebon Clements 509 Doctors Mods                *
.*                  Changed to use new patdstat file layout                   *
.*        V5.08.03  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.02  07/06/2000  Steve Armstrong                               *
.*                  Fixed filename for patecoaf (instead of patecdof)         *
.*        V5.08.01  10/04/2000  Greg Horvat                                   *
.*                  Removed MATADBFD & MATADBIO                               *
.*        V5.08.B01 15/03/2000  Greg Horvat                                   *
.*                  Changed to get the coding from the episode coding files   *
.*             V5.07.01  30/07/1999  Steve Downing                            *
.*                       Redundant code removed, Mods to use DAYSEP           *
.*                  09/11/1998  Glenn Berry      5.07 changes                 *
.******************************************************************************
.*             V5.03.02  Steve Armstrong                              *
.*             Fixed completion message display                       *
.*             Mods to write active bed days to MSACBDAY in patmstsf. *
.*             Mods to write active days for ward instead of days in  *
.*             period - MSDYMTH                                       *
.*             Mods to write active wards with zero stats to patmstsf.*
.*                                                                    *
.**********************************************************************
.
          INC       STD001FD
          INC       BOKRC1FD/INC     * booking master file
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       PATC1SFD/INC     * clinical statistics 1 file
          INC       PATC2SFD/INC     * clinical statistics 2 file
          INC       PATCT1FD/INC     * catchment codes
          INC       PATCODFD/INC     * codes file
          INC       PATCOMM/INC
          INC       PATCONFD/INC     * control file
          INC       PATCSSFD/INC     * clinical statistics summary file
          INC       PATCSTFD/INC     * catchment summary file
          INC       PATDASFD/INC     * discharge & admission statistics file
          INC       PATDAYFD/INC     * daily patient list file
          INC       PATDO1FD/INC     * doctor file
          INC       PATDRGFD/INC     * period date range file
          INC       PATDSCFD/INC     * discharge file
          INC       PATDSTFD/INC     * doctor statistics file
          INC       PATECOFD/INC            * Patient episode operation file
          INC       PATMA1FD/INC     * master file
          INC       PATMI1FD/INC     * admission file
          INC       PMSVX1FD/INC     * admission file
          INC       PATMMBFD/INC     * mbs operations file
          INC       PATMSDFD/INC     * monthly inpatient statistics file Dept.
          INC       PATMSTFD/INC     * monthly inpatient statistics file
          INC       PATNUMFD/INC     * patient ward usage file
          INC       PATTRNFD/INC     * bed transfer file
          INC       PATWBHFD/INC     * ward bed history file
          INC       PATWR1FD/INC     * ward/bed file
          INC       PATWSSFD/INC     * ward specialities stats summary file
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WDACTVVR/INC
          INC       WEBERRFD/INC                 * Web Server Error Logging+
.**********************************************************************
.*                         VARIABLES                                  *
.**********************************************************************
.
ALRDYD    FORM      1       * re-do statistics flag
ASTATFLG  FORM      1       * astat flag 
ADLTDAS   FORM      8       * number of adults
ASDATE    DIM       8       * ccyymmdd
.
BRDFLG    FORM      1       * boarder flag 0 = yes, 1 = no
BJDAY     FORM      3       * julian birth day
BNBTMP    DIM       8       * temporary file 
BNBTMPXX  IFILE SQL, FIXED=12, KEY="u1-8,9-11" 
.
CSTPER    DIM       6       * ccyymm
CURPER    DIM       6       * current period
CMDLINE   DIM       50
CHG       FORM      1       * used in agechk
CJDAY     FORM      3       * julian current day
CSTCODE   DIM       3       * catchment code
CSTADMN   FORM      8       * number of admissions
CSTBDAY   FORM      8       * number of bed days admitted
CSTSAME   FORM      8       * number of sameday patients
CSTMDAY   FORM      8       * number of bed days for sameday
CSTBORD   FORM      8       * number of boarders
CSTBRDY   FORM      8       * number of bed days boarder
C1STYPE   DIM       3       * doctor type
C1SDOCT   DIM       6       * doctor code
C1STYPP   DIM       1       * type of patient
C1SDSCH   FORM      5       * no of discharged
C1STBDY   FORM      5       * no of bed days
C1SPBDY   FORM      5       * no of bed days in period
C2SCLSS   DIM       3       * ward classification
C2SDOCT   DIM       6       * doctor code
C2SEMRG   FORM      5       * emergency admissions
C2SELCT   FORM      5       * elective admissions
C2SDSCH   FORM      5       * discharged admissions
C2SDEAD   FORM      5       * deceased admissions
C2SSTDP   FORM      5       * public bed days in period
C2SPRVP   FORM      5       * private bed days in periodases)
C2SSTDB   FORM      5       * public bed days
C2SPRVB   FORM      5       * private bed days
C2SRNG1   FORM      5       * long stay range 1 (30-60 days)
C2SRNG2   FORM      5       * long stay range 2 (61-120 days)
C2SRNG3   FORM      5       * long stay range 3 (121-180 days)
C2SRNG4   FORM      5       * long stay range 4 (181+ days)
CSSTYPC   DIM       1       * type of clinic "m" or "s"
CSSTYPP   DIM       1       * type of patient
CSSSTRT   FORM      8       * patients at start of period
CSSADMT   FORM      8       * patients admitted during period
CSSDSCH   FORM      8       * patients discharged during period
CSSDIED   FORM      8       * patients died during period
CSSEND    FORM      8       * patients at end of period
CSSBDAY   FORM      8       * bed days (excluding day cases)
CSSDYCS   FORM      8       * number of day cases in period
CSSDBDY   FORM      8       * discharged patient bed days
CSSRNG1   FORM      8       * long stay patients 1 (30-60 days)
CSSRNG2   FORM      8       * long stay patients 2 (61-120 days)
CSSRNG3   FORM      8       * long stay patients 3 (121-180 days)
CSSRNG4   FORM      8       * long stay patients 4 (181+ days)
CSSWAIT   FORM      8       * patients admitted from waiting list
CALDAYS   FORM      4       * number of days
CHGWRD    FORM      1       * change ward flag
CHLDDAS   FORM      8       * number of children
CODEA     INIT      "A "
CODEAC    INIT      "AC"
CODECC    INIT      "CC"
CODECL    INIT      "CL"
CODED     INIT      "D "
CODEDD    INIT      "DD"
CODEP     INIT      "P "
CODES     INIT      "S "
CODEU6    INIT      "U6"
.
DIM4      DIM       4       * used pataged
DASREF    FORM      2       * reference
DASCODE   DIM       3       * admission type or discharge status
DASADLT   FORM      8       * number of adults
DASCHLD   FORM      8       * number of children
DASMALE   FORM      8       * number of males
DASFEML   FORM      8       * number of females
DASINDT   FORM      8       * number of indeterminate
DASUNKN   FORM      8       * number of unknown
DATEF     DIM       8       * fromdate used in reading transfer file
DATET     DIM       8       * todate used in reading transfer file
DATEFLG   FORM      1       * date flag 0 = bed days for patient
.                                       1 = bed days for patient in the period
DAYCASE   FORM      1       * daycase flag
DSHFLG    FORM      1       * discharge flag
.
ENDDATE   DIM       10      * enddate
.
FADMDTE   DIM       8 
FROMDATE  DIM       8
FEMLDAS   FORM      8       * number of females
FLAGSEPS  FORM      1
FORM3     FORM      3
.
INDEX     FORM      2
INDTDAS   FORM      8       * number of indeterminates
.
JYEAR     FORM      2
JDAYS     FORM      3
.
.
LEVFLG    FORM      1       * on leave flag
LYR       FORM      1       * leap year
.
MSTAT     FORM      1       * maternity status
MALEDAS   FORM      8       * number of males
.
NBRDAYS   FORM      4       * number of days
NUMWARD   DIM       3       * ward 
NUMADMN   FORM      6       * number of patients admitted
NUMTRIN   FORM      6       * number of patients transfered in
NUMTOUT   FORM      6       * number of patients transfered out
NUMDSCH   FORM      6       * number of patients discharged
NUMLEAV   FORM      6       * number of patients on leave
NUMRETN   FORM      6       * number of patients returned from leave
.
OYEAR     FORM      "365"
OTDATE    DIM       8
.
PATDEAD   FORM      1        * patient dead ? (0 = no, 1 = yes)
PUBFLG    FORM      1        * public/private flag
PPDATE    DIM       7        * period for printing
PNPTMP    DIM       8        * temporary file 
PNPTMPXX  IFILE SQL, FIXED=9, KEY="u1-8" 
.
RTDAYS    FORM      4 
SRBDAY    DIM       4        * mmdd
SRYEAR    FORM      4        * ccyy
STRDATE   DIM       10       * start date
SELDAT    FORM      3        * number of days in period 
SEPLEVS   FORM      4        * leave days for seperation bed days calculation
SAVLDATE  DIM       8
SAVYEAR   DIM       4
SAVPERD   DIM       2        * Drg period
SAVCLS    DIM       3        * ward class
SAVDS2    FORM      4
SYEAR     DIM       4        * ccyy of the fromdate of the period
SDAYMON   DIM       4        * mmdd of the fromdate of the period
SAVDTYP   DIM       3        * save doctor type
SAVJYR    FORM      2        * save julian year
SSWWARD   DIM       3        * ward
SSWSPEC   DIM       3        * primary doctor type 
SSWADMN   FORM      6        * number of admissions
SSWTRIN   FORM      6        * number of transfers in
SSWDOCS   FORM      6        * number of doctor changes
SSWBDAY   FORM      8        * number of bed days
SSWDSCH   FORM      6        * number of discharges
SSWTOUT   FORM      6        * number of transfers out
SAVDATE   DIM       8
SKEY30    DIM       30
.
TTODATE   DIM       8
TADMDTE   DIM       8
TODATE    DIM       8
TYEAR     DIM       4        * ccyy of the todate of the period
TDAYMON   DIM       4        * mmdd of the todate of the period
.
UNKNDAS   FORM      8        * number of unknowns
.
WDATE1    DIM       8
WDATE2    DIM       8
WORK      FORM      4
WSTAT     FORM      1        * admission status
WTOTDAY   FORM      4
WSBED     FORM      4
WADM      FORM      4
.
Z30       INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz" 
.
.----------------------------------------------------------------------
.         Temporary File Variables for file A
.
.Name     Type   Length   Physical   Start    Description
.----     ----   ------   --------   -----    -----------
DXADMN    DIM         8          8       1    Admission Number  
.
.End of record                           9
.
.  redefine form fields from key
.  -----------------------------
.Name     Type   Length   Start    Description
.----     ----   ------   -----    -----------
XADMN     DIM        8       1    Admission Number     
.
.----------------------------------------------------------------------
.         Temporary File Variables for file B
.
.Name     Type   Length   Physical   Start    Description
.----     ----   ------   --------   -----    -----------
DBADMN    DIM         8          8       1    Admission Number  
.TATYPE   DIM         3          3       9    Admission Type    
.
.End of record                          12
.
.  redefine form fields from key
.  -----------------------------
.Name     Type   Length   Start    Description
.----     ----   ------   -----    -----------
BADMN     DIM         8       1    Admission Number     
.
.----------------------------------------------------------------------
.
PRGID     INIT      "IBAPMS31"
PRGNAM    INIT      "Periodic Statistical Update"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                         MAINLINE                                   *
.**********************************************************************
.
ML0000    CALL      INIT0000                  * initialisation
.
ML1000    CALL      SCR0000                   * select option
          BRANCH    EXIT,ML9000
.
ML2000    CALL      DAT0000                   * keyin period
          BRANCH    EXIT,ML1000
          CALL      CONTQST                   * ok to continue y/n ?
          BRANCH    CEXIT,ML3000,ML2000,ML1000
.
ML3000    MOVE      ZERO,ALRDYD               * stats already done flag 
.
          CALL      EC1S0000        * check if any data in patc1sad for period
          CALL      EC2S0000        * check if any data in patc2saf for period
          CALL      ECSS0000        * check if any data in patcssad for period
          CALL      ECST0000        * check if any data in patcstsf for period
          CALL      EDAS0000        * check if any data in patdasaf for period
          CALL      EMST0000        * check if any data in patmstsf for period
          CALL      EMSD0000        * check if any data in patmsdaf for period
          CALL      EWSS0000        * check if any data in patwssaf for period
.         CALL      ENUM0000        * check if any data in patnumwr for period
.
ML4000    CALL      RSTA0000                  * re-do statistical update ?
          BRANCH    EXIT,ML2000               * dont continue
.
          CALL      CREA0000                  * create temporary file a
          CALL      CREB0000                  * create temporary file b
          CALL      DAYA0000                  * process patdayaf data
          CALL      PTMP0000                  * read through temporary file
          CALL      BDAY0000                  * get active bed days for wards
          CALL      BDAD0000                  * get active bed days for units
.
ML5000    CALL      END0000                   * end of report
.
.----------------------------------------------------------------------
.                          .. The End                                  
.----------------------------------------------------------------------
ML9000    CALL      KILA0000                  * remove temporary file a
          CALL      KILB0000                  * remove temporary file b
          CHAIN     PGM
+
.**********************************************************************
.*                         INIT0000                                   *
.*                      Initialisation                                *
.**********************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"patc1sad";
          OPEN      PATC1SA1,"patc1sad"
.
          DISPLAY   *P64:24,"patc2saf";
          OPEN      PATC2SA1,"patc2saf"
.
          DISPLAY   *P64:24,"patct1cf";
          OPEN      PATCT1C1,"patct1cf"             
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patcssad";
          OPEN      PATCSSA1,"patcssad"
.
          DISPLAY   *P64:24,"patcstsf";
          OPEN      PATCSTS1,"patcstsf"
.
          DISPLAY   *P64:24,"patdasaf";
          OPEN      PATDASA1,"patdasaf"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patdrgaf";
          OPEN      PATDRGA1,"patdrgaf"
          OPEN      PATDRGA3,"patdrgaf"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"patdstat";
          OPEN      PATDSTA1,"patdstat"
.
          DISPLAY   *P64:24,"patecoaf";
          OPEN      PATECOA2,"patecoaf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patmmbsf";
          OPEN      PATMMBS1,"patmmbsf"
.
          DISPLAY   *P64:24,"patmstsf";
          OPEN      PATMSTS1,"patmstsf"
.
          DISPLAY   *P64:24,"patnumwr";
          OPEN      PATNUMW1,"patnumwr"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY   *P64:24,"patwbhaf";
          OPEN      PATWBHA1,"patwbhaf"
.
          DISPLAY   *P64:24,"patwssaf";
          OPEN      PATWSSA1,"patwssaf"
.
          DISPLAY   *P64:24,"bokrc1af";
          OPEN      BOKRC1A1,"bokrc1af"
.
          READ      CONTROLF,TEN;*94,CAGECOFF
          READ      CONTROLF,TEN5;*188,CMATRN
          READ      CONTROLF,TWENTY;*76,PTCNMSDF
          READ      CONTROLF,FIFTY9;*27,CPERMTH:
                                    *147,PTCNLOS1,PTCNLOS2,PTCNLOS3:
                                         PTCNELDR,PTCNLSR1,PTCNLSR2:
                                         PTCNLSR3,PTCNLSR4,PTCNSTAU:
                                         PTCNNUMM,PTCNNEWS,PTCNNDAT
.
          COMPARE   ONE,PTCNMSDF
          GOTO      INIT9999 IF NOT EQUAL   * not using PATMSDFD
.
          DISPLAY   *P64:24,"patmsdaf";
          OPEN      PATMSDA1,"patmsdaf"
.
INIT9999  RETURN
+
.**********************************************************************
.*                         SCR0000                                    *
.*                      Main Option Screen                            *
.**********************************************************************
.
SCR0000   DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF:
                          " = Update Inpatient Statistical files" 
.
SCR1000   KEYIN     *P1:7,"Option : ",*EF,*DV,UNDLN1:
                    *P10:7,*V2LON,OPTION
.
          BRANCH    OPTION,SCR8000
.
          COMPARE   ZERO,OPTION
          GOTO      SCR9000 IF EQUAL
          BEEP
          GOTO      SCR1000
.
SCR8000   MOVE      FALSE,EXIT
          GOTO      SCR9999
.
SCR9000   MOVE      TRUE,EXIT
SCR9999   RETURN
+
.**********************************************************************
.*                         DAT0000                                    *
.*                       Keyin Period                                 *
.**********************************************************************
.
DAT0000   DISPLAY   *P1:8,*EF:
                    *P1:9,"Enter the ",CPERMTH," to be updated : "
.
          PACK      CPTDATE,CCC,CYY,CMM,CDD
          REP       " 0",CPTDATE
          CALL      GPERD
          BRANCH    EXIT,DAT2000 
.
          PACK      CURPER,DRGYR,DRGNUM
          REP       " 0",CURPER             * current period
.
.         Find previous period
.
          PACK      KEY6,CURPER
          CALL      RDSDRGA1
          CALL      RDPDRGA1
.
          PACK      CSTPER,DRGCYR,DRGCNUM
          REP       " 0",CSTPER             * previous calender period
.
          UNPACK    CSTPER,CCENT,CYEAR,CMON
          MOVE      ZERO,CHIGHLT              * 0 = highlight 1 = no highlight
          MOVE      "34",CCOL                 * column for keyin
          MOVE      NINE,CVERT                * row for keyin
          CALL      KEYPER
          BRANCH    OVRCD,DAT0000             * no date keyed in
.
.         Validate the date entered
.
          PACK      KEY6,CCENT,CYEAR,CMON
          REP       " 0",KEY6
          CALL      RDDRGA3
          BRANCH    OVRCD,DAT3000
          PACK      PPDATE,CMON,SLASH,CCENT,CYEAR
.
          MOVE      DRGYR,PTDSYEAR
          MOVE      DRGNUM,INDEX
          MOVE      PTDSYEAR,SAVYEAR
          MOVE      INDEX,SAVPERD
          REPLACE   " 0",SAVPERD
.
.         Pack various start dates
.
          MOVE      DRGFDTE,ACTVFDTE
          UNPACK    DRGFDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,STRDATE
          PACK      FADMDTE,CCENT,CYEAR,CMON,CDAY
.
.         Pack various end dates
.
          MOVE      DRGTDTE,ACTVTDTE
          UNPACK    DRGTDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,ENDDATE
          PACK      TADMDTE,CCENT,CYEAR,CMON,CDAY
.
          PACK      CSTPER,DRGYR,DRGNUM
.
.
.         Check for future keyed in date
.
          MATCH     CSTPER,CURPER
          GOTO      DAT4000 IF LESS
.
.         Check if current period
.
          MATCH     CSTPER,CURPER
          IF        @EQUAL
.
.         Current period, subtract one from today's date
.
            MOVE      CCC,CC
            MOVE      CYY,YY
            MOVE      CMM,MM
            MOVE      CDD,DD
            CALL      DMYCON
.
            SUB       ONE,JULDAY
            MOVE      JULDAY,JWKDAY
            MOVE      JULYR,JWKYER
            MOVE      JULCC,JWKCC
            CALL      JULCON
            PACK      DRGTDTE,CC,YY,MM,DD
            REP       " 0",DRGTDTE
          ENDIF
.
.         Add one to the end date of the period
.
          UNPACK    DRGTDTE,XCENT,XYEAR,XMON,XDAY
          MOVE      XCENT,CC
          MOVE      XYEAR,YY
          MOVE      XMON,MM
          MOVE      XDAY,DD
          CALL      DMYCON
.
          ADD       ONE,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON
          PACK      TTODATE,CC,YY,MM,DD
          REP       " 0",TTODATE
.
.         Calculate the date difference (no. of days in period - SELDAT)
.
          MOVE      ZERO,SELDAT
          DAYSEP    DRGFDTE,TTODATE,SELDAT
.
          UNPACK    DRGTDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,ENDDATE
          DISPLAY   *P43:9,*EL,DRGLDSC:
                    *P43:10,*EL,STRDATE," to ",ENDDATE
          GOTO      DAT8000
.
.         No current period on file
.
DAT2000   MATCH     "IBARSH",PGM
          GOTO      DAT9000 IF EQUAL
. 
          KEYIN     *P1:24,*EL,*B,*V2LON:
                    "** Error: Current date not found in Period file. ":
                    "Hit <ENTER> to exit ",*EOFF,ANS,*P1:24;
          GOTO      DAT9000
.
.         Invalid date entered
.
DAT3000   MATCH     "IBARSH",PGM
          GOTO      DAT9000 IF EQUAL
. 
          KEYIN     *P1:24,*EL,*B,*V2LON:
                    "** Invalid Date **",*EOFF,ANS,*P1:24;
          GOTO      DAT0000
.
.         The period being updated cannot end into the future
.
DAT4000   MATCH     "IBARSH",PGM
          GOTO      DAT9000 IF EQUAL
. 
          KEYIN     *P1:24,*EL,*B,*V2LON:
                    "** To run the statistical update the period can not ":
                    "end into the future **",*EOFF,ANS,*P1:24;
          GOTO      DAT0000
.
DAT8000   MOVE      FALSE,EXIT
          GOTO      DAT9999
.
DAT9000   MOVE      TRUE,EXIT
DAT9999   RETURN
+
.**********************************************************************
.*                         EC1S0000                                   *
.*        Check if any data for period selected in patc1sad           *
.**********************************************************************
.
EC1S0000  PACK      KEY16,CSTPER,SP10
          CALL      RSPTC1S1
EC1S1000  CALL      RKPTC1S1
          BRANCH    OVRCD,EC1S9999
.
          MATCH     PTC1DATE,CSTPER
          GOTO      EC1S9999 IF NOT EQUAL
.
          MOVE      ONE,ALRDYD                * data found for period selected
.
EC1S9999  RETURN
+
.**********************************************************************
.*                         EC2S0000                                   *
.*        Check if any data for period selected in patc2saf           *
.**********************************************************************
.
EC2S0000  PACK      KEY15,CSTPER,SP20
          CALL      RSPTC2S1
EC2S1000  CALL      RKPTC2S1
          BRANCH    OVRCD,EC2S9999
.
          MATCH     PTC2DATE,CSTPER
          GOTO      EC2S9999 IF NOT EQUAL
.
          MOVE      ONE,ALRDYD               * data found for period selected
.
EC2S9999  RETURN
+
.**********************************************************************
.*                         ECSS0000                                   *
.*        Check if any data for period selected in patcssad           *
.**********************************************************************
.
ECSS0000  PACK      KEY8,CSTPER,SP10
          CALL      RSPTCSS1
ECSS1000  CALL      RKPTCSS1
          BRANCH    OVRCD,ECSS9999
.
          MATCH     PTCSDATE,CSTPER
          GOTO      ECSS9999 IF NOT EQUAL
.
          MOVE      ONE,ALRDYD               * data found for period selected
.
ECSS9999  RETURN
+
.**********************************************************************
.*                         ECST0000                                   *
.*        Check if any data for period selected in patcstsf           *
.**********************************************************************
.
ECST0000  PACK      KEY9,SP10
          CALL      RDSCSTS1
ECST1000  CALL      RDKCSTS1
          BRANCH    OVRCD,ECST9999
.
          MATCH     CCCYYMM,CSTPER
          GOTO      ECST1000 IF NOT EQUAL
.
          MOVE      ONE,ALRDYD               * data found for period selected
.
ECST9999  RETURN
+
.**********************************************************************
.*                         EDAS0000                                   *
.*        Check if any data for period selected in patdasaf           *
.**********************************************************************
.
EDAS0000  PACK      KEY11,SP20
          CALL      RSPTDAS1
EDAS1000  CALL      RKPTDAS1
          BRANCH    OVRCD,EDAS9999
.
          MATCH     PTDADATE,CSTPER
          GOTO      EDAS1000 IF NOT EQUAL
.
          MOVE      ONE,ALRDYD               * data found for period selected
.
EDAS9999  RETURN
+
.**********************************************************************
.*                         EMST0000                                   *
.*        Check if any data for period selected in patmstsf           *
.**********************************************************************
.
EMST0000  PACK      KEY15,SP20
          CALL      RDSMSTS1
EMST1000  CALL      RDKMSTS1
          BRANCH    OVRCD,EMST9999
.
          MATCH     MSDATE,CSTPER
          GOTO      EMST1000 IF NOT EQUAL
.
          MOVE      ONE,ALRDYD               * data found for period selected
.
EMST9999  RETURN
+
.**********************************************************************
.*                         EMSD0000            Called by : ML3000     *
.*        Check if any data for period selected in patmsdaf           *
.**********************************************************************
.
EMSD0000  COMPARE   ONE,PTCNMSDF
          GOTO      EMSD9999 IF NOT EQUAL   * not using MSD file
.
          MOVE      SP20,KEY12
          CALL      RSPTMSD1
EMSD1000  CALL      RKPTMSD1
          BRANCH    OVRCD,EMSD9999
.
          MATCH     PTMDDATE,CSTPER
          GOTO      EMSD1000 IF NOT EQUAL   * different date
.
          MOVE      ONE,ALRDYD              * data found for period selected
.
EMSD9999  RETURN
+
.**********************************************************************
.*                         EWSS0000                                   *
.*        Check if any data for period selected in patwssaf           *
.**********************************************************************
.
EWSS0000  PACK      KEY12,CSTPER,SP10
          CALL      RDSWSSA1
EWSS1000  CALL      RDKWSSA1
          BRANCH    OVRCD,EWSS9999
.
          MATCH     WSSDATE,CSTPER
          GOTO      EWSS9999 IF NOT EQUAL
.
          MOVE      ONE,ALRDYD               * data found for period selected
.
EWSS9999  RETURN
+
.**********************************************************************
.*                         ENUM0000                                   *
.*        Check if any data for period selected in patnumwr           *
.**********************************************************************
.
ENUM0000  COMPARE   ONE,PTCNNUMM              * update of patnumfd to be done ?
          GOTO      ENUM9999 IF NOT EQUAL
.
          PACK      KEY9,SP10
          CALL      RDSNUMW1
ENUM1000  CALL      RDKNUMW1
          BRANCH    OVRCD,ENUM9999
.
          MATCH     PDATE,CSTPER
          GOTO      ENUM1000 IF NOT EQUAL
.
          MOVE      ONE,ALRDYD               * data found for period selected
.
ENUM9999  RETURN
+
.**********************************************************************
.*                         RSTA0000                                   *
.*        Ask if to re-do stats update and clear old files if needed  *
.**********************************************************************
.
RSTA0000  BRANCH    ALRDYD,RSTA1000    * data was found for period, ask question
          GOTO      RSTA3000
.
RSTA1000  MATCH     "IBARSH",PGM
          GOTO      RSTA2000 IF EQUAL
. 
          DISPLAY   *P1:24,*EL,*B:
                    "This update has already been done. ":
                    "Do you wish to re-process it (":
                    *V2LON,ANSY,*HOFF,SLASH,*V2LON,ANSN,*HOFF,") ? ",UNDLN1;
          KEYIN     *P73:24,*EL,*V2LON,ANS;
          REP       UPPLOW,ANS
.
          CMATCH    ANSY,ANS
          GOTO      RSTA2000 IF EQUAL
.
          CMATCH    ANSN,ANS
          GOTO      RSTA9000 IF EQUAL
.
          BEEP
          GOTO      RSTA1000
.
.         Before updating statistics again we must first delete the 
.         data already there for the period selected
.
RSTA2000  DISPLAY   *P1:24,*EL,*P20:24,*EL,*V2LON:
                    "Clearing Old Statistics - Please wait"
.
          CALL      DCST0000                  * delete patcstsf data
          CALL      DC1S0000                  * delete patc1sad data      
          CALL      DC2S0000                  * delete patc2saf data
          CALL      DCSS0000                  * delete patcssad data
          CALL      DDAS0000                  * delete patdasaf data
          CALL      DDST0000                  * delete patdstat data
          CALL      DMST0000                  * delete patmstsf data
          CALL      DMSD0000                  * delete patmsdaf data
          CALL      DWSS0000                  * delete patwssaf data
RSTA3000  CALL      DNUM0000                  * delete patnumwr data
.
RSTA8000  MOVE      FALSE,EXIT
          GOTO      RSTA9999
.
RSTA9000  MOVE      TRUE,EXIT
.
RSTA9999  RETURN
+
.**********************************************************************
.*                         DC1S0000                                   *
.*            Delete Records in patc1sad for Period selected          *
.**********************************************************************
.
DC1S0000  PACK      KEY16,CSTPER,SP10
          CALL      RSPTC1S1
          CALL      RKPTC1S1
          BRANCH    OVRCD,DC1S9999
.
          MATCH     PTC1DATE,CSTPER
          GOTO      DC1S9999 IF NOT EQUAL
.
          PACK      KEY16,PTC1DATE,PTC1TYPE,PTC1DOCT,PTC1TYPP
          CALL      DEPTC1S1
          GOTO      DC1S0000
.
DC1S9999  RETURN
+
.**********************************************************************
.*                         DC2S0000                                   *
.*            Delete Records in patc2saf for Period selected          *
.**********************************************************************
.
DC2S0000  PACK      KEY15,CSTPER,SP10
          CALL      RSPTC2S1
          CALL      RKPTC2S1
          BRANCH    OVRCD,DC2S9999
.
          MATCH     PTC2DATE,CSTPER
          GOTO      DC2S9999 IF NOT EQUAL
.
          PACK      KEY15,PTC2DATE,PTC2CLSS,PTC2DOCT
          CALL      DEPTC2S1
          GOTO      DC2S0000
.
DC2S9999  RETURN
+
.**********************************************************************
.*                         DCSS0000                                   *
.*            Delete Records in patcssad for Period selected          *
.**********************************************************************
.
DCSS0000  PACK      KEY8,CSTPER,SP10
          CALL      RSPTCSS1
          CALL      RKPTCSS1
          BRANCH    OVRCD,DCSS9999
.
          MATCH     PTCSDATE,CSTPER
          GOTO      DCSS9999 IF NOT EQUAL
.
          PACK      KEY8,PTCSDATE,PTCSTYPC,PTCSTYPP
          CALL      DEPTCSS1
          GOTO      DCSS0000
.
DCSS9999  RETURN
+
.**********************************************************************
.*                         DCST0000                                   *
.*            Delete Records in patcstaf for Period selected          *
.**********************************************************************
.
DCST0000  PACK      KEY9,SP10
          CALL      RDSCSTS1
DCST1000  CALL      RDKCSTS1
          BRANCH    OVRCD,DCST9999
.
          MATCH     CCCYYMM,CSTPER
          GOTO      DCST1000 IF NOT EQUAL
.
          PACK      KEY9,CCCODE,CCCYYMM
          CALL      DECSTS1
          CALL      RDSCSTS1
          GOTO      DCST1000 
.
DCST9999  RETURN
+
.**********************************************************************
.*                         DDAS0000                                   *
.*            Delete Records in patdasaf for Period selected          *
.**********************************************************************
.
DDAS0000  PACK      KEY11,SP20
          CALL      RSPTDAS1
DDAS1000  CALL      RKPTDAS1
          BRANCH    OVRCD,DDAS9999
.
          MATCH     PTDADATE,CSTPER
          GOTO      DDAS1000 IF NOT EQUAL
.
          PACK      KEY11,PTDAREF,PTDADATE,PTDACODE
          CALL      DEPTDAS1
          CALL      RSPTDAS1
          GOTO      DDAS1000
.
DDAS9999  RETURN
+
.**********************************************************************
.*                         DDST0000                                   * 
.*            Delete Records in patdstaf for Period selected          * 
.********************************************************************** 
.
DDST0000  PACK      KEY13,SAVYEAR,SAVPERD,SP10
          CALL      RDSDSTA1
DDST1000  CALL      RDKDSTA1
          BRANCH    OVRCD,DDST9999
.
          MATCH     PTDSYEAR,SAVYEAR
          GOTO      DDST9999 IF NOT EQUAL
.
          MATCH     PTDSPERD,SAVPERD
          GOTO      DDST9999 IF NOT EQUAL
.
          MOVE      ZERO,PTDSADMS
          MOVE      ZERO,PTDSBEDD
          MOVE      ZERO,PTDSOPRS
          MOVE      ZERO,PTDSMBSC
.
          CALL      UPDSTA1
          GOTO      DDST1000
.
DDST9999  RETURN
+
.**********************************************************************
.*                         DMST0000                                   *
.*            Delete Records in patmstaf for Period selected          *
.**********************************************************************
.
DMST0000  PACK      KEY15,SP20
          CALL      RDSMSTS1
DMST1000  CALL      RDKMSTS1
          BRANCH    OVRCD,DMST9999
.
          MATCH     MSDATE,CSTPER
          GOTO      DMST1000 IF NOT EQUAL
.
          PACK      KEY15,MSWARD,MSDCLS,MSWCLS,MSDATE
          CALL      DEMSTS1
          CALL      RDSMSTS1
          GOTO      DMST1000
.
DMST9999  RETURN
+
.**********************************************************************
.*                         DMSD0000           Called by : RSTA2000    *
.*            Delete Records in patmsdaf for Period selected          *
.**********************************************************************
.
DMSD0000  COMPARE   ONE,PTCNMSDF
          GOTO      DMSD9999 IF NOT EQUAL   * not using MSD file
.
          MOVE      SP20,KEY12
          CALL      RSPTMSD1
DMSD1000  CALL      RKPTMSD1
          BRANCH    OVRCD,DMSD9999
.
          MATCH     PTMDDATE,CSTPER
          GOTO      DMSD1000 IF NOT EQUAL   * not in the period
.
          PACK      KEY12,PTMDDEPT,PTMDWARD,PTMDDATE
          CALL      DEPTMSD1
          CALL      RSPTMSD1
          GOTO      DMSD1000
.
DMSD9999  RETURN
+
.**********************************************************************
.*                         DWSS0000                                   *
.*            Delete Records in patwssaf for Period selected          *
.**********************************************************************
.
DWSS0000  PACK      KEY12,CSTPER,SP10
          CALL      RDSWSSA1
          CALL      RDKWSSA1
          BRANCH    OVRCD,DWSS9999
.
          MATCH     WSSDATE,CSTPER
          GOTO      DWSS9999 IF NOT EQUAL
.
          PACK      KEY12,WSSDATE,WSSWARD,WSSSPEC
          CALL      DEWSSA1
          GOTO      DWSS0000
.
DWSS9999  RETURN
+
.**********************************************************************
.*                         DNUM0000                                   *
.*            Delete Records in patnumwr for Period selected          *
.**********************************************************************
.
DNUM0000  COMPARE   ONE,PTCNNUMM              * delete of patnumwr to be done ?
          GOTO      DNUM9999 IF NOT EQUAL
.
          PACK      KEY9,SP10
          CALL      RDSNUMW1
DNUM1000  CALL      RDKNUMW1
          BRANCH    OVRCD,DNUM9999
.
          MATCH     PDATE,CSTPER
          GOTO      DNUM1000 IF NOT EQUAL
.
          PACK      KEY9,PWARD,PDATE
          CALL      DENUMW1
          CALL      RDSNUMW1
          GOTO      DNUM1000
.
DNUM9999  RETURN
+
.**********************************************************************
.*                         DAYA0000                                   *
.*                   Process PATDAYFD Data                            *
.*      writes to temp file all valid admission numbers from patdayaf *
.**********************************************************************
.
DAYA0000  DISPLAY   *P1:24,*EL,*P20:24,"Admission No : ",*P50:24,"Scanning : ";
.
          UNPACK    DRGTDTE,TYEAR,TDAYMON
          UNPACK    DRGFDTE,SYEAR,SDAYMON
.
DAYA1000  CLOSE     PATDAYA1
          PACK      CFNAME,FILDAYA1,SYEAR
.
          TRAP      CRET0000 IF IO
          OPEN      PATDAYA1,CFNAME
          TRAPCLR   IO
.
          PACK      KEY12,SDAYMON,SP10
          CALL      RSPTDAY1
DAYA2000  CALL      RKPTDAY1
          BRANCH    OVRCD,DAYA5000
.
.         Display day and month that is being processed
.
          UNPACK    PTDYDATE,XMON,XDAY
          DISPLAY   *P1:24,XDAY,SLASH,XMON
          DISPLAY   *P61:24,*EL,PTDYADMN
.
          MATCH     TYEAR,SYEAR
          GOTO      DAYA3000 IF NOT EQUAL
.
          MATCH     PTDYDATE,TDAYMON
          GOTO      DAYA9999 IF LESS
.
DAYA3000  DISPLAY   *P35:24,*V2LON,PTDYADMN
.
.         Only need to write to the temporary file if the admission 
.         number is not already there
.
          PACK      KEY8,PTDYADMN
          CALL      RDTMP1
          BRANCH    OVRCD,DAYA4000
          GOTO      DAYA2000              
.
DAYA4000  MOVE      PTDYADMN,XADMN
          CALL      WRTMP1
          GOTO      DAYA2000              
.
DAYA5000  MATCH     TYEAR,SYEAR
          GOTO      DAYA9999 IF NOT LESS
.
          MOVE      SYEAR,FORM4
          ADD       ONE,FORM4
          MOVE      FORM4,SYEAR
          REP       " 0",SYEAR
          MOVE      "0101",SDAYMON
.
          GOTO      DAYA1000
.
DAYA9999  RETURN
+
.**********************************************************************
.*                         PTMP0000                                   *
.*                   Read through the temporary file                  *
.**********************************************************************
.
PTMP0000  DISPLAY   *P1:24,*EL,*P20:24:
                    "Admission No : ",*P50:24,"Scanning : ";
.
          PACK      KEY8,SP8
          CALL      RDSTMP1
PTMP1000  CALL      RDKTMP1
          BRANCH    OVRCD,PTMP9999
.
          DISPLAY   *P61:24,XADMN
.
          MOVE      ONE,PATDEAD               * patient isn't dead as yet
          MOVE      ONE,ASTATFLG              * patient isn't discharged as yet
.
.         Check if patient on the admission file
.
          MOVE      XADMN,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,PTMP1000
.
.         Check if the patient is cancelled or not
.
          COMPARE   FIVE,ASTAT                * check if patient cancelled
          GOTO      PTMP1000 IF EQUAL
.
.         Check if patient on the master file
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,PTMP1000
.
.         Calculate patients age as from the admission date using 
.         the pataged routine
.
          UNPACK    PBDATE,XCENT,XYEAR,XMON,XDAY
          MOVE      XDAY,IDAY
          MOVE      XMON,IMON
          MOVE      XYEAR,IYEAR
          MOVE      XCENT,ICENT
.
          PACK      ASDATE,ADATE          * admission date with century
          CALL      PATAGED                   * age calculating routine
.
          MATCH     FADMDTE,ADATE             * admission date within period ?
          GOTO      PTMP2000 IF LESS
.
          MATCH     ADATE,TADMDTE
          GOTO      PTMP1000 IF LESS
.
PTMP2000  COMPARE   THREE,ASTAT               * check if patient discharged
          GOTO      PTMP3000 IF NOT EQUAL
.
.         Check if patient on the discharge file
.
          MOVE      XADMN,KEY8
          CALL      RDDSCH1
          BRANCH    OVRCD,PTMP1000
.
          MATCH     FADMDTE,DDATE
          GOTO      PTMP1000 IF LESS
.
.         If the person is discharged their astat value will be a 3
.         So astatflg should remain set to one if the astat value is not a 3
.
          MOVE      ZERO,ASTATFLG             * patient is discharged
.
.         Check whether the patient is dead
.
          PACK      KEY5,CODED,DSTAT
          CALL      RDCODE1
          BRANCH    OVRCD,PTMP2500
.
          MATCH     ANSD,TCINDC1
          GOTO      PTMP2500 IF NOT EQUAL
          MOVE      ZERO,PATDEAD              * patient is dead
          GOTO      PTMP3000 
.
PTMP2500  PACK      KEY5,CODEDD,DDEST
          CALL      RDCODE1
          BRANCH    OVRCD,PTMP3000
.
          MATCH     ANSD,TCINDC1
          GOTO      PTMP3000 IF NOT EQUAL
          MOVE      ZERO,PATDEAD              * patient is dead
.
PTMP3000  DISPLAY   *P35:24,*V2LON,XADMN
.
          CALL      UCST0000                  * update patcstfd file
          CALL      UC1S0000                  * update patc1sfd file
          CALL      UC2S0000                  * update patc2sfd file
          CALL      UCSS0000                  * update patcssfd file
          CALL      UDAS0000                  * update patdasfd file
          CALL      UDST0000                  * update patdstfd file
          CALL      UMST0000                  * update patmstfd file
          CALL      UWSS0000                  * update patwssfd file
          CALL      UNUM0000                  * update patnumfd file
          GOTO      PTMP1000                  * read next temporary file record
.
PTMP9999  RETURN
+
.**********************************************************************
.*                         UCST0000                                   *
.*              Generate data for PATCSTFD file                       *
.**********************************************************************
.
UCST0000  DISPLAY   *P1:24,"patcstaf";
.
          MOVE      SP3,CSTCODE               * catchment code
          MOVE      ZERO,CSTADMN              * number of admissions
          MOVE      ZERO,CSTBDAY              * number of bed days admitted
          MOVE      ZERO,CSTSAME              * number of sameday patients
          MOVE      ZERO,CSTMDAY              * number of bed days for sameday
          MOVE      ZERO,CSTBORD              * number of boarders
          MOVE      ZERO,CSTBRDY              * number of bed days for boarders
.
          MOVE      PPOST,KEY8
          CALL      RDCATC1
          BRANCH    OVRCD,UCST1000
          MOVE      CTCCODE,CSTCODE
.
.         Calculate the number of bed days the patient had in the period
.
UCST1000  MOVE      ZERO,NBRDAYS
          MOVE      FADMDTE,FROMDATE
          MOVE      TTODATE,TODATE            * ttodate = end date of period + 1
          CALL      RTIODAYS                  * bed day calculating routine
.
.         Determine the type of the patient
.
          PACK      KEY5,CODEP,ACLSS
          CALL      RDCODE1
          BRANCH    OVRCD,UCST2000
.
          MOVE      ZERO,FORM1
          MOVE      TCINDC1,FORM1
          BRANCH    FORM1,UCST2000,UCST3000,UCST4000
.
.         Patient is a normal admission
.
UCST2000  MOVE      NBRDAYS,CSTBDAY           * bed days for normal admission
.
          MATCH     FADMDTE,ADATE             * admitted in period ?
          GOTO      UCST5000 IF LESS
.
          MATCH     ADATE,TADMDTE
          GOTO      UCST5000 IF LESS
.
          MOVE      ONE,CSTADMN               * normal admission
          GOTO      UCST5000
.
.         Patient is a sameday admission
.
UCST3000  MOVE      NBRDAYS,CSTMDAY           * bed days for sameday admission
.
          MATCH     FADMDTE,ADATE             * admitted in period ?
          GOTO      UCST5000 IF LESS
.
          MATCH     ADATE,TADMDTE
          GOTO      UCST5000 IF LESS
.
          MOVE      ONE,CSTSAME               * sameday admission
          GOTO      UCST5000
.
.         Patient is a boarder
.
UCST4000  MOVE      NBRDAYS,CSTBRDY           * bed days for boarder admission
.
          MATCH     FADMDTE,ADATE             * admitted in period ?
          GOTO      UCST5000 IF LESS
.
          MATCH     ADATE,TADMDTE
          GOTO      UCST5000 IF LESS
.
          MOVE      ONE,CSTBORD               * boarder admission
.
.         Write or update to the patcstfd file
.
UCST5000  PACK      KEY9,CSTCODE,CSTPER
          CALL      RDCSTS1
          BRANCH    OVRCD,UCST6000
.
          ADD       CSTADMN,CCADMN
          ADD       CSTBDAY,CCABDAY
          ADD       CSTSAME,CCSAMED
          ADD       CSTMDAY,CCSMDAY
          ADD       CSTBORD,CCBOARD
          ADD       CSTBRDY,CCBRDAY
.
          CALL      UPCSTS1
          GOTO      UCST9999
.
UCST6000  MOVE      CSTCODE,CCCODE
          MOVE      CSTPER,CCCYYMM
          MOVE      CSTADMN,CCADMN
          MOVE      CSTBDAY,CCABDAY
          MOVE      CSTSAME,CCSAMED
          MOVE      CSTMDAY,CCSMDAY
          MOVE      CSTBORD,CCBOARD
          MOVE      CSTBRDY,CCBRDAY
.
          CALL      WRCSTS1
.
UCST9999  RETURN
+
.**********************************************************************
.*                         UC1S0000                                   *
.*                Generate the data for PATC1SFD                      *
.**********************************************************************
.
UC1S0000  DISPLAY   *P1:24,"patc1sad";
.
          MOVE      SP3,C1STYPE               * doctor type
          MOVE      ZERO,C1SDOCT              * doctor code
          MOVE      ZERO,C1STYPP              * type of patient
          MOVE      ZERO,C1SDSCH              * no of discharged
          MOVE      ZERO,C1STBDY              * no of bed days
          MOVE      ZERO,C1SPBDY              * no of bed days in period
          MOVE      SP8,STDATE                * save transfer date
          MOVE      " ",STMOVE                * save transfer movement
          MOVE      SP3,STATYPE               * save transfer admission type
          MOVE      SP6,STADOCT               * save transfer doctor
          MOVE      SP6,STWARD                * save transfer ward
.
.         This file only contains details of discharged patients, so 
.         if the patient is not discharged within the period do not 
.         update this file
.
          BRANCH    ASTATFLG,UC1S9999         * patient discharged ?
.
          MATCH     FADMDTE,DDATE             * discharged in period ?
          GOTO      UC1S9999 IF LESS
.
          MATCH     DDATE,TADMDTE
          GOTO      UC1S9999 IF LESS
.
.         Number of discharged patients in the period
.
          MOVE      ONE,C1SDSCH
.
.         If the patient has a claim code with a W, M or V in any
.         of the indicators they are a compensable type of patient
.
          PACK      KEY5,CODECL,ACLAIM
          CALL      RDCODE1
          BRANCH    OVRCD,UC1S4000
.
          MOVE      ZERO,FORM2
UC1S2000  ADD       ONE,FORM2
          COMPARE   SIX,FORM2
          GOTO      UC1S4000 IF NOT LESS
.
          LOAD      ANS,FORM2,TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5
.
          CMATCH    ANSW,ANS                  * workers compensation
          GOTO      UC1S3000 IF EQUAL
          CMATCH    ANSM,ANS                  * motor accident board
          GOTO      UC1S3000 IF EQUAL
          CMATCH    ANSV,ANS                  * veterans affairs
          GOTO      UC1S3000 IF EQUAL
. 
          GOTO      UC1S2000
.
UC1S3000  MOVE      THREE,C1STYPP             * compensable patient
.
.         Loop through the bed transfer file backwards to get the "D" record
.
UC1S4000  PACK      KEY30,XADMN,Z30
          CALL      RDSTRAN2
          CALL      RDPTRAN2           
.
          MOVE      TADOCT,STADOCT
          MOVE      TATYPE,STATYPE
          MOVE      ZERO,C1STBDY       * number of beddays for the patient
          MOVE      ZERO,C1SPBDY       * number of beddays for patient in period
          MOVE      ONE,C1SDSCH        * number of discharged patients
          CALL      UPC10000           * update the patc1sfd file
          MOVE      ZERO,C1SDSCH
.
          BRANCH    ASTATFLG,UC1S5000         * patient discharged ?
.
          MATCH     ADATE,DDATE               * patient a daycase ?
          GOTO      UC1S8000 IF EQUAL
.
.         Calculate the number of bed days for the patient
.
UC1S5000  MOVE      ZERO,DATEFLG
          MOVE      ADATE,DATEF
.
          BRANCH    ASTATFLG,UC1S6000         * patient discharged ?
.
          MOVE      DDATE,DATET               * use discharge date
          GOTO      UC1S7000
.
UC1S6000  MOVE      TTODATE,DATET             * use to date of period
.
.         To handle the changes of attending doctor & admission type,
.         looping through the bed transfer file is required
.
UC1S7000  MOVE      SP8,STDATE                * save transfer date
          MOVE      " ",STMOVE                * save transfer movement
          MOVE      SP3,STATYPE               * save transfer admission type
          MOVE      SP6,STADOCT               * save transfer doctor
          CALL      TRC10000                  * read of transfer file
.
.         Use the period from and to dates to calculate the 
.         number of bed days for the patient in the period
.
          MOVE      ONE,DATEFLG
          MOVE      FADMDTE,DATEF
          MOVE      TTODATE,DATET
.
.         To handle the changes of attending doctor & admission type,
.         looping through the bed transfer file is required
.
          CALL      TRC10000                  * read of transfer file
          GOTO      UC1S9999
.
.         Patient is a daycase 
.
UC1S8000  MOVE      TADOCT,STADOCT
          MOVE      TATYPE,STATYPE
          MOVE      ONE,C1STBDY        * number of beddays for the patient
          MOVE      ONE,C1SPBDY        * number of beddays for patient in period
          MOVE      ZERO,C1SDSCH       * number of discharged patients
.
          CALL      UPC10000           * update the patc1sfd file
.
UC1S9999  RETURN
+
.**********************************************************************
.*                         TRC10000                                   *
.*                   Read through the PATTRNFD file                   *
.**********************************************************************
.
TRC10000  PACK      KEY30,XADMN,SP20
          CALL      RDSTRAN2
TRC11000  CALL      RDKTRAN2
          BRANCH    OVRCD,TRC17000
.
          MATCH     XADMN,TADMN               * same admission number ?
          GOTO      TRC17000 IF NOT EQUAL
.
          MOVE      TMOVE,STMOVE
          RESET     TDATE
          REP       " 0",TDATE
.
          MATCH     ANSA,TMOVE                * current admission record
          GOTO      TRC14000 IF EQUAL
          MATCH     ANSR,TMOVE                * returned from leave record
          GOTO      TRC14000 IF EQUAL
          MATCH     ANSC,TMOVE                * change record 
          GOTO      TRC12000 IF EQUAL
          MATCH     ANSL,TMOVE                * onleave record
          GOTO      TRC12000 IF EQUAL
.
.         Discharge record
.
          MATCH     DATEF,TDATE
          GOTO      TRC19999 IF LESS
          MATCH     DATET,TDATE
          GOTO      TRC17000 IF LESS
          MOVE      DATET,TDATE
          GOTO      TRC17000
.
.         End of period, calculate the data needed, then start new period
.
TRC12000  MATCH     DATEF,TDATE
          GOTO      TRC14000 IF LESS
.
          MATCH     DATET,TDATE
          GOTO      TRC13000 IF LESS
.
          MOVE      DATET,WDATE1
          MOVE      STDATE,WDATE2
          CALL      CLC10000                  * calculate the date difference
          CALL      UPC10000                  * update the patc1sfd file
          GOTO      TRC19999
.
TRC13000  MOVE      TDATE,WDATE1
          MOVE      STDATE,WDATE2
          CALL      CLC10000                  * calculate the date difference
          CALL      UPC10000                  * update the patc1sfd file
.
.         Save new parameters for the start of the new period
.
TRC14000  MATCH     DATET,TDATE
          GOTO      TRC19999 IF NOT LESS
.
          MATCH     DATEF,TDATE
          GOTO      TRC15000 IF NOT LESS
.
          MOVE      DATEF,STDATE
          GOTO      TRC16000
.
TRC15000  MOVE      TDATE,STDATE
.
TRC16000  MOVE      TATYPE,STATYPE            * save admission type
          MOVE      TADOCT,STADOCT            * save doctor 
          GOTO      TRC11000 
.
.         Last record for the patient
.
TRC17000  MATCH     ANSL,STMOVE               * on leave patient ?
          GOTO      TRC19999 IF EQUAL
          MATCH     " ",STMOVE
          GOTO      TRC19999 IF EQUAL
.
          MATCH     ANSD,STMOVE               * discharged patient ?
          GOTO      TRC18000 IF NOT EQUAL
          MOVE      TDATE,WDATE1
          MOVE      STDATE,WDATE2
          GOTO      TRC19000
.
TRC18000  MOVE      DATET,WDATE1
          MOVE      STDATE,WDATE2
TRC19000  CALL      CLC10000                  * calculate the date difference
          CALL      UPC10000                  * update the patc1sfd file
.
TRC19999  RETURN
+
.**********************************************************************
.*                         CLC10000                                   *
.*        Calculate the date difference for the PATC1SFD file         *
.**********************************************************************
.
CLC10000  MOVE      ZERO,CALDAYS
.
          DAYSEP    WDATE2,WDATE1,CALDAYS
.
.         Branch to the appropriate date using the date flag
.
          MOVE      ZERO,C1STBDY              * number of bed days
          MOVE      ZERO,C1SPBDY              * number of bed days in period
.
          BRANCH    DATEFLG,CLC12000
.
.         Number of bed days for the patient
.
          MOVE      CALDAYS,C1STBDY
          GOTO      CLC19999
.
.         Number of bed days for the patient in the period
.
CLC12000  MOVE      CALDAYS,C1SPBDY
CLC19999  RETURN
+
.**********************************************************************
.*                         UPC10000                                   *
.*                   Update the PATC1SFD file                         *
.**********************************************************************
.
UPC10000  
.         Find out the doctor type of the attending doctor
.
          MOVE      SP3,DRTYPE
          PACK      KEY6,STADOCT
          CALL      RDDOCT1
.
          MOVE      DRTYPE,C1STYPE            * type of doctor
          MOVE      STADOCT,C1SDOCT           * doctor code
.
.         If patient is compensable don't determine whether they are a
.         public or private patient
.
          MATCH     "3",C1STYPP
          GOTO      UPC12000 IF EQUAL
.
.         If the patient has a admission type indicator 1 equal 
.         to two then they are a private patient. Anything else 
.         will be a public patient
.
          PACK      KEY5,CODEA,STATYPE
          CALL      RDCODE1
          BRANCH    OVRCD,UPC11000
.
          MATCH     "2",TCINDC1
          GOTO      UPC11000 IF NOT EQUAL
          MOVE      TWO,C1STYPP               * private patient
          GOTO      UPC12000
.
UPC11000  MOVE      ONE,C1STYPP               * public patient
.
.         Write or update the patc1sfd file
.
UPC12000  PACK      KEY16,CSTPER,C1STYPE,C1SDOCT,C1STYPP
          CALL      RDPTC1S1
          BRANCH    OVRCD,UPC13000
.
          ADD       C1SDSCH,PTC1DSCH
          ADD       C1STBDY,PTC1TBDY
          ADD       C1SPBDY,PTC1PBDY
.
          CALL      UPPTC1S1
          GOTO      UPC19999
.
UPC13000  MOVE      CSTPER,PTC1DATE
          MOVE      C1STYPE,PTC1TYPE
          MOVE      C1SDOCT,PTC1DOCT
          MOVE      C1STYPP,PTC1TYPP
          MOVE      C1SDSCH,PTC1DSCH
          MOVE      C1STBDY,PTC1TBDY
          MOVE      C1SPBDY,PTC1PBDY
.
          CALL      WRPTC1S1
.
UPC19999  RETURN
+
.**********************************************************************
.*                         UC2S0000                                   *
.*                Generate the data for PATC2SFD                      *
.**********************************************************************
.
UC2S0000  DISPLAY   *P1:24,"patc2saf";
.
          MOVE      SP3,C2SCLSS               * ward classification
          MOVE      SP6,C2SDOCT               * doctor code
          MOVE      ZERO,C2SEMRG              * emergency admissions
          MOVE      ZERO,C2SELCT              * elective admissions
          MOVE      ZERO,C2SDSCH              * discharged admissions
          MOVE      ZERO,C2SDEAD              * deceased admissions
          MOVE      ZERO,C2SSTDP              * public bed days in period
          MOVE      ZERO,C2SPRVP              * private bed days in periodases)
          MOVE      ZERO,C2SSTDB              * public bed days
          MOVE      ZERO,C2SPRVB              * private bed days
          MOVE      ZERO,C2SRNG1              * long stay range 1 (30-60 days)
          MOVE      ZERO,C2SRNG2              * long stay range 2 (61-120 days)
          MOVE      ZERO,C2SRNG3              * long stay range 3 (121-180 days)
          MOVE      ZERO,C2SRNG4              * long stay range 4 (181+ days)
          MOVE      SP8,STDATE                * save transfer date
          MOVE      " ",STMOVE                * save transfer movement
          MOVE      SP3,STATYPE               * save transfer admission type
          MOVE      SP6,STADOCT               * save transfer doctor
          MOVE      SP6,STWARD                * save transfer ward
.
.         Determine whether the patient was an emergency admission or 
.         an elective admission
.
          MATCH     FADMDTE,ADATE             * admitted within the period
          GOTO      UC2S2000 IF LESS
.
          MATCH     ADATE,TADMDTE
          GOTO      UC2S2000 IF LESS
.
          PACK      KEY5,CODECC,ACARE
          CALL      RDCODE1
          BRANCH    OVRCD,UC2S1000
.
.         An emergency admission is a care class with a 2 in indicator 1
.
          MATCH     "2",TCINDC1
          GOTO      UC2S1000 IF NOT EQUAL
.
          MOVE      ONE,C2SEMRG               * emergency admission
          GOTO      UC2S1500
.
.         An elective admission is one that is not an emergency admission
.
UC2S1000  MOVE      ONE,C2SELCT               * elective admission
.
UC2S1500  PACK      KEY30,XADMN,SP20
          CALL      RDSTRAN2
          CALL      RDKTRAN2
.
          MOVE      TADOCT,STADOCT            * save doctor
          MOVE      TWARD,STWARD              * save ward
          CALL      UPC20000                  * update the patc2sfd file
          MOVE      ZERO,C2SEMRG
          MOVE      ZERO,C2SELCT
.
.         Calculate the number of discharges during the period, and the
.         number of deaths during the period. (Patients who are dead are 
.         not included in the discharges total)
.
UC2S2000  BRANCH    ASTATFLG,UC2S4000         * patient discharged ?
.
          MATCH     FADMDTE,DDATE             * discharged within the period
          GOTO      UC2S9999 IF LESS
.
          MATCH     DDATE,TADMDTE
          GOTO      UC2S9999 IF LESS
.
          BRANCH    PATDEAD,UC2S3000         * patient dead ?
.
          MOVE      ONE,C2SDEAD              * patient is dead
          GOTO      UC2S4000
.
UC2S3000  MOVE      ONE,C2SDSCH              * patient is discharged
.
.         Calculate the length of stay the patient has been in hospital
.
UC2S4000  MOVE      ZERO,NBRDAYS
          MOVE      ADATE,FROMDATE
          MOVE      DDATE,TODATE              
          CALL      RTIODAYS                  * bed day calculating routine
.
.         Patient is in range 1 if in hospital for greater than or 
.         equal to range 1 and less than range 2
.
          COMPARE   PTCNLSR1,NBRDAYS
          GOTO      UC2S7500 IF LESS
.
          COMPARE   PTCNLSR2,NBRDAYS
          GOTO      UC2S5000 IF NOT LESS
.
          MOVE      ONE,C2SRNG1               * range 1
          GOTO      UC2S7500 
.
.         Patient is in range 2 if in hospital for greater than or 
.         equal to range 2 and less than range 3
.
UC2S5000  COMPARE   PTCNLSR3,NBRDAYS
          GOTO      UC2S6000 IF NOT LESS
.
          MOVE      ONE,C2SRNG2               * range 2
          GOTO      UC2S7500 
.
.         Patient is in range 3 if in hospital for greater than or 
.         equal to range 3 and less than range 4
.
UC2S6000  COMPARE   PTCNLSR4,NBRDAYS
          GOTO      UC2S7000 IF NOT LESS
.
          MOVE      ONE,C2SRNG3               * range 3
          GOTO      UC2S7500 
.
.         Patient is in range 4 if in hospital for greater than or 
.         equal to range 4 
.
UC2S7000  MOVE      ONE,C2SRNG4               * range 4
.
UC2S7500  PACK      KEY30,XADMN,Z30
          CALL      RDSTRAN2
          CALL      RDPTRAN2
.
          MOVE      TADOCT,STADOCT            * save doctor
          MOVE      TWARD,STWARD              * save ward
          CALL      UPC20000                  * update the patc2sfd file
          MOVE      ZERO,C2SDSCH
          MOVE      ZERO,C2SDEAD
          MOVE      ZERO,C2SRNG1
          MOVE      ZERO,C2SRNG2
          MOVE      ZERO,C2SRNG3
          MOVE      ZERO,C2SRNG4
.
          MOVE      ZERO,PUBFLG
          MOVE      ZERO,DATEFLG   
.
          MATCH     ADATE,DDATE               * patient a daycase ?
          GOTO      UC2S9000 IF NOT EQUAL
.
          MOVE      ONE,CALDAYS
.
.         If the patient has a admission type indicator 1 equal 
.         to two then they are a private patient. Anything else 
.         will be a public patient
.
UC2S8000  PACK      KEY5,CODEA,TATYPE
          CALL      RDCODE1
          BRANCH    OVRCD,UC2S8100
.
          MATCH     "2",TCINDC1
          GOTO      UC2S8200 IF EQUAL
.
UC2S8100  MOVE      ZERO,PUBFLG               * public patient
          GOTO      UC2S8300
.
UC2S8200  MOVE      ONE,PUBFLG                * private patient
.
.         Branch to the appropriate public or private section
.
UC2S8300  BRANCH    PUBFLG,UC2S8500
.
.         Branch to the appropriate date using the date flag
.
          BRANCH    DATEFLG,UC2S8400
.
.         Number of public bed days for the patient
.
          MOVE      CALDAYS,C2SSTDB
          GOTO      UC2S8800
.
.         Number of public bed days for the patient in the period
.
UC2S8400  MOVE      CALDAYS,C2SSTDP
          GOTO      UC2S8800
.
.         Branch to the appropriate date using the date flag
.
UC2S8500  BRANCH    DATEFLG,UC2S8700
.
.         Number of private bed days for the patient 
.
          MOVE      CALDAYS,C2SPRVB
          GOTO      UC2S8800
.
.         Number of private bed days for the patient in the period
.
UC2S8700  MOVE      CALDAYS,C2SPRVP
.
UC2S8800  CALL      UPC20000                  * update the patc2sfd file
.
          COMPARE   ONE,DATEFLG               * need to do code for bed days in
          GOTO      UC2S9999 IF EQUAL           the period and total bed days
.
          MOVE      ONE,DATEFLG
          MOVE      ZERO,PUBFLG
          MOVE      ZERO,C2SSTDP
          MOVE      ZERO,C2SPRVP
          MOVE      ZERO,C2SSTDB
          MOVE      ZERO,C2SPRVB
.
          GOTO      UC2S8000
.
UC2S9000  MOVE      ADATE,DATEF
          MOVE      DDATE,DATET               * use discharge date
.
.         To handle the changes of attending doctor, admission type & ward,
.         looping through the bed transfer file is required
.
          CALL      TRC20000                  * read the transfer file
.
          MOVE      ZERO,PUBFLG
          MOVE      ONE,DATEFLG    
          MOVE      FADMDTE,DATEF
          MOVE      TADMDTE,DATET
.
.         To handle the changes of attending doctor, admission type & ward,
.         looping through the bed transfer file is required
.
          CALL      TRC20000                  * read the transfer file
.
UC2S9999  RETURN
+
.**********************************************************************
.*                         TRC20000                                   *
.*                   Read through the PATTRNFD file                   *
.**********************************************************************
.
TRC20000  PACK      KEY30,XADMN,SP20
          CALL      RDSTRAN2
TRC21000  CALL      RDKTRAN2
          BRANCH    OVRCD,TRC27000
.
          MATCH     XADMN,TADMN               * same admission number ?
          GOTO      TRC27000 IF NOT EQUAL
.
          MOVE      TMOVE,STMOVE
          RESET     TDATE
          REP       " 0",TDATE
.
          MATCH     ANSA,TMOVE                * current admission record
          GOTO      TRC24000 IF EQUAL
          MATCH     ANSR,TMOVE                * returned from leave record
          GOTO      TRC24000 IF EQUAL
          MATCH     ANSC,TMOVE                * change record 
          GOTO      TRC22000 IF EQUAL
          MATCH     ANSL,TMOVE                * onleave record
          GOTO      TRC22000 IF EQUAL
.
.         Discharge record
.
          MATCH     DATEF,TDATE
          GOTO      TRC29999 IF LESS
          MATCH     DATET,TDATE
          GOTO      TRC27000 IF LESS
          MOVE      DATET,TDATE
          GOTO      TRC27000
.
.         End of period, calculate the data needed, then start new period
.
TRC22000  MATCH     DATEF,TDATE
          GOTO      TRC24000 IF LESS
.
          MATCH     DATET,TDATE
          GOTO      TRC23000 IF LESS
.
          MOVE      DATET,WDATE1
          MOVE      STDATE,WDATE2
          CALL      CLC20000                  * calculate the date difference
          CALL      UPC20000                  * update the patc2sfd file
          MOVE      ZERO,C2SSTDP
          MOVE      ZERO,C2SPRVP
          MOVE      ZERO,C2SSTDB
          MOVE      ZERO,C2SPRVB
          GOTO      TRC29999
.
TRC23000  MOVE      TDATE,WDATE1
          MOVE      STDATE,WDATE2
          CALL      CLC20000                  * calculate the date difference
          CALL      UPC20000                  * update the patc2sfd file
          MOVE      ZERO,C2SSTDP
          MOVE      ZERO,C2SPRVP
          MOVE      ZERO,C2SSTDB
          MOVE      ZERO,C2SPRVB
.
.         Save new parameters for the start of the new period
.
TRC24000  MATCH     DATET,TDATE
          GOTO      TRC29999 IF NOT LESS
          MATCH     DATEF,TDATE
          GOTO      TRC25000 IF NOT LESS
          MOVE      DATEF,STDATE
          GOTO      TRC26000
.
TRC25000  MOVE      TDATE,STDATE
.
TRC26000  MOVE      TATYPE,STATYPE            * save admission type
          MOVE      TADOCT,STADOCT            * save doctor 
          MOVE      TWARD,STWARD              * save ward 
          GOTO      TRC21000 
.
.         Last record for patient
.
TRC27000  MATCH     ANSL,STMOVE               * on leave patient ?
          GOTO      TRC29999 IF EQUAL
          MATCH     " ",STMOVE
          GOTO      TRC29999 IF EQUAL
.
          MATCH     ANSD,STMOVE               * discharged patient ?
          GOTO      TRC28000 IF NOT EQUAL
          MOVE      TDATE,WDATE1
          MOVE      STDATE,WDATE2
          GOTO      TRC29000
.
TRC28000  MOVE      DATET,WDATE1
          MOVE      STDATE,WDATE2
TRC29000  CALL      CLC20000                  * calculate the date difference
          CALL      UPC20000                  * update the patc2sfd file
.
          MOVE      ZERO,C2SSTDP
          MOVE      ZERO,C2SPRVP
          MOVE      ZERO,C2SSTDB
          MOVE      ZERO,C2SPRVB
.
TRC29999  RETURN
+
.**********************************************************************
.*                         CLC20000                                   *
.*        Calculate the date difference for the PATC2SFD file         *
.**********************************************************************
.
CLC20000  MOVE      ZERO,CALDAYS
.
          DAYSEP    WDATE2,WDATE1,CALDAYS
.
.         If the patient has a admission type indicator 1 equal 
.         to two then they are a private patient. Anything else 
.         will be a public patient
.
          PACK      KEY5,CODEA,STATYPE
          CALL      RDCODE1
          BRANCH    OVRCD,CLC22000
.
          MATCH     "2",TCINDC1
          GOTO      CLC23000 IF EQUAL
.
CLC22000  MOVE      ZERO,PUBFLG               * public patient
          GOTO      CLC24000
.
CLC23000  MOVE      ONE,PUBFLG                * private patient
.
.         Branch to the appropriate public or private section
.
CLC24000  BRANCH    PUBFLG,CLC26000
.
.         Branch to the appropriate date using the date flag
.
          BRANCH    DATEFLG,CLC25000
.
.         Number of public bed days for the patient
.
          MOVE      CALDAYS,C2SSTDB
          GOTO      CLC29999
.
.         Number of public bed days for the patient in the period
.
CLC25000  MOVE      CALDAYS,C2SSTDP
          GOTO      CLC29999
.
.         Branch to the appropriate date using the date flag
.
CLC26000  BRANCH    DATEFLG,CLC27000
.
.         Number of private bed days for the patient 
.
          MOVE      CALDAYS,C2SPRVB
          GOTO      CLC29999
.
.         Number of private bed days for the patient in the period
.
CLC27000  MOVE      CALDAYS,C2SPRVP
.
CLC29999  RETURN
+
.**********************************************************************
.*                         UPC20000                                   *
.*                   Update the PATC2SFD file                         *
.**********************************************************************
.
UPC20000  
.         Find the ward classification
.
          MOVE      SP3,WCLASS
          PACK      KEY6,STWARD,SP3
          CALL      RDWARD1
.
          MOVE      WCLASS,C2SCLSS            * ward classification
          MOVE      STADOCT,C2SDOCT           * attending doctor
.
.         Write or update the patc2sfd file
.
          PACK      KEY15,CSTPER,C2SCLSS,C2SDOCT
          CALL      RDPTC2S1
          BRANCH    OVRCD,UPC21000
.
          ADD       C2SEMRG,PTC2EMRG
          ADD       C2SELCT,PTC2ELCT
          ADD       C2SDSCH,PTC2DSCH
          ADD       C2SDEAD,PTC2DEAD
          ADD       C2SSTDP,PTC2STDP
          ADD       C2SPRVP,PTC2PRVP
          ADD       C2SSTDB,PTC2STDB
          ADD       C2SPRVB,PTC2PRVB
          ADD       C2SRNG1,PTC2RNG1
          ADD       C2SRNG2,PTC2RNG2
          ADD       C2SRNG3,PTC2RNG3
          ADD       C2SRNG4,PTC2RNG4
.
          CALL      UPPTC2S1
          GOTO      UPC29999
.
UPC21000  MOVE      CSTPER,PTC2DATE
          MOVE      C2SCLSS,PTC2CLSS
          MOVE      C2SDOCT,PTC2DOCT
          MOVE      C2SEMRG,PTC2EMRG
          MOVE      C2SELCT,PTC2ELCT
          MOVE      C2SDSCH,PTC2DSCH
          MOVE      C2SDEAD,PTC2DEAD
          MOVE      C2SSTDP,PTC2STDP
          MOVE      C2SPRVP,PTC2PRVP
          MOVE      C2SSTDB,PTC2STDB
          MOVE      C2SPRVB,PTC2PRVB
          MOVE      C2SRNG1,PTC2RNG1
          MOVE      C2SRNG2,PTC2RNG2
          MOVE      C2SRNG3,PTC2RNG3
          MOVE      C2SRNG4,PTC2RNG4
.
          CALL      WRPTC2S1
.
UPC29999  RETURN
+
.**********************************************************************
.*                         UCSS0000                                   *
.*             Generate the data PATCSSFD file                        *
.**********************************************************************
.
UCSS0000  DISPLAY   *P1:24,"patcssad";
.
          MOVE      SP1,CSSTYPC            * type of clinic "m" or "s"
          MOVE      SP1,CSSTYPP            * type of patient
          MOVE      ZERO,CSSSTRT           * patients at start of period
          MOVE      ZERO,CSSADMT           * patients admitted during period
          MOVE      ZERO,CSSDSCH           * patients discharged during period
          MOVE      ZERO,CSSDIED           * patients died during period
          MOVE      ZERO,CSSEND            * patients at end of period
          MOVE      ZERO,CSSBDAY           * bed days (excluding day cases)
          MOVE      ZERO,CSSDYCS           * number of day cases in period
          MOVE      ZERO,CSSDBDY           * discharged patient bed days
          MOVE      ZERO,CSSRNG1           * long stay patients 1 (30-60 days)
          MOVE      ZERO,CSSRNG2           * long stay patients 2 (61-120 days)
          MOVE      ZERO,CSSRNG3           * long stay patients 3 (121-180 days)
          MOVE      ZERO,CSSRNG4           * long stay patients 4 (181+ days)
          MOVE      ZERO,CSSWAIT           * patients admitted from waiting list
          MOVE      SP8,STDATE             * save transfer date
          MOVE      " ",STMOVE             * save transfer movement
          MOVE      SP3,STATYPE            * save transfer admission type
          MOVE      SP6,STADOCT            * save transfer doctor
          MOVE      SP6,STWARD             * save transfer ward
.
.         Determine whether the clinic is surgical or medical
.
          PACK      KEY5,CODEU6,AUSR6
          CALL      RDCODE1
          BRANCH    OVRCD,UCSS1000
.
          MATCH     ANSS,TCINDC1
          GOTO      UCSS1000 IF NOT EQUAL
.
          MOVE      ANSS,CSSTYPC              * surgical clinic
          GOTO      UCSS2000
.
UCSS1000  MOVE      ANSM,CSSTYPC              * medical clinic
.
.         If the patient has a claim code with a W, M or V in 
.         indicator 1 they are compensable type of patient
.
UCSS2000  PACK      KEY5,CODECL,ACLAIM
          CALL      RDCODE1
          BRANCH    OVRCD,UCSS3000
.
          MOVE      ZERO,FORM2
UCSS3000  ADD       ONE,FORM2
          COMPARE   SIX,FORM2
          GOTO      UCSS5000 IF NOT LESS
.
          LOAD      ANS,FORM2,TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5
.
          CMATCH    ANSW,ANS                  * workers compensation
          GOTO      UCSS4000 IF EQUAL
          CMATCH    ANSM,ANS                  * motor accident board
          GOTO      UCSS4000 IF EQUAL
          CMATCH    ANSV,ANS                  * veterans affairs
          GOTO      UCSS4000 IF EQUAL
. 
          GOTO      UCSS3000
.
UCSS4000  MOVE      THREE,CSSTYPP             * patient is compensable
.
.         To handle the changes of attending doctor & admission type,
.         looping through the bed transfer file is required
.
UCSS5000  CALL      TRCS0000                  * read transfer file
.
UCSS9999  RETURN
+
.**********************************************************************
.*                         TRCS0000                                   *
.*                   Read through the PATTRNFD file                   *
.**********************************************************************
.
TRCS0000  MATCH     ADATE,FADMDTE
          GOTO      TRCS1500 IF LESS
.
.         Number of patients at start of period
.
          PACK      KEY30,XADMN,FADMDTE,Z30
          CALL      RDSTRAN2
          CALL      RDPTRAN2            
          BRANCH    OVRCD,TRCS1500
.
          MATCH     XADMN,TADMN               * same admission number ?
          GOTO      TRCS1500 IF NOT EQUAL
.
          MATCH     TDATE,FADMDTE
          GOTO      TRCS0500 IF EQUAL
.
          MATCH     ANSL,TMOVE                * on leave patient ?
          GOTO      TRCS1500 IF EQUAL
          GOTO      TRCS1000 
.
TRCS0500  PACK      KEY30,XADMN,FADMDTE,SP20
          CALL      RDSTRAN2
          CALL      RDKTRAN2            
          BRANCH    OVRCD,TRCS1500
.
          MATCH     XADMN,TADMN               * same admission number ?
          GOTO      TRCS1500 IF NOT EQUAL
.
          MATCH     ANSR,TMOVE                * patient returned from leave ?
          GOTO      TRCS1500 IF EQUAL
.
TRCS1000  MOVE      ONE,CSSSTRT
          CALL      UPCS0000                  * update the patcssfd file
          MOVE      ZERO,CSSSTRT
.
.         Number of admissions during period 
.
TRCS1500  MATCH     FADMDTE,ADATE
          GOTO      TRCS2000 IF LESS
.
          MATCH     ADATE,TADMDTE
          GOTO      TRCS2000 IF LESS
.
          PACK      KEY30,XADMN,SP20
          CALL      RDSTRAN2
          CALL      RDKTRAN2            
.
          MOVE      ONE,CSSADMT
          CALL      UPCS0000                  * update the patcssfd file
          MOVE      ZERO,CSSADMT
.
.         Number of discharges during period
.
TRCS2000  BRANCH    ASTATFLG,TRCS7000         * patient discharged ?
.
          MATCH     FADMDTE,DDATE             * discharged within period
          GOTO      TRCS7000 IF LESS
.
          MATCH     DDATE,TADMDTE
          GOTO      TRCS7000 IF LESS
.
          BRANCH    PATDEAD,TRCS2500          * patient dead ?
          GOTO      TRCS3000   
.
TRCS2500  MOVE      ONE,CSSDSCH
          GOTO      TRCS3500
.
.         Number of deaths during period 
.
TRCS3000  MOVE      ONE,CSSDIED
.
.         Calculate the length of stay the patient has been in hospital
.
TRCS3500  MOVE      ZERO,NBRDAYS
          MOVE      ADATE,FROMDATE
          MOVE      DDATE,TODATE
          CALL      RTIODAYS                  * bed day calculating routine
.
.         Patient is in range 1 if in hospital for greater than or 
.         equal to range 1 and less than range 2
.
          COMPARE    PTCNLSR1,NBRDAYS
          GOTO       TRCS6500 IF LESS
.
          COMPARE    PTCNLSR2,NBRDAYS
          GOTO       TRCS4000 IF NOT LESS
.
          MOVE       ONE,CSSRNG1              * range 1
          GOTO       TRCS6500 
.
.         Patient is in range 2 if in hospital for greater than or 
.         equal to range 2 and less than range 3
.
TRCS4000  COMPARE    PTCNLSR3,NBRDAYS
          GOTO       TRCS5500 IF NOT LESS
.
          MOVE       ONE,CSSRNG2              * range 2
          GOTO       TRCS6500 
.
.         Patient is in range 3 if in hospital for greater than or 
.         equal to range 3 and less than range 4
.
TRCS5500  COMPARE    PTCNLSR4,NBRDAYS
          GOTO       TRCS6000 IF NOT LESS
.
          MOVE       ONE,CSSRNG3              * range 3
          GOTO       TRCS6500 
.
.         Patient is in range 4 if in hospital for greater than or 
.         equal to range 4 
.
TRCS6000  MOVE       ONE,CSSRNG4              * range 4
.
TRCS6500  PACK       KEY30,XADMN,Z30
          CALL       RDSTRAN2
          CALL       RDPTRAN2           
.
          CALL       UPCS0000                  * update the patcssfd file
.
          MOVE       ZERO,CSSDSCH
          MOVE       ZERO,CSSDIED
          MOVE       ZERO,CSSRNG1
          MOVE       ZERO,CSSRNG2
          MOVE       ZERO,CSSRNG3
          MOVE       ZERO,CSSRNG4
.
.         Calculate the number of discharged bed days
.
TRCS6700  MOVE      ONE,DATEFLG
          MOVE      ADATE,DATEF
          MOVE      DDATE,DATET
.
          MOVE      SP8,STDATE             * save transfer date
          MOVE      " ",STMOVE             * save transfer movement
          MOVE      SP3,STATYPE            * save transfer admission type
.
          CALL      CCCS0000               * calculate date difference
.
.         Number of patients in at end of period
.
TRCS7000  BRANCH    ASTATFLG,TRCS7500         * patient discharged ?
.
          MATCH     TADMDTE,DDATE
          GOTO      TRCS9000 IF LESS
.
TRCS7500  PACK      KEY30,XADMN,TADMDTE,Z30
          CALL      RDSTRAN2
          CALL      RDPTRAN2
          BRANCH    OVRCD,TRCS9000 
.
          MATCH     XADMN,TADMN               * same admission number ?
          GOTO      TRCS9000 IF NOT EQUAL
.
          MATCH     TDATE,TADMDTE
          GOTO      TRCS8000 IF EQUAL
.
          MATCH     ANSL,TMOVE                * patient on leave ?
          GOTO      TRCS9000 IF EQUAL
          GOTO      TRCS8500
.
TRCS8000  MATCH     ANSR,TMOVE                * patient returned from leave
          GOTO      TRCS9000 IF EQUAL
.
TRCS8500  MOVE      ONE,CSSEND
          CALL      UPCS0000                  * update the patcssfd file
          MOVE      ZERO,CSSEND
.
.         Number of day cases in the period
.
TRCS9000  BRANCH    ASTATFLG,TRCS9200         * patient discharged ?
.
          MATCH     ADATE,DDATE
          GOTO      TRCS9200 IF NOT EQUAL
.
          PACK      KEY30,XADMN,Z30
          CALL      RDSTRAN2
          CALL      RDPTRAN2
.
          MOVE      ONE,CSSDYCS
          CALL      UPCS0000                * update the patcssfd file
          MOVE      ZERO,CSSDYCS
.
.         Calculate the number of patients who were admitted from the w/list
.
TRCS9200  MOVE      XADMN,KEY8
          CALL      RDBKREC1
          BRANCH    OVRCD,TRCS9500
.
          MATCH     SP9,BKREPROC
          GOTO      TRCS9500 IF EQUAL
.
          MATCH     ZEROUR,BKREURNO
          GOTO      TRCS9500 IF EQUAL
.
          PACK      KEY30,XADMN,SP20
          CALL      RDSTRAN2
          CALL      RDKTRAN2
.
          MOVE      ONE,CSSWAIT
          CALL      UPCS0000                 * update the patcssfd file
          MOVE      ZERO,CSSWAIT
.
.         Calculate the number of bed days in the period, excluding day cases
.
TRCS9500  MOVE      ZERO,DATEFLG
          MOVE      FADMDTE,DATEF       
          MOVE      TTODATE,DATET             * ttodate = end date of period + 1
.
          MOVE      SP8,STDATE             * save transfer date
          MOVE      " ",STMOVE             * save transfer movement
          MOVE      SP3,STATYPE            * save transfer admission type
.
          CALL      CCCS0000               * calculate date difference
.
TRCS9999  RETURN
+
.**********************************************************************
.*                         CCCS0000                                   *
.*        Calculate the date difference for the PATCSSFD file         *
.**********************************************************************
.
CCCS0000  PACK      KEY30,XADMN,SP20
          CALL      RDSTRAN2
CCCS1000  CALL      RDKTRAN2
          BRANCH    OVRCD,CCCS7000
.
          MATCH     XADMN,TADMN               * same admission number ?
          GOTO      CCCS7000 IF NOT EQUAL
.
          MOVE      TMOVE,STMOVE
          RESET     TDATE
          REP       " 0",TDATE
.
          MATCH     ANSA,TMOVE                * current admission record
          GOTO      CCCS4000 IF EQUAL
          MATCH     ANSR,TMOVE                * returned from leave record
          GOTO      CCCS4000 IF EQUAL
          MATCH     ANSC,TMOVE                * change record 
          GOTO      CCCS2000 IF EQUAL
          MATCH     ANSL,TMOVE                * onleave record
          GOTO      CCCS2000 IF EQUAL
.
.         Discharge record
.
          MATCH     DATEF,TDATE
          GOTO      CCCS9999 IF LESS
          MATCH     DATET,TDATE
          GOTO      CCCS7000 IF LESS
          MOVE      DATET,TDATE
          GOTO      CCCS7000
.
.         End of period, calculate the data needed, then start new period
.
CCCS2000  MATCH     DATEF,TDATE
          GOTO      CCCS4000 IF LESS
.
          MATCH     DATET,TDATE
          GOTO      CCCS3000 IF LESS
.
          MOVE      DATET,WDATE1
          MOVE      STDATE,WDATE2
          CALL      CLCS0000                  * calculate the date difference
          GOTO      CCCS9999
.
CCCS3000  MOVE      TDATE,WDATE1
          MOVE      STDATE,WDATE2
          CALL      CLCS0000                  * calculate the date difference
.
.         Save new parameters for the start of the new period
.
CCCS4000  MATCH     DATET,TDATE
          GOTO      CCCS9999 IF NOT LESS
.
          MATCH     DATEF,TDATE
          GOTO      CCCS5000 IF NOT LESS
.
          MOVE      DATEF,STDATE
          GOTO      CCCS6000
.
CCCS5000  MOVE      TDATE,STDATE
.
CCCS6000  MOVE      TATYPE,STATYPE             * save admission type
          GOTO      CCCS1000 
.
.         Last record for patient
.
CCCS7000  MATCH     ANSL,STMOVE               * on leave patient ?
          GOTO      CCCS9999 IF EQUAL
          MATCH     " ",STMOVE
          GOTO      CCCS9999 IF EQUAL
.
          MATCH     ANSD,STMOVE               * discharged patient ?
          GOTO      CCCS8000 IF NOT EQUAL
          MOVE      TDATE,WDATE1
          MOVE      STDATE,WDATE2
          GOTO      CCCS9000
.
CCCS8000  MOVE      DATET,WDATE1
          MOVE      STDATE,WDATE2
CCCS9000  CALL      CLCS0000                  * calculate the date difference
.
CCCS9999  RETURN
+
.**********************************************************************
.*                         CLCS0000                                   *
.*        Calculate the date difference for the PATCSSFD file         *
.**********************************************************************
.
CLCS0000  MOVE      ZERO,CALDAYS
.
          DAYSEP    WDATE2,WDATE1,CALDAYS
.
.         Branch to the appropriate calculation of number bed days
.
          BRANCH    DATEFLG,CLCS2000
.
.         Number of bed days in the period, excludes day cases
.
          MOVE      CALDAYS,CSSBDAY
          GOTO      CLCS9999
.
.         Number of discharged bed days 
.
CLCS2000  MATCH     WDATE1,WDATE2
          GOTO      CLCS3000 IF NOT EQUAL
.
          MOVE      ONE,CALDAYS    * patient is a day case so equals one bed day
.
CLCS3000  MOVE      CALDAYS,CSSDBDY
CLCS9999  CALL      UPCS0000               * update the patcssfd file
          MOVE      ZERO,CSSBDAY
          MOVE      ZERO,CSSDBDY
          RETURN
+
.**********************************************************************
.*                         UPCS0000                                   *
.*                   Update the PATCSSFD file                         *
.**********************************************************************
.
UPCS0000
.         If the patient is compensable don't find out whether they are a
.         public or private patient
.
          MATCH     "3",CSSTYPP
          GOTO      UPCS8000 IF EQUAL
.
.         If the patient has a admission type indicator 1 equal 
.         to two then they are a private patient. Anything else 
.         will be a public patient
.
          PACK      KEY5,CODEA,TATYPE
          CALL      RDCODE1
          BRANCH    OVRCD,UPCS8000
.
          MATCH     "2",TCINDC1
          GOTO      UPCS3000 IF EQUAL
          MOVE      ONE,CSSTYPP               * public patient
          GOTO      UPCS8000
.
UPCS3000  MOVE      TWO,CSSTYPP               * private patient
.
.         Write or update the patcssfd file
.
UPCS8000  PACK      KEY8,CSTPER,CSSTYPC,CSSTYPP
          CALL      RDPTCSS1
          BRANCH    OVRCD,UPCS9000
.
          ADD       CSSSTRT,PTCSSTRT
          ADD       CSSADMT,PTCSADMT
          ADD       CSSDSCH,PTCSDSCH
          ADD       CSSDIED,PTCSDIED
          ADD       CSSEND,PTCSEND
          ADD       CSSBDAY,PTCSBDAY
          ADD       CSSDYCS,PTCSDYCS
          ADD       CSSDBDY,PTCSDBDY
          ADD       CSSRNG1,PTCSRNG1
          ADD       CSSRNG2,PTCSRNG2
          ADD       CSSRNG3,PTCSRNG3
          ADD       CSSRNG4,PTCSRNG4
          ADD       CSSWAIT,PTCSWAIT
.
          CALL      UPPTCSS1
          GOTO      UPCS9999
.
UPCS9000  MOVE      CSTPER,PTCSDATE         
          MOVE      CSSTYPC,PTCSTYPC
          MOVE      CSSTYPP,PTCSTYPP
          MOVE      CSSSTRT,PTCSSTRT
          MOVE      CSSADMT,PTCSADMT
          MOVE      CSSDSCH,PTCSDSCH
          MOVE      CSSDIED,PTCSDIED
          MOVE      CSSEND,PTCSEND
          MOVE      CSSBDAY,PTCSBDAY
          MOVE      CSSDYCS,PTCSDYCS
          MOVE      CSSDBDY,PTCSDBDY
          MOVE      CSSRNG1,PTCSRNG1
          MOVE      CSSRNG2,PTCSRNG2
          MOVE      CSSRNG3,PTCSRNG3
          MOVE      CSSRNG4,PTCSRNG4
          MOVE      CSSWAIT,PTCSWAIT
.
          CALL      WRPTCSS1
.
UPCS9999  RETURN
+
.**********************************************************************
.*                         UDAS0000                                   *
.*            Generate the data for PATDASFD file                     *
.**********************************************************************
.
UDAS0000  DISPLAY   *P1:24,"patdasaf";
.
          MOVE      SP3,DASCODE             * admission type or discharge status
          MOVE      ZERO,DASADLT            * number of adults
          MOVE      ZERO,DASCHLD            * number of children
          MOVE      ZERO,DASMALE            * number of males
          MOVE      ZERO,DASFEML            * number of females
          MOVE      ZERO,DASINDT            * number of indeterminates
          MOVE      ZERO,DASUNKN            * number of unknowns
          MOVE      ZERO,DASREF             * reference
          MOVE      SP8,STDATE              * save transfer date
          MOVE      " ",STMOVE              * save transfer movement
          MOVE      SP3,STATYPE             * save transfer admission type
          MOVE      SP6,STADOCT             * save transfer doctor
          MOVE      SP6,STWARD              * save transfer ward
.
.         Find out whether the patient is an adult or child
.
          COMPARE   CAGECOFF,PSAGE
          GOTO      UDAS1000 IF LESS
.
          MOVE      ONE,DASADLT               * patient is an adult
          GOTO      UDAS1500 
.
UDAS1000  MOVE      ONE,DASCHLD               * patient is an child
.
.        Find out whether the patient is male, female, indeterminate or unknown
.
UDAS1500  MATCH     ANSM,PSEX
          IF        @EQUAL
            MOVE      ONE,DASMALE              * patient is male
            GOTO      UDAS2500
          ENDIF
.
          MATCH     ANSF,PSEX
          IF        @EQUAL
            MOVE      ONE,DASFEML              * patient is female
            GOTO      UDAS2500
          ENDIF
.
          MATCH     ANSI,PSEX
          IF        @EQUAL
            MOVE      ONE,DASINDT              * patient is female
            GOTO      UDAS2500
          ENDIF
.
          MATCH     ANSU,PSEX
          IF        @EQUAL
            MOVE      ONE,DASUNKN              * patient is female
            GOTO      UDAS2500
          ENDIF
.
.         Number of admissions during period using admission source
.
UDAS2500  MATCH     FADMDTE,ADATE
          GOTO      UDAS3000 IF LESS
.
          MATCH     ADATE,TADMDTE
          GOTO      UDAS3000 IF LESS
.
          MOVE      FOUR,DASREF
          MOVE      ASRCE,DASCODE
          CALL      UPDA0000                  * update the patdasfd file
.
.         Number of discharges during period by discharge status, and
.         number of discharges during period by discharge destination 
.         Dead people not included
.
UDAS3000  BRANCH    ASTATFLG,UDAS4000         * patient discharged ?
.
          MATCH     FADMDTE,DDATE
          GOTO      UDAS4000 IF LESS
.
          MATCH     DDATE,TADMDTE
          GOTO      UDAS4000 IF LESS
.
          BRANCH    PATDEAD,UDAS3200          * patient dead ?
          GOTO      UDAS3500   
.
UDAS3200  MOVE      FIVE,DASREF
          MOVE      DSTAT,DASCODE             * discharge status
          CALL      UPDA0000                  * update the patdasfd file
.
          MOVE      "22",DASREF
          MOVE      DDEST,DASCODE             * discharge destination
          CALL      UPDA0000                  * update the patdasfd file
          GOTO      UDAS4000
.
.         Number of deaths during period by discharge status, and
.         number of deaths during period by discharge destination
.
UDAS3500  MOVE      SIX,DASREF
          MOVE      DSTAT,DASCODE             * discharge status
          CALL      UPDA0000                  * update the patdasfd file
.
          MOVE      "23",DASREF
          MOVE      DDEST,DASCODE             * discharge destination
          CALL      UPDA0000                  * update the patdasfd file
.
.         To handle the changes of attending doctor & admission type,
.         looping through the bed transfer file is required
.
UDAS4000  CALL      TRDA0000                  * read transfer file
.
UDAS9999  RETURN
+
.**********************************************************************
.*                         TRDA0000                                   *
.*                   Update the PATDASFD file                         *
.**********************************************************************
.
TRDA0000  MATCH     ADATE,FADMDTE
          GOTO      TRDA2500 IF LESS
.
.         Number of patients at start of period
.
          PACK      KEY30,XADMN,FADMDTE,Z30
          CALL      RDSTRAN2
          CALL      RDPTRAN2            
          BRANCH    OVRCD,TRDA2000
.
          MATCH     XADMN,TADMN               * same admission number ?
          GOTO      TRDA2000 IF NOT EQUAL
.
          MATCH     TDATE,FADMDTE
          GOTO      TRDA0500 IF EQUAL
          GOTO      TRDA1000 
.
TRDA0500  PACK      KEY30,XADMN,FADMDTE,SP20
          CALL      RDSTRAN2
          CALL      RDKTRAN2            
          BRANCH    OVRCD,TRDA2000
.
          MATCH     XADMN,TADMN               * same admission number ?
          GOTO      TRDA2000 IF NOT EQUAL
.
          MATCH     ANSA,TMOVE                * current admission record
          GOTO      TRDA2000 IF EQUAL
.
TRDA1000  MOVE      ONE,DASREF
          MOVE      TATYPE,DASCODE
          CALL      UPDA0000                  * update the patdasfd file
.
.         Bed days at start of period
.
TRDA2000  MOVE      TWO,DASREF
          MOVE      ADATE,DATEF
          MOVE      FADMDTE,DATET
.
          MOVE      SP8,STDATE                * save transfer date
          MOVE      " ",STMOVE                * save transfer movement
          MOVE      SP3,STATYPE               * save transfer admission type
.
          CALL      CLDA0000                  * calculate date difference
.
.         Number of admissions during period by admission type
.
TRDA2500  MATCH     FADMDTE,ADATE
          GOTO      TRDA3000 IF LESS
.
          MATCH     ADATE,TADMDTE
          GOTO      TRDA3000 IF LESS
.
          MATCH     ADATE,DDATE                * Daycase patients ?
          GOTO      TRDA2600 IF NOT EQUAL      * No.
.
          PACK      KEY30,XADMN,Z30            * Get admission type at
          CALL      RDSTRAN2                   * discharged time
          CALL      RDPTRAN2
          GOTO      TRDA2700
.
TRDA2600  PACK      KEY30,XADMN,SP20
          CALL      RDSTRAN2
          CALL      RDKTRAN2            
.
TRDA2700  MOVE      THREE,DASREF
          MOVE      TATYPE,DASCODE
          CALL      UPDA0000                  * update the patdasfd file
.
.         Number of discharges during period by admission type.
.         Not including deaths
.
TRDA3000  BRANCH    ASTATFLG,TRDA4000         * patient discharged ?
.
          MATCH     FADMDTE,DDATE             * discharged within the period
          GOTO      TRDA4000 IF LESS
.
          MATCH     DDATE,TADMDTE
          GOTO      TRDA4000 IF LESS
.
          PACK      KEY30,XADMN,Z30
          CALL      RDSTRAN2
          CALL      RDPTRAN2           
.
          BRANCH    PATDEAD,TRDA3500          * patient dead ?
          GOTO      TRDA3700   
.
TRDA3500  MOVE      SEVEN,DASREF
          MOVE      TATYPE,DASCODE
          CALL      UPDA0000                  * update the patdasfd file
          GOTO      TRDA4000
.
.         Number of deaths during period by admission type
.
TRDA3700  MOVE      EIGHT,DASREF
          MOVE      TATYPE,DASCODE
          CALL      UPDA0000                  * update the patdasfd file
.
.         Number of patients in at end of period
.
TRDA4000  BRANCH    ASTATFLG,TRDA4200         * patient discharged ?
.
          MATCH     TTODATE,DDATE             * ttodate = end date of period + 1
          GOTO      TRDA5500 IF LESS
.
TRDA4200  PACK      KEY30,XADMN,TTODATE,Z30
          CALL      RDSTRAN2
          CALL      RDPTRAN2
          BRANCH    OVRCD,TRDA5000 
.
          MATCH     XADMN,TADMN               * same admission number ?
          GOTO      TRDA5000 IF NOT EQUAL
.
          MOVE      NINE,DASREF
          MOVE      TATYPE,DASCODE
          CALL      UPDA0000                  * update the patdasfd file
.
.         Number of bed days at end of the period
.
TRDA5000  MOVE      TEN,DASREF
          MOVE      ADATE,DATEF
          MOVE      TTODATE,DATET
.
          MOVE      SP8,STDATE                * save transfer date
          MOVE      " ",STMOVE                * save transfer movement
          MOVE      SP3,STATYPE               * save transfer admission type
.
          CALL      CLDA0000                  * calculate date difference
.
.         Number of bed days in the period
.
TRDA5500  MOVE      TEN1,DASREF
          MOVE      FADMDTE,DATEF
          MOVE      TTODATE,DATET             * ttodate = end date of period + 1
.
          MOVE      SP8,STDATE                * save transfer date
          MOVE      " ",STMOVE                * save transfer movement
          MOVE      SP3,STATYPE               * save transfer admission type
.
          CALL      CLDA0000                  * calculate date difference
.
.         Number of patients in the period
.
TRDA6000  MOVE      SP8,STDATE                * save transfer date
          MOVE      " ",STMOVE                * save transfer movement
.
          BRANCH    ASTATFLG,TRDA6100         * patient discharged ?
.
          MATCH     ADATE,DDATE               * patient a daycase ?
          GOTO      TRDA6100 IF EQUAL
.
          MATCH     DDATE,FADMDTE             * discharge & from date same ?
          GOTO      TRDA6700 IF EQUAL
.
TRDA6100  MOVE      TEN2,DASREF               * total no. of patients in period
          MOVE      SP3,DASCODE
          CALL      UPDA0000                  * update the patdasfd file
.
          PACK      KEY30,XADMN,SP20
          CALL      RDSTRAN2
TRDA6200  CALL      RDKTRAN2
          BRANCH    OVRCD,TRDA6300    
.
          MATCH     XADMN,TADMN               * same admission number ?
          GOTO      TRDA6300 IF NOT EQUAL
.
          MATCH     TDATE,TTODATE             * ttodate = end date of period + 1
          GOTO      TRDA6200 IF LESS
.
          MOVE      TMOVE,STMOVE              * save transfer movement
          MOVE      TDATE,STDATE              * save transfer date
          GOTO      TRDA6200
.
TRDA6300  MATCH     ANSL,STMOVE               * patient on leave ?
          GOTO      TRDA6500 IF NOT EQUAL
.
.         Patient on leave within the period
.
          MATCH     FADMDTE,STDATE
          GOTO      TRDA6700 IF LESS
.
          MATCH     STDATE,TTODATE            * ttodate = end date of period + 1
          GOTO      TRDA6700 IF LESS
.
.         Only write to the temporary file if the admission number 
.         doesn`t already exist
.
TRDA6500  PACK      KEY30,XADMN,SP20
          CALL      RDSTRAN2
          CALL      RDKTRAN2
.
          PACK      KEY11,XADMN,TATYPE
          CALL      RDTMP2
          BRANCH    OVRCD,TRDA6600
          GOTO      TRDA6700
.
TRDA6600  MOVE      XADMN,BADMN
          CALL      WRTMP2
.
          MOVE      TEN2,DASREF
          MOVE      TATYPE,DASCODE
          CALL      UPDA0000                  * update the patdasfd file
.
.         Number of day cases in the period
.
TRDA6700  BRANCH    ASTATFLG,TRDA7000         * patient discharged ? 
.  
          MATCH     ADATE,DDATE 
          GOTO      TRDA7000 IF NOT EQUAL
.
          PACK      KEY30,XADMN,Z30
          CALL      RDSTRAN2
          CALL      RDPTRAN2
.
          MOVE      TEN3,DASREF
          MOVE      TATYPE,DASCODE
.
          MOVE      TATYPE,STATYPE
          MOVE      ONE,CALDAYS
          CALL      CUDA0000                 * calculate number of bed days
.
          MOVE      ZERO,CALDAYS
          MOVE      TEN1,DASREF
          ADD       ONE,CALDAYS
          CALL      CUDA0000                 * calculate number of bed days
.
.         Find out the patients length of stay and their admission 
.         type at the end of the period
.
TRDA7000  MOVE      ADATE,DATEF
          BRANCH    ASTATFLG,TRDA8000         * patient discharged ?
.
          MATCH     FADMDTE,DDATE             * discharged in period ?
          GOTO      TRDA8000 IF LESS
.
          MATCH     DDATE,TTODATE             * ttodate = end date of period + 1
          GOTO      TRDA8000 IF LESS
.
          MOVE      DDATE,TODATE              * use discharge date
          GOTO      TRDA9000
.
TRDA8000  MOVE      TTODATE,DATET             * ttodate = end date of period + 1
TRDA9000  CALL      DATR0000                  * read transfer file
.
.         Number of patients whose age is greater than or equal to ptcnelder
.
TRDA9500  COMPARE   PTCNELDR,PSAGE
          GOTO      TRDA9999 IF LESS
.
          PACK      KEY30,XADMN,SP20
          CALL      RDSTRAN2
          CALL      RDKTRAN2
.
          MOVE      TEN7,DASREF
          MOVE      TATYPE,DASCODE
          CALL      UPDA0000                  * update the patdasfd file
.
TRDA9999  RETURN
+
.**********************************************************************
.*                         CLDA0000                                   *
.*        Calculate the date difference for the PATDASFD file         *
.**********************************************************************
.
CLDA0000  PACK      KEY30,XADMN,SP20
          CALL      RDSTRAN2
CLDA1000  CALL      RDKTRAN2
          BRANCH    OVRCD,CLDA7000
.
          MATCH     XADMN,TADMN               * same admission number ?
          GOTO      CLDA7000 IF NOT EQUAL
.
          MOVE      TMOVE,STMOVE
          RESET     TDATE
          REP       " 0",TDATE
.
          MATCH     ANSA,TMOVE                * current admission record
          GOTO      CLDA4000 IF EQUAL
          MATCH     ANSR,TMOVE                * returned from leave record
          GOTO      CLDA4000 IF EQUAL
          MATCH     ANSC,TMOVE                * change record 
          GOTO      CLDA2000 IF EQUAL
          MATCH     ANSL,TMOVE                * onleave record
          GOTO      CLDA2000 IF EQUAL
.
.         Discharge record
.
          MATCH     DATEF,TDATE
          GOTO      CLDA9999 IF LESS
          MATCH     DATET,TDATE
          GOTO      CLDA7000 IF LESS
          MOVE      DATET,TDATE
          GOTO      CLDA7000
.
.         End of period, calculate the data needed, then start new period
.
CLDA2000  MATCH     DATEF,TDATE
          GOTO      CLDA4000 IF LESS
.
          MATCH     DATET,TDATE
          GOTO      CLDA3000 IF LESS
.
          MOVE      DATET,WDATE1
          MOVE      STDATE,WDATE2
          CALL      CBDA0000                  * calculate the date difference
          GOTO      CLDA9999
.
CLDA3000  MOVE      TDATE,WDATE1
          MOVE      STDATE,WDATE2
          CALL      CBDA0000                  * calculate the date difference
.
.         Save new parameters for the start of the new period
.
CLDA4000  MATCH     DATET,TDATE
          GOTO      CLDA9999 IF NOT LESS
.
          MATCH     DATEF,TDATE
          GOTO      CLDA5000 IF NOT LESS
.
          MOVE      DATEF,STDATE
          GOTO      CLDA6000
.
CLDA5000  MOVE      TDATE,STDATE
.
CLDA6000  MOVE      TATYPE,STATYPE              * save admission type
          GOTO      CLDA1000 
.
.         Last record for patient
.
CLDA7000  MATCH     ANSL,STMOVE               * on leave patient ?
          GOTO      CLDA9999 IF EQUAL
          MATCH     " ",STMOVE
          GOTO      CLDA9999 IF EQUAL
.
          MATCH     ANSD,STMOVE               * discharged patient ?
          GOTO      CLDA8000 IF NOT EQUAL
          MOVE      TDATE,WDATE1
          MOVE      STDATE,WDATE2
          GOTO      CLDA9000
.
CLDA8000  MOVE      DATET,WDATE1
          MOVE      STDATE,WDATE2
CLDA9000  CALL      CBDA0000                  * calculate the date difference
.
CLDA9999  RETURN
+
.**********************************************************************
.*                         CBDA0000                                   *
.*        Calculate the date difference for the PATDASFD file         *
.**********************************************************************
.
CBDA0000  MOVE      ZERO,CALDAYS
          MOVE      ZERO,ADLTDAS
          MOVE      ZERO,CHLDDAS
          MOVE      ZERO,MALEDAS
          MOVE      ZERO,FEMLDAS
          MOVE      ZERO,INDTDAS
          MOVE      ZERO,UNKNDAS
.
          DAYSEP    WDATE2,WDATE1,CALDAYS
.
          CALL      CUDA0000               * calulate number of be days
.
CBDA9999  RETURN
+
.**********************************************************************
.*                         CUDA0000                                   *
.*        Calculate the number of bed days for each criteria.         *
.**********************************************************************
.
CUDA0000  MOVE      CALDAYS,ADLTDAS
          MOVE      CALDAYS,CHLDDAS
          MOVE      CALDAYS,MALEDAS
          MOVE      CALDAYS,FEMLDAS
          MOVE      CALDAYS,INDTDAS
          MOVE      CALDAYS,UNKNDAS
.
          MULT      DASADLT,ADLTDAS
          MULT      DASCHLD,CHLDDAS
          MULT      DASMALE,MALEDAS
          MULT      DASFEML,FEMLDAS
          MULT      DASINDT,INDTDAS
          MULT      DASUNKN,UNKNDAS
.
.         Write or update the patdasfd file
.
          PACK      KEY11,DASREF,CSTPER,STATYPE
          CALL      RDPTDAS1
          BRANCH    OVRCD,CUDA2000
.
          ADD       ADLTDAS,PTDAADLT
          ADD       CHLDDAS,PTDACHLD
          ADD       MALEDAS,PTDAMALE
          ADD       FEMLDAS,PTDAFEML
          ADD       INDTDAS,PTDAINDT
          ADD       UNKNDAS,PTDAUNKN
.
          CALL      UPPTDAS1
          GOTO      CUDA9999
.
CUDA2000  MOVE      DASREF,PTDAREF
          MOVE      CSTPER,PTDADATE
          MOVE      STATYPE,PTDACODE
          MOVE      ADLTDAS,PTDAADLT
          MOVE      CHLDDAS,PTDACHLD
          MOVE      MALEDAS,PTDAMALE
          MOVE      FEMLDAS,PTDAFEML
          MOVE      INDTDAS,PTDAINDT
          MOVE      UNKNDAS,PTDAUNKN
.
          CALL      WRPTDAS1
.
CUDA9999  RETURN
+
.**********************************************************************
.*                         DATR0000                                   *
.*                   Read through the PATTRNFD file                   *
.**********************************************************************
.
DATR0000  PACK      KEY30,XADMN,SP20
          CALL      RDSTRAN2
DATR1000  CALL      RDKTRAN2
          BRANCH    OVRCD,DATR7000
.
          MATCH     XADMN,TADMN               * same admission number ?
          GOTO      DATR7000 IF NOT EQUAL
.
          MOVE      TMOVE,STMOVE
          RESET     TDATE
          REP       " 0",TDATE
.
          MATCH     ANSA,TMOVE                * current admission record
          GOTO      DATR4000 IF EQUAL
          MATCH     ANSR,TMOVE                * returned from leave record
          GOTO      DATR4000 IF EQUAL
          MATCH     ANSC,TMOVE                * change record 
          GOTO      DATR2000 IF EQUAL
          MATCH     ANSL,TMOVE                * onleave record
          GOTO      DATR2000 IF EQUAL
.
.         Discharge record
.
          MATCH     DATEF,TDATE
          GOTO      DATR9999 IF LESS
          MATCH     DATET,TDATE
          GOTO      DATR7000 IF LESS
          MOVE      DATET,TDATE
          GOTO      DATR7000
.
.         End of period, calculate the data needed, then start new period
.
DATR2000  MATCH     DATEF,TDATE
          GOTO      DATR4000 IF LESS
.
          MATCH     DATET,TDATE
          GOTO      DATR3000 IF LESS
.
          MOVE      DATET,WDATE1
          MOVE      STDATE,WDATE2
          CALL      DACL0000                  * calculate the date difference
          CALL      DAUP0000                  * update the patdasfd file
          GOTO      DATR9999
.
DATR3000  MOVE      TDATE,WDATE1
          MOVE      STDATE,WDATE2
          CALL      DACL0000                  * calculate the date difference
          CALL      DAUP0000                  * update the patdasfd file
.
.         Save new parameters for the start of the new period
.
DATR4000  MATCH     DATET,TDATE
          GOTO      DATR9999 IF NOT LESS
.
          MATCH     DATEF,TDATE
          GOTO      DATR5000 IF NOT LESS
.
          MOVE      DATEF,STDATE
          GOTO      DATR6000
.
DATR5000  MOVE      TDATE,STDATE
.
DATR6000  MOVE      TATYPE,STATYPE            * save admission type
          MOVE      TADOCT,STADOCT            * save doctor 
          GOTO      DATR1000 
.
.         Last record for the patient
.
DATR7000  MATCH     ANSL,STMOVE               * on leave patient ?
          GOTO      DATR9999 IF EQUAL
          MATCH     " ",STMOVE
          GOTO      DATR9999 IF EQUAL
.
          MATCH     ANSD,STMOVE               * discharged patient ?
          GOTO      DATR8000 IF NOT EQUAL
          MOVE      TDATE,WDATE1
          MOVE      STDATE,WDATE2
          GOTO      DATR9000
.
DATR8000  MOVE      DATET,WDATE1
          MOVE      STDATE,WDATE2
DATR9000  CALL      DACL0000                  * calculate the date difference
          CALL      DAUP0000                  * update the patdasfd file
.
DATR9999  RETURN
+
.**********************************************************************
.*                         DACL0000                                   *
.*        Calculate the date difference for the PATDASFD file         *
.**********************************************************************
.
DACL0000  MOVE      ZERO,CALDAYS
.
          DAYSEP    WDATE2,WDATE1,CALDAYS
.
          RETURN
+
.**********************************************************************
.*                         DAUP0000                                   *
.*                   Update the PATDASFD file                         *
.**********************************************************************
.
DAUP0000  
.         To be within range one, the patients length of stay (admission 
.         date to end of period date), must be greater than or equal to 
.         ptcnlos1 and less than ptcnlos2
.
          COMPARE   PTCNLOS1,CALDAYS
          GOTO      DAUP9999 IF LESS
.
          COMPARE   PTCNLOS2,CALDAYS
          GOTO      DAUP1000 IF NOT LESS
.
          MOVE      TEN4,DASREF
          MOVE      STATYPE,DASCODE
          CALL      UPDA0000                  * update the patdasfd file
          GOTO      DAUP9999 
.
.         To be within range two, the patients length of stay (admission 
.         date to end of period date), must be greater than or equal to 
.         ptcnlos2 and less than ptcnlos3
.
DAUP1000  COMPARE   PTCNLOS3,CALDAYS
          GOTO      DAUP2000 IF NOT LESS
.
          MOVE      TEN5,DASREF
          MOVE      STATYPE,DASCODE
          CALL      UPDA0000                  * update the patdasfd file
          GOTO      DAUP9999
.
.         To be within range three, the patients length of stay (admission 
.         date to end of period date), must be greater than or equal to 
.         ptcnlos3 
.
DAUP2000  MOVE      TEN6,DASREF
          MOVE      STATYPE,DASCODE
          CALL      UPDA0000                  * update the patdasfd file
.
DAUP9999  RETURN
+
.**********************************************************************
.*                         UPDA0000                                   *
.*                   Update the PATDASFD file                         *
.**********************************************************************
.
UPDA0000  PACK      KEY11,DASREF,CSTPER,DASCODE
          CALL      RDPTDAS1
          BRANCH    OVRCD,UPDA1000
.
          ADD       DASADLT,PTDAADLT
          ADD       DASCHLD,PTDACHLD
          ADD       DASMALE,PTDAMALE
          ADD       DASFEML,PTDAFEML
          ADD       DASINDT,PTDAINDT
          ADD       DASUNKN,PTDAUNKN
.
          CALL      UPPTDAS1
          GOTO      UPDA9999
.
UPDA1000  MOVE      DASREF,PTDAREF
          MOVE      CSTPER,PTDADATE
          MOVE      DASCODE,PTDACODE
.
          MOVE      DASADLT,PTDAADLT
          MOVE      DASCHLD,PTDACHLD
          MOVE      DASMALE,PTDAMALE
          MOVE      DASFEML,PTDAFEML
          MOVE      DASINDT,PTDAINDT
          MOVE      DASUNKN,PTDAUNKN
.
          CALL      WRPTDAS1
.
UPDA9999  RETURN
+
.**********************************************************************
.*                         UDST0000                                   *
.*            Generate data for PATDSTFD file                         *
.**********************************************************************
.
UDST0000  DISPLAY   *P1:24,"patdstaf";
.
.         Process current admissions, discharged & on leave admissions
.
          BRANCH    ASTAT,UDST9999,UDST2000,UDST2000,UDST2000,UDST9999
.
UDST2000  CALL      PRDS0000                  * process data for patdstfd file
.
UDST9999  RETURN
+
.**********************************************************************
.*                         PRDS0000                                   *
.*                   Process data for patdstfd file                   *
.**********************************************************************
.
PRDS0000  
.         If admitted before the end of the period - process data
.
          MATCH     TTODATE,ADATE
          GOTO      PRDS9999 IF NOT LESS
.
.         Recreate bed day data
.
          CALL      TRDS0000          * update the doctors statistics (bed days)
.
.         If admission date is in range, then recreate admission data
.
          MATCH     FADMDTE,ADATE             * admitted within period
          GOTO      PRDS1000 IF LESS
.
          MATCH     TTODATE,ADATE
          CALL      DSTA0000 IF LESS          * doctors statistics (admissions)
.
.         Check if this person has any ICD9 codes
.
PRDS1000  CALL      OPRR0000                  * update operation statistics
.
.         Check if this person has any MBS codesYA0000
.
          CALL      MBSS0000                  * update mbs statistics
.
PRDS9999  RETURN
+
.**********************************************************************
.*                         DSTA0000                                   *
.*                 Doctors Statistics (Admissions)                    *
.**********************************************************************
.
DSTA0000  MOVE      ADATE,XDATE
.
          CALL      ADOC0000          * get the attending doctor for date xdate
.
          MOVE      ANSA,PTDSTYPE               * attending doctor type
          CALL      ADAD0000                  * update discharge details
.
          MOVE      ADOCTR,PTDSCODE             * referring doctor code
          MATCH     SP6,PTDSCODE
          CALL      UDOC0000 IF EQUAL         * unknown doctor
          MOVE      ANSR,PTDSTYPE               * referring doctor type
          CALL      ADAD0000                  * update discharge details
.
          RETURN
+
.**********************************************************************
.*                         ADAD0000                                   *
.*                Update Discharge Details                            *
.**********************************************************************
.
ADAD0000  PACK      KEY13,SAVYEAR,SAVPERD,PTDSTYPE,PTDSCODE
          CALL      RLDSTA1
          BRANCH    OVRCD,ADAD1000,ADAD2000
          ADD       ONE,PTDSADMS
          CALL      UPDSTA1
          CALL      UUDSTA1
          GOTO      ADAD9999
.
ADAD1000  CALL      CLDS0000
          GOTO      ADAD0000
.
ADAD2000  DISPLAY   *W5
          GOTO      ADAD0000
.
ADAD9999  RETURN
+
.**********************************************************************
.*                         TRDS0000                                   *
.*              Update the Doctors Statistics (Bed Days)              *
.**********************************************************************
.
TRDS0000  MOVE      SP8,STDATE
          MOVE      " ",STMOVE
          MOVE      SP6,STADOCT
.
          BRANCH    ASTATFLG,TRDS0500         * patient discharged ?
.
          MATCH     ADATE,DDATE               * patient a daycase ?
          GOTO      DCAS0000 IF EQUAL         
.
.         Loop through the bed transfer file
.
TRDS0500  PACK      KEY30,XADMN,SP20
          CALL      RDSTRAN2
TRDS1000  CALL      RDKTRAN2
          BRANCH    OVRCD,TRDS4000
.
          MATCH     TADMN,XADMN               * same admission number ?
          GOTO      TRDS4000 IF NOT EQUAL
.
          MOVE      TMOVE,STMOVE
          REP       " 0",TDATE
.
          MATCH     "A",TMOVE                 * admission record
          GOTO      TRDS3000 IF EQUAL
          MATCH     "R",TMOVE                 * return record
          GOTO      TRDS3000 IF EQUAL
          MATCH     "C",TMOVE                 * change record
          GOTO      TRDS2000 IF EQUAL
          MATCH     "L",TMOVE                 * on leave record
          GOTO      TRDS2000 IF EQUAL
.
.         Discharge record
.
          MATCH     FADMDTE,TDATE             * within period ?
          GOTO      TRDS9999 IF LESS
.
          MATCH     TTODATE,TDATE
          GOTO      TRDS4000 IF LESS
.
          MOVE      TTODATE,TDATE             * ttodate = end date of period + 1
          GOTO      TRDS4000
.
.         End of a period, calculate the data needed, and then start new period
.
TRDS2000  MATCH     FADMDTE,TDATE             * within period ?
          GOTO      TRDS3000 IF LESS
.
          MATCH     TTODATE,TDATE             
          GOTO      TRDS2500 IF LESS
.
          MOVE      TTODATE,WDATE1            * ttodate = end date of period + 1
          MOVE      STDATE,WDATE2
.
          CALL      CCDS0000                  * calculate dates difference
          GOTO      TRDS9999
.
TRDS2500  MOVE      TDATE,WDATE1
          MOVE      STDATE,WDATE2
          CALL      CCDS0000                  * calculate dates difference
.
.         Save new parameters for the start of the new period
.
TRDS3000  MATCH     TTODATE,TDATE             * ttodate = end date of period + 1
          GOTO      TRDS9999 IF NOT LESS
.
          MATCH     FADMDTE,TDATE
          GOTO      TRDS3500 IF NOT LESS
.
          MOVE      FADMDTE,STDATE
          GOTO      TRDS3700
.
TRDS3500  MOVE      TDATE,STDATE
.
TRDS3700  MOVE      TADOCT,STADOCT            * save doctor
          GOTO      TRDS1000
.
TRDS4000  MATCH     "L",STMOVE                * on leave patient ?
          GOTO      TRDS9999 IF EQUAL
          MATCH     " ",STMOVE
          GOTO      TRDS9999 IF EQUAL
          MATCH     "D",STMOVE                * discharged patient ?
          GOTO      TRDS4500 IF NOT EQUAL
.
          MOVE      TDATE,WDATE1
          MOVE      STDATE,WDATE2
          GOTO      TRDS5000
.
TRDS4500  MOVE      TTODATE,WDATE1            * ttodate = end date of period + 1
          MOVE      STDATE,WDATE2
TRDS5000  CALL      CCDS0000                  * calculate dates difference
.
TRDS9999  RETURN
+
.**********************************************************************
.*                         DCAS0000                                   *
.*                    Process daycase data                            *
.**********************************************************************
.
DCAS0000  PACK      KEY30,XADMN,SP20
          CALL      RDSTRAN2
DCAS1000  CALL      RDKTRAN2
          BRANCH    OVRCD,DCAS2000
.
          MATCH     TADMN,XADMN               * same admission number ?
          GOTO      DCAS2000 IF NOT EQUAL
.
          MOVE      TADOCT,PTDSCODE
          GOTO      DCAS1000
.
DCAS2000  MOVE      ONE,WTOTDAY
          MOVE      ANSA,PTDSTYPE               * attending doctor type
          MATCH     SP6,PTDSCODE
          CALL      UDOC0000 IF EQUAL         * unknown doctor
.
          CALL      BEDD0000                  * process bed days data 
.
          MOVE      ADOCTR,PTDSCODE
          MATCH     SP6,PTDSCODE
          CALL      UDOC0000 IF EQUAL         * unknown doctor
          MOVE      ANSR,PTDSTYPE               * referring doctor type
.
          CALL      BEDD0000                  * process bed days data 
          RETURN
+
.**********************************************************************
.*                         CCDS0000                                   *
.*             Calculate dates difference                             *
.**********************************************************************
.
CCDS0000  DAYSEP    WDATE2,WDATE1,WTOTDAY
.
          MOVE      STADOCT,PTDSCODE
          MOVE      ANSA,PTDSTYPE               * attending doctor type
          MATCH     SP6,PTDSCODE
          CALL      UDOC0000 IF EQUAL         * unknown doctor
.
          CALL      BEDD0000                  * process bed days data
.
          MOVE      ADOCTR,PTDSCODE             * referring doctor code
          MATCH     SP6,PTDSCODE
          CALL      UDOC0000 IF EQUAL         * unknown doctor
          MOVE      ANSR,PTDSTYPE               * referring doctor type
          CALL      BEDD0000                  * process bed days data
.
          RETURN
+
.**********************************************************************
.*                         BEDD0000                                   *
.*                   Process bed days data                            * 
.**********************************************************************
BEDD0000  PACK      KEY13,SAVYEAR,SAVPERD,PTDSTYPE,PTDSCODE
          CALL      RLDSTA1
          BRANCH    OVRCD,BEDD1000,BEDD2000
          ADD       WTOTDAY,PTDSBEDD
          CALL      UPDSTA1
          CALL      UUDSTA1
          GOTO      BEDD9999
.
BEDD1000  CALL      CLDS0000
          GOTO      BEDD0000
.
BEDD2000  DISPLAY   *W5
          GOTO      BEDD0000
.
BEDD9999  RETURN
.**********************************************************************
.*                         OPRR0000                                   *
.*               Update Operation Statistics                          *
.**********************************************************************
.
OPRR0000  PACK      KEY16,XADMN,SP20                                  *C-240107
          CALL      RSPTECO2
OPRR1000  CALL      RKPTECO2
          BRANCH    OVRCD,OPRR9999
.
          MATCH     XADMN,PTEOADMN
          GOTO      OPRR9999 IF NOT EQUAL
.
          UNPACK    PTEODATE,XDATE
          MATCH     FADMDTE,XDATE
          GOTO      OPRR1000 IF LESS
.
          MATCH     TTODATE,XDATE
          GOTO      OPRR1000 IF NOT LESS
.
          CALL      ADOC0000           * get the attending doctor for date xdate
.
          MOVE      ANSA,PTDSTYPE               * attending doctor type
          CALL      ADOP0000                  * process the icd9 operation data 
.
          MOVE      ADOCTR,PTDSCODE             * referring doctor code
          MATCH     SP6,PTDSCODE
          CALL      UDOC0000 IF EQUAL         * unknown doctor
          MOVE      ANSR,PTDSTYPE               * referring doctor type
.
          CALL      ADOP0000                  * process the icd9 operation data 
          GOTO      OPRR1000
.
OPRR9999  RETURN
+
.**********************************************************************
.*                         ADOP0000                                   *
.*                Process the icd9 operation data                     *
.**********************************************************************
.
ADOP0000  PACK      KEY13,SAVYEAR,SAVPERD,PTDSTYPE,PTDSCODE
          CALL      RLDSTA1
          BRANCH    OVRCD,ADOP1000,ADOP2000
          ADD       ONE,PTDSOPRS
          CALL      UPDSTA1
          CALL      UUDSTA1
          GOTO      ADOP9999
.
ADOP1000  CALL      CLDS0000
          GOTO      ADOP0000
.
ADOP2000  DISPLAY   *W5
          GOTO      ADOP0000
.
ADOP9999  RETURN
+
.**********************************************************************
.*                         MBSS0000                                   *
.*                  Update MBS statistics                             *
.**********************************************************************
.
MBSS0000  PACK      KEY11,XADMN,SP70
          CALL      RDSMMBS1
MBSS1000  CALL      RDKMMBS1
          BRANCH    OVRCD,MBSS9999
.
          MATCH     XADMN,MMADMN              * same admission number ?
          GOTO      MBSS9999 IF NOT EQUAL
.
          UNPACK    MMDATE,XCENT,XYEAR,XMON,XDAY
          PACK      XDATE,XCENT,XYEAR,XMON,XDAY
.
          MATCH     FADMDTE,XDATE
          GOTO      MBSS1000 IF LESS
.
          MATCH     TTODATE,XDATE
          GOTO      MBSS1000 IF NOT LESS
.
          CALL      ADOC0000           * get the attending doctor for date xdate
.
          MOVE      ANSA,PTDSTYPE               * attending doctor type
          CALL      ADMB0000                  * process the mbs operation data 
.
          MOVE      ADOCTR,PTDSCODE             * referring doctor code
          MATCH     SP6,PTDSCODE
          CALL      UDOC0000 IF EQUAL         * unknown doctor
          MOVE      ANSR,PTDSTYPE               * referring doctor type
.
          CALL      ADMB0000                  * process the mbs operation data 
          GOTO      MBSS1000
.
MBSS9999  RETURN
+
.**********************************************************************
.*                         ADMB0000                                   *
.*             Process the mbs operation data                         *
.**********************************************************************
.
ADMB0000  PACK      KEY13,SAVYEAR,SAVPERD,PTDSTYPE,PTDSCODE
          CALL      RLDSTA1
          BRANCH    OVRCD,ADMB1000,ADMB2000
          ADD       ONE,PTDSMBSC
          CALL      UPDSTA1
          CALL      UUDSTA1
          GOTO      ADMB9999
.
ADMB1000  CALL      CLDS0000
          GOTO      ADMB0000
.
ADMB2000  DISPLAY   *W5
          GOTO      ADMB0000
.
ADMB9999  RETURN
+
.**********************************************************************
.*                         CLDS0000                                   *
.*        Create an empty record for the Doctors statistics           *
.**********************************************************************
.
CLDS0000  MOVE      ZERO,PTDSBADM             * budget for admissions
          MOVE      ZERO,PTDSBBED             * budget for bed days
          MOVE      ZERO,PTDSBOPR             * budget for operations
          MOVE      ZERO,PTDSBMBS             * budget for mbs codes
.
CLDS1000  MOVE      ZERO,PTDSADMS             * Admission stats
          MOVE      ZERO,PTDSBEDD             * Bed day stats
          MOVE      ZERO,PTDSOPRS             * Operation stats
          MOVE      ZERO,PTDSMBSC             * Mbs stats
.
CLDS2000  CALL      WRDSTA1
          RETURN
+
.**********************************************************************
.*                         ADOC0000                                   *
.*        Get the attending doctor at a given date                    *
.**********************************************************************
.
ADOC0000  MOVE      SP6,PTDSCODE
.
.         Loop through the bed transfer file
.
          PACK      KEY30,XADMN,SP20
          CALL      RDSTRAN2
ADOC1000  CALL      RDKTRAN2
          BRANCH    OVRCD,ADOC2000
.
          MATCH     TADMN,XADMN               * same admission number ?
          GOTO      ADOC2000 IF NOT EQUAL
.
          MATCH     TDATE,XDATE
          GOTO      ADOC2000 IF LESS
.
          MOVE      TADOCT,PTDSCODE
          GOTO      ADOC1000
.
ADOC2000  MATCH     SP6,PTDSCODE
          CALL      UDOC0000 IF EQUAL          * unknown doctor
          RETURN
+
.**********************************************************************
.*                         UDOC0000                                   *
.*              Code for unknown doctor                               *
.**********************************************************************
.
UDOC0000  MOVE      "NODOCT",PTDSCODE
          RETURN
+
.**********************************************************************
.*                         UMST0000            Called by : PTMP3000   *
.*              Generate data for PATMSTFD file                       *
.**********************************************************************
.
UMST0000  DISPLAY   *P1:24,"patmstaf";
          MOVE      ZERO,PUBFLG               * public/private flag
          MOVE      SP8,STDATE                * save transfer date
          MOVE      " ",STMOVE                * save transfer movement
          MOVE      SP3,STATYPE               * save transfer admission type
          MOVE      SP6,STADOCT               * save transfer doctor
          MOVE      SP6,STWARD                * save transfer ward
          MOVE      SP6,STDEPT                * save transfer dept
.
          CALL      TRMT0000                  * read through bed transfer file
.
UMST9999  RETURN
+
.**********************************************************************
.*                         TRMT0000            Called by : UMST0000   *
.*        Read through the PATTRNFD file  and update for patmsdaf     *
.**********************************************************************
.
TRMT0000  MOVE      ZERO,DAYCASE              * daycase flag
          MOVE      ZERO,LEVFLG               * on leave flag
          MOVE      ZERO,BRDFLG               * boarder flag 
          MOVE      ZERO,CHGWRD               * change ward flag
          MOVE      ZERO,SEPLEVS              * leave days for seperation days
.                                               calculation
          MOVE      FALSE,FLAGSEPS             * initialise flag for calculating
.                                               seperation bed days
          BRANCH    ASTATFLG,TRMT0500         * patient discharged ?
.
          MATCH     ADATE,DDATE               * patient a daycase ?
          GOTO      TRMT0500 IF NOT EQUAL     * no 
.
          MATCH     FADMDTE,ADATE             * patient in period ?
          GOTO      TRMT0500 IF LESS          * no patient isn't
          MOVE      ONE,DAYCASE               * patient is a daycase
.
TRMT0500  MOVE      SP3,MSDCLS                * doctor class
          MOVE      SP3,SAVCLS                * save ward class
          MOVE      SP1,STMOVE
          MOVE      SP3,STWARD
          MOVE      SP3,STDEPT
          MOVE      CSTPER,MSDATE             * move current period to date
          MOVE      CSTPER,PTMDDATE           * move current period to date
          MOVE      SP10,SAVDATE
          MOVE      SP10,OTDATE
          MOVE      SP8,SAVLDATE
.
.         Loop through the bed transfer file
.
          PACK      KEY30,XADMN,SP20
          CALL      RDSTRAN2
TRMT1000  CALL      RDKTRAN2
          BRANCH    OVRCD,TRMT8000
.
          MATCH     XADMN,TADMN               * same admission number ?
          GOTO      TRMT8000 IF NOT EQUAL
.
          MOVE      TMOVE,STMOVE
          RESET     TDATE
          REP       " 0",TDATE
.
          MATCH     ANSA,TMOVE                * current admission record
          GOTO      TRMT1500 IF EQUAL
          MATCH     ANSR,TMOVE                * returned from leave record
          GOTO      TRMT2000 IF EQUAL
          MATCH     ANSL,TMOVE                * onleave record
          GOTO      TRMT5000 IF EQUAL
          MATCH     ANSD,TMOVE                * if not discharge record it must 
          GOTO      TRMT6000 IF NOT EQUAL     * be a change record
.
.         Discharge record
.
          MOVE      TDATE,OTDATE
          MATCH     FADMDTE,TDATE
          GOTO      TRMT9999 IF LESS
          MATCH     TTODATE,TDATE            * ttodate = end date of period + 1
          GOTO      TRMT1200 IF LESS
          MOVE      TTODATE,TDATE
          MOVE      FALSE,FLAGSEPS           * reset seperation bed days flag
          GOTO      TRMT9100
.
TRMT1200  CALL      TDIS0000                 * check discharge record
          MOVE      TRUE,FLAGSEPS            * set seperation bed days flag
          GOTO      TRMT9100
.
.         Admission record
.
TRMT1500  CALL      TADM0000                 * write/update the admission record
          GOTO      TRMT2500
.
.         Return record
.
TRMT2000  MOVE      ONE,LEVFLG                * patient is on leave
.
TRMT2500  MATCH     TTODATE,TDATE            * ttodate = end date of period + 1
          GOTO      TRMT4500 IF NOT LESS
.
          MOVE      TDATE,SAVDATE
. 
          MATCH     FADMDTE,TDATE
          GOTO      TRMT3000 IF NOT LESS
.
          MOVE      FADMDTE,STDATE
.
          COMPARE   ZERO,LEVFLG               * patient on leave ?
          GOTO      TRMT7500 IF EQUAL
.
          MOVE      ZERO,LEVFLG               * patient not on leave
          MOVE      TDATE,SAVDATE
.
          MATCH     TWARD,STWARD              * see if the ward has changed
          GOTO      TRMT2600 IF EQUAL
.
          MOVE      ZERO,SEPLEVS
          MOVE      TDATE,SAVLDATE
.
          GOTO      TRMT7500
.
TRMT2600  MOVE      ZERO,NBRDAYS
          DAYSEP    SAVLDATE,TDATE,NBRDAYS
          ADD       NBRDAYS,SEPLEVS
.
          GOTO      TRMT7500
.
TRMT3000  COMPARE   ZERO,LEVFLG               * patient on leave ?
          GOTO      TRMT4000 IF EQUAL
.
          MATCH     TWARD,STWARD              * match this ward to previous 
          GOTO      TRMT3100 IF EQUAL           ward
.
          MOVE      ONE,CHGWRD                * ward changed
          MOVE      TDATE,OTDATE
.
TRMT3100  MATCH     FADMDTE,STDATE
          GOTO      TRMT3500 IF LESS
.
.         Calculate the number of bed days while patient was on leave
.
          MOVE      STDATE,WDATE2
          MOVE      TDATE,WDATE1
          MOVE      TRUE,FLAGSEPS
          CALL      UPMT0000                  * update the patmstfd file
          MOVE      ZERO,LEVFLG
          GOTO      TRMT4000
.
.         Patient started on leave in the previous period
.
TRMT3500  MOVE      FADMDTE,WDATE2
          MOVE      TDATE,WDATE1
          MOVE      TRUE,FLAGSEPS
          CALL      UPMT0000                  * update the patmstfd file
          CALL      UPMD0000                  * update the patmsdfd file
          MOVE      ZERO,LEVFLG
          MOVE      ZERO,NBRDAYS
          DAYSEP    STDATE,FADMDTE,NBRDAYS
          ADD       NBRDAYS,SEPLEVS
.
TRMT4000  MOVE      TDATE,STDATE
          GOTO      TRMT7500
.
TRMT4500  COMPARE   ZERO,LEVFLG               * patient on leave ?
          GOTO      TRMT9999 IF EQUAL
.
.         Get bed days of on leave patient until end of the period
.
          MOVE      FALSE,FLAGSEPS
          MOVE      TTODATE,WDATE1            * ttodate = end date of period + 1
          MOVE      STDATE,WDATE2
          CALL      UPMT0000                  * update the patmstfd file
          CALL      UPMD0000                  * update the patmsdfd file
          MOVE      ZERO,LEVFLG               * patient not on leave
          GOTO      TRMT9999
.
.         On leave record
.
TRMT5000  MATCH     FADMDTE,TDATE             * within period ?
          GOTO      TRMT5100 IF NOT LESS
.
          MOVE      TDATE,SAVLDATE
          MOVE      TDATE,STDATE
.
          GOTO      TRMT7500
.
TRMT5100  MATCH     TTODATE,TDATE
          GOTO      TRMT5500 IF LESS
.
          MOVE      TTODATE,WDATE1
          MOVE      STDATE,WDATE2
          MOVE      FALSE,FLAGSEPS            * reset seperation bed days flag
          CALL      UPMT0000                  * update the patmstfd file
          CALL      UPMD0000                  * update the patmsdfd file
          GOTO      TRMT9999
.
TRMT5500  MOVE      TDATE,WDATE1
          MOVE      STDATE,WDATE2
          MOVE      FALSE,FLAGSEPS            * reset seperation bed days flag
          CALL      UPMT0000                  * update the patmstfd file
          CALL      UPMD0000                  * update the patmsdfd file
          MOVE      TDATE,STDATE
          GOTO      TRMT7500
.
.         Change record
.
TRMT6000  MATCH     TWARD,STWARD
          GOTO      TRMT1000 IF EQUAL
.
          MATCH     FADMDTE,TDATE             * within period ?
          GOTO      TRMT6100 IF NOT LESS
.
          MOVE      ZERO,SEPLEVS
          MOVE      TDATE,SAVDATE
.
          GOTO      TRMT7500
.
TRMT6100  MATCH     TTODATE,TDATE
          GOTO      TRMT7000 IF LESS
.
          MOVE      TTODATE,WDATE1
          MOVE      STDATE,WDATE2
          MOVE      FALSE,FLAGSEPS            * reset seperation bed days flag
          CALL      UPMT0000                  * update the patmstfd file
          CALL      UPMD0000                  * update the patmsdfd file
          GOTO      TRMT9999
.
TRMT7000
.         Ward is changed during the month
.
          MOVE      ONE,CHGWRD                * ward changed
.
          MOVE      TDATE,WDATE1
          MOVE      STDATE,WDATE2
          MOVE      TDATE,OTDATE
          MOVE      TRUE,FLAGSEPS             * set seperation bed days flag
          CALL      UPMT0000                  * update the patmstfd file
          CALL      UPMD0000                  * update the patmsdfd file
.
          MOVE      TDATE,STDATE
.
.         Save the ward and the ward class
.
TRMT7500  MOVE      SP3,WCLASS                * ward class
          PACK      KEY6,TWARD,SP3
          CALL      RDWARD1
.
          MOVE      TWARD,STWARD            
          MOVE      WCLASS,SAVCLS             
          MOVE      TDEPT,STDEPT            
          GOTO      TRMT1000                  * read the transfer file
.
.         Check the last record
.
TRMT8000  MOVE      STWARD,TWARD              * ward
          MOVE      SAVCLS,WCLASS             * ward class
          MOVE      STDEPT,TDEPT              * dept
.
          MATCH     ANSL,STMOVE               * on leave record ?
          GOTO      TRMT9000 IF NOT EQUAL
.
          MOVE      ONE,LEVFLG                * patient is on leave
          MOVE      TTODATE,WDATE1
          MOVE      STDATE,WDATE2
          MOVE      FALSE,FLAGSEPS            * reset seperation bed days flag
          CALL      UPMT0000                  * update the patmstfd file
          CALL      UPMD0000                  * update the patmsdfd file
          MOVE      ZERO,LEVFLG               * patient not on leave
          GOTO      TRMT9999
.
TRMT9000  MATCH     SP1,STMOVE
          GOTO      TRMT9999 IF EQUAL
.
          MOVE      FALSE,FLAGSEPS            * reset seperation bed days flag
.
.         Last record
.
          MATCH     ANSD,STMOVE               * discharge record ?
          GOTO      TRMT9500 IF NOT EQUAL
.
.         Patient is discharged
.
TRMT9100  MOVE      TDATE,WDATE1
          GOTO      TRMT9700
.
TRMT9500  MOVE      TTODATE,WDATE1
.
TRMT9700  MOVE      STDATE,WDATE2
          CALL      UPMT0000                  * update the patmstfd file
          CALL      UPMD0000                  * update the patmsdfd file
.
TRMT9999  RETURN
+
.**********************************************************************
.*                         UPMT0000                                   *
.*                   Update the PATMSTFD file                         *
.**********************************************************************
.
UPMT0000  PACK      KEY15,STWARD,MSDCLS,SAVCLS,MSDATE
          CALL      RDMSTS1
          COMPARE   ZERO,OVRCD
          GOTO      UPMT0500 IF EQUAL
.
          CALL      CLMT0000                  * clear variables
          CALL      WRMSTS1                   * write to patmstaf
          CALL      RDMSTS1                   * read patmstaf
.
UPMT0500  MOVE      ZERO,NBRDAYS
          MOVE      ZERO,DSHFLG               * patient not discharged
          COMPARE   ZERO,DAYCASE              * patient daycase ?
          GOTO      UPMT1000 IF EQUAL
.
.         A daycase only have one bedday. If this is not the last trans 
.         record, no need to update the beddays only update the 
.         transaction of ward if necessary
.
          CMATCH    ANSD,STMOVE               * patient discharged ?
          GOTO      UPMT3500 IF NOT EQUAL
.
          MOVE      ONE,DSHFLG                * patient is discharged 
          MOVE      ONE,NBRDAYS               * one bed day for daycase patients
          GOTO      UPMT2000
.
.         Calculate the number of beddays for non daycase patients
.
UPMT1000  MATCH     TTODATE,WDATE1
          GOTO      UPMT1500 IF LESS
.
.         Use the end of the period date plus one as the WDATE1
.
          MOVE      TTODATE,WDATE1            * ttodate = end date of period + 1
.
UPMT1500  DAYSEP    WDATE2,WDATE1,NBRDAYS
          BRANCH    LEVFLG,UPMT3000           * patient on leave ?
.
UPMT2000  BRANCH    BRDFLG,UPMT2500           * patient a boarder ? 
.
.         Beddays for patient (not boarders)
.
          ADD       NBRDAYS,MSNBDAY
          GOTO      UPMT3500
.
.         Beddays for boarder
.
UPMT2500  ADD       NBRDAYS,MSBBDAY
          GOTO      UPMT3500
.
.         Beddays for patient on leave
.
UPMT3000  ADD       NBRDAYS,MSLVBD
.
.------ add leave days to the seperation leave days count if appropriate -----
.
          IF        FLAGSEPS = 1              
            ADD       NBRDAYS,SEPLEVS           
          ENDIF                                 
.
UPMT3500  COMPARE   ZERO,CHGWRD               * ward change during period ?
          GOTO      UPMT4000 IF EQUAL
.
          ADD       ONE,MSTROUT               * transfer out
.
.         Check if the original transaction date is within the period
.
UPMT4000  MATCH     FADMDTE,OTDATE             * Ignore transaction not within
          GOTO      UPMT4300 IF LESS           * the period
.
          MATCH     TTODATE,OTDATE
          GOTO      UPMT4300 IF NOT LESS
.
          CMATCH    ANSR,STMOVE                * Ignore the onleave bed days
          GOTO      UPMT4300 IF EQUAL
.
.------ skip if we don't want to calculate seperation bed days at this ------
.------ moment ------
.
          IF        FLAGSEPS <> 1              
            GOTO      UPMT4300                   
          ENDIF                                  
.
.         Calculate separation bed days
.
          CMATCH    ANSD,STMOVE              * patient discharged in periods ?
          GOTO      UPMT4100 IF NOT EQUAL    * No.
.
          COMPARE   ZERO,DAYCASE             * Day case patient ?
          GOTO      UPMT4100 IF EQUAL        * No.
.
          MOVE      ONE,NBRDAYS
          GOTO      UPMT4150
.
.         Calculte the separation be days
.
UPMT4100  MOVE      ZERO,NBRDAYS
          DAYSEP    SAVDATE,OTDATE,NBRDAYS
          MOVE      OTDATE,SAVDATE
.
UPMT4150  ADD       NBRDAYS,MSSEPBD         * Separation bed days
          SUB       SEPLEVS,MSSEPBD
          MOVE      ZERO,SEPLEVS
          MOVE      FALSE,FLAGSEPS
.
UPMT4300  CALL      UPMSTS1
.
.         Check if the ward has changed
.
UPMT4500  COMPARE   ZERO,CHGWRD
          GOTO      UPMT9999 IF EQUAL
.
.         Create/update the file using the current ward
.
UPMT5000  MOVE      SP3,WCLASS                * ward class
          PACK      KEY6,TWARD,SP3
          CALL      RDWARD1
.
          PACK      KEY15,TWARD,MSDCLS,WCLASS,MSDATE
          CALL      RDMSTS1
          COMPARE   ZERO,OVRCD
          GOTO      UPMT5500 IF EQUAL
.
          CALL      CLMT0000                  * clear variables
          CALL      WRMSTS1                   * write to patmstsf
          CALL      RDMSTS1                   * read patmstsf
.
UPMT5500  ADD       ONE,MSTRIN                * transfer in
.
          CALL      UPMSTS1
          MOVE      ZERO,CHGWRD               * ward didn't change
          MOVE      ZERO,DSHFLG               * patient not discharged
.
UPMT9999  RETURN
+
.**********************************************************************
.*                         UPMD0000              Called by : TRMT0000 *
.*                   Update the PATMSDFD file                         *
.**********************************************************************
.
UPMD0000  COMPARE   ONE,PTCNMSDF
          GOTO      UPMD9999 IF NOT EQUAL   * not using MSD file
. 
          PACK      KEY12,STDEPT,STWARD,PTMDDATE
          CALL      RDPTMSD1
          COMPARE   ZERO,OVRCD
          GOTO      UPMD0500 IF EQUAL       * valid record
.
          CALL      CLMD0000                * clear variables
          CALL      WRPTMSD1                * write to patmsdaf
          CALL      RDPTMSD1                * read patmsdaf (get back dept/date)
.
UPMD0500  MOVE      ZERO,NBRDAYS
          MOVE      ZERO,DSHFLG             * patient not discharged
          COMPARE   ZERO,DAYCASE            * patient daycase ?
          GOTO      UPMD1000 IF EQUAL
.
.         A daycase only have one bedday. If this is not the last trans 
.         record, no need to update the beddays only update the 
.         transaction of ward if necessary
.
          CMATCH    ANSD,STMOVE               * patient discharged ?
          GOTO      UPMD3500 IF NOT EQUAL
.
          MOVE      ONE,DSHFLG                * patient is discharged 
          MOVE      ONE,NBRDAYS               * one bed day for daycase patients
          GOTO      UPMD2000
.
.         Calculate the number of beddays for non daycase patients
.
UPMD1000  MATCH     TTODATE,WDATE1
          GOTO      UPMD1500 IF LESS
.
.         Use the end of the period date plus one as the WDATE1
.
          MOVE      TTODATE,WDATE1            * ttodate = end date of period + 1
.
UPMD1500  DAYSEP    WDATE2,WDATE1,NBRDAYS
.
          BRANCH    LEVFLG,UPMD3000           * patient on leave ?
.
UPMD2000  BRANCH    BRDFLG,UPMD2500           * patient a boarder ? 
.
.         Beddays for patient (not boarders)
.
          ADD       NBRDAYS,PTMDNBDY
          GOTO      UPMD3500
.
.         Beddays for boarder
.
UPMD2500  ADD       NBRDAYS,PTMDBBDY
          GOTO      UPMD3500
.
.         Beddays for patient on leave
.
UPMD3000  ADD       NBRDAYS,PTMDLVBD
.
.------ add leave days to the seperation leave days count if appropriate ------
.
          IF        FLAGSEPS = 1              
            ADD       NBRDAYS,SEPLEVS           
          ENDIF                                 
.
UPMD3500  COMPARE   ZERO,CHGWRD               * ward change during period ?
          GOTO      UPMD4000 IF EQUAL
.
          ADD       ONE,PTMDTOUT              * transfer out
.
.         Check if the original transaction date is within the period
.
UPMD4000  MATCH     FADMDTE,OTDATE             * Ignore transaction not within
          GOTO      UPMD4300 IF LESS           * the period
.
          MATCH     TTODATE,OTDATE
          GOTO      UPMD4300 IF NOT LESS
.
          CMATCH    ANSR,STMOVE                * Ignore the onleave bed days
          GOTO      UPMD4300 IF EQUAL
.
.------ skip if we don't want to calculate seperation bed days at this ------
.------ moment ------
.
          IF        FLAGSEPS <> 1              
            GOTO      UPMD4300                   
          ENDIF                                  
.
.         Calculate separation bed days
.
          CMATCH    ANSD,STMOVE              * patient discharged in periods ?
          GOTO      UPMD4100 IF NOT EQUAL    * No.
.
          COMPARE   ZERO,DAYCASE             * Day case patient ?
          GOTO      UPMD4100 IF EQUAL        * No.
.
          MOVE      ONE,NBRDAYS
          GOTO      UPMD4150
.
.         Calculte the separation be days
.
UPMD4100  MOVE      ZERO,NBRDAYS
          DAYSEP    SAVDATE,OTDATE,NBRDAYS
          MOVE      OTDATE,SAVDATE
.
UPMD4150  ADD       NBRDAYS,PTMDSBDY        * Separation bed days
          SUB       SEPLEVS,MSSEPBD
          MOVE      ZERO,SEPLEVS
          MOVE      FALSE,FLAGSEPS
.
UPMD4300  CALL      UPPTMSD1
.
.         Check if the dept has changed
.
UPMD4500  COMPARE   ZERO,CHGWRD 
          GOTO      UPMD9999 IF EQUAL
.
.         Create/update the file using the current dept
.
UPMD5000  PACK      KEY12,TDEPT,TWARD,PTMDDATE
          CALL      RDPTMSD1
          COMPARE   ZERO,OVRCD
          GOTO      UPMD5500 IF EQUAL
.
          CALL      CLMD0000                * clear variables
          CALL      WRPTMSD1                * write to patmsdaf
          CALL      RDPTMSD1                * read patmsdaf (get back dept/date)
.
UPMD5500  ADD       ONE,PTMDTRIN            * transfer in
.
          CALL      UPPTMSD1
.
UPMD9999  RETURN
.**********************************************************************
.*                         TDIS0000                                   *
.*        Check the discharge record for patmstaf                     * 
.**********************************************************************
.
TDIS0000  MOVE      SP3,WCLASS                * ward class
          PACK      KEY6,TWARD,SP3
          CALL      RDWARD1
.
          MOVE      TWARD,MSWARD
          MOVE      WCLASS,MSWCLS
          MOVE      TWARD,STWARD
          MOVE      TDEPT,STDEPT
          MOVE      WCLASS,SAVCLS
.
          PACK      KEY15,MSWARD,MSDCLS,MSWCLS,MSDATE
          CALL      RDMSTS1
          COMPARE   ZERO,OVRCD
          GOTO      TDIS1000 IF EQUAL
.
.         A new record
.
          CALL      CLMT0000                  * clear variables
          CALL      WRMSTS1                   * write to patmstsf
          CALL      RDMSTS1                   * read patmstsf
.
TDIS1000  ADD       ONE,MSDSCH                * discharge
.
.         Check if patient is dead or not
.
          BRANCH    PATDEAD,TDIS2000          * patient dead ?
.
          ADD       ONE,MSDEATH               * dead
.
TDIS2000  CALL      UPMSTS1
          MOVE      ZERO,CHGWRD               * ward didn't change
          MOVE      ZERO,DSHFLG               * patient not discharged
          CALL      DDIS0000
.
TDIS9999  RETURN
.**********************************************************************
.*                         DDIS0000             Called by : TDIS0000  *
.*        Check the discharge record for patmsdaf                     * 
.**********************************************************************
.
DDIS0000  COMPARE   ONE,PTCNMSDF
          GOTO      DDIS9999 IF NOT EQUAL   * not using MSD file 

          PACK      KEY12,STDEPT,MSWARD,MSDATE
          CALL      RDPTMSD1
          COMPARE   ZERO,OVRCD
          GOTO      DDIS1000 IF EQUAL       * record exists
.
.         A new record
.
          CALL      CLMD0000                * clear variables
          CALL      WRPTMSD1                * write to patmsdaf
          CALL      RDPTMSD1                * read patmsdaf
.
DDIS1000  ADD       ONE,PTMDDSCH            * discharge
.
.         Check if patient is dead or not
.
          BRANCH    PATDEAD,DDIS2000        * patient dead ?
.
          ADD       ONE,PTMDDEAD            * dead
.
DDIS2000  CALL      UPPTMSD1
.
DDIS9999  RETURN
.**********************************************************************
.*                         TADM0000                                   *
.*        Check the first transfer record for patmstaf                *
.**********************************************************************
.
TADM0000  MOVE      TDATE,OTDATE
          MATCH     FADMDTE,ADATE             * admitted within period ?
          GOTO      TADM9999 IF LESS
.
          MATCH     ADATE,TTODATE
          GOTO      TADM9999 IF LESS
.
          MOVE      SP3,WCLASS                * ward class
          PACK      KEY6,TWARD,SP3
          CALL      RDWARD1
.
          MOVE      TWARD,MSWARD
          MOVE      WCLASS,MSWCLS
          MOVE      TWARD,STWARD
          MOVE      TDEPT,STDEPT
          MOVE      TDATE,SAVDATE
          MOVE      WCLASS,SAVCLS
.
          PACK      KEY15,MSWARD,MSDCLS,MSWCLS,MSDATE
          CALL      RDMSTS1
          COMPARE   ZERO,OVRCD
          GOTO      TADM1000 IF EQUAL
.
.         A new record
.
          CALL      CLMT0000                  * clear variables
          CALL      WRMSTS1                   * write to patmstsf
          CALL      RDMSTS1                   * read patmstsf
.
.         Check if patient type is public or private
.
TADM1000  MOVE      ONE,PUBFLG                * public/private flag
          MOVE      ZERO,FORM1
          MOVE      ZERO,BRDFLG               * boarder flag
.
          MATCH     SP3,TATYPE
          GOTO      TADM3000 IF EQUAL
.
          PACK      KEY5,CODEA,TATYPE
          CALL      RDCODE1
          BRANCH    OVRCD,TADM3000
.
          MATCH     ANSB,TCINDC3
          IF        @EQUAL
            MOVE      ONE,BRDFLG
          ENDIF
          MOVE      TCINDC1,FORM1
          BRANCH    FORM1,TADM3000,TADM2000
          GOTO      TADM3000
.
TADM2000  MOVE      ZERO,PUBFLG               * patient is private
.
.         Check if boarder or not
.
TADM3000  
...       MOVE      ZERO,BRDFLG
...       MATCH     SP3,ACLSS
...       GOTO      TADM5000 IF EQUAL
.
...       MOVE      ZERO,FORM1
...       PACK      KEY5,CODEP,ACLSS
...       CALL      RDCODE1
...       BRANCH    OVRCD,TADM5000
.
...       MOVE      TCINDC1,FORM1
          COMPARE   ONE,BRDFLG                * patient a boarder ?
          GOTO      TADM5000 IF NOT EQUAL     * no
.
...       MOVE      ONE,BRDFLG                * patient is a boarder
.
          BRANCH    PUBFLG,TADM4000           * public/private type
          ADD       ONE,MSBPRAD               * private boarder
          GOTO      TADM7000
.
TADM4000  ADD       ONE,MSBPBAD               * public boarder
          GOTO      TADM7000
.
TADM5000  BRANCH    PUBFLG,TADM6000           * public/private type
          ADD       ONE,MSNPRAD               * private admission
          GOTO      TADM7000
.
TADM6000  ADD       ONE,MSNPBAD               * public admission
.
.         Check if the patient was admitted from A & E
.
TADM7000  PACK      KEY5,CODES,ASRCE
          CALL      RDCODE1
          BRANCH    OVRCD,TADM8000
.
          CMATCH    ANSA,TCINDC1              * from A & E ?
          GOTO      TADM8000 IF NOT EQUAL     * no
.
          ADD       ONE,MSAEADM               * a & e patient
.
.         Check if the patient admitted in the selected period
.
TADM8000  CALL      UPMSTS1
          MOVE      ZERO,MSNPRAD              * private admission
          MOVE      ZERO,MSNPBAD              * public admission
.
TADM9999  RETURN
.**********************************************************************
.*                         DADM0000            Called by : TADM0000   *
.*        Check the first transfer record for patmsdaf                *
.**********************************************************************
.
DADM0000  COMPARE   ONE,PTCNMSDF
          GOTO      DADM9999 IF NOT EQUAL   * not using MSD file
. 
          MOVE      TDATE,OTDATE
          MATCH     FADMDTE,ADATE           * admitted within period ?
          GOTO      DADM9999 IF LESS
.
          MATCH     ADATE,TTODATE
          GOTO      DADM9999 IF LESS
.
          MOVE      STDEPT,PTMDDEPT        * file deparment
.
          PACK      KEY12,STDEPT,MSWARD,MSDATE  
          CALL      RDPTMSD1
          COMPARE   ZERO,OVRCD
          GOTO      DADM1000 IF EQUAL       * record exists
.
.         A new record
.
          CALL      CLMD0000                * clear variables
          CALL      WRPTMSD1                * write to patmsdaf
          CALL      RDPTMSD1                * read patmsdaf
.
.         Check if patient type is public or private
.
DADM1000  MOVE      ONE,PUBFLG                * public/private flag
          MOVE      ZERO,FORM1
          MOVE      ZERO,BRDFLG               * boarder flag
.
          MATCH     SP3,TATYPE
          GOTO      DADM3000 IF EQUAL
.
          PACK      KEY5,CODEA,TATYPE
          CALL      RDCODE1
          BRANCH    OVRCD,DADM3000
.
          MATCH     ANSB,TCINDC3
          IF        @EQUAL
            MOVE      ONE,BRDFLG
          ENDIF
.
          MOVE      TCINDC1,FORM1
          BRANCH    FORM1,DADM3000,DADM2000
          GOTO      DADM3000
.
DADM2000  MOVE      ZERO,PUBFLG               * patient is private
.
.         Check if boarder or not
.
DADM3000  
...       MOVE      ZERO,BRDFLG
...       MATCH     SP3,ACLSS
...       GOTO      DADM5000 IF EQUAL
.
...       MOVE      ZERO,FORM1
...       PACK      KEY5,CODEP,ACLSS
...       CALL      RDCODE1
...       BRANCH    OVRCD,DADM5000
.
...       MOVE      TCINDC1,FORM1
          COMPARE   ONE,BRDFLG              * patient a boarder ?
          GOTO      DADM5000 IF NOT EQUAL   * no
.
...       MOVE      ONE,BRDFLG              * patient is a boarder
.
          BRANCH    PUBFLG,DADM4000         * public/private type
          ADD       ONE,PTMDBPRA            * private boarder
          GOTO      DADM7000
.
DADM4000  ADD       ONE,PTMDBPBA            * public boarder
          GOTO      DADM7000
.
DADM5000  BRANCH    PUBFLG,DADM6000         * public/private type
          ADD       ONE,PTMDPRAD            * private admission
          GOTO      DADM7000
.
DADM6000  ADD       ONE,PTMDPBAD            * public admission
.
.         Check if the patient was admitted from A & E
.
DADM7000  PACK      KEY5,CODES,ASRCE
          CALL      RDCODE1
          BRANCH    OVRCD,DADM8000
.
          CMATCH    ANSA,TCINDC1            * from A & E ?
          GOTO      DADM8000 IF NOT EQUAL   * no
.
          ADD       ONE,PTMDAEAD            * a & e patient
.
.         Check if the patient admitted in the selected period
.
DADM8000  CALL      UPPTMSD1
          MOVE      ZERO,PTMDPRAD           * private admission
          MOVE      ZERO,PTMDPBAD           * public admission
.
DADM9999  RETURN
+
.**********************************************************************
.*                         CLMT0000                                   *
.*                   Clear the PATMSTFD variables                     *
.**********************************************************************
.
CLMT0000  MOVE      SP3,MSWARD
          MOVE      SP3,MSDCLS
          MOVE      SP3,MSWCLS
          MOVE      SP8,MSDATE
.
          MOVE      ZERO,MSDYMTH
          MOVE      ZERO,MSNPRAD
          MOVE      ZERO,MSNPBAD
          MOVE      ZERO,MSTRIN 
          MOVE      ZERO,MSTROUT
          MOVE      ZERO,MSDSCH 
          MOVE      ZERO,MSNBDAY
          MOVE      ZERO,MSBBDAY
          MOVE      ZERO,MSDEATH
          MOVE      ZERO,MSBPRAD
          MOVE      ZERO,MSBPBAD
          MOVE      ZERO,MSLVBD 
          MOVE      ZERO,MSAEADM
          MOVE      ZERO,MSOBSTH
          MOVE      ZERO,MSOUTTH
          MOVE      ZERO,MSNOPAT
          MOVE      ZERO,MSSEPBD
          MOVE      ZERO,MSACBDAY
          PACK      MSSPARE,SP20,SP20,SP10
CLMT9999  RETURN
.**********************************************************************
.*                         CLMD0000                                   *
.*                   Clear the PATMSDFD variables                     *
.**********************************************************************
.
CLMD0000  MOVE      STDEPT,PTMDDEPT         * department
          MOVE      CSTPER,PTMDDATE         * date
          MOVE      ZERO,PTMDPRAD 
          MOVE      ZERO,PTMDPBAD
          MOVE      ZERO,PTMDTRIN 
          MOVE      ZERO,PTMDTOUT
          MOVE      ZERO,PTMDDSCH 
          MOVE      ZERO,PTMDNBDY
          MOVE      ZERO,PTMDBBDY
          MOVE      ZERO,PTMDDEAD
          MOVE      ZERO,PTMDBPRA
          MOVE      ZERO,PTMDBPBA
          MOVE      ZERO,PTMDLVBD 
          MOVE      ZERO,PTMDAEAD
          MOVE      ZERO,PTMDOBTH
          MOVE      ZERO,PTMDOUTT
          MOVE      SELDAT,PTMDDAYS         * number of days in month
          MOVE      ZERO,PTMDPATS
          MOVE      ZERO,PTMDSBDY
          MOVE      STWARD,PTMDWARD
          PACK      PTMDSPAR,SP20,SP20,SP10
CLMD9999  RETURN
+
.**********************************************************************
.*                         UWSS0000                                   *
.*              Generate data for PATWSSFD file                       *
.**********************************************************************
.
UWSS0000  DISPLAY   *P1:24,"patwssaf";
.
          MOVE      SP3,SSWWARD               * ward
          MOVE      SP3,SSWSPEC               * primary doctor type 
          MOVE      ZERO,SSWADMN              * number of admissions
          MOVE      ZERO,SSWTRIN              * number of transfers in
          MOVE      ZERO,SSWDOCS              * number of doctor changes
          MOVE      ZERO,SSWBDAY              * number of bed days
          MOVE      ZERO,SSWDSCH              * number of discharges
          MOVE      ZERO,SSWTOUT              * number of transfers out
          MOVE      SP8,STDATE                * save transfer date
          MOVE      " ",STMOVE                * save transfer movement
          MOVE      SP3,STATYPE               * save transfer admission type
          MOVE      SP6,STADOCT               * save transfer doctor
          MOVE      SP6,STWARD                * save transfer ward
.
          CALL      WVIS0000                  * process visit details
.
UWSS9999  RETURN
+
.**********************************************************************
.*                         WVIS0000                                   *
.*                   Process the visit file                           *
.**********************************************************************
.
WVIS0000  MATCH     FADMDTE,ADATE              * admitted within the period ?
          GOTO      WVIS0500 IF LESS
.
          MATCH     ADATE,TADMDTE
          GOTO      WVIS0500 IF LESS 
.
.         Get the admission ward and attending doctor 
.
          PACK      KEY30,XADMN,SP20        
          CALL      RDSTRAN2                
          CALL      RDKTRAN2               
          BRANCH    OVRCD,WVIS9000        
.
          MATCH     XADMN,TADMN                * same admission number ?
          GOTO      WVIS9000 IF NOT EQUAL
.
.         Make sure this record is the "A" record 
.
          CMATCH    ANSA,TMOVE                 * current admission record
          GOTO      WVIS9000 IF NOT EQUAL
.
.         Update the statistics
.
          MOVE      ONE,SSWADMN             * increment the number of admissions
          MOVE      ZERO,SSWTRIN
          MOVE      ZERO,SSWDOCS
          MOVE      ZERO,SSWBDAY
          MOVE      ZERO,SSWDSCH
          MOVE      ZERO,SSWTOUT
.
          MOVE      TWARD,STWARD              * save ward
          MOVE      TADOCT,STADOCT            * save doctor
          CALL      UPWS0000                  * update the statistics
.
.         Check if the patient was discharged in the period
.
WVIS0500  BRANCH    ASTATFLG,WVIS1000         * patient discharged ?
.
          MATCH     FADMDTE,DDATE           
          GOTO      WVIS1000 IF LESS      
.
          MATCH     DDATE,TADMDTE        
          GOTO      WVIS1000 IF LESS    
.
.         Get the discharged ward and attending doctor 
.
WVIS0700  PACK      KEY30,XADMN,Z30         
          CALL      RDSTRAN2                
          CALL      RDPTRAN2               
          BRANCH    OVRCD,WVIS9500        
.
          MATCH     XADMN,TADMN                * same admission number ?
          GOTO      WVIS9500 IF NOT EQUAL
.
.         Make sure we have the "D" record
.
          CMATCH    ANSD,TMOVE                 * patient discharged ?
          GOTO      WVIS9500 IF NOT EQUAL
.
.         Update the statistics
.
          MOVE      ZERO,SSWADMN
          MOVE      ZERO,SSWTRIN
          MOVE      ZERO,SSWDOCS
          MOVE      ZERO,SSWBDAY
          MOVE      ONE,SSWDSCH             * increment the number of discharges
          MOVE      ZERO,SSWTOUT
.
          MOVE      TWARD,STWARD              * save ward
          MOVE      TADOCT,STADOCT            * save doctor
          CALL      UPWS0000                  * update the statistics
.
.         Loop over the bed transfer file, and for each change in doctor
.         or ward, update the statistics file.
.
WVIS1000  PACK      KEY30,XADMN,SP20        
          CALL      RDSTRAN2                
WVIS1500  CALL      RDKTRAN2               
          BRANCH    OVRCD,WVIS8500        
.
          MATCH     TADMN,XADMN                 * same admission number ?
          GOTO      WVIS8500 IF NOT EQUAL
.
          MATCH     FADMDTE,TDATE               * within period ?
          GOTO      WVIS2000 IF LESS         
.
          MATCH     TDATE,TADMDTE           
          GOTO      WVIS8500 IF LESS       
.
.         Find out the type of the record 
.
          MOVE      TMOVE,ANS
          REP       "A1R2L3C4D5",ANS
          MOVE      ANS,FORM1             
          BRANCH    FORM1,WVIS2000,WVIS2500,WVIS4500,WVIS6500,WVIS8000
.
.         Admission record 
.
WVIS2000  CALL      SAVD0000                  * Save transfer details
          GOTO      WVIS1500                  * go to next transfer record
.
.         Return record. If no ward or doctor change, save the details
.         Otherwise treat it as a change record, with zero bed days.
.
WVIS2500  MATCH     TWARD,STWARD             
          GOTO      WVIS3000 IF NOT EQUAL   
.
          MATCH     TADOCT,STADOCT         
          GOTO      WVIS3000 IF NOT EQUAL 
.
          GOTO      WVIS4000             
.
.         Either the ward or doctor has been changed, but need to know whether
.         a change in doctor type has occurred or a transfer in and out
.         has occurred
.
WVIS3000  MOVE      ZERO,SSWADMN 
          MOVE      ZERO,SSWTRIN
          MOVE      ZERO,SSWDOCS
          MOVE      ZERO,SSWBDAY
          MOVE      ZERO,SSWDSCH
          MOVE      ZERO,SSWTOUT
.
          CALL      CWDC0000                  * check what type of change 
          BRANCH    EXIT,WVIS3500             * change in doctor
.
.         A transfer out and a transfer in has occurred
.
          MOVE      ZERO,SSWTRIN               
          MOVE      ONE,SSWTOUT              * increment the number of trans out
          CALL      UPWS0000                 * update the statistics
.
          CALL      SAVD0000                 * save transfer details
          MOVE      ZERO,SSWTOUT          
          MOVE      ONE,SSWTRIN              * increment the number of trans in 
          CALL      UPWS0000                 * update the statistics 
          GOTO      WVIS1500                 * go to next transfer record
.
.         A change in doctor occurred, but no change in ward or doctor type
.
WVIS3500  MOVE      ONE,SSWDOCS               * increment the change in doctor
          CALL      UPWS0000                  * update the statistics
          CALL      SAVD0000                  * save transfer details
          GOTO      WVIS1500                  * go to next transfer record
.
.         No change in the transfer details. As this is a return from leave, 
.         we need to resave because we start the bed day calculation
.         from this date, not the on-leave date.
.
WVIS4000  CALL      SAVD0000                  * save transfer details
          GOTO      WVIS1500                  * go to next transfer record
.
.         On-leave record. If no ward or doctor change, just post
.         the bed days. Otherwise treat it as a change record.
.
WVIS4500  MATCH     TWARD,STWARD             
          GOTO      WVIS5000 IF NOT EQUAL   
.
          MATCH     TADOCT,STADOCT         
          GOTO      WVIS5000 IF NOT EQUAL 
.
          GOTO      WVIS6000             
.
.         A change in ward or doctor has occurred. Find out whether
.         a change in doctor type ,or a transfer in and out 
.
WVIS5000  MOVE      ZERO,SSWADMN   
          MOVE      ZERO,SSWTRIN  
          MOVE      ZERO,SSWDOCS 
          MOVE      ZERO,SSWBDAY
          MOVE      ZERO,SSWDSCH
          MOVE      ZERO,SSWTOUT
.
          CALL      CLWS0000                  * calculate the number of bed days
.
          CALL      CWDC0000                  * check what change we have
          BRANCH    EXIT,WVIS5500             * just a change in doctor
.
.         A transfer out and a transfer in has occurred
.
          MOVE      ZERO,SSWTRIN               
          MOVE      ONE,SSWTOUT              * increment the number of trans out
          CALL      UPWS0000                 * update the statistics
.
          CALL      SAVD0000                 * save transfer details
          MOVE      ZERO,SSWBDAY              
          MOVE      ZERO,SSWTOUT               
          MOVE      ONE,SSWTRIN              * increment the number of trans out
          CALL      UPWS0000                 * update the statistics
          GOTO      WVIS1500                 * go to next transfer record
.
.         A change in doctor has occurred, but no change in ward or doctor type
.
WVIS5500  MOVE      ONE,SSWDOCS               * increment the change in doctor
          CALL      UPWS0000                  * update the statistics
          CALL      SAVD0000                  * save transfer details
          GOTO      WVIS1500                  * go to next transfer record
.
.         No change in transfer details. Because this is an
.         on-leave, we still need to calculate and post the bed-days up to
.         this date.
.
WVIS6000  MOVE      ZERO,SSWADMN               
          MOVE      ZERO,SSWTRIN               
          MOVE      ZERO,SSWDOCS              
          MOVE      ZERO,SSWBDAY             
          MOVE      ZERO,SSWDSCH            
          MOVE      ZERO,SSWTOUT           
.
          CALL      CLWS0000                  * calculate the number of bed days
.
          CALL      UPWS0000                  * update the statistics
          CALL      SAVD0000                  * save transfer details
          GOTO      WVIS1500                  * go to next transfer record
.
.         Change record. If no ward or doctor change, just ignore the record
.
WVIS6500  MATCH     TWARD,STWARD             
          GOTO      WVIS7000 IF NOT EQUAL   
.
          MATCH     TADOCT,STADOCT         
          GOTO      WVIS7000 IF NOT EQUAL 
.
          GOTO      WVIS1500             
.
.         A change in ward or doctor has occurred, but need to know whether
.         a change in doctor type or a transfer in and out 
.
WVIS7000  MOVE      ZERO,SSWADMN               
          MOVE      ZERO,SSWTRIN              
          MOVE      ZERO,SSWDOCS             
          MOVE      ZERO,SSWBDAY            
          MOVE      ZERO,SSWDSCH           
          MOVE      ZERO,SSWTOUT          
.
          CALL      CLWS0000                  * calculate the number of bed days
.
          CALL      CWDC0000                  * check what change we have
          BRANCH    EXIT,WVIS7500             * just a change in doctor
.
.         A transfer out and a transfer in has occurred  
.
          MOVE      ZERO,SSWTRIN               
          MOVE      ONE,SSWTOUT              * increment the number of trans out
          CALL      UPWS0000                 * update the statistics
.
          CALL      SAVD0000                 * save transfer details
          MOVE      ZERO,SSWBDAY               
          MOVE      ZERO,SSWTOUT              
          MOVE      ONE,SSWTRIN              * increment the number of trans in
          CALL      UPWS0000                 * update the statistics
          GOTO      WVIS1500                 * go to next transfer record
.
.         A change in doctor has occurred, but no change in ward or doctor type
.
WVIS7500  MOVE      ONE,SSWDOCS               * increment the nchange in doctor
          CALL      UPWS0000                  * update the statistics
          CALL      SAVD0000                  * save transfer details
          GOTO      WVIS1500                  * go to next transfer record
.
.         Discharge record. Just post the bed days up to this record.
.
WVIS8000  MOVE      ZERO,SSWADMN               
          MOVE      ZERO,SSWTRIN              
          MOVE      ZERO,SSWDOCS             
          MOVE      ZERO,SSWBDAY            
          MOVE      ZERO,SSWDSCH           
          MOVE      ZERO,SSWTOUT          
.
          MATCH     ADATE,DDATE               * see if patient is a day case
          GOTO      WVIS8100 IF NOT EQUAL
.
          MOVE      ONE,SSWBDAY
.
          GOTO      WVIS8200
.
WVIS8100  CALL      CLWS0000                  * calculate the number of bed days
.
WVIS8200  CALL      UPWS0000                  * update the statistics
          GOTO      WVIS9999                  
.
.         Patient still in the hospital at the end of the period.
.         Just post the bed days up to end of period
.
WVIS8500  MATCH     ANSL,STMOVE               * we don't want to update any
          GOTO      WVIS9999 IF EQUAL           statistics if the last movement
.                                               was on-leave
          MOVE      ZERO,SSWADMN               
          MOVE      ZERO,SSWTRIN              
          MOVE      ZERO,SSWDOCS             
          MOVE      ZERO,SSWBDAY            
          MOVE      ZERO,SSWDSCH           
          MOVE      ZERO,SSWTOUT          
.
          MOVE      TADMDTE,TDATE            
.
          CALL      CLWS0000                  * calculate the number of bed days
          ADD       ONE,SSWBDAY                
.
          CALL      UPWS0000                  * update the statistics
          GOTO      WVIS9999                 
.
.         Error messages 
.
WVIS9000  MATCH     "IBARSH",PGM
          GOTO      WVIS9999 IF EQUAL
. 
          DISPLAY   *P1:22,*EL,*V2LON,*B:
                    "** Error: Missing #"A#" record in bed transfer file for ":
                    XADMN,". Hit <ENTER> " 
          KEYIN     *P1:23,*EL,*V2LON,"to continue ",*EOFF,ANS;
          DISPLAY   *P1:22,*EL,*P1:23,*EL;
          GOTO      WVIS9999                 
.
WVIS9500  MATCH     "IBARSH",PGM
          GOTO      WVIS9999 IF EQUAL
. 
          DISPLAY   *P1:22,*EL,*V2LON,*B:
                    "** Error: Missing #"D#" record in bed transfer file for ":
                    XADMN,". Hit <ENTER> "
          KEYIN     *P1:23,*EL,*V2LON,"to continue ",*EOFF,ANS;
          DISPLAY   *P1:22,*EL,*P1:23,*EL;
.
WVIS9999  RETURN
+
.**************************************************************************
.*                              UPWS0000                                  *
.*                        Update the PATWSSFD file                        *
.**************************************************************************
.
UPWS0000  
.         Find out the primary doctor type of the attending doctor
.
          MOVE      SP3,DRTYPE              
          MOVE      STADOCT,KEY6             
          CALL      RDDOCT1                
.
          PACK      KEY12,CSTPER,STWARD,DRTYPE
          CALL      RDWSSA1               
          BRANCH    OVRCD,UPWS1000       
.
          ADD       SSWADMN,WSSADMN       
          ADD       SSWTRIN,WSSTRIN      
          ADD       SSWDOCS,WSSDOCS     
          ADD       SSWBDAY,WSSBDAY    
          ADD       SSWDSCH,WSSDSCH   
          ADD       SSWTOUT,WSSTOUT  
.
          CALL      UPWSSA1                  
          GOTO      UPWS9999                
.
UPWS1000  MOVE      CSTPER,WSSDATE
          MOVE      STWARD,WSSWARD
          MOVE      DRTYPE,WSSSPEC
          MOVE      SSWADMN,WSSADMN            
          MOVE      SSWTRIN,WSSTRIN           
          MOVE      SSWDOCS,WSSDOCS          
          MOVE      SSWBDAY,WSSBDAY         
          MOVE      SSWDSCH,WSSDSCH        
          MOVE      SSWTOUT,WSSTOUT       
.
          CALL      WRWSSA1            
.
UPWS9999  RETURN
+
.**************************************************************************
.*                              CLWS0000                                  *
.*                        Calculate the bed days                          *
.**************************************************************************
.
CLWS0000  MATCH     TDATE,TADMDTE             * end of period ? 
          GOTO      CLWS1000 IF LESS          * Yes, use end of period date
.
          UNPACK    TDATE,CCENT,CYEAR,CMON,CDAY    
          MOVE      CCENT,CC
          MOVE      CYEAR,YY
          MOVE      CMON,MM
          MOVE      CDAY,DD
          GOTO      CLWS2000
.
.         Transfer record after the end of period. Use the end of period as
.         the todate for the bed day calculation. We add one to the days
.         because we want to include the last night (TADMDTE), but the
.         calculation used does not include the ending date. This addition
.         of one day fixes this problem.
.
CLWS1000  UNPACK    TADMDTE,CCENT,CYEAR,CMON,CDAY   
          MOVE      CCENT,CC
          MOVE      CYEAR,YY
          MOVE      CMON,MM
          MOVE      CDAY,DD
          ADD       ONE,DD                    * Allow for the last night

.
.         Convert the todate to julian
.
CLWS2000  CALL      DMYCON                 
.
          MOVE      JULDAY,SSWBDAY             
          MOVE      JULYR,SAVJYR            
.
.         Check if the starting date is before the start of period, and
.         if so, use the start of period instead
.
          MATCH     FADMDTE,STDATE            * Before fromdate ?
          GOTO      CLWS3000 IF LESS          * Yes, use the from date
.
          UNPACK    STDATE,CCENT,CYEAR,CMON,CDAY   
          GOTO      CLWS4000
.
.         Use the start of period as the beginning of the bed day calculation.
.
CLWS3000  UNPACK    FADMDTE,CCENT,CYEAR,CMON,CDAY 
.
.         Convert the start date to julian
.
CLWS4000  MOVE      CCENT,CC
          MOVE      CYEAR,YY
          MOVE      CMON,MM
          MOVE      CDAY,DD
.
          CALL      DMYCON                 
.
.         Check if the year has changed. If so, add one year to the todate
.
          COMPARE   JULYR,SAVJYR          
          GOTO      CLWS5000 IF EQUAL    
.
.         Change in year, add 365 + the leap year indicator
.
          ADD       OYEAR,SSWBDAY
          ADD       LEAPYR,SSWBDAY
.
.         Calculate the number days between the two julian dates
.
CLWS5000  SUB       JULDAY,SSWBDAY        
.
CLWS9999  RETURN
+
.**************************************************************************
.*                              SAVD0000                                  *
.*                        Save the transfer details                       *
.**************************************************************************
.
SAVD0000  MOVE      TWARD,STWARD              * transfer ward
          MOVE      TDATE,STDATE              * transfer date
          MOVE      TADOCT,STADOCT            * transfer attending doctor
          MOVE      TMOVE,STMOVE              * transfer move
.
          RETURN
+
.**************************************************************************
.*                              CWDC0000                                  *
.*                   Check for a change in doctor type or ward            *
.**************************************************************************
.
CWDC0000  MATCH     TWARD,STWARD             
          GOTO      CWDC1000 IF NOT EQUAL   
.
          MATCH     TADOCT,STADOCT         
          GOTO      CWDC2000 IF EQUAL     
.
          MOVE      SP3,DRTYPE           
          MOVE      STADOCT,KEY6              * old doctor key
          CALL      RDDOCT1                   * Read in the old doctor type
          MOVE      DRTYPE,SAVDTYP            * Save old doctor type
.
          MOVE      SP3,DRTYPE          
          MOVE      TADOCT,KEY6               * new doctor key
          CALL      RDDOCT1                   * Read in the new doctor type
.
          MATCH     DRTYPE,SAVDTYP           
          GOTO      CWDC2000 IF EQUAL       
.
CWDC1000  MOVE      ZERO,EXIT                 * change in doctor type or ward
          GOTO      CWDC9999
.
CWDC2000  MOVE      ONE,EXIT                  * no change in doctor type or ward
.
CWDC9999  RETURN
+
.**********************************************************************
.*                         UNUM0000                                   *
.*             Generate data for PATNUMFD file                        *
.**********************************************************************
.
UNUM0000  COMPARE   ONE,PTCNNUMM         
          GOTO      UNUM9999 IF NOT EQUAL   * dont update patnumfd file
.
          DISPLAY   *P1:24,"patnumwr";
          MOVE      SP3,NUMWARD         * ward 
          MOVE      ZERO,NUMADMN        * number of patients admitted
          MOVE      ZERO,NUMTRIN        * number of patients transfered in
          MOVE      ZERO,NUMTOUT        * number of patients transfered out
          MOVE      ZERO,NUMDSCH        * number of patients discharged
          MOVE      ZERO,NUMLEAV        * number of patients on leave
          MOVE      ZERO,NUMRETN        * number of patients returned from leave
.
          CALL      NVIS0000                  * process visit details
.
UNUM9999  RETURN
+
.**********************************************************************
.*                         NVIS0000                                   *
.*                   Process the visit file                           *
.**********************************************************************
.
NVIS0000  MATCH     FADMDTE,ADATE         * admitted within the period ?
          GOTO      NVIS0500 IF LESS
.
          MATCH     ADATE,TADMDTE
          GOTO      NVIS0500 IF LESS 
.
.         Get the admission ward 
.
          PACK      KEY30,XADMN,SP20        
          CALL      RDSTRAN2                
          CALL      RDKTRAN2               
          BRANCH    OVRCD,NVIS8000        
.
          MATCH     XADMN,TADMN           * same admission number ?
          GOTO      NVIS8000 IF NOT EQUAL
.
.         Make sure this record is the "A" record 
.
          CMATCH    ANSA,TMOVE              * current admission record
          GOTO      NVIS8000 IF NOT EQUAL
.
.         Update the statistics
.
          MOVE      ONE,NUMADMN             * increment the number of admissions
          MOVE      ZERO,NUMTRIN
          MOVE      ZERO,NUMDSCH
          MOVE      ZERO,NUMTOUT
          MOVE      ZERO,NUMLEAV
          MOVE      ZERO,NUMRETN
.
          MOVE      TWARD,STWARD              * save ward
          CALL      UPNM0000                  * update the statistics
.
.         Check if the patient was discharged in the period
.
NVIS0500  BRANCH    ASTATFLG,NVIS1000         * patient discharged ?
.
          MATCH     FADMDTE,DDATE          
          GOTO      NVIS1000 IF LESS      
.
          MATCH     DDATE,TADMDTE        
          GOTO      NVIS1000 IF LESS    
.
.         Get the discharged ward 
.
NVIS0700  PACK      KEY30,XADMN,Z30         
          CALL      RDSTRAN2                
          CALL      RDPTRAN2               
          BRANCH    OVRCD,NVIS9000        
.
          MATCH     XADMN,TADMN            * same admission number ?
          GOTO      NVIS9000 IF NOT EQUAL
.
.         Make sure we have the "D" record
.
          CMATCH    ANSD,TMOVE                 * patient discharged ?
          GOTO      NVIS9000 IF NOT EQUAL
.
.         Update the statistics
.
          MOVE      ZERO,NUMADMN
          MOVE      ZERO,NUMTRIN
          MOVE      ONE,NUMDSCH             * increment the number of discharges
          MOVE      ZERO,NUMTOUT
          MOVE      ZERO,NUMLEAV
          MOVE      ZERO,NUMRETN
.
          MOVE      TWARD,STWARD             
          CALL      UPNM0000                  * update the statistics
.
.         Loop over the bed transfer file, and for each change in ward, 
.         update the statistics file.
.
NVIS1000  PACK      KEY30,XADMN,SP20        
          CALL      RDSTRAN2                
NVIS1500  CALL      RDKTRAN2               
          BRANCH    OVRCD,NVIS7000        
.
          MATCH     TADMN,XADMN                     * same admission number ?
          GOTO      NVIS7000 IF NOT EQUAL
.
          MATCH     FADMDTE,TDATE                   * within period ?
          GOTO      NVIS2000 IF LESS         
.
          MATCH     TDATE,TADMDTE           
          GOTO      NVIS7000 IF LESS       
.
.         Find out the type of the record 
.
          REP       "A1R2L3C4D5",TMOVE       
          MOVE      TMOVE,FORM1             
          BRANCH    FORM1,NVIS2000,NVIS2500,NVIS4000,NVIS5500,NVIS6500
.
.         Admission record 
.
NVIS2000  CALL      SAVD0000                  * save transfer details
          GOTO      NVIS1500                  * go to next transfer record
.
.         Return record. If no ward change, save the details
.         Otherwise treat it as a change record
.
NVIS2500  MATCH     TWARD,STWARD             
          GOTO      NVIS3000 IF NOT EQUAL   
.
          GOTO      NVIS3500             
.
.         Either the ward has been changed
.
NVIS3000  MOVE      ZERO,NUMADMN 
          MOVE      ZERO,NUMTRIN
          MOVE      ZERO,NUMDSCH
          MOVE      ZERO,NUMTOUT
          MOVE      ONE,NUMRETN              * increment the number of returns
          MOVE      ZERO,NUMLEAV
.
.         A transfer out and a transfer in has occurred
.
          MOVE      ZERO,NUMTRIN               
          MOVE      ONE,NUMTOUT              * increment the number of trans out
          CALL      UPNM0000                 * update the statistics
.
          CALL      SAVD0000                 * save transfer details
          MOVE      ZERO,NUMTOUT          
          MOVE      ONE,NUMTRIN              * increment the number of trans in 
          CALL      UPNM0000                 * update the statistics 
          GOTO      NVIS1500                 * go to next transfer record
.
.         No change in the transfer details. 
.
NVIS3500  CALL      SAVD0000                  * save transfer details
          GOTO      NVIS1500                  * go to next transfer record
.
.         On-leave record. If no ward or doctor change, just post
.         the bed days. Otherwise treat it as a change record.
.
NVIS4000  MATCH     TWARD,STWARD             
          GOTO      NVIS4500 IF NOT EQUAL   
.
          GOTO      NVIS5000             
.
.         A change in ward has occurred. 
.
NVIS4500  MOVE      ZERO,NUMADMN   
          MOVE      ZERO,NUMTRIN  
          MOVE      ZERO,NUMDSCH
          MOVE      ZERO,NUMTOUT
          MOVE      ZERO,NUMRETN
          MOVE      ONE,NUMLEAV             * increment the number of on leaves
.
.         A transfer out and a transfer in has occurred
.
          MOVE      ZERO,NUMTRIN               
          MOVE      ONE,NUMTOUT              * increment the number of trans out
          CALL      UPNM0000                 * update the statistics
.
          CALL      SAVD0000                 * save transfer details
          MOVE      ZERO,NUMTOUT               
          MOVE      ONE,NUMTRIN              * increment the number of trans out
          CALL      UPNM0000                 * update the statistics
          GOTO      NVIS1500                 * go to next transfer record
.
.         No change in transfer details. 
.
NVIS5000  MOVE      ZERO,NUMADMN               
          MOVE      ZERO,NUMTRIN               
          MOVE      ZERO,NUMDSCH            
          MOVE      ZERO,NUMTOUT           
          MOVE      ZERO,NUMRETN
          MOVE      ZERO,NUMLEAV         
.
          CALL      UPNM0000                  * update the statistics
          CALL      SAVD0000                  * save transfer details
          GOTO      NVIS1500                  * go to next transfer record
.
.         Change record. If no ward change, just ignore the record
.
NVIS5500  MATCH     TWARD,STWARD             
          GOTO      NVIS6000 IF NOT EQUAL   
.
          GOTO      NVIS1500             
.
.         A change in ward has occurred
.
NVIS6000  MOVE      ZERO,NUMADMN               
          MOVE      ZERO,NUMTRIN              
          MOVE      ZERO,NUMDSCH           
          MOVE      ZERO,NUMTOUT          
          MOVE      ZERO,NUMRETN
          MOVE      ZERO,NUMLEAV         
.
.         A transfer out and a transfer in has occurred  
.
          MOVE      ZERO,NUMTRIN               
          MOVE      ONE,NUMTOUT              * increment the number of trans out
          CALL      UPNM0000                 * update the statistics
.
          CALL      SAVD0000                 * save transfer details
          MOVE      ZERO,NUMTOUT              
          MOVE      ONE,NUMTRIN              * increment the number of trans in
          CALL      UPNM0000                 * update the statistics
          GOTO      NVIS1500                 * go to next transfer record
.
.         Discharge record. 
.
NVIS6500  MOVE      ZERO,NUMADMN               
          MOVE      ZERO,NUMTRIN              
          MOVE      ZERO,NUMDSCH           
          MOVE      ZERO,NUMTOUT          
          MOVE      ZERO,NUMRETN
          MOVE      ZERO,NUMLEAV         
.
          CALL      UPNM0000                  * update the statistics
          GOTO      NVIS9999                 
.
.         Patient still in the hospital at the end of the period.
.
NVIS7000  MOVE      ZERO,NUMADMN               
          MOVE      ZERO,NUMTRIN              
          MOVE      ZERO,NUMDSCH           
          MOVE      ZERO,NUMTOUT          
          MOVE      ZERO,NUMRETN
          MOVE      ZERO,NUMLEAV         
.
          MOVE      TADMDTE,TDATE            
.
          CALL      UPNM0000                  * update the statistics
          GOTO      NVIS9999                 
.
.         Error messages 
.
NVIS8000  MATCH     "IBARSH",PGM
          GOTO      NVIS9999 IF EQUAL
. 
          DISPLAY   *P1:22,*EL,*V2LON,*B:
                    "** Error: Missing #"A#" record in bed transfer file for ":
                    XADMN,". Hit <ENTER> "
          KEYIN     *P1:23,*EL,*V2LON,"to continue ",*EOFF,ANS;
          DISPLAY   *P1:22,*EL,*P1:23,*EL;
          GOTO      NVIS9999                 
.
NVIS9000  MATCH     "IBARSH",PGM
          GOTO      NVIS9999 IF EQUAL
. 
          DISPLAY   *P1:22,*EL,*V2LON,*B:
                    "** Error: Missing #"D#" record in bed transfer file for ":
                    XADMN,". Hit <ENTER> "
          KEYIN     *P1:23,*EL,*V2LON,"to continue ",*EOFF,ANS;
          DISPLAY   *P1:22,*EL,*P1:23,*EL;
.
NVIS9999  RETURN
+
.**********************************************************************
.*                         UPNM0000                                   *
.*                   Update the PATNUMFD file                         *
.**********************************************************************
.
UPNM0000  PACK      KEY9,STWARD,CSTPER
          CALL      RDNUMW1               
          BRANCH    OVRCD,UPNM1000       
.
          ADD       NUMADMN,PADMN       
          ADD       NUMTRIN,PTRIN      
          ADD       NUMDSCH,PDSCH   
          ADD       NUMTOUT,PTROUT  
          ADD       NUMRETN,PRETN
          ADD       NUMLEAV,PLEAV
.
          CALL      UPNUMW1                  
          GOTO      UPNM9999                
.
UPNM1000  MOVE      STWARD,PWARD
          MOVE      CSTPER,PDATE
          MOVE      NUMADMN,PADMN       
          MOVE      NUMTRIN,PTRIN      
          MOVE      NUMDSCH,PDSCH   
          MOVE      NUMTOUT,PTROUT  
          MOVE      NUMRETN,PRETN
          MOVE      NUMLEAV,PLEAV
.
          CALL      WRNUMW1                  
.
UPNM9999  RETURN
+
.**********************************************************************
.*                         CRET0000                                   *
.*        Create the file that is needed to write to                  *
.**********************************************************************
.
CRET0000  CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    CFNAME,CMDLINE
          APPEND    " 13 U1-4,5-12 ",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      PATDAYA1,CFNAME          
CRET9999  RETURN
+
.**********************************************************************
.*                         END0000                                    *
.*                      End of Report                                 *
.**********************************************************************
.
END0000   DISPLAY   *P1:23,*EL,*P1:24,*EL,*V2LON:
                    "Generation Completed.  ";
          CALL      HITENTER        
          DISPLAY   *P1:14,*EF
.
          CALL      KILA0000                  * remove temporary file a
          CALL      KILB0000                  * remove temporary file b
END9999   RETURN
+
.**********************************************************************
.*                         CREA0000                                   *
.*                  Create Temporary File A                           *
.**********************************************************************
.
CREA0000  CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,PNPTMP
.
          CALL      KILA0000
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    PNPTMP,CMDLINE
          APPEND    " 9 u1-8",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      PNPTMPXX,PNPTMP
CREA9999  RETURN
+
.**********************************************************************
.*                         CREB0000                                   *
.*                  Create Temporary File B                           *
.**********************************************************************
.
CREB0000  CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,BNBTMP
.
          CALL      KILB0000
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    BNBTMP,CMDLINE
          APPEND    " 12 u1-8,9-11",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      BNBTMPXX,BNBTMP
CREB9999  RETURN
+
.**********************************************************************
.*                         KILA0000                                   *
.*                  Remove Temporary File A                           *
.**********************************************************************
.
KILA0000  CLOSE     PNPTMPXX
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    PNPTMP,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
KILA9999  RETURN
+
.**********************************************************************
.*                         KILB0000                                   *
.*                  Remove Temporary File B                           *
.**********************************************************************
.
KILB0000  CLOSE     BNBTMPXX
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    BNBTMP,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
KILB9999  RETURN
+
.**********************************************************************
.*              I/O For Temporary File                                *
.**********************************************************************
.
.         Temporary file A
.
RDSTMP1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READ      PNPTMPXX,KEY8;;
          RETURN
.
RDKTMP1   MOVE      ZERO,OVRCD
          READKS    PNPTMPXX;DXADMN
          MOVE      DXADMN,XADMN
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTMP1    MOVE      ZERO,OVRCD
          RESET     KEY8
          READ      PNPTMPXX,KEY8;DXADMN
          MOVE      DXADMN,XADMN
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTMP1    MOVE      XADMN,DXADMN
          UPDATE    PNPTMPXX;DXADMN
          RETURN
.
WRTMP1    MOVE      XADMN,DXADMN
          RESET     KEY8
          WRITE     PNPTMPXX,KEY8;KEY8
          RETURN
.
.         Temporary file B
.
RDTMP2    MOVE      ZERO,OVRCD
          RESET     KEY11
          READ      BNBTMPXX,KEY11;DBADMN,TATYPE
          MOVE      DBADMN,BADMN
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTMP2    MOVE      BADMN,DBADMN
          RESET     KEY11
          WRITE     BNBTMPXX,KEY11;KEY11
          RETURN
+
.**********************************************************************
.*                              BDAY0000                              *
.*     Get the active bed days for all ward classifications.          *
.* NB. This routine will update the active bed days for records       *
.*     where the doctor class is blank (as this field is currently not*
.*     used).                                                         *
.**********************************************************************
.
BDAY0000  MOVE      SP6,KEY6
          DISPLAY   *P1:24,*EL,"Processing Ward for patmstsf: ";
          CALL      RDSWARD1                     * position at start of file
BDAY1000  CALL      RDKWARD1                     * read next record 
          BRANCH    OVRCD,BDAY9999               * eof - finished
.
          MATCH     SP3,WBED                     * ward header record ?
          IF        @EQUAL
            DISPLAY   *P32:24,*EL,*V2LON,WWARD;
            MOVE      ONE,ACTVWFLG               * don't want weekend days
            PROC      WDACTVDY                   * yes - get days ward active
            COMPARE   ZERO,ACTVDAYS              * ward active during period ?
            GOTO      BDAY5000 IF EQUAL          * no - ignore
.
.           If the record is not on file, then write a record which has
.           all blanks/zeroes as this is used in conjunction with PTCNH82W
.
            PACK      KEY15,WWARD,SP3,WCLASS,CSTPER
            CALL      RDMSTS1                    * record on file ?
            IF        OVRCD = 1
              CALL      CLMT0000                 * no - create new record
              PACK      KEY15,WWARD,SP3,WCLASS,CSTPER
              MOVE      ACTVBDAY,MSACBDAY
              MOVE      ACTVDAYS,MSDYMTH           * load days ward was active
              CALL      WRMSTS1                  
              GOTO      BDAY5000
            ENDIF
.
            ADD       ACTVBDAY,MSACBDAY          * load active bed days
            ADD       ACTVDAYS,MSDYMTH           * load days ward was active
            CALL      UPMSTS1                    * update record
          ENDIF
.
.         Get next ward header record
.
BDAY5000  PACK        KEY6,WWARD,Z30
          CALL        RDSWARD1                   * position in file
          GOTO        BDAY1000                   * get next record
.
BDAY9999  RETURN
+
.**********************************************************************
.*                              BDAD0000                              *
.*     Same as BDAY0000 but for patmsdaf.                             *
.**********************************************************************
.
BDAD0000  COMPARE   ONE,PTCNMSDF
          GOTO      BDAD9999 IF NOT EQUAL   * not using MSD file 

          MOVE      SP6,KEY6
          DISPLAY   *P1:24,*EL,"Processing Ward for patmsdaf: ";
          CALL      RDSWARD1                     * position at start of file
BDAD1000  CALL      RDKWARD1                     * read next record 
          BRANCH    OVRCD,BDAD9999               * eof - finished
.
          MATCH     SP3,WBED                     * ward header record ?
          IF        @EQUAL
            DISPLAY   *P32:24,*EL,*V2LON,WWARD;
            MOVE      ONE,ACTVWFLG               * don't want weekend days
            PROC      WDACTVDY                   * yes - get days ward active
            COMPARE   ZERO,ACTVDAYS              * ward active during period ?
            GOTO      BDAD5000 IF EQUAL          * no - ignore
.
.           If the record is not on file, then write a record which has
.           all blanks/zeroes as this is used in conjunction with PTCNH82W
.
            PACK      KEY12,WCLASS,WWARD,CSTPER
            CALL      RDPTMSD1                   * record on file ?
            IF        OVRCD = 1
              MOVE      WWARD,STWARD
              MOVE      WCLASS,STDEPT
              CALL      CLMD0000                 * no - create new record
              PACK      KEY12,WCLASS,WWARD,CSTPER
              CALL      WRPTMSD1                  
              GOTO      BDAD5000
            ENDIF
.
            CALL      UPPTMSD1                   * update record
          ENDIF
.
.         Get next ward header record
.
BDAD5000  PACK        KEY6,WWARD,Z30
          CALL        RDSWARD1                   * position in file
          GOTO        BDAD1000                   * get next record
.
BDAD9999  RETURN
+
.**********************************************************************
.*              I/O INCLUDES FOR FILES AND GENERAL ROUTINES           *
.**********************************************************************
.
          INC       STD001IO
          INC       CALCDAYS
          INC       DATECONV
          INC       WDACTVDY
          INC       BOKRC1IO/INC       * booking master file
          INC       RTIODAYS           * bed day calculating routine
          iNC       PATAGED            * age calculating routine
          INC       PATC1SIO/INC       * clinical statistics 1 file
          INC       PATC2SIO/INC       * clinical statistics 2 file
          INC       PATCOMN3
          INC       PATCSSIO/INC       * clinical statistics summary file
          INC       PATDASIO/INC       * discharge & admission statistics file
          INC       PATDAYIO/INC       * daily patient list file
          INC       PATDRGIO/INC       * period date range file
          INC       PATECOIO/INC            * Patient episode operation file
          INC       PATCT1IO/INC       * catchment codes
          INC       PATCODIO/INC       * patient codes file
          INC       PATCSTIO/INC       * catchment summary file
          INC       PATDO1IO/INC       * patient doctors file
          INC       PATDSCIO/INC       * patient discharge file
          INC       PATDSTIO/INC       * doctor statistics file
          INC       PATMA1IO/INC       * patients master file
          INC       PATMI1IO/INC       * patients admissions file
          INC       PMSVX1IO/INC       * patients admissions file
          INC       PATMMBIO/INC       * mbs operations file 
          INC       PATMSTIO/INC       * monthly inpatient statistics file
          INC       PATNUMIO/INC       * patient ward usage file
          INC       PATTRNIO/INC       * bed transfer file
          INC       PATWBHIO/INC       * ward bed history file
          INC       PATWR1IO/INC       * ward/bed file
          INC       PATMSDIO/INC       * monthly inpatient statistics file
          INC       PATWSSIO/INC       * ward specialities stats summary file
          INC       TFILENAM                     * Generate Temp File Name
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       WEBERRIO/INC                 * Web Server Error Logging
................................................................................

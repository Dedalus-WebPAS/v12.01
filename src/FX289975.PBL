.*****************************************************************************
.* System    :   Allied Health                                               *
.* Program   :   FX289975                                                    *
.* Desc      :   Fixit for Referral Audit File Action Date                   *
.*****************************************************************************
.* Date      :   08/04/2014                                                  *
.* Author    :   Nicole Torrisi                                              *
.* Function  :   This program will loop through the allied health referral   *
.*               audit table (allaudaf) and find all instances of blank      *
.*               spaces in the action date (alauadat) and replace them with  *
.*               zeroes.                                                     *
.* Mods      :                                                               *
.*        V10.04.00  08/04/2014 Nicole Torrisi     CAR 289975                *
.*                   Program created                                         *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       ALLAUDFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
COUNTER   FORM      12
SAVKEY24  DIM       24
.
.
PRGID     INIT      "FX289975"
PRGNAM    INIT      "Fixit for Referral Audit File Action Date"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000       * proceed with fixit
.
MAIN1000  CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN2000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN2000  CALL      PROC0000               * process
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"allaudaf";
          OPEN      ALLAUDA1,"allaudaf"
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run fixit                               *
.*                        TRUE  (1)  Exit option selected                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*        Loop through all referral audit records and check if the action    *
.*        date contains spaces. If it does, replace the spaces with zeroes   *
.*        and update the record.                                             *
.*****************************************************************************
.
PROC0000  CALL      IBACLOCK                     * get current date/time
          MOVE      ZERO,COUNTER                 * initialise counter
.
.         Loop through and check each referral audit record on file
.
          PACK      KEY24,SP70
          CALL      RSALAUD1                     * position at start of file
PROC0500  CALL      RKALAUD1                     * read next record
.
          DISPLAY   *P20:23,*EL,"Visit Number: ",ALAUVISN;
.
          BRANCH    OVRCD,PROC9000               * eof - finished
.
.         If there are no spaces in the action date, then ignore this record
.
          SCAN      SP1,ALAUADAT                 * blank in action date?
          GOTO      PROC0500 IF NOT EQUAL        * no - get next record   
.
.         This referral audit record has an action date with a blank in it,
.         so replace blanks with zeroes and update    
.
PROC1000  ADD       ONE,COUNTER                  * increment counter
.
          PACK      SAVKEY24,ALAUVISN,ALAUADAT,ALAUATIM,SP70
.
          REPLACE   " 0",ALAUADAT                * replace spaces with zeros
.
          CALL      UPALAUD1                     * update audit record
.
          MOVE      SAVKEY24,KEY24
          CALL      RAALAUD1
.
          GOTO      PROC0500                     * get next audit record
.
PROC9000  DISPLAY   *P1:24,*EL,"Fixit completed.  ",*V2LON,COUNTER,*HOFF:
                    " records updated.  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       ALLAUDIO/INC

.******************************************************************************
.* System   : WAITING LIST                                                    *
.* Program  : IBAWAT36                                                        *
.* Desc     : Booking Analysis                                                *
.******************************************************************************
.* Author   : Paul Duncan                                                     *
.* Date     : 04/05/1990                                                      *
.* Comments :                                                                 *
.* Mods     :                                                                 *
.******************************************************************************
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899                         *
.*           Recompiled as WATTR1FD changes                                   *
.******************************************************************************
.*        V10.05.01 05/01/2015  Ebon Clements CAR 261630                      *
.*                  Removed port number from temp file name                   *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V9.02.04  22/01/2002  Dean Cramer                                   *
.*                  Fix were null unit omits first heading                  . *
.*        V9.02.03  20/12/2001  Dean Cramer                                   *
.*                  Addition to previous fix for sub totals                 . *
.*        V9.02.02  13/11/2001  Dean Cramer                                   *
.*                  Fix so paging correct after previous heading change.    . *
.*        V9.02.01  24/09/2001  Dean Cramer                                   *
.*                  Make so heading will print even no data found for report. *
.*        V5.08.01  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.07.01  27/10/1999 J Habasque                                     *
.*                  Recompiled for changes to WATPROFD - Active/Inactive codes*
.*            V5.01.01  26/07/90 Bill Dew                                     *
.*                      Fix minor screen handling problems.                   *
.*                      Implement calculations on watcataf to get stats on    *
.*                      on cancelled patients.                                *
.*            V5.01.02  03/08/90 Justin Coates                                *
.*                      Added the use of WTCNTPDS : Treatment/Procedure       *
.*                      12/09/90 Bill Dew                                     *
.*                      Fix tempfile key.   Key is too unique.                *
.*                      27/09/90 Bill Dew                                     *
.*                      Close tempfile after user otherwise I*L occurs        *
.*            V5.01.03  14/11/90 i chung                                      *
.*                      Recompile for WATCATIO.INC - incorrect Keys size      *
.*            V5.01.04  23/05/91  Steve O'Gorman                              *
.*                      Fixed Errors found by the New Compiler                *
.*        V5.01.05  22/02/1993    Justin Coates  (DUDLEY)                     *
.*                  allow for status 7 and 8                                  *
.*        V5.01.06  26/05/1994 Ian Rutt                                       *
.*                  Fixed Global Recompile Bugs                               * 
.*        V5.04.01  26/06/1997  Howellsy         505                          *
.*                  Use PATDOCKY instead of PATDOCDS                          *
.******************************************************************************
.$$$$$
.$$$$$
.************************************************************************
.*                        FD INCLUDES                                   *
.************************************************************************
          INC       STD001FD
          INC       IBASEQFD/INC
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PATDSCFD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       TFILEVAR/INC
          INC       WATCATFD/INC
          INC       WATCONFD/INC
          INC       WATPROFD/INC
          INC       WATTR1FD/INC
          INC       WEBERRFD/INC
+
TEMPKEY   DIM       40
TEMPFIL1  IFILE     SQL, FIXED=68, KEY=TEMPKEY
.
.************************************************************************
.*                        CONSTANTS                                     *
.************************************************************************
CODEBC    INIT      "BC"
CODETP    INIT      "TP"
CODEWU    INIT      "WU"
CODEWG    INIT      "WG"
.
DESCOPT1  DIM       35            * it's value is set up in INIT0000
DESCOPT2  INIT      "Unit/Clinic Code Sequence          "
DESCOPT3  INIT      "Doctor Code Sequence               "
.
MENU1P    INIT      "Proc Grp Code Seq"
MENU2P    INIT      "Unit/Clin Code Seq"
MENU3P    INIT      "Doctor Code Seq"
.
KEYOPT1   INIT      "u10-12,13-21"
KEYOPT2   INIT      "u7-9,13-21"
KEYOPT3   INIT      "u1-6,13-21"
.
.************************************************************************
.*                        VARIABLES                                     *
.************************************************************************
ADDEDPR   FORM      8
ADDTOT    FORM      8
ADDTTT    FORM      8
CANHOSPR  FORM      5
CANOTHPR  FORM      4
CANPATPR  FORM      5
CLOSEPR   FORM      8
CLOSETOT  FORM      8
CLOSETTT  FORM      8
CMDLINE   DIM       80
CODE      DIM       2
DESCOPT   DIM       40               * display option
DIM3      DIM       3
DIM8      DIM       8
DISPDATE  DIM       10               * print date
DOCCODE   DIM       6
DOCNAM    DIM       15               * print doctors name
DOCTCODE  DIM       6
DOLIST    FORM      5
F365      FORM      "365"
FRDATE    DIM       8
HOSTOT    FORM      5                * totals of CANHOS's
HOSTTT    FORM      5                * totals of totals of CANHOS's
INPATPR   FORM      6
INPATTOT  FORM      6
INPATTTT  FORM      6
JDOLIST   FORM      3
MAXPR     FORM      4                * total of the means
MAXTOT    FORM      6                * total of the means
MAXTTT    FORM      6                * total of total of the means
MEANTOT   FORM      6                * total of the maximums
MEANTTT   FORM      6                * total of total of the maximums
OTHTOT    FORM      5                * total of the CAMOTH's
OTHTTT    FORM      5                * total of total of the CAMOTH's
PATCNT    FORM      4                * printed patient count
PATNAME   DIM       29               * print patients name
PATTOT    FORM      5                * total of the CANPAT's
PATTTT    FORM      5                * total of total of the CANPAT's
PFRDATE   DIM       10               * print from date
PRCODE    DIM       9
PRDOCT    DIM       6
PRGRP     DIM       3
PRINT34   DIM       34
PRNTHEAD  DIM       20
PROCDESC  DIM       40               * print proc. description
PROCNUMT  FORM      10
PROCSUMT  FORM      10
PRUNIT    DIM       3
PTCNDCQS  FORM      1
PTODATE   DIM       10               * print to date
REMOVPR   FORM      8
REMOVTOT  FORM      8
REMOVTTT  FORM      8
STARTPR   FORM      8
STARTTOT  FORM      9
STARTTTT  FORM      9
STRTFLAG  FORM      1
TODATE    DIM       8
TREATPR   FORM      8
TREATTOT  FORM      8
TREATTTT  FORM      8
UNITCODE  DIM       3
UNITDESC  DIM       17               * print unit description
WATTEMP1  DIM       8
WATTEMP2  DIM       8
WMDOCNAM  DIM       18
.
. ==========================
. TEMP FILES
. ==========================
.file variables
.                          Physical     Start
.WMDOCTOR DIM       6         6           1
.WMUNIT   DIM       3         3           7
.WPGRP    DIM       3         3          10
.WMCODE   DIM       9         9          13
START     FORM      8         5          22
ADDED     FORM      7         5          27
INPAT     FORM      6         4          32
TREAT     FORM      8         5          36
REMOVE    FORM      8         5          41
CLOSE     FORM      8         5          46
MEAN      FORM      4         3          51
MAX       FORM      4         3          54
CANHOS    FORM      5         4          57
CANPAT    FORM      5         4          61
CANOTH    FORM      4         3          65
. EOF                                    68
TEMPFIL2  IFILE     SQL, FIXED=25, KEY="u1-3,4-12"
PROCGRP   DIM       3         3           1
PROCCODE  DIM       9         9           4
PROCSUM   FORM      10        6          13
PROCNUM   FORM      10        6          19
.EOF                                     25
TEMPFIL3  IFILE     SQL, FIXED=25, KEY="u1-3,4-12"
PROCUNIT  DIM       3         3           1
.PROCCODE  DIM       9        9           4
.PROCSUM   FORM      10       6          13
.PROCNUM   FORM      10       6          19
.EOF                                     25
TEMPFIL4  IFILE     SQL, FIXED=28, KEY="u1-6,7-15"
PROCDOCT  DIM       6         6           1
.PROCCODE  DIM       9        9           7
.PROCSUM   FORM      10       6          16
.PROCNUM   FORM      10       6          22
.EOF                                     28
.
.************************************************************************
PRGID     INIT      "IBAWAT36"
PRGNAM    INIT      "Booking Analysis"
VERSION   INIT      "V12.01.00"
.************************************************************************
.*                         MAINLINE                                     *
.************************************************************************
ML0000
          CALL      INIT0000           * Display header and open files
.
ML1000    CALL      OPTN0000           * select option
          BRANCH    MOPTION,ML2000,ML2000,ML2000
          GOTO      ML9999             * exit chosen
.
ML2000    CALL      SCRR0000           * date screen
ML2100    CALL      DRNG0000           * Get date range
ML3000    BRANCH    MOPTION,ML6000,ML4000,ML5000
.
ML4000    CALL      UNIT0000           * keyin unit code
          GOTO      ML6000
.
ML5000    CALL      DOCT0000           *get doctor code
          GOTO      ML6000
.
ML6000    CALL      CONTQST            * ok to continue?
          BRANCH    CEXIT,ML7000,ML2000,ML1000
.
ML7000    CALL      EXTR0000           * extract records and put in tempfile
          CALL      CANC0000           * calculate cancelled patients
          CALL      PBAN0000           * produce printout
          GOTO      ML1000
ML9999    CALL      KILL0000           * Delete temp file
          CHAIN     PGM
.************************************************************************
.*                         INIT0000                                     *
.*               Display Header and open files                          *
.*                   called by : ML0000                                 *
.************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P60:24,"opening controlf"
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,TWENTY1;*247,PTCNDCQS
          READ      CONTROLF,THIRTY7;*140,WTCNTRG1,WTCNTRG2,WTCNTRG3,WTCNTRG4:
                                          WTCNTRG5,WTCNTRG6:
                                     *122,WTCNTPUC,*185,WTCNTPDS
.
          CLEAR     DESCOPT1           * set up Treatment/Procedure value
          APPEND    WTCNTPDS,DESCOPT1
          APPEND    " Group Code Sequence      ",DESCOPT1
          RESET     DESCOPT1
.
          DISPLAY   *P60:24,"opening patcodes"
          OPEN      PATCODE1,"patcodes"
          DISPLAY   *P60:24,"opening patdo1af"
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
          DISPLAY   *P68:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
          DISPLAY   *P68:24,"patdschf"
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P68:24,"watproaf"
          OPEN      WATPROA1,"watproaf"
          DISPLAY   *P68:24,"wattr1af"
          OPEN      WATTR1A3,"wattr1af"
          DISPLAY   *P68:24,"watcataf"
          OPEN      WATCATA2,"watcataf"
.
          MOVE     "60",CLNO
.
          CALL      TFILENAM * Get temp file name in create
          MOVE      TFILNAME,WATTEMP1
.
          CALL      TFILENAM * Get temp file name in create
          MOVE      TFILNAME,WATTEMP2
.
INIT9999  RETURN
.************************************************************************
.*                        OPTN0000                                      *
.*             select option                                            *
.*                   called by : ML1000                                 *
.************************************************************************
OPTN0000  CALL      SCRN0000           * Display screen
.
OPTN1000  KEYIN     *P17:9,*DV,UNDLN1,*P17:9,*V2LON,MOPTION,*DV,*P17:9,MOPTION
          BRANCH    MOPTION,OPTN2000,OPTN2000,OPTN2000
.
          COMPARE   ZERO,MOPTION        * exit chosen
          GOTO      OPTN9999 IF EQUAL
          BEEP                         * invalid entry
          GOTO      OPTN1000
.
OPTN2000  LOAD      DESCOPT,MOPTION,DESCOPT1,DESCOPT2,DESCOPT3
          DISPLAY   *P48:2,*V2LON,"- ",*+,DESCOPT,SP1    * display screen head
.
          LOAD      PRNTHEAD,MOPTION,MENU1P,MENU2P,MENU3P
          MOVE      "- ",KEY2
          PACK      CPHDROPT,KEY2,PRNTHEAD
          GOTO      OPTN9999
.
OPTN9999  RETURN
.************************************************************************
.*                        SCRN0000                                      *
.*                Display main options screen                           *
.*                   called by : OPTN0000                               *
.************************************************************************
SCRN0000  HLINE     *G37,2,48,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = ",DESCOPT1:
                    *P1:6,*V2LON,TWO,*HOFF," = ",DESCOPT2:
                    *P1:7,*V2LON,THREE,*HOFF," = ",DESCOPT3:
                    *P1:9,"Select Option : "
.
SCRN9999  RETURN
.************************************************************************
.*                         SCRR0000                                     *
.*             Screen for date keyins                                   *
.*                   called by : ML1000                                 *
.************************************************************************
SCRR0000  DISPLAY   *P1:11,*EF,"From Date on List : ":
                    *P1:12,"To   Date on List : "
.
SCRR9999  RETURN
.************************************************************************
.************************************************************************
.*                        DTRN0000->DRNG0000                            *
.*           Input the admission date range required                    *
.*                   called by : ML1000               
.    Modified 25/07/1990 Bill Dew
.************************************************************************
DRNG0000
          UNPACK    SP20,PFRDATE,PTODATE
          UNPACK    SP20,FRDATE,TODATE
          UNPACK    SP10,CDAY,CMON,CCENT,CYEAR
.
          MOVE      CCC,CCENT
          MOVE      "21",CCOL               * set up position for keydate
          MOVE      TEN1,CVERT
          MOVE      ZERO,CHIGHLT
          CALL      KEYDATE            * get start date
.
          COMPARE   ZERO,OVRCD         * test for return hit
          GOTO      DRNG1111 IF EQUAL
          MOVE      "Start    ",PFRDATE     * set start if return hit
          DISPLAY   *PCCOL:CVERT,*V2LON,PFRDATE
          GOTO      DRNG2222
DRNG1111
          PACK      FRDATE,CCENT,CYEAR,CMON,CDAY
          REPLACE   " 0",FRDATE
          PACK      PFRDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          REPLACE   " 0",PFRDATE
DRNG2222
          UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          MOVE      TEN2,CVERT         * line for keydate
          CALL      KEYDATE            * get second date range
.
          COMPARE   ZERO,OVRCD         * test for return hit
          GOTO      DRNG3333 IF EQUAL
          MOVE      "99999999",TODATE  * set filedate to highest value
          MOVE      "Finish    ",PTODATE
          DISPLAY   *PCCOL:CVERT,*V2LON,PTODATE
          GOTO      DRNG4444
DRNG3333
          PACK      TODATE,CCENT,CYEAR,CMON,CDAY
          REPLACE   " 0",TODATE
          PACK      PTODATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          REPLACE   " 0",PTODATE
DRNG4444
          MATCH     FRDATE,TODATE      * test dates are correct sequence
          GOTO      DRNG9999 IF NOT LESS
          DISPLAY   *P1:24,*EL,*B,"The To date must be after From date.  ";
          CALL      HITENTER
          GOTO      DRNG0000
DRNG9999  RETURN
.************************************************************************
.*                         UNIT0000                                     *
.*             Keyin the unit that will be printed                      *
.*                   called by : ML4000                                 *
.************************************************************************
UNIT0000  DISPLAY   *P1:14,"Unit/Clinic Code : "
          MOVE      ONE,STRTFLAG           * come back to here if error
.
UNIT1000  KEYIN     *P20:14,*DV,UNDLN3,*P20:14,*V2LON,UNITCODE:
                    *P20:14,*DV,UNITCODE
.
          ENDSET    UNITCODE
          APPEND    SP10,UNITCODE
          RESET     UNITCODE
.
          MATCH     SP3,UNITCODE                * nothing entered?
          GOTO      UNIT8000 IF EQUAL
.
          MATCH     QUEST,UNITCODE              * ?
          GOTO      UNIT4000 IF NOT EQUAL
.
UNIT2000  MOVE      CODEWU,CODE                 * set up category
          MOVE      ONE,CNEWDS                  * new ?
          MOVE      ZERO,STRTFLAG               * come back to here
          CALL      PATCODDS
.
UNIT3000  DISPLAY   *P1:24,"Group Code : "
          KEYIN     *P14:24,*EL,*DV,UNDLN3,*P14:24,UNITCODE
.
          ENDSET    UNITCODE
          APPEND    SP10,UNITCODE
          RESET     UNITCODE
.
          MATCH     SP3,UNITCODE                * nothing entered?
          GOTO      UNIT7000 IF EQUAL
.
          MATCH     QUEST,UNITCODE              * ?
          GOTO      UNIT2000 IF EQUAL
.
UNIT4000  PACK      KEY5,CODEWU,UNITCODE
          CALL      RDCODE1
          BRANCH    OVRCD,UNIT6000              * not on file
.
          BRANCH    STRTFLAG,UNIT5000           * do we have to redisplay
.          
          CALL      REDP0000           * redisplay screen
          DISPLAY   *P20:14,*V2LON,UNITCODE,*HOFF,*P35:14,TDESC
          GOTO      UNIT9999
.
UNIT5000  DISPLAY   *P35:14,TDESC
          MOVE      TDESC,UNITDESC
          GOTO      UNIT9999
.
UNIT6000  DISPLAY   *P1:24,"Invalid code. ";
          CALL       HITENTER
          BRANCH     STRTFLAG,UNIT1000          * were do we go back to
          GOTO       UNIT3000
.
UNIT7000  
          CALL      REDP0000
          GOTO      UNIT0000
UNIT8000
          DISPLAY   *P20:14,*V2LON,*EL,"All"
.
UNIT9999  RETURN
.*******************************************************************************
.                                 REDP0000
.    Display screen after a code screen 
.    Created by Bill Dew 26/07/1990
.*******************************************************************************
REDP0000
          CALL      SCRN0000                    * redisplay screen
          CALL      SCRR0000
          DISPLAY   *P17:9,*V2LON,MOPTION,*P21:11,PFRDATE,*P21:12,PTODATE
          BRANCH    MOPTION,REDP9999,REDP2222,REDP4444
REDP2222
          DISPLAY   *P1:14,"Unit/Clinic Code : "
          GOTO      REDP9999
REDP4444
          DISPLAY   *P1:14,"Doctor Code : "
REDP9999  RETURN
.************************************************************************
.*                         DOCT0000                                     *
.*             Keyin the DOCT that will be printed                      *
.*                   called by : ML4000                                 *
.************************************************************************
DOCT0000  DISPLAY   *P1:14,"Doctor Code : "      
.
          MOVE      SP10,DOCTCODE
          MOVE      "15",ECOL
          MOVE      "14",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          CALL      PATDOCKY
          BRANCH    EXIT,DOCT8000,DOCT0000
.
          MOVE      DCODE,DOCTCODE
.
          PACK      KEY6,DOCTCODE
          MOVE      DSNAME,PACSNAME             * format doctors name
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,DOCNAM
          DISPLAY   *P35:14,DOCNAM
          GOTO      DOCT9999
.
DOCT8000  DISPLAY   *P15:14,*V2LON,*EL,"All"
.
DOCT9999  RETURN
.*******************************************************************************
.                                 CANC0000
.    Created by Bill Dew 27/07/1990
.    Read the watcataf for cancellation codes category BC and test indicators
.    for cancellations by patient hospital or other.
.*******************************************************************************
CANC0000
          PACK      KEY29,WMURNO,WMCODE,WMCNT,SP20
          CALL      RSWTCAT2           * position fptr in watcataf
CANC2222
          CALL      RKWTCAT2
          BRANCH    OVRCD,CANC9999
.
          MATCH     WTCAURNO,WMURNO    * test same patient
          GOTO      CANC9999 IF NOT EQUAL
          MATCH     WTCAPROC,WMCODE    * test same procedure code 
          GOTO      CANC9999 IF NOT EQUAL
          COMPARE   WTCAPCNT,WMCNT     * test same procedure code counter
          GOTO      CANC9999 IF NOT EQUAL
          MATCH     CODEBC,WTCACATF    * test for a cancellation code BC
          GOTO      CANC2222 IF NOT EQUAL
          MATCH     FRDATE,WTCAEDAT    * test if greater end date range
          GOTO      CANC2222 IF LESS
          MATCH     WTCAEDAT,TODATE    * test if greater end date range
          GOTO      CANC2222 IF LESS
.
          PACK      KEY5,CODEBC,WTCACODT
          CALL      RDCODE1            * get 2nd indicator 
          BRANCH    OVRCD,CANC2222
.
          MATCH     ANSH,TCINDC2       * test cancelled by hospital
          GOTO      CANC5555 IF NOT EQUAL
          ADD       ONE,CANHOS         * increment cancelled by hosp counter
          GOTO      CANC2222
CANC5555
          MATCH     ANSP,TCINDC2       * test cancelled by patient
          GOTO      CANC6666 IF NOT EQUAL
          ADD       ONE,CANPAT         * increment cancelled by patient counter
          GOTO      CANC2222
CANC6666
          ADD       ONE,CANOTH         * increment canelled by other code
          GOTO      CANC2222
CANC9999  RETURN
.*******************************************************************************
.*                        EXTR0000                                      *
.*            Extract values and put in tempfile                        *
.*                   called by : ML1000                                 *
.************************************************************************
EXTR0000  CALL      CREA0000
          DISPLAY   *P1:24,*EL,"Extracting record : "
          MOVE      ZERO,STRTFLAG
          PACK      KEY27,SP20,SP10    * position at start
          CALL      RDSTREA3
.
EXTR1000  CALL      RDKTREA3
          BRANCH    OVRCD,EXTR7000
.
          MATCH     WMDATE1,TODATE     * must be on list before to date
          GOTO      EXTR7000 IF LESS
.
.    there were some patients to process
.
          MOVE      ONE,STRTFLAG        * so set wmstat to 1
          MOVE      SP3,WPGRP
          MOVE      WMCODE,KEY9
          CALL      RDPROA1            * get procedure group code
.
          BRANCH    MOPTION,EXTR2000,EXTR3000,EXTR4000
.
EXTR2000  PACK      KEY21,WPGRP,WMCODE,SP30
          GOTO      EXTR5000
.
EXTR3000  PACK      KEY21,WMUNIT,WMCODE,SP30
          MATCH     SP3,UNITCODE       * printing all?
          GOTO      EXTR5000 IF EQUAL
          MATCH     UNITCODE,WMUNIT    * if not - is this the right code
          GOTO      EXTR1000 IF NOT EQUAL * no - go back
          GOTO      EXTR5000
.
EXTR4000  PACK      KEY21,WMDOCTOR,WMCODE,SP30
          MATCH     SP6,DOCTCODE       * printing all?
          GOTO      EXTR5000 IF EQUAL
          MATCH     DOCTCODE,WMDOCTOR  * if not - is this the right code
          GOTO      EXTR1000 IF NOT EQUAL * no - go back
.
EXTR5000  CALL      RDTMPR1
          BRANCH    OVRCD,EXTR6000     * new record
.
EXTR5100
          DISPLAY   *P21:24,*V2LON,WMCODE
          CALL      STRT0000           * update no. of patients on list at start
          CALL      ADDD0000           * update patients added to list
          CALL      INPT0000           * update patients admitted
          CALL      TRET0000           * update patients discharged
          CALL      RMOV0000           * update patients removed
          CALL      CLSE0000           * update patients on list at todate
          CALL      CDOL0000
          CALL      UPMN0000           * update mean tempfile
          CALL      MAX0000
          CALL      CANC0000
          CALL      UPTMPR1
          GOTO      EXTR1000
.
EXTR6000
          DISPLAY   *P21:24,*V2LON,WMCODE
          CALL      SETF0000           * Set var to write
          CALL      STRT0000           * update no. of patients on list at start
          CALL      ADDD0000           * update patients added to list
          CALL      INPT0000           * update patients admitted
          CALL      TRET0000           * update patients discharged
          CALL      RMOV0000           * update patients removed
          CALL      CLSE0000           * update patients on list at todate
          CALL      CDOL0000
          CALL      UPMN0000           * update mean tempfile
          CALL      MAX0000
          CALL      CANC0000
          CALL      WRTMPR1
          GOTO      EXTR1000
.
EXTR7000  BRANCH    STRTFLAG,EXTR9999  * any written to tempfile?
          DISPLAY   *P1:24,"** No patients admitted **",*W,*P1:24,*EL;
          GOTO      EXTR9999
EXTR9999  RETURN
.************************************************************************
.*                        ADDD0000                                      *
.*           Update no. of patients added to list in period             *
.*                   called by : EXTR5000                               *
.************************************************************************
ADDD0000  
          MATCH     FRDATE,WMDATE1        
          GOTO      ADDD9999 IF LESS
          MATCH     WMDATE1,TODATE     * is date on list in range
          GOTO      ADDD9999 IF LESS
.
          ADD       ONE,ADDED          * add one to number added to list  
.
ADDD9999  RETURN
.************************************************************************
.*                        STRT0000                                      *
.*          Update the number of patients on list at start              *
.*                   called by : EXTR5000                               *
.************************************************************************
STRT0000  BRANCH    WMSTAT,STRT1111,STRT1111,STRT1111,STRT1111:
                           STRT9999,STRT9999,STRT1111,STRT1111
.
STRT1111  MATCH     WMDATE1,FRDATE     * were they on the list at the start
          GOTO      STRT9999 IF LESS
.
          ADD       ONE,START          * update patients on list at start 
.
STRT9999  RETURN 
+
.************************************************************************
.*                         INPT0000                                     *
.*           Update the number of patients admitted in period           *
.*                   called by : EXTR5000                               *
.************************************************************************
INPT0000  BRANCH    WMSTAT,INPT9999,INPT9999,INPT9999
.         
          MOVE      WMBOOK,KEY8             * get admission date
          CALL      RDMISS1
          BRANCH    OVRCD,INPT9999
.
          UNPACK    FRDATE,DIM8
          MATCH     KEY8,ADATE           * were they admitted in the period
          GOTO      INPT9999 IF LESS
.
          UNPACK    TODATE,KEY8     * get to date into a dim 6
          MATCH     ADATE,KEY8           * were they admitted in the period
          GOTO      INPT9999 IF LESS
.
          ADD       ONE,INPAT               * update patients admitted
.
INPT9999  RETURN        
+
.************************************************************************
.*                         TRET0000                                     *
.*        Update no. of patients discharged during period               *
.*                   called by : TRET0000                               *
.************************************************************************
TRET0000  BRANCH    WMSTAT,INPT9999,INPT9999,INPT9999,INPT9999
.
          MOVE      WMBOOK,KEY8            * get discharge date
          CALL      RDDSCH1
          BRANCH    OVRCD,TRET9999
.
          UNPACK    FRDATE,KEY8    * get todate into dim 6
          MATCH     KEY8,DDATE          * were they discharged in the period
          GOTO      TRET9999 IF LESS
.
          UNPACK    TODATE,KEY8    * get todate into dim 6
          MATCH     DDATE,KEY8          * were they discharged in the period
          GOTO      TRET9999 IF LESS
.
          ADD       ONE,TREAT              * add one to patients discharged
.
TRET9999  RETURN
.************************************************************************
.*                         RMOV0000                                     *
.*            Update the number of patients removed from list
.*                   called by : EXTR5000                               *
.************************************************************************
RMOV0000  BRANCH    WMSTAT,RMOV9999,RMOV9999,RMOV9999,RMOV9999:
                           RMOV9999,RMOV1000,RMOV9999,RMOV1000
.
RMOV1000  MATCH     FRDATE,WMDATE4        * must be after from date
          GOTO      RMOV9999 IF LESS
.
          MATCH     WMDATE4,TODATE        * must be before todate 
          GOTO      RMOV9999 IF LESS
.
          ADD       ONE,REMOVE
.
RMOV9999  RETURN
.************************************************************************
.*                         CLSE0000                                     *
.*      Update number of people on list at end                          *
.*                   called by :  EXTR5000                              *
.*   Comment 26/07/1990 Bill Dew                                        ******
.*   This close field is the number of patients remaining on the waiting list*
.*   Which have status of 1 2 3 4.  This value can be calculated by          *
.*   summing the number at start, added to list and current inpatients then  *
.*   subtracting the treated and discharged.                                 *
.*   I was ordered by Mr Duncan to make routine headings pretty.        ******
.************************************************************************
CLSE0000
          MOVE      START,CLOSE          * number on at start
          ADD       ADDED,CLOSE          * plus number added
          ADD       INPAT,CLOSE          * plus number inpatient
          SUB       TREAT,CLOSE          * minus patients discharged
          SUB       REMOVE,CLOSE         * minus patients removed
CLSE9999   RETURN
.************************************************************************       
.*                        CDOL0000                                      *
.*              Calculate number of days on list for patient            *
.*                   called by : EXTR1500                               *
.************************************************************************       
CDOL0000  UNPACK    WMDATE1,XCENT,XYEAR,XMON,XDAY
          BRANCH    WMSTAT,CDOL0400,CDOL0400,CDOL0400,CDOL0400:
                           CDOL0450,CDOL0460,CDOL0400,CDOL0460
.
. unscheduled/scheduled/pre-ad/admitted/performed
.
CDOL0400  UNPACK    TODATE,CCENT,CYEAR,CMON,CDAY
          GOTO      CDOL0600
.
. discharged
.
CDOL0450  UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
          GOTO      CDOL0600
.
. removed/not performed
.
CDOL0460  UNPACK    WMDATE4,CCENT,CYEAR,CMON,CDAY
          GOTO      CDOL0600
.
CDOL0600  MOVE      XCENT,CC
          MOVE      XYEAR,YY
          MOVE      XMON,MM
          MOVE      XDAY,DD
          CALL      DMYCON                       * get julien date
          MOVE      JULDAY,JDOLIST
.
          MOVE      CCENT,CC
          MOVE      CYEAR,YY
          MOVE      CMON,MM
          MOVE      CDAY,DD
          CALL      DMYCON                       * get julien date
.
          MATCH     XYEAR,CYEAR                  * same year?
          GOTO      CDOL1000 IF EQUAL
.
          MOVE      ZERO,DOLIST
          MOVE      CYEAR,DOLIST
          MOVE      XYEAR,FORM2
          SUB       FORM2,DOLIST                 * no - how many years then
          MULT      F365,DOLIST                   * convert to days
          ADD       F365,DOLIST
          SUB       JDOLIST,DOLIST               * sub the bit of the first year
                                                 * that they werent on the list
          ADD       JULDAY,DOLIST                * add part of the last year 
                                                 * that they were
.
          SUB       F365,DOLIST                  * if so sub one
          GOTO      CDOL9999
.
CDOL1000  MOVE      JULDAY,DOLIST                * same year - easy
          SUB       JDOLIST,DOLIST
          GOTO      CDOL9999
CDOL9999 
          RETURN
.************************************************************************
.*                         UPMN0000                                     *
.*      Update vakues in 'mean' tempfile                                *
.*                   called by : EXTR1500                               *
.************************************************************************
UPMN0000
          BRANCH    MOPTION,UPMN1000,UPMN3000,UPMN5000
UPMN1000
          PACK      KEY12,WPGRP,WMCODE
          READ      TEMPFIL2,KEY12;PROCGRP,PROCCODE,PROCSUM,PROCNUM
          GOTO      UPMN2000 IF OVER
.
          ADD       DOLIST,PROCSUM       * Update total no. of days on list 
          ADD       ONE,PROCNUM          * Update no. of pat. on list
          UPDATE    TEMPFIL2;PROCGRP,PROCCODE,PROCSUM,PROCNUM
          GOTO      UPMN9999
UPMN2000
          MOVE      DOLIST,PROCSUM       * update em
          MOVE      ONE,PROCNUM 
          MOVE      WPGRP,PROCDOCT
          MOVE      WMCODE,PROCCODE
          WRITE     TEMPFIL2,KEY12;KEY12,PROCSUM,PROCNUM
          GOTO      UPMN9999
.
uPMN3000
          PACK      KEY12,WMUNIT,WMCODE
          READ      TEMPFIL3,KEY12;PROCUNIT,PROCCODE,PROCSUM,PROCNUM
          GOTO      UPMN4000 IF OVER
.
          ADD       DOLIST,PROCSUM       * Update total no. of days on list 
          ADD       ONE,PROCNUM          * Update no. of pat. on list
          UPDATE    TEMPFIL3;PROCUNIT,PROCCODE,PROCSUM,PROCNUM
          GOTO      UPMN9999
UPMN4000
          MOVE      DOLIST,PROCSUM       * update em
          MOVE      ONE,PROCNUM 
          MOVE      WMDOCTOR,PROCDOCT
          MOVE      WMCODE,PROCCODE
          WRITE     TEMPFIL3,KEY12;KEY12,PROCSUM,PROCNUM
          GOTO      UPMN9999
.
UPMN5000  PACK      KEY15,WMDOCTOR,WMCODE
          READ      TEMPFIL4,KEY15;PROCDOCT,PROCCODE,PROCSUM,PROCNUM
          GOTO      UPMN6000 IF OVER
.
          ADD       DOLIST,PROCSUM       * Update total no. of days on list 
          ADD       ONE,PROCNUM          * Update no. of pat. on list
          UPDATE    TEMPFIL4;PROCDOCT,PROCCODE,PROCSUM,PROCNUM
          GOTO      UPMN9999
.
UPMN6000  
          MOVE      DOLIST,PROCSUM       * update em
          MOVE      ONE,PROCNUM 
          MOVE      WMDOCTOR,PROCDOCT
          MOVE      WMCODE,PROCCODE
          WRITE     TEMPFIL4,KEY15;PROCDOCT,PROCCODE,PROCSUM,PROCNUM
.
UPMN9999
          RETURN 
.************************************************************************
.*                         MAX0000                                      *
.*                 Update maximum if needed                             *
.*                   called by : EXTR1500                              *
.************************************************************************
MAX0000
         COMPARE    MAX,DOLIST             * is this number of days greater 
         GOTO       MAX9999 IF LESS
         MOVE       DOLIST,MAX 
MAX9999  RETURN
.************************************************************************
.*                         SETF0000                                     *
.*            Set the fields for writing a new record                   *
.*                   called by : EXTR0000                               *
.************************************************************************
SETF0000  MOVE      ZERO,START  
          MOVE      ZERO,ADDED  
          MOVE      ZERO,INPAT  
          MOVE      ZERO,TREAT  
          MOVE      ZERO,REMOVE
          MOVE      ZERO,CLOSE
          MOVE      ZERO,MEAN     
          MOVE      ZERO,MAX
          MOVE      ZERO,CANHOS 
          MOVE      ZERO,CANPAT 
          MOVE      ZERO,CANOTH 
SETF9999  RETURN
.************************************************************************
.*                         PBAN0000                                      *
.    Print Booking Analysis
.    Created 30/071990 Bill Dew
.************************************************************************
PBAN0000  MOVE      ZERO,CPAGENO      * Set up heading variables
          MOVE      ZERO,PATCNT
          CLOCK     TIME,CTIMEIS
          MOVE      ONE,CNOUNDLN
          DISPLAY   *P1:24,*EL,"Printing Record : "
.
          MOVE      SP20,PRGRP
          MOVE      SP20,PRUNIT
          MOVE      SP20,PRDOCT
          MOVE      SP20,PRCODE
.
PBAN1000  PACK      KEY21,SP30
          CALL      RSTMPR1
.
PBAN1500  CALL      RKTMPR1
          BRANCH    OVRCD,PBAN5000
.
          BRANCH    MOPTION,PBAN1600,PBAN1700,PBAN1800
.
PBAN1600
          IF        CPAGENO=0
               CALL      HEAD0000
          ENDIF
.
          MATCH     WPGRP,PRGRP
          GOTO      PBAN3333 IF EQUAL
          CALL      PRST0000           * Print total for last group
          CALL      HEAD0000
          MOVE      WPGRP,PRGRP
          GOTO      PBAN3333
PBAN1700
.          IF        CPAGENO=0
.               CALL      HEAD0000
.          ENDIF
.
          MATCH     WMUNIT,PRUNIT
          GOTO      PBAN3333 IF EQUAL
          CALL      PRST0000           * Print total for last group
          CALL      HEAD0000
          MOVE      WMUNIT,PRUNIT      * set up previous procedure code
          GOTO      PBAN3333
PBAN1800
          IF        CPAGENO=0
               CALL      HEAD0000
          ENDIF
.
          MATCH     WMDOCTOR,PRDOCT
          GOTO      PBAN3333 IF EQUAL
          CALL      PRST0000           * Print total for last doctor
          CALL      HEAD0000
          MOVE      WMDOCTOR,PRDOCT    * set up previous doctor code
.         GOTO      PBAN3333
PBAN3333
          DISPLAY   *P19:24,*V2LON,WMCODE
          COMPARE   "55",CLNO          * new page?
          CALL      HEAD0000 IF NOT LESS
          CALL      FRMT0000           * format data
          CALL      PRNT0000           * print a line of output
.         
          MOVE      WMCODE,PRCODE      * if so set prcode
          GOTO      PBAN1500
.
PBAN5000  CALL      PRST0000           * print sub-totals
          CALL      END0000            * print grand totals
.
PBAN9999  RETURN
.************************************************************************
.*                        HEAD0000                                      *
.*                 Produce printout heading                             *
.*                   called by : OUT0000,OUT1500,OUT2500                *
.************************************************************************
HEAD0000  
          CALL      HEAD132       
.
          BRANCH    MOPTION,HEAD1000,HEAD2000,HEAD3000
HEAD1000  PRINT     *45,"From Date on List : ",PFRDATE,*N:
                    *45,"To   Date on List : ",PTODATE,*N
          CALL      UND132
          GOTO      HEAD4000
.
HEAD2000  MATCH     SP3,UNITCODE
          GOTO      HEAD2500 IF NOT EQUAL
          PACK      KEY5,CODEWU,WMUNIT
          CALL      RDCODE1
          PRINT     "Unit/Clinic Code : All":
                    *45,"From Date on List : ",PFRDATE,*N:
                    *45,"To   Date on List : ",PTODATE,*N:
                    "Unit/Clinic Code : ",WMUNIT," - ",TDESC
          CALL      UND132[
          GOTO      HEAD4000
.
HEAD2500  PACK      KEY5,CODEWU,UNITCODE
          CALL      RDCODE1
          PRINT     "Unit/Clinic Code : ",UNITCODE," - ",UNITDESC:
                    *45,"From Date on List : ",PFRDATE,*N:
                    *45,"To   Date on List : ",PTODATE,*N;
          CALL      UND132
          GOTO      HEAD4000
.
HEAD3000  MATCH     SP6,DOCTCODE
          GOTO      HEAD3500 IF NOT EQUAL
          MOVE      WMDOCTOR,KEY6
          MOVE      "Unknown doctor",WMDOCNAM
          CALL      RDDOCT1 
          BRANCH    OVRCD,HEAD3100
          MOVE      DSNAME,PACSNAME     * format doctors name
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,WMDOCNAM
HEAD3100  PRINT     "Doctor Code : All":
                    *40,"From Scheduled Admission Date : ",PFRDATE,*N:
                    *40,"From Scheduled Admission Date : ",PTODATE,*N:
                    "Doctor Code : ",WMDOCTOR," - ",WMDOCNAM
          CALL      UND132
          GOTO      HEAD4000
.
.         
HEAD3500  MOVE      WMDOCTOR,KEY6
          MOVE      "Unknown doctor",WMDOCNAM
          CALL      RDDOCT1 
          BRANCH    OVRCD,HEAD3600
          MOVE      DSNAME,PACSNAME     * format doctors name
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,WMDOCNAM

HEAD3600  PRINT     "Doctor Code : ",DOCTCODE," - ",DOCNAM:
                    *40,"From Scheduled Admission Date : ",PFRDATE,*N:
                    *40,"From Scheduled Admission Date : ",PTODATE,*N:
                    "Doctor Code : ",WMDOCTOR," - ",WMDOCNAM
          CALL      UND132
.
HEAD4000  
          PRINT     "|",*44,"|",*68,"Movements",*98,"|",*102,"Statistics":
                    *114,"|",*119,"Cancelled",*132,"|",*N,*1,"|":
                    *3,WTCNTPDS," Description",*44,"|",*49,"Start",*55,"|":
                    *58,"Added",*63,"|",*66,"Inpat",*71,"|",*73,"Treated":
                    *80,"|",*82,"Removed",*89,"|",*93,"Close",*98,"|":
                    *101,"Mean",*106,"|",*110,"Max",*114,"|",*117,"Hos":
                    *120,"|",*123,"Pat",*126,"|",*129,"Oth",*132,"|"
          CALL      UND132
.
          ADD      SEVEN,CLNO
.
HEAD9999  RETURN
.************************************************************************
.*                        FRMT0000                                      *
.*          Convert data to the format to print                         *
.*                   called by : OUT1500,OUT2500                        *
.************************************************************************
FRMT0000   MOVE     WMCODE,PROCDESC
           SCAN     SP6,PROCDESC
           APPEND   " - Unknown ",PROCDESC
           APPEND   WTCNTPDS,PROCDESC
           MOVE     WMCODE,KEY9         
           CALL     RDPROA1              * get proc. description
           BRANCH   OVRCD,FRMT0500
           MOVE     WPDESC,PROCDESC      * move it to the print variable
.
.work out mean - based on group - option 1
.
FRMT0500   BRANCH   MOPTION,FRMT1000,FRMT2000,FRMT3000
.
FRMT1000   PACK     KEY12,WPGRP,WMCODE
           READ     TEMPFIL2,KEY12;PROCGRP,PROCCODE,PROCSUM,PROCNUM
           GOTO     FRMT4000 IF OVER
.
          MOVE      PROCSUM,MEAN       * calculate mean
          DIV       PROCNUM,MEAN
           ADD      PROCSUM,PROCSUMT          * accumulate totals
           ADD      PROCNUM,PROCNUMT
          GOTO      FRMT4000
.
.work out mean - based on unit - option 2
.
FRMT2000   PACK     KEY12,WMUNIT,WMCODE
           READ     TEMPFIL3,KEY12;PROCUNIT,PROCCODE,PROCSUM,PROCNUM
           GOTO     FRMT4000 IF OVER
.
          MOVE      PROCSUM,MEAN       * calculate mean
          DIV       PROCNUM,MEAN
           ADD      PROCSUM,PROCSUMT          * accumulate totals
           ADD      PROCNUM,PROCNUMT
           GOTO     FRMT4000
.
.work out mean - based on doctor - option 3
.
FRMT3000   PACK     KEY15,WMDOCTOR,WMCODE
           READ     TEMPFIL4,KEY15;PROCDOCT,PROCCODE,PROCSUM,PROCNUM
           GOTO     FRMT4000 IF OVER
.
          MOVE      PROCSUM,MEAN       * calculate mean
          DIV       PROCNUM,MEAN
           ADD      PROCSUM,PROCSUMT          * accumulate totals
           ADD      PROCNUM,PROCNUMT
.
FRMT4000   ADD      START,STARTPR
           ADD      ADDED,ADDEDPR
           ADD      INPAT,INPATPR
           ADD      TREAT,TREATPR
           ADD      REMOVE,REMOVPR
           ADD      CLOSE,CLOSEPR 
           ADD      CANHOS,CANHOSPR
           ADD      CANPAT,CANPATPR
           ADD      CANOTH,CANOTHPR
.
           COMPARE  MAX,MAXPR
           GOTO     FRMT9999 IF NOT LESS
. 
           MOVE     MAX,MAXPR
.
FRMT9999   RETURN
.************************************************************************
.*                        PRNT0000                                      *
.*              Print a line of output                                  *
.*                   called by : OUT1500,OUT2500                        *
.************************************************************************
PRNT0000
          PRINT     "|",*3,PROCDESC,*44,"|",*46,STARTPR:
                    *55,"|",*56,ADDEDPR,*64,"|",*65,INPATPR,*71,"|",*72:
                    TREATPR,*80,"|",*81,REMOVPR,*89,"|",*90:
                    CLOSEPR,*98,"|",*101,MEAN,*106,"|",*109:
                    MAXPR,*114,"|",*115,CANHOSPR,*120,"|",*121:
                    CANPATPR,*127,"|",*128,CANOTHPR,*132,"|"
.
          ADD       ONE,CLNO                    * update line number
          ADD       STARTPR,STARTTOT            * update all the total variables
          ADD       ADDEDPR,ADDTOT
          ADD       INPATPR,INPATTOT
          ADD       TREATPR,TREATTOT
          ADD       REMOVPR,REMOVTOT
          ADD       CLOSEPR,CLOSETOT
          ADD       MEAN,MEANTOT
          ADD       MAXPR,MAXTOT
          ADD       CANHOSPR,HOSTOT
          ADD       CANPATPR,PATTOT
          ADD       CANOTHPR,OTHTOT
          SUB       STARTPR,STARTPR              * clear all the variables
          SUB       ADDEDPR,ADDEDPR
          SUB       INPATPR,INPATPR
          SUB       TREATPR,TREATPR
          SUB       REMOVPR,REMOVPR
          SUB       CLOSEPR,CLOSEPR
          SUB       MEAN,MEAN
          SUB       MAXPR,MAXPR
          SUB       CANHOSPR,CANHOSPR
          SUB       CANPATPR,CANPATPR
          SUB       CANOTHPR,CANOTHPR
.
PRNT9999  RETURN
.************************************************************************
.*                        PRST0000                                      *
.*           Print Sub-total for change in group,doc or unit            *
.*                   called by :  OUT1000                               *
.************************************************************************
PRST0000
          COMPARE   ZERO,CPAGENO
          GOTO      PRST9999 IF EQUAL
          CALL      UND132
          BRANCH    MOPTION,PRST1000,PRST2000,PRST3000
.
PRST1000
          CLEAR     PRINT34
          APPEND    "Unknown ",PRINT34
          APPEND    WTCNTPDS,PRINT34
          APPEND    " Sub-Total",PRINT34
.
          PACK      KEY5,CODEWG,PRGRP
          CALL      RDCODE1
          BRANCH    OVRCD,PRST1500
          PACK      PRINT34,SP30,SP30
          CLEAR     PRINT34
          MOVE      TDESC,PRINT34
          SETLPTR   PRINT34,34
          RESET     PRINT34           
          SCAN      SP6,PRINT34
          APPEND    "Sub-Total",PRINT34
.
PRST1500  PRINT     "|",*3,PRINT34;
          GOTO      PRST4000
.
PRST2000  MOVE      "Unknown Unit",TDESC
          PACK      KEY5,CODEWU,PRUNIT
          CALL      RDCODE1
          PRINT     "|",*3,"Total ",PRUNIT," - ",TDESC;
          GOTO      PRST4000
.
PRST3000  MOVE      "Unknown Doctor",WMDOCNAM
          MOVE      PRDOCT,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD,PRST3500
          MOVE      DSNAME,PACSNAME
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,WMDOCNAM
.
PRST3500  PRINT     "|",*3,"Total ",PRDOCT," - ",WMDOCNAM;
. 
PRST4000  PRINT     *44,"|",*45,STARTTOT,*55,"|",*56,ADDTOT,*64,"|":
                    *65,INPATTOT,*71,"|",*72,TREATTOT,*80,"|",*81,REMOVTOT:
                    *89,"|",*90,CLOSETOT,*98,"|",*99,MEANTOT,*106,"|":
                    *107,MAXTOT,*114,"|",*115,HOSTOT,*120,"|",*121,PATTOT:
                    *126,"|",*127,OTHTOT,*132,"|"
          CALL      UND132
          ADD       THREE,CLNO
.
          ADD       STARTTOT,STARTTTT   * update all the total-total variables
          ADD       ADDTOT,ADDTTT
          ADD       INPATTOT,INPATTTT
          ADD       TREATTOT,TREATTTT
          ADD       REMOVTOT,REMOVTTT
          ADD       CLOSETOT,CLOSETTT
          ADD       MEANTOT,MEANTTT
          ADD       MAXTOT,MAXTTT
          ADD       HOSTOT,HOSTTT 
          ADD       PATTOT,PATTTT
          ADD       OTHTOT,OTHTTT
.
          SUB       STARTTOT,STARTTOT   * clear all the total variables
          SUB       ADDTOT,ADDTOT
          SUB       INPATTOT,INPATTOT
          SUB       TREATTOT,TREATTOT
          SUB       REMOVTOT,REMOVTOT
          SUB       CLOSETOT,CLOSETOT
          MOVE      "     0",MEANTOT
          MOVE      "     0",MAXTOT
          MOVE      "    0",HOSTOT
          MOVE      "    0",PATTOT
          MOVE      "   0",OTHTOT
.
PRST9999  RETURN
.************************************************************************
.*                        END0000                                       *
.*             Print total and end of report                            *
.*                   called by :  OUT4000                               *
.************************************************************************
END0000  
          IF        CPAGENO=0
               CALL      HEAD0000
          ENDIF
           
          PRINT     "|",*3,"Grand Total",*44,"|",*45,STARTTTT,*55,"|":
                    *56,ADDTTT,*64,"|":
                    *65,INPATTTT,*71,"|",*72,TREATTTT,*80,"|",*81,REMOVTTT:
                    *89,"|",*90,CLOSETTT,*98,"|",*99,MEANTTT,*106,"|":
                    *107,MAXTTT,*114,"|",*115,HOSTTT,*120,"|",*121,PATTTT:
                    *126,"|",*127,OTHTTT,*132,"|"
          CALL      UND132
          PRINT     *N,*N,"*** End of Report ***"
          SUB       STARTTTT,STARTTTT   * clear all the total-total variables
          SUB       ADDTTT,ADDTTT
          SUB       INPATTTT,INPATTTT
          SUB       TREATTTT,TREATTTT
          SUB       REMOVTTT,REMOVTTT
          SUB       CLOSETTT,CLOSETTT
          SUB       MEANTTT,MEANTTT
          SUB       MAXTTT,MAXTTT
          SUB       HOSTTT,HOSTTT 
          SUB       PATTTT,PATTTT
          SUB       OTHTTT,OTHTTT
END9999   RETURN
.**********************************************************************
.*                              CREA0000                              *
.*                   Create the Indexed Temp File                     *
.**********************************************************************
CREA0000  CALL      KILL0000                       * kill existing temp file
.
.------ Build new indexed temp file ------
.
          MOVE      SP30,TEMPKEY
          LOAD      TEMPKEY,MOPTION,KEYOPT1,KEYOPT2,KEYOPT3
          BRANCH    MOPTION,CREA1000,CREA2000,CREA3000
.
CREA1000  CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    WATTEMP1,CMDLINE
          APPEND    " 68 u10-12,13-21",CMDLINE
          APPEND    SP30,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
          DISPLAY   *P60:24,"Opening tempfile"
          OPEN      TEMPFIL1,WATTEMP1
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    WATTEMP2,CMDLINE
          APPEND    " 25 u1-3,4-12",CMDLINE
          APPEND    SP30,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
          DISPLAY   *P60:24,"Opening tempfile"
          OPEN      TEMPFIL2,WATTEMP2
          GOTO      CREA9999
.
CREA2000  CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    WATTEMP1,CMDLINE
          APPEND    " 68 u7-9,13-21",CMDLINE
          APPEND    SP30,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPFIL1,WATTEMP1
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    WATTEMP2,CMDLINE
          APPEND    " 25 u1-3,4-12",CMDLINE
          APPEND    SP30,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
          DISPLAY   *P60:24,"Opening tempfile"
          OPEN      TEMPFIL3,WATTEMP2
          GOTO      CREA9999
.
CREA3000  CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    WATTEMP1,CMDLINE
          APPEND    " 68 u1-6,13-21",CMDLINE
          APPEND    SP30,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPFIL1,WATTEMP1
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    WATTEMP2,CMDLINE
          APPEND    " 28 u1-6,7-15",CMDLINE
          APPEND    SP30,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
          DISPLAY   *P60:24,"Opening tempfile"
          OPEN      TEMPFIL4,WATTEMP2
CREA9999  RETURN
+
.**********************************************************************
.*                              KILL0000                              *
.*                  Kill the Tempfile if it Already Exists            *
.**********************************************************************
KILL0000  CLEAR     CMDLINE
          CLOSE     TEMPFIL1
          APPEND    "iserase ",CMDLINE
          APPEND    WATTEMP1,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
          CLEAR     CMDLINE
          CLOSE     TEMPFIL2
          CLOSE     TEMPFIL3           * 27/09/1990 BD
          CLOSE     TEMPFIL4
          APPEND    "iserase ",CMDLINE
          APPEND    WATTEMP2,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
KILL9999  RETURN
+
.************************************************************************
.*                    I/O Routines for Tempfile                         *
.************************************************************************
RATMPR1   MOVE      ZERO,OVRCD
          RESET     KEY21
          READ      TEMPFIL1,KEY21;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY21
          READ      TEMPFIL1,KEY21;;
          RETURN
.
RDTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY21
          READ      TEMPFIL1,KEY21;WMDOCTOR,WMUNIT,WPGRP,WMCODE,START,ADDED:
                                  INPAT,TREAT,REMOVE,CLOSE,MEAN,MAX:
                                  CANHOS,CANPAT,CANOTH
          GOTO      OVERCOND IF OVER
          MOVE      DWMCNT,WMCNT
          RETURN
.
RKTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY21
          READKS    TEMPFIL1;WMDOCTOR,WMUNIT,WPGRP,WMCODE,START,ADDED:
                             INPAT,TREAT,REMOVE,CLOSE,MEAN,MAX:
                             CANHOS,CANPAT,CANOTH
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY21
          WRITE     TEMPFIL1,KEY21;WMDOCTOR,WMUNIT,WPGRP,WMCODE,START,ADDED:
                                  INPAT,TREAT,REMOVE,CLOSE,MEAN,MAX:
                                  CANHOS,CANPAT,CANOTH
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTMPR1  
          UPDATE    TEMPFIL1;WMDOCTOR,WMUNIT,WPGRP,WMCODE,START,ADDED:
                            INPAT,TREAT,REMOVE,CLOSE,MEAN,MAX:
                            CANHOS,CANPAT,CANOTH
          RETURN
.
.************************************************************************
.*                        I/O INCLUDES                                  *
.************************************************************************
.
          INC       STD001IO
          INC       PATCODKY
          INC       PATDOCKY
          INC       TFILENAM
.
          INC       IBASEQIO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATDSCIO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       WATCATIO/INC
          INC       WATPROIO/INC
          INC       WATTR1IO/INC
          INC       WEBERRIO/INC

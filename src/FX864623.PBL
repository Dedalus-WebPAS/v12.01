.***************************************************************************
.* System    :   Inpatients                                                *
.* Program   :   FX864623                                                  *
.* Desc      :   Update value for fields PMPXSN16.PMSPX2FD and PMVXUDF17.  *
.*               PMSVX1FD from 2 to 0                                      *
.***************************************************************************
.* Date      :   22/01/2019                                                *
.* Author    :   Richa Phogat                                              *
.* Function  :   This program will update records in table pmspx2af &      *
.*               pmsvx1af                                                  *
.* Modifications  :                                                        *
.*        V10.13.00 22/01/2019  Richa Phogat  TSK 0864623                  *
.*                  Created program                                        *
.*        V10.14.01 08/02/2019  Richa Phogat  TSK 0864623                  *
.*                  Updated DISPLAY under PMSP0000, PMSV0000
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
PMSPCONT  FORM      5
PMSVCONT  FORM      5
.
.
PRGID     INIT      "FX834684"
PRGNAM    INIT      "Update field value for MyHR consent"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN0200       * proceed with clean up
.
MAIN0200  CALL      PMSP0000               * process PMSPX2FD
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
.
MAIN0300  CALL      PMSV0000               * process PMSVX1FD
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               PMSP0000              Called by: MAIN0000   *
.* Update data from value '2' to '0' for MyHR Consent Flag in PMSPX2FD       *
.*****************************************************************************
PMSP0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,*HOFF,"Hit Enter to start"
          CALL      HITENTER
          DISPLAY   *ES,*P1:22,*EL,*V2LON,*HOFF,"Processing PMSPX2FD records.."
.
          MOVE      ZERO,PMSPCONT
          PACK      KEY8,SP70
          CALL      RSPMPX21
PMSP0100  CALL      RKPMPX21
          BRANCH    OVRCD,PMSP9900
.
          MATCH     "2",PMPXSN16
          GOTO      PMSP0200 IF EQUAL
.
          GOTO      PMSP0100
.
PMSP0200  MOVE      "0",PMPXSN16
          ADD       ONE,PMSPCONT
.
          PACK      KEY8,PMPXURNO
          CALL      RAPMPX21
          CALL      UPPMPX21               * Update record in table PMSPX2FD
.
          PRINT     *1,"|",PMPXURNO
          GOTO      PMSP0100
.
PMSP9900  PRINT     *N,*1,"Total number of records updated in PMI Table: ",PMSPCONT
.
          DISPLAY   *P1:22,*EL,"Total number of records updated in PMI table: ":
                    *V2LON,PMSPCONT,*HOFF,SP5;
.
          DISPLAY   *P1:23;
          CALL      HITENTER
.
          MOVE      ZERO,EXIT
.
PMSP9999  RETURN
+
.*****************************************************************************
.*                               PMSV0000              Called by: MAIN0000   *
.* Update data from value '2' to '0' for MyHR Consent Flag in PMSVX1FD       *
.*****************************************************************************
PMSV0000  DISPLAY   *ES,*P1:22,*EL,*V2LON,*HOFF,"Processing PMSVX1FD records.."
          MOVE      ZERO,PMSVCONT
          PACK      KEY8,SP70
          CALL      RSPMVX11
PMSV0100  CALL      RKPMVX11
          BRANCH    OVRCD,PMSV9900
.
          MATCH     "2",PMVXUDF7
          GOTO      PMSV0200 IF EQUAL
.
          GOTO      PMSV0100
.
PMSV0200  MOVE      "0",PMVXUDF7
          ADD       ONE,PMSVCONT
.
          PACK      KEY8,PMVXVISN
          CALL      UPPMVX11               * Update record in table PMSVX1FD
.
          PRINT     *1,"|",PMVXVISN
          GOTO      PMSV0100
.
PMSV9900  PRINT     *N,*1,"Total number of records updated in Visit Table: ",PMSVCONT
.
          DISPLAY   *P1:22,*EL,"Total number of records updated in Visit Table: ":
                    *V2LON,PMSVCONT,*HOFF,SP5;
.
          DISPLAY   *P1:23;
          CALL      HITENTER
.
          MOVE      ZERO,EXIT
.
PMSV9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC


.*****************************************************************************
.*              P R O G R A M  S U M M A R Y                                 *
.*              -------------  -------------                                 *
.*                                                                           *
.*     PROGRAM NAME:     IBAHMS39                                            *
.*                                                                           *
.*     FUNCTION:         RECONCILIATION REPORT FOR G/L INTERFACE             *
.*                                                                           *
.*     DATE WRITTEN:     10/11/2006                                          *
.*                                                                           *
.*     AUTHOR:           J.TAN                                               *
.*                                                                           *
.*     MODIFICATIONS:                                                        *
.*                                                                           *
. *****************************************************************************
.
          INC           STD001FD
          INC           PATCOMM/INC
          INC           IBAGINFD/INC
          INC           PATDRGFD/INC
+
.               VARIABLES DEFINITIONS
.
FDAY      DIM       2
FYR       DIM       4
FMTH      DIM       2
.
LYEAR     FORM      "366"
OYEAR     FORM      "365"
.
SELMTH    DIM       6
.
PRGID     INIT      "IBAHMS39"
PRGNAM    INIT      "Reconciliation Report for G/L Interface"
VERSION   INIT      "V12.01.00"
+
.****************************************************************************
.*                            MAIN0000                                      *
.*                      Controlling Logic (Mainline code)                   *
.****************************************************************************
MAIN0000  CALL      DISPHEAD
.
          MOVE      ZERO,OPTION
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Fees Invoice":
                    *P1:6,*V2LON,TWO,*HOFF," = To be Invoice"
MAIN1000  KEYIN     *P1:8,*EF,"Option : ",*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      MAIN9999 IF EQUAL
.
          BRANCH    OPTION,MAIN2000,MAIN2000
          BEEP
          GOTO      MAIN0000
.
MAIN2000  CALL      KFIL0000         * keyin file
          BRANCH    EXIT,MAIN9999
.
          CALL      CONTQST          * Ok to continue ?
          BRANCH    CEXIT,MAIN3000,MAIN2000,MAIN9999
          BEEP
          GOTO      MAIN2000
.
MAIN3000  CALL      PROC0000         * Process report
.
MAIN9999  CHAIN     PGM
.
.****************************************************************************
.*                            Keyin MM/YY                                   *
.****************************************************************************
KFIL0000  DISPLAY   *P1:17,"  Keyin Day/Month :";
          MOVE      TEN7,CVERT
          CALL      KEYMN000
          BRANCH    OVRCD,KFIL9000
          MOVE      ZERO,EXIT
          GOTO      KFIL9999
.
KFIL9000  MOVE      ONE,EXIT
KFIL9999  RETURN
.
.****************************************************************************
.*               Continue processing                                        *
.****************************************************************************
PROC0000  DISPLAY    *P1:24,*EL,*P10:24,*V2LON:
                     "** Generating Report **",*W2;
.
          MOVE       ZERO,CPAGENO
          CLOCK      TIME,CTIMEIS
          MOVE       "99",CLNO
.
          IF        OPTION=1
            MOVE      "ffin",KEY4      * fees invoice
          ELSE
            MOVE      "tfin",KEY4      * tobe invoiced
          ENDIF
          PACK      KEY8,KEY4,SELMTH
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      IBAGINA1,KEY8  * Reset to Start of File
          TRAPCLR   IO
.
          IF        OVRCD=1
            DISPLAY   *P1:24,*EL,*B,"No records found. ";
            CALL      HITENTER
            GOTO      PROC9999
          ENDIF
          CALL      HEAD0000
.
PROC1000  CALL      RKIBGI1
          BRANCH    OVRCD,PROC8000
.
          ADD       ONE,SECTOR
          IF        SECTOR%100=0
            DISPLAY   *P40:24,*V2LON,SECTOR
          ENDIF
.
          COMPARE   SIXTY,CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PRINT     *1,IBGIACCT,*22,IBGICRAA,*44,IBGIDESC,*78,IBGIVALU
          ADD       ONE,CLNO
          GOTO      PROC1000
.
PROC8000  COMPARE   ZERO TO CPAGENO
          GOTO      PROC9999 IF NOT EQUAL
.
          CALL      HEAD0000
          UNPACK    SELMTH,CMON,CDAY
          PRINT     *N,"No Report Generated for (DD/MM): ",CDAY,SLASH,CMON:
                    *N,"  ***  End of Report  ***"
.
PROC9999  RETURN
.
.****************************************************************************
.*               HEAD0000 - Header                                          *
.****************************************************************************
HEAD0000  CALL      IBACLOCK
          CALL      HEAD132
.
          PRINT     *1,"Debit Account",*22,"Credit Account":
                    *44,"Description",*78,"Amount"
          CALL      UND132
          MOVE      TWO,CLNO 
HEAD9999  RETURN
+
.****************************************************************************
.         Subroutine to key in a valid month and year
.****************************************************************************
KEYMN000  MOVE     TWENTY6,CCOL
          UNPACK   SP4,CYEAR,CMON
          MOVE     CCC,CCENT
.
          MOVE      CCOL,CCOL1               * Column for day keyin
          MOVE      CCOL,CCOL2
          ADD       THREE,CCOL2              * Column for month keyin
.
KEYMN100  CALL      KEYDAY                   * Key in the day
          BRANCH    OVRCD,KEYMN900           * No month entered OR "?" entered
          CALL      KEYMON                   * Key in the month
          BRANCH    OVRCD,KEYMN100           * No day
.
          PACK      SELMTH,XMON,XDAY
          REP       " 0",SELMTH
          GOTO      KEYMN999
.
KEYMN900  DISPLAY    *PCCOL:CVERT,SP10
KEYMN999  RETURN
+
.
         INCLUDE   IBAGINIO/INC
.
         INCLUDE    PATCOMN3
         INCLUDE    STD001IO
         INC        PATDRGIO/INC
.
ENDIT    CHAIN      PGM
         STOP

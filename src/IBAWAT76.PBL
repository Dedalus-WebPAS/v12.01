.******************************************************************************
.* System         : Waiting List                                              *
.* Program        : IBAWAT76.PBL                                              *
.* Name           : WA Waiting List Extract                                   *
.******************************************************************************
.* Date           : 06/01/2011                                                *
.* Author         : Ebon Clements                                             *
.* Function       : Extract waiting list activity for a hospital and date     *
.*                  range to a fixed length text file. Inclue patients        *
.*                  currently on the WL, removals and admissions              *
.* Modifications  :                                                           *
.******************************************************************************
.* V11.03.02 08/05/2023  Jacob Jackson    TSK 0929614                         *
.*           Fix problem where XXXXGEND would add '0' value to report         *
.* V11.03.01 04/05/2023  Jacob Jackson    TSK 0929614                         *
.*           Change "Sex" field to be "Sex At Birth" XXXXSEAB and add new     *
.*           gender field at pos 1060 XXXXGEND                                *
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899                         *
.*           Recompiled as WATTR1FD changes                                   *
.******************************************************************************
.* V11.00.01 10/03/2020  Jill Parkinson Task 0879163                          *
.*           Added SEXDSCIO include                                           *
.* V10.06.05  21/09/2015  Davin             CAR 313906                        *
.*            Recompiled for PMSCEXCD - allow patmx1af/patma1af details       *
.* V10.06.04  24/07/2015 Jill Parkinson  CAR 312602                           *
.*            Changed XXXXPRBD to be packed with WMDATE3 instead of BKREADAT  *
.* V10.06.03  13/07/2015 Jill Parkinson  CAR 311296                           *
.*            Changed XXXXISTY to use THCSCOD instead of TCINDC1              *
.* V10.06.02  08/07/2015 Jill Parkinson  CAR 312602                           *
.*            Fixed BMI calculation to select the last record instead of      *
.*            trying to find the record as at wttxcdte/wttxctim               *
.* V10.06.01  07/05/2015 Sandra Barcham     CAR 312602                        *
.*            Moved Time Not Ready for care from 410 to 380 now 4 characters  *
.* V10.04.01  12/05/2014 Peter Vela         CAR 279020                        *
.*            Added the following variables from position 356 to 385:         *
.*            XXXXPRBR - Premature Booking Reason                             *
.*            XXXXPRBA - Premature Booking Authorisation                      *
.*            XXXXPRBD - Premature Booking Date                               *
.*            XXXXPRRD - Premature Booking Reportable Days on List            *
.*            XXXXANAS - Anaesthetic Assessment (ASA)                         *
.*            XXXXSUOP - Surgeon Options (SOP)                                *
.*            XXXXBMIN - Body Mass Index                                      *
.*            XXXXFREE - Free Space                                           *
.* V10.03.14  07/03/2013 Jill Parkinson     CAR 272075                        *
.*            Fixed overdue date calculation - added setting of clurctdt with *
.*            either wmdate1 or wmkeydt                                       *
.* V10.03.13  26/02/2013 Patrick Adair      CAR 272075                        *
.*            added additonal fields to the Waiting List Extract File         *
.* V10.03.12  26/06/2012 Steve Armstrong    CAR 265412                        *
.*            New counts added should be whole episode not just extract date  *
.* V10.03.11  22/06/2012 Steve Armstrong    CAR 267344                        *
.*            Fixed ONL and ADM to be 4 characters with a trailing space      *
.* V10.03.10  20/06/2012 Sandra Barcham     CAR 267344                        *
.*            Unscheduled patients showing as admitted                        *
.* V10.03.09  20/06/2012 Sandra Barcham     CAR 267344                        *
.*            Unscheduled patients showing as admitted                        *
.* V10.03.08  06/06/2012 Ebon Clements      CAR 265412                        *
.*            Added population of cancellations and postponements counts      *
.* V10.03.07  29/03/2012 Ebon Clements      CAR 260674                        *
.*            Added transaction date test for admission and removals          *
.* V10.03.06  15/03/2012 Sandra Barcham     CAR 261785                        *
.*            Fix DVA Card Colour                                             *
.* V10.03.05  07/03/2012 Ebon Clements      CAR 260674                        *
.*            Fixed population of admission time - XXXXATIM                   *
.*            Fixed population of residential address state - XXXXRSST        *
.* V10.03.04  27/02/2012 Ebon Clements      CAR 260674                        *
.*            Fixed not ready for care days calculation - NRCDAY00            *
.* V10.03.03  24/02/2012 Ebon Clements      CAR 260674                        *
.*            Fixed medical authorisation approved XXXXMEDA                   *
.* V10.03.02  08/02/2012 Jill Parkinson     CAR 259568                        *
.*            Mods to left justify and NOT zero fill XXXXCLID                 *
.* V10.03.01  10/01/2011 Ebon Clements      CAR 244337                        *
.*            Added from and to date to us1 script call                       *
.*            Added update of extract run dates to pmsweaaf                   *
.* V10.03.00  06/01/2011 Ebon Clements      CAR 244337                        *
.*            Created extract                                                 *
.******************************************************************************
.
          INC       STD001FD
.
          INC       IBAPOSFD/INC
          INC       BOKRC1FD/INC
          INC       BOKRX1FD/INC
          INC       PATATRFD/INC
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PMSHCGFD/INC
          INC       PATCONFD/INC
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATWR1FD/INC
          INC       PMSCCDFD/INC
          INC       PMSCEXFD/INC
          INC       PMSHCPFD/INC
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
          INC       PMSWAEFD/INC
          INC       WATCATFD/INC
          INC       WATCHAFD/INC
          INC       WATCONFD/INC                                    * Car 272075
          INC       WATOPAFD/INC
          INC       WATPROFD/INC
          INC       WATSUSFD/INC
          INC       WATTR1FD/INC
          INC       WATTX1FD/INC
          INC       WEBSECFD/INC
.
. wat76tXX.txt - Where xx is the port number
.
EXTRFILE  FILE      ASCII, FIXED=1060
.
.Name     Type    Length    Start  Description                    Field Number
.-------  ----    ------    -----  -----------                    ------------
XXXXEVTY  DIM     4         1      Event Type                         (1)
XXXXMRTY  DIM     1         5      Morbidity Record Type              (2)
XXXXFEDI  DIM     20        6      Feeder Indentifier                 (3)
XXXXWLTY  DIM     2         26     Wait List Type                     (4)
XXXXWLCT  DIM     1         28     Wait List Category                 (5)
XXXXLISD  DIM     8         29     Listing Date                       (6)
XXXXESTB  DIM     4         37     Establishment                      (7)
XXXXCLID  DIM     10        41     Client Identifier                  (8)
XXXXSNAM  DIM     30        51     Surname                            (9)
XXXXGNAM  DIM     30        81     First Given Name                   (10)
XXXXSGNM  DIM     30        111    Second Given Name                  (11)
XXXXRSAD  DIM     50        141    Residential Address                (12)
XXXXRSPC  DIM     6         191    Residential Address Post Code      (13)
XXXXRSSU  DIM     30        197    Residential Address Suburb         (14)
XXXXRSST  DIM     1         227    Residential Address State          (15)
XXXXBDAT  DIM     8         228    Date of Birth                      (16)
XXXXSEAB  DIM     1         236    Sex At Birth                       (17)
XXXXISTY  DIM     1         237    Intended Length of Stay            (18)
XXXXINSU  DIM     1         238    Insurance Status                   (19)
XXXXPAYC  DIM     1         239    Payment Classification             (20)
XXXXCLST  DIM     1         240    Client Listing Status              (21)
XXXXCLUR  DIM     1         241    Clinical Urgency                   (22)
XXXXURDT  DIM     8         242    Urgency Reassignment Date          (23)
XXXXCLRC  DIM     13        250    Clinician Responsible for Care     (24)
XXXXSPWL  DIM     3         263    Specialty on WL                    (25)
XXXXPPRO  DIM     10        266    Principal Procedure                (26)
XXXXAPR1  DIM     10        276    Additional Procedure 1             (27)
XXXXAPR2  DIM     10        286    Additional Procedure 2             (28)
XXXXAPR3  DIM     10        296    Additional Procedure 3 (Blank)     (29)
XXXXAPR4  DIM     10        306    Additional Procedure 4 (Blank)     (30)
XXXXAPR5  DIM     10        316    Additional Procedure 5 (Blank)     (31)
XXXXPDIA  DIM     10        326    Primary Diagnosis                  (32)
XXXXADI1  DIM     10        336    Additional Diagnosis 1 (Blank)     (33)
XXXXADI2  DIM     10        346    Additional Diagnosis 2 (Blank)     (34)
.
.XXXXADI3  DIM     10        356    Additional Diagnosis 3 (Blank)     (35)
.XXXXADI4  DIM     10        366    Additional Diagnosis 4 (Blank)     (36)
.XXXXADI5  DIM     10        376    Additional Diagnosis 5 (Blank)     (37)
.
. New Fields for CAR 279020 replace the above fields.
.
XXXXPRBR  DIM     2         356    Premature Booking Reason           (35)
XXXXPRBA  DIM     2         358    Premature Booking Authorisation    (36)
XXXXPRBD  DIM     8         360    Premature Booking Date             (37)
XXXXPRRD  DIM     4         368    Premature Booking Reportable Days on List
.                                                                     (38)
XXXXANAS  DIM     2         372    Anaesthetic Assessment             (39)
XXXXSUOP  DIM     2         374    Surgeon Options (SOP)              (40)
XXXXBMIN  DIM     4         376    Body Mass Index                    (41)
XXXXTNRC  DIM     4         380    Time Not Ready for Care            (42)
XXXXFREE  DIM     2         380    Free Space                         (43)
.
. re-numbered the fields below
.
XXXXINDP  DIM     2         386    Indicator Procedure                (44)
XXXXSADT  DIM     8         388    Scheduled Admission Date           (45)
XXXXADAT  DIM     8         396    Admission Date                     (46)
XXXXATIM  DIM     4         404    Admission Time                     (47)
XXXXRREM  DIM     2         408    HMDS Removal Reason (Blank)        (48)
XXXXFRE1  DIM     3         410    Free space                         (49)
XXXXCDAT  DIM     8         413    Census Date                        (50)
XXXXRFCS  DIM     30        421    Referring Clinician Surname        (51)
XXXXRFCG  DIM     30        451    Referring Clinician Given Name     (52)
XXXXRFCD  DIM     35        481    Referring Clinician Address        (53)
XXXXCLRS  DIM     30        516    Clinician Resp. for Care Surname   (54)
XXXXCLRG  DIM     50        546    Clinician Resp. for Care Given Name(55)
XXXXRFPH  DIM     18        596    Referring Clinician Phone          (56)
XXXXPRFC  DIM     5         614    Professional Code                  (57)
XXXXTSPY  DIM     4         619    Specialty                          (58)
XXXXPHMP  DIM     18        623    Patient Home Phone                 (59)
XXXXDVAN  DIM     12        641    DVA No.                            (60)
XXXXDVAC  DIM     1         653    DVA Colour                         (61)
XXXXDDAT  DIM     8         654    Patient Date of Death              (62)
XXXXDETY  DIM     1         662    Patient Death Type                 (63)
XXXXNOKN  DIM     175       663    NOK name and Address               (64)
XXXXREFS  DIM     2         838    Referral Source (Blank)            (65)
XXXXEVDC  DIM     3         840    Event Deferrals Count              (66)
XXXXCAWC  DIM     3         843    Count of WL Cancellations          (67)
XXXXCAAC  DIM     3         846    Count of ADM Cancellations         (68)
XXXXEVUR  DIM     4         849    Event Urgeny Reass. Time (Blank)   (69)
XXXXEVDL  DIM     6         853    Event Days on List                 (70)
XXXXEDU1  DIM     4         859    Event Days Urgency Cat 1           (71)
XXXXEDU2  DIM     4         863    Event Days Urgency Cat 2           (72)
XXXXEDU3  DIM     4         867    Event Days Urgency Cat 3           (73)
XXXXPMA1  DIM     36        871    Patient Mailing Address Line 1     (74)
XXXXPMA2  DIM     31        907    Patient Mailing Address Line 2     (75)
XXXXPMAS  DIM     51        938    Patient Mailing Address Suburb     (76)
XXXXPMAP  DIM     4         989    Patient Mailing Address Post Code  (77)
XXXXTPRR  DIM     2         993    TOPAS Reason for Removal (Blank)   (78)
XXXXWARD  DIM     5         995    Ward Code                          (79)
XXXXBTYP  DIM     3         1000   Bed Type                           (80)
XXXXPRDT  DIM     8         1003   Pre Admission Date                 (81)
XXXXTCRS  DIM     1         1011   Tertiary Care Reason (Blank)       (82)
XXXXINDG  DIM     1         1012   Indigenous Status                  (83)
XXXXAIWH  DIM     1         1013   AIHW Reportable/Non Reportable     (84)
XXXXMEDA  DIM     1         1014   Medical Authorosation Approved     (85)
XXXXSRCR  DIM     3         1015   Referral Source                    (86)
XXXXREMR  DIM     3         1018   Removal Reason                     (87)
XXXXTERR  DIM     3         1021   Tertiary Care Reason               (88)
XXXXKEYD  DIM     8         1024   Date Keyed                         (89)
XXXXOVRD  DIM     8         1032   Overdue Date                       (90)
XXXXVISN  DIM     20        1040   Visit Number                       (91)
XXXXGEND  DIM     1         1060   Gender                             (92)
.
. End of Record             1061
.
. ----- Program Variables -----
.
ADHOCEXT  FORM      1
BODMASIN  DIM       5
BODMASRN  DIM       60
BODMASUW  INIT      "<font color=purple>"
BODMASHE  INIT      "<font color=black>"
BODMASFT  INIT      "<font color=red>"
BOLD      INIT      "<b>"
ENDBOLD   INIT      "</b>"
ENDFONT   INIT      "</font>"
CALCTIME  FORM      6
CDYSDAYS  FORM      6
CDYSFDTE  DIM       8
CDYSTDTE  DIM       8
CDYSYEAR  FORM      2
CMDLINE   DIM       127
CONTTYPE  DIM       1
CURRPRTY  DIM       3
CURRDATE  DIM       8
DIM2A     DIM       2
DIM2B     DIM       2
DIM3      DIM       3
DIM8      DIM       8
ENDDDATE  DIM       8
FORM2P1   FORM      2.1
FORM2P1A  FORM      2.1
FORM4P2   FORM      4.2
FORM6A    FORM      6
FORM6P1   FORM      6.1
FORM6P1A  FORM      6.1
FILENAME  DIM       8
FIRSTDAT  DIM       8        (first date CCYYMMDD format)
FIRSTIME  DIM       4        (first time HHMM format)
FIRSTHR   DIM       2
FIRSTMIN  DIM       2
FORM3     FORM      3
FORM3A    FORM      3
FORM6     FORM      6
HOSPCODE  DIM       3
HOUR      DIM       2
HOUR1     FORM      6
HOUR2     FORM      6
LASTDATE  DIM       8        (last date CCYYMMDD format)
LASTTIME  DIM       4        (last time HHMM format)
LASTHOUR  DIM       2
LASTMIN   DIM       2
LPRDAYS   FORM      4
LSTDAYS   FORM      5
LSTDAYFR  DIM       8
LSTDAYTO  DIM       8
LISTPRTY  DIM       1
MIN       DIM       2
MINUTE1   FORM      3
MINUTE2   FORM      3
NRCDAYS   FORM      5
NRCDAYFR  DIM       8
NRCDAYTO  DIM       8
OVERDATE  DATE      8                                               * Car 272075
OVERSTAT  DIM       1                                               * Car 272075
CLURCTDT  DIM       8                                               * Car 272075
CATCHGFL  FORM      1                                               * Car 272075
RECCNT    FORM      3                                               * Car 272075
SAVINDC3  DIM       1                                               * Car 272075
CASEKEYS  DIM       28                                              * Car 272075 
ENDDTE    DIM       8                                               * Car 272075
PBRDDAYS  FORM      3
CATASSGN  FORM      5                                               * Car 272075
PRTYFDAT  DIM       8
PRTYTDAT  DIM       8
PRTODATE  DIM       10
PRFRDATE  DIM       10
PRNTHOSP  DIM       50
SAVKEY35  DIM       35
STARTDAT  DIM       8
TOTALREC  FORM      8
URNUMBER  DIM       8
WEBUSRID  DIM       10
.
ADMIND    FORM       1
ADM       INIT       "ADM "
ONL       INIT       "ONL "
REMIND    FORM       1
REMD      INIT       "REM "
ANSLI     INIT       "i"
.
. ----- Constant Variables -----
.
CATB1     INIT      "b1"
CATB2     INIT      "b2"
CATCT     INIT      "ct"
CATTC     INIT      "tc"
CATTPUC   INIT      "TP" 
CATYH     INIT      "yh"
FNAME     INIT      "wat76t"
.
PRGID     INIT      "IBAWAT76"
PRGNAM    INIT      "WA WL Extraction"
VERSION   INIT      "V12.01.00"
.
.******************************************************************************
.*                                   ML0000                                   *
.*                                 Main Module                                *
.******************************************************************************
.
ML0000    CALL      INIT0000                * Initialise variables & open files
.
ML1000    CALL      OPTN0000                * Choose main option
          BRANCH    OPTION,ML2000
          BRANCH    EXIT,ML9999
.
ML2000    CALL      KADH0000                * Keyin Adhoc extraction
          BRANCH    EXIT,ML1000
.
          CALL      KHOS0000                * Keyin hospital id
          BRANCH    EXIT,ML1000
.
          CALL      KDAT0000                * Keyin the end date
          BRANCH    EXIT,ML1000
.
          CALL      KUSR0000                * Keyin web user id
          BRANCH    EXIT,ML1000
.
          CALL      CONTQST                 * Ok to Continue (Y/N/C) ?
          BRANCH    CEXIT,ML4000,ML2000,ML1000
.
ML4000    CALL      CREX0000                * Create extract text file
.
          CALL      HEAD0000                * Print Report Header
.
          CALL      PROC0000                * Process extract
.
          CALL      EDAT0000                * Update extract run dates
.
          CALL      TOTL0000                * Print totals
.
          CALL      MVEX0000                * Move extract for web index
.
          GOTO      ML1000
.
ML9999    CHAIN     PGM
.
.******************************************************************************
.* Initialise Variable & Open Files                                           *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen heading
.
          DISPLAY   *P64:24,"ibapostf";
          OPEN      IBAPOST1,"ibapostf"
.
          DISPLAY   *P64:24,"bokrc1af";
          OPEN      BOKRC1A1,"bokrc1af"
.
          DISPLAY   *P64:24,"bokrx1af";
          OPEN      BOKRX1A1,"bokrx1af"
.
          DISPLAY   *P64:24,"patatraf";
          OPEN      PATATRA1,"patatraf"
          OPEN      PATATRA2,"patatraf"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY   *P64:24,"pmshcgaf";
          OPEN      PMSHCGA1,"pmshcgaf"
.
          DISPLAY   *P64:24,"pmscexaf";
          OPEN      PMSCEXA1,"pmscexaf"
.
          DISPLAY   *P64:24,"pmsccdaf";
          OPEN      PMSCCDA1,"pmsccdaf"
.
          DISPLAY   *P64:24,"pmshcpaf";
          OPEN      PMSHCPA1,"pmshcpaf"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"pmswaeaf";
          OPEN      PMSWAEA1,"pmswaeaf"
.
          DISPLAY   *P64:24,"websecaf";
          OPEN      WEBSECA1,"websecaf"
.
          DISPLAY   *P64:24,"watchaaf";
          OPEN      WATCHAA1,"watchaaf"
.
          DISPLAY   *P64:24,"watopaaf";
          DISPLAY   *P64:24,"watcataf";
          OPEN      WATCATA2,"watcataf"
.
          DISPLAY   *P64:24,"watopaaf";
          OPEN      WATOPAA1,"watopaaf"
.
          DISPLAY   *P64:24,"watproaf";
          OPEN      WATPROA1,"watproaf"
.
          DISPLAY   *P64:24,"watsusaf";
          OPEN      WATSUSA1,"watsusaf"
.
          DISPLAY   *P64:24,"watsusaf";
          OPEN      WATSUSA2,"watsusaf"
.
          DISPLAY   *P64:24,"wattr1af";
          OPEN      WATTR1A1,"wattr1af"
.
          DISPLAY   *P64:24,"wattx1af";
          OPEN      WATTX1A1,"wattx1af"
.
          DISPLAY   *P56:24,"Opening controlf"
          OPEN      CONTROLF,"controlf"
.
          PACK      FILENAME,FNAME,PORT
          REP       " 0",FILENAME
.
          CALL      IBACLOCK
.
INIT9000  MOVE      ZERO,EXIT
.
INIT9999  RETURN
.
.******************************************************************************
.* Select Option                                                              *
.******************************************************************************
OPTN0000  MOVE      ZERO,EXIT
          MOVE      ZERO,OPTION
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Run":
                    *P1:8,"Select Option : ";
OPTN1000  KEYIN     *P17:8,*V2LON,*DV,UNDLN2,*P17:8,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN9000
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
.
.******************************************************************************
.* Input date range                                                           *
.******************************************************************************
KDAT0000  DISPLAY   *P1:14,*EL,"Start Date : ":
                    *P1:15,*EL,"End   Date : ";
.
          MOVE      "14",CVERT
          MOVE      "15",CCOL
          UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          MOVE      ZERO,CHIGHLT
          MOVE      ZERO,CDEFDTE
          CALL      KEYDATE
          BRANCH    OVRCD,KDAT9500
          PACK      STARTDAT,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STARTDAT
.
          MOVE      ZERO,EXIT
.
.         set up input requirements for KEYDATE
.
          MOVE      ZERO,CDEFDTE
          MOVE      ZERO,CHIGHLT
          MOVE      "15",CCOL
          MOVE      "15",CVERT
.
.         input ending date
.
KDAT1000  UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          CALL      KEYDATE
          BRANCH    OVRCD,KDAT9500
.
          PACK      ENDDDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDDATE
.
          MOVE      ZERO,EXIT
.
          MATCH     ENDDDATE,STARTDAT
          GOTO      KDAT9999 IF EQUAL
          GOTO      KDAT9999 IF LESS
.
          UNPACK    STARTDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P1:24,*EL,*B,*V2LON,"End Date must be greater than ":
                    "Start date ",CPCDATE,".  ";
          CALL      HITENTER
          GOTO      KDAT0000
.
KDAT9500  MOVE      ONE,EXIT
.
KDAT9999  RETURN
.
.******************************************************************************
.* Run the extract                                                            *
.******************************************************************************
PROC0000  MOVE      ZERO,TOTALREC
.
          PACK      KEY19,SP70
          CALL      RDSTREA1
PROC1000  CALL      RDKTREA1
          BRANCH    OVRCD,PROC9999
.
          PACK      KEY19,WMURNO,WMCODE,WMCNT,SP70
          CALL      RDWTTX11
          BRANCH    OVRCD,PROC1000
.
          CALL      VALD0000                     * Validate record
          BRANCH    EXIT,PROC1000
.
          CALL      CLRV0000                     * Clear extract fields
.
          MOVE      WMURNO,URNUMBER
          CALL      PMIF0000                     * Set PMI fields
.
          CALL      WATF0000                     * Set WL fields
.
          CALL      BOKF0000                     * Set BK fields
.
          CALL      PTAF0000                     * Set Attribute fields
.
          CALL      WRIT0000                     * Write to extract flat file
.
          GOTO      PROC1000
.
PROC9999  RETURN
.
.******************************************************************************
. Validate record
.******************************************************************************
.
VALD0000  MOVE      ZERO,ADMIND
          MOVE      ZERO,REMIND
.
          MATCH     HOSPCODE,WTWMIHSP          * Hospital filter
          GOTO      VALD9000 IF NOT EQUAL
.
          IF        WMSTAT>6
            GOTO      VALD9000                   * Ignore 7, 8 & 9 status
          ENDIF
.
          IF        WMSTAT=1 | WMSTAT=2 | WMSTAT = 3
            MATCH     WMDATE1,ENDDDATE           * Unscheduled, Scheduled and
            GOTO      VALD9000 IF LESS           * Pre adm with date on list
          ENDIF                                  * before extract end date
.
          IF        WMSTAT=4 | WMSTAT=5
            PACK      KEY8,WMBOOK,SP70
            CALL      RDMISS1
            BRANCH    OVRCD,VALD9000
.
            MATCH     STARTDAT,ADATE             * Pre Admitted/Admitted before
            IF        @LESS
              CALL      ADMD0000                 * Check admission transaction
              BRANCH    EXIT,VALD9000            * date within extract start/end
              MOVE      ONE,ADMIND
              GOTO      VALD8000
            ENDIF
.
            MATCH     ADATE,ENDDDATE             * Pre Admitted/Admitted after
            IF        @LESS
              MOVE      "2",ADMIND
              GOTO      VALD7000
            ENDIF
          ENDIF
.
          IF        WMSTAT=6
            MATCH     STARTDAT,WMDATE4           * Removed before start date
            IF        @LESS
              CALL      REMD0000                 * Check removal transaction
              BRANCH    EXIT,VALD9000            * date within extract start/end
              MOVE      ONE,REMIND
              GOTO      VALD8000
            ENDIF
.
            MATCH     WMDATE4,ENDDDATE           * Removed after
            IF        @LESS
              MOVE      "2",REMIND
              GOTO      VALD7000
            ENDIF
.
          ENDIF
.
VALD7000  MATCH     WMDATE1,ENDDDATE
          GOTO      VALD9000 IF LESS
.
VALD8000  MOVE      ZERO,EXIT
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT
          GOTO      VALD9999
.
VALD9999  RETURN
.
.******************************************************************************
. Create extract text file
.******************************************************************************
CREX0000  CLOSE     EXTRFILE,DELETE
.
          PREP      EXTRFILE,FILENAME
.
CREX9999  RETURN
.
.******************************************************************************
. Move Extract to Web Directory
.******************************************************************************
MVEX0000  PACK      KEY3,HOSPCODE,SP70
          CALL      RDPTHSP1
          IF        OVRCD=1
            CALL      CLPATHSP
          ENDIF
.
          CLEAR     CMDLINE
          APPEND    "ibawat76.us1 '",CMDLINE
          APPEND    FILENAME,CMDLINE          * Report .txt file
.
          APPEND    "' '",CMDLINE
          APPEND    PTHSHOSP,CMDLINE          * Hospital Id
.
          APPEND    "' '",CMDLINE
.
          MATCH     SP70,PTHSHSID
          IF        @EQUAL
            PACK      KEY4,PTHSHOSP,SP70
          ELSE
            MOVE      PTHSHSID,KEY4           * Health service identifier
          ENDIF
          APPEND    KEY4,CMDLINE
.
          APPEND    "' '",CMDLINE
          APPEND    PRFRDATE,CMDLINE          * From Date
.
          APPEND    "' '",CMDLINE
          APPEND    PRTODATE,CMDLINE          * To Date
.
          APPEND    "' '",CMDLINE
          APPEND    WEBUSRID,CMDLINE          * User Id
.
          APPEND    "'",CMDLINE
.
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
MVEX9999  RETURN
.
.******************************************************************************
.* Clear the extract variables                                                *
.******************************************************************************
CLRV0000  MOVE      SP70,XXXXEVTY
          MOVE      SP70,XXXXMRTY
          MOVE      SP70,XXXXFEDI
          MOVE      SP70,XXXXWLTY
          MOVE      SP70,XXXXWLCT
          MOVE      SP70,XXXXLISD
          MOVE      SP70,XXXXESTB
          MOVE      SP70,XXXXCLID
          MOVE      SP70,XXXXSNAM
          MOVE      SP70,XXXXGNAM
          MOVE      SP70,XXXXSGNM
          MOVE      SP70,XXXXRSAD
          MOVE      SP70,XXXXRSPC
          MOVE      SP70,XXXXRSSU
          MOVE      SP70,XXXXRSST
          MOVE      SP70,XXXXBDAT
          MOVE      SP70,XXXXSEAB
          MOVE      SP70,XXXXISTY
          MOVE      SP70,XXXXINSU
          MOVE      SP70,XXXXPAYC
          MOVE      SP70,XXXXCLST
          MOVE      SP70,XXXXCLUR
          MOVE      SP70,XXXXURDT
          MOVE      SP70,XXXXCLRC
          MOVE      SP70,XXXXSPWL
          MOVE      SP70,XXXXPPRO
          MOVE      SP70,XXXXAPR1
          MOVE      SP70,XXXXAPR2
          MOVE      SP70,XXXXAPR3
          MOVE      SP70,XXXXAPR4
          MOVE      SP70,XXXXAPR5
          MOVE      SP70,XXXXPDIA
          MOVE      SP70,XXXXADI1
          MOVE      SP70,XXXXADI2
.         MOVE      SP70,XXXXADI3
.         MOVE      SP70,XXXXADI4
.         MOVE      SP70,XXXXADI5
.
          MOVE      SP70,XXXXPRBR
          MOVE      SP70,XXXXPRBA
          MOVE      SP70,XXXXPRBD
          MOVE      SP70,XXXXPRRD
          MOVE      SP70,XXXXANAS
          MOVE      SP70,XXXXSUOP
          MOVE      SP70,XXXXBMIN
          MOVE      SP70,XXXXTNRC
          MOVE      SP70,XXXXFREE
.
          MOVE      SP70,XXXXINDP
          MOVE      SP70,XXXXSADT
          MOVE      SP70,XXXXADAT
          MOVE      SP70,XXXXATIM
          MOVE      SP70,XXXXRREM
          MOVE      SP70,XXXXFRE1
          MOVE      SP70,XXXXCDAT
          MOVE      SP70,XXXXRFCS
          MOVE      SP70,XXXXRFCG
          MOVE      SP70,XXXXRFCD
          MOVE      SP70,XXXXCLRS
          MOVE      SP70,XXXXCLRG
          MOVE      SP70,XXXXRFPH
          MOVE      SP70,XXXXPRFC
          MOVE      SP70,XXXXTSPY
          MOVE      SP70,XXXXPHMP
          MOVE      SP70,XXXXDVAN
          MOVE      SP70,XXXXDVAC
          MOVE      SP70,XXXXDDAT
          MOVE      SP70,XXXXDETY
          MOVE      SP70,XXXXNOKN
          MOVE      SP70,XXXXREFS
          MOVE      SP70,XXXXEVDC
          MOVE      SP70,XXXXCAWC
          MOVE      SP70,XXXXCAAC
          MOVE      SP70,XXXXEVUR
          MOVE      SP70,XXXXEVDL
          MOVE      SP70,XXXXEDU1
          MOVE      SP70,XXXXEDU2
          MOVE      SP70,XXXXEDU3
          MOVE      SP70,XXXXPMA1
          MOVE      SP70,XXXXPMA2
          MOVE      SP70,XXXXPMAS
          MOVE      SP70,XXXXPMAP
          MOVE      SP70,XXXXTPRR
          MOVE      SP70,XXXXWARD
          MOVE      SP70,XXXXBTYP
          MOVE      SP70,XXXXPRDT
          MOVE      SP70,XXXXTCRS
          MOVE      SP70,XXXXINDG
          MOVE      SP70,XXXXAIWH
          MOVE      SP70,XXXXMEDA
          MOVE      SP70,XXXXSRCR
          MOVE      SP70,XXXXREMR
          MOVE      SP70,XXXXTERR
          MOVE      SP70,XXXXKEYD
          MOVE      SP70,XXXXOVRD
          MOVE      SP70,XXXXVISN
          MOVE      SP70,XXXXGEND
.
CLRV9999  RETURN
.
.******************************************************************************
.*                                  KHOS0000                                  *
.*                      Keyin hospital ID                                     *
.******************************************************************************
KHOS0000  MOVE       ZERO,EXIT
.
          MOVE       SP70,HOSPCODE
          MOVE       SP70,PTHSHOSP
          DISPLAY    *P1:12,*EL,"Hospital Id : ";
          MOVE       TEN5,ECOL
          MOVE       TEN2,EVERT
          MOVE       SP3,CKYIDEF3
          MOVE       ONE,CKYIMAND           * Mandatory
          OPEN       PATHSPA1,"pathspaf"
.
          CALL       PATHSPKY
          BRANCH     EXIT,KHOS9000,KHOS9000
.
          PACK       HOSPCODE,KEY3,SP70          * Hospital Code
          PACK       PRNTHOSP,PTHSNAME,SP70
.
KHOS3000  DISPLAY   *P20:12,*V2LON,PTHSNAME;
          MOVE       ZERO,EXIT
          GOTO       KHOS9999
.
KHOS9000  MOVE       ONE,EXIT
KHOS9999  RETURN
.
.******************************************************************************
.*  Set PMI based extract fields                                              *
.******************************************************************************
PMIF0000  PACK      KEY8,URNUMBER,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,PMIF9999
.
          PACK      KEY8,PURNO,SP70
          CALL      RDPMPX21
          BRANCH    OVRCD,PMIF9999
.
          PACK      XXXXCLID,PURNO,SP70                             * (8)
          LJUSTIFY  XXXXCLID
.
          PACK      XXXXSNAM,PSNAME,SP70                            * (9)
          PACK      XXXXGNAM,PMPXFNAM,SP70                          * (10)
          PACK      XXXXSGNM,PMPXSNAM,SP70                          * (11)
.
          STRIP     PADD1
          PACK      XXXXRSAD,PADD1,SP1,PADD2,SP70                   * (12)
          RESET     PADD1
          PACK      XXXXRSPC,PPOST,SP70                             * (13)
          PACK      XXXXRSSU,PSUBRB,SP70                            * (14)
.
          PACK      KEY56,PPOST,PADD2,SP10,PADD4,SP70
          CALL      RDIBPOS1                * Read a postcode file record
          BRANCH    OVRCD,PMIF1000
.
          GOTO      PMIF4000
.
PMIF1000  PACK      KEY56,PPOST,PSUBRB,SP10,PADD4,SP70
          CALL      RDIBPOS1                * Read a postcode file record
          BRANCH    OVRCD,PMIF2000
.
          GOTO      PMIF4000
.
PMIF2000  PACK      KEY56,PPOST,SP70
          CALL      RSIBPOS1                * Position & read a postcode file
PMIF2100  CALL      RKIBPOS1                  record
          BRANCH    OVRCD,PMIF4050
.
          MATCH     PPOST,IBPOPCOD
          GOTO      PMIF4050 IF NOT EQUAL   * Different postcode, error
.
          MATCH     "OS ",IBPOSTAT
          GOTO      PMIF2100 IF EQUAL
.
          PACK      XXXXRSSU,IBPOSUBR,SP70                            * (14)
          GOTO      PMIF4000
.
PMIF4000  MATCH     "8888",PPOST
          IF        @EQUAL
            MOVE      "0",XXXXRSST                                  * (15)
            GOTO      PMIF4100
          ENDIF
.
          MATCH     "NSW",IBPOSTAT
          IF        @EQUAL
            MOVE      "1",XXXXRSST                                  * (15)
            GOTO      PMIF4100
          ENDIF
.
          MATCH     "VIC",IBPOSTAT
          IF        @EQUAL
            MOVE      "2",XXXXRSST                                  * (15)
            GOTO      PMIF4100
          ENDIF
.
          MATCH     "QLD",IBPOSTAT
          IF        @EQUAL
            MOVE      "3",XXXXRSST                                  * (15)
            GOTO      PMIF4100
          ENDIF
.
          MATCH     "SA ",IBPOSTAT
          IF        @EQUAL
            MOVE      "4",XXXXRSST                                  * (15)
            GOTO      PMIF4100
          ENDIF
.
          MATCH     "WA ",IBPOSTAT
          IF        @EQUAL
            MOVE      "5",XXXXRSST                                  * (15)
            GOTO      PMIF4100
          ENDIF
.
          MATCH     "TAS",IBPOSTAT
          IF        @EQUAL
            MOVE      "6",XXXXRSST                                  * (15)
            GOTO      PMIF4100
          ENDIF
.
          MATCH     "NT ",IBPOSTAT
          IF        @EQUAL
            MOVE      "7",XXXXRSST                                  * (15)
            GOTO      PMIF4100
          ENDIF
.
          MATCH     "ACT",IBPOSTAT
          IF        @EQUAL
            MOVE      "8",XXXXRSST                                  * (15)
            GOTO      PMIF4100
          ENDIF
.
PMIF4050  MOVE      "0",XXXXRSST                                    * (15)
.
PMIF4100  UNPACK    PBDATE,XCENT,XYEAR,XMON,XDAY
          PACK      XXXXBDAT,XDAY,XMON,XCENT,XYEAR,SP70             * (16)
.
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          IF        DSEXNO>4
            PACK      XXXXSEAB,FOUR,SP70                            * (17)
          ELSE
            PACK      XXXXSEAB,DSEXNO,SP70                          * (17)
          ENDIF
.
          MATCH     SP70,PFUNDH
          IF        @EQUAL
            PACK      XXXXINSU,ANSN,SP70                            * (19)
          ELSE
            PACK      XXXXINSU,ANSY,SP70
          ENDIF
.
          PACK      XXXXPHMP,PTELEP,SP70                            * (58)
.
          MOVE      "3",DIM1
          CALL      GCRD0000                     * Get DVA details
.
          PACK      XXXXDVAN,PMCDCNUM,SP70                          * (59)
.
          MATCH     SP70,PMCDDVAC
          IF        !@EQUAL
            PACK      KEY5,ANSD,ANSX,PMCDDVAC
            CALL      RDCODE1
            IF        OVRCD<>1
              PACK      XXXXDVAC,THCSCOD,SP70                       * (60)
              REP       "G1",XXXXDVAC
              REP       "W2",XXXXDVAC
            ENDIF
          ENDIF
.
          UNPACK    PDECDTE,XCENT,XYEAR,XMON,XDAY
          PACK      XXXXDDAT,XDAY,XMON,XCENT,XYEAR,SP70             * (61)
.
          MATCH     SP70,PMPXDETY
          IF        !@EQUAL
            PACK      KEY5,ANSD,ANSY,PMPXDETY,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              PACK      XXXXDETY,TCINDC1,SP70                       * (62)
            ENDIF
          ENDIF
.
          MOVE      "1",CONTTYPE
          CALL      XCON0000                     * Get NOK details
.
          STRIP     PMCETITL
          STRIP     PMCESNAM
          STRIP     PMCEGNAM
          STRIP     PMCEADD1
          STRIP     PMCEADD2
          STRIP     PMCEADD3
          STRIP     PMCEADD4
          STRIP     PMCEPOST
          PACK      XXXXNOKN,PMCETITL,SP1,PMCESNAM,SP1,PMCEGNAM,SP1:
                             PMCEADD1,SP1,PMCEADD2,SP1,PMCEADD3,SP1:
                             PMCEADD4,SP1,PMCEPOST,SP70             * (63)
.
          MOVE      "2",CONTTYPE
          CALL      XCON0000                     * Get postal address
.
          PACK      XXXXPMA1,PMCEADD1,SP70                          * (73)
          PACK      XXXXPMA2,PMCEADD2,SP70                          * (74)
          PACK      XXXXPMAS,PMCEADD3,SP70                          * (75)
          PACK      XXXXPMAP,PMCEPOST,SP70                          * (76)
.
          MATCH     SP70,PMPXABRG
          IF        !@EQUAL
            PACK      KEY5,ANSV,ANSA,PMPXABRG,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              PACK      XXXXINDG,THCSCOD,SP70                       * (82)
            ENDIF
          ENDIF
.
          PACK      KEY5,ANSG,ANSLI,PMPXUCC4,SP70
          CALL      RDCODE1
          IF        OVRCD<>1
            PACK      XXXXGEND,THCSCOD,SP70                         * (92)
          ENDIF
.
PMIF9999  RETURN
.
.--------------------------------------------------------------------
. Get concession card details
. DIM1 = 1 - Concession Card
.        2 - Safety Net Card
.        3 - DVA Card
.        4 - Pension Card
.--------------------------------------------------------------------
GCRD0000  PACK      KEY19,URNUMBER,SP70
          CALL      RSPMCCD1
GCRD1000  CALL      RKPMCCD1
          BRANCH    OVRCD,GCRD8000
.
          MATCH     URNUMBER,PMCDURNO
          GOTO      GCRD8000 IF NOT EQUAL
.
          PACK      KEY5,CATCT,PMCDCTYP,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,GCRD1000
.
          MATCH     DIM1,TCINDC1
          GOTO      GCRD9999 IF EQUAL
.
          GOTO      GCRD1000
.
GCRD8000  MOVE      SP70,PMCDCNUM
          MOVE      SP70,PMCDCTYP
          MOVE      SP70,PMCDEXDT
          MOVE      SP70,PMCDDVAC
          GOTO      GCRD9999
.
GCRD9999  RETURN
.
.******************************************************************************
.*  Set WL based extract fields                                              *
.******************************************************************************
WATF0000  IF        WMSTAT=1 | WMSTAT=2 | WMSTAT = 3
            MOVE      "ONL ",XXXXEVTY                               * (1)
          ENDIF
.
          IF        WMSTAT=4 | WMSTAT=5
            MOVE      "ADM ",XXXXEVTY
          ENDIF
.
          LOAD      XXXXEVTY,ADMIND,ADM,ONL
.
          IF        WMSTAT=6
            MOVE      "REM ",XXXXEVTY
          ENDIF
.
          LOAD      XXXXEVTY,REMIND,REMD,ONL
.
.
          PACK      XXXXMRTY,ANSW,SP70                              * (2)
.
          PACK      XXXXFEDI,WTWMECNT,SP70
          RJUSTIFY  XXXXFEDI
          REP       " 0",XXXXFEDI                                   * (3)
.
          MATCH     SP70,WMUNIT
          IF        !@EQUAL
            PACK      KEY5,ANSW,ANSU,WMUNIT,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              UNPACK    THCSCOD,KEY2,XXXXWLTY
              RJUSTIFY  XXXXWLTY
              REP       " 0",XXXXWLTY                               * (4)
.
              PACK      XXXXSPWL,TWO,KEY2                           * (25)
              RJUSTIFY  XXXXSPWL
            ENDIF
          ENDIF
.
          MATCH     SP70,WTTXBOTY
          IF        !@EQUAL
            PACK      KEY5,ANSB,ANSK,WTTXBOTY,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              PACK      XXXXWLCT,TCINDC6,SP70                       * (5)
            ENDIF
          ENDIF
.
          UNPACK    WMDATE1,XCENT,XYEAR,XMON,XDAY
          PACK      XXXXLISD,XDAY,XMON,XCENT,XYEAR,SP70             * (6)
.
          PACK      KEY3,WTWMIHSP,SP70
          CALL      RDPTHSP1
          IF        OVRCD<>1
            MOVE      PTHSHSID,KEY4
            PACK      XXXXESTB,KEY4,SP70                            * (7)
          ENDIF
.
          MATCH     SP70,WTTXINTD
          IF        !@EQUAL
            PACK      KEY5,ANSV,ANSI,WTTXINTD,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              PACK      XXXXISTY,THCSCOD,SP70                       * (18)
            ENDIF
          ENDIF
.
          MATCH     SP70,WTTXCTYP
          IF        !@EQUAL
            PACK      KEY5,ANSC,ANSL,WTTXCTYP,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              PACK      XXXXPAYC,TCINDC14,SP70                      * (20)
            ENDIF
          ENDIF
.
          MATCH     SP70,WTTXPLST
          IF        !@EQUAL
            PACK      KEY5,ANSV,ANSL,WTTXPLST,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              PACK       XXXXCLST,THCSCOD,SP70                       * (21)
            ENDIF
          ENDIF
.
          MATCH     SP70,WMPTY
          IF        !@EQUAL
            PACK      KEY5,ANST,ANSP,WMPTY,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              PACK      XXXXCLUR,THCSCOD,SP70                       * (22)
            ENDIF
          ENDIF
.
          UNPACK    WMDATE1,XCENT,XYEAR,XMON,XDAY
          PACK      XXXXURDT,XDAY,XMON,XCENT,XYEAR,SP70             * (23)
.
          PACK      KEY29,WMURNO,WMCODE,WMCNT,ANST,ANSP,Z70
          CALL      RSWTCAT2                * Position on & read code changes
          CALL      RPWTCAT2                  file record
          BRANCH    OVRCD,WATF2000
.
          MATCH     WMURNO,WTCAURNO
          GOTO      WATF2000 IF NOT EQUAL   * Different U/R no
.
          MATCH     WMCODE,WTCAPROC
          GOTO      WATF2000 IF NOT EQUAL   * Different procedure code
.
          COMPARE   WMCNT,WTCAPCNT
          GOTO      WATF2000 IF NOT EQUAL   * Different procedure count
.
          MATCH     "TP",WTCACATF
          GOTO      WATF2000 IF NOT EQUAL   * Different category
.
          UNPACK    WTCAEDAT,XCENT,XYEAR,XMON,XDAY
          PACK      XXXXURDT,XDAY,XMON,XCENT,XYEAR,SP70             * (23)
.
WATF2000  MATCH     SP70,WMDOCTOR
          IF        !@EQUAL
            PACK      KEY6,WMDOCTOR,SP70
            CALL      RDDOCT1
            IF        OVRCD<>1
              PACK      XXXXCLRC,PTDOREGN,SP70                      * (24)
              PACK      XXXXCLRS,DSNAME,SP70                        * (53)
              PACK      XXXXCLRG,DGNAME,SP70                        * (54)
            ENDIF
          ENDIF
.
          PACK      XXXXPPRO,WMCODE,SP70                            * (26)
          PACK      XXXXAPR1,WTWMPRO2                               * (27)
          PACK      XXXXAPR2,WTWMPRO3                               * (28)
.                                                                   * Car 272075
.                                                                   * Date Keyed
          UNPACK    WMKEYDT,XCENT,XYEAR,XMON,XDAY
          PACK      XXXXKEYD,XDAY,XMON,XCENT,XYEAR,SP70             * (88)
.                                                                   * Visit Numb
          PACK      XXXXVISN,WMBOOK,SP70
          RJUSTIFY  XXXXVISN
          REP       " 0",XXXXVISN                                   * (90)
.                                                            * Calc Overdue date
          CALL      NEWOVR00
          MATCH     SP70,OVERDATE
          GOTO      WATF2100 IF EQUAL
.
          UNPACK    OVERDATE,XCENT,XYEAR,XMON,XDAY
          PACK      XXXXOVRD,XDAY,XMON,XCENT,XYEAR,SP70             * (89)
.                                                                   * End 272075
WATF2100  PACK      KEY9,WMCODE,SP70
          CALL      RDPROA1
          IF        OVRCD<>1
            MATCH     SP70,WPGRP
            IF        !@EQUAL
              PACK      KEY5,ANSW,ANSG,WPGRP,SP70
              CALL      RDCODE1
              IF        OVRCD<>1
                PACK      XXXXINDP,THCSCOD,XXXXINDP                 * (43)
              ENDIF
            ENDIF
.
            MOVE      ZERO,FORM6
            SQUEEZE   WPRHAT
            MOVE      WPRHAT,FORM6
            RESET     WPRHAT
            IF        FORM6>0
              PACK      XXXXAIWH,ONE,SP70                           * (83)
            ELSE
              PACK      XXXXAIWH,ZERO,SP70                          * (83)
            ENDIF
          ENDIF
.
          UNPACK    WMDATE3,XCENT,XYEAR,XMON,XDAY
          PACK      XXXXSADT,XDAY,XMON,XCENT,XYEAR,SP70             * (44)
.
          MATCH    "REM ",XXXXEVTY
          IF       @EQUAL
.         IF        WMSTAT=6
            UNPACK    WMDATE4,XCENT,XYEAR,XMON,XDAY
            PACK      XXXXADAT,XDAY,XMON,XCENT,XYEAR,SP70           * (45)
            UNPACK    SP70,DIM2A,DIM2B,DIM1
            UNPACK    WTTXRTIM,DIM2A,DIM1,DIM2B
            PACK      XXXXATIM,DIM2A,DIM2B,SP70                     * (46)
          ENDIF
.
          MATCH     "ADM ",XXXXEVTY
          IF        @EQUAL
.         IF        WMSTAT=4 | WMSTAT=5
            PACK      KEY8,WMBOOK,SP70
            CALL      RDMISS1
            IF        OVRCD<>1
              UNPACK    ADATE,XCENT,XYEAR,XMON,XDAY
              PACK      XXXXADAT,XDAY,XMON,XCENT,XYEAR,SP70         * (45)
              UNPACK    SP70,DIM2A,DIM2B,DIM1
              UNPACK    ATIME,DIM2A,DIM1,DIM2B
              PACK      XXXXATIM,DIM2A,DIM2B,SP70                   * (46)
            ENDIF
          ENDIF
.
          CALL      IBACLOCK
          PACK      XXXXCDAT,CDD,CMM,CCC,CYY,SP70                   * (49)
          REP       " 0",XXXXCDAT
.
          MATCH     SP70,WTTXRCLI
          GOTO      WATF3000 IF EQUAL
.
          PACK      KEY10,WTTXRCLI,SP70
          CALL      RDPMHCP1
          BRANCH    OVRCD,WATF3000
.
          PACK      XXXXRFCS,PMHCSNAM,SP70                          * (50)
          PACK      XXXXRFCG,PMHCGNAM,SP70                          * (51)
.
          PACK      XXXXRFCD,PMHCADR1,SP70                          * (52)
          PACK      XXXXRFPH,PMHCSTEL,SP70                          * (55)
.
          MATCH     SP70,WTTXRCLP
          GOTO      WATF3000 IF EQUAL
.
          PACK      KEY12,WTTXRCLP,WTWMGPPT,SP70
          CALL      RDPMHCG1
          BRANCH    OVRCD,WATF3000
.
          PACK      XXXXRFCD,PMHGADD1,SP70                          * (52)
          PACK      XXXXRFPH,PMHGPTEL,SP70                          * (55)
.
WATF3000  MATCH     SP70,WTTXWARD
          GOTO      WATF4000 IF EQUAL
.
          PACK      XXXXWARD,WTTXWARD,SP70                          * (78)
.
          PACK      KEY6,WTTXWARD,SP70
          CALL      RDWARD1
          BRANCH    OVRCD,WATF4000
.
          MATCH     SP70,WEFRBT
          GOTO      WATF4000 IF EQUAL
.
          PACK      KEY5,ANSB,ANST,WEFRBT,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,WATF4000
.
          PACK      XXXXBTYP,THCSCOD,SP70                           * (79)
.
WATF4000  PACK      KEY27,WMURNO,WMCODE,WMCNT,SP70
          CALL      RSWTOPA1
WATF4100  CALL      RKWTOPA1
          BRANCH    OVRCD,WATF5000
.
          MATCH     WTOPURNO,WMURNO
          GOTO      WATF5000 IF NOT EQUAL
.
          MATCH     WTOPCODE,WMCODE
          GOTO      WATF5000 IF NOT EQUAL
.
          MATCH     WTOPCOUN,DWMCNT
          GOTO      WATF5000 IF NOT EQUAL
.
          MATCH     "0",WTOPSTAT
          GOTO      WATF4100 IF NOT EQUAL
.
          UNPACK    WTOPDATE,XCENT,XYEAR,XMON,XDAY
          PACK      XXXXPRDT,XDAY,XMON,XCENT,XYEAR,SP70             * (80)
.
WATF5000  MATCH     "1",WTTXMAUT
          IF        @EQUAL
            PACK      XXXXMEDA,ANSY,SP70                            * (84)
          ELSE
            MATCH     "0",WTTXMAUT
            IF        @EQUAL
              PACK      XXXXMEDA,ANSN,SP70                          * (84)
            ENDIF
          ENDIF
.
          PACK      XXXXSRCR,WTWMSRCR,SP70                          * (85)
          PACK      XXXXREMR,WMREMOVE,SP70                          * (86)
          PACK      XXXXTERR,WTTXTCRS,SP70                          * (87)
.
          MOVE      WMDATE1,NRCDAYFR   * NRFC from date
          MOVE      ENDDDATE,NRCDAYTO  * NRFC to date
          CALL      NRCDAY00           * Calc not ready for care days
          MOVE      ZERO,FORM4
          MOVE      NRCDAYS,FORM4
          PACK      XXXXTNRC,FORM4,SP70                             * (48)
.
          MOVE      WMDATE1,LSTDAYFR   * List days from date
          MOVE      ENDDDATE,LSTDAYTO  * List days to date
          CALL      LSTDAY00           * Calc days on list
          MOVE      ZERO,FORM6
          ASSIGN    LSTDAYS-NRCDAYS,FORM6
          PACK      XXXXEVDL,FORM6,SP70                             * (69)
.
          MOVE      "1",LISTPRTY
          CALL      PRTDAY00
          PACK      XXXXEDU1,LPRDAYS,SP70                           * (70)
.
          MOVE      "2",LISTPRTY
          CALL      PRTDAY00
          PACK      XXXXEDU2,LPRDAYS,SP70                           * (71)
.
          MOVE      "3",LISTPRTY
          CALL      PRTDAY00
          PACK      XXXXEDU3,LPRDAYS,SP70                           * (72)
.
          CALL      EVDC0000                                        * (65)
.
          CALL      REVT0000                                       
.
          PACK      XXXXANAS,WTTXAASC,SP70
          RJUSTIFY  XXXXANAS
          REP       " 0",XXXXANAS                                   * (39)
.
          MOVE      SP70,XXXXSUOP
          MATCH     SP70,WTTXPRPB
          IF        !@EQUAL & !@EOS
            PACK      KEY5,CATYH,WTTXPRPB,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              PACK      XXXXSUOP,THCSCOD,SP70                       * (40)
            ENDIF  
          ENDIF          
.
WATF9999  RETURN
.
.------------------------------------------------------------
. Calculate Days Not Ready for Care for a date range
. From date NRCDAYFR
. To date NRCDAYTO
.------------------------------------------------------------
NRCDAY00  MOVE      ZERO,NRCDAYS
.
          PACK      KEY21,WMURNO,WMCODE,WMCNT,SP70
          PACK      KEY19,WMURNO,WMCODE,WMCNT,SP70
          CALL      RSWTSUS1
NRCDAY10  CALL      RKWTSUS1
          BRANCH    OVRCD,NRCDAY99
.
          PACK      KEY21,WTSUURNO,WTSUCODE,WTSUCNT,SP70
          MATCH     KEY19,KEY21
          GOTO      NRCDAY99 IF NOT EQUAL
.
          PACK      KEY5,ANSV,ANSL,WTSULSTS
          CALL      RDCODE1
          BRANCH    OVRCD,NRCDAY10
.
          MATCH     "R",TCINDC1
          GOTO      NRCDAY10 IF EQUAL
.
          MATCH     WTSUFDAT,NRCDAYTO    * Suspension start after end date
          GOTO      NRCDAY10 IF LESS
.
          MATCH     NRCDAYFR,WTSUFDAT    * Suspension started before start date
          IF        @LESS
            MATCH     SP70,WTSUTDAT
            IF        !@EQUAL
              MATCH     NRCDAYFR,WTSUTDAT * Skip if suspension ended before
              GOTO      NRCDAY10 IF LESS  * start date range
            ENDIF
            MOVE      NRCDAYFR,WTSUFDAT  * Set suspension start to start date
          ENDIF
.
          MATCH     SP70,WTSUTDAT
          IF        @EQUAL
            MOVE      NRCDAYTO,WTSUTDAT
          ELSE
            MATCH     WTSUTDAT,NRCDAYTO
            GOTO      NRCDAY40 IF NOT LESS
            MOVE      NRCDAYTO,WTSUTDAT
          ENDIF
.
NRCDAY40  DAYSEP    WTSUFDAT,WTSUTDAT,F4
          ASSIGN    F4+NRCDAYS,NRCDAYS    * Do not add extra day to NRFC days
          GOTO      NRCDAY10
.
NRCDAY99  RETURN
.
.------------------------------------------------------------
. Calculate Days on List for a date range
. From date LSTDAYFR
. To date LSTDAYTO
.------------------------------------------------------------
LSTDAY00  IF        WMSTAT>3 & WMSTAT<6
            MATCH     SP70,WMDATE3
            IF        !@EQUAL
              MATCH     LSTDAYTO,WMDATE3
              IF        @LESS
                MOVE      WMDATE3,LSTDAYTO         * admitted or discharged
              ENDIF
            ENDIF
          ENDIF
.
          IF        WMSTAT=6
            MATCH     SP70,WMDATE4
            IF        !@EQUAL
              MATCH     LSTDAYTO,WMDATE4
              IF        @LESS
                MOVE      WMDATE4,LSTDAYTO         * removed
              ENDIF
            ENDIF
          ENDIF
.
          IF        WMSTAT=4|WMSTAT=5
            PACK      KEY8,WMBOOK
            CALL      RDMISS1
            IF        OVRCD<>1
              MATCH     LSTDAYTO,ADATE
              IF        @LESS
                PACK      LSTDAYTO,ADATE
              ENDIF
            ENDIF
          ENDIF
.
          DAYSEP    LSTDAYFR,LSTDAYTO,LSTDAYS
.
LSTDAY99  RETURN
.
.------------------------------------------------------------
. Calculate ready for care days at a specific priority
. LISTPRTY - Priority  code HDP position 1
.------------------------------------------------------------
PRTDAY00  MOVE      ZERO,LPRDAYS
          MOVE      SP70,PRTYFDAT
          MOVE      SP70,PRTYTDAT
          MOVE      WMDATE1,PRTYFDAT
.
          PACK      KEY5,ANST,ANSP,SP70
          CALL      RDSCODE1
PRTDAY10  CALL      RDKCODE1                     * Get priority cat code for
          BRANCH    OVRCD,PRTDAY99               * the specific priority
.
          MATCH     "TP",TCODE
          GOTO      PRTDAY99 IF NOT EQUAL
.
          MATCH     ANSI,PTCOACTV                * Skip inactive
          GOTO      PRTDAY10 IF EQUAL
.
          MATCH     LISTPRTY,THCSCOD             * Match HDP position 1
          GOTO      PRTDAY10 IF NOT EQUAL
.
          PACK      KEY29,WMURNO,WMCODE,WMCNT,ANST,ANSP,SP70
          CALL      RSWTCAT2                * Position on & read code changes
PRTDAY20  CALL      RKWTCAT2                  file record
          BRANCH    OVRCD,PRTDAY80
.
          MATCH     WMURNO,WTCAURNO
          GOTO      PRTDAY80 IF NOT EQUAL   * Different U/R no
.
          MATCH     WMCODE,WTCAPROC
          GOTO      PRTDAY80 IF NOT EQUAL   * Different procedure code
.
          COMPARE   WMCNT,WTCAPCNT
          GOTO      PRTDAY80 IF NOT EQUAL   * Different procedure count
.
          MATCH     "TP",WTCACATF
          GOTO      PRTDAY80 IF NOT EQUAL   * Different category
.
          MOVE      WTCACODT,CURRPRTY
.
          MATCH     SP70,WTCACODF           * Skip first priority change
          GOTo      PRTDAY20 IF EQUAL
.
          MATCH     ACODE,WTCACODF
          IF        !@EQUAL
            MOVE      WTCAEDAT,PRTYFDAT    * Save effective date for next
            GOTO      PRTDAY20             * records to date
          ENDIF
.
          MOVE      WTCAEDAT,PRTYTDAT       * Priority to date
.
          MOVE      PRTYFDAT,NRCDAYFR       * NRFC from date
          MOVE      PRTYTDAT,NRCDAYTO       * NRFC to date
          CALL      NRCDAY00                * Calc not ready for care days
.
          MOVE      PRTYFDAT,LSTDAYFR       * List days from date
          MOVE      PRTYTDAT,LSTDAYTO       * List days to date
          DAYSEP    LSTDAYFR,LSTDAYTO,LSTDAYS
.
          MOVE      ZERO,FORM6
          ASSIGN    LSTDAYS-NRCDAYS,FORM6   * List days - NRFC days = RFC days
          ADD       FORM6,LPRDAYS
.
          MOVE      WTCAEDAT,PRTYFDAT       * Save effective date for next
          GOTO      PRTDAY20
.
PRTDAY80  MATCH     CURRPRTY,ACODE          * Not currently is specific priority
          GOTO      PRTDAY99 IF NOT EQUAL
.
          MOVE      ENDDDATE,PRTYTDAT              * Work out to date
          IF        WMSTAT>3 & WMSTAT<6
            MATCH     SP70,WMDATE3
            IF        !@EQUAL
              MATCH     PRTYTDAT,WMDATE3
              IF        @LESS
                MOVE      WMDATE3,PRTYTDAT         * admitted or discharged
              ENDIF
            ENDIF
          ENDIF
.
          IF        WMSTAT=6
            MATCH     SP70,WMDATE4
            IF        !@EQUAL
              MATCH     PRTYTDAT,WMDATE4
              IF        @LESS
                MOVE      WMDATE4,PRTYTDAT         * removed
              ENDIF
            ENDIF
          ENDIF
.
          IF        WMSTAT=4|WMSTAT=5
            PACK      KEY8,WMBOOK
            CALL      RDMISS1
            IF        OVRCD<>1
              MATCH     PRTYTDAT,ADATE
              IF        @LESS
                PACK      PRTYTDAT,ADATE
              ENDIF
            ENDIF
          ENDIF
.
          MOVE      PRTYFDAT,NRCDAYFR       * NRFC from date
          MOVE      PRTYTDAT,NRCDAYTO       * NRFC to date
          CALL      NRCDAY00                * Calc not ready for care days
.
          MOVE      PRTYFDAT,LSTDAYFR   * List days from date
          MOVE      PRTYTDAT,LSTDAYTO  * List days to date
          CALL      LSTDAY00           * Calc days on list
.
          MOVE      ZERO,FORM6
          ASSIGN    LSTDAYS-NRCDAYS,FORM6   * List days - NRFC days = RFC days
          ADD       FORM6,LPRDAYS
.
PRTDAY99  RETURN
.
.******************************************************************************
.* Write the extract variables                                                *
.******************************************************************************
WRIT0000 WRITE     EXTRFILE,SEQ;XXXXEVTY,XXXXMRTY,XXXXFEDI,XXXXWLTY,XXXXWLCT:
                         XXXXLISD,XXXXESTB,XXXXCLID,XXXXSNAM,XXXXGNAM:     * 10
                         XXXXSGNM,XXXXRSAD,XXXXRSPC,XXXXRSSU,XXXXRSST:
                         XXXXBDAT,XXXXSEAB,XXXXISTY,XXXXINSU,XXXXPAYC:     * 20
                         XXXXCLST,XXXXCLUR,XXXXURDT,XXXXCLRC,XXXXSPWL:
                         XXXXPPRO,XXXXAPR1,XXXXAPR2,XXXXAPR3,XXXXAPR4:     * 30
                         XXXXAPR5,XXXXPDIA,XXXXADI1,XXXXADI2,XXXXPRBR:
                         XXXXPRBA,XXXXPRBD,XXXXPRRD,XXXXANAS,XXXXSUOP:     * 40
                         XXXXBMIN,XXXXTNRC,XXXXFREE,XXXXINDP,XXXXSADT:
                         XXXXADAT,XXXXATIM,XXXXRREM,XXXXFRE1,XXXXCDAT:     * 50
                         XXXXRFCS,XXXXRFCG,XXXXRFCD,XXXXCLRS,XXXXCLRG:
                         XXXXRFPH,XXXXPRFC,XXXXTSPY,XXXXPHMP,XXXXDVAN:     * 60
                         XXXXDVAC,XXXXDDAT,XXXXDETY,XXXXNOKN,XXXXREFS:
                         XXXXEVDC,XXXXCAWC,XXXXCAAC,XXXXEVUR,XXXXEVDL:     * 70
                         XXXXEDU1,XXXXEDU2,XXXXEDU3,XXXXPMA1,XXXXPMA2:
                         XXXXPMAS,XXXXPMAP,XXXXTPRR,XXXXWARD,XXXXBTYP:     * 80
                         XXXXPRDT,XXXXTCRS,XXXXINDG,XXXXAIWH,XXXXMEDA:
                         XXXXSRCR,XXXXREMR,XXXXTERR,XXXXKEYD,XXXXOVRD:     * 90
                         XXXXVISN,XXXXGEND
.
.
. old layout commented out
.
.         WRITE     EXTRFILE,SEQ;XXXXEVTY,XXXXMRTY,XXXXFEDI,XXXXWLTY,XXXXWLCT:
.                        XXXXLISD,XXXXESTB,XXXXCLID,XXXXSNAM,XXXXGNAM:     * 10
.                        XXXXSGNM,XXXXRSAD,XXXXRSPC,XXXXRSSU,XXXXRSST:
.                        XXXXBDAT,XXXXSEAB,XXXXISTY,XXXXINSU,XXXXPAYC:     * 20
.                        XXXXCLST,XXXXCLUR,XXXXURDT,XXXXCLRC,XXXXSPWL:
.                        XXXXPPRO,XXXXAPR1,XXXXAPR2,XXXXAPR3,XXXXAPR4:     * 30
.                        XXXXAPR5,XXXXPDIA,XXXXADI1,XXXXADI2,XXXXADI3:
.                        XXXXADI4,XXXXADI5,XXXXINDP,XXXXSADT,XXXXADAT:     * 40
.                        XXXXATIM,XXXXRREM,XXXXTNRC,XXXXCDAT,XXXXRFCS:
.                        XXXXRFCG,XXXXRFCD,XXXXCLRS,XXXXCLRG,XXXXRFPH:     * 50
.                        XXXXPRFC,XXXXTSPY,XXXXPHMP,XXXXDVAN,XXXXDVAC:
.                        XXXXDDAT,XXXXDETY,XXXXNOKN,XXXXREFS,XXXXEVDC:     * 60
.                        XXXXCAWC,XXXXCAAC,XXXXEVUR,XXXXEVDL,XXXXEDU1:
.                        XXXXEDU2,XXXXEDU3,XXXXPMA1,XXXXPMA2,XXXXPMAS:     * 70
.                        XXXXPMAP,XXXXTPRR,XXXXWARD,XXXXBTYP,XXXXPRDT:
.                        XXXXTCRS,XXXXINDG,XXXXAIWH,XXXXMEDA,XXXXSRCR:     * 80
.                        XXXXREMR,XXXXTERR,XXXXKEYD,XXXXOVRD,XXXXVISN
.
          ADD       ONE,TOTALREC
.
WRIT9999  RETURN
.
.*********************************************************************
.         print a new page
.*********************************************************************
HEAD0000  CALL      HEAD80
.
          UNPACK    STARTDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,PRFRDATE         * formatted start date
.
          UNPACK    ENDDDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,PRTODATE         * formatted start date

          PRINT     *20,"Date From : ",PRFRDATE," to ",PRTODATE
          PRINT     *20,"Hospital : ",PRNTHOSP
.
          IF        ADHOCEXT=1
            MOVE      "Yes",KEY3
          ELSE
            MOVE      "No ",KEY3
          ENDIF
.
          PRINT     *20,"Adhoc Extraction : ",KEY3
          PRINT     *20,"User Id : ",WEBUSRID
.
          CALL      UND80
.
HEAD9999  RETURN
.
.*********************************************************************
. Print totals
.*********************************************************************
TOTL0000  PRINT     *20,"Total Records Extracted : ",TOTALREC
.
          CALL      UND80
.
TOTL9999  RETURN
.
.
.**********************************************************************
.* Keyin Adhoc extraction yes or no
.**********************************************************************
KADH0000    MOVE       ZERO,ADHOCEXT
            MOVE       "N",ANS
.
            KEYIN      *P1:10,*EL,"Adhoc Extraction (Y/N) : ":
                       *P26:10,*V2LON,*RV,ANS;
            RESET      ANS
            REP        UPPLOW,ANS
.
            MATCH     "Y",ANS
            IF         @EQUAL
              MOVE       ONE,ADHOCEXT
              GOTO       KADH8000
            ENDIF
.
            MATCH     "N",ANS
            IF         @EQUAL
              MOVE       ZERO,ADHOCEXT
              GOTO       KADH8000
            ENDIF
.
            GOTO    KADH9100
.
KADH8000    MOVE    ZERO,EXIT
            GOTO    KADH9999
.
KADH9100    MOVE    ONE,EXIT
            GOTO    KADH9999
.
KADH9999    RETURN
.
.**********************************************************************
.* Update extract run dates if not an adhoc extraction
.**********************************************************************
EDAT0000  BRANCH     ADHOCEXT,EDAT9999
.
          MOVE       HOSPCODE,PMWAHOSP
          MOVE       "IBAWAT76",PMWAPROG
          MOVE       STARTDAT,PMWASDAT
          MOVE       ENDDDATE,PMWAEDAT

          PACK      PMWACDAT,CCC,CYY,CMM,CDD
          REP       " 0",PMWACDAT
          CLOCK     TIME,PMWACTIM
          MOVE      WEBUSRID,PMWAWEBC
.
          PACK      PMWASPARR,SP70,SP70
.
          PACK      KEY27,PMWAHOSP,PMWAPROG,PMWASDAT,PMWAEDAT,SP70
          CALL      RAPMWAE1
          IF        OVRCD=1
            CALL      WRPMWAE1
          ENDIF
.
EDAT9999  RETURN
.
.
.**********************************************************************
.* Keyin web user id
.**********************************************************************
KUSR0000  MOVE      SP70,WEBUSRID
          KEYIN     *P1:17,*EF:
                    *P1:17,"Web User Id : ",*V2LON,WEBUSRID
.
          PACK      WEBUSRID,WEBUSRID,SP70
          MATCH     SP70,WEBUSRID
          GOTO      KUSR9100 IF EQUAL
.
          PACK      KEY10,WEBUSRID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,KUSR9100
.
          MOVE      ZERO,EXIT
          GOTO      KUSR9999
.
KUSR9100  MOVE      ONE,EXIT
.
KUSR9999  RETURN
.
.******************************************************************************
. If the removal date is outside the extract date range, check if the
. transaction date was within the extract date range.
.******************************************************************************
REMD0000  PACK      KEY35,WMURNO,WMCODE,DWMCNT,SP70
          CALL      RSWTCHA1
REMD1000  CALL      RKWTCHA1
          BRANCH    OVRCD,REMD9000
.
          MATCH     WMURNO,WTCHURNO            * Correct U/R
          GOTO      REMD9000 IF NOT EQUAL
.
          MATCH     WMCODE,WTCHPROC            * Correct procedure code
          GOTO      REMD9000 IF NOT EQUAL
.
          MATCH     DWMCNT,WTCHPCNT            * Correct count
          GOTO      REMD9000 IF NOT EQUAL
.
          MATCH     "05",WTCHUPTY              * Removal
          GOTO      REMD1000 IF NOT EQUAL
.
          MATCH     STARTDAT,WTCHDATE          * Transaction before
          GOTO      REMD9000 IF LESS           * extract start date
.
          MATCH     WTCHDATE,ENDDDATE          * Transaction after
          GOTO      REMD9000 IF LESS           * extract end date
.
          MOVE      ZERO,EXIT
          GOTO      REMD9999
.
REMD9000  MOVE      ONE,EXIT
          GOTO      REMD9999
.
REMD9999  RETURN
.
.******************************************************************************
. If the admission date is outside the extract date range, check if the
. transaction date was within the extract date range.
.******************************************************************************
ADMD0000  PACK      KEY35,WMURNO,WMCODE,DWMCNT,SP70
          CALL      RSWTCHA1
ADMD1000  CALL      RKWTCHA1
          BRANCH    OVRCD,ADMD9000
.
          MATCH     WMURNO,WTCHURNO            * Correct U/R
          GOTO      ADMD9000 IF NOT EQUAL
.
          MATCH     WMCODE,WTCHPROC            * Correct procedure code
          GOTO      ADMD9000 IF NOT EQUAL
.
          MATCH     DWMCNT,WTCHPCNT            * Correct count
          GOTO      ADMD9000 IF NOT EQUAL
.
          MATCH     "03",WTCHUPTY              * Admission
          GOTO      ADMD1000 IF NOT EQUAL
.
          MATCH     STARTDAT,WTCHDATE          * Transaction before
          GOTO      ADMD9000 IF LESS           * extract start date
.
          MATCH     WTCHDATE,ENDDDATE          * Transaction after
          GOTO      ADMD9000 IF LESS           * extract end date
.
          MOVE      ZERO,EXIT
          GOTO      ADMD9999
.
ADMD9000  MOVE      ONE,EXIT
          GOTO      ADMD9999
.
ADMD9999  RETURN
.
.******************************************************************************
. Calculate the event deferrals count. Count the entries in watchaaf
. with an update type of 08 and 16 within the date range
.******************************************************************************
EVDC0000  MOVE      ZERO,FORM3                 * Set counter to zero
.
          PACK      KEY35,WMURNO,WMCODE,DWMCNT,SP70
          CALL      RSWTCHA1
EVDC1000  CALL      RKWTCHA1
          BRANCH    OVRCD,EVDC9000
.
          MATCH     WMURNO,WTCHURNO            * Correct U/R
          GOTO      EVDC9000 IF NOT EQUAL
.
          MATCH     WMCODE,WTCHPROC            * Correct procedure code
          GOTO      EVDC9000 IF NOT EQUAL
.
          MATCH     DWMCNT,WTCHPCNT            * Correct count
          GOTO      EVDC9000 IF NOT EQUAL
.
          MATCH     "08",WTCHUPTY              * Scheduled Admission Date
          IF        !@EQUAL
            MATCH     "16",WTCHUPTY            * Cancel Booking
            GOTO      EVDC1000 IF NOT EQUAL
          ENDIF
.
.         MATCH     STARTDAT,WTCHDATE          * Transaction before
.         GOTO      EVDC1000 IF LESS           * extract start date
.
.         MATCH     WTCHDATE,ENDDDATE          * Transaction after
.         GOTO      EVDC1000 IF LESS           * extract end date
.
          ADD       ONE,FORM3                  * Add one to counter
          GOTO      EVDC1000
.
EVDC9000  MOVE      FORM3,XXXXEVDC                                  * (65)
          GOTO      EVDC9999
.
EVDC9999  RETURN
.
.******************************************************************************
. Calculate the count of WL cancellations reverting and not reverting to
. the WL. Count the entries in watchaaf where the update type is 12
. cancelled admission within the date range
.
. If here is no removal record type 05 within 10 seconds count as a
. revert to WL. If there is a removal within 10 seconds count as a not reverting. to WL
.******************************************************************************
REVT0000  MOVE      ZERO,FORM3                 * Revert to WL count
          MOVE      ZERO,FORM3A                * Not reverting to WL count
.
          PACK      KEY35,WMURNO,WMCODE,DWMCNT,SP70
          CALL      RSWTCHA1
REVT1000  CALL      RKWTCHA1
          BRANCH    OVRCD,REVT9000
.
          MATCH     WMURNO,WTCHURNO            * Correct U/R
          GOTO      REVT9000 IF NOT EQUAL
.
          MATCH     WMCODE,WTCHPROC            * Correct procedure code
          GOTO      REVT9000 IF NOT EQUAL
.
          MATCH     DWMCNT,WTCHPCNT            * Correct count
          GOTO      REVT9000 IF NOT EQUAL
.
          MATCH     "12",WTCHUPTY              * Cancelled Admission
          GOTO      REVT1000 IF NOT EQUAL
.
.         MATCH     STARTDAT,WTCHDATE          * Transaction before
.         GOTO      REVT1000 IF LESS           * extract start date
.
.         MATCH     WTCHDATE,ENDDDATE          * Transaction after
.         GOTO      REVT1000 IF LESS           * extract end date
.
          PACK      FIRSTDAT,WTCHDATE          * Cancelled adm date and time
          UNPACK    WTCHTIME,HOUR,D1,MIN
          PACK      FIRSTIME,HOUR,MIN
.
.  Check for a removal within 10 seconds
.
          PACK      SAVKEY35,WTCHURNO,WTCHPROC,WTCHPCNT,WTCHDATE,WTCHTIME,SP70
.
REVT2000  CALL      RKWTCHA1
          BRANCH    OVRCD,REVT8000
.
          MATCH     WMURNO,WTCHURNO            * Correct U/R
          GOTO      REVT8000 IF NOT EQUAL
.
          MATCH     WMCODE,WTCHPROC            * Correct procedure code
          GOTO      REVT8000 IF NOT EQUAL
.
          MATCH     DWMCNT,WTCHPCNT            * Correct count
          GOTO      REVT8000 IF NOT EQUAL
.
          MATCH     "05",WTCHUPTY              * Removal
          GOTO      REVT8000 IF NOT EQUAL
.
          PACK      LASTDATE,WTCHDATE          * Removal date and time
          UNPACK    WTCHTIME,HOUR,D1,MIN
          PACK      LASTTIME,HOUR,MIN
.
          CALL      TIMEDIFF
          IF        CALCTIME>10
            GOTO      REVT8000                 * No removal within 10 seconds
          ELSE
            ADD       ONE,FORM3A               * Add one not reverting to WL
          ENDIF
.
          GOTO      REVT8500
.
REVT8000  ADD       ONE,FORM3                  * Add one revert to WL
.
REVT8500  PACK      KEY35,SAVKEY35             * Re position back on original
          CALL      RSWTCHA1                   * cancelled admission records
          GOTO      REVT1000
.
REVT9000  MOVE      FORM3,XXXXCAWC                                  * (66)
          MOVE      FORM3A,XXXXCAAC                                 * (67)
          GOTO      REVT9999
.
REVT9999  RETURN
.
.*******************************************************************************
.*                                  NEWOVR00               Called by: WATF0000 *
.* Calculate Overdue Date                                           Car 272075 *
.* (this is cloned from WATWEB01 25/02/2013)                                   *
.*******************************************************************************
NEWOVR00  MOVE      ZERO,OVERSTAT
          READ      CONTROLF,HUND11;*11,WTCNERFC
          CALL      GETCOD00
          CALL      GTPC0000
          MOVE      CLURCTDT,OVERDATE
.
          IF        CATCHGFL=0
            MATCH     "1",WTCNERFC
            IF        @EQUAL
              MOVE      WMKEYDT,OVERDATE   * no urgency change, use date on list
              MOVE      WMKEYDT,CLURCTDT

            ELSE
              MOVE      WMDATE1,OVERDATE   * no urgency change, use date on list
              MOVE      WMKEYDT,CLURCTDT
            ENDIF
          ENDIF
.
          PACK      KEY5,ANST,ANSP,WMPTY,SP70
          CALL      RDCODE1
          READ      CONTROLF,THIRTY7;*173,WTCNURDY,*177,WTCNSUDY:
                                     *181,WTCNRODY
          MATCH     "U",TCINDC1
          IF        @EQUAL
            ADD       WTCNURDY,OVERDATE
          ENDIF
.
          MATCH     "S",TCINDC1
          IF        @EQUAL
            ADD       WTCNSUDY,OVERDATE
          ENDIF
.
          MATCH     "R",TCINDC1
          IF        @EQUAL
            ADD       WTCNRODY,OVERDATE
          ENDIF
.
          CALL      ADSU0000      * add suspensions within overdue date
.
NEWOVR99  RETURN
.*******************************************************************************
.*                                  ADSU0000               Called by: NEWOVR00 *
.* Add suspensions within overdue date                              CAR 272075 *
.*******************************************************************************
ADSU0000  PACK      CASEKEYS,WMURNO,WMCODE,WMCNT,SP70
          PACK      KEY29,WMURNO,WMCODE,WMCNT,SP70
          CALL      RSWTSUS2
ADSU0010  CALL      RKWTSUS2
          BRANCH    OVRCD,ADSU9999
          PACK      KEY19,WTSUURNO,WTSUCODE,WTSUCNT,SP70
          MATCH     KEY19,CASEKEYS
          GOTO      ADSU9999 IF NOT EQUAL
.
          MATCH     WTSUFDAT,OVERDATE
          GOTO      ADSU9999 IF LESS
.
          MATCH     WTSUFDAT,CLURCTDT
          IF        !@LESS
            MOVE       CLURCTDT,WTSUFDAT
            MATCH      WTSUTDAT,CLURCTDT
            IF         !@LESS
              GOTO       ADSU0010
            ENDIF
          ENDIF
.
          MATCH     SP70,WTSUTDAT
          IF        @EQUAL
            MOVE      SP70,OVERDATE
            GOTO      ADSU9999
          ENDIF
.
          DAYSEP    WTSUFDAT,WTSUTDAT,F3
.
          ADD       F3,OVERDATE
          GOTO      ADSU0010
.
ADSU9999  RETURN
+
.*******************************************************************************
.*                                  ADSU0000               Called by: NEWOVR00 *
.* Get Code Description                                             CAR 272075 *
.*******************************************************************************
GETCOD00  MOVE      SP70,TDESC
          MATCH     SP70,WMPTY
          GOTO      GETCOD99 IF EQUAL
.
          PACK      KEY5,CATTPUC,WMPTY
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE      SP70,ACODE
            MOVE      SP70,TDESC
            UNPACK    SP70,TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5:
                           TCINDC6,TCINDC7,TCINDC8,TCINDC9,TCINDC10
          ENDIF
GETCOD99  RETURN
.******************************************************************************
.*                                  GTPC0000              Called by: NEWOVR00 *
.*                           Get Category TP Changes                          *
.******************************************************************************
GTPC0000  MOVE      ZERO,RECCNT
          MOVE      ZERO,CATASSGN
          CALL      IBACLOCK
          MOVE      ZERO,CATCHGFL
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          MOVE      KEY8,ENDDTE
          PACK      CLURCTDT,ENDDTE
.
          IF        WMSTAT<4 | WMSTAT>6
            GOTO      GTPC0500
          ENDIF
.
          IF        WMSTAT=6
            MOVE      WMDATE4,ENDDTE
            MOVE      ENDDTE,CLURCTDT
            GOTO      GTPC0500
          ENDIF
.
          PACK      KEY8,WMBOOK
          CALL      RDMISS1
          BRANCH    OVRCD,GTPC0500
.
          PACK      ENDDTE,ADATE
          PACK      CLURCTDT,ENDDTE
.
GTPC0500  PACK      KEY29,WMURNO,WMCODE,WMCNT,ANST,ANSP,SP20
          CALL      RSWTCAT2                * Position on & read code changes
GTPC1000  CALL      RKWTCAT2                  file record
          BRANCH    OVRCD,GTPC3000
.
          MATCH     WMURNO,WTCAURNO
          GOTO      GTPC3000 IF NOT EQUAL   * Different U/R no
.
          MATCH     WMCODE,WTCAPROC
          GOTO      GTPC3000 IF NOT EQUAL   * Different procedure code
.
          COMPARE   WMCNT,WTCAPCNT
          GOTO      GTPC3000 IF NOT EQUAL   * Different procedure count
.
          MATCH     "TP",WTCACATF
          GOTO      GTPC3000 IF NOT EQUAL   * Different category
.
          MOVE      SP1,TCINDC3
          MATCH     SP3,WTCACODF
          IF        !@EQUAL
            PACK      KEY5,ANST,ANSP,WTCACODF
            CALL      RDCODE1               * Read a codes file record
          ENDIF
.
          MOVE      TCINDC3,SAVINDC3        * Save the 3rd indicator
.
          MOVE      SP1,TCINDC3
          MATCH     SP3,WTCACODT
          IF        !@EQUAL
            PACK      KEY5,ANST,ANSP,WTCACODT
            CALL      RDCODE1               * Read a codes file record
          ENDIF
.
GTPC2000  ADD       ONE,RECCNT
          IF        RECCNT=1
            MATCH     SP3,WTCACODF
            GOTO      GTPC1000 IF EQUAL     * Blank 1st from change code
          ENDIF
.
          MATCH     TCINDC3,SAVINDC3
          GOTO      GTPC1000 IF EQUAL
          IF        @LESS
            GOTO      GTPC1000
          ENDIF
.
          MATCH     WTCAEDAT,WMDATE1
          IF        @EQUAL
            MOVE      SP8,WTCAEDAT
            GOTO      GTPC1000
          ENDIF
.
          MATCH     SP8,WTCAEDAT
          GOTO      GTPC1000 IF EQUAL
.
          MOVE      WTCAEDAT,CLURCTDT
          REP       " 0",CLURCTDT     * Clinical urgency cat last date
          MOVE      ONE,CATCHGFL
.
          GOTO      GTPC1000
.
GTPC3000  DAYSEP    CLURCTDT,ENDDTE,CATASSGN
.
GTPC9000  MOVE      ZERO,EXIT
GTPC9999  RETURN
.******************************************************************************
.*                                  BOKF0000              Called by: PROC0000 *
.*                           Get Booking Record                               *
.******************************************************************************
BOKF0000  CALL      CLBOKRC1
          CALL      CLBOKRX1
.
          PACK      KEY8,WMBOOK,SP70
          CALL      RDBKREC1
          BRANCH    OVRCD,BOKF9999
.
          PACK      KEY8,WMBOOK,SP70
          CALL      RDBKRX11
          BRANCH    OVRCD,BOKF9999
.
          MATCH     SP70,BKRXEABR
          IF        !@EQUAL & !@EOS
            PACK      KEY5,CATB1,BKRXEABR,SP70
            CALL      RDCODE1
            IF        OVRCD=0
              MOVE      THCSCOD,XXXXPRBR                              * (35)
.
              MATCH     SP70,XXXXPRBR
              IF        !@EQUAL & !@EOS
                MATCH     SP70,WMDATE3
                IF        !@EQUAL & !@EOS
                  UNPACK    WMDATE3,CCENT,CYEAR,CMON,CDAY
                  PACK      XXXXPRBD,CDAY,CMON,CCENT,CYEAR,SP70       * (37)
                ENDIF
              ENDIF
            ENDIF
          ENDIF           
.
          MATCH     SP70,BKRXAUBY
          IF        !@EQUAL & !@EOS
            PACK      KEY5,CATB2,BKRXAUBY,SP70
            CALL      RDCODE1
            IF        OVRCD=0
              MOVE      THCSCOD,XXXXPRBA                              * (36)
            ENDIF
          ENDIF
.
          MATCH     SP70,XXXXPRBR
          IF        !@EQUAL & !@EOS
            MATCH     SP70,XXXXPRBD
            IF        !@EQUAL & !@EOS
.
              CALL      IBACLOCK
              PACK      CURRDATE,CCC,CYY,CMM,CDD,SP70
              REP       " 0",CURRDATE
              MATCH     CURRDATE,WMDATE3
              IF        @LESS
                DAYSEP    WMDATE1,WMDATE3,PBRDDAYS  
              ELSE
                DAYSEP    WMDATE1,CURRDATE,PBRDDAYS
              ENDIF
              MOVE      ZERO,FORM3
              MOVE      XXXXTNRC,DIM3
              SQUEEZE   DIM3 
              MOVE      DIM3,FORM3
              SUB       FORM3,PBRDDAYS 
              PACK      XXXXPRRD,PBRDDAYS,SP70
              LJUSTIFY  XXXXPRRD                                      * (38)
.
            ENDIF
          ENDIF
.
BOKF9999  RETURN
.
.******************************************************************************
.*                                  PTAF0000              Called by: PROC0000 *
.*                           Get Patient Attributes Record                    *
.******************************************************************************
PTAF0000  PACK      KEY35,WMURNO,WMBOOK,Z70
          CALL      RSPTATR2
PTAF0010  CALL      RPPTATR2
          BRANCH    OVRCD,PTAF0090
.
          MATCH     WMURNO,PTARURNO
          GOTO      PTAF0090 IF NOT EQUAL
.
          MOVE      WMBOOK,DIM8
          MATCH     DIM8,PTARVISN
          GOTO      PTAF0090 IF NOT EQUAL
.
          MATCH     "001",PTARTYPE
          GOTO      PTAF0010 IF NOT EQUAL
.
          MATCH     "1",PTARDELR
          GOTO      PTAF0010 IF EQUAL
.
          MATCH     SP70,PTARVAL3
          IF        !@EQUAL & !@EOS
            MOVE      PTARVAL3,XXXXBMIN
            GOTO      PTAF9999
          ENDIF
.
          CALL      CLCBMI00
.
          PACK      XXXXBMIN,BODMASIN,SP70                        * (41)
.
          GOTO      PTAF9999
.
PTAF0090  CALL      CLPATATR
PTAF9999  RETURN
.
.******************************************************************************
.
          INC       STD001IO
.
          INC       PATHSPKY
          INC       SEXDSCIO
          INC       CALCBMIN
          INC       CLBOKRC1
          INC       CLBOKRX1
          INC       CLPATATR
          INC       CLPMSCEX
          INC       PMSCEXCD
          INC       CALCDAYS
          INC       CLPATHSP
          INC       TIMEDIFF
.
          INC       IBAPOSIO/INC
          INC       BOKRC1IO/INC
          INC       BOKRX1IO/INC
          INC       PATATRIO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PMSHCGIO/INC
          INC       PATHSPIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATWR1IO/INC
          INC       PMSCCDIO/INC
          INC       PMSCEXIO/INC
          INC       PMSHCPIO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
          INC       PMSWAEIO/INC
          INC       WATCATIO/INC
          INC       WATCHAIO/INC
          INC       WATOPAIO/INC
          INC       WATPROIO/INC
          INC       WATSUSIO/INC
          INC       WATTR1IO/INC
          INC       WATTX1IO/INC
          INC       WEBSECIO/INC
.

.******************************************************************************
.*                                                                            *
.*                        P R O G R A M   S U M M A R Y                       *
.*                        -------------   -------------                       *
.*                                                                            *
.*     PROGRAM NAME:     IBABIL82                                             *
.*                                                                            *
.*     FUNCTION:         DETAILED OPERATION REPORT                            *
.*                                                                            *
.*     DATE WRITTEN:     19/01/1987                                           *
.*                                                                            *
.*     AUTHOR:           MALCOLM HAYSE                                        *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.*       V12.00.02  02/06/2025 Nikitha B      TSK 0955096                     *
.*                  Alphanumeric error corrections                            *
.*       V12.00.01  15/05/2025 Nikitha B      TSK 0955096                     *
.*                  Alphanumeric changes                                      *
.*       V11.05.01  24/03/2025  Davin         TSK 0956210                     *
.*                  Add ICD10 Ed.13 - Go-live Date PTCNGDX3 (Sect.130 Pos.86) *
.*       V11.02.01  31/03/2022  Davin         TSK 0917793                     *
.*                  Add ICD10 Ed.12 - Go-live Date PTCNGDX2 (Sect.128 Pos.231)*
.*       V10.13.01  05/12/2018  Don Nguyen       TSK 0838558                  *
.*                  Deleted temp file (PSTROL) after processing               *
.*       V10.11.01  01/08/2017  J.Tan          TSK 0841947                    *
.*                  Pouplate ICDRDDTE - to validate ICD codes                 *
.*       V10.10.01  17/03/2017  Davin          TSK 0833093     HDP 2017/2018  *
.*                  Add ICD10 Ed.10 - Go-live Date PTCNGDVX (Sect.110 Pos.85) *
.*       V10.06.01  17/03/2015  Davin           CAR 311669      HDP 2015/16   *
.*                  Add PTCNGDV9 for ICD10 Ed.9 Go-live Date (Sect.110 Pos.77)*
.*       V10.05.01  02/02/2015  Ania P          CAR 261630                    *
.*                  Removed use of PORT for temp file naming                  *
.*       V10.03.02  27/09/2013  Patrick Adair   CAR 290003                    *
.*                  Replaced calls to RDPT10O1 with RDPTICO1 in PSUM1000 and  *
.*                  PSUM9000                                                  *
.*       V10.03.01  23/04/2013  Davin           CAR 284420      HDP 2013/14   *
.*                  Add PTCNGDV8 for ICD10 Ed.8 Go-live Date (Sect.110 Pos.69)*
.*       V10.02.01  23/03/2011  Mike Laming     CAR 240107                    *
.*                  Remove PTCNUCEP PATMRDFD/IO and PATMROFD/IO               *
.*                  Mods for Key changes in PATECDFD & PATECOFD - file change *
.*       V10.00.01  07/04/2010  Mike Laming     CAR 219246      HDP 2010      *
.*                  Add PTCNGDV7 for ICD10 Ed.7 Go-live Date (Sect.110 Pos.61)*
.*        V9.12.01  30/04/2009  WeeLee Koo      CAR 191085                    *
.*                  Allow Print Summary Only                                  *
.*        V9.09.01  20/03/2008  Mike Laming     CAR 163918    HDP 2008        *
.*                  Add PTCNGDV6 for ICD10 Ed.6 Go-live                       *
.*        V9.06.01  15/05/2006  Mike Laming     CAR 105244      2006 HDP      *
.*                  ADD PTCNGDV5 for ICD10 Ed.5 Go-live                       *
.*        V9.04.01  23/08/2004  Lina Vo         CAR 52901                     *
.*                  Added Multi-Hospital processing                           *
.*        V9.03.03  23/06/2004  Henry Son       CAR 49037                     *
.*                  ICD10 Ed.4 changes - add PTCNGDV4.                        *
.*        V9.03.02  30/12/2003  Sylvek Litewka  SRF 20222                     *
.*                  Replaced calls to RDPT10O1 (ICD10 Procedure V2 file read) *
.*                  with RDPTICO1 (latest ICD10 version read).                *
.*        V9.03.01  05/12/2003  Sylvek Litewka  SRF 45729                     *
.*                  Recompiled for changes made to PATECOFD.                  *
.* -------------------------------------------------------------------------- *
.*        V9.02.01  29/08/2002  Jill Habasque  SRF 17573                      *
.*                  Changed to use RTIODAYS instead of NHOSPDAY               *
.*        V5.08.02  10.10.2000 Sandra Barcham                                 *
.*                  Recompiled for WEB Report Scheduler                       *
.*        V5.08.01  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.B01 15/03/2000  Greg Horvat                                   *
.*                  Changed to get the coding from the episode coding files   *
.*                   V5.07.B03  25/01/1999  Jim Stathopoulos                  *
.*                              Report Modifications                          *
.*                   V5.07.B02  01/12/1998  Steve Armstrong                   *
.*                              Removed reference to PATIC9FD                 *
.*                   V5.07.B01  13/11/1998  Jim Stathopoulos                  *
.*                              507 Changes                                   *
.******************************************************************************
.*                       W2.01  15/05/87 P.LO                                 *
.*                              New Operation & Disease file                  *
.*                       W2.02  04/06/87 Graeme Williams                      *
.*                              Used INDEX not SORT.                          *
.*                              Fixed Length of Stay and totals print.        *
.*                       V5.01  03/05/89 S.Barcham        SRF 100402          *
.*                              Fixed 4625 I * L                              *
.*                       V5.02  16/05/89 S.O'Gorman                           *
.*                              Allow for 8-digit U/R's & Admission Numbers   *
.*                              Include STD001                                *
.*                       V5.03  27/05/89 J.Tan            SRF 100750          *
.*                              Fixed Mother Adm. to Form 8 (PATMI1FD).       *
.*                       V5.04  06/06/89 J.Tan            SRF 100809          *
.*                              Mods Locking master file (PATIOMAS)           *
.*                    V5.01.01  07/07/89 K.Peak                               *
.*                              Changed Adm & U/R to 8 characters             *
.*                    V5.01.02  13/09/89 J.Tan            SRF 102094          *
.*                              Fixed name of temp files                      *
.*                    V5.01.121 27/11/90 Glen Hobby                           *
.*                              Recompiled for changes to PATMX1FD            *
.*                    V5.01.122 21/10/91 i chung          SRF 110431          *
.*                              Added Operation Code range parameters         *
.*                    V5.01.123 18/03/92 i chung          SRF 113495          *
.*                              Modified to use CPRIMOP to control the        *
.*                              printing of primary or all operations         *
.*                    V5.01.124 20/02/1993  Graeme Williams                   *
.*                              Increased version number                      *
.*                    V5.01.125 22/06/1993  WHIRLPOOL SRF 123343              *
.*                              Changed to use PATMRO file if using episodes  *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
.
          INC       IBACONFD/INC
          INC       PATDO1FD/INC
          INC       PATCODFD/INC
          INC       PATDSCFD/INC
          INC       PATECOFD/INC
          INC       PATICOFD/INC       * ICD-9CM Operation file
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATTRNFD/INC
          INC       PMSVX1FD/INC
          INC       IBASEQFD/INC  
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
+
.
.         Temporary files for Report
.
PSTROL    FILE      ASCII, FIXED=256             * rollout file
.
GNDFILE   IFILE     SQL, FIXED=9, KEY="u1-8"     * 1st Temp Indexed file
.
.Name     Type     Size    Physical    Start
.----     ----     ----    --------    -----
.KEY6     DIM       8         8          1        Admission Number
.End of Record                           9
+
.
TEMPFILE  IFILE     SQL, FIXED=104, KEY="u1-24"   * 2nd Temp Indexed file
.
.Name     Type     Size    Physical    Start
.----     ----     ----    --------    -----
TMPCODE   DIM       9         9          1
.DAADMNO  DIM       8         8         10
DUNIQUE   DIM       7         7         18
.PNAME    DIM       21        21        25
.AURNO    DIM       8         8         46
.PBDATE   DIM       8         8         54
.PSEX     DIM       1         1         62
.ADATE    DIM       8         8         63
.DDATE    DIM       8         8         71
.NBRDAYS  FORM      4         3         79
.DNAME    DIM       22        22        82
.End of Record                         104
+
.         Temp files Work Variables
.
CMDLINE   DIM       50
FNAMEG    DIM       8
FNAMER    DIM       8
FNAMET    DIM       8
OPCND     FORM      1
STATUS    FORM      1
.
.         WORK AREA
.
ADMDATE   DIM       10                 * DD/MM/CCYY of LDATE
AVGLOS    FORM      6.2
BIRDATE   DIM       10                 * DD/MM/CCYY of RDATE
.
CALDAYS   FORM      4
COL       FORM      2
COUNT     FORM      1
CPRIMOP   FORM      1                  * controlf file variable
.
DIM9      DIM       9
DISADMS   FORM      5
DISDATE   DIM       10                 * DD/MM/CCYY of RDATE
DISDAYS   FORM      6
DNAME     DIM       22                 * DGNAME.DSNAME
DNAMES    DIM       21
.
ENDDATE   DIM       8                  * End Discharge Date in CCYYMMDD format
FROMDATE  DIM       8                  * From Date in CCYYMMDD format
.
GDISADMS  FORM      5
GDISDAYS  FORM      6
.
JDAYS     FORM      3
JYEAR     FORM      2
.
.
LSTADMN   DIM       8
.
NBRDAYS   FORM      4
RTDAYS    FORM      4
.
OPERFR    DIM       9                  * Operation Code From
OPERTO    DIM       9                  * Operation Code To
OPDESC    DIM       29                 * used in OPDS0000
OPFRDESC  DIM       43
OPTODESC  DIM       43
.
PNAME     DIM       22                 * PGNAME.PSNAME
PNAMES    DIM       21                 * Short versions of above Report
PRINSUMM  FORM      1
PSKIP     FORM      1
PTCNI10D  DIM       8
PTCNGDV3  DIM       8
PTCNGDV4  DIM       8                  * Ed.4 golive date              *I-49037
PTCNGDV5  DIM       8                  * Ed.5 golive date             *I-105244
PTCNGDV6  DIM       8                  * Ed.6 Go-live date            *I-163918
PTCNGDV7  DIM       8                  * Ed.7 Go-live date            *I-219246
PTCNGDV8  DIM       8                  * Ed.8 Go-live date            *I-284420
PTCNGDV9  DIM       8                  * Ed.9 Go-live date            *I-311669
PTCNGDVX  DIM       8                  * Ed.10 Go-live date           *I-0833093
PTCNGDX1  DIM       8                       * Go live date for ICD10 Ed.11
PTCNGDX2  DIM       8                       * Go live date for ICD10 Ed.12
PTCNGDX3  DIM       8                       * Go live date for ICD10 Ed.13
.
QUESTFLG  FORM      1                  * "?" Option flag (0=Off, 1=On)
SAVCODE   DIM       9                  * for saving Operation Code
SKEY30    DIM       30
.
TODATE    DIM       8                  * To Date in CCYYMMDD format
TOTADMS   FORM      5
TOTDAYS   FORM      6
UNIQUE    FORM      7
.
VERT      FORM      2
.
WDATE1    DIM       8                  * used in nhospday
WDATE2    DIM       8                  * used in nhospday
.
XDAY1     DIM       2
XDAY2     DIM       2
XMON1     DIM       2
XMON2     DIM       2
XYEAR1    DIM       2
XYEAR2    DIM       2
XCENT1    DIM       2
XCENT2    DIM       2
.
PRGID     INIT      "IBABIL82"
PRGNAM    INIT      "Detailed Operation Report"
VERSION   INIT      "V12.01.00"
+
.
.         DISPLAY SCREEN HEADER AND OPEN FILES
.
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TEN;*184,CPRIMOP
.
          DISPLAY   *P64:24,"paticdof";
          CALL      OPICO1
.
          DISPLAY   *P64:24,"patecoaf";
          OPEN      PATECOA2,"patecoaf"
          OPEN      PATECOA3,"patecoaf"
.
.         Set up filename for temporary files
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMER
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMET
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMEG

          MOVE      ONE,CNOUNDLN            * HEAD132 input requirement
.
.         Set up KEYDATE input requirements
.
          MOVE      ONE,CDEFDTE
          MOVE      ZERO,CHIGHLT
+
.
.         MAIN  MENU
.
START     CALL      MAINMENU
.
START1    HLINE     *G37,2,53,80
          KEYIN     *P17:7,*EF,*DV,UNDLN1,*P17:7,*V2LON,MOPTION;
          COMPARE   ZERO,MOPTION
          GOTO      ENDIT IF EQUAL
.
          BRANCH    MOPTION,KEYDATES
          DISPLAY   *P35:7,*B,*V2LON,"** Invalid Option **",*W2;
          GOTO      START1
.
MAINMENU  HLINE     *G37,2,53,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Generate Report":
                    *P1:7,"Select Option :";
          RETURN
+
.
KEYDATES  DISPLAY   *P53:2,*V2LON," - Generate Report ",*HOFF:
                    *P1:10,*EF,"From Date :":
                    *P1:11,"To   Date :";
+
.
.         Keyin From Date
.
KFDATE    MOVE      TEN3,CCOL
          MOVE      TEN,CVERT
          DISPLAY   *P13:10,*EL,*P13:11,*EF;
.
KFDATE1   UNPACK    SP6,CDAY,CMON,CYEAR
          MOVE      CCC,CCENT
          CALL      KEYDATE
          BRANCH    CQUEST,KFDATE1
          BRANCH    OVRCD,START1
.
          PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",FROMDATE
          UNPACK    FROMDATE INTO XCENT1,XYEAR1,XMON1,XDAY1
.          DISPLAY   *P13:10,*V2LON,XDAY,SLASH,XMON,SLASH,XYEAR;
.
.         Keyin To Date
.
          MOVE      TEN1,CVERT
.
KTDATE    UNPACK    FROMDATE,CCENT,CYEAR,CMON,CDAY
          CALL      KEYDATE
          BRANCH    CQUEST,KTDATE
.
          PACK      TODATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE
          UNPACK    TODATE INTO XCENT2,XYEAR2,XMON2,XDAY2
.
          MATCH     FROMDATE,TODATE         * To Date >= From Date ?
          GOTO      DATESOK IF NOT LESS     * yes   
.
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** To Date MUST be greater than or equal From Date **   ";
          CALL      HITENTER
          GOTO      KTDATE
.
.         Check if dates are ok or not
.
DATESOK   KEYIN     *P1:13,*EF,"Dates Ok (",*V2LON,"Y",*HOFF,"/":
			                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
          REP       UPPLOW,ANS
.
          MATCH     "N" TO ANS
          GOTO      KFDATE IF EQUAL
          MATCH     "Y" TO ANS
          GOTO      GETOPERS IF EQUAL
          BEEP
          GOTO      DATESOK
.
.         get Operation Codes range
.
GETOPERS  DISPLAY   *P1:14,*EF;
          CALL      OPSC0000
          CALL      OPCO0000                * keyin range of Operation Codes
          GOTO      SUMCHK
+
.*****************************************************************************
.*                                 OPSC0000                                  *
.*                       OPeration Codes range SCreen                        *
.*****************************************************************************
OPSC0000  DISPLAY   *P1:15,"From Operation Code :":
                    *P1:16,"To   Operation Code :";
          RETURN
+
.*****************************************************************************
.*                                 OPCO0000                                  *
.*                  routine to get range of OPeration COdes                  *
.*****************************************************************************
OPCO0000  MOVE      SP9,OPERFR
          MOVE      SP9,OPERTO
          MOVE      ZERO,QUESTFLG
          MOVE      TEN5,CCOL1
.
OPCO1000  DISPLAY   *P36:15,"(",*V2LON,"?",*HOFF," for Operations Scan)";
.
OPCO1100  KEYIN     *P23:CCOL1,*DV,SP9,*P23:CCOL1,*DV,UNDLN9:
                    *P23:CCOL1,*V2LON,OPERFR;
.
          ENDSET    OPERFR
          APPEND    SP9,OPERFR
          RESET     OPERFR
          DISPLAY   *P23:CCOL1,*EL,*V2LON,OPERFR;
.
          CMATCH    QUEST,OPERFR
          GOTO      OPCO4000 IF EQUAL
.
          MATCH     SP9,OPERFR
          GOTO      OPCO3000 IF NOT EQUAL
          BRANCH    QUESTFLG,OPCO2000
          DISPLAY   *P23:15,*V2LON,"Start";
          GOTO      OPCO5000
.
OPCO2000  CALL      RDSP0000
          GOTO      OPCO5000
.
OPCO3000  MOVE      OPERFR,KEY9
          MOVE      FROMDATE,ICDRDDTE
          CALL      RDPTICO1
          IF        OVRCD=1 & ICDRDVER>1
            CALL      RD10T9O1
          ENDIF
          BRANCH    OVRCD,OPCO3200
          MOVE      ODESC,OPFRDESC
.
          BRANCH    QUESTFLG,OPCO3100
          DISPLAY   *P36:15,OPFRDESC;
          GOTO      OPCO5000
.
OPCO3100  CALL      RDSP0000
          GOTO      OPCO5000
.
OPCO3200  DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid Operation Code **    ";
          CALL      HITENTER
          BRANCH    QUESTFLG,OPCO4100
          GOTO      OPCO1000
.
OPCO4000  CALL      OPDS0000
          MOVE      "24",CCOL1
          MOVE      ONE,QUESTFLG
.
OPCO4100  DISPLAY   *P1:24,*EL,"From Operation Code :";
          GOTO      OPCO1100
.
OPCO5000  MOVE      ZERO,QUESTFLG
          MOVE      TEN6,CCOL1
.
OPCO5100  DISPLAY   *P36:16,"(",*V2LON,"?",*HOFF," for Operations Scan)";
.
OPCO5200  KEYIN     *P23:CCOL1,*DV,SP9,*P23:CCOL1,*DV,UNDLN9:
                    *P23:CCOL1,*V2LON,OPERTO;
.
          ENDSET    OPERTO
          APPEND    SP9,OPERTO
          RESET     OPERTO
          DISPLAY   *P23:CCOL1,*EL,*V2LON,OPERTO;
.
          CMATCH    QUEST,OPERTO
          GOTO      OPCO8000 IF EQUAL
.
          MATCH     SP9,OPERTO
          GOTO      OPCO7000 IF NOT EQUAL
          MOVE      "zzzzzzzzz",OPERTO
          BRANCH    QUESTFLG,OPCO6000
          DISPLAY   *P23:16,*V2LON,"Finish";
          GOTO      OPCO9999
.
OPCO6000  CALL      RDSP0000
          GOTO      OPCO9999
.
OPCO7000  MOVE      OPERTO,KEY9
          MOVE      FROMDATE,ICDRDDTE
          CALL      RDPTICO1
          IF        OVRCD=1 & ICDRDVER>1
            CALL      RD10T9O1
          ENDIF
          BRANCH    OVRCD,OPCO7400
          MOVE      ODESC,OPTODESC
.
OPCO7100  MATCH     OPERFR,OPERTO
          GOTO      OPCO7200 IF NOT LESS
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** To Code MUST be greater than or equal From Code **   ";
          CALL      HITENTER
          BRANCH    QUESTFLG,OPCO8100
          GOTO      OPCO5100
.
OPCO7200  BRANCH    QUESTFLG,OPCO7300
          DISPLAY   *P36:16,OPTODESC;
          GOTO      OPCO9999
.
OPCO7300  CALL      RDSP0000
          GOTO      OPCO9999
.
OPCO7400  DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid Operation Code **    ";
          CALL      HITENTER
          BRANCH    QUESTFLG,OPCO8100
          GOTO      OPCO5100
.
OPCO8000  CALL      OPDS0000
          MOVE      "24",CCOL1
          MOVE      ONE,QUESTFLG
.
OPCO8100  DISPLAY   *P1:24,*EL,"  To Operation Code :";
          GOTO      OPCO5200
.
OPCO9999  RETURN
+
.*****************************************************************************
.*                                 RDSP0000                                  *
.*                ReDiSPlay Screen after Operation Codes scan                *
.*****************************************************************************
RDSP0000  CALL      MAINMENU
.
          DISPLAY   *P53:2,*V2LON," - Generate Report":
                    *P17:7,MOPTION,*HOFF:
                    *P1:10,"From Date : ":
                           *V2LON,XDAY1,"/",XMON1,"/",XCENT1,XYEAR1,*HOFF:
                    *P1:11,"To   Date : ":
                           *V2LON,XDAY2,"/",XMON2,"/",XCENT2,XYEAR2;
.
          CALL       OPSC0000
.
          MATCH     SP9,OPERFR
          GOTO      RDSP1000 IF EQUAL
          DISPLAY   *P23:15,*EL,*V2LON,OPERFR,*HOFF,*P36:15,OPFRDESC;
          GOTO      RDSP2000
.
RDSP1000  DISPLAY   *P23:15,*EL,*V2LON,"Start";
.
RDSP2000  MATCH     SP9,OPERTO
          GOTO      RDSP9999 IF EQUAL
          MATCH     "zzzzzzzzz",OPERTO
          GOTO      RDSP2100 IF EQUAL
          DISPLAY   *P23:16,*EL,*V2LON,OPERTO,*HOFF,*P36:16,OPTODESC;
          GOTO      RDSP9999
.
RDSP2100  DISPLAY   *P23:16,*EL,*V2LON,"Finish";
.
RDSP9999  RETURN
+
.*****************************************************************************
.*                                 OPDS0000                                  *
.*                           Operation Codes Scan                            *
.*****************************************************************************
OPDS0000  DISPLAY   *P1:4,*EF,*P32:4,*V2LON,*ULON," OPERATION CODES ",*HOFF:
                    *P22:5,"(in Medical Records Operations file)";
.
OPDS1000  MOVE      SP30,KEY25                                        *C-240107
          CALL      RSPTECO3
          CALL      RKPTECO3
          BRANCH    OVRCD,OPDS8000
          MOVE      PTEOCODE,SAVCODE
.
.  ------ loop over entries ------
.
          MOVE      SEVEN,CVERT
          MOVE      ONE,CCOL
          GOTO      OPDS2100
.
OPDS2000  CALL      RKPTECO3
          BRANCH    OVRCD,OPDS9000
          MATCH     PTEOCODE,SAVCODE
          GOTO      OPDS2000 IF EQUAL
          MOVE      PTEOCODE,SAVCODE
.
OPDS2100  MOVE      PTEOCODE,KEY9
          MOVE      "UNK (not in ICD-9CM Op. file)",ODESC
          MOVE      FROMDATE,ICDRDDTE
          CALL      RDPTICO1
          IF        OVRCD=1 & ICDRDVER>1
            CALL      RD10T9O1
          ENDIF
          MOVE      ODESC,OPDESC
.
          DISPLAY   *PCCOL:CVERT,*V2LON,PTEOCODE,*HOFF,SP1,OPDESC;
          ADD       ONE,CVERT
          COMPARE   TWENTY3,CVERT
          GOTO      OPDS2000 IF LESS
.
          MOVE      SEVEN,CVERT
          ADD       "40",CCOL
          COMPARE   "70",CCOL
          GOTO      OPDS2000 IF LESS
.
          DISPLAY   *P1:24,*EL,"(",*V2LON,"C",*HOFF,")ontinue, (",*V2LON:
                               "F",*HOFF,")irst screen or (",*V2LON,"N",*HOFF:
                               ")ext screen :";
.
OPDS3000  KEYIN     *P47:24,*DV,UNDLN1,*P47:24,*V2LON,ANS;
          RESET     ANS
          GOTO      OPDS4000 IF EOS
          REP       UPPLOW,ANS
.
          MATCH     ANSC,ANS                    * Continue ?
          GOTO      OPDS9000 IF EQUAL           * yes
          MATCH     ANSF,ANS                    * First screen ?
          GOTO      OPDS1000 IF EQUAL           * yes
          MATCH     ANSN,ANS                    * Next screen ?
          GOTO      OPDS4000 IF EQUAL           * yes
          BEEP                                  * invalid input - beep &
          GOTO      OPDS3000                                      re-enter
.
OPDS4000  MOVE      SEVEN,CVERT
          MOVE      ONE,CCOL
          DISPLAY   *P1:7,*EF;
          GOTO      OPDS2000
.
. ------  no entries in file  ------
.
OPDS8000  DISPLAY   *P1:24,*EL;
          CALL      HITENTER
          GOTO      OPDS9999
.
. ------  end of Codes Table file  ------
.
OPDS9000  DISPLAY   *P1:24,*EL,"(",*V2LON,"C",*HOFF,")ontinue, (",*V2LON:
                               "F",*HOFF,")irst screen :";
.
OPDS9100  KEYIN     *P30:24,*DV,UNDLN1,*P30:24,*V2LON,ANS;
          RESET     ANS
          GOTO      OPDS9999 IF EOS
          REP       UPPLOW,ANS
.
          MATCH     ANSC,ANS                    * Continue ?
          GOTO      OPDS9999 IF EQUAL           * yes
          MATCH     ANSF,ANS                    * First screen ?
          GOTO      OPDS1000 IF EQUAL           * yes
          BEEP                                  * invalid input - beep &
          GOTO      OPDS9100                                      re-enter
.
OPDS9999  RETURN
+
.******************************************************************************
.*                                  KHOS0000              Called by: ML0000   *
.*                            Enter The Date Ranges                           *
.******************************************************************************
KHOS0000  COMPARE   ONE,IBCNMHOS
          GOTO      KHOS9999 IF NOT EQUAL
.
          DISPLAY   *P1:22,*EL,"Hospital Id : ";
          MOVE      TEN5,ECOL
          MOVE      "22",EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS2000,KHOS9500
          GOTO      KHOS3000
.
KHOS2000  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          MOVE      ZERO,EXIT
.
KHOS3000  CLOSE     PATHSPA1
.
          DISPLAY   *P20:22,*V2LON,PTHSNAME;
          GOTO      KHOS9999
.
KHOS9500  MOVE      ONE,EXIT
KHOS9999  RETURN
+
.*****************************************************************************
.
SUMCHK    DISPLAY   *P1:18,*EF,"Print Summary Only (":
                    *V2LON,ANSY,*HOFF,SLASH,*V2LON,ANSN,*HOFF,") ?";
.
SUMCHK1   MOVE      ONE,PRINSUMM            * print the summary only
          KEYIN     *P28:18,*DV,UNDLN1,*P28:18,*V2LON,ANS;
          REP       UPPLOW,ANS
          DISPLAY   *P28:18,*V2LON,ANS;
.
          MATCH     ANSY,ANS
          GOTO      SEPPAGE IF EQUAL
          MATCH     ANSN,ANS
          GOTO      SUMNO IF EQUAL
          BEEP
          GOTO      SUMCHK1
.
SUMNO     MOVE      ZERO,PRINSUMM           * print details
.
SEPPAGE   KEYIN     *P1:20,"Would you like each OPERATION on a separate Page (":
		    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ? : ",*EL:
                    *V2LON,ANS;
          RESET     ANS
          GOTO      SEPPAGE1 IF NOT EOS
          MOVE      ANSN,ANS
          GOTO      SEPPAGE3
.
SEPPAGE1  REP       UPPLOW,ANS
          MATCH     "Y",ANS
          GOTO      SEPPAGE2 IF EQUAL
          MATCH     "N",ANS
          GOTO      SEPPAGE3 IF EQUAL
          BEEP
          GOTO      SEPPAGE
.
SEPPAGE2  MOVE      ZERO,PSKIP
          GOTO      SEPPAGE4
.
SEPPAGE3  MOVE      ONE,PSKIP 
.
SEPPAGE4  DISPLAY   *P60:20,*V2LON,ANS;
          CALL      KHOS0000
          BRANCH    EXIT,KFDATE
.
.         Check if it's okay to continue
.
OKCONT    CALL      CONTQST
          BRANCH    CEXIT,TRANNO,KFDATE,START1
.                           Yes    No    Cancel
.
.         Data Extraction Process
.
TRANNO    CALL      CREAIDX                 * Create and open index files
          BRANCH    OPCND OF START
.
          MOVE      ZERO,CPAGENO
          MOVE      ZERO,NBRDAYS
.
          MOVE      ZERO,DISADMS
          MOVE      ZERO,DISDAYS
.
          MOVE      ZERO,GDISADMS
          MOVE      ZERO,GDISDAYS

          MOVE      ZERO,UNIQUE
          MOVE      ZERO,TOTADMS 
          MOVE      ZERO,TOTDAYS
.
          MOVE      "60",CLNO
          MOVE      ZEROVISN,LSTADMN
          MOVE      TODATE,ENDDATE          * Save End Discharge Date
.
          DISPLAY   *P1:24,*EL,*V2LON,*P30:24,"** Extracting Data **",*W2:
                    *HOFF,*P1:24,*EL,"Discharged Date :":
                    *P30:24,"No. of Pat.:",*P51:24,"Scanning :";
.
.         Loop over Discharge file
.
          PACK      KEY16 WITH FROMDATE,SP8
          CALL      RDSDSCH2
LOOP1     CALL      RDKDSCH2
          BRANCH    OVRCD OF SORTD
.
          UNPACK    DDATE INTO XCENT,XYEAR,XMON,XDAY
          PACK      XDATE INTO XCENT,XYEAR,XMON,XDAY
          MOVE      XDATE,TODATE
.
          DISPLAY   *P62:24,XDAY,SLASH,XMON,SLASH,XYEAR,SP2,DADMNO;
.
          MATCH     XDATE,ENDDATE
          GOTO      SORTD IF LESS
.
.         Multi-Hospital Processing
.
          COMPARE   ONE,IBCNMHOS
          GOTO      LOOP10 IF NOT EQUAL
.
          MATCH     SP10,PTHSHOSP
          GOTO      LOOP10 IF EQUAL
.
          MOVE      DADMNO,KEY8
          CALL      RDPMVX11
          BRANCH    OVRCD,LOOP1
.
          MATCH     PMVXMHOS,PTHSHOSP
          GOTO      LOOP1 IF NOT EQUAL
.
LOOP10    MOVE      SP9,PTEOCODE 
          PACK      KEY16,DADMNO,SP20                                 *C-240107
          CALL      RSPTECO2
LOOP2     CALL      RKPTECO2
          BRANCH    OVRCD,LOOP1
.
          MATCH     DADMNO,PTEOADMN
          GOTO      LOOP1 IF NOT EQUAL
.
          MATCH     OPERFR,PTEOCODE    * Operation Code >= Operation Code From ?
          GOTO      LOOP2 IF LESS      * no
.
          MATCH     PTEOCODE,OPERTO    * Operation Code To >= Operation Code ?
          GOTO      LOOP2 IF LESS      * no
.
          COMPARE   ONE,CPRIMOP             * Primary Operations only ?
          GOTO      CONTLOOP IF NOT EQUAL   * no - continue processing
          COMPARE   ONE,PTEOUNQN            * yes - primary operation ?
          GOTO      LOOP2 IF NOT EQUAL      * no - read next record
.
CONTLOOP  MATCH     DADMNO,LSTADMN
          GOTO      WRTREC IF EQUAL
.
          DISPLAY   *P19:24,*V2LON,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR;
.
.         Get various bits of info about this admission
.
          MOVE      DADMNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD OF NOADMR
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF NOURC
.
          MOVE      PGNAME,ANS
          PACK      PNAME WITH ANS,DOT,PSNAME
          REP       ", ",PNAME
.
          MOVE      SP30,DNAME
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD OF NOADOC
.
          MOVE      DGNAME,ANS
          PACK      DNAME WITH ANS,DOT,DSNAME
          REP       ", ",DNAME
.
NOADOC    UNPACK    ADATE INTO XCENT,XYEAR,XMON,XDAY
          PACK      FROMDATE WITH XCENT,XYEAR,XMON,XDAY
.
          CALL      RTIODAYS
.
          MOVE      AADMNO,KEY8
          READ      GNDFILE,KEY8;KEY8
          GOTO      WRTREC IF NOT OVER
          ADD       ONE,TOTADMS
          ADD       NBRDAYS,TOTDAYS         * GRAND TOTAL DAYS
          WRITE     GNDFILE,KEY8;KEY8
.
WRTREC    MOVE      UNIQUE TO DUNIQUE
          MOVE      AADMNO,DAADMNO
          PACK      KEY24,PTEOCODE,DAADMNO,DUNIQUE
          WRITE     TEMPFILE,KEY24;KEY24,PNAME,AURNO,PBDATE,PSEX:
                                   ADATE,DDATE,NBRDAYS,DNAME
          ADD       ONE,UNIQUE
          MOVE      AADMNO,LSTADMN
.
          DISPLAY   *P19:24,*V2LON,XDAY,SLASH,XMON,SLASH,XYEAR,*P43:24,TOTADMS;
          GOTO      LOOP2
.
NOADMR    DISPLAY   *P1:22,*EL,*B,*V2LON,*P16:22,"** Missing Admission record":
                    " for Admission ",DADMNO," **";
          GOTO      LOOP1
.
NOURC     DISPLAY   *P1:21,*EL,*B,*V2LON,*P13:21,"** Missing Master file ":
                    "record for U/R Number ",AURNO," **";
          GOTO      LOOP1
.
.         End of Extracting Data
.
SORTD     COMPARE   ZERO,TOTADMS
          GOTO      NOOPRS IF EQUAL
.
          DISPLAY   *P1:24,*EL,*P29:24,*V2LON,"** Generating Report **",*W2:
                    *P1:24,*EL,*HOFF,*P23:24,"Printing Operation Code :";
.
          MOVE      ZERO,CPAGENO
          MOVE      "60",CLNO
.
          COMPARE   ONE,PRINSUMM
          CALL      PSUM0000 IF EQUAL       * only printing the summary
          BRANCH    PRINSUMM,DELFILE
.
          MOVE      SP30 TO KEY24
          READ      TEMPFILE,KEY24;;
.
          READKS    TEMPFILE;TMPCODE,DAADMNO,DUNIQUE,PNAME,AURNO,PBDATE:
                             PSEX,ADATE,DDATE,NBRDAYS,DNAME
          GOTO      ENDP IF OVER
          MOVE      TMPCODE,SAVCODE
          GOTO      READLOP1
.
READLOP   READKS    TEMPFILE;TMPCODE,DAADMNO,DUNIQUE,PNAME,AURNO,PBDATE:
                             PSEX,ADATE,DDATE,NBRDAYS,DNAME
          GOTO      ENDP IF OVER
.
READLOP1  MOVE      DUNIQUE,UNIQUE
          MOVE      DAADMNO,AADMNO
.
          DISPLAY   *P49:24,*V2LON,TMPCODE;
.
          CALL      DISPDA
          ADD       ONE,DISADMS
          ADD       NBRDAYS,DISDAYS
          GOTO      READLOP
.
.         OVER Condition found..print last line of -- and display message
.
ENDP      COMPARE   ZERO,CPAGENO
          GOTO      NOOPRS IF EQUAL
          CALL      DTOTAL
.
          MOVE      TOTDAYS,AVGLOS
          DIV       TOTADMS,AVGLOS
.
          PRINT     *1,"                           -----",*79,"------":
                    *87,"                       ---------":
                    *N," ***   GRAND TOTALS   ***  ",TOTADMS,*79,TOTDAYS:
                    *87,"                       ",AVGLOS:
                    *N,"                           -----",*79,"------":
                    *87,"                       ---------"
.
          PRINT     *N," ***  End of Report  ***"
          DISPLAY   *P1:24,*EL,*P24:24,*V2LON:
                    "** Report Generation Completed **",*W2;
          GOTO      DELFILE
.
NOOPRS    DISPLAY   *P1:24,*EL,*P21:24,*B,*V2LON:
                    "** No Operations for ranges specified **",*W2;
.
DELFILE   CALL      KILLIDX
          GOTO      START1
.
.         Deleting an Indexed file based on Port
.
KILLIDX   CLOSE     TEMPFILE
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMET,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CLOSE     GNDFILE
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMEG,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          RETURN
+
.
CREAIDX   DISPLAY   *P1:24,*EL,*V2LON,*P22:24:
                    "** Creating Temporary Indexed files **",*W1;
.
          CALL      KILLIDX
          PREP      PSTROL,FNAMER
          WRITE     PSTROL,SEQ;"isbuild ",FNAMET," 104 u1-24",015:
                               "isbuild ",FNAMEG," 9 u1-8"
          WEOF      PSTROL,SEQ
.
          CALL      EXEROLL
          OPEN      TEMPFILE,FNAMET
          OPEN      GNDFILE,FNAMEG
          RETURN
+
.
EXEROLL   MOVE      ONE,STATUS
.
          CLEAR     CMDLINE
          APPEND    "sh ",CMDLINE
          APPEND    FNAMER,CMDLINE
          APPEND    ".txt",CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
          BRANCH    STATUS OF INDO1
.
          MOVE      ONE,OPCND
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Temporary Index files not created **    ";
          CALL      HITENTER
.
INDO1     CLOSE     PSTROL,DELETE      * delete rollout file
          RETURN
+
.         Print actual Data 
.
DISPDA    COMPARE   "56",CLNO
          CALL      HEAD IF NOT LESS
.
          MATCH     TMPCODE,SAVCODE
          GOTO      FPRINTC IF EQUAL
          MOVE      TMPCODE,SAVCODE
.
          CALL      DTOTAL
.
          COMPARE   "49",CLNO
          GOTO      FPRINTA IF NOT LESS
.
          BRANCH    PSKIP OF FPRINTB
.
FPRINTA   CALL      HEAD1
          GOTO      FPRINTC
.
FPRINTB   CALL      HEADNT
.
FPRINTC   UNPACK    PBDATE WITH XCENT,XYEAR,XMON,XDAY
          PACK      BIRDATE WITH XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
.
          UNPACK    ADATE WITH XCENT,XYEAR,XMON,XDAY
          PACK      ADMDATE WITH XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
.
          UNPACK    DDATE WITH XCENT,XYEAR,XMON,XDAY
          PACK      DISDATE WITH XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
.
          MOVE      PNAME,PNAMES
          MOVE      DNAME,DNAMES
.
          PRINT     *1,"| ",PNAMES,*24,"|",AURNO,*33,"|",AADMNO:
                    *42,"|",BIRDATE,*53,"| ",PSEX,*57,"|",ADMDATE:
                    *68,"|",DISDATE,*79,"| ",NBRDAYS,*86,"| ",DNAMES,*110,"| ";
.
          MOVE      SP9,PTEOCODE 
          MOVE      ZERO,COUNT
          PACK      KEY16,AADMNO,SP20                                 *C-240107
          CALL      RSPTECO2
.
LOOP3     CALL      RKPTECO2
          BRANCH    OVRCD OF NOMORE
.
          MATCH     AADMNO,PTEOADMN
          GOTO      NOMORE IF NOT EQUAL
.
          MATCH     PTEOCODE,OPCODE
          GOTO      LOOP3 IF EQUAL
.
          BRANCH    COUNT OF ONEC,TWOC
          PRINT     PTEOCODE,SP1;
          MOVE      TWO,COUNT
          GOTO      LOOP3
.
ONEC      PRINT     "|",*24,"|",*33,"|",*42,"|",*53,"|",*57,"|",*68,"|":
                    *79,"|",*86,"|",*110,"| ",PTEOCODE,SP1;
          MOVE      TWO,COUNT
          GOTO      LOOP3
.
TWOC      PRINT     PTEOCODE,SP1,"|"
          MOVE      ONE,COUNT
          ADD       ONE,CLNO
          GOTO      LOOP3
.
NOMORE    BRANCH    COUNT OF ONEX,TWOX
.
TWOX      PRINT     *132,"|"
          ADD       ONE,CLNO
.
ONEX      RETURN
+
.         Subroutine to print sub totals
.
DTOTAL    COMPARE   TEN6,CLNO
          CALL      PAGEND IF NOT LESS
.
          MOVE      DISDAYS,AVGLOS
          DIV       DISADMS,AVGLOS
.
          PRINT     *N," Total Number of Patients  ",DISADMS:
                    *58,"Total Bed Days ",*79,DISDAYS:
                    *87,"Average Length of Stay ",AVGLOS,*N
.
          ADD       THREE,CLNO
          MOVE      ZERO,DISADMS
          MOVE      ZERO,DISDAYS
          RETURN
+
.         HEADINGS for Output
.
HEAD      COMPARE   ZERO,CPAGENO
          CALL      PAGEND IF NOT EQUAL
.
HEAD1     CLOCK     TIME,CTIMEIS
          CALL      HEAD132
.
          IF        IBCNMHOS =1
            PRINT     *40,"Hospital        : ",PTHSNAME
          ENDIF
.
          PRINT     *40,"Date Range      : ",XDAY1,SLASH,XMON1,SLASH,XYEAR1:
                        "  to  ",XDAY2,SLASH,XMON2,SLASH,XYEAR2,*N:
                    *40,"Operation Range : ";
.
          MATCH     SP9,OPERFR
          GOTO      HEAD2 IF EQUAL
          PRINT     OPERFR;
          GOTO      HEAD3
.
HEAD2     PRINT     "  Start  ";
.
HEAD3     PRINT     " to  ";
          MATCH     "zzzzzzzzz",OPERTO
          GOTO      HEAD4 IF EQUAL
          PRINT     OPERTO,*N
          GOTO      HEAD5
.
HEAD4     PRINT     " Finish",*N
.
HEAD5     MOVE      EIGHT,CLNO
.
HEADNT    PACK      ODESC,SP30,SP30,SP10
          MOVE      TMPCODE,KEY9
          MOVE      "UNKNOWN (not in ICD-9CM Operations file)",ODESC
          MOVE      FROMDATE,ICDRDDTE
          CALL      RDPTICO1
          IF        OVRCD=1 & ICDRDVER>1
            CALL      RD10T9O1
          ENDIF
.
          PRINT     *N,*40,"OPERATION : ",TMPCODE,"  ",ODESC,*N
.
          CALL      PAGEND
.
          PRINT     *1,"| Patient's Name",*24,"| U/R No",*33,"| Adm No":
                   *42,"| Date of",*53,"|Sex| Admission| Discharge| Bed":
                   *86,"| Attending Doctor",*110,"|Other Operation Codes|":
                    *N,"|",*24,"|",*33,"|",*42,"|  Birth",*53,"|":
                   *57,"|   Date",*68,"|   Date",*79,"| Days":
                   *86,"|",*110,"|",*132,"|"
.
          CALL      PAGEND
          ADD       FIVE,CLNO
          RETURN
+
.         Separation Line on the Report
.
PAGEND    PRINT     *1,"*-------------------------------------------":
                       "--------------------------------------------":
                       "-------------------------------------------*"
          ADD       ONE,CLNO
          RETURN
+
.****************************************************************************
.*                              PSUM0000                                    *
.*                  Print the summary report only                           *
.****************************************************************************
PSUM0000  MOVE      ZERO,DISADMS
          MOVE      ZERO,DISDAYS
          MOVE      ZERO,AVGLOS
          MOVE      "60",CLNO
          PACK      KEY24,SP30
          RESET     KEY24
          READ      TEMPFILE,KEY24;;
.
          READKS    TEMPFILE;TMPCODE,DAADMNO,DUNIQUE,PNAME,AURNO,PBDATE:
                             PSEX,ADATE,DDATE,NBRDAYS,DNAME
.
          MOVE      TMPCODE,DIM9
          MOVE      SP30 TO KEY24
          READ      TEMPFILE,KEY24;;
.
PSUM1000  READKS    TEMPFILE;TMPCODE,DAADMNO,DUNIQUE,PNAME,AURNO,PBDATE:
                             PSEX,ADATE,DDATE,NBRDAYS,DNAME
          GOTO       PSUM9000 IF OVER
.
          MATCH     TMPCODE,DIM9
          GOTO      PSUM2000 IF EQUAL
          MOVE      DISDAYS,AVGLOS
          DIV       DISADMS,AVGLOS
.
          PACK      ODESC,SP30,SP30,SP10
          PACK      KEY9,DIM9
          MOVE      FROMDATE,ICDRDDTE
          CALL      RDPTICO1
          IF        OVRCD=1 & ICDRDVER>1
            CALL      RD10T9O1
          ENDIF
.
          PRINT     "| ",DIM9,SP2,ODESC,*95,"| ",DISADMS,*108,"| ":
                    DISDAYS,*119,"| ",AVGLOS,*132,"|"
          ADD       ONE,CLNO
          CALL      UND132
.
          MOVE      TMPCODE,DIM9
.
          MOVE      ZERO,DISADMS
          MOVE      ZERO,DISDAYS
          MOVE      ZERO,AVGLOS
.
PSUM2000  ADD       ONE,DISADMS
          ADD       ONE,GDISADMS
          ADD       NBRDAYS,DISDAYS
          ADD       NBRDAYS,GDISDAYS
.
          COMPARE   "56",CLNO
          CALL      PSHD0000 IF NOT LESS       * print summary heading
          GOTO      PSUM1000

.------  Print the last record and the grand totals ------
.
PSUM9000  MOVE      DISDAYS,AVGLOS
          DIV       DISADMS,AVGLOS
.
          MOVE      TMPCODE,DIM9
          PACK      ODESC,SP30,SP30,SP10
          PACK      KEY9,TMPCODE
          MOVE      FROMDATE,ICDRDDTE
          CALL      RDPTICO1
          IF        OVRCD=1 & ICDRDVER>1
            CALL      RD10T9O1
          ENDIF
.
          PRINT     "| ",TMPCODE,SP2,ODESC,*95,"| ",DISADMS,*108,"| ":
                    DISDAYS,*119,"| ",AVGLOS,*132,"|"
          ADD       ONE,CLNO
          CALL      UND132
.
.------ Print the grand totals -----
.
          MOVE      GDISDAYS,AVGLOS
          DIV       GDISADMS,AVGLOS
          PRINT     *1,"|",*132,"|",*N,*1,"| ****  GRAND TOTALS  ****":
                    *97,GDISADMS,*110,GDISDAYS,*121,AVGLOS,*132,"|"
          ADD       TWO,CLNO
          CALL      UND132
.
          COMPARE   "53",CLNO
          CALL      PSHD0000 IF NOT LESS       * print summary heading
          MOVE      TOTDAYS,AVGLOS
          DIV       TOTADMS,AVGLOS
          PRINT     *N,"Actual Patient Bed Days       = ",TOTDAYS:
                    *N,"Actual Number of Patients     = ",TOTADMS:
                    *N,"Actual Average Length of Stay = ",AVGLOS:
                    *N,*N,"NOTE : Figures will differ if a patient has ":
                           "more than one operation in the same period. ":
                    *N,*N,*N,"***  End of Report  ***"
.
          DISPLAY   *P1:24,*EL,*V2LON:
                    "*** Summary Report Completed ***  ";
          CALL      HITENTER
PSUM9999  RETURN
+
.****************************************************************************
.*                              PSHD0000                                    *
.*                  Print the summary report heading                        *
.****************************************************************************
PSHD0000  CLOCK     TIME,CTIMEIS
          CALL      HEAD132
.
          PRINT     *N,*40,"Date Range      : ",XDAY1,SLASH,XMON1,SLASH,XCENT1:
                    XYEAR1,"  to  ",XDAY2,SLASH,XMON2,SLASH,XCENT2,XYEAR2
.
          MOVE      FIVE,CLNO
          PRINT     *40,"Operation Range : ";
.
          MATCH     SP9,OPERFR
          GOTO      PSHD2000 IF EQUAL
          PRINT     OPERFR;
          GOTO      PSHD3000
.
PSHD2000  PRINT     "  Start  ";
.
PSHD3000  PRINT     " to  ";
          MATCH     "zzzzzzzzz",OPERTO
          GOTO      PSHD4000 IF EQUAL
          PRINT     OPERTO
          ADD       ONE,CLNO
          GOTO      PSHD5000
.
PSHD4000  PRINT     " Finish"
          ADD       ONE,CLNO
.
PSHD5000  PRINT     *N;
          CALL      UND132
          PRINT     *1,"| Operations",*95,"| No of Pats",*108,"|":
                    *110,"Bed Days",*119,"| Av. L.O.S. |"
          ADD       TWO,CLNO
          CALL      UND132
.
PSHD9999  RETURN
+
.
ENDIT     CHAIN     PGM
          STOP
+
.==============================================================================
.
          INC       STD001IO
          INC       RTIODAYS
          INC       PATHSPKY
.
          INC       PATDO1IO/INC
          INC       PATCODIO/INC
          INC       PATDSCIO/INC
          INC       PATECOIO/INC
          INC       PATICOIO/INC
          INC       PATHSPIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATTRNIO/INC
          INC       PMSVX1IO/INC
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC
+

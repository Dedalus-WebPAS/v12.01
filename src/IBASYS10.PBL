. ********************************************************
. *             P R O G R A M  S U M M A R Y             *
. *             - - - - - - -  - - - - - - -             *
. *                                                      *
. *     PROGRAM NAME:   IBASYS10                         *
. *                                                      *
. *     FUNCTION:       SYSTEM TRACKING ENQUIRY          *
. *                                                      *
. *     DATE WRITTEN:   03/02/88                         *
. *                                                      *
. *     AUTHOR:         MALCOLM HAYSE (I.B.A)            *
. *                                                      *
. *     MODIFICATIONS:  V9.06.01 09/5/06 Sandra Barcham  *
. *                             Changed name from SYSDATE*
. *                             SYSIDAT                  *
. *                     V5.07.00 10/9/98 Jim Stathopoulos*
. *                             507 Changes              *
. *                     W2.01 M.HAYSE 08/02/88           *
. *                     DISPLAY BUG ON PROG OPT          *
. *                     W2.02 M.HAYSE 10/05/88           *
. *                     ERROR TRACKING FROM MASTER       *
. *                     V5.00 M.HAYSE 18/10/88           *
. *                     SHOW ERRORS ONLY                 *
. *                                                      *
. *                     V5.01 B.Ackland 18/06/89         *
. *                     Fix keyin bug at line 242        *
. *                                                      *
. *                     V5.01.02 11/07/90 Steve O'Gorman *
. *                              SRF 104886    Recompiled*
. *                              for IBACONFD change     *
. *                                                      *
. *                     V6.00    03/09/91 Steve O'Gorman *
. *                              Upgrade to Rel 6        *
. *                                                      *
. ********************************************************
.
          INC       STD001FD
          INC       IBAPASFD/INC
.
          INC       IBACONFD/INC
.
          INC       IBAPORFD/INC
          INC       IBASECFD/INC
          INC       IBASYSFD/INC
.
DIM10     DIM       10
.
.
HEADLNO   FORM      2
.
OUTFLAG   FORM      1
ERRFLAG   FORM      1
.
OPTDESC   DIM       12
OPTDES1   INIT      "By Program"
OPTDES2   INIT      "By Port   "
OPTDES3   INIT      "By User   "
OPTDES4   INIT      "By Date   "
.
VERTPROG  FORM      2
VERTPORT  FORM      2
VERTUSER  FORM      2
VERTDATE  FORM      2
.
SP22      INIT      "                      "
SP24      INIT      "                        "
SP27      INIT      "                           "
SP40      INIT      "                                        "
.
SRDIM17A  DIM       17
.
TODAY     DIM       8       ***** TODAYS DATE CCYYMMDD
.
.
.
USERCODE  DIM       4
USERDATE  DIM       8       (CCYYMMDD)
USERPROG  DIM       8
USERPORT  DIM       2
.
.
PRGID     INIT      "IBASYS10"
PRGNAM    INIT      "System Tracking"
VERSION   INIT      "V12.01.00"
+
.         START OF PROGRAM
.
          DISPLAY   *ES,PRGID:
                    *P56:24,"Opening ibasysa1";
          OPEN      IBASYSA1,"ibasysaf"
.
          DISPLAY   *P64:24,"ibasysa2";
          OPEN      IBASYSA2,"ibasysaf"
.
          DISPLAY   *P64:24,"ibasysa3";
          OPEN      IBASYSA3,"ibasysaf"
.
          DISPLAY   *P64:24,"ibasysa4";
          OPEN      IBASYSA4,"ibasysaf"
.
          DISPLAY   *P64:24,"ibasecu1";
          OPEN      IBASECU1,"ibasecuf"
.
          DISPLAY   *P64:24,"ihapass1";
          OPEN      IHAPASS1,"ihapassf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
.         READ      CONTROLF,TWENTY3;*84,HUSERCD,*129,HSTART
.
          PACK      TODAY WITH CCC,CYY,CMM,CDD
          REP       " 0",TODAY
.
.       ALL FILES WILL USE TODAYS DATE AS DATE OF USUAGE
.
START     CALL      DISPHEAD
.
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = ",OPTDES1:
                    *P1:6,*V2LON,TWO,*HOFF," = ",OPTDES2:
                    *P1:7,*V2LON,THREE,*HOFF," = ",OPTDES3:
                    *P1:8,*V2LON,FOUR,*HOFF," = ",OPTDES4
.
BEGIN     KEYIN     *P1:10,*EL,"Please Select : ",*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      ENDIT IF EQUAL
.
          BRANCH    OPTION OF VALIDOPT,VALIDOPT,VALIDOPT,VALIDOPT
          DISPLAY   *P35:10,*B,*V2LON,"** Invalid Selection **",*W2;
          GOTO      BEGIN
.
VALIDOPT  LOAD      OPTDESC USING OPTION OF OPTDES1,OPTDES2,OPTDES3,OPTDES4
          DISPLAY   *P45:2,*V2LON,"- ",OPTDESC
.
          KEYIN     *P1:15,"Only select Active Programs (":
                    *V2LON,*DV,ANSY,*HOFF,*DV,SLASH:
                    *V2LON,*DV,ANSN,*HOFF:
                    ") ? ",*V2LON,ANS;
.
          MOVE      ZERO,OUTFLAG
.
          MOVE      "( Active ",DIM10
.
          CMATCH    ANSY,ANS
          GOTO      EOUTSTD IF EQUAL
.
          MOVE      ONE,OUTFLAG
          LOAD      OPTDESC USING OPTION OF OPTDES1,OPTDES2,OPTDES3,OPTDES4
          MOVE      "(Non Actv",DIM10
.
EOUTSTD   DISPLAY   *P60:2,*V2LON,DIM10
.
          KEYIN     *P1:17,"Only select Programs with Errors (":
                    *V2LON,*DV,ANSY,*HOFF,*DV,SLASH:
                    *V2LON,*DV,ANSN,*HOFF:
                    ") ? ",*V2LON,ANS;
.
          MOVE      ")         ",DIM10
          MOVE      ONE,ERRFLAG           *** NO DISPLAY ERROR
.
          CMATCH    ANSY,ANS
          GOTO      EERRSTD IF NOT EQUAL
.
          MOVE      "& Errors )",DIM10
          MOVE      ZERO,ERRFLAG         *** DISPLAY ERRORS
.
EERRSTD   DISPLAY   *P70:2,*V2LON,DIM10
.
REPEAT2   CALL      DISPSC
.
          BRANCH    OPTION OF KPROGC,KPORTN,KUSERC,KDATE
.
.             INPUT PROG CENTRE CODE
.
KPROGC    MOVE      SP8,USERPROG
          KEYIN     *P24:VERTPROG,*EL,*DV,UNDLN8:
                    *P24:VERTPROG,*V2LON,USERPROG,*P24:VERTPROG,*DV,USERPROG
          RESET     USERPROG
          GOTO      START IF EOS
.
          ENDSET    USERPROG
          APPEND    SP8,USERPROG
          RESET     USERPROG
.
          MOVE      USERPROG,KEY8
          CALL      RDSECU1
          BRANCH    OVRCD OF INVPROGC
.
          DISPLAY   *P35:VERTPROG,*V2LON,PSECDESC
          GOTO      KDATE
.
INVPROGC  DISPLAY   *P35:VERTPROG,*B,*V2LON:
                    "** Invalid Program ** ",*W2
          GOTO      KPROGC
.
.             INPUT Port NUMBER
.
KPORTN    MOVE      SP2,USERPORT
          KEYIN     *P24:VERTPORT,*EL,*DV,UNDLN2:
                    *P24:VERTPORT,*V2LON,USERPORT:
                    *P24:VERTPORT,*V2LON,*DV,USERPORT
.
          RESET     USERPORT
          REP       " 0",USERPORT
.
          MATCH     "00",USERPORT
          GOTO      START IF EQUAL
.
          DISPLAY   *P24:VERTPORT,*V2LON,USERPORT
          GOTO      KDATE
.
.             INPUT USER CODE
.
KUSERC    MOVE      SP5,USERCODE
          KEYIN     *P24:VERTUSER,*EL,*DV,UNDLN4:
                    *P24:VERTUSER,*V2LON,USERCODE,*P24:VERTUSER,*DV,USERCODE
          RESET     USERCODE
          GOTO      START IF EOS
.
          ENDSET    USERCODE
          APPEND    SP4,USERCODE
          RESET     USERCODE
.
          MOVE      USERCODE,KEY4
          CALL      RDPASS1
          BRANCH    OVRCD OF INVUSERC
.
          DISPLAY   *P35:VERTUSER,*V2LON,SECUSER
          GOTO      KDATE
.
INVUSERC  DISPLAY   *P35:VERTUSER,*B,*V2LON:
                    "** Invalid User ** ",*W2
          GOTO      KUSERC
.
.    DEFAULT TO TODAYS DATE
.
KDATE     UNPACK    TODAY INTO CCENT,CYEAR,CMON,CDAY
.
          MOVE      "24",CCOL
          MOVE      VERTDATE,CVERT
.
          MOVE      VERTDATE,EVERT
          MOVE      "40",ECOL
.
          MOVE      ZERO,CHIGHLT
          CALL      KEYDATE
          BRANCH    OVRCD OF EDATE
.
          PACK      USERDATE WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",USERDATE
.
          CALL      DISPOP
.
          MOVE      HEADLNO,CVERT
          DISPLAY   *P1:CVERT,*EF
.
          BRANCH    OVRCD OF EDATE
          GOTO      KDATE
.
DISPOP    BRANCH    OPTION OF DISPSYS2,DISPSYS1,DISPSYS4,DISPSYS3
          RETURN
.
EDATE     DISPLAY   *P24:VERTDATE,*EL
          BRANCH    OPTION OF KPROGC,KPORTN,KUSERC,START
          GOTO      START
.
.        DISPLAY AVAILABLE DATA
.
DISPSC    LOAD      VERTPORT USING OPTION OF SIX,FOUR,SIX,FIVE
          LOAD      VERTPROG USING OPTION OF FOUR,SEVEN,SEVEN,SIX
          LOAD      VERTUSER USING OPTION OF SEVEN,SIX,FOUR,SEVEN
          LOAD      VERTDATE USING OPTION OF FIVE,FIVE,FIVE,FOUR
.
          MOVE      SEVEN,HEADLNO
.
          BRANCH    OPTION OF DISPSC1,DISPSC2,DISPSC3,DISPSC4
.
DISPSC1   DISPLAY   *P1:4,*EF:
                    *P1:VERTPROG," Program Name        :":
                    *P1:VERTDATE," Date                :"
          RETURN
.
DISPSC2   DISPLAY   *P1:4,*EF:
                    *P1:VERTPORT," Terminal Port No.   :":
                    *P1:VERTDATE," Date                :"
          RETURN
.
DISPSC3   DISPLAY   *P1:4,*EF:
                    *P1:VERTUSER," User Code           :":
                    *P1:VERTDATE," Date                :"
          RETURN
.
DISPSC4   DISPLAY   *P1:4,*EF:
                    *P1:VERTDATE," Date                :"
          RETURN
.
.
.
SFIELD    KEYIN     *P1:24,*EL," (",*V2LON,*DV,ANSP,*HOFF,")rint, (":
                    *V2LON,*DV,ANSE,*HOFF,")xit, (":
                    *V2LON,*DV,ANSN,*HOFF,")ext ? ",*V2LON,ANS;
.
          CMATCH    ANSE,ANS
          GOTO      ENDIT IF EQUAL
.
          CMATCH    ANSP,ANS
          GOTO      PRTDATA IF EQUAL
.
          GOTO      REPEAT2
.
.
PRTDATA   MOVE      SIXTY1,CLNO
          MOVE      ZERO,CPAGENO
          DISPLAY   *P1:24,*EL,*P40:24,*V2LON,"** Generating **",*W2;
          DISPLAY   *P1:24,*EL,*P40:24,*V2LON:
                    "** Generation Completed **",*W2;
          GOTO      SFIELD
.
.
.
.      DISPLAY ALL BY PORT,DATE,TIME,USER,PROGRAM
.
.      USERDATE      DIM   8     *** FROM DATE
.
DISPSYS1  MOVE      HEADLNO,CVERT
.
          DISPLAY   *P1:CVERT,*ULON,"   Date   ":
                    *P12:CVERT,"In  Time",*P21:CVERT,"Out Time":
                    *P31:CVERT,"User Code":
                    *P41:CVERT,"Program",SP20,*P68:CVERT,"System Err "
.
          ADD       ONE,CVERT
.
          PACK      KEY30 WITH USERPORT,USERDATE,SP20
          CALL      RDSSYSA1
DISPOP12  CALL      RDKSYSA1
          BRANCH    OVRCD OF ENDDSP1
.
          MATCH     USERPORT,SYSPORT
          GOTO      ENDDSP1 IF NOT EQUAL
.
.     ONLY DISPLAY PROGRAMS WITH ERRORS
.
          BRANCH    ERRFLAG OF ERR13
.
          MATCH     SP8,SYSERROR
          GOTO      DISPOP12 IF EQUAL
.
.     ONLY DISPLAY ACTIVE PROGRAMS
.
ERR13     BRANCH    OUTFLAG OF DISPOP13
.
          MATCH     SP8,SYSTIMO
          GOTO      DISPOP12 IF NOT EQUAL
.
DISPOP13  UNPACK    SYSIDAT INTO CCENT,CYEAR,CMON,CDAY
          MOVE      ONE,CCOL
          MOVE      ZERO,CHIGHLT
          CALL      DSPDATE
.
          MOVE      SP30,SRDIM17A
          MOVE      SYSPROG,KEY8
          CALL      RDSECU1
          BRANCH    OVRCD OF DISPOP14
.
          MOVE      PSECDESC,SRDIM17A
.
DISPOP14  DISPLAY   *P12:CVERT,SYSTIMI,*P21:CVERT,SYSTIMO:
                    *P31:CVERT,SP2,SYSUSER:
                    *P41:CVERT,SYSPROG,SP1,SRDIM17A:
                    *P69:CVERT,*V2LON,SYSERROR
.
          COMPARE   "22",CVERT
          GOTO      DISPMID IF EQUAL
.
          ADD       ONE,CVERT
          GOTO      DISPOP12
.
DISPMID   DISPLAY   *P1:24,*EL," (",*V2LON,ANSR,*HOFF,")eturn, (":
                    *V2LON,ANSE,*HOFF,")xit, (":
                    *V2LON,ANSM,*HOFF,")ore : ";
          KEYIN     *P29:24,*V2LON,ANS;
.
          CMATCH    ANSR,ANS
          GOTO      ENDDSP2 IF EQUAL
.
          CMATCH    ANSE,ANS
          GOTO      OVERCOND IF EQUAL
.
          MOVE      HEADLNO,CVERT
          ADD       ONE,CVERT
          DISPLAY   *P1:CVERT,*EF
.
          BRANCH    OPTION OF DISPOP22,DISPOP12,DISPOP42,DISPOP32
.
.      End of codes
.
ENDDSP1   DISPLAY   *P1:24,*EL," (",*V2LON,ANSR,*HOFF,")eturn, (":
                    *V2LON,ANSE,*HOFF,")xit : ";
.
          KEYIN     *P21:24,*V2LON,ANS;
.
          CMATCH    ANSE,ANS
          GOTO      OVERCOND IF EQUAL
.
.   SET SO WE GO BACK TO FIRST SCREEN OR LAST ENTER
.
ENDDSP2   MOVE      ZERO,OVRCD
          RETURN
.
.      DISPLAY ALL BY PROGRAM,DATE,TIME,PORT,USER
.
.      USERDATE      DIM   8     *** FROM DATE
.
DISPSYS2  MOVE      HEADLNO,CVERT
.
          DISPLAY   *P1:CVERT,*ULON,"   Date   ":
                    *P12:CVERT,"In  Time",*P21:CVERT,"Out Time":
                    *P31:CVERT,"Port No":
                    *P40:CVERT,"User Code  ",SP15,*P68:CVERT,"System Err "
.
          ADD       ONE,CVERT
.
          PACK      KEY30 WITH USERPROG,USERDATE,SP30
          CALL      RDSSYSA2
DISPOP22  CALL      RDKSYSA2
          BRANCH    OVRCD OF ENDDSP1
.
          MATCH     USERPROG,SYSPROG
          GOTO      ENDDSP1 IF NOT EQUAL
.
.     ONLY DISPLAY PROGRAMS WITH ERRORS
.
          BRANCH    ERRFLAG OF ERR22
.
          MATCH     SP8,SYSERROR
          GOTO      DISPOP22 IF EQUAL
.
.     ONLY DISPLAY ACTIVE PROGRAMS
.
ERR22     BRANCH    OUTFLAG OF DISPOP23
.
          MATCH     SP8,SYSTIMO
          GOTO      DISPOP22 IF NOT EQUAL
.
DISPOP23  UNPACK    SYSIDAT INTO CCENT,CYEAR,CMON,CDAY
          MOVE      ONE,CCOL
          MOVE      ZERO,CHIGHLT
          CALL      DSPDATE
.
          MOVE      SP30,SRDIM17A
          MOVE      SYSUSER,KEY4
          CALL      RDPASS1
          BRANCH    OVRCD OF DISPOP24
          MOVE      SECUSER,SRDIM17A
.
DISPOP24  DISPLAY   *P12:CVERT,SYSTIMI,*P21:CVERT,SYSTIMO:
                    *P31:CVERT,SP2,SYSPORT:
                    *P40:CVERT,SYSUSER,SP1,SRDIM17A:
                    *P69:CVERT,*V2LON,SYSERROR
.
          COMPARE   "22",CVERT
          GOTO      DISPMID IF EQUAL
.
          ADD       ONE,CVERT
          GOTO      DISPOP22
.
.      DISPLAY BY DATE,TIMEIN,PROG,USER,PORT
.
.      USERDATE      DIM   8     *** FROM DATE
.
DISPSYS3  MOVE      HEADLNO,CVERT
          DISPLAY   *P1:CVERT,*ULON,"   Date   ":
                    *P12:CVERT,"In  Time":
                    *P21:CVERT,"Out Time":
                    *P31:CVERT,"Program  ",SP15,*P58:CVERT,"System Err ":
                    *P70:CVERT,"User":
                    *P75:CVERT,"Port"
.
          ADD       ONE,CVERT
.
          PACK      KEY30 WITH USERDATE,SP30
          CALL      RDSSYSA3
DISPOP32  CALL      RDKSYSA3
          BRANCH    OVRCD OF ENDDSP1
.
.     ONLY DISPLAY PROGRAMS WITH ERRORS
.
          BRANCH    ERRFLAG OF ERR33
.
          MATCH     SP8,SYSERROR
          GOTO      DISPOP32 IF EQUAL
.
.     ONLY DISPLAY ACTIVE PROGRAMS
.
ERR33     BRANCH    OUTFLAG OF DISPOP33
.
          MATCH     SP8,SYSTIMO
          GOTO      DISPOP32 IF NOT EQUAL
.
DISPOP33  MOVE      SP30,SRDIM17A
          MOVE      SYSPROG,KEY8
          CALL      RDSECU1
          BRANCH    OVRCD OF DISPOP34
.
          MOVE      PSECDESC,SRDIM17A
.
DISPOP34  UNPACK    SYSIDAT INTO CCENT,CYEAR,CMON,CDAY
          MOVE      ONE,CCOL
          MOVE      ZERO,CHIGHLT
          CALL      DSPDATE
.
          DISPLAY   *P12:CVERT,SYSTIMI,*P21:CVERT,SYSTIMO:
                    *P31:CVERT,SYSPROG,SP1,SRDIM17A:
                    *P58:CVERT,*V2LON,SYSERROR,*HOFF:
                    *P70:CVERT,SYSUSER:
                    *P75:CVERT,SYSPORT
.
          COMPARE   "22",CVERT
          GOTO      DISPMID IF EQUAL
.
          ADD       ONE,CVERT
          GOTO      DISPOP32
.
.      DISPLAY BY USER,DATE,TIME,TERMINAL,PROGRAM
.
DISPSYS4  MOVE      HEADLNO,CVERT
          DISPLAY   *P1:CVERT,*ULON,"  Date  ":
                    *P12:CVERT,"In  Time":
                    *P21:CVERT,"Out Time":
                    *P31:CVERT,"Port No":
                    *P41:CVERT,"Program  ",SP15,*P69:CVERT,"System Err "
.
          ADD       ONE,CVERT
.
          PACK      KEY30 WITH USERCODE,USERDATE,SP30
          CALL      RDSSYSA4
DISPOP42  CALL      RDKSYSA4
          BRANCH    OVRCD OF ENDDSP1
.
          MATCH     USERCODE,SYSUSER
          GOTO      ENDDSP1 IF NOT EQUAL
.
.     ONLY DISPLAY PROGRAMS WITH ERRORS
.
          BRANCH    ERRFLAG OF ERR43
.
          MATCH     SP8,SYSERROR
          GOTO      DISPOP42 IF EQUAL
.
.     ONLY DISPLAY ACTIVE PROGRAMS
.
ERR43     BRANCH    OUTFLAG OF DISPOP43
.
          MATCH     SP8,SYSTIMO
          GOTO      DISPOP42 IF NOT EQUAL
.
DISPOP43  MOVE      SP30,SRDIM17A
          MOVE      SYSPROG,KEY8
          CALL      RDSECU1
          BRANCH    OVRCD OF DISPOP44
.
          MOVE      PSECDESC,SRDIM17A
.
DISPOP44  UNPACK    SYSIDAT INTO CCENT,CYEAR,CMON,CDAY
.
          MOVE      ONE,CCOL
          MOVE      ZERO,CHIGHLT
          CALL      DSPDATE
.
          DISPLAY   *P12:CVERT,SYSTIMI,*P21:CVERT,SYSTIMO:
                    *P31:CVERT,SP2,SYSPORT:
                    *P41:CVERT,SYSPROG,SP1,SRDIM17A:
                    *P68:CVERT,*V2LON,SYSERROR
.
          COMPARE   "22",CVERT
          GOTO      DISPMID IF EQUAL
.
          ADD       ONE,CVERT
          GOTO      DISPOP42
.
          INC       IBAPASIO/INC
          INCLUDE   IBAPORIO/INC
          INC       IBASECIO/INC
          INCLUDE   IBASYSIO/INC
.
          INC       STD001IO
.
ENDIT     CHAIN     PGM
.
          STOP

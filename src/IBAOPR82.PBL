.**********************************************************************
.* System   : Operating Room System                                   *
.* Program  : IBAOPR82                                                *
.* Desc     : Operating Room Master List                              *
.**********************************************************************
.* Date     :  12/12/89                                               *
.* Author   :  K.Peak                                                 *
.*          :  30/03/90  Bill Dew                                     *
.*             Fix printing format of user define fields and make them* 
.*             not print the category decription when the codes are   *
.*             blank. Why is there 15 char print field for a 6 char   *
.*             theatre code, I guess I'll never know.                 *
.* Modifications :                                                    *
.*        V10.07.01 26/10/2015  Steve Armstrong  CAR 323085           *
.*                  Removed declaration for LOWUPP (now defined in    *
.*                  IBACOMM)                                          *
.**********************************************************************
.*                 V5.07.B01 Nicole Harrington 507 rebound 88         *
.*                           Mods to use HEAD132                      *
.**********************************************************************
.*                 10/10/90  J. Ceniza. Changed according to new spe- *
.*                cifications as file OPROPRFD has been changed.      *
.*                  29/10/90  J. Ceniza.  Fixed "?" routine.          *
.**********************************************************************
.
.$$$$$
.    Operating Room System Operating Room Master List (IBAOPR82 )
.    ------------------------------------------------------------
.
.    - Initialisation
.
.    - Display Header
.            - Open Files
.               OPROPRA1
.               PATCODE1
.               CONTROLF
.
.    - Clear Input Variables
.
.    - Display Screen Format
.
.    - Input Range           
.      If "?" entered, call OPRSCOD routine
.
.    - Read through appropriate file
.
.    - Print Report 
.
.    - END
.$$$$$
.
          INC       STD001FD
          INC       OPROPRFD/INC           * Operating Room File
          INC       OPRCONFD/INC           * Operating Room Control file
          INC       PATCODFD/INC           * patient codes file
+
.**********************************************************************
.*                           CONSTANTS                                *
.**********************************************************************
.
CODEY1    INIT      "Y1"
CODEY2    INIT      "Y2"
CODEY3    INIT      "Y3"
CODEY4    INIT      "Y4"
CODEY5    INIT      "Y5"
.
STATUS    DIM       10
STATUS1   INIT      "Active    "
STATUS2   INIT      "Non Active"
+
.**********************************************************************
.*                       GLOBAL VARIABLES                             *
.**********************************************************************
.
CODE      DIM       5
.
DIM15     DIM       15              * used in oprscod
.
ECODE     DIM       6               * to operating room code
ECDESC    DIM       40              * to operating room code description
.
FINISH    INIT      "Finish                                  "
.
.
LNO       FORM      2
.
NUMREC    FORM      4               * Total number of records
OPRDES32  DIM       32              * operating room description
.
PAGENO    FORM      3
.
RDESC1    DIM       10              * operating room category description
RDESC2    DIM       10              
RDESC3    DIM       10             
RDESC4    DIM       10            
RDESC5    DIM       10           
ROOMCODE  DIM       6
.
START     INIT      "Start                                   "
STARTFIN  FORM      1
SCODE     DIM       6               * from operating room code
SCDESC    DIM       40              * from operating room code description
.
UVERT1    FORM      2
UNIQUE    FORM      8
USERDES1  DIM       10              * operating room code description
USERDES2  DIM       10              
USERDES3  DIM       10             
USERDES4  DIM       10            
USERDES5  DIM       10           
.
VAR       FORM      2
VAR1      FORM      2
VAR2      FORM      2
.
PRGNAM    DIM       50
PRGID     INIT      "IBAOPR82"
PRGNAM1   INIT      " MASTER LISTING"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                           MAINLINE                                 *
.**********************************************************************
.
ML0000    CALL      INIT0000               * initialization and open files
.
ML0500    CALL      CLR0000                * clear input variables
.
          CALL      OPTN0000               * select option         
          BRANCH    EXIT,ML9999
.
ML0800    CALL      RNGE0000               * select range
          CALL      CONT0000               * ok to continue
          BRANCH    EXIT,ML1000,ML0800,ML0500
.
ML1000    CALL      CODE0000               * sort data by code sequence
.
          GOTO      ML0500
.
ML9999    CHAIN     PGM                    * chain back to program
+
.**********************************************************************
.*                             INIT0000                               *
.*                      Initialization  Routine                       *
.**********************************************************************
.
INIT0000  DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,FORTY;*81,OPCNODSC
          REP       UPPLOW,OPCNODSC
.
          RESET     OPCNODSC,15
INIT1000  CMATCH    SP1,OPCNODSC
          GOTO      INIT2000 IF NOT EQUAL
          BUMP      OPCNODSC BY -1
          GOTO      INIT2000 IF EOS
          GOTO      INIT1000
.
INIT2000  MOVEFPTR  OPCNODSC,VAR
          RESET     OPCNODSC,VAR
          LENSET    OPCNODSC
          RESET     OPCNODSC
.
          PACK      PRGNAM,OPCNODSC,PRGNAM1
.
          CALL      DISPHEAD
.
          BUMP      OPCNODSC BY 1
          REP       LOWUPP,OPCNODSC
          RESET     OPCNODSC
.
          MOVE      VAR,VAR1
          MOVE      VAR,VAR2
          ADD       TEN4,VAR
          ADD       TWENTY3,VAR1
          ADD       NINE,VAR2
.
          DISPLAY   *P56:24,"Opening opropraf";
          OPEN      OPROPRA1,"opropraf"
.
          DISPLAY   *P64:24,"opropraf";
          OPEN      OPROPRA2,"opropraf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          READ      CONTROLF,FORTY;*203,OPCNUOU1,OPCNUOM1,OPCNUOU2,OPCNUOM2:
                                        OPCNUOU3,OPCNUOM3,OPCNUOU4,OPCNUOM4:
                                        OPCNUOU5,OPCNUOM5
INIT9999  RETURN
+
.**********************************************************************
.*                             CLR0000                                *
.*                 Routine to Clear Local Variables                   *
.**********************************************************************
.
CLR0000   MOVE      ZERO,PAGENO
          MOVE      "60",LNO
.
. ----- Clear the Operating Room Variables -----
.
          MOVE      SP6,OPRMROOM
          PACK      OPRMDESC,SP20,SP20
          PACK      OPRMLOC,SP20
          UNPACK    SP30,OPRMUSR1,OPRMUSR2,OPRMUSR3,OPRMUSR4,OPRMUSR5
.
          RETURN
+
.**********************************************************************
.*                             OPTN0000                               *
.*                          Select Option                             *
.**********************************************************************
.
OPTN0000  MOVE      FALSE,EXIT
.
          DISPLAY   *P1:3,*EF:
                    *P2:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P2:5,*V2LON,ONE,*HOFF,". ",*+,OPCNODSC," Code Sequence"
.
OPTN1000  DISPLAY   *P1:7,*EL,"Option : "
          KEYIN     *P10:7,*V2LON,OPTION
.
          BRANCH    OPTION,OPTN9999
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9000 IF EQUAL
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      TRUE,EXIT
OPTN9999  RETURN
+
.**********************************************************************
.*                             RNGE0000                               *
.*                          Select Range                              *
.**********************************************************************
.
RNGE0000  MOVE      SP6,SCODE
          MOVE      SP6,ECODE
          MOVE      SP30,SCDESC
          MOVE      SP30,ECDESC
.
.         Find Operating Room Code Range
.
RNGE1000  DISPLAY   *P1:9,*EF:
                    *P1:10,*+,OPCNODSC," Code From : ":
                    *P1:11,*+,OPCNODSC," Code To   : " 
.
RNGE1100  KEYIN     *PVAR:10,*EL,*DV,UNDLN6,*PVAR:10,*V2LON,SCODE,*EL
.
          ENDSET    SCODE
          APPEND    SP6,SCODE
          RESET     SCODE
.
          MATCH     QUEST,SCODE
          GOTO      RNGE1200 IF NOT EQUAL
RNGE1150  MOVE      SP6,SCODE
          CALL      OPRSCOD
          MOVE      ZERO,STARTFIN
          CALL      OPRD0000
          BRANCH    EXIT,RNGE1150
          GOTO      RNGE2300
.
RNGE1200  MATCH     SP6,SCODE         
          GOTO      RNGE1500 IF NOT EQUAL
          MOVE      START,SCDESC                   * begin at the start
          DISPLAY   *PVAR:10,*EL,*V2LON,START
          GOTO      RNGE2300
.
RNGE1500  MOVE      SP20,SCDESC
          MOVE      SCODE,KEY6
          CALL      RDOPOPR1
          BRANCH    OVRCD,RNGE2000
          MOVE      OPRMDESC,SCDESC                * operating room description
.
RNGE1700  DISPLAY   *PVAR1:10,SCDESC
          GOTO      RNGE2300
.
RNGE2000  DISPLAY   *P1:24,*EL,"Code Does Not Exist. ";
          CALL      HITENTER
          GOTO      RNGE0000
.
RNGE2300  KEYIN     *PVAR:11,*EL,*DV,UNDLN6,*PVAR:11,*V2LON,ECODE,*EL
.
          ENDSET    ECODE
          APPEND    SP6,ECODE
          RESET     ECODE
.
          MATCH     QUEST,ECODE
          GOTO      RNGE2400 IF NOT EQUAL
RNGE2350  MOVE      SP6,ECODE
          CALL      OPRSCOD
          MOVE      ONE,STARTFIN
          CALL      OPRD0000
          BRANCH    EXIT,RNGE2350
          GOTO      RNGE9999
.
RNGE2400  MATCH     SP6,ECODE
          GOTO      RNGE2500 IF NOT EQUAL
          MOVE      "zzzzzz",ECODE
          MOVE      FINISH,ECDESC                  * finish at end of codes
          DISPLAY   *PVAR:11,*EL,*V2LON,FINISH
          GOTO      RNGE2700
.
RNGE2500  MOVE      SP20,ECDESC
          MOVE      ECODE,KEY6
          CALL      RDOPOPR1
          BRANCH    OVRCD,RNGE3000
          MOVE      OPRMDESC,ECDESC                * operating room description
          DISPLAY   *PVAR1:11,ECDESC
RNGE2700  
          MATCH     SP6,ECODE
          GOTO      RNGE9999 IF EQUAL
.
          MATCH     SCODE,ECODE
          GOTO      RNGE3500 IF LESS
          GOTO      RNGE9999
.
RNGE3000  DISPLAY   *P1:24,*EL,"Code Does Not Exist. ";
          CALL      HITENTER
          GOTO      RNGE2300
.
RNGE3500  DISPLAY   *P1:24,*EL,"To Code must be >= to From Code - ";
          CALL      HITENTER
          
          GOTO      RNGE0000
.
RNGE9999  RETURN
+
.*********************************************************************
.*                            OPRD0000                               *
.*            handles a question mark keyin  on operating room codes *
.*        RETURNS EXIT - TRUE - if "?" inputted                      *
.*                       FALSE - otherwise                           *
.*********************************************************************
.
OPRD0000
          MOVE      FALSE,EXIT                  * initialize exit
.
          DISPLAY   *P1:24,*EL,*+,OPCNODSC," Code : "
          KEYIN     *PVAR2:24,*DV,UNDLN6:
                    *PVAR2:24,*V2LON,ROOMCODE:
                    *PVAR2:24,*DV,ROOMCODE
.
          ENDSET    ROOMCODE
          APPEND    SP6,ROOMCODE 
          RESET     ROOMCODE
.
          MATCH     QUEST,ROOMCODE 
          GOTO      OPRD9000 IF EQUAL
.
          MATCH     SP6,ROOMCODE
          GOTO      OPRD3000 IF NOT EQUAL
.
OPRD2000
          BRANCH    STARTFIN,OPRD2200
          MOVE      SP6,SCODE
          CALL      RDSP0000
          GOTO      OPRD9999
OPRD2200
          MOVE      FINISH,ECDESC 
          CALL      RDSP0000
          DISPLAY   *PVAR:11,*EL,*V2LON,FINISH
          MOVE      "zzzzzz",ECODE
          GOTO      OPRD9999
.
OPRD3000  
          BRANCH    STARTFIN,OPRD4000
          MOVE      SP6,SCODE
          MOVE      SP20,SCDESC
          GOTO      OPRD5000
OPRD4000
          MOVE      SP6,ECODE
          MOVE      SP20,ECDESC
OPRD5000
          MOVE      ROOMCODE,KEY6
          CALL      RDOPOPR1
          BRANCH    OVRCD,OPRD7000
         
          BRANCH    STARTFIN,OPRD5500
          MOVE      ROOMCODE,SCODE
          MOVE      OPRMDESC,SCDESC
          CALL      RDSP0000
          GOTO      OPRD9999 
OPRD5500
          MOVE      ROOMCODE,ECODE
          MOVE      OPRMDESC,ECDESC
          MATCH     SCODE,ECODE
          GOTO      OPRD8000 IF LESS
.
          MOVE      ROOMCODE,ECODE
          CALL      RDSP0000
          DISPLAY   *PVAR:11,*V2LON,ECODE,*HOFF,*PVAR1:11,ECDESC
          GOTO      OPRD9999 
.
OPRD7000  
          BEEP
          DISPLAY   *P1:24,*EL,"Code Does Not Exist. ";
          CALL      HITENTER
          GOTO      OPRD0000
.
OPRD8000  
          BEEP
          DISPLAY   *P1:24,*EL,"To code must be > = to From code. ";
          CALL      HITENTER
          GOTO      OPRD0000
.
OPRD9000  MOVE      TRUE,EXIT               * ? inputted
OPRD9999  RETURN
.**********************************************************************
.*                             CONT0000                               *
.*                          Ok to Continue                            *
.**********************************************************************
.
CONT0000  MOVE      FALSE,EXIT
.
          CALL      CONTQST
          MOVE      ANS,FORM1
          BRANCH    FORM1,CONT9999,CONT9000,CONT9500
.
CONT9000  MOVE      TWO,EXIT
          GOTO      CONT9999
.
CONT9500  MOVE      THREE,EXIT
CONT9999  RETURN
+
.**********************************************************************
.*                             CODE0000                               *
.*                     Sort Data by Code Sequence                     *
.**********************************************************************
.
CODE0000  DISPLAY   *P1:24,*EL,*+,OPCNODSC," Code : "
.
          MOVE      ZERO,NUMREC                  * initialize no. of records
          PACK      KEY6,SCODE
          MATCH     SP6,KEY6
          GOTO      CODE1500 IF NOT EQUAL
.
          CALL      RSOPOPR1
CODE1000  CALL      RKOPOPR1
          BRANCH    OVRCD,CODE9000
          GOTO      CODE2000
.
CODE1500  CALL      RDOPOPR1
          BRANCH    OVRCD,CODE9000
.
CODE2000  MATCH     OPRMROOM,ECODE
          GOTO      CODE9000 IF LESS
.
CODE3000  DISPLAY   *PVAR2:24,*V2LON,OPRMROOM
          CALL      PRNT0000
.
          GOTO      CODE1000
.
CODE9000  CALL      END0000                * end of report
CODE9999  RETURN
+
.**************************************************************************
.*                         PRNT0000                                       *
.*                        Print Data                                      *
.**************************************************************************
.
PRNT0000  COMPARE   "55",LNO
          CALL      HEAD0000 IF NOT LESS
.
          MOVE      STATUS1,STATUS
          LOAD      STATUS,OPRMSTAT,STATUS2
.
          MOVE      OPRMDESC,OPRDES32
PRNT0200
          MOVE      OPRMLOC,DIM19
.
          ADD       ONE,NUMREC                 * increment no. of records
.
          PRINT     *1,"|",OPRMROOM,*9,"|",OPRDES32,*42,"|",STATUS,*54,"|":
                    OPRMSUN,SP2,OPRMMON,SP2,OPRMTUE,SP2,OPRMWED,*77,"|":
                    *132,"|";
.
          MOVE      "77",CCOL
.
          MOVE      SP10,USERDES1
          COMPARE   ONE,OPCNUOU1
          GOTO      PRNT0500 IF NOT EQUAL
          MATCH     SP3,OPRMUSR1
          GOTO      PRNT0500 IF EQUAL
.
          PACK      KEY5,CODEY1,OPRMUSR1
          CALL      RDCODE1
          BRANCH    OVRCD,PRNT0500
          MOVE      TDESC,USERDES1
PRNT0500  
          ADD       ONE,CCOL
          PRINT     *CCOL,USERDES1;
          ADD       TEN,CCOL
          PRINT     *CCOL,"|";
PRNT1000
          MOVE      SP10,USERDES2
          COMPARE   ONE,OPCNUOU2
          GOTO      PRNT1500 IF NOT EQUAL
          MATCH     SP3,OPRMUSR2
          GOTO      PRNT1500 IF EQUAL
.
          PACK      KEY5,CODEY2,OPRMUSR2
          CALL      RDCODE1
          BRANCH    OVRCD,PRNT1500
          MOVE      TDESC,USERDES2
.
PRNT1500  ADD       ONE,CCOL
          PRINT     *CCOL,USERDES2;
          ADD       TEN,CCOL
          PRINT     *CCOL,"|";
.
PRNT2000  
          MOVE      SP10,USERDES3
          COMPARE   ONE,OPCNUOU3
          GOTO      PRNT2500 IF NOT EQUAL
          MATCH     SP3,OPRMUSR3
          GOTO      PRNT2500 IF EQUAL
.
          PACK      KEY5,CODEY3,OPRMUSR3
          CALL      RDCODE1
          BRANCH    OVRCD,PRNT2500
          MOVE      TDESC,USERDES3
.
PRNT2500  ADD       ONE,CCOL
          PRINT     *CCOL,USERDES3;
          ADD       TEN,CCOL
          PRINT     *CCOL,"|";
.
PRNT3000  
          MOVE      SP10,USERDES4
          COMPARE   ONE,OPCNUOU4
          GOTO      PRNT3500 IF NOT EQUAL
          MATCH     SP3,OPRMUSR4
          GOTO      PRNT3500 IF EQUAL
.
          PACK      KEY5,CODEY4,OPRMUSR4
          CALL      RDCODE1
          BRANCH    OVRCD,PRNT3500
          MOVE      TDESC,USERDES4
.
PRNT3500  ADD       ONE,CCOL
          PRINT     *CCOL,USERDES4;
          ADD       TEN,CCOL
          PRINT     *CCOL,"|";
.
PRNT4000  
          MOVE      SP10,USERDES5
          COMPARE   ONE,OPCNUOU4
          GOTO      PRNT4500 IF NOT EQUAL
          MATCH     SP3,OPRMUSR5
          GOTO      PRNT4500 IF EQUAL
.
          PACK      KEY5,CODEY5,OPRMUSR5
          CALL      RDCODE1
          BRANCH    OVRCD,PRNT4500
          MOVE      TDESC,USERDES5
.
PRNT4500  ADD       ONE,CCOL
          PRINT     *CCOL,USERDES5;
          ADD       TEN,CCOL
          PRINT     *CCOL,"|";
.
PRNT5000
          PRINT     *N,*1,"|",*9,"|",DIM19,*42,"|",*54,"|",OPRMTHU,SP2,OPRMFRI:
                    SP2,OPRMSAT,*77,"|",*88,"|",*99,"|",*110,"|",*121,"|":
                    *132,"|";
          PRINT     *N,*1,"|",*9,"|",*42,"|",*54,"|",*77,"|",*88,"|":
                    *99,"|",*110,"|",*121,"|",*132,"|"
          ADD       THREE,LNO
.
PRNT9999  RETURN
+
.**************************************************************************
.*                         END0000                                        *
.*                      End of Report                                     *
.**************************************************************************
.
END0000   COMPARE   ZERO,PAGENO
          GOTO      END1000 IF NOT EQUAL
.
.         No Data Found
.
          DISPLAY   *P1:24,*EL,*P20:24,*V2LON:
                    "** No Record Selected **"
          CALL      HEAD0000
          PRINT     *N,*N,*22,"** END OF REPORT **"
          GOTO      END9999
.
END1000   CALL      PGE0000
.
          PRINT     *N,*33,"Total number of records = ",SP2,NUMREC
          PRINT     *N,*22,"** END OF REPORT **"
          DISPLAY   *P1:24,*EL,*P20:24,*V2LON:
                    "** Report Generation Completed **"
.
END9999   DISPLAY   *P1:14,*EF
          RETURN
+
.**************************************************************************
.*                         HEAD0000                                       *
.*                     Print Heading                                      *
.**************************************************************************
.
HEAD0000  COMPARE   ZERO,CPAGENO
          CALL      PGE0000 IF NOT EQUAL
.
          MOVE      ONE,CNOUNDLN
          CALL      IBACLOCK
          CALL      HEAD132
.
          MATCH     SP6,SCODE          * From Start ?
          GOTO      HEAD0100 IF EQUAL
.
          PRINT     *40,"Code From : ",SCODE,SP3,SCDESC,*N
          GOTO      HEAD0200
.
HEAD0100  PRINT     *40,"Code From : ",START,*N
.
HEAD0200  MATCH     "zzzzzz",ECODE            * To Finish ?
          GOTO      HEAD0300 IF EQUAL         * yes
.
          PRINT     *40,"Code To   : ",ECODE,SP3,ECDESC,*N
          GOTO      HEAD0400
.
HEAD0300  PRINT     *40,"Code To   : ",FINISH,*N
.
HEAD0400  CALL      PGE0000
.
          MOVE      EIGHT,LNO
          PRINT     *1,"|Code",*9,"|Description",*42,"|Status":
                    *54,"|Time Available (mins)",*77,"|":
                    *94,"User defined fields",*132,"|":
                    *N,*1,"|",*9,"|Location",*42,"|":
                    *54,"|Sun   Mon   Tue   Wed |":
                    *132,"|",*N,*1,"|",*9,"|",*42,"|",*54,"|Thu   Fri   Sat ":
                    *77,"|",*132,"|";
.
          MOVE      "77",CCOL
.
          COMPARE   ONE,OPCNUOU1
          GOTO      HEAD1000 IF NOT EQUAL
.
          MOVE      SP10,RDESC1
          PACK      KEY5,CODEY1,SP3
          CALL      RDCODE1
          BRANCH    OVRCD,HEAD0500
          MOVE      TDESC,RDESC1
.
HEAD0500  ADD       ONE,CCOL
          PRINT     *CCOL,RDESC1;
          ADD       TEN,CCOL
          PRINT     *CCOL,"|";
.
HEAD1000  COMPARE   ONE,OPCNUOU2
          GOTO      HEAD2000 IF NOT EQUAL
.
          MOVE      SP10,RDESC2
          PACK      KEY5,CODEY2,SP3
          CALL      RDCODE1
          BRANCH    OVRCD,HEAD1500
          MOVE      TDESC,RDESC2
.
HEAD1500  ADD       ONE,CCOL
          PRINT     *CCOL,RDESC2;
          ADD       TEN,CCOL
          PRINT     *CCOL,"|";
.
HEAD2000  COMPARE   ONE,OPCNUOU3
          GOTO      HEAD3000 IF NOT EQUAL
.
          MOVE      SP10,RDESC3
          PACK      KEY5,CODEY3,SP3
          CALL      RDCODE1
          BRANCH    OVRCD,HEAD2500
          MOVE      TDESC,RDESC3
.
HEAD2500  ADD       ONE,CCOL
          PRINT     *CCOL,RDESC3;
          ADD       TEN,CCOL
          PRINT     *CCOL,"|";
.
HEAD3000  COMPARE   ONE,OPCNUOU4
          GOTO      HEAD4000 IF NOT EQUAL
.
          MOVE      SP10,RDESC4
          PACK      KEY5,CODEY4,SP3
          CALL      RDCODE1
          BRANCH    OVRCD,HEAD3500
          MOVE      TDESC,RDESC4
.
HEAD3500  ADD       ONE,CCOL
          PRINT     *CCOL,RDESC4;
          ADD       TEN,CCOL
          PRINT     *CCOL,"|";
.
HEAD4000  COMPARE   ONE,OPCNUOU5
          GOTO      HEAD5000 IF NOT EQUAL
.
          MOVE      SP10,RDESC5
          PACK      KEY5,CODEY5,SP3
          CALL      RDCODE1
          BRANCH    OVRCD,HEAD4500
          MOVE      TDESC,RDESC5
.
HEAD4500  ADD       ONE,CCOL
          PRINT     *CCOL,RDESC5;
          ADD       TEN,CCOL
          PRINT     *CCOL,"|";
.
HEAD5000  PRINT     *N;
          CALL      PGE0000
          MOVE      TEN2,LNO
.
HEAD9999  RETURN
+
.**********************************************************************
.*                             PGE0000                                *
.*                     Print Page Separator                           *
.**********************************************************************
.
PGE0000   PRINT     "*---------------------------------------":
                    "----------------------------------------":
                    "----------------------------------------":
                    "-----------*"
          ADD       ONE,LNO
          RETURN
+
.**********************************************************************
.*                             RDSP0000                               *
.*                         Redisplay Screen                           *
.**********************************************************************
.
RDSP0000  DISPLAY   *P1:3,*EF:
                    *P2:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P2:5,*V2LON,ONE,*HOFF,". ",*+,OPCNODSC," Code Sequence":
                    *P1:7,*EL,"Option : ",*P10:7,*V2LON,OPTION,*HOFF
.
          MATCH     SP6,SCODE
          GOTO      RDSP2222 IF EQUAL
.
          DISPLAY   *P1:10,*+,OPCNODSC," Code From : ":
                    *P1:11,*+,OPCNODSC," Code To   : ":
                    *PVAR:10,*EL,*V2LON,SCODE,*HOFF,*PVAR1:10,SCDESC
          GOTO      RDSP9999
RDSP2222
          DISPLAY   *P1:10,*+,OPCNODSC," Code From : ",*V2LON,START:
                    *P1:11,*HOFF,*+,OPCNODSC," Code To   : "
.
RDSP9999  RETURN
+
.********************************************************************** 
.*              I/O INCLUDES FOR FILES AND GENERAL ROUTINES           *
.**********************************************************************
.
         INC       STD001IO
         INC       OPRSCOD                 * operating room file routine
         INC       PATCODKY                * patient codes file routine
         INC       PATCODIO/INC            * patient i/o for codes
         INC       OPROPRIO/INC            * operating room i/o
.
.
.

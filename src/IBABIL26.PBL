. *****************************************************************************
. *              P R O G R A M  S U M M A R Y                                 *
. *              -------------  -------------                                 *
. *                                                                           *
. *     PROGRAM NAME:     IBABIL26                                            *
. *                                                                           *
. *     FUNCTION:         OUTSTANDING DEBTORS BY HEALTH FUND                  *
. *                                                                           *
. *     DATE WRITTEN:     19/06/86                                            *
. *                                                                           *
. *     MODIFICATIONS:                                                        *
. *      V12.00.01  21/05/2025  Bella Turco                 TSK 0955096       *
. *                 Alphanumeric changes                                      *
. *      V10.05.01  01/01/2015  Ania P                      CAR 261630        *
. *                 Removed use of PORT for temp file naming                  *
. *       V9.09.01  06/08/2007  Peter Vela    CAR 146855                      *
. *                 Added credit note total to XNETDOL                        *
. *       V9.04.01  01/08/2005  J.Tan         CAR 69331                       *
. *                 Mods for multi hospital                                   *
. *       V9.02.02  28/04/2003  Dean Cramer   CAR 38200                       *
. *                 Fix print overlaps for web                                *
. *       V9.02.01  13/03/2003  Dean Cramer   CAR 36515                       *
. *                 Port from 6.10                                            *
. *                 Change file names where applicable                        *
. *                 Remove options for AAE and Outpatients                    *
. *       V6.09.01  13/11/2001  Tonii  SRF 13489                              *
. *                 Mods to accommodate new length of TPATAMTT,TPATAMTG,      *
. *                 TPATAMTH,TPATAMTI,TPATAMTP,TREBATE, all now FORM 8.2      *
. *****************************************************************************
. *       V6.09.B01 18/12/2000  Ebon Clements SRF 4782                        *
. *                 Added GST Journals into calculations                      *
. *       V6.05.03  08/12/1998 Jill Habasque                                  *
. *                 Removed CCENTRY                                           *
. *       V6.05.02  26/11/1998  Jill Habasque                                 *
. *                 Fixed century display                                     *
. *       V6.05.01  09/11/1998  Nicole Harrington                             *
. *                 Changed to use HEAD132 and print century on dates         *
. *       V6.00     05/03/91 ROD                                              *
. *                 Release 6. Health fund table now 8 chars                  *
. *       V6.00.01  19/02/1992    Justin Coates                               *
. *                 Fix read on health fund file (table = [0000    ])         *
. *       V6.00.03  24/09/1993    Steve O'Gorman   SRF 123507                 *
. *                 Recompiled for PATCALFN.                                  *
. *       V6.00.04  08/11/1993    Greg Horvat                                 *
. *                 Modified to accept KEYDATE                                *
. *       V6.04.01  23/06/1997    ROD       srf 620375                        *
. *                 ignore aae patients                                       *
. *                                                                           *
. *****************************************************************************
.
          INC       STD001FD
          INC       IBAPASFD/INC
          INC       IBASECFD/INC
+
          INC       PMSVX1FD/INC
          INC       PATHSPFD/INC
          INC       PATFN1FD/INC
          INC       PATCODFD/INC
          INC       PATINVFD/INC
          INC       PATDSCFD/INC
          INC       PATDTRFD/INC
          INC       PATMI1FD/INC
          INC       PATMA1FD/INC
          INC       PATSELDT/INC
          INC       OUTSITFD/INC
          INC       IBASEQFD/INC  
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
.       Temporary index file for report
.
ISBUILD   INIT      "isbuild "
KEYFILE   INIT      " 33 U1-14"
PSTIDX    IFILE     SQL, FIXED=33, KEY="U1-14"
.
.Name     Type   Size    Physical   Start
.----     ----   ----    --------   -----
.HCODE    DIM     6         6         1
.HFTABL   DIM     4         4         7
.ZNOINV   FORM    5         4        11
.ZNOBDS   FORM    5         4        15
.ZNOCAS   FORM    5         4        19
.ZNETDL   FORM    9.2       9        23
.                           EOR      32
.
.               VARIBLES DEFINITION
.
CMDLINE    DIM     30
CUDATE     DIM     8        * FROM DATE IN (CCYYMMDD) FORMAT
CODE       DIM     2
COTHSFN    FORM    1
.
DIM8       DIM     8
DIM37      DIM     37
.
FNAMEI     DIM     8
FNAMER     DIM     8
.
GTOTAL     FORM    8.2
GFTOTAL    FORM    8.2
GSTOTAL    FORM    8.2
GNOINV     FORM    7
GNOBDS     FORM    7
GNOCAS     FORM    7
.
IBCNMHOS    FORM          1         Multi Hospital Indicator
.
FIVE9      FORM    "59"
LNO        FORM    2
.
IBCNAESN    DIM         3           AAE System Number
IBCNOPSN    DIM         3           Outpatients System Number
IBCNBLSN    DIM         3           Patient Billing System Number
IBCNMASN    DIM         3           Mamography System Number
IBCNRASN    DIM         3           Radiology System Number
IBCNINSN    DIM         3           Inpatient System Number
.
HYPH       INIT     " - "
SP14       INIT     "              "
TIMEIS     DIM     8
PSTROL     FILE      ASCII, FIXED=256
.
SVFUND     DIM     6
.
XNETDOL    FORM    9.2
XBEDDYS    FORM    5
XDAYCSE    FORM    5
.
....ZERO4      INIT    "0000"
ZERO8      INIT    "0000    "
ZNOINV     FORM    5
ZNOBDS     FORM    5
ZNOCAS     FORM    5
ZNETDL     FORM    9.2
.
PRGID     INIT      "IBABIL26"
PRGNAM    INIT      "OUTSTANDING DEBTORS BY HEALTH FUND"
VERSION   INIT      "V12.01.00"
.
.                    OPEN FILES
.
          CALL      DISPHEAD
.
          DISPLAY       *P56:24,"Opening patfundf";
          OPEN          PATFN1A1,"patfn1af"
.
          DISPLAY       *P64:24,"patinvrf";
          OPEN          PATINVR2,"patinvrf"
.
          DISPLAY       *P64:24,"patdschf";
          OPEN          PATDSCH1,"patdschf"
.
          DISPLAY       *P64:24,"patdtraf";
          OPEN          PATDTRA1,"patdtraf"
.
          DISPLAY       *P64:24,"patmi1af";
          OPEN          PATMI1A1,"patmi1af"
.
          DISPLAY       *P64:24,"patma1af";
          OPEN          PATMA1A1,"patma1af"
.
          DISPLAY       *P64:24,"patmx1af";
          OPEN          PATMX1A1,"patmx1af"
.
          DISPLAY       *P64:24,"patcodes";
          OPEN          PATCODE1,"patcodes"
.
          DISPLAY       *P64:24,"outsitaf";
          OPEN          OUTSITA1,"outsitaf"
.
          DISPLAY       *P64:24,"pmsvx1af";
          OPEN          PMSVX1A1,"pmsvx1af"
.
          DISPLAY       *P64:24,"controlf";
          OPEN          CONTROLF,"controlf"
          READ          CONTROLF,ZERO;*43,IBCNMHOS
.
.         Generate names of temporary files
.
          CALL          TFILENAM
          MOVE          TFILNAME,FNAMEI
.
          CALL          TFILENAM
          MOVE          TFILNAME,FNAMER
.
START   DISPLAY         *P1:4,*EF:
                        *P1:5,"Keyin Cut-Off Date : "
.
        MOVE            ZERO,CPAGENO
        MOVE            "60",LNO
        MOVE            ZERO,GTOTAL
        MOVE            ZERO,GFTOTAL
        MOVE            ZERO,GSTOTAL
        MOVE            ZERO,GNOINV
        MOVE            ZERO,GNOBDS
        MOVE            ZERO,GNOCAS
.
        MOVE            CCC,CCENT
        UNPACK          SP6,CDAY,CMON,CYEAR
        MOVE            "22",CCOL
        MOVE            "5",CVERT
        MOVE            "0",CCANLDTE
        MOVE            "0",CHIGHLT
        DISPLAY         *P22:5,*EL
.  
        CALL            KEYDATE
        BRANCH          OVRCD,ENDIT
.
        MOVE            CDAY,IDAY
        MOVE            CMON,IMON
        MOVE            CYEAR,IYEAR
        MOVE            CCENT,ICENT
.
        PACK            CUDATE WITH ICENT,IYEAR,IMON,IDAY
        REP             " 0",CUDATE   
.
        UNPACK          CUDATE INTO XCENT,XYEAR,XMON,XDAY
+
.
        CALL            KHOS0000
        BRANCH          EXIT,START
.
.             CONFIRMATION ON DATE   
.
REKEY   KEYIN           *P1:24,*EL," Date ok (":
                        *V2LON,"Y",*HOFF,"/":
                        *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
        REP             UPPLOW,ANS
        MATCH           "N" TO ANS
        GOTO            START IF EQUAL
.
        MATCH           "Y" TO ANS
        GOTO            INIT IF EQUAL
.
        BEEP
        GOTO            REKEY
.
.       Create temporary file to store health fund totals in
.
INIT    DISPLAY         *P1:24,*EL:
                        *P20:24,*V2LON,"** INITIALISING HEALTH FUNDS **";
.
          CLOCK     TIME,TIMEIS
          CALL      CREAIDX
          BRANCH    EXIT,START
.
.       Loop through the invoice file and update the temporary file for
.       each health fund
.
        DISPLAY         *P1:24,*EL:
                        *P20:24,*V2LON,"** UPDATING HEALTH FUNDS **"
.
        DISPLAY         *P20:24,*EL,"Accepting Invoice :":
                        *P55:24,"Scanning : ";
        MOVE            SP20 TO KEY16
        CALL            RDSINV2
BROWSE  CALL            RDKINV2
        BRANCH          OVRCD OF REPORT
.
        REP             " 0",PINVDTE
        MATCH           PINVDTE,CUDATE
        GOTO            REPORT IF LESS
.
        MATCH           ZEROVISN,PINVADM
        GOTO            BROWSE IF EQUAL         * Cancelled invoice
.
        COMPARE         THREE,PINVSYS
        GOTO            BROWSE IF LESS
.
        UNPACK          PINVBLCD,XCENT,XYEAR,XMON,XDAY   * Balanced date
        PACK            DIM8,XCENT,XYEAR,XMON,XDAY
        REP             " 0",DIM8
.
        MATCH           DIM8,CUDATE             * Ignore if balanced before
        GOTO            BROWSE IF NOT LESS      * the cutoff date
.
        DISPLAY         *P67:24,*V2LON,PINVNO;
.                 
.       Read admission file to get fund details
.
        MOVE            PINVADM TO KEY8
        CALL            RDMISS1
        BRANCH          OVRCD OF NOADMN
.
.       Check for a master file record ( to be compatible with the ATB )
.
        MOVE            AURNO,KEY8
        CALL            RDMAST1
        BRANCH          OVRCD OF NOMAST
.
        COMPARE         ONE,IBCNMHOS
        GOTO            CHDSCH IF NOT EQUAL
.
        MATCH           SP10,PTHSHOSP
        GOTO            CHDSCH IF EQUAL
.
        MOVE            PINVADM,KEY8
        CALL            RDPMVX11
        BRANCH          OVRCD,BROWSE
.
        MATCH           PMVXMHOS,PTHSHOSP
        GOTO            BROWSE IF NOT EQUAL
.
.       Read discharge file, and check for day cases
.
CHDSCH  MOVE            ZERO,XDAYCSE             * Assume it's not a day case
.
        MOVE            PINVADM TO KEY8
        CALL            RDDSCH1
        BRANCH          OVRCD OF CONT5
.
        MATCH           DDATE TO ADATE
        GOTO            CONT5 IF NOT EQUAL
.
        MOVE            ONE,XDAYCSE              * It's a day case
.
.       Calculate bed days and payment amount
.
CONT5   MOVE            ZERO,XBEDDYS
        MOVE            PINVAMT,XNETDOL
        ADD             PTINGSTJ,XNETDOL
.
        PACK            KEY22 WITH PINVADM,PINVNO,SP3
        CALL            RDSDTRN1
CONT6   CALL            RDKDTRN1
        BRANCH          OVRCD OF CONT7
.
        MATCH           PINVNO TO TREF
        GOTO            CONT7 IF NOT EQUAL
.
        MATCH           PINVADM,TADMNO
        GOTO            CONT7 IF NOT EQUAL
.
        BRANCH          TRECTYPE OF CALCDYS,CONT6,CONT6,CONT6,CALCPAY,CALCPAY
.
        GOTO            CONT6
.
.       Accumulate bed day totals
.
CALCDYS BRANCH          XDAYCSE OF CONT6          * Don't accumulate day cases
        ADD             TNODAYS TO XBEDDYS
        GOTO            CONT6
.
.       Calculate payment/journal dollar amounts
.
CALCPAY PACK            XDATE WITH TFCENT,TFYEAR,TFMONTH,TFDAY
        REP             " 0",XDATE
.
        MATCH           XDATE,CUDATE
        GOTO            CONT6 IF LESS
.
        ADD             TPATAMTT,XNETDOL
        GOTO            CONT6
.
.       Check if this invoice has a zero balance
.
CONT7   COMPARE         ZERO,XNETDOL
        GOTO            BROWSE IF EQUAL
.
.       Check for a blank health fund
.
        MATCH           SP8,AFNDTB
        GOTO            CONT8 IF NOT EQUAL
.
        MOVE            "ZZZZZZZZ",AFNDTB
.
CONT8   MATCH           SP6,AFUNDH
        GOTO            CONT9 IF NOT EQUAL
.
        MOVE            "ZZZZZZ",AFUNDH
.
.       Add Credit note total
.
CONT9   ADD             PTINCRTT,XNETDOL
.
.       Update the temporary file with this data
.
PCKKEY  PACK            KEY14 WITH AFUNDH,AFNDTB
        READ            PSTIDX,KEY14;HCODE,HFTABL,ZNOINV,ZNOBDS:
                                     ZNOCAS,ZNETDL
        GOTO            NEWHFT IF OVER
.
        ADD             ONE,ZNOINV
        ADD             XBEDDYS,ZNOBDS
        ADD             XDAYCSE,ZNOCAS
        ADD             XNETDOL,ZNETDL
.
        UPDATE          PSTIDX;HCODE,HFTABL,ZNOINV,ZNOBDS,ZNOCAS,ZNETDL
        GOTO            BROWSE
.
.       New Fund/Table pair
.
NEWHFT  MOVE            ONE,ZNOINV
        WRITE           PSTIDX,KEY14;KEY14,ZNOINV,XBEDDYS,XDAYCSE,XNETDOL
        GOTO            BROWSE
+
.
.                PRINT HEALTH FUND REPORT
.
REPORT  MOVE            "60",LNO
        DISPLAY         *P1:24,*EL,*P20:24,*V2LON:
                        "** PRINTING REPORT **"
        DISPLAY         *P20:24,*EL,"Printing Fund No : ":
                        *P55:24,"Scanning :";
        MOVE            SP6 TO SVFUND       * init. previous key to spaces
        READ            PSTIDX,SP14;;
BROWSE2 READKS          PSTIDX;AFUNDH,AFNDTB,ZNOINV,ZNOBDS,ZNOCAS,ZNETDL
        GOTO            ENDP IF OVER
.
        DISPLAY         *P67:24,*V2LON,AFUNDH;
.
.   Level break routine to test for change in Health Fund
.
        MATCH           SP6,SVFUND          * check for first time thru'
        GOTO            PRTHDET IF EQUAL    * yes, print header details
.
        MATCH           AFUNDH,SVFUND       * compare this fund vs previous
        GOTO            PRTDETL IF EQUAL    * same fund as previous record
        CALL            PRTSUB              * print sub totals. Fund totals
PRTHDET CALL            DETAIL1             * print header details
.
PRTDETL DISPLAY         *P40:24,*V2LON,AFUNDH;
        CALL            DETAIL2             * print line details
        MOVE            AFUNDH,SVFUND       * save key for level break check.
        GOTO            BROWSE2             * read next record
.
.              FINALISE
.  
ENDP    CALL            PRTSUB              * print last sub total
        COMPARE         ZERO,CPAGENO 
        GOTO            NOFUND2 IF EQUAL 
        CALL            BLKLN 
        CALL            PAGEND 
        PRINT           *1,"!  ",*12,"!  TOTAL",*51,"!  ",GNOINV: 
                        *65,"!  ",GNOBDS,*78,"!   ",GNOCAS:
                        *92,"! ",GTOTAL,*107,"!",*109,GFTOTAL,*122,"!"
        CALL            PAGEND
        PRINT           *N,"  ***  END OF REPORT  ***"
        DISPLAY         *P1:24,*EL:
                        *P20:24,*V2LON:
                        "** Report Generation Completed **"
        CALL            KILLIDX
        GOTO            START

.                  ERROR MESSAGES
.
NOFUND2 DISPLAY         *P1:24,*EL,*P20:24,*V2LON,*B:
                        "*** No Available Debts for Health Funds ***",*W2;
        GOTO            START
.
NOADMN  DISPLAY         *P1:22,*EL,*P10:22,*V2LON,*B:
                        "*** Invoice ",PINVNO:
                        " contains an Invalid Admission No ",PINVADM:
                        " ***",*W2;
.
        COMPARE         "55",LNO
        CALL            HEAD IF NOT LESS
.
        PRINT           *1,"** Invoice ",PINVNO," had invalid admission":
                        " number ",PINVADM:
                        ". Contact I.B.A. Immediately **",*N
        ADD             TWO,LNO
        GOTO            BROWSE
.
NOMAST  DISPLAY         *P1:20,*EL,*P10:20,*V2LON,*B:
                        "*** Invoice ",PINVNO:
                        " contains an Invalid U/R No ",AURNO:
                        " ***",*W2;
.
        COMPARE         "55",LNO
        CALL            HEAD IF NOT LESS
.
        PRINT           *1,"** Invoice ",PINVNO," missing master file":
                        " record ",AURNO:
                        ". Contact I.B.A. Immediately **",*N
        ADD             TWO,LNO
        GOTO            BROWSE
+
.
.       Print the health fund name
.
DETAIL1 COMPARE         "52",LNO
        CALL            HEAD IF NOT LESS
        CALL            BLKLN
.
        MATCH           "ZZZZZZ",AFUNDH
        GOTO            NOFUND IF EQUAL
.
        PACK            KEY14 WITH AFUNDH,ZERO8
        CALL            RDFUND1
        COMPARE         ZERO,OVRCD
        GOTO            PRT1 IF EQUAL
.
        MOVE            "UNKNOWN HEALTH FUND CODE      ",HFNAME
        GOTO            PRT1
.
NOFUND  MOVE            "NO HEALTH FUND                ",HFNAME
        MOVE            SP6,HCODE
.
PRT1    PACK            DIM37,HFNAME,SP10
        PRINT           *1,"!  ",HCODE,*12,"! ",DIM37,*51,"!    ":
                        *65,"!    ",*78,"!     ":
                        *92,"!  ",*107,"!",*122,"!"
        ADD             ONE,LNO
        RETURN
.
.                PRINT DETAILS RECORD
.
DETAIL2 COMPARE         "52",LNO
        CALL            HEAD IF NOT LESS
.
        MATCH           "ZZZZZZZZ",AFNDTB
        GOTO            NOTABL IF EQUAL
.
        PACK            KEY14 WITH AFUNDH,AFNDTB
        CALL            RDFUND1
        COMPARE         ZERO,OVRCD
        GOTO            PRT2 IF EQUAL
.
        MOVE            "UNKNOWN HEALTH FUND TABLE     ",HFNAME
        GOTO            PRT2
.
NOTABL  MOVE            "NO TABLE GIVEN                ",HFNAME
        MOVE            SP8,HFTABL
.
PRT2    COMPARE         NINE,LNO
        PACK            DIM37,HFTABL,HYPH,HFNAME,SP10
        GOTO            CONT3 IF NOT EQUAL
.
        PRINT           *1,"!  ",HCODE,*12,"! ",DIM37:
                        *51,"!    ",ZNOINV:
                        *65,"!    ",ZNOBDS,*78,"!     ",ZNOCAS:
                        *92,"!  ",ZNETDL,*107,"!",*122,"!"
        GOTO            CONT4
.
CONT3   PRINT           *1,"!",*12,"! ",DIM37:
                        *51,"!    ",ZNOINV:
                        *65,"!    ",ZNOBDS,*78,"!     ",ZNOCAS:
                        *92,"!  ",ZNETDL,*107,"!",*122,"!"
.
CONT4   ADD             ZNETDL,GTOTAL
        ADD             ZNETDL,GFTOTAL
        ADD             ZNETDL,GSTOTAL
        ADD             ZNOBDS,GNOBDS
        ADD             ZNOCAS,GNOCAS
        ADD             ZNOINV,GNOINV
        ADD             ONE,LNO
        RETURN
.
. ---- print sub totals when change in Fund. (Fund Totals) ----
.
PRTSUB  PRINT           *1,"!",*12,"!",*51,"!",*65,"!",*78,"!",*92,"!":
                        *107,"!",*109,GSTOTAL,*122,"!"
        ADD             ONE,LNO
        MOVE            ZERO,GSTOTAL
        RETURN
.
+
.
.                 LINE ON THE PAGE
.
PAGEND  PRINT          "*--------------------------------------------------":
                       "------------------------------------------":
                       "----------------------------*"
        ADD            ONE,LNO
        RETURN
.
.                HEADINGS FOR OUTPUT
.
HEAD    MOVE           ONE,CNOUNDLN
        CALL           IBACLOCK 
        COMPARE        ZERO,CPAGENO
        CALL           PAGEND IF NOT EQUAL
.    
        CALL           HEAD132
        PRINT          *40,"AS AT  ",CDAY,"/",CMON,"/",CCENT,CYEAR,*N
.
        IF             IBCNMHOS =1
          PRINT          *40,"Hospital : ",PTHSNAME
        ELSE
          PRINT          *N;
        ENDIF
.
        CALL           PRTSEL
        CALL           PAGEND
.
        PRINT     *1,"!   FUND   ! TABLE",*51,"!  INVOICES   !  BED DAYS  !":
                         "  DAY CASES  !     NET      !  FUND TOTAL  !",*N:       
                    "!          !      ",*51,"!             !            !":
                         "             !      $       !      $       !"
.
        CALL          PAGEND
.
        CLOCK         TIME,TIMEIS
        MOVE          NINE,LNO
        RETURN
.
BLKLN   PRINT      *1,"!          !      ",*51,"!             !            !":
                      "             !              !              !"
        ADD         ONE,LNO
        RETURN
+
.
.      Create an indexed file based on port
.
CREAIDX   CALL      KILLIDX
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,FNAMEI,KEYFILE,SP30
          RESET     CMDLINE
          DISPLAY   *P1:10,*EF;
          EXECUTE   CMDLINE,TASKID
.
          TRAP      NOINDZ IF IO
          OPEN	    PSTIDX,FNAMEI
          TRAPCLR   IO
          MOVE      ZERO,EXIT
          RETURN
.
NOINDZ    DISPLAY   *P1:24,*EL,*B,*V2LON,"** Temporary Index File ":
                    FNAMEI," Not Found **",*W4;
          TRAPCLR   IO
          MOVE      ONE,EXIT
          RETURN
+
.
.       Deleting an indexed file based on port
.
KILLIDX CLOSE         PSTIDX
        DISPLAY       *P1:24,*EL,*P20:24,*V2LON,"** Deleting Indexed File **"
        CLEAR         CMDLINE
        APPEND        "iserase ",CMDLINE
        APPEND        FNAMEI,CMDLINE
        RESET         CMDLINE
        EXECUTE       CMDLINE,TASKID
        DISPLAY       *P1:10,*EF;
        RETURN
+
PRTSEL    COMPARE   ZERO,FSTSYS
          GOTO      AEHEAD IF NOT EQUAL
.
          PRINT     *40,"CONSOLIDATED REPORT",*N,*N;
          GOTO      PRTSEL9
.
AEHEAD    COMPARE   ONE,FSTSYS
          GOTO      OPHEAD IF NOT EQUAL
.
          PRINT     *40,"ACCIDENT & EMERGENCY REPORT",*N,*N;
          GOTO      PRTSEL9
.
OPHEAD    COMPARE   TWO,FSTSYS
          GOTO      IPHEAD IF NOT EQUAL
.
          PRINT     *40,"OUTPATIENT REPORT",*N;
          MATCH     SP6,FSTSITE
          GOTO      ALSITE IF NOT EQUAL
.
          PRINT     *40,"ALL SITES";
          GOTO      OPDEPT
.
ALSITE    PRINT     *40,"SITE",FSTSITE;
.
OPDEPT    MATCH     SP3,FSTDEPT
          GOTO      ALDEPT IF NOT EQUAL
.
          PRINT     *40,"ALL DEPARTMENTS",*N,*N;
          GOTO      PRTSEL9
.
ALDEPT    PRINT     *40,"DEPT",FSTDEPT,*N,*N;
          GOTO      PRTSEL9
.
IPHEAD    COMPARE   THREE,FSTSYS
          GOTO      OTHEAD IF NOT EQUAL
.
          PRINT     *40,"INPATIENT REPORT",*N,*N;
          GOTO      PRTSEL9
.
OTHEAD    PRINT     *40,"OTHER FINANCIAL REPORT",*N,*N;
.
PRTSEL9   RETURN 
.
.******************************************************************************
.*                                  KHOS0000              Called by: ML0000   *
.*                            Enter The hospital                              *
.******************************************************************************
KHOS0000  COMPARE   ONE,IBCNMHOS
          GOTO      KHOS9999 IF NOT EQUAL
.
          DISPLAY   *P1:13,*EL,"Hospital Id : ";
          MOVE      TEN5,ECOL
          MOVE      TEN3,EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS2000,KHOS9500
          GOTO      KHOS3000
.
KHOS2000  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          MOVE      ZERO,EXIT
.
KHOS3000  CLOSE     PATHSPA1
.
          DISPLAY   *P20:13,*V2LON,PTHSNAME;
          GOTO      KHOS9999
.
KHOS9500  MOVE      ONE,EXIT
KHOS9999  RETURN
.
          INC       PATCODDS
          INC       PATSELFN
          INC       PATHSPKY
          INC       PMSVX1IO/INC
          INC       PATHSPIO/INC
          INC       PATFN1IO/INC
          INC       PATINVIO/INC
          INC       PATDTRIO/INC
          INC       PATMI1IO/INC
          INC       PATMA1IO/INC
          INC       PATDSCIO/INC
          INC       PATCODIO/INC
          INC       OUTSITIO/INC
          INC       STD001IO
          INC       IBASECIO/INC
          INC       IBAPASIO/INC
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC
.
ENDIT   CHAIN           PGM
        STOP

.***************************************************************************
.* System    :   NSW EDWARD                                                *
.* Program   :   FX899679                                                  *
.* Desc      :   Write missing add practice EDWARD triggers to pmsidwaf    *
.***************************************************************************
.* Date      :   21/09/2021                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   This program will check all practices have an add         *
.*           :   practice record in the ISP trigger file pmsidwaf          *
.*           :   type 13                                                   *
.* Modifications  :                                                        *
.*        V11.01.00 21/09/2021  Ebon Clements TSK 0899679                  *
.*                  Created program                                        *
.***************************************************************************
.
          INC       STD001FD
.
          INC       PATCONFD/INC
          INC       PMSHCGFD/INC
          INC       PMSIDWFD/INC
.
. FILE DESCRIPTION INCLUDES
. -------------------------
.
. LOCAL VARIABLE DEFINITION
. -------------------------
ADDCOUNT  FORM      8
CHECKCNT  FORM      4
DIM22     DIM       22
.
.
PRGID     INIT      "FX899679"
PRGNAM    INIT      "EDWARD - Write Missing Add Practice Triggers"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,HUND25;*93,PTCNNSSI
.
          OPEN      PMSHCGA1,"pmshcgaf"
          OPEN      PMSIDWA1,"pmsidwaf"
          OPEN      PMSIDWA2,"pmsidwaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Write Missing Add Practice ":
                                           "Triggers"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
PROC0000  MATCH     SP70,PTCNNSSI           * no edward source system
          GOTO      PROC9000 IF EQUAL
.
          MATCH     "0000000000",PTCNNSSI   * no edward source system
          GOTO      PROC9000 IF EQUAL
.
          MOVE      ZERO,CHECKCNT           * Records Checked
          MOVE      ZERO,ADDCOUNT           * Triggers Added
.
          PACK      KEY12,SP70
          CALL      RSPMHCG1
PROC1000  CALL      RKPMHCG1                * Check all practices
          BRANCH    OVRCD,PROC9000
.
          ADD       ONE,CHECKCNT            * Display every 100th practice code
          IF        CHECKCNT > 100
            DISPLAY   *P15:20,*V2LON,PMHGPRAC 
            MOVE      ZERO,CHECKCNT
          ENDIF
.
          PACK      DIM22,SP10,PMHGPRAC,PMHGCNTR,SP70
.
          PACK      KEY41,DIM22,ONE,THREE,SP70,SP70
          CALL      RSPMIDW2
          CALL      RKPMIDW2
          BRANCH    OVRCD,PROC5000           * Check for an add practice
.
          MATCH     DIM22,PMIWCODE           * Same practice and count
          GOTO      PROC5000 IF NOT EQUAL
.
          MATCH     "13 ",PMIWTYPE           * 13 Add practice
          GOTO      PROC1000 IF EQUAL
.
PROC5000  PACK      PMIWCODE,SP10,PMHGPRAC,PMHGCNTR,SP70    * Practice/count
          MOVE      "13 ",PMIWTYPE                          * Add Practice
          PACK      PMIWOLDV,SP70,SP70,SP70,SP70            * Old Value
          PACK      PMIWNEWV,PMHGPRAC,PMHGCNTR,SP70:
                             SP70,SP70,SP70                 * New Value
.
          CALL      IBACLOCK
          PACK      PMIWCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PMIWCDAT
          MOVE      "00:00:00",PMIWCTIM
          MOVE      SP70,PMIWCUID
          PACK      PMIWSPAR,SP70,SP70
.
          PACK      KEY41,PMIWCDAT,PMIWCTIM,PMIWCODE,PMIWTYPE,SP70
          CALL      RAPMIDW1
          IF        OVRCD=1
            CALL      WRPMIDW1
            ADD       ONE,ADDCOUNT            * Add trigger count
          ENDIF
.
          GOTO      PROC1000
.
PROC9000  DISPLAY   *P1:22,*EF,"Triggers Added : ",ADDCOUNT:
                    *P1:24,*EF;
          CALL      HITENTER 
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PMSHCGIO/INC
          INC       PMSIDWIO/INC

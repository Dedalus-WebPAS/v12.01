+
.******************************************************************************
.* System   :  Patient                                                        *
.* Program  :  IBANHI03                                                       *
.* Desc     :  Request U/R Merge                                              *
.******************************************************************************
.* Date     :  23/08/89                                                       *
.* Author   :  S O'Gorman                                                     *
.* Comment  :                                                                 *
.*        V11.00.01 10/03/2020  Jill Parkinson Task 0879163                   *
.*                  Added SEXDSCIO include                                    *
.*        V10.01.01 10/02/2011  Jill Parkinson CAR 233088                     *
.*                  Added pmsvx1af                                            *
.* V10.00.01 09/04/2010 Steve Armstrong   CAR 219045                          *
.*           Recompiled for changes PATMRGFD.                                 *
.*           Mods to populate PTMRSTIM.                                       *
.******************************************************************************
.* V9.11.01  08/04/2009  Jill Habasque     CAR 194133                         *
.*           Changed KEY24 to KEY25 for patmrgaf read                         *
.* V9.03.02  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2             *
.*           - Add Sex Description routine SEXDSC00 with Code "R" - "Intersex"*
.* V9.03.01  29/10/2003 Sandra Barcham   43783                                *
.*           Recompiled for NZHISIIO and NZHISIUP                             *
.******************************************************************************
.* V9.02.00  10/12/2002  Jill  22697                                          *
.*           Ported from 5.10                                                 *
.******************************************************************************
.*        V5.08.03  31/08/2000  Tonii                                         *
.*                  Mods to accept Unknown and Indeterminate as valid patient *
.*                  sex description                                           *
.*        V5.08.02  21/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*             V5.08.01  18.07.2000  Sandra Barcham                           *
.*                  Recompiled for version change                             *
.******************************************************************************
.*             V5.06.01  04/11/1998  J.Tan   SRF 644448                       *
.*                  Fixed page number                                         *
.*                  22/10/1998  Glenn Berry      5.07 changes
.******************************************************************************
.*             V5.04.01  16/06/1997  Steve Armstrong                          *
.*                  Fixed bugs in global recompile                            *
.******************************************************************************
.*             V5.01.128 20/08/93  Peter Eddey                                *
.*                       Added NZHIS Merge routines                           *
.*             V5.01.129 23/11/93  Paul Howells       NZHIS                   *
.*                       Added PATALRFD & IO for NZHISIIO                     *
.*             V5.01.130 16/02/94  Matt Surridge      MTA                     *
.*                       Upgraded error messages to meet standards.           *
.*                       Added HEAD132.                                       *
.*        V5.01.131 24/03/1994    Matt Surridge                               *
.*                  Recompile for NZHISIIO and DONORDET.                      *
.*        V5.01.132 25/05/1994 Ian Rutt                                       *
.*                  Fixed Global Recompile Bugs                               *
.*        V5.01.133 10/08/1994    ROD                                         *
.*                  Recompile for NZHISIIO                                    *
.*        V5.02.01  07.12.94    B.G.Ackland                                   *
.*                  Fixed National Number lookup for merge request            *
.*                  where national number is Not used as local number         *
.*        V5.02.02  09/12/94  Graeme Williams                                 *
.*                  Allow merge requests when DOB and/or sex differ           *
.*        V5.03.01  17/07/95  ROD                SRF 640438                   *
.*                  Fix I*U by re-reading record directly after write         *
.*        V5.03.02  10/04/96  Whirlpool         (NZ 5.04 Mods)                *
.*                  Changed New and Old to Valid and Invalid                  *
.*                  Fixed a couple of bugs in the Print Routines              *
.******************************************************************************
.
          INC       STD001FD
.
          INC       BOKRC1FD/INC
          INC       PATALRFD/INC
          INC       PATMRGFD/INC
          INC       PATCALAG/INC
          INC       PATCODFD/INC
          INC       PATCOMM/INC
          INC       PATCONFD/INC
          INC       PATDHEAD/INC
          INC       PATHOSFD/INC
          INC       PATMA1FD/INC
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC

          INC       PATMI1FD/INC
          INC       PATPR1FD/INC
          INC       PATGSRFD/INC
.
          INC       OUTPREFD/INC
.
          INC       PATNIDFD/INC
          INC       NHIMASFD/INC
          INC       PATDNRFD/INC
......    INC       PATMWSFD/INC
          INC       PATSDNFD/INC
          INC       PATSMWFD/INC
          INC       NZHISIFD/INC
          INC       TMPALIFD/INC
          INC       SCRPSTFD/INC
         
+
.**********************************************************************
.*                           CONSTANTS                                *
.**********************************************************************
.
APPRV1    INIT      "Merge Approved "
APPRV2    INIT      "Merge Rejected "
APPRV3    INIT      "No Action Taken"
.
DATEHED1  INIT      "Requested Merges   "
DATEHED2  INIT      "Outstanding Merges "
DATEHED3  INIT      "Approved Merges    "
DATEHED4  INIT      "Processed Merges   "
DATEHED5  INIT      "Rejected Merges    "
DATEHED6  INIT      "Last Status Change "
.
DATEDES1  INIT      "Requested"
DATEDES2  INIT      "Requested"
DATEDES3  INIT      "Approved "
DATEDES4  INIT      "Processed"
DATEDES5  INIT      "Rejected "
DATEDES6  INIT      "Change   "
.
TOTALALI  FORM       3
.
PIPE      INIT      "|"
.
PRSTAT1   INIT      "Requested"
PRSTAT2   INIT      "Accepted "
PRSTAT3   INIT      "Processed"
PRSTAT4   INIT      "Rejected "
.
RESPYES   INIT      "Yes"
RESPNO    INIT      "No "
RESPUNKN  INIT      "Unk"
.
STATFOUR  INIT      "4"
STATUS1   INIT      "Merge Requested     "
STATUS2   INIT      "Merge Fully Accepted"
STATUS3   INIT      "Merge Processed     "
STATUS4   INIT      "Merge Rejected      "
.
UNDLN65   INIT      "-----------------------------------":
                    "------------------------------"
+
.**********************************************************************
.*                       GLOBAL VARIABLES                             *
.**********************************************************************
.
ACTION    DIM       1
APPRDESC  DIM       15
.
BJDAY     FORM      3
CJDAY     FORM      3
CHG       FORM      1
.
CMDLINE   DIM       80
COUNT     FORM      1
.
DATEDESC  DIM       9
DIM8      DIM       8
DIM10     DIM       10
DIM20     DIM       20
DIM27     DIM       27
DIM42     DIM       42
DISPURNO  DIM       8
.
ENDDATE   DIM       8
EXIT1     FORM      1
.
FIRSTCH   FORM      1
FORM3     FORM      3
FRSTGNAM  DIM       40
FULLGNAM  DIM       80
.
HEADDESC  DIM       18
.
.
LINE      DIM       42
LOCKSTAT  DIM       1
MODE1     DIM       1
NMPNUMB   DIM       20
NAMELINE  DIM       42
NEWDOB    DIM       10
NEWNAME   DIM       42
NEWGNAME  DIM       25
NEWSNAME  DIM       20
NEWSEX    DIM       8
NEWURNO   DIM       8
NWSTDESC  DIM       20
.
OLDDOB    DIM       10
OLDNAME   DIM       42
OLDGNAME  DIM       25
OLDSNAME  DIM       20
OLDSEX    DIM       8
OLDSTAT   FORM      1
OLDURNO   DIM       8
.
REQSTHOS  DIM       30
RESCOUNT  FORM      1
RESPDESC  DIM       3
.
SAVEOLUR  DIM       8
SAVENWUR  DIM       8
SAVERDTE  DIM       8
SAVERHOS  DIM       4
SAVEAPPR  DIM       20
SAVESTAT  FORM      1
SAVESDTE  DIM       8
SCNDGNAM  DIM       40
STATDESC  DIM       20
STRTDATE  DIM       8
.
TEMP42    DIM       42
TODAY     DIM       8
.
SDAY      DIM       2
SMON      DIM       2
SYEAR     DIM       2
SCENT     DIM       2
STRTNAME  DIM       27
PRINT27   DIM       27
PRINT26   DIM       26
.
Z15       INIT      "zzzzzzzzzzzzzzz"
.
PRGID     INIT      "IBANHI03"
PRGNAM    INIT      "Request NHI U/R Merge"
VERSION   INIT      "V12.01.00"
.
+
.**********************************************************************
.*                           MAINLINE                                 *
.**********************************************************************
ML0000    CALL      INIT0000           * initialization
          BRANCH    EXIT,ML9999       * Hospital Not on file
.
ML0100    CALL      SCR0000           * Main Option Screen
          BRANCH    EXIT,ML9999
          BRANCH    OPTION,ML1000,ML2000,ML3000
.
.-------- Request Merge Of U/R No's ----------
.
ML1000    CALL      CLR0000           * Clear Variables
          CALL      USCR0000          * U/R Screen
.
ML1100    CALL      OLUR0000          * Keyin Old U/R Number
          BRANCH    EXIT,ML0100
          CALL      OLVL0000          * Old U/R Number Validation
          BRANCH    EXIT,ML1100
.
ML1200    CALL      NWUR0000          * Keyin New U/R Number
          BRANCH    EXIT,ML1100
          CALL      CHEK0000          * Check that certain details match.....
          CALL      NWVL0000          * Validate New U/R Number
          BRANCH    EXIT,ML1200
.
          CALL      POST0000          * Ok. to Post (Y/N/C)
          BRANCH    EXIT,ML1000,ML0100
.
          CALL      WRIT0000          * Write Request to File
          BRANCH    EXIT,ML9999       * Hospital Not Set-Up
          CALL      APPNOW00          * Approve Merge Now (Y/N) ?
          GOTO      ML0100
.
.-------- Approved/Rejected Proposed Merges ----------
.
ML2000    CALL      CLR0000           * Clear Variables
          CALL      USCR0000
.
          CALL      OLUR0000          * Keyin Old U/R Number
          BRANCH    EXIT,ML0100
.
          CALL      OLCK0000          * Check Old U/R Number on File
          BRANCH    EXIT,ML2000
ML2200    CALL      NWUR0000          * Keyin New U/R Number
          BRANCH    EXIT,ML2000
.
          CALL      NWCK0000          * Check New U/R Number on File
          BRANCH    EXIT,ML2200
.
.######   CALL      CONT0000          * Ok to Continue (Y/N/C)
.######   BRANCH    EXIT,ML2000,ML0100
.
          CALL      LOCK0000          * Lock Merger on File
          BRANCH    EXIT,ML2000       * Record in Use
.
          CALL      DATA0000          * Get Current Merger Data
ML2400    CALL      APRJ0000          * Approve or Reject Merger
.
          CALL      NEWW0000          * Display New Status
.
.#######  CALL      POST0000          * Ok. to Post (Y/N/C)
.#######  BRANCH    EXIT,ML2400,ML2000
.
ML2500    CALL      UPDT0000          * Update the Record Status
.
          GOTO      ML2000
.
.-------- Reporting -------
.
ML3000    CALL      CLR0000           * Clear Variables
          CALL      RSCR0000          * Reporing Screen
          BRANCH    EXIT,ML0100
ML3100    CALL      STRT0000          * Keyin From Date
          BRANCH    EXIT,ML3000
.
          CALL      ENDD0000          * Keyin To Date
          BRANCH    EXIT,ML3100
.
          CALL      CONT0000          * Ok. to Continue (Y/N/C)
          BRANCH    EXIT,ML3100,ML0100
.
          CALL      EXTR0000          * Extract Merges  in Date Range
.
          GOTO      ML3000
.
ML9999    CHAIN     PGM
+
.**********************************************************************
.*                           INIT0000                                 *
.**********************************************************************
.
INIT0000  MOVE      FALSE,EXIT
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patmrgaf"
          OPEN      PATMRGG1,"patmrgaf"
          OPEN      PATMRGG2,"patmrgaf"
          OPEN      PATMRGG3,"patmrgaf"
          OPEN      PATMRGG4,"patmrgaf"
.
          DISPLAY   *P64:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"pmspx2af"
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patpramf"
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
.
          DISPLAY   *P64:24,"pathospf"
          OPEN      PATHOSP1,"pathospf"
          OPEN      PATHOSP2,"pathospf"
.
          DISPLAY   *P64:24,"patgsrnf"
          OPEN      PATGSRN3,"patgsrnf"
          DISPLAY   *P64:24,"patgsrnf"
          OPEN      PATGSRN4,"patgsrnf"
          DISPLAY   *P64:24,"patgsrnf"
          OPEN      PATGSRN2,"patgsrnf"
.
          DISPLAY   *P64:24,"nhimasaf"
          OPEN      NHIMASA1,"nhimasaf"
          OPEN      NHIMASA2,"nhimasaf"
.
          DISPLAY   *P64:24,"controlf"
          OPEN      CONTROLF,"controlf"
.
          PACK      TODAY,CCC,CYY,CMM,CDD
          REP       " 0",TODAY
.
          MOVE      CHOSINDX,DIM2
          PACK      KEY2,DIM2
          CALL      RDHOSP2
.
          COMPARE   ZERO,OVRCD
          GOTO      INIT8000 IF EQUAL
.
          READ      CONTROLF,TWENTY1;*138,PTCNNHII 
          READ      CONTROLF,FIFTY9;*84,CMERGEUR
          READ      CONTROLF,TEN;*244,CHOSINDX   * If read fails using the
          PACK      KEY2,CHOSINDX                * Hospital number passed thru
          CALL      RDHOSP2                      * common the read the con. file
.
          COMPARE   ZERO,OVRCD
          GOTO      INIT8000 IF EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"Hospital Number Not Setup. "; 
          CALL      HITENTER
          MOVE      TRUE,EXIT
          GOTO      INIT9999
.
. try to connect to National System
.
INIT8000  CALL      CONNECT
          IF        OVRCD=1
            MOVE      ONE,EXIT
          ENDIF
.
INIT9999  RETURN
.
.**********************************************************************
.*                            SCR0000                                 *
.*                      Main Option Screen                            *
.**********************************************************************
.
SCR0000   MOVE      FALSE,EXIT
          HLINE     *G37,2,51,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Request Merge of U/R Numbers":
                    *P1:6,*V2LON,"2",*HOFF," = Approve/Reject Proposed Merges":
           *P1:7,*V2LON,"3",*HOFF," = Merge Reporting":
                    *P1:9,"Select Item : _";
SCR0100   KEYIN     *P15:9,*EL,*V2LON,OPTION;
.
          BRANCH    OPTION,SCR2000,SCR3000,SCR4000
.
          COMPARE   ZERO,OPTION
          GOTO      SCR9000 IF EQUAL
.
          BEEP
          GOTO      SCR0100
SCR2000   DISPLAY   *P51:2,*V2LON,"- Request "
          GOTO      SCR9999
SCR3000   DISPLAY   *P51:2,*V2LON,"- Approve/Reject "
          GOTO      SCR9999
SCR4000   DISPLAY   *P51:2,*V2LON,"- Reports "
          GOTO      SCR9999
SCR9000   MOVE      TRUE,EXIT
SCR9999   RETURN
.
.**********************************************************************
.*                            CLR0000                                 *
.*                    Clear Variables Routine                         *
.**********************************************************************
.
CLR0000   UNPACK    SP30,OLDURNO,NEWURNO
          UNPACK    SP30,NEWGNAME 
          UNPACK    SP30,NEWSNAME 
          UNPACK    SP30,OLDGNAME 
          UNPACK    SP30,OLDGNAME 
          UNPACK    SP30,REQSTHOS
          UNPACK    SP30,STATDESC
.
          RETURN
.
.**********************************************************************
.*                           USCR0000                                 *
.*                      Main Option Screen                            *
.**********************************************************************
.
USCR0000  DISPLAY   *P1:3,*EF:
                    *P1:4,"Invalid U/R Number : ":
                    *P1:7,"Valid U/R Number   : "
          RETURN
.
.**********************************************************************
.*                            OLUR0000                                *
.*                    Keyin the Old U/R Number                        *
.**********************************************************************
.
OLUR0000  MOVE      FALSE,EXIT
.
          DISPLAY   *P30:4,*EL:
                    *P30:5,*EL;
.
          UNPACK    SP30,OLDSEX,OLDDOB
          PACK      OLDNAME,SP30,SP30
.
          MOVE      FOUR,CVERT
          MOVE      TWENTY2,CCOL
          MOVE      TWO,SRCHOPT
          MOVE      ZERO,SRCHSYS
.
          CALL      KURN
.
          BRANCH    EXIT,OLUR9000
          BRANCH    OVRCD,OLUR5000
.
          MATCH     ZEROUR,PURNO
          GOTO      OLUR5000 IF EQUAL
.
          MOVE      PURNO,OLDURNO
          MOVE      PGNAME,OLDGNAME              * save given name
          MOVE      PSNAME,OLDSNAME              * save surname
.
          CALL      NAME0000                     * Format Patient's Name
          MOVE      NAMELINE,OLDNAME
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
.
          MATCH     SP2,CDAY
          GOTO      OLUR0500 IF EQUAL
.
          PACK      OLDDOB,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
OLUR0500  MOVE      SP8,OLDSEX
.
.* ****************************************** Start of Changes         *C-49016
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          MOVE      DSEXDESC,OLDSEX
.* ******************************************   End of Changes         *C-49016
.
OLUR3000  DISPLAY   *P34:4,"Name : ",*V2LON,OLDNAME,*HOFF:
                    *P34:5,"DOB  : ",*V2LON,OLDDOB,*HOFF:
                    *P54:5,"Sex : ",*V2LON,OLDSEX;
.
          GOTO      OLUR9999
OLUR5000  DISPLAY   *P1:24,*EL,*B,"U/R Number Not on File. ";
          CALL      HITENTER
.
          GOTO      OLUR0000
OLUR9000  DISPLAY   *PCCOL:CVERT,*EL;
OLUR9999  RETURN
.
.**********************************************************************
.*                            NWUR0000                                *
.*                    Keyin the New U/R Number                        *
.**********************************************************************
.
NWUR0000  MOVE      FALSE,EXIT
.
          DISPLAY   *P30:7,*EL:
                    *P30:8,*EL;
.
          UNPACK    SP30,NEWDOB,NEWSEX
          PACK      NEWNAME,SP30,SP30
.
          MOVE      SEVEN,CVERT
          MOVE      TWENTY2,CCOL
          MOVE      TWO,SRCHOPT
          MOVE      ZERO,SRCHSYS
.
          CALL      KURN
          BRANCH    EXIT,NWUR9000
          BRANCH    OVRCD,NWUR5000
.
          MATCH     ZEROUR,PURNO
          GOTO      NWUR5000 IF EQUAL
.
          MOVE      PURNO,NEWURNO
          MOVE      PGNAME,NEWGNAME              * save new given name
          MOVE      PSNAME,NEWSNAME              * save new surname 
.
          CALL      NAME0000                     * Format Patient's Name
          MOVE      NAMELINE,NEWNAME
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
.
          MATCH     SP2,CDAY
          GOTO      NWUR0500 IF EQUAL
.
          PACK      NEWDOB,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
NWUR0500  MOVE      SP8,NEWSEX
.
.* ****************************************** Start of Changes         *C-49016
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          MOVE      DSEXDESC,NEWSEX
.* ******************************************   End of Changes         *C-49016
.
NWUR3000  DISPLAY   *P34:7,"Name : ",*V2LON,NEWNAME,*HOFF:
                    *P34:8,"DOB  : ",*V2LON,NEWDOB,*HOFF:
                    *P54:8,"Sex : ",*V2LON,NEWSEX;
.
          GOTO      NWUR9999
NWUR5000  DISPLAY   *P1:24,*EL,*B,"U/R Number Not on File. ";
          CALL      HITENTER
.
          GOTO      NWUR0000
NWUR9000  DISPLAY   *PCCOL:CVERT,*EL;
NWUR9999  RETURN
.
.**********************************************************************
.*                            OLVL0000                                *
.*                   Validate the Old U/R Number                      *
.**********************************************************************
.
OLVL0000
.
. -----   Check if Old U/R Number is Already Requested to be Changed -----
. -----   With a Status = 1 -----
.
          PACK      KEY17,ONE,OLDURNO,SP20
          CALL      RSPTMRG1
OLVL0100  CALL      RKPTMRG1
          BRANCH    OVRCD,OLVL1000
.
          MATCH     OLDURNO,PTMROLUR
          GOTO      OLVL1000 IF NOT EQUAL
.
          COMPARE   ONE,PTMRSTAT
          GOTO      OLVL1000 IF NOT EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"Request to Merge ",*V2LON,OLDURNO,*HOFF:
                    " Already on File. ";
          CALL      HITENTER
.
          GOTO      OLVL9000 IF EQUAL
OLVL1000
.
. -----   Check if Old U/R Number is Already Requested to be Changed -----
. -----   With a Status = 2 -----
.
          PACK      KEY17,TWO,OLDURNO,SP20
          CALL      RSPTMRG1
OLVL1100  CALL      RKPTMRG1
          BRANCH    OVRCD,OLVL2000
.
          MATCH     OLDURNO,PTMROLUR
          GOTO      OLVL2000 IF NOT EQUAL
.
          COMPARE   TWO,PTMRSTAT
          GOTO      OLVL2000 IF NOT EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"Request to Merge ",*V2LON,OLDURNO,*HOFF:
                    " Already on File. ";
          CALL      HITENTER
          GOTO      OLVL9000
OLVL2000
.
. -----   Check if Old U/R Number is Already Entered to be Changed -----
. -----   With a Status = 1 -----
.
          PACK      KEY17,ONE,OLDURNO,SP20
          CALL      RSPTMRG2
OLVL2100  CALL      RKPTMRG2
          BRANCH    OVRCD,OLVL3000
.
          MATCH     OLDURNO,PTMRNWUR
          GOTO      OLVL3000 IF NOT EQUAL
.
          COMPARE   ONE,PTMRSTAT
          GOTO      OLVL3000 IF NOT EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"Request to Merge ",*V2LON,OLDURNO,*HOFF:
                    " Already on File. "; 
          CALL      HITENTER
          GOTO      OLVL9000
OLVL3000
.
. -----   Check if Old U/R Number is Already Requested to be Changed -----
. -----   With a Status = 2 -----
.
          PACK      KEY17,TWO,OLDURNO,SP20
          CALL      RSPTMRG1
OLVL3100  CALL      RKPTMRG1
          BRANCH    OVRCD,OLVL7000
.
          MATCH     OLDURNO,PTMRNWUR
          GOTO      OLVL7000 IF NOT EQUAL
.
          COMPARE   TWO,PTMRSTAT
          GOTO      OLVL7000 IF NOT EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"Request to Merge ",*V2LON,OLDURNO,*HOFF:
                    " Already on File. "; 
          CALL      HITENTER
          GOTO      OLVL9000
.
. make sure patient is on national system
.
OLVL7000  PACK      KEY8,OLDURNO
          CALL      RDNHMAS2
          IF        OVRCD=1
            DISPLAY   *P1:24,*EL,*B,"Patient Missing off NHI link file.  ";
            CALL      HITENTER
            GOTO      OLVL9000
          ENDIF
.
          CALL      CONNECT
          BRANCH    OVRCD,OLVL9000
.
          MOVE      NHMANMPI,NZIBNMPI
          CALL      GETHCU
          BRANCH    OVRCD,OLVL9000
.
          CALL      DCHG0000                      * check PMI Local vs National
          BRANCH    EXIT,OLVL9999                 * details diff. Dont continue
.
          MOVE      ZERO,EXIT
          GOTO      OLVL9999
.
OLVL9000  CALL      NZHISERR                    * display error message
          MOVE      TRUE,EXIT
OLVL9999  RETURN
.
.**********************************************************************
.*                            CHEK0000                                *
.*                   Check the following details for a match          *
.**********************************************************************
CHEK0000  MOVE      FALSE,EXIT
          MATCH     OLDDOB,NEWDOB           * old D.O.B. = old D.O.B.
          GOTO      CHEK9200 IF NOT EQUAL
          MATCH     OLDSEX,NEWSEX           * old sex = new sex   
          GOTO      CHEK9300 IF NOT EQUAL
.
          MATCH     NEWGNAME,OLDGNAME           * old name = new name
          GOTO      CHEK9100 IF NOT EQUAL
          MATCH     NEWSNAME,OLDSNAME           * old surname = new surname
          GOTO      CHEK9999 IF EQUAL
.
.-------  The obove mentioned criteria doesn't match therfore no request -----
.
CHEK9100  DISPLAY   *P1:24,*B,*EL,"WARNING : Patient Names not Equal. "; 
          CALL      HITENTER
          GOTO      CHEK9999
.
CHEK9200  DISPLAY   *P1:24,*B,*EL,"WARNING : Dates of Birth not Equal. "; 
          CALL      HITENTER
          GOTO      CHEK9999
.
CHEK9300  DISPLAY   *P1:24,*B,*EL,"WARNING : Genders not Equal. "; 
          CALL      HITENTER
.
CHEK9999  RETURN
.**********************************************************************
.*                            NWVL0000                                *
.*                   Validate the New U/R Number                      *
.**********************************************************************
.
NWVL0000  MOVE      FALSE,EXIT
.
. -----   Check If Old U/R Number and New U/R Number are the Same  -----
.
          MATCH     OLDURNO,NEWURNO
          GOTO      NWVL1000 IF NOT EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"Cannot Merge a U/R Number With Itself. ";
          CALL      HITENTER
          GOTO      NWVL9000
NWVL1000
.
. -----   Check if Actual Merge is on File  -----
.
          PACK      KEY17,ONE,NEWURNO,OLDURNO
          CALL      RSPTMRG1
.
          CALL      RKPTMRG1
          BRANCH    OVRCD,NWVL1500
.
          MATCH     OLDURNO,PTMROLUR
          GOTO      NWVL1500 IF NOT EQUAL
.
          MATCH     NEWURNO,PTMRNWUR
          GOTO      NWVL1500 IF NOT EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"Request to Merge These Two U/R Numbers ":
                    "Already on File. "; 
          CALL      HITENTER
          GOTO      NWVL9000
NWVL1500
.
. -----   Check if Actual Merge is previously on File, but rejected -----
.-------  with a status = "4"                                      -----
.
          PACK      KEY17,FOUR,OLDURNO,NEWURNO        * "4" = rejected
          CALL      RDPTMRG1
          BRANCH    OVRCD,NWVL2000
.
          MATCH     OLDURNO,PTMROLUR
          GOTO      NWVL2000 IF NOT EQUAL
.
          MATCH     NEWURNO,PTMRNWUR
          GOTO      NWVL2000 IF NOT EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"Merge Previously Requested, ":
                    "Use Approve/Reject Option. "; 
          CALL      HITENTER
          GOTO      NWVL9000
.
NWVL2000
.
. -----   Check if New U/R Number is Already Requested to be Merged -----
. -----   as an Old U/R Number With a Status = 1 -----
.
          PACK      KEY17,ONE,NEWURNO,SP20
          CALL      RSPTMRG1
.
          CALL      RKPTMRG1
          BRANCH    OVRCD,NWVL3000
.
          MATCH     NEWURNO,PTMROLUR
          GOTO      NWVL3000 IF NOT EQUAL
.
          COMPARE   ONE,PTMRSTAT
          GOTO      NWVL3000 IF NOT EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"Request to Merge ",*V2LON,NEWURNO,*HOFF:
                    " Already on File. "; 
          CALL      HITENTER
.
          GOTO      NWVL9000 IF EQUAL
NWVL3000
.
. -----   Check if New U/R Number is Already Requested to be Merged -----
. -----   as an Old U/R Number With a Status = 2 -----
.
          PACK      KEY17,TWO,NEWURNO,SP20
          CALL      RSPTMRG1
.
          CALL      RKPTMRG1
          BRANCH    OVRCD,NWVL4000
.
          MATCH     NEWURNO,PTMROLUR
          GOTO      NWVL4000 IF NOT EQUAL
.
          COMPARE   TWO,PTMRSTAT
          GOTO      NWVL4000 IF NOT EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"Request to Merge ",*V2LON,NEWURNO,*HOFF:
                    " Already on File. "; 
          CALL      HITENTER
          GOTO      NWVL9000
NWVL4000
.
. -----   Check if New U/R Number is Already Requested to be Merged -----
. -----   as a New U/R Number With a Status = 1 -----
.
          PACK      KEY17,ONE,NEWURNO,SP20
          CALL      RSPTMRG2
.
          CALL      RKPTMRG2
          BRANCH    OVRCD,NWVL5000
.
          MATCH     NEWURNO,PTMROLUR
          GOTO      NWVL5000 IF NOT EQUAL
.
          COMPARE   TWO,PTMRSTAT
          GOTO      NWVL5000 IF NOT EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"Request to Merge ",*V2LON,NEWURNO,*HOFF:
                    " Already on File. "; 
          CALL      HITENTER
NWVL5000
.
. -----   Check if New U/R Number is Already Requested to be Merged -----
. -----   as a New U/R Number With a Status = 2 -----
.
          PACK      KEY17,TWO,NEWURNO,SP20
          CALL      RSPTMRG2
          CALL      RKPTMRG2
          BRANCH    OVRCD,NWVL7000
.
          MATCH     NEWURNO,PTMROLUR
          GOTO      NWVL7000 IF NOT EQUAL
.
          COMPARE   TWO,PTMRSTAT
          GOTO      NWVL7000 IF NOT EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"Request to Merge ",*V2LON,NEWURNO,*HOFF:
                    " Already on File. "; 
          CALL      HITENTER
          GOTO      NWVL9000
.
. make sure patient is on national system
.
NWVL7000  PACK      KEY8,NEWURNO
          CALL      RDNHMAS2
          IF        OVRCD=1
            DISPLAY   *P1:24,*EL,*B,"Patient Missing off NHI link file.  ";
            CALL      HITENTER
            GOTO      NWVL9000
          ENDIF
.
          CALL      CONNECT
          BRANCH    OVRCD,NWVL9000
.
          MOVE      NHMANMPI,NZIBNMPI
          CALL      GETHCU
          BRANCH    OVRCD,NWVL9000
.
          CALL      DCHG0000                      * check PMI Local vs National
          BRANCH    EXIT,NWVL9999                 * details diff. Dont continue
.
          MOVE      ZERO,EXIT
          GOTO      NWVL9999
.
NWVL9000  CALL      NZHISERR                    * display error message
          MOVE      TRUE,EXIT
.
NWVL9999  RETURN
+
.**********************************************************************
.*  DCHG0000  :  Check if details are different (Local vs National)   *
.**********************************************************************
DCHG0000  MATCH     NZIBSURN,NHMASURN             * same surname?
          GOTO      DCHG5000 IF NOT EQUAL
          MATCH     NZIBSEX,NHMASEX               * same sex?
          GOTO      DCHG5000 IF NOT EQUAL
          MATCH     NZIBDOB,NHMADOB               * same DOB?
          GOTO      DCHG8000 IF EQUAL             * all details are same
.
. the main details have changed so give user warning
.
DCHG5000  DISPLAY   *P1:24,*EL,*B,"Error: PMI details differ between Local ":
                    "& National System.  ";
          CALL      HITENTER
.
          MOVE      ONE,EXIT                      * dont continue
          GOTO      DCHG9999
.
DCHG8000  MOVE      ZERO,EXIT                     * continue with update
.
DCHG9999  RETURN
.
.**********************************************************************
.*                            POST0000                                *
.*                  Ok. to Post a Record (Y/N/C)                      *
.**********************************************************************
.
POST0000  CALL      POSTQST
.
          MOVE      ANS,FORM1
.
          LOAD      EXIT,FORM1,ZERO,ONE,TWO
          GOTO      POST0000 IF OVER
.
          COMPARE   ZERO,EXIT
          GOTO      POST9999 IF EQUAL
.
          CALL      UNLK0000                * Unlock Record on Exit
POST9999  RETURN
.
.**********************************************************************
.*                            WRIT0000                                *
.*                Writw a Request to the Request File                 *
.**********************************************************************
.
WRIT0000  MOVE      OLDURNO,PTMROLUR
          MOVE      NEWURNO,PTMRNWUR
.
          PACK      PTMRRDTE,CCC,CYY,CMM,CDD
          REP       " 0",PTMRRDTE
.
          MOVE      CHOSINDX,DIM2
          PACK      KEY2,DIM2
          CALL      RDHOSP2
          MOVE      HOSCODE,PTMRRHOS
.
          MOVE      SP30,PTMRAPPR
          MOVE      CHOSINDX,HOSINDX             * Use Current Hospital Number
          SUB       ONE,HOSINDX
          RESET     PTMRAPPR,HOSINDX
          APPEND    ANSY,PTMRAPPR
          RESET     PTMRAPPR
          ADD       ONE,HOSINDX
.
          MOVE      ONE,PTMRSTAT
.
          PACK      PTMRSDTE,CCC,CYY,CMM,CDD
          REP       " 0",PTMRSDTE
          CLOCK     TIME,CTIMEIS
          MOVE      CTIMEIS,PTMRSTIM
          REP       " 0",PTMRSTIM
.
          MOVE      SP2,PTMRLOCK
.
          CALL      WRPTMRG1
.
. re-read record   (fix I*U srf 640438)
.
          PACK      KEY17,PTMRSTAT,PTMROLUR,PTMRNWUR
          CALL      RDPTMRG1
.
WRIT9999  RETURN
.
.**********************************************************************
.*                           CONT0000                                 *
.*                     Ok. to Continue (Y/N/C)                        *
.**********************************************************************
.
CONT0000  CALL      CONTQST
.
          MOVE      ANS,FORM1
.
          LOAD      EXIT,FORM1,ZERO,ONE,TWO
          GOTO      CONT0000 IF OVER
.
          RETURN
.
.**********************************************************************
.*                           LOCK0000                                 *
.*                      Lock Merger on File                           *
.**********************************************************************
.
LOCK0000  PACK      KEY17,PTMRSTAT,PTMROLUR,PTMRNWUR
          CALL      RLPTMRG1
.
          MOVE      PTMRSTAT,LOCKSTAT
.
          BRANCH    OVRCD,LOCK1000,LOCK2000
          GOTO      LOCK9999
LOCK1000  DISPLAY   *P1:24,*EL,*B,"Record Not Found On Lock. "; 
          CALL      HITENTER
LOCK2000  DISPLAY   *P1:24,*EL,*B,"Record In Use By Port ",*V2LON:
                    PTMRLOCK,*HOFF,". ";
          CALL      HITENTER
          MOVE      TRUE,EXIT
LOCK9999  RETURN
.
.**********************************************************************
.*                           UNLK0000                                 *
.*                     Unlock Merger on File                          *
.**********************************************************************
.
UNLK0000
.
          MOVE      LOCKSTAT,PTMRSTAT
.
          PACK      KEY17,PTMRSTAT,PTMROLUR,PTMRNWUR
          CALL      ULPTMRG1
.
          RETURN
.
.**********************************************************************
.*                           APRJ0000                                 *
.*                    Get Current Merger Data                         *
.**********************************************************************
.
DATA0000  MOVE      FALSE,EXIT
.
          PACK      KEY4,PTMRRHOS
          CALL      RDHOSP1
          COMPARE   ZERO,OVRCD
          GOTO      DATA1000 IF EQUAL
.
          MOVE      "***  Unknown  Hospital  ***",REQSTHOS
          GOTO      DATA2000
DATA1000  MOVE      HOSDESC,REQSTHOS
.
DATA2000  UNPACK    PTMRRDTE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",CCENT
          REP       " 0",CYEAR
          REP       " 0",CMON
          REP       " 0",CDAY
.
          LOAD      STATDESC,PTMRSTAT,STATUS1,STATUS2,STATUS3,STATUS4
.
          MOVE      PTMRSTAT,OLDSTAT
          MOVE      CHOSINDX,FORM2
.         SUB       ONE,FORM2
          ENDSET    PTMRAPPR
          RESET     PTMRAPPR,FORM2
          MOVE      PTMRAPPR,DIM1
.
          REP       "Y1N2 3",DIM1
          MOVE      DIM1,FORM1
.
          LOAD      APPRDESC,FORM1,APPRV1,APPRV2,APPRV3
.
          DISPLAY   *P1:10,*EF,*ULON,SP20,SP9,"CURRENT MERGER DETAILS":
                    SP20,SP9,*HOFF:
                    *P1:12,"Requesting Hospital : ":
                    *P1:13,"Requested Date      : ":
                    *P1:15,"Current Status      : ":
                    *P1:17,"Current Approval    : ":
                    *P1:19,"New Status          : ";
.
          DISPLAY   *P18:7,*V2LON,PTMRNWUR:
                    *P23:12,PTMRRHOS,*HOFF:
                    *P40:12,REQSTHOS,*V2LON:
                    *P23:13,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR:
                    *P23:15,STATDESC:
                    *P23:17,APPRDESC;
.
          RETURN
.
.**********************************************************************
.*                           APRJ0000                                 *
.*                   Approve, Reject, No Action                       *
.**********************************************************************
.
APRJ0000  DISPLAY   *P1:24,*EL,"(",*V2LON,ANSA,*HOFF,")pprove, ":
                    "(",*V2LON,ANSR,*HOFF,")eject or ":
                    "(",*V2LON,ANSN,*HOFF,")o Action ? _";
APRJ1000  KEYIN     *P38:24,*EL,*V2LON,ANS;
.
          REP       UPPLOW,ANS
          PACK      ANS,ANS,SP10
          MATCH     SP1,ANS
          GOTO      APRJ2000 IF NOT EQUAL
.
          BEEP
          GOTO      APRJ1000
APRJ2000  MOVE      ANS,ACTION
          REP       "N AYRN",ACTION
.
          MOVE      ZERO,FORM1
          REP       "A1R2N3102030405060708090",ANS
          MOVE      ANS,FORM1
          BRANCH    FORM1,APRJ8000,APRJ8000,APRJ8000
.
          BEEP
          GOTO      APRJ1000
APRJ8000  LOAD      APPRDESC,FORM1,APPRV1,APPRV2,APPRV3
          DISPLAY   *P23:17,*EL,*V2LON,APPRDESC;
          RETURN
.
.**********************************************************************
.*                            OLCK0000                                *
.*                 Check Old U/R Number is on File                    *
.**********************************************************************
.
OLCK0000
.
. -----   Check if Old U/R Number is Requested to be Changed -----
. -----   With a Status = 1 -----
.
          PACK      KEY17,ONE,OLDURNO,SP20
          CALL      RSPTMRG1
.
          CALL      RKPTMRG1
          BRANCH    OVRCD,OLCK9000
.
          MATCH     OLDURNO,PTMROLUR
          GOTO      OLCK9999 IF EQUAL
.
.
. -----   Check if Old U/R Number is Requested to be Changed -----
. -----   With a Status = 2 -----
.
          PACK      KEY17,TWO,OLDURNO,SP20
          CALL      RSPTMRG1
.
          CALL      RKPTMRG1
          BRANCH    OVRCD,OLCK9000
.
          MATCH     OLDURNO,PTMROLUR
          GOTO      OLCK9999 IF EQUAL
.
.
. -----   Check if Old U/R Number is Requested to be Changed -----
. -----   With a Status = 4 -----
.
          PACK      KEY17,FOUR,OLDURNO,SP20
          CALL      RSPTMRG1
.
          CALL      RKPTMRG1
          BRANCH    OVRCD,OLCK9000
.
          MATCH     OLDURNO,PTMROLUR
          GOTO      OLCK9999 IF EQUAL
OLCK9000  DISPLAY   *P1:24,*EL,*B,"No Requests on File For U/R ":
                    *V2LON,OLDURNO,*HOFF,". "; 
          CALL      HITENTER
          MOVE      TRUE,EXIT
OLCK9999  RETURN
.
.**********************************************************************
.*                            NWCK0000                                *
.*                 Check New U/R Number is on File                    *
.**********************************************************************
.
NWCK0000
.
. -----   Check if New U/R Number is Requested to be Changed -----
. -----   With a Status = 1 -----
.
          PACK      KEY17,ONE,NEWURNO,SP20
          CALL      RSPTMRG2
.
          CALL      RKPTMRG2
          BRANCH    OVRCD,NWCK9000
.
          MATCH     NEWURNO,PTMRNWUR
          GOTO      NWCK9500 IF EQUAL
.
.
. -----   Check if New U/R Number is Requested to be Changed -----
. -----   With a Status = 2 -----
.
          PACK      KEY17,TWO,NEWURNO,SP20
          CALL      RSPTMRG2
.
          CALL      RKPTMRG2
          BRANCH    OVRCD,NWCK9000
.
          MATCH     NEWURNO,PTMRNWUR
          GOTO      NWCK9500 IF EQUAL
.
.
. -----   Check if New U/R Number is Requested to be Changed -----
. -----   With a Status = 4 -----
.
          PACK      KEY17,FOUR,NEWURNO,SP20
          CALL      RSPTMRG2
.
          CALL      RKPTMRG2
          BRANCH    OVRCD,NWCK9000
.
          MATCH     NEWURNO,PTMRNWUR
          GOTO      NWCK9500 IF EQUAL
NWCK9000  DISPLAY   *P1:24,*EL,*B,"No Requests on File For U/R ":
                    *V2LON,NEWURNO,*HOFF,". ";
          CALL      HITENTER
          MOVE      TRUE,EXIT
          GOTO      NWCK9999
NWCK9500  PACK      KEY17,PTMRSTAT,OLDURNO,NEWURNO
          CALL      RDPTMRG1
.
          COMPARE   ZERO,OVRCD
          GOTO      NWCK9999 IF EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"Merger Not on File. "; 
          CALL      HITENTER
          MOVE      TRUE,EXIT
NWCK9999  RETURN
.----------------------------------------------------------------------
. Approve Merge on the Spot
.----------------------------------------------------------------------
APPNOW00  KEYIN     *P1:24,*EL,"Post Merge Approval Now (":
                    *V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS
          REP       UPPLOW,ANS
          MATCH     ANSN,ANS
          GOTO      APPNOW99 IF EQUAL
          MATCH     ANSY,ANS
          GOTO      APPNOW00 IF NOT EQUAL
.
          MOVE      "Y",ACTION
          CALL      UPDT0000
.
APPNOW99  RETURN
.
.**********************************************************************
.*                           UPDT0000                                 *
.*                 Update Status In Merger Record                     *
.**********************************************************************
.
UPDT0000  MOVE      CHOSINDX,FORM2
.
          MOVE      SP20,DIM20
          RESET     DIM20
          RESET     PTMRAPPR,FORM2
          BUMP      PTMRAPPR
          PACK      DIM20,PTMRAPPR,SP20
          RESET     DIM20
.
          SUB       ONE,FORM2
          RESET     PTMRAPPR,FORM2
          APPEND    ACTION,PTMRAPPR
          APPEND    DIM20,PTMRAPPR
          RESET     PTMRAPPR
UPDT8000  SCAN      ANSN,PTMRAPPR
          GOTO      UPDT8100 IF NOT EQUAL
          MOVE      FOUR,PTMRSTAT
          GOTO      UPDT9000
.
UPDT8100  MOVE      SP4,KEY4
          CALL      RDSHOSP1
UPDT8120  CALL      RDKHOSP1
          BRANCH    OVRCD,UPDT8150
.
          CMATCH    ANSI,PTHOACTV
          GOTO      UPDT8120 IF EQUAL            * ignore inactive hospitals
.
          RESET     PTMRAPPR,HOSINDX
          CMATCH    ANSY,PTMRAPPR
          GOTO      UPDT8120 IF EQUAL
.
          MOVE      ONE,PTMRSTAT
          GOTO      UPDT9000
 
.
. Request a merge on the national system if the merge has been approved by 
. all hospitals (dont update merge status on local system until the NHI have 
. approved the merge. Done in NHI10)
.-----------------------------------------------------------------------------
UPDT8150  CALL      GETNAT00
          BRANCH    EXIT,UPDT9999
          CALL      CONNECT
          CALL      MRGHCU
          CALL      DISCNNCT
          IF        OVRCD=1
            CALL      NZHISERR                    * display error message
          ENDIF
          GOTO      UPDT9999                    * exit out
.
.
UPDT9000  KEYIN     *P1:24,*EL,"Post Request to NHI/MWS (Y/N) ? ":
                    *V2LON,ANS
          REP       UPPLOW,ANS
          MATCH     ANSN,ANS
          GOTO      UPDT9010 IF EQUAL
          MATCH     ANSY,ANS
          GOTO      UPDT9000 IF NOT EQUAL
.
          CALL      GETNAT00
          BRANCH    EXIT,UPDT9999
          CALL      CONNECT
          CALL      MRGHCU
          CALL      DISCNNCT
          IF        OVRCD=1
            CALL      NZHISERR                    * display error message
          ENDIF
.
UPDT9010  LOAD      STATDESC,PTMRSTAT,STATUS1,STATUS2,STATUS3,STATUS4
          DISPLAY   *P23:15,*EL,*V2LON,STATDESC;
.
          COMPARE   PTMRSTAT,OLDSTAT
          GOTO      UPDT9900 IF EQUAL
.
. -----   If Status Changed Then Validate that There is no Other Merger -----
. -----   For these U/R Numbers -----
.
          MOVE      PTMROLUR,SAVEOLUR
          MOVE      PTMRNWUR,SAVENWUR
          MOVE      PTMRRDTE,SAVERDTE
          MOVE      PTMRRHOS,SAVERHOS
          MOVE      PTMRAPPR,SAVEAPPR
          MOVE      PTMRSTAT,SAVESTAT
.
          COMPARE   FOUR,OLDSTAT                 * Only Concerned With Status
          GOTO      UPDT9900 IF NOT EQUAL        * Change From 4 = Rejected
.
          PACK      KEY17,ONE,SAVEOLUR,SP20
          CALL      RSPTMRG1
          CALL      RKPTMRG1
          BRANCH    OVRCD,UPDT9100
.
          MATCH     SAVEOLUR,PTMROLUR
          GOTO      UPDT9100 IF NOT EQUAL
.
          MOVE      SAVEOLUR,DISPURNO
          GOTO      UPDT9899
UPDT9100  PACK      KEY17,TWO,SAVEOLUR,SP20
          CALL      RSPTMRG1
          CALL      RKPTMRG1
          BRANCH    OVRCD,UPDT9200
.
          MATCH     SAVEOLUR,PTMROLUR
          GOTO      UPDT9200 IF NOT EQUAL
.
          MOVE      SAVEOLUR,DISPURNO
          GOTO      UPDT9899
UPDT9200  PACK      KEY17,ONE,SAVENWUR,SP20
          CALL      RSPTMRG2
          CALL      RKPTMRG2
          BRANCH    OVRCD,UPDT9300
.
          MATCH     SAVENWUR,PTMRNWUR
          GOTO      UPDT9300 IF NOT EQUAL
.
          MOVE      SAVENWUR,DISPURNO
          GOTO      UPDT9899
UPDT9300  PACK      KEY17,TWO,SAVENWUR,SP20
          CALL      RSPTMRG2
          CALL      RKPTMRG2
          BRANCH    OVRCD,UPDT9400
.
          MATCH     SAVENWUR,PTMRNWUR
          GOTO      UPDT9400 IF NOT EQUAL
.
          MOVE      SAVENWUR,DISPURNO
          GOTO      UPDT9899
UPDT9400  PACK      KEY17,FOUR,SAVEOLUR,SAVENWUR
          CALL      RDPTMRG1
          MOVE      SAVEOLUR,PTMROLUR
          MOVE      SAVENWUR,PTMRNWUR
          MOVE      SAVERDTE,PTMRRDTE
          MOVE      SAVERHOS,PTMRRHOS
          MOVE      SAVEAPPR,PTMRAPPR
          MOVE      SAVESTAT,PTMRSTAT
.
          GOTO      UPDT9900
UPDT9899  DISPLAY   *P1:24,*EL,*B,"U/R Number ",*V2LON,DISPURNO,*HOFF:
                    "Already Involved in a Requested Merger. ";
          CALL      HITENTER
UPDT9900
          COMPARE   PTMRSTAT,OLDSTAT
          GOTO      UPDT9950 IF EQUAL
.
          PACK      PTMRSDTE,CCC,CYY,CMM,CDD     * Last Status Change Date
          REP       " 0",PTMRSDTE
          CLOCK     TIME,CTIMEIS
          MOVE      CTIMEIS,PTMRSTIM
          REP       " 0",PTMRSTIM
UPDT9950  CALL      UPPTMRG1
UPDT9999  RETURN
.------------------------------------------------------------
. Get National Details for Merge Request
.------------------------------------------------------------
GETNAT00  MOVE      NEWURNO,KEY8
          CALL      RDNHMAS2
          BRANCH    OVRCD,GETNAT90
          IF        CMERGEUR=1
            MOVE      NHMANMPI,NZIBALNO
          ELSE
            MOVE      NHMANMPI,NZIBNMPI
          ENDIF
          MOVE      OLDURNO,KEY8
          CALL      RDNHMAS2
          BRANCH    OVRCD,GETNAT90
          IF        CMERGEUR=1
            MOVE      NHMANMPI,NZIBNMPI
          ELSE
            MOVE      NHMANMPI,NZIBALNO
          ENDIF
          MOVE      ZERO,EXIT
          GOTO      GETNAT99
GETNAT90  DISPLAY   *P1:24,"National Patient Details Missing - ";
          CALL      HITENTER
          MOVE      ONE,EXIT
GETNAT99  RETURN
.**********************************************************************
.*                           NEWW0000                                 *
.*                      Determine New Status                          *
.**********************************************************************
.
NEWW0000  RESET     PTMRAPPR
          MOVE      PTMRAPPR,DIM20
          MOVE      CHOSINDX,FORM2
          SUB       ONE,FORM2
.
          RESET     DIM20,FORM2
          APPEND    ACTION,DIM20
          RESET     DIM20
.
          SCAN      ANSN,DIM20
          GOTO      NEWW1000 IF NOT EQUAL
.
          MOVE      FOUR,PTMRSTAT
          GOTO      NEWW9000
NEWW1000  MOVE      SP4,KEY4
          CALL      RDSHOSP1
NEWW2000  CALL      RDKHOSP1
          BRANCH    OVRCD,NEWW3000
.
          CMATCH    ANSI,PTHOACTV
          GOTO      NEWW2000 IF EQUAL            * ignore inactive hospitals
.
          RESET     PTMRAPPR,HOSINDX
          CMATCH    ANSY,PTMRAPPR
          GOTO      NEWW2000 IF EQUAL
.
          MOVE      ONE,PTMRSTAT
          GOTO      NEWW9000
NEWW3000  MOVE      TWO,PTMRSTAT
NEWW9000  LOAD      NWSTDESC,PTMRSTAT,STATUS1,STATUS2,STATUS3,STATUS4
          DISPLAY   *P23:19,*EL,*V2LON,NWSTDESC;
.
          RETURN
.
.**********************************************************************
.*                           RSCR0000                                 *
.*                      Report Menu Screen                            *
.**********************************************************************
.
RSCR0000  MOVE      FALSE,EXIT
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Requested merges":
                    *P1:6,*V2LON,"2",*HOFF," = Outstanding requested merges":
                    *P1:7,*V2LON,"3",*HOFF," = Approved merges":
                    *P1:8,*V2LON,"4",*HOFF," = Processed merges":
                    *P1:9,*V2LON,"5",*HOFF," = Rejected merges":
                    *P1:10,*V2LON,"6",*HOFF," = Last status changes";
RSCR1000  DISPLAY   *P1:12,*EL,"Select Item : _";
.
          KEYIN     *P15:12,*V2LON,COPTION;
.
          COMPARE   ZERO,COPTION
          GOTO      RSCR9000 IF EQUAL
.
          BRANCH    COPTION,RSCR2000,RSCR2000,RSCR2000,RSCR2000:
                    RSCR2000,RSCR2000
.
          BEEP
          GOTO      RSCR1000
RSCR2000  DISPLAY   *P1:14,"From Date : ":
                    *P1:15,"To Date   : ";
.
          GOTO      RSCR9999
RSCR9000  MOVE      TRUE,EXIT
RSCR9999  RETURN
.
.**********************************************************************
.*                           STRT0000                                 *
.*                        Keyin From Date                             *
.**********************************************************************
.
STRT0000  MOVE      FALSE,EXIT
.
          DISPLAY   *P13:14,*EL:
                    *P13:15,*EL;
.
          UNPACK    SP30,CDAY,CMON,CYEAR,STRTDATE
          MOVE      CCC,CCENT
.
          MOVE      TEN4,CVERT
          MOVE      TEN3,CCOL
          MOVE      CVERT,EVERT
          MOVE      "40",ECOL
          MOVE      ONE,CDEFDTE
          MOVE      ZERO,CHIGHLT
.
          CALL      KEYDATE
.
          MATCH     SP2,CDAY
          GOTO      STRT9000 IF EQUAL
.
          CMATCH    QUEST,CDAY
          GOTO      STRT0000 IF EQUAL
.
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
.
          MATCH     STRTDATE,TODAY
          GOTO      STRT9999 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Date May Not be Greater than Today's ":
                    "Date. ";
          CALL      HITENTER
          GOTO      STRT0000
STRT9000  MOVE      TRUE,EXIT
STRT9999  RETURN
.
.**********************************************************************
.*                           ENDD0000                                 *
.*                         Keyin To Date                              *
.**********************************************************************
.
ENDD0000  MOVE      FALSE,EXIT
          UNPACK    SP30,CDAY,CMON,CYEAR,ENDDATE
          MOVE      CCC,CCENT
.
          MOVE      TEN5,CVERT
          MOVE      TEN3,CCOL
          MOVE      CVERT,EVERT
          MOVE      "40",ECOL
          MOVE      ONE,CDEFDTE
          MOVE      ZERO,CHIGHLT
.
          CALL      KEYDATE
.
          MATCH     SP2,CDAY
          GOTO      ENDD9000 IF EQUAL
.
          CMATCH    QUEST,CDAY
          GOTO      ENDD0000 IF EQUAL
.
          PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY
.
          MATCH     ENDDATE,TODAY
          GOTO      ENDD3000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Date May Not be Greater than Today's ":
                    "Date. "; 
          CALL      HITENTER
          GOTO      ENDD0000
ENDD3000  MATCH     STRTDATE,ENDDATE
          GOTO      ENDD9999 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"To Date May Not be Less than From ":
                    "Date. ";
          CALL      HITENTER
          GOTO      ENDD0000
ENDD9000  MOVE      TRUE,EXIT
ENDD9999  RETURN
.
.**********************************************************************
.*                           EXTR0000                                 *
.*                 Extract Merges in Date Range                       *
.**********************************************************************
.
EXTR0000  LOAD      DATEDESC,COPTION,DATEDES1,DATEDES2,DATEDES3:
                                     DATEDES4,DATEDES5,DATEDES6
          MOVE      ZERO,CPAGENO
          CALL      HEAD0000
.
          PACK      KEY25,STRTDATE,SP30
.
          BRANCH    COPTION,EXTR0100,EXTR0100,EXTR0200:
                            EXTR0200,EXTR0200,EXTR0200
EXTR0100  CALL      RSPTMRG3
          GOTO      EXTR1000
EXTR0200  CALL      RSPTMRG4
EXTR1000  BRANCH    COPTION,EXTR1100,EXTR1100,EXTR1200:
                            EXTR1200,EXTR1200,EXTR1200
EXTR1100  CALL      RKPTMRG3
          BRANCH    OVRCD,EXTR9000
.
          MATCH     PTMRRDTE,ENDDATE
          GOTO      EXTR9000 IF LESS
          GOTO      EXTR2000
EXTR1200  CALL      RKPTMRG4
          BRANCH    OVRCD,EXTR9000
.
          MATCH     PTMRSDTE,ENDDATE
          GOTO      EXTR9000 IF LESS
EXTR2000  BRANCH    COPTION,EXTR2900,EXTR2100,EXTR2200:
                            EXTR2300,EXTR2400,EXTR2900
EXTR2100  COMPARE   ONE,PTMRSTAT
          GOTO      EXTR1000 IF NOT EQUAL
          GOTO      EXTR2900
EXTR2200  COMPARE   TWO,PTMRSTAT
          GOTO      EXTR1000 IF NOT EQUAL
          GOTO      EXTR2900
EXTR2300  COMPARE   THREE,PTMRSTAT
          GOTO      EXTR1000 IF NOT EQUAL
          GOTO      EXTR2900
EXTR2400  COMPARE   FOUR,PTMRSTAT
          GOTO      EXTR1000 IF NOT EQUAL
          GOTO      EXTR2900
EXTR2900  PACK      REQSTHOS,SP30,SP30
          PACK      KEY4,PTMRRHOS
          CALL      RDHOSP1
.
          MOVE      HOSDESC,REQSTHOS
.
          BRANCH    COPTION,EXTR3000,EXTR3000,EXTR4000:
                            EXTR4000,EXTR4000,EXTR4000
EXTR3000  UNPACK    SP30,CDAY,CMON,CYEAR,CCENT
          UNPACK    PTMRRDTE,CCENT,CYEAR,CMON,CDAY
          GOTO      EXTR5000
EXTR4000  UNPACK    SP30,CDAY,CMON,CYEAR,CCENT
.
          BRANCH    COPTION,EXTR4100,EXTR4100,EXTR4200:
                            EXTR4200,EXTR4200,EXTR4200
EXTR4100  UNPACK    PTMRRDTE,CCENT,CYEAR,CMON,CDAY
          GOTO      EXTR5000
.
EXTR4200  UNPACK    PTMRSDTE,CCENT,CYEAR,CMON,CDAY
.
EXTR5000  LOAD      DIM10,PTMRSTAT,PRSTAT1,PRSTAT2,PRSTAT3,PRSTAT4
.
. ------- Actual print -------
.
          DISPLAY   *P1:24,*EL,"Printing : ",*V2LON,PTMROLUR
          COMPARE   "60",CLNO
          GOTO      EXTR6000 IF LESS
          CALL      HEAD0000
.
EXTR6000  PACK      NAMELINE WITH SP20,SP30
          MOVE      PTMROLUR,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF EXTR7000
.
          MOVE      SP30 TO STRTNAME
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT             * format is Surname,Title Given
          CALL      PACNAME
          MOVE      PACFNAME,NAMELINE       * starting name
.
EXTR7000  MOVE      NAMELINE,DIM27
.
          PRINT     *1,PIPE,PTMROLUR,*10,PIPE,DIM27;
.
          PACK      NAMELINE WITH SP20,SP30
          MOVE      PTMRNWUR,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF EXTR8000
.
          MOVE      SP30 TO STRTNAME
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT             * format is Surname,Title Given
          CALL      PACNAME
          MOVE      PACFNAME,NAMELINE       * starting name
.
EXTR8000  MOVE      NAMELINE,DIM27
.
          PRINT     *38,PIPE,PTMRNWUR,*47,PIPE,DIM27:
                    *74,PIPE,REQSTHOS:
                    *109,PIPE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR:
                    *120,PIPE,SP1,DIM10,*132,PIPE:
                    *N,*1,PIPE,*10,PIPE,*38,PIPE,*47,PIPE:
                    *74,PIPE;
.
          CALL      RESP0000                     * Get Hospital Responses
.
          PRINT     ASK,UNDLN65,UNDLN65,ASK
          ADD       ONE,CLNO
.
          GOTO      EXTR1000
EXTR9000  PRINT     *N,*N,"****   END OF REPORT  ****"
.
          RETURN
.
.**********************************************************************
.*                           HEAD0000                                 *
.*                     Print Report Heading                           *
.**********************************************************************
.
HEAD0000  CLOCK     TIME,CTIMEIS
          MOVE      ONE,CNOUNDLN
.
          CALL      HEAD132
.          PRINT     *F,*1,PRGID,*40,CNAME,*118,"DATE : ",CDATE:
.                    *N,VERSION,*40,PRGNAM,*118,"TIME : ",CTIMEIS:
.                    *N,*118,"PAGE :      ",CPAGENO
.
          MOVE      SP30,HEADDESC
          LOAD      HEADDESC,COPTION,DATEHED1,DATEHED2,DATEHED3:
                                     DATEHED4,DATEHED5,DATEHED6
          UNPACK    SP30,SDAY,SMON,SYEAR,SCENT,XDAY,XMON,XYEAR,XCENT
          UNPACK    STRTDATE,SCENT,SYEAR,SMON,SDAY
          UNPACK    ENDDATE,XCENT,XYEAR,XMON,XDAY
.
          PRINT     *40,HEADDESC," for ",SDAY,SLASH,SMON,SLASH,SCENT,SYEAR:
                    " to ",XDAY,SLASH,XMON,SLASH,XCENT,XYEAR:
                    *N,*N,ASK,UNDLN65,UNDLN65,ASK:
                    *N,*1,PIPE," Invalid",*10,PIPE,"           Name":
                       *38,PIPE," Valid  ",*47,PIPE,"           Name":
                       *74,PIPE,"       Requesting Hospital/":
                       *109,PIPE,DATEDESC,*120,PIPE,"  Status":
                       *132,PIPE:
                    *N,*1,PIPE,"   U/R  ",*10,PIPE:
                       *38,PIPE,"  U/R   ",*47,PIPE:
                       *74,PIPE,"       Hospital Responses":
                       *109,PIPE,"   Date",*120,PIPE:
                       *132,PIPE:
                    *N,ASK,UNDLN65,UNDLN65,ASK
          ADD       SIX,CLNO
          RETURN
.
.**********************************************************************
.*                           RESP0000                                 *
.*                   Get Hospital's Responses                         *
.**********************************************************************
.
RESP0000  MOVE      ZERO,RESCOUNT
          MOVE      SP4,KEY4
          CALL      RDSHOSP1
RESP0100  CALL      RDKHOSP1
          BRANCH    OVRCD,RESP9000          * Pipe at Pos 132
.
          CMATCH    ANSI,PTHOACTV
          GOTO      RESP0100 IF EQUAL       * ignore inactive hospitals
.
          ADD       ONE,RESCOUNT
          RESET     PTMRAPPR,HOSINDX
.
          MOVE      PTMRAPPR,ANS
          REP       "Y1N2 3",ANS
.
          MOVE      ANS,FORM1
          LOAD      RESPDESC,FORM1,RESPYES,RESPNO,RESPUNKN
.
          COMPARE   FIVE,RESCOUNT
          CALL      LINE0000 IF NOT LESS    * New Line for Responses
.
          PRINT     HOSCODE,"= ",RESPDESC,SP2;
.
          GOTO      RESP0100
.
RESP9000  PRINT     *109,PIPE,*120,PIPE:
                    *132,PIPE
          ADD       ONE,CLNO
RESP9999  RETURN
.
.**********************************************************************
.*                           LINE0000                                 *
.*                  New Line in Response Print                        *
.**********************************************************************
.
LINE0000  PRINT     *132,PIPE:
                    *N,*1,PIPE,*10,PIPE,*38,PIPE,*47,PIPE,*74,PIPE;

          MOVE      ONE,RESCOUNT
          ADD       ONE,CLNO
          RETURN
.
.**********************************************************************
.*                           NAME0000                                 *
.*                     Format Patient's Name                          *
.**********************************************************************
.
NAME0000  CLEAR     NAMELINE
          RESET     PTITL
          MATCH     SP4,PTITL                     * test title for spaces
          GOTO      NAME2000 IF EQUAL
.
NAME0500  CMATCH    SP1,PTITL
          GOTO      NAME1000 IF EQUAL
          MOVE      PTITL,ANS                    * append title to NAMELINE
          APPEND    ANS,NAMELINE
          BUMP      PTITL
          GOTO      NAME1000 IF EOS
          GOTO      NAME0500
.
NAME1000  APPEND    SP1,NAMELINE
          MATCH     SP20,PGNAME                  * test given name for
          GOTO      NAME1700 IF EQUAL            *    spaces
.
NAME1500  APPEND    PGNAME,NAMELINE
          APPEND    SP1,NAMELINE                 * append given name to
.                                                   *    NAMELINE
NAME1700  APPEND    PSNAME,NAMELINE
          RESET     NAMELINE                     * append surname to
          MOVE      NAMELINE,LINE                *    NAMELINE
          CALL      COMP0000                     * compress blanks
          CALL      CASE0000                     * convert to lower case
          MOVE      LINE,NAMELINE
          GOTO      NAME9999
.
NAME2000  MATCH     SP20,PGNAME                  * test given name for
          GOTO      NAME1500 IF NOT EQUAL        *     spaces
          GOTO      NAME1700
.
NAME9999  RETURN
+
.**********************************************************************
.*                              COMP0000                              *
.*       Routine to compress excessive blanks in LINE                 *
.**********************************************************************
.
COMP0000  MOVE      LINE,DIM42
          PACK      TEMP42,SP20,SP20,SP10,SP5
          MATCH     TEMP42,DIM42                 * test line for spaces
          GOTO      COMP9999 IF EQUAL
          CLEAR     LINE
.
COMP1000  CMATCH    SP1,DIM42
          GOTO      COMP5000 IF NOT EQUAL        * compress leading blanks
          BUMP      DIM42
          GOTO      COMP1000 IF NOT EOS
          GOTO      COMP8000
.
COMP5000  MOVE      DIM42,TEMP42
          MOVE      TEMP42,DIM42
          ENDSET    DIM42
.
COMP5100  CMATCH    SP1,DIM42
          GOTO      COMP7000 IF NOT EQUAL        * compress trailing blanks
          BUMP      DIM42,-1
          GOTO      COMP5100
.
COMP7000  LENSET    DIM42
          RESET     DIM42
.
COMP7100  MOVE      DIM42,ANS
          APPEND    ANS,LINE
          BUMP      DIM42                        * compress multiple blanks
          GOTO      COMP8000 IF EOS              *   from in-between words
          CMATCH    SP1,DIM42
          GOTO      COMP7100 IF NOT EQUAL
          MOVE      DIM42,ANS
          APPEND    ANS,LINE
.
COMP7200  BUMP      DIM42
          CMATCH    SP1,DIM42
          GOTO      COMP7100 IF NOT EQUAL
          GOTO      COMP7200
.
COMP8000  RESET     LINE
          GOTO      COMP9999
.
COMP9999  RETURN
+
.**********************************************************************
.*                             CASE0000                               *
.*  Routine to convert all except first character of each word to     *
.*        lower case                                                  *
.**********************************************************************
CASE0000  MOVE      ONE,FIRSTCH
          RESET     LINE
.
CASE1000  CMATCH    SP1,LINE
          GOTO      CASE5000 IF EQUAL            * match char to space
          BRANCH    FIRSTCH,CASE2000
          MOVE      LINE,ANS
          SCAN      ANS,UPPLOW                   * test to see if char is
          RESET     UPPLOW
          GOTO      CASE1100 IF EQUAL            *   a letter
          MOVE      ONE,FIRSTCH
          GOTO      CASE3000                     * any char but a letter
          *         found
.
CASE1100  OR        040,LINE                     * actual conversion to
          *         lower case (believe it
          *         or not!)
.
CASE2000  MOVE      ZERO,FIRSTCH                 * not on first char any
.                                                   *   more
CASE3000  BUMP      LINE
          GOTO      CASE1000 IF NOT EOS          * move to next char
          RESET     LINE                         * end of line reached
          GOTO      CASE9999
.
CASE5000  MOVE      ONE,FIRSTCH                  * first char was a space
          GOTO      CASE3000
.
CASE9999  RETURN
+
.
.**********************************************************************
.*                            REDISPS                                 *
.*                   Redisplay Screen After "?"                       *
.**********************************************************************
.
REDISPS   CALL      USCR0000
.
          DISPLAY   *P18:4,*V2LON,OLDURNO;
.
.---------- redisplay old u/r details -----
.
          DISPLAY   *P30:4,"Name : ",*V2LON,OLDNAME,*HOFF:
                    *P30:5,"DOB  : ",*V2LON,OLDDOB,*HOFF:
                    *P50:5,"Sex : ",*V2LON,OLDSEX;
.
.
          RETURN
OPEN0000
CLOS0000
GETSVAR   RETURN
+
.**********************************************************************
.*              I/O INCLUDES FOR FILES AND GENERAL ROUTINES           *
.**********************************************************************
          INC       STD001IO
          INC       CALCAGE
          INC       PATSNDX
          INC       PATSNX2
          INC       PATCOMN1
.
          INC       PATALRIO/INC
          INC       PATHOSIO/INC
          INC       PATMRGIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PMSPX2IO/INC
          INC       PATPR1IO/INC
          INC       PATGSRIO/INC
.
          INC       OUTPREIO/INC
          INC       BOKRC1IO/INC
.
          INC       PATCODIO/INC
          INC       PATDNRIO/INC
......    INC       PATMWSIO/INC
          INC       PATSDNIO/INC
          INC       PATSMWIO/INC
          INC       NHIMASIO/INC
          INC       PATNIDIO/INC
          INC       NZHISIIO
          INC       NZHISIUP
          INC       NZIBSRCH
          INC       AGECHK
          INC       SEXDSCIO
          INC       CLPATMAS
.

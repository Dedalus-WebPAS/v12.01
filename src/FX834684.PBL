.***************************************************************************
.* System    :   Inpatients                                                *
.* Program   :   FX834684                                                  *
.* Desc      :   Move data for Comment Type '007' & '011' from table       *
.*               VISCMTFD.INC to a new table VISUCMFD.INC                  *
.***************************************************************************
.* Date      :   13/07/2017                                                *
.* Author    :   Richa Phogat                                              *
.* Function  :   This program will add records to new table visucmaf       *
.* Modifications  :                                                        *
.*        V10.11.03 22/08/2017  Richa Phogat  CAR 0834684                  *
.*                  Update program to correctly save count value 'VSUCUCNT'*
.*        V10.11.02 02/08/2017  Richa Phogat  CAR 0834684                  *
.*                  Updated to display message on completion               *
.*        V10.11.01 13/07/2017  Richa Phogat  CAR 0834684                  *
.*                  Created program                                        *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       VISCMTFD/INC
          INC       VISUCMFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
MINUTES   DIM       2
HOURS     DIM       2
SECONDS   DIM       2
DIM6      DIM       6
DISNUM    FORM      4
RECNUM    FORM      8
RECDEL    FORM      8
.
PRGID     INIT      "FX834684"
PRGNAM    INIT      "Upload new Transport table   "
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              MAIN0000                                  *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN0200       * proceed with clean up
.
MAIN0200  CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN0300:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN0300  CALL      PROC0000               * process
          CALL      DELE0000               * Delete records copied to new table
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: MAIN0000  *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"viscmtaf";
          OPEN      VISCMTA1,"viscmtaf"
.
          DISPLAY   *P64:24,"visucmaf";
          OPEN      VISUCMA1,"visucmaf"
.
          MOVE      ZERO,RECNUM
          MOVE      ZERO,DISNUM
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: MAIN0000  *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.* Copy data from existing table 'VISCMTFD' to new table 'VISUCMFD'          *
.*****************************************************************************
PROC0000  PACK      KEY14,SP70
          CALL      RSVSCMT1
PROC0100  CALL      RKVSCMT1
          BRANCH    OVRCD,PROC9999
.
          MATCH     "007",VSCTTYPE
          GOTO      PROC0200 IF EQUAL
.
          MATCH     "011",VSCTTYPE
          GOTO      PROC0200 IF EQUAL
.
          GOTO      PROC0100
.
PROC0200  MOVE      VSCTVIST,VSUCVISN
          MOVE      VSCTTYPE,VSUCCTYP                  * Move Comment Type
          PACK      VSUCUCNT,SP1,SP1,ONE
          MOVE      VSCTLINE,VSUCLNUM                  * Move Line No
          MOVE      VSCTDATA,VSUCLINE                  * Line
          UNPACK    VSCTUKEY,VSUCUDAT,DIM6,VSUCUUID  
          UNPACK    DIM6,HOURS,MINUTES,SECONDS
          PACK      VSUCUTIM,HOURS,COLON,MINUTES,COLON,SECONDS  
.
          PACK      KEY17,VSUCVISN,VSUCCTYP,VSUCUCNT,VSUCLNUM
          CALL      RAVSUCM1
          BRANCH    OVRCD,PROC0300
          GOTO      PROC0100
.
PROC0300  CALL      WRVSUCM1                           * Add record to new table
          ADD       ONE,RECNUM
          ADD       ONE,DISNUM                         * Display counter
.
.         IF        DISNUM = 5000  | RECNUM = 1
.           MOVE      ZERO,DISNUM
            DISPLAY   *P1:20,"Records Copied : ":
                      *P15:20,*V2LON,RECNUM;
.         ENDIF

          GOTO      PROC0100
.
PROC9999  RETURN
+
.*****************************************************************************
.*                               DELE0000              Called by: MAIN0000   *
.* Delete data from existing table 'VISCMTFD' after copying to 'VISUCMFD'    *
.*****************************************************************************
DELE0000  MOVE      ZERO,RECDEL
          PACK      KEY17,SP70
          CALL      RSVSUCM1
DELE0100  CALL      RKVSUCM1
          BRANCH    OVRCD,DELE0900
.
          PACK      KEY14,VSUCVISN,VSUCCTYP,VSUCLNUM
          CALL      DEVSCMT1                     * Delete record from old table
          ADD       ONE,RECDEL                   * Increment deletion count
          GOTO      DELE0100
.
DELE0900  MOVE      ONE,EXIT
          DISPLAY   *P1:22,"Records Deleted  : ":
                    *P15:22,*V2LON,RECNUM,*HOFF:
                    *P1:24,*EL,"Fixit Completed. ";
          CALL      HITENTER
.
DELE9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       VISCMTIO/INC
          INC       VISUCMIO/INC


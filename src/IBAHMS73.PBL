.*******************************************************************************
.* System         : Waiting List                                               *
.* Program        : IBAHMS73.PBL                                               *
.* Name           : AN-SNAP Functional Assessment 2 Report                     *
.*******************************************************************************
.* Date           : 30/05/2012                                                 *
.* Author         : Patrick Adair                                              *
.* Function       : AN-SNAP Functional Assessment 2 Report                     *
.*                  Report showing the number of Patients who do not have      *
.*                  Discharge FIM details within N days of their Discharge     *
.* Modifications  :                                                            *
.*                                                                             *
.* V10.08.02  07/09/2016 Jill Parkinson TSK 0304920                            *
.*                       Changed to use ddest and dstat in CHKD00000          r*
.* V10.08.01  06/09/2016 Jill Parkinson TSK 0304920                            *
.*                       Added output of total rehab excluding deaths, transfer*
.*                       and care type changes                                 *
.* V10.04.02  13/06/2014 Steve Armstrong    CAR 301639                         *
.*                       Moved call to TFILENAM into INIT0000                  *
.* V10.04.01  29/05/2013 Don Nguyen         CAR 299692                         *
.*                       Modified PRNT0000 to work with PTCNDRSM; skip patient *
.*                       if TCINDC5 = 1,3,4,5,6 or 8.                          *
.*                       Added RDAWORK1 to stop I*D.                           *
.*******************************************************************************
.* V10.03.07  12/09/2013 Don Nguyen         CAR 291209                         *
.*                       Added 2 new columns (Discharge Destination & Hospital)*
.* V10.03.06  28/08/2013 Jill Parkinson     CAR 280865                         *
.*                       Corrected to not include 'within x days' twice        *
.* V10.03.05  26/08/2013 Peter Vela         CAR 290522                         *
.*                       Added Number of Days keyin and validation             *
.* V10.03.04  20/08/2013 Jill Parkinson     CAR 280865                         *
.*                       Added GOTO EXTR6000 in EXTR5000 to stop counting twice*
.* V10.03.03  19/12/2012 Patrick Adair                              CAR 261630 *
.*                       Removed port number from temp file name               *
.* V10.03.02  23/11/2012 Patrick Adair                              CAR 275451 *
.*                       Changed to include/exclude day cases                  *
.*                       Corrected report heading                   CAR 275121 *
.* V10.03.01  30/10/2012 Jill Parkinson CAR 274607                             *
.*                       Modified to print adm's without FIM within 7 days     *
.* V10.03.00  11/07/2012 Patrick Adair                              CAR 257776 *
.*                       Created Report                                        *
.*******************************************************************************
.
          INC       STD001FD                     * Standard Include
.
          INC       IBACONFD/INC                 * Control File
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       PATCODFD/INC                 * Category Codes
          INC       PATCONFD/INC                 * Control File
          INC       PATDADFD/INC                 * Transfer Destination
          INC       PATDSCFD/INC                 * Patient Discharge File
          INC       PATFIMFD/INC                 * Patient FIM Details File
          INC       PATHSPFD/INC                 * Hospital File
          INC       PATMI1FD/INC                 * Patient Admission File
          INC       PATVADFD/INC                 * Transfer Source Code
          INC       PMSVX1FD/INC                 * Patient Visit Ext File
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
.-------------------------------------------------------------------------------
.         temporary file  - Patients without Discharge FIM Details
.
WORK1     IFILE     SQL, FIXED=44, KEY="D1-8,9-16"
.
.Name     Type     Size    Physical   Start       Description
.----     ----     ----    --------   -----       -----------
XXXXURNO  DIM       8         8         1         Patient U/R Number
XXXADMNO  DIM       8         8         9         Patient Visit Number
XXXADATE  DIM       10        10       17         Date of Admission
XXXDDATE  DIM       10        10       27         Date of Discharge
XXXDSTAT  DIM       3         3        37         Discharge Status (Cat D)
XXXDDEST  DIM       3         3        40         Discharge Destination (Cat DD)
XXXNOTW7  DIM       1         1        43         Yes - but Not within N days=Y
.End of record                         44
.
.-------------------------------------------------------------------------------
.                           Variables
.-------------------------------------------------------------------------------
CARECATG  DIM       2                            * category for care type
CMDLINE   DIM       50                           * Command Line
DATEFROM  DIM       8                            * Input From Date
DATETO    DIM       8                            * Input To Date
DAYCNT    FORM      2                            * Number of Days Diffnce
DAYCNTKN  FORM      2                            * Number of Days keyin
DISCDEST  DIM       20                           * Discharge Destination desc
DISCHOSP  DIM       30                           * Discharge Hospital desc
FNAME1    DIM       8                            * temporary file name
HOSPAPPR  DIM       10                           * Hospital Approval Number
HOSPCODE  DIM       3                            * Hospital Code
HOSPNAME  DIM       40                           * Hospital Name
INCLDAYC  DIM       1                            * Include Day Cases
.                                                  0 = No
.                                                  1 = Yes
TODAY     DIM       8                            * Today's Date
TOTNOFIM  FORM      6                            * No of Patients without
.                                                    Discharge FIM Details
TOTNOF7D  FORM      6                            * No of Pats w/out Dsc FIM
.                                                    <= 7 days of Discharge
TOTRHDSC  FORM      6                            * Total Rehab Discharges
TOTRHEXC  FORM      6                            * Total Rehab Discharges (
.                                                 excluding suspended rehab)
.-------------------------------------------------------------------------------
.                            Constants
.-------------------------------------------------------------------------------
CATA      INIT      "A "
CATCC     INIT      "CC"
CATD      INIT      "D "
CATDD     INIT      "DD"
ONEINIT   INIT      "1"
.
PRGID     INIT      "IBAHMS73"
PRGNAM    INIT      "Functional Assessment 2"
VERSION   INIT      "V12.01.00"
.
. ******************************************************************************
. *                                  MAIN0000                                  *
. *                                                                            *
. * Process Flow Control                                                       *
. ******************************************************************************
.
MAIN0000  CALL      INIT0000                 * Initialisation - begin descent
.
MAIN1000  IF        IBCNMHOS=1
            DISPLAY   *P25:6,*EL:
                      *P25:7,*EL:
                      *P25:8,*EL:
                      *P25:9,*EL:
                      *P25:10,*EF;
            CALL      GHOS0000                   * Get Multi Hospital Id
            BRANCH    EXIT,MAIN9999
          ELSE
            DISPLAY   *P1:6,*EL:
                      *P25:7,*EL:
                      *P25:8,*EL:
                      *P25:9,*EF:
                      *P25:10,*EF;
            PACK      HOSPAPPR,CAPPRVNO,SP70 * Hospital Approval Number
            PACK      HOSPNAME,CONAME,SP70   * Hospital Name
          ENDIF
.
          CALL      GSTD0000                 * Keyin the start date
          BRANCH    EXIT,MAIN9999            * Exit if nothing input
.
          CALL      GEND0000                 * Keyin the ending date
          BRANCH    EXIT,MAIN1000            * restart if nothing input
.
          CALL      GDAY0000                     * Keyin Include day cases
          BRANCH    EXIT,MAIN1000                * Nothing input
.
          CALL      GNDY0000                 * Keyin the Number of Days
          BRANCH    EXIT,MAIN1000            * Nothing input
.
          CALL      GCON0000                 * OK to continue
          BRANCH    EXIT,MAIN1000            * restart if no selected
.
          CALL      CRET0000                 * Create temporary files
.
          CALL      EXTR0000                 * Extract Rehab Discharges without
          BRANCH    EXIT,MAIN8000            * Dischg FIM Details - exit if none
.
          CALL      PRNT0000                 * Print the report
.
MAIN8000  CALL      KILL0000                 * Delete temporary files
.
          GOTO      MAIN1000                 * Next run
.
MAIN9999  CHAIN     PGM                      * Return from the nine circles
.
. ******************************************************************************
. *                                 INIT0000              Called by : MAIN0000 *
. *                                                                            *
. * Initialisation procedure                                                   *
. ******************************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"patdadaf";
          OPEN      PATDADA1,"patdadaf"
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"patfimaf";
          OPEN      PATFIMA1,"patfimaf"
.
          DISPLAY   *P64:24,"patvadaf";
          OPEN      PATVADA1,"patvadaf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS        * 1 = Multi Hospital
.
          READ      CONTROLF,TEN;*79,CAPPRVNO         * Hospital Approval Number
.
          READ      CONTROLF,TWO;*4,CONAME            * Company Name
.
          READ      CONTROLF,TWENTY1;*45,PTCNDRSM     * Separation mode in PMS99
.                                                     *   1 = DSTAT, 2 = DDEST
.
          CALL      IBACLOCK
          PACK      TODAY,CCC,CYY,CMM,CDD
          REP       " 0",TODAY
.
          DISPLAY   *P1:6,*EL,"Hospital Id           : ";
          DISPLAY   *P1:7,"From Date             : ":
                    *P1:8,"To   Date             : ":
                    *P1:9,"Include Day Cases Y/N : ":
                    *P1:10,"Number of Days        : ";
.
          MOVE      ZERO,TOTNOFIM                * Init no Discharge FIM
          MOVE      ZERO,TOTNOF7D                * Init no Dsc FIM > 7 Days
          MOVE      ZERO,TOTRHDSC                * Init no of Rehab Discharges
          MOVE      ZERO,TOTRHEXC                * Init no of Rehab Discharges
          MOVE      ZERO,INCLDAYC                * Include Day Cases Flag
          MOVE      SEVENTY,CLNO                 * Init Line Count
          MOVE      ZERO,CPAGENO                 * Init Page Count
          CLOCK     TIME,CTIMEIS                 * Get the starting time
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAME1
.
INIT9999  RETURN
.
.*******************************************************************************
.*                                  GHOS0000               Called by: MAIN0000 *
.*                                                                             *
.* Get Hospital Id                                                             *
.*******************************************************************************
GHOS0000  MOVE      ZERO,EXIT
          DISPLAY   *P25:6,*EL:
                    *P25:7,*EL:
                    *P25:8,*EL:
                    *P25:9,*EL:
                    *P25:10,*EF;
.
          MOVE      SP70,HOSPCODE
          MOVE      SP70,PTHSHOSP
          MOVE      TWENTY5,ECOL                    * Column of key in
          MOVE      SIX,EVERT                    * Row of key in
          MOVE      SP3,CKYIDEF3                 * Default value
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,GHOS98000,GHOS98000
.
          PACK      HOSPCODE,KEY3,SP70           * Hospital Code
          PACK      HOSPAPPR,PTHSAPPR,SP70       * Hospital Approval Number
          PACK      HOSPNAME,PTHSNAME,SP70       * Hospital Name
.
          DISPLAY   *P25:6,*V2LON,PTHSNAME;
          MOVE      ZERO,EXIT
          GOTO      GHOS9999
.
GHOS98000  MOVE     ONE,EXIT
.
GHOS9999  RETURN
.
. ******************************************************************************
. *                                  GSTD0000             Called by : MAIN0000 *
. *                                                                            *
. * Get the start  date of the report                                          *
. *            EXIT = 1   if a nothing was input                               *
. ******************************************************************************
.
GSTD0000  MOVE      ZERO,EXIT
.
          MOVE      SEVEN,CVERT                  * Line for keyin of from date
          MOVE      TWENTY5,CCOL                 * Col for keyin of from date
          MOVE      CVERT,EVERT                  * Col for error message
          MOVE      "40",ECOL                    * Col for error message
          MOVE      ZERO,CHIGHLT                 * No highlight
          MOVE      ONE,CDEFDTE
.
          UNPACK    SP6,CDAY,CMON,CYEAR          * Initialise date
          MOVE      SP8,DATEFROM
          MOVE      CCC,CCENT                    * Initialise century
.
          CALL      KEYDATE                      * Key in the date
          BRANCH    OVRCD,GSTD9000
.
          PACK      DATEFROM,CCENT,CYEAR,CMON,CDAY
          REP       " 0",DATEFROM
          MATCH     DATEFROM,TODAY
          GOTO      GSTD9999 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Date May Not be Greater than Today's ":
                    "Date.  ";
          CALL      HITENTER
          GOTO      GSTD0000
.
GSTD9000  MOVE      ONE,EXIT
.
GSTD9999  RETURN
.
. ******************************************************************************
. *                                  GEND0000             Called by : MAIN0000 *
. *                                                                            *
. * Get the end date of the report                                             *
. *            EXIT = 1   if a nothing was input                               *
. ******************************************************************************
.
GEND0000  MOVE      ZERO,EXIT
.
          MOVE      EIGHT,CVERT                  * Line for keyin of to date
          MOVE      TWENTY5,CCOL                 * Col for keyin of to date
          MOVE      CVERT,EVERT                  * Col for error message
          MOVE      "40",ECOL                    * Col for error message
          MOVE      ZERO,CHIGHLT                 * No highlight
          MOVE      ONE,CDEFDTE
.
          MOVE      SP8,DATETO
.
          CALL      KEYDATE                      * Key in the date
          BRANCH    OVRCD,GEND9000
.
          PACK      DATETO,CCENT,CYEAR,CMON,CDAY
          REP       " 0",DATETO
          MATCH     DATETO,TODAY
          GOTO      GEND2000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Date May Not be Greater than Today's ":
                    "Date.  ";
          CALL      HITENTER
          GOTO      GEND0000
.
GEND2000  MATCH     DATEFROM,DATETO
          GOTO      GEND9999 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"To Date May Not be Less than From ":
                    "Date.  ";
          CALL      HITENTER
          GOTO      GEND0000
.
GEND9000  MOVE      ONE,EXIT
.
GEND9999  RETURN
.
. ******************************************************************************
. *                                  GDAY0000             Called by : MAIN0000 *
. *                                                                            *
. * Include/Exclude Day cases from report?                                     *
. ******************************************************************************
.
GDAY0000  MOVE      ZERO,EXIT
.
GDAY1000  KEYIN     *P25:9,"_",*P25:9,*V2LON,ANS;
.
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
.
          MATCH     ANS,ANSY
          IF        @EQUAL
            MOVE      ONE,INCLDAYC
            GOTO      GDAY9999
          ENDIF
.
          MATCH     ANS,ANSN
          IF        @EQUAL
            MOVE      ZERO,INCLDAYC
            GOTO      GDAY9999
          ENDIF
.
          BEEP
          GOTO      GDAY1000
.
GDAY9999  RETURN
.
. ******************************************************************************
. *                                  GNDY0000             Called by : MAIN0000 *
. *                                                                            *
. ******************************************************************************
.
GNDY0000  MOVE      ZERO,EXIT
          MOVE      ZERO,DAYCNTKN
.
GNDY1000  KEYIN     *P25:10,"__",*P25:10,*V2LON,DAYCNTKN;
.
.         COMPARE   ZERO,DAYCNTKN
.         GOTO      GNDY9999 IF NOT EQUAL
.
.         MOVE      ONE,EXIT
.
GNDY9999  RETURN
.
. ******************************************************************************
. *                                  GCON0000             Called by : MAIN0000 *
. *                                                                            *
. * Ok. to continue (Y/N)                                                      *
. ******************************************************************************
.
GCON0000  MOVE      ZERO,EXIT
          DISPLAY   *P1:24,*EL,"Ok to Continue (":
                    *V2LON,ANSY,*HOFF,SLASH,*V2LON,ANSN,*HOFF,") ? ";
.
GCON1000  KEYIN     *P24:24,"_",*P24:24,*V2LON,ANS;
.
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
.
          MATCH     ANS,ANSY
          GOTO      GCON9999 IF EQUAL
.
          MATCH     ANS,ANSN
          GOTO      GCON9000 IF EQUAL
.
          BEEP
          GOTO      GCON1000
.
GCON9000  MOVE      ONE,EXIT
.
GCON9999  RETURN
.
. ******************************************************************************
. *                                  CRET0000             Called by : MAIN0000 *
. *                                                                            *
. * Create the temporary files                                                 *
. ******************************************************************************
.
CRET0000  CALL      KILL0000                     * Remove any existing files
.
          CLEAR     CMDLINE                      * Create temporary file
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME1,CMDLINE
          APPEND    " 44 D1-8,9-16",CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
          OPEN      WORK1,FNAME1                 * Open temporary file
.
CRET9999  RETURN
.
. ******************************************************************************
. *                                  KILL0000             Called by : MAIN0000 *
. *                                                                   CRET0000 *
. * Delete the temporary index files                                           *
. ******************************************************************************
.
KILL0000  CLOSE     WORK1                        * ensure temporary file closed
.
          CLEAR     CMDLINE                      * remove temporary file
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME1,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
KILL9999  RETURN
.
. ******************************************************************************
. *                                  EXTR0000             Called by : MAIN0000 *
. *                                                                            *
. * Process all Rehab discharges for the period and extract all patients       *
. * without Discharge FIM Details within N days of their Discharge             *
. ******************************************************************************
.
EXTR0000  MOVE      ZERO,EXIT
          DISPLAY   *P1:24,*EL,*P10:24,"Adm. No :":
                    *P58:24,"Scanning:";
.
          READ      CONTROLF,SEVENTY9;*80,PTCNQLDB
          READ      CONTROLF,EIGHTY;*250,PTCNHDPS
          IF        (PTCNHDPS=4 & PTCNQLDB=6)
            MOVE      CATA,CARECATG
          ELSE
            MOVE      CATCC,CARECATG
          ENDIF
.
          PACK      KEY16,DATEFROM               * Read thru Discharge File
          CALL      RDSDSCH2
EXTR1000  CALL      RDKDSCH2
          BRANCH    OVRCD,EXTR9000               * end of records reached
.
          MATCH     DDATE,DATETO
          GOTO      EXTR9000 IF LESS             * end of records reached
.
          PACK      KEY8,DDADMNO,SP70            * Get the Admission Record
          CALL      RDMISS1
          BRANCH    OVRCD,EXTR1000
.
          IF        IBCNMHOS=1
            MATCH     PMVXMHOS,HOSPCODE          * check visit for hospital
            GOTO      EXTR1000 IF NOT EQUAL
          ENDIF
.
          MATCH     "A ",CARECATG                * Read relevant category type
          IF        @EQUAL
            PACK      KEY5,CARECATG,ATYPE,SP70
          ELSE
            PACK      KEY5,CARECATG,ACARE,SP70
          ENDIF
.
          CALL      RDCODE1
          BRANCH    OVRCD,EXTR1000
.
          MATCH     "A ",CARECATG                * Check if Rehab Visit
          IF        @EQUAL
            MATCH   "9",TCINDC8
            GOTO    EXTR2000 IF EQUAL
          ELSE
            MATCH   "2",TCINDC6
            GOTO    EXTR2000 IF EQUAL
          ENDIF
.
          GOTO      EXTR1000                     * Get Next Discharge Record
.
EXTR2000  PACK      KEY8,DAADMNO,SP70            * Read Discharge File
          CALL      RDDSCH1
          IF        OVRCD=1
            CALL      CLPATDSC
          ENDIF
.
          MATCH     "1",INCLDAYC                 * Include Day Visits
          GOTO      EXTR3000 IF EQUAL
.
          MATCH     ADATE,DDATE
          GOTO      EXTR1000 IF EQUAL            * Exclude Day Visits
.
EXTR3000  DISPLAY   *P20:24,*V2LON,AURNO," ",DDADMNO;
          MOVE      SP70,XXXNOTW7                * Not within N days flag
          ADD       ONE,TOTRHDSC                 * Increment Total Rehab Dischr
          CALL      CHKD0000
          IF        FORM1<>1
            ADD       ONE,TOTRHEXC  * Increment Total Rehab Dischr (excl suspen)
          ENDIF
.
          PACK      KEY24,DDADMNO,SP70
          CALL      RSPTFIM1
EXTR4000  CALL      RKPTFIM1
          BRANCH    OVRCD,EXTR5000               * No Fim exists
.
          MATCH     PTFIVISN,DDADMNO
          GOTO      EXTR5000 IF NOT EQUAL
.
          MATCH     ONEINIT,PTFIDELT             * Ignore Deleted FIM
          GOTO      EXTR4000 IF EQUAL
.
          MATCH     "A",PTFITYPE                 * Ignore Admission FIM
          GOTO      EXTR4000 IF EQUAL
.
          MATCH     "P",PTFITYPE                 * Ignore Progress FIM
          GOTO      EXTR4000 IF EQUAL
.
. **** FIM score must be within x days prior to discharge date ***
          MATCH     PTFIDATE,DDATE
          GOTO      EXTR4500 IF LESS
.
          DAYSEP    PTFIDATE,DDATE,DAYCNT * Check if FIM within DAYCNTKN days
          IF        DAYCNT <= DAYCNTKN
            GOTO      EXTR1000                   * get next Discharge Record
          ENDIF
.
EXTR4500  MOVE      ANSY,XXXNOTW7                * Not within DAYCNTKN days flag
          ADD       ONE,TOTNOF7D                 * Increment total Dsc FIM not
.                                                  within N days of discharge
          GOTO      EXTR6000
.
EXTR5000  ADD       ONE,TOTNOFIM                 * Increment Total of no Dsc FIM
.
EXTR6000  UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY  * re-format dates for printing
          CALL      PACDATE
          PACK      XXXADATE,CPCDATE,SP70
.
          UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PACK      XXXDDATE,CPCDATE,SP70
.
          MOVE      DSTAT,XXXDSTAT
          MOVE      DDEST,XXXDDEST
.
          PACK      XXXNOTW7,XXXNOTW7
          MOVE      DURNO,XXXXURNO
          MOVE      DDADMNO,XXXADMNO
.
          PACK      KEY16,XXXXURNO,XXXADMNO,SP70
          CALL      RDAWORK1
          IF        OVRCD=1
            CALL      WRWORK1                    * Write to temporary file
          ENDIF
.
          GOTO      EXTR1000
.
EXTR9000  IF        TOTRHDSC < 1
            MOVE      ONE,EXIT
          ENDIF
.
EXTR9999  PRINT     *N;
          RETURN
.
. ******************************************************************************
. *                                  PRNT0000              Called by: MAIN0000 *
. *                                                                            *
. * Print list of patients without Discharge FIM Details                       *
. ******************************************************************************
.
PRNT0000  DISPLAY   *P1:24,*EL,*P10:24,"Printing: ";
.
          COMPARE   "60",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PACK      KEY16,SP70                   * Position temporary file
          CALL      RDSWORK1
PRNT1000  CALL      RDKWORK1                     * Get the next record
          BRANCH    OVRCD,PRNT9000               * End of file reached
.
          DISPLAY   *P20:24,*V2LON,XXXXURNO," ",XXXADMNO;
.
          COMPARE   "60",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          MOVE      SP70,DISCDEST
          MOVE      SP70,DISCHOSP
.
.         Separation Mode? 
.           1 = DSTAT, 2 = DDEST
.
          COMPARE   ONE,PTCNDRSM
          GOTO      PRNT2000 IF EQUAL
.
          COMPARE   TWO,PTCNDRSM
          GOTO      PRNT3000 IF EQUAL
.
          GOTO      PRNT5000
.
PRNT2000  MATCH     SP70,XXXDSTAT           * Discharge Status?
          GOTO      PRNT5000 IF EQUAL
.
          PACK      KEY5,CATD,XXXDSTAT,SP70
          CALL      RDCODE1
          IF        OVRCD=0
            MOVE      ZERO,F1
            MOVE      TCINDC5,F1
            IF        F1=1 | F1=3 | F1=4 | F1=5 | F1=6 | F1=8
              GOTO      PRNT1000            * Skip patient
            ENDIF
          ENDIF
          GOTO      PRNT5000
.
PRNT3000  MATCH     SP70,XXXDDEST           * Discharge Destination?
          GOTO      PRNT5000 IF EQUAL
.
          PACK      KEY5,CATDD,XXXDDEST,SP70
          CALL      RDCODE1
          IF        OVRCD=0
            MOVE      ZERO,F1
            MOVE      TCINDC5,F1
            IF        F1=1 | F1=3 | F1=4 | F1=5 | F1=6 | F1=8
              GOTO      PRNT1000            * Skip patient
            ENDIF
.
            MOVE      TDESC,DISCDEST
            MATCH     "1",TCINDC2           * Discharged to another facility?
            IF        @EQUAL
              PACK      KEY8,XXXADMNO,SP70
              CALL      RDPTDAD1
              IF        OVRCD=0
                PACK      KEY5,PTDADCTS,SP70
                CALL      RDPTVAD1
                BRANCH    OVRCD,PRNT5000
                MOVE      PTVADESC,DISCHOSP
              ENDIF
            ENDIF
          ENDIF
          GOTO      PRNT5000
.
PRNT5000  PRINT     *1,"|",*3,XXXXURNO:
                    *12,"|",*15,XXXADMNO:
                    *24,"|",*27,XXXADATE:
                    *39,"|",*42,XXXDDATE:
                    *54,"|",*60,XXXNOTW7:
                    *67,"|",*69,DISCDEST:
                    *91,"|",*93,DISCHOSP:
                    *132,"|"
          ADD       ONE,CLNO
.
          GOTO      PRNT1000                     * Get next record
.
PRNT9000  IF        CPAGENO > 0
            CALL      UND132
            PRINT     *N,*N,"*** End of Report ***"
          ENDIF
.
PRNT9999  RETURN
.
. ******************************************************************************
. *                                  HEAD0000              Called by: PRNT0000 *
. *                                                                            *
. * Print Report Heading                                                       *
. ******************************************************************************
.
HEAD0000  CALL      HEAD132                     * Standard heading
.
          UNPACK    DATEFROM,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PRINT     *N,*17,"Hospital : ",HOSPNAME
.
          PRINT     *N,*15,"Date Range : ",CPCDATE," to ";
.
          UNPACK    DATETO,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PRINT     CPCDATE,*N
.                                                * Print Total head page 1 only
          IF        CPAGENO = 1
            PRINT     *N,"Total No Of Rehabilitation Patients without ":
                         "Discharge FIM Details : ",TOTNOFIM
            PRINT     *N,"Total No Of Rehabilitation Patients without ":
                         "Discharge FIM Details":
                      " within ",DAYCNTKN," days of Discharge : ",TOTNOF7D
            PRINT     *N,"Total No of Rehabilitation Discharges for the ":
                         "Period : ",TOTRHDSC,*N
            PRINT  *N,"Total No of Rehabilitation Discharges Excluding Deaths,":
                      "Transfers to Acute and Episode type changes for the ":
                      "Period : ",TOTRHEXC,*N
            ADD       SIX,CLNO
          ENDIF
.
          CALL      UND132
          PRINT     *1,"|":
                    *22,"Patients without Discharge FIM Details":
                    *132,"|"
          CALL      UND132
.
          PRINT     *1,"|   U/R":
                    *12,"| Admission":
                    *24,"|   Date of":
                    *39,"|   Date of":
                    *54,"| Not within ":
                    *67,"| Discharge Destination":
                    *91,"| Hospital":
                    *132,"|"
          PRINT     *1,"|  Number":
                    *12,"|  Number":
                    *24,"|  Admission":
                    *39,"|  Discharge":
                    *54,"|  ",DAYCNTKN," days":
                    *67,"|":
                    *91,"|":
                    *132,"|"
.
          CALL      UND132
          ADD       SIX,CLNO
.
HEAD9999  RETURN
.
. ******************************************************************************
. * I/O routines for temporary files                                           *
. ******************************************************************************
.
RDAWORK1  RESET     KEY16
          MOVE      ZERO,OVRCD
          READ      WORK1,KEY16;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RDSWORK1  READ      WORK1,KEY16;;
          RETURN
.
RDKWORK1  MOVE      ZERO,OVRCD
          READKS    WORK1;XXXXURNO,XXXADMNO,XXXADATE,XXXDDATE,XXXDSTAT:
                          XXXDDEST,XXXNOTW7
          GOTO      OVERCOND IF OVER
          RETURN
.
RDWORK1   MOVE      ZERO,OVRCD
          READ      WORK1,KEY16;XXXXURNO,XXXADMNO,XXXADATE,XXXDDATE,XXXDSTAT:
                                XXXDDEST,XXXNOTW7
          GOTO      OVERCOND IF OVER
          RETURN
.
WRWORK1   WRITE     WORK1,KEY16;XXXXURNO,XXXADMNO,XXXADATE,XXXDDATE,XXXDSTAT:
                                XXXDDEST,XXXNOTW7
          RETURN
.
.*******************************************************************************
.* check if rehab visit was suspended                                          *
.*******************************************************************************
CHKD0000  MOVE      ZERO,FORM1
.
.         Separation Mode?
.           1 = DSTAT, 2 = DDEST
.
          COMPARE   ONE,PTCNDRSM
          GOTO      CHKD2000 IF EQUAL
.
          COMPARE   TWO,PTCNDRSM
          GOTO      CHKD3000 IF EQUAL
.
          GOTO      CHKD9000
.
CHKD2000  MATCH     SP70,DSTAT           * Discharge Status?
          GOTO      CHKD9000 IF EQUAL
.
          PACK      KEY5,CATD,DSTAT,SP70
          CALL      RDCODE1
          IF        OVRCD=0
            MOVE      ZERO,F1
            MOVE      TCINDC5,F1
            IF        F1=1 | F1=3 | F1=4 | F1=5 | F1=6 | F1=8
              GOTO      CHKD9000            * Skip patient
            ENDIF
          ENDIF
          GOTO      CHKD9999
.
CHKD3000  MATCH     SP70,DDEST           * Discharge Destination?
          GOTO      CHKD9000 IF EQUAL
.
          PACK      KEY5,CATDD,DDEST,SP70
          CALL      RDCODE1
          IF        OVRCD=0
            MOVE      ZERO,F1
            MOVE      TCINDC5,F1
            IF        F1=1 | F1=3 | F1=4 | F1=5 | F1=6 | F1=8
              GOTO      CHKD9000            * Skip patient
            ENDIF
          ENDIF
          GOTO      CHKD9999
.
CHKD9000  MOVE      ONE,FORM1
.
CHKD9999  RETURN
+
          INC       STD001IO                     * System Standard Routines
.
          INC       CLPATHSP                     * Clear Hospital Table variables
          INC       CLPATDSC                     * Clear Disharge Record
          INC       PATHSPKY                     * Keyin of a Hospital Id/code
          INC       TFILENAM                     * Generate Temp File Name
.
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       PATCODIO/INC                 * Category Codes
          INC       PATDADIO/INC                 * Transfer Destination
          INC       PATDSCIO/INC                 * Patient Discharge File
          INC       PATFIMIO/INC                 * Patient FIM Details File
          INC       PATHSPIO/INC                 * Hospital File
          INC       PATMI1IO/INC                 * Patient Admission File
          INC       PATVADIO/INC                 * Transfer Source Code
          INC       PMSVX1IO/INC                 * Visit Extension File
          INC       WEBERRIO/INC                 * Web Server Error Logging

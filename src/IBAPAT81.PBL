. *****************************************************************************
. *              P R O G R A M  S U M M A R Y                                 *
. *              -------------  -------------                                 *
. *                                                                           *
. *     PROGRAM NAME:     IBAPAT81                                            *
. *                                                                           *
. *     FUNCTION:         BED USAGE REPORT                                    *
. *                                                                           *
. *     DATE WRITTEN:     09/08/85                                            *
. *                                                                           *
. *     AUTHOR:           MALCOLM HAYSE (I.B.A)                               *
. *                                                                           *
. *     MODIFICATIONS:                                                        *
. *                                                                           *
. *      V12.00.01  30/05/2025  Don Nguyen     TSK 0955096                    *
. *                 Alphanumeric visit number changes                         *
. *      V11.00.01  10/03/2020  Jill Parkinson Task 0879163                   *
. *                 Added SEXDSCIO include                                    *
. *      V10.13.01  05/12/2018  Don Nguyen    TSK 0838558                     *
. *                 Deleted temp file (PSTROL) after processing               *
. *      V10.05.01  01/01/2015  Ania P        CAR 261630                      *
. *                 Removed use of PORT for temp file naming                  *
. *       V9.04.01  27/10/2005  Jill Habasque CAR 67980                       *
. *                 Mods for ward/bed + ward only(winput=2)                   *
. *       V9.03.01  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2      *
. *                 - Add Sex Description routine SEXDSC00 with Code "R" -    *
. *                 "Intersex" added                                          *
. *       V5.08.05  29/08/2000  Caleb Sharman                                 *
. *                 Changed Health Fund variables to be 8 chars               *
. *       V5.08.04  29/08/2000  Tonii                                         *
. *                 Mods to accept Unknown & Indeterminate as valid patient   *
. *                 sex description                                           *
. *       V5.08.03  19/07/2000  Steve Downing                                 *
. *                 Fixed ward range keyin                                    *
. *       V5.08.02  18/07/2000  Steve Downing                                 *
. *                 Mods to use PATWRDKY                                      *
. *       V5.08.01  18/07/2000  Steve Downing                                 *
. *                 Mods to print single or range of wards                    *
. *****************************************************************************
. *       V5.07.01  18/03/1999  Jim Stathopoulos                              *
. *                 Fixed Date anomaly                                        *
. *       V5.07.B02 25/01/1999  Nicole Harrington 507 rebound 140             *
. *                 Mods to display date                                      * 
. *                 09/11/1998  Glenn Berry      5.07 changes                 *
.******************************************************************************
. *                   V5.01.121 27/11/90 Glen Hobby                           *
. *                            Recompiled for changes to                      *
. *                            PATMX1FD                                       *
. *        V5.01.122 08.03.93 Neeriem "I Hate Support" Dye                    *
. *                  Use Keydate                                              *
. *                                                                           *
. *****************************************************************************
.
          INC       STD001FD   
          INC       PATCODFD/INC
          INC       PATTRNFD/INC
          INC       PATMA1FD/INC
          INC       PATWR1FD/INC
          INC       IBASEQFD/INC  
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
+
.
.               WORK AREA
.
.-----------------------------------------------------------
.Name       Type    Length     Physical     Start
.----       ----    ------     --------     -----
.TWARD      DIM        3             3         1
.TBED       DIM        3             3         4
.TDATE      DIM        8             8         7
.TTIME      DIM        8             8        15
XDTADMN     DIM        8             8        22
.TMOVE      DIM        1             1        31
.TURNO      DIM        8             8        32
.
.  END OF RECORD                              40
.
.  redefine form fields from key
.  -----------------------------
.Name       Type    Length     Start
.----       ----    ------     -----
.TADMN      FORM       8         21
.-----------------------------------------------------------
.
PSKIP     FORM      1
DPSKIP    DIM       3
TIMEIS    DIM       8
.
.      EXTRA VARIABLES
.
DHHMM     DIM       5
MIN1      FORM      1
.
XDAY1     DIM       2
XMON1     DIM       2
XYEAR1    DIM       2
XCENT1    DIM       2
.
XDAY2     DIM       2
XMON2     DIM       2
XYEAR2    DIM       2
XCENT2    DIM       2
.
PNAME     DIM       22         * PGNAME.PSNAME
.
DSEX      DIM       8                                         * was 6  *C-49016
.
DMOVE     DIM       15
DMOVEI    INIT      "TRANSFERED IN  "
DMOVEO    INIT      "TRANSFERED OUT "
DMOVER    INIT      "RETURN LEAVE   "
DMOVEL    INIT      "ON LEAVE       "
DMOVEA    INIT      "ADMITTED       "
DMOVED    INIT      "DISCHARGED     "
.
ENDWARD   DIM       6               * output start ward
EWARD     DIM       3               * input end ward
EWRDDESC  DIM       20            * end ward description - for redisplay
.
FROMDATE  DIM       8
TODATE    DIM       8
.
CMDLINE   DIM       30
.
FINISH    INIT      "Finish"
FNAMEI    DIM       8
FNAMER    DIM       8
.
WORK      IFILE     SQL, FIXED=40, KEY="U1-31"
PSTROL    FILE      ASCII, FIXED=256
.
LWARD     DIM       3
LBED      DIM       3
.
RANGE     DIM       6             * temp storage for input parameters
.
SWARD     DIM       3             * input start ward
STTWARD   DIM       6             * output start ward
SWRDDESC  DIM       20            * start ward description - for redisplay
START     INIT      "Start"
.
Z8        INIT      "ZZZZZZZZ"
.
PRGID     INIT      "IBAPAT81"
PRGNAM    INIT      "Bed Usage Report"
VERSION   INIT      "V12.01.00"
+
.         START OF PROGRAM
.
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening pattranf";
          OPEN      PATTRAN1,"pattranf"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATWR1A3,"patwr1af"
+
.
.         START OF PROGRAM
.         MAIN    MENU
.
.
START     MOVE       ZERO,CPAGENO
.
          DISPLAY         *P1:4,*EF,"Keyin From Date :":
                           *P1:6,"  Keyin To Date :"
+
.
.             KEYIN TO DATE
.
KADAT1 
.
.
          MOVE      SP10,CDAY        * no default day/mon
          MOVE      SP10,CMON
          MOVE      SP10,CYEAR
          MOVE      SP10,CCENT
.
          MOVE      " 4",CVERT         * set up row
          MOVE      "20",CCOL          * set up collumn
.
          MOVE      "0",CCANLDTE       * 0=cannot cancel default date, 1=can
          MOVE      "1",CDEFDTE        * 0=3 C/R's to accept date, 1=1 C/R
          REPEAT
          CALL      KEYDATE            * nez modified to use standard routine
          UNTIL     CQUEST=0
.
          MOVE      ZERO,IDAY
          MOVE      CDAY,IDAY
          MOVE      CDAY,XDAY
          MOVE      CMON,IMON
          MOVE      CYEAR,IYEAR
          MOVE      CCENT,ICENT
.
          COMPARE   ZERO TO IDAY
          GOTO      ENDIT IF EQUAL
.
.
ENDAD1
.
          PACK      FROMDATE,ICENT,IYEAR,IMON,IDAY
          REP       " 0",FROMDATE
          UNPACK    FROMDATE,XCENT1,XYEAR1,XMON1,XDAY1
+
.
.             DISPLAY TO DATE 
.
KADAT2 
....
          IF        IDAY=0
            MOVE      SP10,CDAY        * no default day/mon
            MOVE      SP10,CMON
            MOVE      SP10,CYEAR
            MOVE      SP10,CCENT
          ELSE
            MOVE      IDAY,CDAY        * set up day/mon
            MOVE      IMON,CMON
            MOVE      IYEAR,CYEAR
            MOVE      ICENT,CCENT
          ENDIF
.
          MOVE      " 6",CVERT         * set up row
          MOVE      "20",CCOL          * set up column
.
          MOVE      "0",CCANLDTE       * 0=cannot cancel default date, 1=can
          MOVE      "1",CDEFDTE        * 0=3 C/R's to accept date, 1=1 C/R
          REPEAT
          CALL      KEYDATE            * nez modified to use standard routine
          UNTIL     CQUEST=0
.
          MOVE      ZERO,IDAY
          MOVE      CDAY,IDAY
          MOVE      CDAY,XDAY
          MOVE      CMON,IMON
          MOVE      CYEAR,IYEAR
          MOVE      CCENT,ICENT
.
          COMPARE   ZERO TO IDAY
          GOTO      KADAT1 IF EQUAL
.
.
ENDAD2
.
          PACK       TODATE,ICENT,IYEAR,IMON,IDAY
          REP        " 0",TODATE
          UNPACK     TODATE,XCENT2,XYEAR2,XMON2,XDAY2
+
.
.         CHECK IF DATES ARE OK OR NOT
.
REKEY     KEYIN       *P1:8,*EF," Dates Ok (":
	              *V2LON,"Y",*HOFF,"/":
	              *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
          MATCH       ANSN,ANS
          GOTO        KADAT1 IF EQUAL
.
          MATCH       ANSY,ANS
          GOTO        CNTCHK IF EQUAL
.
          BEEP
          GOTO        REKEY
.
CNTCHK    MATCH       FROMDATE,TODATE * 'FROM' DATE MUST BE 
          GOTO        INVDATG IF LESS             * >= 'TO' DATE
.
KEYWRD    CALL        WARD0000
          BRANCH      EXIT,START
.
          KEYIN       *P1:14,*EL,"Would you like each Ward ":
                      "on a separate Page (":
	              *V2LON,"Y",*HOFF,"/":
	              *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
          MATCH        "Y",ANS
          GOTO         ENDS1Y IF EQUAL
.
          MOVE         ONE,PSKIP 
          GOTO         ENDS1
.
ENDS1Y    MOVE         ZERO,PSKIP
ENDS1     MOVE         DYES,DPSKIP
          LOAD         DPSKIP FROM PSKIP OF DNO
          DISPLAY      *P53:14,*V2LON,DPSKIP
.
          CALL         CONTQST
          BRANCH       CEXIT,TRANNO,KEYWRD,START
.
          GOTO         TRANNO
.
INVDATG   DISPLAY      *P1:24,*EL,*B,*V2LON:
                       "** From Date must be Greater ":
                       "than To Date **",*W2,*P1:24,*EL;
          GOTO         KADAT1
+
.
.       Pass 1 : Get data and translate "C" records to O/I pairs
.
TRANNO    CALL       CREAIDX
          DISPLAY    *P1:24,*EL,"PASS 1",*P20:24,"Ward :",*P40:24,"Bed :":
                     *P60:24,"Scanning :";
. 
          PACK       KEY6,SP6
          CALL       RDSWARD1
NEXTW     CALL       RDKWARD1
          BRANCH     OVRCD OF WRTREP
.
          DISPLAY    *P27:24,*V2LON,WWARD,*P47:24,WBED;
.
          MATCH      SP3,WBED
          GOTO       NEWBED IF NOT EQUAL
.
          BRANCH     WINPUT OF NEWBED,NEWBED
          GOTO       NEXTW
.
NEWBED    PACK       KEY30,WWARD,WBED,FROMDATE,SP30
          CALL       RDSTRAN1
.
NEXTT     CALL       RDKTRAN1
          BRANCH     OVRCD OF WRTREP
.
          MATCH      TWARD,WWARD
          GOTO       NEXTW IF NOT EQUAL
.
          MATCH      TBED,WBED
          GOTO       NEXTW IF NOT EQUAL
.
          MATCH      TDATE,TODATE
          GOTO       NEXTW IF LESS
.
OKDATA    DISPLAY    *P71:24,TDATE;
          CALL       WRTREC
          GOTO       NEXTT
.
.         Write a record to the temporary file
.
WRTREC    CMATCH     "C",TMOVE
          GOTO       WRT1 IF EQUAL
.
          PACK       KEY31,TWARD,TBED,TDATE,TTIME,TADMN,TMOVE
          WRITE      WORK,KEY31;KEY31,TURNO
          RETURN
.
.         Change record - Check if we have the last ward/bed information
.
WRT1      MOVE       TWARD,STWARD
          MOVE       TBED,STBED
          MOVE       TADMN,STADMN
          MOVE       TDATE,STDATE
          MOVE       TTIME,STTIME
.
          MOVE       SP3,LWARD
          MOVE       SP3,LBED
.
.         We need to backtrack to find the last ward/bed this patient was in
.
          PACK       KEY30,TADMN,SP30
          CALL       RDSTRAN2
TRLP2     CALL       RDKTRAN2
          BRANCH     OVRCD OF WRTOUT
.
          MATCH      TADMN,STADMN
          GOTO       WRTOUT IF NOT EQUAL
.
          MATCH      TDATE,STDATE
          GOTO       SAVTRN IF NOT EQUAL
.
          MATCH      TTIME,STTIME
          GOTO       WRTOUT IF EQUAL    * This is the C record (same date/time)
.
SAVTRN    MOVE       TWARD,LWARD
          MOVE       TBED,LBED
          GOTO       TRLP2
.
.         If the ward/bed was changed, then write O/I records
.
WRTOUT    MATCH      STWARD,LWARD
          GOTO       WRT2 IF NOT EQUAL
.
          MATCH      STBED,LBED
          RETURN     IF EQUAL
.
WRT2      MOVE       "O",TMOVE
          PACK       KEY31 WITH LWARD,LBED,STDATE,STTIME,STADMN,TMOVE
          WRITE      WORK,KEY31;KEY31,TURNO
.
          MOVE       "I",TMOVE
          PACK       KEY31 WITH STWARD,STBED,STDATE,STTIME,STADMN,TMOVE
          WRITE      WORK,KEY31;KEY31,TURNO
.
          MOVE       STWARD,TWARD
          MOVE       STBED,TBED
.
          RETURN
.
NOTRAN  
          DISPLAY    *P20:24,*B,*V2LON:
                     "** No Bed Transfers during this Period **":
                     *W2,*P20:24,*EL;
          GOTO       START
+
.
.         Print the report
.
WRTREP    MOVE        "60",CLNO
          DISPLAY     *P1:24,"PASS 2";
.
          CLOSE       WORK
          OPEN        WORK,FNAMEI
.
          PACK        KEY31,SWARD,SP30
          READ        WORK,KEY31;;
WRTLP1    READKS      WORK;TWARD,TBED,TDATE,TTIME,XDTADMN,TMOVE,TURNO
          GOTO        ENDP IF OVER
.
          MATCH       SWARD,TWARD
          GOTO        WRTLP1 IF LESS
          MATCH       TWARD,EWARD
          GOTO        WRTLP1 IF LESS
.
          MOVE        XDTADMN,TADMN
          DISPLAY    *P27:24,*V2LON,TWARD,*P46:24,TBED,*P71:24,TDATE;
.
          CALL        DISPDA
          GOTO        WRTLP1
+
.
.         OVER CONDITION FOUND..PRINT LAST LINE OF -- AND DISPLAY MESSAGE
.
ENDP     
          CALL         KILLIDX
          COMPARE      ZERO,CLNO
          GOTO         NOTRAN IF EQUAL
          CALL         PAGEND
          PRINT        *N,"  ***  END OF REPORT  ***"
          DISPLAY      *P40:24,*EL,*V2LON:
                       "** Report Generation Completed **",*W2;
          GOTO         START
+
.
.         PRINT ACTUAL DATA 
.
DISPDA    COMPARE   "50",CLNO
          CALL      HEAD IF NOT LESS
.
          MOVE       SP30,PNAME
          MOVE       TURNO,KEY8
          CALL       RDMAST1
          BRANCH     OVRCD OF DISPCNP
.
          MOVE        PGNAME,ANS
          PACK        PNAME WITH ANS,DOT,PSNAME
          REP         ", ",PNAME
.
DISPCNP   UNPACK    TDATE INTO XCENT,XYEAR,XMON,XDAY
          MOVE      TTIME,DHHMM
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
                                           *       returns DSEXDESC & DSEXNO
          MOVE      DSEXDESC,DSEX
.
CNTPD2    CMATCH    "I",TMOVE
          GOTO      FIN IF EQUAL
.
          CMATCH    "O",TMOVE
          GOTO      FOUT IF EQUAL
.
          CMATCH    "A",TMOVE
          GOTO      FADM IF EQUAL
.
          CMATCH    "D",TMOVE
          GOTO      FDIS IF EQUAL
.
          CMATCH    "R",TMOVE
          GOTO      FRET IF EQUAL
.
          CMATCH    "L",TMOVE
          GOTO      FLEV IF EQUAL
.
FRET      MOVE      DMOVER,DMOVE
          GOTO      FPRINT
.
FLEV      MOVE      DMOVEL,DMOVE
          GOTO      FPRINT
.
FIN       MOVE      DMOVEI,DMOVE
          GOTO      FPRINT
.
FOUT      MOVE      DMOVEO,DMOVE
          GOTO      FPRINT
.
FADM      MOVE      DMOVEA,DMOVE
          GOTO      FPRINT
.
FDIS      MOVE      DMOVED,DMOVE
.
FPRINT    MATCH     TWARD,STWARD
          GOTO      FPRINT1 IF EQUAL
.
.         WE MUST CHECK IF A WARD PER PAGE
.
          BRANCH    PSKIP OF FPRINT0
          CALL      HEAD
          GOTO      FPRINTA
.
FPRINT0   CALL      HEAD2
          GOTO      FPRINTA
.
FPRINT1   MATCH     TBED,STBED
          GOTO      FPRINT2 IF EQUAL
.
FPRINTA   MOVE      "UNKNOWN BED         ",WBDESC
          PACK      KEY6 WITH TWARD,TBED
          CALL      RDWARD1
.
          PRINT     *1,"! ",TBED,*6,"! ",WBDESC:
                    *27,"! ",DMOVE:
                    *50,"! ",XDAY,"/",XMON,"/",XCENT,XYEAR:
                    *63,"! ",DHHMM:
                    *71,"! ",TADMN,*81,"! ",TURNO:
                    *91,"! ",PNAME,*117,"! ",PSAGE:
                    *122,"! ",DSEX,*132,"!"
.
          MOVE      TBED,STBED
          GOTO      ENDPRT
.
FPRINT2   PRINT     *1,"!",*6,"!",*27,"! ",DMOVE:
                    *50,"! ",XDAY,"/",XMON,"/",XCENT,XYEAR:
                    *63,"! ",DHHMM:
                    *71,"! ",TADMN,*81,"! ",TURNO:
                    *91,"! ",PNAME,*117,"! ",PSAGE:
                    *122,"! ",DSEX,*132,"!"
.
ENDPRT    ADD       ONE,CLNO
          RETURN
+
.
.          LAST LINE ON THE TRAN
.
PAGEND    PRINT     *1,"*-----------------------------------------":
                       "-------------------------------------------":
                       "----------------------------------------------*"
          ADD       ONE,CLNO
          RETURN
+
.
.                 HEADINGS FOR OUTPUT
.
HEAD      COMPARE   ZERO,CPAGENO
          CALL      PAGEND IF NOT EQUAL
.
          MOVE      ONE,CNOUNDLN
          CALL      IBACLOCK
          CALL      HEAD132
.
          PRINT     *N,*40,"FROM DATE : ",XDAY1,"/",XMON1,"/",XCENT1,XYEAR1:
                    *65,"TO DATE : ",XDAY2,"/",XMON2,"/",XCENT2,XYEAR2 
          MOVE      FOUR,CLNO
          GOTO      HEAD3
.
HEAD2     CALL      PAGEND
.
HEAD3     MOVE      "UNKNOWN WARD        ",WBDESC
          PACK      KEY6 WITH TWARD,SP3
          CALL      RDWARD1
.
          PRINT     *N,*50,"WARD : ",TWARD,"  ",WBDESC,*N
          ADD       THREE,CLNO
          CALL      PAGEND
.
          PRINT     *1,"! Bed",*6,"! Bed Description":
                    *27,"! Movement":
                    *50,"! Date",*63,"! Time":
                    *71,"! Adm No",*81,"! U/R No":
                    *91,"! Patients Name",*117,"! Age":
                    *122,"! Sex",*132,"!"
.
          CALL      PAGEND
          ADD       ONE,CLNO
          MOVE      SP6,STBED
          MOVE      TWARD,STWARD
          RETURN
+
.
.      Create an indexed file based on port
.       for accumulating Admission Type totals
.
CREAIDX   CALL      TFILENAM
          MOVE      TFILNAME,FNAMEI
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMER

.
.         CHECK IF PREVIOUSLY EXISTS..IF SO DELETED IT 
. 
          CALL        KILLIDX
.
          PREP        PSTROL,FNAMER
          WRITE       PSTROL,SEQ;"isbuild ",FNAMEI," 40 U1-31"
          WEOF        PSTROL,SEQ
.
          CLEAR       CMDLINE
          APPEND      "sh ",CMDLINE
          APPEND      FNAMER,CMDLINE
          APPEND      ".txt",CMDLINE
          RESET       CMDLINE
.
          DISPLAY     *P1:13,*EF;
          EXECUTE     CMDLINE,TASKID
          DISPLAY     *P1:13,*EF;
.
          CLOSE       PSTROL,DELETE
.
          TRAP        NOINDZ IF IO
          OPEN	    WORK,FNAMEI
          TRAPCLR     IO
ENDC      RETURN
.
NOINDZ    DISPLAY     *P1:24,*EL,*B,*V2LON:
                      "** Temporary Index File ":
                      FNAMEI," Not Found **",*W4;
          TRAPCLR     IO
          RETURN
+
.
.         Deleting an indexed file based on port
.
KILLIDX   CLOSE      WORK
.
          DISPLAY    *P1:24,*EL,*P20:24,*V2LON:
                     "** Deleting Indexed File **",*W;
.
          CLEAR      CMDLINE
          APPEND     "iserase ",CMDLINE
          APPEND     FNAMEI,CMDLINE
          RESET      CMDLINE
          EXECUTE    CMDLINE,TASKID
          DISPLAY    *P1:13,*EF
          RETURN
+
.*******************************************************************************
.*                               WARD0000                                      *
.*        get starting and ending Ward range             called by : RPAR0000  *
.*        Returns:    EXIT: 0 = Valid codes entered                            *
.*                          1 = Exit character entered                         *
.*******************************************************************************
.         get starting ward
.
WARD0000  DISPLAY   *P1:10,"Starting Ward   :":
                    *P20:10,*V2LON,SWARD,*EL
          MOVE      "20",ECOL
          MOVE      "10",EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND
          CALL      PATWRDKY
.
          MOVE      WWARD,SWARD
          ENDSET    SWARD
          APPEND    SP6,SWARD
          RESET     SWARD
.
          BRANCH    EXIT,WARD2000,WARD9000   * 1=null input, 2=? option
.
WARD1000  DISPLAY   *P25:10,*EL,WBDESC
          MOVE      WBDESC,SWRDDESC          * set start ward desc for redisplay
          GOTO      WARD4000
.
.         null start ward input - display "START"
.
WARD2000  DISPLAY   *P20:10,*EL,*V2LON,START
          MOVE      START,STTWARD            * set start ward output variable
          GOTO      WARD4000
.
WARD4000  DISPLAY   *P1:12,"Finishing Ward  :":
                    *P20:12,*V2LON,EWARD,*EL
.
          MOVE      "20",ECOL
          MOVE      "12",EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND
          CALL      PATWRDKY
.
          MOVE      WWARD,EWARD          
          ENDSET    EWARD
          APPEND    SP6,EWARD
          RESET     EWARD
.
          BRANCH    EXIT,WARD6000,WARD4500   * 1=null input, 2=? option
          GOTO      WARD5000
.
WARD4500  DISPLAY   *P20:12,*EL
          GOTO      WARD0000
.
WARD5000  DISPLAY   *P25:12,*EL,WBDESC
          MOVE      WBDESC,EWRDDESC          * set end ward output variable
          GOTO      WARD8000
.
.         null end ward input - display "FINISH"
.
WARD6000  DISPLAY   *P20:12,*EL,*V2LON,FINISH
          MOVE      FINISH,ENDWARD           * set end ward output variable
          MOVE      Z8,EWARD                 * set end ward to highest value
          GOTO      WARD8000
.
.         Check that ending ward is equal to or greater than starting ward
.
WARD8000  MATCH     SWARD,EWARD
          GOTO      WARD9500 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"End ward must be same as or come after":
                                  " Start ward - ";
          CALL      HITENTER
          GOTO      WARD4000
.
WARD9000  MOVE      ONE,EXIT
          GOTO      WARD9999
.
WARD9500  MOVE      ZERO,EXIT
.
WARD9999  RETURN
+
.
          INC       PATWRDKY
          INC       SEXDSCIO
          INC       PATTRNIO/INC
          INC       PATCODIO/INC
          INC       PATMA1IO/INC
          INC       PATWR1IO/INC
          INC       STD001IO
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC
.
ENDIT    CHAIN      PGM
         STOP

.******************************************************************************
.*                                                                            *
.*                        P R O G R A M   S U M M A R Y                       *
.*                        -------------   -------------                       *
.*                                                                            *
.*     PROGRAM NAME:     IBAHOS64                                             *
.*                                                                            *
.*     FUNCTION:         DISCHARGES FOR A DAY REPORT                          *
.*                                                                            *
.*     DATE WRITTEN:     19/07/88                                             *
.*                                                                            *
.*     AUTHOR:           KARIN PEAK                                           *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.*                                                                            *
.*        V12.00.01 22/05/2025  Alina Bhari   TSK 0955096                     *
.*                  Added alpanumeric visit number generation -DADMNO         *
.*        V10.13.01 05/12/2018  Don Nguyen    TSK 0838558                     *
.*                  Deleted temp file (PSTROL) after processing               *
.*        V10.10.01 07/04/2017  Ania P        CAR 261630                      *
.*                  Removed use of PORT                                       *
.*        V10.02.02 27/04/2011  Saroeun Soeur CAR 241405                      *
.*                  fixed non multi hospital                                  *
.*        V10.02.01 15/04/2011 Saroeun Soeur  CAR 241405                      *
.*                  added hospital filter for multi hospital                  *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V9.02.01  12/09/2001  Mike Laming                                   *
.*                  Add 'New Line' character to Report Heading                *
.*        V5.08.01  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.07.B02 28/01/1999  Nicole Harrington                             *
.*                  507 rebounds                                              *
.*        V5.07.B01 13/11/1998  Davin                                         *
.*                  Mods for 507                                              *
.******************************************************************************
.*        V5.04.01  02/05/1997  Steve Armstrong   SRF 642857                  *
.*                  Changed temp file name "pstidx" to standard format        *
.******************************************************************************
.*                      V5.01     19/05/89 D.Di.Paola                         *
.*                                Alter report to allow "8" digit U/R & Adm No*
.*                                Put in STD001FD & Keydate Routine           *
.*                      V5.02     27/05/89 J.Tan           SRF 100750         *
.*                                Fixed Mother Adm. to Form 8 (PATMI1FD).     *
.*                      V5.03     06/06/89 J.Tan           SRF 100809         *
.*                                Mods Locking master file (PATIOMAS).        *
.*                      V5.01.121 27/11/90 Glen Hobby                         *
.*                                Recompiled for changes to PATMX1FD          *
.*                      V5.01.122 13/02/92 i chung         SRF 107749         *
.*                                Fix so to print discharges for date         *
.*                                requested only                              *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD   
.
          INC       PATCODFD/INC
          INC       PATDSCFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATTRNFD/INC
          INC       PMSVX1FD/INC
          INC       PATHSPFD/INC
          INC       IBASEQFD/INC 
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC

+
.
.         Work Area
.
CMDLINE   DIM       50
.
FNAMER    DIM       8
FNAMET    DIM       8
.
DSCHRGS   FORM      4                        * no. of discharges for that day
STATUS    FORM      1
.
.         Extra Variables
.
DIM5      DIM       5
DIM21     DIM       21
.
ADTIM     DIM       5
DSTIM     DIM       5
.
SRTKEY    DIM       23
TODATE    DIM       8        * TO DATE IN (CCYYMMDD) FORMAT
.
XDAY1     DIM       2
XMON1     DIM       2
XYEAR1    DIM       2
XCENT1    DIM       2
.
XDAY2     DIM       2
XMON2     DIM       2
XYEAR2    DIM       2
XCENT2    DIM       2
.
HOSNAME   DIM       50        * hospital name - multi hospital
HOSCODE   DIM       3         * hospital code - multi hospital
IBCNMHOS  FORM      1         * multi hospital indicator
.
SORT1     INIT      "u46-53,54-61"
SORT2     INIT      "u62-69,46-53,54-61"
SORT3     INIT      "u1-20,21-45,46-53,54-61"
.
PSTROL    FILE      ASCII, FIXED=256
TEMPFILE  IFILE     SQL, FIXED=94, KEY=SRTKEY
.
.***************************************************************
.         Temp file variables declaration
.***************************************************************
.
.Name      Type      Length      Physical      Start
.----      ----      ------      --------      -----
.PSNAME    DIM          20            20         1
.PGNAME    DIM          25            25         21
ADMNO      DIM           8             8         46
DUNIQUE    DIM           8             8         54
URNO       DIM           8             8         62
.ACLAIM    DIM           3             3         70
.ACLSS     DIM           3             3         73
.PTITL     DIM           4             4         76
.ADATE     DIM           8             8         80
.ATYPE     DIM           3             3         88
.AUSR2     DIM           3             3         91
.
.End of Record                                   94
.
.  redefine form fields for key
.  ----------------------------
.Name      Type      Length      Start
.----      ----      ------      -----
.DADMNO     DIM          8          1
UNIQUE      FORM         8          1
.DURNO      FORM         8          1
+
.***************************************************************
.
PRGID     INIT      "IBAHOS64"
PRGNAM    INIT      "Discharges For A Day Report"
VERSION   INIT      "V12.01.00"
+
.
.         Open files
.
          DISPLAY   *ES;
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"pmsvx1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMER
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMET
.
. ------  Start of Program  ------
.
.           MAIN  MENU
.
START     MOVE      ZERO,CPAGENO
          MOVE      SP1,ANS
.
          HLINE     *G37,2,54,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Admission Number Sequence":
                    *P1:6,*V2LON,TWO,*HOFF," = U/R Number Sequence":
                    *P1:7,*V2LON,THREE,*HOFF," = Surname Sequence":
                    *P1:9,"Select Option : ";
.
START1    KEYIN     *P17:9,*EL,*DV,UNDLN1,*P17:9,*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
.
          BRANCH    OPTION OF KADMN,KNUMB,KSURN
          BEEP
          GOTO      START1
.
KADMN     DISPLAY   *P54:2,*V2LON,"- Admission Number Sequence "
          GOTO      KDDATE
.
KNUMB     DISPLAY   *P54:2,*V2LON,"- U/R Number Sequence "
          GOTO      KDDATE
.
KSURN     DISPLAY   *P54:2,*V2LON,"- Surname Sequence "
          GOTO      KDDATE
.
NODSCH    DISPLAY   *P1:24,*EL,*B,*V2LON,"No Discharges for date requested - ";
          CALL      HITENTER
          GOTO      START
+
.------------------------------------------------------------------------------
.         KHOS0000
.         Enter The Hospital
.------------------------------------------------------------------------------
KHOS0000  COMPARE   ONE,IBCNMHOS
          GOTO      KHOS9999 IF NOT EQUAL
.
          DISPLAY   *P1:21,*EL,"Hospital Id : ";
          MOVE      TEN5,ECOL
          MOVE      TWENTY1,EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS2000,KHOS9500
          GOTO      KHOS3000
.
KHOS2000  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          MOVE      ZERO,EXIT
.
KHOS3000  CLOSE     PATHSPA1
.
          DISPLAY   *P20:21,*V2LON,PTHSNAME;
          PACK      HOSNAME,PTHSNAME,SP70
          PACK      HOSCODE,PTHSHOSP,SP70
          GOTO      KHOS9999
.
KHOS9500  MOVE      ONE,EXIT
KHOS9999  RETURN
+
.
.************************************************************************
.*                            KDDATE                                    *
.*                     keyin discharge date                             *
.************************************************************************
KDDATE    DISPLAY   *P1:13,*EF,"Generate Report for "
.
          MOVE      "21",CCOL                       * parameters 
          MOVE      "13",CVERT
          MOVE      "45",ECOL
          MOVE      "13",EVERT
          MOVE      ZERO,CDEFDTE
          MOVE      ZERO,CHIGHLT
.
KADAT1    DISPLAY   *P1:24,*EL; 
.
          MOVE      SP8,TODATE                      * clear date variables
          MOVE      SP2,CDAY
          MOVE      SP2,CMON
          MOVE      SP2,CYEAR
          MOVE      CCC,CCENT
.
          CALL      KEYDATE                         * call keydate routine
          BRANCH    CQUEST,KADAT1
          BRANCH    OVRCD,START
.
          PACK      TODATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE
.
.         initialize date for report heading
.
          UNPACK    TODATE,XCENT1,XYEAR1,XMON1,XDAY1
          MOVE      CCENT,XCENT1
.
          CALL      KHOS0000            * keyin hospital (jay)
          BRANCH    EXIT,KDDATE
.
.         check if Date is okay or not
.
REKEY     KEYIN     *P1:24,*EL,"Proceed with current options? (",*V2LON,"Y",*HOFF,"/":
	            *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
          MATCH     "Y" TO ANS
          GOTO      STRTPRO IF EQUAL
.
          MATCH     "N" TO ANS
          GOTO      KADAT1 IF EQUAL
          BEEP
          GOTO      REKEY
.
.         Create and open temp index file
.
STRTPRO   DISPLAY   *P1:24,*EL,*P19:24,*V2LON,"** Creating Temp Index File - ":
                               *BLINKON,"Please wait",*HOFF,*V2LON," **";
          CALL      KILLIDX
          CALL      CREAIDX
          COMPARE   ZERO,STATUS
          GOTO      START IF EQUAL
.
          OPEN      TEMPFILE,FNAMET
.
          MOVE      ONE,UNIQUE
          MOVE      ZERO,DSCHRGS
          MOVE      "60",CLNO
.
.         Extract data by looping through Discharge file & writing to temp file
.
          DISPLAY   *P1:24,*EL,*V2LON,"** ",*BLINKON,"Extracting Data",*HOFF:
                               *V2LON," **",*HOFF:
                    *P30:24,"Discharged Adm :",*P61:24,"Scanning :";
.
          PACK      KEY16,TODATE,SP20
          CALL      RDSDSCH2
.
NEXTREC   CALL      RDKDSCH2
          BRANCH    OVRCD OF GENRPT
.
          MATCH     DDATE,TODATE
          GOTO      GENRPT IF NOT EQUAL
.
          IF        IBCNMHOS=1
            MATCH     SP70,HOSCODE        * All Hospitals
            IF        !@EQUAL
              PACK      KEY8,DADMNO,SP70
              CALL      RDPMVX11
              IF        OVRCD <> 1
                MATCH     PMVXMHOS,HOSCODE    * Correct hospital
                GOTO      NEXTREC IF NOT EQUAL
              ELSE
                GOTO      NEXTREC
              ENDIF
            ENDIF
          ENDIF
.
          DISPLAY   *P72:24,DADMNO;
          CALL      INITVAR
.
          MOVE      DURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF INVMASTD
.
          MOVE      DADMNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD OF INVADMN
.
          DISPLAY   *P47:24,*V2LON,DADMNO;
.
          MOVE      DADMNO,ADMNO
          MOVE      DURNO,URNO
          MOVE      UNIQUE,DUNIQUE
.
.         write to temporary index file
.
          BRANCH    OPTION OF TEMP1,TEMP2,TEMP3
.
TEMP1     PACK      KEY16 WITH DADMNO,UNIQUE
          RESET     KEY16
          WRITE     TEMPFILE,KEY16;PSNAME,PGNAME,ADMNO:
                                   DUNIQUE,URNO,ACLAIM,ACLSS:
                                   PTITL,ADATE,ATYPE,AUSR2
          GOTO      CONTREC
.
TEMP2     PACK      KEY24 WITH DURNO,DADMNO,UNIQUE
          RESET     KEY24
          WRITE     TEMPFILE,KEY24;PSNAME,PGNAME,ADMNO:
                                   DUNIQUE,URNO,ACLAIM,ACLSS:
                                   PTITL,ADATE,ATYPE,AUSR2
          GOTO      CONTREC
.
TEMP3     PACK      KEY61 WITH PSNAME,PGNAME,DADMNO,UNIQUE
          RESET     KEY61
          WRITE     TEMPFILE,KEY61;PSNAME,PGNAME,ADMNO:
                                   DUNIQUE,URNO,ACLAIM,ACLSS:
                                   PTITL,ADATE,ATYPE,AUSR2
.
CONTREC   ADD       ONE,UNIQUE
          ADD       ONE,DSCHRGS
          GOTO      NEXTREC
.
. ------  master files error messages  ------
.
INVMASTD  DISPLAY   *P1:21,*B,*EL,"** Discharge ",*V2LON,DADMNO,*HOFF:
                    " has an invalid U/R ",*V2LON,DURNO,*HOFF,".  ":
                    "Please contact I.B.A. **",*W2;
.
          PRINT     *N,"** Discharge ",DADMNO," has an invalid U/R ":
                    DURNO,"...  Please contact I.B.A. **"
          GOTO      INVALID
.
INVADMN   DISPLAY   *P1:21,*EL,*B,"** Discharge ",DADMNO," has no admission ":
                    "information.  Please contact I.B.A. **",*W2;
.
INVALID   DISPLAY   *P1:21,*EL;
          GOTO      NEXTREC
.
.         Initialize temp index file variables
.
INITVAR   MOVE      SP20,PSNAME
          MOVE      SP30,PGNAME
          MOVE      SP4,PTITL
          MOVE      SP8,ADATE
          MOVE      SP3,ACLAIM
          MOVE      SP3,ACLSS
          MOVE      SP3,ATYPE
          MOVE      SP3,AUSR2
          RETURN
+
.
. ------  Generate Report  ------
.
GENRPT    DISPLAY   *P1:24,*EL,*P6:24,*V2LON,"** ",*BLINKON,"Generating Report":
                               *HOFF,*V2LON," **";
.
          CLOSE     TEMPFILE
          TRAP      NOOPEN IF IO
          OPEN      TEMPFILE,FNAMET
          TRAPCLR   IO
          GOTO      GENRPT1
.
NOOPEN    DISPLAY   *P1:24,*B,*EL,"Temporary index file ",*V2LON,FNAMET:
                              *HOFF," not found - ";
          CALL      HITENTER
          GOTO      START
.
GENRPT1   BRANCH    OPTION,LOOPA,LOOPU
          GOTO      LOOPS
.
LOOPA     MOVE      SP20 TO KEY16
          RESET     KEY16
          READ      TEMPFILE,KEY16;;
.
          DISPLAY   *P45:24,"Admission Number :";
          GOTO      READTMP
.
LOOPU     MOVE      SP20 TO KEY24
          RESET     KEY24
          READ      TEMPFILE,KEY24;;
.
          DISPLAY   *P45:24,"U/R Number :";
          GOTO      READTMP
.
LOOPS     PACK      KEY61,SP30,SP30,SP1
          RESET     KEY61
          READ      TEMPFILE,KEY61;;
.
          DISPLAY   *P40:24,"Patient Name :";
.
READTMP   MOVE      ZERO,OVRCD
          READKS    TEMPFILE;PSNAME,PGNAME,ADMNO:
                             DUNIQUE,URNO,ACLAIM,ACLSS:
                             PTITL,ADATE,ATYPE,AUSR2
          CALL      OVERCOND IF OVER
          MOVE      ADMNO,DADMNO
          MOVE      URNO,DURNO
          BRANCH    OVRCD,ENDP
.
          BRANCH    OPTION,GENRPT2,GENRPT3
          DISPLAY   *P55:24,*EL,*V2LON,PSNAME;
          GOTO      GENRPT4
.
GENRPT2   DISPLAY   *P64:24,*EL,*V2LON,DADMNO;
          GOTO      GENRPT4
.
GENRPT3   DISPLAY   *P58:24,*EL,*V2LON,DURNO;
.
GENRPT4   CALL      PRTDATA
          GOTO      READTMP
.
.         OVER condition found.. Print last line of -- and display message
.
ENDP      COMPARE   ZERO,CPAGENO
          GOTO      NODSCH IF EQUAL
.
          COMPARE   "55",CLNO
          GOTO      ENDP1 IF LESS
          CALL      HEAD
          GOTO      ENDP2
.
ENDP1     CALL      PAGEND
.
ENDP2     PRINT     *N,"Total number of Discharges = ",DSCHRGS,*N,*N:
                    *N,"*** End of Report ***"
.
          DISPLAY   *P1:24,*EL,*P24:24,*EL,*V2LON:
                    "** Report Generation Completed **",*W2;
.
          CALL      KILLIDX
          GOTO      START
.
.         PRINT ACTUAL DATA 
.
PRTDATA   COMPARE   "56",CLNO
          CALL      HEAD IF NOT LESS
.
          MOVE      PGNAME,DIM21
          MOVE      DTIME,DIM5
          MOVE      DIM5,DSTIM
          MOVE      ATIME,DIM5
          MOVE      DIM5,ADTIM
.
          UNPACK    ADATE,XCENT2,XYEAR2,XMON2,XDAY2
.
          PRINT     *1,"|",PSNAME,SP1,PTITL,SP1,DIM21,*50,"|  ",ACLAIM:
                    *57,"|   ",ACLSS,*66,"|  ",AUSR2,*74,"|   ",ATYPE:
                    *83,"| ",XDAY2,"/",XMON2,"/",XCENT2,XYEAR2:
                    *96,"| ",DURNO,*107,"| ",DADMNO,*118,"|"
.
          ADD       ONE,CLNO
          RETURN
+
.
.         Last Line on the page
.
PAGEND    PRINT     "*---------------------------------------------------":
                    "--------------------------------------------------------":
                    "---------*"
          ADD       ONE,CLNO
          RETURN
.
. ------  Headings for output  ------
.
HEAD      COMPARE   ZERO,CPAGENO
          CALL      PAGEND IF NOT EQUAL
.
          MOVE      ONE,CNOUNDLN
          CALL      IBACLOCK
          CALL      HEAD132
          ADD       ONE,CLNO
.
          IF        IBCNMHOS=1
            PRINT     *40,"Hospital Id :",HOSNAME
            ADD       ONE,CLNO
          ENDIF
.
          BRANCH    OPTION OF PRNTA,PRNTB,PRNTC
.
PRNTA     PRINT     *N,*40,"(Admission Number Sequence)";
          GOTO      PRTCONT
.
PRNTB     PRINT     *N,*40,"(U/R Number Sequence)";
          GOTO      PRTCONT
.
PRNTC     PRINT     *N,*40,"(Surname Sequence)";
.
PRTCONT   PRINT     *N,*40,"Discharges for ":                          *C-90201
                    XDAY1,"/",XMON1,"/",XCENT1,XYEAR1,*N               *C-90201
.
          CALL      PAGEND
.
          PRINT     *1,"|   -------------",*20,"Patient Name  -------------":
                    *50,"| Comp.",*57,"| Patient",*66,"| Charge":
                    *74,"| Patient",*83,"|",*96,"|",*107,"|",*118,"|",*N:
.
                    *1,"| Surname",*29,"Given",*50,"| Code ":
                    *57,"|  Cat. ",*66,"| Stat. ":
                    *74,"| Class.",*83,"| Adm Date":
                    *96,"|  U/R No.",*107,"|  Adm No.",*118,"|"
.
          CALL      PAGEND
          MOVE      NINE,CLNO
          RETURN
+
.
.         Deleting an index file based on PORT
.
KILLIDX   CLOSE     TEMPFILE
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMET,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          RETURN
+
.
CREAIDX   LOAD      SRTKEY USING OPTION OF SORT1,SORT2,SORT3
.
          PREP      PSTROL,FNAMER
          WRITE     PSTROL,SEQ;"isbuild ",FNAMET," 94 ",SRTKEY
          WEOF      PSTROL,SEQ
.
          MOVE      ONE,STATUS
.
          CLEAR     CMDLINE
          APPEND    "sh ",CMDLINE
          APPEND    FNAMER,CMDLINE
          APPEND    ".txt",CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
          BRANCH    STATUS TO INDOK
.
          DISPLAY   *P20:24,*B,*V2LON,"** The Index File not created **",*W3;
.
INDOK     CLOSE     PSTROL,DELETE
          RETURN  
+
.
ENDIT     CHAIN     PGM
          STOP
.
.==============================================================================
.
          INCLUDE   STD001IO 
.
          INC       PATHSPKY
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC
          INC       PATCODIO/INC
          INC       PATDSCIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATTRNIO/INC
          INC       PMSVX1IO/INC
          INC       PATHSPIO/INC
          

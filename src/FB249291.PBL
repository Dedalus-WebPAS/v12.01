.******************************************************************************
.* System    :   Patient Management System                                    *
.* Program   :   FB249291                                                     *
.* Desc      :   Copy/update 'Billing Comments' from viscmtaf                 *
.******************************************************************************
.* Date      :   21/04/2015                                                   *
.* Author    :   J.Tan                                                        *
.* Function  :   This program will copy/update 'Billing Comments'             *
.*               (Visit Note Type - 003) from 'viscmtaf' to the new           *
.*               file ('vismdtaf' and 'vismtxaf').                            *
.*               Checking if it's already been copied previously              *
.* Mods      :                                                                *
.******************************************************************************
.* V10.05.00 :   21/04/2015 J.Tan          CAR 291099                         *
.*               Copy Inpat Billing Comments to vismdtaf file                 *
.******************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       VISCMTFD/INC
          INC       VISMDTFD/INC
          INC       VISMTXFD/INC
          INC       PATVISFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
DIM2A     DIM       2
DIM2B     DIM       2
DIM2C     DIM       2
FORM6     FORM      6
.
LIN1BLNK  FORM      1
NOTETYPE  DIM       3
NOTENUMB  FORM      6
CMTKEY14  DIM       14
RECNUM    FORM      8
.
SAVKEY17  DIM       17
SAVKEY20  DIM       20
SNOTENUM  DIM       6
PRNTFLAG  FORM      1
.
PRGID     INIT      "FB249291"
PRGNAM    INIT      "Billing Comments Conversion "
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
.
          CALL      PRNT0000               * Print only ?
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                * display heading
.
          OPEN      VISCMTA1,"viscmtaf"
          OPEN      VISMDTA1,"vismdtaf"
          OPEN      VISMTXA1,"vismtxaf"
          OPEN      PATVISA1,"patvisaf"
.
          MOVE      ZERO,RECNUM
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF:
                          ". Billing Comments(Comment Type=003)"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION            * exit selected ?
          GOTO      OPTN9500 IF EQUAL       * yes
.
          BRANCH    COPTION,OPTN2000
.
          BEEP
          GOTO      OPTN0500
.
OPTN2000  MOVE      "003",NOTETYPE          * Billing Notes/Comments
          GOTO      OPTN9000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.         Print only option
.**************************************************************************
PRNT0000  MOVE      ONE,PRNTFLAG                 * Default to print only
          KEYIN     *P1:10,*EL,"Do you want to Print only (Y/N) ? ":
                    ANS;
          REP       "yYnN",ANS
          CMATCH    "Y",ANS
          GOTO      PRNT9999 IF EQUAL
          CMATCH    "N",ANS
          GOTO      PRNT1000 IF EQUAL
          BEEP
          GOTO      PRNT0000
.
PRNT1000  MOVE      ZERO,PRNTFLAG
PRNT9999  RETURN
+
.**************************************************************************
.*                  CLVSMD00                          Called by: PROC0000 *
.*                                                                        *
.**************************************************************************
CLVSMD00  MOVE      SP70,VSMDVISN
          MOVE      SP70,VSMDTYPE
          MOVE      SP70,VSMDNOTE
          MOVE      SP70,VSMDDATE
          MOVE      SP70,VSMDTIME
          MOVE      SP70,VSMDUSER
          MOVE      SP70,VSMDOCCG
          MOVE      ZERO,VSMDDELT
          MOVE      SP70,VSMDDDAT
          MOVE      SP70,VSMDDTIM
          MOVE      SP70,VSMDDUSE
          MOVE      SP70,VSMDDOCC
          MOVE      SP70,VSMDSPAR
.
CLVSMD99  RETURN
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
PROC0000  MOVE      ONE,NOTENUMB            * Note number
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,*EL,"Record No. :";
.
          PRINT    *1,"Old Comments have been copied to the following Patients:"
          PRINT     *N,*1,"U/R No",*15,"Adm.No"
.
          PACK      KEY8,SP70
          CALL      RDSVISA1
PROC1000  CALL      RDKVISA1                * Check all patvisaf records
          BRANCH    OVRCD,PROC9200
.
          ADD       ONE,RECNUM              * Update record counter
          IF        (RECNUM%1000) = 0
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          MOVE      ZERO,LIN1BLNK           * Set line 1 blank to no
.
          PACK      KEY14,PVIBILL,NOTETYPE,SP70
          CALL      RSVSCMT1
PROC1100  CALL      RKVSCMT1                * Check for Notes/Comments
          BRANCH    OVRCD,PROC1000
.
          MATCH     DPVIBILL,VSCTVIST
          GOTO      PROC1000 IF NOT EQUAL
.
          MATCH     NOTETYPE,VSCTTYPE
          GOTO      PROC1000 IF NOT EQUAL
.
          MATCH     "  1",VSCTLINE
          IF        @EQUAL
            MATCH    SP70,VSCTDATA
            IF       @EQUAL | @EOS
              MOVE     ONE,LIN1BLNK         * Line 1 is blank so skip 
              GOTO     PROC1100
            ENDIF
          ENDIF
.
          MATCH     "  2",VSCTLINE
          IF        @EQUAL
            MATCH    SP70,VSCTDATA
            IF       @EQUAL | @EOS
              BRANCH   LIN1BLNK,PROC1100    * Line 1 & 2 are blank so skip 
            ENDIF
          ENDIF
.
          PACK      CMTKEY14,VSCTVIST,VSCTTYPE,VSCTLINE,SP70 
.
          CALL      CCOP0000                * Check if it has been copied
          BRANCH    EXIT,PROC1000           * already being copied
.
          MOVE      CMTKEY14,KEY14
          CALL      RDVSCMT1
          BRANCH    OVRCD,PROC1000
.
          PRINT     *1,PVIURNO,*15,PVIBILL
.
          BRANCH    PRNTFLAG,PROC1000       * Print only
.
.         Create/update Notes/Comments header record
.
          PACK      KEY17,VSCTVIST,VSCTTYPE,NOTENUMB,SP70 
          CALL      RDVSMDT1                * header record exists?
          IF        OVRCD=0
            CALL      FMDT0000              * Fixed existing note number
          ENDIF
.
.*---------------------------------------------------------------------
.         Create a header record -
.         the first Note number is for comments from  viscmtaf file
.*---------------------------------------------------------------------
.
          PACK      KEY17,VSCTVIST,VSCTTYPE,NOTENUMB,SP70
          CALL      RDVSMDT1                * header record exists?
          COMPARE   ZERO,OVRCD
          GOTO      PROC9000 IF EQUAL       * Note number one shouldn't exist
.
          CALL      CLVSMD00
.
          MOVE      VSCTVIST,VSMDVISN
          MOVE      VSCTTYPE,VSMDTYPE
          MOVE      NOTENUMB,VSMDNOTE
.
          MATCH     SP70,VSCTUKEY
          IF        !@EQUAL
            UNPACK    VSCTUKEY,VSMDDATE,DIM2A,D1,DIM2B,D1,DIM2C,VSMDUSER
            MATCH     ":",D1
            IF        !@EQUAL
              UNPACK    VSCTUKEY,VSMDDATE,DIM2A,DIM2B,DIM2C,VSMDUSER
            ENDIF
            PACK      VSMDTIME,DIM2A,COLON,DIM2B,COLON,DIM2C,SP70
          ENDIF
.
          PACK      KEY17,VSMDVISN,VSMDTYPE,VSMDNOTE,SP70
          CALL      WRVSMDT1
.
          GOTO      PROC8000
.
.         Reposition back to viscmtaf file
.
PROC7000  CALL      RKVSCMT1                * Check for Notes/Comments
          BRANCH    OVRCD,PROC1000
.
          MATCH     DPVIBILL,VSCTVIST
          GOTO      PROC1000 IF NOT EQUAL
.
          MATCH     NOTETYPE,VSCTTYPE
          GOTO      PROC1000 IF NOT EQUAL
.
.         Create/update Notes/Comments Free Text record...
.
PROC8000  PACK      KEY20,VSCTVIST,VSCTTYPE,NOTENUMB,VSCTLINE,SP70
          CALL      RDVSMTX1
          IF        OVRCD=1
            MOVE      VSCTVIST,VSMTVISN
            MOVE      VSCTTYPE,VSMTTYPE
            MOVE      NOTENUMB,VSMTNOTE
            MOVE      VSCTLINE,VSMTUNIQ
            MOVE      VSCTDATA,VSMTCMNT
            MOVE      SP70,VSMTSPAR
.
            PACK      KEY20,VSMTVISN,VSMTTYPE,VSMTNOTE,VSMTUNIQ,SP70
            CALL      WRVSMTX1
          ENDIF
.
          GOTO      PROC7000
.
PROC9000  PRINT     *1,"Billing Comments from viscmtaf not written for Visit: ":
                    VSCTVIST
.
          GOTO      PROC1000
.
PROC9200  CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P15:20,*V2LON,RECNUM,*HOFF:
                    *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS:
                    *P1:24,*EL;
          CALL      HITENTER
PROC9999  RETURN
+
.**************************************************************************
.*                               CCOP0000              Called by: PROC0000*
.*        Check if it had been copied                                     *
.**************************************************************************
CCOP0000  MOVE      ZERO,EXIT
          PACK      KEY14,PVIBILL,NOTETYPE,SP70
          CALL      RSVSCMT1
CCOP0200  CALL      RKVSCMT1                * Check for Notes/Comments
          BRANCH    OVRCD,CCOP9000
.
          MATCH     DPVIBILL,VSCTVIST
          GOTO      CCOP9000 IF NOT EQUAL
.
          MATCH     NOTETYPE,VSCTTYPE
          GOTO      CCOP9000 IF NOT EQUAL
.
          PACK      KEY17,VSCTVIST,VSCTTYPE,NOTENUMB,SP70 
          CALL      RDVSMDT1                * header record exists?
          BRANCH    OVRCD,CCOP9200
.
.         Check every line of comments
.
          PACK      KEY20,VSMDVISN,VSMDTYPE,NOTENUMB,SP70
          CALL      RSVSMTX1
CCOP1000  CALL      RKVSMTX1
          BRANCH    OVRCD,CCOP9200
.
          MATCH     VSMTVISN,VSMDVISN
          GOTO      CCOP9200 IF NOT EQUAL
          MATCH     VSMTTYPE,VSMDTYPE
          GOTO      CCOP9200 IF NOT EQUAL   * Not billing comment
          MATCH     "     1",VSMTNOTE
          GOTO      CCOP9200 IF NOT EQUAL   * different Note number
.
          MATCH     VSCTDATA,VSMTCMNT       * Same comments?
          GOTO      CCOP1000 IF NOT EQUAL
.
          GOTO      CCOP0200
.
CCOP9000  MOVE      ONE,EXIT
          GOTO      CCOP9999
.
CCOP9200  MOVE      ZERO,EXIT
CCOP9999  RETURN
+
.**************************************************************************
.*                               FMDT0000              Called by: PROC0000*
.*        Increase the existing Note number to next number                *
.**************************************************************************
FMDT0000  MOVE      SP70,SNOTENUM
          PACK      KEY11,VSCTVIST,VSCTTYPE,SP70
.
          PACK      KEY17,VSCTVIST,VSCTTYPE,Z70
FMDT1000  CALL      RSVSMDT1                * header record exists?
          CALL      RPVSMDT1
          BRANCH    OVRCD,FMDT3000
.
          MATCH     VSCTVIST,VSMDVISN
          GOTO      FMDT3000 IF NOT EQUAL
.
          MATCH     VSCTTYPE,VSMDTYPE
          GOTO      FMDT3000 IF NOT EQUAL
.
          MATCH     SP70,VSMDNOTE
          GOTO      FMDT1000 IF EQUAL
.
          PACK      SAVKEY17,VSMDVISN,VSMDTYPE,VSMDNOTE,SP70
          MOVE      VSMDNOTE,SNOTENUM
.
          MOVE      ZERO,FORM6
          MOVE      VSMDNOTE,FORM6
          ADD       ONE,FORM6
          MOVE      FORM6,VSMDNOTE
          CALL      UPVSMDT1
.
FMDT3000  MATCH     SP70,SNOTENUM
          GOTO      FMDT9999 IF EQUAL
.
          UNPACK    KEY11,VSMDVISN,VSMDTYPE
          PACK      KEY20,VSMDVISN,VSMDTYPE,SNOTENUM,SP70
FMDT4000  CALL      RSVSMTX1
          CALL      RKVSMTX1
          BRANCH    OVRCD,FMDT8000
.
          MATCH     VSMDVISN,VSMTVISN
          GOTO      FMDT8000 IF NOT EQUAL
.
          MATCH     VSMDTYPE,VSMTTYPE
          GOTO      FMDT8000 IF NOT EQUAL
.
          MATCH     SNOTENUM,VSMTNOTE             * Same note number?
          GOTO      FMDT8000 IF NOT EQUAL
.
          PACK      SAVKEY20,VSMTVISN,VSMTTYPE,VSMTNOTE,VSMTUNIQ,SP70
.
          MOVE      VSMDNOTE,VSMTNOTE
          CALL      UPVSMTX1
.
          MOVE      SAVKEY20,KEY20
          GOTO      FMDT4000
.
FMDT8000  MOVE      SAVKEY17,KEY17
          MOVE      SP70,SNOTENUM
          GOTO      FMDT1000
.
FMDT9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       VISCMTIO/INC
          INC       VISMDTIO/INC
          INC       VISMTXIO/INC
          INC       PATVISIO/INC

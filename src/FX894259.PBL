.***************************************************************************
.* System    :   Patient                                                   *
.* Program   :   FX894259                                                  *
.* Desc      :   Write missinge EDWARD UPI records                         *
.***************************************************************************
.* Date      :   29/07/2020                                                *
.* Author    :   Jill Parkinson                                            *
.* Function  :   This program will loop watsusaf and write records to      *
.*               watchaaf for edward trigger information if required       *
.* Modifications  :                                                        *
.* V11.00.01 31/07/2020 Jill Parkinson Task 0894259                        *
.*           Mods to only compare wtsufdat to wtchcval as to date may have *
.*           been added later                                              *
.* V11.00.00 29/07/2020 Jill Parkinson Task 0894259                        *
.*           Created program                                               *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       WATSUSFD/INC
          INC       WATCHAFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
RECNUM    FORM      8
.
PRGID     INIT      "FX894259"
PRGNAM    INIT      "Fix EDWARD WL suspension trigger records"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      WATSUSA1,"watsusaf"
          OPEN      WATCHAA1,"watchaaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Print only":
                    *P1:6,*V2LON,ONE,*HOFF,". Run and Update"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000,OPTN9000    * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
PROC0000  DISPLAY   *P1:15,*EL,"Processing WL suspension record : ";
.
          PACK      KEY21,SP70
          CALL      RSWTSUS1
PROC1000  CALL      RKWTSUS1                       * Loop alternate id
          BRANCH    OVRCD,PROC9000
.
          DISPLAY   *P25:15,*EL,*V2LON,WTSUURNO
.
          PACK      KEY35,WTSUURNO,WTSUCODE,WTSUCNT,SP70
          CALL      RSWTCHA1
PROC1500  CALL      RKWTCHA1
          BRANCH    OVRCD,PROC2000
.
          MATCH     WTSUURNO,WTCHURNO
          GOTO      PROC2000 IF NOT EQUAL
.
          MATCH     WTSUCODE,WTCHPROC
          GOTO      PROC2000 IF NOT EQUAL
.
          MATCH     DWTSUCNT,WTCHPCNT
          GOTO      PROC2000 IF NOT EQUAL
.
          MATCH     "22",WTCHUPTY
          GOTO      PROC1500 IF NOT EQUAL
.
          UNPACK    WTCHCVAL,KEY8
          MATCH     KEY8,WTSUFDAT
          GOTO      PROC1000 IF EQUAL * trigger record already exists
          GOTO      PROC1500
.
PROC2000  PACK      WTCHURNO,WTSUURNO,SP70
          PACK      WTCHPROC,WTSUCODE,SP70
          PACK      WTCHPCNT,WTSUCNT,SP70
.
          PACK      WTCHDATE,WTSUFDAT
          REPLACE   " 0",WTCHDATE
          PACK      WTCHTIME,ZERO,ZERO,COLON,ZERO,ZERO,COLON,ZERO,ONE
.
          PACK      WTCHREAS,SP70
          MOVE      WTSUUSER,WTCHUSID
          PACK      WTCHOVAL,SP70
          PACK      WTCHCVAL,WTSUFDAT,WTSUTDAT,WTSULSTS,WTSUREAS,DWTSUCNT,SP70
.
          PACK      KEY35,WTCHURNO,WTCHPROC,WTCHPCNT,WTCHDATE,WTCHTIME,SP70
          CALL      RDWTCHA1
          IF        OVRCD=1
            IF        COPTION=2
              CALL      WRWTCHA1
              PRINT     *1,"Added record for ",WTCHURNO,WTCHPROC,WTCHPCNT:
                        WTCHDATE,WTCHTIME
            ELSE
              PRINT     *1,"Record will be added for ",WTCHURNO,WTCHPROC:
                        WTCHPCNT,WTCHDATE,WTCHTIME
            ENDIF
          ENDIF
.
          GOTO      PROC1000
.
PROC9000  DISPLAY   *P1:20,*EF,"WL Suspension EDWARD Update Complete.  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       WATSUSIO/INC
          INC       WATCHAIO/INC

. *****************************************************************************
. *              P R O G R A M  S U M M A R Y                                 *
. *              -------------  -------------                                 *
. *                                                                           *
. *     PROGRAM NAME:     NZPBIL35                                            *
. *                                                                           *
. *     FUNCTION:         INVOICE AUDIT LISTING WITH CASH                     *
. *                                                                           *
. *     DATE WRITTEN:     04/09/86                                            *
. *                                                                           *
. *     AUTHOR:           Graeme Williams (I.B.A)                             *
. *                                                                           *
. *     MODIFICATIONS:                                                        *
. *        V12.00.01 15/05/2025 Nikitha B     TSK 0955096                     *
. *                  Alphanumeric changes                                     *
. *        V10.13.01 07/12/2013 J.Tan         TSK 0859743                     *
. *                  Mod for OTRECTYP=6 for Theatre item                      *
. *        V9.11.03  23/04/09 J.Tan    CAR 194893                             *
. *                  Mods to print Cancelled admission invoices               *
. *        V9.11.01  29/01/09 J.Tan    CAR 188549                             *
. *                  Fixed GST journals                                       *
. *                  Mods to print rounding total                             *
. *        V9.10.01  24/07/08 J.Tan    CAR 170967                             *
. *                  Mods for SX Billing                                      *
. *        V9.08.05  21/02/08 J.Tan    CAR 160240                             *
. *                  Fixed NZ procedure patDTR record to be included in THEA. *
. *        V9.08.04  13/06/07 J.Tan    CAR 142173                             *
. *                  Mods for Emergency event                                 *
. *        V9.08.03  02/03/07 J.Tan    CAR 130473                             *
. *                  Fixed problem with GST from Invoice total                *
. *        V9.08.02  09/02/2007  J.Tan         CAR 120001                     *
. *                  Mods for NZ Private billing                              *
. *        V9.08.01  12/12/06 J.Tan    CAR 127726                             *
. *                  Mods for Johns Hopkins GST Discount                      *
. *        V9.07.01  07/09/06 J.Tan    CAR 103365                             *
. *                  Mods for Johns Hopkins discount and rounding             *
. *        V9.04.02  03/08/05 J.Tan    CAR 62750                              *
. *                  Removed redefined amount variables                       *
. *        V9.04.01  23/08/2004 J.Tan   CAR 52835                             *
. *                  Mods for Multi hospital                                  *
. *        V9.03.03  02/06/2004 J.Tan   CAR 50396                             *
. *                  Mods to check for credit notes                           *
. *        V9.03.02  13/08/2003 Lina Vo       CAR 41196                       *
. *                  Recompiled for big changes in PATSELFN.                  *
. *        V9.03.01  11/06/2003 Dean Cramer   CAR 38200                       *
. *                  Remove web by-pass included in error                     *
. *        V9.02.01  24/04/2003 Dean Cramer   CAR 38200                       *
. *                  Fix entry range heading for web                          *
. *        V5.08.04  05/12/2000  Caleb Sharman                                *
. *                  Mods to invoice number displays                          *
. *        V5.08.03  28/08/2000  Caleb Sharman                                *
. *                  Changed Health Fund variables to be 8 chars              *
. *                   V5.08.02  01/05/2000  J.Tan                             *
. *                             Fixed report for not using Aust.GST           *
. *                   V5.08.B04 18/02/2000  J.Tan                             *
. *                             Mods to include GST                           *
. *                   V5.08.B01 08/01/2000  Steve Armstrong                   *
. *                             Mods to use PATSTRYR to calculate first day   *
. *                             of financial year                             *
. *****************************************************************************
. *                   V5.07.01  23/04/1999  J.Tan                             *
. *                             Mods to include century                       *
. *                   V5.07.B02 27/01/1999  Nicole Harrington 507 rebounds    *
. *                             Mods to print century on date range           *
. *                   V5.07.B01 11/11/1998  Jim Stathopoulos                  *
. *                             507 Changes                                   *
. *****************************************************************************
. *                       V4.01 22/11/88 Graeme Williams                      *
. *                             Allow for PINVSYS = 4                         *
. *                       V4.02 31/01/89 Karin Peak (I.B.A)                   *
. *                             Added PATSELFN                                *
. *                       V4.03 10/02/89 Graeme Willaism                      *
. *                             Make OUTDTRO site dependent                   *
. *                       V5.01 14/01/89 J.Tan                                *
. *                             Fixed AAEIODEA                                *
. *                       V5.02 13/04/89 Graeme Williams                      *
. *                             New index added to Dtrans                     *
. *                             SRF 5645                                      *
. *                       V5.03 05/05/89 Graeme Williams                      *
. *                             Added PINVCADM to inv file                    *
. *                             SRF 4219                                      *
. *                       V5.04 15/05/89 DIG                                  *
. *                             Changed to standard and                       *
. *                             modified print for 8 digit                    *
. *                             admission numbers                             *
. *                       V5.05 16/05/89 Karin Peak                           *
. *                             Split AAEDETFD into DEA & B                   *
. *                       V5.06 27/05/89 J.Tan                                *
. *                             Fixed Mother Adm. to form8                    *
. *                             (PATMI1FD).     SRF 100750                    *
. *                       V5.07 03.06.89 S.Barcham                            *
. *                             Split OUTBOK & OUTMAS incl                    *
. *                             SRF 100751                                    *
. *                       V5.08 06/06/89 J.Tan                                *
. *                             Mods Locking master file                      *
. *                             (PATIOMAS)     SRF 100809                     *
. *                       V5.09 24.06.89 S.Barcham                            *
. *                             Recompiled for OUTBB1FD                       *
. *                       V5.10 30/06/89 DIG                                  *
. *                             Re-Compiled for changes to                    *
. *                             OUTSITFD                                      *
. *                                                                           *
. *                    V5.01.01 06/07/89  S. O'Gorman                         *
. *                             Converted for N.Z.                            *
. *                    V5.01.02 20/11/89 J.Tan                                *
. *                             Added other financial opt.                    *
. *                             in PATSELFN.   SRF 102252                     *
. *                    V5.01.03 11/07/90 Paul Duncan                          *
. *                             recomp for OUTBOA & OUTBOB                    *
. *                   V5.01.121 27/11/90 Glen Hobby                           *
. *                            Recompiled for changes to                      *
. *                            PATMX1FD                                       *
. *       V5.01.122 12/06/92  Graeme Williams               SRF 114865        *
. *                 Changes for 9 character misc items                        *
. *                                                                           *
. *       V5.01.123 23.02.92 Neeriem "I Hate Support" Dye (I.B.A.)            *
. *                 Modify to use standard routine KEYDATE                    *
. *       V5.01.124 10/03/93 Steve O'Gorman                                   *
. *                 Incremented Version for Tolive                            *
. *       V5.01.125 24/03/1994  Steve Armstrong                               *
. *                 Mods for Mt. Alvernia to include doctors charges when     *
. *                 looping through patdtraf (TRECTYPE = 4)                   *
. *       V5.01.126 11/04/1994  Steve Armstrong                               *
. *                 Changed PATCODDS to PATCODDS                              *
. *                 Mods to standardise                                       *
. *       V5.03.B1  06/06/95  Paul Howells                  SRF 136735        *
. *                 Recompiled for PATSELFN                                   *
. *****************************************************************************
.
          INC       STD001FD
.
          INC       RSHWEBDF
          INC       AAEDE1FD/INC
          INC       AAEDEBFD/INC
          INC       AAEDTRFD/INC
.
          INC       IBACONFD/INC
          INC       IBASECFD/INC
          INC       IBAPASFD/INC
.
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
          INC       OUTDTRFD/INC
          INC       OUTSITFD/INC
.
          INC       PATCODFD/INC
          INC       PATDRGFD/INC
          INC       PATDTRFD/INC
          INC       PATHSPFD/INC
          INC       PATINVFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATSELDT/INC
          INC       PATTRNFD/INC
          INC       PATVISFD/INC
          INC       PMSVX1FD/INC
.
. LOCAL VARIABLES
. ---------------
AFIRST    DIM       6
ALAST     DIM       6
.
CDESC     DIM       20
CFEETYP   FORM      1
CODE      DIM       2
CSTRTYR   DIM       8                            * for PATSTRYR
CRNTOTAL  FORM      12.2
.
DIM8      DIM       8
DIM30     DIM       30
.
ERRFLAG   FORM      1
.
FIRST     DIM       8
FORM7P2   FORM      7.2
FORM8P2   FORM      8.2
FORM12P2  FORM      12.2
FROMDATE  DIM       8
.
IDATET    DIM       8
.
LAST      DIM       8
LOOPFLAG  FORM      1
.
ITEMGST   FORM      12.2
PDETFLAG  FORM      1
PRTFLAG   FORM      1
PATSTCUR  DIM       4                            * for PATSTRYR
PINVBED   FORM      6
PINVADJM  FORM      12.2
PINVACCM  FORM      12.2
PINVMISC  FORM      12.2
PINVTHET  FORM      12.2
PINVTOTL  FORM      12.2
PNAME     DIM       15
PPAYCASH  FORM      12.2
PPAYJRNL  FORM      12.2
PPAYTOTL  FORM      12.2
.
RNDTOTAL  FORM      12.2
SFIRST    FORM      8
STKEY     DIM       8
STMAST    DIM       2
.
TINVACCM  FORM      12.2
TINVBED   FORM      6
TINVMISC  FORM      12.2
TINVTHET  FORM      12.2
TINVTOTL  FORM      12.2
TINVADJM  FORM      12.2
TINVGSTM  FORM      12.2
TODATE    DIM       8
TOTONLY   FORM      1
TOTCRNT    FORM     12.2
TOTINVC    FORM     12.2
TOTINVS    FORM     12.2
GSTINVC    FORM     12.2
GSTINVS    FORM     12.2
TPAYCASH  FORM      12.2
TPAYJRNL  FORM      12.2
TPAYTOTL  FORM      12.2
COTHSFN   FORM      1
.
.
. PROGRAM CONSTANTS
. -----------------
FIVE9     FORM      "59"
.
.
PRGID     INIT      "NZPBIL35" 
PRGNAM    INIT      "Invoice Audit Listing"
VERSION   INIT      "V12.01.00"
+
.
.         START OF PROGRAM
.
          CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patinvrf";
          OPEN      PATINVR1,"patinvrf"
.
          DISPLAY   *P64:24,"patinvrf";
          OPEN      PATINVR2,"patinvrf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdrgaf";
          OPEN      PATDRGA2,"patdrgaf"
.
          DISPLAY   *P64:24,"patdtraf";
          OPEN      PATDTRA1,"patdtraf"
.
          DISPLAY   *P64:24,"aaedtref";
          OPEN      AAEDTRE1,"aaedtref"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA1,"patvisaf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"outsitaf";
          OPEN      OUTSITA1,"outsitaf"
.
          DISPLAY   *P64:24,"aaede1af";
          OPEN      AAEDE1A1,"aaede1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TEN9;*212,CFEETYP
          READ      CONTROLF,ZERO;*43,IBCNMHOS,*85,IBCNUGST
+
.
.        START OF PROGRAM
.
.           MAIN        MENU
.
.             SELECT OPTION
.
START   MOVE       ZERO,CPAGENO
        MOVE       ONE,ERRFLAG
        MOVE       SP30,DIM30
        MOVE       ZERO,PDETFLAG
.
        HLINE     *G37,2,51,80
        DISPLAY   *P1:4,*EF:
                  *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                  *P1:5,*V2LON,ONE,*HOFF," = Invoice Number Sequence":
                  *P1:6,*V2LON,TWO,*HOFF," = Invoice Date Sequence"
.
REKSTRT KEYIN     *P1:8,"Option : ",*EL,*V2LON,OPTION
.
        COMPARE   ZERO TO OPTION
        GOTO      ENDIT IF EQUAL
.
BRNCH   BRANCH    OPTION OF KINVR,KDATER,PDET000
        BEEP
        GOTO      REKSTRT
.
NOMAST  DISPLAY   *P1:24,*EL,*B,"No Patients on File.  ";
        CALL      HITENTER
        GOTO      START
.
PDET000 MOVE      TWO,OPTION
        MOVE      ONE,PDETFLAG
        GOTO      KDATER
+
.
.               PRINT VIA INVOICE NUMBER
.
KINVR
        MOVE       "- Invoice Number Sequence ***",DIM30
        PACK       FIRST,SP8
        KEYIN      *P51:2,*V2LON,"- Invoice Number Sequence ",*HOFF:
                   *P1:4,*EF:
                   *P1:6,"Starting Invoice Number : ":
                   *V2LON,*DE,*JR,FIRST
.
        RESET      FIRST
        MATCH      SP8,FIRST                     * anything entered ?
        IF         @EQUAL
          DISPLAY    *P27:6,*V2LON,"Start"
        ELSE
          DISPLAY    *P27:6,*V2LON,FIRST
        ENDIF
.
ENTLST  PACK       LAST,SP8
        KEYIN      *P1:8,"  Ending Invoice Number : ":
                   *V2LON,*DE,*JR,LAST
.
        RESET      LAST
        MATCH      SP8,LAST                      * anything entered ?
        IF         @EQUAL
          DISPLAY    *P27:8,*V2LON,"Finish"
          MOVE       "99999999",LAST
        ELSE
          MATCH      FIRST,LAST
          IF         @LESS
            DISPLAY    *P1:24,*V2LON," Ending invoice must":
                       " be after or equal to starting invoice          ";
            CALL       HITENTER
            GOTO       ENTLST
          ELSE
            DISPLAY    *P27:8,*V2LON,LAST
          ENDIF
        ENDIF
.
        MATCH      SP8,FIRST
        IF         @EQUAL
          MOVE       SP8,STKEY
        ELSE
          MOVE       FIRST,SFIRST
          SUB        ONE,SFIRST
          IF         SFIRST < 0
            MOVE       SP8,STKEY
          ELSE
            MOVE       SFIRST,STKEY
          ENDIF
        ENDIF
        GOTO       MASTN2
.
.       Key in date range
.
KDATER  MOVE       "- INVOICE DATE SEQUENCE ***",DIM30
        DISPLAY    *P51:2,*V2LON,"- Invoice Date Sequence ",*HOFF:
                   *P1:4,*EF:
                   *P1:6,"Starting Date :"
        MOVE       SIX,CVERT
.
        CALL       PATSTRYR                      * get start of financial year
.
        UNPACK     CSTRTYR,XCENT,XYEAR,XMON,XDAY
        MOVE       XDAY,IDAY
        MOVE       XMON,IMON
        MOVE       XYEAR,IYEAR
        MOVE       XCENT,ICENT
.
        CALL       KEYDTE
        PACK       FROMDATE WITH ICENT,IYEAR,IMON,IDAY
        REP        " 0",FROMDATE
.
        DISPLAY    *P1:8,"  Ending Date :"
        MOVE       EIGHT,CVERT
KDATE2  MOVE       CDD,IDAY
        MOVE       CMM,IMON
        MOVE       CYY,IYEAR
        MOVE       CCC,ICENT
        CALL       KEYDTE
        PACK       TODATE WITH ICENT,IYEAR,IMON,IDAY
        REP        " 0",TODATE
.
        MATCH      FROMDATE,TODATE
        GOTO       MASTN2 IF NOT LESS
.
        DISPLAY    *P1:24,*EL,*B,"End Date must be >= Start Date.  ";
        CALL       HITENTER
        GOTO       KDATE2
.
MASTN2  KEYIN      *P1:10,*EL,"Totals Only to be Printed (":
                              *V2LON,"Y",*HOFF,"/":
                              *V2LON,"N",*HOFF,") ? ",*V2LON,ANS
        REP        UPPLOW,ANS
.
        CMATCH     "Y" TO ANS
        GOTO       MASTN2Y IF EQUAL
.
        CMATCH     "N" TO ANS
        GOTO       MASTN2N IF EQUAL
.
        BEEP
        GOTO       MASTN2
.
MASTN2Y MOVE       ONE,TOTONLY
        GOTO       MASTN2A
.
MASTN2N MOVE       ZERO,TOTONLY
.
MASTN2A COMPARE    ONE,IBCNMHOS
        GOTO       MASTN2C IF NOT EQUAL
.
        DISPLAY    *P1:12,*EL,"Hospital Id : ";
        MOVE       TEN5,ECOL
        MOVE       "12",EVERT
        MOVE       SP3,CKYIDEF3
        MOVE       ZERO,CKYIMAND           * Not Mandatory
        OPEN       PATHSPA1,"pathspaf"
.
        CALL       PATHSPKY
        BRANCH     EXIT,MASTN2B,START
        GOTO       MASTN2C 
.
MASTN2B MOVE       "All Hospitals",PTHSNAME
        MOVE       SP10,PTHSHOSP
.
MASTN2C MOVE       ZERO,EXIT
        CALL       PATSELFN
        BRANCH     EXIT,START
        MOVE       ZERO,LOOPFLAG
.
        CALL       CONTQST
        BRANCH     CEXIT,MASTN3:                 * yes
                         BRNCH:                  * no
                         ENDIT                   * cancel
.
MASTN3  
        MOVE       ZERO,TINVBED
        MOVE       ZERO,TINVACCM
        MOVE       ZERO,TINVTHET
        MOVE       ZERO,TINVMISC
        MOVE       ZERO,TINVTOTL
        MOVE       ZERO,TINVADJM
        MOVE       ZERO,TINVGSTM
        MOVE       ZERO,TPAYCASH
        MOVE       ZERO,TPAYJRNL
        MOVE       ZERO,TPAYTOTL
        MOVE       ZERO,CRNTOTAL
        MOVE       ONE,PRTFLAG
        MOVE       ZERO,TOTINVS
        MOVE       ZERO,GSTINVS
        MOVE       ZERO,TOTCRNT
        MOVE       ZERO,RNDTOTAL
.
        BRANCH     OPTION OF STINV,STDAT
.
STINV   MOVE       STKEY,KEY8
        CALL       RDSINV1
        GOTO       STCONT
.
STDAT   PACK       KEY16 WITH FROMDATE,SP8
        CALL       RDSINV2
.
STCONT  
.       MOVE       ZERO,CPAGENO
        MOVE       ZERO,CNOUNDLN
        MOVE       "60",CLNO
        CALL       IBACLOCK
        DISPLAY    *P1:24,*EL,"Invoice ## :",*P40:24,"Date :";
.
NEXTN   BRANCH     OPTION OF RDINV,RDDAT
.
RDINV   CALL       RDKINV1
        BRANCH     OVRCD OF ENDP
.
        MATCH       PINVNO,LAST
        GOTO        ENDP IF LESS
        GOTO        NEXTN2
.
RDDAT   CALL        RDKINV2
        BRANCH      OVRCD OF ENDP
.
        MATCH       PINVDTE,TODATE
        GOTO        ENDP IF LESS
.
NEXTN2  DISPLAY     *P70:24,PINVNO;
.
        COMPARE     FSTSYS,PINVSYS
        GOTO        NEXTN IF LESS
.
        COMPARE     PINVSYS,FEDSYS
        GOTO        NEXTN IF LESS
.
        MATCH       FSTSITE,PINVSITE
        GOTO        NEXTN IF LESS
.
        MATCH       PINVSITE,FEDSITE
        GOTO        NEXTN IF LESS
.
        MATCH       SP3,FSTDEPT
        GOTO        CNTI IF EQUAL
.
        MOVE        PINVSITE,KEY6
        CALL        RDSITA1
        BRANCH      OVRCD,NEXTN
.
        MATCH       FSTDEPT,OSTSYST
        GOTO        NEXTN IF LESS
.
        MATCH       OSTSYST,FEDDEPT
        GOTO        NEXTN IF LESS
.
CNTI    MOVE        ZERO,PINVBED
        MOVE        ZERO,PINVACCM
        MOVE        ZERO,PINVADJM
        MOVE        ZERO,PINVTHET
        MOVE        ZERO,PINVMISC
        MOVE        ZERO,PINVTOTL
        MOVE        ZERO,PPAYCASH
        MOVE        ZERO,PPAYJRNL
        MOVE        ZERO,PPAYTOTL
        MOVE        ZERO,ITEMGST
        MOVE        ZERO,TOTINVC
        MOVE        ZERO,GSTINVC
.
        MOVE        PINVUR,PURNO
        MOVE        PURNO,KEY8
        CALL        RDMAST1
.
        COMPARE     ONE,LOOPFLAG
        GOTO        NEXTN3 IF NOT EQUAL
.
.       We are printing invoices for cancelled admissions, where no record in
.       visit file
.
        PACK        KEY8,PINVADM,SP8
        CALL        RDVISA1
        BRANCH      OVRCD,NEXTN4     * must be cancelled admission
        CALL        RDPMVX11
        BRANCH      OVRCD,NEXTN4
        GOTO        NEXTN
.
NEXTN3  PACK        KEY8,PINVADM,SP8
        CALL        RDVISA1
        BRANCH      OVRCD,NEXTN
        CALL        RDPMVX11
        BRANCH      OVRCD,NEXTN
.
        IF          IBCNMHOS=1
          MATCH       SP10,PTHSHOSP
          IF          !@EQUAL
            MATCH       PMVXMHOS,PTHSHOSP
            GOTO        NEXTN IF NOT EQUAL
          ENDIF
        ENDIF
.
NEXTN4  CALL        DISPDA
.
        UNPACK      PINVDTE WITH XCENT,XYEAR,XMON,XDAY
        DISPLAY     *P13:24,*V2LON,PINVNO:
                    *P47:24,XDAY,"/",XMON,"/",XYEAR;
.
        MATCH       ZEROVISN,PINVADM
        GOTO        NEXTN IF EQUAL
.
        ADD         TOTINVC,TOTINVS
        ADD         GSTINVC,TOTINVS
        ADD         GSTINVC,GSTINVS
.
        IF          PDETFLAG=1
          PRINT       *1,"PINVADM ",PINVADM," INVOICE NO: ":
                      PINVNO," DATE ",PINVDTE:
                      " AMT ",TOTINVC," TOT ",TOTINVS;
          IF          PTINCRTT<>0
            PRINT       "...CRD NOTE:",PTINCRTT;
            ADD         PTINCRTT,TOTCRNT
          ENDIF
          PRINT     *N;
        ENDIF
.
.       Check Dtran totals against invoice data
.
        IF          IBCNUGST=0 
          MOVE        PINVAMT,FORM12P2
          SUB         PTINGSTA,FORM12P2       * don't include GST
          COMPARE     PINVTOTL,FORM12P2
          CALL        INVERR IF NOT EQUAL
        ELSE
          MOVE        PINVTOTL,FORM12P2
          COMPARE     PINVTOTL,PINVAMT
          CALL        INVERR IF NOT EQUAL
          ADD         PTINGSTJ,PINVJOUR
        ENDIF
.
        MULT        SEQ,PINVJOUR
        COMPARE     PPAYJRNL,PINVJOUR
        CALL        JRNLERR IF NOT EQUAL
.
        ADD         PINVHFDA,PINVPATA
        ADD         PINVOTHA,PINVPATA
        ADD         PTINROUN,RNDTOTAL
.
        MULT        SEQ,PINVPATA
        COMPARE     PPAYCASH,PINVPATA
        CALL        CASHERR IF NOT EQUAL
.
        GOTO        NEXTN
+
.
.           OVER CONDITION FOUND..PRINT LAST LINE OF -- AND DISPLAY MESSAGE
.
ENDP     IF        IBCNUGST=3
           MOVE      TOTINVS,FORM12P2
           SUB       GSTINVS,FORM12P2
           IF        PDETFLAG=1
             PRINT     *N,*1,"Total INV=",FORM12P2:
                       "   GST=",GSTINVS:
                       *66,"     TOTAL=",TOTINVS:
                       "   CRD NOTE=",TOTCRNT
             CALL     UND132
           ENDIF
         ENDIF
.
         BRANCH       TOTONLY OF PRNTTOT
         COMPARE      ZERO,CPAGENO
         GOTO         NOMAST IF EQUAL
.
PRNTTOT  COMPARE      ONE,LOOPFLAG
         GOTO         PRNT100 IF NOT EQUAL
.
         COMPARE      ZERO,TINVTOTL
         GOTO         PRNT100 IF NOT EQUAL
         COMPARE      ZERO,CRNTOTAL
         GOTO         PRNT800 IF EQUAL
.
PRNT100  COMPARE    "57",CLNO
         CALL       HEAD IF NOT LESS
.
         COMPARE    "9",CLNO
         CALL       PAGEND IF NOT EQUAL
.
         BRANCH     PRTFLAG,PRNT200
.
         PRINT      *37,"TOTALS",*43,TINVBED;
         MOVE       TINVTHET,FORM8P2
         PRINT      *67,FORM8P2;
.
         MOVE       TINVTOTL,FORM8P2
         PRINT      *89,FORM8P2;
.
         IF         RNDTOTAL<>0
           PRINT      *99,"*";
         ENDIF
         PRINT      *100,"!";
.
         MOVE       TPAYJRNL,FORM8P2
         PRINT      *111,FORM8P2;
.
         MOVE       TINVACCM,FORM8P2
         PRINT      *N,*56,FORM8P2;
.
         MOVE       TINVMISC,FORM8P2
         PRINT      *78,FORM8P2;
.
         MOVE       TPAYCASH,FORM8P2
         PRINT      *100,FORM8P2;
.
         MOVE       TPAYTOTL,FORM8P2
         PRINT      *122,FORM8P2
         IF         CRNTOTAL<>0
           PRINT      *85,CRNTOTAL,*100,"!"
         ENDIF
         GOTO       PRNT400
.
PRNT200  PRINT      *37,"TOTALS",*43,TINVBED;
.
         PRINT      *56,TINVACCM,*74,TINVMISC,*92,TPAYCASH,*110,TPAYTOTL
         IF         CFEETYP=9
           PRINT      *51,TINVADJM;
         ELSE
           IF         CFEETYP=5
             PRINT      *51,TINVGSTM;
           ENDIF
         ENDIF
         PRINT      *66,TINVTHET,*83,TINVTOTL;
.
         IF         RNDTOTAL<>0
           PRINT      *98,"*";
         ENDIF
         PRINT      *101,TPAYJRNL,*118,CRNTOTAL
.
PRNT400  BRANCH     LOOPFLAG,PRNT800
.
.        Check if there is any cancelled admissions
.
         MOVE       ONE,LOOPFLAG
         GOTO       MASTN3
.
PRNT800  PRINT      *N," * Denotes Invoices Changed by Rounding Total: ":
                    RNDTOTAL
.
         PRINT      *N,*N,"*** END OF REPORT ***"
.
.        Check if any errors were found
.
         BRANCH       ERRFLAG OF ENDREP
.
         PRINT        *N,"*** Errors Found in Report. ":
                      "Contact I.B.A Immediately. ":
                      "REPORT MAY BE INACCURATE ***"
.
ENDREP   DISPLAY      *P1:24,*EL,*EL,*V2LON:
                      "** Report Generation Completed **",*W;
         GOTO         START
+
.
.
.     PRINT ACTUAL DATA 
.
DISPDA  MATCH      ZEROVISN,PINVADM
        GOTO       CANCLI IF EQUAL
.
        MOVE       PGNAME,ANS
        PACK       PNAME WITH ANS,DOT,PSNAME
.
        PACK       KEY22 WITH PINVADM,PINVNO,SP6
.
.       Loop through the appropriate DTRAN for this invoice
.
        BRANCH     PINVSYS OF AAEDTR,OUTDTR,INPDTR,OTHDTR,NEXT9
        GOTO       INPDTR
.
NEXTT   BRANCH     PINVSYS OF ANXTT,ONXTT,INXTT,ONXTT,NEXT9
        GOTO       INXTT
.
.       A + E invoice
.
AAEDTR
        CALL       RDSDTRE1
.
        MOVE       PINVADM,ADANUMB
        MOVE       ADANUMB,KEY8
        CALL       RDDETA1
        MOVE       ADACLASS,ATYPE
.
ANXTT   CALL       RDKDTRE1
        BRANCH     OVRCD OF ENDT
.
        MATCH      PINVNO,ATINVNO
        GOTO       ENDT IF NOT EQUAL
.
        MATCH      ATNUMB,PINVADM
        GOTO       ENDT IF NOT EQUAL
.
        MOVE       ATPATAMT,TPATAMTT
        MOVE       ZERO,PTDTGSTM
        IF         IBCNUGST=3
          MOVE       ATDTGSTM,PTDTGSTM    * GST for AAE
        ENDIF
        MOVE       ZERO,PTDTGSTL
        BRANCH     ATRECTYP OF MISC,PYMNT,JRNAL,PYMNT,MISC
        GOTO       ANXTT
.
.       Other financial invoice
.
OTHDTR  MOVE       SP6,PVISITE
        MOVE       PINVADM,PVIBILL
        MOVE       PVIBILL,KEY8
        CALL       RDVISA1
.
        MOVE       "out",OSTFILE
        CLOSE      OUTDTRO1
        PACK       CFNAME,OSTFILE,FILDTRO1
        OPEN       OUTDTRO1,CFNAME
.
        PACK       KEY22 WITH SP2,KEY20
        CALL       RDSDTRO1
        GOTO       ONXTT
.
.       Outpatient invoice
.
OUTDTR  MOVE       SP6,PVISITE
        MOVE       PINVADM,OBAOUTNO
        MOVE       OBAOUTNO,KEY8
        CALL       RDVISA1
.
        MOVE       "out",OSTFILE
        MOVE       PVISITE,KEY6
        CALL       RDSITA1
.
        PACK       CFNAME,OSTFILE,FILBB1A1
        CALL       OPOTBB11
.
        CLOSE      OUTDTRO1
        PACK       CFNAME,OSTFILE,FILDTRO1
        OPEN       OUTDTRO1,CFNAME
.
        MOVE       OBAOUTNO,KEY8
        CALL       RDBOKB1
        MOVE       OBACLASS,ATYPE
.
        CLOSE     OUTBB1A1
.
        CALL       RDSDTRO1
ONXTT   CALL       RDKDTRO1
        BRANCH     OVRCD OF ENDT
.
        MATCH      PINVNO,OTINVNO
        GOTO       ENDT IF NOT EQUAL
.
        MATCH      OTNUMB,PINVADM
        GOTO       ENDT IF NOT EQUAL
.
        MOVE       OTPATAMT,TPATAMTT
        IF         IBCNUGST=3 
          MOVE       OTDTGSTM,PTDTGSTM          * GST for OUT
        ENDIF
        MOVE       ZERO,PTDTGSTL
        BRANCH     OTRECTYP OF MISC,PYMNT,JRNAL,MISC,ADJM,THET
        GOTO       ONXTT
.
.       In-patient invoice
.
INPDTR  CALL       RDSDTRN1
.
        MOVE       PINVADM,KEY8
        CALL       RDMISS1
.
INXTT   CALL       RDKDTRN1
        BRANCH     OVRCD OF ENDT
.
        MATCH      PINVNO,TREF
        GOTO       ENDT IF NOT EQUAL
.
        MATCH      TADMNO,PINVADM
        GOTO       ENDT IF NOT EQUAL
.
        BRANCH     TRECTYPE OF ACCM,THET,MISC,MISC,PYMNT,JRNAL,ADJM,THET
        GOTO       INXTT
.
ADJM    ADD        TPATAMTT,PINVADJM
        ADD        TPATAMTT,PINVTOTL
        GOTO       NEXTT
.
MISC    ADD        TPATAMTT,PINVMISC
        ADD        TPATAMTT,PINVTOTL
        IF         IBCNUGST=3
          ADD        PTDTGSTM,PINVMISC
          ADD        PTDTGSTM,PINVTOTL
        ENDIF
        GOTO       NEXTT
.
ACCM    ADD        TPATAMTT,PINVACCM
        ADD        TPATAMTT,PINVTOTL
        COMPARE    THREE,IBCNUGST
        GOTO       LUMPS IF EQUAL        * GST Amt from invoice total
.
        IF         IBCNUGST=3
          ADD        PTDTGSTM,PINVACCM
          ADD        PTDTGSTM,PINVTOTL
        ENDIF
.
.       If the accomodation amount is no zero, then add in bed days,
.       otherwise check transfer file to see if this a zero rate charge
.       or an on-leave record.
.
        COMPARE    ZERO TO TPATAMTT
        GOTO       BEDS IF NOT EQUAL
.
        PACK       IDATET WITH TTCENT,TTYEAR,TTMONTH,TTDAY    * Accom. date to
        REP        " 0",IDATET
.
.       Check the first record of the accommodation to-date. If this
.       on-leave, then this record will be a "R" record or "L" record.
.
        PACK       KEY30 WITH TADMNO,IDATET,SP20
        CALL       RDSTRAN2
        CALL       RDKTRAN2
        BRANCH     OVRCD OF BEDS
.
        CMATCH     "R",TMOVE
        GOTO       LUMPS IF EQUAL
.
        PACK       IDATET WITH TTCENT,TTYEAR,TTMONTH,TTDAY    * Accom. date to
        REP        " 0",IDATET
.
BEDS    ADD        TNODAYS,PINVBED
.
LUMPS   MOVE       PTDTLSPT,FORM12P2
        ADD        PTDTLSRB,FORM12P2      * Total lumpsum
        IF         IBCNUGST=3
          ADD        PTDTGSTL,FORM12P2    * GST of lumpsum amount
        ENDIF
.
        ADD        FORM12P2,PINVACCM
        ADD        FORM12P2,PINVTOTL
        GOTO       NEXTT
.
THET    ADD        TPATAMTT,PINVTHET
        ADD        TPATAMTT,PINVTOTL
        IF         IBCNUGST=3
          ADD        PTDTGSTM,PINVTHET
          ADD        PTDTGSTM,PINVTOTL
        ENDIF
        GOTO       NEXTT
.
GVSUB   SUB        TPATAMTG,PINVACCM
        SUB        TPATAMTG,PINVTOTL
        GOTO       NEXTT
.
PYMNT   ADD        TPATAMTT,PPAYCASH
        ADD        TPATAMTT,PPAYTOTL
        GOTO       NEXTT
.
JRNAL   ADD        TPATAMTT,PPAYJRNL
        ADD        TPATAMTT,PPAYTOTL
        IF         IBCNUGST=3
          ADD        PTDTGSTM,PPAYJRNL      * GST Journal
          ADD        PTDTGSTM,PPAYTOTL      * GST Journal
        ENDIF
        GOTO       NEXTT
.
ENDT    ADD        PINVTOTL,TOTINVC
        IF         IBCNUGST=3
          ADD        PTINGSTA,PINVTOTL
          ADD        PTINGSTA,GSTINVC
        ENDIF
.
        MULTIPLY   SEQ,PPAYCASH
        MULTIPLY   SEQ,PPAYJRNL
        MULTIPLY   SEQ,PPAYTOTL
.
        BRANCH     TOTONLY OF ACCTOTS
.
        COMPARE    "57",CLNO
        CALL       HEAD IF NOT LESS
        UNPACK     PINVDTE WITH XCENT,XYEAR,XMON,XDAY
.
        PRINT      *1,PINVNO,*10,XDAY,"/",XMON,"/",XYEAR,*19,PNAME:
                   *35,PINVADM,*43,PINVBED,*52,ATYPE;
.
        BRANCH     PRTFLAG,PRTFULL
.
        MOVE       PINVACCM,FORM7P2
        PRINT      *57,FORM7P2;
.
        MOVE       PINVTHET,FORM7P2
        PRINT      *68,FORM7P2;
.
        MOVE       PINVMISC,FORM7P2
        PRINT      *79,FORM7P2;
.
        MOVE       PINVTOTL,FORM7P2
        PRINT      *90,FORM7P2;
.
        IF         PTINROUN<>0
          PRINT      *99,"*";
        ENDIF
        PRINT      *100,"!";
.
        MOVE       PPAYCASH,FORM7P2
        PRINT      *101,FORM7P2;
.
        MOVE       PPAYJRNL,FORM7P2
        PRINT      *112,FORM7P2;
.
        MOVE       PPAYTOTL,FORM7P2
        PRINT      *123,FORM7P2
        ADD        ONE,CLNO
        CALL       PCRN0000               * Print credit notes if any
        GOTO       ACCTOTS
.
PRTFULL PRINT      *56,PINVACCM,*74,PINVMISC,*92,PPAYCASH,*110,PPAYTOTL
        IF         CFEETYP=9
          PRINT      *51,PINVADJM;
        ELSE
          IF         CFEETYP=5
            PRINT      *51,PTINGSTA;
          ENDIF
        ENDIF
        PRINT      *66,PINVTHET,*83,PINVTOTL;
.
        IF         PTINROUN<>0
          PRINT      *98,"*";
        ENDIF
        PRINT      *101,PPAYJRNL,*118,PTINCRTT
        ADD        TWO,CLNO
.
ACCTOTS ADD        PINVBED,TINVBED
        ADD        PINVACCM,TINVACCM
        ADD        PINVTOTL,TINVTOTL
        ADD        PINVMISC,TINVMISC
        ADD        PINVTHET,TINVTHET
        ADD        PPAYCASH,TPAYCASH
        ADD        PPAYJRNL,TPAYJRNL
        ADD        PPAYTOTL,TPAYTOTL
        ADD        PTINCRTT,CRNTOTAL
        ADD        PINVADJM,TINVADJM
        ADD        PTINGSTA,TINVGSTM
NEXT9   RETURN
.
.       A cancelled invoice
.
CANCLI  BRANCH     TOTONLY OF CANRET
.
        COMPARE    "57",CLNO
        CALL       HEAD IF NOT LESS
.
        PRINT      *1,PINVNO,*10,"-- CANCELLED --"
        ADD        ONE,CLNO
CANRET  RETURN
+
.
.       Errors found in data. Indicates a difference between Dtran file
.       and the invoice file
.
INVERR  COMPARE    "57",CLNO
        CALL       HEAD IF NOT LESS
        PRINT      *1,"***** Invoice total for invoice ",PINVNO:
                   " may be incorrect. Invoice says $",PINVAMT:
                   ", Debtors Transaction says $",FORM12P2
        ADD        ONE,CLNO
        MOVE       ZERO,ERRFLAG
        RETURN
.
JRNLERR COMPARE    "57",CLNO
        CALL       HEAD IF NOT LESS
        PRINT      *1,"***** Journal total for invoice ",PINVNO:
                   " may be incorrect. Invoice says $",PINVJOUR:
                   ", Debtors Transaction says $",PPAYJRNL
        ADD        ONE,CLNO
        MOVE       ZERO,ERRFLAG
        RETURN
.
CASHERR COMPARE    "57",CLNO
        CALL       HEAD IF NOT LESS
        PRINT      *1,"*****    Cash total for invoice ",PINVNO:
                   " may be incorrect. Invoice says $",PINVPATA:
                   ", Debtors Transaction says $",PPAYCASH
        ADD        ONE,CLNO
        MOVE       ZERO,ERRFLAG
        RETURN
+
.
.          LAST LINE ON THE PAGE
.
PAGEND    PRINT     "----------------------------------------------":
                    "------------------------------------------":
                    "--------------------------------------------"
          ADD       ONE,CLNO
          RETURN
+
.
.                 HEADINGS FOR OUTPUT
.
HEAD      CALL      HEAD132
          IF        IBCNMHOS =1
            PRINT     *40,"Hospital : ",PTHSHOSP,"    ",PTHSNAME
          ENDIF
.
          BRANCH    OPTION OF HDINV,HDDAT
.
HDINV     MATCH     SP8,FIRST
          IF        @EQUAL
            PRINT     *40,"STARTING INVOICE : Start"
          ELSE
            PRINT     *40,"STARTING INVOICE : ",FIRST
          ENDIF
.
          MATCH     "99999999",LAST
          IF        @EQUAL
            PRINT     *40,"ENDING INVOICE   : Finish",*N
          ELSE
            PRINT     *40,"ENDING INVOICE   : ",LAST,*N
          ENDIF
.
          CALL      PRTSEL
          GOTO      HDCONT
.
HDDAT     UNPACK    FROMDATE WITH XCENT,XYEAR,XMON,XDAY
          PRINT     *40,"STARTING DATE : ",XDAY,"/",XMON,"/",XCENT,XYEAR
          UNPACK    TODATE WITH XCENT,XYEAR,XMON,XDAY
          PRINT     *40,"  ENDING DATE : ",XDAY,"/",XMON,"/",XCENT,XYEAR,*N
.
          CALL      PRTSEL
.
HDCONT    IF        LOOPFLAG=1
            PRINT     *40,"Invoices for Cancelled Admissions ";
            IF        IBCNMHOS=1
              PRINT     "From All Hospitals";
            ENDIF    
            PRINT     *N
            ADD       TWO,CLNO
          ENDIF
          IF        PRTFLAG=1
            PRINT    *1,"Invoice",*10,"Invoice",*19,"Patient Name":
                     *37,"Adm.",*46,"A/C",*50," Pat.";
.
            IF       CFEETYP=9
              PRINT    *58,"Adj.";
            ELSE
              IF       CFEETYP=5
                PRINT    *58,"GST";
              ENDIF
            ENDIF
.
            PRINT    *63,"   Accom":
                     *73," Theatre":
                     *81,"   Other",*90," Invoice",*98,"!":
                     *99,"    Cash",*108," Journal":
                     *117," Payment",*126," Credit":
                    *N,"Number",*12,"Date",*36,"Number",*45,"Days",*50," Class";
.
            IF       CFEETYP=9 | CFEETYP=5
              PRINT    *57,"Total";
            ENDIF
.
            PRINT    *63,"   Total",*73,"   Total":
                     *81,"   Total",*90,"   Total",*98,"!":
                     *99,"   Total",*108,"   Total":
                     *117,"   Total",*126,"   Note"
          ELSE
            PRINT    *1,"Invoice",*10,"Invoice",*19,"Patient Name":
                     *37,"Adm.",*46,"A/C",*50," Pat.":
                     *59,"   Accom",*69,"  Theatre":
                     *79,"     Other",*89,"    Invoice",*100,"!":
                     *102,"     Cash",*112,"   Journal":
                     *122,"    Payment":
                    *N,"Number",*12,"Date",*36,"Number",*45,"Days",*50," Class":
                     *59,"   Total",*69,"    Total":
                     *79,"     Total",*89,"      Total",*100,"!":
                     *102,"    Total",*112,"     Total":
                     *122,"      Total"
          ENDIF
.
          CALL      PAGEND
          MOVE      TEN1,CLNO
          IF        IBCNMHOS=1
            ADD       ONE,CLNO
          ENDIF
          RETURN
+
.
.         Subroutine to key in a valid date
.
KEYDTE
          IF        IDAY=0
            MOVE      SP10,CDAY        * no default day/mon
            MOVE      SP10,CMON
            MOVE      SP10,CYEAR
            MOVE      SP10,CCENT
          ELSE
            MOVE      IDAY,CDAY        * set up day/mon
            MOVE      IMON,CMON
            MOVE      IYEAR,CYEAR
            MOVE      ICENT,CCENT
          ENDIF
.
          MOVE      "17",CCOL          * set up position
.
          MOVE      "0",CCANLDTE       * 0=cannot cancel default date, 1=can
          MOVE      "1",CDEFDTE        * 0=3 C/R's to accept date, 1=1 C/R
          REPEAT
            CALL      KEYDATE          * nez modified to use standard routine
          UNTIL     CQUEST=0
.
          MOVE      ZERO,IDAY
          MOVE      CDAY,IDAY
.
          RETURN
+
PRTSEL    COMPARE   ZERO,FSTSYS
          GOTO      AEHEAD IF NOT EQUAL
.
          PRINT     *40,"CONSOLIDATED REPORT",*N,*N;
          COMPARE   ZERO,FSTSYS
          RETURN    IF EQUAL
.
AEHEAD    COMPARE   ONE,FSTSYS
          GOTO      OPHEAD IF NOT EQUAL
.
          PRINT     *40,"ACCIDENT & EMERGENCY REPORT",*N,*N;
          COMPARE   ONE,FSTSYS
          RETURN    IF EQUAL
.
OPHEAD    COMPARE   TWO,FSTSYS
          GOTO      IPHEAD IF NOT EQUAL
.
          PRINT     *40,"OUTPATIENT REPORT",*N;
          MATCH     SP6,FSTSITE
          GOTO      ALSITE IF NOT EQUAL
.
          PRINT     *40,"ALL SITES";
          GOTO      OPDEPT
.
ALSITE    PRINT     *40,"SITE ",FSTSITE;
.
OPDEPT    MATCH     SP3,FSTDEPT
          GOTO      ALDEPT IF NOT EQUAL
.
          PRINT     *54,"ALL DEPARTMENTS",*N,*N;
          GOTO      PRTSEL9
.
ALDEPT    PRINT     *54,"DEPARTMENT ",FSTDEPT,*N,*N;
          GOTO      PRTSEL9
.
IPHEAD    COMPARE   THREE,FSTSYS
          GOTO      OTHEAD IF NOT EQUAL
.
          PRINT     *40,"INPATIENT REPORT",*N,*N;
          GOTO      PRTSEL9
.
OTHEAD    PRINT     *40,"OTHER FINANCIAL REPORT",*N,*N;
.
PRTSEL9   RETURN
.
........................................................................
.        check if any credit notes amount 
........................................................................
PCRN0000  COMPARE   ZERO,PTINCRTT
          GOTO      PCRN9999 IF EQUAL
.
          PRINT     *10,"-- CREDIT NOTE --":
                    *85,PTINCRTT,*100,"!"
          ADD       ONE,CLNO
PCRN9999  RETURN
+
.
ENDIT     CHAIN      PGM
          STOP
.
.
          INC       PATCODDS
          INC       PATSELFN
          INC       PATSTRYR
          INC       PATHSPKY
.
          INC       AAEDE1IO/INC
          INC       AAEDTRIO/INC
          INC       IBASECIO/INC
          INC       IBAPASIO/INC
          INC       OUTBB1IO/INC
          INC       OUTDTRIO/INC
          INC       OUTSITIO/INC
          INC       PATCODIO/INC
          INC       PATDRGIO/INC
          INC       PATDTRIO/INC
          INC       PATHSPIO/INC
          INC       PATINVIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATTRNIO/INC
          INC       PATVISIO/INC
          INC       PMSVX1IO/INC
.
          INC       STD001IO

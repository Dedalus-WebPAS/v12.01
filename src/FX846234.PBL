.*****************************************************************************
.*    Program      : FX846234                                                *
.*    Description  : Write triggers for discharges from an admission number  *
.*                   input text file                                         *
.*                                                                           *
.*    Author       : Peter Vela   TSK 846234                                 *
.*    Date         : 12/10/2017                                              *
.*    Modifications:                                                         *
.*****************************************************************************
.
          INC       STD001FD
.
          INC       PATCODFD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PMSWTRFD/INC
.
.
. LOCAL VARIABLES
. ---------------
CMDLINE   DIM       100
.
DIM4      DIM       4
DEFPATH   DIM       60
DIM60     DIM       60
DOTTXT    INIT      ".txt"
.
FULLNAME  DIM       12
FILENAME  DIM       8
FULLPATH  DIM       60
.
INPFILE   DIM       8
.
NEWFILE   DIM       60
NEWPATH   DIM       60
.
OLDPATH   DIM       60
.
RECNUM    FORM      8
.
PATHFNAM  DIM       72
.
SAVEFLG   FORM      1
.
LOADFILE  FILE      ASCII, FIXED=9
.
.Name     Type    Length  Pos  Description
.----     ----    ------  ---  -----------
XADMNNUM  DIM       8      1   Admission Number
.
.End of Record             9
.
. PROGRAM CONSTANTS
. -----------------
SP60      INIT    "                                                            "
.
.
.
PRGNAM    INIT      "Write HMDS Triggers From Text File"
PRGID     INIT      "FX846234"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                             MAIN0000                                      *
.*                         Program Mainline                                  *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
          CALL      OPTN0000                      * get option
          BRANCH    COPTION,MAIN1000              * run fixit
          GOTO      MAIN9999                      * exit selected
.
MAIN1000  CALL      KDIR0000               * Keyin directory
          BRANCH    EXIT,MAIN9999
.
          CALL      KFIL0000               * Keyin filename
          BRANCH    EXIT,MAIN9999
.
          CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN7000:               * yes
                          MAIN0000:               * no
                          MAIN9999                * cancel
.
MAIN7000  CALL      OFIX0000                      * run fix
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
.
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
.
          OPEN      PATCODE1,"patcodes"
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      PMSWTRA1,"pmswtraf"
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        Get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Ok to proceed                           *
.*                        TRUE  (1)  Exit option selected                    *
.*              COPTION - option selected                                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Write triggers from admn no text file."
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ":
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                            OFIX0000                                       *
.*            fix to populate pmswtraf for discharges with cat A = 5 or 0    *
.*****************************************************************************
.
OFIX0000  DISPLAY   *P1:20,*EL,"Record No. :";
.
OFIX0500  READ      LOADFILE,SEQ;XADMNNUM
          GOTO      OFIX9000 IF OVER
.
          PACK      KEY9,THREE,XADMNNUM,SP70
          CALL      RDMISS2
          BRANCH    OVRCD,OFIX0500                * eof - finished
.
          MATCH     SP70,PMVXMHOS
          GOTO      OFIX0500 IF EQUAL             * must have hospital
.
          PACK      KEY19,PMVXMHOS,AADMNO,SP70
          CALL      RAPMWTR1
          IF        OVRCD=1
            PACK      PMWTHOSP,PMVXMHOS,SP70
            PACK      PMWTVISN,AADMNO,SP70
            PACK      PMWTSDAT,SP70
            PACK      PMWTMDAT,SP70
            CALL      IBACLOCK                    * Get the current date
            PACK      PMWTCDAT,CCC,CYY,CMM,CDD
            REP       " 0",PMWTCDAT
            MOVE      CTIMEIS,PMWTCTIM
            MOVE      "FX846234  ",PMWTWEBC
            PACK      PMWTSPAR,SP70,SP70
.
            PACK      KEY19,PMWTHOSP,PMWTVISN,PMWTSDAT,SP70
            CALL      WRPMWTR1
            ADD       ONE,RECNUM                    * increment record counter
          ENDIF
.         
          IF        (RECNUM%1000) = 0 | (RECNUM%1000) = 1
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          GOTO      OFIX0500                       * get next record
.         
OFIX9000  DISPLAY   *P15:20,*V2LON,RECNUM,*HOFF:
                    *P1:24,*EL,"Update completed.  ";
          CALL      HITENTER
.
OFIX9999  RETURN
+
.********************************************************************
.*                          KDIR0000                                *
.*  Keyin directory                                                 *
.********************************************************************
KDIR0000  MOVE      SP60,FULLPATH
.
          DISPLAY   *P1:24,*EL,"The directory path must end with a slash (/) ";
.
.         Keyin the path for the upload file
.
          KEYIN     *P1:9,*EL,"Keyin Path :":
                    *P14:9,*EL,*DV,*RV,FULLPATH:
                    *P14:9,FULLPATH;
.
KDIR3000  ENDSET    FULLPATH
          APPEND    SP60,FULLPATH
          RESET     FULLPATH
.
          SQUEEZE   FULLPATH
          CMATCH    EXITCHAR,FULLPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,FULLPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR0000
.
          GOTO      KDIR9500
.
KDIR9000  MOVE      ONE,EXIT
.
KDIR9500  DISPLAY   *P1:24,*EL;
.
KDIR9999  RETURN
.
.**********************************************************************
.*                               VALD0000                             *
.*        Check if it a valid directory                               *
.**********************************************************************
VALD0000  MOVE      ZERO,EXIT
          SQUEEZE   DIM60
          ENDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory path must end with a slash (/) ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory does not exist ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT            * Not valid
VALD9999  RETURN
.
.********************************************************************
.*                          KFIL0000                                *
.*  Keyin filename                                                  *
.********************************************************************
KFIL0000  KEYIN     *P1:11,*EF,"Keyin File Name : ",FULLNAME
.
          PACK      FULLNAME,FULLNAME,SP60
.
          MATCH     SP70,FULLNAME           * anything entered ?
          IF        @EQUAL
            DISPLAY   *P1:24,*EL,*B,"The file name is a mandatory field ";
            CALL      HITENTER
            GOTO      KFIL0000
          ENDIF
.
          SQUEEZE   FULLNAME
.
          CMATCH    EXITCHAR,FULLNAME       * Exit
          GOTO      KFIL9000 IF EQUAL
.
          MOVELPTR  FULLNAME,F2
          IF        F2<EIGHT
            DISPLAY   *P1:24,*EL,*B,"The file name must be 8 characters ";
            CALL      HITENTER
            GOTO      KFIL0000
          ENDIF
.
          UNPACK    FULLNAME,FILENAME,DIM4
.
          SCAN      ".",FILENAME
          IF        @EQUAL
            DISPLAY   *P1:24,*EL,*B,"The file name must be 8 characters ";
            CALL      HITENTER
            GOTO      KFIL0000
          ENDIF
.
          MATCH     DOTTXT,DIM4
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"The file name must end in .txt ";
            CALL      HITENTER
            GOTO      KFIL0000
          ENDIF
.
          PACK      PATHFNAM,FULLPATH,FILENAME,DOTTXT
          STRIP     PATHFNAM
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      LOADFILE,PATHFNAM        * open file
          TRAPCLR   IO
.
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*+,*B,PATHFNAM,"does not exist.  ";
            CALL      HITENTER
            GOTO      KFIL0000
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      KFIL9999
.
KFIL9000  MOVE      ONE,EXIT
          GOTO      KFIL9999
.
KFIL9999  RETURN
.
+
.*****************************************************************************
.*        I/O Routines                                                       *
.*****************************************************************************
.
          INC       STD001IO
.
          INC       PATCODIO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PMSWTRIO/INC

.***************************************************************************
.* System    :   Inpatients                                                *
.* Program   :   FIXPTCMT                                                  *
.* Desc      :   Fix program for Casemix Matrix file                       *
.***************************************************************************
.* Date      :   20/09/2012                                                *
.* Author    :   J.Tan     CAR 271474                                      *
.* Function  :   This program copies recods from pmscmtaf file to          *
.*               patcmtaf file (Casemix Matrix header file)                *
.*                                                                         *
.* Modifications :                                                         *
.*                                                                         *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PMSCMTFD/INC
          INC       PATCMTFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
RECNUM    FORM      8                       * Record counter
.
PRGID     INIT      "FIXPTCMT"
PRGNAM    INIT      "Fix program for pmscmtaf"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                * Display heading
.
          MOVE      ZERO,RECNUM             * Initialize Record counter
.
          OPEN      PMSCMTA1,"pmscmtaf"
          OPEN      PATCMTA1,"patcmtaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Update Casemix Matrix Header"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
PROC0000  DISPLAY   *P1:20,*EL,"Record No. : ";
          PACK      KEY34,SP70
          CALL      RSPMCMT1
PROC0100  CALL      RKPMCMT1
          BRANCH    OVRCD,PROC9999
          ADD       ONE,RECNUM                   * Update record counter
          IF        (RECNUM%100) = 0
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          PACK      KEY18,PMCCCCOD,PMCCHFUN,PMCCHFTY,PMCCCONT,SP70
          CALL      RDPTCMT1
          COMPARE   ZERO,OVRCD
          GOTO      PROC0100 IF EQUAL           * already exists
.
          UNPACK    KEY18,PTCMCCOD,PTCMHFUN,PTCMHFTY,PTCMCNTR
          MOVE      SP70,PTCMSPRE
          CALL      WRPTCMT1
.
          GOTO      PROC0100
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       PMSCMTIO/INC
          INC       PATCMTIO/INC

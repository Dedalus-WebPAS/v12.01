.***************************************************************************
.* System    :   Allied Health tables (allaudre and allauden)              *
.* Program   :   ALVNHCLR                                                  *
.* Desc      :   Program for clearing Allied Health audit tables           *
.*               (allaudre and allauden)                                   * 
.***************************************************************************
.* Date      :   19/10/2009                                                *
.* Author    :   WeeLee Koo                                                *
.* Function  :   This program will be executed by a script which will pass *
.*               a number of days. Using the days passed, the program      *
.*               calculate the date for record deletion, ie all            *
.*               records on or before this date will be deleted.           *
.*                                                                         *
.* Mods      :                                                             *
.*                                                                         *
.*  V9.12.00 :   19/10/2009  WeeLee Koo    CAR 120073                      *
.*               Delete records in allaudre and allauden where record date *
.*               is less than WORKDATE.                                    *
.***************************************************************************
.
          INC       STD002FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       ALLENCFD/INC                                       *I-90201
          INC       ALLREFFD/INC                                       *I-90201
          INC       ALLCONFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
CURRDATE  DATE      8
WORKDATE  DATE      8
MONTKEPT  FORM      2
TOTLDAYS  FORM      5
.
.
. PROGRAM CONSTANTS
. -----------------
.
PRGID     INIT      "ALVNHCLR"
PRGNAM    INIT      "Clear Allied Health Audit Tables"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      SETX0000                * Setup common variables
          CALL      INIT0000                * initialisation and open files
          BRANCH    EXIT,ML9999             * invalid time
.
          CALL      GDTM0000                * Calculate starting date/time
          CALL      PROC0000                * Delete appropriate records
.
ML9999    STOP                              * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.* Returns:  MONTKEPT    = no of months to be kept                          *
.*           EXIT      0 = valid selection                                  *
.*                     1 = invalid selection                                *
.****************************************************************************
.
INIT0000  MOVE      ZERO,EXIT
          CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"allaudre";
          OPEN      ALLAUDRE,"allaudre"
.
          DISPLAY   *P64:24,"allauden";
          OPEN      ALLAUDEN,"allauden"
.
          KEYIN     *P1:6,*EL,"Number of months to be kept? ",MONTKEPT
.
          COMPARE   ZERO,MONTKEPT
          GOTO      INIT9000 IF LESS
.
          GOTO      INIT9999
.
INIT9000  MOVE      ONE,EXIT
.
INIT9999  RETURN
+
.**************************************************************************
.*                               GDTM0000              Called by: ML0000  *
.*                   Calculate the starting date                          *
.* Requires: MONTKEPT  (no of months where user wants to keep the records)*
.* Returns:  WORKDATE  (starting date for deletion of prior records)      *
.**************************************************************************
.
.         Get the current date 
.
GDTM0000  CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
          MOVE      CURRDATE,WORKDATE
.
          ASSIGN    (MONTKEPT*30),TOTLDAYS      * convert passed months to days
.
          SUB       TOTLDAYS,WORKDATE
          UNPACK    WORKDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,CC
          MOVE      CYEAR,YY
          MOVE      CMON,MM
          MOVE      CDAY,DD
          MOVE      "01",DD
          PACK      WORKDATE,CC,YY,MM,DD
          REP       " 0",WORKDATE
.
GDTM9999  RETURN
+
.**************************************************************************
.*                               PROC0000                                 *
.*                        Check records & delete them                     *
.**************************************************************************
.
PROC0000  PACK      KEY19,SP70
          CALL      ASALREF                      * allaudre
PROC1000  CALL      AKALREF
          BRANCH    OVRCD,PROC2000 
.
          MATCH     WORKDATE,ALREAUDD
          GOTO      PROC2000 IF NOT LESS
.
          PACK      KEY19,ALREAUDD,ALREAUDT,ALREAUDP,ALREAUDR
          CALL      ADALREF                      * delete record 
          GOTO      PROC1000
.
PROC2000  PACK      KEY19,SP70
          CALL      ASALENC                      * allauden
PROC3000  CALL      AKALENC
          BRANCH    OVRCD,PROC9999
.
          MATCH     WORKDATE,ALENAUDD
          GOTO      PROC9999 IF NOT LESS
.
          PACK      KEY19,ALENAUDD,ALENAUDT,ALENAUDP,ALENAUDR
          CALL      ADALENC                      * delete record
          GOTO      PROC3000
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD002IO
.
          INC       ALLREFIO/INC
          INC       ALLENCIO/INC

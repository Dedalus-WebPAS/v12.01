.***************************************************************************
.* System    :   Patient Management System                                 *
.* Program   :   FXPTBAND                                                  *
.*               This program will set Theatre banding in patdtraf and     *
.*               pmsmtiaf file                                             *
.***************************************************************************
.* Date      :   21/07/2009                                                *
.* Author    :   J.Tan                                                     *
.* Mods      :                                                             *
.* V11.00.01     03/03/2020  J.Tan          TSK 0838262                    *
.*               Added Effective from and to date to MBS Item file         *
.* V10.01.01     10/03/2011 Jill Parkinson CAR 233088                      *
.*               Added pmsvx1af                                            *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
.
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATDTRFD/INC
          INC       PMSMTIFD/INC
          INC       PATITMFD/INC
          INC       PATCODFD/INC
          INC       PATFN1FD/INC
.
PRGID     INIT      "FXPTBAND"
PRGNAM    INIT      "Generate Theatre Banding for patdtraf and pmsmtiaf"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
.
          CALL      PROC0000               * process records for update
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                     * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patmi1af";
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patdtraf";
          OPEN      PATDTRA3,"patdtraf"
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"pmsmtiaf";
          OPEN      PMSMTIA2,"pmsmtiaf"
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patfn1af";
          OPEN      PATFN1A1,"patfn1af"
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patitemn";
          OPEN      PATITEM1,"patitemn"
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Valid option selected                  *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Generate Theatre Banding"
.
OPTN1000  KEYIN     *P1:7,*EL,"Select option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION
          GOTO      OPTN9500 IF EQUAL                 * exit
.
          BRANCH    COPTION,OPTN2000
.
          BEEP
          GOTO      OPTN1000
.
OPTN2000  CALL      CONTQST                          * Ok to continue ?
          BRANCH    CEXIT,OPTN9000,OPTN1000,OPTN9500 * Yes, no, cancel
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.****************************************************************************
.*                                PROC0000            Called by: ML0000     *
.*              read through admission file                                 *
.****************************************************************************
PROC0000  DISPLAY   *P1:24,*EL,"Processing Admission Number: ";

          MOVE      "2",KEY1             * Current Admission status
          CALL      GADM0000
.
          MOVE      "3",KEY1             * Discharged status
          CALL      GADM0000
.
          MOVE      "4",KEY1             * On Leave status
          CALL      GADM0000
.
          DISPLAY   *P1:24,*EL,"Processing complete.  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
.****************************************************************************
.         Loop through Admssion file for the selected status
.****************************************************************************
GADM0000  PACK      KEY9,KEY1,SP70
          CALL      RDSMISS2                     * position at start of file
GADM1000  CALL      RDKMISS2                     * read next record
          BRANCH    OVRCD,GADM9999               * end of file
.
          MATCH     KEY1,DASTAT
          GOTO      GADM9999 IF NOT EQUAL
.
          DISPLAY   *P30:24,*EL,*V2LON,DAADMNO;
          CALL      PADM0000
.
          GOTO      GADM1000
.
GADM9999  RETURN
+
.****************************************************************************
.         Get all theatre records in patdtraf and pmsmtiaf
.****************************************************************************
PADM0000  MOVE      "2",DTRECTYP                 * Theatre record
          PACK      KEY18,DAADMNO,DTRECTYP,SP70
          CALL      RDSDTRN3                     * position at start of file
PADM1000  CALL      RDKDTRN3                     * read next record
          BRANCH    OVRCD,PADM6000               * end of file
.
          MATCH     DAADMNO,DTADMNO
          GOTO      PADM6000 IF NOT EQUAL
.
          COMPARE   TWO,TRECTYPE
          GOTO      PADM6000 IF NOT EQUAL         * not a theatre item record
.
          MOVE      TITEMNO,KEY9
          PACK      KEY17,KEY9,ADATE,SP70
          CALL      PATITMRD
          BRANCH    OVRCD,PADM1000               * Invalid MBS item
.
          CALL      GBND0000                     * Generate theatre banding
          BRANCH    EXIT,PADM1000
.
          MOVE      KEY3,PTDTSUNQ                * Theatre banding
          CALL      UPDTRN3
          GOTO      PADM1000
.
PADM6000  MOVE      "2",PMMIRTYP                 * Theatre record
          PACK      KEY15,DAADMNO,PMMIRTYP,SP70
          CALL      RSPMMTI2
PADM7000  CALL      RKPMMTI2
          BRANCH    OVRCD,PADM9999
.
          MATCH     DAADMNO,PMMIVISN
          GOTO      PADM9999 IF NOT EQUAL
.
          MATCH     "2",PMMIRTYP
          GOTO      PADM9999 IF NOT EQUAL        * not a theatre item record
.
          MOVE      PMMIITEM,KEY9
          PACK      KEY17,KEY9,ADATE,SP70
          CALL      PATITMRD
          BRANCH    OVRCD,PADM7000               * Invalid MBS item
.
          CALL      GBND0000                     * Generate theatre banding
          BRANCH    EXIT,PADM7000
.
          MOVE      KEY3,PMMISUNQ                * Theatre banding
          CALL      UPPMMTI2
          GOTO      PADM7000
.
PADM9999  RETURN
+
.****************************************************************************
.         Generate Theatre banding in patdtraf and pmsmtiaf
.****************************************************************************
GBND0000  MOVE      ZERO,FORM2
          MOVE      ZERO,HFBAND
          PACK      KEY5,ANSC,ANSL,ACLAIM
          CALL      RDCODE1
          MOVE      TCFORM6,HFBAND          * default banding from claim code
.
          PACK      KEY14 WITH AFUNDH,ZERO,ZERO,ZERO,ZERO,SP20
          CALL      RDFUND1
          MOVE      HFBAND,FORM2          * HF banding
.
          COMPARE   ZERO,FORM2
          GOTO      GBND8000 IF EQUAL
.
          MOVE      SP70,KEY3
          LOAD      KEY3,FORM2,IBAND1,IBAND2,IBAND3,IBAND4,IBAND5,IBAND6:
                               IBAND7,IBAND8,IBAND9,IBAND10,IBAND11,IBAND12:
                               IBAND13,IBAND14,IBAND15,IBAND16
          MOVE      ZERO,EXIT
          GOTO      GBND9999
.
GBND8000  MOVE      ONE,EXIT
GBND9999  RETURN
+
. =========================================================================
.         I/O routines
. =========================================================================
UPDTRN3  MOVE      TADMNO,DTADMNO
         MOVE      TTRANSN1,DTTRANSN1
         MOVE      TFCENT,DTFCENT
         MOVE      TFYEAR,DTFYEAR
         MOVE      TFMONTH,DTFMONTH
         MOVE      TFDAY,DTFDAY
         MOVE      TRECTYPE,DTRECTYP
.
         UPDATE    PATDTRA3;DTADMNO,DTTRANSN1,TTYPEDEB,TDCODE,TPATAMTT:
                                 TPATAMTG,TPATAMTH,TPATAMTI,TPATAMTP:
                                 DTFCENT,DTFYEAR,DTFMONTH,DTFDAY:
                                 TTCENT,TTYEAR,TTMONTH,TTDAY:
                                 TTYPE,TITEMNO,TREF,TPAYTYPE,TRECEIPT:
                                 TDDESC,TINVPRT,DTRECTYP:
                                 TNODAYS,TCLAIM,TREBATE:
                                 TDOCTA,TSERVS,TDOCTO,TCHGDTE,TCHGTIM:
                                 TSESSION,TMISGRP,TDEPTYP:
                                 TDWARD,TCLMTYP,TADMTYP,TBATCHN:
                                 TNINVS,PTDTLSPT,PTDTLSRB,PTDTDTYP:
                                 PTDTDES2,PTDTEPSD,PTDTCCU:
                                 PTDTGSTA,PTDTGSTC,PTDTGSTM:
                                 PTDTEFFD,PTDTGSTL,PTDTTIME:
                                 PTDTCRST,PTDTUNIQ,PTDTBTYP:
                                 PTDTCNTR,PTDTCPRC,PTDTCONT:
                                 PTDTSUNQ,PTDTBTCH,PTDTADPT,PTDTADRB:
                                 TSPARE
.
         RETURN
.
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       PATITMRD
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATDTRIO/INC
          INC       PMSMTIIO/INC
          INC       PATITMIO/INC
          INC       PATCODIO/INC
          INC       PATFN1IO/INC

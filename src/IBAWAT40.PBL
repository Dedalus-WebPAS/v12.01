.******************************************************************************
.* System   : WAITING LISTS                                                   *
.* Program  : IBAWAT40                                                        *
.* Desc     : Procedure Codes Master Listing                                  *
.******************************************************************************
.* Date     : 27/9/1988                                                       *
.* Author   : Thai-Phuong La                                                  *
.* Comments :                                                                 *
.* Mods     :                                                                 *
.*        V5.07.01  26/10/1999 J Habasque                                     *
.*                  Recompiled for WATDSPRO                                   *
.*            V5.05.01  31/03/1998  Davin  506                                *
.*                      Print fields - WTWPHDPE & WTWPDRGC                    *
.*            V5.04.01  14/04/1997  howellsy   RHA                            *
.*                      added wprhat, wpchet                                  *
.******************************************************************************
.*            V5.01.01  19/06/89 M.Ohleiter                                   *
.*                      Changes for new release 5.01                          *
.*            V5.01.02  22/02/90 E.Phan                                       *
.*                      Replaced WPTYPE with WPESTLS                          *
.*            V5.01.10  03/07/90 Bill Dew                                     *
.*                      Re-write code to conform to standards and update to   *
.*                      new waiting list system.                              *
.*            V5.01.11  02/08/90 Justin Coates                                *
.*                      Allowed for WTCNTPUC and WTCNTPDS                     *
.*            V5.01.12  19/03/91 i chung           SRF 108123                 *
.*                      Fixed page overflow problem and bugs in ? options     *
.*                      found during testing                                  *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
          INC       PATCODFD/INC
          INC       WATCONFD/INC
          INC       WATPROFD/INC
.
.         Variables Declaration
.
.
CODE      DIM       2
COL       FORM      2        * used in WATDSPRO
DESC1     DIM       40
DESC2     DIM       40
DIM7      DIM       7
DIM35     DIM       35
DISPACT   FORM      1
PTCNURHA  FORM      1
ENDCR     DIM       9
GRDTOTAL  FORM      8        * grand total of procedure codes
GROUPCOD  DIM       3
GROUPE    DIM       3        * to group code
GROUPS    DIM       3        * start from group code
PROCCODE  DIM       9
PROCE     DIM       9        * to procedure code
PROCS     DIM       9        * start from procedure code
SCRNFLAG  FORM      1
SUBTOTAL  FORM      8        * sub total of group codes
STARTCR   DIM       9
VERT      FORM      2        * used in WATDSPRO
.
.         Constants Declaration
.
CODEWG    INIT      "WG"
NUM37     FORM      "37"
FINISH    INIT      "Finish   "
START     INIT      "Start    "
OPT1      INIT      " - Code Sequence "
OPT2      INIT      " - Group Code Sequence "
.
PRGNAM    DIM       30            * it's value is determined in 'INIT0000'
PRGID     INIT      "IBAWAT40"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.                    MAINLINE   Lets rock & roll
.**************************************************************************
ML0000
.MLQQ      SPLOPEN   "data"
          CALL      INIT0000           * initialization
ML1000    CALL      OPTN0000           * display options
          BRANCH    MOPTION,ML1111,ML2222
ML9999
.MLQQQ     SPLCLOSE  "data"
          CHAIN     PGM
ML1111
          CALL      CLRV0000
          CALL      SCRN1000           * display screen prompts
          CALL      PRNG0000           * Get procedure code range
          CALL      CONTQST
          BRANCH    CEXIT,ML2000,ML1111,ML1000
.
ML2000    CALL      PPCS0000           * print in procedure code sequence
          GOTO      ML1000
ML2222
          CALL      CLRV0000
          CALL      SCRN1000           * display screen prompts
          CALL      GRNG0000           * get group code range
          CALL      CONTQST
          BRANCH    CEXIT,ML4000,ML2222,ML1000
.
ML4000    CALL      PGCS0000           * print group code sequence
          GOTO      ML1000
+
.****************************************************************************
.*                                INIT0000                                  *
.****************************************************************************
INIT0000  DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,NUM37;*122,WTCNTPUC,*185,WTCNTPDS
          READ      CONTROLF,SEVENTY9;*138,PTCNURHA
.
          CLEAR     PRGNAM                  * set up program name
          APPEND    WTCNTPUC,PRGNAM
          APPEND    " Codes Master Listing",PRGNAM
          RESET     PRGNAM
.
          CALL      DISPHEAD
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"watproaf";
          OPEN      WATPROA1,"watproaf"
          OPEN      WATPROA2,"watproaf"
          OPEN      WATPROA3,"watproaf"
.
          MOVE      ONE,CNEWDS
.
          MOVE      ZERO,CNOUNDLN            * set input requirement for HEAD132
.
INIT9999  RETURN
.**************************************************************************
.                                 OPTN0000
.**************************************************************************
OPTN0000  MOVE      PRGNAM,KEY60
          CALL      DISPLIN2
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit ":
                    *P1:5,*V2LON,"1",*HOFF," = ",WTCNTPDS," Code sequence ":
                    *P1:6,*V2LON,"2",*HOFF," = Group Code sequence ":
                    *P1:8,"Select Option :";
OPTN1111
          KEYIN     *P17:8,*EL,*DV,UNDLN1,*P17:8,*V2LON,MOPTION;
          COMPARE   ZERO,MOPTION
          GOTO      OPTN9999 IF EQUAL
.
          DISPLAY   *P17:8,*V2LON,MOPTION;
          BRANCH    MOPTION,OPTN2222,OPTN3333
          BEEP
          GOTO      OPTN1111
OPTN2222
          PACK      KEY60,PRGNAM,OPT1
          CALL      DISPLIN2
          GOTO      OPTN9999
OPTN3333
          PACK      KEY60,PRGNAM,OPT2
          CALL      DISPLIN2
.
OPTN9999  RETURN
+
.**************************************************************************
CLRV0000
          UNPACK    SP20,PROCS,PROCE
          UNPACK    SP20,GROUPS,GROUPE
          UNPACK    SP20,STARTCR,ENDCR
          MOVE      SP30,DESC1
          MOVE      SP30,DESC2
.
CLRV9999  RETURN
+
.**************************************************************************
SCRN0000
          DISPLAY   *P1:4,*EF,*V2LON,"0",*HOFF," = Exit ":
                    *P1:5,*V2LON,"1",*HOFF," = ",WTCNTPDS," Code sequence ":
                    *P1:6,*V2LON,"2",*HOFF," = Group Code sequence ":
                    *P1:8,"Select Option : ",*V2LON,MOPTION;
SCRN1000
          BRANCH    MOPTION,SCRN1111,SCRN2222
SCRN1111
          DISPLAY   *P1:12,*EF,"From ",WTCNTPDS," Code :":
                    *P1:13,"To   ",WTCNTPDS," Code :"
          GOTO      SCRN4444
SCRN2222
          DISPLAY   *P1:12,*EF,"    From Group Code :":
                    *P1:13,"    To   Group Code :"
SCRN4444
          DISPLAY   *P23:12,*V2LON,STARTCR,*HOFF,*P33:12,DESC1:
                    *P23:13,*V2LON,ENDCR,*HOFF,*P33:13,DESC2
.
SCRN9999  RETURN
+
.**************************************************************************
.                                 PRNG0000
.    Get procedure code range
.**************************************************************************
PRNG0000  
          MOVE      SP9,PROCS
          MOVE      SP9,PROCE
          MOVE      ONE,SCRNFLAG            * set the redisplay flag
PRNG1000
          DISPLAY   *P23:12,UNDLN9,*P23:12;
PRNG1111
          CALL      GPRO0000                * get starting procedure code
          MOVE      PROCCODE,PROCS          * set the start proc code
.
          MATCH     SP9,PROCCODE            * test for return hit = start
          GOTO      PRNG2222 IF NOT EQUAL
.
          MOVE      START,STARTCR           * set the display code description
          MOVE      SP30,DESC1
          CALL      REDP0000
          GOTO      PRNG3333
PRNG2222
          CALL      VPRO0000                * Validate starting procedure code
          BRANCH    EXIT,PRNG1000,PRNG1111
          MOVE      WPDESC,DESC1
          MOVE      PROCCODE,STARTCR
          CALL      REDP0000
PRNG3333
          BRANCH    SCRNFLAG,PRNG4444
          GOTO      PRNG5000
PRNG4444
          DISPLAY   *P23:12,*V2LON,STARTCR,*HOFF,*P33:12,DESC1
PRNG5000
          MOVE      ONE,SCRNFLAG            * reset redisplay flag
PRNG5100
          DISPLAY   *P23:13,UNDLN9,*P23:13;
PRNG5555
          CALL      GPRO0000                * get ending procedure code
          MOVE      PROCCODE,PROCE          * set the end proc code
.
          MATCH     SP9,PROCCODE            * test for return hit = finish
          GOTO      PRNG6666 IF NOT EQUAL
.
          MOVE      FINISH,ENDCR            * set the display code description
          MOVE      "ZZZZZZZZZ",PROCE       * set end code range
          MOVE      SP30,DESC2
          CALL      REDP0000
          GOTO      PRNG7000
PRNG6666
          CALL      VPRO0000                * Validate ending procedure code
          BRANCH    EXIT,PRNG5100,PRNG5555
          MOVE      WPDESC,DESC2            * copy the procedure desc
          MOVE      PROCCODE,ENDCR
.
.         test for valid Procedure Code range
.
          MATCH     PROCS,PROCE
          GOTO      PRNG8888 IF LESS
          CALL      REDP0000
PRNG7000
          BRANCH    SCRNFLAG,PRNG7777
          GOTO      PRNG9999
PRNG7777
          DISPLAY   *P23:13,*V2LON,ENDCR,*HOFF,*P33:13,DESC2;
          GOTO      PRNG9999 
PRNG8888
          DISPLAY   *P1:24,*EL,*B,"End Code must be greater than or equal ":
                                  "to Start Code.  ";
          CALL      HITENTER
          BRANCH    SCRNFLAG,PRNG5100
          GOTO      PRNG5555
.
PRNG9999  RETURN
+
.**************************************************************************
REDP0000
          BRANCH    SCRNFLAG,REDP9999
          CALL      SCRN0000
REDP9999  RETURN
+
.**************************************************************************
.    Get a procedure code and deal with ? routines
GPRO0000
          BRANCH    SCRNFLAG,GPRO2222       * test if keyin on code screen
GPRO1111
          DISPLAY   *P1:24,*EL,WTCNTPDS," Code : ",UNDLN9,*P18:24;
GPRO2222
          KEYIN     *V2LON,PROCCODE         * get procedure code
          ENDSET    PROCCODE
          APPEND    SP9,PROCCODE
          RESET     PROCCODE
.
          CMATCH    "?",PROCCODE            * test for ? keyin
          GOTO      GPRO9999 IF NOT EQUAL
          MOVE      ZERO,DISPACT
          CALL      WATDSPRO                * procedure code ? routine
          MOVE      ZERO,SCRNFLAG
          GOTO      GPRO1111
.
GPRO9999  RETURN
+
.**************************************************************************
.    Validate procedure code
VPRO0000
          PACK      KEY9,PROCCODE
          CALL      RDPROA1                 * get procedure details
          BRANCH    OVRCD,VPRO8888
.
          COMPARE   ONE,WTWPACTV            * inactive code
          GOTO      VPRO8000 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      VPRO9999
.
VPRO8000  DISPLAY   *P1:24,*EL,*B,"Inactive Code. ";
          CALL      HITENTER
          GOTO      VPRO9000
VPRO8888
          DISPLAY   *P1:24,*EL,*B,"Invalid ",WTCNTPDS," Code.  ";
          CALL      HITENTER
VPRO9000  MOVE      TWO,EXIT
          LOAD      EXIT,SCRNFLAG,ONE
.
VPRO9999  RETURN
+
.*****************************************************************************
.                                 GRNG0000
.    Get group code range
.**************************************************************************
GRNG0000  
          MOVE      SP3,GROUPS
          MOVE      SP3,GROUPE
          MOVE      ONE,SCRNFLAG            * set the redisplay flag
GRNG1000
          DISPLAY   *P23:12,UNDLN3,*P23:12;
GRNG1111
          CALL      GGRP0000                * get starting group code
          MOVE      GROUPCOD,GROUPS         * set the start proc code
.
          MATCH     SP3,GROUPCOD            * test for return hit = start
          GOTO      GRNG2222 IF NOT EQUAL
.
          MOVE      START,STARTCR           * set the display code description
          MOVE      SP30,DESC1
          CALL      REDP0000
          GOTO      GRNG3333
GRNG2222
          CALL      VGRP0000                * Validate starting group code
          BRANCH    EXIT,GRNG1000,GRNG1111
          MOVE      TDESC,DESC1
          MOVE      GROUPCOD,STARTCR
          CALL      REDP0000
GRNG3333
          BRANCH    SCRNFLAG,GRNG4444
          GOTO      GRNG5000
GRNG4444
          DISPLAY   *P23:12,*V2LON,STARTCR,*HOFF,*P33:12,DESC1;
GRNG5000
          MOVE      ONE,SCRNFLAG            * reset the redisplay flag
GRNG5100
          DISPLAY   *P23:13,UNDLN3,*P23:13;
GRNG5555
          CALL      GGRP0000                * get ending group code
          MOVE      GROUPCOD,GROUPE         * set the end group code
.
          MATCH     SP3,GROUPCOD            * test for return hit = finish
          GOTO      GRNG6666 IF NOT EQUAL
.
          MOVE      FINISH,ENDCR            * set the display code description
          MOVE      "ZZZ",GROUPE            * set end code range
          MOVE      SP30,DESC2
          CALL      REDP0000
          GOTO      GRNG7000
GRNG6666
          CALL      VGRP0000                * Validate ending group code
          BRANCH    EXIT,GRNG5100,GRNG5555
          MOVE      TDESC,DESC2
          MOVE      GROUPCOD,ENDCR
.
.         test for valid Group Code range
.
          MATCH     GROUPS,GROUPE
          GOTO      GRNG8888 IF LESS
          CALL      REDP0000
GRNG7000
          BRANCH    SCRNFLAG,GRNG7777
          GOTO      GRNG9999
GRNG7777
          DISPLAY   *P23:13,*V2LON,ENDCR,*HOFF,*P33:13,DESC2;
          GOTO      GRNG9999 
GRNG8888
          DISPLAY   *P1:24,*EL,*B,"End Code must be greater than or equal ":
                                  "to Start Code.  ";
          CALL      HITENTER
          BRANCH    SCRNFLAG,GRNG5100
          GOTO      GRNG5555
.
GRNG9999  RETURN
+
.**************************************************************************
.    Get a group code and deal with ? routines
GGRP0000
          BRANCH    SCRNFLAG,GGRP2222       * test if keyin on code screen
GGRP1111
          DISPLAY   *P1:24,*EL,"Group Code : ___",*P14:24;
GGRP2222
          KEYIN     *V2LON,GROUPCOD         * get group code
          ENDSET    GROUPCOD
          APPEND    SP3,GROUPCOD
          RESET     GROUPCOD
.
          CMATCH    "?",GROUPCOD            * test for ? screen
          GOTO      GGRP9999 IF NOT EQUAL
.
          PACK      CODE,CODEWG             * set the code category
          CALL      PATCODDS                * code ? routine
          MOVE      ZERO,SCRNFLAG
          GOTO      GGRP1111
.
GGRP9999  RETURN
+
.**************************************************************************
.    Validate group code
VGRP0000
          PACK      KEY5,CODEWG,GROUPCOD    * check group code
          CALL      RDCODE1
          BRANCH    OVRCD,VGRP8888
          MOVE      ZERO,EXIT
          GOTO      VGRP9999
VGRP8888
          DISPLAY   *P1:24,*EL,*B,"Invalid Group Code.  ";
          CALL      HITENTER
          MOVE      TWO,EXIT
          LOAD      EXIT,SCRNFLAG,ONE
.
VGRP9999  RETURN
+
.**************************************************************************
.                                 PPCS0000
.    Print procedure code sequence read first key in watproaf
.***************************************************************************
PPCS0000
          DISPLAY   *P1:24,*EL,*V2LON,"** ",*BLINKON,"Generating Report":
                           *HOFF,*V2LON," **",*HOFF:
                    *P32:24,WTCNTPDS," :",*P59:24,"Scanning : ";
.
          MOVE      ZERO,GRDTOTAL
          MOVE      ZERO,CPAGENO
          CALL      HEAD0000
.
          MATCH     SP9,PROCS               * test starting at the first record
          GOTO      PPCS1111 IF NOT EQUAL
          PACK      KEY9,PROCS,SP9
          CALL      RDSPROA1                * position fptr at start of record
          GOTO      PPCS2222
PPCS1111
          PACK      KEY9,PROCS,SP9
          CALL      RDPROA1                 * full read on file
          BRANCH    OVRCD,PPCS9000
          GOTO      PPCS3333
PPCS2222
          CALL      RDKPROA1                * read next record
          BRANCH    OVRCD,PPCS9000
PPCS3333
          DISPLAY   *P70:24,WPCODE;
          MATCH     WPCODE,PROCE            * test if greater than endcode
          GOTO      PPCS9000 IF LESS
.
          DISPLAY   *P44:24,*V2LON,WPCODE;  * display code being printed
          CALL      PRNT0000                * print record
          ADD       ONE,GRDTOTAL            * increment total
          GOTO      PPCS2222
PPCS9000
          COMPARE   ZERO,GRDTOTAL
          GOTO      PPCS9100 IF NOT EQUAL
          DISPLAY   *P1:24,*EL,*V2LON,*B,"** No ",WTCNTPDS," Codes on file":
                                         " **     ";
          CALL      HITENTER
          GOTO      PPCS9999
.
PPCS9100  COMPARE   FIFTY5,CLNO
          GOTO      PPCS9200 IF LESS
          CALL      HEAD0000
          GOTO      PPCS9300
.
PPCS9200  CALL      LINE0000
.
PPCS9300  PRINT     *N,"Total ",WTCNTPDS," Codes   : ",GRDTOTAL:
                    *N,*N,*N,"*** End of Report ***"
          DISPLAY   *P1:24,*EL,*P24:24,*B,*V2LON:
                    "** Report Generation Completed **",*W;
.
PPCS9999  RETURN
+
.**************************************************************************
.                                 PGCS0000
.***************************************************************************
PGCS0000
          DISPLAY   *P1:24,*EL,*V2LON,"** ",*BLINKON,"Generating Report":
                           *HOFF,*V2LON," **",*HOFF:
                    *P40:24,"Group :",*P62:24,"Scanning : ";
.
          MOVE      ZERO,GRDTOTAL
          MOVE      ZERO,SUBTOTAL
          MOVE      ZERO,CPAGENO
          MOVE      SP3,GROUPCOD          * init the last group code
          CALL      HEAD0000
.
          PACK      KEY12,GROUPS,SP20     * position at start code
          CALL      RDSPROA2              * position the fptr
PGCS1111
          CALL      RDKPROA2              * read next record
          BRANCH    OVRCD,PGCS9000
.
          DISPLAY   *P73:24,WPGRP;
          MATCH     WPGRP,GROUPE          * test if greater than endcode 
          GOTO      PGCS9000 IF LESS
.
          MATCH     WPGRP,GROUPCOD        * test for a change in group code
          GOTO      PGCS5555 IF EQUAL
          MATCH     SP3,GROUPCOD          * test if first record
          GOTO      PGCS4444 IF EQUAL
          CALL      LINE0000
          PRINT     "Total ",WTCNTPDS," for ",GROUPCOD," : ",SUBTOTAL,*N
          ADD       THREE,CLNO            * increment the printed line counter
.
          COMPARE   FIFTY5,CLNO           * check for page overflow
          GOTO      PGCS2222 IF LESS
          CALL      HEAD0000
          GOTO      PGCS3333
PGCS2222
          CALL      LINE0000
          ADD       ONE,CLNO              * increment the printed line counter
PGCS3333
          MOVE      ZERO,SUBTOTAL         * zero subtotal counter for group type
PGCS4444
          MOVE      WPGRP,GROUPCOD        * set the last group code
PGCS5555
          DISPLAY   *P48:24,*V2LON,WPGRP  * display code being printed
          CALL      PRNT0000              * print record
          ADD       ONE,GRDTOTAL          * increment total
          ADD       ONE,SUBTOTAL          * increment subtotal
          GOTO      PGCS1111
PGCS9000
          COMPARE   ZERO,GRDTOTAL
          GOTO      PGCS9100 IF NOT EQUAL
          DISPLAY   *P1:24,*EL,*V2LON,*B,"** No Group Codes on file **     ";
          CALL      HITENTER
          GOTO      PGCS9999
.
PGCS9100  CALL      LINE0000
          PRINT     "Total ",WTCNTPDS," for ",GROUPCOD," : ",SUBTOTAL
          ADD       TWO,CLNO
.
          COMPARE   FIFTY5,CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PRINT     *N,*N,"Total ",WTCNTPDS," Codes   : ",GRDTOTAL:
                    *N,*N,*N,"*** End of Report ***"
          DISPLAY   *P1:24,*EL,*P24:24,*B,*V2LON:
                    "** Report Generation Completed **",*W;
.
PGCS9999  RETURN
+
.***************************************************************************
.                                 PRNT0000
.    print record details and check for full page
.***************************************************************************
PRNT0000  COMPARE   CLNO,FIFTY5
          CALL      HEAD0000 IF LESS
.
          MOVE      "Unknown             ",TDESC
          PACK      KEY5,CODEWG,WPGRP  * obtain group description
          CALL      RDCODE1
.
          MOVE      WPDESC,DIM35
          IF        PTCNURHA = 1
            MOVE      TDESC,DIM7
            PRINT     *1,"|",*5,WPCODE,*18,"|",*20,DIM35,*56,"|":
                      *61,WPTIME,*70,"|",*75,WPESTLS,*82,"|":
                      *85,WPGRP,*90,"|",*92,DIM7:
                      *100,"| ",WPCHET:
                      *109,"| ",WPRHAT,*117,"|",*119,WTWPHDPE,*124,"|":
                      *126,WTWPDRGC,*132,"|"
          ELSE
            PRINT     *1,"|",*5,WPCODE,*18,"|",*20,DIM35,*56,"|":
                      *61,WPTIME,*70,"|",*75,WPESTLS,*82,"|":
                      *85,WPGRP,*90,"|",*93,TDESC,*117,"|",*119,WTWPHDPE:
                      *124,"|",*126,WTWPDRGC,*132,"|"
          ENDIF
          ADD       ONE,CLNO
.
PRNT9999  RETURN
+
.***************************************************************************
.                                 HEAD0000
.***************************************************************************
HEAD0000
          CLOCK     TIME,CTIMEIS
          CALL      HEAD132
.
          BRANCH    MOPTION,HEAD1111,HEAD3333
HEAD1111
          PRINT     "From ",WTCNTPDS," Code : ",STARTCR,SP3,DESC1
          PRINT     "To   ",WTCNTPDS," Code : ",ENDCR,SP3,DESC2,*N
          GOTO      HEAD6666
HEAD3333
          PRINT     "From Group Code : ",STARTCR,SP3,DESC1
          PRINT     "To   Group Code : ",ENDCR,SP3,DESC2,*N
HEAD6666
          CALL      LINE0000           * draw line 1 of table heading
.
          IF        PTCNURHA = 1
            PRINT     *1,"|",*3,WTCNTPDS," Code",*18,"|":
                      *24,"Description ",*56,"|",*58,"Time (mins)":
                      *70,"|",*72,"Est Stay",*82,"|",*84,"Group":
                      *90,"|",*92,"Grp Des":
                      *100,"| CHE Th":
                      *109,"| RHA Th",*117,"|",*119,"HDPE",*124,"|":
                      *127,"DRG",*132,"|"
          ELSE
            PRINT     *1,"|",*3,WTCNTPDS," Code",*18,"|":
                      *24,"Description ",*56,"|",*58,"Time (mins)":
                      *70,"|",*72,"Est Stay",*82,"|",*84,"Group":
                      *90,"|",*94,"Group Description",*117,"|":
                      *119,"HDPE",*124,"|",*127,"DRG",*132,"|"
          ENDIF
.
          CALL      LINE0000           * draw line 3 of table heading
          ADD       SIX,CLNO
.
HEAD9999  RETURN
+
.****************************************************************************
.*                          LINE0000                                        *
.*              print header line and last line of the page                 *
.****************************************************************************
LINE0000  PRINT     *1,"*-------------------------------------------":
                       "--------------------------------------------":
                       "-------------------------------------------*"
LINE9999  RETURN
+
.==============================================================================
.
          INC       STD001IO
          INC       PATCODKY
          INC       WATDSPRO
.
          INC       PATCODIO/INC
          INC       WATPROIO/INC

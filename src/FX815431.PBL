.***************************************************************************
.* System    :   Outpatients                                               *
.* Program   :   FX815431                                                  *
.* Desc      :   Load Appointment Request Comments                         *
.***************************************************************************
.* Date      :   22/03/2017                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   This program will load appointment request comments       *
.*               from the old file VISCMTFD to the new file OUTRCMFD       *
.*                                                                         *
.* V10.10.00  22/03/2017  Ebon Clements     TSK 0815431                    *
.*            Created program                                              *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       ALLCONFD/INC
          INC       OUTARTFD/INC
          INC       OUTRCMFD/INC
          INC       VISCMTFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
DISNUM    FORM      4
FORM10    FORM      10
RECNUM    FORM      8
.
PRGID     INIT      "FX815431"
PRGNAM    INIT      "Load OUTRCMFD Appointment Request Comments"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      CONTROLF,"controlf"
          OPEN      OUTARTA2,"outartaf"
          OPEN      OUTRCMA1,"outrcmaf"
          OPEN      VISCMTA1,"viscmtaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  DISPLAY   *P1:20,*EL,"Record No. : ";
          MOVE      ZERO,RECNUM
          MOVE      ZERO,DISNUM
.
          PACK      KEY33,ZERO,SP70
          CALL      RSOTART2
PROC1000  CALL      RKOTART2           * Loop appointment requests
          BRANCH    OVRCD,PROC9999
.
          MATCH     "0",OTARSTAT                  
          IF        !@EQUAL
            MATCH     "1",OTARSTAT            * Requested and Pending 
            GOTO      PROC9999 IF NOT EQUAL   * reschedule only
          ENDIF
.
          ADD       ONE,RECNUM                    * update record counter
          ADD       ONE,DISNUM                    * Display counter
.
          IF        DISNUM = 2000  | RECNUM = 1
            MOVE      ZERO,DISNUM
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          MATCH     SP70,OTARCMID             * Comments already in
          GOTO      PROC1000 IF NOT EQUAL     * outrcmaf
.
          PACK      KEY14,OTARREFN,ZERO,ONE,TWO,SP70
          CALL      RSVSCMT1
PROC2000  CALL      RKVSCMT1                  * Check for appointment
          BRANCH    OVRCD,PROC1000            * request comments
.
          MATCH     VSCTVIST,OTARREFN         * Correct referral number
          GOTO      PROC1000 IF NOT EQUAL
.
          MATCH     "012",VSCTTYPE            * Appointment request
          GOTO      PROC1000 IF NOT EQUAL
.
          MATCH     SP70,VSCTDATA             * Ignore if all spaces
          GOTO      PROC2000 IF EQUAL
.
          MOVE      "121",PRXCODE           * System Lock Sector 121
          CALL      GETSLK00                * Get System Lock of Sector 121
.
          READ      CONTROLF,HUND21;*112,ALCNNUCI
          MOVE      ALCNNUCI,FORM10
          ADD       ONE,FORM10
          MOVE      FORM10,ALCNNUCI
          WRITAB    CONTROLF,HUND21;*112,ALCNNUCI
.
          CALL      RELSLK00                * Release System Lock of Sector 121
          SUB       ONE,FORM10
          MOVE      FORM10,ALCNNUCI         * Next unique comment id
.
          MOVE      ALCNNUCI,OTARCMID       * Allocate comment id
          CALL      UPOTART2
.
          MOVE      ZERO,F3                   * New line number
          PACK      KEY14,OTARREFN,ZERO,ONE,TWO,SP70
          CALL      RSVSCMT1
PROC3000  CALL      RKVSCMT1                  * Check for appointment
          BRANCH    OVRCD,PROC1000            * request comments
.
          MATCH     VSCTVIST,OTARREFN         * Correct referral number
          GOTO      PROC1000 IF NOT EQUAL
.
          MATCH     "012",VSCTTYPE            * Appointment request
          GOTO      PROC1000 IF NOT EQUAL
.
          MOVE      OTARCMID,OTRCUNIQ         * Comment ID from outartaf
          ADD       ONE,F3
          MOVE      F3,OTRCLINE               * Line number from viscmtaf
.
          PACK      KEY13,OTRCUNIQ,OTRCLINE,SP70
          CALL      RAOTRCM1
          IF        OVRCD=1
            MOVE      SP70,OTRCCUID           * Write comment to outrcmaf
            PACK      OTRCCDAT,CCC,CYY,CMM,CDD
            REP       " 0",OTRCCDAT
            CLOCK     TIME,OTRCCTIM
            UNPACK    SP70,OTRCUUID,OTRCUDAT,OTRCUTIM,OTRCSPAR
            MOVE      VSCTDATA,OTRCCOMM
            CALL      WROTRCM1
          ENDIF
.
          GOTO      PROC3000
.  
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       OUTARTIO/INC
          INC       OUTRCMIO/INC
          INC       VISCMTIO/INC

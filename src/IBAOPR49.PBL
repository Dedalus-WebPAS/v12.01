.******************************************************************************
.*    Program      : IBAOPR49                                                 *
.*    Description  : Theatre - Surgeon Utilisation Analysis                   *
.*                                                                            *
.*    Author       : ROD                                                      *
.*    Date         : 31/01/94                                                 *
.*                                                                            *
.*    Modifications:                                                          *
.*                                                                            *
.*        V10.04.01 23/06/2014   J.Tan         CAR 261630                     *
.*                  Removed port number from temp file name                   *
.*        V9.12.00  15/12/2009  Mike Laming   CAR 211875                      *
.*                  Ported from v6.17 - field not available - see "OPCOTTIM"  *
.******************************************************************************
.*        V6.12.01  30/08/2004  Guomin Fu   CAR 52575                         *
.*                  Recompiled for KYOPRCLI                                   *
.*        V6.10.01  07/04/2003  CAR 36800   Steve Downing                     *
.*                  Mods to layout to allow 7 digit figures for minute fields *
.*        V6.05.01  24/11/1998  Nicole Harrington                             *
.*                  Removed CCENTRY                                           *
.*        V6.04.01  03/02/98 J.Tan    SRF 621618                              *
.*                  Mods variables to form6 instead of form5                  *
.*        V6.03.B2  ROD  22/06/1995                                           *
.*                  Allow current period to be input                          *
.******************************************************************************
.
          INC       STD001FD
          INC       IBASEQFD/INC            * Sequential Numbers File
          INC       TFILEVAR/INC            * Generate Temp File Name
          INC       WEBERRFD/INC            * Web Server Error Logging
.
          INC       OPRCSUFD/INC
          INC       OPRCOUFD/INC
          INC       PATDRGFD/INC
          INC       PATCONFD/INC
.
ATOTBKEX  FORM      7
AVANAOP   FORM      6
AVEBKCAS  FORM      3.1
AVOPER    FORM      6
AVRECV    FORM      6
CMDLINE   DIM       70
CPTDATE   DIM       8
CURRDATE  DIM       8
ENDDESC   DIM       25
ENDPER    DIM       6
FNAME     DIM       8
PUTILBK   FORM      3.1
PUTILEX   FORM      3.1
PUTILSE   FORM      3.1
STARTPER  DIM       6
STRTDESC  DIM       25
TAVANAOP  FORM      6
TAVOPER   FORM      6
TAVRECV   FORM      6
TOTCOL2   FORM      7
TOTCOL3   FORM      7
TOTCOL4   FORM      7
TOTCOL5   FORM      7
TOTCOL6   FORM      7
TOTCOL7   FORM      7
TOTCOL8   FORM      7
TOTCOL9   FORM      7
TOTCOL10  FORM      7
TOTCOL11  FORM      3.1
TOTCOL12  FORM      3.1
TOTCOL13  FORM      3.1
TOTCOL14  FORM      3.1
UTOTBKEX  FORM      7
.
. Temp file definition
TEMPFILE  IFILE     SQL, FIXED=41, KEY="U1-6"
XXDOCT    DIM       6      1      * Doctor
XXALLBK   FORM      7      7      * Session Allocation Booked
XXALLEX   FORM      7     12      * Session Allocation Extra
XXUSEBK   FORM      7     19      * Session Time Used Booked
XXUSEEX   FORM      7     24      * Session Time Used Extra
XXUSESH   FORM      7     29      * Session Time Used Shared
XXTHCAS   FORM      6     34      * Throughput cases
XXTHTIM   FORM      7     40      * Throughput theatre mins
.       End of record     41 
.
PRGID     INIT      "IBAOPR49"
PRGNAM    INIT      "Surgeon Utilisation Analysis"
VERSION   INIT      "V12.01.00"
+
.***********************************************************************
.*  ML0000  :  Mainline code                                           *
.***********************************************************************
ML0000    CALL      INIT0000                      * initialisation
.
ML1000    CALL      MENU0000                      * menu
          BRANCH    EXIT,ML9999                   * Exit selected
.
ML2000    CALL      KPER0000                      * keyin date range
          BRANCH    EXIT,ML1000                   * nothing input
.
          CALL      CONTQST                       * Ok to Continue
          BRANCH    CEXIT,ML5000,ML2000,ML1000
.
ML5000    CALL      LOAD0000                      * load data into temp file
          CALL      RPRT0000                      * print report (from temp fil)
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.***********************************************************************
.*  INIT0000  :  Initialisation                                        *
.***********************************************************************
INIT0000  CALL      DISPHEAD
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,FIFTY9;*27,CPERMTH
.
          OPEN      OPRCOUA1,"oprcouaf"
          OPEN      OPRCSUA1,"oprcsuaf"
          OPEN      PATDRGA3,"patdrgaf"
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,FNAME
          MOVE      ZERO,CNOUNDLN
.
INIT9999  RETURN
+
.***********************************************************************
.*  MENU0000  :  Main Menu                                             *
.***********************************************************************
MENU0000  DISPLAY   *P1:4,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Print Report":
                    *P1:7,"Select option ? "
.
MENU1000  KEYIN     *P17:7,*DV,UNDLN1:
                    *P17:7,*V2LON,MOPTION;
.
          MOVE      ZERO,EXIT
          BRANCH    MOPTION,MENU9999
          IF        MOPTION<>0
            BEEP
            GOTO      MENU1000
          ENDIF
          MOVE      ONE,EXIT                      * Exit
.
MENU9999  RETURN
+
.***********************************************************************
.*  LOAD0000  :  Load data into temp file                              *
.***********************************************************************
LOAD0000  CALL      CRET0000                      * create temp file
          DISPLAY   *P1:24,*EL,*P20:24,"Searching... [         ]";
.
          PACK      KEY16,STARTPER,SP30
          CALL      RSOPCSU1
LOAD1000  CALL      RKOPCSU1
          BRANCH    OVRCD,LOAD5000
.
          MATCH     OPCSDATE,ENDPER               * still in period requested?
          GOTO      LOAD5000 IF LESS
.
          MOVE      ZERO,XXTHCAS
          MOVE      ZERO,XXTHTIM
          MOVE      ZERO,XXALLBK
          MOVE      ZERO,XXALLEX
          MOVE      ZERO,XXUSEBK
          MOVE      ZERO,XXUSEEX
.
          MOVE      OPCSDOCT,XXDOCT
          PACK      KEY6,XXDOCT
          CALL      RDTEMP1
          IF        OVRCD=0
            MATCH     ANSB,OPCSTYPE
            IF        @EQUAL
              ADD       OPCSBTIM,XXALLBK
              ADD       OPCSUTIM,XXUSEBK
            ELSE
              ADD       OPCSBTIM,XXALLEX
              ADD       OPCSUTIM,XXUSEEX
            ENDIF
            ADD       OPCSSTIM,XXUSESH
            CALL      UPTEMP1
          ELSE
            MATCH     ANSB,OPCSTYPE
            IF        @EQUAL
              MOVE      OPCSBTIM,XXALLBK
              MOVE      OPCSUTIM,XXUSEBK
            ELSE
              MOVE      OPCSBTIM,XXALLEX
              MOVE      OPCSUTIM,XXUSEEX
            ENDIF
            MOVE      OPCSSTIM,XXUSESH
            CALL      WRTEMP1
          ENDIF
          DISPLAY   *P34:24,*V2LON,XXDOCT;
          GOTO      LOAD1000
.
.
LOAD5000  PACK      KEY21,STARTPER,SP30
          CALL      RSOPCOU1
LOAD5100  CALL      RKOPCOU1
          BRANCH    OVRCD,LOAD9999
.
          MATCH     OPCODATE,ENDPER               * still in period requested?
          GOTO      LOAD9999 IF LESS
.
          MOVE      ZERO,XXALLBK
          MOVE      ZERO,XXALLEX
          MOVE      ZERO,XXUSEBK
          MOVE      ZERO,XXUSEEX
          MOVE      ZERO,XXUSESH
.
          MOVE      OPCODOCT,XXDOCT
          PACK      KEY6,XXDOCT
          CALL      RDTEMP1
          IF        OVRCD=0
            ADD       OPCONCAS,XXTHCAS
....        ADD       OPCOTTIM,XXTHTIM
            CALL      UPTEMP1
          ELSE
            MOVE      OPCONCAS,XXTHCAS
....        MOVE      OPCOTTIM,XXTHTIM
            CALL      WRTEMP1
          ENDIF
          DISPLAY   *P34:24,*V2LON,XXDOCT;
          GOTO      LOAD5100
.
LOAD9999  RETURN
+
.***********************************************************************
.*  RPRT0000  :  Print Report                                          *
.***********************************************************************
RPRT0000  DISPLAY   *P1:24,*EL,*P35:24,"Printing...";
.
          CALL      IBACLOCK
          MOVE      ZERO,CPAGENO
          MOVE      ZERO,CNOUNDLN
          MOVE      "60",CLNO                     * force new page
          CALL      ITOT0000                      * initialise totals
.
          PACK      KEY6,SP10
          CALL      RSTEMP1  
RPRT0500  CALL      RKTEMP1                       * get next record
          BRANCH    OVRCD,RPRT9000                * no more records
.
. check if we need a new page
.
          COMPARE   "55",CLNO
          GOTO      RPRT5000 IF LESS
.
. we need a new page
.
          CALL      HEAD132
          PRINT     *20,"Start date : ",STRTDESC,*N:
                    *20,"End date   : ",ENDDESC,*N
.
          CALL      UND132
          PRINT     *1,"|        |                            | ":
                    "                                    |                  | ":
                    "   % Util. of   |                 |",*N:
                    *1,"| Doctor |    Session  Allocation     | ":
                    "         Session Time Used          |    Throughput    | ":
                    " session alloc. |        Average  |",*N:
                    *1,"|        |  Booked |  Extra |  Total  |  ":
                    "Booked |  Extra |  Total  | Shared |  Cases | Thet Min| ":
                    "Booked | +Extra |      Break/Case |"

          CALL      UND132
          MOVE      TEN,CLNO
.
. print a line
.
RPRT5000  ASSIGN    (XXALLBK+XXALLEX),ATOTBKEX     * total alloc booked + extra
          ASSIGN    (XXUSEBK+XXUSEEX),UTOTBKEX     * total used booked + extra
          ASSIGN    (XXUSEBK/XXALLBK)*100,PUTILBK  * % util ses alloc Booked
          ASSIGN    (UTOTBKEX/(XXALLBK+XXALLEX))*100,PUTILEX
          ASSIGN    (XXTHTIM/UTOTBKEX)*100,PUTILSE      * % util of session
          ASSIGN    (UTOTBKEX-XXTHTIM)/XXTHCAS,AVEBKCAS    * ave Break/case
.
          IF        XXALLBK=0
            MOVE      ZERO,PUTILBK            * cant divide by 0 cause = 999.9
           ENDIF
          IF        XXALLBK=0 & XXALLEX=0
            MOVE      ZERO,PUTILEX
          ENDIF
          IF        UTOTBKEX=0
            MOVE      ZERO,PUTILSE
          ENDIF
          IF        XXTHCAS=0
            MOVE      ZERO,AVEBKCAS
          ENDIF
.
.
          PRINT     *1,"| ",XXDOCT," | ",XXALLBK," |",XXALLEX," | ",ATOTBKEX:
                    " | ",XXUSEBK," |",XXUSEEX," | ",UTOTBKEX," |",XXUSESH:
                    " | ",XXTHCAS," | ",XXTHTIM," |  ",PUTILBK," |  ":
                    PUTILEX," |           ",AVEBKCAS," |"
....                PUTILEX," |  ",PUTILSE," |  ",AVEBKCAS," |"
.  
          ADD       XXALLBK,TOTCOL2               * increment totals
          ADD       XXALLEX,TOTCOL3               * increment totals
          ADD       XXUSEBK,TOTCOL5               * increment totals
          ADD       XXUSEEX,TOTCOL6               * increment totals
          ADD       XXUSESH,TOTCOL8               * increment totals
          ADD       XXTHCAS,TOTCOL9               * increment totals
          ADD       XXTHTIM,TOTCOL10              * increment totals
.
          ADD       ONE,CLNO                      * increment line counter
          GOTO      RPRT0500
.
. no more records
.
RPRT9000  IF        CPAGENO = ZERO
            CALL      HEAD132
          ELSE
            ASSIGN    (TOTCOL2+TOTCOL3),TOTCOL4
            ASSIGN    (TOTCOL5+TOTCOL6),TOTCOL7
            ASSIGN    (TOTCOL5/TOTCOL2)*100,TOTCOL11
            ASSIGN    (TOTCOL7/TOTCOL4)*100,TOTCOL12
            ASSIGN    (TOTCOL10/TOTCOL7)*100,TOTCOL13
            ASSIGN    (TOTCOL7-TOTCOL10)/TOTCOL9,TOTCOL14
.
            IF        TOTCOL2=0
              MOVE      ZERO,TOTCOL11       * cant divide by zero cause = 999.9
            ENDIF
            IF        TOTCOL4=0
              MOVE      ZERO,TOTCOL12
            ENDIF
            IF        TOTCOL7=0
              MOVE      ZERO,TOTCOL13
            ENDIF
            IF        TOTCOL9=0
              MOVE      ZERO,TOTCOL14
            ENDIF
.
            CALL      UND132
            PRINT     *1,"| Total  | ",TOTCOL2," |",TOTCOL3," | ":
                      TOTCOL4," | ",TOTCOL5," |",TOTCOL6," | ",TOTCOL7:
                      " |",TOTCOL8," |",TOTCOL9," | ",TOTCOL10," |  ":
                      TOTCOL11," |  ",TOTCOL12," |           ":
                      TOTCOL14," |"
            CALL      UND132
            IF        CLNO>50
              CALL      HEAD132
              PRINT     *20,"Start date : ",STRTDESC,*N:
                        *20,"End date   : ",ENDDESC,*N
            ENDIF
            PRINT     *N,"Session Allocation",*21,"= Session time allocated ":
                      "for session used (Booked and Extra)":
                      *N,"Session Time Used",*21,"= Session duration for  ":
                      "session used (Booked and Extra)":
                      *N,"Session Time Shared",*21,"= Reallocated time (where ":
                      "session not used by the doctor but used by other ":
                      "doctor)":
                      *N,"Throughput - Cases",*21,"= Number of cases for ":
                      "session used (Booked plus Extra)":
                      *N,"Throughput - Theatre Mins",*27,"= Total of theatre ":
                      "duration for each case":
                      *N,*21,"  (Multiple team will have operation time from ":
                      "the earliest start time of 1st team to the end time of ":
                      "last team)":
                      *N,"% Utilisation of Session Allocation":
                      *N,"  Booked  = Booked session time used divided by ":
                      "booked session time allocated X100":
                      *N,"  + Extra = Total session time used divided by ":
                      "total session time allocated X100":
....                  *N,*N,"% Utilisation of Session":
....                  *N,"  Theatre minutes (Start of operation to end of ":
....                  "operation) divided by total session time used X100":
                      *N,*N,"Average break per case = Session time used ":
                      "less theatre minutes divided by number of cases"
          ENDIF
          PRINT     *N,*1,"*** End of report ***" 
          CALL      KILL0000                      * erase temp file
.
RPRT9999  RETURN
+
.*************************************************************************
.*  ITOT0000  : initialise totals                                        *
.*************************************************************************
ITOT0000  MOVE      ZERO,TOTCOL2
          MOVE      ZERO,TOTCOL3
          MOVE      ZERO,TOTCOL4
          MOVE      ZERO,TOTCOL5
          MOVE      ZERO,TOTCOL6
          MOVE      ZERO,TOTCOL7
          MOVE      ZERO,TOTCOL8
          MOVE      ZERO,TOTCOL9
          MOVE      ZERO,TOTCOL10
          MOVE      ZERO,TOTCOL11
          MOVE      ZERO,TOTCOL12
          MOVE      ZERO,TOTCOL13
          MOVE      ZERO,TOTCOL14
ITOT9999  RETURN
+
.*************************************************************************
.*  Temp file IO's                                                       *
.*************************************************************************
RSTEMP1   RESET     KEY6
          READ      TEMPFILE,KEY6;;
          RETURN
.
RATEMP1   MOVE      ZERO,OVRCD
          RESET     KEY6
          READ      TEMPFILE,KEY6;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY6
          READ      TEMPFILE,KEY6;XXDOCT,XXALLBK,XXALLEX,XXUSEBK,XXUSEEX:
                                  XXUSESH,XXTHCAS,XXTHTIM
          GOTO      OVERCOND IF OVER
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMPFILE;XXDOCT,XXALLBK,XXALLEX,XXUSEBK,XXUSEEX:
                             XXUSESH,XXTHCAS,XXTHTIM
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTEMP1   UPDATE    TEMPFILE;XXDOCT,XXALLBK,XXALLEX,XXUSEBK,XXUSEEX:
                             XXUSESH,XXTHCAS,XXTHTIM
          RETURN
.
WRTEMP1   RESET     KEY6
          WRITE     TEMPFILE,KEY6;XXDOCT,XXALLBK,XXALLEX,XXUSEBK,XXUSEEX:
                                  XXUSESH,XXTHCAS,XXTHTIM
          RETURN
.
DETEMP1   DELETE    TEMPFILE,KEY6
          RETURN
+
.*************************************************************************
.*  KILL0000  :  erase temporary file                                    *
.*************************************************************************
KILL0000  CLOSE     TEMPFILE
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
KILL9999  RETURN
+
.*************************************************************************
.*  CRET0000  :  create temporary file                                   *
.*************************************************************************
CRET0000  CALL      KILL0000 
          CLEAR     CMDLINE 
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME,CMDLINE
          APPEND    " 41 U1-6",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPFILE,FNAME
.
          CALL      CLET0000                     * Clear tempfile
.
CRET9999  RETURN
+
.****************************************************************************
.*        Clear tempfile                                                    *
.****************************************************************************
CLET0000  PACK      KEY6,SP10
          CALL      RSTEMP1
          CALL      RKTEMP1
          BRANCH    OVRCD,CLET9999
.
          PACK      KEY6,XXDOCT
          CALL      DETEMP1
          GOTO      CLET0000
.
CLET9999  RETURN
+
.**********************************************************************
.*  KPER0000  :  keyin a period                                       *
.**********************************************************************
KPER0000  DISPLAY   *P1:9,*EF,"Starting ",CPERMTH,*P17:9,":":
                    *P1:11,"Ending ",CPERMTH,*P17:11,":"
.
          MOVE      NINE,CVERT                    * set up defaults
          MOVE      "19",CCOL
          MOVE      "24",EVERT
          MOVE      "1",ECOL
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
          MOVE      ZERO,CHIGHLT
          UNPACK    SP4,CMON,CYEAR
          MOVE      CCC,CCENT
.
KPER1000  CALL      KEYPER                        * keyin a date
          BRANCH    CQUEST,KPER1000               * don't allow for "?"
          BRANCH    OVRCD,KPER9000                * nothing entered
.
. no check if on PATDRGFD
.
          PACK      KEY6,ICENT,IYEAR,IMON
          REP       " 0",KEY6                     * zero fill date
          CALL      RDDRGA3
          BRANCH    OVRCD,KPER8500
.
          PACK      STRTDESC,DRGCNUM,SLASH,DRGCYR,SP8,DRGMDSC
          DISPLAY   *P31:9,DRGMDSC
          PACK      STARTPER,DRGYR,DRGNUM         * save start period
.
. ending date of selected period must be less than current date
.
          MATCH     DRGFDTE,CURRDATE
          GOTO      KPER8550 IF LESS
          GOTO      KPER8550 IF EQUAL
.
. --- now keyin Ending period ---
.
KPER5000  MOVE      "11",CVERT                    * set up defaults
          MOVE      "19",CCOL
          UNPACK    SP4,CYEAR,CMON
.
KPER6000  CALL      KEYPER                        * keyin a date
          BRANCH    CQUEST,KPER6000               * don't allow for "?"
          BRANCH    OVRCD,KPER0000                * nothing entered (default)
.
. no check if on PATDRGFD
.
          PACK      KEY6,ICENT,IYEAR,IMON
          REP       " 0",KEY6                     * zero fill date
          CALL      RDDRGA3
          BRANCH    OVRCD,KPER8400
.
          PACK      ENDDESC,DRGCNUM,SLASH,DRGCYR,SP8,DRGMDSC
          DISPLAY   *P31:11,DRGMDSC
          PACK      ENDPER,DRGYR,DRGNUM           * save start period
.
. ending period must be less than current period
.
          MATCH     DRGFDTE,CURRDATE
          GOTO      KPER8450 IF LESS
          GOTO      KPER8450 IF EQUAL
.
. ending period must be >= starting period
.
KPER6100  MATCH     ENDPER,STARTPER                 
          GOTO      KPER8700 IF LESS
          GOTO      KPER8700 IF EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"Invalid Range.  ";
          CALL      HITENTER
          GOTO      KPER0000
.
KPER8400  DISPLAY   *P1:24,*B,*EL,"Invalid Ending Period.  ";
          CALL      HITENTER
          GOTO      KPER5000
.
KPER8450  DISPLAY   *P1:24,*B,*EL,"Period must not be into future.  ";
          CALL      HITENTER
          GOTO      KPER5000
.
KPER8500  DISPLAY   *P1:24,*B,*EL,"Invalid Starting Period.  ";
          CALL      HITENTER
          GOTO      KPER0000
.
KPER8550  DISPLAY   *P1:24,*B,*EL,"Period must not be into future.  ";
          CALL      HITENTER
          GOTO      KPER0000
.
KPER8700  MOVE      FALSE,EXIT
          GOTO      KPER9999
.
KPER9000  MOVE      TRUE,EXIT                     * nothing entered 
.
KPER9999  RETURN
.
          INC       STD001IO
          INC       PATCOMN3
          INC       TFILENAM                * Generate Temp File Name
          INC       IBASEQIO/INC            * Sequential Numbers File
          INC       WEBERRIO/INC            * Web Server Error Logging
.
          INC       OPRCSUIO/INC
          INC       OPRCOUIO/INC
          INC       PATDRGIO/INC

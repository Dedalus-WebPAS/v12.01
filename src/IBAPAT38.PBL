.***************************************************************************
.* System    :   Patient                                                   *
.* Program   :   IBAPAT38                                                  *
.* Desc      :   Bed Board Processing                                      *
.***************************************************************************
.* Date      :   02/10/2008                                                *
.* Author    :   Ebon Clements                                             *
.* Mods      :                                                             *
.*-------------------------------------------------------------------------*
.* V12.00.01 19/05/2025 Thanh T         TSK 0955096                        *
.*           Changed for alphanumeric visit number                         *
.*-------------------------------------------------------------------------*
.* V11.05.01 17.12.2024  Ebon Clements  TSK 0951086                        *
.*           Increased size of NBRDAYS                                     *
.* V11.01.04 13/09/2021  Ebon Clements  TSK 0910279                        *
.*           Recompiled for BEDBDVCD (BEDBDPRO) - Post Op Ward             *
.* V11.01.03 27/08/2021  Ebon Clements  TSK 0910383                        *
.*           Recompiled for BEDBDVCD (BEDBDPRO) - Post Op / Expected Ward  *
.* V11.01.02 22/07/2021  Ebon Clements  TSK 0909330                        *
.*           Recompiled for BEDBDPRO - Post op ward                        *
.* V11.01.01 02/07/2021  Thanh T        TSK 0905755                        *
.*           Recompiled as BEDBDPRO changes                                *
.*-------------------------------------------------------------------------*
.*           :   V10.05.01 11/08/2014  Don Nguyen     CAR 297774           *
.*           :             Recompiled for changes to PMSBRQFD/IO           *
.*           :   V10.03.02 26/03/2013  Patrick Adair  CAR 281548           *
.*           :             Removed extraneous CLBOKRC1 code                *
.*           :   V10.03.01 10/05/2012  Peter Vela     CAR 232137           *
.*           :             Recompiled for BEDBDVCD                         *
.*           :   V10.01.03 11/01/2011  Ebon Clements  CAR 215701           *
.*           :             Only set the H/F stepdown indicator if hospital *
.*           :             days are greater than the expected LOS, not     *
.*           :             equal - UPHS0000                                *
.*           :   V10.01.02 25/11/2010  Ebon Clements  CAR 234288           *
.*           :             Fixed delete of prior expected patients         *
.*           :   V10.01.01 22/11/2010  Ebon Clements  CAR 215701           *
.*           :             Fixed I * C on discharge file                   *
.*           :   V9.12.04  03/12/2009  Ebon Clements  CAR 204362           *
.*           :             Recompiled for BEDBDPRO - Clear post op ward    *
.*           :   V9.12.03  11/11/2009  Ebon Clements  CAR 208991           *
.*           :             Recompiled for BEDBDVCD - Post op expected due  *  
.*           :             date                                            *
.*           :   V9.12.02  13/08/2009  Ebon Clements  CAR                  *
.*           :             Fixed option 1 load bed board pmsepbaf          *
.*           :   V9.12.01  24/06/2009  Ebon Clements  CAR 199232           *
.*           :             Recompiled for BEDBDVCD                         *
.*           :   V9.11.07  12/01/2009  Ebon Clements  CAR 174080           *
.*           :             Added option 6                                  *
.*           :   V9.11.06  10/12/2008  Ebon Clements  CAR 174080           *
.*           :             Include ward only wards in bed board load       *
.*           :   V9.11.05  01/12/2008  J.Tan          CAR 174070           *
.*           :             Added option to Update Health Fund Stepdown     *
.*           :   V9.11.04  01/12/2008  Ebon Clements  CAR 174057           *
.*           :             Changed to use BEDBDPRO                         *
.*           :   V9.11.03  28/10/2008  Ebon Clements  CAR 174077           *
.*           :             Added gender by room functionality              *
.*           :   V9.11.02  28/10/2008  Ebon Clements  CAR 174068           *
.*           :             Added urnumber to bed board and expected patient*
.*           :             files                                           *
.*           :   V9.11.01  09/10/2008  Ebon Clements  CAR 174077           *
.*           :             Added bed request file                          *
.*           :   V9.11.00  02/10/2008  Ebon Clements  CAR 174035           *
.*           :             Created program                                 *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       BOKRC1FD/INC
          INC       BOKRX1FD/INC
          INC       IBACONFD/INC
          INC       PATDSCFD/INC
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATTRNFD/INC
          INC       PATVISFD/INC
          INC       PATWR1FD/INC
          INC       PMSBBDFD/INC
          INC       PMSBRQFD/INC
          INC       PMSEPBFD/INC
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
ADMISSNO  DIM       8
BEDRWARD  INIT      "~~B"
CURRDATE  DIM       8
CALDAYS   FORM      4
FROMDATE  DIM       8
HOSPCODE  DIM       3
NBRDAYS   FORM      4
SAVESEQ1  FORM      2
SAVESEQ2  FORM      2
SAVESEQ3  FORM      2
SAVESEQ4  FORM      2
SAVESEQ5  FORM      2
SAVEROOM  DIM       3
TODATE    DIM       8
THETFLAG  FORM      1
URNUMBER  DIM       8
USERID    DIM       10
WARDCODE  DIM       3
WORKDATE  DATE      8
WRITEFLG  FORM      1
.
PRGID     INIT      "IBAPAT38"
PRGNAM    INIT      "Bed Board Processing"
VERSION   INIT      "V12.01.00"
.--------------------------------------------------------------------------
.                               ML0000                                      
.                       Controlling Logic (Mainline code)                  
.--------------------------------------------------------------------------
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
.
          BRANCH    COPTION,ML1000,ML2000,ML3000,ML4000,ML5000:
                            ML6000
.
ML1000    CALL      KWRD0000               * Keyin ward details
          BRANCH    EXIT,ML9999
.
          CALL      KHOS0000               * Keyin Hospital details
          BRANCH    EXIT,ML9999
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML1100:          * yes
                          ML1000:          * no
                          ML1000           * cancel
.
ML1100    CALL      LOAD0000               * Load ward details to bed board
          GOTO      ML9999                            
.
ML2000    CALL      KHOS0000               * Keyin Hospital details
          BRANCH    EXIT,ML9999
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2100:          * yes
                          ML2000:          * no
                          ML2000           * cancel
.
ML2100    CALL      LPAT0000               * Load patients
          GOTO      ML9999
.
ML3000    CALL      KHOS0000               * Keyin Hospital details
          BRANCH    EXIT,ML9999
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML3100:          * yes
                          ML3000:          * no
                          ML3000           * cancel
.
ML3100    CALL      DEXP0000    
          GOTO      ML9999
.
ML4000    CALL      KWRD0000               * Keyin ward details
          BRANCH    EXIT,ML9999
.
          CALL      KHOS0000               * Keyin Hospital details
          BRANCH    EXIT,ML9999
.         
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML4100:          * yes
                          ML4000:          * no
                          ML4000           * cancel
.
ML4100    CALL      DELT0000               * Clear bed board table
          GOTO      ML9999
.
ML5000    CALL      UPDT0000               * Update HF Stepdown
          GOTO      ML9999
.
ML6000    CALL      KHOS0000               * Keyin Hospital details
          BRANCH    EXIT,ML9999
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML6100:          * yes
                          ML6000:          * no
                          ML6000           * cancel
.
ML6100    CALL      CPAT0000               * Clear patients
          GOTO      ML9999
.
ML9999    CHAIN     PGM                    * chain back to program
.
.----------------------------------------------------------------------------
.                                 INIT0000                                   
.                              initialisation                                
.   The initialisation routine is used to display headings and open files.   
.----------------------------------------------------------------------------
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      BOKRC1A1,"bokrc1af"
          OPEN      BOKRC1A4,"bokrc1af"
          OPEN      BOKRX1A1,"bokrx1af"
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PATMI1A6,"patmi1af"
          OPEN      PATTRAN2,"pattranf"
          OPEN      PATHSPA1,"pathspaf"
          OPEN      PATVISA1,"patvisaf"
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATWR1A3,"patwr1af"
          OPEN      PATWR1A4,"patwr1af"
          OPEN      PMSBBDA1,"pmsbbdaf"
          OPEN      PMSBBDA2,"pmsbbdaf"
          OPEN      PMSBRQA1,"pmsbrqaf"
          OPEN      PMSEPBA1,"pmsepbaf"
          OPEN      PMSEPBA2,"pmsepbaf"
          OPEN      PMSPX2A1,"pmspx2af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
INIT9999  RETURN
.
.----------------------------------------------------------------------------
.                                 OPTN0000                                   
.                         get user options or exit                           
.                                                                            
.     Returns:  EXIT    = FALSE (0)  Run clean up                            
.                         TRUE  (1)  Exit option selected                    
.----------------------------------------------------------------------------
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Populate Bed Board Table" :
                                           " Ward/Bed Details":
                    *P1:6,*V2LON,TWO,*HOFF,". Upload Patients into Bed Board" :
                                           " Views":
                    *P1:7,*V2LON,THREE,*HOFF,". Remove Prior Expected Patient":
                                           " Records":
                    *P1:8,*V2LON,FOUR,*HOFF,". Clear Bed Board table":
                    *P1:9,*V2LON,FIVE,*HOFF,". Update Health Fund Stepdown":
                    *P1:10,*V2LON,SIX,*HOFF,". Clear Patients from Bed Board" :
                                           " Views"
.
OPTN0500  KEYIN     *P1:12,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:12,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000,OPTN9000,OPTN9000,OPTN9000,OPTN9000:
                            OPTN9000
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
.
.--------------------------------------------------------------------------
. keyin the ward code                                                     
.--------------------------------------------------------------------------
KWRD0000  DISPLAY   *P1:13,"Ward :",*EL
          MOVE      "8",ECOL
          MOVE      "13",EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND
          CALL      PATWRDKY
.
          MOVE      WWARD,WARDCODE
          ENDSET    WARDCODE
          APPEND    SP6,WARDCODE
          RESET     WARDCODE
.
          BRANCH    EXIT,KWRD2000,KWRD9000
.
          DISPLAY   *P15:13,*EL,WBDESC
          GOTO      KWRD4000
.
KWRD2000  DISPLAY   *P15:13,*EL,*V2LON,"All"
.
KWRD4000  MOVE      ZERO,EXIT
          GOTO      KWRD9999
.
KWRD9000  MOVE      ONE,EXIT
          GOTO      KWRD9999
.
KWRD9999  RETURN
.
.--------------------------------------------------------------------------
.  Keyin the hospital code                                                
.--------------------------------------------------------------------------
KHOS0000 DISPLAY    *P1:14,*EL,"Hospital Id : ";
         MOVE       TEN5,ECOL
         MOVE       "14",EVERT
         MOVE       SP3,CKYIDEF3
         MOVE       ZERO,CKYIMAND           * Not Mandatory
         OPEN       PATHSPA1,"pathspaf"
.
         CALL       PATHSPKY
         BRANCH     EXIT,KHOS1000,KHOS2000
         MOVE       PTHSHOSP,HOSPCODE
         MOVE       ZERO,EXIT
         GOTO       KHOS9999
.
KHOS1000 MOVE       SP70,HOSPCODE
         MOVE       ZERO,EXIT
         GOTO       KHOS9999
.
KHOS2000 MOVE       ONE,EXIT
KHOS9999 RETURN
.
.--------------------------------------------------------------------------
. Process wards and load bed board details                        
.--------------------------------------------------------------------------
LOAD0000  MATCH     SP70,WARDCODE                * Single ward or All
          GOTO      LOAD5000 IF NOT EQUAL
.
          PACK      KEY6,SP70
          CALL      RDSWARD3
LOAD1000  CALL      RDKWARD3
          BRANCH    OVRCD,LOAD9000
.
          MATCH     SP70,WBED
          GOTO      LOAD9000 IF NOT EQUAL  
.
          COMPARE   WACTIVE,ZERO                 * List only active wards
          GOTO      LOAD1000 IF NOT EQUAL
.
          IF        IBCNMHOS=1
            MATCH     HOSPCODE,PTWDHOSN
            GOTO      LOAD1000 IF NOT EQUAL
          ELSE
            MATCH     SP70,PTWDHOSN
            GOTO      LOAD1000 IF NOT EQUAL
          ENDIF
.
          MOVE      WWARD,WARDCODE              
          CALL      LBED0000                     * Process a single ward
.
          GOTO      LOAD1000
.
LOAD5000  CALL      LBED0000                     * Process a single ward
.
LOAD9000  CALL      WEXT0000                     * Write additions records
.
LOAD9999  RETURN
.
.--------------------------------------------------------------------------
.  Load selected ward details to bed board and expected patients 
.--------------------------------------------------------------------------
LBED0000  CALL      PATS0000                * Check patients
          BRANCH    EXIT,LBED9999
.
          PACK      KEY6,WARDCODE,SP70
          CALL      RDWARD1
          IF        OVRCD<>1
            CALL      RDPWARD1
          ENDIF
LBED1000  CALL      RDKWARD1
          BRANCH    OVRCD,LBED9999
.
          MATCH     WARDCODE,WWARD
          GOTO      LBED9999 IF NOT EQUAL
.
          COMPARE   WACTIVE,ZERO 
          GOTO      LBED1000 IF NOT EQUAL
.
          PACK      KEY20,PTWDHOSN,WWARD,SP70
          CALL      RSPMBBD1
LBED2000  CALL      RKPMBBD1
          BRANCH    OVRCD,LBED2500
.
          MATCH     PTWDHOSN,PMBDHOSP
          GOTO      LBED2500 IF NOT EQUAL
.
          MATCH     WWARD,PMBDWARD
          GOTO      LBED2500 IF NOT EQUAL
.
          MATCH     WBED,PMBDBEDD
          IF        @EQUAL
            MOVE      ONE,WRITEFLG                * Yes pmsbbdaf record 
            GOTO      LBED3000 
          ENDIF
.
          GOTO      LBED2000
.
LBED2500  MOVE      ZERO,PMBDSEQ1
          MOVE      ZERO,PMBDSEQ2
          MOVE      ZERO,PMBDSEQ3
          MOVE      ZERO,PMBDSEQ4
          MOVE      ZERO,PMBDSEQ5
          MOVE      SP70,PMBDROOM
          MOVE      ZERO,WRITEFLG                * No pmsbbdaf record 
.
LBED3000  MOVE      PMBDSEQ1,SAVESEQ1            * Save ward master record
          MOVE      PMBDSEQ2,SAVESEQ2            * sequence number
          MOVE      PMBDSEQ3,SAVESEQ3
          MOVE      PMBDSEQ4,SAVESEQ4
          MOVE      PMBDSEQ5,SAVESEQ5
          MOVE      PMBDROOM,SAVEROOM
.
          CALL      LBEX0000                    * Load expected patient beds
.
          BRANCH    WRITEFLG,LBED1000
.
          PACK      KEY20,PTWDHOSN,WWARD,SP3,WBED,SP70
          CALL      RDPMBBD1
          COMPARE   ONE,OVRCD
          GOTO      LBED1000 IF NOT EQUAL
.
          CALL      CLPMSBBD
.
          MOVE      PTWDHOSN,PMBDHOSP
          MOVE      WWARD,PMBDWARD
          MOVE      SP70,PMBDROOM
          MOVE      WBED,PMBDBEDD
.
          MOVE      SAVESEQ1,PMBDSEQ1            * Update sequence number
          MOVE      SAVESEQ2,PMBDSEQ2            * in all bed records for
          MOVE      SAVESEQ3,PMBDSEQ3            * this ward
          MOVE      SAVESEQ4,PMBDSEQ4
          MOVE      SAVESEQ5,PMBDSEQ5
.
          PACK      PMBDDATC,CCC,CYY,CMM,CDD
          REP       " 0",PMBDDATC
          CLOCK     TIME,PMBDTIMC
          MOVE      SP70,PMBDWEBC
.
          CALL      WRPMBBD1
.
          GOTO      LBED1000
.
LBED9999  RETURN
.
.--------------------------------------------------------------------------
.  Check that the bed board and expected patient files have no patients
.  for this ward before loading new ward/bed details
.--------------------------------------------------------------------------
.
PATS0000  PACK      KEY20,HOSPCODE,WARDCODE,SP70
          CALL      RSPMBBD1
PATS1000  CALL      RKPMBBD1
          BRANCH    OVRCD,PATS5000
.         
          IF        IBCNMHOS=1
            MATCH     HOSPCODE,PMBDHOSP
            GOTO      PATS5000 IF NOT EQUAL
          ENDIF
.         
          MATCH     WARDCODE,PMBDWARD
          GOTO      PATS5000 IF NOT EQUAL
.
          MATCH     SP70,PMBDURNO
          GOTO      PATS9100 IF NOT EQUAL
.
          MATCH     SP70,PMBDCADM
          GOTO      PATS9100 IF NOT EQUAL
.
          GOTO      PATS1000
.
PATS5000  PACK      KEY20,HOSPCODE,WARDCODE,SP70
          CALL      RSPMEPB1
PATS6000  CALL      RKPMEPB1
          BRANCH    OVRCD,PATS9000
.
          IF        IBCNMHOS=1
            MATCH     HOSPCODE,PMEBHOSP
            GOTO      PATS9000 IF NOT EQUAL
          ENDIF
.
          MATCH     WARDCODE,PMEBWARD
          GOTO      PATS9000 IF NOT EQUAL
.
          MATCH     SP70,PMEBURNO
          GOTO      PATS9100 IF NOT EQUAL
.
          MATCH     SP70,PMEBADMN
          GOTO      PATS9100 IF NOT EQUAL
.
          GOTO      PATS6000
.
PATS9000  MOVE      ZERO,EXIT
          GOTO      PATS9999
.
PATS9100  MOVE      ONE,EXIT
          GOTO      PATS9999
.
PATS9999  RETURN
.--------------------------------------------------------------------------
. Load ward/bed details to expected patient file
.--------------------------------------------------------------------------
LBEX0000  PACK      KEY20,PTWDHOSN,WWARD,SAVEROOM,WBED,SP70
          CALL      RDPMEPB1
          COMPARE   ONE,OVRCD
          GOTO      LBEX9999 IF NOT EQUAL
.
          CALL      CLPMSEPB
.
          MOVE      PTWDHOSN,PMEBHOSP
          MOVE      WWARD,PMEBWARD
          MOVE      SAVEROOM,PMEBROOM
          MOVE      WBED,PMEBBEDD
.
          MOVE      SAVESEQ1,PMEBSEQ1            * Update sequence number
          MOVE      SAVESEQ2,PMEBSEQ2            * in all bed records for
          MOVE      SAVESEQ3,PMEBSEQ3            * this ward
          MOVE      SAVESEQ4,PMEBSEQ4
          MOVE      SAVESEQ5,PMEBSEQ5
.
          PACK      PMEBDATC,CCC,CYY,CMM,CDD
          REP       " 0",PMEBDATC
          CLOCK     TIME,PMEBTIMC
          MOVE      SP70,PMEBWEBC
.
          CALL      WRPMEPB1
.
LBEX9999  RETURN
.--------------------------------------------------------------------------
.  Write extra ward records to the bed board.
.                ~~W = Ward not specified
.                ~~B = Bed Requests
.--------------------------------------------------------------------------
WEXT0000  CALL      CLPMSBBD
.
          BRANCH    IBCNMHOS,WEXT5000
.
          MOVE      HOSPCODE,PMBDHOSP
          MOVE      "~~W",PMBDWARD               * Ward not specified
.
          PACK      PMBDDATC,CCC,CYY,CMM,CDD
          REP       " 0",PMBDDATC
          CLOCK     TIME,PMBDTIMC
          MOVE      SP70,PMBDWEBC
.
          PACK      KEY20,PMBDHOSP,PMBDWARD,PMBDROOM,PMBDBEDD,PMBDCADM,SP70
          CALL      RAPMBBD1
          IF        OVRCD=1
            CALL      WRPMBBD1
          ENDIF
.
WEXT5000  MOVE      HOSPCODE,PMBDHOSP
          MOVE      "~~B",PMBDWARD               * Bed requests
.
          PACK      PMBDDATC,CCC,CYY,CMM,CDD
          REP       " 0",PMBDDATC
          CLOCK     TIME,PMBDTIMC
          MOVE      SP70,PMBDWEBC
.
          PACK      KEY20,PMBDHOSP,PMBDWARD,PMBDROOM,PMBDBEDD,PMBDCADM,SP70
          CALL      RAPMBBD1
          IF        OVRCD=1
            CALL      WRPMBBD1
          ENDIF
.
          CALL      CLPMSEPB
.
          MOVE      HOSPCODE,PMEBHOSP
          MOVE      "~~W",PMEBWARD               * Ward not specified
.         
          PACK      PMEBDATC,CCC,CYY,CMM,CDD
          REP       " 0",PMEBDATC
          CLOCK     TIME,PMEBTIMC
          MOVE      SP70,PMEBWEBC
.         
          PACK      KEY20,PMEBHOSP,PMEBWARD,PMEBROOM,PMEBBEDD,PMEBADMN,SP70
          CALL      RAPMEPB1
          IF        OVRCD=1
            CALL      WRPMEPB1
          ENDIF
.
          MOVE      HOSPCODE,PMEBHOSP
          MOVE      "~~B",PMEBWARD               * Bed requests
.
          PACK      PMEBDATC,CCC,CYY,CMM,CDD
          REP       " 0",PMEBDATC
          CLOCK     TIME,PMEBTIMC
          MOVE      SP70,PMEBWEBC
.
          PACK      KEY20,PMEBHOSP,PMEBWARD,PMEBROOM,PMEBBEDD,PMEBADMN,SP70
          CALL      RAPMEPB1
          IF        OVRCD=1
            CALL      WRPMEPB1
          ENDIF
.
WEXT9999  RETURN
.
.--------------------------------------------------------------------------
.  Delete prior expected patient records
.--------------------------------------------------------------------------
DEXP0000  CALL      IBACLOCK
          PACK      WORKDATE,CCC,CYY,CMM,CDD
          REP       " 0",WORKDATE
          SUB       THREE,WORKDATE               * 3 days prior to today
.
          PACK      KEY20,HOSPCODE,SP70
          CALL      RSPMEPB1
DEXP1000  CALL      RKPMEPB1
          BRANCH    OVRCD,DEXP9999
.
          MATCH     HOSPCODE,PMEBHOSP
          GOTO      DEXP9999 IF NOT EQUAL
.
          MATCH     SP70,PMEBEDAT
          GOTO      DEXP1000 IF EQUAL
.
          MATCH     WORKDATE,PMEBEDAT
          GOTO      DEXP1000 IF NOT LESS
.
          MATCH     SP70,PMEBADMN
          GOTO      DEXP1000 IF EQUAL
.
          PACK      KEY20,PMEBHOSP,PMEBWARD,PMEBROOM,PMEBBEDD,PMEBADMN,SP70
          CALL      DEPMEPB1
.
          GOTO      DEXP1000
.
DEXP9999  RETURN
.
.--------------------------------------------------------------------------
.  Load patients into bed board views
.--------------------------------------------------------------------------
LPAT0000  CALL      LEXP0000                     * Load Expected Patients
.
          PACK      KEY9,TWO,SP70
          CALL      RDSMISS2
LPAT1000  CALL      RDKMISS2
          BRANCH    OVRCD,LPAT9999
.
          COMPARE   TWO,ASTAT                    * Current Admission
          GOTO      LPAT9999 IF NOT EQUAL
.
          MATCH     SP70,AWARD                   * Standby Patient
          GOTO      LPAT1000 IF EQUAL
.
          MOVE      AADMNO,ADMISSNO
          MOVE      AURNO,URNUMBER
          PROC      BBUP0000                * Bed Board
.
          GOTO      LPAT1000
.
LPAT9999  RETURN
.
.--------------------------------------------------------------------------
.  Load expected patients
.--------------------------------------------------------------------------
LEXP0000  PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          PACK      KEY9,ONE,SP70
          CALL      RDSMISS2
LEXP1000  CALL      RDKMISS2
          BRANCH    OVRCD,LEXP3000
.
          COMPARE   ONE,ASTAT                    * Pre Admission
          GOTO      LEXP3000 IF NOT EQUAL
.
          MATCH     CURRDATE,ADATE
          GOTO      LEXP1000 IF LESS
.
          MOVE      AADMNO,ADMISSNO
          MOVE      AURNO,URNUMBER
          PROC      BBUP0000              * Bed Board 
.                                                * post op
          GOTO      LEXP1000
.
LEXP3000  PACK      KEY20,TWO,SP70
          CALL      RDSMISS6
LEXP4000  CALL      RDKMISS6                     * Load any standby patients
          BRANCH    OVRCD,LEXP5000
.
          COMPARE   TWO,ASTAT                    * Current admission
          GOTO      LEXP5000 IF NOT EQUAL
.
          MATCH     SP70,AWARD                   * Standby patient
          GOTO      LEXP5000 IF NOT EQUAL
.
          MOVE      AURNO,URNUMBER
          MOVE      AADMNO,ADMISSNO
          PROC      BBUP0000              * Bed Board 
.                                                * post op
          GOTO      LEXP4000
.
LEXP5000  PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          PACK      KEY16,CURRDATE,SP70
          CALL      RSBKREC4
LEXP6000  CALL      RKBKREC4
          BRANCH    OVRCD,LEXP9999
.
          COMPARE   ZERO,BKRESTAT                * Booked only
          GOTO      LEXP6000 IF NOT EQUAL
.
          MOVE      BKREURNO,URNUMBER
          MOVE      BKREBOOK,ADMISSNO
          PROC      BBUP0000              * Bed Board 
.                                            
          GOTO      LEXP6000
.
LEXP9999  RETURN
.
.--------------------------------------------------------------------------
. Delete all bed board detail records
.--------------------------------------------------------------------------
DELT0000  CALL      DELX0000                     * Delete expected patients
.
          PACK      KEY20,HOSPCODE,SP70
          CALL      RSPMBBD1
DELT1000  CALL      RKPMBBD1
          BRANCH    OVRCD,DELT9999
.
          IF        IBCNMHOS=1
            MATCH     HOSPCODE,PMBDHOSP
            GOTO      DELT9999 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,WARDCODE
          IF        !@EQUAL
            MATCH     WARDCODE,PMBDWARD
            GOTO      DELT1000 IF NOT EQUAL
          ENDIF
.
          PACK      KEY20,PMBDHOSP,PMBDWARD,PMBDROOM,PMBDBEDD,PMBDCADM,SP70
          CALL      DEPMBBD1

          GOTO      DELT0000
.
DELT9999  RETURN
.
.--------------------------------------------------------------------------
. Delete all expected patient detail records
.--------------------------------------------------------------------------
DELX0000  PACK      KEY20,HOSPCODE,SP70
          CALL      RSPMEPB1
DELX1000  CALL      RKPMEPB1
          BRANCH    OVRCD,DELX9999
.
          IF        IBCNMHOS=1
            MATCH     HOSPCODE,PMEBHOSP
            GOTO      DELX9999 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,WARDCODE
          IF        !@EQUAL
            MATCH     WARDCODE,PMEBWARD
            GOTO      DELX1000 IF NOT EQUAL
          ENDIF
.
          PACK      KEY20,PMEBHOSP,PMEBWARD,PMEBROOM,PMEBBEDD,PMEBADMN,SP70
          CALL      DEPMEPB1

          GOTO      DELX0000
.
DELX9999  RETURN
+
.--------------------------------------------------------------------------
.  Load patients for HF stepdown      
.--------------------------------------------------------------------------
UPDT0000  MOVE      SP70,KEY20
          CALL      RSPMBBD1
UPDT1000  CALL      RKPMBBD1
          BRANCH    OVRCD,UPDT9999
.
          MATCH     SP70,PMBDCADM           * Current admission
          GOTO      UPDT1000 IF EQUAL
.
          MOVE      PMBDCADM,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,UPDT1000
.
          MOVE      ADATE,FROMDATE
          PACK      TODATE,CCC,CYY,CMM,CDD
          IF        ASTAT=3
            CALL      RDDSCH1
            IF        OVRCD=0
              MOVE      DDATE,TODATE
            ENDIF
          ENDIF
          REP       " 0",TODATE
          CALL      NHOSPDAY
.
          CALL      UPHS0000                * Update HF stepdown
          GOTO      UPDT1000
.
UPDT9999  RETURN
+
.--------------------------------------------------------------------------
.  Update HF stepdown                 
.--------------------------------------------------------------------------
UPHS0000  MATCH     "Y",PTMICMXP
          GOTO      UPHS6000 IF NOT EQUAL    * Not C/P
.
.         If the Current LOS is greater than Expected LOS, set indicator to 1
.
          COMPARE   NBRDAYS,ASTAY
          GOTO      UPHS8000 IF LESS
.
          GOTO      UPHS9999
.
.         Per diem
.
UPHS6000  PACK      KEY30,AADMNO,Z70
          CALL      RDSTRAN2
          CALL      RDPTRAN2
          BRANCH    OVRCD,UPHS9999
          MATCH     TADMN,AADMNO
          GOTO      UPHS9999 IF NOT EQUAL
.
          COMPARE   NBRDAYS,TRBEND
          GOTO      UPHS8000 IF LESS
.
          GOTO      UPHS9999
.
UPHS8000  MOVE      "1",PMBDHFST
          CALL      UPPMBBD1
          GOTO      UPHS9999
.
UPHS9999  RETURN
+
.--------------------------------------------------------------------------
. Clear patients from bed board and expected patient files
.--------------------------------------------------------------------------
CPAT0000  PACK      KEY20,HOSPCODE,SP70
          CALL      RSPMBBD1
CPAT1000  CALL      RKPMBBD1
          BRANCH    OVRCD,CPAT5000
.
          IF        IBCNMHOS=1
            MATCH     HOSPCODE,PMBDHOSP
            GOTO      CPAT5000 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,PMBDURNO
          IF        @EQUAL
            MATCH     SP70,PMBDCADM
            GOTO      CPAT1000 IF EQUAL
          ENDIF
.
          MOVE      PMBDURNO,URNUMBER
          MOVE      PMBDCADM,ADMISSNO
          CALL      BBCL0000                * Clear patient from bed board table
.
          GOTO      CPAT0000
.
CPAT5000  PACK      KEY20,HOSPCODE,SP70
          CALL      RSPMEPB1
CPAT6000  CALL      RKPMEPB1
          BRANCH    OVRCD,CPAT9999
.
          IF        IBCNMHOS=1
            MATCH     HOSPCODE,PMEBHOSP
            GOTO      CPAT9999 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,PMEBURNO
          IF        @EQUAL
            MATCH     SP70,PMEBADMN
            GOTO      CPAT6000 IF EQUAL
          ENDIF
.
          MOVE      PMEBURNO,URNUMBER
          MOVE      PMEBADMN,ADMISSNO
          CALL      EXCL0000           * Clear patient from expected patient
.                                      * file
          GOTO      CPAT5000
.
CPAT9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       BEDBDPRO
          INC       BEDBDVCD
          INC       STD001IO
          INC       PATWRDKY
          INC       PATHSPKY
          INC       CLBOKRX1
          INC       CLPMSBBD
          INC       CLPMSEPB
          INC       NHOSPDAY
.
          INC       BOKRC1IO/INC
          INC       BOKRX1IO/INC
          INC       PATDSCIO/INC
          INC       PATMI1IO/INC
          INC       PATMA1IO/INC
          INC       PATHSPIO/INC
          INC       PATTRNIO/INC
          INC       PATVISIO/INC
          INC       PATWR1IO/INC
          INC       PMSBBDIO/INC
          INC       PMSBRQIO/INC
          INC       PMSEPBIO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC

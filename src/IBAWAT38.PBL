.******************************************************************************
.* System        :  Waiting Lists                                             *
.* Program       :  IBAWAT38.PBL                                              *
.* Description   :  Condensed Unscheduled Patients Report                     *
.******************************************************************************
.* Date          :  17/07/1990                                                *
.* Author        :  Justin Coates                                             *
.* Function      :  Print all patients with a one (unscheduled) or            *
.*                  six (cancelled) procedure status                          *
.*                  in either Doctor Code order or Unit/Clinic order          *
.* Modifications :                                                            *
.*                                                                            *
.******************************************************************************
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899                         *
.*           Recompiled as WATTR1FD changes                                   *
.******************************************************************************
.*       V10.05.01  01/08/2015  Ania P                      CAR 261630        *
.*                  Removed the use of port for naming temp files             *
.*        V5.08.01  29/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.04.01  26/06/1997  Howellsy         505                          *
.*                  Use PATDOCKY instead of PATDOCDS                          *
.******************************************************************************
.*        V5.01.02  23/05/91  Steve O'Gorman                                  *
.*                  Fixed Errors found by the New Compiler                    *
.*        V5.01.03  19/02/1993    Justin Coates  (DUDLEY)                     *
.*                  allow for status 7/8                                      *
.*        V5.01.04  06/03/1994    Paul Howells   (DUDLEY)                     *
.*                  Change to Dudley requirements.                            *
.******************************************************************************
.
.         IBAWAT38.PBL        Condensed Unsheduled Patients Report
.         ============        ====================================
.         KSEQ0000 - determine which report to do
.
.         UNCL0000 - produces a report for Unit/Clinic order
.             KDAT0000 - enter the from and to dates
.             KUCC0000 - enter the Unit/Clinic for the report
.             CLRP0000 - produce the report
.
.         DOCT0000 - produces a report for Doctor code order
.             KDAT0000 - enter the from and to dates
.             KDOC0000 - enter the Doctor code for the report
.             DORP0000 - produce the report
.         
.
          INC       STD001FD                * common variables
          INC       PATCODFD/INC            * codes file
          INC       PATDO1FD/INC            * doctors file
          INC       PATMA1FD/INC            * master file
          INC       WATCONFD/INC            * WL controlfile
          INC       WATTR1FD/INC            * WL treatment file
          INC       WATSEAFD/INC
          INC       WATSEIFD/INC
          INC       WATSUSFD/INC
          INC       WATCATFD/INC
          INC       IBASEQFD/INC 
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
. ------ program variables ------
.
ASATDTE   DIM       8
CODE      DIM       2                       * PATDSCOD
CMDLINE   DIM       80                      * line for 'execute' command
CNTPATCL  FORM      5                       * no. patients in a unit/clinic
CNTPATTL  FORM      5                       * total no. of patients in report
CNTCAN    FORM      5
CNTUN     FORM      5
CURRDATE  DIM       8                       * current date ccyymmdd
DATEFROM  DIM       8                       * from date for report ccyymmdd
DATETO    DIM       8                       * to date for report ccyymmdd
DIM3      DIM       3                       * PATDSDOC
DIM14     DIM       14                      * dummy variable
DIM26     DIM       26                      * holds unit : code - description
DIM31     DIM       31                      * holds doct : code - description
DOCCODE   DIM       6
DOCTCODE  DIM       6                       * doctor code entered
DOCTDESC  DIM       40                      * doctor description
NEXTDAY   DIM       8
PTCNDCQS  FORM      1
QUPER     FORM      1                       * ? performed flag
UNCLCODE  DIM       3                       * unit/clinic code entered
UNCLDESC  DIM       20                      * unit/clinic description
WTSPDTCK  DIM       8         * date to check for suspension
WTSPSTRT  DIM       8         * start of suspension
WTSPSTOP  DIM       8         * end   of suspension
.
. ------ variables for the report ------
.
PRDESC    DIM       36                      * procedure description
PRDOCT    DIM       6                       * previous doct
PRFROM    DIM       30                      * from date and description
PRNAME    DIM       21                      * patients name
PRPRTY    DIM       8                       * priority
PRTO      DIM       30                      * to date and description
PRUNDO1   DIM       45                      * current unit/doct code
PRUNDO2   DIM       45                      * unit/doct code entered
PRUNIT    DIM       3                       * previous unit
.
. ------ variables needed for the temporary index file ------
.
CLINFILE  IFILE     SQL, FIXED=101, KEY="U76-78,79-86,1-8,9-17,18-19"
DOCTFILE  IFILE     SQL, FIXED=101, KEY="U70-75,79-86,1-8,9-17,18-19"
.
.Name     Type      Length   Phys. Sta-Fin
.----     ----      ------   ----- -------
XWMURNO   DIM       8         8      1-8    * patients UR number
XWMCODE   DIM       9         9      9-17   * procedure code
DXWMCNT   DIM       2         2     18-19   * procedure code count as a dim
XWMDESC   DIM       50        50    20-69   * procedure description
XWMDOCTO  DIM       6         6     70-75   * doctors description
XWMUNIT   DIM       3         3     76-78   * unit/clinic code
XWMDATE1  DIM       8         8     79-86   * clinical dec to adm ccyymmdd
XWMPTY    DIM       3         3     87-89   * priority code
XWMSTAT   FORM      1         2     90-91   * procedure status
XWMUDEF1  DIM       3         3     92-94   * UD field 1
XWMUDEF2  DIM       3         3     95-97   * UD field 2
XWMPCAT   DIM       3         3     98-100  * patient classification code
.
.End of Record                     101-101
.
XWMCNT    FORM      2                       * procedure code count as a form
.
. ------ program constants ------
.
ALL       INIT      "All"                   * default U/C search code
CATA      INIT      "A "                    * Pat. Class category
CATTP     INIT      "TP"                    * Priority category
CATW1     INIT      "W1"                    * UD field 1 category
CATWU     INIT      "WU"                    * U/C category
DASH      INIT      " - "                   * a dash
FILPREC   INIT      "wat38c"                * prefix for temp file clinic
FILPRED   INIT      "wat38d"                * prefix for temp file doctors
NEEDNP    FORM      "48"                    * limit when new tble headg printed
PREFDOCT  INIT      "Doctor Code : "        * Doctor Code prefix for report
PREFUNCL  INIT      "Unit/Clinic Code : "   * Unit/Clinic prefix for report
PREFFROM  INIT      "From Date         : "  * From date prefix for report
PREFTO    INIT      "To   Date         : "  * To date prefix for report
PRINTLIM  FORM      "55"                    * no. lines to print per page
START     INIT      "Start"                 * default from date
CODECC    INIT      "CC"
Z30       INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
.
PRGNAM    INIT      "Condensed Unscheduled Patients"
PRGID     INIT      "IBAWAT38"
VERSION   INIT      "V12.01.00"
.
.*********************************************************************
.*                  ML0000                                           *
.*        Mainline Code / Controlling Logic                          *
.*********************************************************************
ML0000    CALL      INIT0000                * initialization and open files
.
ML0500    CALL      KSEQ0000                * enter the report type
          BRANCH    MOPTION,ML1000:         * unit/clinic sequence
                            ML2000          * doctor code sequence
.
          GOTO      ML9999
.
. ------ produce a report in Unit/Clinic sequence ------
.
ML1000    CALL      UNCL0000                * produce Unit/Clinic report
          GOTO      ML0500
.
. ------ produce a report in Doctor Code sequence ------
.
ML2000    CALL      DOCT0000                * produce Doctor Code report
          GOTO      ML0500
.
ML9999    CHAIN     PGM
+
.*********************************************************************
.*                  INIT0000                    Called by : ML0000   *
.*        Initialization and open files                              *
.*********************************************************************
INIT0000  DISPLAY   *P1:1,*EF               * clear screen
          CALL      DISPHEAD                * display program heading
          CALL      IBACLOCK                * get current date/time
          CALL      TFILENAM
.
          OPEN      PATCODE1,"patcodes"     * open codes file
          OPEN      PATDO1A1,"patdo1af"     * open doctors file
          OPEN      PATDO1A2,"patdo1af"     * open doctors file
          OPEN      PATMA1A1,"patma1af"     * open master file
          OPEN      PATMX1A1,"patmx1af"     * open master extension file
          OPEN      WATTR1A6,"wattr1af"     * WL treatment file
          OPEN      WATCATA1,"watcataf"
          OPEN      WATCATA2,"watcataf"
          OPEN      WATSUSA1,"watsusaf"
          OPEN      WATSEIA1,"watseiaf"
          OPEN      WATSEIA2,"watseiaf"
          OPEN      WATSEAA1,"watseaaf"
          OPEN      CONTROLF,"controlf"     * WL control file
.
          READ      CONTROLF,TWENTY1;*247,PTCNDCQS
          READ      CONTROLF,THIRTY7;*122,WTCNTPUC,*185,WTCNTPDS
.
          MOVE      ONE,CNEWDS              * new ? option
.
INIT9999  RETURN
+
.*********************************************************************
.*                  KSEQ0000                    Called by : ML0500   *
.*        Enter the sequence to report by                            *
.*        Returns : MOPTION = 0   exit selected                      *
.*                  MOPTION = 1   unit/clinic selected               *
.*                  MOPTION = 2   doctor code selected               *
.*********************************************************************
KSEQ0000  DISPLAY   *P1:3,*EF:
                    *V2LON,*P1:4,"0",*P1:5,"1",*P1:6,"2",*HOFF:
                    *P3:4,"= Exit":
                    *P3:5,"= Unit/Clinic Code Sequence":
                    *P3:6,"= Doctor Code Sequence":
                    *P1:8,"Select Option : "
.
KSEQ1000  KEYIN     *P17:8,*DV,UNDLN1:
                    *P17:8,*V2LON,MOPTION:
                    *P17:8,*DV,MOPTION 
.
          BRANCH    MOPTION,KSEQ9999,KSEQ9999  * 1 or 2 entered
.
          COMPARE   ZERO,MOPTION            * was 0 entered
          GOTO      KSEQ9999 IF EQUAL       * yes
.
          BEEP
          GOTO      KSEQ1000                * invalid selection
.
KSEQ9999  RETURN
+
.*********************************************************************
.*                  UNCL0000                    Called by : ML1000   *
.*        Produce a report in Unit/Clinic sequence                   *
.*********************************************************************
UNCL0000  DISPLAY   *P55:2,*V2LON,"- Unit/Clinic Code Sequence "
.
UNCL0500  CALL      KDAT0000                * enter the dates
          BRANCH    EXIT,UNCL9000           * no to date entered
.
          CALL      KUCC0000                * enter the Unit/Clinic Code
.
          CALL      CONTQST                 * continue question
          BRANCH    CEXIT,UNCL1000:         * Yes entered
                          UNCL0500:         * No entered
                          UNCL9000          * Cancel entered
.
UNCL1000  CALL      CLRP0000                * generate the report
.
UNCL9000  HLINE     *G37,2,55,80            * clear the sub-heading
.
UNCL9999  RETURN
+
.*********************************************************************
.*                  DOCT0000                    Called by : ML2000   *
.*        Produce a report in Doctor Code sequence                   *
.*********************************************************************
DOCT0000  DISPLAY   *P55:2,*V2LON,"- Doctor Code Sequence "
.
          CALL      KDAT0000                * enter the dates
          BRANCH    EXIT,DOCT9000           * no to date entered
.
          CALL      KDOC0000                * enter the doctors code
.
          CALL      CONTQST                 * continue question
          BRANCH    CEXIT,DOCT1000:         * Yes entered
                          DOCT0000:         * No entered
                          DOCT9000          * Cancel entered
.
DOCT1000  CALL      DORP0000                * generate the report
.
DOCT9000  HLINE     *G37,2,55,80            * clear the sub-heading
.
DOCT9999  RETURN
+
.*********************************************************************
.*                  KDAT0000                    Called by : UNCL0000 *
.*        Enter the from date and to date for the report    DOCT0000 *
.*        Returns   DATEFROM      the from date                      *
.*                  DATETO        the to date                        *
.*                  EXIT = 1      nothing entered for 'dateto'       *
.*********************************************************************
KDAT0000  DISPLAY   *P1:10,*EF:
                    *P1:11,"From Date         : ":
                    *P1:12,"To   Date         : "
.
KDAT0100  CALL      IBACLOCK                * get current date and time
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE           * zero fill
.
. ------ set up the common parameters for keydate ------
.
          MOVE      ONE,CCANLDTE            * can cancel default
          MOVE      ONE,CDEFDTE             * hit enter once to accept default
          MOVE      ZERO,CHIGHLT            * use highlighting
          MOVE      CCC,CCENT               * default to current century
          UNPACK    SP6,CYEAR,CMON,CDAY     * no default date
.
. ------ enter the from date ------
.
          MOVE      TEN1,CVERT              * row for input
          MOVE      "22",CCOL               * column for input
          CALL      KEYDATE                 * enter the date
.
          MATCH     SP2,CYEAR               * was a date entered ?
          GOTO      KDAT1000 IF EQUAL       * no, use default
.
          PACK      DATEFROM,CCENT,CYEAR,CMON,CDAY
          REP       " 0",DATEFROM           * zero fill
          RESET     DATEFROM
.
. ------ check that FROM date is not into the future ------
.
          MATCH     DATEFROM,CURRDATE       * is current date < entered date ?
          GOTO      KDAT5000 IF LESS        * yes so invalid
.
          CALL      PACDATE                 * format date
          PACK      PRFROM,PREFFROM,CPCDATE * set up the date for printing
          RESET     PRFROM   
          GOTO      KDAT2000                * enter to date
.
. ------ use default from date ------
.
KDAT1000  DISPLAY   *PCCOL:CVERT,*V2LON,START      * display start
          PACK      DATEFROM,START,SP8      * set up the date
          RESET     DATEFROM
          PACK      PRFROM,PREFFROM,DATEFROM * set up the date for printing
          RESET     PRFROM   
.
. ------ enter the to date ------
.
KDAT2000  MOVE      TEN2,CVERT              * row for input
          MOVE      "22",CCOL               * column for input
          UNPACK    SP6,CYEAR,CMON,CDAY     * no default date
          CALL      KEYDATE                 * enter the date
.
          MATCH     SP2,CYEAR               * was a date entered ?
          GOTO      KDAT9000 IF EQUAL       * no, use default
.
          PACK      DATETO,CCENT,CYEAR,CMON,CDAY
          REP       " 0",DATETO             * zero fill
          RESET     DATETO   
          CALL      PACDATE                 * format date
.
. ------ check that TO date isnt into the future ------
.
          MATCH     DATETO,CURRDATE         * is current date < entered ??
          GOTO      KDAT5100 IF LESS        * yes, invalid answer
.
. ------ check that to date is after from date ------
.
          MATCH     START,DATEFROM          * was default entered
          GOTO      KDAT2100 IF EQUAL       * yes, cont do check
.
          MATCH     DATEFROM,DATETO         * is to < from date ?
          GOTO      KDAT5200 IF LESS        * yes, invalid to date
.
KDAT2100  MOVE      FALSE,EXIT              * set exit status
          PACK      PRTO,PREFTO,CPCDATE     * set up print date line
          RESET     PRTO
          GOTO      KDAT9999
.
. ------ ERROR: FROM date is into the future ------
.
KDAT5000  DISPLAY   *P1:24,*B,"The FROM date musn't be into the future.  ",*EL;
          CALL      HITENTER
          GOTO      KDAT0100
.
. ------ ERROR: TO date is into the future ------
.
KDAT5100  DISPLAY   *P1:24,*B,"The TO date musn't be into the future.  ",*EL;
          CALL      HITENTER
          GOTO      KDAT2000
.
. ------ ERROR: to date is less than from date ------
.
KDAT5200  DISPLAY   *P1:24,*B,"The TO date must not be before the FROM date.  ";
          CALL      HITENTER
          GOTO      KDAT2000
.
. ------ no to date entered ------
.
KDAT9000  MOVE      ONE,EXIT                * no date entered
          UNPACK    SP8,DATEFROM            * clear from date
          UNPACK    SP8,DATETO              * clear to date
.
KDAT9999  RETURN
+
.*********************************************************************
.*                  KUCC0000                    Called by : UNCL0000 *
.*        Keyin the Unit/Clinic Code                                 *
.*        Returns : UNCLCODE      the unit/clinic code               *
.*                  UNCLDESC      the unit/clinic description        *
.*********************************************************************
KUCC0000  MOVE      ONE,QUPER               * ? not performed
          MOVE      TEN5,CVERT              * row for input
          MOVE      "24",CCOL               * column for input
.
          DISPLAY   *P1:15,"For Unit/Clinic Code : "
.
KUCC1000  KEYIN     *PCCOL:CVERT,*DV,UNDLN3:
                    *PCCOL:CVERT,*V2LON,UNCLCODE:
                    *PCCOL:CVERT,*DV,UNCLCODE
.
          ENDSET    UNCLCODE
          APPEND    SP3,UNCLCODE
          RESET     UNCLCODE
.
          MATCH     SP3,UNCLCODE            * was anything entered ?
          GOTO      KUCC4000 IF EQUAL       * no
.
          MATCH     QUEST,UNCLCODE          * was ? entered ?
          GOTO      KUCC2000 IF NOT EQUAL   * no
.
. ------ ? entered ------
.
          PACK      CODE,CATWU              * set up category for codes file
          CALL      PATCODDS                * ? routine
          MOVE      ZERO,QUPER              * set ? as performed
          MOVE      "24",CVERT              * input on line 24
          MOVE      "24",CCOL               * input in col 24
          PACK      UNCLCODE,SP3            * clear the code
          DISPLAY   *P1:24,"For Unit/Clinic Code : ",*EL
          GOTO      KUCC1000

. ------ validate what was entered ------
.
KUCC2000  PACK      KEY5,CATWU,UNCLCODE     * key = cat and value entered
          CALL      RDCODE1                 * read codes file
          BRANCH    OVRCD,KUCC3000          * invalid entry
.
          MOVE      TDESC,UNCLDESC          * set up description
          PACK      PRUNDO2,PREFUNCL,UNCLCODE,DASH,TDESC * set up print heading
          RESET     PRUNDO2
          GOTO      KUCC5000
.
. ------ ERROR: invalid code entered ------
.
KUCC3000  DISPLAY   *P1:24,*B,"Invalid Unit/Clinic Code entered.  ",*EL;
          CALL      HITENTER
          PACK      UNCLCODE,SP3            * clear the code
.
          BRANCH    QUPER,KUCC1000          * ? not entered
.
          DISPLAY   *P1:24,"For Unit/Clinic Code : ",*EL
          GOTO      KUCC1000
.
. ------ only enter hit ------
.
KUCC4000  PACK      UNCLCODE,ALL,SP1        * set up default value
          PACK      UNCLDESC,SP20           * clear description
          PACK      PRUNDO2,PREFUNCL,ALL,SP30 * set up 'all' description
          RESET     PRUNDO2
.
. ------ valid entry ------
.
KUCC5000  BRANCH    QUPER,KUCC8000          * ? not entered
.
          CALL      RDIS0000                * redisplay the screen
.
          MATCH     ALL,UNCLCODE            * was anything entered ??
          GOTO      KUCC0000 IF EQUAL       * no, so re-enter
.
          DISPLAY   *P1:15,"For Unit/Clinic Code : "
.
KUCC8000  DISPLAY   *P24:15,*V2LON,UNCLCODE,*P40:15,*HOFF,UNCLDESC
.
KUCC9999  RETURN
+
.*********************************************************************
.*                  KDOC0000                    Called by : DOCT0000 *
.*        Keyin the Doctors Code                                     *
.*        Returns : DOCTCODE      the doctor code                    *
.*                  DOCTDESC      the doctor description             *
.*********************************************************************
KDOC0000  DISPLAY   *P1:15,"For Doctor Code   : "
.
          MOVE      SP10,DOCTCODE
          MOVE      "22",ECOL
          MOVE      "15",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          CALL      PATDOCKY
          BRANCH    EXIT,KDOC4000,KDOC0000
.
          MOVE      DCODE,DOCTCODE
.
KDOC2000  
.
. check security
.
          PACK      KEY4,PASSCODE
          CALL      RDWTSEA1
          IF        OVRCD=1
            PACK      KEY13,PASSCODE,DOCTCODE
            CALL      RSWTSEI2
            CALL      RKWTSEI2
            BRANCH    OVRCD,KDOC9000
.
            MATCH     PASSCODE,WTSIUSER
            GOTO      KDOC9000 IF NOT EQUAL
            MATCH     DOCTCODE,WTSICONS
            GOTO      KDOC9000 IF NOT EQUAL
          ENDIF
.
          PACK      KEY6,DOCTCODE           * key = cat and value entered
          CALL      FDOC0000                * format docs name
.
          PACK      DOCTDESC,PACFNAME       * store the name
          PACK      PRUNDO2,PREFDOCT,DOCTCODE,DASH,DOCTDESC * set up print head.
          RESET     PRUNDO2
          GOTO      KDOC8000
.
. ------ only enter hit ------
.
KDOC4000  PACK      DOCTCODE,ALL,SP3        * set up default value
          PACK      DOCTDESC,SP20           * clear description
          PACK      PRUNDO2,PREFDOCT,ALL,SP30 * set up 'all' description
          RESET     PRUNDO2
.
KDOC8000  DISPLAY   *P22:15,*V2LON,DOCTCODE,*P40:15,*HOFF,DOCTDESC
          GOTO      KDOC9999
.
KDOC9000  DISPLAY   *P1:24,*EL,*B,"Invalid Security Clearance.  ";
          CALL      HITENTER
          GOTO      KDOC0000
.
KDOC9999  RETURN
+
.*********************************************************************
.*                  RDIS0000                    Called by : KUCC0000 *
.*        Redisplay the screen after ?                      KDOC0000 *
.*********************************************************************
RDIS0000  DISPLAY   *P1:3,*EF:
                    *V2LON,*P1:4,"0",*P1:5,"1",*P1:6,"2",*HOFF:
                    *P3:4,"= Exit":
                    *P3:5,"= Unit/Clinic Code Sequence":
                    *P3:6,"= Doctor Code Sequence":
                    *P1:8,"Select Option : ",*V2LON,MOPTION
.
          MATCH     START,DATEFROM          * is there a date ?
          GOTO      RDIS1000 IF NOT EQUAL   * yes
.
          DISPLAY   *P1:11,"From Date on List : ",*V2LON,DATEFROM,*HOFF
          GOTO      RDIS2000

RDIS1000  UNPACK    DATEFROM,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          DISPLAY   *P1:11,"From Date on List : ",*V2LON,CPCDATE,*HOFF
.
RDIS2000  UNPACK    DATETO,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          DISPLAY   *P1:12,"To   Date on List : ",*V2LON,CPCDATE,*HOFF
.
RDIS9999  RETURN
+
.*********************************************************************
.*                  CLRP0000                    Called by : UNCL0000 *
.*        Generates the report for Unit/Clinic sequence              *
.*********************************************************************
CLRP0000  CALL      CRTC0000                * create temp file
          DISPLAY   *P1:24,"Obtaining the valid entries.",*EL
.
. ------ start looping through 6rd index of treatment file ------
.
CLRP1000  MATCH     START,DATEFROM          * do we start at beginning ?
          GOTO      CLRP1500 IF NOT EQUAL   * no, we have a date
.
          PACK      KEY28,SP8,SP8,SP9,SP2   * start at beginning
          CALL      RDSTREA6                * positional read
          GOTO      CLRP2000                * continue
.
CLRP1500  PACK      KEY28,DATEFROM,SP8,SP9,SP2 * start at entered date
          CALL      RDSTREA6                * positional read
.
.
. -----------------------------------------
. ------ next read on treatment file ------
. -----------------------------------------
.
CLRP2000  CALL      RDKTREA6                * next read
          BRANCH    OVRCD,CLRP5000          * finished report
.
          DISPLAY   *P70:24,WMURNO          * display UR to show working
.
CLRP2500  CALL      VADA0000                * validate the data
          BRANCH    EXIT,CLRP2000:          * invalid data
                         CLRP5000           * at end of date range
.
. ------ have a correct unit/clinic so write a temp file record ------
.
CLRP4000  MOVE      WMCNT,DWMCNT            * set up as a dim
          PACK      KEY30,WMUNIT,WMDATE1,WMURNO,WMCODE,DWMCNT
          WRITE     CLINFILE,KEY30;WMURNO,WMCODE,DWMCNT,WMDESC,WMDOCTOR,WMUNIT:
                                   WMDATE1,WMPTY,WMSTAT,WMUDEF1,WMUDEF2,WMPCAT
.
          DISPLAY   *P70:24,*V2LON,WMURNO   * highlight UR to show it is valid
.
          GOTO      CLRP2000                * read next record
.
. -----------------------------------------------------------
. ------ have finished looping through treatment file, ------
. ------ start doing the report                        ------
. -----------------------------------------------------------
.
CLRP5000  PACK      KEY30,SP30,SP2          * key to position at start
          READ      CLINFILE,KEY30;;        * positional read
          CALL      IBACLOCK                * get current date and time
          MOVE      ZERO,CPAGENO            * initialize page number
          MOVE      ZERO,CNTPATTL           * clear total count
          MOVE      ZERO,CNTPATCL           * clear patient count
          MOVE      ZERO,CNTCAN
          MOVE      ZERO,CNTUN
          MOVE      "Unit/Clinc Code Seq.",CPHDROPT
          DISPLAY   *P1:24,"Producing the report now.",*EL
.
. ------ have to do a read here to get the name of the first unit ------
.
          READKS    CLINFILE;XWMURNO,XWMCODE,DXWMCNT,XWMDESC,XWMDOCTO,XWMUNIT:
                             XWMDATE1,XWMPTY,XWMSTAT,XWMUDEF1,XWMUDEF2,XWMPCAT
          GOTO      CLRP7000 IF OVER
          MOVE      DXWMCNT,XWMCNT 
.
. ------ stores the first unit's name ------
.
          PACK      KEY5,CATWU,XWMUNIT      * unit as the key
          CALL      RDCODE1                 * read codes file
          PACK      PRUNDO1,PREFUNCL,XWMUNIT,DASH,TDESC
          RESET     PRUNDO1
          MOVE      XWMUNIT,PRUNIT          * update previous unit
.
          CALL      NEWP0000                * print new page and table heading
          GOTO      CLRP5500                * continue
.
. ------ read next record of temp file ------
.
CLRP5100  READKS    CLINFILE;XWMURNO,XWMCODE,DXWMCNT,XWMDESC,XWMDOCTO,XWMUNIT:
                             XWMDATE1,XWMPTY,XWMSTAT,XWMUDEF1,XWMUDEF2,XWMPCAT
          GOTO      CLRP9000 IF OVER
          MOVE      DXWMCNT,XWMCNT 
.
. ------ see if it is a different clinic ------
.
          MATCH     PRUNIT,XWMUNIT          * is previous same as current ??
          GOTO      CLRP5500 IF EQUAL       * yes
.
. ---------------------------------------------------------------
. ------ have a new clinic type (only applies when ALL)    ------
. ------ do underlines for last clinic, give patient count ------
. ------ set up new clinic name, do new table heading      ------
. ------ update the previous clinic with the new one       ------
. ---------------------------------------------------------------
.
          PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*"
          UNPACK    PRUNDO1,DIM19,DIM26     * get the two parts
          PRINT     *N,*N,"Total Patients Cancelled for ",DIM26,"   : ",CNTCAN:
                    *N,"Total Patients Unscheduled for ",DIM26," : ",CNTUN:
                    *N,"Total Patients Printed for ",DIM26,"     : ",CNTPATCL
          ADD       THREE,CVERT             * increment line counter
          MOVE      ZERO,CNTPATCL           * clear patient count
          MOVE      ZERO,CNTCAN
          MOVE      ZERO,CNTUN
.
. ------ get the new clinics description ------
.
          PACK      KEY5,CATWU,XWMUNIT      * unit as the key
          CALL      RDCODE1                 * read codes file
          PACK      PRUNDO1,PREFUNCL,XWMUNIT,DASH,TDESC
          RESET     PRUNDO1
          MOVE      XWMUNIT,PRUNIT          * update previous unit
.
. ------ see if there is enough room on the page for new table heading ------
.
          COMPARE   CVERT,NEEDNP            * will table fit on page ??
          GOTO      CLRP5400 IF LESS        * no, print new page heading
.
          CALL      PTBH0000                * print new table
          GOTO      CLRP5500
.
CLRP5400  CALL      NEWP0000                * print new page heading
.
. ------ obtain the current information to display ------
.
CLRP5500  CALL      FINF0000                * format the information
.
. ------ print the line see if it will fit on page ------
.
          COMPARE   CVERT,PRINTLIM          * is new page needed ??
          GOTO      CLRP6000 IF NOT LESS    * no
.
CLRP5600  CALL      NEWP0000                * print new page heading
.
CLRP6000  CALL      PLIN0000                * print the line
          GOTO      CLRP5100                * read next record
.
. ------ there is no data on file ------
.
CLRP7000  CALL      NEWP0000                * print the heading
          PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*":
                    *N,*N,"There are no patients for the entered values."
          GOTO      CLRP9050
.
. ------ report is finished ------
.
CLRP9000  UNPACK    PRUNDO1,DIM19,DIM26     * get the two parts
          PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*":
.
                    *N,*N,"Pat. Canc = Removed from Waiting List":
...                    *N,"S/N Pat   =":
                    *N,"Same Day  = Day Stay":
                    *N,"Prv Pat   = Private Patient (Y/N)":
                    *N,"M/C Pat   = Research Group":
                    *N,*N,"Total Patients Cancelled for ",DIM26,SP3,"     : ":
                    CNTCAN:
                    *N,"Total Patients Unscheduled for ",DIM26,SP3,"   : ":
                    CNTUN:
                    *N,"Total Patients Printed for ",DIM26,SP5,"     : ":
                    CNTPATCL:
                    *N,"Grand Total Patients Printed ",SP20,SP9,"     : ":
                    CNTPATTL
.
CLRP9050  PRINT     *N,*N,"***  END OF REPORT ****"
.
          CALL      DETP0000                * delete the temp file
          MOVE      "CONDENSED UNSCHEDULED PATIENTS         ",PRGNAM 
.
CLRP9999  RETURN
+
.*********************************************************************
.*                  DORP0000                    Called by : DOCT0000 *
.*        Generates the report for Doctor Code sequence              *
.*********************************************************************
DORP0000  CALL      CRTD0000                * create temp file
          DISPLAY   *P1:24,"Obtaining the valid entries now.",*EL
.
. ------ start looping through 3rd index of treatment file ------
.
. DISPLAY *P1:10,DATEFROM
. KEYIN ANS
.
DORP1000  MATCH     START,DATEFROM          * do we start at beginning ?
          GOTO      DORP1500 IF NOT EQUAL   * no, we have a date
.
          PACK      KEY28,SP8,SP8,SP9,SP2   * start at beginning
          CALL      RDSTREA6                * positional read
          GOTO      DORP2000                * continue
.
DORP1500  PACK      KEY28,DATEFROM,SP8,SP9,SP2 * start at entered date
          CALL      RDSTREA6                * positional read
.
.
. -----------------------------------------
. ------ next read on treatment file ------
. -----------------------------------------
.
DORP2000  CALL      RDKTREA6                * next read
          BRANCH    OVRCD,DORP5000          * finished report
.
          DISPLAY   *P70:24,WMURNO          * display UR to show working
.
DORP2500  CALL      VADA0000                * validate the data
          BRANCH    EXIT,DORP2000:          * invalid data
                         DORP5000           * at end of date range
.
. ------ have a correct doctor code so write a temp file record ------
.
DORP4000  MOVE      WMCNT,DWMCNT            * set up as a dim
          PACK      KEY33,WMDOCTOR,WMDATE1,WMURNO,WMCODE,DWMCNT
          WRITE     DOCTFILE,KEY33;WMURNO,WMCODE,DWMCNT,WMDESC,WMDOCTOR,WMUNIT:
                                   WMDATE1,WMPTY,WMSTAT,WMUDEF1,WMUDEF2,WMPCAT
.
          DISPLAY   *P70:24,*V2LON,WMURNO   * highlight UR to show it is valid
.
          GOTO      DORP2000                * read next record
.
. -----------------------------------------------------------
. ------ have finished looping through treatment file, ------
. ------ start doing the report                        ------
. -----------------------------------------------------------
.
DORP5000  PACK      KEY33,SP30,SP2          * key to position at start
          READ      DOCTFILE,KEY33;;        * positional read
          CALL      IBACLOCK                * get current date and time
          MOVE      ZERO,CPAGENO            * initialize page number
          MOVE      ZERO,CNTPATTL           * clear total count
          MOVE      ZERO,CNTPATCL           * clear patient count
          MOVE      ZERO,CNTCAN
          MOVE      ZERO,CNTUN
          MOVE      "Doctor Code Sequence",CPHDROPT
          DISPLAY   *P1:24,"Producing the report now.",*EL
.
. ------ have to do a read here to get the name of the first doct ------
.
          CALL      NEWP0000                * Print New Page
.
          READKS    DOCTFILE;XWMURNO,XWMCODE,DXWMCNT,XWMDESC,XWMDOCTO,XWMUNIT:
                             XWMDATE1,XWMPTY,XWMSTAT,XWMUDEF1,XWMUDEF2,XWMPCAT
          GOTO      DORP7000 IF OVER
          MOVE      DXWMCNT,XWMCNT 
.
. ------ stores the first doctors name ------
.
          PACK      KEY6,XWMDOCTO           * doct as the key
          CALL      FDOC0000                * format docs name
          PACK      PRUNDO1,PREFDOCT,XWMDOCTO,DASH,PACFNAME
          RESET     PRUNDO1
          MOVE      XWMDOCTO,PRDOCT         * update previous doct
.
.          CALL      NEWP0000                * print new page
          GOTO      DORP5500                * continue
.
. ------ read next record of temp file ------
.
DORP5100  READKS    DOCTFILE;XWMURNO,XWMCODE,DXWMCNT,XWMDESC,XWMDOCTO,XWMUNIT:
                             XWMDATE1,XWMPTY,XWMSTAT,XWMUDEF1,XWMUDEF2,XWMPCAT
          GOTO      DORP9000 IF OVER
          MOVE      DXWMCNT,XWMCNT 
.
. ------ see if it is a different doctor code ------
.
          MATCH     PRDOCT,XWMDOCTO         * is previous same as current ??
          GOTO      DORP5500 IF EQUAL       * yes
.
. ---------------------------------------------------------------
. ------ have a new doctor type (only applies when ALL)    ------
. ------ do underlines for last doctor, give patient count ------
. ------ set up new doctor name, do new table heading      ------
. ------ update the previous doctor with the new one       ------
. ---------------------------------------------------------------
.
          PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*"
          UNPACK    PRUNDO1,DIM14,DIM31     * get the two parts
          PRINT     *N,*N,"Total Patients Cancelled for ",DIM31,"   : ",CNTCAN:
                    *N,"Total Patients Unscheduled for ",DIM31," : ",CNTUN:
                    *N,"Total Patients Printed for ",DIM31,"     : ",CNTPATCL
          ADD       THREE,CVERT             * increment line counter
          MOVE      ZERO,CNTPATCL           * clear patient count
          MOVE      ZERO,CNTCAN
          MOVE      ZERO,CNTUN
.
. ------ get the new doctors name ------
.
          PACK      KEY6,XWMDOCTO           * doct as the key
          CALL      FDOC0000                * format docs name
          PACK      PRUNDO1,PREFDOCT,XWMDOCTO,DASH,PACFNAME
          RESET     PRUNDO1
          MOVE      XWMDOCTO,PRDOCT         * update previous doct
.
. ------ see if there is enough room on the page for new table heading ------
.
          COMPARE   CVERT,NEEDNP            * will table fit on page ??
          GOTO      DORP5400 IF LESS        * no, print new page heading
.
          CALL      PTBH0000                * print new table
          GOTO      DORP5500
.
DORP5400  CALL      NEWP0000                * print new page
.
. ------ obtain the current information to display ------
.
DORP5500  CALL      FINF0000                * format the datay
.
. ------ print the line see if it will fit on page ------
.
          COMPARE   CVERT,PRINTLIM          * is new page needed ??
          GOTO      DORP6000 IF NOT LESS    * no
.
DORP5600  CALL      NEWP0000                * print new page heading
.
DORP6000  CALL      PLIN0000                * print the line
          GOTO      DORP5100                * read next record
.
. ------ there are no patients in report ------
.
DORP7000  PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*":
                    *N,*N,"There are no patients for the entered values."
          GOTO      DORP9050
.
. ------ report is finished ------
.
DORP9000  UNPACK    PRUNDO1,DIM14,DIM31     * get the two parts
          PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*":
                    *N,*N,"Pat. Canc = Removed from Waiting List":
...                    *N,"S/N Pat   =":
                    *N,"Same Day  = Day Stay":
                    *N,"Prv Pat   = Private Patient (Y/N)":
                    *N,"M/C Pat   = Research Group":
                    *N,*N,"Total Patients Cancelled for ",DIM31,"   : ",CNTCAN:
                    *N,"Total Patients Unscheduled for ",DIM31," : ",CNTUN:
                    *N,"Total Patients Printed for ",DIM31,"     : ",CNTPATCL:
                    *N,"Grand Total Patients Printed ",SP20,SP9,"     : ":
                    CNTPATTL
.
DORP9050  PRINT     *N,*N,*N,"***  END OF REPORT ****"
.
          CALL      DETP0000                * delete temp file
          MOVE      "CONDENSED UNSCHEDULED PATIENTS         ",PRGNAM
.
DORP9999  RETURN
+
.*********************************************************************
.*                  VADA0000                    Called by : CLRP0000 *
.*        Validate the record just read in                  DORP0000 *
.*        Returns : EXIT = 1      invalid item                       *
.*                  EXIT = 2      at end of date range               *
.*********************************************************************
VADA0000  PACK      KEY8,WMURNO             * UR number as key
          CALL      RDMAST1                 * read master file
          BRANCH    OVRCD,VADA9000          * invalid UR number
.
. ------ check that still in the date range ------
.
          MATCH     WTWMDTAD,DATETO          * is dateto < file date ??
          GOTO      VADA9500 IF LESS        * yes, finishced report
.
.
.******** check if planned patient as at date ********
.
          UNPACK    DATETO,XCENT,XYEAR,XMON,XDAY
          MOVE      XCENT,CC
          MOVE      XYEAR,YY
          MOVE      XMON,MM
          MOVE      XDAY,DD
          CALL      DMYCON
          ADD       ONE,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON
          PACK      NEXTDAY,CC,YY,MM,DD
          REP       " 0",NEXTDAY
.
          PACK      KEY29,WMURNO,WMCODE,WMCNT,ANSC,ANSC,NEXTDAY,SP10
          CALL      RSWTCAT2
          CALL      RPWTCAT2
          BRANCH    OVRCD,VADA2000
.
          MATCH     WMURNO,WTCAURNO
          GOTO      VADA2000 IF NOT EQUAL
.
          MATCH     WMCODE,WTCAPROC
          GOTO      VADA2000 IF NOT EQUAL
.
          COMPARE   WMCNT,WTCAPCNT
          GOTO      VADA2000 IF NOT EQUAL
.
          MATCH     CODECC,WTCACATF
          GOTO      VADA2000 IF NOT EQUAL
.
          PACK      KEY5,ANSC,ANSC,WTCACODT,SP5
          CALL      RDCODE1
          BRANCH    OVRCD,VADA2000
          MATCH     ANSP,TCINDC3
          GOTO      VADA9000 IF EQUAL          * exclude planned patients
.
.
VADA2000
. ******* see if suspended at report date *******
.
          MOVE      DATETO,WTSPDTCK       * set date to check for
          CALL      WATONSUS                * see if suspended
          COMPARE   ZERO,EXIT
          GOTO      VADA9000 IF EQUAL       * suspended at given date
.
. ------ check that have the correct status of 1 or 6 ------
.
.                          unched    sched   pre-adm  admitted
          BRANCH    WMSTAT,VADA3000,VADA9000,VADA9000,VADA9000:
                           VADA9000,VADA3000,VADA3000
.                          dischged cancelld perform  not perf
.
. ------ check on unit or doctor ------
.
VADA3000  BRANCH    MOPTION,VADA4000,VADA5000
          GOTO      VADA9000
.
. ------ check that have the correct unit/clinic ------
.
VADA4000  MATCH     ALL,UNCLCODE            * are we using all codes ??
          GOTO      VADA8000 IF EQUAL       * yes, use this value
.
          MATCH     UNCLCODE,WMUNIT         * is it the correct unit ???
          GOTO      VADA9000 IF NOT EQUAL   * no, read next record
.
          GOTO      VADA8000                * valid item
.
. ------ check that have the correct doctor code ------
.
VADA5000  MATCH     ALL,DOCTCODE            * are we using all docts ??
          GOTO      VADA8000 IF EQUAL       * yes, use this value
.
          MATCH     DOCTCODE,WMDOCTOR       * is it the correct doct ???
          GOTO      VADA9000 IF NOT EQUAL   * no, read next record
.
VADA8000  MOVE      FALSE,EXIT              * valid item
          GOTO      VADA9999
.
VADA9000  MOVE      TRUE,EXIT               * invalid item
          GOTO      VADA9999
.
VADA9500  MOVE      TWO,EXIT                * at end of date range
.
VADA9999  RETURN
+
.*********************************************************************
.*                  FINF0000                    Called by : CLRP0000 *
.*        Format the information to be displayed            DORP0000 *
.*********************************************************************
FINF0000  PACK      KEY8,XWMURNO            * UR number as key
          CALL      RDMAST1                 * read master file
          MOVE      PTITL,PACTITLE          * title
          MOVE      PGNAME,PACGNAME         * given name
          MOVE      PSNAME,PACSNAME         * surname
          MOVE      TWO,PACFRMT             * format as Surname, Title Given
          CALL      PACNAME                 * format the name
          PACK      PRNAME,PACFNAME         * store the name
          RESET     PRNAME
.
. ------ format the priority description ------
.
          PACK      KEY5,CATTP,XWMPTY       * key of priority
          CALL      RDCODE1                 * read codes file
          PACK      PRPRTY,TDESC            * get the description
          RESET     PRPRTY
.
. ------ format the date on list ------
.
          UNPACK    XWMDATE1,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
. ------ format the description ------
.
          MOVE      XWMDESC,PRDESC          * shorten the display length
.
FINF9999  RETURN
+
.*********************************************************************
.*                  FDOC0000                    Called by : DORP0000 *
.*        Format the doctors name                                    *
.*        Paras   : KEY6          the doctors code                   *
.*        Returns : PACFNAME      the name                           *
.*                  EXIT = 1      invalid code                       *
.*********************************************************************
FDOC0000  PACK      PACFNAME,SP30,SP30,SP5  * clear the description 
          CALL      RDDOCT1                 * read doctors file
          BRANCH    OVRCD,FDOC9000          * invalid doctors code
.
          MOVE      DTITL,PACTITLE          * title
          MOVE      DGNAME,PACGNAME         * given name
          MOVE      DSNAME,PACSNAME         * surname
          MOVE      TWO,PACFRMT             * format as Surname, Title Given
          CALL      PACNAME                 * format the name
.
          MOVE      FALSE,EXIT              * set exit status
          GOTO      FDOC9999
.
FDOC9000  MOVE      TRUE,EXIT               * invalid code
.
FDOC9999  RETURN
+
.*********************************************************************
.*                  NEWP0000                    Called by : CLRP0000 *
.*        Prints the new page heading for a report          DORP0000 *
.*        Para's  : CPHRDOPT      sub heading                        *
.*********************************************************************
NEWP0000  MOVE      ZERO,CNOUNDLN           * underlines at end of page
          MOVE      "CONDENSED UNSCHEDULED PATIENTS REPORT -",PRGNAM
          RESET     PRGNAM
.
NEWP0500  CALL      HEAD132                 * do the new heading
.
.          PRINT     *1,PRFROM,*45,PRUNDO2   * from date, search option
          PRINT     *1,PRFROM
          PRINT     *1,PRTO                 * to date
.
. ------ print the table heading ------
.
          MOVE      SEVEN,CVERT             * set line counter
          CALL      PTBH0000                * print a table heading
.
NEWP9999  RETURN
+
.*********************************************************************
.*                  PLIN0000                    Called by : CLRP0000 *
.*        Prints a single data line of the report           DORP0000 *
.*        ie. the information about a patients procedure             *
.*        Paras/Returns : CNTPATCL    count of pats                  *
.*                        CNTPATTL    count of total patients        *
.*                        CVERT       current row number             *
.*********************************************************************
PLIN0000  PRINT     *1,"|",XWMURNO,*10,"|",*12,PRNAME,*33,"|",*35,PTELEP:
                    *47,"|",*49,CPCDATE,*60,"|",*62,PRDESC,*99,"|",*101,PRPRTY:
                    *110,"|",*114,"|";
.
. ------ obtain the same day indicator ------
.
          MOVE      ANSY,ANS                * indicate is a same day
          PACK      KEY5,CATW1,XWMUDEF1     * UD field 1 as key
          CALL      RDCODE1                 * read codes file
.
          MATCH     ANSD,TCINDC1            * is first ind a D ??
          GOTO      PLIN0500 IF EQUAL       * yes, so put a Y
.
          MOVE      SP1,ANS                 * indicate not same day
.
PLIN0500  PRINT     *116,ANS,*119,"|";
.
. ------ obtain the public/private patient indicator ------
.
          PACK      KEY5,CATA,XWMPCAT       * key of pat cat
          CALL      RDCODE1                 * read codes file
          MOVE      ANSN,ANS                * set up as no
.
          MATCH     "1",TCINDC1             * is indicator = 1 ?
          GOTO      PLIN1000 IF EQUAL       * yes, so keep as no
.
          MOVE      ANSY,ANS                * set up as Y
.
PLIN1000  PRINT     *121,ANS,*123,"|",*124,XWMUDEF2,*127,"|";
.
          MOVE      SP1,ANS                 * clear ans
          LOAD      ANS,XWMSTAT,SP1,SP1,SP1,SP1,SP1,ANSY * cancelled ind.
.
          PRINT     *129,ANS,*132,"|"
.
          DISPLAY   *P70:24,XWMURNO         * print UR to show it is working
.
. ------ increment counters ------
.
          IF        XWMSTAT=6
            ADD       ONE,CNTCAN
          ENDIF
          IF        XWMSTAT=1
            ADD       ONE,CNTUN
          ENDIF
.
          ADD       ONE,CVERT               * add to line counter
          ADD       ONE,CNTPATCL            * add to patient count for clinic
          ADD       ONE,CNTPATTL            * add to total pat. count
.
PLIN9999  RETURN
+
.*********************************************************************
.*                  PTBH0000                    Called by : NEWP0000 *
.*        Prints a new table heading                        CLRP0000 *
.*        Needed at a new page or for a new unit/clinic     DORP0000 *
.*        Paras/Returns : CVERT      current row number              *
.*********************************************************************
PTBH0000  PRINT     *N,PRUNDO1:
                    *N,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*":
                    *N,*1,"|",*3,"U/R",*10,"|",*33,"|",*35,"Private",*47,"|":
                      *52,"Date",*60,"|",*99,"|",*110,"|S/N|Same|Prv|M/C|Pat.|":
                    *N,*1,"|",*3,"Number",*10,"|",*12,"Patients Name",*33,"|":
                       *35,"Telephone No",*47,"|",*51,"On List",*60,"| ":
                       *62,WTCNTPDS," Description",*99,"|",*101,"Priority":
                       *110,"|Pat|Day |Pat|Pat|Canc|":
                    *N,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*"
.
          ADD       SIX,CVERT               * add to line count
.
PTBH9999  RETURN
+
.*********************************************************************
.*                  CRTC0000                    Called by : CLRP0000 *
.*        Create temporary file for Unit/Clinic sequence             *
.*********************************************************************
CRTC0000  CALL      DETP0000                  * delete the temp file
          DISPLAY   *V2LON,*BLINKON,*P1:24,"Please wait.",*EL
          CLEAR     CMDLINE               
          APPEND    "isbuild ",CMDLINE   
          APPEND    TFILNAME,CMDLINE    
          APPEND    " 101 U76-78,79-86,1-8,9-17,18-19",CMDLINE  
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID            * create temp file
          OPEN      CLINFILE,TFILNAME         * open temp. file
.
CRTC9999  RETURN
+
.*********************************************************************
.*                  CRTD0000                    Called by : DORP0000 *
.*        Create temporary file for Doctor sequence                  *
.*********************************************************************
CRTD0000  CALL      DETP0000                  * delete the temp file
          DISPLAY   *V2LON,*BLINKON,*P1:24,"Please wait.",*EL
          CLEAR     CMDLINE                
          APPEND    "isbuild ",CMDLINE      
          APPEND    TFILNAME,CMDLINE        
          APPEND    " 101 U70-75,79-86,1-8,9-17,18-19",CMDLINE  
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID            * create temp file
          OPEN      DOCTFILE,TFILNAME         * open temp. file
.
CRTD9999  RETURN
+
.*********************************************************************
.*                  DETP0000                    Called by : CLRP0000 *
.*        Delete temporary file                             DORP0000 *
.*********************************************************************
DETP0000  CLEAR     CMDLINE                   
          APPEND    "iserase ",CMDLINE        
          APPEND    TFILNAME,CMDLINE          
          RESET     CMDLINE
          CLOSE     CLINFILE                  * close temp files so iserase
          CLOSE     DOCTFILE                  * command works properly
          EXECUTE   CMDLINE,TASKID            * execute the delete command
.
          DISPLAY   *P1:24,"Temporary file deleted.",*EL
.
DETP9999  RETURN
+
.
. ------ I/O routines needed ------
.
          INC       STD001IO                * standard IO routines
          INC       PATCODIO/INC            * codes file
          INC       PATDO1IO/INC            * doctors file
          INC       PATMA1IO/INC            * master file
          INC       WATTR1IO/INC            * WL treatment file
          INC       WATSEAIO/INC
          INC       WATSEIIO/INC
          INC       WATSUSIO/INC
          INC       WATCATIO/INC
          INC       WATONSUS
          INC       PATCODKY
          INC       PATDOCKY
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC

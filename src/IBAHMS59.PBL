. *              P R O G R A M  S U M M A R Y                                 *
. *              -------------  -------------                                 *
. *                                                                           *
. *     PROGRAM NAME:     IBAHMS59                                            *
. *                                                                           *
. *     FUNCTION:         Discharged Event NWAU Calculations                  *
. *                                                                           *
. *     DATE WRITTEN:     23/05/2022                                          *
. *                                                                           *
. *     MODIFICATIONS:                                                        *
. *                                                                           *
. *---------------------------------------------------------------------------*
. *        V12.00.02 14/07/2025  Nikitha B      TSK 0959690                   *
. *                  Recompiled for ABFRFINV-Added NWAU Calculations for VINAH*
. *        V12.00.01 09/07/2025 J.Tan             TSK 0961623                 *
. *                  Added PENDFUND for ABFPTINV                              *
. *        V11.04.07 25/10/2024 J.Tan             TSK 0951578                 *
. *                  Recompiled for ABFPTINV-Added Min.to roundup ICU Hrs(ABF)*
. *        V11.04.06 23/09/2024  J.Tan          TSK 0949848                   *
. *                  Recompiled for ABFPTINV - DRG List used in for COVID Adj.*
. *        V11.04.05 01/08/2024  J.Tan          TSK 0947804                   *
. *                  Recompiled for ABFMHINV -Changes for Disch.after 1/7/2023*
. *        V11.04.04 17/07/2024  J.Tan          TSK 0947315                   *
. *                  Recompiled for ABFPTINV (LOS ICU adjusted & Newborn)     *
. *        V11.04.03 02/04/2024  J.Tan          TSK 0931613                   *
. *                  Recompiled for ABFPTINV (Contract Effective date)        *
. *        V11.04.02 27/03/2024  J.Tan          TSK 0931613                   *
. *                  Recompiled for ABFSAINV - set up PRDSDATE for Rehab.     *
. *        V11.04.01 19/02/2024  J.Tan          TSK 0939019                   *
. *                  Recompiled for ABFPTINV - checking for ICD10             *
. *        V11.03.19 17/11/2023  J.Tan          TSK 0940089                   *
. *                  Recompiled for ABFPTINV - checking for blank Disc.DRG    *
. *        V11.03.18 30/10/2023  J.Tan          TASK 0938079                  *
. *                  Mod Emergency to print Discharge Date on header report   *
. *        V11.03.17 04/10/2023  J.Tan          TASK 0938230                  *
. *                  Recompiled for ABFAECCL - Emergency ABF                  *
. *        V11.03.16 28/09/2023  J.Tan          TSK 0931613                   *
. *                  Mod for ABF Sub-Acute Palliative phase of Care           *
. *        V11.03.15 20/09/2023  J.Tan          TSK 0916870                   *
. *                  Recompiled for ABFPTINV-Default NEP value with blank code*
. *        V11.03.14 28/08/2023  J.Tan          TSK 0916870                   *
. *                  Recompiled for ABFPTINV - Claim code Cat CL Multiple NEP * 
. *        V11.03.13 13/07/2023  J.Tan          TSK 0923703                   *
. *                  Fixed checking for Daycase Private patient               *
. *        V11.03.12 07/07/2023  J.Tan         TSK 0923703                    *
. *                  Fixed processing Inpatient Mental Health                 *
. *        V11.03.11 25/05/2023  J.Tan         TSK 0923703                    *
. *                  Added Mental Health Event                                *
. *        V11.03.10 17/05/2023  J.Tan         TSK 0930981                    *
. *                  Mod to Admitted Acute ABF calculation for 2023/2024      *
. *        V11.03.09 01/05/2023  J.Tan         TSK 0930981                    *
. *                  Mod to store Referral Encounter to a temporary file      *
. *        V11.03.08 21/04/2023  J.Tan         TSK 0930981                    *
. *                  Recompiled for ABFRFINV - increased array variables      *
. *        V11.03.07 24/04/2023  J.Tan         TSK 0930639                    *
. *                  Mod Inpatient to exclude Sub-Acute Event                 *
. *        V11.03.06 21/04/2023  J.Tan         TSK 0930981                    *
. *                  Recompiled for ABFRFINV - fixed monthly encounter        *
. *        V11.03.05 19/04/2023  J.Tan         TSK 0930981                    *
. *                  Recompiled for ABFVARS - increased variable REFENCNT     *
. *        V11.03.04 11/01/2023  J.Tan         TSK 0906597                    *
. *                  Recompiled for ABFRFINV - added Remoteness adjustmnet    *
. *        V11.03.03 24/11/2022 J.Tan    TSK 0906597                          *
. *                  Recompiled for ABFRFINV - Charge monthly for Monthly Ref.*
. *        V11.03.02 18/11/2022 J.Tan           TSK 0906597                   *
. *                  Fixed I*C patabfaf file                                  *
. *        V11.03.01 15/11/2022 J.Tan           TSK 0906597                   *
. *                  Recompiled ABFRFINV - Effective date for Monthly referral*
. *        V11.02.05 11/11/2022  J.Tan          TSK 0906597                   *
. *                  Recompiled for ABFOTINV - Multidisciplinary clinic       *
. *                  Cat CQ TCINDC2=M                                         *
. *        V11.02.04 10/08/2022 J.Tan    TSK 0906597                          *
. *                  Added Outpatient, Referral and Sub-Acute options         *
. *        V11.02.03 04/08/2022 J.Tan    TSK 0906596                          *
. *                  Mods to removed checking NOFEE after ABFAEINV            *
. *        V11.02.02 18/07/2022 J.Tan    TSK 0906596                          *
. *                  Mods for Claim code Indic2=N (NWAUFLAG)                  *
. *        V11.02.01 29/06/2022 J.Tan           TSK 0920712                   *
. *                  Recompiled for ABFAECCL - I*D on emrvcdaf file           *
. *        V11.02.00 23/05/2022  J.Tan          TSK 0906596                   *
. *                  Created report                                           *
. *                                                                           *
. ******************************************************************************
.
          INC       STD001FD
          INC       PATCOMM/INC
          INC       PABXVARS/INC
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
          INC       WEBSECFD/INC
          INC       PATCONFD/INC
          INC       ABFVARS/INC
          INC       PATDHEAD/INC
.
          INC       IBAPOSFD/INC
          INC       ALLENCFD/INC
          INC       ALLREFFD/INC
          INC       ALLDEPFD/INC
          INC       ALLLNKFD/INC
.
          INC       OUTMA1FD/INC
          INC       OUTSITFD/INC
          INC       OUTSESFD/INC
          INC       OUTHEDFD/INC
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
          INC       OUTCANFD/INC
          INC       OUTCTYFD/INC
          INC       OUTDTRFD/INC
.
          INC       PATCHCFD/INC
          INC       PATRDEFD/INC
          INC       PATCODFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATDSCFD/INC
          INC       PATVISFD/INC
          INC       PMSSNPFD/INC
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
          INC       PATHSPFD/INC
          INC       PATDTRFD/INC
          INC       PATTRNFD/INC
          INC       PATINVFD/INC
          INC       PATIPEFD/INC
          INC       PATINPFD/INC
          INC       PATDO1FD/INC
.
          INC       PMSABFFD/INC
          INC       PATABFFD/INC
          INC       PATICUFD/INC
          INC       PATECDFD/INC
.
          INC       PATFN1FD/INC
          INC       PATFX1FD/INC
          INC       PATDGWFD/INC
          INC       PATPGRFD/INC
          INC       PATWR1FD/INC
          INC       PATFINFD/INC
          INC       PATCFAFD/INC
          INC       PMSMTIFD/INC
          INC       PMSEDGFD/INC
          INC       PMSCIDFD/INC
.
          INC       EMRVISFD/INC
          INC       EMRVCDFD/INC
          INC       AAEDE1FD/INC
          INC       AAEDTRFD/INC
.
.               WORK AREA
.
TMPENCA1   IFILE    SQL, FIXED=10, KEY="U1-8"
.
.NAME     TYPE    LENGTH     PHYSICAL     START     DESCRIPTION
.----     ----    ------     --------     -----     -----------
TMPVISNO  DIM       8           8           1       Visit Number
TMPSPARE  DIM       1           1           9       Spare
.End of Record                              10
.
ALCNEAUD  DIM       1
ALCNRAUD  DIM       1
ALCNCIEP  DIM       1       * Populate Clinic Identifier
ALCNDEVP  DIM       1       * Display Event Program on VINAH screens
ALCNLAUD  DIM       1       * Using Referral Link File Audit 0=No 1=Yes
.
ADMISSNO  DIM       8
ACDAYCS   FORM      1
BJDAY     FORM      5
BOUTFLAG  FORM      "0"
.
CJDAY     FORM      5
CURRDATE  DIM       8
CONTRCID  DIM       6
CEFFDATE  DIM       8
CNTRCEFR  FORM      1
CALDAYS   FORM      3
CDYSDAYS  FORM      6                       * CALCDAYS number of days
CDYSFDTE  DIM       8                       * CALCDAYS from date
CDYSTDTE  DIM       8                       * CALCDAYS to date
CDYSYEAR  FORM      2                       * CALCDAYS year
CNEXTINV  DIM       8
CMDLINE   DIM       80
CNTRNUM   FORM      8
.
DDRGCODE  DIM       4
DRGVER    DIM       3
DMDCCODE  DIM       4
DTDATE    DIM       8
DFDATE    DIM       8
DIM3      DIM       3
DIM20     DIM       20
.
SAVCONTR  DIM       6
.
SKEY27    DIM       27
STATLOOP  FORM      1
SESSKEYS  DIM       25
IBCNMHOS  FORM      1
FRMDATE   DIM       8
ENDDATE   DIM       8
FNAMEN    DIM       8
FNAMEP    DIM       8
FNAMET    DIM       8
ENDRMNTH  FORM      6
FORM6     FORM      6
FORM3     FORM      3
FORM3A    FORM      3
FORM8     FORM      8
FORM11P2  FORM      11.2
FORM12D2  FORM      12.2
FORM8D4   FORM      8.4
FCENT1    DIM       2
FYEAR1    DIM       2
FMON1     DIM       2
FDAY1     DIM       2
TCENT1    DIM       2
TYEAR1    DIM       2
TMON1     DIM       2
TDAY1     DIM       2

ICUFLAG   FORM      1
PENDFUND  DIM       6
PPAYABLE  FORM      12.2
NODAYS    FORM      6
NWAUFLAG  FORM      1
NOABFREF  FORM      1

GROSSTOT  FORM      12.2
LSDESC1   DIM       30
LSDESC2   DIM       35
LINEGST   FORM      12.2
IDATEF    DIM       8
IDATET    DIM       8
FROMDATE  DIM       8
TODATE    DIM       8
.
NBRDAYS   FORM      6
NETTOT    FORM      12.2
NOFEE     FORM      1
REBTOT    FORM      12.2
TOTXTRA   FORM      6.2
SAMTG     FORM      6.2
LINETOT   FORM      12.2
LSTRATE   FORM      6.2

TOTDAYS   FORM      6
TOTAMNT   FORM      12.2
.
HOSPID    DIM       3
HOSPNAME  DIM       50
WDATE1    DIM       8
WORKDATE  DATE      8
WEBFLAG   FORM      "0"
USERID    DIM       10
.
.         Web Variables (only the include files use these variables)
.
DAYFORMT  DIM       4
NJAN      INIT      "January"
NFEB      INIT      "February"
NMAR      INIT      "March"
NAPR      INIT      "April"
NMAY      INIT      "May"
NJUN      INIT      "June"
NJUL      INIT      "July"
NAUG      INIT      "August"
NSEP      INIT      "September"
NOCT      INIT      "October"
NNOV      INIT      "November"
NDEC      INIT      "December"
.
HTMLFILE  FIFO      ASCII, FIXED=250
MTHNAM3   DIM       3
MTHNAM3A  DIM       3
CFPAYCOD  DIM       3        * 3M Codefinder PAY Code
.
XLINETOT  FORM      6.2
XLINEGST  FORM      6.2
.
PROGFLAG  FORM      1
RECFLAG   FORM      1
INVDFLAG  FORM      1
FGSTFLAG  FORM      1
.
PRGID     INIT      "IBAHMS59"
PRGNAM    INIT      "Discharged Event NWAU Calculations"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                              MAIN0000                                      *
.*                  Controlling Logic (Mainline code)                         *
.******************************************************************************
.
MAIN0000  CALL      INIT0000            * Initialisation and open files
          BRANCH    OVRCD,MAIN9999
.
MAIN1000  CALL      OPTN0000            * Select options
          BRANCH    EXIT,MAIN9999       * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN2000:   * Proceed to process
                            MAIN2000:   * Proceed to process
                            MAIN2000:   * Proceed to process
                            MAIN2000:   * Proceed to process
                            MAIN2000:   * Proceed to process
                            MAIN2000    * Proceed to process
.
          GOTO      MAIN1000            * invalid selection; get again
.
MAIN2000  CALL      KDAT0000
          CALL      GUSR0000            * get web user id
          CALL      KHOS0000
.
          CALL      CONTQST             * Ok to continue ?
          BRANCH    CEXIT,MAIN5000:     * Yes
                          MAIN2000:     * No; prompt options again
                          MAIN1000      * Cancel
.
MAIN5000  CALL      PROC0000            * Processing
          GOTO      MAIN1000
.
MAIN9999  CHAIN     PGM                 * Chain back to program
+
.******************************************************************************
.*                                INIT0000             Called by: MAIN0000    *
.*                  Display headings and open files                           *
.******************************************************************************
.
INIT0000  CALL      DISPHEAD                      * display heading
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,HUND06;*225,ALCNDEVP:
                                    *247,ALCNCIEP * Clinic Identifier population
          READ      CONTROLF,HUND28;*105,PTCNUABF

.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"emrvcdaf";
          OPEN      EMRVCDA2,"emrvcdaf"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA1,"patvisaf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"aaede1af";
          OPEN      AAEDE1A1,"aaede1af"
.
          OPEN      PATTRAN2,"pattranf"
          OPEN      PATWR1A1,"patwr1af"
.
          OPEN      PMSABFA1,"pmsabfaf"
          OPEN      PMSABFA3,"pmsabfaf"
.
          OPEN      OUTSITA1,"outsitaf"
.
          OPEN      ALLREFA1,"allrefaf"
          OPEN      ALLREFA3,"allrefaf"
          OPEN      ALLENCA1,"allencaf"
          OPEN      ALLENCA4,"allencaf"
          OPEN      ALLENCA6,"allencaf"
          OPEN      ALLDEPA1,"alldepaf"
          OPEN      ALLLNKA1,"alllnkaf"
          OPEN      WEBSECA1,"websecaf"
.
          OPEN      OUTCTYA1,"outctyaf"
          OPEN      PATRDEA1,"patrdeaf"
          OPEN      PATCHCO2,"patchcof"
.
          MOVE      ZERO,NOABFREF
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATABFA2,"patabfaf"
          TRAPCLR   IO
          MOVE      OVRCD,NOABFREF
          OPEN      PATABFA1,"patabfaf"
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMET
.
...  used in ABFAECCL
          CALL      TFILENAM                * Get file name calculating Exponent
          MOVE      TFILNAME,FNAMEN
.
INIT9999  RETURN
.
.******************************************************************************
.*                                OPTN0000             Called by: MAIN0000    *
.*                  Get user options or exit                                  *
.*                                                                            *
.*    Returns:  EXIT    = FALSE (0)  Valid option selected                    *
.*                        TRUE  (1)  Exit option selected                     *
.*              COPTION = 0 Exit                                              *
.*                        1 Run Report                                        *
.******************************************************************************
.
OPTN0000  MATCH     "1",PTCNUABF
          GOTO      OPTN9500 IF NOT EQUAL   * Not using ABF
.
          DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Inpatient":
                    *P1:6,*V2LON,TWO,*HOFF," = Emergency":
                    *P1:7,*V2LON,THREE,*HOFF," = Outpatient":
                    *P1:8,*V2LON,FOUR,*HOFF," = Sub-Acute":
                    *P1:9,*V2LON,FIVE,*HOFF," = Referral/Contact":
                    *P1:10,*V2LON,SIX,*HOFF," = Inpatient Mental Health"
.
OPTN1000  KEYIN     *P1:12,*EL,"Select option : ",*DV,UNDLN1:
                    *P17:12,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION
          GOTO      OPTN9500 IF EQUAL             * exit
.
          BRANCH    COPTION,OPTN9000,OPTN9000,OPTN9000,OPTN9000,OPTN9000:
                            OPTN9000
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.******************************************************************************
.         Processing
.******************************************************************************
PROC0000  DISPLAY   *P1:24,*EL,*P20:24,"Admission No :":
                    *P50:24,"Scanning :";
.
          CALL      HEAD0000
          MOVE      ONE,NWAUFLAG
.
          MOVE      ZERO,MREFFLAG
          BRANCH    COPTION,PROC1000,PROC2000,PROC3000,PROC4000,PROC5000:
                            PROC6000
          GOTO      PROC9999
.
PROC1000  CALL      INPP0000          * Process Inpatient
          GOTO      PROC9999
.
PROC2000  CALL      EMRP0000          * Process Inpatient
          GOTO      PROC9999
.
PROC3000  CALL      OUTP0000          * Process Outpatient
          GOTO      PROC9999
.
PROC4000  CALL      SACT0000          * Process Sub-Acute Inpatient
          GOTO      PROC9999
.
PROC5000  CALL      RENC0000          * Referral Encounter
          CALL      REFC0000          * Referral for Calendar Month/Contacts
          GOTO      PROC9999
.
PROC6000  CALL      IPMH0000          * Process Inpatient Mental Health
          GOTO      PROC9999
.
PROC9999  RETURN
+
.******************************************************************************
.         Discharges
.******************************************************************************
INPP0000  PACK      KEY16 WITH FRMDATE,SP8
          CALL      RDSDSCH2
INPP1000  CALL      RDKDSCH2
          BRANCH    OVRCD OF INPP9000
.
          MATCH     DDATE,ENDDATE           * check the end date
          GOTO      INPP9000 IF LESS
.
          DISPLAY   *P62:24,DADMNO;
.
          MOVE      DADMNO,KEY8
          CALL      RDVISA1
          BRANCH    OVRCD,INPP1000
.
          CALL      RDMISS1
          BRANCH    OVRCD,INPP1000
.
          COMPARE   FIVE,ASTAT
          GOTO      INPP1000 IF EQUAL       * Cancelled
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF INPP1000
.
          PACK      KEY8,DADMNO,SP8
          CALL      RDPMVX11
          BRANCH    OVRCD,INPP1000
.
          IF        IBCNMHOS =1
            MATCH     SP10,HOSPID
            IF        !@EQUAL
              MATCH     PMVXMHOS,HOSPID
              GOTO      INPP1000 IF NOT EQUAL
            ENDIF
          ENDIF
.
          PACK      KEY5,ANSC,ANSL,ACLAIM
          CALL      RDCODE1
          BRANCH    OVRCD,INPP1000
.
          MATCH     "N",TCINDC2               * NWAU calculated ?
          GOTO      INPP3000 IF NOT EQUAL
.
          MATCH     SP70,ACARE
          GOTO      INPP2000 IF EQUAL
.
          PACK      KEY5,ANSC,ANSC,ACARE
          CALL      RDCODE1
          BRANCH    OVRCD,INPP2000
.
          MATCH     "X",TCINDC17
          GOTO      INPP3000 IF EQUAL         * Exclude NWAU calc
.
          MATCH     SP70,PTCDUDF6
          GOTO      INPP2000 IF EQUAL
.
          MATCH     "11",PTCDUDF6
          GOTO      INPP3000 IF EQUAL         * Exclude MH
.
.         Exclude Sub-Acute - Care class
.          UDF 6 2=Rehabilitation, 3=Palliative, 4=GEM,
.                5=Psychogeriatric, 6=Maintenance, 11=Mental Health Care
.
          MOVE      ZERO,FORM2
          SQUEEZE   PTCDUDF6
          MOVE      PTCDUDF6,FORM2
.
          COMPARE   "11",FORM2
          GOTO      INPP3000 IF EQUAL       * Exclude Mental Health Care
.
          BRANCH    FORM2,INPP2000,INPP3000,INPP3000,INPP3000,INPP3000:
                          INPP3000
.
INPP2000  MOVE      ZERO,NWAUAMNT         * NWAU amount in 6 decimal places
          PROC      ABFPTINV              * Calculate ABF Invoice
          BRANCH    NOFEE,INPP3000        * ABF PW not setup
.
          MOVE      AADMNO,ADMISSNO
          CALL      PDAT0000              * Print data
.
          GOTO      INPP1000
.
INPP3000  MOVE      AADMNO,KEY8
          CALL      DABF0000              * Deleting existing ABF details
          GOTO      INPP1000
.
INPP9000  CALL      UND132
INPP9999  RETURN
+
.******************************************************************************
.         Processing Emergency
.******************************************************************************
EMRP0000  OPEN      EMRVISA4,"emrvisaf" 
.
.         Loop through patients arrived between 7 days before the Keyin 
.         Discharge Fromdate and the Keyin Todate
.         to cover patients discharged within the Keyin Discharge Date Range
.
          MOVE      FRMDATE,WORKDATE
          SUB       SEVEN,WORKDATE
          MOVE      WORKDATE,WDATE1
.
          PACK      KEY24,WDATE1,SP70
          CALL      RSEMVIS4
EMRP1000  CALL      RKEMVIS4
          BRANCH    OVRCD,EMRP9000
.
          MATCH     EMVIDATE,ENDDATE              * check Arrival date
          GOTO      EMRP9000 IF LESS
.
          BRANCH    EMVISTAT,EMRP1000,EMRP2000,EMRP2000
          GOTO      EMRP1000
.
EMRP2000  DISPLAY   *P61:24,*EL,*V2LON,EMVIADMN;  * Display number on screen
.
.         Check if Discharged within the selected date range
.
          MATCH     FRMDATE,EMVIDDAT
          GOTO      EMRP1000 IF LESS          * discharged less than fromdate
          MATCH     EMVIDDAT,ENDDATE
          GOTO      EMRP1000 IF LESS          * discharged greater than todate
.
          MOVE      EMVIADMN,KEY8
          CALL      RDVISA1
          BRANCH    OVRCD OF EMRP1000
.
          CALL      RDDETA1
          BRANCH    OVRCD OF EMRP1000
.
          MOVE      ADAURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF EMRP1000
.
          PACK      KEY8,ADANUMB,SP8
          CALL      RDPMVX11
          BRANCH    OVRCD,EMRP1000
.
          IF        IBCNMHOS =1
            MATCH     SP10,HOSPID
            IF        !@EQUAL
              MATCH     PMVXMHOS,HOSPID
              GOTO      EMRP1000 IF NOT EQUAL
            ENDIF
          ENDIF
.
          MATCH     SP70,ADACOMP
          GOTO      EMRP3000 IF EQUAL
.
          PACK      KEY5,ANSC,ANSL,ADACOMP
          CALL      RDCODE1
          BRANCH    OVRCD,EMRP3000
.
          MATCH     "N",TCINDC2               * NWAU calculated ?
          GOTO      EMRP3000 IF NOT EQUAL
.
          MATCH     SP70,EMVICCLS
          IF        !@EQUAL
            PACK      KEY5,ANSC,ANSC,EMVICCLS
            CALL      RDCODE1
            IF        OVRCD=0
              MATCH     "X",TCINDC17
              GOTO      EMRP3000 IF EQUAL      * Exclude NWAU calc
            ENDIF
          ENDIF
.
          PROC      ABFAECCL               * Emergency ABF Care Class
.
          CALL      CCOD0000               * Check Coding
          BRANCH    EXIT,EMRP3000
.
          MOVE      ZERO,NWAUAMNT          * NWAU amount in 6 decimal places
          PROC      ABFAEINV               * Emergency ABF Price weight
.
          MOVE      ADANUMB,ADMISSNO
          CALL      PDAT0000              * Print data
.
          GOTO      EMRP1000
.
EMRP3000  MOVE      ADANUMB,KEY8
          CALL      DABF0000              * Deleting existing ABF details
          GOTO      EMRP1000
.
EMRP9000  CALL      UND132
EMRP9999  RETURN
+
.******************************************************************************
.         Check Coding 
.******************************************************************************
CCOD0000  PACK      KEY17,ADANUMB,SP70
          CALL      RSEMVCD2
CCOD1000  CALL      RKEMVCD2
          BRANCH    OVRCD,CCOD9000
.
          MATCH     DADANUMB,EMVCVIST
          GOTO      CCOD9000 IF NOT EQUAL
.
          MATCH     "006",EMVCTYPE            * check for Coding type AECC class
          GOTO      CCOD1000 IF NOT EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      CCOD9999
.
CCOD9000  MOVE      ONE,EXIT
CCOD9999  RETURN
+
.******************************************************************************
.         Process Outpatient
.******************************************************************************
OUTP0000  PACK      KEY6,SP70
          CALL      RDSSITA1
OUTP1000  CALL      RDKSITA1                * Check all OP sites
          BRANCH    OVRCD,OUTP9000
.
          CALL      OPNOUT00                * Open Files
          BRANCH    OVRCD,OUTP1000
.
.         Loop through Session file for the date range
.
          PACK      KEY25,OSTSITE,FRMDATE,SP70
          CALL      RDSSESA6
OUTP2000  CALL      RDKSESA6
          BRANCH    OVRCD,OUTP1000
.
          MATCH     OSTSITE,OSESITE
          GOTO      OUTP1000 IF NOT EQUAL
.
          MATCH     OSEDATE,ENDDATE
          GOTO      OUTP1000 IF LESS
.
          PACK      KEY18,OSESITE,OSECLIN,OSEDAY,OSESTRT,SP70
          CALL      RDMASA1
          BRANCH    OVRCD,OUTP2000
.
          PACK      KEY28,OSESITE,OSECLIN,OSEDAY,OSESTRT,OSESSHNO:
                               OSESSDAT,SP70
          CALL      RDOTHED1
          BRANCH    OVRCD,OUTP2000
.
          PACK      KEY12,OTHESITE,OTHECTYP,SP70
          CALL      RDCTYA1
          BRANCH    OVRCD,OUTP2000
.
          IF        IBCNMHOS =1
            MATCH     SP10,HOSPID
            IF        !@EQUAL
              MATCH     OTHEHOSP,HOSPID
              GOTO      OUTP2000 IF NOT EQUAL   * different hospital
            ENDIF
          ENDIF
.
.         Get all bookings with status attended for the session
.
          PACK      SESSKEYS,OSESITE,OSECLIN,OSEDATE,OSESTRT,SP70
          PACK      KEY28,SESSKEYS,SP70
          CALL      RDSBOKA1
OUTP3000  CALL      RDKBOKA1                * next read on details file
          BRANCH    OVRCD,OUTP2000          * end of session
.
          PACK      KEY25,OBASITE,OBACLIN,OBADATE,OBASTRT
          MATCH     SESSKEYS,KEY25
          GOTO      OUTP2000 IF NOT EQUAL   * different sessio
.
          COMPARE   FOUR,OBASTAT
          GOTO      OUTP4000 IF NOT EQUAL   * Not Attended
.
          PACK      KEY8,OBAOUTNO,SP70
          CALL      RDBOKB1
          BRANCH    OVRCD,OUTP4000
.
          MOVE      OBAOUTNO,KEY8
          CALL      RDVISA1
          BRANCH    OVRCD,OUTP4000
.
          PACK      KEY8,OBAURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,OUTP4000          * invalid UR number
.
          PACK      KEY5,ANSC,ANSL,OBACOMP
          CALL      RDCODE1
          BRANCH    OVRCD,OUTP4000
.
          MATCH     "N",TCINDC2               * NWAU calculated ?
          GOTO      OUTP4000 IF NOT EQUAL
.
.         NWAU calc.
. 
          MOVE      ZERO,NWAUAMNT         * NWAU amount in 6 decimal places
          PROC      ABFOTINV              * Outpatient ABF Price weight
.
          MOVE      OBAOUTNO,ADMISSNO
          CALL      PDAT0000              * Print data
.
          GOTO      OUTP3000
.
OUTP4000  MOVE      OBAOUTNO,KEY8
          CALL      DABF0000              * Deleting existing ABF details
          GOTO      OUTP3000
.
OUTP9000  CALL      UND132
OUTP9999  RETURN
+
.******************************************************************************
.         Referral Encounter
.******************************************************************************
RENC0000  CALL      CRTP0000              * Create a tempfile
.
.         loop through Encounter file to get visit numbers with Encounters
.         within the date range 
.
          MOVE      ZERO,CNTRNUM
          PACK      KEY24,FRMDATE,SP70
          CALL      RSALENC4               * Call Positional read routine
RENC1000  CALL      RKALENC4               * Call Sequential read
          BRANCH    OVRCD,RENC6000
.
          MATCH     ALENSDAT,ENDDATE       * Checks end date with Servicedate
          GOTO      RENC6000 IF LESS
.
          MATCH     "1",ALENRSTA           * Cancelled
          GOTO      RENC1000 IF EQUAL
.
          MOVE      ALENVISN,KEY8
          CALL      RDALREF1
          BRANCH    OVRCD,RENC1000
.
.         Check for Month Calendar Referral or Contacts
.
          CALL      CMRF0000               * Calendar Month referral ?
          BRANCH    MREFFLAG,RENC1000      * skip Calendar Month referral
.
.         Store visit number to a temporary file (check if it's added already)
.
          CALL      CVIS0000               * Save the visit number
          GOTO      RENC1000
.
.         Generates ABF/NWAU calculations for visit numbers in the temp.file
.
RENC6000  COMPARE   ZERO,CNTRNUM
          GOTO      RENC9999 IF EQUAL
.
          MOVE      ZERO,MREFFLAG
          MOVE      SP70,KEY8
          CALL      RSTEMP1
RENC7000  CALL      RKTEMP1
          BRANCH    OVRCD,RENC9999
.
          MOVE      TMPVISNO,KEY8
          CALL      RDALREF1
          BRANCH    OVRCD,RENC7000
.
          CALL      VREF0000               * Validating referral
          BRANCH    EXIT,RENC7000
.
          MATCH     "1",ALRESTAT
          GOTO      RENC8000 IF NOT EQUAL  * Not active
.
          MOVE      ZERO,NWAUAMNT          * NWAU amount in 6 decimal places
          CALL      ABFRFINV               * Process ABF/NWAU Calculations
.
          GOTO      RENC7000
.
.         Referral is NOT active/closed, just print data
.
RENC8000  PACK      KEY32,ALREVISN,SP70
          CALL      RSALENC6               * Call Positional read routine
RENC8200  CALL      RKALENC6               * Call Sequential read
          BRANCH    OVRCD,RENC7000
.
          MATCH     ALREVISN,ALENVISN
          GOTO      RENC7000 IF NOT EQUAL  * different visit
.
          MATCH     "1",ALENRSTA           * Cancelled
          GOTO      RENC8200 IF EQUAL
.
.         Check if there is existing ABF record
.
          MOVE      ALREVISN,KEY8
          PACK      ENCOUNTR,ALENENCT,SP70    * Encounter number
          PACK      KEY27,KEY8,ENCOUNTR,SP70
          CALL      RSPTABF1
          CALL      RKPTABF1
          BRANCH    OVRCD,RENC8200
.
          MATCH     KEY8,PTABADMN       * Same admission no ?
          GOTO      RENC8200 IF NOT EQUAL
          MATCH     ENCOUNTR,PTABENCT   * Same counter ?
          GOTO      RENC8200 IF NOT EQUAL
.
          MATCH     SP70,PTABINVN
          GOTO      RENC8200 IF NOT EQUAL  * Invoice must be blank
.
          PACK      ENCOUNTR,ALENENCT,SP70    * Encounter number
          MOVE      ALREVISN,ADMISSNO
          CALL      PDAT0000              * Print data
.
          GOTO      RENC8200
.
RENC9999  RETURN
+
.******************************************************************************
.         checking if the visit is already in the temporary file
.******************************************************************************
CVIS0000  MOVE      ALREVISN,KEY8
          CALL      RDTEMP1
          COMPARE   ZERO,OVRCD
          GOTO      CVIS9999 IF EQUAL     * Already exists
.
          MOVE      ALREVISN,TMPVISNO
          MOVE      SP70,TMPSPARE
          CALL      WRTEMP1
.
          ADD       ONE,CNTRNUM
CVIS9999  RETURN
+
.******************************************************************************
.         Referral for month
.******************************************************************************
REFC0000  MOVE      ZERO,STATLOOP
          MOVE      ONE,MREFFLAG
          CALL      SUBH0000
.
.         Loop through Referral for Date range and Status 1 to 4
.
REFC1000  ADD       ONE,STATLOOP
          COMPARE   STATLOOP,FIVE
          GOTO      REFC9000 IF LESS
.
          PACK      KEY17,STATLOOP,FRMDATE,SP70
          CALL      RSALREF3
REFC1100  CALL      RKALREF3
          BRANCH    OVRCD,REFC1000
.
          MOVE      SP70,KEY1
          MOVE      STATLOOP,KEY1
          MATCH     KEY1,ALRESTAT
          GOTO      REFC1000 IF NOT EQUAL  * different status
.
          MATCH     ALRERDAT,ENDDATE       * Checks end date with referral date
          GOTO      REFC1000 IF LESS
.
.         Check Date Active 
.
          MATCH     FRMDATE,ALREDACT
          GOTO      REFC1100 IF LESS
.
          MATCH     ALREDACT,ENDDATE
          GOTO      REFC1100 IF LESS
.
          CALL      VREF0000               * Validating referral
          BRANCH    EXIT,REFC1100
.
.         Check for Month Calendar Referral or Contacts
.
          CALL      CMRF0000               * Calendar Month referral ?
          COMPARE   ONE,MREFFLAG
          GOTO      REFC1100 IF NOT EQUAL  * Not a calendar month referral
.
          MOVE      ZERO,NWAUAMNT          * NWAU amount in 6 decimal places
          CALL      ABFRFINV               * Process ABF/NWAU Calculations
          GOTO      REFC1100
.
REFC9000  CALL      UND132
REFC9999  RETURN
+
.******************************************************************************
.         Validate referral 
.******************************************************************************
VREF0000  MOVE      ALREURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,VREF9000
.
          IF        IBCNMHOS =1
            MATCH     SP10,HOSPID
            IF        !@EQUAL
              MATCH     ALREHOSN,HOSPID
              GOTO      VREF9000 IF NOT EQUAL   * different hospital
            ENDIF
          ENDIF
          MOVE      ZERO,EXIT
          GOTO      VREF9999
.
VREF9000  MOVE      ONE,EXIT
VREF9999  RETURN
+
.******************************************************************************
.         Process Sub-Acute Inpatient
.******************************************************************************
SACT0000  PACK      KEY16 WITH FRMDATE,SP8
          CALL      RDSDSCH2
SACT1000  CALL      RDKDSCH2
          BRANCH    OVRCD OF SACT9000
.
          MATCH     DDATE,ENDDATE           * check the end date
          GOTO      SACT9000 IF LESS
.
          DISPLAY   *P62:24,DADMNO;
.
          MOVE      DADMNO,KEY8
          CALL      RDVISA1
          BRANCH    OVRCD,SACT1000
.
          CALL      RDMISS1
          BRANCH    OVRCD,SACT1000
.
          COMPARE   FIVE,ASTAT
          GOTO      SACT1000 IF EQUAL       * Cancelled
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF SACT1000
.
          PACK      KEY8,DADMNO,SP8
          CALL      RDPMVX11
          BRANCH    OVRCD,SACT1000
.
          IF        IBCNMHOS =1
            MATCH     SP10,HOSPID
            IF        !@EQUAL
              MATCH     PMVXMHOS,HOSPID
              GOTO      SACT1000 IF NOT EQUAL
            ENDIF
          ENDIF
.
          MOVE      ZERO,PCAREFLG
          MOVE      SP70,PCARETYP
          PACK      KEY5,ANSC,ANSL,ACLAIM
          CALL      RDCODE1
          BRANCH    OVRCD,SACT1000
.
          MATCH     "N",TCINDC2               * NWAU calculated ?
          GOTO      SACT3000 IF NOT EQUAL
.
.         Selected Sub-Acute Inpatient Events
.
          MATCH     SP70,ACARE
          GOTO      SACT3000 IF EQUAL
.
          PACK      KEY5,ANSC,ANSC,ACARE
          CALL      RDCODE1
          BRANCH    OVRCD,SACT3000
.
          MATCH     "X",TCINDC17
          GOTO      SACT3000 IF EQUAL         * Exclude NWAU calc
.
          MATCH     SP70,PTCDUDF6
          GOTO      SACT3000 IF EQUAL
.
.         UDF 6, 2=Rehabilitation, 3=Palliative, 4=GEM,
.                5=Psychogeriatric, 6=Maintenance
.
          MOVE      ZERO,FORM1
          SQUEEZE   PTCDUDF6
          MOVE      PTCDUDF6,FORM1
.
          PACK      PCARETYP,FORM1,SP70
.
.         Sub Acute Palliative care will be stored in patabfaf,
.         others will be in pmsabfaf file
.
          BRANCH    FORM1,SACT3000,SACT2100,SACT2000,SACT2100,SACT2100:
                          SACT2100
          GOTO      SACT3000
.
.         NWAU Calculations
.
SACT2000  MOVE      SP70,KEY2
          UNPACK    THCSCOD,KEY2
          MATCH     "8",KEY2
          GOTO      SACT2100 IF NOT EQUAL * NOT Palliative phase of Care

          MOVE      ONE,PCAREFLG
          CALL      CSNP0000              * Process ABF for each phase of care
.
          GOTO      SACT1000
.
SACT2100  MOVE      ZERO,NWAUAMNT
          PROC      ABFSAINV
          BRANCH    NOFEE,SACT3000
.
          MOVE      DADMNO,ADMISSNO
          CALL      PDAT0000              * Print data
.
          GOTO      SACT1000
.
SACT3000  MOVE      AADMNO,KEY8
          CALL      DABF0000              * Deleting existing ABF details
          GOTO      SACT1000
.
SACT9000  CALL      UND132
SACT9999  RETURN
+
.******************************************************************************
.         Check if AN-SNAP code extracted from QUICKSNAP
.******************************************************************************
CSNP0000  UNPACK    SP70,PRDSDATE,PRDEDATE
          MOVE      ZERO,NSNAPFLG
.
          PACK      KEY27,DDADMNO,SP70
CSNP1000  CALL      RSPTABF1
CSNP1200  CALL      RKPTABF1
          BRANCH    OVRCD,CSNP4000
.
          MATCH     DDADMNO,PTABADMN
          GOTO      CSNP4000 IF NOT EQUAL
.
          MATCH     SP70,PTABINVN
          GOTO      CSNP1200 IF NOT EQUAL
.
          MATCH     "070",PTABADJT          * AN-SNAP Adjustment
          GOTO      CSNP1200 IF NOT EQUAL
.
.         Check if AN-SNAP record extracted from QuickSnap (IBAPMS63)
.
          MATCH     "QUICKSNAP",PTABCUID
          GOTO      CSNP2000 IF EQUAL
          MATCH     "QUICKSNAP",PTABUUID
          GOTO      CSNP4000 IF NOT EQUAL
.
.         Check if Episode number is populated in patabfaf file from QUICKSNAP
.
CSNP2000  MOVE      SP70,D1
          UNPACK    PTABCDES,KEY8,D1          * KEY8 = AN-SNAP Code/Version
          MATCH     SP70,D1
          GOTO      CSNP4000 IF EQUAL         * AN-SNAP is blank
.
.         PTABCDES contains ANSNAP code, Version, Episode no and phase End Date
.
          MOVE      PTABENCT,PRDSDATE
          UNPACK    PTABCDES,PTRDSNAP,SLASH,PTRDVERS:
                             KEY1,DPTRDEPN,KEY1,PRDEDATE
          MOVE      DPTRDEPN,PTRDEPNO
          MOVE      DPTRDEPN,PRDEPSOD
.
          PACK      SKEY27,PTABADMN,PTABENCT,PTABINVN,PTABADJT,SP70
.
          MOVE      ONE,NSNAPFLG
          MOVE      ZERO,NWAUAMNT         * NWAU amount in 6 decimal places
          PROC      ABFSAINV
          BRANCH    NOFEE,CSNP1200
.
          MOVE      DADMNO,ADMISSNO
          CALL      PDAT0000              * Print data
.
          UNPACK    SKEY27,PTABADMN,PTABENCT,PTABINVN
          PACK      KEY27,PTABADMN,PTABENCT,PTABINVN,Z70
          GOTO      CSNP1000
.
.         Get the Episode number from Rehabilitation details
.
CSNP4000  BRANCH    NSNAPFLG,CSNP9999
.
          MOVE      ADATE,PRDSDATE           * Episode Start date
          REP       " 0",PRDSDATE
          PACK      PRDEDATE,CCC,CYY,CMM,CDD    * Use current date
          IF        ASTAT=3
            MOVE      DDATE,PRDEDATE           * Episode End date
          ENDIF
          REP       " 0",PRDEDATE
          MOVE      ZERO,CHGCCLSS
.
          PACK      KEY10,DDADMNO,SP70
          CALL      RSPTRDE1
CSNP4100  CALL      RKPTRDE1
          BRANCH    OVRCD,CSNP9999
.
          MATCH     DDADMNO,DPTRDADM
          GOTO      CSNP9999 IF NOT EQUAL
.
          MOVE      DPTRDEPN,PRDEPSOD
          CALL      NEPS0000                * Get end of episode
.
          MOVE      ZERO,NWAUAMNT         * NWAU amount in 6 decimal places
          PROC      ABFSAINV
          BRANCH    NOFEE,CSNP4100
.
          MOVE      DADMNO,ADMISSNO
          CALL      PDAT0000              * Print data
.
          GOTO      CSNP4100
.
CSNP9999  RETURN
+
.--------------------------------------------------------------------------
. Get Start & End of episode and Change of care class
.--------------------------------------------------------------------------
NEPS0000  COMPARE   ZERO,CHGCCLSS
          GOTO      NEPS1000 IF EQUAL
.
          IF        NSNAPFLG=1
            MATCH     PRDEDATE,CHDATE
            GOTO      NEPS9999 IF NOT LESS  * no change
          ENDIF
.
          GOTO      NEPS1200              * get next care class change
.
NEPS1000  PACK      KEY26,DPTRDADM,DPTRDEPN,SP70
          CALL      RDSCHCO2
NEPS1200  CALL      RDKCHCO2
          BRANCH    OVRCD,NEPS8000
.
          MATCH     DDADMNO,DCHADMN
          GOTO      NEPS8000 IF NOT EQUAL
.
          MATCH     DPTRDEPN,CHEPISNO     * Check episode number
          GOTO      NEPS2000 IF NOT EQUAL
.
          MATCH     "CC",CHCATG           * care class change ?
          GOTO      NEPS1200 IF NOT EQUAL
.
          PACK      KEY5,CHCATG,CHCODE
          CALL      RDCODE1
          BRANCH    OVRCD,NEPS1200
.
          MOVE      ZERO,FORM1
          SQUEEZE   PTCDUDF6
          MOVE      PTCDUDF6,FORM1
          COMPARE   THREE,FORM1
          GOTO      NEPS1200 IF NOT EQUAL     * NOT Palliative care
.
          MOVE      SP70,KEY2
          UNPACK    THCSCOD,KEY2
          MATCH     "8",KEY2
          GOTO      NEPS1200 IF NOT EQUAL    * NOT Palliative phase of Care
.
          IF        NSNAPFLG=0
            MOVE      CHDATE,PRDEDATE       * set End date of episode care
          ENDIF
          MOVE      CHCODE,PRDCCLSS
          ADD       ONE,CHGCCLSS
          GOTO      NEPS9999
.
NEPS2000  IF        NSNAPFLG=0
            MOVE      CHDATE,PRDSDATE       * start of new episode
          ENDIF
          GOTO      NEPS1200
.
NEPS8000  IF        NSNAPFLG=0
            MOVE      DDATE,PRDEDATE        * use discharged date
          ENDIF
          MOVE      ACARE,PRDCCLSS
.
NEPS9999  RETURN
+
.******************************************************************************
.         Process Inpatient Mental Health
.******************************************************************************
IPMH0000  PACK      KEY16 WITH FRMDATE,SP8
          CALL      RDSDSCH2
IPMH1000  CALL      RDKDSCH2
          BRANCH    OVRCD OF IPMH9000
.
          MATCH     DDATE,ENDDATE           * check the end date
          GOTO      IPMH9000 IF LESS
.
          DISPLAY   *P62:24,DADMNO;
.
          MOVE      DADMNO,KEY8
          CALL      RDVISA1
          BRANCH    OVRCD,IPMH1000
.
          CALL      RDMISS1
          BRANCH    OVRCD,IPMH1000
.
          COMPARE   FIVE,ASTAT
          GOTO      IPMH1000 IF EQUAL       * Cancelled
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF IPMH1000
.
          PACK      KEY8,DADMNO,SP8
          CALL      RDPMVX11
          BRANCH    OVRCD,IPMH1000
.
          IF        IBCNMHOS =1
            MATCH     SP10,HOSPID
            IF        !@EQUAL
              MATCH     PMVXMHOS,HOSPID
              GOTO      IPMH1000 IF NOT EQUAL
            ENDIF
          ENDIF
.
          PACK      KEY5,ANSC,ANSL,ACLAIM
          CALL      RDCODE1
          BRANCH    OVRCD,IPMH1000
.
          MATCH     "N",TCINDC2               * NWAU calculated ?
          GOTO      IPMH3000 IF NOT EQUAL
.
.         Selected Sub-Acute Inpatient Events
.
          MATCH     SP70,ACARE
          GOTO      IPMH3000 IF EQUAL
.
          PACK      KEY5,ANSC,ANSC,ACARE
          CALL      RDCODE1
          BRANCH    OVRCD,IPMH3000
.
          MATCH     "X",TCINDC17
          GOTO      IPMH3000 IF EQUAL         * Exclude NWAU calc
.
          MATCH     "11",PTCDUDF6
          GOTO      IPMH3000 IF NOT EQUAL     * Not MH
.
          MOVE      ZERO,NWAUAMNT         * NWAU amount in 6 decimal places
          PROC      ABFMHINV
          BRANCH    NOFEE,IPMH3000
.
          MOVE      DADMNO,ADMISSNO
          CALL      PDAT0000              * Print data
.
          GOTO      IPMH1000
.
IPMH3000  MOVE      AADMNO,KEY8
          CALL      DABF0000              * Deleting existing ABF details
          GOTO      IPMH1000
.
IPMH9000  CALL      UND132
IPMH9999  RETURN
+
.******************************************************************************
.         Print record 
.******************************************************************************
PDAT0000  COMPARE   "50",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          MOVE      ZERO,NEPAMNTS
          MOVE      ZERO,GROSSTOT
          MOVE      ZERO,ABFCHRGE
.
          UNPACK    PGNAME,KEY1
          MOVE      ".",ANS
          PACK      KEY22,KEY1,ANS,PSNAME
.
          BRANCH    COPTION,PDAT2000,PDAT3000,PDAT4000,PDAT2000,PDAT5000:
                            PDAT2000
          GOTO      PDAT9999
.
.         Print Inpatient/Inpatient Mental Health/Sub=Acute
.
PDAT2000  PRINT     *1,"|",KEY22,*25,"| ",ADMISSNO;
.
          MATCH     "3 ",PCARETYP
          GOTO      PDAT6000 IF EQUAL      * Palliative Phase of care
.
          UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *36,"|",CPCDATE;
.
          UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *48,"|",CPCDATE;
.
          COMPARE   FOUR,COPTION
          GOTO      PDAT2010 IF NOT EQUAL  * Not a sub-acute
.
.         Sub-Acute

          MOVE      SP70,KEY6              * AN-SNAP Code
          PACK      KEY19,ADMISSNO,ZERO,SEVENTY,SP70   * AN-SNAP Code/Version
          CALL      RDPMABF3
          IF        OVRCD=0
            UNPACK    PMABCDES,KEY6
          ENDIF
.
          PRINT     *60,"|",KEY6;
          GOTO      PDAT2020
.
PDAT2010  COMPARE   SIX,COPTION
          GOTO      PDAT23000 IF NOT EQUAL  * Not Inpatient MH
.
.         Inpatient Mental health
. 
          MOVE      SP70,KEY10
          PACK      KEY19,ADMISSNO,ZERO,SEVENTY3,SP70   * AMHCC End Class Code
          CALL      RDPMABF3
          IF        OVRCD=0
            UNPACK    PMABCDES,KEY10
          ENDIF
          PRINT     *60,"|",KEY10;
.
PDAT2020  MOVE      ZERO,PMABADJV
          MOVE      ZERO,FORM8D4
          PACK      KEY19,ADMISSNO,ZERO,SIXTY2,SP70      * PW
          CALL      RDPMABF3
          IF        OVRCD=0
            MOVE      PMABADJV,FORM8D4
          ENDIF
          GOTO      PDAT2800
.
.         Inpatient
.
PDAT2300  MOVE      SP70,PMABADRG
          MOVE      ZERO,PMABADJV
          MOVE      ZERO,FORM8D4
          PACK      KEY19,ADMISSNO,ZERO,SIXTY2,SP70      * PW
          CALL      RDPMABF3
          IF        OVRCD=0
            MOVE      PMABADJV,FORM8D4
          ENDIF
.
          UNPACK    PMABADRG,KEY8
          PRINT     *60,"|",KEY8:
                    *69,"/",PTDSDDRG;
.
PDAT2800  PRINT     *75,"|",FORM8D4;
.
          MOVE      ZERO,PMABADJV
          MOVE      ZERO,FORM8D4
          PACK      KEY19,ADMISSNO,ZERO,SIXTY1,SP70      * NEP
          CALL      RDPMABF3
          IF        OVRCD=0
            MOVE      PMABADJV,FORM8D4
            MOVE      PMABADJV,NEPAMNTS
          ENDIF
          PRINT     *89,"|",FORM8D4;
.
          MOVE      ZERO,PMABADJV
          MOVE      ZERO,FORM8D4
          PACK      KEY19,ADMISSNO,ZERO,FIFTY3,SP70      * NWAU
          CALL      RDPMABF3
          IF        OVRCD=0
            MOVE      PMABADJV,FORM8D4
          ENDIF
          PRINT     *103,"|",FORM8D4;
.
          MOVE      NWAUAMNT,ABFCHRGE
          MULT      NEPAMNTS,ABFCHRGE         * ABF Charge= NWAU x NEP
          MOVE      ABFCHRGE,GROSSTOT
.
          MOVE      GROSSTOT,FORM11P2
          PRINT     *117,"|",FORM11P2:
                    *132,"|"
          GOTO      PDAT9000
.
.         Print Emergency data
.
PDAT3000  PRINT     *1,"|",KEY22,*25,"| ",ADMISSNO;
          UNPACK    EMVIDDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *36,"| ",CPCDATE;
.
          MOVE      ZERO,PMABADJV
          MOVE      SP70,KEY25
          PACK      KEY19,ADMISSNO,ZERO,SIXTY,SP70      * AECC Class
          CALL      RDPMABF3
          IF        OVRCD=0
            UNPACK    PMABCDES,KEY25
          ENDIF
          PRINT     *48,"|",KEY25;
.
          MOVE      ZERO,PMABADJV
          MOVE      ZERO,FORM8D4
          PACK      KEY19,ADMISSNO,ZERO,SIXTY2,SP70      * PW
          CALL      RDPMABF3
          IF        OVRCD=0
            MOVE      PMABADJV,FORM8D4
          ENDIF
          PRINT     *75,"|",FORM8D4;
.
          MOVE      ZERO,PMABADJV
          MOVE      ZERO,FORM8D4
          PACK      KEY19,ADMISSNO,ZERO,SIXTY1,SP70      * NEP
          CALL      RDPMABF3
          IF        OVRCD=0
            MOVE      PMABADJV,FORM8D4
            MOVE      PMABADJV,NEPAMNTS
          ENDIF
          PRINT     *89,"|",FORM8D4;
.
          MOVE      ZERO,PMABADJV
          MOVE      ZERO,FORM8D4
          PACK      KEY19,ADMISSNO,ZERO,FIFTY3,SP70      * NWAU
          CALL      RDPMABF3
          IF        OVRCD=0
            MOVE      PMABADJV,FORM8D4
          ENDIF
          PRINT     *103,"|",FORM8D4;
.
          MOVE      NWAUAMNT,ABFCHRGE
          MULT      NEPAMNTS,ABFCHRGE         * ABF Charge= NWAU x NEP
          MOVE      ABFCHRGE,GROSSTOT
.
          MOVE      GROSSTOT,FORM11P2
          PRINT     *117,"|",FORM11P2:
                    *132,"|"
          GOTO      PDAT9000
.
.         Print Outpatient data
.
PDAT4000  PRINT     *1,"|",KEY22,*25,"| ",ADMISSNO;
          UNPACK    OBADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *36,"| ",CPCDATE;
.
          MOVE      ZERO,PMABADJV
          MOVE      SP70,KEY25
          PACK      KEY19,ADMISSNO,ZERO,THIRTY2,SP70      * Tier 2 
          CALL      RDPMABF3
          IF        OVRCD=0
            UNPACK    PMABCDES,KEY25
          ENDIF
          PRINT     *48,"|",KEY25;
.
          MOVE      ZERO,PMABADJV
          MOVE      ZERO,FORM8D4
          PACK      KEY19,ADMISSNO,ZERO,SIXTY2,SP70      * PW
          CALL      RDPMABF3
          IF        OVRCD=0
            MOVE      PMABADJV,FORM8D4
          ENDIF
          PRINT     *75,"|",FORM8D4;
.
          MOVE      ZERO,PMABADJV
          MOVE      ZERO,FORM8D4
          PACK      KEY19,ADMISSNO,ZERO,SIXTY1,SP70      * NEP
          CALL      RDPMABF3
          IF        OVRCD=0
            MOVE      PMABADJV,FORM8D4
            MOVE      PMABADJV,NEPAMNTS
          ENDIF
          PRINT     *89,"|",FORM8D4;
.
          MOVE      ZERO,PMABADJV
          MOVE      ZERO,FORM8D4
          PACK      KEY19,ADMISSNO,ZERO,FIFTY3,SP70      * NWAU
          CALL      RDPMABF3
          IF        OVRCD=0
            MOVE      PMABADJV,FORM8D4
          ENDIF
          PRINT     *103,"|",FORM8D4;
.
          MOVE      NWAUAMNT,ABFCHRGE
          MULT      NEPAMNTS,ABFCHRGE         * ABF Charge= NWAU x NEP
          MOVE      ABFCHRGE,GROSSTOT
.
          MOVE      GROSSTOT,FORM11P2
          PRINT     *117,"|",FORM11P2:
                    *132,"|"
          GOTO      PDAT9000
.
.
.         Print Referral/Contact data
.
PDAT5000  BRANCH    NOABFREF,PDAT9999
.
          IF        MREFFLAG=1
            PRINT     *1,"|",KEY22,*24,"|",ADMISSNO;
            PRINT     *36,"| ",ENCOUNTR;
          ELSE
            MOVE      ZERO,FORM8
            MOVE      ALENENCT,FORM8
            MOVE      FORM8,FORM2
            PRINT     *1,"|",KEY22,*24,"|",ADMISSNO,"/",FORM2;
            MOVE      ALENENCT,ENCOUNTR
            UNPACK    ALENSDAT,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            PRINT     *36,"| ",CPCDATE;
          ENDIF
.
          MOVE      ZERO,PTABADJV
          MOVE      SP70,KEY25
          PACK      KEY27,ADMISSNO,ENCOUNTR,ZERO,THIRTY2,SP70      * Tier 2
          CALL      RDPTABF2
          IF        OVRCD=0
            UNPACK    PTABCDES,KEY25
          ENDIF
          PRINT     *49,"|",KEY25;
.
          MOVE      ZERO,PTABADJV
          MOVE      ZERO,FORM8D4
          PACK      KEY27,ADMISSNO,ENCOUNTR,ZERO,SIXTY2,SP70      * PW
          CALL      RDPTABF2
          IF        OVRCD=0
            MOVE      PTABADJV,FORM8D4
          ENDIF
          PRINT     *75,"|",FORM8D4;
.
          MOVE      ZERO,PTABADJV
          MOVE      ZERO,FORM8D4
          PACK      KEY27,ADMISSNO,ENCOUNTR,ZERO,SIXTY1,SP70      * NEP
          CALL      RDPTABF2
          IF        OVRCD=0
            MOVE      PTABADJV,FORM8D4
            MOVE      PTABADJV,NEPAMNTS
          ENDIF
          PRINT     *89,"|",FORM8D4;
.
          MOVE      ZERO,PTABADJV
          MOVE      ZERO,FORM8D4
          PACK      KEY27,ADMISSNO,ENCOUNTR,ZERO,FIFTY3,SP70      * NWAU
          CALL      RDPTABF2
          IF        OVRCD=0
            MOVE      PTABADJV,FORM8D4
          ENDIF
          PRINT     *103,"|",FORM8D4;
.
          MOVE      NWAUAMNT,ABFCHRGE
          MULT      NEPAMNTS,ABFCHRGE         * ABF Charge= NWAU x NEP
          MOVE      ABFCHRGE,GROSSTOT
.
          MOVE      GROSSTOT,FORM11P2
          PRINT     *117,"|",FORM11P2:
                    *132,"|"
          GOTO      PDAT9000
.
.         Palliative phase of care
.
PDAT6000  UNPACK    PRDSDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *36,"|",CPCDATE;
.
          UNPACK    PRDEDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *48,"|",CPCDATE;
.
          MOVE      SP70,KEY6              * AN-SNAP Code
          PACK      KEY27,ADMISSNO,PRDSDATE,ZERO,SEVENTY,SP70   * AN-SNAP
          CALL      RDPTABF2
          IF        OVRCD=0
            UNPACK    PTABCDES,KEY6
          ENDIF
          PRINT     *60,"|",KEY6;
.
          MOVE      ZERO,PTABADJV
          MOVE      ZERO,FORM8D4
          PACK      KEY27,ADMISSNO,PRDSDATE,ZERO,SIXTY2,SP70      * PW
          CALL      RDPTABF2
          IF        OVRCD=0
            MOVE      PTABADJV,FORM8D4
          ENDIF
.
          PRINT     *75,"|",FORM8D4;
.
          MOVE      ZERO,PTABADJV
          MOVE      ZERO,FORM8D4
          PACK      KEY27,ADMISSNO,PRDSDATE,ZERO,SIXTY1,SP70      * NEP
          CALL      RDPTABF2
          IF        OVRCD=0
            MOVE      PTABADJV,FORM8D4
            MOVE      PTABADJV,NEPAMNTS
          ENDIF
          PRINT     *89,"|",FORM8D4;
.
          MOVE      ZERO,PTABADJV
          MOVE      ZERO,FORM8D4
          PACK      KEY27,ADMISSNO,PRDSDATE,ZERO,FIFTY3,SP70      * NWAU
          CALL      RDPTABF2
          IF        OVRCD=0
            MOVE      PTABADJV,FORM8D4
          ENDIF
          PRINT     *103,"|",FORM8D4;
.
          MOVE      NWAUAMNT,ABFCHRGE
          MULT      NEPAMNTS,ABFCHRGE         * ABF Charge= NWAU x NEP
          MOVE      ABFCHRGE,GROSSTOT
.
          MOVE      GROSSTOT,FORM11P2
          PRINT     *117,"|",FORM11P2:
                    *132,"|"
.
          GOTO      PDAT9000
.
PDAT9000  ADD       ONE,CLNO
PDAT9999  RETURN
+
.******************************************************************************
.         Header
.******************************************************************************
HEAD0000  PACK      CPHDROP1,SP70,SP70
          CLEAR     CPHDROP1
          APPEND    "NWAU Calculations ",CPHDROP1
.
          BRANCH    COPTION,HEAD1100,HEAD1200,HEAD1300,HEAD1400,HEAD1500:
                            HEAD1600
.
HEAD1100  APPEND    "- Inpatient",CPHDROP1
          GOTO      HEAD2000
.
HEAD1200  APPEND    "- Emergency",CPHDROP1
          GOTO      HEAD2000
.
HEAD1300  APPEND    "- Outpatient",CPHDROP1
          GOTO      HEAD2000
.
HEAD1400  APPEND    "- Sub-Acute",CPHDROP1
          GOTO      HEAD2000
.
HEAD1500  APPEND    "- Referral/Contact",CPHDROP1
          GOTO      HEAD2000
.
HEAD1600  APPEND    "- Inpatient Mental Health",CPHDROP1
          GOTO      HEAD2000
.
HEAD2000  APPEND    SP70,CPHDROP1
          RESET     CPHDROP1
.
          PACK      CPHDROP2,SP70,SP70
          IF        IBCNMHOS =1
            CLEAR     CPHDROP2
            APPEND    "Hospital  : ",CPHDROP2
            APPEND    HOSPID,CPHDROP2
            APPEND    SP2,CPHDROP2
            APPEND    HOSPNAME,CPHDROP2
            APPEND    SP70,CPHDROP2
            APPEND    SP20,CPHDROP2
            RESET     CPHDROP2
          ENDIF
.
          CALL      HEAD132
.
          UNPACK    FRMDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *N,*40,"From Date : ",CPCDATE;
.
          UNPACK    ENDDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *66,"to : ",CPCDATE
.
          ADD       ONE,CLNO
.
          CALL      SUBH0000
.
HEAD9999  RETURN
+
.******************************************************************************
.         Sub - Header
.******************************************************************************
SUBH0000  CALL      UND132
.
          BRANCH    COPTION,SUBH5100,SUBH5200,SUBH5300,SUBH5400,SUBH5500:
                            SUBH6000
          GOTO      SUBH9999
.
.         Inpatient
.
SUBH5100  PRINT     *1,"|Name",*25,"| Visit No.":
                    *36,"|Adm.Date",*48,"|Dis.Date",*60,"|Prov.DRG":
                    *69,"/ DRG":
                    *75,"|     PW",*89,"|     NEP":
                    *103,"|  Calc.NWAU":
                    *117,"| ABF Price ($)":
                    *132,"|"
          GOTO      SUBH9000
.
.         Emergency
.
SUBH5200  PRINT     *1,"|Name",*25,"| Visit No.":
                    *36,"|Disch.Date":
                    *48,"|AECC Class":
                    *75,"|     PW",*89,"|     NEP":
                    *103,"|  Calc.NWAU":
                    *117,"| ABF Price ($)":
                    *132,"|"
          GOTO      SUBH9000
.
.         Outpatient
.
SUBH5300  PRINT     *1,"|Name",*25,"| Visit No.":
                    *36,"|Attend.Date":
                    *48,"|Tier 2    ":
                    *75,"|     PW",*89,"|     NEP":
                    *103,"|  Calc.NWAU":
                    *117,"| ABF Price ($)":
                    *132,"|"
          GOTO      SUBH9000
.
.         Sub-Acute
.
SUBH5400  PRINT     *1,"|Name",*25,"| Visit No.":
                    *36,"|Adm.Date",*48,"|Dis.Date",*60,"|AN-SNAP V4":
                    *75,"|     PW",*89,"|     NEP":
                    *103,"|  Calc.NWAU":
                    *117,"| ABF Price ($)":
                    *132,"|"
          GOTO      SUBH9000
.
.         Referral(Contact)
.
SUBH5500  PRINT     *1,"|Name",*24,"| Visit No.";
.
          IF        MREFFLAG=1
            PRINT     *36,"|Active Month";
          ELSE
            PRINT     *36,"|Attend.Date";
          ENDIF
.
          PRINT     *49,"|Tier 2    ":
                    *75,"|     PW",*89,"|     NEP":
                    *103,"|  Calc.NWAU":
                    *117,"| ABF Price ($)":
                    *132,"|"
          GOTO      SUBH9000
.
.         Inpatient Mental Health
.
SUBH6000  PRINT     *1,"|Name",*25,"| Visit No.":
                    *36,"|Adm.Date",*48,"|Dis.Date",*60,"|AMHCC End Clss":
                    *75,"|     PW",*89,"|     NEP":
                    *103,"|  Calc.NWAU":
                    *117,"| ABF Price ($)":
                    *132,"|"
.
          GOTO      SUBH9000
.
SUBH9000  CALL      UND132
          ADD       FOUR,CLNO
.
SUBH9999  RETURN
+
.******************************************************************************
.         Keyin from and to date
.******************************************************************************
KDAT0000  UNPACK    SP70,FRMDATE,ENDDATE
          KEYIN     *P1:4,*EF:
                    *P1:6,"  From Date : ":
                    *P1:8,"  To   Date : "
.
.         From date
.
KDAT1000  UNPACK    SP70 WITH CCENT,CYEAR,CMON,CDAY
          MOVE      "6",CVERT
          MOVE      "24",CCOL
          CALL      KEYDATE                 * Keyin from date
          BRANCH    OVRCD,KDAT9999
.
          MATCH     SP2,CDAY
          GOTO      KDAT9999 IF EQUAL
.
          DISPLAY   *P24:6,*EL,*V2LON,CDAY,*HOFF,SLASH,*V2LON,CMON:
                    *HOFF,SLASH,*V2LON,CCENT,CYEAR
.
          PACK      FRMDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",FRMDATE
.
          MATCH     FRMDATE,CURRDATE
          GOTO      KDAT8000 IF LESS
.
.         End date
.
KDAT2000  UNPACK    SP70 WITH CCENT,CYEAR,CMON,CDAY
          MOVE      "8",CVERT
          MOVE      "24",CCOL
          CALL      KEYDATE
          BRANCH    OVRCD,KDAT1000
.
          MATCH     SP2,CDAY
          GOTO      KDAT1000 IF EQUAL
.
          DISPLAY   *P24:8,*EL,*V2LON,CDAY,*HOFF,SLASH,*V2LON,CMON:
                    *HOFF,SLASH,*V2LON,CCENT,CYEAR
.
          PACK      ENDDATE WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDATE
.
          MATCH     ENDDATE,CURRDATE
          GOTO      KDAT8200 IF LESS
.
          MATCH     FRMDATE,ENDDATE
          GOTO      KDAT9999 IF NOT LESS
.
          DISPLAY   *P1:24,*B,*EL,*V2LON,"** From Date must be <= ":
                    "To Date **",*W2;
          GOTO      KDAT1000
.
KDAT8000  DISPLAY   *P1:24,*B,*EL,*V2LON,"** From Date must be <= ":
                    "current Date **",*W2;
          GOTO      KDAT1000
.
KDAT8200  DISPLAY   *P1:24,*B,*EL,*V2LON,"** To Date must be <= ":
                    "to current Date **",*W2;
          GOTO      KDAT2000
.
KDAT9999  RETURN
+
.*******************************************************************************
.*                                  GUSR0000               Called by: MAIN0000 *
.*                               Get Web User Id                               *
.*******************************************************************************
GUSR0000  KEYIN     *P1:18,*EF,"Web User Id  : ",*V2LON,USERID
.
          PACK      USERID,USERID,SP70
          PACK      KEY10,USERID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,GUSR9100
.
          MOVE      ZERO,EXIT
          GOTO      GUSR9999
.
GUSR9100  MOVE      ONE,EXIT
GUSR9999  RETURN
+
.******************************************************************************
.         Keyin Hospital id
.******************************************************************************
KHOS0000  MOVE       SP10,HOSPID
          COMPARE    ONE,IBCNMHOS
          GOTO       KHOS9999 IF NOT EQUAL
.
          DISPLAY    *P1:20,*EL,"Hospital Id : ";
          MOVE       TEN5,ECOL
          MOVE       "20",EVERT
          MOVE       SP3,CKYIDEF3
          MOVE       ZERO,CKYIMAND           * Not Mandatory
          OPEN       PATHSPA1,"pathspaf"
.
          CALL       PATHSPKY
          BRANCH     EXIT,KHOS2000,KHOS9999
          MOVE       PTHSHOSP,HOSPID
          MOVE       PTHSNAME,HOSPNAME
          GOTO       KHOS4000
.
KHOS2000  MOVE       "All Hospitals",HOSPNAME
          MOVE       SP10,HOSPID
.
KHOS4000  DISPLAY    *P23:20,*V2LON,PTHSNAME;
KHOS9999  RETURN
+
.*******************************************************************************
.         Deleting existing ABF Patient details (pmsabfaf)
.*******************************************************************************
DABF0000  PACK      KEY19,KEY8,SP70
          CALL      RSPMABF1
          CALL      RKPMABF1
          BRANCH    OVRCD,DABF9999
.
          MATCH     KEY8,PMABADMN       * Same admission no ?
          GOTO      DABF9999 IF NOT EQUAL
.
          MATCH     SP70,PMABINVN
          GOTO      DABF9999 IF NOT EQUAL  * Invoice must be blank
.
          PACK      KEY19,PMABADMN,PMABINVN,PMABADJT
          CALL      DEPMABF1
          GOTO      DABF0000
.
DABF9999  RETURN
+
.------------------------------------------------------------
. Open Outpateint Files
.------------------------------------------------------------
OPNOUT00  MOVE      ZERO,OVRCD
          PACK      CFNAME,OSTFILE,FILMA1A1
          CLOSE     OUTMA1A1
          OPEN      OUTMA1A1,CFNAME        
          TRAPCLR   IO
          BRANCH    OVRCD,OPNOUT99
.
          PACK      CFNAME,OSTFILE,FILBOKA1  * Get the name of the BOKA1 file
          CLOSE     OUTBOKA1
          OPEN      OUTBOKA1,CFNAME           * Open the file
          TRAPCLR   IO
          BRANCH    OVRCD,OPNOUT99
.
          PACK      CFNAME,OSTFILE,FILSESA6  * Get the name of the SESA6 file
          CLOSE     OUTSESA6
          OPEN      OUTSESA6,CFNAME           * Open the file
          TRAPCLR   IO
          BRANCH    OVRCD,OPNOUT99
.
          PACK      CFNAME,OSTFILE,FILBOKA1
          CLOSE     OUTBB1A1
          CALL      OPOTBB11               * Open the file
          TRAPCLR   IO
          BRANCH    OVRCD,OPNOUT99
.
          PACK      CFNAME,OSTFILE,FILHEDA1  * Get the name of the HEDA1 file
          CLOSE     OUTHEDA1
          OPEN      OUTHEDA1,CFNAME
          TRAPCLR   IO
.
OPNOUT99  RETURN
+
.******************************************************************************
.         Create a temporary file for Referral Encounter
.******************************************************************************
CRTP0000  CALL      KILL0000                * Delete the tempfile
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMET,CMDLINE
          APPEND    " 10 U1-8",CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID          * isbuild the temp file
.
          OPEN      TMPENCA1,FNAMET
          MOVE      ZERO,EXIT
.
CRTP9999  RETURN
+
.******************************************************************************
.*                                 KILL0000               Called by: ML0000   *
.*                            Delete The Tempfile                  & OPEN0000 *
.******************************************************************************
KILL0000  CLOSE     TMPENCA1
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMET,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID          * iserase the temp file
.
KILL9000  MOVE      ZERO,EXIT
KILL9999  RETURN
+
.******************************************************************************
WRTEMP1   RESET     KEY8
          WRITE     TMPENCA1,KEY8;TMPVISNO,TMPSPARE
          RETURN
.
RDTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READ      TMPENCA1,KEY8;TMPVISNO,TMPSPARE
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTEMP1   RESET     KEY8
          READ      TMPENCA1,KEY8;;
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TMPENCA1;TMPVISNO,TMPSPARE
          GOTO      OVERCOND IF OVER
          RETURN
+
.******************************************************************************
.         IO includes
.
          INC       STD001IO
          INC       WEBERRIO/INC
          INC       IBASEQIO/INC            * Sequential Numbers File
.
          INC       IBAPOSIO/INC
          INC       ALLENCIO/INC
          INC       ALLREFIO/INC
          INC       ALLDEPIO/INC
          INC       ALLLNKIO/INC
.
          INC       OUTMA1IO/INC
          INC       OUTSITIO/INC
          INC       OUTSESIO/INC
          INC       OUTHEDIO/INC
          INC       OUTBOAIO/INC
          INC       OUTBB1IO/INC
          INC       OUTCANIO/INC
          INC       OUTCTYIO/INC
.
          INC       PATCHCIO/INC
          INC       PATRDEIO/INC
          INC       PATCODIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATDSCIO/INC
          INC       PATVISIO/INC
          INC       PMSSNPIO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
          INC       PATHSPIO/INC
          INC       PATDTRIO/INC
          INC       PATTRNIO/INC
          INC       PATINVIO/INC
          INC       PATIPEIO/INC
          INC       PATINPIO/INC
          INC       PATDO1IO/INC
.
          INC       PMSABFIO/INC
          INC       PATABFIO/INC
          INC       PATICUIO/INC
          INC       PATECDIO/INC
.
          INC       PATFN1IO/INC
          INC       PATFX1IO/INC
          INC       PATDGWIO/INC
          INC       PATPGRIO/INC
          INC       PATWR1IO/INC
          INC       PATFINIO/INC
          INC       PATCFAIO/INC
          INC       PMSMTIIO/INC
          INC       PMSEDGIO/INC
          INC       PMSCIDIO/INC
.
          INC       EMRVISIO/INC
          INC       EMRVCDIO/INC
          INC       AAEDE1IO/INC
          INC       AAEDTRIO/INC
          INC       OUTDTRIO/INC
.
          INC       WEBSECIO/INC
.
          INC       GETDRG
          INC       GETEFDRG
          INC       VALCONTR
          INC       GETCNEFF
          INC       NHOSPDAY
          INC       CALCDAYS
.
          INC       ABFPTINV
          INC       ABFAEINV
          INC       ABFOTINV
          INC       ABFSAINV                * Sub-Acute
          INC       ABFRFINV                * Referral
          INC       ABFMHINV                * Mental Health
.
          INC       ABFAECCL
          INC       PATHSPKY
          INC       TFILENAM                * Generate Temp File Name
.
.
PATCALF   
GHSP0000
PRLN0000
NXTTRAN1
WAAE0000
WOUT0000
          RETURN
+

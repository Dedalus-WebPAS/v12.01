. **********************************************************
. *              P R O G R A M  S U M M A R Y              *
. *              -------------  -------------              *
. *                                                        *
. *     PROGRAM NAME:     IBAPAT53                         *
. *                                                        *
. *     FUNCTION:         WARD MASTER REGISTER LISTING     *
. *                                                        *
. *     DATE WRITTEN:     12/06/85                         *
. *                                                        *
. *     AUTHOR:           MALCOLM HAYSE (I.B.A)            *
. *                                                        *
. *     MODIFICATIONS:                                     *
. *     V10.03.01 27/06/2013 Ebon Clements  CAR 287511     *
. *               Change to only print 4 character ward    *
. *                extension                               *
. *                    V5.01.01 22/08/89 Graeme Williams   *
. *                             Added option for speciality*
. *                             listing                    *
. *                    V5.01.02 17.05.90 D.Di.Paola        *
. *                             Included No. of Beds for   *
. *                             Stats in report. SRF 104953*
. *                    V5.01.03 29/05/91 J.Tan  SRF 108720 *
. *                             Mods to print active indic.*
. *                    V5.01.04 22/11/91 J.Tan  SRF 110966 *
. *                             Fixed printing active indic*
. *                    V5.01.05 22/01/93 Steve O'gorman    *
. *                             Modified to look at the    *
. *                             new files                  *
. *        V5.01.06  24.02.92 Neeriem "I Hate Support" Dye *
. *                  Reformat print to look beautiful for  *
. *                  new fields                            *
. *                                                        *
. **********************************************************
.
          INC    STD001FD
.
          INC    PATCODFD/INC
          INC       PATWR1FD/INC
          INC    PATWSPFD/INC
.
.               WORK AREA
.
CODEDT          INIT    "DT"
CODEHU          INIT    "HU"
CODEIA          INIT    "IA"
CODEIC          INIT    "IC"
.
DIM3            DIM     3
.
SAVWARD         DIM     3
PRTWARD         DIM     3
TOTWARD         FORM    4
PRTSPEC         DIM     20
PRTHOSN         DIM     20
PRTIAGE         DIM     20
PRTICRE         DIM     20
.
.
WRDESC          DIM     20
WREXTN          DIM     4
WRBLINE         FORM    2
WRNBEDS         FORM    3
WRHCS           FORM    3
WRSERV          DIM     3
WRCLSS          DIM     3
WROCCBED        FORM    3
DAYNIG          INIT    "     "
DAYNIG0         INIT    "Day  "
DAYNIG1         INIT    "Both "
DAYNIG2         INIT    "Night"
INTSEX          INIT    "      "
INTSEXE         INIT    "Either"
INTSEXF         INIT    "Male  "
INTSEXM         INIT    "Female"
.
FIELD           FORM    2
WRDEXTN4  DIM       4                 * DIM 4 ward extension
.
PRGID     INIT      "IBAPAT53"
PRGNAM    INIT      "Ward/bed Register"
VERSION   INIT      "V12.01.00"
+
.         START OF PROGRAM
.
          CALL          DISPHEAD
.
          DISPLAY       *P56:24,"Opening patward1";
          OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY       *P64:24,"patwspaf";
          OPEN          PATWSPA1,"patwspaf"
.
          DISPLAY       *P64:24,"patcodes";
          OPEN          PATCODE1,"patcodes"
.
OPFIL     DISPLAY       *P64:24,"controlf";
          OPEN          CONTROLF,"controlf"
+
.
.        START OF PROGRAM
.
START   MOVE       ZERO,CPAGENO
.
        DISPLAY     *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Ward/Bed Sequence":
                    *P1:6,*V2LON,TWO,*HOFF," = Ward Speciality Listing"
.
          KEYIN     *P1:8,*EL,"Option : ",*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
REPEAT    BRANCH    OPTION OF KWARD,KWSPA
          BEEP
          GOTO      START
+
.
.               PRINT VIA WARD NUMBER
.
KWARD   
        CLOCK           TIME,CTIMEIS
        MOVE            SP6,KEY6
        CALL            RDSWARD1
        MOVE            "60",CLNO
        MOVE            SP3,SAVWARD
.
        DISPLAY         *P20:24,*EL,"Ward :",*P40:24,"Bed :";
.
NEXTA   CALL            RDKWARD1
	BRANCH          OVRCD OF ENDP
.
        CALL            DISPDA
.
        DISPLAY         *P27:24,*V2LON,WWARD,*P46:24,WBED
        GOTO            NEXTA
NOWARD  DISPLAY         *P20:24,*V2LON,"** No Ward/Beds on file **",*W3;
        GOTO            START
.
.       Ward speciality report
.
KWSPA   CALL            GEN0000
        GOTO            START
+
.
.           OVER CONDITION FOUND..PRINT LAST LINE OF -- AND DISPLAY MESSAGE
.
ENDP     COMPARE      ZERO,CPAGENO
         GOTO         NOWARD IF EQUAL
         CALL         UND132
         PRINT        *N,"  ***  END OF REPORT  ***"
         DISPLAY      *P40:24,*EL,*V2LON:
                      "** Report Generation Completed **",*W3;
         GOTO         START
+
.
.     PRINT ACTUAL DATA 
.
DISPDA    COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
.
	MOVE            ONE,FIELD
.
PRNTR   COMPARE         "10",FIELD
	GOTO            PRNTCNT IF EQUAL
        ADD             ONE,FIELD
PRNTCNT  
          COMPARE   SIX,CLNO
          GOTO      FPRINT1 IF EQUAL
.
.         PRINT DATA TO PRINTER
.
FPRINT   MATCH     WWARD,SAVWARD
         GOTO      FPRINT2 IF EQUAL
.
FPRINT1  MATCH     SP3,WBED
         GOTO      FPRINTN IF NOT EQUAL
.
.        SAVE THE WARD DESCRIPTION THEN GO AND GET BEDS
.
         MOVE      WBDESC,WRDESC
         MOVE      WEXTN,WREXTN
         MOVE      WBLINE,WRBLINE
         MOVE      WNBEDS,WRNBEDS
         MOVE      WOCCBED,WROCCBED
         MOVE      WHCSSUB,WRHCS
         MOVE      WSERV,WRSERV
         MOVE      WCLASS,WRCLSS
         BRANCH    WINPUT OF PRTWRDO
         GOTO      DISPEND
.
PRTWRDO  MOVE      SP30,WBDESC
         MOVE      SP3,WRSERV
         MOVE      SP70,WEXTN
.
FPRINTN   CALL      UND132
          MOVE      YES,DIM3
          LOAD      DIM3,WACTIVE,NO
.
          MOVE      DAYNIG0,DAYNIG
          LOAD      DAYNIG,PTWDDNIN,DAYNIG1,DAYNIG2
          REP       " E",PTWDSEX
          MATCH     "M",PTWDSEX
          IF        @EQUAL
            MOVE      INTSEXM,INTSEX
          ELSE
            MATCH     "F",PTWDSEX
            IF        @EQUAL
              MOVE      INTSEXF,INTSEX
            ELSE
              MOVE      INTSEXE,INTSEX
            ENDIF
          ENDIF
.
          MOVE      WBDESC,KEY18
          MOVE      WEXTN,WRDEXTN4
          PRINT     *1,"!",WWARD,*6,WRDESC,*27,WREXTN:
                    SP3,WRBLINE,SP4,WROCCBED,SP4,WRNBEDS:
                    SP2,WRHCS,SP2,WRSERV,SP2,WRCLSS:
                    SP2,DAYNIG,SP2,PTWDSEX,SP1,PTWDIAGE,SP1,PTWDICRE:
                    SP3,PTWDHOSN:
                    *90,"!",WBED,SP1,KEY18,SP1,DIM3,SP2:
                    WEFRBT,SP2,WRTYPE,SP1,WRDEXTN4,*132,"!"
          ADD       ONE,CLNO
          MOVE      WWARD,SAVWARD
          GOTO      ENDPRT
.
FPRINT2   CALL      PAGSEP
          MOVE      YES,DIM3
          LOAD      DIM3,WACTIVE,NO
.
          MOVE      WBDESC,KEY18
          MOVE      WEXTN,WRDEXTN4
          PRINT     *1,"! ":
                    *90,"!",WBED,SP1,KEY18,SP1,DIM3,SP2:
                    WEFRBT,SP2,WRTYPE,SP1,WRDEXTN4,*132,"!"
          ADD       ONE,CLNO
.
ENDPRT    
.
DISPEND   RETURN
.
+
.
.          LAST LINE ON THE WARD
.
PAGSEP    PRINT     *1,"!",*90,"---------":
                    "---------------------------------!"
          ADD       ONE,CLNO
          RETURN
+
.
.                 HEADINGS FOR OUTPUT
.
HEAD      CALL      HEAD132
.
          PRINT     *N;
          CALL      UND132
          PRINT     *1,"!Ward",*27,"Extn",*32," No.",*39,"Beds":
                    *46,"Beds",*52,"HDP",*56,"Serv",*61,"Class":
                    *67,"Day/",*72,"Sex",*76,"Age",*80,"Care",*85,"Hos/":
                    *90,"!Bed":
                    *111,"Active",*119,"EFR":
                    *123,"Room",*128,"Extn",*132,"!": 
                    *N,*1,"!",*32,"Lines",*39,"Stats",*46,"Wards":
                    *67,"Night",*76,"Grp",*80,"Int.",*85,"Unit":
                    *90,"!",*123,"Type",*132,"!"
          MOVE      SEVEN,CLNO
          RETURN
+
.**************************************************************************
.*                              GEN0000                                   *
.*                          Generate the report                           *
.**************************************************************************
GEN0000   MOVE      ZERO,CPAGENO             * Initialise page count
          MOVE      "60",CLNO                * Initialise line count
          CLOCK     TIME,CTIMEIS             * Initialise starting time
          MOVE      SP3,SAVWARD              * Initialise the current ward
.
          DISPLAY   *P1:23,*EL,*P20:23,"Generating : ";
.
          MOVE      SP6,KEY6                 * Start at the beginning of file
          CALL      RDSWSPA1                 * Position to start of period
GEN1000   CALL      RDKWSPA1                 * Read the next record
          BRANCH    OVRCD,GEN9000            * Finished generating report
.
          DISPLAY   *P33:23,*V2LON,WSPWARD,SP1,WSPSPEC;
.
.         Check for a change in ward
.
          MATCH     WSPWARD,SAVWARD          * Still the same ward
          GOTO      GEN2000 IF EQUAL         * Yes. Just print record
.
          MATCH     SP3,SAVWARD              * First time through ?
          CALL      TOTW0000 IF NOT EQUAL    * No. Print ward break.
.
          MOVE      WSPWARD,SAVWARD          * Save current ward
          MOVE      WSPWARD,PRTWARD          * Set up ward to print on report
          MOVE      ZERO,TOTWARD
.
.         Print this record
.
GEN2000   CALL      PRNT0000                 * Print this record
          ADD       WSPNBED,TOTWARD          * Add up the number of beds
          GOTO      GEN1000                  * Get the next record
.
.         End of processing.
.
GEN9000   COMPARE   ZERO,CPAGENO             * Has anything been printed ?
          GOTO      GEN9900 IF EQUAL         * No. Just print heading
.
          CALL      TOTW0000                 * Print ward break
          PRINT     *N,"  ***  End of Report  ***"
          GOTO      GEN9999
.
.         Nothing printed on report
.
GEN9900   KEYIN     *P1:24,*EL,*V2LON,*B:
                    "** No data in file. Hit <ENTER> to continue ** ":
                    *EOFF,ANS,*P1:24,*EL;
.
          CALL      HEAD0000                 * Print the report heading
          PRINT     *N,"  ***  End of Report  ***"
.
GEN9999   RETURN
.**************************************************************************
.*                              PRNT0000                                  *
.*                          Print a data record                           *
.**************************************************************************
PRNT0000  MOVE      "No Speciality Given ",PRTSPEC
          MATCH     SP3,WSPSPEC              * Do we have a doctor type ?
          GOTO      PRNT1000 IF EQUAL        * No. Use the default description
.
          MOVE      "Unknown Speciality  ",TDESC
          PACK      KEY5,CODEDT,WSPSPEC      * Key for doctor type
          CALL      RDCODE1
          MOVE      TDESC,PRTSPEC            * Save the description
.
.         Print the record
.
PRNT1000  COMPARE   "50",CLNO                * Need a new page ?
          CALL      HEAD0000 IF NOT LESS     * Yes. Print new page heading
.
          PRINT     *10,"!  ",PRTWARD:
                    *20,"! ",WSPSPEC,SP3,PRTSPEC:
                    *58,"!",*63,WSPNBED:
                    *70,"!"
          ADD       ONE,CLNO                 * Increment line count
.
.         We only want to print the ward the first time (and at the start
.         of each page), so we re-initialise the print variable to blanks
.         at this point. It is initialised whenever we change wards, or
.         print a new page.
.
          MOVE      SP3,PRTWARD              * Clear ward print variable
.
PRNT9999  RETURN
.**************************************************************************
.*                              HEAD0000                                  *
.*                          Print heading                                 *
.**************************************************************************
HEAD0000  ADD      ONE,CPAGENO
.
          COMPARE  ONE,CPAGENO
          CALL     PAGEND IF NOT EQUAL
.
          PRINT    *F,PRGID,*20,CNAME,*66,"DATE : ",CDATE:
                   *N,VERSION,*20,"WARD SPECIALITY REGISTER":
                   *66,"TIME : ",CTIMEIS:
                   *N,*66,"PAGE :      ",CPAGENO
          MOVE     THREE,CLNO 
          CLOCK    TIME,CTIMEIS
.
          PRINT     *N;
          ADD       ONE,CLNO                 * Increment line count
.
          CALL      PAGEND                   * Print page break
          PRINT     *10,"!  Ward":
                    *20,"! Speciality":
                    *58,"! ## of Beds":
                    *70,"!"
          ADD       ONE,CLNO                 * Increment line count
.
          CALL      PAGEND                   * Print page break
.
.         Make sure the current ward is print at the start of the new page
.
          MOVE      SAVWARD,PRTWARD          * Pack up ward for print
.
HEAD9999  RETURN
.
PAGEND    PRINT     *10,"*-----------------------------":
                        "------------------------------*"
.                   
          ADD       ONE,CLNO
          RETURN
.
.         Print the total number of beds in the ward
.
TOTW0000  CALL      PAGEND
          PRINT     *10,"!":
                    *20,"! Total":
                    *58,"!",*62,TOTWARD:
                    *70,"!"
          ADD       ONE,CLNO                 * Increment line count
          CALL      PAGEND
TOTW9999  RETURN
+
         INC       PATCODIO/INC
         INC       PATWR1IO/INC
         INCLUDE    PATWSPIO/INC
.
         INCLUDE    STD001IO
.
ENDIT    CHAIN      PGM
         STOP

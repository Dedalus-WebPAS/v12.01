.******************************************************************************
.* System    :   Outpatients/Allied Health                                    *
.* Program   :   MIGOP2AH                                                     *
.******************************************************************************
.* Date      :   10/07/2007                                                   *
.* Author    :   Jill Habasque                                                *
.* Function  :   This program will load outpatient referrals to allied health *
.*                                                                            *
.* Modifications                                                              *
.******************************************************************************
.* V12.00.01 13/05/2025  Ebon Clements        TSK 0955096                     *
.*           Added alpanumeric visit number generation - OUTRF350 - GENANVIS  *
.*        V11.02.01 07/02/2022  Thanh T       TSK 0896905                     *
.*                  Recompiled for ALLDEPFD changes                           *
.******************************************************************************
.*        V10.07.01 10/03/2016 Peter McMullem CAR 0815423                     *
.*                  Use mandatory env var QISNAME to detect Oracle            *
.*        V10.05.01 14/11/2014 Ebon Clements   CAR 292656                     *
.*                  Added link cancelled OP visit to a ref functionality      *
.*        V10.04.01 11/02/2014  Jill Parkinson CAR 297174                     *
.*                  Changed option 2 to ignore cancelled and fixed future date*
.*                  check                                                     *
.*        V10.03.01 25/02/2013 Ania P         CAR 281021                      *
.*                  Removed unnecessary modification of constants.            *
.*        V9.11.02  07/04/2009  Peter Vela    CAR 183976                      *
.*                  Move spaces to PMVXPILC before WRPMVX11                   *
.*        V9.11.01  16/02/2009  Peter Vela    CAR 183976                      *
.*                  Move spaces to PMVXPOEC before WRPMVX11                   *
.*        V9.09.05  04/01/2008  J.Tan         CAR 135498                      *
.*                  Mods for NZ MHINC Extract                                 *
.*        V9.09.04  18/12/2007  Jill Habasque CAR 157940                      *
.*                  Added check for closed referrals                          *
.*        V9.09.03  03/12/2007  Jill Habasque CAR 154901                      *
.*                  Fixed packing of CURRDATE                                 *
.*        V9.09.02  26/11/2007  Jill Habasque CAR 135504                      *
.*                  Added clear of ALRERCLO,ALREDCLO and ALRERCAN             *
.*        V9.09.01  26/07/2007  Jill Habasque CAR 135504                      *
.*                  Fixed up created and updated dates and times              *
.******************************************************************************
.
          INC       STD001FD
          INC       ALLCONFD/INC
          INC       ALLDEPFD/INC
          INC       ALLLNKFD/INC
          INC       ALLREFFD/INC
          INC       OUTRF1FD/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATVISFD/INC
          INC       PMSVX1FD/INC
.
. TEMPFILE DEFINITION
. -------------------------
TMPMAPA1  FILE    ASCII, FIXED=21
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
MAPSITE   DIM       6      6        1     OP Site Code
MAPTYPE   DIM       6      6        7     OP Clinic Type
MAPCLIN   DIM       6      6        13    OP Clinic ID
MAPAHDP   DIM       3      3        18    AH Department
.                                   21    End of Record
.
TMPOUTA1  FILE    ASCII, FIXED=525
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
.OTRLOUTN  DIM       8      8        1     Outpatient Number
.OTRLURNO  DIM       8      8        9     U/R Number
.OTRLDATE  DIM       8      8        17    Letter Date
.OTRLRDOC  DIM       10     10       25    Referring HCP
.OTRLCONS  DIM       6      6        35    Consultant
.OTRLDEPT  DIM       3      3        41    Department
.OTRLPRIO  DIM       3      3        44    Priority
.OTRLDIAG  DIM       50     50       47    Diagnosis
.OTRLUSD1  DIM       3      3        97    User Defined Field 1
.OTRLUSD2  DIM       3      3        100   User Defined Field 2
.OTRLUSD3  DIM       3      3        103   User Defined Field 3
.OTRLUSD4  DIM       3      3        106   User Defined Field 4
.OTRLBSIT  DIM       6      6        109   Booked Site
.OTRLBCLI  DIM       6      6        115   Booked Clinic
.OTRLBDTE  DIM       8      8        121   Booked Date
.OTRLBSTR  DIM       5      5        129   Booked Start Time
.OTRLBSLT  DIM       3      3        134   Booked Slot Number
.OTRLRFGP  DIM       10     10       137   Referring GP
.OTRLGPPC  DIM       10     10       147   GP Practice Code
.OTRLRQST  DIM       20     20       157   Request Number
.OTRLCDTE  DIM       8      8        177   Request for Appointment Canc. Date
.OTRLREAS  DIM       3      3        185   Reason for Request        "RQ"
.OTRLSPFC  DIM       3      3        188   Specialty Function Code   "OF"
.OTRLCAD1  DIM       35     35       191   Correspondence Address Line 1
.OTRLCAD2  DIM       35     35       226   Correspondence Address Line 2
.OTRLCAD3  DIM       35     35       261   Correspondence Address Line 3
.OTRLCAD4  DIM       35     35       296   Correspondence Address Line 4
.OTRLCPC   DIM       8      8        331   Correspondence Post Code
.OTRLECRN  DIM       20     20       339   ECR Approval Number
.OTRLGPFH  DIM       20     20       359   GPFH Approval Number
.OTRLGPPT  DIM       2      2        379   GP Practice count
.OTRLOPER  DIM       4      4        381   Operator who recorded O/P referral
.OTRLCTYP  DIM       6      6        385   Clinic Type of Referral
.OTRLSITE  DIM       6      6        391   Site Code of Referral
.OTRLSRCE  DIM       3      3        397   Source of Referral
.OTRLRHCP  DIM       10     10       400   Responsible HCP
.OTRLDREC  DIM       8      8        410   Date Referral Received
.OTRLDPRI  DIM       8      8        418   Date of Priority
.OTRLPURC  DIM       3      3        426   Expected Purchaser
.OTRLTREF  FORM      1      1        428   Referral Type (0=Outpatient, 1=Other)
.OTRLRANK  DIM       6      6        430   Patient Rank
.OTRLUDAT  DIM       8      8        436   Date Record Updated (CCYYMMDD)
.OTRLUTIM  DIM       8      8        444   Time Record Updated (hh:mm:ss)
.OTRLUUID  DIM       10     10       452   Web User Id Updated (websecaf)
.OTRLCDAT  DIM       8      8        462   Date Record Created (CCYYMMDD)
.OTRLCTIM  DIM       8      8        470   Time Record Created (hh:mm:ss)
.OTRLCUID  DIM       8      8        478   Web User Id Created (websecaf)
.OTRLCLAM  DIM       3      3        486   Claim Code (Category CL)
.OTRLSPRE  DIM       36     36       489   Spare
.
.End of Record                      525
. LOCAL VARIABLE DEFINITION
. -------------------------
AADMNO    DIM       8
CLOSEREA  DIM       3
CODE      DIM       3
COUNT     FORM      8
CURRDATE  DIM       8
FNAME     DIM       200
STATUS    FORM      1
.
PRGID     INIT      "MIGOP2AH"
PRGNAM    INIT      "Migrate OP Refs to AH Refs   "
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          CALL      KASC0000               * Keyin mapping file path
          BRANCH    EXIT,ML9999
.
          CALL      REAS0000               * Keyin closure reason
          BRANCH    EXIT,ML9999
.
          CALL      SAVF0000               * save files/ warn if oracle
.
          BRANCH    COPTION,ML1000,ML1000: * proceed with clean up
                            ML1000
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      ALLDEPA1,"alldepaf"
          OPEN      ALLLNKA1,"alllnkaf"
          OPEN      ALLREFA1,"allrefaf"
          OPEN      OUTRF1A1,"outrf1af"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATVISA1,"patvisaf"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,HUND06;*213,ALCNLAUD
.
          MATCH     "1",ALCNLAUD
          IF        @EQUAL
            OPEN      ALLAUDLN,"allaudln"
          ENDIF
.
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          PREP      TMPOUTA1,"mvdrf1af"
          MOVE      SP70,DIM19
          GETENV    "ORACLE_SID",DIM19
          PACK      DIM19,DIM19,SP70
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,"= Migrate Unbooked Referrals only":
                    *P1:6,*V2LON,TWO,*HOFF,"= Migrate Unbooked and Future ":
                                           " Booked Referrals":
                    *P1:7,*V2LON,THREE,*HOFF,"= Migrate All Referrals (":
                                             "Unbooked, Booked, Cancelled)"
.
OPTN0500  KEYIN     *P1:9,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:9,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000,OPTN9000:   * run clean-up
                            OPTN9000
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  MOVE      ZERO,COUNT
          CLOSE     TMPMAPA1
          OPEN      TMPMAPA1,FNAME
.
PROC1000  READ      TMPMAPA1,SEQ;MAPSITE,MAPTYPE,MAPCLIN,MAPAHDP
          GOTO      PROC9999 IF OVER
.
          PACK      KEY3,MAPAHDP,SP70
          CALL      RDALDEP1
          BRANCH    OVRCD,PROC1000
.
          CALL      OUTRF000                * loop through outrf1af
          GOTO      PROC1000
.
PROC9999  DISPLAY   *P1:24,*V2LON,"Records processed ",COUNT;
          CALL      HITENTER
          RETURN
+
.******************************************************************************
.*                                 KASC0000                                   *
.*                            Keyin ASCI file                                 *
.****************************************************************************** 
KASC0000  MOVE      ZERO,EXIT
          PACK      FNAME,SP70,SP70,SP70
.
          KEYIN     *P1:10,*EL,"Mapping Path & File Name : ",*V2LON,FNAME
          PACK      FNAME,FNAME,SP70,SP70,SP70
.
          MATCH     SP70,FNAME
          GOTO      KASC9000 IF EQUAL
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      TMPMAPA1,FNAME
          TRAPCLR   IO
.
          BRANCH    OVRCD,KASC8000
.
.  --- check allied health department is valid ---
.
KASC1000  READ      TMPMAPA1,SEQ;MAPSITE,MAPTYPE,MAPCLIN,MAPAHDP
          GOTO      KASC9999 IF OVER
.
          PACK      KEY3,MAPAHDP,SP70
          CALL      RDALDEP1
          BRANCH    OVRCD,KASC8100
          GOTO      KASC1000
.
KASC8000  DISPLAY   *P1:24,*EL,*B,"File not found.  ";
          CALL      HITENTER
          GOTO      KASC0000
.
KASC8100  DISPLAY   *P1:22,*EL,*B,"Mapping File Error: Invalid Allied ":
                                  "Health Department in Input file":
                    *P1:24,*EL;
          CALL      HITENTER
          GOTO      KASC9000
.
KASC9000  MOVE      ONE,EXIT
.
KASC9999  RETURN
+
.***************************************************************************
.*                               REAS0000                                  *
.*                          Close referral reason                          *
.***************************************************************************
REAS0000  MOVE      "22",ECOL
          MOVE      "11",EVERT
          MOVE      "LL",CODE
          MOVE      SP3,CLOSEREA
          MOVE      SP3,CKYIDEF3
          MOVE      ONE,CKYIMAND
          MOVE      ZERO,CKYIMODE
.
          DISPLAY   *P1:EVERT,"Reason for Closure :",*EF;
          CALL      PATCODKY                * Keyin a codes file code
          BRANCH    EXIT,REAS9999,REAS9999
.
          MOVE      ACODE,CLOSEREA
          DISPLAY   *P32:EVERT,*EL,TDESC;

.
REAS9999  RETURN
.
+
.******************************************************************************
.*                                 OUTRF000                                   *
.*                            Loop through outrf1af                           *
.****************************************************************************** 
OUTRF000  PACK      KEY8,SP70
          CALL      RSOTRFL1
OUTRF100  CALL      RKOTRFL1
          BRANCH    OVRCD,OUTRF999
.
          COMPARE   ZERO,OTRLTREF
          GOTO      OUTRF100 IF NOT EQUAL
.
          MATCH     MAPSITE,OTRLSITE
          GOTO      OUTRF100 IF NOT EQUAL
.
          MATCH     SP70,MAPTYPE
          IF        !@EQUAL
            MATCH     MAPTYPE,OTRLCTYP
            GOTO      OUTRF100 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,MAPCLIN
          IF        !@EQUAL
            MATCH     MAPCLIN,OTRLCONS
            GOTO      OUTRF100 IF NOT EQUAL
          ENDIF
.
          IF        COPTION=1
            MATCH     SP70,OTRLBSIT            * ignore booked records
            GOTO      OUTRF100 IF NOT EQUAL
.
            MATCH     SP70,OTRLCDTE            * ignore cancelled records
            GOTO      OUTRF100 IF NOT EQUAL
          ENDIF
.
          IF        COPTION=2
            MATCH     SP70,OTRLCDTE            * ignore cancelled records
            GOTO      OUTRF100 IF NOT EQUAL
.
            MATCH     SP70,OTRLBSIT            * get unbooked
            GOTO      OUTRF200 IF EQUAL
.           
            MATCH     CURRDATE,OTRLBDTE        * get future booked records
            GOTO      OUTRF100 IF LESS
          ENDIF
.
OUTRF200  MATCH     SP70,OTRLCDTE              * cancelled record
          GOTO      OUTRF300 IF EQUAL
.
          MOVE      "4",STATUS
          MOVE      OTRLOUTN,AADMNO
          GOTO      OUTRF400
.
OUTRF300  MATCH     SP70,OTRLBDTE
          IF        @EQUAL
            MOVE      "0",STATUS
            MOVE      OTRLOUTN,AADMNO
            GOTO      OUTRF400
          ENDIF
.
          MATCH     CURRDATE,OTRLBDTE
          IF        !@LESS
            MOVE      "1",STATUS
          ELSE
            MOVE      "2",STATUS
          ENDIF
.
OUTRF350  READ      CONTROLF,HUND30;*75,PTCNUANV      * Using AN Visit Numbers
.
          MATCH     "1",PTCNUANV
          IF        @EQUAL
            CALL      GANV0000                * Get next AN visit number
            MOVE      PTCNNXTV,AADMNO
          ELSE
            MOVE      " 10",PRXCODE   * System Lock Sector 10
            CALL      GETSLK00        * Get System Lock of Sector 10
            READ      CONTROLF,TEN;*1,FORM8V
            ADD       ONE,FORM8V
            WRITAB    CONTROLF,TEN;*1,FORM8V
            CALL      RELSLK00        * Release System Lock of Sector 10
            SUB       ONE,FORM8V
            MOVE      FORM8V,AADMNO
          ENDIF
.
          PACK      KEY8,AADMNO,SP70
          CALL      RDAVISA1
          COMPARE   ZERO,OVRCD
          GOTO      OUTRF350 IF EQUAL       * get the next Admission Number
.
OUTRF400  CALL      WRREF000                   * write allrefaf record
          BRANCH    EXIT,OUTRF100
.
          CALL      WRVIS000                   * write patvisaf record
          BRANCH    EXIT,OUTRF100
.
          CALL      WRVX1000                   * write pmsvx1af record
          BRANCH    EXIT,OUTRF100
.
          IF        STATUS=1|STATUS=2
            CALL      WRLNK000
          ENDIF
          CALL      WROUT000                   * write output file
. 
          PACK      KEY8,OTRLOUTN,SP70
          CALL      DEOTRFL1                   * delete outrf1af record
          ADD       ONE,COUNT
          IF        (COUNT%100) = 0
            DISPLAY   *P1:24,*V2LON,"Records processed ",COUNT
          ENDIF
.
          GOTO      OUTRF100
.
OUTRF999  RETURN
+
.******************************************************************************
.*                                 WRREF000                                   *
.*                            Write allrefaf record                           *
.****************************************************************************** 
WRREF000  MOVE      ZERO,EXIT
          PACK      KEY8,AADMNO,SP70
          CALL      RDALREF1
          IF        OVRCD<>1
            GOTO      WRREF900
          ENDIF
.
          PACK      ALREVISN,AADMNO,SP70
          PACK      ALREURNO,OTRLURNO,SP70
          PACK      ALRESTAT,STATUS,SP70
          PACK      ALREDACT,OTRLBDTE,SP70
          PACK      ALRELINK,SP70
          PACK      ALRERCLO,SP70
          PACK      ALREDCLO,SP70
          MATCH     "2",ALRESTAT
          IF        @EQUAL
            MATCH     CURRDATE,OTRLBDTE
            IF        !@LESS
              PACK      ALRERCLO,SP70
              PACK      ALREDCLO,SP70
            ELSE
              PACK      ALRERCLO,CLOSEREA,SP70
              PACK      ALREDCLO,OTRLBDTE,SP70
            ENDIF
          ENDIF
          PACK      ALRETCLO,SP70
          PACK      ALREUCLO,SP70
          MATCH     SP70,OTRLCDTE
          IF        !@EQUAL
            PACK      ALRERCAN,OTRLREAS,SP70
          ELSE
            PACK      ALRERCAN,SP70
          ENDIF
          PACK      ALREDCAN,OTRLCDTE,SP70
          PACK      ALRETCAN,SP70
          PACK      ALREUCAN,SP70
          PACK      ALRERINA,SP70
          PACK      ALREDINA,SP70
          PACK      ALRETINA,SP70
          PACK      ALREDINU,SP70
          PACK      ALREUINA,SP70
          PACK      ALRERDAT,OTRLDATE,SP70
          PACK      ALRERDEP,SP70
          PACK      ALRERDOC,SP70
          PACK      ALRERHCP,OTRLRDOC,SP70
          PACK      ALRERHCR,OTRLGPPC,SP70
          PACK      ALRERHCT,OTRLGPPT,SP70
          PACK      ALREDEPT,MAPAHDP,SP70
          PACK      ALREUNIT,OTRLDEPT,SP70
          PACK      ALREPRTY,OTRLPRIO,SP70
          PACK      ALREPRO1,SP70
          PACK      ALREPRO2,SP70
          PACK      ALREPRO3,SP70
          PACK      ALREDIA1,SP70
          PACK      ALREDIA2,SP70
          PACK      ALREDIA3,SP70
          PACK      ALRESRCE,OTRLSRCE,SP70
          PACK      ALRECOMP,OTRLCLAM,SP70
          PACK      ALREBULK,ZERO,SP70
          PACK      ALREDREV,SP70
          PACK      ALREHCP,OTRLRHCP,SP70
          PACK      ALREDREC,OTRLDREC,SP70
          PACK      ALREUC01,OTRLUSD1,SP70
          PACK      ALREUC02,OTRLUSD2,SP70
          PACK      ALREUC03,OTRLUSD3,SP70
          PACK      ALREUC04,OTRLUSD4,SP70
          PACK      ALREUC05,SP70
          PACK      ALREUC06,SP70
          PACK      ALREUC07,SP70
          PACK      ALREUC08,SP70
          PACK      ALREUC09,SP70
          PACK      ALREUC10,SP70
          PACK      ALREUC11,SP70
          PACK      ALREUC12,SP70
          PACK      ALREUC13,SP70
          PACK      ALREUC14,SP70
          PACK      ALREUC15,SP70
          PACK      ALREUC16,SP70
          PACK      ALREUC17,SP70
          PACK      ALREUC18,SP70
          PACK      ALREUC19,SP70
          PACK      ALREUC20,SP70
          PACK      ALREUC21,SP70
          PACK      ALREUC22,SP70
          PACK      ALREUC23,SP70
          PACK      ALREUC24,SP70
          PACK      ALREUC25,SP70
          PACK      ALREUC26,SP70
          PACK      ALREUC27,SP70
          PACK      ALREUC28,SP70
          PACK      ALREUC29,SP70
          PACK      ALREUC30,SP70
          PACK      ALREUC31,SP70
          PACK      ALREUC32,SP70
          PACK      ALREUC33,SP70
          PACK      ALREUC34,SP70
          PACK      ALREUC35,SP70
          PACK      ALREUC36,SP70
          PACK      ALREUC37,SP70
          PACK      ALREUC38,SP70
          PACK      ALREUC39,SP70
          PACK      ALREUC40,SP70
          PACK      ALREUYN1,ZERO
          PACK      ALREUYN2,ZERO
          PACK      ALREUYN3,ZERO
          PACK      ALREUYN4,ZERO
          PACK      ALREUHC1,OTRLRHCP,SP70
          PACK      ALREUHC2,OTRLRFGP,SP70
          PACK      ALREUHC3,SP70
          PACK      ALREUHC4,SP70
          PACK      ALREUFR1,OTRLDIAG,SP70
          PACK      ALREUFR2,OTRLGPFH,SP70
          PACK      ALREUDT1,SP70
          PACK      ALREUTM1,SP70
          PACK      ALREUDT2,SP70
          PACK      ALREUTM2,SP70
          PACK      ALREUDT3,SP70
          PACK      ALREUTM3,SP70
          PACK      ALREUDT4,SP70
          PACK      ALREUTM4,SP70
          MOVE      ZERO,ALREUNO1
          MOVE      ZERO,ALREUNO2
          PACK      ALRECDAT,OTRLCDAT,SP70
          PACK      ALRECTIM,OTRLCTIM,SP70
          PACK      ALRECUID,OTRLCUID,SP70
          PACK      ALREUDAT,OTRLUDAT,SP70
          PACK      ALREUTIM,OTRLUTIM,SP70
          PACK      ALREUUID,OTRLUUID,SP70
          PACK      ALRECTYP,OTRLCTYP,SP70
          PACK      ALREHOSN,ALDEHOSP,SP70
          PACK      ALREFHCP,OTRLCAD1,SP70
          PACK      ALREHCP1,OTRLCAD2,SP70
          PACK      ALREHCP2,OTRLCAD3,SP70
          PACK      ALREHCP3,OTRLCAD4,SP70
          PACK      ALREHCPC,OTRLCPC,SP70
          PACK      ALREHCPS,SP70
          PACK      ALREHCPB,SP70
          PACK      ALREHCPF,SP70
          PACK      ALREHCPP,SP70
          PACK      ALRESTYP,SP70
          PACK      ALREPRCM,OTRLECRN,SP70
          PACK      ALREPSIT,OTRLSITE,SP70
          PACK      ALRECLID,OTRLCONS,SP70
          PACK      ALRECSTA,SP70
          PACK      ALRERREJ,SP70
          PACK      ALRECCOM,OTRLRQST,SP70
          PACK      ALRERTYP,OTRLSPFC,SP70
          PACK      ALREREXP,SP70
          PACK      ALREMDAT,SP70
          PACK      ALREPRDT,OTRLDPRI,SP70
          PACK      ALRESREJ,SP70
          PACK      ALRESRDT,SP70
          PACK      ALRESRTM,SP70
          PACK      ALRESRUI,SP70
          PACK      ALREUDT5,SP70
          PACK      ALREUTM5,SP70
          PACK      ALREUYN5,SP70
          PACK      ALREUDID,OTRLOPER,SP70
          PACK      ALREUDT6,SP70
          PACK      ALREUTM6,SP70
          PACK      ALREUYN6,SP70
          PACK      ALREREVD,SP70
          PACK      ALREPURC,OTRLPURC,SP70
          PACK      ALREOUTP,OTRLSPFC,SP70
          PACK      ALRECONT,SP70
          PACK      ALRESCOR,OTRLRANK,SP70
          PACK      ALREMOHR,SP70
          PACK      ALREEVPR,SP70
          PACK      ALRESPAR,SP70
.
          CALL      ARMHI000                * Set MHINC Indicator to 'unsent'
          PACK      KEY8,ALREVISN,SP70
          CALL      WRALREF1
          GOTO      WRREF999
.
WRREF900  PRINT     *1,"Error: allrefaf record already exists for ",AADMNO
          MOVE      ONE,EXIT
          GOTO      WRREF999
.
WRREF999  RETURN
+
.******************************************************************************
.         MHINC - Add Referral for MH Department and Primary Ref.
.******************************************************************************
ARMHI000  PACK      KEY5,ANSC,ANSG,ALREDEPT,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,ARMHI999
.
          MATCH     "M",TCINDC4
          GOTO      ARMHI999 IF NOT EQUAL      * Not a Mental Health department
.
          MATCH     "1",ALREUYN4
          GOTO      ARMHI999 IF NOT EQUAL      * Not Primary Referral
.
          MOVE      "0",ALREMOHR               * Unsent
.
ARMHI999  RETURN
+
.******************************************************************************
.*                                 WRVIS000                                   *
.*                            Write patvisaf record                           *
.******************************************************************************
WRVIS000  MOVE      ZERO,EXIT
          PACK      KEY8,AADMNO,SP70
          CALL      RDPTVIS1
          IF        OVRCD<>1
            GOTO      WRVIS900
          ENDIF
.
          PACK      PVIURNO,OTRLURNO,SP70
          PACK      PVIDATE,OTRLDATE,SP70 
          PACK      DPVIBILL,AADMNO,SP70
          MOVE      "9",PVITYPE
          PACK      PVIDOCT,OTRLRHCP,SP70
          MOVE      "2",PVISTAT
          MOVE      "1",PVITRAN
          PACK      PVILOCK,SP70
          PACK      PVISITE,OTRLSITE,SP70
          PACK      PVIRESI,SP70
          PACK      PVIUNQE,SP70
          MOVE      " 2",PVISYST
          MOVE      "10",PTVITYPE
          PACK      PVISPAR,SP70
.
          PACK      KEY8,DPVIBILL,SP70
          CALL      WRPTVIS1
          GOTO      WRVIS999
.         
WRVIS900  PRINT     *1,"Error: patvisaf record already exists for ",AADMNO
          MOVE      ONE,EXIT
          GOTO      WRVIS999
.         
WRVIS999  RETURN    
+ 
.******************************************************************************
.*                                 WRVX1000                                   *
.*                            Write pmsvx1af record                           *
.******************************************************************************
WRVX1000  MOVE      ZERO,EXIT
          PACK      KEY8,AADMNO,SP70
          CALL      RDPMVX11
          IF        OVRCD<>1
            GOTO      WRVX1900
          ENDIF     
.         
          PACK      PMVXVISN,AADMNO,SP70
          PACK      PMVXVSDT,OTRLDATE,SP70
          PACK      PMVXDOCA,OTRLRHCP,SP70
          PACK      PMVXDOCT,SP70
          PACK      PMVXCSNR,SP70
          PACK      PMVXPOST,SP70
          PACK      PMVXPIND,SP70
          PACK      PMVXVLOC,SP70
          PACK      PMVXCFSG,SP70
          PACK      PMVXINGP,SP70
          PACK      PMVXCPTH,SP70
          PACK      PMVXHCP1,SP70
          PACK      PMVXHCP2,SP70
          PACK      PMVXHCP3,SP70
          PACK      PMVXHCP4,SP70
          PACK      PMVXRHC1,SP70
          PACK      PMVXRH1G,SP70
          PACK      PMVXRH1C,SP70
          PACK      PMVXRHC2,SP70
          PACK      PMVXRH2G,SP70
          PACK      PMVXRH2C,SP70
          PACK      PMVXRHC3,SP70
          PACK      PMVXRH3G,SP70
          PACK      PMVXRH3C,SP70
          PACK      PMVXRHC4,SP70
          PACK      PMVXRH4G,SP70
          PACK      PMVXRH4C,SP70
          PACK      PMVXABRG,SP70
          PACK      PMVXCADM,SP70
          PACK      PMVXIRAD,SP70
          PACK      PMVXIDUS,SP70
          PACK      PMVXCRAV,SP70
          PACK      PMVXRCCT,SP70
          PACK      PMVXAPWD,SP70
          PACK      PMVXAPBD,SP70
          PACK      PMVXPOWD,SP70
          PACK      PMVXPOBD,SP70
          PACK      PMVXPSTY,SP70
          PACK      PMVXVALW,SP70
          PACK      PMVXPALW,SP70
          PACK      PMVXINDC,SP70
          PACK      PMVXMDAV,SP70
          PACK      PMVXFRMS,SP70
          PACK      PMVXCSNG,SP70
          MOVE      ZERO,PMVXPWGT
          MOVE      ZERO,PMVXPHGT
          PACK      PMVXDHWR,SP70
          PACK      PMVXEDC1,SP70
          PACK      PMVXEDC2,SP70
          PACK      PMVXEDC3,SP70
          PACK      PMVXHOME,SP70
          PACK      PMVXUDF1,SP70
          PACK      PMVXUDF2,SP70
          PACK      PMVXUDF3,SP70
          PACK      PMVXUDF4,SP70
          PACK      PMVXUDT1,SP70
          PACK      PMVXUDT2,SP70
          PACK      PMVXUDT3,SP70
          PACK      PMVXUDT4,SP70
          PACK      PMVXUDT5,SP70
          PACK      PMVXUDT6,SP70
          PACK      PMVXCDTE,OTRLCDAT,SP70
          PACK      PMVXCTIM,OTRLCTIM,SP70
          PACK      PMVXWEBC,OTRLCUID,SP70
          PACK      PMVXLUPD,OTRLUDAT,SP70
          PACK      PMVXLUPT,OTRLUTIM,SP70
          PACK      PMVXWEBU,OTRLUUID,SP70
          PACK      PMVXMHLS,SP70
          PACK      PMVXPICD,SP70
          PACK      PMVXRMOR,SP70
          PACK      PMVXASUT,SP70
          PACK      PMVXELPS,SP70
          PACK      PMVXEMPL,SP70
          PACK      PMVXFINP,SP70
          PACK      PMVXOCCP,SP70
          PACK      PMVXPALC,SP70
          PACK      PMVXPPAL,SP70
          PACK      PMVXPPPT,SP70
          PACK      PMVXPSOS,SP70
          PACK      PMVXPYSP,SP70
          PACK      PMVXQLDB,SP70
          PACK      PMVXREAD,SP70
          PACK      PMVXUDIN,SP70
          PACK      PMVXUREA,SP70
          PACK      PMVXUSAC,SP70
          PACK      PMVXUVTH,SP70
          PACK      PMVXINTR,SP70
          PACK      PMVXLNG1,SP70
          PACK      PMVXASGC,SP70
          PACK      PMVXMHOS,ALDEHOSP,SP70
          PACK      PMVXDSPD,SP70
          PACK      PMVXDSIF,SP70
          PACK      PMVXPRMI,SP70
          PACK      PMVXPRAI,SP70
          PACK      PMVXPRES,SP70
          PACK      PMVXUITR,SP70
          PACK      PMVXRPOA,SP70
          PACK      PMVXRCN1,SP70
          PACK      PMVXRCN2,SP70
          PACK      PMVXRHFU,SP70
          PACK      PMVXRDVA,SP70
          PACK      PMVXRSLO,SP70
          PACK      PMVXMLOC,SP70
          PACK      PMVXPACC,SP70
          PACK      PMVXEDU1,SP70
          PACK      PMVXEDU2,SP70
          PACK      PMVXEDU3,SP70
          PACK      PMVXPRGM,SP70
          PACK      PMVXCONF,SP70
          PACK      PMVXEOCL,SP70
          PACK      PMVXSLOC,SP70
          PACK      PMVXSCLI,SP70
          PACK      PMVXSTRA,SP70
          PACK      PMVXQASN,SP70
          PACK      PMVXPOEC,SP70
          PACK      PMVXPGID,SP70
          PACK      PMVXPILC,SP70
          PACK      PMVXSPAR,SP70
.
          PACK      KEY8,PMVXVISN,SP70
          CALL      WRPMVX11
.         
          GOTO      WRVX1999
.
WRVX1900  PRINT     *1,"Error: pmsvx1af record already exists for ",AADMNO
          MOVE      ONE,EXIT
          GOTO      WRVX1999
.         
WRVX1999  RETURN    
.******************************************************************************
.*                                 WRLNK000                                   *
.*                            Write alllnkaf record                           *
.******************************************************************************
WRLNK000  MOVE      ZERO,EXIT
          PACK      KEY16,AADMNO,OTRLOUTN,SP70
          CALL      RDALLNK1
          IF        OVRCD<>1
            GOTO      WRLNK900
          ENDIF
.
          PACK      ALLNVISN,AADMNO,SP70
          PACK      ALLNLNKV,OTRLOUTN,SP70
          PACK      ALLNSITE,OTRLBSIT,SP70
          PACK      ALLNCLIN,OTRLBCLI,SP70
          PACK      ALLNDATE,OTRLBDTE,SP70
          PACK      ALLNSTRT,OTRLBSTR,SP70
          PACK      ALLNSLOT,OTRLBSLT,SP70
          MOVE      "0",ALLNMOHR            * Set to Unsent
          MOVE      ZERO,ALLNSTAT
          PACK      ALLNSPAR,SP70
.
          PACK      KEY16,ALLNVISN,ALLNLNKV,SP70
          CALL      WRALLNK1
.
          MOVE      ONE,AUDTTYPE            * write history record
          CALL      WAALLN00
          GOTO      WRLNK999
.
WRLNK900  PRINT     *1,"Error: alllnkaf record already exists for ",AADMNO
          MOVE      ONE,EXIT
          GOTO      WRLNK999
.
WRLNK999  RETURN
+
.******************************************************************************
.*                                 WROUT000                                   *
.*                            Write to output file                            *
.******************************************************************************
WROUT000  WRITE     TMPOUTA1,SEQ;OTRLOUTN,OTRLURNO,OTRLDATE,OTRLRDOC:
                                  OTRLCONS,OTRLDEPT,OTRLPRIO,OTRLDIAG:
                                  OTRLUSD1,OTRLUSD2,OTRLUSD3,OTRLUSD4:
                                  OTRLBSIT,OTRLBCLI,OTRLBDTE,OTRLBSTR:
                                  OTRLBSLT:
                                  OTRLRFGP,OTRLGPPC,OTRLRQST,OTRLCDTE:
                                  OTRLREAS,OTRLSPFC,OTRLCAD1,OTRLCAD2:
                                  OTRLCAD3,OTRLCAD4,OTRLCPC,OTRLECRN,OTRLGPFH:
                                  OTRLGPPT,OTRLOPER,OTRLCTYP,OTRLSITE:
                                  OTRLSRCE,OTRLRHCP,OTRLDREC,OTRLDPRI,OTRLPURC:
                                  OTRLTREF,OTRLRANK:
                                  OTRLUDAT,OTRLUTIM,OTRLUUID,OTRLCDAT:
                                  OTRLCTIM,OTRLCUID,OTRLCLAM:
                                  OTRLSPRE
.
WROUT999  RETURN
+
.******************************************************************************
.*                                 SAVF0000                                   *
.*                        Save files or warn if oracle                        *
.******************************************************************************
SAVF0000  MATCH     SP70,DIM19
          GOTO      SAVF9000 IF NOT EQUAL
.
          KEYIN     *P1:13,*EL,"Do you want to save the original files (Y/N)? ":
                    ANS;
          REP       "yYnN",ANS
          CMATCH    "Y",ANS
          GOTO      SAVF1000 IF EQUAL
          CMATCH    "N",ANS
          GOTO      SAVF9999 IF EQUAL
          BEEP
          GOTO      SAVF0000
.
SAVF1000  DISPLAY   *P1:24,*EL,"Saving outrf1af",*W;
          EXECUTE   "cp `ldat outrf1af.dat` savrf1af.dat",TASKID
          EXECUTE   "cp `ldat outrf1af.idx` savrf1af.idx",TASKID
.
          DISPLAY   *P1:24,*EL,"Saving allrefaf",*W;
          EXECUTE   "cp `ldat allrefaf.dat` savrefaf.dat",TASKID
          EXECUTE   "cp `ldat allrefaf.idx` savrefaf.idx",TASKID
.
          DISPLAY   *P1:24,*EL,"Saving patvisaf",*W;
          EXECUTE   "cp `ldat patvisaf.dat` savvisaf.dat",TASKID
          EXECUTE   "cp `ldat patvisaf.idx` savvisaf.idx",TASKID
.
          DISPLAY   *P1:24,*EL,"Saving pmsvx1af",*W;
          EXECUTE   "cp `ldat pmsvx1af.dat` savvx1af.dat",TASKID
          EXECUTE   "cp `ldat pmsvx1af.idx` savvx1af.idx",TASKID
.
          DISPLAY   *P1:24,*EL,"Saving alllnkaf",*W;
          EXECUTE   "cp `ldat alllnkaf.dat` savlnkaf.dat",TASKID
          EXECUTE   "cp `ldat alllnkaf.idx` savlnkaf.idx",TASKID
.
          GOTO      SAVF9999
.
SAVF9000  DISPLAY   *P1:13,"Warning: Oracle sites should save a copy of ":
                           "outrf1af, allrefaf, patvisaf, pmsvx1af, alllnkaf"
          GOTO      SAVF9999
.
SAVF9999  RETURN
.
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       PATCODKY
          INC       GENANVIS
. 
          INC       ALLDEPIO/INC
          INC       ALLLNKIO/INC
          INC       ALLREFIO/INC
          INC       OUTRF1IO/INC
          INC       PATCODIO/INC
          INC       PATVISIO/INC
          INC       PMSVX1IO/INC

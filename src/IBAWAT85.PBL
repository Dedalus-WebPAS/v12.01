.******************************************************************************
.* System   : Waiting Lists                                                   *
.* Program  : IBAWAT85                                                        *
.* Desc.    : MULTPLE LISTING REPORT (On waiting list more than once).        *
.******************************************************************************
.* Date     : 01/12/2001                                                      *
.* Author   : Dean Cramer.                                                    *
.* Mods     :                                                                 *
. *****************************************************************************
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899                         *
.*           Recompiled as WATTR1FD changes                                   *
.******************************************************************************
.*           V10.06.01  29/07/2015  Don Nguyen        CAR 319786              *
.*                      Fixed ALRT0000 to use the correct KEY16               *
.*           V10.05.01  01/08/2015  Ania P            CAR 261630              *
.*                      Removed use of PORT for temp file naming              *
.*            V9.02.02  10/01/2002 Dean Cramer                                *
.*                      Fix where temp file truncates item count because to   *
.*                      short.                                                *
.*                      Fix Days Waiting & Since RFC Days Calculation.        *
.*            V9.02.01  27/12/2001 Dean Cramer                                *
.*                      Make so overcond on theatre procedure file returns to *
.*                      next temp read as should.                             *
.******************************************************************************
.
          INC       STD001FD
          INC       WATTR1FD/INC
          INC       WATTX1FD/INC
          INC       WATMESFD/INC
          INC       WATSUSFD/INC
          INC       PATALRFD/INC
          INC       PATMA1FD/INC
          INC       BOKRC1FD/INC
          INC       IBASEQFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
+
.***************************************************************************
.*                          VARIABLES DECLARATION                          *
.***************************************************************************
CMDLINE   DIM       30
CURDTE8   DIM       8                       * Current date (ccyymmdd)
CDYSDAYS  FORM      6                       * For CALCDAYS
CDYSFDTE  DIM       8                       * For CALCDAYS
CDYSTDTE  DIM       8                       * For CALCDAYS
CDYSYEAR  FORM      2                       * For CALCDAYS
.
DIM17A    DIM       17
DIM17B    DIM       17
DIM17C    DIM       17
DIM21     DIM       21
.
FIRST     DIM       1
.
PRDATE1   DIM       10
PRDATE2   DIM       10
.
SADATE    DIM       8
SAXDATE   DIM       8
SASTAT    DIM       1
SAALERT   DIM       1
SANOCA    FORM      4
SADAYW    FORM      4
SASRFC    FORM      4
WXCNT     FORM      2
.
. ------- Temp file for collation of data -------
.
WATTMP    DIM       10
WATTMPXX  IFILE     SQL, FIXED=11, KEY="U1-8"
.
WXURNO    DIM       8        8        1        UR No.        
DWXCNT    DIM       2        2        9        Procedure Count 
. End of Record                       11
.
PRGID     INIT      "IBAWAT85"
PRGNAM    INIT      "Multiple Listing Report"
VERSION   INIT      "V12.01.00"
.
.**************************************************************************
.*                              MAINLINE                                  *
.**************************************************************************
ML0000    CALL      INIT0000          * Initialize & open files
.
ML1000    CALL      OPTN0000          * Select option
          BRANCH    OPTION,ML2000
.
          GOTO      ML9999            * Remove temp file
.
ML2000 
          CALL      WTMP0000          * Extract data for printing
.
. ------- Printing Section -------
.
          CALL      PRIN0000          * printing
          CALL      TAIL0000
          GOTO      ML1000
.
ML9999    CALL      KILL0000          * Remove temp file
          CHAIN     PGM
.
.***************************************************************************
.*        Initialisation & Open files                                      *
.***************************************************************************
.
INIT0000  CALL      DISPHEAD
          CALL      TFILENAM
          CALL      IBACLOCK
          MOVE      XDATE,SAXDATE
          REP       " 0",SAXDATE
.
          DISPLAY   *P64:24,"wattr1af";
          OPEN      WATTR1A1,"wattr1af"
.
          DISPLAY   *P64:24,"wattx1af";
          OPEN      WATTX1A1,"wattx1af"
.
          DISPLAY   *P64:24,"watmesaf";
          OPEN      WATMESA1,"watmesaf"
.
          DISPLAY   *P64:24,"watsusaf";
          OPEN      WATSUSA1,"watsusaf"
.
          DISPLAY   *P64:24,"patalrtf";
          OPEN      PATALRT1,"patalrtf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"bokrc1af";
          OPEN      BOKRC1A6,"bokrc1af"
.
          CALL      CREA0000
          RETURN
+
.******************************************************************************
.                                 OPTN0000
.******************************************************************************
OPTN0000  DISPLAY   *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Multiple Listing Report":
                    *P1:7,"Select Option :";
OPTN1000
          KEYIN     *P17:7,*EL,*DV,UNDLN1,*P17:7,*V2LON,OPTION;
          COMPARE   ZERO,OPTION
          GOTO      OPTN9999 IF EQUAL
          BRANCH    OPTION,OPTN9999
          BEEP
          GOTO      OPTN1000
.
OPTN9999  RETURN
+
.***************************************************************************
.*        Deleting  an indexed file based on port                          *
.***************************************************************************
KILL0000  CLOSE     WATTMPXX
          PACK      CMDLINE,SP30
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    TFILNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          RETURN
+
.***************************************************************************
.*        Creating an indexed file based on port                           *
.***************************************************************************
CREA0000  CALL      KILL0000
          PACK      CMDLINE,SP30
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    TFILNAME,CMDLINE
          APPEND    " 11 u1-8",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      WATTMPXX,TFILNAME
          RETURN
+
.****************************************************************************
.                                 WTMP0000
.                                     
.****************************************************************************
WTMP0000  PACK      KEY19,SP70
          CALL      RDSTREA1
WTMP1000  CALL      RDKTREA1
          BRANCH    OVRCD,WTMP9999
.
          MATCH     "1",DWMSTAT             
          GOTO      WTMP1000 IF LESS 
.
          MATCH     "4",DWMSTAT             
          GOTO      WTMP1000 IF NOT LESS 
.
          PACK      KEY8,WMURNO,SP10
          CALL      RDTMPXX
          BRANCH    OVRCD,WTMP2000  
.
          ADD       ONE,WXCNT
          CALL      UPTMPXX
          GOTO      WTMP1000
.
WTMP2000  MOVE      WMURNO,WXURNO
          MOVE      ONE,WXCNT
          CALL      WRTMPXX
          GOTO      WTMP1000
. 
WTMP9999  RETURN
+
.****************************************************************************
.*        Drive with temp file and read relevant files for printing         *
.****************************************************************************
PRIN0000  MOVE      "80",CLNO
          MOVE      ZERO,CPAGENO
          PACK      KEY8,SP70
          CALL      RDSTMPXX
PRIN1000  CALL      RDKTMPXX
          BRANCH    OVRCD,PRIN9999
.
          COMPARE   WXCNT,ONE
          GOTO      PRIN1000 IF NOT LESS
.
          PACK      KEY19,WXURNO,SP70
          CALL      RDSTREA1
          MOVE      "0",FIRST
PRIN2000  CALL      RDKTREA1
          BRANCH    OVRCD,PRIN1000
          MATCH     WXURNO,WMURNO
          GOTO      PRIN8000 IF NOT EQUAL
.
          MATCH     "1",DWMSTAT             
          GOTO      PRIN2000 IF LESS 
.
          MATCH     "4",DWMSTAT             
          GOTO      PRIN2000 IF NOT LESS 
.
          PACK      KEY19,WMURNO,WMCODE,WMCNT
          CALL      RDWTTX11
          BRANCH    OVRCD,PRIN1000
.
          MATCH     "0",FIRST
          GOTO      PRIN3000 IF NOT EQUAL
.
          CALL      PRUR0000
          BRANCH    EXIT,PRIN1000
          CALL      LSEP0000
          MOVE      ONE,FIRST        
.
PRIN3000  MOVE      WMDATE1,SADATE
          MOVE      ANSR,SASTAT
.
          CALL      SUSP0000
.
          CALL      DAYW0000
.
          CALL      ALRT0000
.
          CALL      COMM0000
.
          CALL      NOCA0000
.
          CALL      SRFC0000
.
          CALL      PRNT0000
.
          GOTO      PRIN2000          
.
PRIN8000  CALL      LSEP0000
          GOTO      PRIN1000
.        
PRIN9999  RETURN
.****************************************************************************
.*        Print UR and Name                                                 *
.****************************************************************************
PRUR0000  PACK      KEY8,WMURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,PRUR9000
.
          MOVE      PTITL,PACTITLE
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      TWO,PACFRMT
          CALL      PACNAME                 * Pack Full Name
.
          COMPARE   "60",CLNO
          GOTO      PRUR1000 IF LESS 
          CALL      HEAD0000                 * Print header
.
PRUR1000  PRINT     *1,"UR Number : ",WMURNO,"   ",PACFNAME,"  ","Age : ",PSAGE
          MOVE      ZERO,EXIT 
          GOTO      PRUR9999
.
PRUR9000  MOVE      ONE,EXIT                                                  
.
PRUR9999  RETURN
.****************************************************************************
.*        Check for Suspension                                              *
.****************************************************************************
SUSP0000  PACK      KEY21,WMURNO,WMCODE,WMCNT,SP70
          CALL      RDWTSUS1
SUSP1000  CALL      RKWTSUS1
          BRANCH    OVRCD,SUSP9999
.
          MATCH     WMURNO,WTSUURNO
          GOTO      SUSP9999 IF NOT EQUAL
.
          MATCH     WMCODE,WTSUCODE
          GOTO      SUSP9999 IF NOT EQUAL
.
          COMPARE   WMCNT,WTSUCNT
          GOTO      SUSP9999 IF NOT EQUAL
.
          MOVE      WTSUTDAT,SADATE
          MATCH     SAXDATE,WTSUTDAT
          GOTO      SUSP1000 IF LESS
.           
          MOVE      "N",SASTAT
.
SUSP9999  RETURN
.****************************************************************************
.*        Get Days Waiting                                                  *
.****************************************************************************
DAYW0000  MOVE      ZERO,SADAYW 
          MOVE      WMDATE1,CDYSFDTE
          MOVE      XDATE,CDYSTDTE 
          CALL      CALCDAYS
          SUB       ONE,CDYSDAYS
          MOVE      CDYSDAYS,SADAYW         
DAYW9999  RETURN
.****************************************************************************
.*        Check for Alert                                                   *
.****************************************************************************
ALRT0000  PACK      SAALERT,SP10      
          PACK      KEY16,WMURNO,SP70
          CALL      RSPTALR1
ALRT1000  CALL      RKPTALR1
          BRANCH    OVRCD,SUSP9999
.
          MATCH     PTALURNO,WMURNO 
          GOTO      ALRT9999 IF NOT EQUAL
.           
          MOVE      "Y",SAALERT
.
ALRT9999  RETURN
.****************************************************************************
.*        Check for Comment                                                 *
.****************************************************************************
COMM0000  UNPACK    SP70,DIM17A,DIM17B          
          PACK      KEY21,WMURNO,WMCODE,WMCNT,SP1,ONE,SP70
          CALL      RDMESA1 
          BRANCH    OVRCD,COMM9999
.
          UNPACK    WATEXT,DIM17A,DIM17B
.
COMM9999  RETURN
.****************************************************************************
.*        Get Number of Cancelled                                           *
.****************************************************************************
NOCA0000  MOVE      ZERO,SANOCA                 
          PACK      KEY16,WMURNO,SP70
          CALL      RSBKREC6
NOCA1000  CALL      RKBKREC6
          BRANCH    OVRCD,NOCA9999
.
          MATCH     WMURNO,BKREURNO
          GOTO      NOCA9999 IF NOT EQUAL 
.
          MATCH     WMCODE,BKREPROC
          GOTO      NOCA1000 IF NOT EQUAL
.
          COMPARE   WMCNT,BKRECNT
          GOTO      NOCA1000 IF NOT EQUAL
.
          ADD       ONE,SANOCA             
          GOTO      NOCA1000       
.
NOCA9999  RETURN
.****************************************************************************
.*        Get Days Since RFC                                                *
.****************************************************************************
SRFC0000  MOVE      ZERO,SASRFC 
          MATCH     "N",SASTAT
          GOTO      SRFC9999 IF EQUAL
.
          MOVE      SADATE,CDYSFDTE
          MOVE      XDATE,CDYSTDTE 
          CALL      CALCDAYS
          SUB       ONE,CDYSDAYS
          MOVE      CDYSDAYS,SASRFC         
SRFC9999  RETURN
.****************************************************************************
.****************************************************************************
.*        Printing Subroutines                                              *
.****************************************************************************
PRNT0000  COMPARE   "60",CLNO
          GOTO      PRNT1000 IF LESS 
          CALL      HEAD0000                 * Print header
.
PRNT1000  UNPACK    SADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,PRDATE1           
.
          UNPACK    WMDATE3,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,PRDATE2           
.
          MOVE      WMREASON,DIM21
          MOVE      WMSTAY,FORM4
.
          REP       "1U2S3P",DWMSTAT
.
          PRINT     *1,"|",PRDATE1,*12,"!",WMDOCTOR,*19,"!",*21,WTTXCTYP:
                    *26,"!",*28,WMPTY,*32,"!",*34,SADAYW,*41,"!",DIM21,*64,"!":
                    *66,SASTAT:
                    *71,"!",*73,WTTXEQR1,*80,"!",DIM17A,*98,"!",*100,SANOCA:
                    *104,"!",FORM4,*109,"!",PRDATE2,*120,"!",SASRFC:
                    *126,"!",*128,DWMSTAT,*132,"!"
.
          MOVE      WMDESC,DIM21
.
          PRINT     *1,"|",*12,"!",WMUNIT,*19,"!",*26,"!":
                    *32,"!",*41,"!",DIM21,*64,"!":
                    *71,"!",*73,SAALERT,*80,"!",DIM17B,*98,"!":
                    *104,"!",*109,"!",*120,"!":
                    *126,"!",*132,"!"
          ADD       TWO,CLNO
.
PRNT9999  RETURN
.****************************************************************************
.*        Heading Print Routine                                             *
.****************************************************************************
HEAD0000  CLOCK     TIME,CTIMEIS
          CALL      HEAD132
.
          CALL      LSEP0000
.
          PRINT     *1,"|",*12,"| Cons ",*19,"! Ins. !":
                    *32,"! Days   !Prov. Diagnosis",*64,"! RFC  !Equ Req !":
                    *98,"!No.of!Est.! Prop Adm",*120,"!Since!     !"
          PRINT     *1,"|RFC Date",*12,"| Unit ",*19,"! Class! Cat":
                    *32,"! Waiting!Procedure",*64,"!Status!Alerts  ! Comments":
                    *98,"!Canc.!Stay!  Date",*120,"! RFC ! Stat!"
.
          CALL      LSEP0000
.
          MOVE      EIGHT,CLNO
HEAD9999  RETURN
.****************************************************************************
.*        Trailer Print Routine                                             *
.****************************************************************************
TAIL0000  COMPARE   "60",CLNO
          GOTO      TAIL1000 IF LESS
          CALL      HEAD0000                 * Print header
.
TAIL1000  PRINT     *N,*1,"RFC Status = N (Not Ready for Care)"
          PRINT     *12,"= R (ready for Care)"
          PRINT     *N,*1,"Status = U (Unscheduled)"
          PRINT     *8,"= S (Scheduled)"
          PRINT     *8,"= P (Pre-Admitted)"
.
          PRINT     *N,"*** End of Report ***"
          DISPLAY   *P1:24,*EL,*P28:24,*V2LON,"** Generation Completed **"
.
TAIL9999  RETURN
.****************************************************************************
.*        Printed Line Separator                                            *
.****************************************************************************
LSEP0000  PRINT     *1,"*-------------------------------------------":
                       "--------------------------------------------":
                       "-------------------------------------------*"
          ADD       ONE,CLNO
LSEP9999  RETURN
+
.****************************************************************************
.*        Temp file I/O subroutines                                         *
.****************************************************************************
RDTMPXX   MOVE      ZERO,OVRCD
          RESET     KEY8 
          READ      WATTMPXX,KEY8;WXURNO,DWXCNT  
          GOTO      OVERCOND IF OVER
          MOVE      DWXCNT,WXCNT
          RETURN
.
RDSTMPXX  MOVE      ZERO,OVRCD
          RESET     KEY8 
          READ      WATTMPXX,KEY8;;
          RETURN
.
RDKTMPXX  MOVE      ZERO,OVRCD
          READKS    WATTMPXX;WXURNO,DWXCNT                                     
          GOTO      OVERCOND IF OVER
          MOVE      DWXCNT,WXCNT
          RETURN
.
WRTMPXX   MOVE      ZERO,OVRCD
          RESET     KEY8 
          MOVE      WXCNT,DWXCNT 
          WRITE     WATTMPXX,KEY8;WXURNO,DWXCNT                        
          RETURN
.
UPTMPXX   MOVE      WXCNT,DWXCNT
          UPDATE    WATTMPXX;WXURNO,DWXCNT                                     
          RETURN
+
.****************************************************************************
.
          INC       STD001IO
          INC       CALCDAYS
          INC       WATTR1IO/INC
          INC       WATTX1IO/INC
          INC       WATMESIO/INC
          INC       WATSUSIO/INC
          INC       PATALRIO/INC
          INC       PATMA1IO/INC
          INC       BOKRC1IO/INC
          INC       TFILENAM
          INC       IBASEQIO/INC
          INC       WEBERRIO/INC

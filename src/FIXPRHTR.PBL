.***************************************************************************
.* System    :   Inpatients                                                *
.* Program   :   FIXPRHTR                                                  *
.* Desc      :   Fix program for Private practice holding file             *
.***************************************************************************
.* Date      :   13/09/2005                                                *
.* Author    :   J.Tan     CAR 74625                                       *
.* Function  :   This program will change referral date/doctor             *
.*               (prhtrdat and prhtrefd) from prihtraf to be same as       *
.*               (prhrrdat and prhrrefd) from prihreaf                     *
.*                                                                         *
.* Modifications : V9.04.01 J.Tan   CAR 74625                              *
.*                 Fixed I*U                                               *
.*                                                                         *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PRIHTRFD/INC
          INC       PRIHREFD/INC
          INC       PRIHDBFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
ERRFLAG   FORM     1
RECNUM    FORM      8                       * Record counter
CURRDATE  DIM       8                       * Current Date
SKEY54    DIM       54
.
PRGID     INIT      "FIXPRHTR"
PRGNAM    INIT      "Fix program for prihtraf"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                * Display heading
.
          MOVE      ZERO,RECNUM             * Initialize Record counter
.
          CALL      IBACLOCK                * Get current date
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          OPEN      PRIHTRA1,"prihtraf"
          OPEN      PRIHREF1,"prihreff"
          OPEN      PRIHDBT2,"prihdbtf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Fixing Referral Date"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
PROC0000  PRINT     *1,"U/R No ",*12,"Unique No",*24," HTRDATE ",*36," HRRDATE":
                    *48," HTREFD ",*60," HRREFD "
          PACK      KEY54,SP70
          CALL      RSPRHT1
PROC1000  CALL      RKPRHT1
          BRANCH    OVRCD,PROC9999
.
          ADD       ONE,RECNUM                   * Update record counter
          IF        (RECNUM%1000) = 0
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
          PACK      SKEY54,DPRHTUNQ,PRHTPRAC,PRHTDOCT,PRHTPIND,PRHTREFD,PRHTRDAT,DPRHTFLG,PRHTITMN,PRHTSUBN,DPRHTNUM
.
          PACK      KEY23,PRHTUNIQ,PRHTPRAC,PRHTDOCT,PRHTPIND,SP70
          CALL      RDPRHR1
          BRANCH    OVRCD,PROC1000
.
          COMPARE   ONE,PRHRPINV                 * Invoice to be printed?
          GOTO      PROC1000 IF NOT EQUAL
.
          MATCH     PRHRRDAT,PRHTRDAT
          GOTO      PROC3000 IF NOT EQUAL
.
          MATCH     PRHRREFD,PRHTREFD
          GOTO      PROC1000 IF EQUAL
.
PROC3000  MOVE      PRHTUNIQ,KEY8
          CALL      RDPRHD2
          IF        OVRCD=1
            MOVE      ZERO,PRHDDEBT
          ENDIF
.
          MOVE      PRHRRDAT,PRHTRDAT
          MOVE      PRHRREFD,PRHTREFD
.
          PACK      KEY54,DPRHTUNQ,PRHTPRAC,PRHTDOCT,PRHTPIND,PRHTREFD,PRHTRDAT,DPRHTFLG,PRHTITMN,PRHTSUBN,DPRHTNUM
          CALL      RDPRHT1
          COMPARE   ZERO,OVRCD
          GOTO      PROC4000 IF EQUAL    * Already exist
.
          MOVE      SKEY54,KEY54         * read the old record
          CALL      RDPRHT1
          BRANCH    OVRCD,PROC1000
MMMM
          PRINT     *1,PRHDDEBT,*12,PRHTUNIQ,*24,PRHTRDAT,*36,PRHRRDAT:
                    *48,PRHTREFD,*60,PRHRREFD
          MOVE      PRHRRDAT,PRHTRDAT
          MOVE      PRHRREFD,PRHTREFD
          CALL      UPPRHT1
.
PROC4000  MOVE      SKEY54,KEY54
          CALL      RDPRHT1
          GOTO      PROC1000
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       PRIHTRIO/INC
          INC       PRIHREIO/INC
          INC       PRIHDBIO/INC

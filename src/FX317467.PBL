.***************************************************************************
.* System    :   Inpatients                                                *
.* Program   :   FX317467                                                  *
.***************************************************************************
.* Date      :   02/06/2015                                                *
.* Author    :   Jill Parkinson                                            *
.* Function  :   This program will loop through all visit extension records*
.*               and move pmvxudf7 if it is a valid Category YF code to    *
.*               pmvxudf8                                                  *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATCODFD/INC
          INC       PMSVX1FD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
PRGID     INIT      "FX317467"
PRGNAM    INIT      "Move Category YF from pmvxudf7 to pmvxudf8"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      PATCODE1,"patcodes"
          OPEN      PMSVX1A1,"pmsvx1af"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  PACK      KEY8,SP70
          CALL      RSPMVX11
PROC1000  CALL      RKPMVX11
          BRANCH    OVRCD,PROC9999
.
          MATCH     SP70,PMVXUDF7
          GOTO      PROC1000 IF EQUAL
.
          PACK      KEY5,ANSY,ANSF,PMVXUDF7,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,PROC1000
.
          MOVE      PMVXUDF7,PMVXUDF8
          MOVE      SP70,PMVXUDF7
          CALL      UPPMVX11
.
          PRINT     *1,PMVXVISN," moved"
          GOTO      PROC1000
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       PATCODIO/INC
          INC       PMSVX1IO/INC

.*******************************************************************************
.* System         : AN-SNAP                                                    *
.* Program        : IBAHMS75.PBL                                               *
.* Name           : AN-SNAP Rehabilitation Plan 2 Report                       *
.*******************************************************************************
.* Date           : 30/05/2012                                                 *
.* Author         : Patrick Adair                                              *
.* Function       : AN-SNAP Rehabilitation Plan 2 Report                       *
.*                  Report showing the number of Patients who do not have      *
.*                  a Discharge Rehab Plan within 7 days of Discharge          *
.* Modifications  :                                                            *
.*                                                                             *
.* V10.10.01  20/06/2017 Ebon Clements  TSK 0837511                            *
.*                       Check visit date is after therapy start date EXTP1100 *
.* V10.06.01  26/08/2015 Jill Parkinson CAR 304902                             *
.*                       Changed to ignore admissions unless astat=3           *
.* V10.04.02  13/06/2014 Steve Armstrong    CAR 301639                         *
.*                       Moved call to TFILENAM into INIT0000                  *
.* V10.04.01  29/05/2013 Don Nguyen         CAR 299692                         *
.*                       Modified PRNT0000 to work with PTCNDRSM; skip patient *
.*                       if TCINDC5 = 1,3,4,5,6 or 8.                          *
.*                       Added 'GOTO OVERCOND IF OVER' in RDAWORK1.            *
.*******************************************************************************
.* V10.03.06  05/09/2013 Jill Parkinson     CAR 291233                         *
.*                       Added RDAWORK1 to stop I*D                            *
.* V10.03.05  21/08/2013 Jill Parkinson     CAR 280947                         *
.*                       Changed calculations to use programs - not visits     *
.* V10.03.04  11/06/2013 Patrick Adair                              CAR 280948 *
.*                       Corrected report descriptions to reflect discharges   *
.* V10.03.03  19/12/2012 Patrick Adair                              CAR 261630 *
.*                       Removed port number from temp file name               *
.* V10.03.02  23/11/2012 Patrick Adair                              CAR 275451 *
.*                       Changed to include/exclude day cases                  *
.* V10.03.01  18/09/2012 Patrick Adair                              CAR 273174 *
.*                       Added PATDSCH1                                        *
.* V10.03.00  11/07/2012 Patrick Adair                              CAR 257776 *
.*                       Created Report                                        *
.*******************************************************************************
.
          INC       STD001FD                     * Standard Include
.
          INC       IBACONFD/INC                 * Control File
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       PATCODFD/INC                 * Category Codes
          INC       PATCONFD/INC                 * Control File
          INC       PATFN1FD/INC                 * Patient Health Fund File
          INC       PATDSCFD/INC                 * Patient Discharge File
          INC       PATHSPFD/INC                 * Hospital File
          INC       PATMA1FD/INC                 * Patient Master File
          INC       PATMI1FD/INC                 * Patient Admission File
          INC       PATTRNFD/INC                 * Patient Transfer File
          INC       PATVISFD/INC                 * Patient Visit File
          INC       PMSUPGFD/INC                 * Program Details File
          INC       PMSVX1FD/INC                 * Patient Visit Ext File
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
.-------------------------------------------------------------------------------
.         temporary file  - Patients without Discharge Rehab Plan Date
.
WORK1     IFILE     SQL, FIXED=53, KEY="U1-8,9-16"
.
.Name     Type     Size    Physical   Start       Description
.----     ----     ----    --------   -----       -----------
XXXXURNO  DIM       8         8         1         Patient U/R Number
XXXADMNO  DIM       8         8         9         Patient Visit Number
XXXADATE  DIM       10        10       17         Date of Admission
XXXDDATE  DIM       10        10       27         Date of Discharge
XXXDSTAT  DIM       3         3        37         Discharge Status (Cat D)
XXXDDEST  DIM       3         3        40         Discharge Destination (Cat DD)
XXXTDATE  DIM       10        10       43         Therapy Start Date
.End of record                         53
.
.-------------------------------------------------------------------------------
.                           Variables
.-------------------------------------------------------------------------------
CALCLOS   FORM      6
CARECATG  DIM       2                            * category for care type
CMDLINE   DIM       50                           * Command Line
DATEFROM  DIM       8                            * Input From Date
DATETO    DIM       8                            * Input To Date
DAYCNT    FORM      2                            * Number of Days Difference
FNAME1    DIM       8                            * temporary file name
HOSPAPPR  DIM       10                           * Hospital Approval Number
HOSPCODE  DIM       3                            * Hospital Code
HOSPNAME  DIM       40                           * Hospital Name
INCLDAYC  DIM       1                            * Include Day Cases
.                                                  0 = No
.                                                  1 = Yes
TODAY     DIM       8                         * Today's Date
TOTNORHP  FORM      6                         * Total without rehab plan
NORHBLOS  FORM      6                         * Total without rehab plan,LOS>7
TOTNOF7D  FORM      6                         * Rehab plan not within 7 days
TOTRADMS  FORM      6                         * Total Rehab Programs  
TOTR7DAY  FORM      6                         * Total Rehab Programs, LOS >7
CALDAYS   FORM      4                       * For NHOSPDAY
FROMDATE  DIM       8                       * From date for NHOSPDAY
NBRDAYS   FORM      6
TODATE    DIM       8                       * To date for NHOSPDAY
.-------------------------------------------------------------------------------
.                            Constants
.-------------------------------------------------------------------------------
CATA      INIT      "A "
CATCC     INIT      "CC"
CATD      INIT      "D "
CATDD     INIT      "DD"
ONEINIT   INIT      "1"
.
PRGID     INIT      "IBAHMS75"
PRGNAM    INIT      "Rehabilitation Plan 2"
VERSION   INIT      "V12.01.00"
.
. ******************************************************************************
. *                                  MAIN0000                                  *
. *                                                                            *
. * Process Flow Control                                                       *
. ******************************************************************************
.
MAIN0000  CALL      INIT0000                     * Initialise - begin descent
.
MAIN1000  IF        IBCNMHOS=1
            DISPLAY   *P25:6,*EL:
                      *P25:7,*EL:
                      *P25:8,*EL:
                      *P25:9,*EF;
            CALL      GHOS0000                   * Get Multi Hospital Id
            BRANCH    EXIT,MAIN9999
          ELSE
            DISPLAY   *P1:6,*EL:
                      *P25:7,*EL:
                      *P25:8,*EL:
                      *P25:9,*EF;
            PACK      HOSPAPPR,CAPPRVNO,SP70     * Hospital Approval Number
            PACK      HOSPNAME,CONAME,SP70       * Hospital Name
          ENDIF
.
          CALL      GSTD0000                     * Keyin the start date
          BRANCH    EXIT,MAIN9999                * Exit if nothing input
.
          CALL      GEND0000                     * Keyin the ending date
          BRANCH    EXIT,MAIN1000                * Exit if nothing input
.
          CALL      GDAY0000                     * Keyin Include day cases
          BRANCH    EXIT,MAIN1000                * Nothing input
.
          CALL      GCON0000                     * OK to continue
          BRANCH    EXIT,MAIN1000                * restart if no selected
.
          CALL      CRET0000                     * Create temporary files
.
          CALL      EXTP0000                    * Extract Rehab patients w/out
.                                                  discharge Rehab Plan
          CALL      PRNT0000                     * Print the report
.
          CALL      KILL0000                     * Delete temporary files
.
          GOTO      MAIN1000                     * Next run
.
MAIN9999  CHAIN     PGM                          * Return from the nine circles
.
. ******************************************************************************
. *                                 INIT0000              Called by : MAIN0000 *
. *                                                                            *
. * Initialisation procedure                                                   *
. ******************************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"patfn1af";
          OPEN      PATFN1A1,"patfn1af"
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A3,"patmi1af"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA2,"patvisaf"
.
          DISPLAY   *P64:24,"pmsupgaf";
          OPEN      PMSUPGA1,"pmsupgaf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS        * 1 = Multi Hospital
.
          READ      CONTROLF,TEN;*79,CAPPRVNO         * Hospital Approval Number
.
          READ      CONTROLF,TWO;*4,CONAME            * Company Name
.
          READ      CONTROLF,TWENTY1;*45,PTCNDRSM     * Separation mode in PMS99
.                                                     *   1 = DSTAT, 2 = DDEST
.
          CALL      IBACLOCK
          PACK      TODAY,CCC,CYY,CMM,CDD
          REP       " 0",TODAY
.
          DISPLAY   *P1:6,*EL,"Hospital Id           : ";
          DISPLAY   *P1:7,"From Date             : ":
                    *P1:8,"To   Date             : ":
                    *P1:9,"Include Day Cases Y/N : ";
.
          MOVE      ZERO,TOTRADMS                * Init no of Rehab Programs
          MOVE      ZERO,TOTR7DAY                * Init no of Rehab Programs
          MOVE      ZERO,TOTNORHP                * Total without rehab plan
          MOVE      ZERO,NORHBLOS                * Total without rehab plan
          MOVE      ZERO,TOTNOF7D                * Rehab plan not within 7 days
          MOVE      ZERO,INCLDAYC                * Include Day Cases Flag
          MOVE      SEVENTY,CLNO                 * Init Line Count
          MOVE      ZERO,CPAGENO                 * Init Page Count
          CLOCK     TIME,CTIMEIS                 * Get the starting time
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAME1
.
INIT9999  RETURN
.
.*******************************************************************************
.*                                  GHOS0000               Called by: MAIN0000 *
.*                                                                             *
.* Get Hospital Id                                                             *
.*******************************************************************************
GHOS0000  MOVE      ZERO,EXIT
          DISPLAY   *P25:6,*EL:
                    *P25:7,*EL:
                    *P25:8,*EL:
                    *P25:9,*EF;
.
          MOVE      SP70,HOSPCODE
          MOVE      SP70,PTHSHOSP
          MOVE      TWENTY5,ECOL                 * Column of key in
          MOVE      SIX,EVERT                    * Row of key in
          MOVE      SP3,CKYIDEF3                 * Defualt value
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,GHOS98000,GHOS98000
.
          PACK      HOSPCODE,KEY3,SP70           * Hospital Code
          PACK      HOSPAPPR,PTHSAPPR,SP70       * Hospital Approval Number
          PACK      HOSPNAME,PTHSNAME,SP70       * Hospital Name
.
          DISPLAY   *P25:6,*V2LON,PTHSNAME;
          MOVE      ZERO,EXIT
          GOTO      GHOS9999
.
GHOS98000  MOVE     ONE,EXIT
.
GHOS9999  RETURN
.
. ******************************************************************************
. *                                  GSTD0000             Called by : MAIN0000 *
. *                                                                            *
. * Get the start  date of the report                                          *
. *            EXIT = 1   if a nothing was input                               *
. ******************************************************************************
.
GSTD0000  MOVE      ZERO,EXIT
.
          MOVE      SEVEN,CVERT                  * Line for keyin of from date
          MOVE      TWENTY5,CCOL                    * Col for keyin of from date
          MOVE      CVERT,EVERT                  * Col for error message
          MOVE      "40",ECOL                    * Col for error message
          MOVE      ZERO,CHIGHLT                 * No highlight
          MOVE      ONE,CDEFDTE
.
          UNPACK    SP6,CDAY,CMON,CYEAR          * Initialise date
          MOVE      SP8,DATEFROM
          MOVE      CCC,CCENT                    * Initialise century
.
          CALL      KEYDATE                      * Key in the date
          BRANCH    OVRCD,GSTD9000
.
          PACK      DATEFROM,CCENT,CYEAR,CMON,CDAY
          REP       " 0",DATEFROM
          MATCH     DATEFROM,TODAY
          GOTO      GSTD9999 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Date May Not be Greater than Today's ":
                    "Date.  ";
          CALL      HITENTER
          GOTO      GSTD0000
.
GSTD9000  MOVE      ONE,EXIT
.
GSTD9999  RETURN
.
. ******************************************************************************
. *                                  GEND0000             Called by : MAIN0000 *
. *                                                                            *
. * Get the end date of the report                                             *
. *            EXIT = 1   if a nothing was input                               *
. ******************************************************************************
.
GEND0000  MOVE      ZERO,EXIT
.
          MOVE      EIGHT,CVERT                  * Line for keyin of to date
          MOVE      TWENTY5,CCOL                    * Col for keyin of to date
          MOVE      CVERT,EVERT                  * Col for error message
          MOVE      "40",ECOL                    * Col for error message
          MOVE      ZERO,CHIGHLT                 * No highlight
          MOVE      ONE,CDEFDTE
.
          MOVE      SP8,DATETO
.
          CALL      KEYDATE                      * Key in the date
          BRANCH    OVRCD,GEND9000
.
          PACK      DATETO,CCENT,CYEAR,CMON,CDAY
          REP       " 0",DATETO
          MATCH     DATETO,TODAY
          GOTO      GEND2000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Date May Not be Greater than Today's ":
                    "Date.  ";
          CALL      HITENTER
          GOTO      GEND0000
.
GEND2000  MATCH     DATEFROM,DATETO
          GOTO      GEND9999 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"To Date May Not be Less than From ":
                    "Date.  ";
          CALL      HITENTER
          GOTO      GEND0000
.
GEND9000  MOVE      ONE,EXIT
.
GEND9999  RETURN
.
. ******************************************************************************
. *                                  GDAY0000             Called by : MAIN0000 *
. *                                                                            *
. * Include/Exclude Day cases from report?                                     *
. ******************************************************************************
.
GDAY0000  MOVE      ZERO,EXIT
.
GDAY1000  KEYIN     *P25:9,"_",*P25:9,*V2LON,ANS;
.
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
.
          MATCH     ANS,ANSY
          IF        @EQUAL
            MOVE      ONE,INCLDAYC
            GOTO      GDAY9999
          ENDIF
.
          MATCH     ANS,ANSN
          IF        @EQUAL
            MOVE      ZERO,INCLDAYC
            GOTO      GDAY9999
          ENDIF
.
          BEEP
          GOTO      GDAY1000
.
GDAY9999  RETURN
.
. ******************************************************************************
. *                                  GCON0000             Called by : MAIN0000 *
. *                                                                            *
. * Ok. to continue (Y/N)                                                      *
. ******************************************************************************
.
GCON0000  MOVE      ZERO,EXIT
          DISPLAY   *P1:24,*EL,"Ok to Continue (":
                    *V2LON,ANSY,*HOFF,SLASH,*V2LON,ANSN,*HOFF,") ? ";
.
GCON1000  KEYIN     *P24:24,"_",*P24:24,*V2LON,ANS;
.
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
.
          MATCH     ANS,ANSY
          GOTO      GCON9999 IF EQUAL
.
          MATCH     ANS,ANSN
          GOTO      GCON9000 IF EQUAL
.
          BEEP
          GOTO      GCON1000
.
GCON9000  MOVE      ONE,EXIT
.
GCON9999  RETURN
.
. ******************************************************************************
. *                                  CRET0000             Called by : MAIN0000 *
. *                                                                            *
. * Create the temporary files                                                 *
. ******************************************************************************
.
CRET0000  CALL      KILL0000                     * Remove any existing files
.       
          CLEAR     CMDLINE                      * Create temporary file
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME1,CMDLINE
          APPEND    " 53 U1-8,9-16",CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
          OPEN      WORK1,FNAME1                 * Open temporary file
.
CRET9999  RETURN
.
. ******************************************************************************
. *                                  KILL0000             Called by : MAIN0000 *
. *                                                                   CRET0000 *
. * Delete the temporary index files                                           *
. ******************************************************************************
.
KILL0000  CLOSE     WORK1                        * ensure temporary file closed
.
          CLEAR     CMDLINE                      * remove temporary file
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME1,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
KILL9999  RETURN
.
. ******************************************************************************
. *                                  EXTP0000             Called by : MAIN0000 *
. *                                                                            *
. * Process all programs for the period and Extract all patients without a     *
. * Discharege Rehabilitation Plan                                             *
. * Count those without a Rehab Plan within 7 days of their discharge          *
. ******************************************************************************
.
EXTP0000  DISPLAY   *P1:24,*EL,*P10:24,"Adm. No :":
                    *P58:24,"Scanning:";
.
          PACK      KEY31,SP70                   * Read thru Program Details
          CALL      RSPMUPG1
EXTP1000  CALL      RKPMUPG1
          BRANCH    OVRCD,EXTP9999
.
. ****   is the program completed? ****
          MATCH     SP70,PMUGENDD
          GOTO      EXTP1000 IF EQUAL
.
. ****   is the program completed in the selected date range?  ****
          MATCH     DATEFROM,PMUGENDD            * Check completed in range
          GOTO      EXTP1000 IF LESS
.
          MATCH     PMUGENDD,DATETO              * Check completed in range
          GOTO      EXTP1000 IF LESS
.
EXTP1100  PACK      KEY24,PMUGURNO,PMUGENDD,SP70
          CALL      RDSVISA2
EXTP2000  CALL      RDPVISA2
          BRANCH    OVRCD,EXTP1000
.
          MATCH     PVIURNO,PMUGURNO
          GOTO      EXTP1000 IF NOT EQUAL
.
          MATCH     PMUGSDAT,PVIDATE
          GOTO      EXTP1000 IF LESS
.
          MATCH     SP70,PMUGEDAT
          IF        !@EQUAL
            MATCH     PVIDATE,PMUGEDAT
            GOTO      EXTP1000 IF LESS
          ENDIF
.
          COMPARE   THREE,PVITYPE
          GOTO      EXTP2000 IF NOT EQUAL
.
          PACK      KEY8,PVIBILL
          CALL      RDPTMIS1
          BRANCH    OVRCD,EXTP2000
.
          MOVE      ZERO,CALCLOS
          MOVE      SP70,DDATE
.
          IF        ASTAT<>3
            GOTO      EXTP2000
          ENDIF
.
          PACK      KEY8,AADMNO,SP70
          CALL      RDDSCH1
          IF        OVRCD<>1
            PACK      FROMDATE,ADATE
            PACK      TODATE,DDATE
            CALL      NHOSPDAY
            MOVE      NBRDAYS,CALCLOS
          ENDIF
.
. *** are we including day cases ?
          MATCH     "1",INCLDAYC
          GOTO      EXTP2500 IF EQUAL
.
          MATCH     ADATE,DDATE
          GOTO      EXTP2000 IF EQUAL
.
EXTP2500  IF        IBCNMHOS=1
            MATCH     PMVXMHOS,HOSPCODE          * check visit is for hospital
            GOTO      EXTP2000 IF NOT EQUAL
          ENDIF
.
          MATCH     PMUGATYP,ATYPE
          GOTO      EXTP2000 IF NOT EQUAL
.
          MATCH     SP70,PMUGCLAM
          IF        !@EQUAL
            MATCH     PMUGCLAM,ACLAIM
            GOTO      EXTP2000 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,PMUGFUND
          GOTO      EXTP3000 IF EQUAL
.
          MATCH     PMUGFUND,AFUNDH
          GOTO      EXTP2000 IF NOT EQUAL
.
          PACK      KEY14,AFUNDH,AFNDTB,SP10
          CALL      RDFUND1
          BRANCH    OVRCD,EXTP2000
.
          MATCH     PMUGTABT,PTHFBCAT
          GOTO      EXTP2000 IF NOT EQUAL
.
. *** found a matching visit for this program ****
EXTP3000  ADD       ONE,TOTRADMS     * count total completed rehab programs
.
          IF        CALCLOS>6
            ADD       ONE,TOTR7DAY  * count total completed rehab LOS>7 programs
          ENDIF
.
          MATCH     SP70,PMUGRDDT
          GOTO      EXTP3100 IF EQUAL
.
          DAYSEP    DDATE,PMUGRDDT,DAYCNT    * Check if plan within 7days
          IF        DAYCNT < 8
            GOTO      EXTP1000               * get next Program Details Record
          ENDIF
.
          DISPLAY   *P20:24,*V2LON,AURNO," ",DAADMNO;
.
          ADD       ONE,TOTNOF7D             * Increment total not within 7 days
          GOTO      EXTP3200
.                                                  within 7 days of Discharge
EXTP3100  ADD       ONE,TOTNORHP             * Increment Total of no Rehab plan
          COMPARE   SEVEN,CALCLOS            * increment to no rehab, LOS>7
          IF        !@LESS
            ADD       ONE,NORHBLOS
          ENDIF
.
EXTP3200  PACK      KEY8,DAADMNO,SP70        * Read Discharge File
          CALL      RDDSCH1
          IF        OVRCD=1
            CALL      CLPATDSC
          ENDIF
.
EXTP5000  UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY  * re-format dates for printing
          CALL      PACDATE
          PACK      XXXADATE,CPCDATE,SP70
.
          UNPACK    PMUGSDAT,CCENT,CYEAR,CMON,CDAY *re-format dates for printing
          CALL      PACDATE
          PACK      XXXTDATE,CPCDATE,SP70
.
          PACK      XXXDDATE,SP70
          MATCH     DDATE,SP70
          IF        !@EQUAL
            UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            PACK      XXXDDATE,CPCDATE,SP70
          ENDIF
.
          MOVE      AURNO,XXXXURNO
          MOVE      DAADMNO,XXXADMNO
          MOVE      DSTAT,XXXDSTAT
          MOVE      DDEST,XXXDDEST
.
          PACK      KEY16,XXXXURNO,XXXADMNO,SP70
          CALL      RDAWORK1
          IF        OVRCD=1
            CALL      WRWORK1                    * Write to temporary file
          ENDIF
.
          GOTO      EXTP1000
.
EXTP9999  RETURN
.
. ******************************************************************************
. *                                  PRNT0000              Called by: MAIN0000 *
. *                                                                            *
. * Print list of patients without Discharge Rehab Plan dates                  *
. ******************************************************************************
.
PRNT0000  DISPLAY   *P1:24,*EL,*P10:24,"Printing: ";
.
          COMPARE   "60",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PACK      KEY16,SP70                   * Position temporary file
          CALL      RDSWORK1
PRNT1000  CALL      RDKWORK1                     * Get the next record
          BRANCH    OVRCD,PRNT9000               * End of file reached
.
          DISPLAY   *P20:24,*V2LON,XXXXURNO," ",XXXADMNO;
.
          COMPARE   "60",CLNO
          CALL      HEAD0000 IF NOT LESS
.
.         Separation Mode?
.           1 = DSTAT, 2 = DDEST
.
          COMPARE   ONE,PTCNDRSM
          GOTO      PRNT2000 IF EQUAL
.
          COMPARE   TWO,PTCNDRSM
          GOTO      PRNT3000 IF EQUAL
.
          GOTO      PRNT5000
.
PRNT2000  MATCH     SP70,XXXDSTAT           * Discharge Status?
          GOTO      PRNT5000 IF EQUAL
.
          PACK      KEY5,CATD,XXXDSTAT,SP70
          CALL      RDCODE1
          IF        OVRCD=0
            MOVE      ZERO,F1
            MOVE      TCINDC5,F1
            IF        F1=1 | F1=3 | F1=4 | F1=5 | F1=6 | F1=8
              GOTO      PRNT1000            * Skip patient
            ENDIF
          ENDIF
          GOTO      PRNT5000
.
PRNT3000  MATCH     SP70,XXXDDEST           * Discharge Destination?
          GOTO      PRNT5000 IF EQUAL
.
          PACK      KEY5,CATDD,XXXDDEST,SP70
          CALL      RDCODE1
          IF        OVRCD=0
            MOVE      ZERO,F1
            MOVE      TCINDC5,F1
            IF        F1=1 | F1=3 | F1=4 | F1=5 | F1=6 | F1=8
              GOTO      PRNT1000            * Skip patient
            ENDIF
          ENDIF
          GOTO      PRNT5000
.
PRNT5000  PRINT     *1,"|",*4,XXXXURNO:
                    *16,"|",*23,XXXADMNO:
                    *37,"|",*43,XXXTDATE:
                    *59,"|",*65,XXXADATE:
                    *80,"|",*86,XXXDDATE:
                    *132,"|"
          ADD       ONE,CLNO
.
          GOTO      PRNT1000                     * Get next record
.
PRNT9000  IF        CPAGENO > 0
            CALL      UND132
            PRINT     *N,*N,"*** End of Report ***"
          ENDIF
.
PRNT9999  RETURN
.
. ******************************************************************************
. *                                  HEAD0000              Called by: PRNT0000 *
. *                                                                            *
. * Print Report Heading                                                       *
. ******************************************************************************
.
HEAD0000  CALL      HEAD132                     * Standard heading
.
          UNPACK    DATEFROM,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PRINT     *N,*17,"Hospital : ",HOSPNAME
.
          PRINT     *N,*15,"Date Range : ",CPCDATE," to ";
.
          UNPACK    DATETO,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PRINT     CPCDATE,*N
.                                                * Print Total head page 1 only
          IF        CPAGENO = 1
            PRINT     *N,"Total No Of Rehab Patients ":
                         "without a Discharge Rehab Plan Date: ",*73,TOTNORHP
            PRINT     *N,"Total No Of Rehabilitation Patients ":
                         "without a Discharge Rehab Plan Date, ",*N:
                      *40,"with LOS > 7 days: ",*73,NORHBLOS
            PRINT     *N,"Total No Of Rehabilitation Patients without ":
                         "a Discharge Rehab Plan"
            PRINT     *40,"within 7 days of Discharge : ",*73,TOTNOF7D
            PRINT     *N,"Total No of Rehabilitation Programs for the ":
                         "Period  : ",*73,TOTRADMS,*N
            PRINT     *N,"Total No of Rehabilitation Programs for the ":
                         "Period with LOS > 7 days : ",*73,TOTR7DAY,*N
            ADD       FOUR,CLNO
          ENDIF
.
          CALL      UND132
          PRINT     *1,"|":
        *12,"Patients without a Rehabilitation Plan within 7 days of discharge":
                    *132,"|"
          CALL      UND132
.
          PRINT     *1,"|  U/R Number":
                    *16,"|  Admission Number":
                    *37,"|  Therapy Start Date":
                    *59,"|  Date of Admission":
                    *80,"|  Date of Discharge":
                    *132,"|"
          CALL      UND132
          ADD       FIVE,CLNO
.
HEAD9999  RETURN
.
. ******************************************************************************
. * I/O routines for temporary files                                           *
. ******************************************************************************
.
RDAWORK1  RESET     KEY16
          MOVE      ZERO,OVRCD
          READ      WORK1,KEY16;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RDSWORK1  READ      WORK1,KEY16;;
          RETURN
.
RDKWORK1  MOVE      ZERO,OVRCD
          READKS    WORK1;XXXXURNO,XXXADMNO,XXXADATE,XXXDDATE,XXXDSTAT:
                          XXXDDEST,XXXTDATE
          GOTO      OVERCOND IF OVER
          RETURN
.
RDWORK1   MOVE      ZERO,OVRCD
          READ      WORK1,KEY16;XXXXURNO,XXXADMNO,XXXADATE,XXXDDATE,XXXDSTAT:
                          XXXDDEST,XXXTDATE
          GOTO      OVERCOND IF OVER
          RETURN
.
WRWORK1   WRITE     WORK1,KEY16;XXXXURNO,XXXADMNO,XXXADATE,XXXDDATE,XXXDSTAT:
                          XXXDDEST,XXXTDATE
          RETURN
.
.*******************************************************************************
.
          INC       STD001IO                     * System Standard Routines
.
          INC       CLPATHSP                     * Clear Hospital Table variables
          INC       CLPATDSC                     * Clear Disharge Record
          INC       PATHSPKY                     * Keyin of a Hospital Id/code
          INC       TFILENAM                     * Generate Temp File Name
          INC       NHOSPDAY  
.
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       PATCODIO/INC                 * Category Codes
          INC       PATFN1IO/INC                 * Patient Discharge File
          INC       PATDSCIO/INC                 * Patient Discharge File
          INC       PATHSPIO/INC                 * Hospital File
          INC       PATMA1IO/INC                 * Patient Master File
          INC       PATMI1IO/INC                 * Patient Admission File
          INC       PATTRNIO/INC                 * Patient Transfer File
          INC       PATVISIO/INC                 * Patient Visit File
          INC       PMSUPGIO/INC                 * Program Details File
          INC       PMSVX1IO/INC                 * Patient Visit Ext File
          INC       WEBERRIO/INC                 * Web Server Error Logging

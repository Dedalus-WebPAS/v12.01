.---------------------------------------------------------------------
. Get Common System Parameters
.---------------------------------------------------------------------
. Function: This routine will retrieve the common system parameter
.           for a given id, hospital id and user id        
. Requires: CPARPARM   DIM  8  (Parameter ID)          
.           CPARMHOS   FORM 1  (Multi Hospital flag)          
.           CPARUSER   DIM  8  (Logon User ID)           
.                                                                         
. Returns : CPARERRO   FORM 1  (1 - Read failed
.                                0 - Read OK)           
.           All fields from COMPARFD
. 
. Need to include in the server the following files:
.   COMPARFD/INC
.   GTCMPATD/INC 
.   COMPARIO/INC
.   CLCOMPAR
.----------------------------------------------------------------------
. Modifications: 
.----------------------------------------------------------------------
. V12.00.01  23/10/2025  Nikitha B     TSK 0958929
.            GTCMPH00 label created to retrieve the common system parameter
.            for given Hospital id.
. V11.01.00  23/02/2021  Thanh T       TSK 0891886
.            Initial version
.----------------------------------------------------------------------
.
GTCMPA00  CALL      CLCOMPAR
          MOVE      1,CPARERRO
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      COMPARA1,"comparaf"          * File create during live
          TRAPCLR   IO                           * V11.00 release
          BRANCH    OVRCD,GTCMPA99
.
GTCMPA10  COMPARE   ZERO,CPARMHOS                * Not multi hospital
          GOTO      GTCMPA50 IF EQUAL
.
          PACK      KEY10,CPARUSER,SP70
          CALL      RDWBSE1                      * Read user id
          BRANCH    OVRCD,GTCMPA50
.
          MATCH     SP70,WBSEHOSP                * Users hospital blank
          GOTO      GTCMPA50 IF EQUAL
.
          PACK      KEY11,WBSEHOSP,CPARPARM,SP70
          CALL      RDCMPAR1                     * Read hospital parameter
          BRANCH    OVRCD,GTCMPA50               * Try default with no hospital
.
          MATCH     "0",CMPAACTV
          GOTO      GTCMPA80 IF EQUAL            * found active data 
.
GTCMPA50  PACK      KEY11,SP3,CPARPARM,SP70
          CALL      RDCMPAR1                     * Read default system parameter
          BRANCH    OVRCD,GTCMPA90
.
          MATCH     "0",CMPAACTV
          GOTO      GTCMPA80 IF EQUAL            * found active data
.
          GOTO      GTCMPA90
.
GTCMPA80  MOVE      0,CPARERRO
.
GTCMPA90  CLOSE     COMPARA1
.
GTCMPA99  RETURN
.------------------------------------------------------------------------------
. This routine will retrieve the common system parameter
. for a given hospital id.
.------------------------------------------------------------------------------
GTCMPH00  CALL      CLCOMPAR
          MOVE      1,CPARERRO
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      COMPARA1,"comparaf"          * File create during live
          TRAPCLR   IO                           * V11.00 release
          BRANCH    OVRCD,GTCMPH99
.
GTCMPH10  COMPARE   ZERO,CPARMHOS                * Not multi hospital
          GOTO      GTCMPH50 IF EQUAL
.
          MATCH     SP70,CPARHPID                * User's Visit hospital blank
          GOTO      GTCMPH50 IF EQUAL
.
          PACK      KEY11,CPARHPID,CPARPARM,SP70
          CALL      RDCMPAR1                     * Read hospital parameter
          BRANCH    OVRCD,GTCMPH50               * Try default with no hospital
.
          MATCH     "0",CMPAACTV
          GOTO      GTCMPH80 IF EQUAL
.
GTCMPH50  PACK      KEY11,SP3,CPARPARM,SP70
          CALL      RDCMPAR1                     * Read default system parameter
          BRANCH    OVRCD,GTCMPH90
.
          MATCH     "0",CMPAACTV
          GOTO      GTCMPH80 IF EQUAL            * found active data
.
          GOTO      GTCMPH90
.
GTCMPH80  MOVE      0,CPARERRO                   * Read OK
.
GTCMPH90  CLOSE     COMPARA1
.
GTCMPH99  RETURN
.------------------------------------------------------------------------------

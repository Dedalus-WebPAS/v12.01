******************************************************************************
.* System         :  Patient Billing                                          *
.* Program        :  IBAPMS53                                                 *
.* Description    :  Upload ABF Emergency Classification Records              *
.******************************************************************************
.* Date           :  10/08/2021                                               *
.* Author         :  J.Tan                                                    *
.* Function       :  Upload ABF Emergency Classification Records              *
.* Modifications  :                                                           *
.*----------------------------------------------------------------------------*
. V11.02.02  16/08/2022 Alina Bhari      TSK 0923131
.            Recompile for EMRICDPR - Emergency diagnosis maintenance keyword
.            search field by 70 character
. V11.02.01  01/06/2022  Thanh T       TSK 0906596
.            Added option 8 to upload emricdaf and emrkedef tables
. V11.01.03  29/10/2021  J.Tan         TSK 0913056
.            Mod Upload ECC value set record to write to keyword file pmsukiaf
. V11.01.02  06/09/2021  Thanh T       TSK 0905305
.            Changed to validate for numeric field with corrrect decimal size
. V11.01.01  03/09/2021  Thanh T       TSK 0905305
.            Changed to validate for numeric field with corrrect decimal size  
. V11.01.00  10/08/2021  J.Tan         TSK 0905305                           
.            20/08/2021  Thanh T       TSK 0905305                           
.            Created New upload for ABF Emergency class records  
.******************************************************************************
.
          INC       STD001FD
.
          INC       IBACONFD/INC
          INC       PATCONFD/INC
          INC       PATCODFD/INC            * Codes file
.
          INC       EMRACIFD/INC
          INC       EMRACTFD/INC
          INC       EMRACAFD/INC
          INC       EMRACPFD/INC
          INC       EMRACCFD/INC
          INC       PMSVSCFD/INC
          INC       PMSVMTFD/INC
          INC       PMSUKIFD/INC       * Key Words file for Value Set ID
          INC       EMRICDFD/INC
          INC       EMRKEDFD/INC
.
.         Temporary file for AECC Coefficients for Intercept
.         are loaded from the upload file provided by the customer (PROC0000)
.
. AECC Coefficients for Intercept Upload file
.
EMACITXT  FILE       HL7, FIXED=347      * Upload file for AECC Coeff.Intercept
.
.Name     Type      Length Physical Description
.----     ----      ------ -------- -----------
XACISUBV  DIM       30     30       1     ECDG Subcategory
XACIFDAT  DIM       8      8        31    Effective Date From (CCYYMMDD)
XACITDAT  DIM       8      8        39    Effective Date to (CCYYMMDD)
XACISUBD  DIM       256    256      47    ECDG Subcategory Description
XACICODE  DIM       30     30       303   ECDG Code
XACICINT  FORM      4.8    7        333   Coefficient for Intercept
XACICITS  FORM      4.8    7        340   Coefficient for Intcpt ECDG Subcat
XACIAFLG  DIM       1      1        347   Active/Inactive Flag
.
.End of Record      348
.
.  AECC Coefficients for Transport Mode Upload file
.
EMACTTXT  FILE       HL7, FIXED=107
.
.Name     Type      Length Physical Description
.----     ----      ------ -------- -----------
XACTCODE  DIM       30     30       1     ECDG Code
XACTFDAT  DIM       8      8        31    Date Effective From (CCYYMMDD)
XACTTDAT  DIM       8      8        39    Date Effective To (CCYYMMDD)
XACTADMF  FORM      3.6    6        47    Admitted Flag
XACTDSDC  FORM      3.6    6        53    Discharged Deceased
XACTRABA  FORM      3.6    6        59    At own Risk After Being Attended
XACTRTAH  FORM      3.6    6        65    Referral to Another Hospital
XACTABAM  FORM      3.6    6        71    Arrival by Ambulance
XACTTCT1  FORM      3.6    6        77    Triage Category 1
XACTTCT2  FORM      3.6    6        83    Triage Category 2
XACTTCT3  FORM      3.6    6        89    Triage Category 3
XACTTCT4  FORM      3.6    6        95    Triage Category 4
XACTTCT5  FORM      3.6    6        101   Triage Category 5
XACTAFLG  DIM       1      1        107   Inactive Flag
.
.End of Record      108
.
.  AECC Coefficients for Age Group Upload file
.
EMACATXT  FILE       HL7, FIXED=95
.
.Name     Type      Length Physical Description
.----     ----      ------ -------- -----------
XACACODE  DIM       30     30       1     ECDG Code
XACAFDAT  DIM       8      8        31    Effective Date From
XACATDAT  DIM       8      8        39    Effective Date To
XACAAG04  FORM      3.6    6        47    Age 0 to 4
XACAAG59  FORM      3.6    6        53    Age 5 to 9
XACAAG14  FORM      3.6    6        59    Age 10 to 14
XACAAG69  FORM      3.6    6        65    Age 15 to 69
XACAAG74  FORM      3.6    6        71    Age 70 to 74
XACAAG79  FORM      3.6    6        77    Age 75 to 79
XACAAG84  FORM      3.6    6        83    Age 80 to 84
XACAAG85  FORM      3.6    6        89    Age 85 and Over
XACAAFLG  DIM       1      1        95    Active/Inactive flag
.
.End of Record      96
.
.  AECC Coefficients for Admit Predictor Upload file
.
EMACPTXT  FILE       HL7, FIXED=89
.
.Name     Type      Length Physical Description
.----     ----      ------ -------- -----------
XACPCODE  DIM       30     30       1     ECDG Code
XACPFDAT  DIM       8      8        31    Date Effective From (CCYYMMDD)
XACPTDAT  DIM       8      8        39    Date Effective To (CCYYMMDD)
XACPAGE1  FORM      3.6    6        47    Age 0 to 14
XACPAGE2  FORM      3.6    6        53    Age 80+
XACPTCT1  FORM      3.6    6        59    Triage Category 1
XACPTCT2  FORM      3.6    6        65    Triage Category 2
XACPTCT3  FORM      3.6    6        71    Triage Category 3
XACPTCT4  FORM      3.6    6        77    Triage Category 4
XACPTCT5  FORM      3.6    6        83    Triage Category 5
XACPAFLG  DIM       1      1        89    Inactive Flag
.
.End of Record      90
.
.  AECC Coefficients for ECDG Complexity Upload file
.
EMACCTXT  FILE       HL7, FIXED=170
.
.Name     Type      Length Physical Description
.----     ----      ------ -------- -----------
XACCCODE  DIM       30     30       1     ECDG Code
XACCFDAT  DIM       8      8        31    Date Effective From (CCYYMMDD)
XACCTDAT  DIM       8      8        39    Date Effective To (CCYYMMDD)
XACCCLAS  DIM       6      6        47    AECC Class
XACCDESC  DIM       100    100      53    ECDG Description
XACCDMIN  DIM       10     10       153   Minimum Score
XACCMAXS  FORM      3.6    6        163   Maximum Score
XACCMXSI  DIM       1      1        169   Maximum Score of Infinity
XACCAFLG  DIM       1      1        170   Active/Inactive flag
.
.End of Record      171
.
.  AECC Value Set Upload file
.
PMVSCTXT  FILE       HL7, FIXED=546
.
.Name     Type      Length Physical Description
.----     ----      ------ -------- -----------
XMVCVSID  DIM       3      3        1     Value Set ID
XMVCCODE  DIM       30     30       4     Code
XMVCPTRM  DIM       256    256      34    Preferred Term
XMVCFNAM  DIM       256    256      290   Fully Specified Name
XMVCACTV  DIM       1      1        546   Active Flag
.
.End of Record      547
.
.  EMR Diagnosis to ECC Value Upload file
.
PMVMTTXT  FILE       HL7, FIXED=171
.
.Name     Type      Length Physical Description
.----     ----      ------ -------- -----------
XMVMTNAM  DIM       8      8        1     Table Name
XMVMTIDX  DIM       30     30       9     Table Index Value
XMVMMVSI  DIM       3      3        39    Map to Value Set ID
XMVMMCOD  DIM       30     30       42    Map to Code
XMVMMDES  DIM       100    100      72    Map to Description
.
.End of Record      172
.
.  EMR Diagnosis Codes Upload file
.
EMICDTXT  FILE       HL7, FIXED=169
.
.Name     Type      Length Physical Description
.----     ----      ------ -------- -----------
XEICCODE  DIM       9      9        1     EMR Code
XEICDESC  DIM       70     70       10    EMR Description
XEIC10CD  DIM       9      9        80    ICD 10 Code
XEICAFLG  DIM       1      1        89    Active Flag
XEICVEMD  DIM       9      9        90    VEMD Code
XEICKEYW  DIM       70     70       99    KEY Word
XEICMETH  DIM       1      1        169   Methamphetamine Related
.
.End of Record      170
.
. ----- Program Variables -----
.
ADDCOUNT  FORM      8
UPDCOUNT  FORM      8
ERRCOUNT  FORM      8
CMDLINE   DIM       200
WORKLINE  DIM       200
DIM2C     DIM       2
DIM2Y     DIM       2
DIM2M     DIM       2
DIM2D     DIM       2
DIM3      DIM       3
DIM4      DIM       4
DIM10     DIM       10
DIM14     DIM       14
DIM15     DIM       15
DIM30     DIM       30
DIM100    DIM       100
FORM3     FORM      3
.
CURRDATE  DIM       8             * current date
CURRCENT  FORM      2             * current century
ERRFLAG   FORM      1
ERRORMSG  DIM       50
ERRORMS1  DIM       50
FNAMEI    DIM       500
LINECNTR  FORM      8
OUTFNAME  DIM       8
PATHNAME  DIM       150
USERID    DIM       10
VALFDATE  DIM       8             * Validate from date
VALTDATE  DIM       8             * Validate to date
AUTOEDAT  DATE      8
ENDKEY38  DIM       38
ENDKEY48  DIM       48
SAVKEY10  DIM       10
SAVKEY30  DIM       30
.
MSCORE36  FORM      3.6
TEMPDATE  DIM       8
ERORFLAG  FORM      1 
ERORDESC  DIM       40
FLDNAME   DIM       40
.
DINFIELD  DIM       30
DINTLEN   FORM      2
DDECLEN   FORM      2
DNEGALLW  FORM      1 
DTOTLEN   FORM      2    
WORKFLD   DIM       30
WORKFLDI  DIM       30
WORKFLDD  DIM       30 
.
XFLD0001  DIM       30
XFLD0002  DIM       30
XFLD0003  DIM       30
XFLD0004  DIM       30
XFLD0005  DIM       30
XFLD0006  DIM       30
XFLD0007  DIM       30
XFLD0008  DIM       30
XFLD0009  DIM       30
XFLD0010  DIM       30
XFLD0011  DIM       30
XFLD0012  DIM       30
XFLD0013  DIM       30
XFLD0014  DIM       30
XFLD0015  DIM       30
.
XFLDX100  DIM       100
XFLDY100  DIM       100
XFLDX256  DIM       256
XFLDY256  DIM       256
.
CISBLANK  INIT      "Is Blank"
CINVALID  INIT      "Invalid"
CMUST01   INIT      "Must be 0 or 1"
CMUST02   INIT      "Must be 0, 1 or 2"
CMUST03   INIT      "Must be Blank, 0 or 1"
.            
HYPHEN    INIT      "-"
ALPHANUM  INIT      "a~b~c~d~e~f~g~h~i~j~k~l~m~n~o~p~q~r~s~t~u~v~w~x~y~z~A~B~C~D~E~F~G~H~I~J~K~L~M~N~O~P~Q~R~S~T~U~V~W~X~Y~Z~1~2~3~4~5~6~7~8~9~0~ ~"
.
.------------------------------------------------------------------------------
PRGID     INIT      "IBAPMS53"
PRGNAM    INIT      "Upload ABF Emergency Classification Records"
VERSION   INIT      "V12.01.00"
.------------------------------------------------------------------------------
+
.******************************************************************************
.*                                Main Module                                 *
.******************************************************************************
ML0000    CALL      INIT0000                * Initailize variables & open files
          CALL      KOPT0000                * Keyin Upload or Update option
          BRANCH    EXIT,ML9999
.
ML0100    BRANCH    COPTION,ML1000,ML1000,ML1000,ML1000,ML1000:
                            ML1000,ML1000,ML1000
          GOTO      ML9999
.
ML1000    CALL      KASC0000                * Keyin file
          BRANCH    EXIT,ML9900
.
ML1100    CALL      SCRD0000                * Display screen
.
          CALL      KUSR0000                * Keyin user id
          BRANCH    EXIT,ML0000
.
          CALL      KPTH0000                * Keyin pathname
          BRANCH    EXIT,ML0000
.
          CALL      SFLD0000
          BRANCH    EXIT,ML1100
.
          CALL      PRHD0000                * Print header
.
          CALL      CTOT0000                * Clear total vars
.
          CALL      PROC0000                * with Updating file
.
          CALL      PTOT0000                * Print report totals
.
          GOTO      ML9999
.
ML9900    CALL      HEAD132
          PRINT     *N,"Failed to open upload file. "
.
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                     Initialize Variables & Open Files                      *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen heading
.
          CALL      IBACLOCK
.
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"emraciaf";
          OPEN      EMRACIA1,"emraciaf"
.
          DISPLAY   *P64:24,"emractaf";
          OPEN      EMRACTA1,"emractaf"
.
          DISPLAY   *P64:24,"emracaaf";
          OPEN      EMRACAA1,"emracaaf"
.
          DISPLAY   *P64:24,"emracpaf";
          OPEN      EMRACPA1,"emracpaf"
.
          DISPLAY   *P64:24,"emraccaf";
          OPEN      EMRACCA1,"emraccaf"
.
          DISPLAY   *P64:24,"pmsvscaf";
          OPEN      PMSVSCA1,"pmsvscaf"
.
          DISPLAY   *P64:24,"pmsvmtaf";
          OPEN      PMSVMTA1,"pmsvmtaf"
          OPEN      PMSUKIA1,"pmsukiaf"
          OPEN      PMSUKIA2,"pmsukiaf"
.
          DISPLAY   *P64:24,"emricdaf";
          OPEN      EMRICDA1,"emricdaf"
          OPEN      EMRKEDA1,"emrkedaf"
.
          OPEN      CONTROLF,"controlf"
.
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
          MOVE      CCC,CURRCENT
.
          MOVE      SP70,ERRORMSG
          MOVE      SP70,ERRORMS1
.
INIT9999  RETURN
+
.******************************************************************************
.*                            Choose Upload or Update files                   *
.******************************************************************************
KOPT0000  MOVE      ZERO,EXIT
          MOVE      ZERO,COPTION
          DISPLAY   *P1:3,*EF:
                    *P1:3,*V2LON,"0",*HOFF," = Exit":
                    *P1:4,*V2LON,"1",*HOFF," = Upload AECC Coefficients":
                                 " for Intercept":
                    *P1:5,*V2LON,"2",*HOFF," = Upload AECC Coefficients":
                                 " for Transport Mode":
                    *P1:6,*V2LON,"3",*HOFF," = Upload AECC Coefficients":
                                 " for Age Group":
                    *P1:7,*V2LON,"4",*HOFF," = Upload AECC Coefficients":
                                 " for Admitted Predictors":
                    *P1:8,*V2LON,"5",*HOFF," = Upload AECC Coefficients":
                                 " for ECDG Complexity":
                    *P1:9,*V2LON,"6",*HOFF," = ECC Value Set Upload":
                    *P1:10,*V2LON,"7",*HOFF," = EMR Diagnosis to ECC Value":
                                 " Set ID Codes":
                    *P1:11,*V2LON,"8",*HOFF," = EMR Diagnosis Codes"
.
          DISPLAY   *P1:13,"Select Option : ";
.
KOPT1000  KEYIN     *P17:13,*V2LON,COPTION;
          BRANCH    COPTION,KOPT9999,KOPT9999,KOPT9999,KOPT9999,KOPT9999:
                            KOPT9999,KOPT9999,KOPT9999
.
          COMPARE   ZERO,COPTION
          GOTO      KOPT9000 IF EQUAL
          BEEP
          GOTO      KOPT1000
.
KOPT9000  MOVE      ONE,EXIT
KOPT9999  RETURN
+
.******************************************************************************
.*                            Keyin file                                      *
.******************************************************************************
KASC0000  MOVE      ZERO,EXIT
          PACK      FNAMEI WITH SP70,SP70,SP70
.
          IF        COPTION=1
            DISPLAY   *P20:14,*EF,"AECC Coefficients for Intercept Load File";
          ENDIF
          IF        COPTION=2
            DISPLAY   *P20:14,*EF,"AECC Coefficients for Transport Mode":
                                  " Load File";
          ENDIF
          IF        COPTION=3
            DISPLAY   *P20:14,*EF,"AECC Coefficients for Age Group Load File";
          ENDIF
          IF        COPTION=4
            DISPLAY   *P20:14,*EF,"AECC Coefficients for Admitted Predictors":
                                  " Load File";
          ENDIF
          IF        COPTION=5
            DISPLAY   *P20:14,*EF,"AECC Coefficients for ECDG Complexity":
                                  " Load File";
          ENDIF
          IF        COPTION=6
            DISPLAY   *P20:14,*EF,"ECC Value Set Load File";
          ENDIF
          IF        COPTION=7
            DISPLAY   *P20:14,*EF,"EMR Diagnosis to ECC Value Set ID Code":
                                  " Load File";
          ENDIF
          IF        COPTION=8
            DISPLAY   *P20:14,*EF,"EMR Diagnosis Code Load File";
          ENDIF
.
          KEYIN     *P1:15,*EL,"Keyin Path & File Name : ",*V2LON,FNAMEI
.
          PACK      FNAMEI,FNAMEI,SP70,SP70,SP70
.
          MATCH     SP70,FNAMEI
          GOTO      KASC9000 IF EQUAL
.
          SCAN      ".txt",FNAMEI
          IF        !@EQUAL
            SQUEEZE   FNAMEI
            ENDSET    FNAMEI
            APPEND    ".txt",FNAMEI      * add ".txt" if there is a path
            APPEND    SP70,FNAMEI
            RESET     FNAMEI
          ENDIF
          RESET     FNAMEI
.
          IF        COPTION=1
            DISPLAY   *P26:15,*V2LON,*EL,FNAMEI;
            MOVE      ZERO,OVRCD
            TRAP      OVERCOND IF IO
            OPEN      EMACITXT,FNAMEI
            TRAPCLR   IO
            BRANCH    OVRCD,KASC8000
          ENDIF
.
          IF        COPTION=2
            DISPLAY   *P26:15,*V2LON,*EL,FNAMEI;
            MOVE      ZERO,OVRCD
            TRAP      OVERCOND IF IO
            OPEN      EMACTTXT,FNAMEI
            TRAPCLR   IO
            BRANCH    OVRCD,KASC8000
          ENDIF
.
          IF        COPTION=3
            DISPLAY   *P26:15,*V2LON,*EL,FNAMEI;
            MOVE      ZERO,OVRCD
            TRAP      OVERCOND IF IO
            OPEN      EMACATXT,FNAMEI
            TRAPCLR   IO
            BRANCH    OVRCD,KASC8000
          ENDIF
.
          IF        COPTION=4
            DISPLAY   *P26:15,*V2LON,*EL,FNAMEI;
            MOVE      ZERO,OVRCD
            TRAP      OVERCOND IF IO
            OPEN      EMACPTXT,FNAMEI
            TRAPCLR   IO
            BRANCH    OVRCD,KASC8000
          ENDIF
.
          IF        COPTION=5
            DISPLAY   *P26:15,*V2LON,*EL,FNAMEI;
            MOVE      ZERO,OVRCD
            TRAP      OVERCOND IF IO
            OPEN      EMACCTXT,FNAMEI
            TRAPCLR   IO
            BRANCH    OVRCD,KASC8000
          ENDIF
.
          IF        COPTION=6
            DISPLAY   *P26:15,*V2LON,*EL,FNAMEI;
            MOVE      ZERO,OVRCD
            TRAP      OVERCOND IF IO
            OPEN      PMVSCTXT,FNAMEI
            TRAPCLR   IO
            BRANCH    OVRCD,KASC8000
          ENDIF
.
          IF        COPTION=7
            DISPLAY   *P26:15,*V2LON,*EL,FNAMEI;
            MOVE      ZERO,OVRCD
            TRAP      OVERCOND IF IO
            OPEN      PMVMTTXT,FNAMEI
            TRAPCLR   IO
            BRANCH    OVRCD,KASC8000
          ENDIF
.
          IF        COPTION=8
            DISPLAY   *P26:15,*V2LON,*EL,FNAMEI;
            MOVE      ZERO,OVRCD
            TRAP      OVERCOND IF IO
            OPEN      EMICDTXT,FNAMEI
            TRAPCLR   IO
            BRANCH    OVRCD,KASC8000
          ENDIF
.
          CALL      KOUT0000                * Keyin output file
          BRANCH    EXIT,KASC9000
.
          CALL      EXCUS000
          MOVE      ZERO,EXIT
          GOTO      KASC9999
.
KASC8000  DISPLAY   *P1:24,*EL,*B,"File not found.  ";
          CALL      HITENTER
          GOTO      KASC0000
.
KASC9000  MOVE      ONE,EXIT
.
KASC9999  RETURN
+
.******************************************************************************
.*                            Screen display                                  *
.******************************************************************************
SCRD0000  DISPLAY   *P1:17,*EF:
                    *P1:17," 1. Keyin User id      :":
                    *P1:18," 2. Keyin Path name    :";
SCRD9999  RETURN
+
.******************************************************************************
.*                     Keyin user id                                          *
.******************************************************************************
KUSR0000  KEYIN     *P26:17,*EL,*V2LON,USERID;
          PACK      USERID,USERID,SP70
          MATCH     SP70,USERID
          GOTO      KUSR9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KUSR9999
.
KUSR9500  MOVE      ONE,EXIT
KUSR9999  RETURN
+
.******************************************************************************
.*                     Keyin path name                                        *
.******************************************************************************
KPTH0000  PACK      PATHNAME,SP70,SP70,SP70
          KEYIN     *P26:18,*EL,*V2LON,PATHNAME;
          ENDSET    PATHNAME
          APPEND    SP70,PATHNAME
          APPEND    SP70,PATHNAME
          APPEND    SP70,PATHNAME
          RESET     PATHNAME
.
.         PACK      PATHNAME,PATHNAME,SP70,SP70,SP70
          MATCH     SP70,PATHNAME
          GOTO      KPTH9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KPTH9999
.
KPTH9500  MOVE      ONE,EXIT
KPTH9999  RETURN
+
.******************************************************************************
.*                     Keyin output file                                      *
.******************************************************************************
KOUT0000  DISPLAY   *P1:16,"Keyin Output file      :";
.
          KEYIN     *P26:16,*EL,*V2LON,OUTFNAME;
          PACK      OUTFNAME,OUTFNAME,SP70
          MATCH     SP70,OUTFNAME
          GOTO      KOUT9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KOUT9999
.
KOUT9500  MOVE      ONE,EXIT
KOUT9999  RETURN
+
.******************************************************************************
.*                        Select Field                                        *
.******************************************************************************
SFLD0000  CALL      MAINQST                 * Select Item, (P)ost, (C)ancel ?
.
          COMPARE   ZERO,CCITEM
          GOTO      SFLD9999 IF EQUAL
          GOTO      SFLD1000 IF LESS
.
          MOVE      ZERO,EXIT
.
          PERFORM   CCITEM,KUSR0000:             * Keyin user id
                           KPTH0000              * Keyin pathname
          BRANCH    EXIT,SFLD9999
          GOTO      SFLD0000
.
SFLD1000  MOVE      ONE,EXIT
SFLD9999  RETURN
+
.******************************************************************************
.*                        Print the selection details                         *
.******************************************************************************
PRHD0000  CALL      HEAD132
.
          BRANCH    COPTION,PRHD1000,PRHD2000,PRHD3000,PRHD4000,PRHD5000:
                            PRHD6000,PRHD7000,PRHD8000
          GOTO      PRHD9999
.
PRHD1000  PRINT     *N,"Upload AECC Coefficients for Intercept"
          PRINT     *N,"Date     : ",CDATE:
                    *N,"Time     : ",CTIMEIS:
                    *N,"User ID  : ",USERID
.
          PRINT     *N,"AECC Coefficients for Intercept Load file: ":
                    *N,"     ",PATHNAME
          CALL      UND132
.
          PRINT     *1,"| Line No.",*12,"|Error Messages",*64,"|ECDG Sub.":
                    *96,"|Eff From",*106,"|Eff To",*116,"|ECDG Code":
                    *132,"|"
          GOTO      PRHD9000
.
PRHD2000  PRINT     *N,"Upload AECC Coefficients for Transport Mode"
          PRINT     *N,"Date     : ",CDATE:
                    *N,"Time     : ",CTIMEIS:
                    *N,"User ID  : ",USERID
.
          PRINT     *N,"AECC Coefficients for Transport Mode Load file":
                    *N,"     ",PATHNAME
          CALL      UND132
.
          PRINT     *1,"| Line No.",*12,"|Error Messages",*64,"|ECDG Code":
                    *96,"|Eff From",*106,"|Eff To":
                    *132,"|"
          GOTO      PRHD9000
.
PRHD3000  PRINT     *N,"Upload AECC Coefficients for Age Group"
          PRINT     *N,"Date     : ",CDATE:
                    *N,"Time     : ",CTIMEIS:
                    *N,"User ID  : ",USERID
.
          PRINT     *N,"AECC Coefficients for Age Group Load file":
                    *N,"     ",PATHNAME
          CALL      UND132
.
          PRINT     *1,"| Line No.",*12,"|Error Messages",*64,"|ECDG Code":
                    *96,"|Eff From",*106,"|Eff To":
                    *132,"|"
          GOTO      PRHD9000
.
PRHD4000  PRINT     *N,"Upload AECC Coefficients for Admit Predictor"
          PRINT     *N,"Date     : ",CDATE:
                    *N,"Time     : ",CTIMEIS:
                    *N,"User ID  : ",USERID
.
          PRINT     *N,"AECC Coefficients for Admit Predictor Load file":
                    *N,"     ",PATHNAME
          CALL      UND132
.
          PRINT     *1,"| Line No.",*12,"|Error Messages",*64,"|ECDG Code":
                    *96,"|Eff From",*106,"|Eff To":
                    *132,"|"
          GOTO      PRHD9000
.
PRHD5000  PRINT     *N,"Upload AECC Coefficients for ECDG Complexity"
          PRINT     *N,"Date     : ",CDATE:
                    *N,"Time     : ",CTIMEIS:
                    *N,"User ID  : ",USERID
.
          PRINT     *N,"AECC Coefficients for ECDG Complexity Load file":
                    *N,"     ",PATHNAME
          CALL      UND132
.
          PRINT     *1,"| Line No.",*12,"|Error Messages",*64,"|ECDG Code":
                    *96,"|Min Score ",*108,"|Eff From",*119,"|Eff To":
                    *132,"|"
          GOTO      PRHD9000
.
PRHD6000  PRINT     *N,"Upload AECC Value Set"
          PRINT     *N,"Date     : ",CDATE:
                    *N,"Time     : ",CTIMEIS:
                    *N,"User ID  : ",USERID
.
          PRINT     *N,"ECC Value Set Load file":
                    *N,"     ",PATHNAME
          CALL      UND132
.
          PRINT     *1,"| Line No.",*12,"|Error Messages",*64,"|Set ID":
                    *72,"|Code":
                    *132,"|"
          GOTO      PRHD9000
.
PRHD7000  PRINT     *N,"Upload EMR Diagnosis to ECC Value"
          PRINT     *N,"Date     : ",CDATE:
                    *N,"Time     : ",CTIMEIS:
                    *N,"User ID  : ",USERID
.
          PRINT     *N,"EMR Diagnosis to ECC Value Load file":
                    *N,"     ",PATHNAME
          CALL      UND132
.
          PRINT     *1,"| Line No.",*12,"|Error Messages",*64,"|Table Name":
                    *76,"|Table Index Value ",*108,"|Set ID":
                    *132,"|"
          GOTO      PRHD9000
.
PRHD8000  PRINT     *N,"Upload EMR Diagnosis Codes"
          PRINT     *N,"Date     : ",CDATE:
                    *N,"Time     : ",CTIMEIS:
                    *N,"User ID  : ",USERID
.
          PRINT     *N,"EMR Diagnosis Codes Load file":
                    *N,"     ",PATHNAME
          CALL      UND132
.
          PRINT     *1,"| Line No.",*12,"|Error Messages",*64,"|EMR Code":
                    *132,"|"
          GOTO      PRHD9000
.
PRHD9000  CALL      UND132
          GOTO      PRHD9999
.
PRHD9999  RETURN
+
.******************************************************************************
.*                        Generate EMR AECC tables                            *
.******************************************************************************
PROC0000  MOVE      ZERO,LINECNTR
.
          BRANCH    COPTION,PROC1000,PROC2000,PROC3000,PROC4000,PROC5000:
                            PROC6000,PROC7000,PROC8000
          GOTO      PROC9999
.
PROC1000  CALL      PACI0000         * Process AECC Coefficients for Intercept
          GOTO      PROC9999
.
PROC2000  CALL      PACT0000         * Process AECC Coefficients for Trans Mode
          GOTO      PROC9999
.
PROC3000  CALL      PACA0000         * Process AECC Coefficients for Age Group
          GOTO      PROC9999
.
PROC4000  CALL      PACP0000         * Process AECC Coefficients for Admit Pred
          GOTO      PROC9999
.
PROC5000  CALL      PACC0000         * Process AECC Coefficients for ECDG Compl
          GOTO      PROC9999
.
PROC6000  CALL      PVSC0000         * Process AECC Value Set 
          GOTO      PROC9999
.
PROC7000  CALL      PVMT0000         * Process EMR Diagnosis to ECC Value 
          GOTO      PROC9999
.
PROC8000  CALL      EMRCD000         * Process EMR Diagnosis Code
          GOTO      PROC9999
.
PROC9999  RETURN
+
.******************************************************************************
.*                  Generate AECC Coefficients for Intercept                  *
.******************************************************************************
PACI0000  OPEN      EMACITXT,FNAMEI
.
PACI1000  READ      EMACITXT,SEQ;XFLD0001,XFLDX256,XFLD0003,XFLD0004,XFLD0005:
                                 XFLD0006,XFLD0007,XFLD0008
          GOTO      PACI9000 IF OVER
.
          PACK      XFLD0001,XFLD0001,SP70
          MATCH     SP70,XFLD0001            * ECDG Subcategory 
          GOTO      PACI2000 IF NOT EQUAL
.
          PACK      XFLD0003,XFLD0003,SP70
          MATCH     SP70,XFLD0003            * ECDG Code
          GOTO      PACI2000 IF NOT EQUAL
.
          GOTO      PACI1000
.
PACI2000  ADD       ONE,LINECNTR             * Text file line counter for error
          CALL      PFLD0000                 * pack fields with spaces
.
          CALL      VALD0000                 * Validate fields
          BRANCH    ERRFLAG,PACI1000
.
          MOVE      XFLD0001,XACISUBV        * ECDG Subcategory 
          MOVE      XFLDX256,XACISUBD        * ECDG Subcategory Description 
          MOVE      XFLD0003,XACICODE        * ECDG Code
.
          MOVE      ZERO,XACICINT
          SQUEEZE   XFLD0004
          MOVE      XFLD0004,XACICINT        * Coefficient for intercept
.
          MOVE      ZERO,XACICITS
          SQUEEZE   XFLD0005
          MOVE      XFLD0005,XACICITS        * Coeffic for Intercept ECDG Subcat
.
          PACK      XACIFDAT,XFLD0006,SP70   * effective from date
          PACK      XACITDAT,XFLD0007,SP70   * effective to date
.
          MOVE      XFLD0008,XACIAFLG        * Active/Inactive flag
.
          MOVE      XACIFDAT,VALFDATE        * Validate from date
          MOVE      XACITDAT,VALTDATE        * Validate to date
          MOVE      SP70,ENDKEY38            * Auto end date key
          PACK      KEY38,XACISUBV,XACIFDAT
          CALL      RAVL0000                 * Check for date overlap
          IF        EXIT=1
            MOVE      "Invalid Effective From/To Date (Overlap)",ERRORMSG
            CALL      PERR0000
            GOTO      PACI1000
          ENDIF
.
          CALL      CLEMRACI                 * clear fields
.
          PACK      KEY38,XACISUBV,XACIFDAT
          CALL      RDEMACI1
          IF        OVRCD=1
            CALL      SETF0000 
            ADD       ONE,ADDCOUNT           * Total records added
           MOVE      USERID,EMRAUSRC
            CALL      IBACLOCK
            PACK      EMRADATC,CCC,CYY,CMM,CDD
            REP       " 0",EMRADATC
            MOVE      CTIMEIS,EMRATIMC
            CALL      WREMACI1
          ELSE
            CALL      SETF0000
            ADD       ONE,UPDCOUNT           * Total records updated
            MOVE      USERID,EMRAUSRU
            CALL      IBACLOCK
            PACK      EMRADATU,CCC,CYY,CMM,CDD
            REP       " 0",EMRADATU
            MOVE      CTIMEIS,EMRATIMU
            CALL      UPEMACI1
          ENDIF
.
.         Check if Effective todate of previous record need to be set
.
          MATCH     SP70,ENDKEY38                * Auto end prev record
          IF        !@EQUAL
            MOVE      EMRAFDAT,AUTOEDAT          * Set end date to 1 day
            SUB       ONE,AUTOEDAT               * prior to new records start
            PACK      KEY38,ENDKEY38,SP70
            CALL      RDEMACI1
            IF        OVRCD=0
              MATCH     SP70,EMRATDAT            * blank end date
              IF        @EQUAL
                MOVE      AUTOEDAT,EMRATDAT      * set end date
                REP       " 0",EMRATDAT
                MOVE      USERID,EMRAUSRU
                CALL      IBACLOCK
                PACK      EMRADATU,CCC,CYY,CMM,CDD
                REP       " 0",EMRADATU
                MOVE      CTIMEIS,EMRATIMU
                CALL      UPEMACI1
              ENDIF
            ENDIF
          ENDIF
          GOTO      PACI1000
.
PACI9000  CLOSE     EMACITXT
PACI9999  RETURN
+
.******************************************************************************
.*                  Generate AECC Coefficients for Transport Mode             *
.******************************************************************************
PACT0000  OPEN      EMACTTXT,FNAMEI
.
PACT1000  READ      EMACTTXT,SEQ;XFLD0001,XFLD0002,XFLD0003,XFLD0004,XFLD0005:
                                 XFLD0006,XFLD0007,XFLD0008,XFLD0009,XFLD0010:
                                 XFLD0011,XFLD0012,XFLD0013,XFLD0014
          GOTO      PACT9000 IF OVER
.
          PACK      XFLD0001,XFLD0001,SP70
          MATCH     SP70,XFLD0001            * ECDG Code
          GOTO      PACT2000 IF NOT EQUAL
.
          PACK      XFLD0002,XFLD0002,SP70
          MATCH     SP70,XFLD0002            * Admitted Flag
          GOTO      PACT2000 IF NOT EQUAL
.
          GOTO      PACT1000
.
PACT2000  ADD       ONE,LINECNTR             * Text file line counter for error
          CALL      PFLD0000                 * pack fields with spaces
.
          CALL      VALD0000                 * Validate fields
          BRANCH    ERRFLAG,PACT1000
.
          MOVE      XFLD0001,XACTCODE        * ECDG Code
.
          MOVE      ZERO,XACTADMF
          SQUEEZE   XFLD0002
          MOVE      XFLD0002,XACTADMF        * Admitted Flag
.
          MOVE      ZERO,XACTDSDC
          SQUEEZE   XFLD0003
          MOVE      XFLD0003,XACTDSDC        * Discharged Deceased
.
          MOVE      ZERO,XACTRABA
          SQUEEZE   XFLD0004
          MOVE      XFLD0004,XACTRABA        * At own Risk After Being Attended
.
          MOVE      ZERO,XACTRTAH
          SQUEEZE   XFLD0005
          MOVE      XFLD0005,XACTRTAH        * Referral to Another Hospital
.
          MOVE      ZERO,XACTABAM
          SQUEEZE   XFLD0006
          MOVE      XFLD0006,XACTABAM        * Arrival by Ambulance
.
          MOVE      ZERO,XACTTCT1
          SQUEEZE   XFLD0007
          MOVE      XFLD0007,XACTTCT1        * Triage Category 1
.
          MOVE      ZERO,XACTTCT2
          SQUEEZE   XFLD0008
          MOVE      XFLD0008,XACTTCT2        * Triage Category 2
.
          MOVE      ZERO,XACTTCT3
          SQUEEZE   XFLD0009
          MOVE      XFLD0009,XACTTCT3        * Triage Category 3
.
          MOVE      ZERO,XACTTCT4
          SQUEEZE   XFLD0010
          MOVE      XFLD0010,XACTTCT4        * Triage Category 4
.
          MOVE      ZERO,XACTTCT5
          SQUEEZE   XFLD0011
          MOVE      XFLD0011,XACTTCT5        * Triage Category 5
.
          PACK      XACTFDAT,XFLD0012,SP70   * effective from date
          PACK      XACTTDAT,XFLD0013,SP70   * effective to date
.
          MOVE      XFLD0014,XACTAFLG        * Active/Inactive flag
          MATCH     SP1,XACTAFLG
          IF        @EQUAL
            MOVE      ZERO,XACTAFLG
          ENDIF         
.
          MOVE      XACTFDAT,VALFDATE        * Validate from date
          MOVE      XACTTDAT,VALTDATE        * Validate to date
          MOVE      SP70,ENDKEY38            * Auto end date key
          PACK      KEY38,XACTCODE,XACTFDAT,SP70
          CALL      REVL0000                 * Check for date overlap
          IF        EXIT=1
            MOVE      "Invalid Effective From/To Date (Overlap)",ERRORMSG
            CALL      PERR0000
            GOTO      PACT1000
          ENDIF
.
          CALL      CLEMRACT                 * clear fields
.
          PACK      KEY38,XACTCODE,XACTFDAT,SP70
          CALL      RDEMACT1
          IF        OVRCD=1
            CALL      SETF0000
            ADD       ONE,ADDCOUNT           * Total records added
            MOVE      USERID,EMATCUID
            CALL      IBACLOCK
            PACK      EMATCDAT,CCC,CYY,CMM,CDD
            REP       " 0",EMATCDAT
            MOVE      CTIMEIS,EMATCTIM
            CALL      WREMACT1
          ELSE
            CALL      SETF0000
            ADD       ONE,UPDCOUNT           * Total records updated
            MOVE      USERID,EMATUUID
            CALL      IBACLOCK
            PACK      EMATUDAT,CCC,CYY,CMM,CDD
            REP       " 0",EMATUDAT
            MOVE      CTIMEIS,EMATUTIM
            CALL      UPEMACT1
          ENDIF
.
.         Check if Effective todate of previous record need to be set
.
          MATCH     SP70,ENDKEY38                * Auto end prev record
          IF        !@EQUAL
            MOVE      EMATFDAT,AUTOEDAT          * Set end date to 1 day
            SUB       ONE,AUTOEDAT               * prior to new records start
            PACK      KEY38,ENDKEY38,SP70
            CALL      RDEMACT1
            IF        OVRCD=0
              MATCH     SP70,EMATTDAT            * blank end date
              IF        @EQUAL
                MOVE      AUTOEDAT,EMATTDAT      * set end date
                REP       " 0",EMATTDAT
                MOVE      USERID,EMATUUID
                CALL      IBACLOCK
                PACK      EMATUDAT,CCC,CYY,CMM,CDD
                REP       " 0",EMATUDAT
                MOVE      CTIMEIS,EMATUTIM
                CALL      UPEMACT1
              ENDIF
            ENDIF
          ENDIF
          GOTO      PACT1000
.
PACT9000  CLOSE     EMACTTXT
PACT9999  RETURN
.
.******************************************************************************
.*                  Generate AECC Coefficients for Age Group                  *
.******************************************************************************
PACA0000  OPEN      EMACATXT,FNAMEI
.
PACA1000  READ      EMACATXT,SEQ;XFLD0001,XFLD0002,XFLD0003,XFLD0004,XFLD0005:
                                 XFLD0006,XFLD0007,XFLD0008,XFLD0009,XFLD0010:
                                 XFLD0011,XFLD0012
          GOTO      PACA9000 IF OVER
.
          PACK      XFLD0001,XFLD0001,SP70
          MATCH     SP70,XFLD0001            * ECDG Code 
          GOTO      PACA2000 IF NOT EQUAL
.
          PACK      XFLD0002,XFLD0002,SP70
          MATCH     SP70,XFLD0002            * Age 0 to 4
          GOTO      PACA2000 IF NOT EQUAL
.
          GOTO      PACA1000
.
PACA2000  ADD       ONE,LINECNTR             * Text file line counter for error
          CALL      PFLD0000                 * pack fields with spaces
.
          CALL      VALD0000                 * Validate fields
          BRANCH    ERRFLAG,PACA1000
.
          MOVE      XFLD0001,XACACODE        * ECDG Code
.
          MOVE      ZERO,XACAAG04
          SQUEEZE   XFLD0002
          MOVE      XFLD0002,XACAAG04        * Age 0 to 4
.
          MOVE      ZERO,XACAAG59
          SQUEEZE   XFLD0003
          MOVE      XFLD0003,XACAAG59        * Age 5 to 9
.
          MOVE      ZERO,XACAAG14
          SQUEEZE   XFLD0004
          MOVE      XFLD0004,XACAAG14        * Age 10 to 14
.
          MOVE      ZERO,XACAAG69
          SQUEEZE   XFLD0005
          MOVE      XFLD0005,XACAAG69        * Age 15 to 69
.
          MOVE      ZERO,XACAAG74
          SQUEEZE   XFLD0006
          MOVE      XFLD0006,XACAAG74        * Age 70 to 74
.
          MOVE      ZERO,XACAAG79
          SQUEEZE   XFLD0007
          MOVE      XFLD0007,XACAAG79        * Age 75 to 79
.
          MOVE      ZERO,XACAAG84
          SQUEEZE   XFLD0008
          MOVE      XFLD0008,XACAAG84        * Age 80 to 84
.
          MOVE      ZERO,XACAAG85
          SQUEEZE   XFLD0009
          MOVE      XFLD0009,XACAAG85        * Age 85 and Over
.
          PACK      XACAFDAT,XFLD0010,SP70   * effective from date
          PACK      XACATDAT,XFLD0011,SP70   * effective to date
.
          MOVE      XFLD0012,XACAAFLG        * Active/Inactive flag
.
          MOVE      XACAFDAT,VALFDATE        * Validate from date
          MOVE      XACATDAT,VALTDATE        * Validate to date
          MOVE      SP70,ENDKEY38            * Auto end date key
          PACK      KEY38,XACACODE,XACAFDAT,SP70
          CALL      RIVL0000                 * Check for date overlap
          IF        EXIT=1
            MOVE      "Invalid Effective From/To Date (Overlap)",ERRORMSG
            CALL      PERR0000
            GOTO      PACA1000
          ENDIF
.
          CALL      CLEMRACA                 * clear fields
.
          PACK      KEY38,XACACODE,XACAFDAT,SP70
          CALL      RDEMACA1
          IF        OVRCD=1
            CALL      SETF0000
            ADD       ONE,ADDCOUNT           * Total records added
            MOVE      USERID,EMAACUID
            CALL      IBACLOCK
            PACK      EMAACDAT,CCC,CYY,CMM,CDD
            REP       " 0",EMAACDAT
            MOVE      CTIMEIS,EMAACTIM
            CALL      WREMACA1
          ELSE
            CALL      SETF0000
            ADD       ONE,UPDCOUNT           * Total records updated
            MOVE      USERID,EMAAUUID
            CALL      IBACLOCK
            PACK      EMAAUDAT,CCC,CYY,CMM,CDD
            REP       " 0",EMAAUDAT
            MOVE      CTIMEIS,EMAAUTIM
            CALL      UPEMACA1
          ENDIF
.
.         Check if Effective todate of previous record need to be set
.
          MATCH     SP70,ENDKEY38                * Auto end prev record
          IF        !@EQUAL
            MOVE      EMAAFDAT,AUTOEDAT          * Set end date to 1 day
            SUB       ONE,AUTOEDAT               * prior to new records start
            PACK      KEY38,ENDKEY38,SP70
            CALL      RDEMACA1
            IF        OVRCD=0
              MATCH     SP70,EMAATDAT            * blank end date
              IF        @EQUAL
                MOVE      AUTOEDAT,EMAATDAT      * set end date
                REP       " 0",EMAATDAT
                MOVE      USERID,EMAAUUID
                CALL      IBACLOCK
                PACK      EMAAUDAT,CCC,CYY,CMM,CDD
                REP       " 0",EMAAUDAT
                MOVE      CTIMEIS,EMAAUTIM
                CALL      UPEMACA1
              ENDIF
            ENDIF
          ENDIF
          GOTO      PACA1000
.
PACA9000  CLOSE     EMACATXT
PACA9999  RETURN
.
.******************************************************************************
.*                  Generate AECC Coefficients for Admit Predictor            *
.******************************************************************************
PACP0000  OPEN      EMACPTXT,FNAMEI
.
PACP1000  READ      EMACPTXT,SEQ;XFLD0001,XFLD0002,XFLD0003,XFLD0004,XFLD0005:
                                 XFLD0006,XFLD0007,XFLD0008,XFLD0009,XFLD0010:
                                 XFLD0011
          GOTO      PACP9000 IF OVER
.
          PACK      XFLD0001,XFLD0001,SP70
          MATCH     SP70,XFLD0001            * ECDG Code 
          GOTO      PACP2000 IF NOT EQUAL
.
          PACK      XFLD0002,XFLD0002,SP70   
          MATCH     SP70,XFLD0002            * Age 0 to 14
          GOTO      PACP2000 IF NOT EQUAL
.
          GOTO      PACP1000
.
PACP2000  ADD       ONE,LINECNTR             * Text file line counter for error
          CALL      PFLD0000                 * pack fields with spaces
.
          CALL      VALD0000                 * Validate fields
          BRANCH    ERRFLAG,PACP1000
.
          MOVE      XFLD0001,XACPCODE        * ECDG Code
.
          MOVE      ZERO,XACPAGE1
          SQUEEZE   XFLD0002
          MOVE      XFLD0002,XACPAGE1        * Age 0 to 14
.
          MOVE      ZERO,XACPAGE2
          SQUEEZE   XFLD0003
          MOVE      XFLD0003,XACPAGE2        * Age 80+
.
          MOVE      ZERO,XACPTCT1
          SQUEEZE   XFLD0004
          MOVE      XFLD0004,XACPTCT1        * Triage Category 1
.
          MOVE      ZERO,XACPTCT2
          SQUEEZE   XFLD0005
          MOVE      XFLD0005,XACPTCT2        * Triage Category 2
.
          MOVE      ZERO,XACPTCT3
          SQUEEZE   XFLD0006
          MOVE      XFLD0006,XACPTCT3        * Triage Category 3
.
          MOVE      ZERO,XACPTCT4
          SQUEEZE   XFLD0007
          MOVE      XFLD0007,XACPTCT4        * Triage Category 4
.
          MOVE      ZERO,XACPTCT5
          SQUEEZE   XFLD0008
          MOVE      XFLD0008,XACPTCT5        * Triage Category 5
.
          PACK      XACPFDAT,XFLD0009,SP70   * effective from date
          PACK      XACPTDAT,XFLD0010,SP70   * effective to date
.
          MOVE      XFLD0011,XACPAFLG        * Active/Inactive flag
.
          MOVE      XACPFDAT,VALFDATE        * Validate from date
          MOVE      XACPTDAT,VALTDATE        * Validate to date
          MOVE      SP70,ENDKEY38            * Auto end date key
          PACK      KEY38,XACPCODE,XACPFDAT,SP70
          CALL      RPVL0000                 * Check for date overlap
          IF        EXIT=1
            MOVE      "Invalid Effective From/To Date (Overlap)",ERRORMSG
            CALL      PERR0000
            GOTO      PACP1000
          ENDIF
.
          CALL      CLEMRACP                 * clear fields
.
          PACK      KEY38,XACPCODE,XACPFDAT,SP70
          CALL      RDEMACP1
          IF        OVRCD=1
            CALL      SETF0000
            ADD       ONE,ADDCOUNT           * Total records added
            MOVE      USERID,EMAPCUID
            CALL      IBACLOCK
            PACK      EMAPCDAT,CCC,CYY,CMM,CDD
            REP       " 0",EMAPCDAT
            MOVE      CTIMEIS,EMAPCTIM
            CALL      WREMACP1
          ELSE
            CALL      SETF0000
            ADD       ONE,UPDCOUNT           * Total records updated
            MOVE      USERID,EMAPUUID
            CALL      IBACLOCK
            PACK      EMAPUDAT,CCC,CYY,CMM,CDD
            REP       " 0",EMAPUDAT
            MOVE      CTIMEIS,EMAPUTIM
            CALL      UPEMACP1
          ENDIF
.
.         Check if Effective todate of previous record need to be set
.
          MATCH     SP70,ENDKEY38                * Auto end prev record
          IF        !@EQUAL
            MOVE      EMAPFDAT,AUTOEDAT          * Set end date to 1 day
            SUB       ONE,AUTOEDAT               * prior to new records start
            PACK      KEY38,ENDKEY38,SP70
            CALL      RDEMACP1
            IF        OVRCD=0
              MATCH     SP70,EMAPTDAT            * blank end date
              IF        @EQUAL
                MOVE      AUTOEDAT,EMAPTDAT      * set end date
                REP       " 0",EMAPTDAT
                MOVE      USERID,EMAPUUID
                CALL      IBACLOCK
                PACK      EMAPUDAT,CCC,CYY,CMM,CDD
                REP       " 0",EMAPUDAT
                MOVE      CTIMEIS,EMAPUTIM
                CALL      UPEMACP1
              ENDIF
            ENDIF
          ENDIF
          GOTO      PACP1000
.
PACP9000  CLOSE     EMACPTXT
PACP9999  RETURN
.
.******************************************************************************
.*                  Generate AECC Coefficients for ECDG Complexity            *
.******************************************************************************
PACC0000  OPEN      EMACCTXT,FNAMEI
.
PACC1000  READ      EMACCTXT,SEQ;XFLD0001,XFLDX100,XFLD0003,XFLD0004,XFLD0005:
                                 XFLD0006,XFLD0007,XFLD0008,XFLD0009
          GOTO      PACC9000 IF OVER
.
          PACK      XFLD0001,XFLD0001,SP70
          MATCH     SP70,XFLD0001            * ECDG Code 
          GOTO      PACC2000 IF NOT EQUAL
.
          PACK      XFLDX100,XFLDX100,SP70   * ECDG Description
          PACK      DIM100,SP70,SP70 
          MATCH     DIM100,XFLDX100
          GOTO      PACC2000 IF NOT EQUAL
.
          GOTO      PACC1000 IF EQUAL
.
PACC2000  ADD       ONE,LINECNTR             * Text file line counter for error
          CALL      PFLD0000                 * pack fields with spaces
.
          CALL      VALD0000                 * Validate fields
          BRANCH    ERRFLAG,PACC1000
.
          MOVE      XFLD0001,XACCCODE        * ECDG Code
.
          MOVE      XFLDX100,XACCDESC        * ECDG Description
          MOVE      XFLD0003,XACCCLAS        * AECC Class 
.
          MOVE      ZERO,MSCORE36
          SQUEEZE   XFLD0004
          MOVE      XFLD0004,MSCORE36
          MOVE      MSCORE36,XACCDMIN        * Minimum Score
.
          MOVE      ZERO,XACCMAXS
          SQUEEZE   XFLD0005
          MOVE      XFLD0005,XACCMAXS        * Maximum Score 
.
          MOVE      XFLD0006,XACCMXSI        * Maximum Score of Infinity
.
          PACK      XACCFDAT,XFLD0007,SP70   * effective from date
          PACK      XACCTDAT,XFLD0008,SP70   * effective to date
.
          MOVE      XFLD0009,XACCAFLG        * Active/Inactive flag
.
          MOVE      XACCFDAT,VALFDATE        * Validate from date
          MOVE      XACCTDAT,VALTDATE        * Validate to date
          MOVE      SP70,ENDKEY48            * Auto end date key
          PACK      KEY48,XACCCODE,XACCDMIN,XACCFDAT,SP70
          CALL      RRVL0000                 * Check for date overlap
          IF        EXIT=1
            MOVE      "Invalid Effective From/To Date (Overlap)",ERRORMSG
            CALL      PERR0000
            GOTO      PACC1000
          ENDIF
.
          CALL      CLEMRACC                 * clear fields
.
PACC5000  PACK      KEY48,XACCCODE,XACCDMIN,XACCFDAT,SP70
          CALL      RDEMACC1
          IF        OVRCD=1
            CALL      SETF0000
            ADD       ONE,ADDCOUNT           * Total records added
            MOVE      USERID,EMACCUID
            CALL      IBACLOCK
            PACK      EMACCDAT,CCC,CYY,CMM,CDD
            REP       " 0",EMACCDAT
            MOVE      CTIMEIS,EMACCTIM
            CALL      WREMACC1
          ELSE
            CALL      SETF0000
            ADD       ONE,UPDCOUNT           * Total records updated
            MOVE      USERID,EMACUUID
            CALL      IBACLOCK
            PACK      EMACUDAT,CCC,CYY,CMM,CDD
            REP       " 0",EMACUDAT
            MOVE      CTIMEIS,EMACUTIM
            CALL      UPEMACC1
          ENDIF
.
.         Check if Effective todate of previous record need to be set
.
          MATCH     SP70,ENDKEY48                * Auto end prev record
          IF        !@EQUAL
            MOVE      EMACFDAT,AUTOEDAT          * Set end date to 1 day
            SUB       ONE,AUTOEDAT               * prior to new records start
            PACK      KEY48,ENDKEY48,SP70
            CALL      RDEMACC1
            IF        OVRCD=0
              MATCH     SP70,EMACTDAT            * blank end date
              IF        @EQUAL
                MOVE      AUTOEDAT,EMACTDAT      * set end date
                REP       " 0",EMACTDAT
                MOVE      USERID,EMACUUID
                CALL      IBACLOCK
                PACK      EMACUDAT,CCC,CYY,CMM,CDD
                REP       " 0",EMACUDAT
                MOVE      CTIMEIS,EMACUTIM
                CALL      UPEMACC1
              ENDIF
            ENDIF
          ENDIF
          GOTO      PACC1000
.
PACC9000  CLOSE     EMACCTXT
PACC9999  RETURN
.
.******************************************************************************
.*                  Generate AECC Value Set                                   *
.******************************************************************************
PVSC0000  OPEN      PMVSCTXT,FNAMEI
.
PVSC1000  READ      PMVSCTXT,SEQ;XFLD0001,XFLD0002,XFLDX256,XFLDY256,XFLD0005
          GOTO      PVSC9000 IF OVER
.
          PACK      XFLD0001,XFLD0001,SP70
          MATCH     SP70,XFLD0001            * Value Set ID
          GOTO      PVSC2000 IF NOT EQUAL
.
          PACK      XFLD0002,XFLD0002,SP70
          MATCH     SP70,XFLD0002            * Code
          GOTO      PVSC2000 IF NOT EQUAL
.
          GOTO      PVSC1000
.
PVSC2000  ADD       ONE,LINECNTR             * Text file line counter for error
          CALL      PFLD0000                 * pack fields with spaces
.
          CALL      VALD0000                 * Validate fields
          BRANCH    ERRFLAG,PVSC1000
.
          MOVE      XFLD0001,XMVCVSID        * Value Set ID
          MOVE      XFLD0002,XMVCCODE        * Code
          MOVE      XFLDX256,XMVCPTRM        * Preferred Term
          MOVE      XFLDY256,XMVCFNAM        * Fully Specified Name
          MOVE      XFLD0005,XMVCACTV        * Active/Inactive flag
.
PVSC5000  PACK      KEY33,XMVCVSID,XMVCCODE,SP70
          CALL      RDPMVSC1
          IF        OVRCD=1
            CALL      SETF0000
            ADD       ONE,ADDCOUNT           * Total records added
            CALL      WRPMVSC1
          ELSE
            CALL      SETF0000
            ADD       ONE,UPDCOUNT           * Total records updated
            CALL      UPPMVSC1
          ENDIF
          PROC      PMSUKIUP      * Updates keyword index
.
          GOTO      PVSC1000
.
PVSC9000  CLOSE     PMVSCTXT
PVSC9999  RETURN
.
.******************************************************************************
.*                  EMR Diagnosis to ECC Value                                *
.******************************************************************************
PVMT0000  OPEN      PMVMTTXT,FNAMEI
.
PVMT1000  READ      PMVMTTXT,SEQ;XFLD0001,XFLD0002,XFLD0003,XFLD0004,XFLDX100
          GOTO      PVMT9000 IF OVER
.
          PACK      XFLD0001,XFLD0001,SP70
          MATCH     SP70,XFLD0001            * Table Name
          GOTO      PVMT2000 IF NOT EQUAL
.
          PACK      XFLD0002,XFLD0002,SP70
          MATCH     SP70,XFLD0002            * Table Index Value
          GOTO      PVMT2000 IF NOT EQUAL
.
          PACK      XFLD0003,XFLD0003,SP70
          MATCH     SP70,XFLD0003            * Map to Value Set ID
          GOTO      PVMT2000 IF NOT EQUAL
.
          GOTO      PVMT1000
.
PVMT2000  ADD       ONE,LINECNTR             * Text file line counter for error
          CALL      PFLD0000                 * pack fields with spaces
.
          CALL      VALD0000                 * Validate fields
          BRANCH    ERRFLAG,PVMT1000
.
          MOVE      XFLD0001,XMVMTNAM        * Table Name
          MOVE      XFLD0002,XMVMTIDX        * Table Index Value
          MOVE      XFLD0003,XMVMMVSI        * Map to Value Set ID
          MOVE      XFLD0004,XMVMMCOD        * Map to Code
          MOVE      XFLDX100,XMVMMDES        * Map to Description
.
PVMT5000  PACK      KEY41,XMVMTNAM,XMVMTIDX,XMVMMVSI,SP70
          CALL      RDPMVMT1
          IF        OVRCD=1
            CALL      SETF0000
            ADD       ONE,ADDCOUNT           * Total records added
            CALL      WRPMVMT1
          ELSE
            CALL      SETF0000
            ADD       ONE,UPDCOUNT           * Total records updated
            CALL      UPPMVMT1
          ENDIF
.
          GOTO      PVMT1000
.
PVMT9000  CLOSE     PMVMTTXT
PVMT9999  RETURN
.
.******************************************************************************
.*                  EMR Diagnosis Code                                        *
.******************************************************************************
EMRCD000  OPEN      EMICDTXT,FNAMEI
.
EMRCD100  READ      EMICDTXT,SEQ;XFLD0001,XFLDX100,XFLD0002,XFLD0003,XFLD0004:
                                 XFLDY100,XFLD0005
          GOTO      EMRCD900 IF OVER
.
          PACK      XFLD0001,XFLD0001,SP70
          MATCH     SP70,XFLD0001            *  EMR Code
          GOTO      EMRCD200 IF NOT EQUAL
.
          PACK      XFLDX100,XFLDX100,SP70,SP70
          MATCH     SP70,XFLDX100           * EMR Description
          GOTO      EMRCD200 IF NOT EQUAL
.
          GOTO      EMRCD100
.
EMRCD200  ADD       ONE,LINECNTR             * Text file line counter for error
          CALL      PFLD0000                 * pack fields with spaces
.
          CALL      VALD0000                 * Validate fields
          BRANCH    ERRFLAG,EMRCD100
.
          MOVE      XFLD0001,XEICCODE        * EMR Code
          MOVE      XFLDX100,XEICDESC        * EMR Description
          MOVE      XFLD0002,XEIC10CD        * ICD 10 Code
          MOVE      XFLD0003,XEICAFLG        * Active Flag
          MOVE      XFLD0004,XMVMMCOD        * VEMD Code
          MOVE      XFLDY100,XEICKEYW        * KEY Word
          MOVE      XFLD0005,XEICMETH        * Methamphetamine Related
.
EMRCD500  PACK      KEY9,XEICCODE,SP70
          CALL      RDEMICD1
          IF        OVRCD=1
            CALL      SETF0000
            ADD       ONE,ADDCOUNT           * Total records added
            CALL      WREMICD1
          ELSE
            CALL      SETF0000
            ADD       ONE,UPDCOUNT           * Total records updated
            CALL      UPEMICD1
          ENDIF
.
          PROC      EMRICDUP                * Update Key Word Index
.
          GOTO      EMRCD100
.
EMRCD900  CLOSE     EMICDTXT
EMRCD999  RETURN
+
.******************************************************************************
. Set fields    
.******************************************************************************
SETF0000  BRANCH   COPTION,SETF1000,SETF2000,SETF30000,SETF4000,SETF5000:
                           SETF6000,SETF7000,SETF8000
          GOTO     SETF9999
.
SETF1000  CALL     SFACI000
          GOTO     SETF9999
.
SETF2000  CALL     SFACT000
          GOTO     SETF9999
.
SETF3000  CALL     SFACA000
          GOTO     SETF9999
.
SETF4000  CALL     SFACP000
          GOTO     SETF9999
.
SETF5000  CALL     SFACC000
          GOTO     SETF9999
.
SETF6000  CALL     SFVSC000
          GOTO     SETF9999
.
SETF7000  CALL     SFVMT000
          GOTO     SETF9999
.
SETF8000  CALL     SEMCD000
          GOTO     SETF9999
.
SETF9999  RETURN
.
.******************************************************************************
. Set fields - AECC Coefficients for Intercept
.******************************************************************************
SFACI000  MOVE     XACISUBV,EMRASUBV   * ECDG Subcategory
          MOVE     XACIFDAT,EMRAFDAT   * Effective Date From (CCYYMMDD)
          MOVE     XACITDAT,EMRATDAT   * Effective Date to (CCYYMMDD)
          MOVE     XACISUBD,EMRASUBD   * ECDG Subcategory Description
          MOVE     XACICODE,EMRACODE   * ECDG Code
          MOVE     XACICINT,EMRACINT   * Coefficient for Intercept
          MOVE     XACICITS,EMRACITS   * Coefficient for Intcpt ECDG Subcat
          MOVE     XACIAFLG,EMRAAFLG   * Active/Inactive Flag
.
SFACI999  RETURN
.
.******************************************************************************
. Set fields - AECC Coefficients for Transport Mode
.******************************************************************************
SFACT000  MOVE     XACTCODE,EMATCODE   * ECDG Code
          MOVE     XACTFDAT,EMATFDAT   * Effective Date From (CCYYMMDD)
          MOVE     XACTTDAT,EMATTDAT   * Effective Date to (CCYYMMDD)
          MOVE     XACTADMF,EMATADMF   * Admitted Flag
          MOVE     XACTDSDC,EMATDSDC   * Discharged Deceased
          MOVE     XACTRABA,EMATRABA   * At own Risk After Being Attended
          MOVE     XACTRTAH,EMATRTAH   * Referral to Another Hospital
          MOVE     XACTABAM,EMATABAM   * Arrival by Ambulance
          MOVE     XACTTCT1,EMATTCT1   * Triage Category 1
          MOVE     XACTTCT2,EMATTCT2   * Triage Category 2
          MOVE     XACTTCT3,EMATTCT3   * Triage Category 3
          MOVE     XACTTCT4,EMATTCT4   * Triage Category 4
          MOVE     XACTTCT5,EMATTCT5   * Triage Category 5
          MOVE     XACTAFLG,EMATAFLG   * Active/Inactive Flag
.
SFACT999  RETURN
.
.******************************************************************************
. Set fields - AECC Coefficients for Age Group
.******************************************************************************
SFACA000  MOVE     XACACODE,EMAACODE   * ECDG Code
          MOVE     XACAFDAT,EMAAFDAT   * Effective Date From (CCYYMMDD)
          MOVE     XACATDAT,EMAATDAT   * Effective Date to (CCYYMMDD)
          MOVE     XACAAG04,EMAAAG04   * Age 0 to 4
          MOVE     XACAAG59,EMAAAG59   * Age 5 to 9
          MOVE     XACAAG14,EMAAAG14   * Age 10 to 14
          MOVE     XACAAG69,EMAAAG69   * Age 15 to 69
          MOVE     XACAAG74,EMAAAG74   * Age 70 to 74
          MOVE     XACAAG79,EMAAAG79   * Age 75 to 79
          MOVE     XACAAG84,EMAAAG84   * Age 80 to 84
          MOVE     XACAAG85,EMAAAG85   * Age 85 and Over
          MOVE     XACAAFLG,EMAAAFLG   * Active/Inactive Flag
.
SFACA999  RETURN
.
.******************************************************************************
. Set fields - AECC Coefficients for Admit Predictor
.******************************************************************************
SFACP000  MOVE     XACPCODE,EMAPCODE   * ECDG Code
          MOVE     XACPFDAT,EMAPFDAT   * Effective Date From (CCYYMMDD)
          MOVE     XACPTDAT,EMAPTDAT   * Effective Date to (CCYYMMDD)
          MOVE     XACPAGE1,EMAPAGE1   * Age 0 to 14
          MOVE     XACPAGE2,EMAPAGE2   * Age 80+
          MOVE     XACPTCT1,EMAPTCT1   * Triage Category 1
          MOVE     XACPTCT2,EMAPTCT2   * Triage Category 2
          MOVE     XACPTCT3,EMAPTCT3   * Triage Category 3
          MOVE     XACPTCT4,EMAPTCT4   * Triage Category 4
          MOVE     XACPTCT5,EMAPTCT5   * Triage Category 5
          MOVE     XACPAFLG,EMAPAFLG   * Active/Inactive Flag
.
SFACP999  RETURN
.
.******************************************************************************
. Set fields - AECC Coefficients for ECDG Complexity
.******************************************************************************
SFACC000  MOVE     XACCCODE,EMACCODE   * ECDG Code
          MOVE     XACCFDAT,EMACFDAT   * Effective Date From (CCYYMMDD)
          MOVE     XACCTDAT,EMACTDAT   * Effective Date to (CCYYMMDD)
          MOVE     XACCCLAS,EMACCLAS   * AECC Class
          MOVE     XACCDESC,EMACDESC   * ECDG Description
          MOVE     XACCDMIN,EMACMINS   * Minimum Score
          MOVE     XACCMAXS,EMACMAXS   * Maximum Score
          MOVE     XACCMXSI,EMACMXSI   * Maximum Score of Infinity
          MOVE     XACCAFLG,EMACAFLG   * Active/Inactive Flag
.
SFACC999  RETURN
.
.******************************************************************************
. Set fields - AECC Value Set 
.******************************************************************************
SFVSC000  MOVE     XMVCVSID,PMVCVSID   * Value Set ID
          MOVE     XMVCCODE,PMVCCODE   * Code
          MOVE     XMVCPTRM,PMVCPTRM   * Preferred Term
          MOVE     XMVCFNAM,PMVCFNAM   * Fully Specified Name
          MOVE     XMVCACTV,PMVCACTV   * Active Flag
.
SFVSC999  RETURN
.
.******************************************************************************
. Set fields - EMR Diagnosis to ECC Value
.******************************************************************************
SFVMT000  MOVE     XMVMTNAM,PMVMTNAM   * Table Name
          MOVE     XMVMTIDX,PMVMTIDX   * Table Index Value
          MOVE     XMVMMVSI,PMVMMVSI   * Map to Value Set ID
          MOVE     XMVMMCOD,PMVMMCOD   * Map to Code
          MOVE     XMVMMDES,PMVMMDES   * Map to Description
.
SFVMT999  RETURN
.
.******************************************************************************
. Set fields - EMR Diagnosis Codes
.******************************************************************************
SEMCD000  MOVE     XEICCODE,EMIC9COD   * EMR Code
          MOVE     XEICDESC,EMICDESC   * EMR Description
          MOVE     XEIC10CD,EMIC10CD   * ICD 10 Code
          MOVE     XEICAFLG,EMICACTV   * Active Flag
          MOVE     XMVMMCOD,EMICVEMD   * VEMD Code
          MOVE     XEICKEYW,EMICKEYW   * KEY Word
          MOVE     XEICMETH,EMICMETH   * Methamphetamine Related
.
SEMCD999  RETURN
.
.******************************************************************************
.         Execute us1 command before processing
.******************************************************************************
EXCUS000  CLEAR     CMDLINE
          APPEND    "ibapms53.us1 ",CMDLINE
          SQUEEZE   FNAMEI
          APPEND    FNAMEI,CMDLINE
          APPEND    SP1,CMDLINE
          APPEND    OUTFNAME,CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
EXCUS999  RETURN
+
.******************************************************************************
. Validate input text file fields
.******************************************************************************
VALD0000  MOVE      ZERO,EXIT
          MOVE      ZERO,ERRFLAG
.
          BRANCH    COPTION,VALD1000,VALD2000,VALD3000,VALD4000,VALD5000:
                            VALD6000,VALD7000,VALD8000
          GOTO      VALD9999
.
VALD1000  CALL      VACI0000        * Val AECC Coefficients for Intercept
          GOTO      VALD9999
.
VALD2000  CALL      VACT0000        * Val AECC Coefficients for Trans Mode
          GOTO      VALD9999
.
VALD3000  CALL      VACA0000        * Val AECC Coefficients for Age Group 
          GOTO      VALD9999
.
VALD4000  CALL      VACP0000        * Val AECC Coefficients for Admit Predictor 
          GOTO      VALD9999
.
VALD5000  CALL      VACC0000        * Val AECC Coefficients for ECDG Complexity
          GOTO      VALD9999
.
VALD6000  CALL      VVSC0000        * Val AECC Value Set
          GOTO      VALD9999
.
VALD7000  CALL      VVMT0000        * Val EMR Diagnosis to ECC Value
          GOTO      VALD9999
.
VALD8000  CALL      VEMCD000        * Val EMR Diagnosis Codes
          GOTO      VALD9999
.
VALD9999  RETURN
+
.******************************************************************************
. Validate AECC Coefficients for Intercept input text file fields
.******************************************************************************
VACI0000  MOVE      XFLD0001,XACISUBV        * ECDG Subcategory 
          MOVE      XFLD0003,XACICODE        * ECDG Code
          MOVE      XFLD0006,XACIFDAT        * effective from date
          MOVE      XFLD0007,XACITDAT        * effective to date
.
          MATCH     SP70,XFLD0001
          IF        @EQUAL
            MOVE      "ECDG Subcode is blank",ERRORMSG
            CALL      PERR0000
          ENDIF
.
          MATCH     SP70,XFLDX256
          IF        @EQUAL
            MOVE      "ECDG Subcategory Description is blank",ERRORMSG
            CALL      PERR0000
          ENDIF
.
          MATCH     SP70,XFLD0003
          IF        @EQUAL
            MOVE      "ECDG Code is blank",ERRORMSG
            CALL      PERR0000
          ENDIF
.
          MOVE      XFLD0004,DINFIELD
          MOVE      "Coefficient for Intercept",FLDNAME   
          CALL      C48NUM00
.
          MOVE      XFLD0005,DINFIELD
          MOVE      "Coefficient for Intercept ECDG Subgroup",FLDNAME
          CALL      C48NUM00
.
          MOVE      XFLD0006,DINFIELD
          MOVE      "Date Effective From",FLDNAME
          CALL      CEFFFR00
.
          MATCH     SP70,XFLD0007
          IF        !@EQUAL
            PACK      TEMPDATE,XFLD0007,SP70
            MOVE      "Date Effective To",FLDNAME
            CALL      VDAT0000
          ENDIF
.      
          MOVE      XFLD0008,DINFIELD
          MOVE      "Active/Inactive Flag",FLDNAME
          CALL      CACTIV00
.
VACI9999  RETURN
.
.------------------------------------------------------------------------------
. Validate an effective from date field
.------------------------------------------------------------------------------
CEFFFR00  MATCH     SP70,DINFIELD
          IF        @EQUAL | @EOS
            PACK      ERRORMSG,FLDNAME,SP1,CISBLANK
            CALL      PERR0000
            GOTO      CEFFFR99 
          ENDIF
.
          PACK      TEMPDATE,DINFIELD,SP70
          CALL      VDAT0000
.
CEFFFR99  RETURN
.
.------------------------------------------------------------------------------
. Validate an active/inactive flag field
.------------------------------------------------------------------------------
CACTIV00  MATCH     SP70,DINFIELD
          IF        @EQUAL | @EOS
            PACK      ERRORMSG,FLDNAME,SP1,CISBLANK
            CALL      PERR0000
            GOTO      CACTIV99
          ENDIF
.
          SQUEEZE   DINFIELD
          MOVELPTR  DINFIELD,FORM1
          IF        FORM1 > 1
            PACK      ERRORMSG,FLDNAME,SP1,CMUST01
            CALL      PERR0000
            GOTO      CACTIV99
          ENDIF
.
          PACK      KEY1,DINFIELD,SP70
          MATCH     "0",KEY1
          IF        !@EQUAL
            MATCH     "1",KEY1
            IF        !@EQUAL
              PACK      ERRORMSG,FLDNAME,SP1,CMUST01
              CALL      PERR0000
            ENDIF
          ENDIF
.
CACTIV99  RETURN
.
.------------------------------------------------------------------------------
. Validate a numeric field size 3.6 
.------------------------------------------------------------------------------
C36NUM00  MATCH     SP70,DINFIELD
          IF        @EQUAL | @EOS
            PACK      ERRORMSG,FLDNAME,SP1,CISBLANK
            CALL      PERR0000
            GOTO      C36NUM99
          ENDIF
.
          STRIP     DINFIELD
          TYPE      DINFIELD
          IF        !@EQUAL
            PACK      ERRORMSG,CINVALID,SP1,FLDNAME
            CALL      PERR0000
            GOTO      C36NUM99
          ENDIF
.
C36NUM20  MOVE      3,DINTLEN
          MOVE      6,DDECLEN
          MOVE      1,DNEGALLW
.
          CALL      NUMDC000
.
C36NUM99  RETURN
.
.------------------------------------------------------------------------------
. Validate a numeric field size 4.8 
.------------------------------------------------------------------------------
C48NUM00  MATCH     SP70,DINFIELD
          IF        @EQUAL | @EOS
            PACK      ERRORMSG,FLDNAME,SP1,CISBLANK
            CALL      PERR0000
            GOTO      C48NUM99
          ENDIF
.
          STRIP     DINFIELD
          TYPE      DINFIELD
          IF        !@EQUAL
            PACK      ERRORMSG,CINVALID,SP1,FLDNAME
            CALL      PERR0000
            GOTO      C48NUM99
          ENDIF
.
C48NUM20  MOVE      4,DINTLEN
          MOVE      8,DDECLEN
          MOVE      1,DNEGALLW
.
          CALL      NUMDC000
.
C48NUM99  RETURN
.
.******************************************************************************
. Validate a numeric with decimal field
.  
. Requires:  DINFIELD - Number/Decimal field 
.            FLDNAME  - Field Name
.            DINTLEN  - Integer length 
.            DDECLEN  - Decimal length
.            DNEGALLW - 1 (Yes), 0 (No) 
. Returns:   ERORFLAG - 1 if an error in date validation occurs.
.            ERORDESC - Description of error when an error is occurred
.******************************************************************************
NUMDC000  MOVE      SP70,ERORFLAG
          STRIP     DINFIELD 
          MOVE      DINFIELD,WORKFLD
          MATCH     SP70,WORKFLD               * blank number ?
          IF        @EQUAL | @EOS
            GOTO      NUMDC999
          ENDIF
.
          ASSIGN    DINTLEN+DDECLEN+1,DTOTLEN 
          MOVELPTR  WORKFLD,FORM2
          IF        FORM2 > DTOTLEN
            MOVE      "Total Length > Max Length Allowed",ERORDESC
            CALL      NERR0000
            GOTO      NUMDC999
          ENDIF
.
NUMDC200  SCAN      ".",WORKFLD
          IF        @EQUAL
            BUMP      WORKFLD
            MOVEFPTR  WORKFLD,FORM2
            MOVE      WORKFLD,WORKFLDD
            SUB       TWO,FORM2    
            MOVE      DINFIELD,WORKFLD
            SETLPTR   WORKFLD,FORM2
            MOVE      WORKFLD,WORKFLDI
          ELSE
            MOVE      WORKFLD,WORKFLDI
            MOVE      SP70,WORKFLDD
          ENDIF
.
          SQUEEZE   WORKFLDI
          MOVELPTR  WORKFLDI,FORM2
          IF        FORM2 > DINTLEN
            MOVE      "Integer Length",ERORDESC
            CALL      NERR0000
            GOTO      NUMDC999
          ENDIF
.
          SQUEEZE   WORKFLDD
          MOVELPTR  WORKFLDD,FORM2
          IF        FORM2 > DDECLEN
            MOVE      "Decimal Length",ERORDESC
            CALL      NERR0000
            GOTO      NUMDC999
          ENDIF
.
          COMPARE   ZERO,DNEGALLW
          IF        @EQUAL
            SCAN      HYPHEN,WORKFLD
            IF        @EQUAL
              MOVE      "Negative SIGN Not Allowed",ERORDESC
              CALL      NERR0000
              GOTO      NUMDC999
            ENDIF
          ENDIF
.
NUMDC999  RETURN
.
.------------------------------------------------------------------------------
. Write error message for a numeric field  
.------------------------------------------------------------------------------
NERR0000  MOVE      ONE,ERORFLAG
          STRIP     FLDNAME
          STRIP     ERORDESC
.
          PACK      DIM100,CINVALID,SP1,FLDNAME,SP1,HYPHEN,SP1,ERORDESC,SP70
          STRIP     DIM100
          MOVELPTR  DIM100,FORM3
          IF        FORM3 > 50
            PACK      ERRORMSG,CINVALID,SP1,FLDNAME
            PACK      ERRORMS1,HYPHEN,SP1,ERORDESC
          ELSE
            MOVE      DIM100,ERRORMSG
            MOVE      SP70,ERRORMS1
          ENDIF
.
          CALL      PERR0000
.
NERR9999  RETURN
.
.******************************************************************************
. Validate AECC Coefficient for Transport Mode input text file fields
.******************************************************************************
VACT0000  MOVE      XFLD0001,XACTCODE        * ECDG Code
          MOVE      XFLD0012,XACTFDAT        * effective from date
          MOVE      XFLD0013,XACTTDAT        * effective to date
.
          MATCH     SP70,XFLD0001
          IF        @EQUAL
            MOVE      "ECDG Code is blank",ERRORMSG
            CALL      PERR0000
          ENDIF
.
          MOVE      XFLD0002,DINFIELD
          MOVE      "Admitted Yes",FLDNAME   
          CALL      C36NUM00
.
          MOVE      XFLD0003,DINFIELD
          MOVE      "Discharged Deceased",FLDNAME   
          CALL      C36NUM00
.
          MOVE      XFLD0004,DINFIELD
          MOVE      "Left At own Risk After Being Attended",FLDNAME   
          CALL      C36NUM00
.
          MOVE      XFLD0005,DINFIELD
          MOVE      "Referral to Another Hospital",FLDNAME   
          CALL      C36NUM00
.
          MOVE      XFLD0006,DINFIELD
          MOVE      "Arrival by Ambulance",FLDNAME   
          CALL      C36NUM00
.
          MOVE      XFLD0007,DINFIELD
          MOVE      "Triage Category 1",FLDNAME   
          CALL      C36NUM00
.
          MOVE      XFLD0008,DINFIELD
          MOVE      "Triage Category 2",FLDNAME   
          CALL      C36NUM00
.
          MOVE      XFLD0009,DINFIELD
          MOVE      "Triage Category 3",FLDNAME   
          CALL      C36NUM00
.
          MOVE      XFLD0010,DINFIELD
          MOVE      "Triage Category 4",FLDNAME   
          CALL      C36NUM00
.
          MOVE      XFLD0011,DINFIELD
          MOVE      "Triage Category 5",FLDNAME   
          CALL      C36NUM00
.
          MOVE      XFLD0012,DINFIELD
          MOVE      "Date Effective From",FLDNAME
          CALL      CEFFFR00
.
          MATCH     SP70,XFLD0013
          IF        !@EQUAL
            PACK      TEMPDATE,XFLD0013,SP70
            MOVE      "Date Effective To",FLDNAME
            CALL      VDAT0000
          ENDIF
.      
          MOVE      XFLD0014,DINFIELD
          MOVE      "Active/Inactive Flag",FLDNAME
          CALL      CACTIV00
.
VACT9999  RETURN
+
.******************************************************************************
. Validate AECC Coefficient for Age Group input text file fields
.******************************************************************************
VACA0000  MOVE      XFLD0001,XACACODE        * ECDG Code
          MOVE      XFLD0010,XACAFDAT        * effective from date
          MOVE      XFLD0011,XACATDAT        * effective to date
.
          MATCH     SP70,XFLD0001
          IF        @EQUAL
            MOVE      "ECDG Code is blank",ERRORMSG
            CALL      PERR0000
          ENDIF
.
          MOVE      XFLD0002,DINFIELD
          MOVE      "Age 0 to 4",FLDNAME   
          CALL      C36NUM00
.
          MOVE      XFLD0003,DINFIELD
          MOVE      "Age 5 to 9",FLDNAME   
          CALL      C36NUM00
.
          MOVE      XFLD0004,DINFIELD
          MOVE      "Age 10 to 14",FLDNAME   
          CALL      C36NUM00
.
          MOVE      XFLD0005,DINFIELD
          MOVE      "Age 15 to 69",FLDNAME   
          CALL      C36NUM00
.
          MOVE      XFLD0006,DINFIELD
          MOVE      "Age 70 to 74",FLDNAME   
          CALL      C36NUM00
.
          MOVE      XFLD0007,DINFIELD
          MOVE      "Age 75 to 79",FLDNAME   
          CALL      C36NUM00
.
          MOVE      XFLD0008,DINFIELD
          MOVE      "Age 80 to 84",FLDNAME   
          CALL      C36NUM00
.
          MOVE      XFLD0009,DINFIELD
          MOVE      "Age 85 and Over",FLDNAME   
          CALL      C36NUM00
.
          MOVE      XFLD0010,DINFIELD
          MOVE      "Date Effective From",FLDNAME
          CALL      CEFFFR00
.
          MATCH     SP70,XFLD0011
          IF        !@EQUAL
            PACK      TEMPDATE,XFLD0011,SP70
            MOVE      "Date Effective To",FLDNAME
            CALL      VDAT0000
          ENDIF
.      
          MOVE      XFLD0012,DINFIELD
          MOVE      "Active/Inactive Flag",FLDNAME
          CALL      CACTIV00
.
VACA9999  RETURN
+
.******************************************************************************
. Validate AECC Coefficient for Admit Predictor input text file fields
.******************************************************************************
VACP0000  MOVE      XFLD0001,XACPCODE        * ECDG Code
          MOVE      XFLD0009,XACPFDAT        * effective from date
          MOVE      XFLD0010,XACPTDAT        * effective to date
.
          MATCH     SP70,XFLD0001
          IF        @EQUAL
            MOVE      "ECDG Code is blank",ERRORMSG
            CALL      PERR0000
          ENDIF
.
          MOVE      XFLD0002,DINFIELD
          MOVE      "Age 0 to 14",FLDNAME   
          CALL      C36NUM00
.
          MOVE      XFLD0003,DINFIELD
          MOVE      "Age 80 and Over",FLDNAME   
          CALL      C36NUM00
.
          MOVE      XFLD0004,DINFIELD
          MOVE      "Triage 1",FLDNAME   
          CALL      C36NUM00
.
          MOVE      XFLD0005,DINFIELD
          MOVE      "Triage 2",FLDNAME   
          CALL      C36NUM00
.
          MOVE      XFLD0006,DINFIELD
          MOVE      "Triage 3",FLDNAME   
          CALL      C36NUM00
.
          MOVE      XFLD0007,DINFIELD
          MOVE      "Triage 4",FLDNAME   
          CALL      C36NUM00
.
          MOVE      XFLD0008,DINFIELD
          MOVE      "Triage 5",FLDNAME   
          CALL      C36NUM00
.
          MOVE      XFLD0009,DINFIELD
          MOVE      "Date Effective From",FLDNAME
          CALL      CEFFFR00
.
          MATCH     SP70,XFLD0010
          IF        !@EQUAL
            PACK      TEMPDATE,XFLD0010,SP70
            MOVE      "Date Effective To",FLDNAME
            CALL      VDAT0000
          ENDIF
.      
          MOVE      XFLD0011,DINFIELD
          MOVE      "Active/Inactive Flag",FLDNAME
          CALL      CACTIV00
.
VACP9999  RETURN
+
.******************************************************************************
. Validate AECC Coefficient for ECDG Complexity input text file fields
.******************************************************************************
VACC0000  MOVE      XFLD0001,XACCCODE        * ECDG Code
          MOVE      XFLD0004,XACCDMIN        * minimum score
          MOVE      XFLD0007,XACCFDAT        * effective from date
          MOVE      XFLD0008,XACCTDAT        * effective to date
.
          MATCH     SP70,XFLD0001
          IF        @EQUAL
            MOVE      "ECDG Code is blank",ERRORMSG
            CALL      PERR0000
          ENDIF
.
          PACK      DIM100,SP70,SP70 
          MATCH     DIM100,XFLDX100
          IF        @EQUAL
            MOVE      "ECDG Description is blank",ERRORMSG
            CALL      PERR0000
          ENDIF
.
          MATCH     SP70,XFLD0003
          IF        @EQUAL
            MOVE      "AECC Class is blank",ERRORMSG
            CALL      PERR0000
          ENDIF
.
          MOVE      XFLD0004,DINFIELD
          MOVE      "Minimum Score",FLDNAME
          CALL      C36NUM00
.      
          MOVE      XFLD0006,DINFIELD
          MOVE      "Max Score of Infinity",FLDNAME
          CALL      CACTIV00
.
          MATCH     "0",XFLD0006
          IF        @EQUAL
            MOVE      XFLD0005,DINFIELD
            MOVE      "Maximum Score",FLDNAME
            CALL      C36NUM00
          ENDIF
.
          MOVE      ZERO,MSCORE36
          MATCH     "1",XFLD0006
          IF        @EQUAL
            MATCH     SP70,XFLD0005
            IF        @EQUAL
              MOVE      MSCORE36,XFLD0005
            ENDIF
            MOVE      XFLD0005,DINFIELD
            MOVE      "Maximum Score",FLDNAME
            CALL      C36NUM00
          ENDIF
.
          MOVE      XFLD0007,DINFIELD
          MOVE      "Date Effective From",FLDNAME
          CALL      CEFFFR00
.
          MATCH     SP70,XFLD0008
          IF        !@EQUAL
            PACK      TEMPDATE,XFLD0008,SP70
            MOVE      "Date Effective To",FLDNAME
            CALL      VDAT0000
          ENDIF
.      
          MOVE      XFLD0009,DINFIELD
          MOVE      "Active/Inactive Flag",FLDNAME
          CALL      CACTIV00
.
VACC9999  RETURN
.
.******************************************************************************
. Validate AECC Value Set input text file fields
.******************************************************************************
VVSC0000  MOVE      XFLD0001,XMVCVSID        * Value Set ID
          MOVE      XFLD0002,XMVCCODE        * Code
.
          MATCH     SP70,XFLD0001
          IF        @EQUAL
            MOVE      "Value Set ID is blank",ERRORMSG
            CALL      PERR0000
          ENDIF
.
          MATCH     SP70,XFLD0002
          IF        @EQUAL
            MOVE      "Code is blank",ERRORMSG
            CALL      PERR0000
          ENDIF
.
VVSC8000  PACK      KEY1,XFLD0005,SP70
          MATCH     SP70,KEY1
          IF        !@EQUAL
            REP       " 010",KEY1
            MATCH     "0",KEY1
            IF        !@EQUAL
              MOVE      "Active/Inactive must be blank, 0 or 1",ERRORMSG
              CALL      PERR0000
            ENDIF
          ELSE
            MOVE      ZERO,XFLD0005
          ENDIF
.
VVSC9999  RETURN
.
.******************************************************************************
. Validate EMR Diagnosis to ECC Value input text file fields
.******************************************************************************
VVMT0000  MOVE      XFLD0001,XMVMTNAM        * Table Name
          MOVE      XFLD0002,XMVMTIDX        * Table Index Value
          MOVE      XFLD0003,XMVMMVSI        * Map to Value Set ID
.
          MATCH     SP70,XFLD0001
          IF        @EQUAL
            MOVE      "Table Name is blank",ERRORMSG
            CALL      PERR0000
          ENDIF
.
          MATCH     SP70,XFLD0002
          IF        @EQUAL
            MOVE      "Table Index Value is blank",ERRORMSG
            CALL      PERR0000
          ENDIF
.
          MATCH     SP70,XFLD0003
          IF        @EQUAL
            MOVE      "Map to Value Set ID is blank",ERRORMSG
            CALL      PERR0000
          ENDIF
.
          MATCH     SP70,XFLD0004
          IF        @EQUAL
            MOVE      "Map to Code is blank",ERRORMSG
            CALL      PERR0000
          ENDIF
.
          PACK      DIM100,SP70,SP70
          MATCH     DIM100,XFLDX100
          IF        @EQUAL
            MOVE      "Map to Description is blank",ERRORMSG
            CALL      PERR0000
          ENDIF
.
VVMT9999  RETURN
.
.******************************************************************************
. Validate EMR Diagnosis Codes input text file fields
.******************************************************************************
VEMCD000  MOVE      XFLD0001,XEICCODE        * EMR Code 
          MOVE      XFLDX100,XEICDESC        * EMR Description
          MOVE      XFLD0002,XEIC10CD        * ICD 10 Code 
          MOVE      XFLD0003,XEICAFLG        * Active Flag 
          MOVE      XFLD0004,XEICVEMD        * VEMD Code
          MOVE      XFLDY100,XEICKEYW        * KEY Word 
          MOVE      XFLD0005,XEICMETH        * Methamphetamine Related
.
          MATCH     SP70,XFLD0001
          IF        @EQUAL
            MOVE      "EMR Code is blank",ERRORMSG
            CALL      PERR0000
          ENDIF
.
          MATCH     SP70,XFLDX100
          IF        @EQUAL
            MOVE      "EMR Description is blank",ERRORMSG
            CALL      PERR0000
          ENDIF
.
          MATCH     SP70,XFLD0003
          IF        @EQUAL
            MOVE      "Active Flag is blank",ERRORMSG
            CALL      PERR0000
          ELSE
            MOVE      XFLD0003,DINFIELD
            SQUEEZE   DINFIELD
            MOVELPTR  DINFIELD,FORM1
            IF        FORM1 > 1
              MOVE      "Active Flag",FLDNAME
              PACK      ERRORMSG,FLDNAME,SP1,CMUST02
              CALL      PERR0000
            ELSE
              PACK      KEY1,DINFIELD,SP70
              MATCH     "0",KEY1
              IF        !@EQUAL
                MATCH     "1",KEY1
                IF        !@EQUAL
                  MATCH     "2",KEY1
                  IF        !@EQUAL
                    PACK      ERRORMSG,FLDNAME,SP1,CMUST02
                    CALL      PERR0000
                  ENDIF
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
          MATCH     SP70,XEICKEYW
          IF        @EQUAL
            MOVE      "KEY Word is blank",ERRORMSG
            CALL      PERR0000
          ENDIF
.
          MATCH     SP70,XFLD0005
          IF        !@EQUAL
            MOVE      XFLD0005,DINFIELD
            SQUEEZE   DINFIELD
            MOVELPTR  DINFIELD,FORM1
            IF        FORM1 > 1
              MOVE      "Methamphetamine Related",FLDNAME
              PACK      ERRORMSG,FLDNAME,SP1,CMUST03
              CALL      PERR0000
            ELSE
              PACK      KEY1,DINFIELD,SP70
              MATCH     "0",KEY1
              IF        !@EQUAL
                MATCH     "1",KEY1
                IF        !@EQUAL
                  PACK      ERRORMSG,FLDNAME,SP1,CMUST03
                  CALL      PERR0000
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
VEMCD999  RETURN
.
.*****************************************************************************
. Validate a date field
.
. Requires:  TEMPDATE - Date in format ccyymmdd
.            FLDNAME  - Date Field Name
. Returns:   ERORFLAG - 1 if an error in date validation occurs.
.            ERORDESC - Description of error when an error is occurred
.*****************************************************************************
VDAT0000  MOVE      SP70,ERORFLAG
          MATCH     SP8,TEMPDATE                 * blank date ?
          GOTO      VDAT9999 IF EQUAL            * yes
.
. Validate that the date has been fully populated
.
          SQUEEZE   TEMPDATE
          MOVELPTR  TEMPDATE,FORM1
          IF        FORM1 <> 8
            MOVE      "Insufficient digits",ERORDESC
            CALL      NERR0000
            GOTO      VDAT9999
          ENDIF
.
. Validate that the date is numeric
.
VDAT0100  TYPE      TEMPDATE
          IF        !@EQUAL
            MOVE      "Not numeric",ERORDESC
            CALL      NERR0000
            GOTO      VDAT9999
          ENDIF
.
          UNPACK    TEMPDATE,DIM2C,DIM2Y,DIM2M,DIM2D
.
. Validate the century is >= 18 and <= current century
.
          MOVE      DIM2C,FORM2
          IF        FORM2 < 18 | FORM2 > CURRCENT
            MOVE      "Century not valid",ERORDESC
            CALL      NERR0000
            GOTO      VDAT9999
          ENDIF
.
. Validate the month
.
          MOVE      DIM2M,FORM2
          IF        FORM2 < 1 | FORM2 > 12
            MOVE      "Month not valid",ERORDESC
            CALL      NERR0000
            GOTO      VDAT9999
          ENDIF
.
. Validate the day is:
.   < 32 for January, March, May, July, August, October & December
.   < 31 for April, June, September & November
.   < 29 for February, except in a leap year where it is < 30
.
          BRANCH    FORM2,VDAT1000:              * Jan
                          VDAT3000:              * Feb
                          VDAT1000:              * Mar
                          VDAT2000:              * Apr
                          VDAT1000:              * May
                          VDAT2000:              * Jun
                          VDAT1000:              * Jul
                          VDAT1000:              * Aug
                          VDAT2000:              * Sep
                          VDAT1000:              * Oct
                          VDAT2000:              * Nov
                          VDAT1000               * Dec
.
. Validate days for month of 31 days
.
VDAT1000  MOVE      DIM2D,FORM2
          IF        FORM2 < 1 | FORM2 > 31
            GOTO      VDAT3900
          ENDIF
          GOTO      VDAT9999
.
. Validate days for month of 30 days
.
VDAT2000  MOVE      DIM2D,FORM2
          IF        FORM2 < 1 | FORM2 > 30
            GOTO      VDAT3900
          ENDIF
          GOTO      VDAT9999
.
. Validate days for February
. A leap year is one where:
.  1. Every year divisible by 4 is a leap year.
.  2. But every year divisible by 100 is NOT a leap year
.  3. Unless the year is also divisible by 400, then it is still a
.     leap year.
.
VDAT3000  MOVE      DIM2D,FORM2
          PACK      DIM4,DIM2C,DIM2Y
          MOVE      DIM4,FORM4
          IF        (FORM4%4) = 0
            IF        (FORM4%100) = 0
              IF        (FORM4%400) = 0
                GOTO      VDAT3200
              ENDIF
            ELSE
              GOTO      VDAT3200
            ENDIF
          ENDIF
.
. Check for normal Feb days
.
VDAT3100  IF        FORM2 < 1 | FORM2 > 28
            GOTO      VDAT3900
          ENDIF
          GOTO      VDAT9999
.
. Check for leap year Feb days
.
VDAT3200  IF        FORM2 < 1 | FORM2 > 29
            GOTO      VDAT3900
          ENDIF
          GOTO      VDAT9999
.
VDAT3900  MOVE      "Day not valid",ERORDESC
          CALL      NERR0000
          GOTO      VDAT9999
.
VDAT9999  RETURN
.
.******************************************************************************
. Print error line
.******************************************************************************
PERR0000  ADD       ONE,ERRCOUNT            * Total error count
          MOVE      ONE,ERRFLAG
          MOVE      ONE,EXIT
.
          BRANCH    COPTION,PERR1000,PERR2000,PERR3000,PERR4000,PERR5000:
                            PERR6000,PERR7000,PERR8000
          GOTO      PERR9999
.
PERR1000  CALL      PEACI000
          GOTO      PERR9999
.
PERR2000  CALL      PEACT000
          GOTO      PERR9999
.
PERR3000  CALL      PEACA000
          GOTO      PERR9999
.
PERR4000  CALL      PEACP000
          GOTO      PERR9999
.
PERR5000  CALL      PEACC000
          GOTO      PERR9999
.
PERR6000  CALL      PEVSC000
          GOTO      PERR9999
.
PERR7000  CALL      PEVMT000
          GOTO      PERR9999
.
PERR8000  CALL      PEMCD000 
          GOTO      PERR9999
.
PERR9999  RETURN
.
.******************************************************************************
. Print error line - AECC Coefficients for Intercept
.******************************************************************************
PEACI000  PACK      DIM14,XACICODE,SP70
          PRINT     *1,"|",LINECNTR,*12,"|",ERRORMSG,*64,"|",XACISUBV:
                    *96,"|",XACIFDAT,*106,"|",XACITDAT:
                    *116,"|",DIM14:
                    *132,"|"
.
          MATCH     SP70,ERRORMS1
          IF        !@EQUAL
            PRINT     *1,"|",*12,"|",ERRORMS1,*64,"|":
                      *96,"|",*106,"|":
                      *116,"|":
                      *132,"|"
          ENDIF
.
          MOVE      SP70,ERRORMSG
          MOVE      SP70,ERRORMS1
.
PEACI999  RETURN
.
.******************************************************************************
. Print error line - AECC Coefficients for Transport Mode
.******************************************************************************
PEACT000  PRINT     *1,"|",LINECNTR,*12,"|",ERRORMSG,*64,"|",XACTCODE:
                    *96,"|",XACTFDAT,*106,"|",XACTTDAT:
                    *132,"|"
.
          MATCH     SP70,ERRORMS1
          IF        !@EQUAL
            PRINT     *1,"|",*12,"|",ERRORMS1,*64,"|":
                      *96,"|",*106,"|":
                      *132,"|"
          ENDIF
.
          MOVE      SP70,ERRORMSG
          MOVE      SP70,ERRORMS1 
.
PEACT999  RETURN
.
.******************************************************************************
. Print error line - AECC Coefficients for Age Group 
.******************************************************************************
PEACA000  PRINT     *1,"|",LINECNTR,*12,"|",ERRORMSG,*64,"|",XACACODE:
                    *96,"|",XACAFDAT,*106,"|",XACATDAT:
                    *132,"|"
.
          MATCH     SP70,ERRORMS1
          IF        !@EQUAL
            PRINT     *1,"|",*12,"|",ERRORMS1,*64,"|":
                      *96,"|",*106,"|":
                      *132,"|"
          ENDIF
.
          MOVE      SP70,ERRORMSG
          MOVE      SP70,ERRORMS1
.
PEACA999  RETURN
.
.******************************************************************************
. Print error line - AECC Coefficients for Admit Predictor
.******************************************************************************
PEACP000  PRINT     *1,"|",LINECNTR,*12,"|",ERRORMSG,*64,"|",XACPCODE:
                    *96,"|",XACPFDAT,*106,"|",XACPTDAT:
                    *132,"|"
.
          MATCH     SP70,ERRORMS1
          IF        !@EQUAL
            PRINT     *1,"|",*12,"|",ERRORMS1,*64,"|":
                      *96,"|",*106,"|":
                      *132,"|"
          ENDIF
.
          MOVE      SP70,ERRORMSG
          MOVE      SP70,ERRORMS1
.
PEACP999  RETURN
.
.******************************************************************************
. Print error line - AECC Coefficients for ECDG Complexity
.******************************************************************************
PEACC000  PRINT     *1,"|",LINECNTR,*12,"|",ERRORMSG,*64,"|",XACCCODE:
                    *96,"|",XACCDMIN,*108,"|",XACCFDAT,*119,"|",XACCTDAT:
                    *132,"|"
.
          MATCH     SP70,ERRORMS1
          IF        !@EQUAL
            PRINT     *1,"|",*12,"|",ERRORMS1,*64,"|":
                    *96,"|",*108,"|",*119,"|":
                    *132,"|"
          ENDIF
.
          MOVE      SP70,ERRORMSG
          MOVE      SP70,ERRORMS1
.
PEACC999  RETURN
.
.******************************************************************************
. Print error line - AECC Value Set
.******************************************************************************
PEVSC000  PRINT     *1,"|",LINECNTR,*12,"|",ERRORMSG,*64,"|",XMVCVSID:
                    *72,"|",XMVCCODE:
                    *132,"|"
.
          MATCH     SP70,ERRORMS1
          IF        !@EQUAL
            PRINT     *1,"|",*12,"|",ERRORMS1,*64,"|":
                      *72,"|":
                      *132,"|"
          ENDIF
.
          MOVE      SP70,ERRORMSG
          MOVE      SP70,ERRORMS1
.
PEVSC999  RETURN
.
.******************************************************************************
. Print error line - EMR Diagnosis to ECC Value 
.******************************************************************************
PEVMT000  PRINT     *1,"|",LINECNTR,*12,"|",ERRORMSG,*64,"|",XMVMTNAM:
                    *76,"|",XMVMTIDX,*108,"|",XMVMMVSI:
                    *132,"|"
.
          MATCH     SP70,ERRORMS1
          IF        !@EQUAL
            PRINT     *1,"|",*12,"|",ERRORMS1,*64,"|":
                      *76,"|",*108,"|":
                      *132,"|"
          ENDIF
.
          MOVE      SP70,ERRORMSG
          MOVE      SP70,ERRORMS1
.
PEVMT999  RETURN
.
.******************************************************************************
. Print error line - EMR Diagnosis Codes
.******************************************************************************
PEMCD000  PRINT     *1,"|",LINECNTR,*12,"|",ERRORMSG,*64,"|",XEICCODE: 
                    *132,"|"
.
          MATCH     SP70,ERRORMS1
          IF        !@EQUAL
            PRINT     *1,"|",*12,"|",ERRORMS1,*64,"|":
                      *132,"|"
          ENDIF
.
          MOVE      SP70,ERRORMSG
          MOVE      SP70,ERRORMS1
.
PEMCD999  RETURN
+
.******************************************************************************
. Pack fields with spaces
.******************************************************************************
PFLD0000  PACK      XFLD0001,XFLD0001,SP70
          PACK      XFLD0002,XFLD0002,SP70
          PACK      XFLD0003,XFLD0003,SP70
          PACK      XFLD0004,XFLD0004,SP70
          PACK      XFLD0005,XFLD0005,SP70
          PACK      XFLD0006,XFLD0006,SP70
          PACK      XFLD0007,XFLD0007,SP70
          PACK      XFLD0008,XFLD0008,SP70
          PACK      XFLD0009,XFLD0009,SP70
          PACK      XFLD0010,XFLD0010,SP70
          PACK      XFLD0011,XFLD0011,SP70
          PACK      XFLD0012,XFLD0012,SP70
          PACK      XFLD0013,XFLD0013,SP70
          PACK      XFLD0014,XFLD0014,SP70
          PACK      XFLD0015,XFLD0015,SP70
.
          PACK      XFLDX100,XFLDX100,SP70,SP70
          PACK      XFLDY100,XFLDY100,SP70,SP70
          PACK      XFLDX256,XFLDX256,SP70,SP70,SP70,SP70
          PACK      XFLDY256,XFLDY256,SP70,SP70,SP70,SP70
.
PFLD9999  RETURN
+
.******************************************************************************
. Clear total vars
.******************************************************************************
CTOT0000  MOVE      ZERO,ERRCOUNT
          MOVE      ZERO,ADDCOUNT
          MOVE      ZERO,UPDCOUNT
.
CTOT999   RETURN
+
.******************************************************************************
. Print total line
.******************************************************************************
PTOT0000  CALL      UND132
.
          PRINT     *1,"| Total in file : ",LINECNTR,*132,"|"
          PRINT     *1,"| Total Errors  : ",ERRCOUNT,*132,"|"
.
          PRINT     *1,"| Total Added   : ",ADDCOUNT,*132,"|"
          PRINT     *1,"| Total Updated : ",UPDCOUNT,*132,"|"
.
          CALL      UND132
.
PTOT9999  RETURN
+
.******************************************************************************
.
          INC       STD001IO
          INC       CLEMRACI                 * clear fields
          INC       CLEMRACT                 
          INC       CLEMRACA                 
          INC       CLEMRACP                 
          INC       CLEMRACC                 
.
          INC       EMRACIVL
          INC       EMRACTVL
          INC       EMRACPVL
          INC       EMRACAVL
          INC       EMRACCVL
          INC       PMSUKIPR                * Value Set Code Key Word Proc
          INC       EMRICDPR                * Update Key Word Index
.
          INC       PATCODIO/INC
          INC       EMRACIIO/INC
          INC       EMRACTIO/INC
          INC       EMRACAIO/INC
          INC       EMRACPIO/INC
          INC       EMRACCIO/INC
          INC       PMSVSCIO/INC
          INC       PMSVMTIO/INC
          INC       PMSUKIIO/INC       * Key Words file for Value Set ID
          INC       EMRICDIO/INC       
          INC       EMRKEDIO/INC
.

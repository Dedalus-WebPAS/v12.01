.*****************************************************************************
.*  System       :  Outpatient                                               *
.*  Program      :  IBAOUT81                                                 *
.*  Function     :  Outpatient Attendance by Clinic Speciality &             *
.*                                           GP Practice                     *
.*****************************************************************************
.*  Date         :  14/05/93                                                 *
.*  Author       :  Whirlpool                                                *
.*  Modifactions :  19/05/93 Whirlpool                                       *
.*                           Recompiled for OUTOSFFD                         *
.*        V10.05.01 18/12/2014  Ebon Clements     CAR 261630                 *
.*                  Removed port number from temp file name                  *
.*        V5.01.03  21/09/1993    LOFTY                                      *
.*                  fix temp file name and delete it when finished           *
.*        V5.01.04  03/11/1993    Justin Coates  QUOTE 440017 SRF 440204     *
.*                  allow for new GP practice file                           *
.*        V5.01.05  17/01/1993    ROD        SRF 440562 - Dudley             *
.*                  Split into New & Follow-up for GP Practice sequence      *
.*---------------------------------------------------------------------------*
.*        V5.03.01  12/04/1995    Max White  SRF 441212                      *
.*                  Any visit where indicator 1 not = 'N' to be counted as   *
.*                  a revisit.                                               *
.*****************************************************************************
.
          INC       STD001FD
          INC       IBASEQFD/INC
          INC       PATCODFD/INC
          INC       PMSHCGFD/INC
          INC       PMSHCPFD/INC
          INC       PMSHCLFD/INC
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
          INC       OUTOSFFD/INC
          INC       OUTSITFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
DOL       DIM       10
CFNAMEA   DIM       8
CODE      DIM       2
CMDLINE   DIM       50
DESC17    DIM       17
DIM3      DIM       3
DIM6ARRY  DIM       6[34]
DIM8      DIM       8
DIM25     DIM       25
DIM58     DIM       58
DIM60     DIM       60
DIM75     DIM       75
DIM80     DIM       80
EGPP      DIM       10
EGPPDESC  DIM       35
EDOL      DIM       10
ENDDATE   DIM       8
FSTTOTAL  FORM      5
FUPTOTAL  FORM      5
SAVSITE   DIM       6
SAVPRAC   DIM       10
SDOL      DIM       10
SGPP      DIM       10
SGPPDESC  DIM       35
STRTDATE  DIM       8
.
GPONE     INIT      " 1"
START     INIT      "Start "
FINISH    INIT      "Finish"
ZEDS      INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
TOTAL     FORM      6
GRANDTOT  FORM      7
TOTFIRST   FORM      6
TOTREVIS  FORM      6
.
TODAY     DIM       8
.
PMHGFLAG  FORM      "0"             Flag for display of active GP prac's in ?
.                                   0 = Don't Display Inactive Gp's Prac's
.                                   1 = Display all Gp Practices
.
PMHGQARR  DIM       12[17]        * array for pmshcgds
PMHGQCNT  FORM      2             * count of items on screen
PMHLFLAG  FORM      "0"                   Flag to display all links in ?
.                                         0 = Don't display inactive links
.                                         1 = Display all links
.
TMPFILE1  IFILE  SQL, FIXED=12, KEY="U1-3"
.
XX1SPEC   DIM       3    3      1
XX1FIRST  FORM      5    4      4
XX1REVIS  FORM      5    4      8
.                                  
.                              12
.
TMPFILE2  IFILE  SQL, FIXED=24, KEY="u1-15"
.
XX2PRAC   DIM      10   10      1
XX2PCCNT  DIM       2    2     11
XX2SPEC   DIM       3    3     13
XX2FIRST  FORM      5    4     16
XX2REVIS  FORM      5    4     20
.                              24
XX2TOTAL  FORM      6    4     12
.
PRGID     INIT      "IBAOUT81"
PRGNAM    INIT      "Outpatient Attendance Report"                       
VERSION   INIT      "V12.01.00"
.
.*****************************************************************************
.*                              ML0000                                       *
.*                             Mainline                                      *
.*****************************************************************************
.
ML0000    CALL      INIT0000                * initialistion
.
ML1000    CALL      MENU0000                * Program Menu
          BRANCH    EXIT,ML9995
.
ML2000    CALL      DSCR0000
          CALL      KDAT0000                * Keyin the date range
          BRANCH    EXIT,ML9995
.
ML3000    CALL      KGPR0000                * Keyin the Practice  range
          BRANCH    EXIT,ML9995
.
          CALL      CONTQST
          BRANCH    CEXIT,ML5000,ML2000,ML1000
.
ML5000    CALL      CTBF0000                * create the tempfile
          CALL      EXTR0000                * Extract the data
          BRANCH    EXIT,ML1000
 
          IF        MOPTION = 1
            CALL      PRSP0000              * Print the report by speciality
          ELSE
            CALL      PRGP0000              * Print the report by gp
          ENDIF
          GOTO      ML1000
.
ML9995    CALL      KTBF0000
ML9999    CHAIN     PGM
.
.*****************************************************************************
.*                              INIT0000                                     *
.*                        Initialisation section                             *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD
.
          CALL      IBACLOCK
          PACK      TODAY,CCC,CYY,CMM,CDD
          REP       " 0",TODAY
          OPEN      PATCODE1,"patcodes"
          OPEN      PMSHCGA1,"pmshcgaf"
          OPEN      PMSHCLA1,"pmshclaf"
          OPEN      OUTSITA1,"outsitaf"
          OPEN      OUTOSFA3,"outosfaf"
.
          MOVE      ONE,CNEWDS
          CALL      TFILENAM * Get temp file name in create
          MOVE      TFILNAME,CFNAMEA
          
INIT9999  RETURN
.
.*****************************************************************************
.*                              MENU0000                                     *
.*                        Program Menu Routine                               *
.*****************************************************************************
.
MENU0000  MOVE      ZERO,EXIT
.
          HLINE     *G37,2,54,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = By Speciality":
                    *P1:6,*V2LON,"2",*HOFF," = By GP Practice":
                    *P1:8,"Select Option : "

MENU1000  KEYIN     *P17:8,*V2LON,MOPTION:
                    *P17:8,*DV,MOPTION
.
          BRANCH    MOPTION,MENU2000,MENU3000
.
          COMPARE   ZERO,MOPTION
          GOTO      MENU9000 IF EQUAL
.
          BEEP
          GOTO      MENU1000
MENU2000  DISPLAY   *P54:2,*V2LON,"- By Speciality "
          GOTO      MENU9999
.
MENU3000  DISPLAY   *P54:2,*V2LON,"- By GP Practice "
          GOTO      MENU9999
.
MENU9000  MOVE      ONE,EXIT
.
MENU9999  RETURN
                   
.
.*****************************************************************************
.*                              DSCR0000                                     *
.*                        Display the screen                                 *
.*****************************************************************************
.
DSCR0000  DISPLAY   *P1:10,*EF:
                    *P1:10,"Date        from : ":
                    *P1:11,"              to : ":
                    *P1:13,"GP Practice from : ":
                    *P1:14,"              to : "
.
DSCR9999  RETURN
.*****************************************************************************
.*                              KDAT0000                                     *
.*                        Keyin the date on list range                       *
.*****************************************************************************
.
KDAT0000  MOVE      ZERO,EXIT
          MOVE      CCC,CCENT
          UNPACK    SP30,CDAY,CMON,CYEAR
          MOVE      "20",CCOL
          MOVE      TEN,CVERT
          MOVE      ONE,CCANLDTE
          MOVE      ZERO,CHIGHLT
.
          DISPLAY   *P20:10,*EL,*P20:11,*EL
          CALL      KEYDATE
.
          MATCH     SP2,CDAY
          IF        @EQUAL
            BEEP 
            GOTO      KDAT0000
          ENDIF
.
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDATE
.
          MATCH     STRTDATE,TODAY
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Date cannot be into the future. ";
            CALL      HITENTER
            GOTO      KDAT0000
          ENDIF
          PACK      SDOL,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.
KDAT5000  MOVE      TEN1,CVERT
          CALL      KEYDATE
.
          MATCH     SP2,CDAY
          IF        @EQUAL
            BEEP
            GOTO      KDAT5000
          ENDIF
.
          PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDATE
          MATCH     ENDDATE,TODAY
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Date cannot be into the future. ";
            CALL      HITENTER
            GOTO      KDAT5000
          ENDIF
          PACK      EDOL,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.
          MATCH     STRTDATE,ENDDATE
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Dates must be in sequence. ";
            CALL      HITENTER
            GOTO      KDAT0000
          ENDIF
.
KDAT9999  RETURN
.
.
.*****************************************************************************
.*                           KGPR0000                                        *
.*                    Keyin the GP Practice codes  Range                     *
.*****************************************************************************
.
KGPR0000  MOVE      ZERO,EXIT
          MOVE      "20",ECOL
          MOVE      TEN3,EVERT
          CALL      KGPP0000
          IF        EXIT = 2
            MOVE      ONE,EXIT
            GOTO      KGPR9999
          ENDIF
.
          IF        EXIT = 1
            MOVE      ZERO,EXIT
            PACK      SGPPDESC,START,SP30
            MOVE      PMHGPRAC,SGPP
            DISPLAY   *PECOL:EVERT,*V2LON,START
          ELSE
            MOVE      ZERO,EXIT
            PACK      SGPPDESC,PMHGPRAC,SP2,PMHGPNAM
            MOVE      PMHGPRAC,SGPP
            DISPLAY   *PECOL:EVERT,*V2LON,SGPP,*HOFF,*P30:EVERT,PMHGPNAM
          ENDIF
.
          MOVE      TEN4,EVERT
          CALL      KGPP0000
          IF        EXIT = 2
            MOVE      ONE,EXIT
            GOTO      KGPR9999
          ENDIF
.
          IF        EXIT = 1
            MOVE      ZERO,EXIT
            PACK      EGPPDESC,FINISH,SP30
            MOVE      ZEDS,EGPP
            DISPLAY   *PECOL:EVERT,*V2LON,FINISH
          ELSE
            MOVE      ZERO,EXIT
            PACK      EGPPDESC,PMHGPRAC,SP2,PMHGPNAM
            MOVE      PMHGPRAC,EGPP
            DISPLAY   *PECOL:EVERT,*V2LON,EGPP,*HOFF,*P30:EVERT,PMHGPNAM
          ENDIF
.
          MATCH       SGPP,EGPP
          IF          @LESS
            DISPLAY     *P1:24,*EL,*B,"Practice codes must be in sequence. ";
            CALL        HITENTER
            DISPLAY     *P20:13,*EL,*P20:14,*EL
            GOTO        KGPR0000
          ENDIF

KGPR9999  RETURN
.
.*****************************************************************************
.*                           KGPP0000                                        *
.*                    Keyin the GP Practice codes                            *
.*****************************************************************************
KGPP0000  MOVE      ZERO,EXIT
          KEYIN     *PECOL:EVERT,*V2LON,PMHGPRAC:
                    *PECOL:EVERT,*DV,PMHGPRAC
.
          PACK      PMHGPRAC,PMHGPRAC,SP6
          MATCH     SP6,PMHGPRAC
          IF        @EQUAL
            MOVE      ONE,EXIT
            GOTO      KGPP9999
          ENDIF
.
          MATCH     EXITCHAR,PMHGPRAC
          IF        @EQUAL
            MOVE      TWO,EXIT
            GOTO      KGPP9999
          ENDIF
.
          MATCH     QUEST,PMHGPRAC
          GOTO      KGPP5000 IF EQUAL
.
          PACK      KEY12,PMHGPRAC,GPONE
          CALL      RDPMHCG1
          IF        OVRCD = 1
            BEEP
            DISPLAY   *P1:24,*EL,"Practice is not on file. ";
            CALL      HITENTER
            GOTO      KGPP0000
          ENDIF
          GOTO        KGPP9999
.
KGPP5000  CALL      GETSCR00
.
KGPP5100  PACK      KEY8,SP20
          CALL      PMSHCGDS
.
          PACK      PMHGPRAC,PMHGPRAC,SP6
          MATCH     SP6,PMHGPRAC
          IF        @EQUAL
            MOVE      ONE,EXIT
            CALL      PUTSCR00
            GOTO      KGPP9999
          ENDIF
.
          MATCH     EXITCHAR,PMHGPRAC
          IF        @EQUAL
            MOVE      TWO,EXIT
            CALL      FRESCR00
            GOTO      KGPP9999
          ENDIF
.
          PACK      KEY12,PMHGPRAC,PMHGCNTR
          CALL      RDPMHCG1
          IF        OVRCD = 1
            BEEP
            DISPLAY   *P1:24,*EL,"Practice is not on file. ";
            CALL      HITENTER
            GOTO      KGPP5100
          ENDIF
.
          CALL      PUTSCR00
.
KGPP9999  RETURN
.
.*****************************************************************************
.*                           EXTR0000                                        *
.*                        Extract the relevant data                          *
.*****************************************************************************
.
EXTR0000  DISPLAY   *P1:24,*EL,"Extracting - "
.
          MOVE      ONE,FORM1
          MOVE      SP8,DIM8
          MOVE      SP3,SAVSITE
.
          PACK      KEY34,STRTDATE,SP30
          CALL      RSOTOSF3
EXTR1000  CALL      RKOTOSF3
          BRANCH    OVRCD,EXTR9000 
.
.-------- Test if within date range -------
.
          MATCH     OTOSDATE,ENDDATE
          GOTO      EXTR9000 IF LESS
.
.-------- Open correct files ------
.
          MATCH     SAVSITE,OTOSSITE
          IF        !@EQUAL
            PACK      KEY6,OTOSSITE
            CALL      RDSITA1
            BRANCH    OVRCD,EXTR1000
.
            MOVE      OTOSSITE,SAVSITE
            PACK      CFNAME,OSTFILE,FILBOKA1
            OPEN      OUTBOKA1,CFNAME
            PACK      CFNAME,OSTFILE,SP10
            CALL      OPOTBB11
          ENDIF
.
          PACK      KEY28,OTOSSITE,OTOSCLID,OTOSDATE,OTOSTIME,SP30
          CALL      RDSBOKA1
EXTR2000  CALL      RDKBOKA1
          BRANCH    OVRCD,EXTR1000
.
.-------- Test for correct session ------
.
          PACK      DIM25,OBASITE,OBACLIN,OBADATE,OBASTRT
          MATCH     DIM25,KEY28
          GOTO      EXTR1000 IF NOT EQUAL
.
          COMPARE   FOUR,OBASTAT
          GOTO      EXTR2000 IF NOT EQUAL
.
          PACK      KEY8,OBAOUTNO
          CALL      RDBOKB1
          BRANCH    OVRCD,EXTR2000
.
          PACK      OBACLASS,OBACLASS,SP3
          MATCH     SP3,OBACLASS
          GOTO      EXTR2000 IF EQUAL
.
          PACK      OTBBGPPC,OTBBGPPC,SP6
          MATCH     SP6,OTBBGPPC
          GOTO      EXTR2000 IF EQUAL
.
.-------  Test if required GP practice range ------
.
          MATCH     SGPP,OTBBGPPC
          GOTO      EXTR2000 IF LESS
.
          MATCH     OTBBGPPC,EGPP
          GOTO      EXTR2000 IF LESS
.
          PACK      KEY5,ANSC,ANSV,OBAVISIT
          CALL      RDCODE1
          BRANCH    OVRCD,EXTR2000
.
. now write/update temp files
.
          IF        MOPTION = 1
            MOVE      OBACLASS,XX1SPEC
            PACK      KEY3,OBACLASS
            CALL      RDXXOSP1
            IF        OVRCD = 1
              MATCH     TCINDC1,ANSN
              IF        @EQUAL
                MOVE      ONE,XX1FIRST
                MOVE      ZERO,XX1REVIS
              ELSE
                MOVE      ONE,XX1REVIS
                MOVE      ZERO,XX1FIRST
              ENDIF
              CALL      WRXXOSP1
            ELSE
              MATCH     TCINDC1,ANSN
              IF        @EQUAL
                ADD       ONE,XX1FIRST
              ELSE
                ADD       ONE,XX1REVIS
              ENDIF
              CALL      UPXXOSP1
            ENDIF
          ELSE
.    update 2nd temp file option 2
            MOVE      OTBBGPPC,XX2PRAC
.           MOVE      OTBBGPPT,XX2PCCNT
            MOVE      ONE,FORM2
            MOVE      OTBBGPPT,FORM2
            MOVE      FORM2,XX2PCCNT
            MOVE      OBACLASS,XX2SPEC
            PACK      KEY15,XX2PRAC,XX2PCCNT,OBACLASS
            CALL      RDXXOGP1
            IF        OVRCD = 1
              MATCH     TCINDC1,ANSN
              IF        @EQUAL
                MOVE      ONE,XX2FIRST
                MOVE      ZERO,XX2REVIS
              ELSE
                MOVE      ONE,XX2REVIS
                MOVE      ZERO,XX2FIRST
              ENDIF
              CALL      WRXXOGP1
            ELSE
              MATCH     TCINDC1,ANSN
              IF        @EQUAL
                ADD       ONE,XX2FIRST
              ELSE
                ADD       ONE,XX2REVIS
              ENDIF
              CALL      UPXXOGP1
            ENDIF
          ENDIF
          MOVE      ZERO,FORM1
.
          UNPACK    OBADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P14:24,*EL,*V2LON,CPCDATE,SP2,OBASTRT,SP2,OBACLIN
.
          GOTO      EXTR2000
.
EXTR9000  MOVE      FORM1,EXIT
          IF        FORM1 = 1
            DISPLAY   *P1:24,*EL,"No data found. "; 
            CALL      HITENTER
          ENDIF
        
EXTR9999  RETURN
.
.*****************************************************************************
.*                           PRSP0000                                        *
.*                        Print the report                                   *
.*****************************************************************************
.
PRSP0000  MOVE      ZERO,CPAGENO
          MOVE      ONE,CNOUNDLN
          MOVE      "60",CLNO
          MOVE      ZERO,GRANDTOT
          MOVE      ZERO,TOTAL
          MOVE      ZERO,TOTFIRST
          MOVE      ZERO,TOTREVIS
.
          DISPLAY   *P1:24,*EL,"Printing....."
.
          UNPACK    SP20,KEY3
          CALL      RSXXOSP1
PRSP1000  CALL      RKXXOSP1
          BRANCH    OVRCD,PRSP9000
.
          IF        CLNO > 58 
            CALL      HEAD0000        
          ENDIF
.
          ADD       ONE,CLNO
.
          ASSIGN    (XX1FIRST + XX1REVIS),TOTAL
          CALL      FRMT0000                * Format details for printing
          PRINT     *1,"|",*3,XX1SPEC,*9,TDESC,*38,"|",*42,XX1FIRST:
                    *52,"|",*56,XX1REVIS,*66,"|",*70,TOTAL,*80,"|"
.
          ADD       TOTAL,GRANDTOT
          ADD       XX1FIRST,TOTFIRST
          ADD       XX1REVIS,TOTREVIS
          GOTO      PRSP1000
.
PRSP9000  CALL      UND80
          PRINT     *1,"Totals",*41,TOTFIRST,*55,TOTREVIS,*69,GRANDTOT
          PRINT     *41,"-------",*55,"-------",*69,"--------"
          PRINT     *N:
                    "***** End of Report *****"

PRSP9999  RETURN
.
.
.*****************************************************************************
.*                           PRGP0000                                        *
.*                        Print the report                                   *
.*****************************************************************************
.
PRGP0000  MOVE      ZERO,CPAGENO
          MOVE      ZERO,CNOUNDLN
          MOVE      "60",CLNO
          MOVE      ZERO,GRANDTOT
          MOVE      ZERO,FSTTOTAL
          MOVE      ZERO,FUPTOTAL
.
          DISPLAY   *P1:24,*EL,"Printing....."
          UNPACK    SP20,KEY15
          CALL      RSXXOGP1
          CALL      RKXXOGP1
          MOVE      XX2PRAC,SAVPRAC
.
          UNPACK    SP20,KEY15
          CALL      RSXXOGP1
PRGP1000  CALL      RKXXOGP1
          BRANCH    OVRCD,PRGP9000
.
          IF        CLNO > 56 
            MATCH     XX2PRAC,SAVPRAC
            IF        !@EQUAL
              CALL       UND132
              PRINT      *103,FSTTOTAL,*115,FUPTOTAL,*124,GRANDTOT
              PRINT      *103,"-----",*115,"-----",*124,"--------",*N
              MOVE       ZERO,GRANDTOT
              MOVE       ZERO,FSTTOTAL
              MOVE       ZERO,FUPTOTAL
              MOVE       ONE,CNOUNDLN
            ENDIF
              
            CALL      HEAD0000        
            MOVE      ZERO,CNOUNDLN
            MOVE      XX2PRAC,SAVPRAC
            MOVE      ZERO,FORM1
          ENDIF
.
          MATCH     XX2PRAC,SAVPRAC
          IF        !@EQUAL
            CALL       UND132
            PRINT      *103,FSTTOTAL,*115,FUPTOTAL,*124,GRANDTOT
            PRINT      *103,"-----",*115,"-----",*124,"--------",*N
            ADD        THREE,CLNO
            MOVE       XX2PRAC,SAVPRAC
            MOVE       ZERO,GRANDTOT
            MOVE       ZERO,FSTTOTAL
            MOVE       ZERO,FUPTOTAL
            MOVE       ZERO,FORM1
            CALL       UND132 
          ENDIF
.
          CALL      FRMT0000                * Format details for printing
.
          ASSIGN    (XX2FIRST+XX2REVIS),XX2TOTAL   * increment GRAND totals
.
          MOVE      DIM80,DIM58
          MOVE      TDESC,DESC17
          IF        FORM1=0
            PRINT   *1,"|",*3,XX2PRAC,*12,DIM58,*75,"| ",XX2SPEC,SP1,DESC17:
                    *99,"|   ",XX2FIRST,"   |   ",XX2REVIS,"   | ",XX2TOTAL:
                    *132,"|"
            MOVE    ONE,FORM1
          ELSE
            PRINT   *1,"|",*75,"| ",XX2SPEC,SP1,DESC17:
                    *99,"|   ",XX2FIRST,"   |   ",XX2REVIS,"   | ",XX2TOTAL:
                    *132,"|"
          ENDIF
.
          ADD       ONE,CLNO
          ADD       XX2TOTAL,GRANDTOT
          ADD       XX2FIRST,FSTTOTAL
          ADD       XX2REVIS,FUPTOTAL
          GOTO      PRGP1000
.
PRGP9000 
          CALL       UND132
          PRINT      *103,FSTTOTAL,*115,FUPTOTAL,*124,GRANDTOT
          PRINT      *103,"-----",*115,"-----",*124,"--------",*N
          ADD        TWO,CLNO
          MOVE       ZERO,GRANDTOT
          PRINT     *N:
                    "***** End of Report *****"

PRGP9999  RETURN
.
.*****************************************************************************
.*                           FRMT0000                                        *
.*                     Format all details to print                           *
.*****************************************************************************
.
FRMT0000  MOVE      ZERO,EXIT
          MOVE      SP20,TDESC
          IF        MOPTION = 1
            PACK      KEY5,ANSA,SP1,XX1SPEC
          ELSE
            PACK      KEY5,ANSA,SP1,XX2SPEC
          ENDIF
          CALL      RDCODE1
.
          IF        MOPTION = 2
            PACK      PMHGPNAM,SP20,SP20
            PACK      PMHGADD1,SP20,SP20
            PACK      PMHGADD2,SP20,SP20
            PACK      PMHGADD3,SP20,SP20
            PACK      PMHGADD4,SP20,SP20
            PACK      PMHCPOST,SP20
            PACK      KEY12,XX2PRAC,XX2PCCNT
            CALL      RDPMHCG1
            STRIP     PMHGPNAM
            STRIP     PMHGADD1
            STRIP     PMHGADD2
            STRIP     PMHGADD3
            STRIP     PMHGADD4
            STRIP     PMHCPOST
            PACK      DIM80,PMHGPNAM,SP1,SLASH,SP1,PMHGADD1,SP1,SLASH,SP1:
                      PMHGADD2
          ENDIF
.
          GOTO      FRMT9999
.
FRMT9000  MOVE      ONE,EXIT
.
FRMT9999  RETURN
.
.*****************************************************************************
.*                           HEAD0000                                        *
.*                        Print the report heading                           *
.*****************************************************************************
.
HEAD0000  BRANCH    MOPTION,HEAD1000,HEAD5000
          GOTO      HEAD9999
.
HEAD1000  CALL      HEAD80
.
          PRINT     *N,*20,"Period      from : ",SDOL:
                    *N,*20,"              to : ",EDOL

          PRINT     *N,*20,"GP Practice from : ",SGPPDESC:
                    *N,*20,"              to : ",EGPPDESC
          PRINT     " "
          CALL      UND80
          PRINT     *1,"|",*38,"|",*41,"FIRST":
                    *52,"|",*55,"FOLLOW-UP",*66,"|",*69,"TOTAL",*80,"|"
          PRINT     *1,"|",*3,"SPECIALITY",*38,"|",*41,"ATTENDERS":
                    *52,"|",*55,"ATTENDERS",*66,"|",*69,"ATTENDERS",*80,"|"
          CALL      UND80

          ADD       NINE,CLNO
          GOTO      HEAD9999
.
HEAD5000  CALL      HEAD132
.
          PRINT     *N,*40,"Period      from : ",SDOL:
                    *N,*40,"              to : ",EDOL
          PRINT     *N,*40,"GP Practice from : ",SGPPDESC:
                    *N,*40,"              to : ",EGPPDESC
          PRINT     " "
.
          CALL      UND132
          PRINT     *1,"|",*3,"PRACTICE",*75,"|",*99,"|   First   |":
                    " Follow-Up | ",*132,"|"
          PRINT     *1,"|",*3,"CODE",*12,"ADDRESS":
                    *75,"| CLINIC SPECIALITY",*99,"| Attenders | Attenders":
                    " | TOTAL  |"
          CALL      UND132
.
          ADD       NINE,CLNO  
.
HEAD9999  RETURN
.
.*****************************************************************************
.*                           CTBF0000                                        *
.*                        Create the 1st Tempfile                            *
.*****************************************************************************
.
CTBF0000  CALL      KTBF0000
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    CFNAMEA,CMDLINE
          IF        MOPTION = 1
            APPEND    " 12 U1-3",CMDLINE
          ELSE
            APPEND    " 24 U1-15",CMDLINE
          ENDIF
          APPEND    SP30,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          IF        MOPTION = 1
            OPEN      TMPFILE1,CFNAMEA
          ELSE
            OPEN      TMPFILE2,CFNAMEA
          ENDIF

CTBF9999  RETURN
.
.
.*****************************************************************************
.*                           KTBF0000                                        *
.*                        Kill the 1st Tempfile                              *
.*****************************************************************************
.
KTBF0000  CLOSE     TMPFILE1
          CLOSE     TMPFILE2
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    CFNAMEA,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          
KTBF9999  RETURN
.
.==========================
.
RDXXOSP1  RESET     KEY3
          MOVE      ZERO,OVRCD
          READ      TMPFILE1,KEY3;XX1SPEC,XX1FIRST,XX1REVIS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSXXOSP1  RESET     KEY3
          READ      TMPFILE1,KEY3;;         
          RETURN
.
RKXXOSP1  MOVE      ZERO,OVRCD
          READKS    TMPFILE1;XX1SPEC,XX1FIRST,XX1REVIS
          GOTO      OVERCOND IF OVER
          RETURN
.
RPXXOSP1  MOVE      ZERO,OVRCD
          READKP    TMPFILE1;XX1SPEC,XX1FIRST,XX1REVIS
          GOTO      OVERCOND IF OVER
          RETURN
.
WRXXOSP1  RESET     KEY3
          WRITE     TMPFILE1,KEY3;XX1SPEC,XX1FIRST,XX1REVIS
          RETURN
.
UPXXOSP1  UPDATE    TMPFILE1;XX1SPEC,XX1FIRST,XX1REVIS
          RETURN
.
.==========================
.
RDXXOGP1  RESET     KEY15
          MOVE      ZERO,OVRCD
          READ      TMPFILE2,KEY15;XX2PRAC,XX2PCCNT,XX2SPEC,XX2FIRST,XX2REVIS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSXXOGP1  RESET     KEY15
          READ      TMPFILE2,KEY15;;         
          RETURN
.
RKXXOGP1  MOVE      ZERO,OVRCD
          READKS    TMPFILE2;XX2PRAC,XX2PCCNT,XX2SPEC,XX2FIRST,XX2REVIS
          GOTO      OVERCOND IF OVER
          RETURN
.
RPXXOGP1  MOVE      ZERO,OVRCD
          READKP    TMPFILE2;XX2PRAC,XX2PCCNT,XX2SPEC,XX2FIRST,XX2REVIS
          GOTO      OVERCOND IF OVER
          RETURN
.
WRXXOGP1  RESET     KEY15
          WRITE     TMPFILE2,KEY15;XX2PRAC,XX2PCCNT,XX2SPEC,XX2FIRST,XX2REVIS
          RETURN
.
UPXXOGP1  UPDATE    TMPFILE2;XX2PRAC,XX2PCCNT,XX2SPEC,XX2FIRST,XX2REVIS
          RETURN
.
.*****************************************************************************
.*                            I/O Section                                    *
.*****************************************************************************
.
          INC       STD001IO
          INC       IBASEQIO/INC
          INC       PATCODIO/INC
          INC       PMSHCGIO/INC
          INC       PMSHCLIO/INC
          INC       OUTBOAIO/INC
          INC       OUTBB1IO/INC
          INC       OUTOSFIO/INC
          INC       OUTSITIO/INC
          INC       WEBERRIO/INC
.
          INC       PMSHCGDS     
          INC       TFILENAM

.*****************************************************************************
.* System    :   Patient Management System                                   *
.* Program   :   CHKPTLOC                                                    *
.* Desc      :   Check the data integrity of patlocaf records                *
.*****************************************************************************
.* Date      :   09/07/2012                                                  *
.* Author    :   Steve Armstrong    CAR 267675                               *
.* Function  :   This program will loop through the patlocaf file and for    *
.*               every record found, it will check the following things:     *
.*                 - Are the names for the patient the same as those on the  *
.*                   PMI master file.                                        *
.*                 - Is the patient currently admitted.                      *
.*               Where there are inconsistencies, they will be reported.     *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATLOCFD/INC
          INC       PATMA1FD/INC
          INC       PMSPX2FD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
ERORDESC  DIM       44
RECCOUNT  FORM      8
.
.
. PROGRAM CONSTANTS
. -----------------
PIPE      INIT      "|"
.
.
PRGID     INIT      "CHKPTLOC"
PRGNAM    INIT      "Data Integrity Report for PATLOCFD"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000       * proceed with report
.
MAIN1000  CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN2000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN2000  CALL      PROC0000               * process
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patlocaf";
          OPEN      PATLOCA1,"patlocaf"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run report                              *
.*                        TRUE  (1)  Exit option selected                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Report"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run report
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*              Loop through the location file and check names against       *
.*              the PMI master file.                                         *
.*****************************************************************************
.
PROC0000  DISPLAY   *P1:24,*EL,"Processing:";
.
          MOVE      ZERO,CPAGENO                 * initialise print variables
          MOVE      ZERO,CNOUNDLN
          CALL      IBACLOCK
          MOVE      ZERO,RECCOUNT                * initialise record count
.
          CALL      HEAD0000
.
          PACK      KEY88,SP70,SP20
          CALL      RDSLOCA1                     * position at start of file
PROC0500  CALL      RDKLOCA1                     * read next record
          BRANCH    OVRCD,PROC9000               * eof - finished.
.
          IF        (RECCOUNT%1000) = 0 | RECCOUNT = 1
            DISPLAY   *P13:24,*EL,*V2LON,LURNO,SP1,LSNAME;
          ENDIF
.
          MATCH     SP8,LURNO
          IF        @EQUAL
            MOVE      "U/R missing",ERORDESC
            CALL      PERR0000
            GOTO      PROC0500                   * get next record
          ENDIF
.
          MOVE      LURNO,KEY8
          CALL      RDMAST1                      * PMI master record found ?
          IF        OVRCD = 1
            MOVE      "PMI Master Record not on file",ERORDESC
            CALL      PERR0000
            GOTO      PROC0500                   * get next record
          ENDIF
.
          IF        PSTAT <> 0
            MOVE      "PMI Master Record not valid",ERORDESC
            CALL      PERR0000
            GOTO      PROC0500                   * get next record
          ENDIF
.
          MOVE      LURNO,KEY8
          CALL      RDPMPX21                     * PMI extension record found ?
          IF        OVRCD = 1
            MOVE      "PMI Master Extension Record not on file",ERORDESC
            CALL      PERR0000
            GOTO      PROC0500                   * get next record
          ENDIF
.
          MATCH     LSNAME,PTMASNAM              * matching surname ?
          IF        !@EQUAL
            MOVE      "Location Surname doesn't match PMI",ERORDESC
            CALL      PERR0000
            GOTO      PROC0500                   * get next record
          ENDIF
.
          MATCH     LGNAME,PMPXFNAM              * matching first given name ?
          IF        !@EQUAL
            MOVE      "Location First Given name doesn't match PMI",ERORDESC
            CALL      PERR0000
            GOTO      PROC0500                   * get next record
          ENDIF
.
          MATCH     PTLCGNM2,PMPXSNAM            * matching second given name ?
          IF        !@EQUAL
            MOVE      "Location Second Given name doesn't match PMI",ERORDESC
            CALL      PERR0000
          ENDIF
.
          GOTO      PROC0500                     * get next record
.
PROC9000  DISPLAY   *P1:24,*EL,"Data integrity check completed.  ";
          CALL      HITENTER
.
          CALL      LINE0000
          PRINT     *N,*1,"*** End of Report ***"
.
PROC9999  RETURN
+
.****************************************************************************
.*                            PERR0000                 Called by: PROC0000  *
.*                  Print an error line to the report                       *
.* Requires:  Valid read on patlocaf record                                 *
.*            ERORDESC - error description                                  *
.****************************************************************************
.
PERR0000  COMPARE   CLNO,SIXTY                   * page full ?
          CALL      HEAD0000 IF LESS             * yes
.
          PRINT     *1,PIPE,*3,LURNO,*12,PIPE,*14,LSNAME,*55,PIPE:
                    *57,ERORDESC,*132,PIPE
.
          ADD       ONE,CLNO                     * increment line count
.
PERR9999  RETURN
+
.****************************************************************************
.*                            HEAD0000                 Called by: PROC0000  *
.*                       Print page heading                                 *
.****************************************************************************
.
HEAD0000  CALL      HEAD132                      * display page header
.
          CALL      LINE0000                     * draw line across page
.
          PRINT     *1,PIPE,*3,"U/R No.",*12,PIPE,*14,"Surname",*55:
                    PIPE,*57,"Error Message",*132,PIPE
.
          CALL      LINE0000                     * draw line across page
.
          MOVE      SIX,CLNO                     * set page line count
.
HEAD9999  RETURN
+
.****************************************************************************
.*                            LINE0000                 Called by: HEAD0000  *
.*                      Draw line across page                     PROC0000  *
.****************************************************************************
.
LINE0000  PRINT     "*-----------------------------------------------":
                    "------------------------------------------------":
                    "-----------------------------------*"
.
LINE9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PATLOCIO/INC
          INC       PATMA1IO/INC
          INC       PMSPX2IO/INC

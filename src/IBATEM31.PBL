.**********************************************************************
.*                                                                    *
.* Program       : IBATEM31                                           *
.*                                                                    *
.* Description   : Master listing program for IBA header detail       *
.*                 file for user defined stationery                   *
.*                                                                    *
.* Date          : 26/03/90                                           *
.*                                                                    *
.* Author        : Neeriem Dye                                        *
.*                                                                    *
.*                                                                    *
.**********************************************************************
.* Mods     :                                                         *
.*       V10.03.01  25/10/2012  Patrick Adair           CAR 273181    *
.*                  Recompiled for IBADSTYP                           *
.*        V9.12.01  30/09/2009  Peter Vela CAR 205008                 *
.*                  Recompiled for IBADSTYP                           *
.*        V9.10.01  29/07/2008  Peter Vela CAR 151951                 *
.*                  Recompiled for IBADSTYP                           *
.*        V9.08.01  23/03/2007  Peter Vela                            *
.*                  Recompiled for IBADSTYP                           *
.*        C2.00.05  30/10/2000  Dean Cramer                           *
.*                  Include a sub totals routine for Options 1 & 2    *
.*        C2.00.04  30/10/2000  Dean Cramer  SRF 6856                 *
.*                  Fix lists' items total                            *
.*        C2.00.03  07/06/2000  Charles Handaya                       *
.*                  Added new Stationery Types (15 & 16) for Receipt  *
.*        C2.00.02  28/04/2000  Jill Habasque                         *
.*                  Fixed printing of report header                   *
.*        C2.00.01  06/04/2000  Charles Handaya                       *
.*                  Added new Stationery Types (13 & 14) for Debtor   *
.*        C2.00.00  25/02/2000  Davin                                 *
.*                  Combined release 5 & 6 (all stationery types)     *
.**********************************************************************
.*        V5.07.02  09/07/1999  Nicole Harrington                     *
.*                  Added IBASTYP7 (Emergency Form)                   *
.*        V5.07.01  01/04/1999  Davin                                 *
.*                  Added IBASTYP6 (Health Fund Claim Form)           *
.*        V5.07     06/10/98  Jim Stathopoulos                        *
.*                  507 Changes                                       *
.**********************************************************************
.*                  04/07/90 Peter Eddey                              *
.*                          Added includes to handle stationery       *
.*                          code & associated ? option                *
.*        V5.01.02  27/05/1994 Ian Rutt                               *
.*                  Fixed Global Recompile Bugs                       * 
.*        V5.02.01  02/05/1995  Steve Armstrong                       *
.*                  Recompiled for IBASTPDS                           *
.**********************************************************************
.
.$$$$$
.    Master Listing For Header Detail File ( IBATEM31 )
.    --------------------------------------------------
.
.    - Initialisation
.
.            - Display Header
.            - Open Files
.               IBATDET1
.               IBATMHA1
.               IBATMHA2
.
.    - Processing Options
.
.            - 0 = Exit
.            - 1 = Print by Stationery Code
.                  - Get Stationery Code to Print
.                  - Go to Start of Stationery Code on the File IBATMHA1
.                  - Print The Details of the Selected Stationery Code
.                  - Get and Print Header Details from the File IBATDET1 for
.                    the Chosen Stationery Code
.            - 2 = Print by Stationery Type
.                  - Get Stationery Type to Print
.                  - Go to Start of Stationery Type on the File IBATMHA2
.                  - For Each Stationery Code Linked to the Stationery Type :
.                      - Print The Details of the Current Stationery Code
.                      - Get and Print Header Details from the File IBATDET1 for
.                        the Current Stationery Code
.            - 3 = Print All Stationery Codes
.                  - Go to Start of File IBATMHA1
.                  - For Each Stationery Code 
.                      - Print The Details of the Current Stationery Code
.                      - Get and Print Header Details from the File IBATDET1 for
.                        the Current Stationery Code
.
.    - END
.$$$$$
.
          INC       STD001FD
          INC       STDHLPDF
          INC       IBATMHFD/INC
          INC       IBATMDFD/INC
          INC       IBADSTYP/INC
.**********************************************************************
.*                           CONSTANTS                                *
.**********************************************************************
.
PRGID     INIT      "IBATEM31"
PRGNAM    INIT      "Printing Details Master Listing"
VERSION   INIT      "V12.01.00"
.**********************************************************************
.*                       GLOBAL VARIABLES                             *
.**********************************************************************
.
COUNT     FORM      3
.
.
ITEMS     FORM      4
ITEMS2    FORM      4
PRTCODE   DIM       6
PRTTYPE   DIM       3
TYPENAME  DIM       30
TYPENUM   FORM      3
TYPERNGE  DIM       30
.
.**********************************************************************
.*                           MAINLINE                                 *
.**********************************************************************
.
ML0000    CALL      INIT0000               * initialization and open files
.
ML0100    CALL      OPTN0000               * select "print by" option
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000,ML2000,ML3000
.
.---- print by stat code ----
.
ML1000    CALL      CODE0000               * select code to print
          BRANCH    EXIT,ML0100            * EXIT = 1 if no type entered
          CALL      PRNT0000               * print report
          GOTO      ML0100
.
.---- print by stat type ----
.
ML2000    CALL      PTYP0000               * select type to print
          BRANCH    EXIT,ML0100            * EXIT = 1 if no type entered
          CALL      PRNT0000               * print report
          GOTO      ML0100
.
.---- print all codes ----
.
ML3000    CALL      PRNT0000               * print report
          GOTO      ML0100
.
ML9999    CHAIN      PGM                    * chain back to program
+
.**********************************************************************
.*        INIT0000                               Called by ML0000     *
.*        Initialization  Routine                                     *
.**********************************************************************
INIT0000  CALL      DISPHEAD                     * display heading
.
          DISPLAY   *P57:24,"Opening ibatmhaf";  * open header detail file
          OPEN      IBATMHA1,"ibatmhaf"
          OPEN      IBATMHA2,"ibatmhaf"
.
          DISPLAY   *P57:24,"Opening ibatmdaf";  * open printing detail file
          OPEN      IBATDET1,"ibatmdaf"
.
          MOVE      ONE,CNOUNDLN                 * don't print line at eop
          ADD       FIVE,NSORTNUM
.
INIT9999 RETURN
.****************************************************************************
.*        OPTN0000                                Called by : ML0000        *
.*        get user options or exit                                          *
.*                                                                          *
.*    valid options     --->  (0) Exit                                      *
.*                            (1) Print by Stationery Code                  *
.*                            (2) Print by Stationery Type                  *
.*                            (3) Print All Stationery Codes                *
.****************************************************************************
.
. ---- display option screen ----
.
OPTN0000  DISPLAY   *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                      *P1:5,*V2LON,ONE,*HOFF," = Print by Stationery Code":
                      *P1:6,*V2LON,TWO,*HOFF," = Print by Stationery Type":
                      *P1:7,*V2LON,THREE,*HOFF," = Print All Stationery Codes":
                      *P1:9,"Select Option : _";
          KEYIN     *P17:9,*V2LON,COPTION
          COMPARE   ZERO,COPTION
          GOTO      OPTN9000 IF EQUAL              * exit      
          COMPARE   FOUR,COPTION
          GOTO      OPTN0000 IF NOT LESS           * invalid option, re-enter
.
          MOVE      FALSE,EXIT
          GOTO      OPTN9999
.
OPTN9000  MOVE      TRUE,EXIT                      * set EXIT = 1 
.
OPTN9999  RETURN
.**************************************************************************
.*        PTYP0000                     Called by :   ML000                *
.*        Select Type to Print                                            *
.*                  Returns : PRTTYPE (type to print (SP3 for all))       *
.*                            TYPENAME (name of type)                     *
.**************************************************************************
PTYP0000  MOVE      TEN,CVERT
          MOVE      ZERO,TYPENUM
          MOVE      SP3,PRTTYPE
          DISPLAY   *P1:4,*EF
.
PTYP1000  KEYIN     *P1:CVERT,"Select Stationery Type To Print : ___":
                    *P35:CVERT,*V2LON,PRTTYPE,*EL;
          MOVE      PRTTYPE,TYPENUM              * convert to number
          PACK      PRTTYPE,PRTTYPE,SP3          * pad out with blanks
.
          MATCH     SP3,PRTTYPE
          GOTO      PTYP9000 IF EQUAL            * no type entered
.
          MATCH     "?",PRTTYPE
          GOTO      PTYP7000 IF EQUAL            * user needs help
.
          MOVE      TYPENUM,PRTTYPE              * right justify PRTTYPE
          CALL      TYPE0000                     * get TYPENAME
          BRANCH    OVRCD,PTYP8000               * illegal type entered ?
.
          MOVE      TYPENAME,TYPERNGE
          MOVE      ZERO,EXIT
          GOTO      PTYP9999                     * legal entry
.
.--- user help for stationery type ----
.
PTYP7000  CALL      QTYP0000
          MOVE      TWENTY4,CVERT
          GOTO      PTYP1000
.
.--- illegal type entered ----
.
PTYP8000  DISPLAY   *B,*P1:24,"Type Does Not Exist - ";
          CALL      HITENTER
          GOTO      PTYP1000
.
PTYP9000  MOVE      ONE,EXIT
          MOVE      SP30,TYPENAME
.
PTYP9999  RETURN
.**************************************************************************
.*        CODE0000                     Called by :   ML000                *
.*        Select Code to Print                                            *
.**************************************************************************
CODE0000  UNPACK    SP20,PRTCODE,IBTHSCOD
          DISPLAY   *P1:3,*EF
          MOVE      "35",CCOL
          MOVE      "6",CVERT
          DISPLAY   *P1:CVERT,"Select Stationery Code To Print : ";
          MOVE      ZERO,CKEYTYP
          CALL      KIBTHA00                * keyin stationery code
          BRANCH    EXIT,CODE9000,CODE9000
.
          MOVE      IBTHSCOD,PRTCODE
          MOVE      ZERO,EXIT
          GOTO      CODE9999
.
CODE9000  MOVE      ONE,EXIT
CODE9999  RETURN
.**************************************************************************
.*        PRNT0000                     Called by :   ML000                *
.*        Print report                                                    *
.*                  Requires : COPTION (1=by desc, 2=by field num)        *
.**************************************************************************
PRNT0000  DISPLAY   *P50:24,"Printing :  ";
          MOVE      ZERO,ITEMS                   * clear counter
          MOVE      ZERO,ITEMS2                  * clear counter
          MOVE      ZERO,CPAGENO                 * reset page no
          CLOCK     TIME,CTIMEIS                 * get time
.
          CALL      FILE0000                     * go to correct file position
          CALL      NREC0000                     * get next record
          CALL      HEAD0000                     * print header
          CALL      WRIT0000
          ADD       ONE,ITEMS
          ADD       ONE,ITEMS2
          ADD       ONE,CLNO 
.
PRNT0900  CALL      NREC0000                     * get next record
          BRANCH    OVRCD,PRNT9000               * end of file ?
.
          COMPARE   "55",CLNO                    * end of page ?
          CALL      HEAD0000 IF NOT LESS
          CALL      WRIT0000                     * print details
          DISPLAY   *P61:24,*V2LON,DIBDTROW,",",DIBDTCOL;
          ADD       ONE,ITEMS
          ADD       ONE,ITEMS2
          ADD       ONE,CLNO 
.
          GOTO      PRNT0900                     * start new page
.
PRNT9000  CALL      LINE0000                     * print line
          CALL      SUBT0000                     * print sub total
          PRINT     *+,*N,*12,"Total Number Of Items Printed : ",ITEMS:
                    *N,*N,"*** END OF REPORT ****"
.
PRNT9999  RETURN
.*********************************************************************
.*        HEAD0000                             Called by : PRNT0000  *
.*        Print page header                                          *
.*********************************************************************
HEAD0000  COMPARE   ZERO,CPAGENO                 * first page ?
          CALL      LINE0000 IF NOT EQUAL
          COMPARE   ZERO,CPAGENO                 * first page ?
          CALL      SUBT0000 IF NOT EQUAL
          MOVE      IBTHSTFM,TYPENUM
          CALL      TYPE0000                     * get type name
          CALL      HEAD132                      * print header
          BRANCH    COPTION,HEAD1000,HEAD2000,HEAD3000
.
.---- print by stationery code ----
.
HEAD1000  PRINT     *+,*40,"Print by Stationery Code ":
                    *N,*40,"------------------------"
          GOTO      HEAD4000 
.
.---- print by stationery type ----
.
HEAD2000  PRINT     *+,*40,"Print by Stationery Type ":
                    *N,*40,"------------------------"
          GOTO      HEAD4000 
.
.---- print all stationery codes ----
.
HEAD3000  PRINT     *+,*40,"Print All Stationery Codes":
                    *N,*40,"--------------------------"
.
HEAD4000  PRINT     *13,"Stationery Code : ",PRTCODE," - ",IBTHDESC:
                    *76,"Maximum Column Width for Printing : ",IBTHWIDT:
                    *N,*13,"Stationery Type : ",IBTHSTFM," - ",TYPENAME:
                    *76,"Paper Length Of Stationery : ",IBTHLENG
          ADD       FOUR,CLNO
          CALL      LINE0000                     * print line
          PRINT     *13,"|",*16,"Row",*21,"|",*24:
                    "Column",*32,"|",*35,"Item Type",*46,"|":
                    *49,"Text/Field Number",*96,"|",*99,"Print Length",*113,"|"
          ADD       ONE,CLNO
          CALL      LINE0000                     * print line
.
HEAD9999  RETURN
.*********************************************************************
.*        LINE0000                     Called by : PRNT0000,HEAD0000 *
.*        Print line                                                 *
.*********************************************************************
LINE0000  ADD       ONE,CLNO
          PRINT     *13,"*-------------------------------------------------":
                       "--------------------------------------------------*"
LINE9999  RETURN
.*********************************************************************
.*        SUBT0000                     Called by : HEAD0000          *
.*        Print Items Sub Total                                      *
.*********************************************************************
SUBT0000  COMPARE   "1",COPTION
          GOTO      SUBT9999 IF EQUAL

          ADD       ONE,CLNO
          PRINT     *+,*N,*12,"Sub Total Of Items Printed    : ",ITEMS2
          MOVE      ZERO,ITEMS2
SUBT9999  RETURN
.*********************************************************************
.*        WRIT0000                    Called by : PRNT0000           *
.*        Write data                                                 *
.*********************************************************************
WRIT0000  PRINT     *13,"|",*16,DIBDTROW,*21,"|",*25,DIBDTCOL,*32,"|",*37;
          BRANCH    IBDTINDC,WRIT1000,WRIT2000
.
.---- text item ---
.
WRIT1000  PRINT     "Text";
          GOTO      WRIT3000
.
.---- data item ---
.
WRIT2000  PRINT     "Data";
.
WRIT3000  PRINT     *46,"|",*49,IBDTTXFL,*96,"|",*103,IBDTPLEN,*113,"|"
.
WRIT9999  RETURN
.*********************************************************************
.*        FILE0000                    Called by : PRNT0000           *
.*        Go to first requested record on file                       *
.*                 Requires : For COPTION value of :                 *
.*                            (1) PRTCODE (code to print)            *
.*                            (2) PRTTYPE (type to print)            *
.*                  Returns : For COPTION value of :                 *
.*                            (2,3) PRTCODE (code currently being    *
.*                                 printed )                         *
.*********************************************************************
FILE0000  BRANCH    COPTION,FILE1000,FILE2000,FILE3000
.
.---- go to correct position in file (by stationery code) ----
.
FILE1000  PACK      KEY6,PRTCODE,SP6
          CALL      RSIBTH1
          PACK      KEY12,PRTCODE,SP10,SP10
          CALL      RSIBDET1
          GOTO      FILE9999
.
.---- go to correct position in file (by stationery type) ----
.
FILE2000  PACK      KEY9,PRTTYPE,SP9
          MOVE      SP6,PRTCODE                  * clear PRTCODE
          CALL      RSIBTH2
          MOVE      IBTHSCOD,PRTCODE
          PACK      KEY12,PRTCODE,SP10,SP10
          CALL      RSIBDET1                     * go to first desired record
          GOTO      FILE9999
.
.---- go to correct position in file (all codes) ----
.
FILE3000  PACK      KEY6,SP6
          MOVE      SP6,PRTCODE                  * clear PRTCODE
          CALL      RSIBTH1
          MOVE      IBTHSCOD,PRTCODE
          PACK      KEY12,PRTCODE,SP10,SP10
          CALL      RSIBDET1                     * go to first desired record
.
FILE9999  RETURN
.*********************************************************************
.*        NREC0000                    Called by : PRNT0000           *
.*        Get next record                                            *
.*                Requires : COPTION,PRTCODE,PRTTYPE                 *
.*                 Returns : OVRCD (1=no more records,0=more records)*
.*                           CLNO (=55 if new code)                  *
.*********************************************************************
NREC0000  CALL      RKIBDET1
          BRANCH    OVRCD,NREC9999               * end of file
          BRANCH    COPTION,NREC1000,NREC2000,NREC3000
.
.--- any more details for PRTCODE ----
.
NREC1000  MATCH     PRTCODE,IBDTSCOD
          GOTO      NREC9999 IF EQUAL            * record ok ?
          GOTO      NREC9000
.
.--- any more details for PRTTYPE ----
.
NREC2000  MATCH     PRTCODE,IBDTSCOD
          GOTO      NREC9999 IF EQUAL            * record ok ?
          CALL      RKIBTH2                     * get next code
          BRANCH    OVRCD,NREC9000               * no more codes
          MATCH     PRTTYPE,IBTHSTYP
          GOTO      NREC9000 IF NOT EQUAL        * no more codes for type ?
          MOVE      IBTHSCOD,PRTCODE             * store current PRTCODE
          PACK      KEY12,PRTCODE,SP10
          CALL      RSIBDET1                     * go to start of next code
          MOVE      "60",CLNO
          GOTO      NREC0000
.
.--- any more details for current PRTCODE ----
.
NREC3000  MATCH     PRTCODE,IBDTSCOD
          GOTO      NREC9999 IF EQUAL            * record ok ?
          CALL      RKIBTH1                     * get next code
          BRANCH    OVRCD,NREC9000               * no more codes
          MOVE      IBTHSCOD,PRTCODE             * store current PRTCODE
          PACK      KEY12,PRTCODE,SP10
          CALL      RSIBDET1                     * go to start of next code
          MOVE      "60",CLNO
          GOTO      NREC0000
.
NREC9000  MOVE      ONE,OVRCD                    * no more records
.
NREC9999  RETURN
.
.**************************************************************************
.*        TYPE0000                     Called by : PRNT000                *
.*        Display chosen type                                             *
.*                  Requrements : TYPENUM  for type no. to display        *
.*                  Returns     : OVRCD (=1 if no such type, =0 if ok)    *
.*                                TYPENAME (stationery type name)         *
.**************************************************************************
TYPE0000  MOVE      SP30,TYPENAME
          MOVE      ZERO,OVRCD
          COMPARE   ONE,TYPENUM
          GOTO      TYPE1000 IF LESS
.
          COMPARE   TYPENUM,NSORTNUM
          GOTO      TYPE1000 IF LESS
.
          COMPARE   FORTY,TYPENUM
          GOTO      TYPE0500 IF NOT LESS
.
          MOVE      IBASTYP0,TYPENAME
          LOAD      TYPENAME,TYPENUM,IBASTYP1,IBASTYP2,IBASTYP3,IBASTYP4:
                                     IBASTYP5,IBASTYP6,IBASTYP7,IBASTYP8:
                                     IBASTYP9,IBASTP10,IBASTP11,IBASTP12:
                                     IBASTP13,IBASTP14,IBASTP15,IBASTP16
          GOTO      TYPE9999
.
TYPE0500  MOVE      TYPENUM,NSORTYPE
          SUB       FORTY,NSORTYPE
          CALL      NSMORDES
          BRANCH    OVRCD,TYPE9999
          MOVE      NSORDESC,TYPENAME
          GOTO      TYPE9999
.
TYPE1000  MOVE      ONE,OVRCD                    * illegal type
.
TYPE9999  RETURN
.**************************************************************************
.*        QTYP0000                     Called by : STYP0000               *
.*        User help for stationery type                                   *
.**************************************************************************
QTYP0000 
          SUB        FORTY,NSORTNUM
          CALL       IBASTPDS
          ADD        FORTY,NSORTNUM
.
QTYP9999  RETURN
.********************************************************************** 
.*              I/O INCLUDES FOR FILES AND GENERAL ROUTINES           *
.**********************************************************************
.
          INC       IBASTPDS
          INC       NSMORTYP
          INC       STD001IO
          INC       STDHLPCD
          INC       IBATMHKY
          INC       IBATMHIO/INC
          INC       IBATMDIO/INC

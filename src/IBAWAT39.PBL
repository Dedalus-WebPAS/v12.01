.******************************************************************************
.* System   :  WAITING LIST                                                   *
.* Program  :  IBAWAT39                                                       *
.* Desc     :  Unscheduled Patients Report                                    *
.* Function :  Print all patients with a one (unscheduled) in either Doctor   *
.*                  Code order or Unit/Clinic order or Surname order          *
.******************************************************************************
.* Author   : Justin "I Pooch Male Dogs" Coates                               *
.* Date     : 19/07/1990                                                      *
.* Comments :                                                                 *
.* Mods     :                                                                 *
.*                                                                            *
.******************************************************************************
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899                         *
.*           Recompiled as WATTR1FD changes                                   *
.******************************************************************************
.*        V11.00.01 10/03/2020  Jill Parkinson Task 0879163                   *
.*                  Added SEXDSCIO include                                    *
.*       V10.05.01  01/08/2015  Ania P                      CAR 261630        *
.*                  Removed use of PORT for temp file naming                  *
.*        V9.03.01  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2      *
.*                  - Add Sex Description routine SEXDSC00 with Code "R" -    *
.*                  "Intersex" added                                          *
.*        V5.08.02  01/09/2000  Tonii                                         *
.*                  Mods to accept Unknown and Indeterminate as valid patient *
.*                  sex description                                           *
.*        V5.08.01  29/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.07.04  26/10/1999  J Habasque                                    *
.*                  Recompiled for WATDSPRO                                   *
.*        V5.07.03  22/09/1999  Steve Downing                                 *
.*                  Fixed position of cursor for Doctor code                  *
.*        V5.07.02  13/08/1999  Steve Downing                                 *
.*                  Mods to use DAYSEP                                        *
.*        V5.07.01  22/07/1999  Steve Downing                                 *
.*                  Included century in length of wait calculations           *
.******************************************************************************
.*        V5.04.03  26/06/1997  Howellsy         505                          *
.*                  Use PATDOCKY instead of PATDOCDS                          *
.*        V5.04.02  15/04/1997  howellsy   RHA                                *
.*                  Recompiled for WATDSPRO                                   *
.*        V5.04.01  18/12/1996  Howellsy         SRF 642436                   *
.*                  Use WATCNTCN to derive the number of cancellations        *
.******************************************************************************
.*        V5.01.01  12/10/90 Bill Dew                                         *
.*                  Fix minor bugs found in r5 testing                        *
.*        V5.01.02  19/10/90 DIG / i chung (14/11/90)                         *
.*                  Recompile for WATCATIO.INC - incorrect Keys size          *
.*        V5.01.03  27/11/90 Glen Hobby                                       *
.*                  Recompiled for changes to                                 *
.*                  PATMX1FD                                                  *
.*        V5.01.04  02.03.93 Neeriem "I Hate Support" Dye (I.B.A.)            *
.*                  Reformat print for new address lengths                    *
.*        V5.01.05  26/05/1994 Ian Rutt                                       *
.*                  Fixed Global Recompile Bugs                               * 
.*                                                                            *
.******************************************************************************
.
.         IBAWAT39.PBL        Unsheduled Patients Report
.         ============        ==========================
.         KSEQ0000 - determine which report to do
.
.         UNCL0000 - produces a report for Unit/Clinic order
.             KOPT0000 - enter the three search option limits
.             KDAT0000 - enter the from and to dates
.             KUCC0000 - enter the Unit/Clinic code
.             CLRP0000 - produce the report (both options catered for)
.
.         DOCT0000 - produces a report for Doctor code order
.             KOPT0000 - enter the three search option limits
.             KDAT0000 - enter the from and to dates
.             KDOC0000 - enter the Doctor code
.             DORP0000 - produce the report (both options catered for)
.         
.         SURN0000 - produces a report for Surname order
.             KOPT0000 - enter the three search option limits
.             KDAT0000 - enter the from and to dates
.             SURP0000 - produce the report
.
.
          INC       STD001FD                * common variables
          INC       PATCOMM/INC             * needed for 'gperd'/'patcomn3'
          INC       PATCODFD/INC            * codes file
          INC       PATDO1FD/INC            * doctors file
          INC       PATDRGFD/INC            * date ranges for gperd
          INC       PATMA1FD/INC            * master file
          INC       WATCATFD/INC            * WL category change file
          INC       WATCONFD/INC            * WL control file
          INC       WATPROFD/INC            * WL procedures file
          INC       WATTR1FD/INC            * WL treatment file
          INC       WATWTAFD/INC            * WL movement analysis file
          INC       IBASEQFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
. ------ program variables ------
.
PTCNURHA  FORM      1
CODE      DIM       2                       * PATCODDS
CODEW2    INIT      "W2"
COL       FORM      3                       * for WATDSPRO
CMDLINE   DIM       78                      * line for 'execute' command
CNTPATCL  FORM      5                       * no. patients in a unit/clinic
CNTPATTL  FORM      5                       * total no. of patients in report
CNTURG    FORM      4                       * number of urgent patients
CNTURGOV  FORM      4                       * number of overdue urgent patients
CNTURGNO  FORM      4                       * number of non-overdue urgent pats
CNTSMU    FORM      4                       * number of semi-urgent patients
CNTSMUOV  FORM      4                       * number of overdue semi-urgent pats
CNTSMUNO  FORM      4                       * number of non-overdue semi-urg pat
CNTNON    FORM      4                       * number of non-urgent patients
CNTNONOV  FORM      4                       * number of overdue non-urgent pats
CNTNONNO  FORM      4                       * number of non-overdue non-urg pats
CNTSTAGD  FORM      4                       * number of staged patients
CNTDEFD   FORM      4                       * number of deferred patients
CNTUNK    FORM      4                       * number of unknown patients
CURRDATE  DIM       8                       * current date ccyymmdd
CURDAYS   FORM      3                       * current julian day
CURCCYR   FORM      4                       * current julian year with century
DCURCCYR  DIM       4                       * current julian year with century
CURLEAP   FORM      1                       * current leap year
.
CODEDOCT  DIM       6                       * doctor code entered
CODEUNCL  DIM       3                       * unit/clinic code entered
CODEPROC  DIM       9                       * procedure code entered
DESCDOCT  DIM       65                      * doctor description
DESCUNCL  DIM       20                      * unit/clinic description
DESCPROC  DIM       50                      * procedure code description
.
DAYSODUE  FORM      4                       * number of days overdue
DIM3      DIM       3                       * PATDOCDS
DIM14     DIM       14                      * dummy variable
DIM26     DIM       26                      * holds unit : code - description
DIM31     DIM       31                      * holds doct : code - description
DISPACT   FORM      1
DOCCODE   DIM       6
.
FROMAGE   FORM      4                       * starting age
FROMDATE  DIM       8                       * from date for report ccyymmdd
FROMWAIT  FORM      4                       * starting length of wait
.
KEY21OR   DIM       21                      * original values for cat. change
KEY21NE   DIM       21                      * read in values for cat. change
.
PREVDT    DIM       6                       * previous doct
PTCNDCQS  FORM      1
QUPER     FORM      1                       * ? performed flag
.
TOAGE     FORM      4                       * ending age
TODATE    DIM       8                       * to date for report ccyymmdd
TOWAIT    FORM      4                       * ending length of wait
.
VERT      FORM      2                       * for WATDSPRO
.
WAITDAYS  FORM      3                       * days between wait and current date
WLDAYS    FORM      3                       * WL julian day
WLCCYR    FORM      4   
DWLCCYR   DIM       4   
WLLEAP    FORM      1                       * WL leap year
.
. ------ variables for the report ------
.
ADDRESS   DIM       50                      * address
PRCANC    DIM       17                      * booking cancellation reason
PRCLIN    DIM       24                      * clinic to print
PRDOCT    DIM       31                      * doctors name
PREXSR    DIM       80                      * extra search parameters values
PRFROM    DIM       30                      * from date and description
PRMETCN   DIM       12                      * metropolitan/country
PNAME     DIM       50                      * patients name
PRPRTY    DIM       8                       * priority
PRPUBPRI  DIM       9                       * public/private indicator
PRSEX     DIM       8                       * male or female           *C-49016
PRTO      DIM       30                      * to date and description
PRUNDO1   DIM       45                      * current unit/doct code
PRUNDO2   DIM       45                      * unit/doct code entered
PRUNIT    DIM       3                       * previous unit
.
. ------ variables needed for the temporary index file ------
.
CLISFILE  IFILE     SQL, FIXED=233, KEY="U57-59,113-132,1-8,9-17,18-19"
CLIDFILE  IFILE     SQL, FIXED=233, KEY="U57-59,68-75,1-8,9-17,18-19"
DOCSFILE  IFILE     SQL, FIXED=233, KEY="U51-56,113-132,1-8,9-17,18-19"
DOCDFILE  IFILE     SQL, FIXED=233, KEY="U51-56,68-75,1-8,9-17,18-19"
SURNFILE  IFILE     SQL, FIXED=233, KEY="U113-132,1-8,9-17,18-19"
.
.Name     Type      Length  Phys.  Sta-Fin
.-----------------------------------------
XWMURNO   DIM       8         8      1-8    * patients UR number
XWMCODE   DIM       9         9      9-17   * procedure code
DXWMCNT   DIM       2         2     18-19   * procedure code count/dim
XWMDESC   DIM       31        31    20-50   * procedure description
XWMDOCTO  DIM       6         6     51-56   * doctors description
XWMUNIT   DIM       3         3     57-59   * unit/clinic code
XWMSTAY   FORM      5         4     60-63   * estimated length of stay
XWMTIME   FORM      5         4     64-67   * estimated time for procedure
XWMDATE1  DIM       8         8     68-75   * date on list ccyymmdd
XWMPTY    DIM       3         3     76-78   * priority code
XWMBOOKR  DIM       3         3     79-81   * latest booking removal reason
XWMBOKDA  DIM       8         8     82-89   * latest booking removal date
XWMUDEF2  DIM       3         3     90-92   * UD field 2 (Metro/Country)
XWMDEFDA  DIM       8         8     92-100  * deferred/staged admiss date
XWTWMHOS  FORM      3         3    101-103  * number of times hosp. canc'd
XWTWMPAT  FORM      3         3    104-106  * number of times patient canc'd
XWTWMOTH  FORM      3         3    107-109  * number of times other canc'd
XWMPCAT   DIM       3         3    110-112  * patient classification code
XPSNAME   DIM       20        20   113-132  * patient surname
.
.End of Record                     233-233
.
XWMCNT    FORM      2                       * procdure code count/form
.
. ------ program constants ------
.
ALL       INIT      "All"                   * default U/C search code
CATA      INIT      "A "                    * Pat. Class category
CATBC     INIT      "BC"                    * booking cancellation reason
CATTP     INIT      "TP"                    * Priority category
CATWU     INIT      "WU"                    * U/C category
DASH      INIT      " - "                   * a dash
FILPREC   INIT      "wat39c"                * prefix for temp file clinic
FILPRED   INIT      "wat39d"                * prefix for temp file doctors
FILPRES   INIT      "wat39s"                * prefix for temp file surname
FINISH    INIT      "Finish"                * default to description
NEEDNP    FORM      "48"                    * limit when new tble headg printed
PREFDOCT  INIT      "Doctor Code : "        * Doctor Code prefix for report
PREFUNCL  INIT      "Unit/Clinic Code : "   * Unit/Clinic prefix for report
PREFFROM  INIT      "From Date on list : "  * From date prefix for report
PREFTO    INIT      "To   Date on list : "  * To date prefix for report
PRINTLIM  FORM      "55"                    * no. lines to print per page
SP50      INIT      "                                                   "
START     INIT      "Start"                 * default from value
Z8        INIT      "zzzzzzzz"              * used in previous read
.
PRGID     INIT      "IBAWAT39"
PRGNAM    INIT      "Unscheduled Patients Report"
VERSION   INIT      "V12.01.00"
.
.*********************************************************************
.*                  ML0000                                           *
.*        Mainline Code / Controlling Logic                          *
.*********************************************************************
ML0000   
          CALL      INIT0000                * initialization and open files
.
ML0500    CALL      KSEQ0000                * enter the report type
          BRANCH    MOPTION,ML1000:         * unit/clinic surname sequence
                            ML1000:         * unit/clinic date sequence
                            ML2000:         * doctor code surname sequence
                            ML2000:         * doctor code date sequence
                            ML3000          * surname sequence
.
          GOTO      ML9999
.
. ------ produce a report in Unit/Clinic sequence ------
.
ML1000    CALL      UNCL0000                * produce Unit/Clinic report
          GOTO      ML0500
.
. ------ produce a report in Doctor Code sequence ------
.
ML2000    CALL      DOCT0000                * produce Doctor Code report
          GOTO      ML0500
.
. ------ produce a report in Surname sequence ------
.
ML3000    CALL      SURN0000                * produce Surname report
          GOTO      ML0500
.
ML9999    CHAIN     PGM
+
.*********************************************************************
.*                  INIT0000                    Called by : ML0000   *
.*        Initialization and open files                              *
.*********************************************************************
INIT0000  DISPLAY   *P1:1,*EF               * clear screen
          CALL      DISPHEAD                * display program heading
          CALL      IBACLOCK                * get current date/time
          CALL      TFILENAM
.
          OPEN      CONTROLF,"controlf"     * open control file
          OPEN      PATCODE1,"patcodes"     * open codes file
          OPEN      PATDO1A1,"patdo1af"     * open doctors file
          OPEN      PATDO1A2,"patdo1af"     * open doctors file
          OPEN      PATMA1A1,"patma1af"     * open master file
          OPEN      PATMX1A1,"patmx1af"     * open master extension file
          OPEN      WATTR1A3,"wattr1af"     * WL treatment file
          OPEN      WATPROA1,"watproaf"     * WL procdure file
          OPEN      WATPROA3,"watproaf"     * WL procdure file ? option
          OPEN      WATCATA2,"watcataf"     * WL category change file
          OPEN      WATWTAA1,"watwtaaf"     * WL movement analysis file
.
          MOVE      ONE,CNEWDS              * new ? option
          READ      CONTROLF,TWENTY1;*247,PTCNDCQS
          READ      CONTROLF,THIRTY7;*173,WTCNURDY,WTCNSUDY,WTCNRODY
          READ      CONTROLF,THIRTY7;*122,WTCNTPUC,*185,WTCNTPDS
.
INIT9999  RETURN
+
.*********************************************************************
.*                  KSEQ0000                    Called by : ML0500   *
.*        Enter the sequence to report by                            *
.*        Returns : MOPTION = 0   exit selected                      *
.*                  MOPTION = 1   unit/clinic surname selected       *
.*                  MOPTION = 2   unit/clinic date selected          *
.*                  MOPTION = 3   doctor code surname selected       *
.*                  MOPTION = 4   doctor code date selected          *
.*                  MOPTION = 5   surname seq selected               *
.*********************************************************************
KSEQ0000  DISPLAY   *P1:3,*EF:
                    *V2LON,*P1:4,"0",*P1:5,"1",*P1:6,"2",*P1:7,"3":
                    *P1:8,"4",*P1:9,"5",*HOFF:
                    *P3:4,"= Exit":
                    *P3:5,"= Surname within Unit/Clinic Code Sequence":
                    *P3:6,"= Date on List within Unit/Clinic Code Sequence":
                    *P3:7,"= Surname within Doctor Code Sequence":
                    *P3:8,"= Date on List within Doctor Code Sequence":
                    *P3:9,"= Surname Sequence":
                    *P1:11,"Select Option : "
.
KSEQ1000  KEYIN     *P17:11,*DV,UNDLN1:
                    *P17:11,*V2LON,MOPTION:
                    *P17:11,*DV,MOPTION 
.
          BRANCH    MOPTION,KSEQ9999,KSEQ9999,KSEQ9999,KSEQ9999,KSEQ9999
.
          COMPARE   ZERO,MOPTION            * was 0 entered
          GOTO      KSEQ9999 IF EQUAL       * yes
.
          BEEP
          GOTO      KSEQ1000                * invalid selection
.
KSEQ9999  RETURN
+
.*********************************************************************
.*                  UNCL0000                    Called by : ML1000   *
.*        Produce a report in Unit/Clinic sequence                   *
.*********************************************************************
UNCL0000  DISPLAY   *P54:2,*V2LON,"- Unit/Clinic Code Sequence "
.
          CALL      KOPT0000                * enter the search options
          BRANCH    EXIT,UNCL9000           * exit selected
.
UNCL0500  CALL      KDAT0000                * enter the dates
          BRANCH    EXIT,UNCL0000           * no to date entered
.
          CALL      KUCC0000                * enter the Unit/Clinic Code
.
          CALL      CONTQST                 * continue question
          BRANCH    CEXIT,UNCL1000:         * Yes entered
                          UNCL0500:         * No entered
                          UNCL9000          * Cancel entered
.
UNCL1000  CALL      CLRP0000                * generate the report
.
UNCL9000  HLINE     *G37,2,54,80            * clear the sub-heading
.
UNCL9999  RETURN
+
.*********************************************************************
.*                  DOCT0000                    Called by : ML2000   *
.*        Produce a report in Doctor Code sequence                   *
.*********************************************************************
DOCT0000  DISPLAY   *P54:2,*V2LON,"- Doctor Code Sequence "
.
          CALL      KOPT0000                * enter the search options
          BRANCH    EXIT,DOCT9000           * exit selected
.
DOCT0500  CALL      KDAT0000                * enter the dates
          BRANCH    EXIT,DOCT0000           * no to date entered
.
          CALL      KDOC0000                * enter the doctors code
.
          CALL      CONTQST                 * continue question
          BRANCH    CEXIT,DOCT1000:         * Yes entered
                          DOCT0500:         * No entered
                          DOCT9000          * Cancel entered
.
DOCT1000  CALL      DORP0000                * generate the report
.
DOCT9000  HLINE     *G37,2,54,80    * clear the sub-heading
.
DOCT9999  RETURN
+
.*********************************************************************
.*                  SURN0000                    Called by : ML3000   *
.*        Produce a report in Surname sequence                       *
.*********************************************************************
SURN0000  DISPLAY   *P54:2,*V2LON,"- Surname Sequence "
.
          CALL      KOPT0000                * enter the search options
          BRANCH    EXIT,SURN9000           * exit selected
.
SURN0500  CALL      KDAT0000                * enter the dates
          BRANCH    EXIT,SURN0000           * no to date entered
.
          CALL      CONTQST                 * continue question
          BRANCH    CEXIT,SURN1000:         * Yes entered
                          SURN0500:         * No entered
                          SURN9000          * Cancel entered
.
SURN1000  CALL      SURP0000                * generate the report
.
SURN9000  HLINE     *G37,2,54,80            * clear the sub-heading
.
SURN9999  RETURN
+
.*********************************************************************
.*                  KOPT0000                    Called by : UNCL0000 *
.*        Enter the 3 search options               DOCT0000 SURN0000 *
.*********************************************************************
KOPT0000  DISPLAY   *P1:3,*EF:
                    *V2LON,*P1:4,"0",*P1:5,"1",*P1:6,"2",*P1:7,"3",*HOFF:
                    *P03:4,"= Sequence Options":
                    *P03:5,"= Age Range in Years":
                    *P03:6,"= Length of Wait in Days":
                    *P03:7,"= ",WTCNTPDS," Code"
.
          DISPLAY   *P01:9,"Select Option : "
.
KOPT1000  KEYIN     *P17:9,*DV,UNDLN1:
                    *P17:9,*V2LON,FORM1:
                    *P17:9,*DV,FORM1
.
          BRANCH    FORM1,KOPT1100,KOPT1200,KOPT1300
.
          COMPARE   ZERO,FORM1
          GOTO      KOPT9000 IF EQUAL
.
          BEEP
          GOTO      KOPT1000
.
KOPT1100  CALL      KAGE0000                * enter the age ranges
          GOTO      KOPT8000
.
KOPT1200  CALL      KWAT0000                * enter the wait ranges
          GOTO      KOPT8000
.
KOPT1300  CALL      KPRO0000                * enter the procedure code
          GOTO      KOPT8000
.
KOPT8000  MOVE      FALSE,EXIT              * set exit flag as continue
          GOTO      KOPT9999
.
KOPT9000  MOVE      TRUE,EXIT               * set exit flag as exit
.
KOPT9999  RETURN
+
.*********************************************************************
.*                  KAGE0000                    Called by : KOPT0000 *
.*        Enter the range of ages to search between                  *
.*        Returns : FROMAGE       starting age                       *
.*                  TOAGE         ending age                         *
.*********************************************************************
KAGE0000  KEYIN     *P1:11,"From Age  : ",*P1:12,"To Age    : ":
                    *P13:11,*DV,UNDLN4,*DV,SP1:
                    *P13:11,*V2LON,FROMAGE:
                    *P13:11,*DV,FROMAGE
.
          COMPARE   ZERO,FROMAGE
          GOTO      KAGE1000 IF EQUAL       * only return entered
.
. ------ valid FROM age entered ------
.
          CLEAR     PREXSR                  * clear the variable
          APPEND    "Ages : ",PREXSR        * set up the variable
          APPEND    FROMAGE,PREXSR          * set up the variable
          APPEND    " to ",PREXSR           * set up the variable
          GOTO      KAGE2000
.
. ------ only enter hit ------
.
KAGE1000  MOVE      "0000",FROMAGE          * set age to zero
          DISPLAY   *P13:11,*V2LON,START
          CLEAR     PREXSR                  * clear the variable
          APPEND    "Ages : Start to ",PREXSR   * set up the variable
.
. ------ enter the to age ------
.
KAGE2000  KEYIN     *P13:12,*DV,UNDLN4,*DV,SP2:
                    *P13:12,*V2LON,TOAGE:
                    *P13:12,*DV,TOAGE
.
          COMPARE   ZERO,TOAGE
          GOTO      KAGE4000 IF EQUAL       * only return entered
.
. ------ check that to age is after from age ------
.
          COMPARE   FROMAGE,TOAGE           * is toage < fromage
          GOTO      KAGE7000 IF LESS        * yes, invalid
.
. ------ valid TO age entered ------
.
          APPEND    TOAGE,PREXSR            * set up the variable
          APPEND    " Years",PREXSR    * BD
          RESET     PREXSR
          GOTO      KAGE9999
.
. ------ only enter hit ------
.
KAGE4000  MOVE      "9999",TOAGE            * set age to zero
          DISPLAY   *P13:12,*V2LON,FINISH
          APPEND    "Finish",PREXSR
          RESET     PREXSR
          GOTO      KAGE9999
.
. ------ ERROR: to age is less than from age ------
.
KAGE7000  DISPLAY   *P1:24,*B,"The TO age must be after the FROM age.  ";
          CALL      HITENTER
          GOTO      KAGE2000
.
KAGE9999  RETURN
+
.*********************************************************************
.*                  KWAT0000                    Called by : KOPT0000 *
.*        Enter the range of waiting days to search between          *
.*        Returns : FROMWAIT      starting wait day amount           *
.*                  TOWAIT        ending wait day amount             *
.*********************************************************************
KWAT0000  KEYIN     *P1:11,"From Wait : ",*P1:12,"To Wait   : ":
                    *P13:11,*DV,UNDLN4,*DV,SP1:
                    *P13:11,*V2LON,FROMWAIT:
                    *P13:11,*DV,FROMWAIT
.
          COMPARE   ZERO,FROMWAIT
          GOTO      KWAT1000 IF EQUAL       * only return entered
.
. ------ a day amount entered ------
.
          CLEAR     PREXSR                  * clear the variable
          APPEND    "Length of Wait : ",PREXSR        * set up the variable
          APPEND    FROMWAIT,PREXSR         * set up the variable
          APPEND    " to ",PREXSR           * set up the variable
          GOTO      KWAT2000
.
. ------ only enter hit ------
.
KWAT1000  MOVE      "0000",FROMWAIT         * set age to zero
          DISPLAY   *P13:11,*V2LON,START
          CLEAR     PREXSR                  * clear the variable
          APPEND    "Length of Wait : Start to ",PREXSR   * set up the variable
.
. ------ enter the to age ------
.
KWAT2000  KEYIN     *P13:12,*DV,UNDLN4,*DV,SP2:
                    *P13:12,*V2LON,TOWAIT:
                    *P13:12,*DV,TOWAIT
.
          COMPARE   ZERO,TOWAIT
          GOTO      KWAT4000 IF EQUAL       * only return entered
.
. ------ check that to day is after from day ------
.
          COMPARE   FROMWAIT,TOWAIT         * is today < fromday
          GOTO      KWAT7000 IF LESS        * yes, invalid
.
. ------ valid to days entered ------
.
          APPEND    TOWAIT,PREXSR           * add the extra value
          APPEND    " Days",PREXSR     * BD
          RESET     PREXSR
          GOTO      KWAT9999
.
. ------ only enter hit ------
.
KWAT4000  MOVE      "9999",TOWAIT           * set day to zero
          DISPLAY   *P13:12,*V2LON,FINISH
          APPEND    FINISH,PREXSR           * add finish value
          RESET     PREXSR
          GOTO      KWAT9999
.
. ------ ERROR: to age is less than from age ------
.
KWAT7000  DISPLAY   *P1:24,*B,"The TO length must be after the FROM length.  ";
          CALL      HITENTER
          GOTO      KWAT2000
.
KWAT9999  RETURN
+
.*********************************************************************
.*                  KPRO0000                    Called by : KOPT0000 *
.*        Enter the procedure code to search for                     *
.*        Returns : CODEPROC      the procedure code                 *
.*                  DESCPROC      the procedure description          *
.*********************************************************************
KPRO0000  MOVE      ONE,QUPER               * set ? as not performed
          MOVE      TEN3,CCOL               * column for input
          MOVE      TEN1,CVERT              * row for input
          DISPLAY   *P1:11,WTCNTPDS," : "
.
KPRO0500  KEYIN     *PCCOL:CVERT,*DV,UNDLN9:
                    *PCCOL:CVERT,*V2LON,CODEPROC:
                    *PCCOL:CVERT,*DV,CODEPROC
.
          ENDSET    CODEPROC
          APPEND    SP9,CODEPROC
          RESET     CODEPROC
.
          MATCH     SP9,CODEPROC            * was anything entered ?
          GOTO      KPRO4000 IF EQUAL       * no, use default
.
          MATCH     QUEST,CODEPROC          * was ? entered ?
          GOTO      KPRO1000 IF NOT EQUAL   * no
.
. ------ ? entered ------
.
          MOVE      ZERO,DISPACT
          CALL      WATDSPRO                * ? routine
          MOVE      TWENTY4,CVERT           * new row
          MOVE      TWENTY2,CCOL            * new col
          MOVE      ZERO,QUPER              * set ? as performed
          MOVE      SP9,CODEPROC            * clear variable
          DISPLAY   *P1:24,"    ",WTCNTPDS," Code : ",*EL
          GOTO      KPRO0500
.
. ------ validate what was entered ------
.
KPRO1000  PACK      KEY9,CODEPROC           * procedure code as key
          CALL      RDPROA1                 * read procedure file
          BRANCH    OVRCD,KPRO7000          * invalid code
.
          COMPARE   ONE,WTWPACTV            * inactive code
          GOTO      KPRO6500 IF EQUAL
.
          PACK      DESCPROC,WPDESC         * set up description
          CLEAR     PREXSR                  * clear the variable
          APPEND    WTCNTPDS,PREXSR         * add proc/treat desc
          APPEND    " Code : ",PREXSR
          APPEND    CODEPROC,PREXSR
          APPEND    SP3,PREXSR
          APPEND    WPDESC,PREXSR
          RESET     PREXSR
          GOTO      KPRO8000
.
. ------ only enter hit ------
.
KPRO4000  PACK      CODEPROC,ALL,SP6        * set up code as all
          PACK      DESCPROC,SP30,SP30      * clear description
          CLEAR     PREXSR                  * clear the variable
          APPEND    WTCNTPDS,PREXSR         * add proc/treat desc
          APPEND    " Code : ",PREXSR
          APPEND    ALL,PREXSR
          RESET     PREXSR
          GOTO      KPRO8000
.
. ------ ERROR: invalid code entered ------
KPRO6500  DISPLAY   *P1:24,*B,"Inactive Code. ",*EL;
          CALL      HITENTER
          GOTO      KPRO7200
.
KPRO7000  DISPLAY   *P1:24,*B,"Invalid ",WTCNTPDS," code entered.  ",*EL;
          CALL      HITENTER
.
KPRO7200  BRANCH    QUPER,KPRO0500          * reenter on line 16
.
          DISPLAY   *P1:24,"    ",WTCNTPDS," Code : ",*EL
          GOTO      KPRO0500
.
. ------ valid entry ------
.
KPRO8000  BRANCH    QUPER,KPRO9000          * ? not entered
.
          CALL      RDIS0000                * redisplay the screen
.
          MATCH     ALL,CODEPROC            * was anything entered ??
          GOTO      KPRO0000 IF EQUAL       * no, so re-enter code
.
          GOTO      KPRO9999
.
KPRO9000
          MATCH     ALL,CODEPROC
          GOTO      KPRO9111 IF NOT EQUAL
          DISPLAY   *P13:11,*V2LON,CODEPROC
KPRO9111
          DISPLAY   *P13:11,CODEPROC,SP1,*V2LON,DESCPROC
.
KPRO9999  RETURN
.*********************************************************************
.*                  KDAT0000                    Called by : UNCL0000 *
.*        Enter the from date and to date for the report    DOCT0000 *
.*        Returns   FROMDATE      the from date                      *
.*                  TODATE        the to date                        *
.*                  EXIT = 1      nothing entered for 'dateto'       *
.*********************************************************************
KDAT0000  DISPLAY   *P1:13,*EF:
                    *P1:14,"From Date on List : ":
                    *P1:15,"To   Date on List : "
.
. ------ set up the common parameters for keydate ------
.
          MOVE      ONE,CCANLDTE            * can cancel default
          MOVE      ONE,CDEFDTE             * hit enter once to accept default
          MOVE      ZERO,CHIGHLT            * use highlighting
          CALL      IBACLOCK                * get current date
          PACK      CURRDATE,CCC,CYY,CMM,CDD * the current date
          REP       " 0",CURRDATE           * zero fill
.
KDAT0500  MOVE      CCC,CCENT               * default to current century
          UNPACK    SP6,CYEAR,CMON,CDAY     * no default date
.
. ------ enter the from date ------
.
          MOVE      TEN4,CVERT              * row for input
          MOVE      "22",CCOL               * column for input
          CALL      KEYDATE                 * enter the date
.
          MATCH     SP2,CYEAR               * was a date entered ?
          GOTO      KDAT1000 IF EQUAL       * no, use default
.
          PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",FROMDATE           * zero fill
          RESET     FROMDATE
.
. ------ check that the date isnt into the future ------
.
          MATCH     FROMDATE,CURRDATE       * is current >= entered date ??
          GOTO      KDAT0700 IF NOT LESS    * yes
.
. ------ ERROR: From date is into the future ------
.
          DISPLAY   *P1:24,*B,"The FROM date musn't be into the future.  ",*EL;
          CALL      HITENTER
          GOTO      KDAT0500
.
KDAT0700  CALL      PACDATE                 * format date
          PACK      PRFROM,PREFFROM,CPCDATE * set up the date for printing
          RESET     PRFROM   
          GOTO      KDAT2000                * enter to date
.
. ------ use default from date ------
.
KDAT1000  DISPLAY   *PCCOL:CVERT,*V2LON,START      * display start
          PACK      FROMDATE,START,SP8      * set up the date
          RESET     FROMDATE
          PACK      PRFROM,PREFFROM,FROMDATE * set up the date for printing
          RESET     PRFROM   
.
. ------ enter the to date ------
.
KDAT2000  MOVE      TEN5,CVERT              * row for input
          MOVE      "22",CCOL               * column for input
          UNPACK    SP6,CYEAR,CMON,CDAY     * no default date
          CALL      KEYDATE                 * enter the date
.
          MATCH     SP2,CYEAR               * was a date entered ?
          GOTO      KDAT9000 IF EQUAL       * no, use default
.
          PACK      TODATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE             * zero fill
          RESET     TODATE   
.
. ------ check that the date isnt into the future ------
.
          MATCH     TODATE,CURRDATE         * is current >= entered date ??
          GOTO      KDAT2050 IF NOT LESS    * yes
.
. ------ ERROR: To date is into the future ------
.
          DISPLAY   *P1:24,*B,"The TO date mustn't be into the future.  ",*EL;
          CALL      HITENTER
          GOTO      KDAT2000
.
KDAT2050  CALL      PACDATE                 * format date
.
. ------ check that to date is after from date ------
.
          MATCH     START,FROMDATE          * was default entered
          GOTO      KDAT2100 IF EQUAL       * yes, cont do check
.
          MATCH     FROMDATE,TODATE         * is to < from date ?
          GOTO      KDAT5200 IF LESS        * yes, invalid to date
.
KDAT2100  MOVE      FALSE,EXIT              * set exit status
          PACK      PRTO,PREFTO,CPCDATE     * set up print date line
          RESET     PRTO
          GOTO      KDAT9999
.
. ------ ERROR: to date is less than from date ------
.
KDAT5200  DISPLAY   *P1:24,*B,"The TO date mustn't be before the FROM date.  ";
          CALL      HITENTER
          GOTO      KDAT2000
.
. ------ no to date entered ------
.
KDAT9000  MOVE      ONE,EXIT                * no date entered
          UNPACK    SP8,FROMDATE            * clear from date
          UNPACK    SP8,TODATE              * clear to date
.
KDAT9999  RETURN
+
.*********************************************************************
.*                  KUCC0000                    Called by : UNCL0000 *
.*        Keyin the Unit/Clinic Code                                 *
.*        Returns : CODEUNCL      the unit/clinic code               *
.*                  DESCUNCL      the unit/clinic description        *
.*********************************************************************
KUCC0000  MOVE      ONE,QUPER               * ? not performed
          MOVE      TEN7,CVERT              * row for input
          MOVE      "24",CCOL               * column for input
.
          DISPLAY   *P1:17,"For Unit/Clinic Code : "
.
KUCC1000  KEYIN     *PCCOL:CVERT,*DV,UNDLN3:
                    *PCCOL:CVERT,*V2LON,CODEUNCL:
                    *PCCOL:CVERT,*DV,CODEUNCL
.
          ENDSET    CODEUNCL
          APPEND    SP3,CODEUNCL
          RESET     CODEUNCL
.
          MATCH     SP3,CODEUNCL            * was anything entered ?
          GOTO      KUCC4000 IF EQUAL       * no
.
          MATCH     QUEST,CODEUNCL          * was ? entered ?
          GOTO      KUCC2000 IF NOT EQUAL   * no
.
. ------ ? entered ------
.
          PACK      CODE,CATWU              * set up category for codes file
          CALL      PATCODDS                * ? routine
          MOVE      ZERO,QUPER              * set ? as performed
          MOVE      "24",CVERT              * input on line 24
          MOVE      "24",CCOL               * input in col 24
          PACK      CODEUNCL,SP3            * clear the code
          DISPLAY   *P1:24,"For Unit/Clinic Code : ",*EL
          GOTO      KUCC1000

. ------ validate what was entered ------
.
KUCC2000  PACK      KEY5,CATWU,CODEUNCL     * key = cat and value entered
          CALL      RDCODE1                 * read codes file
          BRANCH    OVRCD,KUCC3000          * invalid entry
.
          MOVE      TDESC,DESCUNCL          * set up description
          PACK      PRUNDO2,PREFUNCL,CODEUNCL,DASH,TDESC * set up print heading
          RESET     PRUNDO2
          GOTO      KUCC5000
.
. ------ ERROR: invalid code entered ------
.
KUCC3000  DISPLAY   *P1:24,*B,"Invalid Unit/Clinic Code entered.  ",*EL;
          CALL      HITENTER
          PACK      CODEUNCL,SP3            * clear the code
.
          BRANCH    QUPER,KUCC1000          * ? not entered
.
          DISPLAY   *P1:24,"For Unit/Clinic Code : ",*EL
          GOTO      KUCC1000
.
. ------ only enter hit ------
.
KUCC4000  PACK      CODEUNCL,ALL,SP1        * set up default value
          PACK      DESCUNCL,SP20           * clear description
          PACK      PRUNDO2,PREFUNCL,ALL,SP30 * set up 'all' description
          RESET     PRUNDO2
.
. ------ valid entry ------
.
KUCC5000  BRANCH    QUPER,KUCC8000          * ? not entered
.
          CALL      RDIS0000                * redisplay the screen
.
          MATCH     ALL,CODEUNCL            * was anything entered ??
          GOTO      KUCC0000 IF EQUAL       * no, so re-enter
.
          DISPLAY   *P1:17,"For Unit/Clinic Code : "
.
KUCC8000  DISPLAY   *P24:17,*V2LON,CODEUNCL,*HOFF,*P40:17,DESCUNCL
.
KUCC9999  RETURN
+
.*********************************************************************
.*                  KDOC0000                    Called by : DOCT0000 *
.*        Keyin the Doctors Code                                     *
.*        Returns : CODEDOCT      the doctor code                    *
.*                  DESCDOCT      the doctor description             *
.*********************************************************************
KDOC0000  MOVE      TEN7,CVERT              * row for input
          MOVE      "24",CCOL               * column for input
.
          DISPLAY   *P1:17,"For Doctor Code      : "
.
          MOVE      SP10,CODEDOCT
          MOVE      "24",ECOL
          MOVE      "17",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          CALL      PATDOCKY
          BRANCH    EXIT,KDOC4000,KDOC0000
.
          MOVE      DCODE,CODEDOCT
.
.
KDOC2000  PACK      KEY6,CODEDOCT           * key = cat and value entered
          CALL      FDOC0000                * format the name
          PACK      DESCDOCT,PACFNAME       * store the name
          PACK      PRUNDO2,PREFDOCT,CODEDOCT,DASH,DESCDOCT * set up heading
          RESET     PRUNDO2
          GOTO      KDOC5000
.
KDOC4000  PACK      CODEDOCT,ALL,SP3        * set up default value
          PACK      DESCDOCT,SP20           * clear description
          PACK      PRUNDO2,PREFDOCT,ALL,SP30 * set up 'all' description
          RESET     PRUNDO2
.
KDOC5000  DISPLAY   *P24:17,*V2LON,CODEDOCT,*P40:17,*HOFF,DESCDOCT
.
KDOC9999  RETURN
+
.*********************************************************************
.*                  RDIS0000                    Called by : KUCC0000 *
.*        Redisplay the screen after ?                      KDOC0000 *
.*********************************************************************
RDIS0000  DISPLAY   *P1:3,*EF:
                    *P1:4,"0",*P1:5,"1",*P1:6,"2",*P1:7,"3",*HOFF:
                    *P03:4,"= Sequence Options":
                    *P03:5,"= Age Range in Years":
                    *P03:6,"= Length of Wait in Days":
                    *P03:7,"= ",WTCNTPDS," Code":
                    *P01:9,"Select Option : ",*V2LON,FORM1
.
. ------ redisplay the appropriate extra search criteria ------
.
          BRANCH    FORM1,RDIS0100,RDIS2000,RDIS4000
.
. ------ display the 'age range' values only ------
.
RDIS0100  COMPARE   "0000",FROMAGE          * is there a from age value ??
          GOTO      RDIS0500 IF NOT EQUAL   * yes
.
          DISPLAY   *P1:11,"From Age  : ",*V2LON,START
          GOTO      RDIS1000
.
RDIS0500  DISPLAY   *P1:11,"From Age  : ",*V2LON,FROMAGE
.
RDIS1000  COMPARE   "9999",TOAGE            * is there a to age value ??
          GOTO      RDIS1500 IF NOT EQUAL   * yes there is
.
          DISPLAY   *P1:12,"To Age    : ",*V2LON,FINISH
          GOTO      RDIS6000
.
RDIS1500  DISPLAY   *P1:12,"To Age    : ",*V2LON,TOAGE
          GOTO      RDIS6000
.
. ------ display the 'length of wait' values only ------
.
RDIS2000  COMPARE   "0000",FROMWAIT         * is there a from wait value ??
          GOTO      RDIS2500 IF NOT EQUAL   * yes
.
          DISPLAY   *P1:11,"From Wait : ",*V2LON,START
          GOTO      RDIS3000
.
RDIS2500  DISPLAY   *P1:11,"From Wait : ",*V2LON,FROMWAIT
.
RDIS3000  COMPARE   "9999",TOWAIT           * is there a to wait value ??
          GOTO      RDIS3500 IF NOT EQUAL   * yes there is
.
          DISPLAY   *P1:12,"To Wait   : ",*V2LON,FINISH
          GOTO      RDIS6000
.
RDIS3500  DISPLAY   *P1:12,"To Wait   : ",*V2LON,TOWAIT
          GOTO      RDIS6000
.
. ------ display the procedure information only ------
.
RDIS4000  DISPLAY   *P1:11,WTCNTPDS," : ",*V2LON,CODEPROC,*HOFF,SP1,DESCPROC
.
. ------ display the From date ------
.
RDIS6000  MATCH     START,FROMDATE          * is there a date ?
          GOTO      RDIS7500 IF NOT EQUAL   * yes
.
          DISPLAY   *P1:14,"From Date on List : ",*V2LON,START,*HOFF
          GOTO      RDIS8000

RDIS7500  UNPACK    FROMDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          DISPLAY   *P1:14,"From Date on List : ",*V2LON,CPCDATE,*HOFF
.
. ------ display the To date ------
.
RDIS8000  UNPACK    TODATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          DISPLAY   *P1:15,"To   Date on List : ",*V2LON,CPCDATE,*HOFF
.
RDIS9999  RETURN
+
.*********************************************************************
.*                  CLRP0000                    Called by : UNCL0000 *
.*        Generates the report for Unit/Clinic sequence              *
.*        There are two cases :                                      *
.*           MOPTION = 1 - will produce patients in surname order for*
.*                         the specified clinic b/w the entered dates*
.*           MOPTION = 2 - will produce patients for the clinic b/w  *
.*                         the entered dates (in any surname order)  *
.*********************************************************************
CLRP0000  CALL      CRTC0000                * create temp file
          CALL      CLCN0000                * clear the count variables
          DISPLAY   *P1:24,"Obtaining the valid entries now.",*EL
.
. ------ start looping through 3rd index of treatment file ------
.
CLRP1000  MATCH     START,FROMDATE          * do we start at beginning ?
          GOTO      CLRP1500 IF NOT EQUAL   * no, we have a date
.
          PACK      KEY27,SP8,SP8,SP9,SP2   * start at beginning
          CALL      RDSTREA3                * positional read
          GOTO      CLRP2000                * continue
.
CLRP1500  PACK      KEY27,FROMDATE,SP8,SP9,SP2 * start at entered date
          CALL      RDSTREA3                * positional read
.
. -----------------------------------------
. ------ next read on treatment file ------
. -----------------------------------------
.
CLRP2000  CALL      RDKTREA3                * next read
          BRANCH    OVRCD,CLRP5000          * finished report
.
          DISPLAY   *P70:24,WMURNO          * display UR number so shows working
.
CLRP2500  CALL      VADA0000                * validate the data
          BRANCH    EXIT,CLRP2000:          * invalid data
                         CLRP5000           * at end of date range so finished
.
. --------------------------------------------------------------------
. ------ have a correct unit/clinic so write a temp file record ------
. --------------------------------------------------------------------
.
CLRP3000  CALL      WATCNTCN                * get the booking canc. stats
.
. ------ write to the temp file ------
.
CLRP3500  PACK      KEY30,WMUNIT,WMDATE1,WMURNO,WMCODE,DWMCNT
          MOVE      WMDESC,XWMDESC          * shorten the description
          MOVE      WMCNT,DWMCNT            * set up as a dim
          WRITE     CLIDFILE,KEY30;WMURNO,WMCODE,DWMCNT,XWMDESC,WMDOCTOR,WMUNIT:
                                  WMSTAY,WMTIME,WMDATE1,WMPTY,WTCACODT,WTCAEDAT:
                                   WMUDEF2,WTWMDEDA,XWTWMHOS,XWTWMPAT,XWTWMOTH:
                                   WMPCAT,PSNAME
.
          DISPLAY   *P70:24,*V2LON,WMURNO   * highlight UR to show valid person
.
          GOTO      CLRP2000                * read next record
.
. -----------------------------------------------------------
. ------ have finished looping through treatment file, ------
. ------ start doing the report by reading temp file   ------
. ------ there are two possible indexes to read on     ------
. -----------------------------------------------------------
.
CLRP5000  CALL      IBACLOCK                * get current date and time
          MOVE      ZERO,CPAGENO            * initialize page number
          MOVE      ZERO,CNTPATTL           * clear total count
          MOVE      ZERO,CNTPATCL           * clear patient count
          MOVE      "Unit/Clinc Code Seq.",CPHDROPT
          DISPLAY   *P1:24,"Producing the report now.....",*EL
.
. ------ do an initial positional read then next read to obtain ------
. ------ the first clinics name before the heading is printed   ------
.
          BRANCH    MOPTION,CLRP5025,CLRP5050
.
. ------ do the read on the surname index ------
.
CLRP5025  PACK      KEY42,SP30,SP10,SP2     * key to position at start
          READ      CLISFILE,KEY42;;        * positional read
.
          READKS    CLISFILE;XWMURNO,XWMCODE,DXWMCNT,XWMDESC,XWMDOCTO,XWMUNIT:
                             XWMSTAY,XWMTIME,XWMDATE1,XWMPTY,XWMBOOKR,XWMBOKDA:
                             XWMUDEF2,XWMDEFDA,XWTWMHOS,XWTWMPAT,XWTWMOTH:
                             XWMPCAT,XPSNAME
          GOTO      CLRP7000 IF OVER
          MOVE      DXWMCNT,XWMCNT 
          GOTO      CLRP5075
.
. ------ do the read on the date index ------
.
CLRP5050  PACK      KEY30,SP30              * key to position at start
          READ      CLIDFILE,KEY30;;        * positional read
.
          READKS    CLIDFILE;XWMURNO,XWMCODE,DXWMCNT,XWMDESC,XWMDOCTO,XWMUNIT:
                             XWMSTAY,XWMTIME,XWMDATE1,XWMPTY,XWMBOOKR,XWMBOKDA:
                             XWMUDEF2,XWMDEFDA,XWTWMHOS,XWTWMPAT,XWTWMOTH:
                             XWMPCAT,XPSNAME
          GOTO      CLRP7000 IF OVER
          MOVE      DXWMCNT,XWMCNT 
.
. ------ stores the first unit's name ------
.
CLRP5075  PACK      KEY5,CATWU,XWMUNIT      * unit as the key
          CALL      RDCODE1                 * read codes file
          PACK      PRUNDO1,PREFUNCL,XWMUNIT,DASH,TDESC * unit name
          RESET     PRUNDO1
          MOVE      XWMUNIT,PRUNIT          * update previous unit
.
          CALL      NEWP0000                * print new page and table heading
          GOTO      CLRP5500                * continue
.
. -------------------------------------------
. ------ read next record of temp file ------
. -------------------------------------------
.
CLRP5100  BRANCH    MOPTION,CLRP5200:       * read using surname index
                            CLRP5300:       * read using date index
                            CLRP9999,CLRP9999,CLRP9999 * invalid values
.
. ------ next read of the surname index ------
.
CLRP5200  READKS    CLISFILE;XWMURNO,XWMCODE,DXWMCNT,XWMDESC,XWMDOCTO,XWMUNIT:
                             XWMSTAY,XWMTIME,XWMDATE1,XWMPTY,XWMBOOKR,XWMBOKDA:
                             XWMUDEF2,XWMDEFDA,XWTWMHOS,XWTWMPAT,XWTWMOTH:
                             XWMPCAT,XPSNAME
          GOTO      CLRP9000 IF OVER
          MOVE      DXWMCNT,XWMCNT 
          GOTO      CLRP5400
.
. ------ next read of the date index ------
.
CLRP5300  READKS    CLIDFILE;XWMURNO,XWMCODE,DXWMCNT,XWMDESC,XWMDOCTO,XWMUNIT:
                             XWMSTAY,XWMTIME,XWMDATE1,XWMPTY,XWMBOOKR,XWMBOKDA:
                             XWMUDEF2,XWMDEFDA,XWTWMHOS,XWTWMPAT,XWTWMOTH:
                             XWMPCAT,XPSNAME
          GOTO      CLRP9000 IF OVER
          MOVE      DXWMCNT,XWMCNT 
.
. ------ see if it is a different clinic ------
.
CLRP5400  MATCH     PRUNIT,XWMUNIT          * is previous same as current ??
          GOTO      CLRP5500 IF EQUAL       * yes, dont need a new heading
.
. ---------------------------------------------------------------
. ------ have a new clinic type (only applies when ALL)    ------
. ------ do underlines for last clinic, give patient count ------
. ------ set up new clinic name, do new table heading      ------
. ------ update the previous clinic with the new one       ------
. ---------------------------------------------------------------
.
          PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*"
          UNPACK    PRUNDO1,DIM19,DIM26     * get the two parts
          PRINT     *N,"Total Patients Printed for ",DIM26," is ",CNTPATCL
          ADD       THREE,CVERT             * increment line counter
          MOVE      ZERO,CNTPATCL           * clear patient count
.
. ------ get the new units details ------
.
          PACK      KEY5,CATWU,XWMUNIT      * unit as the key
          CALL      RDCODE1                 * read codes file
          PACK      PRUNDO1,PREFUNCL,XWMUNIT,DASH,TDESC * store new unit
          RESET     PRUNDO1
          MOVE      XWMUNIT,PRUNIT          * update previous unit
.
. ------ see if there is enough room on the page for new table heading ------
.
          COMPARE   CVERT,NEEDNP            * will table fit on page ??
          GOTO      CLRP5450 IF NOT LESS    * yes, print new table heading only
.
          CALL      NEWP0000                * print new page and table heading
          GOTO      CLRP5500
.
CLRP5450  CALL      PTBH0000                * print new table
.
. ------ obtain the current information to display ------
.
CLRP5500  CALL      FINF0000                * format the information
.
. ------ print the line but first see if it will fit on page ------
.
          COMPARE   CVERT,PRINTLIM          * is new page needed ??
          GOTO      CLRP6000 IF NOT LESS    * no
.
CLRP5600  CALL      NEWP0000                * print new page heading
.
CLRP6000  CALL      PPAT0000                * print the line
          GOTO      CLRP5100                * read next record
.
. ------ there is no data obtained for this report ------
.
CLRP7000  CALL      NEWP0000                * print a page heading
          PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*":
                    *N,*N,"There are no patients for the entered values."
          GOTO      CLRP9050
.
. --------------------------------
. ------ report is finished ------
. --------------------------------
.
CLRP9000  UNPACK    PRUNDO1,DIM19,DIM26     * get the two parts
          PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*":
                    *N,*N,"Total Patients Printed for ",DIM26," : ",CNTPATCL:
                    *N,*N,"Grand Total Patients Printed ",SP20,SP4," : ":
                    CNTPATTL
          CALL      STPR0000                * prints the stats information
.
CLRP9050  PRINT     *N,*N,*N,"***  END OF REPORT ****"
.
          CALL      DETP0000                * delete the temp file
          MOVE      "UNSCHEDULED PATIENTS REPORT   ",PRGNAM  * get back name
.
CLRP9999  RETURN
+
.*********************************************************************
.*                  DORP0000                    Called by : DOCT0000 *
.*        Generates the report for Doctor Code sequence              *
.*        There are two cases :                                      *
.*           MOPTION = 3 - will produce patients in surname order for*
.*                         the specified doctor b/w the entered dates*
.*           MOPTION = 4 - will produce patients for the doctor b/w  *
.*                         the entered dates (in any surname order)  *
.*********************************************************************
DORP0000  CALL      CRTD0000                * create temp file
          CALL      CLCN0000                * clear the count variables
          DISPLAY   *P1:24,"Obtaining the valid entries now.",*EL
.
. ------ start looping through 3rd index of treatment file ------
.
DORP1000  MATCH     START,FROMDATE          * do we start at beginning ?
          GOTO      DORP1500 IF NOT EQUAL   * no, we have a date
.
          PACK      KEY27,SP8,SP8,SP9,SP2   * start at beginning
          CALL      RDSTREA3                * positional read
          GOTO      DORP2000                * continue
.
DORP1500  PACK      KEY27,FROMDATE,SP8,SP9,SP2 * start at entered date
          CALL      RDSTREA3                * positional read
.
. -----------------------------------------
. ------ next read on treatment file ------
. -----------------------------------------
.
DORP2000  CALL      RDKTREA3                * next read
          BRANCH    OVRCD,DORP5000          * finished report
.
          DISPLAY   *P70:24,WMURNO          * display UR number so shows working
.
DORP2500  CALL      VADA0000                * validate the data
          BRANCH    EXIT,DORP2000:          * invalid data
                         DORP5000           * at end of date range so finished
.
. --------------------------------------------------------------------
. ------ have a correct doctor code so write a temp file record ------
. --------------------------------------------------------------------
.
DORP3000  CALL      WATCNTCN                * get the cancellation stats
.
DORP3500  PACK      KEY33,WMDOCTOR,WMDATE1,WMURNO,WMCODE,DWMCNT
          MOVE      WMDESC,XWMDESC          * shorten the description
          MOVE      WMCNT,DWMCNT            * set up as a dim
          WRITE     DOCDFILE,KEY33;WMURNO,WMCODE,DWMCNT,XWMDESC,WMDOCTOR,WMUNIT:
                                  WMSTAY,WMTIME,WMDATE1,WMPTY,WTCACODT,WTCAEDAT:
                                   WMUDEF2,WTWMDEDA,XWTWMHOS,XWTWMPAT,XWTWMOTH:
                                   WMPCAT,PSNAME
.
          DISPLAY   *P70:24,*V2LON,WMURNO   * highlight UR to show valid person
.
          GOTO      DORP2000                * read next record
.
. -----------------------------------------------------------
. ------ have finished looping through treatment file, ------
. ------ start doing the report by reading temp file   ------
. ------ there are two possible indexes to read on     ------
. -----------------------------------------------------------
.
DORP5000  CALL      IBACLOCK                * get current date and time
          MOVE      ZERO,CPAGENO            * initialize page number
          MOVE      ZERO,CNTPATTL           * clear total count
          MOVE      ZERO,CNTPATCL           * clear patient count
          MOVE      "Doctor Code Sequence",CPHDROPT
          DISPLAY   *P1:24,"Producing the report now.....",*EL
.
. ------ do an initial positional read then next read to obtain ------
. ------ the first clinics name before the heading is printed   ------
.
          BRANCH    MOPTION,DORP9999,DORP9999,DORP5025,DORP5050
.
. ------ do the read on the surname index ------
.
DORP5025  PACK      KEY45,SP30,SP10,SP5     * key to position at start
          READ      DOCSFILE,KEY45;;        * positional read
.
          READKS    DOCSFILE;XWMURNO,XWMCODE,DXWMCNT,XWMDESC,XWMDOCTO,XWMUNIT:
                             XWMSTAY,XWMTIME,XWMDATE1,XWMPTY,XWMBOOKR,XWMBOKDA:
                             XWMUDEF2,XWMDEFDA,XWTWMHOS,XWTWMPAT,XWTWMOTH:
                             XWMPCAT,XPSNAME
          GOTO      DORP7000 IF OVER
          MOVE      DXWMCNT,XWMCNT 
          GOTO      DORP5075
.
. ------ do the read on the date index ------
.
DORP5050  PACK      KEY33,SP30,SP3          * key to position at start
          READ      DOCDFILE,KEY33;;        * positional read
.
          READKS    DOCDFILE;XWMURNO,XWMCODE,DXWMCNT,XWMDESC,XWMDOCTO,XWMUNIT:
                             XWMSTAY,XWMTIME,XWMDATE1,XWMPTY,XWMBOOKR,XWMBOKDA:
                             XWMUDEF2,XWMDEFDA,XWTWMHOS,XWTWMPAT,XWTWMOTH:
                             XWMPCAT,XPSNAME
          GOTO      DORP7000 IF OVER
          MOVE      DXWMCNT,XWMCNT 
.
. ------ stores the first doctors name ------
.
DORP5075  PACK      KEY6,XWMDOCTO           * doct as the key
          CALL      FDOC0000                * get docs name
          PACK      PRUNDO1,PREFDOCT,XWMDOCTO,DASH,PACFNAME * doct name
          RESET     PRUNDO1
          MOVE      XWMDOCTO,PREVDT         * update previous unit
.
          CALL      NEWP0000                * print new page and table heading
          GOTO      DORP5500                * continue
.
. -------------------------------------------
. ------ read next record of temp file ------
. -------------------------------------------
.
DORP5100  BRANCH    MOPTION,DORP9999,DORP9999: * invalid values
                            DORP5200:       * read using surname index
                            DORP5300:       * read using date index
                            DORP9999        * invalid value
.
. ------ next read of the surname index ------
.
DORP5200  READKS    DOCSFILE;XWMURNO,XWMCODE,DXWMCNT,XWMDESC,XWMDOCTO,XWMUNIT:
                             XWMSTAY,XWMTIME,XWMDATE1,XWMPTY,XWMBOOKR,XWMBOKDA:
                             XWMUDEF2,XWMDEFDA,XWTWMHOS,XWTWMPAT,XWTWMOTH:
                             XWMPCAT,XPSNAME
          GOTO      DORP9000 IF OVER
          MOVE      DXWMCNT,XWMCNT 
          GOTO      DORP5400
.
. ------ next read of the date index ------
.
DORP5300  READKS    DOCDFILE;XWMURNO,XWMCODE,DXWMCNT,XWMDESC,XWMDOCTO,XWMUNIT:
                             XWMSTAY,XWMTIME,XWMDATE1,XWMPTY,XWMBOOKR,XWMBOKDA:
                             XWMUDEF2,XWMDEFDA,XWTWMHOS,XWTWMPAT,XWTWMOTH:
                             XWMPCAT,XPSNAME
          GOTO      DORP9000 IF OVER
          MOVE      DXWMCNT,XWMCNT 
.
. ------ see if it is a different doctor ------
.
DORP5400  MATCH     PREVDT,XWMDOCTO         * is previous same as current ??
          GOTO      DORP5500 IF EQUAL       * yes, dont need a new heading
.
. ---------------------------------------------------------------
. ------ have a new doctor code (only applies when ALL)    ------
. ------ do underlines for last clinic, give patient count ------
. ------ set up new doctor name, do new table heading      ------
. ------ update the previous doctor with the new one       ------
. ---------------------------------------------------------------
.
          PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*"
          UNPACK    PRUNDO1,DIM14,DIM31     * get the two parts
          PRINT     *N,"Total Patients Printed for ",DIM31," is ",CNTPATCL
          ADD       THREE,CVERT             * increment line counter
          MOVE      ZERO,CNTPATCL           * clear patient count
.
. ------ get the new docts details ------
.
          PACK      KEY6,XWMDOCTO           * doct as the key
          CALL      FDOC0000                * get docs name
          PACK      PRUNDO1,PREFDOCT,XWMDOCTO,DASH,PACFNAME * doct name
          RESET     PRUNDO1
          MOVE      XWMDOCTO,PREVDT         * update previous unit
.
. ------ see if there is enough room on the page for new table heading ------
.
          COMPARE   CVERT,NEEDNP            * will table fit on page ??
          GOTO      DORP5450 IF NOT LESS    * yes, print new table heading only
.
          CALL      NEWP0000                * print new page and table heading
          GOTO      DORP5500
.
DORP5450  CALL      PTBH0000                * print new table
.
. ------ obtain the current information to display ------
.
DORP5500  CALL      FINF0000                * format the data to be printed
.
. ------ print the line but first see if it will fit on page ------
.
          COMPARE   CVERT,PRINTLIM          * is new page needed ??
          GOTO      DORP6000 IF NOT LESS    * no
.
DORP5600  CALL      NEWP0000                * print new page heading
.
DORP6000  CALL      PPAT0000                * print the line
          GOTO      DORP5100                * read next record
.
. ------ there is no data obtained for this report ------
.
DORP7000  CALL      NEWP0000                * print a page heading
          PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*":
                    *N,*N,"There are no patients for the entered values."
          GOTO      DORP9050
.
. --------------------------------
. ------ report is finished ------
. --------------------------------
.
DORP9000  UNPACK    PRUNDO1,DIM14,DIM31     * get the two parts
          PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*":
                    *N,*N,"Total Patients Printed for ",DIM31," : ",CNTPATCL:
                    *N,*N,"Grand Total Patients Printed ",SP20,SP9," : ":
                    CNTPATTL
          CALL      STPR0000                * prints the stats information
.
DORP9050  PRINT     *N,*N,*N,"***  END OF REPORT ****"
.
          CALL      DETP0000                * delete the temp file
          MOVE      "UNSCHEDULED PATIENTS REPORT   ",PRGNAM  * get back name
.
DORP9999  RETURN
+
.*********************************************************************
.*                  SURP0000                    Called by : SURN0000 *
.*        Produce the surname report                                 *
.*        Will show all patients in surname order from all clinics   *
.*        between the specified dates                                *
.*********************************************************************
SURP0000  CALL      CRTS0000                * create temp file
          CALL      CLCN0000                * clear the count variables
          DISPLAY   *P1:24,"Obtaining the valid entries now.",*EL
.
. ------ start looping through 3rd index of treatment file ------
.
SURP1000  MATCH     START,FROMDATE          * do we start at beginning ?
          GOTO      SURP1500 IF NOT EQUAL   * no, we have a date
.
          PACK      KEY27,SP8,SP8,SP9,SP2   * start at beginning
          CALL      RDSTREA3                * positional read
          GOTO      SURP2000                * continue
.
SURP1500  PACK      KEY27,FROMDATE,SP8,SP9,SP2 * start at entered date
          CALL      RDSTREA3                * positional read
.
. -----------------------------------------
. ------ next read on treatment file ------
. -----------------------------------------
.
SURP2000  CALL      RDKTREA3                * next read
          BRANCH    OVRCD,SURP5000          * finished report
.
          DISPLAY   *P70:24,WMURNO          * display UR number so shows working
.
SURP2500  CALL      VADA0000                * validate the data
          BRANCH    EXIT,SURP2000:          * invalid data
                         SURP5000           * at end of date range so finished
.
. --------------------------------------------------------------
. ------ have a correct value so write a temp file record ------
. --------------------------------------------------------------
.
SURP3000  CALL      WATCNTCN                * get the booking canc stats
.
SURP3500  PACK      KEY39,PSNAME,WMURNO,WMCODE,DWMCNT
          MOVE      WMDESC,XWMDESC          * shorten the description
          MOVE      WMCNT,DWMCNT            * set up as a dim
          WRITE     SURNFILE,KEY39;WMURNO,WMCODE,DWMCNT,XWMDESC,WMDOCTOR,WMUNIT:
                                  WMSTAY,WMTIME,WMDATE1,WMPTY,WTCACODT,WTCAEDAT:
                                   WMUDEF2,WTWMDEDA,XWTWMHOS,XWTWMPAT,XWTWMOTH:
                                   WMPCAT,PSNAME
.
          DISPLAY   *P70:24,*V2LON,WMURNO   * highlight UR to show valid person
.
          GOTO      SURP2000                * read next record
.
. -----------------------------------------------------------
. ------ have finished looping through treatment file, ------
. ------ start doing the report by reading temp file   ------
. -----------------------------------------------------------
.
SURP5000  CALL      IBACLOCK                * get current date and time
          MOVE      ZERO,CPAGENO            * initialize page number
          MOVE      ZERO,CNTPATTL           * clear total count
          MOVE      ZERO,CNTPATCL           * clear patient count
          MOVE      "Surname Sequence    ",CPHDROPT
          DISPLAY   *P1:24,"Producing the report now.....",*EL
          UNPACK    SP50,PRUNDO1            * clear the unit/doctor type
          UNPACK    SP50,PRUNDO2            * clear the unit/doctor type
          CALL      NEWP0000                * print new page
.
          PACK      KEY39,SP30,SP9          * key for initial read
          READ      SURNFILE,KEY39;;        * positional read
.
. -------------------------------------------
. ------ read next record of temp file ------
. -------------------------------------------
.
SURP5100  READKS    SURNFILE;XWMURNO,XWMCODE,DXWMCNT,XWMDESC,XWMDOCTO,XWMUNIT:
                             XWMSTAY,XWMTIME,XWMDATE1,XWMPTY,XWMBOOKR,XWMBOKDA:
                             XWMUDEF2,XWMDEFDA,XWTWMHOS,XWTWMPAT,XWTWMOTH:
                             XWMPCAT,XPSNAME
          GOTO      SURP9000 IF OVER
          MOVE      DXWMCNT,XWMCNT 
.
. ------ obtain the current information to display ------
.
SURP5500  CALL      FINF0000                * format the information
.
. ------ print the line but first see if it will fit on page ------
.
          COMPARE   CVERT,PRINTLIM          * is new page needed ??
          GOTO      SURP6000 IF NOT LESS    * no
.
SURP5600  CALL      NEWP0000                * print new page heading
.
SURP6000  CALL      PPAT0000                * print the line
          GOTO      SURP5100                * read next record
.
. ------ there is no data obtained for this report ------
.
SURP7000  CALL      NEWP0000                * print a page heading
          PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*":
                    *N,*N,"There are no patients for the entered values."
          GOTO      SURP9050
.
. --------------------------------
. ------ report is finished ------
. --------------------------------
.
SURP9000  PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*":
                    *N,*N,"Grand Total Patients Printed ",SP20,SP9," : ":
                    CNTPATTL
          CALL      STPR0000                * prints the stats information
.
SURP9050  PRINT     *N,*N,*N,"***  END OF REPORT ****"
.
          CALL      DETP0000                * delete the temp file
          MOVE      "UNSCHEDULED PATIENTS REPORT   ",PRGNAM  * get back name
.
SURP9999  RETURN
+
.*********************************************************************
.*                  RAGE0000                    Called by : CLRP0000 *
.*        Determines if patients age is in the allowed      DORP0000 *
.*        range as entered                                  SURP0000 *
.*        Returns : EXIT = 1      age not in the range               *
.*********************************************************************
RAGE0000  PACK      KEY8,WMURNO             * UR number a skey
          CALL      RDMAST1                 * read master file for patients age
.
. ------ allowable values : lower limit <= patients age <= upper limit ------
. ------ invalid values   : patients age < lower limit ------
. ------ invalid values   : patients age > upper limit ------
.
          COMPARE   FROMAGE,PSAGE           * is patients age < lower limit ??
          GOTO      RAGE9000 IF LESS        * yes, invalid age
.
          COMPARE   PSAGE,TOAGE             * is upper limit < patients age ??
          GOTO      RAGE9000 IF LESS        * yes, invalid age
.
          MOVE      FALSE,EXIT              * age is in the correct range
          GOTO      RAGE9999
.
RAGE9000  MOVE      TRUE,EXIT               * age not in the range
.
RAGE9999  RETURN
+
.*********************************************************************
.*                  RWAT0000                    Called by : CLRP0000 *
.*        Determines if days waiting is in the allowed      DORP0000 *
.*        range as entered                                  SURP0000 *
.*        Returns : EXIT = 1      day not in the range               *
.*********************************************************************
RWAT0000  CALL      DADW0000                * determine # days on the list
.
. ------ allowable values : lower limit <= days waiting <= upper limit ------
. ------ invalid values   : days waiting < lower limit ------
. ------ invalid values   : days waiting > upper limit ------
.
          COMPARE   FROMWAIT,WAITDAYS       * is days waiting < lower limit ??
          GOTO      RWAT9000 IF LESS        * yes, invalid days
.
          COMPARE   WAITDAYS,TOWAIT         * is upper limit < days waiting ??
          GOTO      RWAT9000 IF LESS        * yes, invalid days
.
          MOVE      FALSE,EXIT              * days is in the correct range
          GOTO      RWAT9999
.
RWAT9000  MOVE      TRUE,EXIT               * days not in the range
.
RWAT9999  RETURN
+
.*********************************************************************
.*                  RPRO0000                    Called by : CLRP0000 *
.*        Determines if the procedure is same as entered    DORP0000 *
.*        Returns : EXIT = 1      procedure incorrect       SURP0000 *
.*********************************************************************
RPRO0000  MATCH     ALL,CODEPROC            * are all procedures valid ??
          GOTO      RPRO8000 IF EQUAL       * yes, so valid
.
          MATCH     CODEPROC,WMCODE         * is the read in code valid ??
          GOTO      RPRO8000 IF EQUAL       * yes
.
          MOVE      TRUE,EXIT               * invalid procedure code
          GOTO      RPRO9999
.
RPRO8000  MOVE      FALSE,EXIT              * valid procedure code
.
RPRO9999  RETURN
+
.********************************************************************
.*                  DADW0000                   Called by : RWAT0000 *
.*        Determines how many days a patient has been on the list   *
.*        Paras   : WMDATE1       the waiting list date             *
.*        Returns : CURDAYS       current julian day                *
.*                  CURYR         current julian year               *
.*                  CURLEAP       if curr. is a leap year = 1       *
.*                  WLDAYS        WL julian day                     *
.*                  WLYR          WL julian year                    *
.*                  WLLEAP        WL leap year                      *
.*                  WAITDAYS      days between wait and current     *
.********************************************************************
DADW0000  
.
.------ turn the current date into julian date ------
.
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
          DAYSEP    WMDATE1,CURRDATE,WAITDAYS
.
DADW9999  RETURN
+
.*********************************************************************
.*                  FINF0000                    Called by : CLRP0000 *
.*        Format the information for printing     DORP0000  SURP0000 *
.*********************************************************************
FINF0000  PACK      KEY8,XWMURNO            * UR number as key
          CALL      RDMAST1                 * read master file
          MOVE      PTITL,PACTITLE          * title
          MOVE      PGNAME,PACGNAME         * given name
          MOVE      PSNAME,PACSNAME         * surname
          MOVE      TWO,PACFRMT             * format as Surname, Title Given
          CALL      PACNAME                 * format the name
          PACK      PNAME,PACFNAME          * store the name
          RESET     PNAME
.
          CALL      ADDR0000                * format the address
.
. ------ format the priority description ------
.
          PACK      KEY5,CATTP,XWMPTY       * key of priority
          CALL      RDCODE1                 * read codes file
          PACK      PRPRTY,TDESC            * get the description
          RESET     PRPRTY
          MOVE      XWMDATE1,WMDATE1        * set up waiting list date
          CALL      STAT0000                * update the priority stats
.
. ------ format the date on list ------
.
          UNPACK    XWMDATE1,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
. ------ format the doctors name ------
.
          PACK      KEY6,XWMDOCTO           * docs name as key
          CALL      FDOC0000                * get docs name
          MOVE      PACFNAME,PRDOCT         * set up docs name
.
. ------ format the unit/clinic ------
.
          PACK      KEY5,CATWU,XWMUNIT      * key of unit
          CALL      RDCODE1                 * read codes file
          PACK      PRCLIN,XWMUNIT,SP1,TDESC
.
FINF9999  RETURN
+
.*********************************************************************
.*                  STAT0000                    Called by : CLRP0000 *
.*        Updates the statistics on the number of urgent,   DORP0000 *
.*        non-urgent and semi-urgernt patients also the     SURP0000 *
.*        number of staged, deferred and unknown patients            *
.*        according to their priority codes                          *
.*        Paras   : XWMPTY        priority code                      *
.*        Updates : CNTURG        number of urgent patients          *
.*                  CNTURGOV      number of overdue urgent patients  *
.*                  CNTURGNO      number of non-overdue urgent pats  *
.*                  CNTSMU        number of semi-urgent patients     *
.*                  CNTSMUOV      number of overdue semi-urgent pats *
.*                  CNTSMUNO      number of non-overdue semi-urg pats*
.*                  CNTNON        number of non-urgent patients      *
.*                  CNTNONOV      number of overdue non-urgent pats  *
.*                  CNTNONNO      number of non-overdue non-urg pats *
.*                  CNTSTAGD      number of staged patients          *
.*                  CNTDEFD       number of deferred patients        *
.*                  CNTUNK        number of unknown patinets         *
.*                  DAYSODUE      number of days overdue             *
.*********************************************************************
STAT0000  PACK      KEY5,CATTP,XWMPTY       * key of priority
          CALL      RDCODE1                 * read codes file
          BRANCH    OVRCD,STAT9999          * invalid priority
.
          MATCH     ANSD,TCINDC2            * is the second ind a D ??
          GOTO      STAT0500 IF NOT EQUAL   * no.
.
. ------ update the number of deferred patients ------
.
          ADD       ONE,CNTDEFD             * increment amount
          MOVE      ZERO,DAYSODUE           * clear overdue variable
          GOTO      STAT9999
.
STAT0500  MATCH     ANST,TCINDC2            * is the second ind a T ??
          GOTO      STAT1000 IF NOT EQUAL   * no.
.
. ------ update the number of staged patients ------
.
          ADD       ONE,CNTSTAGD            * increment amount
          MOVE      ZERO,DAYSODUE           * clear overdue variable
          GOTO      STAT9999
.
. ------ determine if patient priority is urgent ------
.
STAT1000  MATCH     ANSU,TCINDC1            * is first ind a U ??
          GOTO      STAT2000 IF NOT EQUAL   * no.
.
. ------ update the number of urgent patients ------
.
          ADD       ONE,CNTURG              * increment amount
          CALL      DADW0000                * calc days on waiting list
          COMPARE   WAITDAYS,WTCNURDY       * is limit < days waiting ??
          GOTO      STAT1500 IF LESS        * yes.
.
. ------ update the number of non--overdue urgent patients ------
.
          ADD       ONE,CNTURGNO            * increment amount
          MOVE      ZERO,DAYSODUE           * clear overdue variable
          GOTO      STAT9999
.
. ------ update the number of overdue urgent patients ------
.
STAT1500  ADD       ONE,CNTURGOV            * increment amount
          MOVE      WAITDAYS,DAYSODUE       * determine days overdue
          SUB       WTCNURDY,DAYSODUE       * days overdue
          GOTO      STAT9999
.
. ------ determine if patient priority is semi-urgent ------
.
STAT2000  MATCH     ANSS,TCINDC1            * is first ind a S ??
          GOTO      STAT3000 IF NOT EQUAL   * no.
.
. ------ update the number of semi-urgent patients ------
.
          ADD       ONE,CNTSMU              * increment amount
          CALL      DADW0000                * calc days on waiting list
          COMPARE   WAITDAYS,WTCNSUDY       * is limit < days waiting ??
          GOTO      STAT2500 IF LESS        * yes.
.
. ------ update the number of non--overdue semi-urgent patients ------
.
          ADD       ONE,CNTSMUNO            * increment amount
          MOVE      ZERO,DAYSODUE           * clear overdue variable
          GOTO      STAT9999
.
. ------ update the number of overdue semi-urgent patients ------
.
STAT2500  ADD       ONE,CNTSMUOV            * increment amount
          MOVE      WAITDAYS,DAYSODUE       * determine days overdue
          SUB       WTCNSUDY,DAYSODUE       * days overdue
          GOTO      STAT9999
.
. ------ determine if patient priority is non-urgent ------
.
STAT3000  MATCH     ANSR,TCINDC1            * is first ind a R ??
          GOTO      STAT4000 IF NOT EQUAL   * no.
.
. ------ update the number of non-urgent patients ------
.
          ADD       ONE,CNTNON              * increment amount
          CALL      DADW0000                * calc days on waiting list
          COMPARE   WAITDAYS,WTCNRODY       * is limit < days waiting ??
          GOTO      STAT3500 IF LESS        * yes.
.
. ------ update the number of non--overdue non-urgent patients ------
.
          ADD       ONE,CNTNONNO            * increment amount
          MOVE      ZERO,DAYSODUE           * clear overdue variable
          GOTO      STAT9999
.
. ------ update the number of overdue non-urgent patients ------
.
STAT3500  ADD       ONE,CNTNONOV            * increment amount
          MOVE      WAITDAYS,DAYSODUE       * determine days overdue
          SUB       WTCNRODY,DAYSODUE       * days overdue
          GOTO      STAT9999
.
. ------ update the number of unknown patients ------
.
STAT4000  ADD       ONE,CNTUNK              * increment amount
          MOVE      ZERO,DAYSODUE           * clear overdue variable
.
STAT9999  RETURN
+
.*********************************************************************
.*                  FDOC0000                    Called by : lots     *
.*        Obtain the doctors name and format it                      *
.*        Paras   : KEY6          contains the doctors code          *
.*        Returns : PACFNAME      doctors name (65 chars)            *
.*********************************************************************
FDOC0000  PACK      PACFNAME,SP30,SP30,SP5  * clear the variable
          CALL      RDDOCT1                 * read doct file
          BRANCH    OVRCD,FDOC9999          * invalid code
          MOVE      DTITL,PACTITLE          * title
          MOVE      DGNAME,PACGNAME         * given name
          MOVE      DSNAME,PACSNAME         * surname
          MOVE      TWO,PACFRMT             * format as Surname, Title Given
          CALL      PACNAME                 * format the name
.
FDOC9999  RETURN
+
.**********************************************************************
.*                             ADDR0000                               *
.*                     Pack Patients Address                          *
.**********************************************************************
.
ADDR0000
          PACK      ADDRESS,SP70
          CLEAR     ADDRESS
.
          MATCH     SP70,PADD1
          IF        !@EQUAL
            STRIP     PADD1
            APPEND    PADD1,ADDRESS
            APPEND    ",",ADDRESS
          ENDIF
.
          MATCH     SP70,PADD2
          IF        !@EQUAL
            STRIP     PADD2
            APPEND    PADD2,ADDRESS
            APPEND    ",",ADDRESS
          ENDIF
.
          MATCH     SP70,PSUBRB
          IF        !@EQUAL
            STRIP     PSUBRB
            APPEND    PSUBRB,ADDRESS
            APPEND    ",",ADDRESS
          ENDIF
.
          MATCH     SP70,PADD4
          IF        !@EQUAL
            STRIP     PADD4
            APPEND    PADD4,ADDRESS
            APPEND    " ",ADDRESS
          ENDIF
.
          MATCH     SP70,PPOST
          IF        !@EQUAL
            SQUEEZE   PPOST
            APPEND    PPOST,ADDRESS
          ENDIF
.
          RESET     ADDRESS
.
          RETURN
+
.*********************************************************************
.*                  VADA0000                    Called by : CLRP0000 *
.*        Does the validation of record read in             DORP0000 *
.*        Returns : EXIT = 1      invalid record            SURP0000 *
.*                  EXIT = 2      at end of date range               *
.*********************************************************************
VADA0000  PACK      KEY8,WMURNO             * UR number as key
          CALL      RDMAST1                 * read master file for surname
          BRANCH    OVRCD,VADA9000          * invalid UR so dont use
.
. ------ check that still in the date range ------
.
          MATCH     WMDATE1,TODATE          * is dateto < file date ??
          GOTO      VADA9500 IF LESS        * yes, finished report
.
. ------ check that have the correct status of 1 ------
.
          BRANCH    WMSTAT,VADA3000         * a status of one, unscheduled
.
          GOTO      VADA9000                * not the correct status
.
. ------ now check that we are in the correct subrange ------
.
VADA3000  CALL      SUBV0000                * validate the subrange
          BRANCH    EXIT,VADA9000           * invalid item so ignore
.
. ------ check on the unit or doctor ------
.
.    12/10/90 BD
          BRANCH    MOPTION,VADA4000,VADA4000,VADA5000,VADA5000,VADA8000
.
. ------ check that have the correct unit/clinic ------
.
VADA4000  MATCH     ALL,CODEUNCL            * are we using all codes ??
          GOTO      VADA8000 IF EQUAL       * yes, use this value
.
          MATCH     CODEUNCL,WMUNIT         * is it the correct unit ???
          GOTO      VADA9000 IF NOT EQUAL   * no, read next record
.
          GOTO      VADA8000                * valid record
.
. ------ check that have the correct doctor code ------
.
VADA5000  MATCH     ALL,CODEDOCT            * are we using all docts ??
          GOTO      VADA8000 IF EQUAL       * yes, use this value
.
          MATCH     CODEDOCT,WMDOCTOR       * is it the correct doct ???
          GOTO      VADA9000 IF NOT EQUAL   * no, read next record
.
VADA8000  MOVE      FALSE,EXIT              * valid data
          GOTO      VADA9999
.
VADA9000  MOVE      TRUE,EXIT               * invalid data
          GOTO      VADA9999
.
VADA9500  MOVE      TWO,EXIT                * at end of date range
.
VADA9999  RETURN
.
.*********************************************************************
.*                  SUBV0000                    Called by : CLRP0000 *
.*        Validates that item read in is in the correct     DORP0000 *
.*        subrange as entered                               SURP0000 *
.*        Returns : EXIT = 1      item is invalid                    *
.*********************************************************************
SUBV0000  BRANCH    FORM1,SUBV4100:         * using age subrange
                          SUBV4400:         * using days waiting subrange
                          SUBV4700          * using procedure subrange
.
. ------ check that the age is in the correct range ------
.
SUBV4100  CALL      RAGE0000                * determine if in correct range
          BRANCH    EXIT,SUBV9000           * not in the correct range
          GOTO      SUBV8000                * valid age
.
. ------ check that the days waiting is in the correct range ------
.
SUBV4400  CALL      RWAT0000                * validate the days waiting
          BRANCH    EXIT,SUBV9000           * invalid day
          GOTO      SUBV8000                * valid day
.
. ------ check that it is the correct procedure code ------
.
SUBV4700  CALL      RPRO0000                * determine if proc. is valid
          BRANCH    EXIT,SUBV9000           * invalid code
.
SUBV8000  MOVE      FALSE,EXIT              * valid item
          GOTO      SUBV9999
.
SUBV9000  MOVE      TRUE,EXIT               * invalid code
.
SUBV9999  RETURN
+
.*********************************************************************
.*                  NEWP0000                    Called by : CLRP0000 *
.*        Prints the new page heading for a report          DORP0000 *
.*        Para's  : CPHRDOPT      sub heading                        *
.*********************************************************************
NEWP0000  MOVE      ZERO,CNOUNDLN           * underlines at end of page
          MOVE      "UNSCHEDULED PATIENTS REPORT -",PRGNAM
          RESET     PRGNAM
.
NEWP0500  CALL      HEAD132                 * do the new heading
          PRINT     PREXSR                  * print the extra search variables
.
          PRINT     *1,PRFROM,*45,PRUNDO2   * from date, search type
          PRINT     *1,PRTO                 * to date
.
. ------ print the table heading ------
.
          MOVE      SEVEN,CVERT             * set line counter
          CALL      PTBH0000                * print a table heading
.
NEWP9999  RETURN
+
.*********************************************************************
.*                  PPAT0000                    Called by : CLRP0000 *
.*        Prints a single data line of the report           DORP0000 *
.*        ie. the information about a patients procedure             *
.*        Updates : CNTPATCL      count of pats                      *
.*                  CNTPATTL      count of total patients            *
.*                  CVERT         current row number                 *
.*********************************************************************
PPAT0000  PRINT     *1,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*":
                    *N,"|",XWMURNO,*12,PNAME,*62,"|",XWMDESC:
                    *94,"|Added   : ",CPCDATE,*132,"|"
.
. -------------------------------------------
. ------ print line two about patient ------
. -------------------------------------------
.
PPAT2500  PRINT     *1,"|",*12,ADDRESS,*62,"|",PRDOCT,*94,"|Priority: ";
.
. ------ determine if a staged or deferred admission ------
.
          PACK      KEY5,CATTP,XWMPTY       * key of priority
          CALL      RDCODE1                 * read codes file
          BRANCH    OVRCD,PPAT3100          * not on file
.
          MATCH     ANST,TCINDC2            * is it a staged admission ??
          GOTO      PPAT2700 IF EQUAL       * yes
.
          MATCH     ANSD,TCINDC2            * is it a deferred admission
          GOTO      PPAT3100 IF NOT EQUAL   * no
.
          PRINT     "Deferred Admiss. ";
          GOTO      PPAT2900
.
PPAT2700  PRINT     "Staged Admission ";
.
PPAT2900  UNPACK    XWMDEFDA,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *121,CPCDATE,*132,"|"
          GOTO      PPAT5000
.
PPAT3100  PRINT     PRPRTY,SP1,"O/Due:",DAYSODUE," Days",*132,"|"
.
. -------------------------------------------
. ------ print line three about patient ------
. -------------------------------------------
.
PPAT5000  PRINT     *1,"|",*12,"Home ",PTELEP,SP1,"Bus ",PTELEB:
                    *62,"|",PRCLIN,*94,"|";
.
. ------ see if there is a booking cancellation reason ------
.
          MATCH     SP3,XWMBOOKR            * is there a booking canc. reason ??
          GOTO      PPAT5500 IF EQUAL       * no, dont display anything
.
. ------ display the stats on booking cancellations ------
.
          PRINT     "Cancel  : Hos=",XWTWMHOS," Pat=",XWTWMPAT:
                    " Oth=",XWTWMOTH;
          GOTO      PPAT5500
.
PPAT5500  PRINT     *132,"|"
.
. -------------------------------------------
. ------ print line four about patient ------
. -------------------------------------------
.
PPAT7500  PACK      KEY5,CATA,XWMPCAT       * get whether public or private
          CALL      RDCODE1                 * read codes file
          MOVE      "(Public) ",PRPUBPRI    * set up as public
.
          MATCH     "1",TCINDC1             * is indicator = 1 ?
          GOTO      PPAT7700 IF EQUAL       * yes, so keep as public
.
          MOVE      "(Private)",PRPUBPRI    * set as private
.
PPAT7700  MOVE      SP8,PRSEX
.* ****************************************** Start of Changes         *C-49016
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          MOVE      DSEXDESC,PRSEX
.* ******************************************   End of Changes         *C-49016
.
PPAT7900  PRINT     *1,"|",*2,PRPUBPRI,*12,"Sex: ",PRSEX,*25,"DOB: ";
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * format the date
.
          PRINT     CPCDATE,*42,"M/C: ";
.
          MOVE      SP20,PRMETCN       * clear description
          MATCH     SP3,XWMUDEF2
          GOTO      PPAT8100 IF EQUAL
          MOVE      "Unknown     ",TDESC    * default to metropolitan
          PACK      KEY5,CODEW2,XWMUDEF2
          CALL      RDCODE1
          MOVE      TDESC,PRMETCN
.
PPAT8100  PRINT     PRMETCN,*62,"|Stay : ",XWMSTAY,SP2,"Duration :":
                    XWMTIME,*94,"|";
.
. ------ determine if a there is a latest booking cancellation ------
.
          MATCH     SP3,XWMBOOKR            * is there a booking canc ??
          GOTO      PPAT8300 IF EQUAL       * no.
.
          UNPACK    XWMBOKDA,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * format the cancellation date
          MOVE      SP20,TDESC              * default if read fails
          PACK      KEY5,CATBC,XWMBOOKR     * key of booking canc reason
          CALL      RDCODE1                 * read codes file
          MOVE      TDESC,PRCANC            * set up reason
          PRINT     "Latest  : ",CPCDATE," ",PRCANC;
.
PPAT8300  PRINT     *132,"|"
.
          DISPLAY   *P70:24,XWMURNO         * display UR number to show working
.
. ------ increment the counters ------
.
PPAT9000  ADD       FIVE,CVERT              * add to line counter
          ADD       ONE,CNTPATCL            * add to patient count for clinic
          ADD       ONE,CNTPATTL            * add to total pat. count
.
PPAT9999  RETURN
+
.*********************************************************************
.*                  PTBH0000                    Called by : NEWP0000 *
.*        Prints a new table heading                        CLRP0000 *
.*        Needed at a new page or for a new unit/clinic     DORP0000 *
.*        Paras/Returns : CVERT      current row number              *
.*********************************************************************
PTBH0000  PRINT     *N,PRUNDO1:
                    *N,"*-----------------------------------------------":
                       "------------------------------------------------":
                       "-----------------------------------*":
                    *N,*1,"|",*3,"U/R No ",*12,"Personal Details ":
                    *62,"|Medical Details ",*94,"|Waiting List Details":
                    *132,"|"
.
          ADD       FOUR,CVERT              * add to line count
.
PTBH9999  RETURN
+
.*********************************************************************
.*                  CLCN0000                    Called by : CLRP0000 *
.*        Clear the satistic count variables       DORP0000 SURP0000 *
.*********************************************************************
CLCN0000  MOVE      ZERO,CNTURG             * number of urgent patients
          MOVE      ZERO,CNTURGOV           * overdue urgent
          MOVE      ZERO,CNTURGNO           * non-overdue urgent
.
          MOVE      ZERO,CNTSMU             * number of semi-urgent patients
          MOVE      ZERO,CNTSMUOV           * overdue semi-urgent
          MOVE      ZERO,CNTSMUNO           * non-overdue semi-urgent
.
          MOVE      ZERO,CNTNON             * number of non-urgent patients
          MOVE      ZERO,CNTNONOV           * overdue non-urgent
          MOVE      ZERO,CNTNONNO           * non-overdue non-urgent
.
          MOVE      ZERO,CNTSTAGD           * number of staged patients
          MOVE      ZERO,CNTDEFD            * number of deferred patients
          MOVE      ZERO,CNTUNK             * number of unknown patients
.
CLCN9999  RETURN
+
.*********************************************************************
.*                  STPR0000                    Called by : CLRP0000 *
.*        Prints the stats regarding priorities             DORP0000 *
.*        at the end of the report                          SURP0000 *
.*********************************************************************
STPR0000  PRINT     *N,"Report Summary :":
                    *N,*N,"Urgent",*16,CNTURG,*25,"Semi-urgent ",*39,CNTSMU:
                    *49,"Non-urgent ",*63,CNTNON,*73,"Staged ",CNTSTAGD:
                    *90,"Deferred ",CNTDEFD,*109,"Unknown ",CNTUNK:
                    *N,*2,"Overdue",*16,CNTURGOV,*27,"Overdue",*39,CNTSMUOV:
                    *51,"Overdue",*63,CNTNONOV,*N,*2,"Not Overdue",*16,CNTURGNO:
                    *27,"Not Overdue",*39,CNTSMUNO,*51,"Not Overdue ",CNTNONNO
.
STPR9999  RETURN
+
.*********************************************************************
.*                  CRTC0000                    Called by : CLRP0000 *
.*        Create temporary file for Unit/Clinic sequence             *
.*        Need two indexes for the two available options             *
.*********************************************************************
CRTC0000  CALL      DETP0000                  * delete the temp file
          DISPLAY   *V2LON,*BLINKON,*P1:24,"Please wait.",*EL
.
          CLEAR     CMDLINE                 
          APPEND    "isbuild ",CMDLINE  
          APPEND    TFILNAME,CMDLINE    
          APPEND    " 233 U57-59,113-132,1-8,9-17,18-19",CMDLINE
          APPEND    " U57-59,68-75,1-8,9-17,18-19",CMDLINE 
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID            * create temp file
          OPEN      CLISFILE,TFILNAME         * open temp. file index 1
          OPEN      CLIDFILE,TFILNAME         * open temp. file index 2
.
CRTC9999  RETURN
+
.*********************************************************************
.*                  CRTD0000                    Called by : DORP0000 *
.*        Create temporary file for Doctor sequence                  *
.*        Needs two indexes for the two available options            *
.*********************************************************************
CRTD0000  CALL      DETP0000                  * delete the temp file
          DISPLAY   *V2LON,*BLINKON,*P1:24,"Please wait.",*EL
          CLEAR     CMDLINE              
          APPEND    "isbuild ",CMDLINE   
          APPEND    TFILNAME,CMDLINE       
          APPEND    " 233 U51-56,113-132,1-8,9-17,18-19",CMDLINE  
          APPEND    " U51-56,68-75,1-8,9-17,18-19",CMDLINE  
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID            * create temp file
          OPEN      DOCSFILE,TFILNAME         * open temp. file index 1
          OPEN      DOCDFILE,TFILNAME         * open temp. file index 2
.
CRTD9999  RETURN
+
.*********************************************************************
.*                  CRTS0000                    Called by : SURP0000 *
.*        Create temporary file for Surname sequence                 *
.*********************************************************************
CRTS0000  CALL      DETP0000                  * delete the temp file
          DISPLAY   *V2LON,*BLINKON,*P1:24,"Please wait.",*EL
          CLEAR     CMDLINE               
          APPEND    "isbuild ",CMDLINE    
          APPEND    TFILNAME,CMDLINE        
          APPEND    " 233 U113-132,1-8,9-17,18-19",CMDLINE 
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID            * create temp file
          OPEN      SURNFILE,TFILNAME         * open temp. file
.
CRTS9999  RETURN
+
.*********************************************************************
.*                  DETP0000                    Called by : CLRP0000 *
.*        Delete temporary file                    SURP0000 DORP0000 *
.*********************************************************************
DETP0000  CLEAR     CMDLINE                   
          APPEND    "iserase ",CMDLINE        
          APPEND    TFILNAME,CMDLINE          
          RESET     CMDLINE
          CLOSE     CLISFILE                * close temp file so that the
          CLOSE     CLIDFILE                * iserase will properly kill
          CLOSE     DOCSFILE                
          CLOSE     DOCDFILE                 
          CLOSE     SURNFILE                
          EXECUTE   CMDLINE,TASKID          * execute the delete command
.
DETP9999  RETURN
.
. ------ I/O routines needed ------
.
          INC       STD001IO                * standard IO routines
          INC       PATCOMN3                * used for 'gperd'
          INC       PATCODKY                * ? routine on codes file
          INC       PATDOCKY                * ? routine on doctors file
          INC       WATDSPRO                * ? on procedures
          INC       SEXDSCIO
          INC       PATDRGIO/INC            * date ranges for gperd
          INC       PATCODIO/INC            * codes file
          INC       PATDO1IO/INC            * doctors file
          INC       PATMA1IO/INC            * master file
          INC       WATCATIO/INC            * WL category change file
          INC       WATPROIO/INC            * WL procedures file
          INC       WATTR1IO/INC            * WL treatment file
          INC       WATWTAIO/INC            * WL movement analysis file
          INC       WATCNTCN                * WL Cancellations            
          INC       TFILENAM
          INC       IBASEQIO/INC
          INC       WEBERRIO/INC
 

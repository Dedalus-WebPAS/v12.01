.******************************************************************************
.*                                                                            *
.*                        P R O G R A M   S U M M A R Y                       *
.*                        -------------   -------------                       *
.*                                                                            *
.*     PROGRAM NAME:   IBABOK11                                               *
.*                                                                            *
.*     FUNCTION:       BOOKINGS ENQUIRY                                       *
.*                                                                            *
.*     DATE WRITTEN:   10/02/1988                                             *
.*                                                                            *
.*     AUTHOR:         MALCOLM HAYSE                                          *
.*                                                                            *
.*     MODIFICATIONS: 
.*        V9.03.01  29/08/2003 J.Tan   CAR 42933                              *
.*                  Recompiled for PATSRCH                                    *
.*        V9.03.00  02/07/2003  Tonii  CAR 40436 (THC60)                      *
.*                  Ported program from r5.10                                 *
.*                  Remove all references to PATSUR file (old surname file).  *
.*                  Removed PTCNSNDX parameter                                *
.******************************************************************************
.*        V11.02.01 10/02/2022  Thanh T       TSK 0889899                     *
.*                  Recompiled as WATTR1FD changes                            *
.*        V5.08.01  21/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.07.03  02/09/1999  Jill Habasque                                 *
.*                  Changed to use POPTION instead of OPTION as ? search      *
.*                  was changing the value                                    *
.*        V5.07.02  12/08/1999  Jill Habasque SRF 633665                      *
.*                  Fixed display of century                                  *
.*                  V5.07.01  21/04/1999  Jim Stathopoulos                    *
.*                            Fixed ? keyword doctor code display             *
.*                  V5.07.B01 15/01/1999  Nicole Harrington 507 rebound 081   *
.*                            Mods to display century on dates                *
.                   22/10/1998  Glenn Berry      5.07 changes
.******************************************************************************
.*                                                                            *
.*                  V5.01.01  29/06/89 M.Ohleiter                             *
.*                            Updates for Release 5.01                        *
.*                            13/07/89 K.Peak                                 *
.*                            Fixed sreen layout                              *
.*                            11/08/89 M.Ohleiter       SRF 101671            *
.*                            Fixed book date display                         *
.*                            21/08/89 S. O'Gorman                            *
.*                            Added U/R Number to Screen                      *
.*                  V5.01.02  25/10/89 K.Peak           SRF 102267            *
.*                            Updated BOKMASFD with the current changes       *
.*                  V5.01.03  12/12/89 K.Peak           SRF 103377            *
.*                            Recompiled for 5th index in BOKMASFD            *
.*                  V5.01.04  06/02/90 Steve O'Gorman                         *
.*                            Added Patient Alerts                            *
.*                  V5.01.05  23/03/90 J.Tan            SRF 103967            *
.*                            Fixed display ward description                  *
.*                                                                            *
.*        V5.01.15  30/05/1990  P Duncan                                      *
.*                  Converted to new system specification                     *
.*                  29/05/1990  Bill Dew                                      *
.*                  Fixed minor screen problems                               *
.*                  09/09/1990  Bill Dew                                      *
.*                  Attempt to fix ? U/R Number surname search and fix I*U    * 
.*                  error after ? on booking type                             *
.*                  13/08/1990  Bill Dew                                      *
.*                  Fixed extraction routine to display patient name as       *
.*                  surname, title, given name and display Booking Type code  *
.*                  description.  In extraction routine for options 1, 2, 3,  *
.*                  and 4 display only booked patients.                       *
.*       V5.01.121  24/01/1991  Peter Eddey                                   *
.*                  Changed to use BOKRC1FD instead of BOKMASFD.              *
.*                  Bill was correct - some of this code is absolute Sadness  *
.*                  Who is the idiot using KEY variables to display dates and *
.*                  other data in - general opinion suggests that they have   *
.*                  TOTALLY LOST IT !!!  This is very uncool coding.          *
.*       V5.01.122  22/05/1991  Steve O'Gorman                                *
.*                  Fixed errors found by the new Compiler                    *
.*       V5.01.123  15/10/1992  i chung          SRF 117464                   *
.*                  Modified to display all patients irrespective of status & *
.*                  "By Booking Date" option to input date range instead of   *
.*                  single date.  Added "Status" column.                      *
.*                  Also fixed "?" redisplay problem with "Doctor Code" option*
.*       V5.01.124  22/04/93 DIG                                              *
.*                  Re-Compiled for changes to PATALERT.                      *
.*       V5.01.125  24.08/93 ROD                                              *
.*                  Re-Compiled for            NZHIS                          *
.*       V5.01.126  14/12/93 Rob Leonard  SRF 440540                          *
.*                  Display if deceased                                       *
.*       V5.01.127  26/05/1994 Ian Rutt                                       *
.*                  Fixed Global Recompile Bugs                               * 
.*       V5.01.128  17/06/1994  Rob Leonard   SRF 440859                      *
.*                  Change key when searching by U/R to speed up search       *
.*        V5.04.01  26/06/1997  Howellsy         505                          *
.*                  Use PATDOCKY instead of PATDOCDS                          *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
          INC       NZHISIFD/INC                 * Include for NZHIS vars
          INC       PATCALAG/INC
          INC       PATCONFD/INC                 * Include for NZHIS vars
          INC       PATNHIFD/INC                 * Include for NZHIS vars
          INC       PATCOMM/INC
          INC       PATDHEAD/INC
.
          INC       BOKCONFD/INC
          INC       BOKRC1FD/INC
          INC       OUTPREFD/INC
.
          INC       PATALRFD/INC
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PATGSRFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATPR1FD/INC
          INC       PATVISFD/INC
          INC       PATWR1FD/INC
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
.
          INC       WATTR1FD/INC
+
.
.         PATCONFD Variables
.
... CASSOCD   FORM      1                  * 1 = Yes Associated Doc,  2 = No
... CMATRN    FORM      1
CODEBP    DIM       2                  * Preferred Accomodation (CATA HPREFACCM)
... CTHETR    FORM      1
PROCTREA  DIM       9
.
.         Work Variables
.
BDATE     DIM       10
BOKSTATS  DIM       4
BOOKDOCT  DIM       6
BOOKNAME  DIM       20
BOOKNUMB  FORM      8
BOOKTYPE  DIM       3
BJDAY     FORM      3
CJDAY     FORM      3
.
CODE      DIM       2
COL       FORM      2
... CCNOTES   DIM       1
.
DESCACCM  DIM       20
DESCCANC  DIM       20
DESCDOC1  DIM       30
DESCDOC2  DIM       30
DESCMEMB  DIM       7
DESCPREA  DIM       7                  * Maternity Only
DESCPREV  DIM       7
DESCTYPE  DIM       20
DESCWARD  DIM       20
DIM3      DIM       3
DISPDATE  DIM       8 
DOCCODE   DIM       6
.
EBOOKDAT  DIM       8                  * End Date in CCYYMMDD format
FRSTGNAM  DIM       40
FULLGNAM  DIM       80
HEADLNO   FORM      2
.
NMPNUMB   DIM       20
OPTDESC   DIM       17
PATDESC5  DIM       40
POPTION   FORM      1
.
SBOOKDAT  DIM       8                  * Start Date in CCYYMMDD format
SCNDGNAM  DIM       40
SCRNFLAG  FORM      1
STIME     DIM       5                  * Store time for print
STRTFLAG  FORM      1
.
VERT      FORM      2
VERTBOOK  FORM      2
VERTNAME  FORM      2
VERTDOCT  FORM      2
VERTDATE  FORM      2
VERTTYPE  FORM      2
.
.         Constants
.
BOKSTAT0  INIT      "Book"
BOKSTAT1  INIT      "PreA"
BOKSTAT2  INIT      "Canc"
BOKSTAT3  INIT      "Admt"
BOKSTAT4  INIT      "Dsch"
.
CODEBC    INIT      "BC"               * Booking Cancelation
CODEBK    INIT      "BK"               * Booking Type
.
UNKNOWN   INIT      "Unknown"
.
OPTDES1   INIT      "By Booking Number"
OPTDES2   INIT      "By Patient Name  "
OPTDES3   INIT      "By Doctor Code   "
OPTDES4   INIT      "By Booking Date  "
OPTDES5   INIT      "By U/R Number    "
.
.
PRGID     INIT      "IBABOK11"
PRGNAM    INIT      "Booking Enquiry"
VERSION   INIT      "V12.01.00"
+
.
.         DISPLAY SCREEN HEADER AND OPEN FILES
.
.MLQQ      SPLOPEN   "data"
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,TEN;*197,CASSOCD
          READ      CONTROLF,TEN5;*188,CMATRN,CTHETR
          READ      CONTROLF,TWENTY7;*43,CODEBP,*46,HBWAIT
          READ      CONTROLF,FIFTY9;*132,CALRDESC
          READ      CONTROLF,THIRTY7;*185,PROCTREA
.
          DISPLAY   *P64:24,"bokrc1af";
          OPEN      BOKRC1A1,"bokrc1af"
          OPEN      BOKRC1A2,"bokrc1af"
          OPEN      BOKRC1A3,"bokrc1af"
          OPEN      BOKRC1A4,"bokrc1af"
          OPEN      BOKRC1A6,"bokrc1af"
.
          DISPLAY   *P64:24,"outpreaf";
          OPEN      OUTPREA1,"outpreaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PATMI1A3,"patmi1af"
.
          DISPLAY   *P64:24,"patpramf";
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
.
.          DISPLAY   *P64:24,"watproaf";
.          OPEN      WATPROA1,"watproaf"
          DISPLAY   *P64:24,"wattr1af";
          OPEN      WATTR1A1,"wattr1af"
.
INIT1000  DISPLAY   *P64:24,"patgsrnf";
          OPEN      PATGSRN1,"patgsrnf"
          OPEN      PATGSRN2,"patgsrnf"
          OPEN      PATGSRN3,"patgsrnf"
          OPEN      PATGSRN4,"patgsrnf"
.
INIT2000  MOVE      ONE,CNEWDS              * set "?" routines to new standard
+
.
.         Start of Main Codes
.
START     HLINE     *G37,2,48,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = ",OPTDES1:
                    *P1:6,*V2LON,TWO,*HOFF," = ",OPTDES2:
                    *P1:7,*V2LON,THREE,*HOFF," = ",OPTDES3:
                    *P1:8,*V2LON,FOUR,*HOFF," = ",OPTDES4;
.
          MOVE      TEN,CVERT
          COMPARE   ONE,HBWAIT
          GOTO      BEGIN IF NOT EQUAL
          DISPLAY   *P1:9,*V2LON,FIVE,*HOFF," = ",OPTDES5;
          MOVE      TEN1,CVERT
.
BEGIN     KEYIN     *P1:CVERT,*EL,"Select Option : ",*V2LON,POPTION;
.
          COMPARE   ZERO,POPTION
          GOTO      ENDIT IF EQUAL
.
          COMPARE   ONE,HBWAIT
          GOTO      REPEATW IF EQUAL
.
REPEAT    BRANCH    POPTION OF VALIDOPT,VALIDOPT,VALIDOPT,VALIDOPT
          BEEP
          GOTO      BEGIN
.
REPEATW   BRANCH    POPTION OF VALIDOPT,VALIDOPT,VALIDOPT,VALIDOPT,VALIDOPT
          BEEP
          GOTO      BEGIN
.
VALIDOPT  LOAD      OPTDESC,POPTION,OPTDES1,OPTDES2,OPTDES3,OPTDES4,OPTDES5
          DISPLAY   *P48:2,*V2LON,"- ",*+,OPTDESC,SP1;
.
REPEAT2   CALL      DISPSC
.
          BRANCH    POPTION,KBOOK,KNAME,KDOCT,KDATE,KUR0000
.
.******************************************************************************
.             INPUT BOOKING NUMBER
.******************************************************************************
KBOOK     MOVE      ZERO,BOOKNUMB
          DISPLAY   *P1:24,*EL;
.
          BRANCH    POPTION,KBOOK0P5
.
          DISPLAY   *P1:VERTBOOK,*EL," Booking Number    : ",UNDLN8;
          KEYIN     *P22:VERTBOOK,*V2LON,BOOKNUMB;
          DISPLAY   *P22:VERTBOOK,*V2LON,BOOKNUMB;
          GOTO      KBOOK111
.
KBOOK0P5  KEYIN     *P30:VERTBOOK,*EL,*DV,UNDLN8,*P30:VERTBOOK,*V2LON,BOOKNUMB;
          DISPLAY   *P30:VERTBOOK,*V2LON,BOOKNUMB;
.
KBOOK111  COMPARE   ZERO,BOOKNUMB
          GOTO      START IF EQUAL
.
          MOVE      BOOKNUMB,KEY8
          CALL      RDBKREC1
          BRANCH    OVRCD OF INVBOK,INUSE
.
          BRANCH    BKRESTAT OF PADMN,BKCAN
          COMPARE   ZERO,BKRESTAT
          GOTO      INVSTAT IF NOT EQUAL
.
          MATCH     ZEROUR,BKREURNO
          GOTO      CNTBK IF NOT EQUAL
          MOVE      SP30,PADD1
          MOVE      SP30,PADD2
          MOVE      SP30,PSUBRB
          MOVE      SP5,PPOST
.
CNTBK     DISPLAY   *P1:24,*EL
          CALL      GPMI0000                * Get the PMI details 
          BRANCH    EXIT,NOPMI              * Missing PMI details
.
          COMPARE   ONE,POPTION
          CALL      DISPSC1 IF NOT EQUAL
.
          CALL      DISPDA
          GOTO      SFIELD
.
INVBOK    DISPLAY   *P1:24,*B,*V2LON:
                    "** Booking No. does not exist ** ";
          CALL      HITENTER
          GOTO      KBOOK
.
INUSE     DISPLAY   *P1:24,*B,*V2LON:
                    "** Booking in use on Terminal ",BKRELOCK," ** ";
          CALL      HITENTER
          GOTO      KBOOK
.
PADMN     DISPLAY   *P1:24,*B,*V2LON,"** Booking is pre-admitted ** ";
          CALL      HITENTER
          GOTO      KBOOK
.
NOPMI     DISPLAY   *P1:24,*B,*V2LON,"** PMI details missing ** ";
          CALL      HITENTER
          GOTO      KBOOK
.
BKCAN     DISPLAY   *P1:24,*B,*V2LON,"** Booking is cancelled ** ";
          CALL      HITENTER
          GOTO      CNTBK
.
INVSTAT   BRANCH    BKRESTAT OF INVSTAT1,INVSTAT2,INVSTAT3,INVSTAT4
.
INVSTAT1  DISPLAY   *P1:24,*B,*V2LON:
                    "** Patient has been pre-admitted ** ";
          CALL      HITENTER
          GOTO      KBOOK
.
INVSTAT2  DISPLAY   *P1:24,*B,*V2LON:
                    "** Booking has been cancelled ** ";
          CALL      HITENTER
          GOTO      KBOOK
.
INVSTAT3  DISPLAY   *P1:24,*B,*V2LON,"** Patient is admitted ** ";
          CALL      HITENTER
          GOTO      KBOOK
.
INVSTAT4  DISPLAY   *P1:24,*B,*V2LON,"** Patient is discharged ** ";
          CALL      HITENTER
          GOTO      KBOOK

.******************************************************************************
.                                 KTYPE
.       Input Booking Type              Created by Bill Dew on the 29/05/1990
.******************************************************************************
KTYPE     MOVE      ONE,SCRNFLAG       * initialize redisplay flag
          MOVE      SP20,DESCTYPE
          DISPLAY   *P22:VERTTYPE,*EL,"___",*P22:VERTTYPE;
.
KTYPE111  KEYIN     *V2LON,BOOKTYPE
          ENDSET    BOOKTYPE
          APPEND    SP3,BOOKTYPE
          RESET     BOOKTYPE
.
          MATCH     SP3,BOOKTYPE
          GOTO      KTYPE333 IF NOT EQUAL
.
. ------ only <Return> hit so display All ------
.
          CALL      REDPTYPE
          DISPLAY   *P22:VERTTYPE,*V2LON,"All",*EL;
          MOVE      SP20,DESCTYPE
          GOTO      NOTYPE
.
KTYPE333  CMATCH    EXITCHAR,BOOKTYPE
          GOTO      START IF EQUAL
.
          CMATCH    QUEST,BOOKTYPE
          GOTO      KTYPE666 IF NOT EQUAL
          MOVE      CODEBK,CODE
          CALL      PATCODDS
          MOVE      ZERO,SCRNFLAG
.
KTYPE444  DISPLAY   *P1:24,*EL,"Booking Type : ___",*P16:24;
          GOTO      KTYPE111
.
KTYPE666  MOVE      SP20,TDESC
          PACK      KEY5,CODEBK,BOOKTYPE
          CALL      RDCODE1
          BRANCH    OVRCD,KTYPE888
          MOVE      TDESC,DESCTYPE
          CALL      REDPTYPE
          DISPLAY   *P22:VERTTYPE,*V2LON,BOOKTYPE,*HOFF,*P35:VERTTYPE,DESCTYPE;
          GOTO      NOTYPE
.
KTYPE888  DISPLAY   *P1:24,*EL,*B,*V2LON,"** Booking Type not on file **    ";
          CALL      HITENTER
          BRANCH    SCRNFLAG,KTYPE
          GOTO      KTYPE444
.
NOTYPE    COMPARE   FIVE,POPTION            * Test for new routine via U/R Number
          GOTO      NOTYPE22 IF NOT EQUAL
          CALL      EXTR0000               * extract records by U/R Number
          BRANCH    EXIT,KUR0000,START
          GOTO      KBOOK
.
NOTYPE22  CALL      DPSL0000               * display patient selection
          MOVE      HEADLNO,CVERT
          DISPLAY   *P1:CVERT,*EF;
          DISPLAY   *P22:VERTTYPE,*EL;
          BRANCH    POPTION,KBOOK,KNAME,KDOCT,KDATE,KUR0000
          GOTO      START
.
.         Redisplay screen after "?" routine
.
REDPTYPE  BRANCH    SCRNFLAG,REDPTYP9
          MOVE      ONE,SCRNFLAG
.
KTYPE1    CALL      DISPSC
          BRANCH    POPTION,KTYPE,TYPESC2,TYPESC3,TYPESC4,TYPESC5;
.
TYPESC2   DISPLAY   *P22:VERTNAME,*V2LON,BOOKNAME;
          RETURN
.
TYPESC3   DISPLAY   *P22:VERTDOCT,*V2LON,BOOKDOCT,*HOFF,*P35:VERTDOCT,DESCDOC1;
          RETURN
.
TYPESC4   MOVE      "22",CCOL
          MOVE      VERTDATE,CVERT
          UNPACK    SBOOKDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      ZERO,CHIGHLT
          CALL      DSPDATE
.
          DISPLAY   *P35:VERTDATE,"to   ";
          MOVE      "40",CCOL
          UNPACK    EBOOKDAT,CCENT,CYEAR,CMON,CDAY
          CALL      DSPDATE
          RETURN
.
TYPESC5   DISPLAY   *P22:4,*V2LON,PURNO,*HOFF,*P35:4,PATDESC5;
.
REDPTYP9  RETURN
+
.************************************************************************
.*                         EXTR0000                                     *
.*             Extract all required records from the booking master     *
.*             Re-written by Bill Dew 04/10/1990                        *
.*             Bill Dew is an absolute Knob Head                        *
.************************************************************************
EXTR0000  CALL      SCRN0000                * Display U/R screen
.
          OPEN      BOKRC1A6,"bokrc1af"
          MOVE      ZERO,STRTFLAG           * signal no records found
          MOVE      EIGHT,CVERT
          PACK      KEY16,PURNO,SP8
          CALL      RSBKREC6
.
EXTR1000  CALL      RKBKREC6
          BRANCH    OVRCD,EXTR4000
.
.          BRANCH    BKRESTAT,EXTR1000,EXTR1000,EXTR1000,EXTR1000
..                                           * Only want booked patients
          MATCH     BKREURNO,PURNO
          GOTO      EXTR4000 IF LESS
.
          MATCH     SP3,BOOKTYPE             * All booking types ?
          GOTO      EXTR2000 IF EQUAL        * yes
.
          MATCH     BKRETYPE,BOOKTYPE        * same booking type ?
          GOTO      EXTR1000 IF NOT EQUAL    * no, get next record
.
EXTR2000  COMPARE   "23",CVERT              * test for a full screen
          GOTO      EXTR6000 IF EQUAL
.
EXTR3000  DISPLAY   *P1:CVERT,*V2LON,BKREBOOK;
          PACK      KEY19,BKREURNO,BKREPROC,BKRECNT
          CALL      RDTREA1
          BRANCH    OVRCD,EXTR3100
          MOVE      WMDESC,KEY41
          GOTO      EXTR3200
.
EXTR3100  MOVE      "Unknown",KEY41
.
EXTR3200  UNPACK    BKREEDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,BDATE
.
          MOVE      BOKSTAT0,BOKSTATS
          LOAD      BOKSTATS,BKRESTAT,BOKSTAT1,BOKSTAT2,BOKSTAT3,BOKSTAT4
.
          DISPLAY   *P10:CVERT,BDATE,*P21:CVERT,BKREPROC,*P30:CVERT,KEY41:
                    *P72:CVERT,BOKSTATS,*P78:CVERT,BKRETYPE;
          MOVE      ONE,STRTFLAG            * one has been printed so = 1
          ADD       ONE,CVERT
          GOTO      EXTR1000
.
EXTR4000  BRANCH    STRTFLAG,EXTR5000
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "*** No records for this U/R Number ***    ";
          CALL      HITENTER
          NORETURN
          GOTO      KUR0000
.
EXTR5000  KEYIN     *P1:24,*EL,"(",*V2LON,*DV,ANSN,*HOFF,")ext, (":
                    *V2LON,*DV,ANSE,*HOFF,")xit, (":
                    *V2LON,*DV,ANSC,*HOFF,")ontinue, (":
                    *V2LON,*DV,ANSF,*HOFF,")irst page : ",*V2LON,ANS;
          RESET     ANS
          GOTO      EXTR8000 IF EOS
          REP       UPPLOW,ANS
          DISPLAY   *P44:24,*V2LON,ANS;
.
          MATCH     ANSN,ANS                * Next ?
          GOTO      EXTR8000 IF EQUAL       * yes
          MATCH     ANSE,ANS                * Exit ?
          GOTO      EXTR9000 IF EQUAL       * yes
          MATCH     ANSC,ANS                * Continue ? 
          GOTO      EXTR7000 IF EQUAL       * yes
          MATCH     ANSF,ANS                * First page ?
          GOTO      EXTR0000 IF EQUAL       * yes
          BEEP
          GOTO      EXTR5000
.
EXTR6000  KEYIN     *P1:24,*EL,"(",*V2LON,*DV,ANSN,*HOFF,")ext, (":
                    *V2LON,*DV,ANSE,*HOFF,")xit, (":
                    *V2LON,*DV,ANSC,*HOFF,")ontinue, (":
                    *V2LON,*DV,ANSF,*HOFF,")irst page, (":
                    *V2LON,*DV,ANSM,*HOFF,")ore : ",*V2LON,ANS;
          RESET     ANS
          GOTO      EXTR6100 IF EOS
          REP       UPPLOW,ANS
          DISPLAY   *P52:24,*V2LON,ANS;
.
          MATCH     ANSN,ANS                * Next ?
          GOTO      EXTR8000 IF EQUAL       * yes
          MATCH     ANSE,ANS                * Exit ?
          GOTO      EXTR9000 IF EQUAL       * yes
          MATCH     ANSC,ANS                * Continue ? 
          GOTO      EXTR7000 IF EQUAL       * yes
          MATCH     ANSF,ANS                * First page ?
          GOTO      EXTR0000 IF EQUAL       * yes
          MATCH     ANSM,ANS                * More ?
          GOTO      EXTR6100 IF EQUAL       * yes
          BEEP
          GOTO      EXTR6000
.
EXTR6100  MOVE      EIGHT,CVERT
          DISPLAY   *P1:CVERT,*EF;
          GOTO      EXTR3000
.
EXTR7000  MOVE      ZERO,EXIT               * "Continue" option selected
          GOTO      EXTR9999
.
EXTR8000  MOVE      ONE,EXIT                * "Next" option selected
          GOTO      EXTR9999
.
EXTR9000  MOVE      TWO,EXIT                * "Exit" option selected
.
EXTR9999  RETURN
+
.************************************************************************
.*                         SCRN0000                                     *
.*        Display screen header for U/R Number option                   *
.*                   called by : EXTR0000                               *
.************************************************************************
SCRN0000  DISPLAY   *P1:7,*V2LON,*ULON,"Book No.",*P10:7,"   Date   ":
                    *P21:7,PROCTREA,SP30,SP10,SP1,*P72:7,"Stat",*P77:7,"Type";
SCRN9999  RETURN
+
.************************************************************************
.             INPUT PATIENT NAME
.************************************************************************
KNAME     MOVE      SP2,BOOKNAME
          KEYIN     *P22:VERTNAME,*DV,UNDLN10,*DV,UNDLN10:
                    *P22:VERTNAME,*V2LON,BOOKNAME;
.
          RESET     BOOKNAME
          GOTO      START IF EOS
          DISPLAY   *P22:VERTNAME,*V2LON,BOOKNAME;
          GOTO      KTYPE
.
.************************************************************************
.             INPUT ATTENDING DOCTOR
.************************************************************************
KDOCT     MOVE      SP30,DESCDOC1
          MOVE      SP10,BOOKDOCT
          MOVE      "22",ECOL
          MOVE      VERTDOCT,EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          CALL      PATDOCKY
          BRANCH    EXIT,START,START 
.
          MOVE      DCODE,BOOKDOCT
.
KDOCT3    MOVE      BOOKDOCT,KEY6
          MOVE      DGNAME,PACGNAME
          MOVE      DSNAME,PACSNAME
          MOVE      TWO,PACFRMT
          MOVE      DTITL,PACTITLE
          CALL      PACNAME
          MOVE      PACFNAME,DESCDOC1
.
          DISPLAY   *PECOL:VERTDOCT,BOOKDOCT:
                    *P35:VERTDOCT,DESCDOC1;
          GOTO      KTYPE
.
INVDOCTC  DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid Doctor Code **    ";
          CALL      HITENTER
          BRANCH    SCRNFLAG,KDOCT
          GOTO      KDOCT2
.
REDBDOCT  BRANCH    SCRNFLAG,REDBDOC9
          MOVE      ONE,SCRNFLAG
          CALL      DISPSC
          DISPLAY   *P22:VERTDOCT,*V2LON,BOOKDOCT;
REDBDOC9  RETURN
+
.************************************************************************
.             INPUT DATE RANGE
.************************************************************************
KDATE     MOVE      ZERO,CHIGHLT
          MOVE      ONE,CDEFDTE
          MOVE      "24",EVERT
          MOVE      "01",ECOL
          MOVE      VERTDATE,CVERT
          MOVE      CCC,CCENT
.
.         ** Start Date **
.
KDATE1    MOVE      "22",CCOL
          UNPACK    SP6,CYEAR,CMON,CDAY
          CALL      KEYDATE
          BRANCH    CQUEST,KDATE1
          BRANCH    OVRCD OF START
.
          PACK      SBOOKDAT WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",SBOOKDAT
.
.         ** End Date **
.
          MOVE      "40",CCOL
.
KDATE2    UNPACK    SBOOKDAT,CCENT,CYEAR,CMON,CDAY
          CALL      KEYDATE
          BRANCH    CQUEST,KDATE2
.
          PACK      EBOOKDAT WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",EBOOKDAT
.
          MATCH     SBOOKDAT,EBOOKDAT
          GOTO      KTYPE IF NOT LESS
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** End Date must be greater than or equal Start Date ** ";
          CALL      HITENTER
          GOTO      KDATE2
+
.************************************************************************
.             INPUT U/R NUMBER
.************************************************************************
KUR0000   DISPLAY   *P22:4,*EL,*P22:5,*EF;
          MOVE      "22",CCOL
          MOVE      "4",CVERT
          CALL      KURN
          BRANCH    EXIT,START
          BRANCH    OVRCD,KUR1000
.
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,PATDESC5
          DISPLAY   *P70:3,*EL,*P35:4,PATDESC5;
          IF        PCEASE=ONE
            DISPLAY   *P70:3,*V2LON,"Deceased"
          ENDIF
          GOTO      KTYPE
.
KUR1000   DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid U/R Number **    ";
          CALL      HITENTER
          GOTO      KUR0000
.
REDISPS   DISPLAY   *P1:3,*EF:
                    *P1:4," U/R Number        :":
                    *P1:5," Booking Type      :";
          RETURN
+
.************************************************************************
.        DISPLAY AVAILABLE DATA
.
DISPSC    LOAD      VERTNAME USING POPTION OF SEVEN,FOUR,SEVEN,FIVE,SEVEN
          LOAD      VERTBOOK USING POPTION OF FOUR,SIX,SIX,SIX,SIX
          LOAD      VERTDOCT USING POPTION OF EIGHT,SEVEN,FOUR,EIGHT,EIGHT
          LOAD      VERTDATE USING POPTION OF SIX,SIX,SIX,FOUR,SIX
          LOAD      VERTTYPE USING POPTION OF FIVE,FIVE,FIVE,FIVE,FIVE
.
          MOVE      SEVEN,HEADLNO
.
          BRANCH    POPTION OF DISP1,DISPSC2,DISPSC3,DISPSC4,DISPSC5
.
DISPSC1   DISPLAY   *P1:4,*EF,*P1:VERTBOOK," Booking Number    :";
          GOTO      DISP1
.
DISPSC2   DISPLAY   *P1:3,*EF,*P1:VERTNAME," Patient Surname   :":
                              *P1:VERTTYPE," Booking Type      :";
          RETURN
.
DISPSC3   DISPLAY   *P1:3,*EF,*P1:VERTDOCT," Doctor Code       :":
                              *P1:VERTTYPE," Booking Type      :";
          RETURN
.
DISPSC4   DISPLAY   *P1:3,*EF,*P1:VERTDATE," Date Range        :":
                             *P35:VERTDATE,"to   __/__/____":
                              *P1:VERTTYPE," Booking Type      :";
          RETURN
.
DISPSC5   DISPLAY   *P1:3,*EF,*P1:4," U/R Number        :":
                       *P1:VERTTYPE," Booking Type      :";
          RETURN
+
.
SFIELD    KEYIN     *P1:24,*EL,"(",*V2LON,*DV,ANSP,*HOFF,")rint, (":
                    *V2LON,*DV,ANSE,*HOFF,")xit, (":
                    *V2LON,*DV,ANSN,*HOFF,")ext ? ",*V2LON,ANS;
          RESET     ANS
          GOTO      REPEAT2 IF EOS
          REP       UPPLOW,ANS
          DISPLAY   *P27:24,*V2LON,ANS;
.
          MATCH     ANSP,ANS
          GOTO      PRTDATA IF EQUAL
          MATCH     ANSE,ANS
          GOTO      START IF EQUAL
          MATCH     ANSN,ANS
          GOTO      REPEAT2 IF EQUAL
          BEEP
          GOTO      SFIELD
.
PRTDATA   MOVE      SIXTY1,CLNO
          MOVE      ZERO,CPAGENO
.
          DISPLAY   *P1:24,*EL,*V2LON,"*** Generating Enquiry *** ";
          CALL      PRNTDA
          PRINT     *N,"***  End of Enquiry  ***"
          DISPLAY   *P1:24,*EL,*V2LON:
                    "*** Enquiry Generation Completed *** ";
          CALL      HITENTER
          GOTO      SFIELD
.
PRNTDA    CALL      HEAD
.
          PRINT     *N,"Booking Number           : ",BKREBOOK;
          COMPARE   TWO,BKRESTAT            * test for removed status
          GOTO      PRNTDA1 IF NOT EQUAL
          PRINT     *45,"** Booking is Cancelled  **";
.
PRNTDA1   PRINT     *N,*N,"U/R Number               : ",BKREURNO:
                    *N,"Title                    : ",PTITL:
                    *N,"Surname                  : ",PSNAME:
                    *N,"Given Name               : ",PGNAME
.
          PRINT     *N,*N,"Address Line 1           : ",PADD1:
                    *N,"Address Line 2           : ",PADD2:
                    *N,"Address Line 3           : ",PSUBRB:
                    *N,"Postcode                 : ",PPOST:
                    *N,"Private Telephone Number : ",PTELEP
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     "Date of Birth            : ",*28,CPCDATE
.
          PRINT     *N,"Attending Doctor         : ",BKREADOC,*45,DESCDOC1
.
          BRANCH    HBWAIT OF PRNTDAW
          BRANCH    CASSOCD OF PRNTDAD
          GOTO      PRNTDAX
.
PRNTDAD   PRINT     "Associated Doctor        : ",BKRESDOC,*45,DESCDOC2
.
PRNTDAX   PRINT     *N,"Benefit Fund Memb        : ",DESCMEMB:
                    *N,"Previous Inpatient       : ",DESCPREV:
                    *N,*N,"Booking Type             : ",BKRETYPE,*45,DESCTYPE:
                    *N,"Expected Due Date        : ";
.
          UNPACK    BKREEDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *28,CPCDATE
.
          PRINT     *N,"Preferred Accom.         : ",BKREACCM,*45,DESCACCM:
                    *N,"Booking Date             : ";
.
          UNPACK    BKREADAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *28,CPCDATE
          GOTO      PRNTNEXT
.
PRNTDAW   PRINT     *N,"Booking Type             : ",BKRETYPE,*45,DESCTYPE
.
          UNPACK    BKREPDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     "Pre-Assessment Date      : ",CPCDATE
.
          MOVE      BKREPTIM,STIME
          PRINT     "Pre-Assessment Time      : ",STIME
.
          UNPACK    BKREEDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     "Scheduled Admission Date : ",CPCDATE
.
          MOVE      BKREATIM,STIME
          PRINT     "Scheduled Admission Time : ",STIME:
                    *N,"Ward                     : ",BKREWARD,*45,DESCWARD
.          
PRNTNEXT  COMPARE   TWO,BKRESTAT
          RETURN    IF NOT EQUAL
          PRINT     "Reason Cancellation      : ",BKRECANC,*45,DESCCANC
          RETURN
+
.
HEAD      CLOCK     TIME,CTIMEIS
          ADD       ONE,CPAGENO
          PRINT     *F,"IBABOK11",*20,CNAME,*65,"DATE : ",CDATE:
                    *N,VERSION,*20,"BOOKING ENQUIRY - ",OPTDESC:
                               *65,"TIME : ",CTIMEIS:
                    *N,*65,"PAGE : ",SP5,CPAGENO
          MOVE       THREE,CLNO
          RETURN
+
.
SCRNHEAD  MOVE      HEADLNO,CVERT
.
          DISPLAY   *P1:CVERT,*V2LON,*ULON,"Book No.":
                    *P10:CVERT,"   Date   ":
                    *P21:CVERT,"Patient Name's",SP10,SP4:
                    *P50:CVERT,"  D.O.B.  ":
                    *P61:CVERT,"Stat":
                    *P66:CVERT,"Booking Type   ";
          ADD       ONE,CVERT
          RETURN
+
.*******************************************************************************
.                                 DPSL0000
.    Display patient selection    
.    Created by Bill Dew 02/10/1990 : To replace the absolute sadness.
.    02/10/1990 Bill Dew : It took me 60 mins to figure out that the original
.    crap was wrong.  In that time spent It would have been more feasible
.    to rewrite code into a readable and maintainable manner rather than
.    making poor code work.   Time spent re-writing was 90 mins
.
.    This routine only applies for options 2 3 4 
.    This code was copied from IBABOK02
.******************************************************************************
DPSL0000  CALL      SCRNHEAD
          CALL      FPTR0000                * position file pointer
.
DPSL1111  CALL      RBOK0000                * read next bokrc1af record
          BRANCH    OVRCD,DPSL7777          * end of file
..
..    Display only the Booked bokmasaf records thus cannot display
..    pre-admitted, cancelled, admitted or discharged records.
..
..          BRANCH    BKRESTAT,DPSL1111,DPSL1111,DPSL1111,DPSL1111
.
          MATCH     SP3,BOOKTYPE            * test if using all booking types
          GOTO      DPSL2222 IF EQUAL
          MATCH     BOOKTYPE,BKRETYPE       * test for selected booking type
          GOTO      DPSL1111 IF NOT EQUAL
.
DPSL2222  COMPARE   "23",CVERT              * test for a full screen
          GOTO      DPSL3333 IF EQUAL
          CALL      DREC0000                * Display a line of patient details
          GOTO      DPSL1111
.
DPSL3333  KEYIN     *P1:24,*EL,"(",*V2LON,*DV,ANSN,*HOFF,")ext, (":
                    *V2LON,*DV,ANSE,*HOFF,")xit, (":
                    *V2LON,*DV,ANSC,*HOFF,")ontinue, (":
                    *V2LON,*DV,ANSF,*HOFF,")irst page, (":
                    *V2LON,*DV,ANSM,*HOFF,")ore : ",*V2LON,ANS;
          RESET     ANS
          GOTO      DPSL4444 IF EOS
          REPLACE   UPPLOW,ANS
          DISPLAY   *P52:24,*V2LON,ANS;
.
          MATCH     ANSN,ANS                * Next ?
          GOTO      DPSL8888 IF EQUAL       * yes
          MATCH     ANSE,ANS                * Exit ?
          GOTO      DPSL6666 IF EQUAL       * yes
          MATCH     ANSC,ANS                * Continue ?
          GOTO      DPSL5555 IF EQUAL       * yes
          MATCH     ANSF,ANS                * First page ?
          GOTO      DPSL0000 IF EQUAL       * yes
          MATCH     ANSM,ANS                * More ?
          GOTO      DPSL4444 IF EQUAL       * yes
          BEEP
          GOTO      DPSL3333
.
.          COMPARE   "23",CVERT              * check for a full screen
.          GOTO      DPSL3333 IF NOT EQUAL
DPSL4444  MOVE      HEADLNO,CVERT           * get next screen
          ADD       ONE,CVERT
          DISPLAY   *P1:CVERT,*EF;
          CALL      DREC0000
          GOTO      DPSL1111
.
DPSL5555  NORETURN                          * this is due to the initial sadness
          GOTO      KBOOK
.
DPSL6666  NORETURN                          * this is due to the initial sadness
          GOTO      START
.
DPSL7777  KEYIN     *P1:24,*EL,"(",*V2LON,*DV,ANSN,*HOFF,")ext, (":
                    *V2LON,*DV,ANSE,*HOFF,")xit, (":
                    *V2LON,*DV,ANSC,*HOFF,")ontinue, (":
                    *V2LON,*DV,ANSF,*HOFF,")irst page : ",*V2LON,ANS;
          RESET     ANS
          GOTO      DPSL8888 IF EOS
          REPLACE   UPPLOW,ANS
          DISPLAY   *P44:24,*V2LON,ANS;
.
          MATCH     ANSN,ANS                * Next ?
          GOTO      DPSL8888 IF EQUAL       * yes
          MATCH     ANSE,ANS                * Exit ?
          GOTO      DPSL6666 IF EQUAL       * yes
          MATCH     ANSC,ANS                * Continue ?
          GOTO      DPSL5555 IF EQUAL       * yes
          MATCH     ANSF,ANS                * First page ?
          GOTO      DPSL0000 IF EQUAL       * yes
          BEEP
          GOTO      DPSL7777
.
DPSL8888  COMPARE   FOUR,POPTION
          GOTO      DPSL9999 IF NOT EQUAL
          DISPLAY   *P22:4,SP10,*P40:4,"__/__/____",*P22:6,*EL;
.
DPSL9999  RETURN
+
.******************************************************************************
.                                 FPTR0000
.    Position file pointer concerning the selected key index.
.******************************************************************************
FPTR0000  BRANCH    POPTION,FPTR9999,FPTR1111,FPTR2222,FPTR3333
.
FPTR1111  PACK      KEY28,BOOKNAME,SP20
          CALL      RSBKREC2
          GOTO      FPTR9999
.
FPTR2222  PACK      KEY22,BOOKDOCT,SP20
          CALL      RSBKREC3
          GOTO      FPTR9999
.
FPTR3333  PACK      KEY16,SBOOKDAT,SP10
          CALL      RSBKREC4
.
FPTR9999  RETURN
+
.******************************************************************************
.                                 RBOK0000
.         read bokrc1af record by option
.******************************************************************************
RBOK0000  BRANCH    POPTION,RBOK9999,RBOK1111,RBOK2222,RBOK3333
.
RBOK1111  CALL      RKBKREC2                * Order by patient name
          BRANCH    OVRCD,RBOK9990
          RESET     BKRESNAM                * Set Form ptr to start of string
          MATCH     BOOKNAME,BKRESNAM
          GOTO      RBOK9990 IF NOT EQUAL
          GOTO      RBOK9000
.
RBOK2222  CALL      RKBKREC3                * Order by Doctor sequence
          BRANCH    OVRCD,RBOK9990
          MATCH     BOOKDOCT,BKREADOC       * test for same doctor
          GOTO      RBOK9990 IF NOT EQUAL
          GOTO      RBOK9000
.
RBOK3333  CALL      RKBKREC4               * Order by booking date sequence
          BRANCH    OVRCD,RBOK9990
          MATCH     BKREEDAT,EBOOKDAT
          GOTO      RBOK9990 IF LESS
.
RBOK9000  MOVE      ZERO,OVRCD
          GOTO      RBOK9999
.
RBOK9990  MOVE      ONE,OVRCD
.
RBOK9999  RETURN
+
.******************************************************************************
.                                 DREC0000
.******************************************************************************
DREC0000  DISPLAY   *P1:CVERT,*V2LON,BKREBOOK;
.
          MOVE      TEN,CCOL
          UNPACK    BKREEDAT,CCENT,CYEAR,CMON,CDAY
.
.         MOVE      CCC,CCENT
          MOVE      ONE,CHIGHLT
          CALL      DSPDATE                 * display booking date
.
          CALL      GPMI0000                * get pmi details
          BRANCH    EXIT,DREC1000
.
          MOVE      PGNAME,PACGNAME
          MOVE      PSNAME,PACSNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME                 * set up patient's name
          MOVE      PACFNAME,KEY30
          DISPLAY   *P21:CVERT,KEY30;
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P49:CVERT,SP1,CPCDATE;    * display date
.
DREC1000  MOVE      BOKSTAT0,BOKSTATS
          LOAD      BOKSTATS,BKRESTAT,BOKSTAT1,BOKSTAT2,BOKSTAT3,BOKSTAT4
          DISPLAY   *P61:CVERT,BOKSTATS;    * display bokrc1af status
.
          PACK      KEY5,CODEBK,BKRETYPE
          CALL      RDCODE1                 * get booking type desc
          MOVE      TDESC,KEY15
          DISPLAY   *P66:CVERT,KEY15;
          ADD       ONE,CVERT
.
DREC9999  RETURN
+
.******************************************************************************
.
.         MAIN BOOKING DETAIL SCREEN
.
DISP1     BRANCH    HBWAIT OF DISP2D
          BRANCH    CASSOCD OF DISP1D
.
          DISPLAY   *P1:4,*EF:
                    *P1:4," Booking Number    :":
                    *P1:5," U/R Number        :":
                    *P1:6," Title             :":
                    *P1:7," Surname           :":
                    *P1:8," Given Name        :":
                    *P1:9," Address Line 1    :":
                    *P1:10," Address Line 2    :":
                    *P1:11," Address Line 3    :":
                    *P1:12," Postcode          :":
                    *P1:13," Date of Birth     :":
                    *P1:14," Home Telephone No.:":
                    *P1:15," Attending Doctor  :":
                    *P1:17," Memb Benefit Fund :":
                    *P1:18," Prev. In-Patient  :":
                    *P1:19," Booking Type      :":
                    *P1:20," Expected Due Date :":
                    *P1:21," Preferred Accom.  :";
.
          COMPARE   ONE,CMATRN
          RETURN    IF NOT EQUAL
.
          DISPLAY   *P1:22," Deposit Paid (Y/N):";
          RETURN
+
.         WITH ASSOCIATED DOCTOR
.
DISP1D    DISPLAY   *P1:4,*EF:
                    *P1:4," Booking Number    :":
                    *P1:5," U/R Number        :":
                    *P1:6," Title             :":
                    *P1:7," Surname           :":
                    *P1:8," Given Name        :":
                    *P1:9," Address Line 1    :":
                    *P1:10," Address Line 2    :":
                    *P1:11," Address Line 3    :":
                    *P1:12," Postcode          :":
                    *P1:13," Date of Birth     :":
                    *P1:14," Home Telephone No.:":
                    *P1:15," Attending  Doctor :":
                    *P1:16," Associated Doctor :":
                    *P1:17," Memb Benefit Fund :":
                    *P1:18," Prev. In-Patient  :":
                    *P1:19," Booking Type      :":
                    *P1:20," Expected Due Date :":
                    *P1:21," Preferred Accom.  :";
.
          COMPARE   ONE,CMATRN
          RETURN    IF NOT EQUAL
.
          DISPLAY   *P1:22," Deposit Paid (Y/N):";
          RETURN
+
.         Waiting Lists
.
DISP2D    DISPLAY   *P1:4,*EF:
                    *P1:4," Booking Number           :":
                    *P1:5," U/R Number               :":
                    *P1:6," Title                    :":
                    *P1:7," Surname                  :":
                    *P1:8," Given Name               :":
                    *P1:9," Address Line 1           :":
                    *P1:10," Address Line 2           :":
                    *P1:11," Address Line 3           :":
                    *P1:12," Postcode                 :":
                    *P1:13," Date of Birth            :":
                    *P1:14," Private Telephone Number :":
                    *P1:15," Attending Doctor         :":
                    *P1:16," Booking Type             :":
                    *P1:17," Pre-Assessment Date      :":
                    *P1:18," Pre-Assessment Time      :":
                    *P1:19," Scheduled Admission Date :":
                    *P1:20," Scheduled Admission Time :":
                    *P1:21," Ward                     :";
          RETURN
+
.         Subroutine to display data for requested booking no. onto the screen
.
DISPDA    MOVE      SP30,DESCDOC1
          MOVE      BKREADOC,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD OF NOADOCT
.
          MOVE      DGNAME,PACGNAME
          MOVE      DSNAME,PACSNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,DESCDOC1
.
NOADOCT   MOVE      SP30,DESCDOC2
          BRANCH    HBWAIT OF DISPDX4
          BRANCH    CASSOCD OF NOSDOCT
.
          MOVE      BKRESDOC,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD OF NOSDOCT
.
          MOVE      DGNAME,PACGNAME
          MOVE      DSNAME,PACSNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,DESCDOC2
.
NOSDOCT   MOVE      UNKNOWN,DESCMEMB
          CMATCH    ANSY,BKREOMEM
          GOTO      DISPDX1 IF NOT EQUAL
          MOVE      YES,DESCMEMB
          GOTO      DISPDX2
.
DISPDX1   CMATCH    ANSN,BKREOMEM
          GOTO      DISPDX2 IF NOT EQUAL
          MOVE      NO,DESCMEMB
.
DISPDX2   MOVE      UNKNOWN,DESCPREV
          CMATCH    ANSY,BKREOMEM
          GOTO      DISPDX3 IF NOT EQUAL
          MOVE      YES,DESCPREV
          GOTO      DISPDX4
.
DISPDX3   CMATCH    ANSN,BKREOMEM
          GOTO      DISPDX4 IF NOT EQUAL
          MOVE      NO,DESCPREV
.
DISPDX4   MOVE      SP20,TDESC
          MOVE      TDESC,DESCTYPE
          MATCH     SP3,BKRETYPE
          GOTO      DISPDX5 IF EQUAL
.
          PACK      KEY5 WITH CODEBK,BKRETYPE
          CALL      RDCODE1
          MOVE      TDESC,DESCTYPE
.
          BRANCH    HBWAIT OF DISPDX7
.
DISPDX5   MOVE      SP20,TDESC
          MOVE      TDESC,DESCACCM
          MATCH     SP3,BKREACCM
          GOTO      DISPDX6 IF EQUAL
.
          PACK      KEY5 WITH CODEBP,BKREACCM
          CALL      RDCODE1
          MOVE      TDESC,DESCACCM
          GOTO      DISPDX6
.
DISPDX7   MOVE      SP20,DESCWARD
          MOVE      BKREWARD,KEY6
          CALL      RDWARD1
          BRANCH    OVRCD OF DISPDX6
          MOVE      WBDESC,DESCWARD
          MOVE      WWARD,BKREWARD
.
DISPDX6   BRANCH    HBWAIT OF DISPDA21
          MOVE      TWENTY2,CCOL
          GOTO      DISPDA2
.
DISPDA21  MOVE      "30",CCOL
.
DISPDA2   DISPLAY   *PCCOL:4,*V2LON,BKREBOOK,*PCCOL:5,BKREURNO:
                    *PCCOL:6,PTITL,*PCCOL:7,PSNAME,*PCCOL:8,PGNAME;
.
DISPDWT2  DISPLAY   *PCCOL:9,*V2LON,PADD1,*PCCOL:10,PADD2,*PCCOL:11,PSUBRB:
                    *PCCOL:12,PPOST;
.
DISPDWT3  UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
.
          CALL      PACDATE
          DISPLAY   *PCCOL:13,*V2LON,CPCDATE,*HOFF
.
          BRANCH    HBWAIT OF DISPDA3
.
          DISPLAY   *P22:14,*V2LON,PTELEP:
                    *P22:15,BKREADOC,*HOFF,*P31:15,DESCDOC1:
                    *P22:16,*V2LON,BKRESDOC,*HOFF,*P31:16,DESCDOC2:
                    *P22:17,*V2LON,DESCMEMB,*P22:18,DESCPREV:
                    *P22:19,BKRETYPE,*HOFF,*P31:19,DESCTYPE;
.
          MOVE      TWENTY,CVERT
          MOVE      "22",CCOL
.
          UNPACK    BKREEDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      ZERO,CHIGHLT
          DISPLAY   *PCCOL:CVERT,CPCDATE
.
          DISPLAY   *P22:21,*V2LON,BKREACCM,*HOFF,*P31:21,DESCACCM;
.
          COMPARE   ONE,CMATRN
          GOTO      CNTDA2 IF NOT EQUAL
.
          MOVE      NO,DESCPREA
          CMATCH    ANSY,BKREPREA
          GOTO      CNT2D19 IF NOT EQUAL
          MOVE      YES,DESCPREA
.
CNT2D19   DISPLAY   *P22:22,*V2LON,DESCPREA;
          GOTO      CNTDA2
.
.         Waiting Lists
.
DISPDA3   DISPLAY   *P30:14,*V2LON,PTELEP:
                    *P30:15,BKREADOC,*HOFF,*P39:15,DESCDOC1:
                    *P30:16,*V2LON,BKRETYPE,*HOFF,*P39:16,DESCTYPE;
.
          MOVE      ZERO,CHIGHLT
.
          MOVE      TEN7,CVERT
          MOVE      "30",CCOL
          UNPACK    BKREPDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *PCCOL:CVERT,*V2LON,CPCDATE,*HOFF
.
          MOVE      TEN8,CVERT
          MOVE      "30",CCOL
          UNPACK    BKREPTIM,CHOUR,DIM1,CMIN
          CALL      DSPTIME
.
          MOVE      TEN9,CVERT
          MOVE      "30",CCOL
          UNPACK    BKREEDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *PCCOL:CVERT,*V2LON,CPCDATE,*HOFF
.
          MOVE      TWENTY,CVERT
          MOVE      "30",CCOL
          UNPACK    BKREATIM,CHOUR,DIM1,CMIN
          CALL      DSPTIME
.
          DISPLAY   *P30:21,*V2LON,BKREWARD,*HOFF,*P39:21,DESCWARD;
.
.         CONTINUE
.
CNTDA2    COMPARE   TWO,BKRESTAT            * bokrc1af status = cancelled ?
          RETURN    IF NOT EQUAL            * no
.
.         if Cancelled
.
          MOVE      SP20,TDESC
          MOVE      TDESC,DESCCANC
.
          MATCH     SP3,BKRECANC
          RETURN    IF EQUAL
.
          PACK      KEY5 WITH CODEBC,BKRECANC
          CALL      RDCODE1
          MOVE      TDESC,DESCCANC
          BRANCH    HBWAIT OF CNTDA4
          MOVE      "31",CCOL1
.
          COMPARE   ONE,CMATRN
          GOTO      CNTDA3 IF NOT EQUAL
          DISPLAY   *P1:23," Cancellation      :":
                    *PCCOL:23,*V2LON,BKRECANC,*HOFF,*PCCOL1:23,DESCCANC;
          RETURN
.
CNTDA3    DISPLAY   *P1:22," Cancellation      :";
          GOTO      CNTDA5
.
CNTDA4    MOVE      "39",CCOL1
          DISPLAY   *P1:22," Cancellation",*P27:22,":";
.
CNTDA5    DISPLAY   *PCCOL:22,*V2LON,BKRECANC,*HOFF,*PCCOL1:22,DESCCANC;
          RETURN
+
.*********************************************************************
.* GPMI0000 : Get the PMI details for a booking                      *
.*********************************************************************
GPMI0000  MATCH     ZEROUR,BKREURNO
          GOTO      GPMI1000 IF EQUAL
.
          MOVE      BKREURNO,PURNO
          MOVE      PURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,GPMI1000
          GOTO      GPMI8000
.
GPMI1000  MOVE      BKREBOOK,PURNO
          MOVE      PURNO,KEY8
          CALL      RDPRAM1
          BRANCH    OVRCD,GPMI9000
.
GPMI8000  MOVE      ZERO,EXIT
          DISPLAY   *P70:3,*EL
          IF        PCEASE=ONE
            DISPLAY   *P70:3,*V2LON,"Deceased"
          ENDIF
          GOTO      GPMI9999
.
GPMI9000  MOVE      ONE,EXIT
.
GPMI9999  RETURN
+
.************************************************************************
GETSVAR   RETURN                       * Used by KURN
+
.
ENDIT
.MLQQQ     SPLCLOSE  "data"
          CHAIN     PGM
          STOP
+
.==============================================================================
.
          INC       STD001IO
          INC       CALCAGE
          INC       PATALERT
          INC       PATCOMN1
          INC       PATSNDX
          INC       PATSNX2
.
          INC       PATCODKY
          INC       PATDOCKY
.
          INC       BOKRC1IO/INC
          INC       OUTPREIO/INC
.
          INC       PATALRIO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATGSRIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATPR1IO/INC
          INC       PATVISIO/INC
          INC       PATWR1IO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
.
          INC       WATTR1IO/INC
          INC       NZCOMPIO/INC
AGECHK
CLPATMAS  RETURN
+

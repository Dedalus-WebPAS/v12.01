.******************************************************************************
.*                                                                            *
.*                        P R O G R A M   S U M M A R Y                       *
.*                        -------------   -------------                       *
.*                                                                            *
.*     PROGRAM NAME:    IBAPAT86                                              *
.*                                                                            *
.*     FUNCTION:        ADMISSIONS AND DISCHARGES FOR A DAY REPORT            *
.*                                                                            *
.*     DATE WRITTEN:    18/09/85                                              *
.*                                                                            *
.*     AUTHOR:          MALCOLM HAYSE                                         *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.*        V12.00.01 30/05/2025 Jacob Jackson   TSK 0955096                    *
.*                  Alphanumeric changes                                      *
.*        V11.00.01 10/03/2020  Jill Parkinson Task 0879163                   *
.*                  Added SEXDSCIO include                                    *
.*        V10.10.01 07/04/2017  Peter Vela    TSK 0833050                     *
.*                  Added Intersex/Other validation in CNTNS01                * 
.* V10.03.01  31/12/2012 Patrick Adair                             CAR 261630 *
.*                       Removed port number from temp file name              *
.*        V9.04.01  11/10/2004  Lina Vo       CAR 54175                       *
.*                  Added Multi Hospital                                      *
.*        V9.03.01  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2      *
.*                  - Add Sex Description routine SEXDSC00 with Code "R" -    *
.*                  "Intersex" added                                          *
.*        V9.02.02  22/02/2002  Dean Cramer                                   *
.*                  Include Ward sequence option                              *
.*        V9.02.01  19/10/2001  Dean Cramer                                   *
.*                  Fix where discharge transfer status not printing          *
.*        V5.08.04  30/11/2000  Ebon Clements    SRF 7430                     *
.*                  Use PTCNDTRF to display disharge details with             *
.*                  DSTAT description or DDEST description                    *
.*        V5.08.04  10.10.2000 Sandra Barcham                                 *
.*                  Recompiled for WEB Report Scheduler                       *
.*        V5.08.03  16/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.02  06/06/2000  Tonii                                         *
.*                  Changed from KEY10 to KEY14 for the health fund file      *
.******************************************************************************
.*        V5.08.B01 10/04/2000  Greg Horvat                                   *
.*                  Removed MATADAFD MATADBFD & MATADBIO                      *
.*                      V5.07.01  23/04/1999  Jim Stathopoulos                *
.*                                SRF 645344                                  *
.*                      V5.07.B02 02/02/1999  Jim Stathopoulos                *
.*                                Proper 5.07 Mods                            *
.*                  09/11/1998  Glenn Berry      5.07 changes                 *
.******************************************************************************
.*                      V5.06.01  07/11/1998  Steve Armstrong   srf 644575    *
.*                                Standardised temp. files                    *
.******************************************************************************
.*                      V5.01.02  11/10/89 K.Peak          SRF 102647         *
.*                                Removed Health Fund/Table and Claim Type    *
.*                      V5.01.03  27/10/89 K.Peak          SRF 102827         *
.*                                Changed CTYPCHG to be CTYPREP               *
.*                      V5.01.04  08/11/89 K.Peak          SRF 102896         *
.*                                Fix printing of heading                     *
.*                      V5.01.05  23/05/90 J.Tan           SRF 104901         *
.*                                Mods PATFN1FD                               *
.*                      V5.01.121 27/11/90 Glen Hobby                         *
.*                                Recompiled for changes to PATMX1FD          *
.*                      V5.01.122 19/02/91 i chung         SRF 107784         *
.*                                a) Fix Admissions' line spacing problem     *
.*                                b) Fix line count problem                   *
.*                                c) Swap Given Names & Surname print position*
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
.
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PATDSCFD/INC
          INC       PATFN1FD/INC
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATTRNFD/INC
          INC       PMSVX1FD/INC
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
+
.         WORK AREA
.
PSTADMP1 IFILE     SQL, FIXED=161, KEY="U1-28"
PSTDSHP1 IFILE     SQL, FIXED=161, KEY="U1-28"
.
.-----------------------------------------------------------------
.Name      Type     Length     Physical     Start
.----      ----     ------     --------     -----
DIM20      DIM         20           20         1
XDAADMNO   DIM          8            8        21
.AURNO     DIM          8            8        29
D5TIME     DIM          5            5        37
.PSNAME    DIM         20           20        42
.PGNAME    DIM         25           25        62
DHOSP      DIM          7            7        87
DDSTAT     DIM         18           18        94
DCLMTAB    DIM         23           23       112
DNAME      DIM         20           20       135
.TWARD     DIM          3            3       155
.TBED      DIM          3            3       158
.
.End of Record                               161
.
XDDADMNO   DIM          8            8        21
.DURNO     DIM          8            8        29
.
+
.-----------------------------------------------------------------
.
MATATMP1 IFILE     SQL, FIXED=99, KEY="U1-29"
MATDTMP1 IFILE     SQL, FIXED=99, KEY="U1-29"
.
.-----------------------------------------------------------------
.Name      Type     Length     Physical     Start
.----      ----     ------     --------     -----
NBNAME     DIM         20           20         1
NBMATNO    DIM          8            8        21
NBPLUR     DIM          1            1        29
NBURNO     DIM          8            8        30
NBSEX      DIM          1            1        38
NBDOB      DIM          8            8        39
NBSTAT     FORM         1            2        47
NBGNAME    DIM         25           25        49
.PGNAME    DIM         25           25        74
.
.End of Record                                99
.
WSTADMP1 IFILE     SQL, FIXED=161, KEY="U1-14"
WSTDSHP1 IFILE     SQL, FIXED=161, KEY="U1-14"
.
.-----------------------------------------------------------------
.Name      Type     Length     Physical     Start
.----      ----     ------     --------     -----
XWARD      DIM          3            3         1
XBED       DIM          3            3         4
XWAADMNO   DIM          8            8         7
.AURNO     DIM          8            8        15
.D5TIME    DIM          5            5        23
.PSNAME    DIM         20           20        28
.PGNAME    DIM         25           25        48
.DHOSP     DIM          7            7        73
.DDSTAT    DIM         18           18        80
.DCLMTAB   DIM         23           23        98
.DNAME     DIM         20           20       121
.DIM20     DIM         20           20       141
.
.End of Record                               161
.
..XDDADMNO   DIM          8            8        21
.DURNO     DIM          8            8        29
.
+
.-----------------------------------------------------------------
.
.
CMDLINE   DIM       50
DIM6      DIM       6
DIM11     DIM       11
.
FNAMER    DIM       8
FNAMEU    DIM       8
.
IBCNMHOS  FORM      1
.
MODEH     FORM      1
OPCND     FORM      1
.
SEC       FORM      1
STATUS    FORM      1
.
TOTPAT    FORM      8
.
.      EXTRA VARIABLES
.
ACTDATE   DIM       8
.
CMATRN    FORM      1
CODED     INIT      "D "
CODEDD    INIT      "DD"
CODECL    INIT      "CL"
..CTRF      FORM      4                                                 D-49016
..CTRM      FORM      4                                                 D-49016
CTRSEX    FORM      4[9]                                               *I-49016
CTYPREP   FORM      1
.
DIM7      DIM       7
DHEAD     DIM       24
DHEAD1    INIT      "(Admission No. Sequence)"
DHEAD2    INIT      "(Surname Sequence)      "
DHEAD3    INIT      "(Ward Sequence)      "
DOCNAME   DIM       18
DSEX      DIM       8                                         * was 6  *C-49016
.
MABFLAG   FORM      1
MABSTAT1  INIT      "DISCHARGED "
MABSTAT2  INIT      "DECEASED   "
MIN1      FORM      1
.
PATADM    FORM      1
PTCNDTRF  FORM      1
PTCNHDPS  FORM      1
.
TEMPFIL1  DIM       8
TEMPFIL2  DIM       8
TEMPFIL3  DIM       8
TEMPFIL4  DIM       8
TEMPFIL5  DIM       8
TEMPFIL6  DIM       8
TODATE    DIM       8
.
XCENT1    DIM       2
XDAY1     DIM       2
XMON1     DIM       2
XYEAR1    DIM       2
.
ERASE     INIT      "iserase "
ISBUILD   INIT      "isbuild "
UKEY1     INIT      " 161 U1-28"
UKEY2     INIT      " 99 U1-29"
UKEY3     INIT      " 161 U1-14"
.
.
PRGID     INIT      "IBAPAT86"
PRGNAM    INIT      "Admissions And Discharges For A Day Report"
VERSION   INIT      "V12.01.00"
+
.
.         Display Program Header and open files
.
          CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"patfn1af";
          OPEN      PATFN1A1,"patfn1af"
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A3,"patmi1af"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.  
          READ      CONTROLF,ZERO;*43,IBCNMHOS 
          READ      CONTROLF,TEN5;*188,CMATRN 
          READ      CONTROLF,FIFTY9;*91,CTYPREP 
          READ      CONTROLF,TWENTY1;*216,PTCNDTRF
          READ      CONTROLF,EIGHTY;*250,PTCNHDPS
.
          CALL      CREA0000                     * create temp files
.
          COMPARE   ONE,CMATRN
          GOTO      START IF NOT EQUAL
+
.        START OF PROGRAM
.
.         Select Option
.
START     MOVE      ZERO,CPAGENO
          MOVE      ZERO,TOTPAT
.....     MOVE      ZERO,CTRM                                           D-49016
.....     MOVE      ZERO,CTRF                                           D-49016
          CALL      CLRCTR00                * set CTRSEX to zero       *I-49016
.
          DISPLAY   *P1:3,*EF:
                   *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Admission Number Sequence":
                    *P1:6,*V2LON,TWO,*HOFF," = Patient Surname Sequence":
                    *P1:7,*V2LON,THREE,*HOFF," = Ward Sequence":
                    *P1:9,"Select Option :";
.
START1    KEYIN     *P17:9,*EF,*DV,UNDLN1,*P17:9,*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
          BRANCH    OPTION OF OPTHDR,OPTHDR,OPTHDR
          BEEP
          GOTO      START1
.
OPTHDR    LOAD      DHEAD USING OPTION OF DHEAD1,DHEAD2,DHEAD3
.
. ------  Keyin Hospital report is being generated for  ------
.
          CALL      KHOS0000
          BRANCH    EXIT,START1
.
. ------  Keyin date report is being generated for  ------
.
          DISPLAY   *P1:14,*EF,"Generate report for ";
.
          MOVE      TEN4,CVERT
          MOVE      "21",CCOL
          MOVE      CVERT,EVERT
          MOVE      "50",ECOL
          MOVE      ONE,CDEFDTE
          MOVE      ZERO,CHIGHLT
.
KDATE1    DISPLAY   *P21:14,*EF;
          UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
.
          CALL      KEYDATE
          BRANCH    CQUEST,KDATE1
          BRANCH    OVRCD,START1
.
          PACK      TODATE WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE
          UNPACK    TODATE INTO XCENT1,XYEAR1,XMON1,XDAY1
.
.         Check if Date is ok or not
.
REKEY     KEYIN     *P1:24,*EF," Date ok (",*V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
.
          MATCH     "N" TO ANS
          GOTO      KDATE1 IF EQUAL
.
          MATCH     "Y" TO ANS
          GOTO      SURNAM IF EQUAL
          BEEP
          GOTO      REKEY
.
.         Access via Patient Surname
.
SURNAM    BRANCH    OPTION OF KADMNO
          MOVE      "60",CLNO
          MOVE      ZERO,TOTPAT
.
          CALL      CLER0000
          BRANCH    OPCND OF ENDIT
          GOTO      KADMNO
.
INVMASTA  DISPLAY   *P15:23,*B,*EL,"** Admission ",*V2LON,AADMNO,*HOFF:
                    " has an invalid U/R ",*V2LON,AURNO,*HOFF," **";
.
          PRINT     "** Admission ",AADMNO," has an invalid U/R ":
                    AURNO,".  Please contact I.B.A. **"
          ADD       ONE,CLNO
          GOTO      NEXTA
.
.         use Date on Admission KEY 4  
.
KADMNO    PACK      KEY16,TODATE,SP8
          CALL      RDSMISS3
          MOVE      "60",CLNO
          MOVE      ZERO,TOTPAT
.
          DISPLAY   *P1:24,*EL,*P31:24,"Admission :";
.
NEXTA     CALL      RDKMISS3
          BRANCH    OVRCD OF ENDP1
.
          COMPARE   ONE,ASTAT
          GOTO      NEXTA IF EQUAL
.
          COMPARE   FIVE,ASTAT
          GOTO      NEXTA IF EQUAL
.
          UNPACK    ADATE,XCENT,XYEAR,XMON,XDAY
          PACK      ACTDATE,XCENT,XYEAR,XMON,XDAY
          REP       " 0",ACTDATE
.
          MATCH     ACTDATE,TODATE
          GOTO      ENDP1 IF NOT EQUAL
.
.         Check for Hospital
.
          IF        IBCNMHOS=1
            MATCH     SP10,PTHSHOSP
            IF        !@EQUAL
              MOVE      AADMNO,KEY8
              CALL      RDPMVX11
              BRANCH    OVRCD,NEXTA
              MATCH     PTHSHOSP,PMVXMHOS
              GOTO      NEXTA IF NOT EQUAL
            ENDIF
          ENDIF
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF INVMASTA
.
          MOVE      SP30,DNAME
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD OF OKDATA1
          MOVE      DGNAME,ANS
          PACK      DNAME WITH ANS,DOT,DSNAME
          MOVE      DNAME,DOCNAME
.
OKDATA1   PACK      KEY30,AADMNO,SP30
          CALL      RDSTRAN2
          CALL      RDKTRAN2
          BRANCH    OVRCD TO NOADREC
.
          MATCH     AADMNO,TADMN
          GOTO      NOADREC IF NOT EQUAL
.
          MATCH     "A" TO TMOVE
          GOTO      OKDAT1A IF EQUAL
.
NOADREC   MOVE      SP3 TO TWARD
          MOVE      SP3 TO TBED
.
OKDAT1A   MOVE      ATIME,D5TIME
          MOVE      SP30,DCLMTAB
.
          MATCH     SP3,ACLAIM
          GOTO      OKDR1 IF EQUAL
.
          MATCH     "0" TO ACLAIM
          GOTO      OKDR1 IF EQUAL
.
          PACK      KEY5 WITH CODECL,ACLAIM
          CALL      RDCODE1
          BRANCH    OVRCD,PDF1
          MOVE      TDESC,DCLMTAB
          GOTO      PDF1
.
OKDR1     MATCH     SP6,AFUNDH
          GOTO      PDF1 IF EQUAL

          MOVE      "0000    ",HFTABL
          PACK      KEY14 WITH AFUNDH,HFTABL,SP10
          CALL      RDFUND1
          BRANCH    OVRCD OF PDF0
          MOVE      HFNAME,DCLMTAB
.
PDF0      PACK      KEY14 WITH AFUNDH,AFNDTB,SP10
          CALL      RDFUND1
          BRANCH    OVRCD OF PDF1
          MOVE      HFNAME,DCLMTAB
.
PDF1      DISPLAY   *P44:24,*V2LON,AADMNO;
.
          BRANCH    OPTION OF NORMPA,SURNPA,WARDPA
.
SURNPA    MOVE      PSNAME,DIM20
          PACK      KEY28 WITH DIM20,AADMNO
.
          READ      PSTADMP1,KEY28;ANS
          GOTO      NEXTA IF NOT OVER
.
.          MOVE      SP3 TO DDSTAT
          WRITE     PSTADMP1,KEY28;KEY28,AURNO,D5TIME,PSNAME,PGNAME:
                                   DHOSP,DDSTAT,DCLMTAB,DNAME,TWARD,TBED
          GOTO      NEXTA
.
WARDPA    MOVE      PSNAME,DIM20
          PACK      KEY14 WITH TWARD,TBED,AADMNO
.
          READ      WSTADMP1,KEY14;ANS
          GOTO      NEXTA IF NOT OVER
.
          WRITE     WSTADMP1,KEY14;KEY14,AURNO,D5TIME,PSNAME,PGNAME:
                                   DHOSP,DDSTAT,DCLMTAB,DNAME,DIM20
          GOTO      NEXTA
.
NORMPA    CALL      DISPDA
          GOTO      NEXTA
.
INVMASTD  DISPLAY   *P15:23,*B,*EL,"** Discharge ",*V2LON,DADMNO,*HOFF:
                    " has an invalid U/R ",*V2LON,DURNO,*HOFF," **";
.
          PRINT     "** Discharge ",DADMNO," has an invalid U/R ":
                    DURNO,".  Please contact I.B.A. **"
          ADD       ONE,CLNO
          GOTO      NEXTD
.
NOADMN    DISPLAY   *P1:24,*EL,*P22:24,*B,*V2LON:
                    "** No Admissions for date requested **",*W3;
+
DSCHNO1   COMPARE   ZERO,CPAGENO
          GOTO      DSCHNO2 IF EQUAL
.
          CALL      PAGEND
.
.
.* ****************************************** Start of Changes         *C-49016
          MOVE      CTRSEX[1],FORM4
          ADD       CTRSEX[2],FORM4
          ADD       CTRSEX[3],FORM4
.
          COMPARE   ZERO,FORM4
          GOTO      DSCHNO1A IF EQUAL
.
DSXEPP    PRINT     *12,"NUMBER OF NEWBORN MALES    : ",CTRSEX[1],*N:
                    *12,"NUMBER OF NEWBORN FEMALES  : ",CTRSEX[2]
          COMPARE   ZERO,CTRSEX[3]
          GOTO      DSXEPP01 IF EQUAL
          PRINT     *12,"NUMBER OF NEWBORN INDETER  : ",CTRSEX[3]
DSXEPP01  COMPARE   ZERO,CTRSEX[4]
          GOTO      DSXEPP02 IF EQUAL
          PRINT     *12,"NUMBER OF NEWBORN INTERSEX : ",CTRSEX[4]
DSXEPP02  COMPARE   ZERO,CTRSEX[9]
          GOTO      DSXEPP03 IF EQUAL
          PRINT     *12,"NUMBER OF NEWBORN UNKNOWNS : ",CTRSEX[9]
.
DSXEPP03  PRINT     *N,"  ***  End of Admissions  *** "
.
          MOVE      ZERO,CPAGENO
          CALL      CLRCTR00                * set CTRSEX to zero       *I-49016
.* ******************************************   End of Changes         *C-49016
.
          GOTO      DSCHNO2
.
DSCHNO1A  PRINT     *N,"  Total patients = ",TOTPAT,*N:
                    *N,"  *** End of Admissions ***"
          MOVE      ZERO,CPAGENO
.
.         Print Discharges
.
DSCHNO2   PACK      KEY16,TODATE,SP8
          CALL      RDSDSCH2
          MOVE      "60",CLNO
          MOVE      ZERO,TOTPAT
.
          DISPLAY   *P1:24,*EL,*P31:24,"Discharge :";
.
NEXTD     CALL      RDKDSCH2
          BRANCH    OVRCD OF ENDP2
.
          UNPACK    DDATE,XCENT,XYEAR,XMON,XDAY
          PACK      ACTDATE,XCENT,XYEAR,XMON,XDAY
          REP       " 0",ACTDATE
.
          MATCH     ACTDATE,TODATE
          GOTO      ENDP2 IF NOT EQUAL
.
.         Check for Hospital
.
          IF        IBCNMHOS=1
            MATCH     SP10,PTHSHOSP
            IF        !@EQUAL
              MOVE      DADMNO,KEY8
              CALL      RDPMVX11
              BRANCH    OVRCD,NEXTD
              MATCH     PTHSHOSP,PMVXMHOS
              GOTO      NEXTD IF NOT EQUAL
            ENDIF
          ENDIF
.
          MOVE      DURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF INVMASTD
.
          MOVE      DADMNO,KEY8
          CALL      RDMISS1
.
          MOVE      SP30,DNAME
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD OF OKDATA2
          MOVE      DGNAME,ANS
          PACK      DNAME WITH ANS,DOT,DSNAME
          MOVE      DNAME,DOCNAME
.
OKDATA2   PACK      KEY30,AADMNO,ACTDATE,SP30
          CALL      RDSTRAN2
.
OKLOOP    CALL      RDKTRAN2
          BRANCH    OVRCD TO NODISRC
.
          MATCH     AADMNO,TADMN
          GOTO      NODISRC IF NOT EQUAL
.
          MATCH     "D" TO TMOVE
          GOTO      OKLOOP IF NOT EQUAL
          GOTO      OKDATA3
.
NODISRC   MOVE      SP3 TO TWARD
          MOVE      SP3 TO TBED
.
OKDATA3   MOVE      SP20,DDSTAT
.
.          MATCH     SP3,DSTAT
.          GOTO      OKDA1 IF EQUAL
.
          IF        PTCNDTRF = 1
            PACK      KEY5 WITH CODED,DSTAT
          ELSE
            PACK      KEY5 WITH CODEDD,DDEST
          ENDIF
          CALL      RDCODE1
          BRANCH    OVRCD,OKDA1
          MOVE      TDESC,DDSTAT
.
OKDA1     MOVE      DTIME,D5TIME
.
          DISPLAY   *P44:24,*V2LON,DADMNO;
.
          BRANCH    OPTION OF NORMPD,SURNPD,WARDPD
.
SURNPD    MOVE      PSNAME,DIM20
          PACK      KEY28 WITH DIM20,DADMNO
.
          READ      PSTDSHP1,KEY28;ANS
          GOTO      NEXTD IF NOT OVER
.
          MOVE      SP7 TO DHOSP
          MOVE      SP30 TO DCLMTAB
.
          WRITE     PSTDSHP1,KEY28;KEY28,DURNO,D5TIME,PSNAME,PGNAME:
                                   DHOSP,DDSTAT,DCLMTAB,DNAME,TWARD,TBED
          GOTO      NEXTD
.
WARDPD    MOVE      PSNAME,DIM20
          PACK      KEY14 WITH TWARD,TBED,DADMNO
.
          READ      WSTDSHP1,KEY28;ANS
          GOTO      NEXTD IF NOT OVER
.
          MOVE      SP7 TO DHOSP
          MOVE      SP30 TO DCLMTAB
.
          WRITE     WSTDSHP1,KEY14;KEY14,DURNO,D5TIME,PSNAME,PGNAME:
                                   DHOSP,DDSTAT,DCLMTAB,DNAME,DIM20     
          GOTO      NEXTD
.
NORMPD    CALL      DISPDA2
          GOTO      NEXTD
.
NODSCH    DISPLAY   *P1:24,*EL,*P22:24,*B,*V2LON:
                    "** No Discharges for date requested **",*W3;
.
          BRANCH    OPTION OF ENDREP,ENDD,ENDW
          GOTO      START
.
.         End-of-File or data is beyond date requested - Admissions
.
ENDP1     BRANCH    OPTION OF CNTEP1,DSCHNO1,DSCHNO1
.
CNTEP1    COMPARE   ZERO,CPAGENO
          GOTO      NOADMN IF EQUAL
.
          BRANCH    CTYPREP,CNTP1
.
          MOVE      ZERO,CPAGENO
          CALL      PAGEND
          GOTO      CNTP2
.
CNTP1     MOVE      ZERO,CPAGENO
          CALL      PAGEND2
.
CNTP2     PRINT     *N,"  Total patients = ",TOTPAT,*N:
                    *N,"  *** End of Admissions ***"
.
...       MOVE      ZERO,CTRM
...       MOVE      ZERO,CTRF
          CALL      CLRCTR00                * set CTRSEX to zero       *I-49016
          GOTO      DSCHNO1
.
.         End-of-File or data is beyond date requested - Discharges
.
ENDP2     BRANCH    OPTION OF CNTEP2,ENDD,ENDW
.
CNTEP2    COMPARE   ZERO,CPAGENO
          GOTO      NODSCH IF EQUAL
.
          BRANCH    CTYPREP,CNTN1
          CALL      PAGEND
          GOTO      CNTN2
.
CNTN1     CALL      PAGEND2
.
CNTN2     PRINT     *N,"  Total patients = ",TOTPAT,*N:
                    *N,"  *** End of Discharges ***"
          MOVE      ZERO,CPAGENO
.
ENDREP    DISPLAY   *P1:24,*EL,*P24:24,*V2LON:
                    "** Report Generation Completed **",*W2;
          GOTO      START
.
.         OVER CONDITION FOUND..
.
ENDD      DISPLAY   *P18:24,*EL,"Generating Admission :";
          MOVE      ZERO,CPAGENO
          MOVE      ZERO,PATADM         * inpatient 
          MOVE      "60",CLNO
. 
          MOVE      SP30,KEY28
          READ      PSTADMP1,KEY28;;
.
NEXTR     READKS    PSTADMP1;DIM20,XDAADMNO,AURNO,D5TIME,PSNAME,PGNAME:
                             DHOSP,DDSTAT,DCLMTAB,DNAME,TWARD,TBED
          GOTO      CNTNR IF OVER
.
          MOVE      XDAADMNO,AADMNO
.
          DISPLAY   *P41:24,*V2LON,PSNAME;
          CALL      DISPDA
          GOTO      NEXTR
.
ENDW      DISPLAY   *P18:24,*EL,"Generating Admission :";
          MOVE      ZERO,CPAGENO
          MOVE      ZERO,PATADM         * inpatient 
          MOVE      "60",CLNO
          MOVE      SP30,KEY14
          READ      WSTADMP1,KEY14;;
.
NEXTW     READKS    WSTADMP1;TWARD,TBED,XWAADMNO,AURNO,D5TIME,PSNAME,PGNAME:
                             DHOSP,DDSTAT,DCLMTAB,DNAME,DIM20     
          GOTO      CNTNR IF OVER
.
          MOVE      XWAADMNO,AADMNO
.
          DISPLAY   *P41:24,*V2LON,XWARD,"/",XBED;
          CALL      DISPDA
          GOTO      NEXTW
.
CNTNR     COMPARE   ZERO,CPAGENO
          GOTO      CNTNR2 IF EQUAL
.
          BRANCH    CTYPREP,CNTR1
.
          MOVE      ZERO,CPAGENO
          MOVE      ONE,PATADM
          CALL      PAGEND
          GOTO      CNTR2
.
CNTR1     MOVE      ZERO,CPAGENO
          MOVE      ONE,PATADM
          CALL      PAGEND2
.
CNTR2     PRINT     *N,"  Total patients = ",TOTPAT,*N:
                    *N,"  *** End of Admissions ***"
.
CNTNR2    COMPARE   ONE,CMATRN
          GOTO      CNTNXR IF NOT EQUAL
.
          DISPLAY   *P14:24,*EL,"Generating Newborn Admission :";
          MOVE      "60",CLNO
          MOVE      ZERO,TOTPAT
          MOVE      SP30,KEY29
          
          READ      MATATMP1,KEY29;;
.
NEXTNA    READKS    MATATMP1;NBNAME,NBMATNO,NBPLUR,NBURNO:
                             NBSEX,NBDOB,NBSTAT,NBGNAME,PGNAME
.
          GOTO      CNTNS IF OVER
.
          DISPLAY   *P45:24,*V2LON,NBNAME;
          CALL      DISPDAN
          GOTO      NEXTNA
.
CNTNS     COMPARE   ZERO,CPAGENO
          GOTO      NONXR IF EQUAL
.
.
.* ****************************************** Start of Changes         *C-49016
          CALL      PAGEND
          PRINT     *12,"Number of Newborn Males    : ",CTRSEX[1],*N:
                    *12,"                  Females  : ",CTRSEX[2]
          COMPARE   ZERO,CTRSEX[3]
          GOTO      CNTNS01 IF EQUAL
          PRINT     *12,"Number of Newborn Indeter. : ",CTRSEX[3]
CNTNS01   COMPARE   ZERO,CTRSEX[4]
          GOTO      CNTNS02 IF EQUAL
.
          IF        PTCNHDPS=3
            PRINT     *12,"Number of Newborn Other : ",CTRSEX[4]
          ELSE
            PRINT     *12,"Number of Newborn Intersex : ",CTRSEX[4]
          ENDIF
.
CNTNS02   COMPARE   ZERO,CTRSEX[9]
          GOTO      CNTNS03 IF EQUAL
          PRINT     *12,"Number of Newborn Unknowns : ",CTRSEX[9]
.
CNTNS03   PRINT     *N,"  ***  End of Admissions  ***"
.
          MOVE      ZERO,CPAGENO
          CALL      CLRCTR00                * set CTRSEX to zero
.* ******************************************   End of Changes         *C-49016
.
          GOTO      CNTNXR
.
NONXR     BRANCH    PATADM OF CNTNXR
          DISPLAY   *P1:24,*EL,*P22:24,*B,*V2LON:
                    "** No Admissions for date requested **",*W3;
.
CNTNXR    DISPLAY   *P18:24,*EL,"Generating Discharge :";
          MOVE      ZERO,CPAGENO
          MOVE      "60",CLNO
          MOVE      ZERO,TOTPAT
          COMPARE   THREE,OPTION
          GOTO      DSCHW IF EQUAL
.
          MOVE      SP30,KEY28
          READ      PSTDSHP1,KEY28;;
.
NEXTRD    READKS    PSTDSHP1;DIM20,XDDADMNO,DURNO,D5TIME,PSNAME,PGNAME:
                             DHOSP,DDSTAT,DCLMTAB,DNAME,TWARD,TBED
          GOTO      ENDP IF OVER
.
          MOVE      XDDADMNO,DADMNO
.
          DISPLAY   *P41:24,*V2LON,PSNAME;
          CALL      DISPDA2
          GOTO      NEXTRD
.
DSCHW     MOVE      SP20,KEY14
          READ      WSTDSHP1,KEY14;;
.
NEXTWD    READKS    WSTDSHP1;XWARD,XBED,XWAADMNO,DURNO,D5TIME,PSNAME,PGNAME:
                             DHOSP,DDSTAT,DCLMTAB,DNAME,DIM20     
          GOTO      ENDP IF OVER
.
          MOVE      XWAADMNO,DADMNO
          MOVE      XWARD,TWARD
          MOVE      XBED,TBED
.
          DISPLAY   *P41:24,*V2LON,PSNAME;
          CALL      DISPDA2
          GOTO      NEXTWD
.
ENDP      COMPARE   ONE,CMATRN
          GOTO      ENDX IF NOT EQUAL
.
          DISPLAY   *P14:24,*EL,"Generating Newborn Discharge :";
          MOVE      "60",CLNO
          MOVE      SP30,KEY29
          READ      MATDTMP1,KEY29;;
.
NEXTDD    READKS    MATDTMP1;NBNAME,NBMATNO,NBPLUR,NBURNO:
                             NBSEX,NBDOB,NBSTAT,NBGNAME,PGNAME
          GOTO      ENDX IF OVER
.
          DISPLAY   *P45:24,*V2LON,NBNAME;
          CALL      DISPDAD
          GOTO      NEXTDD
.
ENDX      COMPARE   ZERO,CPAGENO
          GOTO      NONXRD IF EQUAL
.
          BRANCH    CTYPREP,ENDX1
          CALL      PAGEND
          GOTO      ENDX2
.
ENDX1     CALL      PAGEND2
.
ENDX2     PRINT     *N,"  Total patients = ",TOTPAT,*N:
                    *N,"  *** End of Discharges ***"
          MOVE      ZERO,CPAGENO
          GOTO      CNTEDP
.
NONXRD    DISPLAY   *P1:24,*EL,*P22:24,*B,*V2LON:
                    "** No Discharges for date requested **",*W3;
.
CNTEDP    DISPLAY   *P1:24,*EL,*P24:24,*V2LON:
                   "** Report Generation Completed **",*W2;
          GOTO      START
+
.
.         Print actual Admission data
.
DISPDA    MOVE      ZERO,MABFLAG
          MOVE      ZERO,MODEH
.
          COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
.
          MOVE      DNAME TO DOCNAME
          PRINT     *1,"|",AURNO,*11,"|",AADMNO,*21,"| ",D5TIME:
                    *29,"| ",PSNAME,PGNAME,*76,"| ",TWARD,"/",TBED;
.
          BRANCH    CTYPREP,DISPA2
.
DISPA1    PRINT     *86,"| ",DCLMTAB,*112,"| ",DOCNAME,*132,"|":
                    *N,"|",*11,"|",*21,"|",*29,"|":
                    *76,"|",*86,"|",*112,"|",*132,"|"
          GOTO      DISPA3
.
DISPA2    PRINT     *86,"| ",DOCNAME,*107,"| ":
                    *N,"|",*11,"|",*21,"|",*29,"|":
                    *76,"|",*86,"| ",*107,"|"
.
DISPA3    ADD       ONE,TOTPAT
          ADD       TWO,CLNO
          RETURN
+
.
.         Print Newborn Admission Data
.
DISPDAN   MOVE      ONE,MABFLAG
          MOVE      ONE,MODEH
.
          COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
.
          UNPACK    NBDOB INTO XCENT,XYEAR,XMON,XDAY
.
.
.* ****************************************** Start of Changes         *C-49016
          MOVE      NBSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          MOVE      DSEXDESC,DIM7
          ADD       ONE,CTRSEX[DSEXNO]
.* ******************************************   End of Changes         *C-49016
.
DISPP     MOVE      PGNAME,DNAME
          MOVE      DNAME,DOCNAME
          PRINT     *1,"| ",NBMATNO,SLASH,NBPLUR,*12,"| ",NBNAME:
                    *35,"| ",NBURNO,*46,"| ",DIM7:
                    *55,"|",XDAY,SLASH,XMON,SLASH,XCENT,XYEAR,"|":
                    *68,NBGNAME,*93,"|":
                    *N,"|",*12,"|(",DOCNAME,")",*35,"|",*46,"|":
                    *55,"|",*66,"|",*93,"|"
.
          ADD       TWO,CLNO
          ADD       ONE,TOTPAT
          RETURN
+
.
.         Print actual Discharge Data
.
DISPDA2   MOVE      ZERO,MABFLAG
          MOVE      ZERO,MODEH
.
          COMPARE   "55",CLNO
          CALL      HEAD2 IF NOT LESS
.
          MOVE      DNAME TO DOCNAME
          PRINT     *1,"|",DURNO,*11,"|",DADMNO,*21,"| ",D5TIME:
                    *29,"| ",PSNAME,PGNAME,*76,"| ",TWARD,"/",TBED;
.
          BRANCH    CTYPREP,DSPDA1
.
          PRINT     *86,"| ",DDSTAT,*112,"| ",DOCNAME,*132,"|":
                    *N,"|",*11,"|",*21,"|",*29,"|",*76,"|":
                    *86,"|",*112,"|",*132,"|"
          GOTO      DSPDA2
.
DSPDA1    PRINT     *86,"| ",DOCNAME,*107,"|":
                    *N,"|",*11,"|",*21,"|",*29,"|",*76,"|":
                    *86,"|",*107,"|"
.
DSPDA2    ADD       TWO,CLNO
          ADD       ONE,TOTPAT
          RETURN
+
.
.         Print Newborn Discharge Details
.
DISPDAD   MOVE      ONE,MABFLAG
          MOVE      TWO,MODEH
.
          COMPARE   "55",CLNO
          CALL      HEAD2 IF NOT LESS
.
          MOVE      MABSTAT1,DIM11
          COMPARE   TWO,NBSTAT
          GOTO      DISPU IF EQUAL
.
          MOVE      MABSTAT2,DIM11
.
DISPU     UNPACK    NBDOB INTO XCENT,XYEAR,XMON,XDAY
          MOVE      DMALE,DIM6
.
          CMATCH    ANSF,NBSEX
          GOTO      DISPZP IF NOT EQUAL
.
          MOVE      DFEMALE,DIM6
.
DISPZP    MOVE      PGNAME,DNAME
          MOVE      DNAME,DOCNAME
          PRINT     *1,"| ",NBMATNO,SLASH,NBPLUR,*12,"| ",NBNAME:
                    *35,"| ",NBURNO,*46,"| ",DIM6:
                    *55,"|",XDAY,SLASH,XMON,SLASH,XCENT,XYEAR,"|":
                    *66,"| ",NBGNAME,*93,"| ",DIM11," |":
                    *N,"|",*12,"|(",DOCNAME,")",*35,"|",*46,"|":
                    *55,"|",*66,"|",*93,"|",*107,"|"
.
          ADD       TWO,CLNO
          ADD       ONE,TOTPAT
          RETURN
+
.
.         Last Line on the Page
.
PAGEND    BRANCH    MODEH OF PAGEND1,PAGEND2
.
          PRINT     "*---------------------------------------------------":
                    "--------------------------------------------------------":
                    "-----------------------*"
          ADD       ONE,CLNO
          RETURN
.
PAGEND1   PRINT     "*-------------------------------------------":
                    "------------------------------------------------*"
          ADD       ONE,CLNO
          RETURN
.
PAGEND2   PRINT     "*---------------------------------------------------":
                    "------------------------------------------------------*"
          ADD       ONE,CLNO
          RETURN
+
.         Headings for Admissions output
.
HEAD      ADD       ONE,CPAGENO
          CLOCK     TIME,CTIMEIS
.
          BRANCH    CTYPREP,HED
          COMPARE   ONE,CPAGENO
          CALL      PAGEND IF NOT EQUAL
          GOTO      HED0
.
HED       COMPARE   ONE,CPAGENO
          CALL      PAGEND2 IF NOT EQUAL
.
HED0      PRINT     *F,PRGID,*40,CNAME,*116,"DATE : ",CDATE:
                    *N,VERSION,*40,PRGNAM,*116,"TIME :   ",CTIMEIS,*N:
                    *116,"PAGE : ",*130,CPAGENO
.
          IF        IBCNMHOS=1
            PRINT     *40,"Hospital : ",PTHSNAME
          ENDIF
.
          BRANCH    MABFLAG OF HED1
.
          PRINT     *40,"Admissions for : ",XDAY1,"/",XMON1,"/",XCENT1,XYEAR1:
                    "     ",DHEAD,*N
          GOTO      HED2
.
HED1      PRINT     *40,"Newborn Admissions for : ",XDAY1,"/",XMON1,"/":
                    XCENT1,XYEAR1,"     ",DHEAD,*N
.
HED2      BRANCH    CTYPREP,HED3
          CALL      PAGEND
          GOTO      HED4
.
HED3      CALL      PAGEND2
.
HED4      BRANCH    MABFLAG OF HEADA
.
          PRINT     *1,"| U/R No.",*11,"| Adm No.":
                    *21,"| Time",*29,"| Patient's Name",*76,"|Ward/Bed";
.
          BRANCH    CTYPREP,HED5 
.
          PRINT     *86,"| Health Fund / Table",*112,"| Attending Doctor  |":
                    *N,*1,"|",*11,"|",*21,"|",*29,"|":
                    *76,"|",*86,"| or Claim Type",*112,"|",*132,"|"
          GOTO      HEADP
.
HED5      PRINT     *86,"| Attending Doctor",*107,"|"
          MOVE      "7",CLNO
          CALL      PAGEND2
          GOTO      HEADP1
.
HEADA     PRINT     *1,"| Reg. ",*12,"| Mother's Surname ":
                    *35,"| Mother's",*46,"| Baby's ":
                    *55,"| Baby's ",*66,"| Baby's Given Name ",*93,"|":
                    *N,*1,"| Number ",*12,"|",*35,"| U/R # ":
                    *46,"| Sex ",*55,"|Birth Date",*66,"|",*93,"|"
.
HEADP     MOVE      "8",CLNO
          CALL      PAGEND
.
HEADP1    MOVE      ONE,MABFLAG
          RETURN
+
.         Headings for Discharges output
.
HEAD2     ADD       ONE,CPAGENO
          CLOCK     TIME,CTIMEIS
.
          BRANCH    CTYPREP,HDD
          COMPARE   ONE,CPAGENO
          CALL      PAGEND IF NOT EQUAL
          GOTO      HDD0
.
HDD       COMPARE   ONE,CPAGENO
          CALL      PAGEND2 IF NOT EQUAL
.
HDD0      PRINT     *F,PRGID,*40,CNAME,*116,"DATE : ",CDATE:
                    *N,VERSION,*40,PRGNAM,*116,"TIME :   ",CTIMEIS,*N:
                    *116,"PAGE : ",*130,CPAGENO
.
          IF        IBCNMHOS=1
            PRINT     *40,"Hospital : ",PTHSNAME
          ENDIF
.
          BRANCH    MABFLAG OF HDD1
.
          PRINT     *40,"Discharges for : ",XDAY1,"/",XMON1,"/",XCENT1,XYEAR1:
                    "     ",DHEAD,*N
          GOTO      HDD2
.
HDD1      PRINT     *40,"Newborn Discharges for : ",XDAY1,"/",XMON1,"/":
                    XCENT1,XYEAR1,"     ",DHEAD,*N
.
HDD2      BRANCH    CTYPREP,HDD3
          CALL      PAGEND
          GOTO      HDD4
.
HDD3      CALL      PAGEND2
.
HDD4      BRANCH    MABFLAG OF HEADDX
.
          BRANCH    CTYPREP,HDD5
.
          PRINT     *1,"| U/R No.",*11,"| Adm No.":
                    *21,"| Time",*29,"| Patient's Name":
                    *76,"|Ward/Bed",*86,"| Status":
                    *112,"| Attending Doctor",*132,"|"
          GOTO      HEADPX
.
HDD5      PRINT     *1,"| U/R No.",*11,"| Adm No.":
                    *21,"| Time",*29,"| Patient's Name":
                    *76,"|Ward/Bed",*86,"| Attending Doctor",*107,"|"
          GOTO      HEADPX
.
HEADDX    PRINT     *1,"| Reg. ",*12,"| Mother's Surname ":
                    *35,"| Mother's",*46,"| Baby's ":
                    *55,"| Baby's ",*66,"| Baby's Given Name ",*93,"|":
                    " Status ",*107,"|":
                    *N,*1,"| Number ",*12,"|",*35,"| U/R # ":
                    *46,"| Sex ",*55,"|Birth Date",*66,"|",*93,"|",*107,"|"
          MOVE      "8",CLNO
          GOTO      HEADPX1
.
HEADPX    MOVE      "7",CLNO
.
HEADPX1   BRANCH    CTYPREP,HEADPX2
          CALL      PAGEND
          RETURN
.
HEADPX2   CALL      PAGEND2
          RETURN
+
.
.* ****************************************** Start of Addition        *C-49016
CLRCTR00  MOVE      ZERO,FORM1
          WHILE     FORM1 < 9
            ADD       ONE,FORM1
            MOVE      ZERO,CTRSEX[FORM1]
          DO
.
          RETURN
.* ******************************************   End of Addition        *C-49016
.
.
ENDIT     CALL      KILL0000                     * remove temp files
          CHAIN     PGM
          STOP
+
.*******************************************************************************
.       Keyin hospital ID
.*******************************************************************************
KHOS0000  MOVE       ZERO,EXIT
          COMPARE    ONE,IBCNMHOS
          GOTO       KHOS9999 IF NOT EQUAL
.
          MOVE       SP70,PTHSHOSP
          DISPLAY    *P1:12,*EL,"Hospital Id : ";
          MOVE       TEN5,ECOL
          MOVE       TEN2,EVERT
          MOVE       SP3,CKYIDEF3
          MOVE       ZERO,CKYIMAND           * Not Mandatory
          OPEN       PATHSPA1,"pathspaf"
.
          CALL       PATHSPKY
          BRANCH     EXIT,KHOS1000,KHOS9000
          GOTO       KHOS3000
.
KHOS1000  MOVE       "All Hospitals",PTHSNAME
          MOVE       SP70,PTHSHOSP
.
KHOS3000  DISPLAY   *P20:12,*V2LON,PTHSNAME;
          MOVE       ZERO,EXIT
          GOTO      KHOS9999
.
KHOS9000  MOVE       ONE,EXIT
KHOS9999  RETURN
+
.**************************************************************************
.*                              CLER0000               Called by:         *
.*             Clear temp files of all records                            *
.**************************************************************************
.
CLER0000  MOVE      SP30,KEY28
          READ      PSTADMP1,KEY28;;             * position at start of file
          READKS    PSTADMP1;DIM20,XDAADMNO,AURNO,D5TIME,PSNAME,PGNAME:
                             DHOSP,DDSTAT,DCLMTAB,DNAME,TWARD,TBED
          GOTO      CLER1000 IF OVER
.
          PACK      KEY28,DIM20,XDAADMNO
          DELETE    PSTADMP1,KEY28               * delete record
          GOTO      CLER0000                     * get next record
.
CLER1000  MOVE      SP30,KEY28
          READ      PSTDSHP1,KEY28;;             * position at start of file
          READKS    PSTDSHP1;DIM20,XDAADMNO,DURNO,D5TIME,PSNAME,PGNAME:
                             DHOSP,DDSTAT,DCLMTAB,DNAME,TWARD,TBED
          GOTO      CLER2000 IF OVER
.
          PACK      KEY28,DIM20,XDAADMNO
          DELETE    PSTDSHP1,KEY28               * delete record
          GOTO      CLER1000                     * get next record
.
CLER2000  MOVE      SP30,KEY14
          READ      WSTADMP1,KEY14;;             * position at start of file
          READKS    WSTADMP1;XWARD,XBED,XWAADMNO,AURNO,D5TIME,PSNAME,PGNAME:
                             DHOSP,DDSTAT,DCLMTAB,DNAME,DIM20        
          GOTO      CLER3000 IF OVER
.
          PACK      KEY14,XWARD,XBED,XWAADMNO
          DELETE    WSTADMP1,KEY14               * delete record
          GOTO      CLER2000                     * get next record
.
CLER3000  MOVE      SP30,KEY14
          READ      WSTDSHP1,KEY14;;             * position at start of file
          READKS    WSTDSHP1;XWARD,XBED,XWAADMNO,DURNO,D5TIME,PSNAME,PGNAME:
                             DHOSP,DDSTAT,DCLMTAB,DNAME,DIM20
          GOTO      CLER4000 IF OVER
.
          PACK      KEY14,XWARD,XBED,XWAADMNO
          DELETE    WSTDSHP1,KEY14               * delete record
          GOTO      CLER3000                     * get next record
.
CLER4000  COMPARE   CMATRN,ONE
          GOTO      CREA9999 IF NOT EQUAL
.
CLER5000  MOVE      SP30,KEY29
          READ      MATATMP1,KEY29;;
          READKS    MATATMP1;NBNAME,NBMATNO,NBPLUR,NBURNO:
                             NBSEX,NBDOB,NBSTAT,NBGNAME,PGNAME
          GOTO      CLER6000 IF OVER
.
          PACK      KEY29,NBNAME,NBMATNO
          DELETE    MATATMP1,KEY29               * delete record
          GOTO      CLER5000                     * get next record
.
CLER6000  MOVE      SP30,KEY29
          READ      MATDTMP1,KEY29;;
          READKS    MATDTMP1;NBNAME,NBMATNO,NBPLUR,NBURNO:
                             NBSEX,NBDOB,NBSTAT,NBGNAME,PGNAME
          GOTO      CLER9999 IF OVER
.
          PACK      KEY29,NBNAME,NBMATNO
          DELETE    MATDTMP1,KEY29               * delete record
          GOTO      CLER6000                     * get next record
.
CLER9999  RETURN
+
.**************************************************************************
.*                              CREA0000               Called by:         *
.*             create a new temporary file                                *
.**************************************************************************
.
CREA0000  CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,TEMPFIL1
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,TEMPFIL2
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,TEMPFIL3
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,TEMPFIL4
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,TEMPFIL5
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,TEMPFIL6
.
          CALL      KILL0000                     * remove existing file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,TEMPFIL1,UKEY1
          EXECUTE   CMDLINE,TASKID               * create temporary index file
          OPEN	    PSTADMP1,TEMPFIL1
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,TEMPFIL2,UKEY1
          EXECUTE   CMDLINE,TASKID               * create temporary index file
          OPEN	    PSTDSHP1,TEMPFIL2
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,TEMPFIL5,UKEY3
          EXECUTE   CMDLINE,TASKID               * create temporary index file
          OPEN	    WSTADMP1,TEMPFIL5
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,TEMPFIL6,UKEY3
          EXECUTE   CMDLINE,TASKID               * create temporary index file
          OPEN	    WSTDSHP1,TEMPFIL6
.
          COMPARE   CMATRN,ONE
          GOTO      CREA9999 IF NOT EQUAL
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,TEMPFIL3,UKEY2
          EXECUTE   CMDLINE,TASKID               * create temporary index file
          OPEN	    MATATMP1,TEMPFIL3
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,TEMPFIL4,UKEY2
          EXECUTE   CMDLINE,TASKID               * create temporary index file
          OPEN	    MATDTMP1,TEMPFIL4
.
CREA9999  RETURN
+
.****************************************************************************
.*                              KILL0000               Called by:           *
.*               close and erase the temporary file                         *
.****************************************************************************
.
KILL0000  CLOSE     PSTADMP1                     * close file
          CLOSE     PSTDSHP1                     * close file
          CLOSE	    MATATMP1
          CLOSE	    MATDTMP1
          CLOSE     WSTADMP1                     * close file
          CLOSE     WSTDSHP1                     * close file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ERASE,TEMPFIL1       * set file erase command
          EXECUTE   CMDLINE,TASKID               * erase temp file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ERASE,TEMPFIL2       * set file erase command
          EXECUTE   CMDLINE,TASKID               * erase temp file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ERASE,TEMPFIL5       * set file erase command
          EXECUTE   CMDLINE,TASKID               * erase temp file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ERASE,TEMPFIL6       * set file erase command
          EXECUTE   CMDLINE,TASKID               * erase temp file
.
          COMPARE   CMATRN,ONE
          GOTO      KILL9999 IF NOT EQUAL
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ERASE,TEMPFIL3       * set file erase command
          EXECUTE   CMDLINE,TASKID               * erase temp file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ERASE,TEMPFIL4       * set file erase command
          EXECUTE   CMDLINE,TASKID               * erase temp file
.
KILL9999 RETURN
+
.==============================================================================
.
          INCLUDE   STD001IO
.
          INC       PATHSPKY
          INC       SEXDSCIO
          INC       TFILENAM                     * Generate Temp File Name
.
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATDSCIO/INC
          INC       PATFN1IO/INC
          INC       PATHSPIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATTRNIO/INC
          INC       PMSVX1IO/INC
          INC       WEBERRIO/INC                 * Web Server Error Logging

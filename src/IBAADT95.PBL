.*******************************************************************************
.*              P R O G R A M  S U M M A R Y                                   *
.*              -------------  -------------                                   *
.*                                                                             *
.*     PROGRAM NAME:     IBAADT95                                              *
.*                                                                             *
.*     FUNCTION:         Bad Debt Patient  Report                              *
.*                                                                             *
.*     DATE WRITTEN:     13/09/04                                              *
.*                                                                             *
.*     AUTHOR:           Guomin Fu                                             *
.*                                                                             *
.*     MODIFICATIONS:                                                          *
.*                                                                             *
.*     V12.00.01 16/05/2025  Ebon Clements      TSK 0955096                    *
.*               Alphanueric visit number changes                              *
.*     V10.13.01 05/12/2018  Don Nguyen         TSK 0838558                    *
.*               Deleted temp file (ROLLFILE) after processing                 *
.*     V10.09.01 13/02/2017  Ania P             CAR 261630                     *
.*               Removed use of PORT                                           *
.*      V9.04.01 01/08/2005  J.Tan              CAR 69331                      *
.*               Mods for multi hospital                                       *
.*      V5.12.00 13/09/2004  Guomin Fu          CAR 45088                      *
.*               Clone from IBAHOS04                                           *
.*******************************************************************************
.
          INC       STD001FD
          INC       IBAPASFD/INC
.
          INC       IBASECFD/INC
          INC       PATINVFD/INC
          INC       PATVISFD/INC
          INC       HL7BMTFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATWC1FD/INC
          INC       PATSELDT/INC
          INC       OUTSITFD/INC
          INC       PATDSCFD/INC
          INC       PATHSPFD/INC
          INC       PMSVX1FD/INC
          INC       IBASEQFD/INC 
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
.
.        EXTRA   VARIABLES
.
ROLLFILE  FILE      ASCII, FIXED=256
SORTSTR   DIM       20
SORT1     INIT      "u1-37"
SORT2     INIT      "u77-79,1-37"
.
WORKFILE  IFILE     SQL, FIXED=112, KEY=SORTSTR
.
.
. ----- Tempfile consists of following vars -----
.
. Name          Type      Length     Physical     Start   Description
. ----          ----      ------     --------     -----   -----------
.WSNAME         DIM       20         20             1
.WGNAME         DIM        1          1            21
DWADMNO         DIM        8          8            22
.WINVNO         DIM        8          8            30
.WINVTOT        FORM       8.2        6            38
.WFDINS         DIM        6          6            44
.WFUNP          FORM       6.2        5            50
.WINSP          FORM       6.2        5            55
.WPATP          FORM       6.2        5            60
.WJOUP          FORM       8.2        6            65
.WBALANCE       FORM       8.2        6            71
.WCLAIM         DIM        3          3            77
.WINVDTE        DIM        8          8            80
DWINVUR         DIM        8          8            88
. ----- EOR 112-----
.
ZEROES    FORM      " .00"
.
CMDLINE   DIM       30
FNAME     DIM       8
TEMPFILE  DIM       8
DATFILEW  INIT      "workfl"
TMPFILEH  INIT      "tmprol"
.
COUNT     FORM      2
CODE      DIM       2
CODECL    INIT      "CL"
.
DIM8      DIM       8
DIM22     DIM       22
.
FIVE9     FORM      "59"
FROMDATE  DIM       8
PNAME     DIM       22
.
SAVPG     FORM      4
SAVCLM    DIM       3
.
LNO       FORM      2
LN55      FORM      "55"
.
. ----- Variables from tempfile WORKFILE
.
WADMNO    DIM       8
WINVUR    FORM      8
WSNAME    DIM      20
WGNAME    DIM       1
WINVNO    DIM       8 
WINVTOT   FORM      8.2
WFDINS    DIM       6 
WFUNP     FORM      6.2
WINSP     FORM      6.2
WPATP     FORM      6.2
WJOUP     FORM      8.2
WBALANCE  FORM      8.2
WCLAIM    DIM       3
WINVDTE   DIM       8
WADATE    DIM       8
WDDATE    DIM       8
WTADATE   DIM       10
WTDDATE   DIM       10
.
XDAY1     DIM       2
XMON1     DIM       2
XYEAR1    DIM       2
XCENT1    DIM       2
.
TINVTOT   FORM      8.2
TFUNP     FORM      6.2
TINSP     FORM      6.2
TPATP     FORM      6.2
TJOUP     FORM      8.2
TBALANCE  FORM      8.2
TODATE    DIM       8
TYPE      DIM       3
AAE       INIT      "AAE"
OUT       INIT      "OUT"
INP       INIT      "INP"
OTH       INIT      "OTH"
.
IBCNMHOS  FORM      1
IBCNAESN  DIM       3           AAE System Number
IBCNOPSN  DIM       3           Outpatients System Number
IBCNBLSN  DIM       3           Patient Billing System Number
IBCNMASN  DIM       3           Mamography System Number
IBCNRASN  DIM       3           Radiology System Number
IBCNINSN  DIM       3           Inpatient System Number
.
GINVTOT   FORM      8.2
GFUNP     FORM      6.2
GINSP     FORM      6.2
GPATP     FORM      6.2
GJOUP     FORM      8.2
GBALANCE  FORM      8.2
.
.
PRGID     INIT      "IBAADT95"
PRGNAM    INIT      "BAD DEBT PATIENT REPORT"
VERSION   INIT      "V12.01.00"
.
.         START OF PROGRAM
.
          CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening patinvrf";
          OPEN      PATINVR2,"patinvrf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patwc1af";
          OPEN      PATWC1A1,"patwc1af"
.
          DISPLAY   *P64:24,"outsitaf";
          OPEN      OUTSITA1,"outsitaf"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA1,"patvisaf"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,TEN3;*188,CMABINS,*194,CVETINS
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          CALL TFILENAM
          PACK FNAME,TFILNAME 
.
          CALL TFILENAM
          PACK TEMPFILE,TFILNAME
.
MENU      MOVE      SP3,SAVCLM
          MOVE      SP4,SAVPG
.
BEGIN     CALL      DISPHEAD
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Surname Sequence":
                    *P1:6,*V2LON,TWO,*HOFF," = Surname Sequence with ":
                                               "Claim Code"
.
         KEYIN 	    *P1:8,*EL,"Please Select : ",*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
REPEAT    BRANCH    OPTION OF KSORT10,KSORT20
          DISPLAY   *P35:8,*EL,*B,*V2LON,"** Invalid Selection **",*W2;
          GOTO      BEGIN 
.
KSORT10   MOVE      SORT1,SORTSTR        * Surname sequence only
          GOTO      KADAT
.
KSORT20   MOVE      SORT2,SORTSTR        * Claim code and surname
.
.         Keyin from date
.
KADAT     DISPLAY   *P1:12,*EF,"Invoice From Date : ":
                    *P1:14,"Invoice To Date   :"
.
          MOVE      TEN2,CVERT
          MOVE      TWENTY2,CCOL
          MOVE      ZERO,CHIGHLT
          MOVE      ZERO,CDEFDTE
.
          PACK      FROMDATE,CCC,CYY,CMM,CDD
          UNPACK    FROMDATE,CCENT,CYEAR,CMON,CDAY
.
          MOVE      "45",ECOL           * Error message
          MOVE      CVERT,EVERT
          CALL      KEYDATE
          BRANCH    OVRCD OF BEGIN
.
          PACK      FROMDATE WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",FROMDATE
.
.         Keyin todate
.
          MOVE      TEN4,CVERT
          MOVE      TWENTY2,CCOL
.
          MOVE      "45",ECOL          * Error message
          MOVE      CVERT,EVERT
          CALL      KEYDATE
          BRANCH    OVRCD OF KADAT
.
          PACK      TODATE WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE
.
          MATCH     FROMDATE,TODATE
          GOTO      INVDATE IF LESS
.
CONFIRM   CALL      KHOS0000
          BRANCH    EXIT,KADAT
.
          CALL      PATSELFN
          BRANCH    EXIT,BEGIN
.
          KEYIN     *P1:24,*EL,"Ok to continue (":
                    *V2LON,*DV,ANSY,*HOFF,*DV,SLASH,*V2LON,*DV,ANSN,*HOFF:
                    ") ? ",*V2LON,ANS,*C,*EL
.
          REP       UPPLOW,ANS
          CMATCH    ANSY,ANS
          GOTO      PROCESS IF EQUAL
.
          CMATCH    ANSN,ANS
          GOTO      BEGIN IF EQUAL
.
          BEEP
          GOTO      CONFIRM
.
INVDATE   DISPLAY   *P1:24,*EL,*P20:24,*V2LON,*B:
                    "** Fromdate must be less than Todate **",*W3;
          GOTO      KADAT
.
.
PROCESS   DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** Initialise Work File **",*W2;
          CALL      CREATWRK
.
          DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** Process Outstanding Debtors **",*W2;
          DISPLAY   *P1:24,*EL,*P10:24:
                    "Processing Invoice Number :",*P50:24,"Scanning :"
.
          PACK      KEY16 WITH FROMDATE,SP8
          CALL      RDSINV2 
LOPINV    CALL      RDKINV2
          BRANCH    OVRCD OF ENDINV
.
.         Check with the Todate
.
          MATCH     PINVDTE,TODATE
          GOTO      ENDINV IF LESS
.
.         Ignore  cancelled invoice
.
          MATCH     ZEROVISN,PINVADM
          GOTO      LOPINV IF EQUAL
.
          DISPLAY   *P61:24,PINVNO;
.
          COMPARE   FSTSYS,PINVSYS
          GOTO      LOPINV IF LESS
.
          COMPARE   PINVSYS,FEDSYS
          GOTO      LOPINV IF LESS
.
          MATCH     FSTSITE,PINVSITE
          GOTO      LOPINV IF LESS
.
          MATCH     PINVSITE,FEDSITE
          GOTO      LOPINV IF LESS
.
          MATCH     SP3,FSTDEPT
          GOTO      CNTI IF EQUAL
.
          MOVE      PINVSITE,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD,LOPINV
.
          MATCH     FSTDEPT,OSTSYST
          GOTO      LOPINV IF LESS
.
          MATCH     OSTSYST,FEDDEPT
          GOTO      LOPINV IF LESS
.
CNTI      MOVE      PINVAMT,WBALANCE
          ADD       PINVHFDA,WBALANCE
          ADD       PINVOTHA,WBALANCE
          ADD       PINVPATA,WBALANCE
          ADD       PINVJOUR,WBALANCE
          ADD       PTINGSTJ,WBALANCE
.
          DISPLAY   *P38:24,*V2LON,PINVNO;
.
          PACK      PSNAME WITH SP10,SP10
          PACK      PGNAME WITH SP10,SP10,SP10
.
          MOVE      PINVUR,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF INVMAST
.
          COMPARE   ONE,PBDEBT
          GOTO      LOPINV IF NOT EQUAL
.
          COMPARE   ONE,IBCNMHOS
          GOTO      CNTI100 IF NOT EQUAL
.
          MATCH     SP10,PTHSHOSP
          GOTO      CNTI100 IF EQUAL
.
          MOVE      PINVADM,KEY8
          CALL      RDPMVX11
          BRANCH    OVRCD,LOPINV
.
          MATCH     PMVXMHOS,PTHSHOSP
          GOTO      LOPINV IF NOT EQUAL
.
CNTI100   MOVE      PGNAME,WGNAME
          MOVE      PINVADM,DWADMNO
          MOVE      PINVUR,DWINVUR
          MOVE      PSNAME,WSNAME
          PACK      KEY37 WITH WSNAME,WGNAME,DWADMNO,PINVNO
.
          MOVE      PINVCLM,WCLAIM
          MOVE      PINVADM,WADMNO
          MOVE      PINVNO,WINVNO
          MOVE      PINVAMT,WINVTOT
          MOVE      PINVHFDA,WFUNP
          MOVE      PINVOTHA,WINSP
          MOVE      PINVPATA,WPATP
          MOVE      PINVJOUR,WJOUP
          ADD       PTINGSTJ,WJOUP
          MOVE      PINVDTE,WINVDTE
.
          MOVE      SP8,WADATE
          MOVE      SP8,WDDATE
          UNPACK    SP5 WITH TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5
          MOVE      "CL",TCODE
          PACK      KEY5 WITH TCODE,PINVCLM
          CALL      RDCODE1
.
          MOVE      ZERO,COUNT
CLMLOOP   ADD       ONE,COUNT
          COMPARE   SIX,COUNT
          GOTO      NOTACLM IF NOT LESS
.
          LOAD      ANS USING COUNT OF TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5
.
          REP       UPPLOW,ANS
          CMATCH    "W",ANS
          GOTO      WORKCMP IF EQUAL
.
          CMATCH    "M",ANS
          GOTO      MABCODE IF EQUAL
.
          CMATCH    "V",ANS
          GOTO      VETCODE IF EQUAL
.
          GOTO      CLMLOOP
.
NOTACLM   MOVE      SP6,AFUNDH
          MOVE      PINVADM,KEY8
          IF        PINVSYS=3
            CALL      RDMISS1
            IF        OVRCD<>1 
              MOVE      AFUNDH,WFDINS
            ENDIF
          ELSE
            MOVE      PINVUR,KEY8
            CALL      RDMAST1
            IF        OVRCD<>1
              MOVE      PFUNDH,WFDINS
            ENDIF
          ENDIF
          GOTO      OKWRT
.
WORKCMP   MOVE      "(WC)  ",WFDINS
          MOVE      PINVADM,KEY8
          CALL      RDWCOM1
          BRANCH    OVRCD OF OKWRT
.
          MATCH     SP6,WCICODE
          GOTO      OKWRT IF EQUAL
.
          MOVE      WCICODE,WFDINS
          GOTO      OKWRT
.
MABCODE   MOVE      "(MAB) ",WFDINS
          MATCH     SP6,CMABINS
          GOTO      OKWRT IF EQUAL
          MOVE      CMABINS,WFDINS
          GOTO      OKWRT
.
VETCODE   MOVE      "(VET) ",WFDINS
          MATCH     SP6,CVETINS
          GOTO      OKWRT IF EQUAL
.
          MOVE      CVETINS,WFDINS
.
OKWRT     MOVE      PINVADM,KEY8
          IF        PINVSYS=3        
            CALL      RDMISS1
            IF        OVRCD<>1
              MOVE      ADATE,WADATE
            ENDIF
          ELSE
            CALL      RDVISA1
            IF        OVRCD<>1
              MOVE      PVIDATE,WADATE
            ENDIF
          ENDIF
.
          CALL      RDDSCH1 
          IF        OVRCD<>1
            MOVE      DDATE,WDDATE 
          ENDIF
          CALL      WRWORK1
          GOTO      LOPINV
.
.      PRINT OUTSTANDING DEBTOR REPORT
.
ENDINV 
          MOVE      ZERO,TINVTOT
          MOVE      ZERO,TFUNP
          MOVE      ZERO,TINSP
          MOVE      ZERO,TPATP
          MOVE      ZERO,TJOUP
          MOVE      ZERO,TBALANCE
.
          MOVE      ZERO,GINVTOT
          MOVE      ZERO,GFUNP
          MOVE      ZERO,GINSP
          MOVE      ZERO,GPATP
          MOVE      ZERO,GJOUP
          MOVE      ZERO,GBALANCE
          MOVE      SP3,SAVCLM
.
          DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** Generating Bad Debt Patient Report **":
                    *W2,*C,*EL
.
          DISPLAY   *P1:24,*EL,*P10:24:
                    "Printing Patient Name :"
.
          MOVE      ZERO,CPAGENO
          MOVE      "60",LNO
.
          PACK      KEY37 WITH SP30,SP5
          CALL      RDSWORK1
PRTLOP    CALL      RDKWORK1
          BRANCH    OVRCD OF EOPRT
. 
          PACK      DIM22 WITH WGNAME,DOT,WSNAME
          MOVE      WADMNO,DIM8
.
          DISPLAY   *P34:24,*V2LON,DIM22;
.
          COMPARE   ZERO,CPAGENO
          GOTO      NOTFST IF NOT EQUAL
.
          MOVE      WADMNO,PINVADM       **** SAVE ADMISSION NUMBER
          MOVE      WADMNO,DIM8
          GOTO      OKCONT
.
NOTFST    MATCH     WADMNO,PINVADM
          GOTO      NOTSAM IF NOT EQUAL
.
          MOVE      SP8,DIM8
          MOVE      SP30,DIM22
          GOTO      OKCONT
.
NOTSAM    MOVE      WADMNO,PINVADM       **** SAVE ADMISSION NUMBER
.
OKCONT    MULT      SEQ,WFUNP
          MULT      SEQ,WINSP
          MULT      SEQ,WPATP
          MULT      SEQ,WJOUP
.
          BRANCH    OPTION,OKCNT1
.
          MATCH     WCLAIM,SAVCLM
          GOTO      OKCNT1 IF EQUAL
.
          MOVE      WCLAIM,SAVCLM
.
          COMPARE   ZERO,CPAGENO
          GOTO      OKCNT1 IF EQUAL
.
          CALL      SUBTOT
          CALL      PRTHED0
.
OKCNT1    COMPARE   LN55,LNO
          CALL      PRINTH IF NOT LESS
.
          MOVE      "***",TYPE                    * default
          PACK      KEY8,DIM8
          CALL      RDVISA1
          BRANCH    OVRCD,PRNT000
.
          LOAD      TYPE,PVITYPE,AAE,OUT,INP,OTH  * load description
.
PRNT000   UNPACK    WADATE WITH CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,WTADATE
          UNPACK    SP8,CCENT,CYEAR,CMON,CDAY
          UNPACK    WDDATE WITH CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,WTDDATE
          PRINT     *1,"!",DWINVUR,*10,"!",DIM22,*33,"!",DIM8:
                    *42,"!",TYPE,*49,"!",WFDINS,*56,"!",WTADATE:
                    *67,"!",WTDDATE,*78,"!",WINVNO,*87,"!",WBALANCE:
                    *100,"!"
.                           
          ADD       WINVTOT,TINVTOT
          ADD       WFUNP,TFUNP
          ADD       WINSP,TINSP
          ADD       WPATP,TPATP
          ADD       WJOUP,TJOUP
          ADD       WBALANCE,TBALANCE
.
          ADD       ONE,LNO
          GOTO      PRTLOP
.
EOPRT     COMPARE   ZERO,CPAGENO
          GOTO      NODEBT IF EQUAL
.
          COMPARE   ONE,OVRCD
          GOTO      PRTLIN IF NOT EQUAL
.
          CALL      LINE
.
PRTLIN    BRANCH    OPTION OF PRTTOT,PRTGRD
.
PRTTOT    PRINT     *87,"!",TBALANCE,*100,"!",*N 
.
          GOTO      CONPRT
.
PRTGRD    CALL      SUBTOT
          PRINT     *45,GBALANCE,*N 
.
CONPRT    PRINT     *N,*N,"*** End Of Report ***"
.
          DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** Generation Completed Successfully **":
                    *W2,*C,*EL
          CALL      DELETWRK
          GOTO      BEGIN
.
SUBTOT    COMPARE   ONE,OVRCD
          GOTO      SBTOT1 IF EQUAL
.        
          CALL      LINE
.
SBTOT1    PRINT     *45,"!",TBALANCE:
                    *N,"*-----------------------------------------":
                       "-----------------------------------------":
                       "------------------*"     
.
          ADD       TINVTOT,GINVTOT
          ADD       TFUNP,GFUNP
          ADD       TINSP,GINSP
          ADD       TPATP,GPATP
          ADD       TJOUP,GJOUP
          ADD       TBALANCE,GBALANCE
.
          MOVE      ZERO,TINVTOT
          MOVE      ZERO,TFUNP
          MOVE      ZERO,TINSP
          MOVE      ZERO,TPATP
          MOVE      ZERO,TJOUP
          MOVE      ZERO,TBALANCE
          RETURN
.
NODEBT    DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** No Outstanding Debtors **":
                    *W3,*C,*EL
          CALL      DELETWRK
          GOTO      BEGIN
.
PRINTH    COMPARE   ZERO,CPAGENO
          CALL      LINE IF NOT EQUAL
.
PRTHED0   UNPACK    FROMDATE WITH XCENT,XYEAR,XMON,XDAY
          UNPACK    TODATE WITH XCENT1,XYEAR1,XMON1,XDAY1
.
          BRANCH    OPTION OF PRTHED1,PRTHED2
.
PRTHED1   CALL      IBACLOCK
          MOVE      ONE,CNOUNDLN
          CALL      HEAD132
          PRINT     *N,*40,"SURNAME SEQUENCE":
                    *N,*40,"Invoice From Date : ",XDAY,SLASH,XMON,SLASH,XCENT:
                    XYEAR:
                    " to ",XDAY1,SLASH,XMON1,SLASH,XCENT1,XYEAR1 
          GOTO      CPRINTH
.
PRTHED2   CLOCK     TIME,CTIMEIS
          MOVE      SP20,TDESC
.
          MATCH     SP3,WCLAIM
          GOTO      PRTHED3 IF EQUAL
.
          PACK      KEY5 WITH CODECL,WCLAIM
          CALL      RDCODE1
.
PRTHED3   MOVE      ONE,CNOUNDLN
          CALL      HEAD132
          PRINT     *N,*40,"SURNAME SEQUENCE WITH CLAIM CODE":
                    *N,*40,"Invoice From Date : ",XDAY,SLASH,XMON,SLASH,XCENT:
                    XYEAR:
                    " to ",XDAY1,SLASH,XMON1,SLASH,XCENT1,XYEAR1:
                    *N,*40,"CLAIM CODE - ",WCLAIM,"   ",TDESC
. 
CPRINTH   IF          IBCNMHOS =1
            PRINT     *40,"Hospital : ",PTHSNAME
          ELSE
            PRINT     *N;
          ENDIF
.
          CALL      PRTSEL
          CALL      LINE
.
          PRINT     *1,"! U/R No !",*11," Patient Name ",*33,"! Adm No ":
                    *42,"! Sys  ","!Health","! Adm     ",*67,"!Discharge":
                    *78,"! Invoice",*87,"! Balance",*100,"!":       
                    *N,*1,"!",*10,"!",*33,"!":
                    *42,"! Type",*49,"!Fund",*56,"! Date",*67,"!Date":
                    *78,"! No":
                    *87,"!",*100,"!"
          CALL      LINE
.
          MOVE      TEN,LNO
          RETURN
.
LINE      PRINT     *1,"*-----------------------------":
                       "------------------------------":
                       "------------------------------":
                       "------------------------------":
                       "-----------*"
          RETURN
.
INVMAST   DISPLAY   *P1:23,*EL,*B,"U/R No.: ",*V2LON,PINVUR:
                    "  *** Master File Missing ":
                    "- Contact IBA *** ",*W2;
          GOTO      LOPINV
.
RDSWORK1  RESET     KEY37
          READ      WORKFILE,KEY37;;
          RETURN  
.
RDWORK1   MOVE      ZERO,OVRCD
          RESET     KEY37
          READ      WORKFILE,KEY37;WSNAME,WGNAME,DWADMNO,WINVNO,WINVTOT:
                                   WFDINS,WFUNP,WINSP,WPATP,WJOUP,WBALANCE:
                                   WCLAIM,WINVDTE,DWINVUR,WADATE,WDDATE
          GOTO      OVERCOND IF OVER
          MOVE      DWADMNO,WADMNO
          MOVE      DWINVUR,WINVUR
          RETURN  
.
RDKWORK1  MOVE      ZERO,OVRCD
          READKS    WORKFILE;WSNAME,WGNAME,DWADMNO,WINVNO,WINVTOT:
                             WFDINS,WFUNP,WINSP,WPATP,WJOUP,WBALANCE:
                             WCLAIM,WINVDTE,DWINVUR,WADATE,WDDATE
          GOTO      OVERCOND IF OVER
          MOVE      DWADMNO,WADMNO
          MOVE      DWINVUR,WINVUR
          RETURN  
.
WRWORK1   RESET     KEY37
          MOVE      ZERO,OVRCD
          WRITE     WORKFILE,KEY37;KEY37,WINVTOT:
                             WFDINS,WFUNP,WINSP,WPATP,WJOUP,WBALANCE:
                             WCLAIM,WINVDTE,DWINVUR,WADATE,WDDATE
          RETURN  
.
UPWORK1   UPDATE    WORKFILE;WSNAME,WGNAME,DWADMNO,WINVNO,WINVTOT:
                             WFDINS,WFUNP,WINSP,WPATP,WJOUP,WBALANCE:
                             WCLAIM,WINVDTE,DWINVUR,WADATE,WDDATE
          RETURN  
.
CREATWRK  MOVE      ZERO,OVRCD
          CALL      DELETWRK
          PREP      ROLLFILE,TEMPFILE
.
          BRANCH    OPTION OF IDXSUR
.
          WRITE     ROLLFILE,SEQ;"isbuild ",FNAME," 112 ","u77-79,1-37"
          GOTO      IDXNOW
.
IDXSUR    WRITE     ROLLFILE,SEQ;"isbuild ",FNAME," 112 ","u1-37"
.
IDXNOW    WEOF      ROLLFILE,SEQ
          CLOSE     ROLLFILE
          CALL      MAINWRK1
          OPEN      WORKFILE,FNAME
          RETURN
.
.
DELETWRK  CLOSE     WORKFILE
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          RETURN
.
MAINWRK1  CLEAR     CMDLINE
          APPEND    "sh ",CMDLINE
          APPEND    TEMPFILE,CMDLINE
          APPEND    ".txt",CMDLINE
          RESET     CMDLINE
.
          DISPLAY   *P1:10,*EF;
          EXECUTE   CMDLINE,TASKID
          DISPLAY   *P1:10,*EF;
.
          PREP      ROLLFILE,TEMPFILE
          CLOSE     ROLLFILE,DELETE
          RETURN
.
PRTSEL    COMPARE   ZERO,FSTSYS
          GOTO      AEHEAD IF NOT EQUAL
.
          PRINT     *40,"CONSOLIDATED REPORT",*N,*N;
          COMPARE   ZERO,FSTSYS
          RETURN    IF EQUAL
.
AEHEAD    COMPARE   ONE,FSTSYS
          GOTO      OPHEAD IF NOT EQUAL
.
          PRINT     *40,"ACCIDENT & EMERGENCY REPORT",*N,*N;
          COMPARE   ONE,FSTSYS
          RETURN    IF EQUAL
.
OPHEAD    COMPARE   TWO,FSTSYS
          GOTO      IPHEAD IF NOT EQUAL
.
          PRINT     *40,"OUTPATIENT REPORT",*N;
          MATCH     SP6,FSTSITE
          GOTO      ALSITE IF NOT EQUAL
.
          PRINT     *40,"ALL SITES";
          GOTO      OPDEPT
.
ALSITE    PRINT     *40,"SITE",FSTSITE;
.
OPDEPT    MATCH     SP3,FSTDEPT
          GOTO      ALDEPT IF NOT EQUAL
.
          PRINT     *54,"ALL DEPARTMENTS",*N,*N;
          GOTO      PRTSEL9
.
ALDEPT    PRINT     *54,"DEPT",FSTDEPT,*N,*N;
          GOTO      PRTSEL9
.
IPHEAD    COMPARE   THREE,FSTSYS
          GOTO      OTHEAD IF NOT EQUAL
.
          PRINT     *40,"INPATIENT REPORT",*N,*N;
          GOTO      PRTSEL9
.
OTHEAD    PRINT     *40,"OTHER FINANCIAL REPORT",*N,*N;
.
PRTSEL9   RETURN 
.
.******************************************************************************
.*                                  KHOS0000              Called by: ML0000   *
.*                            Enter The hospital                              *
.******************************************************************************
KHOS0000  COMPARE   ONE,IBCNMHOS
          GOTO      KHOS9999 IF NOT EQUAL
.
          DISPLAY   *P1:17,*EL,"Hospital Id : ";
          MOVE      TEN5,ECOL
          MOVE      TEN7,EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS2000,KHOS9500
          GOTO      KHOS3000
.
KHOS2000  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          MOVE      ZERO,EXIT
.
KHOS3000  CLOSE     PATHSPA1
.
          DISPLAY   *P20:17,*V2LON,PTHSNAME;
          GOTO      KHOS9999
.
KHOS9500  MOVE      ONE,EXIT
KHOS9999  RETURN
+
.******************************************************************************
.
          INC       STD001IO
          INC       PATCODDS
          INC       PATSELFN
          INC       PATHSPKY
          INC       TFILENAM
          INC       PMSVX1IO/INC
          INC       PATHSPIO/INC
          INC       PATCODIO/INC
          INC       PATVISIO/INC
          INC       HL7BMTIO/INC
          INC       PATWC1IO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATINVIO/INC
          INC       OUTSITIO/INC
          INC       PATDSCIO/INC
          INC       IBAPASIO/INC
          INC       IBASECIO/INC
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC
.
ENDIT     CHAIN     PGM
          STOP
.

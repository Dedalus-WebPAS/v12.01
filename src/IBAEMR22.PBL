.******************************************************************************
.*              P R O G R A M  S U M M A R Y                                  *
.*              -------------  -------------                                  *
.*                                                                            *
.*     PROGRAM NAME:    IBAEMR22                                              *
.*                                                                            *
.*     FUNCTION:        DOCTOR VISITS REPORT                                  *
.*                                                                            *
.*     DATE WRITTEN:    10/10/88                                              *
.*                                                                            *
.*     AUTHOR:          M.HAYSE (I.B.A)                                       *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.*        V9.12.01  21/01/2010 Ebon Clements     CAR 214346                   *
.*                  Exclude cancelled visits                                  *
.*        V9.11.00  09/04/2009 WEELEE KOO                                     *
.*                  Import from V6.16 and reformat codes.                     *
.*        V6.06.01  09/02/1999  Jill Habasque                                 *
.*                  Fixed DISPHEAD display                                    *
.*        V6.05.04  24/11/1998  Jill Habasque                                 *
.*                  Removed CCENTRY                                           *
.*        V6.05.03  02/11/1998  Nicole Harrington                             *
.*                  Fixed date displays                                       *
.*        V6.05.02  09/10/1998  Nicole Harrington                             *
.*                  Changed to use HEAD132 and DISPHEAD                       *
.*        V6.05.01  29/09/1998  Steve Armstrong  SRF 627503                   *
.*                  Recompiled for AAEDEAFD changes                           *
.******************************************************************************
. *                      V4.01 03/01/89 J.Tan   (I.B.A.)   *
. *                            Print patient given name    *
. *                            SRF 4221                    *
. *                      V4.02 13/01/89 J.Tan   (I.B.A.)   *
. *                            Add total number of patients*
. *                            SRF 4057                    *
. *                      V5.01 10//03/89 J.Tan  (I.B.A.)   *
. *                             Fixed AAEIODEA. SRF 4868   *
. *                      V5.02 21/04/89 K.Peak (I.B.A.)    *
. *                             Added CONTROLF. SRF 100081 *
. *                      V5.03 04/05/89 K.Peak (I.B.A.)    *
. *                             Added HNUMDES & fixed the  *
. *                             print layout for N.Zealand *
. *                      V5.04 16/05/89 K.Peak (I.B.A.)    *
. *                             New Zealand Changes        *
. *                      V5.05 05.06.89 S.Barcham          *
. *                            Recompiled for locking in   *
. *                            PATIOMAS        SRF 100809  *
. *                      V5.51 22.01.90 K.Peak SRF 103667  *
. *                            Added ADADIGC diag code to  *
. *                            AAEDEAFD & AAEIODEA         *
. **********************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. --------------------------
.
          INC       AAECONFD/INC          * controlfile ppp
          INC       EMRVISFD/INC

          INC       IBACONFD/INC          * control file
          INC       PATCONFD/INC          * inpatient controlfile
          INC       PATDO1FD/INC          * patient doctor file 
          INC       PATMA1FD/INC          * patient master file 
.
. LOCAL VARIABLE DEFINITION
. --------------------------.
.
FROMDATE  DIM       8        * FROMdate ccyymmdd
TODATE    DIM       8        * TOdate ccyymmdd
TOTPAT    FORM      5        * Total number of patients
DIM32     DIM       32       * patient name to print
DIM22     DIM       22       * doctor name to print
DIM5      DIM       5        * format time (EMVITIME-arrival time) 
DRTIME    DIM       5        * format time (EMVIDRTM-seen by doc)
LSTDOCT   DIM       6
DESCOPT   DIM       23       * option description in heading
VAR       FORM      1
.
. CONSTANT DEFINITION
. -------------------
.
PRGID     INIT      "IBAEMR22"
PRGNAM    INIT      "Doctor Visit Report"
VERSION   INIT      "V12.01.00"
.
.*****************************************************************************
.*                              MAIN0000                                     *
.*                       Controlling Logic (Mainline code)                   *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN1000  CALL      GTOP0000               * get user option
          BRANCH    EXIT,MAIN9999
.
MAIN2000  CALL      DATE0000               * get date range
          BRANCH    EXIT,MAIN1000
.
MAIN3000  CALL      COFM0000               * ok to continue?
          BRANCH    EXIT,MAIN1000
.
          PERFORM   OPTION,SGDC0000:       * 1 - single doctor  
                           DCCD0000:       * 2 - doc code sequence
                           DCSR0000:       * 3 - doc surname sequence
                           DTSQ0000;       * 4 - date sequence
.
MAIN5000  CALL      GRNT0000               * print grand total
.
MAIN9999  CHAIN     PGM
+
.
.*****************************************************************************
.*                       Initialization                                      *
.*                                                                           *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"emrvisaf";
          OPEN      EMRVISA4,"emrvisaf"
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,SEVENTY9;*115,PTCNDGUP,*116,PTCNDGUA,*239,PTCNDGAD:
                                    *240,PTCNDGDS,*241,PTCNDGTR,*242,PTCNDGRG
.
          READ      CONTROLF,ZERO;*2,CDD,CMM,CYY,CCC
          READ      CONTROLF,THIRTY;*168,HNUMDES
.
          RESET     HNUMDES,9
.
INIT1000  CMATCH    SP1,HNUMDES
          GOTO      INIT2000 IF NOT EQUAL
          BUMP      HNUMDES BY -1
          GOTO      INIT2000 IF EOS
          GOTO      INIT1000
.
INIT2000  MOVEFPTR  HNUMDES,VAR
          RESET     HNUMDES,VAR
          LENSET    HNUMDES
          RESET     HNUMDES
.
.
INIT9999  RETURN
.
.*****************************************************************************
.*                      Get User Option                                      *
.*                                                                           *
.*****************************************************************************
.
GTOP0000  MOVE      ZERO,CPAGENO
          MOVE      "60",CLNO
          MOVE      SP6,LSTDOCT
          MOVE      ZERO,TOTPAT
          MOVE      ZERO,EXIT
.
          HLINE     *G37,2,50,80
          DISPLAY   *P39:4,*EF:
                    *P1:5,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:6,*V2LON,ONE,*HOFF," = Single Doctor":
                    *P1:7,*V2LON,TWO,*HOFF," = All Doctors Code-sequence":
                    *P1:8,*V2LON,THREE,*HOFF," = All Doctors ":
                    "Surname-sequence":
                    *P1:9,*V2LON,FOUR,*HOFF," = Date Sequence"
.
          KEYIN     *P1:11,"Option : ",*V2LON,OPTION
.
          COMPARE   ZERO,OPTION
          GOTO      GTOP9000 IF EQUAL
.
          BRANCH    OPTION,GTOP1000:     * single doctor  
                           GTOP2000:     * code seq
                           GTOP3000:     * surname seq
                           GTOP4000      * date seq
.
          BEEP
          GOTO      GTOP0000
.
.         Screen display in heading
.
GTOP1000  DISPLAY   *P50:2,*+,"- ",*V2LON,"Single Doctor"
          MOVE      "Single Doctor          ",DESCOPT
          GOTO      GTOP9999
.
GTOP2000  DISPLAY   *P50:2,*+,"- ",*V2LON,"Code Sequence"
          MOVE      "Doctor Code Sequence   ",DESCOPT
          GOTO      GTOP9999
.
GTOP3000  DISPLAY   *P50:2,*+,"- ",*V2LON,"Surname Sequence"
          MOVE      "Doctor Surname Sequence",DESCOPT
          GOTO      GTOP9999
.
GTOP4000  DISPLAY   *P50:2,*+,"- ",*V2LON,"Date Sequence"
          MOVE      "Date Sequence          ",DESCOPT
          GOTO      GTOP9999
.
GTOP9000  MOVE      ONE,EXIT
.
GTOP9999  RETURN
.
.*****************************************************************************
.*                      Get user keyin Date                                  *
.*                                                                           *
.*****************************************************************************
.
DATE0000  DISPLAY   *P1:12,*EF:
                    *P1:12,"From  Date  :":
                    *P1:13,"To    Date  :"
.
          UNPACK    SP8,CCENT,CYEAR,CMON,CDAY
.
DATE1000  MOVE      TEN5,CCOL
          MOVE      TEN2,CVERT
.
          MOVE      "40",ECOL
          MOVE      CVERT,EVERT
          MOVE      CCC,CCENT
.
DATE2000  CALL      KEYDATE
          BRANCH    OVRCD,DATE9100
.
          PACK      FROMDATE WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",FROMDATE
.
DATE3000  UNPACK    SP8,CCENT,CYEAR,CMON,CDAY
.
          MOVE      TEN5,CCOL
          MOVE      TEN3,CVERT
          MOVE      "40",ECOL
          MOVE      CVERT,EVERT
.
          MOVE      CCC,CCENT
.
DATE4000  CALL      KEYDATE
          BRANCH    OVRCD,DATE1000
.
          PACK      TODATE WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE
.
          MATCH     FROMDATE,TODATE
          GOTO      DATE9000 IF LESS
.
          MOVE      ZERO,EXIT
          GOTO      DATE9999
.
DATE9000  DISPLAY   *P43:24,*B,*V2LON:
                    "** FROM Date Greater Than TO Date **",*W2,*P1:24,*EL;
          GOTO      DATE0000
.
DATE9100  MOVE      ONE,EXIT
.
DATE9999  RETURN
.
.*****************************************************************************
.*                       Get User Confirmation                               *
.*                                                                           *
.*****************************************************************************
.
COFM0000  KEYIN     *P1:15,*EL,"Ok to Continue (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF:
                    ") ? ",*V2LON,ANS;
.
          CMATCH    ANSN,ANS
          GOTO      COFM2000 IF EQUAL
.
          CMATCH    ANSY,ANS
          GOTO      COFM1000 IF EQUAL
.
          BEEP
          GOTO      COFM0000
.
COFM1000  MOVE      ZERO,EXIT
          GOTO      COFM9999
.
COFM2000  MOVE      ONE,EXIT
.
COFM9999  RETURN
.
.*****************************************************************************
.*                       If User Option = Single Doctor (1)                  *
.*                                                                           *
.*****************************************************************************
.
SGDC0000  MOVE      SP6,DCODE
          DISPLAY   *P1:15,*EL,"Doctor Code : "
          KEYIN     *P15:15,*EL,*DV,UNDLN6,*P15:15,*V2LON,DCODE
          RESET     DCODE
          GOTO      SGDC9000 IF EOS
.
          ENDSET    DCODE
          APPEND    SP6,DCODE
          RESET     DCODE
.
          DISPLAY   *P1:24,*EL,"Doctor : ",*V2LON,DCODE;
          CALL      DCSQ0000             * doctor sequence
.
          MOVE      ZERO,EXIT
          GOTO      SGDC9999
.
SGDC9000  MOVE      ONE,EXIT
.
SGDC9999  RETURN
.
.*****************************************************************************
.*                       If User Option = All Doctors Code-Sequence (2)      *
.*                                                                           *
.*****************************************************************************
.
DCCD0000  DISPLAY   *P1:24,*EL,"Doctor : ";
.
          MOVE      SP6,KEY6
          CALL      RDSDOCT1
DCCD1000  CALL      RDKDOCT1
          BRANCH    OVRCD,DCCD9000
.
          DISPLAY   *P15:24,*V2LON,DCODE;
          CALL      DCSQ0000     *  doctor sequence
.
          GOTO      DCCD1000
.
DCCD9000  MOVE      SP70,DCODE
          DISPLAY   *P15:24,*V2LON,DCODE;
          CALL      DCSQ0000     *  blank doctor sequence
.
DCCD9999  RETURN
.
.*****************************************************************************
.*                       If User Option = All Doctors Surname-Sequence (3)   *
.*                                                                           *
.*****************************************************************************
.
DCSR0000  MOVE      SP20,KEY12
          CALL      RDSDOCT2
DCSR1000  CALL      RDKDOCT2
          BRANCH    OVRCD,DCSR9000
.
          DISPLAY   *P15:24,*V2LON,DCODE,*HOFF,SP5,DSNAME;
          CALL      DCSQ0000     *  doctor sequence 
.
          GOTO      DCSR1000
.
DCSR9000  MOVE      SP70,DCODE
          DISPLAY   *P15:24,*V2LON,DCODE;
          CALL      DCSQ0000     *  blank doctor sequence
.
DCSR9999  RETURN
.
.*****************************************************************************
.*                       Subroutine for doctor sequence                      *
.*                                                                           *
.*****************************************************************************
.
DCSQ0000  PACK      KEY24,FROMDATE,SP30
          CALL      RSEMVIS4
DCSQ1000  CALL      RKEMVIS4
          BRANCH    OVRCD,DCSQ9999
.
          MATCH     EMVIDATE,TODATE
          GOTO      DCSQ9999 IF LESS 
.
          COMPARE   FOUR,EMVISTAT
          GOTO      DCSQ1000 IF EQUAL
.
          MATCH     DCODE,EMVIDOCT
          GOTO      DCSQ1000 IF NOT EQUAL
.
          CALL      PRNT0000
          GOTO      DCSQ1000
.
DCSQ9999  RETURN
.
.*****************************************************************************
.*                       If User Option = Date Sequence (4)                  *
.*                                                                           *
.*****************************************************************************
.
DTSQ0000  PACK      KEY24 WITH FROMDATE,SP30
          CALL      RSEMVIS4
DTSQ1000  CALL      RKEMVIS4
          BRANCH    OVRCD,DTSQ9999
.
          MATCH     EMVIDATE,TODATE
          GOTO      DTSQ9999 IF LESS
.
          COMPARE   FOUR,EMVISTAT
          GOTO      DTSQ1000 IF EQUAL
.
          CALL      PRNT0000
          GOTO      DTSQ1000
.
DTSQ9999  RETURN
.
.*****************************************************************************
.*                       Print Record Lines                                  *
.*                                                                           *
.*****************************************************************************
.
PRNT0000  COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          DISPLAY   *P55:24,"Date : ",EMVIDATE,SP1,EMVITIME;
.
          PRINT     *1,"|";
.
          MATCH     EMVIDOCT,LSTDOCT
          GOTO      PRNT1000 IF EQUAL
.
          CALL      DCNM0000                 * get doctor name
.
          PRINT     *2,EMVIDOCT,SP2,DIM22;
          MOVE      EMVIDOCT,LSTDOCT
.
PRNT1000  PRINT     *36,"| ";
.
          UNPACK    EMVIDATE INTO CCENT,CYEAR,CMON,CDAY
.
          CALL      PACDATE
          PRINT     *38,CPCDATE;
.
          CALL      PTNM0000                 * get patient name
          MOVE      EMVITIME,DIM5
          MOVE      EMVIDRTM,DRTIME
.
          PRINT     *49,"| ",DIM5,*57,"|  ",DRTIME,*67,"|  ",EMVIDURN:
                    *76,"|",EMVIURNO,*85,"|",EMVIADMN,*96,"| ",DIM32,*132,"|"
          ADD       ONE,CLNO
          ADD       ONE,TOTPAT
.
PRNT9999  RETURN
.
.*****************************************************************************
.*                       Print Grand Totals                                  *
.*                                                                           *
.*****************************************************************************
.
GRNT0000  COMPARE   ZERO,CPAGENO
          GOTO      GRNT9000 IF EQUAL
.
          CALL      PDOT0000
.
          PRINT      *N,"Total number of Patients : ",TOTPAT:
                     *N,*N,"** END OF REPORT **"
          DISPLAY   *P1:24,*EL:
                    *P40:24,*V2LON,"*** Generation Completed ***",*W2;
          GOTO      GRNT1000

GRNT1000  MOVE      ONE,EXIT
          GOTO      GRNT9999
.
GRNT9000  DISPLAY   *P1:24,*EL,*B:
                    *P40:24,*V2LON,"*** No Available Data ***",*W2;
          GOTO      GRNT1000
.
GRNT9999  RETURN
.
.*****************************************************************************
.*                       Display Heading                                     *
.*                                                                           *
.*****************************************************************************
.
HEAD0000  COMPARE   ZERO,CPAGENO
          CALL      PDOT0000 IF NOT EQUAL
.
.          CLOCK     TIME,CTIMEIS
.
          MOVE      ONE,CNOUNDLN
          MOVE      DESCOPT,CPHDROPT
          CALL      IBACLOCK
          CALL      HEAD132
.
          UNPACK    FROMDATE INTO CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *50,"From : ",*58,CPCDATE
.
          UNPACK    TODATE INTO CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *N,*50,"To   : ",*58,CPCDATE
.
          PRINT     *N
          CALL      PDOT0000
          PRINT       *1,"|Doctor  Doctor Name",*36,"| Visit":
                      *49,"|Arrival",*57,"|   Time":
                      *67,"|Duration",*76,"| U/R ",*85,"| ",HNUMDES:
                      *96,"| Patient Name",*132,"|":
                      *N,"|Code",*36,"| Date",*49,"| Time":
                      *57,"|Consulted",*67,"| (mins) ":
                      *76,"| Number",*85,"|",*96,"|":
                      *132,"|"
          CALL      PDOT0000
          MOVE      TEN2,CLNO
.
          MOVE      SP6,LSTDOCT     ** PRINT DOC NAME ON 1ST LINE NEW PAGE
.
HEAD9999  RETURN
.
.*****************************************************************************
.*                       Print dotted lines                                  *
.*                                                                           *
.*****************************************************************************
.
PDOT0000  PRINT     *1,"*--------------------------------------------":
                    "--------------------------------------------":
                    "------------------------------------------*"
          ADD       ONE,CLNO
.
PDOT9999  RETURN
.
.*****************************************************************************
.*                       Get doctor name in a dim22                          *
.*                                                                           *
.*****************************************************************************
.
DCNM0000  MOVE        SP30,DIM22
.
          MATCH       SP70,EMVIDOCT
          IF          @EQUAL
            MOVE        "Blank",DIM22
          ENDIF
.
          MOVE        EMVIDOCT,KEY6
          CALL        RDDOCT1
          BRANCH      OVRCD,OVERCOND
          MOVE        DGNAME,ANS
.
          ENDSET      DTITL
DCNM1000  CMATCH      SP1,DTITL
          GOTO        DCNM2000 IF NOT EQUAL
          BUMP        DTITL BY -1
          GOTO        DCNM1000 IF NOT EOS
.
DCNM2000  LENSET      DTITL
          RESET       DTITL
.
          PACK        DIM22 WITH DTITL,SP1,ANS,DOT,DSNAME
.
DCNM9999  RETURN
.
.*****************************************************************************
.*                       Get PATIENT name in a dim32                         *
.*                                                                           *
.*****************************************************************************
.
PTNM0000  PACK        DIM32 WITH SP20,SP20
          MOVE        EMVIURNO,PURNO
          MOVE        PURNO,KEY8
          CALL        RDMAST1
          BRANCH      OVRCD,OVERCOND
.
          MOVE        PTITL,DIM32
PTNM1000  CMATCH      SP1,DIM32
          GOTO        PTNM2000 IF EQUAL
.
          BUMP        DIM32 BY 1
          GOTO        PTNM1000 IF NOT EOS
          APPEND      SP1,DIM32
.
PTNM2000  APPEND      PGNAME,DIM32
.
PTNM3000  CMATCH      SP1,DIM32
          GOTO        PTNM4000 IF NOT EQUAL
.
          BUMP        DIM32 BY -1
          GOTO        PTNM3000 IF NOT EOS
.
PTNM4000  APPEND      SP1,DIM32
          APPEND      PSNAME,DIM32
.
PTNM9999  RETURN
.
.*****************************************************************************
.*                        I/O's Includes                                     *
.*                                                                           *
.*****************************************************************************
.
          INC       STD001IO
.
          INC       EMRVISIO/INC
          INC       PATDO1IO/INC         * doctor IO pat
          INC       PATMA1IO/INC
.
.

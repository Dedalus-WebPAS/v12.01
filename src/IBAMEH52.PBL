.*****************************************************************************
.*    Program      : IBAMEH52                                                *
.*    Description  : Leave/Discharge Report                                  *
.*                                                                           *
.*    Author       : ROD    (04/12/91)                                       *
.*    Function     : List all MH discharges, going on trial leave and        *
.*                   returns from trial leave on the specified date.         *
.*                                                                           *
.*    Modifications:                                                         *
.******************************************************************************
.*        V12.00.01 21.05.2025  DA Sarkies       TSK 0905641                 *
.*                  Updated for Alpha-Numeric Visit Number                   *
.******************************************************************************
.*        V10.04.01 28/04/2014  Steve Armstrong  CAR 261630                  *
.*                  Mods for non-port based tempfile use.                    *
.*****************************************************************************
.*        V10.02.01 28/03/2011  Mike Laming   CAR 240107                     *
.*                  Mods to PATECDaf & PATECOaf variables & Keys -file change*
.*****************************************************************************
.*        V10.01.02 10/02/2011  Jill Parkinson CAR 233088                    *
.*                  Added pmsvx1af                                           *
.*****************************************************************************
.*        V10.00.01 31/03/2010  Mike Laming  CAR 217575                      *
.*                  Recompiled for changes to MEHDIAFD                       *
.*****************************************************************************
.*        V9.03.01  04/12/2003  Sylvek Litewka  SRF 20222                    *
.*                  Removed reads of ICD Disease files as code description   *
.*                  should be obtained from patecdaf.PTEDDESC.               *
.*****************************************************************************
.*        V9.02.00  25/10/2002  Lina Vo     SRF 22733                        *
.*                  Ported to V9                                             *
.*****************************************************************************
.*        V5.08.01  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.B02 16/03/2000  Greg Horvat                                  *
.*                  Changed to get the coding from the episode coding files  *
.*        V5.08.B01 30/12/1999  Steve Armstrong                              *
.*                  Fixed global recompile bugs                              *
.*****************************************************************************
.*        V5.07.02 15/10/1999  Steve Armstrong   646015                      *
.*                 Fixed heading on report                                   *
.*        V5.07.01 28/09/1999  Andrew Peel  SRF  646015                      *
.*                 Fixed dates on report                                     *
.*        V5.07.00 27/10/1998  Davin                                         *
.*                 Mods for 507                                              *
.*****************************************************************************
.*        V5.05.01 22/09/1997  Steve Armstrong                               *
.*                 Mods for AXIS V to be a free text description.            *
.*                 (MEHRVDIA)                                                *
.*****************************************************************************
.*                   V5.01.002  30/06/92  ROD                                *
.*                              Ignore patients current status               *
.*                   V5.01.003  06/07/92  ROD                                *
.*                              Use Admiss date not last retn trial lve date *
.*                              23/07/92  ROD              SRF 116114        *
.*                              Use Legal status at appropriate date.        *
.*                   V5.01.004  29/07/92  ROD              SRF 116179        *
.*                              Use leave type on "R" record                 *
.*                   V5.01.005  07/08/92  ROD                                *
.*                              Changed to use day file                      *
.*                   V5.01.006  14/01/94  Sandra Barcham                     *
.*                              fix global recompile                         *
.*        V5.04.01  07/11/1996  Howellsy                                      *
.*                  Added Review Time & Legal Status Time.                    *
.*                                                                           *
.*****************************************************************************
.
          INC       STD001FD
.
          INC       IBASEQFD/INC
          INC       MEHVI1FD/INC
          INC       MEHDIAFD/INC
          INC       MEHDS1FD/INC
          INC       MEHLEGFD/INC
          INC       PATCODFD/INC
          INC       PATDAYFD/INC
          INC       PATDSCFD/INC
          INC       PATECDFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATTRNFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
CMDLINE   DIM       80
CMONDAY   DIM       4
CURRDATE  DIM       8
DAYFILE   DIM       8
DRDATE    DIM       10
DSADMD    DIM       10
DSDLEAV   DIM       10
DSDOB     DIM       10
FNAME     DIM       8
FNDTL     FORM      1
PTCNI10D  DIM       8
RDATE     DIM       8
SAVKEY30  DIM       30
SAVLTYP   DIM       3
SAVTDATE  DIM       8
ZED20     INIT      "zzzzzzzzzzzzzzzzzzzz"
.
. Temp file vars
MEHTMPA1  IFILE     SQL, FIXED=235, KEY="U1-8,9-10"
XXURNO    DIM       8         1       U/R Number
DXXUNIQ   DIM       2         9       Unique counter
XXNAME    DIM       36       11       Patients Name
XXDOB     DIM       8        47       Date of Birth
XXADMD    DIM       8        55       Admission date
XXDLEAV   DIM       8        63       Date on leave
XXDRETL   DIM       8        71       Date returned from trial leave
XXLSCOD   DIM       2        79       Legal Status code (HDP equiv)
XXREFC    DIM       2        81       Referred to code (HDP equiv)
XXDIA1    DIM       9        83       Diagnosis code 1
XXDIA2    DIM       9        92       Diagnosis code 2
XXDDAT    DIM       8       101       Discharge date
XXDTYP    DIM       4       109       Discharge type (HDP equiv)
XXLTYP    DIM       2       113       Type of leave (HDP equiv)
XXCDTH    DIM       9       115       Cause of Death
XXDDES1   DIM      30       124       Diagnosis desc 1
XXDDES2   DIM      30       154       Diagnosis desc 2
XXDISD    DIM      20       184       Discharge description
XXMOVE    DIM       1       204       Type of movement
XXCDTHD   DIM      30       205       Cause of Death description
.              Rec length   235
XXUNIQ    FORM      2
.
PRGID     INIT      "IBAMEH52"
PRGNAM    INIT      "HSS PRS985 - Leave/discharge Report"
VERSION   INIT      "V12.01.00"
.
.****************************************************************************
.* ML0000  :  Mainline code                                                 *
.****************************************************************************
ML0000    CALL      INIT0000                      * initialisation
ML0500    CALL      CRET0000                      * create temp file
.
ML1000    CALL      MENU0000                      * get menu options
          BRANCH    EXIT,ML9000                   * Exit selected
.
ML2000    CALL      DATE0000                      * Get Date
          BRANCH    EXIT,ML1000                   * nothing input
.
          CALL      CONTQST                       * Ask Continue?
          BRANCH    CEXIT,ML5000,ML2000,ML1000    * Yes, No, Cancel
.
ML5000    CALL      LOAD0000                      * Load data into temp file
.
          CALL      RPRT0000                      * Print Report
          GOTO      ML2000
.
ML9000    CALL      KILL0000                      * erase temp file
.
ML9999    CHAIN     PGM
+
.****************************************************************************
.* INIT0000  :  Initialisation                                              *
.****************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P60:24,"controlf"
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P60:24,"patdschf"
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P60:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P60:24,"mehdiaaf"
          OPEN      MEHDIAA1,"mehdiaaf"
.
          DISPLAY   *P60:24,"mehds1af"
          OPEN      MEHDS1A1,"mehds1af"
.
          DISPLAY   *P60:24,"mehlegaf"
          OPEN      MEHLEGA1,"mehlegaf"
.
          DISPLAY   *P60:24,"mehvi1af"
          OPEN      MEHVI1A1,"mehvi1af"
.
          DISPLAY   *P60:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P60:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P60:24,"pattranf"
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P60:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P60:24,"patecdaf"
          OPEN      PATECDA2,"patecdaf"
.
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
INIT9999  RETURN
+
.****************************************************************************
.* MENU0000  :  Main Menu                                                   *
.****************************************************************************
MENU0000  DISPLAY   *P1:4,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = By U/R Number"
.
MENU1000  KEYIN     *P1:7,"Select Option ? ":
                    *P17:7,*DV,UNDLN1:
                    *P17:7,*V2LON,OPTION
.
          IF        OPTION = 0
                      MOVE   ONE,EXIT
                      GOTO   MENU9999
          ENDIF
.
          MOVE      ZERO,EXIT
          BRANCH    OPTION,MENU9999
          BEEP
          GOTO      MENU1000
.
MENU9999  RETURN
+
.****************************************************************************
.* DATE0000  :  Get the Report date                                         *
.****************************************************************************
DATE0000  DISPLAY   *P1:24,*EL,*P1:9,"Enter the report date : "
.
DATE1000  MOVE      NINE,CVERT
          MOVE      "25",CCOL
          MOVE      CCC,CCENT
          UNPACK    SP10,CYEAR,CMON,CDAY
          MOVE      ONE,CDEFDTE
.
          CALL      KEYDATE
          BRANCH    CQUEST,DATE1000
          BRANCH    OVRCD,DATE9000
.
          PACK      RDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",RDATE
.
          MATCH     CURRDATE,RDATE
          GOTO      DATE8000 IF LESS
          GOTO      DATE8000 IF EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"Date must not be into future.  ";
          CALL      HITENTER
          GOTO      DATE1000
.
DATE8000  CALL      PACDATE
          MOVE      CPCDATE,DRDATE                 * display var
.
. now open the day file for the keyed in date
.
          PACK      CMONDAY,CMON,CDAY
          REP       " 0",CMONDAY
        
          PACK      DAYFILE,FILDAYA1,CCENT,CYEAR
          REP       " 0",DAYFILE
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATDAYA1,DAYFILE
          TRAPCLR   IO
          IF        OVRCD=1
            DISPLAY   *P1:24,*EL,*B,"Day file not set up.  ";
            CALL      HITENTER
            DISPLAY   *P1:24,*EL
            GOTO      DATE9000
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      DATE9999
.
DATE9000  MOVE      ONE,EXIT
.
DATE9999  RETURN
+
.****************************************************************************
.* RPRT0000  :  Print Report                                                *
.****************************************************************************
RPRT0000  DISPLAY   *P30:24,"Printing...";
.
          CALL      IBACLOCK                      * initialise a few things
          MOVE      ZERO,CPAGENO
          MOVE      ONE,CNOUNDLN
          MOVE      TEN,CLNO                      * line counter
.
          CALL      HEAD132                       * Print 3 line header
.
          PRINT     *40,"Report Date : ",DRDATE,*N
.
          CALL      PTHD0000                      * print table headings
.
. Now loop thru temp file and print data
.
          PACK      KEY10,SP10
          CALL      RSTEMP1
RPRT1000  CALL      RKTEMP1
          BRANCH    OVRCD,RPRT8000                * no more records
.
          COMPARE   "55",CLNO                     * need a new page?
          GOTO      RPRT2000 IF LESS
.
. set up for new page
.
          CALL      HEAD132
          PRINT     *40,"Report Date : ",DRDATE
          CALL      PTHD0000                      * print table headings
          MOVE      TEN,CLNO
.
RPRT2000  UNPACK    XXDOB,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DSDOB                  * display var (DOB)
.
          UNPACK    XXADMD,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DSADMD                 * display var (Adm Date)
.
          UNPACK    XXDLEAV,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DSDLEAV                * display var (Lve Date)
.
          MATCH     "R",XXMOVE                    * Return from Leave record?
          GOTO      RPRT4000 IF NOT EQUAL
.
          PRINT     *1,XXURNO,*11,XXNAME,*49,DSDOB,*60,DSADMD,*71,DSDLEAV: 
                    *83,XXLSCOD,*100,XXREFC,*114,"RETURNED FROM LEAVE"
          GOTO      RPRT5000
.
RPRT4000  PRINT     *1,XXURNO,*11,XXNAME,*49,DSDOB,*60,DSADMD,*71,DSDLEAV: 
                    *83,XXLSCOD,*100,XXREFC,*129,XXLTYP
.
RPRT5000  PRINT     *49,"Main Diagnosis : ",XXDDES1,*100,"I.C.D. Code : ":
                    XXDIA1,*N,*49,"Associated Diag: ",XXDDES2,*100:
                    "I.C.D. Code : ",XXDIA2
          ADD       TWO,CLNO
.
          MATCH     "R",XXMOVE                    * Return from leave record?
          IF        @EQUAL
            PRINT     *49,"RETURNED FROM LEAVE : ",DRDATE
            ADD       ONE,CLNO
          ENDIF
.
          MATCH     ANSD,XXMOVE                   * discharge?
          IF        @EQUAL
            UNPACK    XXDDAT,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            PRINT     *49,"Disch Type     : ",XXDTYP,SP2,XXDISD:
                      *116,"Date : ",CPCDATE
            ADD       ONE,CLNO
            MATCH     SP9,XXCDTH          * dead code blank?
            IF        !@EQUAL
              PRINT     *49,"Cause of Death : ",XXCDTH,*100:
                        "I.C.D. Code : ",XXCDTHD
              ADD       ONE,CLNO
            ENDIF
          ENDIF
.
          PRINT     *N
          GOTO      RPRT1000
.
. we have finished printing patient data so print end of report details
.
RPRT8000  PRINT     *N,"*** End of Report ***"
.
RPRT9999  RETURN
+
.***************************************************************************
.*  LOAD0000  :  Load data into temp file                                  *
.***************************************************************************
LOAD0000  DISPLAY   *P1:24,*EL,*P30:24,"Loading data... [        ]"
          MOVE      ONE,XXUNIQ                    * init unique counter
.
          PACK      KEY12,CMONDAY,SP20
          CALL      RSPTDAY1
LOAD1000  CALL      RKPTDAY1
          BRANCH    OVRCD,LOAD9000                * no more records
.
          MATCH     CMONDAY,PTDYDATE              * same date?
          GOTO      LOAD9000 IF NOT EQUAL
.
          DISPLAY   *P47:24,PTDYADMN;
.
          PACK      KEY8,PTDYADMN                 * MH patient?
          CALL      RDMHVIS1
          BRANCH    OVRCD,LOAD1000
.
          CALL      IDAT0000                      * init temp vars
.
. check Discharge/Leave status
.
          PACK      KEY30,MHVIADMN,RDATE,SP30
          CALL      RDSTRAN2
LOAD2000  CALL      RDKTRAN2
          BRANCH    OVRCD,LOAD1000
.
          MATCH     MHVIADMN,TADMN                * same admission number?
          GOTO      LOAD1000 IF NOT EQUAL
.
          MATCH     RDATE,TDATE                   * same date?
          GOTO      LOAD1000 IF NOT EQUAL
.
          MATCH     ANSA,TMOVE                    * Admission? Ignore
          GOTO      LOAD2000 IF EQUAL
          MATCH     ANSC,TMOVE                    * Change record? Ignore
          GOTO      LOAD2000 IF EQUAL
.
. Make sure Leave is 'Trial Leave'
.
          MATCH     ANSL,TMOVE                    * Leave?
          IF        @EQUAL
. use leave type on "R" record if it exists
            MOVE      PTTRLTYP,SAVLTYP            * use this if no "R" record
            PACK      SAVKEY30,TADMN,TDATE,TTIME,TWARD,TBED
            CALL      RDKTRAN2                    * get "R" record if exists
            BRANCH    OVRCD,LOAD2100
.
            MATCH     MHVIADMN,TADMN              * same admission number?
            GOTO      LOAD2100 IF NOT EQUAL
.
            MOVE      SAVLTYP,PTTRLTYP            * use leave from "R" record
.
LOAD2100    MOVE      SAVKEY30,KEY30              * restore record
            CALL      RDTRAN2
.
            MOVE      SAVLTYP,PTTRLTYP
            PACK      KEY5,ANST,ANSL,PTTRLTYP     * check if Trial leave
            CALL      RDCODE1
            BRANCH    OVRCD,LOAD2000
            MATCH     ANST,TCINDC1                * Trial Leave?
            GOTO      LOAD2000 IF NOT EQUAL
          ENDIF   {leave}
.
          MATCH     ANSR,TMOVE                    * Return from Leave?
          IF        @EQUAL
            MOVE      ONE,FNDTL                   * found Trial lve pat flag
            PACK      SAVKEY30,TADMN,TDATE,TTIME,TWARD,TBED
            CALL      RDPTRAN2
            BRANCH    OVRCD,LOAD2500
.
            MATCH     MHVIADMN,TADMN             * same admission number?
            GOTO      LOAD2500 IF NOT EQUAL
.
            MATCH     ANSL,TMOVE
            GOTO      LOAD2490 IF EQUAL
          ELSE
            GOTO      LOAD2800                   * Discharge/Leave
          ENDIF   {return}
.
LOAD2490  MOVE      TDATE,XXDLEAV                 * date of leave
          MOVE      ZERO,FNDTL                    * found Trial lve pat flag
LOAD2500  MOVE      SAVKEY30,KEY30                * restore key
          CALL      RDTRAN2
          BRANCH    FNDTL,LOAD2000                * Trial leave? (No)
.
. check if patient went on leave & returned on the same day. If so ignore.
.
LOAD2800  MATCH     ANSL,TMOVE                    * On leave?
          GOTO      LOAD5000 IF NOT EQUAL
.
          PACK      SAVKEY30,TADMN,TDATE,TTIME,TWARD,TBED
          CALL      RDKTRAN2                      * check next record (TMOVE=R)
          BRANCH    OVRCD,LOAD3000
.
          MATCH     MHVIADMN,TADMN                * same admission number?
          GOTO      LOAD3000 IF NOT EQUAL
.
          MATCH     RDATE,TDATE                   * same date?
          GOTO      LOAD3000 IF NOT EQUAL
.
          MATCH     ANSR,TMOVE                    * Return from leave?
          GOTO      LOAD1000 IF EQUAL             * Ignore
          GOTO      LOAD3000
.
. re-read record
.
LOAD3000  MOVE      SAVKEY30,KEY30                * restore key
          CALL      RDTRAN2
.
. now set up details for report
.
LOAD5000  MOVE      TMOVE,XXMOVE                  * Movement type
.
          PACK      KEY8,MHVIADMN
          CALL      RDMISS1
          BRANCH    OVRCD,LOAD1000                * ignore if no admission rec
.
          PACK      KEY8,AURNO
          CALL      RDMAST1
          BRANCH    OVRCD,LOAD1000                * ignore if no master rec
.
          MOVE      PURNO,XXURNO
          STRIP     PSNAME
          PACK      XXNAME,PSNAME,COMMA,SP1,PGNAME
          MOVE      PBDATE,XXDOB
.
          CALL      LADM0000                      * get last admission date
.
          MATCH     ANSD,TMOVE                    * Type Discharge?
          IF        @EQUAL
            MOVE      SP6,XXDLEAV
            MOVE      SP8,DDATE
            MOVE      SP3,DSTAT
            PACK      KEY8,MHVIADMN
            CALL      RDDSCH1
            MOVE      DDATE,XXDDAT                * discharge date
.
            MOVE      SP4,THCSCOD
            PACK      KEY5,ANSD,SP1,DSTAT
            CALL      RDCODE1
            MOVE      THCSCOD,XXDTYP              * discharge type (HDP equiv)
            MOVE      TDESC,XXDISD                * discharge description
            CALL      DEAD0000                    * check if patient is dead
          ELSE
            CALL      LTYP0000                    * get type of leave
          ENDIF
.
          CALL      GDIA0000                      * get diagnosis
.
          MOVE      SP6,XXDRETL
          MATCH     ANSL,TMOVE                    * Type Leave?
          IF        @EQUAL
            MOVE      TDATE,XXDLEAV         * leave date
          ENDIF
.
          MATCH     ANSR,TMOVE                    * Type Return from Leave?
          IF        @EQUAL
            MOVE      TDATE,XXDRETL         * return from leave date
          ENDIF
.
. Get Legal status (HDP equivalent) at current date
.
          MOVE      SP4,THCSCOD
          PACK      KEY21,MHVIADMN,ZED20
          CALL      RSMHLEG1
LOAD6000  CALL      RPMHLEG1
          BRANCH    OVRCD,LOAD6500
.
          MATCH     MHVIADMN,MHLEADMN             * same admission no.?
          GOTO      LOAD6500 IF NOT EQUAL
.
          MATCH     MHLEDATE,RDATE                * <= report date?
          GOTO      LOAD6000 IF LESS
.
          PACK      KEY5,ANSL,ANSS,MHLESTAT
          CALL      RDCODE1
LOAD6500  MOVE      THCSCOD,XXLSCOD               * this is a comment
.
. Get Referred to code (HDP equivalent)
.
          MOVE      SP2,XXREFC
          PACK      KEY8,MHVIADMN
          CALL      RDMHDSC1
          IF        OVRCD=0
            MOVE      SP4,THCSCOD
            PACK      KEY5,ANSR,ANSE,MHDSREFT
            CALL      RDCODE1
            MOVE      THCSCOD,XXREFC                * this is a comment
          ENDIF
.
. now post details to temp file
.
          DISPLAY   *P47:24,*V2LON,MHVIADMN;
.
          PACK      KEY10,XXURNO,XXUNIQ
          CALL      WRTEMP1
          ADD       ONE,XXUNIQ
          GOTO      LOAD2000
.
LOAD9000  DISPLAY   *P1:24,*EL
.
LOAD9999  RETURN
+
.**************************************************************************
.*  DEAD0000  :  Check if Patient is Dead                                 *
.**************************************************************************
DEAD0000  MATCH     ANSD,TCINDC1                  * 1st indicator 'D' (dead)?
          GOTO      DEAD9000 IF NOT EQUAL
.
. patient is dead so get cause of death
.
          PACK      KEY16,MHVIADMN,ZED20
          CALL      RSPTECD2
DEAD1000  CALL      RPPTECD2
          BRANCH    OVRCD,DEAD9000
.
          MATCH     MHVIADMN,PTEDADMN             * same admission no.?
          GOTO      DEAD9000 IF NOT EQUAL
.
          MATCH     ANSD,PTEDTYPE                 * Associated code?
          GOTO      DEAD9000 IF NOT EQUAL
.         
          MOVE      PTEDCODE,XXCDTH               * death code
          MOVE      PTEDDESC,XXCDTHD              * death desc
          GOTO      DEAD9999
.
DEAD9000  MOVE      SP9,XXCDTH
          MOVE      SP30,XXCDTHD
.
DEAD9999  RETURN
+
.**************************************************************************
.*  PTHD0000  :  Print Table Heading                                      *
.**************************************************************************
PTHD0000  PRINT     *1," U/R No.  Patient Name",*50,"D.O.B.",*60,"Adm Date":
                    *71,"Leave",*83,"Legal Status",*100,"Referred to":
                    *128,"Leave",*N,*129,"Type"
.
PTHD9999  RETURN
+
.*************************************************************************
.*  LTYP0000  :  Get Type of Leave                                       *
.*************************************************************************
LTYP0000  MOVE      SP2,XXLTYP
          MATCH     ANSD,TMOVE
          GOTO      LTYP9999 IF EQUAL
.
LTYP1000  MOVE      SP4,THCSCOD
          PACK      KEY5,ANST,ANSL,PTTRLTYP
          CALL      RDCODE1
          MOVE      THCSCOD,XXLTYP
.
LTYP9999  RETURN
+
.*************************************************************************
.*  GDIA0000  :  Get Diagnosis                                           *
.*************************************************************************
GDIA0000  MOVE      SP9,XXDIA1
          MOVE      SP9,XXDIA2
          MOVE      SP30,XXDDES1
          MOVE      SP30,XXDDES2
.
          MATCH     ANSD,TMOVE                    * discharged?
          GOTO      GDIA4000 IF NOT EQUAL
.
          MATCH     SP8,ACODDTE                   * coding been done?
          GOTO      GDIA4000 IF EQUAL             * No.
.
. get Diagnosis from Medical Records
.
          PACK      KEY16,MHVIADMN,ZED20
          CALL      RSPTECD2
GDIA1000  CALL      RPPTECD2
          BRANCH    OVRCD,GDIA4000
.
          MATCH     MHVIADMN,PTEDADMN             * same admission no.?
          GOTO      GDIA4000 IF NOT EQUAL
.
          MATCH     ANSA,PTEDTYPE                 * Primary Disease
          GOTO      GDIA1000 IF NOT EQUAL
.
          MOVE      PTEDCODE,XXDIA1               * diagnosis code 1
          MOVE      PTEDDESC,XXDDES1              * diagnosis 1 desc
.
          CALL      RKPTECD2                      * get secondary diagnosis
          BRANCH    OVRCD,GDIA9999
.
          MATCH     MHVIADMN,PTEDADMN             * same admission no.?
          GOTO      GDIA9999 IF NOT EQUAL
.
          MOVE      PTEDCODE,XXDIA2               * diagnosis code 2
          MOVE      PTEDDESC,XXDDES2              * diagnosis 2 desc
          GOTO      GDIA9999
.
. get Diagnosis from Mental Health
.
GDIA4000  PACK      KEY16,MHVIADMN,ZED20
          CALL      RSMHDIA1
          CALL      RPMHDIA1
          BRANCH    OVRCD,GDIA9999
.
          MATCH     MHVIADMN,MHDIADMN             * same admission number?
          GOTO      GDIA9999 IF NOT EQUAL
.
          MOVE      MHDIICD1,XXDIA1
          MOVE      MHDIDES1,XXDDES1
          MOVE      MHDIICD2,XXDIA2
          MOVE      MHDIDES2,XXDDES2
.
GDIA9999  RETURN
+
.*************************************************************************
.*  LADM0000  :  Get last admission (Return from leave or Admission)     *
.*************************************************************************
LADM0000  PACK      SAVKEY30,TADMN,TDATE,TTIME,TWARD,TBED    * save key
.
          MOVE      ADATE,XXADMD                  * admission date
          GOTO      LADM9999
.
.------------------------------------------------------------------------------
.
.          PACK      KEY30,TADMN,ZED20,ZED20
.          CALL      RDSTRAN2
.LADM1000  CALL      RDPTRAN2
.          BRANCH    OVRCD,LADM7000
..
.          MATCH     MHVIADMN,TADMN                * same admission number?
.          GOTO      LADM7000 IF NOT EQUAL
..
.          MATCH     ANSR,TMOVE                    * Return from Leave?
.          GOTO      LADM1000 IF NOT EQUAL
..
.          MOVE      TDATE,SAVTDATE                * save date
..
.. we have a return record so search back for a Leave record with ind1 = "T"
..
.LADM2000  CALL      RDPTRAN2
.          BRANCH    OVRCD,LADM7000
..
.          MATCH     MHVIADMN,TADMN                * same admission number?
.          GOTO      LADM7000 IF NOT EQUAL
..
.          MATCH     ANSL,TMOVE                    * Return from Leave?
.          GOTO      LADM2000 IF NOT EQUAL
.
.          PACK      KEY5,ANST,ANSL,PTTRLTYP
.          CALL      RDCODE1
.          BRANCH    OVRCD,LADM1000
..
.          MATCH     ANST,TCINDC1                  * Trial Leave?
.          GOTO      LADM1000 IF NOT EQUAL
..
.          MOVE      SAVTDATE,XXADMD               * new admission date
..
.LADM7000  MOVE      SAVKEY30,KEY30                * restore key
.          CALL      RDTRAN2
.
LADM9999  RETURN
+
.*************************************************************************
.*  IDAT0000  :  initialise vars                                         *
.*************************************************************************
IDAT0000  UNPACK    SP70,XXURNO,XXNAME,XXDOB,XXADMD,XXDLEAV,XXDRETL
          UNPACK    SP70,XXLSCOD,XXREFC,XXDIA1,XXDIA2,XXDDAT,XXDTYP:
                         XXLTYP,XXCDTH
          UNPACK    SP70,XXDDES1,XXDDES2
          UNPACK    SP70,XXDISD,XXCDTHD
.
IDAT9999  RETURN
+
.*************************************************************************
.*  KILL0000  :  erase temp file                                         *
.*************************************************************************
KILL0000  CLOSE     MEHTMPA1
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
KILL9999  RETURN
+
.*************************************************************************
.*                             CRET0000            Called by:            *
.*                      create temporary file                            *
.*************************************************************************
CRET0000  CALL      TFILENAM                     * get new tempfile name
          MOVE      TFILNAME,FNAME
.
          CALL      KILL0000 
          CLEAR     CMDLINE 
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME,CMDLINE
          APPEND    " 235 U1-8,9-10",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      MEHTMPA1,FNAME
.
CRET9999  RETURN
+
.***************************************************************************
.*  Temp file I/O's                                                        *
.***************************************************************************
RATEMP1   MOVE      ZERO,OVRCD
          RESET     KEY10
          READ      MEHTMPA1,KEY10;ANS
          GOTO      OVERCOND IF OVER
          RETURN            
.
RSTEMP1   RESET     KEY10
          READ      MEHTMPA1,KEY10;;
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    MEHTMPA1;XXURNO,DXXUNIQ,XXNAME,XXDOB,XXADMD,XXDLEAV,XXDRETL:
                     XXLSCOD,XXREFC,XXDIA1,XXDIA2,XXDDAT,XXDTYP,XXLTYP,XXCDTH:
                             XXDDES1,XXDDES2,XXDISD,XXMOVE,XXCDTHD
          GOTO      OVERCOND IF OVER
          MOVE      DXXUNIQ,XXUNIQ
          RETURN
.
WRTEMP1   RESET     KEY10
          MOVE      XXUNIQ,DXXUNIQ
          WRITE     MEHTMPA1,KEY10;XXURNO,DXXUNIQ,XXNAME,XXDOB,XXADMD,XXDLEAV:
               XXDRETL,XXLSCOD,XXREFC,XXDIA1,XXDIA2,XXDDAT,XXDTYP,XXLTYP,XXCDTH:
                       XXDDES1,XXDDES2,XXDISD,XXMOVE,XXCDTHD
          RETURN
.
.
          INC       STD001IO
.
          INC       TFILENAM
.
          INC       IBASEQIO/INC
          INC       MEHVI1IO/INC
          INC       MEHDIAIO/INC
          INC       MEHDS1IO/INC
          INC       MEHLEGIO/INC
          INC       PATCODIO/INC
          INC       PATDAYIO/INC
          INC       PATDSCIO/INC
          INC       PATECDIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATTRNIO/INC
          INC       PMSVX1IO/INC
          INC       WEBERRIO/INC

.******************************************************************************
.*               P R O G R A M  S U M M A R Y                                 *
.*               -------------  -------------                                 *
.*                                                                            *
.*      PROGRAM NAME:     IBAHOS51                                            *
.*                                                                            *
.*      FUNCTION:         IN-PATIENT CENSUS REPORT                            *
.*                                                                            *
.*      DATE WRITTEN:     27/05/88                                            *
.*                                                                            *
.*      AUTHOR:           JENI TAN      (I.B.A)                               *
.*                                                                            *
.*      MODIFICATIONS:                                                        *
.*        V12.00.01 16/05/2025  J.Tan          TSK 0955096                    *
.*                  Added alpanumeric visit number generation                 *
.*        V11.02.03 04/05/2022  Jacob Jackson   TSK 0864505                   *
.*                  Replace BRANCH with PERFORM so code doesn't end early     *
.*        V11.02.02 10/02/2022  Jacob Jackson   TSK 0864505                   *
.*                  Rename 'PRNAME' to 'PRFRNAME' due to conflict             *
.*        V11.02.01 20/01/2022  Jacob Jackson TSK 0864505                     *
.*                  Added optional preferred name(s) to name column           *
.*        V10.13.01 05/12/2018  Don Nguyen    TSK 0838558                     *
.*                  Deleted temp file (PSTROL) after processing               *
.*        V10.10.01 20/04/2017 Ania P         CAR 260631                      *
.*                  Remove use of PORT                                        *
.*        V10.02.01 31/05/2011 Peter McMullen CAR 240725                      *
.*                  Show ward short name instead of code                      *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V9.04.01  23/08/2004  Lina Vo CAR 52901                             *
.*                  Added Multi-Hospital processing                           *
.******************************************************************************
.*        V9.03.01  20/02/2004  Henry Son        CAR 47118                    *
.*                  Fix print alignment.                                      *
.******************************************************************************
.*        V5.08.01  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.07.01  19/02/1999  Jim Stathopoulos                              *
.*                  Report Modifications                                      *
.*        V5.07.B02 28/01/1999  Nicole Harrington                             *
.*                  507 rebounds                                              *
.*        V5.07.B01 12/11/1998  Davin                                         *
.*                  Mods for 507                                              *
.******************************************************************************
.*        V5.04.01  02/05/1997  Steve Armstrong   SRF 642857                  *
.*                  Changed temp file name "pstidx" to standard format        *
.******************************************************************************
. *                       W2.01 15/07/88 G.Williams (I.B.A)*
. *                             Added new file - PATNOBFD  *
. *                       V4.01 25/11/88 Graeme Williams   *
. *                             Include Standby Patients   *
. *                       V5.00 18/05/89 DIG               *
. *                             Changed to standard and    *
. *                             modified print for 8 digit *
. *                             U/R's etc                  *
. *                       V5.01 27/05/89 J.Tan             *
. *                             Fixed Mother Adm. to form8 *
. *                             (PATMI1FD).     SRF 100750 *
. *                       V5.02 06/06/89 J.Tan             *
. *                             Mods Locking master file   *
. *                             (PATIOMAS)     SRF 100809  *
. *                       V5.03 17/06/89 J.Tan             *
. *                             Removed Standby file       *
. *                             SRF 101101                 *
. *                    V5.01.01 05/07/89 K.Peak            *
. *                             Changed Adm & U/R to 8 chrs*
. *                   V5.01.121 27/11/90 Glen Hobby        *
. *                            Recompiled for changes to   *
. *                            PATMX1FD                    *
. *                                                        *
. **********************************************************
.
          INC       STD001FD
+
          INC       IBACONFD/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATWR1FD/INC
          INC       PATNOBFD/INC
          INC       PATDO1FD/INC
          INC       PMSPX2FD/INC
          INC       IBASEQFD/INC 
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
+
.               WORK AREA
.
DALPHA   DIM     6
DNAME    DIM     24 
.
CODE     DIM     2
CODEA    INIT    "A "
.
.
PSTROL   FILE      ASCII, FIXED=256
.
TEMPFILE IFILE     SQL, FIXED=115, KEY="u1-28"
.
. -----  Temporary file layout -----
.
.NAME    TYPE     LENGTH  PHYSICAL   START     DESCRIPTION
.----    ----     ------  --------   -----     -----------
.PSNAME  DIM        20      20         1       
DUNIQUE  DIM         8       8        21
.PURNO   DIM         8       8        29
.WARDSNAM DIM       10      10        37
.WBED    DIM         3       3        47
.DNAME   DIM        24      24        50
.PGNAME  DIM        25      25        74
.PTITL   DIM         4       4        99
.ADATE   DIM         8       8       103
.ATYPE   DIM         3       3       112
.                           ---- EOR 115 -----
.
.
FNAMET   DIM       8
FNAMER   DIM       8
.
CMDLINE  DIM       50
DIM18    DIM       18
UNIQUE   FORM      8
.
FIVE5    FORM    "55"
.
NAME     DIM     22         * MFNAME.MSNAME
DMSTAT   DIM     9
WARDSNAM DIM     10
.
DSEX     DIM    6
.
NAME23   DIM     20         * Pat Name 20 chars
PRENAOPD FORM    1
.
PRGID     INIT      "IBAHOS51"
PRGNAM    INIT      "In-patient Census Report"
VERSION   INIT      "V12.01.00"
+
.         START OF PROGRAM
.
          CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY   *P64:24,"patnobef";
          OPEN      PATNOBE1,"patnobef"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,HUND28;*226,PTCNNMPR
.
          MOVE      PTCNNMPR,PRENAOPT
          IF        PRENAOPT > 0
            DISPLAY   *P64:24,"pmspx2af"
            OPEN      PMSPX2A1,"pmspx2af"
          ENDIF
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMER
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMET
+
.        START OF PROGRAM
.
START   MOVE       ZERO,CPAGENO
.
OPT2
        DISPLAY   *P1:4,*EF:
                  *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                  *P1:5,*V2LON,ONE,*HOFF," = Patient Surname Sequence";
.
        KEYIN     *P1:7,"Option : ",*EL,*V2LON,OPTION
.
        COMPARE   ZERO TO OPTION
        GOTO      ENDIT IF EQUAL
.
BRNCH   BRANCH    OPTION OF SURNAM
        BEEP
        GOTO      OPT2
+
.
.           ACCESS VIA PATIENT SURNAME
.
SURNAM    CALL       KHOS0000
          BRANCH     EXIT,START
          CALL       KILLIDX
          CALL       CREAIDX
.
          OPEN       TEMPFILE,FNAMET
          MOVE       ONE TO UNIQUE
.
          MOVE     "60",CLNO
          DISPLAY  *P1:24,*EL,*P20:24,"Ward :",*P40:24,"Bed :"
.
          MOVE     SP6,KEY6
          CALL     RDSWARD1
NEXTA     CALL     RDKWARD1
          BRANCH   OVRCD OF SORTFI
.
          COMPARE   ONE,IBCNMHOS
          GOTO      NEXTA1 IF NOT EQUAL
.
          MATCH     SP10,PTHSHOSP
          GOTO      NEXTA1 IF EQUAL
.
          MATCH     PTWDHOSN,PTHSHOSP
          GOTO      NEXTA IF NOT EQUAL
.
NEXTA1    MATCH    SP3,WBED
          GOTO     CHKNOBE IF EQUAL
.
          MATCH    ZEROVISN,WADMNO
          GOTO     CHKSTBY IF EQUAL
.
          CALL     GETDATA
.
.       Check if there is any standby patients here
.
CHKSTBY MATCH    ZEROVISN,WSTBY
        GOTO     NEXTA IF EQUAL
.
        MOVE     WSTBY,WADMNO
        CALL     GETDATA
        GOTO     NEXTA
.
.
.       New ward - check if this is a ward-only ward
.
CHKNOBE MOVE     PTWDSDES,WARDSNAM  * keep short name of ward
        COMPARE  ONE,WINPUT
        GOTO     NEXTA IF NOT EQUAL
.
.
        PACK     KEY13 WITH WWARD,SP10
        CALL     RDSNOBE1
NOBLP   CALL     RDKNOBE1
        BRANCH   OVRCD OF NEXTA
.
        MATCH    WWARD,NBWARD
        GOTO     NEXTA IF NOT EQUAL
.
        MOVE     NBADMNO,WADMNO
        CALL     GETDATA
        GOTO     NOBLP
.
.       Subroutine to process an admission
.
GETDATA MOVE     WADMNO,KEY8
        CALL     RDMISS1
        BRANCH   OVRCD OF LOSTDAT1
.
        MOVE     AURNO,KEY8
        CALL     RDMAST1
.
        MOVE     SP30,DNAME
        MOVE     ADOCTA,KEY6
        CALL     RDDOCT1
.
        MOVE     DGNAME,ANS
        PACK     DNAME WITH ANS,DOT,DSNAME
        MOVE     DSNAME,DALPHA
.
SRTNAME MOVE     UNIQUE,DUNIQUE
        PACK     KEY28 WITH PSNAME,DUNIQUE
        WRITE    TEMPFILE,KEY28;PSNAME,DUNIQUE,PURNO,WARDSNAM,WBED:
                                DNAME,PGNAME,PTITL,ADATE,ATYPE
.
        ADD      ONE,UNIQUE
        DISPLAY  *P27:24,*V2LON,WWARD,*P46:24,WBED
        RETURN
.
NOOPEN  DISPLAY   *P1:24,*B,*EL,"** TEMPORARY FILE ":
                                 FNAMET," NOT FOUND **",*W2;
        RETURN
.
SORTFI  DISPLAY   *P1:24,*EL,*P10:24,"** Generating Report **",*W;
.
        CLOSE     TEMPFILE
        TRAP      NOOPEN IF IO
        OPEN      TEMPFILE,FNAMET
        TRAPCLR   IO
.
        DISPLAY   *P1:12,*EF
.
        CLOCK     TIME,CTIMEIS
        DISPLAY   *P1:24,*EL,*P20:24,"Patient Name :";
        MOVE      SP30 TO KEY28
        READ      TEMPFILE,KEY28;;
.
READLOP READKS    TEMPFILE;PSNAME,DUNIQUE,PURNO,WARDSNAM,WBED:
                                DNAME,PGNAME,PTITL,ADATE,ATYPE
        GOTO      ENDP IF OVER
        MOVE      DUNIQUE,UNIQUE
.
        DISPLAY   *P40:24,*EL,*V2LON,PSNAME
.
        CALL      DISPDA
        GOTO      READLOP
.
NOWARD  DISPLAY   *P20:24,*EL,*B,*V2LON:
                  "** No Available Data on File **",*W2;
        GOTO      START
.
INVWARD DISPLAY   *P20:24,*EL,*B,*V2LON:
                  "** Ward does not Exist **",*W2;
        GOTO      START
.
.
.           OVER CONDITION FOUND..PRINT LAST LINE OF -- AND DISPLAY MESSAGE
.
ENDP    COMPARE   ZERO,CPAGENO
        GOTO      NOWARD IF EQUAL
        CALL      PAGEND
        PRINT     *N,"  ***  END OF REPORT  ***"
        DISPLAY   *P1:24,*EL,*P40:24,*V2LON:
                  "** Report Generation Completed **",*W2;
         CALL     KILLIDX
         GOTO     START
.
LOSTDAT1 DISPLAY  *P1:23,*EL,*V2LON:
                  "*** Ward/Bed ",*V2LON,WWARD,"/",WBED,*HOFF:
                  " has invalid Admission ",*V2LON,WADMNO,*HOFF," ***"
.
         COMPARE   FIVE5,CLNO
         CALL      HEAD IF NOT LESS
.
         PRINT    *N,"*** Ward/Bed ",WARDSNAM,"/",WBED:
                  " has invalid Admission ",WADMNO," ***":
                  " Contact I.B.A Immediately  ***",*N 
         ADD       THREE,CLNO
         RETURN
.
+
.
OPMSFI    IF        PRENAOPT > 0
            MOVE      PURNO,KEY8
            CALL      RDPMPX21
          ENDIF
          RETURN
.
.     PRINT ACTUAL DATA VIA SURNAME 
.
DISPDA    COMPARE   FIVE5,CLNO
          CALL      HEAD IF NOT LESS
.
          UNPACK    ADATE,XCENT,XYEAR,XMON,XDAY
.
          MOVE      SP20,TDESC
          MOVE      SP20,DIM18
          MATCH     SP3,ATYPE
.                                      * truncate name to 23 chars     *I-47118
          PACK      NAME23,PGNAME,SP70                                 *I-47118
.
          GOTO      DISP10 IF EQUAL
.
          PACK      KEY5 WITH CODEA,ATYPE
          CALL      RDCODE1
          BRANCH    OVRCD OF DISP10
.
          MOVE      TDESC,DIM18
DISP10    CALL      OPMSFI
          PRINT     *1,"!",PSNAME,PTITL," ",NAME23:                    *C-47118
                    *47,"! ",WARDSNAM,*60,"! ",WBED:
                    *66,"! ",XDAY,SLASH,XMON,SLASH,XCENT,XYEAR:
                    *79,"!",DNAME,*104,"!",PURNO,*113,"!",DIM18,*132,"!"
.
          IF        PRENAOPT > 0
            CLEAR     PRNAMES
            CLEAR     PRFRNAME
            MOVE      PMPXPFGN,PREGNAME
            MOVE      PMPXPFSN,PRESNAME
            PERFORM   PRENAOPT,DISPPNMG,DISPPNMS,DISPPNMB
          ENDIF
          ADD       ONE,CLNO
          RETURN
.
+
.
DISPPNMG  MATCH     SP70,PMPXPFGN
          IF        !@EQUAL
            CALL      PRNM0000
            CALL      DISPPNM
          ENDIF
          RETURN
.
DISPPNMS  MATCH     SP70,PMPXPFSN
          IF        !@EQUAL
            CALL      PRNM0000
            CALL      DISPPNM
          ENDIF
          RETURN
.
DISPPNMB  MOVE      PRENAOPT,PRENAOPD
          MATCH     SP70,PMPXPFGN
          IF        !@EQUAL
            MATCH     SP70,PMPXPFSN
            IF        @EQUAL
              MOVE      1,PRENAOPT
            ENDIF
            CALL      PRNM0000
            CALL      DISPPNM
          ELSE
            MATCH     SP70,PMPXPFSN
            IF        !@EQUAL
              MOVE      2,PRENAOPT
              CALL      PRNM0000
              CALL      DISPPNM
            ENDIF
          ENDIF
          MOVE      PRENAOPD,PRENAOPT
          RETURN
.
DISPPNM   PRINT       *1,"!",PRFRNAME,*47,"! ",*60,"! ",*66,"! ":
                      *79,"!",*104,"!",*113,"!",*132,"!"
          RETURN
.
.          LAST LINE ON THE CODE
.
PAGEND    PRINT     *1,"*-----------------------------------------":
                       "-------------------------------------------":
                       "----------------------------------------------*"
          ADD       ONE,CLNO
          RETURN
+
.
.
.                 HEADINGS FOR OUTPUT
.
HEAD      COMPARE   ZERO,CPAGENO
          CALL      PAGEND IF NOT EQUAL
.
          MOVE      FOUR,CLNO
.
          MOVE      ONE,CNOUNDLN
          MOVE      "(SURNAME SEQUENCE)",CPHDROPT
          CALL      IBACLOCK
          CALL      HEAD132
.
          IF        IBCNMHOS =1
            PRINT     *40,"Hospital : ",PTHSNAME
            ADD       ONE,CLNO
          ENDIF
.
          CALL     PAGEND
.
         PRINT     *1,"! Patients Name":
                   *47,"!Ward",*60,"! Bed",*66,"!  Adm.Date":
                   *79,"!   Doctor Name",*104,"! U/R No":
                   *113,"! Classification",*132,"!"
         ADD       ONE,CLNO
         CALL      PAGEND
         RETURN
.
+
.******************************************************************************
.*                                  KHOS0000              Called by: ML0000   *
.*                            Enter The hospital                              *
.******************************************************************************
KHOS0000  COMPARE   ONE,IBCNMHOS
          GOTO      KHOS9999 IF NOT EQUAL
.
          DISPLAY   *P1:9,*EL,"Hospital Id : ";
          MOVE      TEN5,ECOL
          MOVE      NINE,EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS2000,KHOS9500
          GOTO      KHOS3000
.
KHOS2000  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          MOVE      ZERO,EXIT
.
KHOS3000  CLOSE     PATHSPA1
.
          DISPLAY   *P20:9,*V2LON,PTHSNAME;
          GOTO      KHOS9999
.
KHOS9500  MOVE      ONE,EXIT
KHOS9999  RETURN
+
.         Deleting  AN INDEXED FILE BASED ON PORT
.
KILLIDX   CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMET,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          RETURN
.
CREAIDX   PREP      PSTROL,FNAMER
          WRITE     PSTROL,SEQ;"isbuild ",FNAMET," 115 u1-28"
          WEOF      PSTROL,SEQ
.
          DISPLAY   *P1:13,*EF,*P1:14
.
          CLEAR     CMDLINE
          APPEND    "sh ",CMDLINE
          APPEND    FNAMER,CMDLINE
          APPEND    ".txt",CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
          CLOSE     PSTROL,DELETE
.
          DISPLAY   *P1:13,*EF,*P1:14
          RETURN  
+
          INC       STD001IO
          INC       PATHSPKY
          INC       PATCODIO/INC
          INC       PATHSPIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATDO1IO/INC
          INC       PATWR1IO/INC
          INC       PATNOBIO/INC
          INC       PMSPX2IO/INC
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC
.
+
.
ENDIT    CHAIN      PGM
         STOP

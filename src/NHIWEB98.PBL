.------------------------------------------------------------------------------
. Program       : NHIWEB98
.
. Function      : Patient Enquiries Template Server
.
. Modifications :
.------------------------------------------------------------------------------
. V12.00.01 22/05/2025  Alina Bhari   TSK 0955096
.           Added alpanumeric visit number generation 
.           -GETPAT45,GETLTR00,ICDTYP00,ICDTYP50
. V11.04.01 28/10/2024  Ebon Clements  TSK 0948906
.           Recompiled for NSHISWIO - GETMWS AE2034 Unrecoverable read error
. V11.02.01 10/02/2022  Thanh T          TSK 0905641 
.           Recompiled as OUTHEDFD/OUTMA1FD/WATTR1FD changes  
. V11.01.01 22/02/2021  J.Tan          TSK 0888639
.           Changed patmmbs counter record to DIM3
. V11.00.02 09/12/2020  Thanh T        TSK 0872840
.           Added PMSCCDFD/PMSCCDIO as PMSPX2TM changes
. V11.00.01 10/03/2020  J.Tan          TSK 0838262
.           Added Effective from and to date to MBS Item file
. V10.15.03 20/02/2020  Peter Vela      TSK 0887845
.           Recompiled for NZHISWIO
. V10.15.02 25/11/2019  Ebon Clements   TSK 0860142
.           Recompiled for NSHISWIO - GETMWS AE2034 Unrecoverable read error
. V10.15.01 23/10/2019  Ebon Clements   TSK 0308709
.           Recompiled for OUTBOATM - Clinic type linked visit types
.------------------------------------------------------------------------------
. V10.14.03 30/05/2019  Thanh T.        TSK 0872539
.           Recompiled for NZHISWIO changes
. V10.14.02 21/03/2019  Tracey Nguyen   TSK 0838668
.           Added CHARTREV for OUTBOATM
. V10.14.01 12/03/2019  Tracey Nguyen   TSK 0863060
.           Recompiled for PATVISTM - Added VISS6400 & VISS6500
.------------------------------------------------------------------------------
. V10.12.01  18/05/2018  Thanh T.      TSK 0851528
.            Recompiled for PATVISTM changed
.------------------------------------------------------------------------------
. V10.11.07 27/11/2017  Thanh Tieu     TSK 0821710
.           Recompiled as PATMASTM changed
. V10.11.06 11/09/2017  Thanh T.       TSK 0821710
.           Recompiled for OUTBOATM
. V10.11.05 08/08/2017  Thanh T.       TSK 0821710
.           Audit Amission/outpatient visit comments.
. V10.11.04 08/08/2017  Thanh T.       TSK 0821710
.           Recompiled OUTBOATM.
. V10.11.03 30/07/2017  Thanh T.       TSK 0821710
.           Audit admission/outpatient/UR comments. changed PATMASTM/PMSPX2TM.
. V10.11.02 26/07/2017  Thanh T.       TSK 0821710
.           Recompiled PASMISTM
. V10.11.01 14/07/2017  Thanh T.       TSK 0821710
.           Audit Amission/outpatient visit comments. PATMISTM changed.
.------------------------------------------------------------------------------
. V10.07.01 19/10/2015  Don Nguyen     CAR 316625
.           Added ALLCASFD/IO and opened file
.----------------------------------------------------------------------
. V10.06.05 26/08/2015  Jill Parkinson CAR 318680
.           Recompiled for changes to NZHISWIO
. V10.06.04 25/08/2015  Jill Parkinson CAR 320905
.           Recompiled for changes to NZHISWIO
. V10.06.03 13/08/2015  Don Nguyen     CAR 308621 
.           Recompiled for PATVISTM - Added VISS6100
.           11/08/2015  Peter Vela     CAR 319532
.           Recompiled for OUTBOATM - Added slot template location to APPTAB00
. V10.06.02 30/04/2015  Don Nguyen     CAR 314266
.           Recompiled for OUTBOATM - Modified OBOA3300 with test on indicator 1
. V10.06.01 12/03/2015  Steve Armstrong  CAR 314108
.           Removed reference to PATCGPFD (No Longer in use)                
.------------------------------------------------------------------------------
. V10.05.02 24/02/2015  Don Nguyen     CAR 303885
.           Recompiled for OUTBOATM - Modified APPTAB00 to output PTHSNAME.
. V10.05.01 21/10/2014  Ebon Clements  CAR 268970
.           Recompiled for OUTBOATM -  Change to use OUTCLIFD index 1 
.           instead of index 2
.------------------------------------------------------------------------------
. V10.04.02 09/05/2014  Don Nguyen     CAR 300097
.           Recompiled for NHIMASTM - Fixed SETMWD00 to clear date values
. V10.04.01 07/04/2014  Peter Vela     CAR 286258
.           Recompiled for PATMASTM
.------------------------------------------------------------------------------
. V10.03.08 09/10/2013  Patrick Adair  CAR 289210
.           Recompiled for NHIMASTM - end date processing in TABHCE20
. V10.03.07 26/09/2013  Jill Parkinson CAR 291691
.           Recompiled for NZHISWIO - over 15 medical warnings looping
. V10.03.06 30/06/2013  Davin          CAR 283202
.           Recompiled for changes to hl7 messaging
. V10.03.05 18/07/2013  Jill Parkinson CAR 288525  
.           Recompiled for NZHISWIO
. V10.03.04 01/05/2013  Steve Armstrong  CAR 284376
.           Recompiled for changes to DGCLICUP.
. V10.03.03 03/04/2013  Davin          CAR 270648
.           Recompiled for changes to HL7 messaging includes
. V10.03.02 16/05/2012  Steve Armstrong  CAR 256322
.           Recompiled for changes to NZHISWIO.
. V10.03.01 02/02/2012  Jill Parkinson   CAR 259481
.           Recompiled for NZHISWIO - SETMED00 call to GETHCU fixed
.------------------------------------------------------------------------------
. V10.02.02 13/07/2011  Ebon Clements     CAR 242246
.           Recompiled for OUTBOATM - Display hospital on cancelled appt list
. V10.02.01 28/03/2011  Mike Laming   CAR 240107
.           Mods to PATECDaf & PATECOaf variables & Keys - file change
.------------------------------------------------------------------------------
. V10.00.02 18/08/2010  Davin             CAR 223805
.           Recompiled for PATVISTM
. V10.00.01 03/06/2010  Ebon Clements  CAR 208220
.           Recompiled for OUTBOATM -  Added comment icon link to APPTSU00
.------------------------------------------------------------------------------
. V9.12.02  07/12/2009  Peter McMullen CAR 210130
.           change call DOCNAM00 to DOCNM000 
. V9.12.01  29/07/2009  Ebon Clements CAR 202109
.           Added A/H visit type to GETPAT00
.------------------------------------------------------------------------------
. V9.08.03  01/03/2007  Jill Habasque CAR 131919
.           Added open of PATAUDAL  
. V9.08.02  22/02/2007  Jill Habasque CAR 131919
.           Added open of PATDGWFD  
. V9.08.01  25/01/2007  Peter Vela CAR 127930
.           Recompiled for MRTMASFD
.------------------------------------------------------------------------------
. V9.05.01  09/03/2006  Ebon Clements CAR 78533
.           Mods for PATCODTM - Hospital specific category codes
. V9.05.B02 30/01/2006  Ebon Clements CAR 93186
.           Mods for REDIRECT length change. Recompiled for IBAWEBDF
.------------------------------------------------------------------------------
. V9.04.04  21/11/2005  Peter Vela    CAR 68675
.           Recompiled for PATDSCTM
. V9.04.03  01/03/2005  Peter Vela    CAR 57547
.           Recompiled for PATVADFD and PATVADIO
. V9.04.02  09.11.2004  Peter Vela     CAR 53371
.           Recompiled for PATVISTM
. V9.04.01  18.08.2004  Lina Vo        CAR 52746
.           Added open of pathspaf and included PATHSPFD & IO for PATVISTM
.------------------------------------------------------------------------------
. V9.03.08  27/07/2004  Peter Vela               CAR 51304
.           Recompiled for OUTBOATM
. V9.03.07  14/07/2004  Peter Vela               CAR 51518
.           Recompiled for OUTBOATM
. V9.03.06  07/06/2004 Peter Vela      CAR 49016
.           2004 Statutory Changes - recompiled for PMSPX2TM
. V9.03.05  26/03/2004 Peter Vela      CAR 13038
.           Recompiled for PATDOCCD - Doctor Name format Parameter
. V9.03.04  11/12/2003 Sylvek Litewka  CAR 20222
.           Modified procedures GETICD00 and GETICO00 to use RDPTICD1 and
.           RDPTICO1 routines when reading ICD files.
. V9.03.03  07/11/2003 Sylvek Litewka  CAR 44328
.           Recompiled for PATECDFD, PATECOFD and PTAMMBFD changes.
. V9.03.02  28/08/2003 Jill Habasque CAR 42883
.           Moved call to CHKHCU00
. V9.03.01  02/07/2003 Jill Habasque CAR 40660
.           Recompiled for PATDSCTM
.------------------------------------------------------------------------------
. V9.02.12  11/03/2003 J.Tan
.           Recompiled for PATMISTM - admitting point
. V9.02.11  07/02/2003 Tonii CAR 35992
.           Recompiled for PATMISTM - inactive codes in patfundf
. V9.02.10  07/12/2002  Lina Vo                  SRF 22709
.           Recompiled for OUTBOATM - open outcanaf with site prefix
. V9.02.09  21/11/2002  Pat Dirito               SRF 33644
.           Fixed read of PTCNI10D was reading incorrect position
. V9.02.08  09/10/2002  Peter Vela    SRF 17693
.           Recompiled for PATMISTM - Admitting Point
. V9.02.07  03/10/2002  Peter Vela    SRF 17693
.           Recompiled for PATVISTM - Admitting Point
. V9.02.06  23/08/2002  Ebon Clements SRF 17673
.           Recompiled for OUTBOATM - Visit type selection list
. V9.02.05  23/07/2002  J.Tan
.           Recompiled for PATMISTM - added patasfaf file
. V9.02.04  14/05/2002  J.Tan
.           Recompiled for PATMISTM - added casemix code
. V9.02.03  10/02/2002 Sandra Barcham
.           Read CTYPLAB from correct sector
. V9.02.02  14.01.2002 Sai
.           Recompiled after including OUTCONFD,OUTRSHFD/IO
.           CLNHIMAS and DAYOFWEK and also declared variables
.           SLOTTIME,TEMPTIME for OUTBOATM 
.------------------------------------------------------------------------------
. V9.00.001 14.08.2001 J.Tan
.           Recompiled for PATMASTM
. V9.00.B02 13.07.2001 Sandra Barcham
.           Change GP to HCP
.------------------------------------------------------------------------------
.                V5.10.B01 17/04/2001 Glenn Saunders
.                          Change PATSUR to PATGSR (old file decommissioned)
.                V5.09.02  15/01/2001 Bronko Sosic  
.                          Recompiled for PATMASTM 
.                V5.08.03  28/09/2000 Bronko Sosic  
.                          Recompiled for PATMASTM / PATMISTM
.                V5.08.02  16/08/2000  Caleb Sharman
.                          Changed Health Fund variables to be 8 chars
.                V5.08.01  07.06.2000 B.G.Ackland
.                          Recompiled for PATMASTM 
.                V5.07.03  02/02/2000 Steven Watkins
.                          Recompiled for PATMASTM / PATMISTM
.                V5.07.02  28.01.2000 B.G.Ackland
.                          New Standards
.                V5.07.01  09.10.1999 K.C.Nelson 
.                          Added open for OUTBOKA1        
.                V5.07.00  04.10.1999 B.G.Ackland
.                          Changed bgcolor to HeadingCell
.                V5.06.00  26.10.1998 B.G.Ackland
.                          Created from PATWEB98 for NZHIS Release
.---------------------------------------------------------------------
          INC       IBAWEBDF    
.
          INC       ALLCONFD/INC
          INC       AAEDE1FD/INC
          INC       AAEDI1FD/INC
          INC       ALLREFFD/INC
.
          INC       APSMASFD/INC
.
          INC       NZHISIFD/INC
          INC       NHIDCDFD/INC
          INC       NHIDMCFD/INC
          INC       NHIDNRFD/INC
          INC       NHIETHFD/INC
          INC       NHIMASFD/INC
          INC       NHIMCCFD/INC
          INC       NHIRELFD/INC
          INC       PATALRFD/INC
          INC       PATGSRFD/INC
          INC       TMPALIFD/INC
.
          INC       PATCMCFD/INC
          INC       PATCALAG/INC
          INC       PMSUCHFD/INC
          INC       PMSUCDFD/INC
          INC       PATMASTD/INC
          INC       PATCONFD/INC
          INC       VISMDTFD/INC
          INC       VISMTXFD/INC
          INC       PATDADFD/INC
          INC       PATDGWFD/INC
          INC       PATDO1FD/INC
          INC       PATFN1FD/INC
          INC       PATDSCFD/INC
          INC       PATHSPFD/INC
          INC       PATIN1FD/INC
          INC       PATITMFD/INC
          INC       PATMI1FD/INC
          INC       PATMA1FD/INC
          INC       PATNIDFD/INC
          INC       PMSPX2FD/INC
          INC       PMSPX2TD/INC
          INC       PATMMBFD/INC
          INC       PATPR1FD/INC
          INC       PATASFFD/INC
          INC       PATRE1FD/INC
          INC       PATTRNFD/INC
          INC       PATVADFD/INC
          INC       PATVISFD/INC
          INC       PATWR1FD/INC
.
          INC       PMSCCDFD/INC   * Concession Card Details
          INC       PMSHCGFD/INC
          INC       PMSHCLFD/INC
          INC       PMSHCPFD/INC
          INC       PMSHCGTD/INC
          INC       PMSHCLTD/INC
          INC       PMSHCPTD/INC
          INC       PMSVX1FD/INC
          INC       OUTSITFD/INC
          INC       OUTCANFD/INC
          INC       OUTCLIFD/INC
          INC       OUTCTYFD/INC
          INC       OUTCVTFD/INC
          INC       OUTDIAFD/INC
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
          INC       OUTBOCFD/INC
          INC       OUTMA1FD/INC
          INC       OUTHEDFD/INC
          INC       OUTSESFD/INC
          INC       OUTXSCFD/INC
          INC       OUTXWPFD/INC
          INC       OUTCONFD/INC        * Outpatient System Parameters
          INC       OUTRSHFD/INC
          INC       OUTSIPFD/INC
          INC       RESLNKFD/INC
          INC       MRTMASFD/INC
.
          INC       PATECDFD/INC            * included as supercedes patmdisf
          INC       PATECOFD/INC            * included as supercedes patmoprf
.          INC       PATMOPFD/INC           * commented superceded by patecoaf
.          INC       PATMDIFD/INC           * commented superceded by patecdaf
          INC       PATICDFD/INC
          INC       PATICOFD/INC
          INC       SCRMASFD/INC
          INC       VISCMTFD/INC
          INC       WATTR1FD/INC
          INC       PATMISTD/INC
.
. Includes & Variables for 5.06
.
          INC       PATDHEAD/INC
          INC       ALLCASFD/INC
.
NEWSLOTK  DIM       28
FORM8     FORM      8
DIM3      DIM       3
DIM5      DIM       5
DIM30     DIM       30                                                 *I-51518
DIM60     DIM       60
F12P2     FORM      12.2
F8P2      FORM      8.2
WAITSTAT  FORM      1
OUTCDESC  DIM       20
VTYPDESC  DIM       20
STATDESC  DIM       20
LINK2PRT  FORM      1       * Link to parent variable
LINKVTYP  FORM      1
WAITUNSH  INIT      "Unscheduled"
WAITSCHD  INIT      "Scheduled"
WAITPREA  INIT      "Pre-admitted"
.
CASEKEYS  DIM       28
CFILEPRE  DIM       3   * Current Open File Prefix for Outpatient Files
.
. Variables added for new OUTBOATM
.
CHARTREV  FORM      1        * Chart Review               * T0838668
CLINDESC  DIM       30
CLILDESC  DIM       25
DIDATE    DIM       11
ENDATE    DIM       11
PNTLOCTN  DIM       20
TMPDATE   DIM       8
OVERBOOK  FORM      1
.
.
HCEEND    FORM      3
HCETOT    FORM      3
HCECNT    FORM      3
HTMLLNK2  DIM       127
READDNR   FORM      1
TOTALALI  FORM      3
.NZHISWRN  FORM      1        * NZHIS change mode indicator
NZHISMOD  FORM      1         * NZHIS change mode indicator
FILNHIAF  FILE      ASCII, FIXED=256       * tmp save file for forking
.
CPPURNO   DIM       8
CREGOPT   FORM      1
FORM3     FORM      3
COUNT     FORM      3
SEVRDESC  DIM       20
DNRCOD01  DIM       1 
DNRCOD02  DIM       1 
DNRCOD03  DIM       1 
DNRCOD04  DIM       1 
DNRCOD05  DIM       1 
DNRCOD06  DIM       1 
DNRCOD07  DIM       1 
DNRCOD08  DIM       1 
DNRCOD09  DIM       1 
DNRCOD10  DIM       1 
NMPNUMB   DIM       20        * actual number keyed in
Z15       INIT      "zzzzzzzzzzzzzzz"
.
MBSDESC   DIM       40
DIM4      DIM       4
DIM6      DIM       6
SHOWMAP   FORM      1
SHOWHOSC  FORM      1
CODTYP    FORM      2
DISNAM70  DIM       70
NAME70    DIM       70
.
VISCOUNT  FORM      5
.
CATD1     INIT      "D1"
CATD2     INIT      "D2"
CATD3     INIT      "D3"
CATD4     INIT      "D4"
CATD5     INIT      "D5"
CATU6     INIT      "U6"
CATU7     INIT      "U7"
VISTDESC  DIM       25
VISTEMER  INIT      "Emergency"
VISTOUTP  INIT      "Outpatient"
VISTINPT  INIT      "Inpatient"
.
INPSTAT1  INIT      "Pre-Admission"
INPSTAT2  INIT      "Current Inpatient"
INPSTAT3  INIT      "Inpatient"
INPSTAT4  INIT      "On-Leave Inpatient"
INPSTAT5  INIT      "Cancelled Admission"
.
MOVEDESC  DIM       15
MOVEADM   INIT      "Admitted  "
MOVECHG   INIT      "Change    "
MOVEDIS   INIT      "Discharged"
MOVELVE   INIT      "On Leave  "
MOVERET   INIT      "Return    "
.
ATYPDESC  DIM       25
WARDNAME  DIM       20
TRANDATE  DIM       12
ADMNDATE  DIM       12
DISCDATE  DIM       12
.
ENDDAT    DIM       8
DLINKPRG  DIM       8
DLINKREP  DIM       2
DLINKTEM  DIM       3
DOCTCODE  DIM       6
TEMPTIME  DIM       5
SLOTTIME  DIM       5
VISCMFLG  FORM      1
VISTFLAG  FORM      1
.
.-----------------------------------------------------------------------
PRGID     INIT      "NHIWEB98"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "Patient Enquiries Template Server"
.-----------------------------------------------------------------------
.
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          CALL      CONNECT
.
          BRANCH    REPORTNO,MAIN1100
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "INVALID OPTION",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  CALL      GETPAT00
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
.------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------
INIT0000  CALL      INTMAS00
          OPEN      AAEDE1A1,"aaede1af"  
          OPEN      ALLREFA1,"allrefaf"
          OPEN      AAEDI1A1,"aaedi1af"  
          OPEN      APSMASA1,"apsmasaf"  
          OPEN      PATRE1A1,"patre1af"  
          OPEN      WATTR1A1,"wattr1af"  
          OPEN      PATFN1A1,"patfn1af"  
          OPEN      PATHSPA1,"pathspaf"  
          OPEN      PATAUDA2,"pataudal"
          OPEN      PATIN1A1,"patin1af"  
          OPEN      PATAUDA2,"pataudal"
.
          OPEN      IBAPRTA1,"ibaprtaf"        * For Printer Selection
          OPEN      IBACODE1,"ibacodef"        * For Printer Selection
          OPEN      IHAPASS1,"ihapassf"        * For Printer Selection
          OPEN      IBAPDFA1,"ibapdfaf"        * For default printer selection
          OPEN      IBATMHA1,"ibatmhaf"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATTRAN2,"pattranf"
          OPEN      PATVADA1,"patvadaf"
          OPEN      PATVISA2,"patvisaf"
          OPEN      PATMMBS1,"patmmbsf"
          OPEN      PATITEM1,"patitemn"
.          OPEN      PATMOPR1,"patmoprf"    * commented superceded by patecoaf
          OPEN      PATECOA2,"patecoaf"     * Added as per srf 14169
.          OPEN      PATMDIS1,"patmdisf"    * commented superceded by patecdaf
          OPEN      PATECDA2,"patecdaf"     * Added as per srf 14169
          OPEN      SCRMASA1,"scrmasaf"
          OPEN      VISMDTA1,"vismdtaf"
          OPEN      VISMTXA1,"vismtxaf"
.
          OPEN      PMSCCDA1,"pmsccdaf"
          OPEN      PMSHCGA1,"pmshcgaf"
          OPEN      PMSHCLA1,"pmshclaf"
          OPEN      PMSHCLA2,"pmshclaf"
          OPEN      PMSHCPA1,"pmshcpaf"
          OPEN      PMSHCPA2,"pmshcpaf"
.
          CALL      OPICD1                  * Open ICD Disease files
          CALL      OPICO1                  * Open ICD Operation files
.
          READ      CONTROLF,TEN;*126,CLABPG,CLABPRT
.
          READ      CONTROLF,TWENTY1;*13,PTCNUNAK,*56,PTCNDEFT,*119,PTCNRPIK:
                             *138,PTCNNHII,PTCNNHID,PTCNDONR,PTCNMEDW,PTCNUDNG:
                             *178,PTCNMPTR,PTCNCDRS,PTCNLNCD,PTCNLP01:
                             *189,PTCNUONL:
                             PTCNPCON,PTCNNHIF,*204,PTCNAWRN,*226,PTCNMWAD:
                             *237,PTCNPAOF,*238,PTCNAGEC
          READ      CONTROLF,FIFTY9;*35,CTYPLAB
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          IF        IBCNMHOS=1
            OPEN      MLTCODA1,"mltcodaf"
            OPEN      MLTCODA2,"mltcodaf"
          ENDIF
.
          OPEN      NHIMASA1,"nhimasaf"
          OPEN      NHIMASA2,"nhimasaf"
          OPEN      NHIETHA1,"nhiethaf"
          OPEN      NHIDNRA1,"nhidnraf"
          OPEN      NHIRELA1,"nhirelaf"
          OPEN      NHIDCDA1,"nhidcdaf"
          OPEN      NHIDMCA1,"nhidmcaf"
          OPEN      NHIMCCA1,"nhimccaf"
          OPEN      PATMWSA1,"patmwsaf"
          OPEN      TMPALIA1,"tmpaliaf"
          OPEN      VISCMTA1,"viscmtaf"
          OPEN      PATDGWA1,"patdgwaf"
          OPEN      WEBSECA3,"websecaf"                                *I-51518
.
          CALL      CONNECT
.
          MOVE      "out",CFILEPRE       * Save Current File Prefix
          CALL      OPNOUT00             * Open Outpatient Files
          OPEN      OUTSIPA1,"outsipaf"
.
          OPEN      ALLCASA1,"allcasaf"
.
INIT9999  RETURN
.------------------------------------------------------------
. Open Outpateint Files
.------------------------------------------------------------
OPNOUT00  PACK      CFNAME,CFILEPRE,FILBOKA3  * Get the name of the BOKA3 file
          OPEN      OUTBOKA3,CFNAME           * Open the file
          PACK      CFNAME,CFILEPRE,FILBOKA2  * Get the name of the BOKA2 file
          OPEN      OUTBOKA2,CFNAME           * Open the file
          PACK      CFNAME,CFILEPRE,FILBOKA1  * Get the name of the BOKA1 file
          OPEN      OUTBOKA1,CFNAME           * Open the file
          PACK      CFNAME,CFILEPRE,FILSESA1  * Get the name of the SESA1 file
          OPEN      OUTSESA1,CFNAME           * Open the file
          PACK      CFNAME,CFILEPRE,FILCLIA1  * Get the name of the CLIA1 file
          OPEN      OUTCLIA1,CFNAME           * Open the file
          PACK      CFNAME,CFILEPRE,FILCTYA1  * Get the name of the CTYA1 file
          OPEN      OUTCTYA1,CFNAME           * Open the file
          PACK      CFNAME,CFILEPRE,FILMA1A1  * Get the name of the MA1A1 file
          OPEN      OUTMA1A1,CFNAME           * Open the file
          PACK      CFNAME,CFILEPRE,FILCANA1  * Get the name of the CANA1 file
          OPEN      OUTCANA1,CFNAME           * Open the file
          PACK      CFNAME,CFILEPRE,FILCANA2  * Get the name of the CANA2 file
          OPEN      OUTCANA2,CFNAME           * Open the file
          PACK      CFNAME,CFILEPRE,FILBB1A1
          CALL      OPOTBB11               * Open the file
          PACK      CFNAME,CFILEPRE,FILBOKC1
          OPEN      OUTBOKC1,CFNAME           * Open the file
          PACK      CFNAME,CFILEPRE,FILHEDA1
          OPEN      OUTHEDA1,CFNAME           * Open the file
          PACK      CFNAME,CFILEPRE,FILCVTA1  * Get the name of the CVTA1 file
          OPEN      OUTCVTA1,CFNAME           * Open the file
          RETURN
.----------------------------------------------------------------------
. Initialise Connection to NHI/MWS System
.----------------------------------------------------------------------
.INNHI000  MOVE      ZERO,NZIBCBAS
.          MOVE      ZERO,NZIBCADD
.          MOVE      ZERO,NZIBVNEW
.          MOVE      ZERO,NZIBOLDA
.          MOVE      ZERO,NZIBCCON
.          MOVE      ZERO,NZIBPERS
.          MOVE      ZERO,NZIBCCAD
.          MOVE      ZERO,NZIBCNHI
.          MOVE      ZERO,NZIBREGO
.          CALL      CONNECT                       * Connect to National System
.          IF        OVRCD=1
.            CALL      WEBERR00
.          ENDIF
.          RETURN
.------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          MOVE      SP70,TEMPLATE
          MOVE      SP70,URNUMBER
          MOVE      SP70,ADMISSNO
          PACK      REDIRECT,SP70,SP70,SP70,SP70
          RETURN
.------------------------------------------------------------
. Set Parameters
.------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
          ENDIF
.
          MATCH     "urnumber=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,URNUMBER
          ENDIF
.
          MATCH     "admissno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ADMISSNO
          ENDIF
          RETURN
.------------------------------------------------------------
. Output Patient Enquiry Page
.------------------------------------------------------------
GETPAT00  MOVE      SP70,DOCTCODE
          CALL      IBACLOCK
.
          CALL      CLAAEDET
          CALL      CLAAEDIS
          CALL      CLOUTBOA
          CALL      CLOUTBOB
          CALL      CLPATMIS
          CALL      CLPATDSC
          CALL      CLPMSPX2
.
          REP       "+ ",URNUMBER
          REP       "+ ",ADMISSNO
.
          MOVE      URNUMBER,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,GETPAT95
.
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          IF        OVRCD=1
            CALL      CLPMSPX2
          ENDIF
.  
          MOVE      URNUMBER,KEY8
          CALL      RDNHMAS2
          BRANCH    OVRCD,GETPAT96
.         
.         CALL      CHKHCU00
.         BRANCH    EXIT,GETPAT99
          MOVE      ZERO,READDNR     * Set Flag as Donor Details Not Read
.
          MATCH     SP70,ADMISSNO
          GOTO      GETPAT80 IF EQUAL
.
          MOVE      ADMISSNO,KEY8
          CALL      RDVISA1
          BRANCH    OVRCD,GETPAT80
.
          MOVE      ADMISSNO,KEY8
          CALL      RDRESP1
          MOVE      OVRCD,RESPFLAG
.
          BRANCH    PVITYPE,GETPAT20,GETPAT40,GETPAT60
.
          IF        PVITYPE=9
            MATCH    " 2",PVISYST
            GOTO     GETPAT70 IF EQUAL
          ENDIF
. 
          GOTO      GETPAT97
.
GETPAT20  MOVE      ADMISSNO,KEY8
          CALL      RDDETA1
          CALL      RDDISA1
          GOTO      GETPAT80
.
GETPAT40  MOVE      ADMISSNO,KEY8
          CALL      RDBOKB1
          BRANCH    OVRCD,GETPAT80
          CALL      RDOTBOC1
          BRANCH    OVRCD,GETPAT80
.
          PACK      CASEKEYS,PVISITE,PVIDOCT,PVIDATE,SP70
          PACK      KEY28,PVISITE,PVIDOCT,PVIDATE,SP70
          PACK      KEY20,PVISITE,PVIDOCT,PVIDATE,SP70
          CALL      RDSBOKA1
GETPAT45  CALL      RDKBOKA1
          BRANCH    OVRCD,GETPAT50
          PACK      CASEKEYS,OBASITE,OBACLIN,OBADATE,OBASTRT,OBASLOT,SP70
          MATCH     KEY20,CASEKEYS
          GOTO      GETPAT50 IF NOT EQUAL
          MATCH     OBAOUTNO,PVIBILL         * billing number
          GOTO      GETPAT80 IF EQUAL
          GOTO      GETPAT45
.
GETPAT50  CALL      CLOUTBOA
          GOTO      GETPAT80
.
GETPAT60  MOVE      ADMISSNO,KEY8
          CALL      RDMISS1
          CALL      RDDSCH1
          IF        OVRCD=0
            MOVE      DDATE,ICDRDDTE   * Needed for RDPTICD1 & RDPTICO1 reads
          ENDIF
.
          GOTO      GETPAT80
.
GETPAT70  MOVE      ADMISSNO,KEY8
          CALL      RDALREF1
          IF        OVRCD=1
            CALL      CLALLREF
          ENDIF     
.
GETPAT80  CALL      PATNAM00
          CLEAR     WEBTITLE
          APPEND    PATFNAME,WEBTITLE
          APPEND    SP1,WEBTITLE
          APPEND    WBTPDES,WEBTITLE
          RESET     WEBTITLE
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,GETPAT99
.
          CALL      WEBBOD00   * Output Templated Body of HTML 
          CALL      CONNECT
          BRANCH    EXIT,GETPAT99
          CALL      CHKHCU00
          BRANCH    EXIT,GETPAT99
.
GETPAT90  CALL      WEBEND00   * Output End of Web Page
          GOTO      GETPAT99
.
GETPAT95  MOVE      "Invalid U/R No - ",WEBTITLE
          CALL      WEBERR00
          GOTO      GETPAT99
.
GETPAT96  MOVE      "Invalid NHI No - ",WEBTITLE
          CALL      WEBERR00
          GOTO      GETPAT99
.
GETPAT97  MOVE      "Invalid Visit No - ",WEBTITLE
          CALL      WEBERR00
          GOTO      GETPAT99
.
GETPAT99  RETURN
.------------------------------------------------------------
. Process Fields in Body of HTML Template
.------------------------------------------------------------
PROFLD00  BRANCH    REPORTNO,PROFLD10
          GOTO      PROFLD99
.
PROFLD10  BRANCH    FLDSETNO,PROFLD11,PROFLD12,PROFLD13,PROFLD14,PROFLD15:
                             PROFLD16,PROFLD17,PROFLD18,PROFLD19
          GOTO      PROFLD99
.
PROFLD11  CALL      MASF0000
          GOTO      PROFLD99
.
PROFLD12  CALL      MISF0000
          GOTO      PROFLD99
.
PROFLD13  CALL      DSCF0000
          GOTO      PROFLD99
.
PROFLD14  CALL      MSXF0000
          GOTO      PROFLD99
.
PROFLD15  CALL      TABL0000     * Table Displays
          GOTO      PROFLD99
.
PROFLD16  CALL      VISF0000
          GOTO      PROFLD99
.
PROFLD17  CALL      NHIF0000
          GOTO      PROFLD99
.
PROFLD18  CALL      AAEF0000
          GOTO      PROFLD99
.
PROFLD19  CALL      OBOA0000
          GOTO      PROFLD99
.
PROFLD99  RETURN
.------------------------------------------------------------
. Table Outputs (Visit, Transfers, etc)
.------------------------------------------------------------
TABL0000  BRANCH    FLDITMNO,TABL1100,TABL1200,TABL1300,TABL1400,TABL1500:
                             TABL1600,TABL1700
          GOTO      TABL9999
.
TABL1100  CALL      VISTAB00     * Visits
          GOTO      TABL9999
.
TABL1200  CALL      TRNTAB00     * Transfers
          GOTO      TABL9999
.
TABL1300  CALL      MBSTAB00     * MBS Coding
          GOTO      TABL9999
.
TABL1400  CALL      OPRTAB00     * Operation Coding
          GOTO      TABL9999
.
TABL1500  CALL      DISTAB00     * Disease Coding
          GOTO      TABL9999
.
TABL1600  CALL      BOKTAB00     * Future Outpatient Bookings
          GOTO      TABL9999
.
TABL1700  CALL      WATTAB00     * Waiting List 
          GOTO      TABL9999
. 
TABL9999  RETURN
.------------------------------------------------------------
. Output Visit Table Details
.------------------------------------------------------------
VISTAB00  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
          WRITE     HTMLFILE;"<tr><td class=HeadingCell>Visit No.</td>":
                             "<td class=HeadingCell>Visit Date </td>":
                             "<td class=HeadingCell>Visit Type</td>":
                             "<td class=HeadingCell>Doctor/Clinic</td></tr>"
          MOVE      ZERO,VISCOUNT
          PACK      KEY24,URNUMBER,Z70
          CALL      RDSVISA2
VISTAB10  CALL      RDPVISA2
          BRANCH    OVRCD,VISTAB90
          MATCH     URNUMBER,PVIURNO
          GOTO      VISTAB90 IF NOT EQUAL
          ADD       ONE,VISCOUNT
.
          CALL      GETVIS00
.
          WRITE     HTMLFILE;"<tr><td><a href='javascript:linkPatient(#"":
                                URNUMBER,"#",#"",PVIBILL,"#",#"":
                                PVITYPE,"#")'>":
                                PVIBILL,"</a></td>":
                             "<td>",ADMNDATE,"</td>":
                             "<td>",VISTDESC,"</td>":
                             "<td>",DOCFNAME,"&nbsp;</td></tr>"
          GOTO      VISTAB10
.
VISTAB90  IF        VISCOUNT=0
            WRITE     HTMLFILE;"<tr><td align=center colspan=4>":
                               "<b>No Visits Found</td></tr>"
          ENDIF
.
VISTAB99  RETURN
.------------------------------------------------------------
. Get Visit Details
.------------------------------------------------------------
GETVIS00  UNPACK    PVIDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      ADMNDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          PACK      DOCFNAME,SP70
          MATCH     SP6,PVIDOCT
          GOTO      GETVIS50 IF EQUAL
.
.------ For outpatients, PVIDOCT is the clinic code, not the doctor's code ----
.
          BRANCH    PVITYPE,GETVIS20,GETVIS10,GETVIS20
          GOTO      GETVIS20
.
.------ Read the clinic file to get either the doctors name, or doctor code ---
.
GETVIS10  MOVE      "out",OSTFILE
          MOVE      PVISITE,KEY6
          CALL      RDSITA1
.
          PACK      CFNAME WITH OSTFILE,FILCLIA1
          OPEN      OUTCLIA1,CFNAME
.
          PACK      KEY20,PVISITE,PVIDOCT,PVIDATE,SP70  * Read for current
          CALL      RDCLIA1                             * Effective date 
          IF        OVRCD=1
            PACK      KEY20,PVISITE,PVIDOCT,PVIDATE,Z70  * Read for current
            CALL      RDSCLIA1
            CALL      RDPCLIA1
            IF        OVRCD=1
              MATCH     PVISITE,OCASITE
              IF        !@EQUAL
                CLEAR     DOCFNAME
                APPEND    "Invalid Clinic - ",DOCFNAME
                APPEND    PVIDOCT,DOCFNAME
                RESET     DOCFNAME
                GOTO      GETVIS50
              ENDIF
.
              MATCH     PVIDOCT,OCACLIN
              IF        !@EQUAL
                CLEAR     DOCFNAME
                APPEND    "Invalid Clinic - ",DOCFNAME
                APPEND    PVIDOCT,DOCFNAME
                RESET     DOCFNAME
                GOTO      GETVIS50
              ENDIF
            ENDIF
          ENDIF
.
          CLEAR     DISPNAME
          MOVE      OCADESC,NAME35
          CALL      NAMSTR00
          RESET     DISPNAME
          MOVE      DISPNAME,DOCFNAME
          MATCH     SP6,OCADOCT    
          GOTO      GETVIS50 IF EQUAL
          MOVE      OCADOCT,PVIDOCT      * Use the doctor's code from the clinic
.
GETVIS20  MOVE      PVIDOCT,KEY6         * Read the doctor's file to get nam
          CALL      RDDOCT1
          IF        OVRCD=1
            CLEAR     DOCFNAME
            APPEND    "Invalid Doctor - ",DOCFNAME
            APPEND    PVIDOCT,DOCFNAME
            RESET     DOCFNAME
            GOTO      GETVIS50
          ENDIF
          MOVE      DTITL,FMTTITLE       * format doctor's name
          MOVE      DGNAME,FMTGNAME
          MOVE      DSNAME,FMTSNAME
          CALL      DOCNM000
.
GETVIS50  MOVE      "&nbsp;",VISTDESC
          MOVE      "&nbsp;",DISCDATE
          MOVE      "&nbsp;",WARDNAME
          LOAD      VISTDESC,PVITYPE,VISTEMER,VISTOUTP,VISTINPT
          COMPARE   THREE,PVITYPE
          GOTO      GETVIS99 IF NOT EQUAL
          MOVE      PVIBILL,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD OF GETVIS99
          LOAD      VISTDESC,ASTAT,INPSTAT1,INPSTAT2,INPSTAT3,INPSTAT4:
                                   INPSTAT5
.
          MOVE      PVIBILL,KEY8
          CALL      RDDSCH1
          IF        OVRCD=0
            UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      DISCDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
            CALL      GETLTR00
          ENDIF
.
          PACK      KEY6,AWARD,SP70
          CALL      RDWARD1
          IF        OVRCD=0
            PACK      WARDNAME,WBDESC,SP70
            STRIP     WARDNAME
          ENDIF
.   
GETVIS99  RETURN
.------------------------------------------------------------
. Get Last Transfer Record
.------------------------------------------------------------
GETLTR00  PACK      KEY30,PVIBILL,Z70
          CALL      RDSTRAN2
          CALL      RDPTRAN2
          BRANCH    OVRCD,GETLTR99
          MATCH     PVIBILL,TADMN
          GOTO      GETLTR99 IF NOT EQUAL
          MATCH     "D",TMOVE
          GOTO      GETLTR99 IF NOT EQUAL
.
          MOVE      TWARD,AWARD
.
GETLTR99  RETURN
.------------------------------------------------------------
. Output Bed Transfer Table Details
.------------------------------------------------------------
TRNTAB00  WRITE     HTMLFILE;"<tr>":
                    "<td class=HeadingCell align=center>Date/Time</td>":
                    "<td class=HeadingCell>Ward/Bed</td>":
                    "<td class=HeadingCell align=right>Charge</td>":
                    "<td class=HeadingCell>Adm Type</td>":
                    "<td class=HeadingCell>Doctor</td>":
                    "<td class=HeadingCell>Movement</td></tr>"
          MOVE      ZERO,VISCOUNT
          PACK      KEY30,ADMISSNO,SP70
          CALL      RDSTRAN2
TRNTAB10  CALL      RDKTRAN2
          BRANCH    OVRCD,TRNTAB90
          MATCH     ADMISSNO,DTADMN  
          GOTO      TRNTAB90 IF NOT EQUAL
          ADD       ONE,VISCOUNT
.
          CALL      GETTRN00
.
          UNPACK    TDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      TRANDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          MOVE      TTIME,KEY5
          WRITE     HTMLFILE;"<tr><td>",TRANDATE,SP1,KEY5,"</a></td>":
                             "<td>",WARDNAME,SP1,TBED,"</td>":
                             "<td align=right>",TRATEF,"</td>":
                             "<td>",ATYPDESC,"</td>":
                             "<td>",DOCFNAME,"&nbsp;</td>":
                             "<td>",MOVEDESC,"&nbsp;</td></tr>"
          GOTO      TRNTAB10
.
TRNTAB90  IF        VISCOUNT=0
            WRITE     HTMLFILE;"<tr><td align=center colspan=6>":
                               "<b>No Bed Transfer Records Found</td></tr>"
          ENDIF
.
TRNTAB99  RETURN
.------------------------------------------------------------
. Get Transfer Details
.------------------------------------------------------------
GETTRN00  MOVE      "&nbsp;",WARDNAME
          PACK      KEY6,TWARD,SP70
          CALL      RDWARD1
          IF        OVRCD=0
            PACK      WARDNAME,WBDESC,SP70
            STRIP     WARDNAME
          ENDIF
.
          MOVE      TADOCT,KEY6
          CALL      RDDOCT1
          IF        OVRCD=0
            MOVE      DTITL,FMTTITLE       * format doctor's name
            MOVE      DGNAME,FMTGNAME
            MOVE      DSNAME,FMTSNAME
            CALL      DOCNM000
          ELSE
            CLEAR     DOCFNAME
            APPEND    "Invalid Doctor - ",DOCFNAME
            APPEND    PVIDOCT,DOCFNAME
            RESET     DOCFNAME
          ENDIF
.
          ADD       TRATEH,TRATEF
          ADD       TEXTRA,TRATEF
          SUB       TDISC,TRATEF
          SUB       TALLW,TRATEF
.
          MOVE      TMOVE,ANS
          REP       "A1D2C3L4R5",ANS
          MOVE      ZERO,F1
          MOVE      ANS,F1
          MOVE      "&nbsp;",MOVEDESC
          LOAD      MOVEDESC,F1,MOVEADM,MOVEDIS,MOVECHG,MOVELVE,MOVERET
.
          MOVE      "&nbsp;",ATYPDESC
          MOVE      "A ",KEY2
          PACK      KEY5,KEY2,TATYPE
          CALL      RDCODE1
          IF        OVRCD=0
            CLEAR     DISPNAME
            PACK      NAME35,TDESC,SP70
            CALL      NAMSTR00
            APPEND    SP70,DISPNAME
            RESET     DISPNAME
            MOVE      DISPNAME,ATYPDESC
            STRIP     ATYPDESC
          ENDIF
.
          RETURN
.------------------------------------------------------------
. Output MBS Coding Table Details
.------------------------------------------------------------
MBSTAB00  WRITE     HTMLFILE;"<tr>":
                    "<td class=HeadingCell>MBS Code</td>":
                    "<td class=HeadingCell>MBS Name</td>":
                    "<td class=HeadingCell align=center>Date</td>":
                    "<td class=HeadingCell align=center>Start Time</td>":
                    "<td class=HeadingCell align=center>End Time</td></tr>"
          MOVE      ZERO,VISCOUNT
          PACK      KEY11,ADMISSNO,SP70
          CALL      RDSMMBS1
MBSTAB10  CALL      RDKMMBS1
          BRANCH    OVRCD,MBSTAB90
          MATCH     ADMISSNO,DMMADMN  
          GOTO      MBSTAB90 IF NOT EQUAL
          ADD       ONE,VISCOUNT
.
          MOVE      MMCODE,KEY9
          PACK      KEY17,MMCODE,MMDATE,SP70
          CALL      PATITMRD            * read item file
          IF        OVRCD=1
            MOVE      "Invalid Code",IDESC
          ENDIF
.
          UNPACK    MMDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      TRANDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          MOVE      TTIME,KEY5
          WRITE     HTMLFILE;"<tr><td>",MMCODE,"</a></td>":
                             "<td>",IDESC,"</td>":
                             "<td align=center>",TRANDATE,"</td>":
                             "<td align=center>",MMSTIM,"</td>":
                             "<td align=center>",MMETIM,"</td></tr>"
          GOTO      MBSTAB10
.
MBSTAB90  IF        VISCOUNT=0
            WRITE     HTMLFILE;"<tr><td align=center colspan=5>":
                               "<b>No MBS Coding Records Found</td></tr>"
          ENDIF
.
MBSTAB99  RETURN
.------------------------------------------------------------
. Output Operation ICD9/10 Coding Table Details
.------------------------------------------------------------
OPRTAB00  UNPACK    DIM127,D1,ANS,DIM127
          MOVE      ZERO,SHOWMAP
          MOVE      ANS,SHOWMAP
          WRITE     HTMLFILE;"<tr>":
                             "<td class=HeadingCell colspan=5 align=center>";
          WRITE     HTMLFILE;"ICD9 Coding";
.          CALL      ICDTYP00    * Determine Default ICD9 or 10 and Show Heading
          WRITE     HTMLFILE;"</td></tr>"
          WRITE     HTMLFILE;"<tr>":
                    "<td class=HeadingCell>Operation Code</td>":
                    "<td class=HeadingCell>Description</td>":
                    "<td class=HeadingCell align=center>Date</td>":
                    "<td class=HeadingCell align=center>Start Time</td>":
                    "<td class=HeadingCell align=center>End Time</td></tr>"
          MOVE      ZERO,VISCOUNT
          PACK      KEY16,ADMISSNO,SP70
          CALL      RSPTECO2
OPRTAB10  CALL      RKPTECO2
          BRANCH    OVRCD,OPRTAB90
          MATCH     ADMISSNO,DPTEOADM  
          GOTO      OPRTAB90 IF NOT EQUAL
          ADD       ONE,VISCOUNT
.
          CALL      GETICO00
.
          MATCH     SP70,PTEODESC           * Is Free Format Description blank?
          IF        !@EQUAL
            MOVE      PTEODESC,ODESC        * No, use Free Format Description
          ENDIF
.
          CLEAR     DISNAM70
          PACK      NAME70,ODESC,SP70
          CALL      LONGNM00
          APPEND    SP70,DISNAM70
          RESET     DISNAM70
          MOVE      DISNAM70,ODESC
          STRIP     DDESC
.
          UNPACK    PTEODATE,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      TRANDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          MOVE      TTIME,KEY5
          WRITE     HTMLFILE;"<tr><td>",PTEOTYPE,SP1,OPCODE,"</a></td>":
                             "<td>",ODESC,"</td>":
                             "<td align=center>",TRANDATE,"</td>":
                             "<td align=center>",PTEOSTTM,"</td>":
                             "<td align=center>",PTEOEDTM,"</td></tr>"
          GOTO      OPRTAB10
.
OPRTAB90  IF        VISCOUNT=0
            WRITE     HTMLFILE;"<tr><td align=center colspan=5>":
                               "<b>No Operation Coding Records Found</td></tr>"
          ENDIF
.
OPRTAB99  RETURN
.
.------------------------------------------------------------
. Output Operation ICD9/10 Coding Table Details
.------------------------------------------------------------
DISTAB00  UNPACK    DIM127,D1,ANS,DIM127
          MOVE      ZERO,SHOWMAP
          MOVE      ANS,SHOWMAP
          WRITE     HTMLFILE;"<tr>":
                             "<td class=HeadingCell colspan=2 align=center>";
.
          WRITE     HTMLFILE;"ICD9 Coding";
.          CALL      ICDTYP00     * Determine if ICD9 or ICD10 and Add Heading
.
          WRITE     HTMLFILE;"</td></tr>"
          WRITE     HTMLFILE;"<tr>":
                    "<td class=HeadingCell>Disease Code</td>":
                    "<td class=HeadingCell>Description</td></tr>"
          MOVE      ZERO,VISCOUNT
          PACK      KEY16,ADMISSNO,SP70
          CALL      RSPTECD2
DISTAB10  CALL      RKPTECD2
          BRANCH    OVRCD,DISTAB90
          MATCH     ADMISSNO,DPTEDADM  
          GOTO      DISTAB90 IF NOT EQUAL
          ADD       ONE,VISCOUNT
.
          CALL      GETICD00
.
          MATCH     SP70,PTEDDESC           * Is Free Format Description blank?
          IF        !@EQUAL
            MOVE      PTEDDESC,DDESC        * No, use Free Format Description.
          ENDIF
.
          CLEAR     DISNAM70
          PACK      NAME70,DDESC,SP70
          CALL      LONGNM00
          APPEND    SP70,DISNAM70
          RESET     DISNAM70
          MOVE      DISNAM70,DDESC
          STRIP     DDESC
.
          WRITE     HTMLFILE;"<tr><td>",PTEDTYPE,SP1,DPCODE,"</a></td>":
                             "<td>",DDESC,"</td></tr>"
          GOTO      DISTAB10
.
DISTAB90  IF        VISCOUNT=0
            WRITE     HTMLFILE;"<tr><td align=center colspan=2>":
                               "<b>No Disease Coding Records Found</td></tr>"
          ENDIF
.
DISTAB99  RETURN
.-------------------------------------------------
. Format Long Description
.-------------------------------------------------
LONGNM00  CMATCH    SP1,NAME70               * Remove Leading Blanks
          IF        @EQUAL
            BUMP      NAME70
            GOTO      LONGNM00 IF NOT EOS
            GOTO      LONGNM99               * entire name if blank
          ENDIF
          PACK      KEY70,NAME70,SP30        * reset form pointer
          MOVE      KEY70,NAME70
.
          SCAN      SP1,NAME70             * Locate the blank
          GOTO      LONGNM20 IF NOT EQUAL
          BUMP      NAME70,-1              * go back 1 to end of name
          MOVEFPTR  NAME70,CCITEM          * another handly form field
          RESET     NAME70                 * reset the form pointer
          SETLPTR   NAME70,CCITEM          * set logical length to end of name
.
.         Save this name
.
          UNPACK    NAME70,KEY1,KEY80
          REP       DOWNCASE,KEY80
          APPEND    KEY1,DISNAM70
          STRIP     KEY80
          APPEND    KEY80,DISNAM70
          APPEND    SP1,DISNAM70
.
.         Check for any more names
.
          SETLPTR   NAME70,70              * reset logical length
          ADD       ONE,CCITEM             * position of 1st blank
          RESET     NAME70,CCITEM          * reset to this point
          PACK      KEY70,NAME70,SP70      * reset form pointer
          MOVE      KEY70,NAME70
          GOTO      LONGNM00
.
.         Rest of the string is one name
.
LONGNM20  UNPACK    NAME70,KEY1,KEY80
          REP       DOWNCASE,KEY80
          APPEND    KEY1,DISNAM70
          STRIP     KEY80
          APPEND    KEY80,DISNAM70
          APPEND    SP1,DISNAM70
.
LONGNM99  RETURN
.------------------------------------------------------------
. Get ICD Disease Details 
.------------------------------------------------------------
GETICD00  MOVE      PTEDCODE,KEY9
          CALL      RDPTICD1                * read icd code file
.          COMPARE   ZERO,SHOWMAP
.          GOTO      GETICD99 IF EQUAL
.          IF        CODTYP=9
.            PACK      KEY9,DICD10CD
.            CALL      RDPT10D1            * Read ICD10 disease record
.          ELSE
.            PACK      KEY9,PT0DMI9C
.            CALL      RD10T9D1            * Read ICD9 disease record
.          ENDIF
GETICD99  RETURN
.------------------------------------------------------------
. Get ICD Operation Details 
.------------------------------------------------------------
GETICO00  MOVE      PTEOCODE,KEY9
          CALL      RDPTICO1              * read icd code file
.          COMPARE   ZERO,SHOWMAP
.          GOTO      GETICO99 IF EQUAL
.          IF        CODTYP=9
.            PACK      KEY9,OICD10CD
.            CALL      RDPT10O1            * Read ICD10 disease record
.          ELSE
.            PACK      KEY9,PT0OMI9C
.            CALL      RD10T9O1            * Read ICD9 disease record
.          ENDIF
GETICO99  RETURN
.------------------------------------------------------------
. Get Coding Type
.------------------------------------------------------------
ICDTYP00  READ      CONTROLF,EIGHTY8;*59,PTCNI10D
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          MATCH     PTCNI10D,KEY8
          IF        !@LESS
            MOVE       "10",CODTYP          * Using ICD10 codes
          ELSE
           MOVE       "9",CODTYP           * Using ICD9 codes
          ENDIF
.
          MOVE      "10",CODTYP
          PACK      KEY16,AADMNO,SP10
          CALL      RSPTECD2                * Position on & read a patient
          CALL      RKPTECD2                  disease file record
          BRANCH    OVRCD,ICDTYP50
.
          MATCH     AADMNO,PTEDADMN
          GOTO      ICDTYP50 IF NOT EQUAL   * Different admin no
          MATCH     PTCNI10D,PTEDDTCD
          GOTO      ICDTYP90 IF NOT LESS    * Coding date >= ICD10 start date
          MOVE      "9",CODTYP
          GOTO      ICDTYP90
.
ICDTYP50  PACK      KEY16,AADMNO,SP10
          CALL      RSPTECO2                * Position on & read a patient
          CALL      RKPTECO2                  disease file record
          BRANCH    OVRCD,ICDTYP90
.
          MATCH     AADMNO,PTEOADMN
          GOTO      ICDTYP90 IF NOT EQUAL   * Different admin no
          MATCH     PTCNI10D,PTEODTCD
          GOTO      ICDTYP90 IF NOT LESS    * Coding date >= ICD10 start date
          MOVE      "9",CODTYP
.
ICDTYP90  IF        CODTYP=9
            IF        SHOWMAP=1
              WRITE     HTMLFILE;"ICD10 Coding (Mapped from ICD9)";
            ELSE
              WRITE     HTMLFILE;"ICD9 Coding";
            ENDIF
          ELSE
            IF        SHOWMAP=1
              WRITE     HTMLFILE;"ICD9 Coding (Mapped from ICD10)";
            ELSE
              WRITE     HTMLFILE;"ICD10 Coding";
            ENDIF
          ENDIF
ICDTYP99  RETURN
.------------------------------------------------------------
. Output Table of Future Bookings Details
.------------------------------------------------------------
BOKTAB00  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
          PACK      ENDDAT,CCC,CYY,CMM,CDD
          REP       " 0",ENDDAT
          MOVE      ZERO,VISCOUNT
          PACK      KEY36,URNUMBER,Z70
          CALL      RDSBOKA2
BOKTAB10  CALL      RDPBOKA2
          BRANCH    OVRCD,BOKTAB90
          MATCH     URNUMBER,OBAURNO
          GOTO      BOKTAB90 IF NOT EQUAL
          MATCH     ENDDAT,OBADATE
          GOTO      BOKTAB90 IF LESS
          ADD       ONE,VISCOUNT 
          IF        VISCOUNT=1
            WRITE     HTMLFILE;"<tr><td class=HeadingCell>Booking No.</td>":
                               "<td class=HeadingCell align=center>Date</td>":
                               "<td class=HeadingCell>Clinic</td>":
                               "<td class=HeadingCell>Clinic Type</td></tr>" 
          ENDIF
          UNPACK    OBADATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
.
          PACK      KEY12,OBASITE,OBACTYP,SP70
          CALL      RDCTYA1
.
          PACK      KEY20,OBASITE,OBACLIN,OBADATE,SP70
          CALL      RDCLIA1
          IF        OVRCD=1
            PACK      KEY20,OBASITE,OBACLIN,OBADATE,Z70
            CALL      RDSCLIA1
            CALL      RDPCLIA1
            BRANCH    OVRCD,BOKTAB10
.
            MATCH     OBASITE,OCASITE
            GOTO      BOKTAB10 IF NOT EQUAL
.
            MATCH     OBACLIN,OCACLIN
            GOTO      BOKTAB10 IF NOT EQUAL
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td><a href='javascript:linkPatient(#"":
                                URNUMBER,"#",#"",OBAOUTNO,"#",#"2#")'>":
                                OBAOUTNO,"</a></td>":
                             "<td>",CDAY,SP1,MTHNAM3,SP1,CYEAR,CCENT:
                             " at ",OBATIME,"</td>":
                             "<td>",OCADESC,"&nbsp;</td>":
                             "<td>",OCTDESC,"&nbsp;</td></tr>"
          GOTO      BOKTAB10
.
BOKTAB90  
BOKTAB99  RETURN
.------------------------------------------------------------
. Output Table of Waiting List Details
.------------------------------------------------------------
WATTAB00  
          PACK      ENDDAT,CCC,CYY,CMM,CDD
          REP       " 0",ENDDAT
          MOVE      ZERO,VISCOUNT
          MOVE      ZERO,WAITSTAT
WATTAB05  ADD       ONE,WAITSTAT
          PACK      KEY20,WAITSTAT,URNUMBER,Z70
          LOAD      STATDESC,WAITSTAT,WAITUNSH,WAITSCHD,WAITPREA
          CALL      RDSTREA2
WATTAB10  CALL      RDKTREA2
          BRANCH    OVRCD,WATTAB90
          COMPARE   WAITSTAT,WMSTAT
          GOTO      WATTAB20 IF NOT EQUAL
          MATCH     URNUMBER,WMURNO
          GOTO      WATTAB20 IF NOT EQUAL
          ADD       ONE,VISCOUNT 
          IF        VISCOUNT=1
            WRITE     HTMLFILE;"<tr><td class=HeadingCell>Procedure</td>":
                      "<td class=HeadingCell align=center>Date on List</td>":
                      "<td class=HeadingCell>Reason/Diagnosis</td>":
                      "<td class=HeadingCell>Status</td></tr>"
          ENDIF
          UNPACK    WMDATE2,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
.
          WRITE     HTMLFILE;"<tr><td><a href='javascript:linkWaiting(#"":
                                URNUMBER,"#",#"",WMCODE,"#",#"",WMCNT,"#")'>":
                                WMCODE,"</a></td>":
                             "<td>",CDAY,SP1,MTHNAM3,SP1,CYEAR,CCENT,"</td>":
                             "<td>",WMDESC,"&nbsp;</td>":
                             "<td>",STATDESC,"&nbsp;</td></tr>"
          GOTO      WATTAB10
.
WATTAB20  COMPARE   THREE,WAITSTAT
          GOTO      WATTAB05 IF NOT EQUAL
.
WATTAB90  
WATTAB99  RETURN
.
.
          INC       ALLREFIO/INC
          INC       PMSCCDIO/INC   * Concession Card Details
          INC       PMSUCHIO/INC
          INC       PMSUCDIO/INC
          INC       PATDGWIO/INC
          INC       PATDO1IO/INC
          INC       VISMDTIO/INC
          INC       VISMTXIO/INC
          INC       PATDADIO/INC
          INC       PATDSCIO/INC
          INC       PATHSPIO/INC
          INC       PATIN1IO/INC
          INC       PATITMIO/INC
          INC       PATMA1IO/INC
          INC       PATNIDIO/INC
          INC       PMSPX2IO/INC
          INC       PATMMBIO/INC
          INC       PATPR1IO/INC
          INC       PATASFIO/INC
          INC       PATMI1IO/INC
          INC       PATRE1IO/INC
          INC       PATTRNIO/INC
          INC       PATVISIO/INC
          INC       PATWR1IO/INC
          INC       PATFN1IO/INC
          INC       PATICOIO/INC
          INC       PATICDIO/INC
          INC       PATCMCIO/INC
.          INC       PATMOPIO/INC           * No longer reqd as per srf 14169
.          INC       PATMDIIO/INC           * No longer reqd as per srf 14169
          INC       PATECOIO/INC            * Added as per srf 14169
          INC       PATECDIO/INC            * Added as per srf 14169          
          INC       PATVADIO/INC
          INC       SCRMASIO/INC
          INC       PMSVX1IO/INC
          INC       PATDOCTM
          INC       PMSHCPTM
          INC       PMSHCGTM
          INC       PMSHCLTM
          INC       PATMASTM
          INC       CLPMSPX2
          INC       PMSPX2TM
          INC       PATMSXTM
          INC       PATVISTM
          INC       PATMISTM
          INC       PATDSCTM
          INC       NAMSTRCD
          INC       IBAWEBCD
          INC       CALCAGE
          INC       DGCLICUP
          INC       PMIGTNID
.
          INC       AAEDI1IO/INC
          INC       AAEDE1IO/INC
          INC       AAEMASTM
.
          INC       APSMASIO/INC
.
          INC       NHIMASTM    
          INC       NHIDCDIO/INC
          INC       NHIDMCIO/INC
          INC       NHIDNRIO/INC
          INC       NHIETHIO/INC
          INC       NHIMASIO/INC
          INC       NHIMCCIO/INC
          INC       NHIRELIO/INC
.          INC       PATMWSIO/INC
          INC       PATALRIO/INC
          INC       TMPALIIO/INC
.
          INC       OUTBOAIO/INC
          INC       OUTBB1IO/INC
          INC       OUTBOCIO/INC
          INC       OUTCANIO/INC
          INC       OUTCVTIO/INC
          INC       OUTDIAIO/INC
          INC       OUTSITIO/INC
          INC       OUTCTYIO/INC
          INC       OUTCLIIO/INC
          INC       OUTMA1IO/INC
          INC       OUTHEDIO/INC
          INC       OUTSESIO/INC
          INC       OUTXSCIO/INC
          INC       OUTSIPIO/INC
          INC       OUTBOATM  
          INC       VISCMTIO/INC
          INC       WATTR1IO/INC
          INC       RESLNKIO/INC
          INC       MRTMASIO/INC
          INC       OUTXWPIO/INC
          INC       OUTRSHIO/INC
          INC       ALLCASIO/INC
.
          INC       CLNHIMAS
          INC       CLOUTHED
          INC       DAYOFWEK
.
          INC       CLALLREF
          INC       CLPATMIS
          INC       CLPATDSC
          INC       NZHISWIO
.

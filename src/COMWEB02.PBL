.----------------------------------------------------------------------
. Program       : COMWEB02
.
. Function      : Emergency Flag Clear for Locked UR numbers
.
. Modifications : 
.
.----------------------------------------------------------------------
. V12.00.09 04/09/2025  J.Tan         TSK 0925576
.           Fixed Admissno for for Invoice Payment plan
. V12.00.08 28/08/2025  J.Tan         TSK 0925576
.           Fixed Admissno for for invoice Payment plan
. V12.00.07 26/08/2025  J.Tan         TSK 0925576
.           Fixed Admissno for for invoice Payment plan
. V12.00.06 18/08/2025  J.Tan         TSK 0925576
.           Mod for multiple future action records for Payment plan
. V12.00.05 04/07/2025  J.Tan         TSK 0963128
.           Fixed set Payment plan for Private Practice
. V12.00.04 23/06/2025  J.Tan         TSK 0961696
.           Mod to set Payment plan indicator on Delete Payment plan           
. V12.00.03 23/05/2025  Bella Turco   TSK 0925576
.           Recompiled for UPFAPPLN                                            
. V12.00.02 20/05/2025  Bella Turco   TSK 0925576
.           Recompiled for UPFAPPLN                                            
. V12.00.01 16/05/2025  Bella Turco   TSK 0955096
.           Alphanumeric changes
.           15/05/2025  Bella Turco   TSK 0925576
.           Fixed issues with Delete/Clear Payment Plan templates DELDBT00 and 
.           CLRDBT00
. V11.05.07 08/05/2025  Bella Turco   TSK 0925576
.           Added new tag TABLE140 for getting invoices for Delete Payment Plan
.           Recompiled for UPFAPPLN - Checking for invoice number
. V11.05.06 07/05/2025  Bella Turco   TSK 0925576
.           Added new error msg SETDBT92                                  
. V11.05.05 15/04/2025  Bella Turco   TSK 0925576
.           Delete Future Actions when deleting Payment Plan DELDBT00
. V11.05.04 15/04/2025  Bella Turco   TSK 0925576
.           New template functionality Delete Payment Plan DELDBT00 
. V11.05.03 25/03/2025  Bella Turco   TSK 0925576
.           Fixed "Payment Plan Indicator set/cleared" message SETDBT00/CLRDBT00
. V11.05.02 18/12/2024  J.Tan         TSK 0945920
.           Mod Clear Payment plan to set FA Completion date (Cat FA indc3=P)
.           03/02/2025  Bella Turco   TSK 0925760
.           Added functionality for new Payment Plan fields and set & clear
.           Payment Plan by Admission (SETDBT00)
. V11.05.01 02/12/2024  Thanh T       TSK 0925576
.           Recompiled for PATDETFD changes
. V11.02.01 09/02/2022  Thanh T       TSK 0905641
.           Recompiled as OUTMA1FD (PATMASTM) changes
. V11.01.01 05/03/2021  DAS            TSK 0902780
.           Added closing <tr> in school evaluation list 
. V11.00.01 09/12/2020  Thanh T        TSK 0872840
.           Added PMSCCDFD/PMSCCDIO as PMSPX2TM changes
. V10.13.01 31/07/2018  J.Tan          Task 0857465
.           Mod setup PTDESYST for Private practice
. V10.11.03 27/11/2017  Thanh Tieu     Task 0821710
.           Recompiled as PATMASTM changed
. V10.11.02 30/07/2017  Thanh T.       TSK 0821710
.           Audit admission/outpatient/UR comments. changed PATMASTM/PMSPX2TM.
. V10.11.01 14/07/2017  Thanh T.       TSK 0821710
.           Audit Amission/outpatient visit comments. PATMISTM changed.
. V10.05.01 29/08/2014  J.Tan             CAR 298984
.           Added new fields from patdetaf file(Amount to be repaid & Freq.paym)
. V10.04.05 03/07/2014  J.Tan             CAR 295681
.           Fixed to clear VISITTYP
. V10.04.04 10/04/2014  J.Tan             CAR 295681
.           Fixed Set Debt Collection Agency for PP
. V10.04.03 07/04/2014  Peter Vela      CAR 286258
.           Recompiled for PATMASTM
. V10.04.02 31.03.2014  B.G.Ackland  CAR 299363
.           Replace META Tag Redirect with Javascript in Common RDIREC00
.           Routine
. V10.04.01 05/03/2014  J.Tan           CAR 295681
.           Added SYSTEM to patdetaf file
.----------------------------------------------------------------------
. V10.03.03 03/06/2013  Steve Armstrong   CAR 286203
.           Fixed I*C on PATDETA2.
. V10.03.02 03/05/2012  J.Tan          CAR 262427
.           Changed 'Patient Plan' to 'Payment Plan'
. V10.03.01 16/01/2012  Sandra Barcham  CAR 256563
.           Recompiled for PATMASTM
.----------------------------------------------------------------------
. V10.02.04 11/10/2011  Mark Coupland  CAR 252429
.           Recompiled for PMSPX2TM and PATMASTM
. V10.02.03 13/09/2011 Jill Parkinson  CAR 248652
.           Recompiled for PMSPX2TM - Added read of pmsvx1af in GETCURIP
.
.        V10.02.02 20/04/2010  Mike Laming    CAR 233229
.                  Added UR Number (PTDEURNO) in PATDETFD (Key 2) - mods to 
.                  CLRDBT00 & PTDET000 to set Patient Debt Icons correctly
.        V10.02.01 12/04/2010  J.Tan          CAR 233229
.                  Fixed spelling of Collection
.        V10.01.01 22/11/2010  Mike Laming    CAR 233229
.                  Add Set/Clear routines for new Debt Indicators at ULURNO00
.                  Added Debt Alert variables to TABLE00
.        V9.11.01  21/01/2009  Mike Laming    CAR 188016
.                  Add Schools Address & Region Tags in IBAEDATM.PBL
.        V9.10.03  10/08/2008  Mike Laming    CAR 152286
.                  Add test for Referral No. at SCELST00 & EDALST00 for changes
.                  on 09/09/2008
.                  Add tag for Department Code from ALLREFFD on Admission No.
.        V9.10.02  16/07/2008  Mike Laming    CAR 152286
.                  Change read of IBASCEFD at SCELST00 as per changes 04/07/08
.        V9.10.01  20/05/2008  Mike Laming    CAR 152286
.                  Added reports 03 (RCH Education Assessment - IBAEDAFD etc)
.                  and 04 (RCH School Evaluation - IBASCEFD etc)
.        V9.08.04  31/01/2007  Peter Vela     CAR 127930
.                  Recompiled for MRTMASFD
.        V9.08.03  03/01/2007  Jill Habasque CAR 120004
.                  Fixed unlocking of ur number
.        V9.08.02  17/10/2006  Davin         CAR 76341
.                  Added option 2 - Health Assessment Details
.        V9.08.01  28/09/2006  Jill Habasque CAR 119579
.                  Mods to set bad debt indicator
.        V9.05.01  09/03/2006  Ebon Clements CAR 78533
.                  Mods for PATCODTM - Hospital specific category codes
.        V9.05.B02 30/01/2006  Ebon Clements CAR 93186
.                  Mods for REDIRECT length change. Recompiled for IBAWEBDF
.        V9.05.B01 12/01/2006  Peter Vela    CAR 90085
.                  Added clear Bad Debt indicator functionality
.        V9.04.01  01/03/2005  Peter Vela    CAR 57547
.                  Recompiled for PATVADFD and PATVADIO
.        V9.03.03  07/06/2004 Peter Vela CAR 49016
.                  2004 Statutory Changes - recompiled for PMSPX2TM
.        V9.03.02  26/03/2004 Peter Vela CAR 13038
.                  Recompiled for PATDOCCD - Doctor Name format Parameter
.        V9.03.01  02/07/2003 Jill Habasque CAR 40660
.                  Removed PATDSCTM
.        V9.02.00  01/04/2003  Tonii SRS 19275
.                  Created Server
.----------------------------------------------------------------------
          INC       IBAWEBDF
          INC       WEBDATDF
.
          INC       IBAASSFD/INC
          INC       IBAASSTD/INC
          INC       IBAEDAFD/INC   * Education Needs Assesment
          INC       IBAEDATD/INC
          INC       IBASCEFD/INC   * School Evaluation form
          INC       IBASCETD/INC
          INC       ALLREFFD/INC   * Allied Health Referral table
          INC       PATDO1FD/INC   * Doctor File
          INC       PATMI1FD/INC
          INC       PATMA1FD/INC
          INC       PATVISFD/INC   * Patients Visits File
          INC       PMSVX1FD/INC   * Visit Extension File
          INC       PATCONFD/INC
          INC       PATEORFD/INC   * External Organisation table
          INC       PATALRFD/INC
          INC       OUTSITFD/INC
          INC       PMSUCHFD/INC
          INC       PMSUCDFD/INC
          INC       PATMASTD/INC
          INC       PATHSPFD/INC
          INC       PATCFAFD/INC
          INC       PATPR1FD/INC
          INC       PATFACFD/INC
          INC       PMSPX2FD/INC
          INC       PATFN1FD/INC
          INC       PATWR1FD/INC
          INC       MRTMASFD/INC
          INC       NHIMASFD/INC
          INC       NHIETHFD/INC
          INC       PATCALAG/INC
          INC       PMSPX2TD/INC
          INC       PATRE1FD/INC
          INC       PATDGWFD/INC
          INC       PATITMFD/INC
          INC       PATDADFD/INC
          INC       PATVADFD/INC
          INC       PATCMCFD/INC
          INC       PATASFFD/INC
          INC       PATDETFD/INC                                      *I-233229
          INC       PMSHCGTD/INC
          INC       PMSHCGFD/INC
          INC       PMSHCPFD/INC
          INC       PMSHCPTD/INC
          INC       APSMASFD/INC
          INC       PATIN1FD/INC
          INC       PATDSCFD/INC
          INC       PATINVFD/INC
          INC       PRIINVFD/INC
          INC       PMSCCDFD/INC            * Concession Card Details
.
          INC       RCPBNKFD/INC
          INC       RCPDTEFD/INC
.
.----------------------------------------------------------------------
NEXTPAGE  DIM       1       * Nextpage parameter CGI parameter
UPDTTYPE  DIM       1       * Update Type
STARTKEY  DIM       10      * Starting Key
CURRDATE  DIM       8       * Current Date
DATFIELD  DATE      8
DIM12     DIM       12
DIM15     DIM       15
FORM8     FORM      8 
FORM3     FORM      3 
FORM12P2  FORM      12.2
F12P2     FORM      12.2
DOCTCODE  DIM       8
DEBTTYPE  DIM       1
DEBTAGNT  DIM       8
ASSMCNTR  DIM       8
HLTHCNTR  DIM       8
INVOICEN  DIM       8 
PERIODPY  FORM      12.2
OUTSTAND  FORM      12.2
NUMREPAY  FORM      5.2
FRMREPAY  FORM      5       * Repayment Frequency
FREQRPAY  DIM       3
PAYSTART  DIM       8
LASTPYDT  DIM       8
FUACTION  DIM       3
PYMTPLAN  DIM       1
PSAGETYP  DIM       3
MBSDESC   DIM       70
OUTSAMNT  FORM      12.2
FORM122   FORM      12.2
SAVAMNT   FORM      12.2
SAVADMNO  DIM       8
PSAGECON  DIM       3
ALCNRAUD  DIM       1
DIM6      DIM       6
DIM10     DIM       10
DIM11     DIM       11
DIM50     DIM       50
SVKEY1    DIM       1
VISITTYP  DIM       1
.
CATEF     INIT      "EF"
PATONLYC  INIT      "Patient Only Debt Indicator cleared."
PATPLANC  INIT      "Payment Plan Indicator cleared."
DBTCOLLC  INIT      "Debt Collection Agency Indicator cleared."
PATONLYS  INIT      "Patient Only Debt Indicator set."
PATPLANS  INIT      "Payment Plan Indicator set."
DBTCOLLS  INIT      "Debt Collection Agency Indicator set."
DOLLAR    INIT      "$"
.----------------------------------------------------------------------
PRGID     INIT      "COMWEB02"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "UR Emergency Flag Clear"
.----------------------------------------------------------------------
.
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200,MAIN1300,MAIN1400
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "Invalid Option",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  CALL      ULURNO00        * Clear UR flag          
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1200  CALL      HLTHAS00        * Health Assessment Details
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1300  CALL      EDUASS00        * Education Needs Assessment Details
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1400  CALL      SCHEVL00        * School Evaluation Details
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
.------------------------------------------------------------
. Output Next Page Based on CGI Parameter nextpage
.------------------------------------------------------------
NXTPAG00  MOVE      ZERO,F1
          MOVE      NEXTPAGE,F1
          BRANCH    F1,NXTPAG10,NXTPAG20,NXTPAG30,NXTPAG40
.
          CALL      WINCLS00
          GOTO      NXTPAG99
.
NXTPAG10  CALL      RDIREC00
          GOTO      NXTPAG99
.
NXTPAG20  CALL      GOBACK00      * Go Back 1 Html page
          GOTO      NXTPAG99
.
NXTPAG30  CALL      DAH20000      * Go Back 2 html pages
          GOTO      NXTPAG99
.
NXTPAG40  CALL      PREDIR00      * Redirect Parent
          GOTO      NXTPAG99
.
NXTPAG99  RETURN
.------------------------------------------------------------------------------
.  Redirect Parent URL
.------------------------------------------------------------------------------
PREDIR00  CALL      OPENHTML
          BRANCH    EXIT,PREDIR90
.
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "alert(#"",WEBTITLE,"#");":
                             " parent.location.href=#"",REDIRECT,"#";":
                             "}":
                             "</script>"
          CALL      CLOSHTML
          GOTO      PREDIR99
.
PREDIR90  DISPLAY   "*** Fifo Not Found ***"
.
PREDIR99  RETURN
.-------------------------------------------------------------------------------
.  Goes back 1 page 
.-------------------------------------------------------------------------------
GOBACK00  CALL      OPENHTML
          BRANCH    EXIT,GOBACK99
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "    alert(#"",WEBTITLE,"#");":
                             "    self.close();":
                             "    opener.history.go(-1);":
                             "}":
                             "</script>"
          CALL      CLOSHTML
GOBACK99  RETURN
.------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------
INIT0000  CALL      INTMAS00     * Open routine for PATMASTM 
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
          OPEN      PATHSPA1,"pathspaf"
          OPEN      PATVISA2,"patvisaf"
          OPEN      PATDGWA1,"patdgwaf"
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATDETA1,"patdetaf"                               *I-233229
          OPEN      PATDETA2,"patdetaf"                               *I-286203
          OPEN      PATFACT1,"patfactf"
          OPEN      PATCFAA1,"patcfaaf"
          OPEN      PATINVR1,"patinvrf"
          OPEN      PATFN1A1,"patfn1af"
          OPEN      PATINVR3,"patinvrf"
          OPEN      PRIINVO3,"priinvof"
          OPEN      PMSCCDA1,"pmsccdaf"
          OPEN      PATMI1A1,"patmi1af"
.
          OPEN      IBAASSA1,"ibaassaf"
          OPEN      ALLREFA1,"allrefaf"     
.
          OPEN      RCPDTEA6,"rcpdteaf"     
          OPEN      RCPBNKA1,"rcpbnkaf"     
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          IF        IBCNMHOS=1
            OPEN      MLTCODA1,"mltcodaf"
            OPEN      MLTCODA2,"mltcodaf"
          ENDIF
.
INIT9999  RETURN
.------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          MOVE      SP70,TEMPLATE
          MOVE      SP70,NEXTPAGE
          PACK      REDIRECT,SP70,SP70,SP70,SP70
          MOVE      SP70,STARTKEY
          MOVE      SP70,UPDTTYPE
          MOVE      SP70,ASSMCNTR
          MOVE      SP70,HLTHCNTR
          MOVE      SP70,VISITTYP
          MOVE      SP70,INVOICEN
          MOVE      ZERO,PERIODPY
          MOVE      ZERO,OUTSTAND
          MOVE      ZERO,NUMREPAY
          MOVE      SP70,FREQRPAY
          MOVE      SP70,PAYSTART
          MOVE      SP70,LASTPYDT
          MOVE      SP70,FUACTION
          CALL      CPIBAS00
          CALL      CPIBEA00
          CALL      CPIBSE00
CLRPAR99  RETURN
.------------------------------------------------------------
. Set Parameters
.------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updttype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDTTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "urnumber=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,URNUMBER
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "admissno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ADMISSNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "invoicen=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,INVOICEN
            RJUSTIFY  INVOICEN
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "debttype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,DEBTTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "debtagnt=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,DEBTAGNT
            GOTO      SETPAR99
          ENDIF
.  
          MATCH     "visittyp=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,VISITTYP
            GOTO      SETPAR99
          ENDIF
.  
          MATCH     "nextpage=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NEXTPAGE
            GOTO      SETPAR99
          ENDIF 
.
          MATCH     "redirect=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REDIRECT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "assmcntr=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ASSMCNTR
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "hlthcntr=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,HLTHCNTR
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "ibass",QRYNAME
          IF        @EQUAL
            CALL      SPIBS000
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "ibeda",QRYNAME         * Education Needs Assessment
          IF        @EQUAL
            CALL      SPIEA000
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "ibsce",QRYNAME         * School Evaluation form    
          IF        @EQUAL
            CALL      SPISE000
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "periodpy=",QRYNAME
          IF        @EQUAL
            SQUEEZE   QRYDATA
            MOVE      QRYDATA,PERIODPY
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "outstand=",QRYNAME
          IF        @EQUAL
            SQUEEZE   QRYDATA
            MOVE      QRYDATA,OUTSTAND
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "numrepay=",QRYNAME
          IF        @EQUAL
            SQUEEZE   QRYDATA
            MOVE      QRYDATA,NUMREPAY
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "freqrpay=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FREQRPAY
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "paystart=",QRYNAME
          IF        @EQUAL
            RESET     QRYDATA
            MATCH     SP70,QRYDATA
            GOTO      SETPAR99 IF EQUAL
            UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
            CALL      SETMTH00            * Set CMON from MTHNAM3
            PACK      PAYSTART,CCENT,CYEAR,CMON,CDAY
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "lastpydt=",QRYNAME
          IF        @EQUAL
            RESET     QRYDATA
            MATCH     SP70,QRYDATA
            GOTO      SETPAR99 IF EQUAL
            UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
            CALL      SETMTH00            * Set CMON from MTHNAM3
            PACK      LASTPYDT,CCENT,CYEAR,CMON,CDAY
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "fuaction=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FUACTION
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "pymtplan=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,PYMTPLAN
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
.------------------------------------------------------------
. Input U/R Number
.------------------------------------------------------------
ULURNO00  MOVE      URNUMBER,KEY8   * Read Master File
          CALL      RDMAST1
          BRANCH    OVRCD,ULURNO91
.
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          IF        OVRCD=1
            CALL      CLPMSPX2
          ENDIF
.
          MATCH     SP70,UPDTTYPE
          GOTO      ULURNO80 IF EQUAL
.
          MATCH     "U",UPDTTYPE        * Update (unlock UR record)
          GOTO      ULURNO10 IF EQUAL
.
          MATCH     "N",UPDTTYPE        * Clear Bad debt indicator
          GOTO      ULURNO20 IF EQUAL
.
          MATCH     "S",UPDTTYPE        * Set the Bad debt indicator
          GOTO      ULURNO30 IF EQUAL
.                                                            * start  *I-233229
          MATCH     "P",UPDTTYPE        * Set Patient Debt indicators
          GOTO      ULURNO40 IF EQUAL
.
          MATCH     "Q",UPDTTYPE        * Clear Patient Debt indicators
          GOTO      ULURNO50 IF EQUAL                        * end    *I-233229
.
          MATCH     "R",UPDTTYPE        * Delete Patient Debt indicators
          GOTO      ULURNO60 IF EQUAL
.
          MOVE      "Invalid Update Type",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ULURNO99
.
ULURNO10  MATCH     SP2,PPORT
          GOTO      ULURNO92 IF EQUAL
.
          PACK      KEY8,URNUMBER
          CALL      UPMAST1         * Update UR record to clear port 
          MOVE      ZERO,EXIT
          GOTO      ULURNO99
.
ULURNO20  COMPARE   ZERO,PBDEBT
          GOTO      ULURNO93 IF EQUAL
.
          MOVE      ZERO,PBDEBT
.
          PACK      KEY8,URNUMBER
          CALL      UPMAST1
          MOVE      ZERO,EXIT
.
          CLEAR     WEBTITLE
          MOVE      "Bad Debt Cleared.",WEBTITLE
          RESET     WEBTITLE
          MOVE      ZERO,EXIT
          GOTO      ULURNO99
.
ULURNO30  COMPARE   ONE,PBDEBT
          GOTO      ULURNO94 IF EQUAL
.
          MOVE      ONE,PBDEBT
.
          PACK      KEY8,URNUMBER
          CALL      UPMAST1
          MOVE      ZERO,EXIT
.
          CLEAR     WEBTITLE
          MOVE      "Bad Debt Set.",WEBTITLE
          RESET     WEBTITLE
          MOVE      ZERO,EXIT
.
          GOTO      ULURNO99
.
ULURNO40  CALL      SETDBT00                * set Patient debt ind. & "patdetaf"
          GOTO      ULURNO99
.
ULURNO50  CALL      CLRDBT00                * Clear Patient debt in "patdetaf"
          CALL      CFAC0000                * Clear Future Action
          GOTO      ULURNO99
.
ULURNO60  CALL      DELDBT00                * Delete Payment Plan 
          GOTO      ULURNO99
.
.
ULURNO80  MOVE      URNUMBER,KEY8   * Read Master File
          CALL      RDMAST1
          BRANCH    OVRCD,ULURNO91
.
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          IF        OVRCD=1
            CALL      CLPMSPX2
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "U/R Cleared",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,ULURNO99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      ULURNO99
.
ULURNO90  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ULURNO99
.
ULURNO91  MOVE      "Can't find patient Master Details. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ULURNO99
.
ULURNO92  MOVE      "U/R Number is not busy. Not cleared. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ULURNO99
.
ULURNO93  MOVE      "Patient does not have bad debt. Not cleared.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ULURNO99
.
ULURNO94  MOVE      "Patient already has the bad debt indicator set.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ULURNO99
.
ULURNO99  RETURN
.------------------------------------------------------------
. Clear Patient Debt indicators
.------------------------------------------------------------
CLRDBT00  CALL      CLPATDET                * clear Patient Debt Indicators
          MOVE      "4",PTDESYST            * Set to private practice
.
          MATCH     "4",PYMTPLAN            * payment plan for Admission ?
          IF        @EQUAL
            MOVE      ADMISSNO,KEY8
            CALL      RDVISA1
            IF        OVRCD=0
              MOVE      PVITYPE,PTDESYST    * Set System for Admission
              GOTO      CLRDBT05
            ENDIF
          ENDIF
.
          MOVE      SP70,KEY8
          MOVE      INVOICEN,KEY8           * payment plan for Invoice?
          CALL      RDINV1
          IF        OVRCD=0
            MATCH     URNUMBER,PINVUR
            IF        @EQUAL
              MOVE      PINVSYS,PTDESYST    * Set System
            ENDIF
          ENDIF
.
CLRDBT05  PACK      KEY10,DEBTTYPE,INVOICEN,PTDESYST,SP10
          CALL      RDPTDET1                * read "patdetaf"
          IF        OVRCD = 0        
            PACK      PTDEDTCL,CCC,CYY,CMM,CDD * set "Date Debt Cleared"
            REP       " 0",PTDEDTCL
            MATCH     "3",DEBTTYPE          * clear Debt Collection Agenct
            IF        @EQUAL
              MOVE      SP3,PTDEAGNT
            ENDIF     
            PACK      PTDEUDAT,CCC,CYY,CMM,CDD
            REP       " 0",PTDEUDAT
            CLOCK     TIME,PTDEUTIM
            MOVE      USERID,PTDEUUID
            CALL      UPPTDET1
          ELSE
            CLEAR     DIM50                 * (should never happen)
            MATCH     "4",PYMTPLAN
            IF        @EQUAL
              APPEND    "Admission ",DIM50
            ELSE
              APPEND    "Invoice ",DIM50
            ENDIF
            APPEND    INVOICEN,DIM50
            APPEND    " is not an Indicated record",DIM50
            RESET     DIM50
            GOTO      CLRDBT80
          ENDIF
.
          CALL      PYINDC00                * Payment plan indicator
.
          MATCH     "1",KEY1
          IF        @EQUAL
            MOVE      "Indicator not reset - other Debts exist",DIM50
          ELSE
            LOAD      DIM50,FORM1,PATONLYC,PATPLANC,DBTCOLLC,PATPLANC
          ENDIF
.
CLRDBT80  CLEAR     WEBTITLE
          MOVE      DIM50,WEBTITLE
          RESET     WEBTITLE
          MOVE      ZERO,EXIT
.
CLRDBT99  RETURN
+
.------------------------------------------------------------
. Delete Patient Debt indicators
.------------------------------------------------------------
DELDBT00  CALL      CLPATDET                * clear Patient Debt Indicators
          MOVE      "4",PTDESYST            * Set to private practice
.
          MATCH     "4",PYMTPLAN            * Payment plan Admission ?
          IF        @EQUAL
            MOVE      ADMISSNO,KEY8
            CALL      RDVISA1
            IF        OVRCD=0
              MOVE      PVITYPE,PTDESYST    * Set System for Admission
              GOTO      DELDBT05
            ENDIF
          ENDIF
.
          MOVE      SP70,KEY8
          MOVE      INVOICEN,KEY8
          CALL      RDINV1
          IF        OVRCD=0
            MATCH     URNUMBER,PINVUR
            IF        @EQUAL
              MOVE      PINVSYS,PTDESYST    * Set System
              MOVE      PINVADM,ADMISSNO
            ENDIF
          ENDIF
.
DELDBT05  PACK      KEY10,DEBTTYPE,INVOICEN,PTDESYST,SP10
          CALL      RDPTDET1
          BRANCH    OVRCD,DELDBT95
.
          CALL      DEPTDET1
.
          MATCH     "4",PTDESYST            * Private practice ?
          GOTO      DELDBT20 IF EQUAL
.
.delete future actions
.
          PACK      KEY19,ADMISSNO,Z70
DELDBT08  CALL      RDSFACT1
DELDBT10  CALL      RDPFACT1
          BRANCH    OVRCD,DELDBT20
          MATCH     ADMISSNO,DFADMNO
          GOTO      DELDBT20 IF NOT EQUAL
.
          MATCH     "4",DEBTTYPE                 * Admission Payment Plan
          IF        @EQUAL
            MATCH     SP70,PTFCINVN
            GOTO      DELDBT12 IF EQUAL          * Admission FA
          ENDIF
.
          MATCH     "2",DEBTTYPE                 * Invoice Payment Plan
          IF        @EQUAL
            MATCH     PTFCINVN,PTDEINVN
            GOTO      DELDBT12 IF EQUAL          * Invoice FA
          ENDIF
.
          GOTO      DELDBT10
.
DELDBT12  MATCH     FACODE,SP70
          GOTO      DELDBT10 IF EQUAL
.
          MOVE      "FA",CATEGORY
          MOVE      FACODE,CODE
          CALL      GETCOD00
.
          MATCH     "P",TCINDC3
          GOTO      DELDBT10 IF NOT EQUAL
          PACK      KEY19,DFADMNO,FADATE,FACODE,SP70
          CALL      DEFACT1                        * Delete Future Action
          GOTO      DELDBT08                       * next record
.
.         Check if there is any other Debt invoices
.
DELDBT20  CALL      PYINDC00                * Payment plan indicator
          GOTO      DELDBT97
.
DELDBT95  CLEAR     WEBTITLE
          MOVE      "Payment Plan could not be deleted",WEBTITLE
          RESET     WEBTITLE
          MOVE      ZERO,EXIT
          GOTO      DELDBT99 
.
DELDBT97  CLEAR     WEBTITLE
          MOVE      "Payment Plan deleted",WEBTITLE
          RESET     WEBTITLE
          MOVE      ZERO,EXIT
          GOTO      DELDBT99
.
DELDBT99  RETURN
+
.------------------------------------------------------------
.         Check if Payment plan indicator need to be set
.         Patient Debt 2 = Payment Plan Invoice
.                      4 = Payment Plan Admission
.------------------------------------------------------------
PYINDC00  MOVE      ZERO,KEY1               * check for other Debt Invoices
          MOVE      ZERO,F1
          MOVE      DEBTTYPE,F1             * Patient Debt type
          IF        F1=2 | F1=4
            PACK      KEY18,URNUMBER,SP70
          ELSE
            PACK      KEY18,URNUMBER,DEBTTYPE,SP20
          ENDIF
          CALL      RSPTDET2
PYINDC10  CALL      RKPTDET2
          BRANCH    OVRCD,PYINDC30
.
          MATCH     URNUMBER,PTDEURNO
          GOTO      PYINDC30 IF NOT EQUAL   * finish if not current UR No.
.
          IF        F1=2 | F1=4
            MATCH     "2",PTDETYPE
            GOTO      PYINDC20 IF EQUAL       * Payment Plan Invoice
            MATCH     "4",PTDETYPE
            GOTO      PYINDC20 IF EQUAL       * Payment Plan Admission
            GOTO      PYINDC10
          ELSE
            MATCH     DEBTTYPE,PTDETYPE
            GOTO      PYINDC30 IF NOT EQUAL
          ENDIF
.
PYINDC20  MATCH     SP8,PTDEDTCL
          GOTO      PYINDC10 IF NOT EQUAL   * bypass if already cleared
.
          MOVE      "1",KEY1                * must be another Invoice -
.
PYINDC30  MOVE      DEBTTYPE,FORM1
          LOAD      SVKEY1,FORM1,PMPXSN12,PMPXSN13,PMPXSN14,PMPXSN13
          MATCH     SVKEY1,KEY1
          GOTO      PYINDC99 IF EQUAL
.
          STORE     KEY1,FORM1,PMPXSN12,PMPXSN13,PMPXSN14,PMPXSN13
          CALL      UPPMPX21                * update the indicator
          MOVE      URNUMBER,PINVUR
          PROC      FLAGDEBT                * reset Debt Indicators
.
PYINDC99  RETURN
+
.------------------------------------------------------------
. Set Date Completed Future action after Clear Payment Plan
.------------------------------------------------------------
CFAC0000   MOVE     ADMISSNO,DPINVADM        * admission number
           MOVE     PTDESYST,DPINVSYS        * system
.
           MATCH    "4",PYMTPLAN
           IF       @EQUAL
             MOVE     SP70,PINVNO
           ELSE
             MOVE     INVOICEN,PINVNO        * Invoice number
           ENDIF
          CALL      UPFAPPLN                 * Update FA Completion date
.
CFAC9999  RETURN
+
.------------------------------------------------------------
. Set Patient Debt indicators
.------------------------------------------------------------
SETDBT00  CALL      CLPATDET                * set Patient Debt Indicators
          MOVE      "4",PTDESYST            * set to Private Practice
          MOVE      ADMISSNO,SAVADMNO       * save current admission
.
          MATCH     "4",PYMTPLAN            * Admission Payment plan
          IF        @EQUAL
            MOVE      ADMISSNO,KEY8
            CALL      RDVISA1
            IF        OVRCD=0
              MOVE      PVITYPE,PTDESYST    * Set System for Admission
              GOTO      SETDBT10
            ENDIF
          ENDIF
.
          MATCH     "4",VISITTYP            * Private Practice ?
          IF        !@EQUAL
            MOVE      INVOICEN,KEY8
            CALL      RDINV1
            IF        OVRCD=0
              MOVE      PINVSYS,PTDESYST    * Set System
              MOVE      PINVADM,ADMISSNO    * use the admission of invoice
            ENDIF
          ENDIF
.
.         DEBTTYPE=2 - Invoice payment plan, PTDEINVN= invoice number
.         DEBTTYPE=4 - Admission payment plan, PTDEINVN= admission number
.
SETDBT10  PACK      KEY10,DEBTTYPE,INVOICEN,PTDESYST,SP10
          CALL      RDPTDET1                * read "patdetaf"
          BRANCH    OVRCD,SETDBT20
.
.         record exists
.
          MATCH     "4",PTDETYPE          * Payment plan Admission
          IF        @EQUAL
            MATCH     PTDEINVN,ADMISSNO
            GOTO      SETDBT90 IF EQUAL
          ELSE
            MATCH     "2",PTDETYPE        * Payment plan Invoice
            IF        @EQUAL
              MATCH     PTDEINVN,INVOICEN
              GOTO      SETDBT91 IF EQUAL
            ENDIF
          ENDIF
.
          MOVE      SP8,PTDEDTCL          * clear "Date Debt Cleared"
          MATCH     "3",DEBTTYPE          * set Debt Collection Agenct
          IF        @EQUAL
            MOVE      DEBTAGNT,PTDEAGNT
          ENDIF
          MATCH     SP3,DEBTAGNT          * assigned to Debt Collect.Agency ?
          IF        @EQUAL
            MOVE      CURRDATE,PTDEDTCL
          ENDIF
          MOVE      PERIODPY,PTDERPAY
          MOVE      FREQRPAY,PTDEFREQ
.
          MATCH     "4",PTDESYST            * Private Practice ?
          IF        !@EQUAL
            MOVE      PAYSTART,PTDESDAT
            MOVE      LASTPYDT,PTDEEDAT
            MOVE      NUMREPAY,PTDENOFP
            MOVE      FUACTION,PTDECOD1
          ENDIF
.
          PACK      PTDEUDAT,CCC,CYY,CMM,CDD
          REP       " 0",PTDEUDAT
          CLOCK     TIME,PTDEUTIM
          MOVE      USERID,PTDEUUID
          CALL      UPPTDET1
.
          GOTO      SETDBT30
.
.         New record
.
SETDBT20  MOVE      DEBTTYPE,PTDETYPE
          MOVE      INVOICEN,PTDEINVN
          MATCH     "3",DEBTTYPE          * set Debt Collection Agenct
          IF        @EQUAL
            MOVE      DEBTAGNT,PTDEAGNT
          ENDIF
.
          MOVE      PERIODPY,PTDERPAY
          MOVE      FREQRPAY,PTDEFREQ
.
.         Check if Future Action code already exists
.
          MATCH     "4",PTDESYST            * Private Practice ?
          GOTO      SETDBT24 IF EQUAL
.
.         PACK      KEY19,ADMISSNO,PAYSTART,FUACTION,SP70
.         CALL      RDFACT1
.         IF        OVRCD<>1
.           GOTO      SETDBT92
.         ENDIF
.
          MOVE      PAYSTART,PTDESDAT
          MOVE      LASTPYDT,PTDEEDAT
          MOVE      NUMREPAY,PTDENOFP
          MOVE      FUACTION,PTDECOD1
.
SETDBT24  PACK      PTDECDAT,CCC,CYY,CMM,CDD
          REP       " 0",PTDECDAT
          CLOCK     TIME,PTDECTIM
          MOVE      USERID,PTDECUID
          MOVE      URNUMBER,PTDEURNO                               *I-233229
          CALL      WRPTDET1
.
SETDBT30  CALL      SETFA000              * write Future Action patfactf
          MOVE      SAVADMNO,ADMISSNO     * restore the Admission no
.
          MOVE      DEBTTYPE,FORM1
          LOAD      DIM50,FORM1,PATONLYS,PATPLANS,DBTCOLLS,PATPLANS
.                                           * set relevant Patient Debt Indicat.
          LOAD      KEY1,FORM1,PMPXSN12,PMPXSN13,PMPXSN14,PMPXSN13
          MATCH     "1",KEY1
          GOTO      SETDBT60 IF EQUAL  
          MOVE      "1",KEY1
          STORE     KEY1,FORM1,PMPXSN12,PMPXSN13,PMPXSN14,PMPXSN13
.
          CALL      UPPMPX21
          MOVE      PMPXURNO,PINVUR
          PROC      FLAGDEBT                * reset Debt Indicators
.
SETDBT60  CLEAR     WEBTITLE
          MOVE      DIM50,WEBTITLE
          RESET     WEBTITLE
          MOVE      ZERO,EXIT
          GOTO      SETDBT99
.
SETDBT90  CLEAR     WEBTITLE
          MOVE      "This Admission is already on a Payment Plan",WEBTITLE
          RESET     WEBTITLE
          MOVE      ZERO,EXIT
          GOTO      SETDBT99
.
SETDBT91  CLEAR     WEBTITLE
          MOVE      "This Invoice is already on a Payment Plan",WEBTITLE
          RESET     WEBTITLE
          MOVE      ZERO,EXIT
          GOTO      SETDBT99
.
SETDBT92  CLEAR     WEBTITLE
          MOVE      "Future Action already exists for this date & FA code",WEBTITLE
          RESET     WEBTITLE
          MOVE      ZERO,EXIT
          GOTO      SETDBT99
.
SETDBT99  RETURN
+
.------------------------------------------------------------
. Write Future Action for Payment Plan
.------------------------------------------------------------
SETFA000  MATCH     "4",PTDESYST            * Private Practice
          GOTO      SETFA999 IF EQUAL       * No Future action plan for PP
.
          MOVE      NUMREPAY,FRMREPAY       * Repayment Frequency
          MOVE      "FP",KEY2
          PACK      KEY5,KEY2,PTDEFREQ
          CALL      RDCODE1
          BRANCH    OVRCD,SETFA999
          MOVE      TCFORM6,F5              * No of days for Repayment
.
.         Get days of Repayment frequency
.
          MOVE      ZERO,OUTSAMNT
          CALL      GOST0000                *  Get O/S amt for an Inv./Adm
.
.         Write one Future Action record for each date of the Payment required
.
          MOVE      PAYSTART,D8                       * Future Action date
          MOVE      SP70,DIM15
          MOVE      PTDERPAY,DIM15                    * payment amount
.
SETFA100  PACK      KEY19,ADMISSNO,D8,FUACTION,SP70
          CALL      RDFACT1
          BRANCH    OVRCD,SETFA300
.
.         DEBTTYPE=2 - Invoice payment plan
.         DEBTTYPE=4 - Admission payment plan
.
          MOVE      SP70,PTFCINVN
          MATCH     "2",DEBTTYPE
          IF        @EQUAL
            MOVE      INVOICEN,PTFCINVN               * Invoice No
          ENDIF
.
          CALL      SETOG000                          * Occupational Group
          MOVE      AFUNDH,PTFCHFND                   * Health Fund
          MOVE      SP70,PTFCDTCP                     * Date completed
.
          SQUEEZE   DIM15
          PACK      FACOMM,DOLLAR,DIM15,SP70          * Comments
.
          MOVE      USERID,PTFCUUID                   * User ID Updated
          PACK      PTFCUDAT,CCC,CYY,CMM,CDD          * Date Updated
          REP       " 0",PTFCUDAT
          CLOCK     TIME,PTFCUTIM                     * Time Updated
.
          CALL      UPFACT1
          GOTO      SETFA500
.
SETFA300  CALL      CLPATFAC                        * clear patfactf fields
          MOVE      ADMISSNO,FADMNO                   * Admission No
          MOVE      FUACTION,FACODE                   * Future Action Code
          MOVE      URNUMBER,PTFCURNO                 * UR Number
.
          CALL      SETOG000                          * Occupational Group
          MOVE      AFUNDH,PTFCHFND                   * Health Fund
.
.         DEBTTYPE=2 - Invoice payment plan
.         DEBTTYPE=4 - Admission payment plan
.
          MOVE      SP70,PTFCINVN
          MATCH     "2",DEBTTYPE
          IF        @EQUAL
            MOVE      INVOICEN,PTFCINVN               * Invoice No
          ENDIF
.
          MOVE      D8,FADATE                         * Future Action Date
          SQUEEZE   DIM15
          PACK      FACOMM,DOLLAR,DIM15,SP70          * Comments
.
          MOVE      USERID,PTFCCUID                   * User ID Created
          PACK      PTFCCDAT,CCC,CYY,CMM,CDD          * Date Created
          REP       " 0",PTFCCDAT
          CLOCK     TIME,PTFCCTIM                     * Time Created
.
          PACK      KEY19,FADMNO,FADATE,FACODE,SP70
          CALL      WRFACT1
.
.         Set next payment date as the Future action date
.
SETFA500  SUB       PTDERPAY,OUTSAMNT
          SUB       ONE,FRMREPAY        * reduce no of payments required
.
          COMPARE   ZERO,FRMREPAY
          GOTO      SETFA999 IF EQUAL
          COMPARE   TWO,FRMREPAY
          GOTO      SETFA600 IF LESS    * last payment
.
          MOVE      D8,DATFIELD
          ADD       F5,DATFIELD         * add No of payments required
          MOVE      DATFIELD,D8         * next future action date
          MOVE      PTDERPAY,DIM15
.
          GOTO      SETFA100
.
SETFA600  MOVE      LASTPYDT,D8         * Last date
          MOVE      OUTSAMNT,DIM15
          GOTO      SETFA100
.
SETFA999  RETURN
+
.------------------------------------------------------------
. Get Outstanding amount for an Invoice/Admission
.------------------------------------------------------------
GOST0000  MOVE      ZERO,F1                 * flag for Admission
.
          MATCH     "4",DEBTTYPE            * Payment plan Admission ?
          GOTO      GOST4000 IF EQUAL
          MATCH     "2",DEBTTYPE            * Payment plan Invoice ?
          GOTO      GOST9999 IF NOT EQUAL
.
.         Invoice payment plan
.
          MOVE      ONE,F1                 * flag for an invoice only
.
.         Admission payment plan
.
GOST4000  CALL      ADOUST00                * Outstanding amount for Admission
.
GOST9999  RETURN
+
.------------------------------------------------------------
. Get Occupational Group for Future Action
.------------------------------------------------------------
SETOG000  PACK      KEY8,ADMISSNO,SP70
          CALL      RDMISS1
          BRANCH    OVRCD,SETOG999
.
          PACK      KEY3,ACLAIM,SP70
          CALL      RDPTCFA1                        * read patcfaaf
          BRANCH    OVRCD,SETOG100
          MATCH     SP70,PTCFOCGR
          GOTO      SETOG100 IF EQUAL
          MOVE      PTCFOCGR,PTFCOCGR               * Occupational Group
.
SETOG100  PACK      KEY14,AFUNDH,AFNDTB,SP70
          CALL      RDFUND1                         * read patfn1af
          BRANCH    OVRCD,SETOG999
          MATCH     SP70,PTHFOCGR
          GOTO      SETOG999 IF EQUAL
          MOVE      PTHFOCGR,PTFCOCGR               * Occupational Group
.
SETOG999  RETURN
+
.------------------------------------------------------------
. Health Assessment Details
.------------------------------------------------------------
HLTHAS00  MATCH     SP70,UPDTTYPE
          GOTO      HLTHAS70 IF EQUAL
.           
          MATCH     "A",UPDTTYPE    * Add Formaction
          GOTO      HLTHAS10 IF EQUAL
.         
          MATCH     "U",UPDTTYPE    * Update Formaction        
          GOTO      HLTHAS20 IF EQUAL 
.                            
          MATCH     "D",UPDTTYPE    * Delete Formaction        
          GOTO      HLTHAS30 IF EQUAL 
.                            
          GOTO      HLTHAS90 
.
.   ************ ADD FORMACTION ************
.
HLTHAS10  MOVE      ZERO,F8
          PACK      KEY16,URNUMBER,Z70
          CALL      RSIBASS1
          CALL      RPIBASS1
          BRANCH    OVRCD,HLTHAS15
.
          MATCH     URNUMBER,IBASURNO
          GOTO      HLTHAS15 IF NOT EQUAL
.
          MOVE      IBASCNTR,F8
.
          COMPARE   "99999999",F8           * Max 99999999
          GOTO      HLTHAS93 IF EQUAL
.
HLTHAS15  CALL      CLIBAS00                * clear file vars
          ADD       ONE,F8
.
          COMPARE   "99999999",F8           * Max 99999999
          GOTO      HLTHAS93 IF EQUAL
.
          CALL      MVIBAS00
.
          MOVE      URNUMBER,IBASURNO
          MOVE      F8,IBASCNTR
.
          PACK      IBASCDAT,CCC,CYY,CMM,CDD
          REP       " 0",IBASCDAT
          CLOCK     TIME,IBASCTIM
          MOVE      USERID,IBASCUID
          PACK      IBASUDAT,SP70
          PACK      IBASUTIM,SP70
          PACK      IBASUUID,SP70
          MOVE      SP70,IBASSPAR
.
          PACK      KEY16,IBASURNO,IBASCNTR,SP70
          CALL      RAIBASS1
          COMPARE   ONE,OVRCD
          GOTO      HLTHAS15 IF NOT EQUAL
.
          CALL      WRIBASS1
.
          MOVE      ZERO,EXIT
          GOTO      HLTHAS99
.
HLTHAS20  PACK      KEY16,IBASS001,IBASS002,SP70
          CALL      RDIBASS1
          BRANCH    OVRCD,HLTHAS92
.
          CALL      MVIBAS00
.
          PACK      IBASUDAT,CCC,CYY,CMM,CDD
          REP       " 0",IBASUDAT
          CLOCK     TIME,IBASUTIM
          MOVE      USERID,IBASUUID
.
          CALL      UPIBASS1
.
          MOVE      ZERO,EXIT
          GOTO      HLTHAS99
.
HLTHAS30  PACK      KEY16,IBASS001,IBASS002,SP70
          CALL      RDIBASS1
          BRANCH    OVRCD,HLTHAS92
.
          CALL      DEIBASS1
.
          MOVE      ZERO,EXIT
          GOTO      HLTHAS99
.
.         ************ DISPLAY FORMACTION **********
.
HLTHAS70  MOVE      URNUMBER,KEY8   * Read Master File
          CALL      RDMAST1
          BRANCH    OVRCD,HLTHAS91
.
          MOVE      URNUMBER,KEY8   * Read Master Extn File 2
          CALL      RDPMPX21
          BRANCH    OVRCD,HLTHAS91
.
          PACK      KEY16,URNUMBER,HLTHCNTR,SP70
          CALL      RDIBASS1
          IF        OVRCD=1
            CALL      CLIBAS00
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Health Assessment Details",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,HLTHAS99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      HLTHAS99
.
HLTHAS90  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      HLTHAS99
.
HLTHAS91  MOVE      "Can't find patient Master Details. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      HLTHAS99
.
HLTHAS92  MOVE      "Health Assessment Details Not Found. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      HLTHAS99
.
HLTHAS93  MOVE      "Health Assessment Details Limit Reached. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      HLTHAS99
.
HLTHAS99  RETURN
+
.------------------------------------------------------------
. Education Needs Assessment Details
.------------------------------------------------------------
EDUASS00  MATCH     SP70,UPDTTYPE
          OPEN      IBAEDAA1,"ibaedaaf"          * just in case (not in INIT000)
          MATCH     SP70,UPDTTYPE
          GOTO      EDUASS70 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add Formaction
          GOTO      EDUASS10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update Formaction
          GOTO      EDUASS20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete Formaction
          GOTO      EDUASS30 IF EQUAL
.
          GOTO      EDUASS90
.
.   ************ ADD FORMACTION ************
.
EDUASS10  MOVE      ZERO,F8
          PACK      KEY16,URNUMBER,ADMISSNO,SP20
          CALL      RDIBEDA1
          BRANCH    OVRCD,EDUASS15
.
          GOTO      EDUASS94
.
EDUASS15  CALL      CLIBEA00                * clear file vars
          ADD       ONE,F8
.
....      COMPARE   "99999999",F8           * Max 99999999
....      GOTO      EDUASS93 IF EQUAL
.
          CALL      MVIBEA00
.
          MOVE      URNUMBER,IBEAURNO
          MOVE      ADMISSNO,IBEAVISN
          MOVE      F8,IBEACNTR
.
          PACK      IBEACDAT,CCC,CYY,CMM,CDD
          REP       " 0",IBEACDAT
          CLOCK     TIME,IBEACTIM
          MOVE      USERID,IBEACUID
          PACK      IBEAUDAT,SP70
          PACK      IBEAUTIM,SP70
          PACK      IBEAUUID,SP70
          MOVE      SP70,IBEASPAR
.
          PACK      KEY16,IBEAURNO,IBEAVISN,SP70
          CALL      RAIBEDA1
          COMPARE   ONE,OVRCD
          GOTO      EDUASS94 IF NOT EQUAL
.
          CALL      WRIBEDA1
.
          MOVE      ZERO,EXIT
          GOTO      EDUASS99
.
EDUASS20  MOVE      ZERO,EXIT
          PACK      KEY16,IBEDA001,IBEDA002,SP20
          CALL      RDIBEDA1
          BRANCH    OVRCD,EDUASS92
.
          CALL      MVIBEA00
.
          PACK      IBEAUDAT,CCC,CYY,CMM,CDD
          REP       " 0",IBEAUDAT
          CLOCK     TIME,IBEAUTIM
          MOVE      USERID,IBEAUUID
.
          CALL      UPIBEDA1
.
          MOVE      ZERO,EXIT
          GOTO      EDUASS99
.
EDUASS30  MOVE      ZERO,EXIT
          PACK      KEY16,IBEDA001,IBEDA002,SP20
          CALL      RDIBEDA1
          BRANCH    OVRCD,EDUASS92
.
          CALL      DEIBEDA1
.
          MOVE      ZERO,EXIT
          GOTO      EDUASS99
.
.         ************ DISPLAY FORMACTION **********
.
EDUASS70  MOVE      URNUMBER,KEY8   * Read Master File
          CALL      RDMAST1
          BRANCH    OVRCD,EDUASS91
.
          MOVE      URNUMBER,KEY8   * Read Master Extn File 2
          CALL      RDPMPX21
          BRANCH    OVRCD,EDUASS91
.
          PACK      KEY16,URNUMBER,ADMISSNO,SP70
          CALL      RDIBEDA1
          IF        OVRCD=1
            CALL      CLIBEA00
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Education Needs Assessment Details",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,EDUASS99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      EDUASS99
.
EDUASS90  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EDUASS99
.
EDUASS91  MOVE      "Can't find patient Master Details. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EDUASS99
.
EDUASS92  MOVE      "Education Assessment Details Not Found. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EDUASS99
.
EDUASS93  MOVE      "Education Assessment Details Limit Reached. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EDUASS99
.
EDUASS94  MOVE      "An Education Assessment already exists. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      EDUASS99
.
EDUASS99  RETURN
+
.------------------------------------------------------------
. School Evaluation Details
.------------------------------------------------------------
SCHEVL00  MATCH     SP70,UPDTTYPE
          OPEN      IBASCEA1,"ibasceaf"          * just in case (not in INIT000)
          MATCH     SP70,UPDTTYPE
          GOTO      SCHEVL70 IF EQUAL
.         
          MATCH     "A",UPDTTYPE    * Add Formaction
          GOTO      SCHEVL10 IF EQUAL 
.
          MATCH     "U",UPDTTYPE    * Update Formaction
          GOTO      SCHEVL20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete Formaction
          GOTO      SCHEVL30 IF EQUAL
.
          GOTO      SCHEVL90
.
.   ************ ADD FORMACTION ************
.
SCHEVL10  MOVE      ZERO,F8
          PACK      KEY16,URNUMBER,ADMISSNO,SP20
          CALL      RDIBSEA1
          BRANCH    OVRCD,SCHEVL15
.
          GOTO      SCHEVL94
.
SCHEVL15  CALL      CLIBSE00                * clear file vars
          ADD       ONE,F8
.
....      COMPARE   "99999999",F8           * Max 99999999
....      GOTO      SCHEVL93 IF EQUAL
.
          CALL      MVIBSE00
.
          MOVE      URNUMBER,IBSEURNO
          MOVE      ADMISSNO,IBSEVISN
          MOVE      F8,IBSECNTR
.
          PACK      IBSECDAT,CCC,CYY,CMM,CDD
          REP       " 0",IBSECDAT
          CLOCK     TIME,IBSECTIM
          MOVE      USERID,IBSECUID
          PACK      IBSEUDAT,SP70
          PACK      IBSEUTIM,SP70
          PACK      IBSEUUID,SP70
          MOVE      SP70,IBSESPAR
.
          PACK      KEY16,IBSEURNO,IBSEVISN,SP70
          CALL      RAIBSEA1
          COMPARE   ONE,OVRCD
          GOTO      SCHEVL94 IF NOT EQUAL
.
          CALL      WRIBSEA1
.
          MOVE      ZERO,EXIT
          GOTO      SCHEVL99
.
SCHEVL20  MOVE      ZERO,EXIT
          PACK      KEY16,IBSCE001,IBSCE002,SP20
          CALL      RDIBSEA1
          BRANCH    OVRCD,SCHEVL92
.
          CALL      MVIBSE00
.
          PACK      IBSEUDAT,CCC,CYY,CMM,CDD
          REP       " 0",IBSEUDAT
          CLOCK     TIME,IBSEUTIM
          MOVE      USERID,IBSEUUID
.
          CALL      UPIBSEA1
.
          MOVE      ZERO,EXIT
          GOTO      SCHEVL99
.
SCHEVL30  MOVE      ZERO,EXIT
          PACK      KEY16,IBSCE001,IBSCE002,SP20
          CALL      RDIBSEA1
          BRANCH    OVRCD,SCHEVL92
.
          CALL      DEIBSEA1
.
          MOVE      ZERO,EXIT
          GOTO      SCHEVL99
.
.         ************ DISPLAY FORMACTION **********
.
SCHEVL70  MOVE      URNUMBER,KEY8   * Read Master File
          CALL      RDMAST1
          BRANCH    OVRCD,SCHEVL91
.
          MOVE      URNUMBER,KEY8   * Read Master Extn File 2
          CALL      RDPMPX21
          BRANCH    OVRCD,SCHEVL91
.
          PACK      KEY16,URNUMBER,ADMISSNO,SP70
          CALL      RDIBSEA1
          IF        OVRCD=1
            CALL      CLIBSE00
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "School Evaluation Details",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,SCHEVL99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      SCHEVL99
.
SCHEVL90  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SCHEVL99
.
SCHEVL91  MOVE      "Can't find patient Master Details. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SCHEVL99
.
SCHEVL92  MOVE      "School Evaluation Details Not Found. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SCHEVL99
.
SCHEVL93  MOVE      "School Evaluation Details Limit Reached. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SCHEVL99
.
SCHEVL94  MOVE      "An School Evaluation already exists. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SCHEVL99
.
SCHEVL99  RETURN
+
.------------------------------------------------------------
. Process Fields         * Unlock UR record
.------------------------------------------------------------
PROFLD00  BRANCH    REPORTNO,PROFLD10,PROFLD20,PROFLD30,PROFLD40
.
          GOTO      PROFLD99 
.
PROFLD10  BRANCH    FLDSETNO,PROFLD11,PROFLD12
          GOTO      PROFLD99
.
PROFLD11  CALL      MASF0000    * Master File Info
          GOTO      PROFLD99
.
PROFLD12  CALL      TABLE000    * General Tags
          GOTO      PROFLD99
.
PROFLD20  BRANCH    FLDSETNO,PROFLD21,PROFLD22,PROFLD23
          GOTO      PROFLD99
.
PROFLD21  CALL      MASF0000    * Master File Info
          GOTO      PROFLD99
.
PROFLD22  CALL      IBAS0000    * Health Assessment Details
          GOTO      PROFLD99
.
PROFLD23  CALL      TABL0000    * General Tags
          GOTO      PROFLD99
.
PROFLD30  BRANCH    FLDSETNO,PROFLD31,PROFLD32,PROFLD33
          GOTO      PROFLD99
.
PROFLD31  CALL      MASF0000    * Master File Info
          GOTO      PROFLD99
.
PROFLD32  CALL      IBEA0000    * Education Needs Assessment Details
          GOTO      PROFLD99
.
PROFLD33  CALL      TABLE000    * General Tags
          GOTO      PROFLD99
..
PROFLD40  BRANCH    FLDSETNO,PROFLD41,PROFLD42,PROFLD43
          GOTO      PROFLD99
.
PROFLD41  CALL      MASF0000    * Master File Info
          GOTO      PROFLD99
.
PROFLD42  CALL      IBSE0000    * School Evaluation Details
          GOTO      PROFLD99
.
PROFLD43  CALL      TABLE000    * General Tags
          GOTO      PROFLD99
.
PROFLD99  RETURN
+
.------------------------------------------------------------------------------
. General tags
.------------------------------------------------------------------------------
TABL0000  BRANCH    FLDITMNO,TABL0010
.
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      TABL9999
.
TABL0010  CALL      ASSLST00                * Health Assessment List
          GOTO      TABL9999
.
TABL9999  RETURN
.
.
TABLE000  BRANCH    FLDITMNO,TABLE010,TABLE020,TABLE030,TABLE040,TABLE050:
                             TABLE060,TABLE070,TABLE080,TABLE090,TABLE100:
                             TABLE110,TABLE120,TABLE130,TABLE140
.
          WRITE     HTMLFILE;"COMWEB02 - Invalid IBA Tag";
          GOTO      TABLE999
.
TABLE010  CALL      EDALST00                * Education Needs Assessment List
          GOTO      TABLE999
.
TABLE020  CALL      EDAEXI00                * Educ.Needs Assessments exist
          GOTO      TABLE999
.
TABLE030  CALL      SCELST00                * School Evaluation List
          GOTO      TABLE999
.
TABLE040  CALL      SCEEXI00                * School Evaluation exists
          GOTO      TABLE999
.
TABLE050  CALL      ALDEPT00                * Allied Health Department Code
          GOTO      TABLE999
.
TABLE060  CALL      PTDET000                * Patient Debt Invoices.
          GOTO      TABLE999
.
TABLE070  MOVE      "dc",CATEGORY
          MOVE      SP3,CODE                * Debt Collection Agencies
          CALL      CODITM00
          GOTO      TABLE999
.
TABLE080  MOVE      "FP",CATEGORY
          MOVE      SP3,CODE                * Repayment frequency
          CALL      CODITM00
          GOTO      TABLE999
.
TABLE090  WRITE     HTMLFILE;PTDERPAY;      * Amount to be repaid
          GOTO      TABLE999
.
TABLE100  MOVE      "FA",CATEGORY
          MOVE      SP3,CODE                * Future Action      
          CALL      CODITM00
          GOTO      TABLE999
.
TABLE110  CALL      NUMINV00                * Number of Invoices for Visit 
          GOTO      TABLE999
.
TABLE120  MOVE      ZERO,F1                 * flag for Admission
          CALL      ADOUST00                * Outstanding amount for Admission
          PACK      DIM15,SP7
          MOVE      OUTSAMNT,DIM15
          SQUEEZE   DIM15
          WRITE     HTMLFILE;DIM15;
.
          GOTO      TABLE999
.
TABLE130  CALL      GADMN000                * Get patdetaf Admission record   
          GOTO      TABLE999
.
TABLE140  CALL      PTDED000                * Get invoices for Delete Pmt Plan
          GOTO      TABLE999
.
TABLE999  RETURN
.
.
TABLS999  RETURN
+
.*******************************************************************************
.*                    Get patdetaf Admission record info for Clear Payment Plan*
.*******************************************************************************
GADMN000  PACK      KEY18,URNUMBER,FOUR,SP70
          CALL      RSPTDET2
GADMN005  CALL      RKPTDET2
          BRANCH    OVRCD,GADMN999          
.
          MATCH     URNUMBER,PTDEURNO
          GOTO      GADMN999 IF NOT EQUAL
.
          MATCH     "008",TEMPLATE            * Delete Payment Plan template
          IF        !@EQUAL
            MATCH     SP8,PTDEDTCL
            GOTO      GADMN005 IF NOT EQUAL   * bypass if already cleared
          ENDIF
.
          MATCH     ADMISSNO,PTDEINVN
          GOTO      GADMN005 IF NOT EQUAL
.
          UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,GADMN010,GADMN020,GADMN030,GADMN040,GADMN050,GADMN060
          GOTO      GADMN999
.
GADMN010  REP       " 0",PTDESDAT            * payments starting          
          UNPACK    PTDESDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DIM11,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          WRITE     HTMLFILE;DIM11;
          GOTO      GADMN999
.
GADMN020  WRITE     HTMLFILE;PTDERPAY;   * amount to be repaid per period
          GOTO      GADMN999
.
GADMN030  MATCH     SP70,PTDEFREQ        * repayment frequency
          IF        !@EQUAL
            MOVE      "FP",KEY2
            PACK      KEY5,KEY2,PTDEFREQ
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      SP70,TDESC
            ENDIF
            WRITE     HTMLFILE;TDESC;
          ELSE
            WRITE     HTMLFILE;SP70;
          ENDIF
          GOTO      GADMN999
.
GADMN040  MATCH     PTDECOD1,SP70
          GOTO      GADMN999 IF EQUAL
          MOVE      "FA",CATEGORY
          MOVE      PTDECOD1,CODE
          CALL      GETCOD00
          MATCH     "P",TCINDC3
          GOTO      GADMN999 IF NOT EQUAL
          WRITE     HTMLFILE;TDESC;
          GOTO      GADMN999
.
GADMN050  WRITE     HTMLFILE;PTDENOFP;       * number of payments
          GOTO      GADMN999
.
GADMN060  REP       " 0",PTDEEDAT            * expected last payment date
          UNPACK    PTDEEDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DIM11,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          WRITE     HTMLFILE;DIM11;
          GOTO      GADMN999
.
GADMN999  RETURN
+
.*******************************************************************************
.*                Calculate outstanding amount for Admission                   *
.*******************************************************************************
ADOUST00  MOVE      ZERO,OUTSAMNT 
          MOVE      ZERO,FORM122
.
          PACK      ADMISSNO,ADMISSNO,SP70
          MATCH     SP70,ADMISSNO
          GOTO      ADOUST99 IF EQUAL
.
          PACK      KEY16,ADMISSNO,SP70
          CALL      RDSINV3
ADOUST10  CALL      RDKINV3
          BRANCH    OVRCD,ADOUST20
          MATCH     ADMISSNO,DPINVADM
          GOTO      ADOUST20 IF NOT EQUAL
.
          MATCH     "1",PTINCRST        * skip if fully credited  
          GOTO      ADOUST10 IF EQUAL 
.
.         Check if for an invoice only
.
          IF        F1=1
            MATCH     INVOICEN,PINVNO
            GOTO      ADOUST10 IF NOT EQUAL    * different invoice
          ENDIF
.
          MOVE      PINVAMT,FORM122
          ADD       PINVJOUR,FORM122
          ADD       PTINGSTJ,FORM122
          ADD       PTINCRTT,FORM122
          ADD       FORM122,OUTSAMNT
.
          BRANCH    F1,ADOUST20         * for single invoice only
          GOTO      ADOUST10            * read next record
.
.         Get payment
.
ADOUST20  PACK      KEY25,ADMISSNO,SP70
          CALL      RSRCDTE6
ADOUST30  CALL      RKRCDTE6
          BRANCH    OVRCD,ADOUST99
.
          MATCH     ADMISSNO,RCDTVIST
          GOTO      ADOUST99 IF NOT EQUAL
.
.         F1=0 flag for an Admission
.         F1=1 flag for an invoice only
.
          IF        F1=1
            PACK      KEY12,SP4,INVOICEN,SP70
            MATCH     KEY12,RCDTINVN
            GOTO      ADOUST30 IF NOT EQUAL    * different invoice
          ELSE
            UNPACK    RCDTINVN,KEY4,KEY8
            CALL      RDINV1
            BRANCH    OVRCD,ADOUST30
            MATCH     "1",PTINCRST          * skip if fully credited
            GOTO      ADOUST30 IF EQUAL
          ENDIF
.
          PACK      KEY12,RCDTRECN
          CALL      RDRCBNK1
          BRANCH    OVRCD,ADOUST30
          MATCH     "1",RCBNSTAT            * check if cancelled
          GOTO      ADOUST30 IF EQUAL
.
          MULT      "-1",RCDTAMNT
          ADD       RCDTAMNT,OUTSAMNT
          GOTO      ADOUST30
.
ADOUST99  RETURN
+
.*******************************************************************************
.*                Calculate number of invoices for the visit                   *
.*******************************************************************************
NUMINV00  MOVE      ZERO,F1
          PACK      ADMISSNO,ADMISSNO,SP70
          MATCH     SP70,ADMISSNO
          GOTO      NUMINV20 IF EQUAL           * Check private practice
.
          PACK      KEY16,ADMISSNO,Z70
          CALL      RSPTINV3
          CALL      RPPTINV3
          BRANCH    OVRCD,NUMINV20
          MATCH     ADMISSNO,DPINVADM
          GOTO      NUMINV20 IF NOT EQUAL 
.
          GOTO      NUMINV80
.
.         Check private practice invoice
.
NUMINV20  PACK      KEY18,URNUMBER,SP70
          CALL      RSPRIN3
NUMINV30  CALL      RKPRIN3
          BRANCH    OVRCD,NUMINV90
.
          MATCH     URNUMBER,PRINDEBT       * Same U/R ?
          GOTO      NUMINV90 IF NOT EQUAL
.
NUMINV80  MOVE      ONE,F1                  * found an invoice
.
NUMINV90  WRITE     HTMLFILE;F1;
NUMINV99  RETURN
+
.*******************************************************************************
.*                Display Health Assessment Details List                       *
.*******************************************************************************
ASSLST00  PACK      KEY16,URNUMBER,SP70     * Loop through Health Assessments
          CALL      RSIBASS1                * Positional Read
ASSLST10  CALL      RKIBASS1                * Sequential Read
          BRANCH    OVRCD,ASSLST90
.
          MATCH     URNUMBER,IBASURNO
          GOTO      ASSLST90 IF NOT EQUAL
.
          MOVE      "003",LINKTEM
.
ASSLST20  CALL      ASSROW00                * Displaying the Table Row
          GOTO      ASSLST10
.
ASSLST90  WRITE      HTMLFILE;"<tr bgcolor=#FF0000>":
                              "<td colspan=10 align=center>":
                              "No more Health Assessments</td>"
.
ASSLST99  RETURN
+
.*******************************************************************************
.                           Display Of Assessment Row
.*******************************************************************************
ASSROW00  WRITE     HTMLFILE;"<tr>":
                    "<td nowrap>":
                    "<a HREF ='javascript:HlthAssLink(#"",IBASURNO,"#",#"":
                    IBASCNTR,"#",#"",LINKTEM,"#")'>":
                    "<img src=../images/AppointmentIcon.gif hspace=4":
                    " border=0 align=absmiddle></a>":
                    IBASCNTR,"</td>":
                    "</tr>"
ASSROW99  RETURN
+
.*******************************************************************************
.*                Display Education Needs Assessment Details List              *
.*******************************************************************************
EDALST00  MOVE      ZERO,EXIT
          OPEN      IBAEDAA1,"ibaedaaf"
          PACK      KEY16,URNUMBER,SP70     * Loop through Education Ass's    
          CALL      RSIBEDA1                * Positional Read
EDALST10  CALL      RKIBEDA1                * Sequential Read
          BRANCH    OVRCD,EDALST90
.
          MATCH     URNUMBER,IBEAURNO
          GOTO      EDALST90 IF NOT EQUAL
          MATCH     ADMISSNO,IBEAVISN
          GOTO      EDALST10 IF NOT EQUAL
.
          MOVE      "003",LINKTEM
.
EDALST20  CALL      EDAROW00                * Displaying the Table Row
          GOTO      EDALST10
.
EDALST90  WRITE      HTMLFILE;"<tr bgcolor=#FF0000>":
                              "<td colspan=10 align=center>":
                              "No more Education Needs Assessments</td>"
.
EDALST99  RETURN
+
.*******************************************************************************
.                           Display Of Assessment Row
.*******************************************************************************
EDAROW00  MOVE      ZERO,EXIT
          UNPACK    IBEACDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      KEY11,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR 
.         
          PACK      WBSENAM,IBEACUID,SP30   
          PACK      KEY10,IBEACUID,SP10
          CALL      RDWBSE1
.         
          WRITE     HTMLFILE;"<tr>":
                    "<td nowrap>":
                    "<a HREF ='javascript:EduAssLink(#"",IBEAURNO,"#",#"":
                    IBEAVISN,"#",#"",LINKTEM,"#")'>":
                    "<img src=../images/AppointmentIcon.gif hspace=4":
                    " border=0 align=absmiddle></a>":
                    " Created on ",KEY11," at ",IBEACTIM,"    by ":
                    WBSENAM,"</td><td>",ADMISSNO,"</td></tr>"
.
EDAROW99  RETURN
.*******************************************************************************
.*                Check if Education Needs Assessment exists                   *
.*******************************************************************************
EDAEXI00  MOVE      ZERO,EXIT
          MOVE      ZERO,F1
          OPEN      IBAEDAA1,"ibaedaaf"
          PACK      KEY16,URNUMBER,SP70     * Loop through Education Ass's
          CALL      RSIBEDA1                * Positional Read
EDAEXI10  CALL      RKIBEDA1                * Sequential Read
          BRANCH    OVRCD,EDAEXI90
.
          MATCH     URNUMBER,IBEAURNO
          GOTO      EDAEXI90 IF NOT EQUAL
          MATCH     ADMISSNO,IBEAVISN
          GOTO      EDAEXI10 IF NOT EQUAL
.
          MOVE      ONE,F1
          GOTO      EDAEXI10
.
EDAEXI90  WRITE      HTMLFILE;F1;
.
EDAEXI99  RETURN
+
.*******************************************************************************
.*                Display School Evaluation Details List                       *
.*******************************************************************************
SCELST00  MOVE      ZERO,EXIT
          OPEN      IBASCEA1,"ibasceaf"
          PACK      KEY16,URNUMBER,SP70     * Loop through School Evaluations
          CALL      RSIBSEA1                * Positional Read
SCELST10  CALL      RKIBSEA1                * Sequential Read
          BRANCH    OVRCD,SCELST90
.
          MATCH     URNUMBER,IBSEURNO
          GOTO      SCELST90 IF NOT EQUAL
          MATCH     ADMISSNO,IBSEVISN
          GOTO      SCELST10 IF NOT EQUAL
.
          MOVE      "003",LINKTEM
.
SCELST20  CALL      SCAROW00                * Displaying the Table Row
          GOTO      SCELST10
.
SCELST90  WRITE      HTMLFILE;"<tr bgcolor=#FF0000>":
                              "<td colspan=10 align=center>":
                              "No more School Evaluations</td></tr>"
SCELST99  RETURN
+
.*******************************************************************************
.                           Display Of School Evaluation Row
.*******************************************************************************
SCAROW00  MOVE      ZERO,EXIT
          UNPACK    IBSECDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      KEY11,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          PACK      WBSENAM,IBSECUID,SP30
          PACK      KEY10,IBSECUID,SP10
          CALL      RDWBSE1
.
          WRITE     HTMLFILE;"<tr>":
                    "<td nowrap>":
                    "<a HREF ='javascript:SchEvalLink(#"",IBSEURNO,"#",#"":
                    IBSEVISN,"#",#"",LINKTEM,"#")'>":
                    "<img src=../images/AppointmentIcon.gif hspace=4":
                    " border=0 align=absmiddle></a>":
                    " Created on ",KEY11," at ",IBSECTIM,"    by ":
                    WBSENAM,"</td><td>",ADMISSNO,"</td></tr>"
SCAROW99  RETURN
.*******************************************************************************
.*                Check if School Evaluation exists                            *
.*******************************************************************************
SCEEXI00  MOVE      ZERO,EXIT
          MOVE      ZERO,F1
          OPEN      IBASCEA1,"ibasceaf"
          PACK      KEY16,URNUMBER,SP70     * Loop through School Evaluations
          CALL      RSIBSEA1                * Positional Read
SCEEXI10  CALL      RKIBSEA1                * Sequential Read
          BRANCH    OVRCD,SCEEXI90
.
          MATCH     URNUMBER,IBSEURNO
          GOTO      SCEEXI90 IF NOT EQUAL
          MATCH     ADMISSNO,IBSEVISN
          GOTO      SCEEXI10 IF NOT EQUAL
.
          MOVE      ONE,F1
          GOTO      SCEEXI10
.
SCEEXI90  WRITE      HTMLFILE;F1;
.
SCEEXI99  RETURN
+
.*******************************************************************************
.*                get Department code for current Admission No.                *
.*******************************************************************************
ALDEPT00  MOVE      ZERO,EXIT
          MOVE      "XXX",ALREDEPT
          PACK      KEY8,ADMISSNO,SP8
          CALL      RDALREF1
.
          WRITE      HTMLFILE;ALREDEPT;
.
ALDEPT99  RETURN
+
.*******************************************************************************
.*                get Patient Debt Invoices (by type)                          *
.*******************************************************************************
PTDET000  UNPACK    DIM127,D1,KEY1,DIM127
.         
          MOVE      ZERO,F1
          MOVE      KEY1,F1
.
          PACK      KEY18,URNUMBER,KEY1,SP20
          CALL      RSPTDET2
PTDET100  CALL      RKPTDET2
          BRANCH    OVRCD,PTDET999
.
          MATCH     URNUMBER,PTDEURNO
          GOTO      PTDET999 IF NOT EQUAL
.
          MATCH     KEY1,PTDETYPE
          GOTO      PTDET999 IF NOT EQUAL
.
          MATCH     SP8,PTDEDTCL
          GOTO      PTDET100 IF NOT EQUAL
.
          WRITE     HTMLFILE;"<option value=#"",PTDEINVN,"#">":
                             PTDEINVN,"</option>"
          GOTO      PTDET100
.
.
PTDET999  RETURN
+
.*******************************************************************************
.*                get Patient Debt Invoices (by type) Delete Payment Plan      *
.*******************************************************************************
PTDED000  UNPACK    DIM127,D1,KEY1,DIM127
.
          MOVE      ZERO,F1
          MOVE      KEY1,F1
.
          PACK      KEY18,URNUMBER,KEY1,SP20
          CALL      RSPTDET2
PTDED100  CALL      RKPTDET2
          BRANCH    OVRCD,PTDED999
.
          MATCH     URNUMBER,PTDEURNO
          GOTO      PTDED999 IF NOT EQUAL
.
          MATCH     KEY1,PTDETYPE
          GOTO      PTDED999 IF NOT EQUAL
.
          WRITE     HTMLFILE;"<option value=#"",PTDEINVN,"#">":
                             PTDEINVN,"</option>"
          GOTO      PTDED100
.
.
PTDED999  RETURN
.------------------------------------------------------------
          INC       IBAWEBCD
          INC       WEBDATCD
.
          INC       IBAASSIO/INC
          INC       IBAEDAIO/INC   * Education Needs Assessment
          INC       IBASCEIO/INC   * School Evaluation form     
          INC       ALLREFIO/INC   * Allied Health Referral table
          INC       PATDO1IO/INC   * Doctor File
          INC       PATMI1IO/INC
          INC       PATMA1IO/INC
          INC       PATVISIO/INC   * Patients Visits File
          INC       PMSVX1IO/INC   * Visit Extension File
          INC       PATALRIO/INC
          INC       OUTSITIO/INC
          INC       PMSUCHIO/INC
          INC       PMSUCDIO/INC
          INC       PATPR1IO/INC
          INC       PMSPX2IO/INC
          INC       PATFN1IO/INC
          INC       PATWR1IO/INC
          INC       PATCFAIO/INC
          INC       PATFACIO/INC
          INC       MRTMASIO/INC
          INC       NHIMASIO/INC
          INC       NHIETHIO/INC
          INC       PATRE1IO/INC
          INC       PATDGWIO/INC
          INC       PATHSPIO/INC
          INC       PATITMIO/INC
          INC       PATDADIO/INC
          INC       PATVADIO/INC
          INC       PATCMCIO/INC
          INC       PATASFIO/INC
          INC       PMSHCGIO/INC
          INC       PMSHCPIO/INC
          INC       PMSCCDIO/INC            * Concession Card Details
.
          INC       APSMASIO/INC
          INC       PATIN1IO/INC
          INC       PATDSCIO/INC
          INC       PATINVIO/INC
          INC       PRIINVIO/INC
          INC       PATEORIO/INC   * External Organisation table
          INC       PATDETIO/INC                                      *I-233229
.
          INC       RCPBNKIO/INC
          INC       RCPDTEIO/INC
.
          INC       UPFAPPLN
          INC       CLNHIMAS                   * Clear nhimasaf file variables
          INC       CLPMSPX2
          INC       CLPATDET                                          *I-233229
          INC       CLPATFAC
          INC       PMSOSDTM           * Outstanding Debts routine    *I-233229
          INC       IBAASSTM
          INC       IBAEDATM           * Education Needs Assessment
          INC       IBASCETM           * School Evaluation form     
          INC       PATMASTM
          INC       PATDOCTM
          INC       PMSHCGTM
          INC       PMSHCPTM
          INC       PMSPX2TM
          INC       PATMSXTM
          INC       PATDOCCD
          INC       CALCAGE
          INC       NAMSTRCD
          INC       DAYOFWEK
.

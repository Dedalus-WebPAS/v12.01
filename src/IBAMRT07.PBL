.---------------------------------------------------------------------
. Program       : IBAMRT07
.
. Function      : Medical Records Requests Cancel server
.
. Modifications : 
.
. V10.10.01 01/05/2017  Ebon Clements  TSK 0837392
.           Fixed print layout
. V10.03.01 22/05/2013  Ania P         CAR 216497
.           Added MRRDFUID PACK of new var MRRDFUID before UPMRRQD1
. V10.02.01 04/07/2011  Ebon Clements  CAR 240724
.           Mods for MRT hospital/location - Added hospital 
.           to location key
. V9.08.01  31/01/2007  Peter Vela     CAR 127930
.           Recompled for MRTRQHFD
. V5.09.B00 09/02/2001 Bronko Sosic 
.           Created Program
.----------------------------------------------------------------------
          INC       STD001FD    
          INC       WEBCONFD/INC
          INC       MRTMASFD/INC
          INC       MRTRQDFD/INC
          INC       MRTRQHFD/INC
          INC       MRTLOCFD/INC
          INC       MRTMOVFD/INC
.
CURRDATE  DIM       8                       * Current Date
DISADATE  DIM       10                      * Display Admission Date
DISDDATE  DIM       10                      * Display Discharge Date
DISCOUNT  FORM      2                       * Disease Code Counter
DOWNCASE  INIT      "AaBbCcDdEeFfGgHhIiJjKkLlMmNnOoPpQqRrSsTtUuVvWwXxYyZz"
LETTERN   INIT      "N"
.
LOCATION  DIM       7                       * Extract ID
LOCNTEST  DIM       7
DOCMTYPE  DIM       4                       * Document Type
OPENDATE  DIM       8                       * From Date
CLOSDATE  DIM       8                       * To Date
.
EXTRCTFL  FILE      ASCII, FIXED=400     * Extract File (Adjust Length to Suit)
EXTRCTFN  DIM       13                   * Extract File Name
EXTRCTEX  INIT      ".csv"               * Extract File Name Extension
.
MTHNAM3   DIM       3
.
.
SOPTION   FORM      1
.
TABDELM   EQU       0x09
.
JAN       INIT      "January"
FEB       INIT      "February"
MAR       INIT      "March"
APR       INIT      "April"
MAY       INIT      "May"
JUN       INIT      "June"
JUL       INIT      "July"
AUG       INIT      "August"
SEP       INIT      "September"
OCT       INIT      "October"
NOV       INIT      "November"
DEC       INIT      "December"
.
.------------------------------------------------------------------------
PRGID     INIT      "IBAMRT07"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "Medical Records Requests Canceled"
.------------------------------------------------------------------------
MAIN0000  CALL      INIT0000       * Initialise 
          KEYIN     *P1:5,"Option       : ",SOPTION
          KEYIN     *P1:5,"From Date    : ",OPENDATE
          KEYIN     *P1:5,"To Date      : ",CLOSDATE
          KEYIN     *P1:5,"Document Type: ",DOCMTYPE
          KEYIN     *P1:5,"Destination  : ",LOCATION
.
          BRANCH    SOPTION,MAIN1100
          GOTO      MAIN9999
.
MAIN1100  CALL      DELREQ00          * Clear Request file for filled status "N"
          GOTO      MAIN9000
.  
MAIN9000  
.
MAIN9999  CHAIN     PGM
.------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------
INIT0000  CALL      IBACLOCK      * todays date used for a new Doctor code
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.   
          OPEN      MRTMASA2,"mrtmasaf"
          OPEN      MRTRQDR1,"mrtrqdaf"
          OPEN      MRTRQHR1,"mrtrqhaf"
          OPEN      MRTRQHR2,"mrtrqhaf"
          OPEN      MRTMOVA1,"mrtmovaf"
          OPEN      MRTLOCA1,"mrtlocaf"
.
INIT9999  RETURN
.----------------------------------------------------------------
.                          DELREQ00
.  Clear Medical Records Requests File (MRTRQDFD & MRTRQHFD) for
.  any record that has a filled status of "N"
.----------------------------------------------------------------
DELREQ00  MOVE      ZERO,CLNO
          MOVE      ZERO,CPAGENO
          CALL      HEAD132
.
          UNPACK    CURRDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PRINT     *40,"Date of Cancel: ",CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          ADD       ONE,CLNO
.
          PRINT     *1,"User ID",*12,"UR No.",*21,"Date/Time",*39:
                    "Reason",*71,"Current Location",*95,"Destination",*127:
                    "Volume"

          ADD       ONE,CLNO
.
          PRINT     "----------",*12,"--------",*21,"-----------------":
                    *39,"------------------------------":
                    *71,"-----------------------":
                    *95,"------------------------------",*127,"-----"         
          ADD       ONE,CLNO          
.
          PACK      KEY23,OPENDATE,SP70
          CALL      RSMRRQH2
DELREQ10  CALL      RKMRRQH2                * Read the Request Header File
          BRANCH    OVRCD,DELREQ99

.          MATCH     MRRQDTRQ,OPENDATE       * Check for date range
.          IF        !@LESS
.            GOTO      DELREQ10
.          ENDIF
.
          MATCH     MRRQDTRQ,CLOSDATE       * Check for date range
          IF        @LESS
            GOTO      DELREQ99
          ENDIF
.
          RESET     LOCATION
          MATCH     LOCATION,SP70 
          IF        !@EQUAL
            PACK      LOCNTEST,MRRQHOSC,MRRQLOCR,SP70
            MATCH     LOCATION,LOCNTEST     * Checks for Request Location
            GOTO      DELREQ10 IF NOT EQUAL
          ENDIF 
.
          PACK      KEY20,MRRQRQNO,SP70
          CALL      RSMRRQD1
DELREQ20  CALL      RKMRRQD1                * Read the Request Details File 
          BRANCH    OVRCD,DELREQ10
.
          MATCH     MRRQRQNO,MRRDRQNO
          GOTO      DELREQ10 IF NOT EQUAL
.
          MATCH     MRRDFILL,LETTERN 
          GOTO      DELREQ20 IF NOT EQUAL
.
          PACK      KEY10,MRRDRKEY,SP70
          CALL      RDMRMAS2                * Reads MRT Master File
          BRANCH    OVRCD,DELREQ20
.
          RESET     DOCMTYPE
          MATCH     DOCMTYPE,SP70 
          IF        !@EQUAL
            MATCH     DOCMTYPE,MRMADOTY     * Checks for Document Type
            GOTO      DELREQ20 IF NOT EQUAL
          ENDIF 
.
          ADD       ONE,CLNO                * Increment number of lines
          COMPARE   "55",CLNO
          CALL      HEAD132 IF EQUAL
.
          MOVE      SP70,MRMODESC      *Reason For Movement
          PACK      KEY4,MRRQMOVR
          CALL      RDMRMOV1
.
          MOVE      SP70,MRLODESC      *Currnet Location  
          PACK      KEY7,MRMACHOS,MRMACLOC
          CALL      RDMRLOC1
          MOVE      MRLODESC,KEY20
.
          MOVE      SP70,MRLODESC      *Location Required
          PACK      KEY7,MRRQHOSC,MRRQLOCR
          CALL      RDMRLOC1
.
          UNPACK    MRRQDAYE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      KEY19,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,SP1,MRRQTIYM

          STRIP     MRRQUSID
          STRIP     MRMAURNO
          STRIP     KEY19
          STRIP     MRMODESC
          STRIP     KEY20
          STRIP     MRLODESC
.
          PRINT     *+,MRRQUSID,*12,MRMAURNO,*21,KEY19,*39:
                    MRMODESC,*71,KEY20,*95,MRLODESC,*127,MRMAVOLN
.         
          MOVE      "C",MRRDFILL            * Update to Cancelled
          PACK      MRRDFUID,SP70          
          CALL      UPMRRQD1
.
          PACK      KEY10,MRRDRQNO,SP70
          CALL      RDMRRQH1
          BRANCH    OVRCD,DELREQ20
          
          CLOCK     TIME,MRRQTMUP
          CALL      IBACLOCK
          PACK      MRRQDTUP,CCC,CYY,CMM,CDD
          REP       " 0",MRRQDTUP
          MOVE      "AUTO",MRRQUSUP
          CALL      UPMRRQH1
.
          GOTO      DELREQ20
.
DELREQ99  RETURN
.----------------------------------------------------------------
.
          INC       STD001IO
.
          INC       MRTMASIO/INC
          INC       MRTRQDIO/INC
          INC       MRTRQHIO/INC
          INC       MRTLOCIO/INC
          INC       MRTMOVIO/INC

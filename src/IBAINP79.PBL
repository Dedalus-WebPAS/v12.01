.******************************************************************************
.* System         :  Patient Billing                                          *
.* Program        :  IBAINP79                                                 *
.* Description    :  Re-Admission within X Days Report                        *
.******************************************************************************
.* Date           :  13/12/1994                                               *
.* Author         :  Greg Horvat                                              *
.* Function       :  This report asks for an admission date range and the     *
.*                   number of re-admission days. The report will then print  *
.*                   every patient within the date range & re-admission days  *
.*                   who was discharged & then re-admitted prior to the       *
.*                   current admission date.                                  *
.* Modifications  :                                                           *
.******************************************************************************
.* V12.00.01 27/05/2025  PJ Le Febour   TSK 0955096 AlphaNum VisitNum         *
.*           amend  COMPARE  FORM8,PVBILL to MATCH  DIM8,PVBILL               *
.*                  COMPARE  TADMN,DADMNO  to MATCH  TADMN,DADMNO             * 
.*                  COMPARE   AADMNO,PTEDADMN to MATCH  AADMNO,PTEDADMN       *
.*                                                                            *
.******************************************************************************
.* V10.04.02 15/06/2014  Jill Parkinson CAR 301639                            *
.*           Moved TFILENAM to INIT0000                                       *
.* V10.04.01 28/04/2014  Sandra Barcham CAR 300230                            *
.*           Fixed Multi-hospital option                                      *
.* V10.03.01  28/12/2012 Patrick Adair                             CAR 261630 *
.*                       Removed port number from temp file name              *
.* V10.02.01 28/03/2011  Mike Laming   CAR 240107                             *
.*           Mods to PATECDaf & PATECOaf variables & Keys - file change       *
.* V9.12.01  18/09/2009  Saroeun Soeur     206214                             *
.*           PTCNHDPS to check for NZ                                         *
.*           CPLRM000 - plan admission field                                  *
.* V9.09.02  06/03/2008  Sandra Barcham    151804                             *
.*           Allow multiple versions of ICD10 table                           *
.* V9.09.01  15/02/2008  Mike Laming   CAR 160842                             *
.*           Add Multi-Hospital to selection                                  *
.* V9.08.01  22/02/2007 Sandra Barcham   134298                               *
.*           Fix category selection for planned Re-admission                  *
.* V9.06.01  14/06/2006 Prasad Kvkg   63053                                   *
.*           Added New Column - Planned Re-Admission                          *
.* V9.03.01  29/10/2003 Sandra Barcham   43783                                *
.*           Recompiled for NZHISIIO and NZHISIUP                             *
.******************************************************************************
.* V9.02.02  10/02/2003  Lina Vo                                              *
.*           Removed open of outpreaf - not used.                             *
.* V9.02.01  01/11/2001  Greg Horvat                                          *
.*           Recompiled for ACTCOMN1 - Added xtra option to the               *
.*           parameter PNSWRACE & added xtra option to the parameter          *
.*           PTCNADMT                                                         *
.******************************************************************************
.*        V5.10.B01 12/04/2001  Glenn Saunders                                *
.*                  Remove all references to PATSUR file (old surname file).  *
.*        V5.08.03  05.10.2000 J.Tan                                          *
.*                  Recompiled for WEB Report Scheduler                       *
.*        V5.08.02  05.10.2000 J.Tan                                          *
.*                  Fixed printing DRG                                        *
.*        V5.08.01  16/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.B01 15/03/2000  Greg Horvat                                   *
.*                  Changed to get the coding from the episode coding files   *
.*        V5.07.02  24/11/1999  Davin                                         *
.*                  Changed for 4 character drgs                              *
.*        V5.07.01  19/10/1999  Steve Downing  SRF 646137                     *
.*                  Added PTCNDCQS - Single column for doctor code search     *
.*       V5.07.B02  19/11/1998  Jill Habasque                                 *
.*                  Mods to include centuries                                 *
.*        V5.04.01  14/06/1997  Steve Armstrong                               *
.*                  Fixed bugs in global recompile                            *
.*        V5.03.02  29/11/1995  Greg Horvat      SRF 640740                   *
.*                  Changed to report all patients who have been readmitted   *
.*        V5.03.01  03/08/1995    Justin Coates  SRF 610991                   *
.*                  increment line counter so paginates properly              *
.*        V5.03.B2  29/05/1995  ROD                                           *
.*                  print blank line b/w each patient (not each line)         *
.*        V5.02.00  19/04/1995  Paul Howells     SRF 134943                   *
.*                  Ported from 6.03                                          *
.*        V6.03.01  28/03/1995  Greg Horvat      SRF 135765                   *
.*                  Changed to report patients who are currently discharged   *
.*                  also                                                      *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
          INC       NZHISIFD/INC
          INC       PATCALAG/INC
          INC       PATCOMM/INC
          INC       PATDHEAD/INC
          INC       PMIVARS/INC
.
          INC       BOKRC1FD/INC            * Booking master file
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       NHIMASFD/INC            * NHI master file
          INC       OUTPREFD/INC            * Outpatients pre attendance file
          INC       PATALRFD/INC            * Patients alerts file
          INC       PATCONFD/INC            * Parameters file
          INC       PATCODFD/INC            * Codes file
          INC       PATDO1FD/INC            * Doctors file
          INC       PATDSCFD/INC            * Discharge file
          INC       PATECDFD/INC            * Patient episode disease file
          INC       PATGSRFD/INC            * Surname / Given Name File
          INC       PATICDFD/INC            * ICD-9CM disease file
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC            * Patient master file
          INC       PATMI1FD/INC            * Patient admission file
          INC       PATPR1FD/INC            * Patient pre admission file
          INC       PATTRNFD/INC            * Patient transfer file
          INC       PATVISFD/INC            * Visit file
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
          INC       SCRPSTFD/INC
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
. ----- Tempfile Definition -----
.
TEMP1     IFILE     SQL, FIXED=209, KEY="U1-8,119-126,9-12"
TEMP2     IFILE     SQL, FIXED=209, KEY="U127-134,1-8,119-126,9-12"
TEMP3     IFILE     SQL, FIXED=209, KEY="U193-198,1-8,119-126,9-12"
TEMP4     IFILE     SQL, FIXED=209, KEY="U119-126"
.
FILELINE  INIT      " 209 U1-8,119-126,9-12 U127-134,1-8,119-126,9-12 ":
                    "U193-198,1-8,119-126,9-12 U119-126"
.
.Name     Type      Length   Physical   Start   Description
.----     ----      ------   --------   -----   -----------
URNO      DIM       8        8          1       U/R number
DTMPCONT  DIM       4        4          9       Tempfile counter
PATNAME   DIM       18       18         13      Patient name
CURADMIN  DIM       8        8          31      Current admission number
CURADDTE  DIM       8        8          39      Current admission date(ccyymmdd)
CURCODE   DIM       41       41         47      Current principal diagnosis
CURATYPE  DIM       3        3          88      Current admission type
CURWARD   DIM       3        3          91      Current ward
CURITENT  DIM       3        3          94      Current intention
CURDSDTE  DIM       8        8          97      Current discharge date(ccyymmdd)
CURDSDOC  DIM       6        6         105      Current discharge doctor
CURDRG    DIM       4        4         111      Current DRG
CDYSDAYS  FORM      5        4         115      Diff. between from & to dates
PREADMIN  DIM       8        8         119      Previous admission number
PREADDTE  DIM       8        8         127      Previous admiss date(ccyymmdd)
PRECODE   DIM       41       41        135      Previous principal diagnosis
PREATYPE  DIM       3        3         176      Previous admission type
PREWARD   DIM       3        3         179      Previous ward
PREITENT  DIM       3        3         182      Previous intention
PREDSDTE  DIM       8        8         185      Previous dischge date(ccyymmdd)
PREDSDOC  DIM       6        6         193      Previous discharge doctor
PREDRG    DIM       4        4         199      Previous DRG
PPLREADM  DIM       3        3         203      Planned Re-Admission for Previous Admission number
CPLREADM  DIM       3        3         206      Planned Re-Admission for Current Admission number
.
.End of Record                         209
.
. ----- Program Variables -----
.
BJDAY     FORM      3
BLNKLINE  FORM      1                       * Print a blank line flag
.
CJDAY     FORM      3
CDYSFDTE  DIM       8                       * From date for CALCDAYS
CDYSTDTE  DIM       8                       * To date for CALCDAYS
CDYSYEAR  FORM      2                       * For CALCDAYS
CMDLINE   DIM       100
CODE      DIM       2
COUNT     FORM      2
CURDATE   DIM       8                       * Current date (ccyymmdd)
CDIM39    DIM       39                      * Dummy variable Principle diagnosis for Current Admn.
.
DIM3      DIM       3
DIM3A     DIM       3
DIM8      DIM       8
DIM25     DIM       25
DOCCODE   DIM       6                       * Doctor code
DOCDESC   DIM       22                      * Doctor description
DSPEDDOC  DIM       6                       * Display ending doctor
DSPEDDTE  DIM       10                       * Display ending date
DSPEDINR  DIM       6                       * Display ending intent to readmit
DSPEDUR   DIM       8                       * Display ending U/R number
DSPSTDOC  DIM       6                       * Display starting doctor
DSPSTDTE  DIM       10                      * Display starting date
DSPSTINR  DIM       6                       * Display starting intent to readmit
DSPSTUR   DIM       8                       * Display starting U/R number
.
ENDDATE   DIM       8                       * Ending date (ccyymmdd)
ENDDOCT   DIM       6                       * Ending doctor code
ENDINRA   DIM       3                       * Ending intent to re admit
ENDURNO   DIM       8                       * Ending U/R number
.
FNAME     DIM       8                       * Tempfile name
FORM3     FORM      3
FORM8     FORM      8
FRSTGNAM  DIM       40
FULLGNAM  DIM       80
.
IBCNMHOS  FORM      1
INTENT    DIM       3                       * Readmission intention
KIHOSPID  DIM       3
KIHSNAME  DIM       35
PDIM39    DIM       39                      * Dummy variable Principle diagnosis for Previous Admn
.
PATCOUNT  FORM      3                       * Patient report counter
PRINCODE  DIM       50                      * Principal diagnosis
.
READMDAY  FORM      2                       * Number of re-admission days
.
SAVKEY16  DIM       16                      * Save admission file key
SCNDGNAM  DIM       40
SOPTION   FORM      2                       * Sub option
STRTDATE  DIM       8                       * Starting date (ccyymmdd)
STRTDOCT  DIM       6                       * Starting doctor code
STRTINRA  DIM       3                       * Starting intent to re admit
STRTURNO  DIM       8                       * Starting U/R number
.
TMPCOUNT  FORM      4                       * Tempfile counter
.
. ----- Program Constants -----
.
CODEA     INIT      "A "
CODED1    INIT      "D1"
CODED2    INIT      "D2"
CODED3    INIT      "D3"
CODED4    INIT      "D4"
CODED5    INIT      "D5"
CODEU6    INIT      "U6"
CODEU7    INIT      "U7"
CODEVR    INIT      "VR"
.
OPTION1   INIT      " - Summary Report   "
OPTION2   INIT      " - Detailed Report  "
.
SP100     INIT      "                                                  ":
                    "                                                  "
.
ZED24     INIT      "zzzzzzzzzzzzzzzzzzzzzzzz"
.
TEST      FORM      1
.
PRGID     INIT      "IBAINP79"
PRGNAM    INIT      "Re-Admissions Within X Days Report"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                  ML0000                                    *
.*                                Main Module                                 *
.******************************************************************************
ML0000    CALL      INIT0000                * Initialize variables & open files
ML1000    CALL      OPTN0000                * Choose option
          BRANCH    OPTION,ML2000,ML5000
          BRANCH    EXIT,ML9000
.
. ----- Summary Report -----
.
ML2000    CALL      DPSS0000                * Display summary screen 
          CALL      DAYS0000                * Keyin number of re-admission days
          BRANCH    EXIT,ML1000
.
ML2500    CALL      BLNK0000                * Print a blank line (Y/N) ?
          BRANCH    EXIT,ML2000
.
ML3000    CALL      DATE0000                * Keyin the admission date range
          BRANCH    EXIT,ML2500
.
          CALL      CONTQST                 * Ok to Continue (Y/N/C) ?
          BRANCH    CEXIT,ML3500,ML3000,ML1000
.                         Yes    No     Cancel
.
ML3500    CALL      LODS0000                * Load summary tempfile
          CALL      PRTS0000                * Print summary details
          GOTO      ML1000
.
. ----- Detailed Report -----
.
ML5000    CALL      SUBO0000                * By U/R number, surname or doctor
          BRANCH    EXIT,ML1000
.
          CALL      DPDS0000                * Display detailed screen 
ML5500    CALL      DAYS0000                * Keyin number of re-admission days
          BRANCH    EXIT,ML5000
.
ML6000    CALL      BLNK0000                * Print a blank line (Y/N) ?
          BRANCH    EXIT,ML5500
.
ML6500    CALL      DATE0000                * Keyin the discharge date range
          BRANCH    EXIT,ML6000
.
ML7000    CALL      DOCT0000                * Keyin doctor code range
          BRANCH    EXIT,ML6500
.
ML7500    CALL      URNO0000                * Keyin U/R number range
          BRANCH    EXIT,ML7000
.
ML8000    CALL      INRA0000                * Keyin intent to readmit
          BRANCH    EXIT,ML7500
.
          CALL      CONTQST                 * Ok to Continue (Y/N/C) ?
          BRANCH    CEXIT,ML8500,ML8000,ML5000
.                         Yes    No     Cancel
.
ML8500    CALL      LODD0000                * Load detailed tempfile
          CALL      PRTD0000                * Print detailed details
          GOTO      ML5000
.
ML9000    CALL      KILL0000                * Delete tempfile
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                 INIT0000               Called by: ML0000   *
.*                     Initialize Variables & Open Files                      *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen header
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"nhimasaf";
          OPEN      NHIMASA1,"nhimasaf"
.
          DISPLAY   *P64:24,"outpreaf";
          OPEN      OUTPREA1,"outpreaf"
.
          DISPLAY   *P64:24,"patalrtf";
          OPEN      PATALRT1,"patalrtf"
.
          DISPLAY   *P64:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"patecdaf";
          OPEN      PATECDA2,"patecdaf"
.
          DISPLAY   *P64:24,"patgsrnf";
          OPEN      PATGSRN2,"patgsrnf"
.
          CALL      OPICD1
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A3,"patmi1af"
.
          DISPLAY   *P64:24,"patpramf";
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN1,"pattranf"
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA2,"patvisaf"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TEN;*40,PTCNINRA
          READ      CONTROLF,TWENTY1;*209,PTCNADMT,*247,PTCNDCQS
          READ      CONTROLF,EIGHTY;*250,PTCNHDPS
.         
          IF        IBCNMHOS = 1
            DISPLAY   *P64:24,"pathspaf";
            OPEN      PATHSPA1,"pathspaf"
          ENDIF
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAME
.
.         WEB will always have the intent re-admit display on the screen
.
          MATCH     "IBARSH99",PGM
          IF        @EQUAL
            MOVE      ONE,PTCNINRA        * always using intention to re-admit
            IF        PTCNADMT=0
              MOVE       ONE,PTCNADMT     * default to user defined field one
            ENDIF
          ENDIF
.
          CLOCK     TIME,CTIMEIS
          MOVE      ONE,CNOUNDLN
          PACK      CURDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURDATE
          MOVE      ONE,CNEWDS
INIT9999  RETURN
+
.******************************************************************************
.*                                 OPTN0000               Called by: ML0000   *
.*                               Choose Option                                *
.******************************************************************************
OPTN0000  MOVE      ZERO,OPTION
          DISPLAY   *P56:2,*EF
          HLINE     *G37,2,57,80
          DISPLAY   *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Summary Report":
                    *P1:6,*V2LON,"2",*HOFF," = Detailed Report ":
                    "(Discharged Re-Admissions Only)":
                    *P1:8,*HOFF,"Select Option : ";
.
OPTN1000  KEYIN     *P17:8,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN5000,OPTN5000
.
          BEEP
          GOTO      OPTN1000
.
OPTN5000  LOAD      DIM19,OPTION,OPTION1,OPTION2
          DISPLAY   *P56:2,*V2LON,DIM19
          LOAD      CPHDROPT,OPTION,OPTION1,OPTION2
.
OPTN6000  UNPACK    SP30,KIHOSPID,KIHSNAME
          IF        IBCNMHOS = 1
            MOVE      "09",CVERT
            MOVE      "18",CCOL
            CALL      KYHOSPID                * keyin Hospital Id.
            BRANCH    EXIT,OPTN9000
.
            PACK      KIHOSPID,PTHSHOSP,SP10  * Hospital Id.
            PACK      KIHSNAME,PTHSNAME,SP35  * Hospital Name
          ELSE
            KEYIN     *P1:09,"Key any value  : ",*P18:09,*V2LON,ANS;
          ENDIF
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************

.*                             Choose Sub Option                              *
.******************************************************************************
SUBO0000  MOVE      ZERO,SOPTION
          DISPLAY   *P1:4,*EF,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = By U/R Number":
                    *P1:6,*V2LON,"2",*HOFF," = By Doctor":
                    *P1:8,*HOFF,"Select Option : ";
.
SUBO1000  KEYIN     *P17:8,*V2LON,SOPTION;
.
          COMPARE   ZERO,SOPTION
          GOTO      SUBO9500 IF EQUAL
.
          BRANCH    SOPTION,SUBO9000,SUBO9000
.
          BEEP
          GOTO      SUBO1000
.
SUBO9000  MOVE      ZERO,EXIT
          GOTO      SUBO9999
.
SUBO9500  MOVE      ONE,EXIT
SUBO9999  RETURN
+
.******************************************************************************
.*                                 DPSS0000               Called by: ML0000   *
.*                          Display Summary Screen                            *
.******************************************************************************
DPSS0000  DISPLAY   *P1:10,*EF:
                    *P1:10,"Number of Re-Admission Days :":
                    *P1:11,"Print a Blank Line between patient line (":
                           *V2LON,ANSY,*HOFF,"/",*V2LON,"N",*HOFF,") ?":
                    *P1:13,"Admission Date From :":
                    *P1:14,"Admission Date To   :";
DPSS9999  RETURN
+
.******************************************************************************
.*                                 DPDS0000               Called by: ML0000   *
.*                          Display Detailed Screen                           *
.******************************************************************************
DPDS0000  DISPLAY   *P1:10,*EF:
                    *P1:10,"Number of Re-Admission Days :":
                    *P1:11,"Print a Blank Line between each patient (":
                           *V2LON,ANSY,*HOFF,"/",*V2LON,"N",*HOFF,") ?":
                    *P1:13,"Discharge Date From :":
                    *P1:14,"Discharge Date To   :":
                    *P1:16,"Doctor Range From   :":
                    *P1:17,"Doctor Range To     :":
                    *P1:19,"U/R Number From     :":
                    *P1:20,"U/R Number To       :";
          IF        PTCNINRA=1 & PTCNADMT>0
            DISPLAY   *P1:22,"Intent Re-Admit From:":
                      *P1:23,"Intent Re-Admit To  :";
          ENDIF
DPDS9999  RETURN
+
.******************************************************************************
.*                                 DAYS0000               Called by: ML0000   *
.*                    Keyin The Number Of Re-Admission Days                   *
.******************************************************************************
DAYS0000  MOVE      "10",CVERT
          DISPLAY   *P31:CVERT,*EL;
DAYS1000  MOVE      "28",DIM2
          KEYIN     *P31:CVERT,*V2LON,*RV,DIM2;
.
          TYPE      DIM2
          GOTO      DAYS2000 IF EQUAL       * Numeric value
.
          ENDSET    DIM2
          APPEND    SP2,DIM2
          RESET     DIM2
.
          CMATCH    EXITCHAR,DIM2
          GOTO      DAYS9500 IF EQUAL       * Exit from question
.
          MATCH     SP2,DIM2
          GOTO      DAYS9500 IF EQUAL       * Exit from question
.
          BEEP
          GOTO      DAYS1000
.
DAYS2000  MOVE      DIM2,READMDAY
          IF        READMDAY<0 | READMDAY>99
            BEEP
            GOTO      DAYS1000
          ENDIF
.
          DISPLAY   *P31:CVERT,*V2LON,READMDAY;
          MOVE      ZERO,EXIT
          GOTO      DAYS9999
.
DAYS9500  DISPLAY   *P31:CVERT,*EL;
          MOVE      ONE,EXIT
DAYS9999  RETURN
+
.******************************************************************************
.*                                 BLNK0000               Called by: ML0000   *
.*                         Print A Blank Line (Y/N) ?                         *
.******************************************************************************
BLNK0000  MOVE      ZERO,BLNKLINE
          MOVE      "11",CVERT
          DISPLAY   *P49:CVERT,*EL;
BLNK1000  MOVE      ANSN,ANS
          KEYIN     *P49:CVERT,*V2LON,*RV,ANS;
          REP       UPPLOW,ANS
.
          ENDSET    ANS
          APPEND    SP1,ANS
          RESET     ANS
.
          CMATCH    EXITCHAR,ANS
          GOTO      BLNK9500 IF EQUAL       * Exit from question
.
          MATCH     SP1,ANS
          GOTO      BLNK9500 IF EQUAL       * Exit from question
.
          MATCH     ANSY,ANS
          GOTO      BLNK2000 IF EQUAL       * Blank line
.
          MATCH     ANSN,ANS
          GOTO      BLNK3000 IF EQUAL       * No blank line
.
          BEEP
          GOTO      BLNK1000
.
BLNK2000  MOVE      ONE,BLNKLINE            * Blank line
          GOTO      BLNK9000
.
BLNK3000  MOVE      ZERO,BLNKLINE           * No blank line
BLNK9000  MOVE      ZERO,EXIT
          GOTO      BLNK9999
.
BLNK9500  DISPLAY   *P49:CVERT,*EL;
          MOVE      ONE,EXIT
BLNK9999  RETURN
+
.******************************************************************************
.*                                 DATE0000               Called by: ML0000   *
.*                             Keyin Date Range                               *
.******************************************************************************
DATE0000  MOVE      "23",CCOL
          MOVE      ONE,CCANLDTE            * Cancel default date
.
. ----- Enter Starting Date -----
.
DATE1000  MOVE      "13",CVERT
          DISPLAY   *PCCOL:CVERT,*EL;
          ADD       ONE,CVERT
          DISPLAY   *PCCOL:CVERT,*EL;
          SUB       ONE,CVERT
          MOVE      CCC,CCENT
          UNPACK    SP6,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin the starting date
          BRANCH    OVRCD,DATE9500
.
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY     * Starting date range
          REP       " 0",STRTDATE
          CALL      PACDATE                 * Pack the starting date
          MOVE      CPCDATE,DSPSTDTE
          REP       " 0",DSPSTDTE
.
          MATCH     STRTDATE,CURDATE
          IF        @LESS
            DISPLAY    *P1:24,*EL,*B,"Keyed in date is greater than today's ":
                       "date. ";
            CALL       HITENTER
            GOTO       DATE1000
          ENDIF
.
. ----- Enter Ending Date -----
.
DATE2000  MOVE      "14",CVERT
          DISPLAY   *PCCOL:CVERT,*EL;
          CALL      KEYDATE                 * Keyin the ending date
          BRANCH    OVRCD,DATE1000
.
          PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY * Ending date range
          REP       " 0",ENDDATE
          CALL      PACDATE                 * Pack the ending date
          MOVE      CPCDATE,DSPEDDTE
          REP       " 0",DSPEDDTE
.
          MATCH     ENDDATE,CURDATE
          IF        @LESS
            DISPLAY    *P1:24,*EL,*B,"Keyed in date is greater than today's ":
                       "date. ";
            CALL       HITENTER
            GOTO       DATE2000
          ENDIF
.
          MATCH     STRTDATE,ENDDATE
          GOTO      DATE9000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Invalid date range. ";
          CALL      HITENTER
          GOTO      DATE1000
.
DATE9000  MOVE      ZERO,EXIT
          GOTO      DATE9999
.
DATE9500  MOVE      ONE,EXIT
DATE9999  RETURN
+
.******************************************************************************
.*                                 DOCT0000               Called by: ML0000   *
.*                          Keyin Doctor Code Range                           *
.******************************************************************************
DOCT0000  MOVE      "23",ECOL               * Set up parameters
          MOVE      SP6,CKYIDEF6
          MOVE      ZERO,CKYIMAND
.
. ----- Starting Doctor Code -----
.
DOCT1000  MOVE      "16",EVERT
          DISPLAY   *PECOL:EVERT,*EL;
          ADD       ONE,EVERT
          DISPLAY   *PECOL:EVERT,*EL;
          SUB       ONE,EVERT
          CALL      PATDOCKY                * Keyin the doctor code
          BRANCH    EXIT,DOCT2000,DOCT9500
.                        Space    Exit
.
          MOVE      DOCCODE,STRTDOCT        * Starting doctor code
          CALL      FDNM0000                * Format doctor name
          DISPLAY   *P32:EVERT,PACFNAME
          MOVE      DOCCODE,DSPSTDOC
          GOTO      DOCT3000
.
DOCT2000  MOVE      SP6,STRTDOCT            * Starting doctor code
          MOVE      "Start",DSPSTDOC
          DISPLAY   *PECOL:EVERT,*V2LON,DSPSTDOC;
.
. ----- Ending Doctor Code -----
.
DOCT3000  MOVE      "17",EVERT
          DISPLAY   *PECOL:EVERT,*EL;
          CALL      PATDOCKY                * Keyin the doctor code
          BRANCH    EXIT,DOCT5000,DOCT1000
.                        Space    Exit
.
          MOVE      DOCCODE,ENDDOCT         * Ending doctor code
          MATCH     STRTDOCT,ENDDOCT
          GOTO      DOCT4000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Invalid doctor code range. ";
          CALL      HITENTER
          GOTO      DOCT1000
.
DOCT4000  CALL      FDNM0000                * Format doctor name
          DISPLAY   *P32:EVERT,PACFNAME
          MOVE      DOCCODE,DSPEDDOC
          GOTO      DOCT9000
.
DOCT5000  MOVE      "zzzzzz",ENDDOCT        * Ending doctor code
          MOVE      "Finish  ",DSPEDDOC
          DISPLAY   *PECOL:EVERT,*V2LON,DSPEDDOC;
.
DOCT9000  MOVE      ZERO,EXIT
          GOTO      DOCT9999
.
DOCT9500  DISPLAY   *PECOL:EVERT,*EL;
          MOVE      ONE,EXIT
DOCT9999  RETURN
+
.******************************************************************************
.*                                 FDNM0000               Called by: DOCT0000 *
.*                            Format Doctors Name                             *
.******************************************************************************
FDNM0000  MOVE      DTITL,PACTITLE
          MOVE      DSNAME,PACSNAME
          MOVE      DGNAME,PACGNAME
          MOVE      TWO,PACFRMT
          CALL      PACNAME
FDNM9999  RETURN
+
.******************************************************************************
.*                                 URNO0000               Called by: ML0000   *
.*                          Keyin U/R Number Range                            *
.******************************************************************************
URNO0000  MOVE      "23",CCOL
          MOVE      "2",SRCHOPT
          MOVE      "0",SRCHSYS
.
. ----- Starting U/R Number -----
.
URNO1000  MOVE      "19",CVERT
          DISPLAY   *PCCOL:CVERT,*EL;
          ADD       ONE,CVERT
          DISPLAY   *PCCOL:CVERT,*EL;
          SUB       ONE,CVERT
          CALL      KURN                    * Keyin the U/R number
          BRANCH    EXIT,URNO3000
.
          COMPARE   ONE,OVRCD
          GOTO      URNO2000 IF NOT EQUAL   * Valid U/R number
.
          SCAN      EXITCHAR,PURNO
          GOTO      URNO9500 IF EQUAL       * Exitchar entered
.
          DISPLAY   *P1:24,*EL,*B,"Invalid or missing U/R number. ";
          CALL      HITENTER
          GOTO      URNO1000
.
URNO2000  MOVE      PURNO,STRTURNO          * Starting U/R number
          MOVE      PURNO,DSPSTUR
          GOTO      URNO4000
.
URNO3000  MOVE      SP8,STRTURNO            * Starting U/R number
          MOVE      "Start   ",DSPSTUR
          DISPLAY   *PCCOL:CVERT,*EL,*V2LON,DSPSTUR;
.
. ----- Ending U/R Number -----
.
URNO4000  MOVE      "20",CVERT
          DISPLAY   *PCCOL:CVERT,*EL;
          CALL      KURN                    * Keyin the U/R number
          BRANCH    EXIT,URNO6000
.
          COMPARE   ONE,OVRCD
          GOTO      URNO5000 IF NOT EQUAL   * Valid U/R number
.
          SCAN      EXITCHAR,PURNO
          GOTO      URNO1000 IF EQUAL       * Exitchar entered
.
          DISPLAY   *P1:24,*EL,*B,"Invalid or missing U/R number. ";
          CALL      HITENTER
          GOTO      URNO4000
.
URNO5000  MOVE      PURNO,ENDURNO           * Ending U/R number
          MOVE      PURNO,DSPEDUR
          GOTO      URNO9000
.
URNO6000  MOVE      "zzzzzzzz",ENDURNO      * Ending U/R number
          MOVE      "Finish  ",DSPEDUR
          DISPLAY   *PCCOL:CVERT,*EL,*V2LON,DSPEDUR;
.
URNO9000  MOVE      ZERO,EXIT
          GOTO      URNO9999
.
URNO9500  DISPLAY   *PCCOL:CVERT,*EL;
          MOVE      ONE,EXIT
URNO9999  RETURN
+
.******************************************************************************
.*                                 INRA0000               Called by: ML0000   *
.*                          Keyin Intent To Readmit                           *
.******************************************************************************
INRA0000  COMPARE   ONE,PTCNINRA
          GOTO      INRA9999 IF NOT EQUAL 
.
          MOVE      "23",ECOL
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND
          LOAD      CODE,PTCNADMT,CODED1,CODED2,CODED3,CODED4,CODED5:
                                  CODEU6,CODEU7,CODEVR
.
. ----- Starting Intent To Readmit -----
.
INRA1000  MOVE      "22",EVERT
          DISPLAY   *PECOL:EVERT,*EL
          ADD       ONE,EVERT
          DISPLAY   *PECOL:EVERT,*EL;
          SUB       ONE,EVERT
          CALL      PATCODKY                * Keyin intent to readmit
          BRANCH    EXIT,INRA2000,INRA9500
.                        Space    Exit
.
          MOVE      ACODE,STRTINRA          * Starting intent to readmit
          MOVE      TDESC,DSPSTINR
          DISPLAY   *P32:EVERT,*V2LON,DSPSTINR;
          GOTO      INRA3000
.
INRA2000  MOVE      SP10,STRTINRA           * Starting intent to readmit
          MOVE      "Start ",DSPSTINR
          DISPLAY   *P23:EVERT,*V2LON,DSPSTINR;
.
. ----- Ending Intent To Readmit -----
.
INRA3000  MOVE      "23",EVERT
          DISPLAY   *PECOL:EVERT,*EL;
          CALL      PATCODKY                * Keyin intent to readmit
          BRANCH    EXIT,INRA5000,INRA1000
.                        Space    Exit
.
          MOVE      ACODE,ENDINRA           * Ending intent to readmit
          MATCH     STRTINRA,ENDINRA
          GOTO      INRA4000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Invalid intent to readmit range. ";
          CALL      HITENTER
          GOTO      INRA1000
.
INRA4000  MOVE      TDESC,DSPEDINR
          DISPLAY   *P32:EVERT,DSPEDINR;
          GOTO      INRA9000
.
INRA5000  MOVE      "zzzzzzzz",ENDINRA      * Ending intent to readmit
          MOVE      "Finish",DSPEDINR
          DISPLAY   *P23:EVERT,*V2LON,DSPEDINR;
.
INRA9000  MOVE      ZERO,EXIT
          GOTO      INRA9999
.
INRA9500  DISPLAY   *PECOL:EVERT,*EL;
          MOVE      ONE,EXIT
INRA9999  RETURN
+
.******************************************************************************
.*                                 LODS0000               Called by: ML0000   *
.*                             Load The Tempfile                              *
.******************************************************************************
LODS0000  MOVE      ZERO,EXIT
          MOVE      ONE,TEST
          MOVE      ZERO,TMPCOUNT
          CALL      OPNT0000                * Open the tempfile
          DISPLAY   *P1:24,*EL,"Scanning : ",*P35:24,"Loading : ";
.
. ----- Get A Current Admission -----
.
          PACK      KEY16,STRTDATE,SP20
          CALL      RDSMISS3                * Position on  & read the admission
LODS1000  CALL      RDKMISS3                  file
          BRANCH    OVRCD,LODS9999
.
          MATCH     ADATE,ENDDATE
          GOTO      LODS9999 IF LESS        * Out of date range, exit
.
          UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format admission date
          DISPLAY   *P12:24,*V2LON,AURNO,SP4,CPCDATE;
.
          IF        ASTAT=1 | ASTAT=5
            GOTO      LODS1000              * Ignore pre admitted or cancelled
          ENDIF
.
          PACK      KEY8,AADMNO,SP70
          CALL      RDPMVX11                * Read the visit extension file
          BRANCH    OVRCD,LODS1000
.         
          MATCH     SP3,KIHOSPID                                      *I-160842
          IF        !@EQUAL & IBCNMHOS = 1
            MATCH     KIHOSPID,PMVXMHOS     * check if current Hospital
            GOTO      LODS1000 IF NOT EQUAL
          ENDIF
.
          CALL      CHKX0000                * Check for admin type exclusion
          BRANCH    EXIT,LODS1000
.
          CALL      GADD0000                * Get admission principal diagnosis
          CALL      GCTD0000                * Get current details
.
.         Get Planned Re-Admission for Current Admission number
.
           CALL      CPLRM000
           BRANCH    EXIT,LODS1000
.
. ----- Get A Previous Admission For Current U/R -----
.
          PACK      SAVKEY16,CURADDTE,CURADMIN
          PACK      KEY24,AURNO,CURADDTE,CURADMIN
          CALL      RDAVISA2                * Position on the visit file
LODS2000  CALL      RDPVISA2                * Read the visit file
          BRANCH    OVRCD,LODS3000
.
          MATCH     AURNO,PVIURNO
          GOTO      LODS3000 IF NOT EQUAL   * Different U/R, exit
.
          MOVE      CURADMIN,DIM8 
          MATCH     DIM8,PVIBILL
          GOTO      LODS2000 IF EQUAL       * Original admission
.
          COMPARE   "3",PVITYPE
          GOTO      LODS2000 IF NOT EQUAL   * Not an inpatient, get next admin
.
          PACK      KEY8,PVIBILL
          CALL      RDMISS1                 * Read the admission file
          BRANCH    OVRCD,LODS2000
.
          PACK      KEY8,PVIBILL
          CALL      RDPMVX11                * Read the visit extension file
          BRANCH    OVRCD,LODS2000
.         
          MATCH     SP3,KIHOSPID                                      *I-160842
          IF        !@EQUAL & IBCNMHOS = 1
            MATCH     KIHOSPID,PMVXMHOS     * check if current Hospital
            GOTO      LODS2000 IF NOT EQUAL
          ENDIF
.
          COMPARE   THREE,ASTAT
          GOTO      LODS2000 IF NOT EQUAL   * Admission must be discharged
.
          PACK      KEY8,AADMNO
          CALL      RDTEMP4                 * Check to see if previous admin
          COMPARE   ONE,OVRCD                 already exists in tempfile
          GOTO      LODS2000 IF NOT EQUAL   * Previous admission already exists
.
          PACK      KEY8,AURNO
          CALL      RDMAST1                 * Read the master file
          BRANCH    OVRCD,LODS2000
.
          PACK      KEY8,AADMNO
          CALL      RDDSCH1                 * Get previous discharge record
          BRANCH    OVRCD,LODS2000
.
          PACK      CDYSFDTE,DDATE   * Previous discharge date
          PACK      CDYSTDTE,CURADDTE   * Current admission date
          CALL      CALCDAYS                * Calculate diff between 2 date
          SUB       ONE,CDYSDAYS
          IF        CDYSDAYS<0 | CDYSDAYS>READMDAY
            GOTO      LODS2000              * Out of re admission range
          ENDIF
.
          MOVE      PURNO,URNO              * U/R number
          ADD       ONE,TMPCOUNT            * Tempfile counter
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,PATNAME        * Patients name
.
          CALL      GDSD0000                * Get discharge principal diagnosis
          CALL      GRSD0000                * Get previous details
.
          PACK      KEY20,URNO,PREADMIN,TMPCOUNT
          CALL      RATEMP1                 * Read the 1st tempfile
          IF        OVRCD=1
            CALL      WRTEMP1               * Write to the 1st tempfile
            DISPLAY   *P45:24,*V2LON,URNO;
          ENDIF
.
. ----- Reposition On Original Admission -----
.
LODS3000  MOVE      SAVKEY16,KEY16
          CALL      RDMISS3
          GOTO      LODS1000
.
LODS9999  RETURN
+
.******************************************************************************
.*                                 LODD0000               Called by: ML0000   *
.*                             Load The Tempfile                              *
.******************************************************************************
LODD0000  MOVE      ZERO,EXIT
          MOVE      ZERO,TMPCOUNT
          CALL      OPNT0000                * Open the tempfile
          DISPLAY   *P1:24,*EL,"Scanning : ",*P35:24,"Loading : ";
.
. ----- Get A Current Discharge -----
.
          PACK      KEY16,STRTDATE,SP20
          CALL      RDSDSCH2                * Position on & read the discharge
LODD1000  CALL      RDKDSCH2                  file
          BRANCH    OVRCD,LODD9999
.
          MATCH     DDATE,ENDDATE
          GOTO      LODD9999 IF LESS        * Out of date range, exit
.
          UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format discharge date
          DISPLAY   *P12:24,*V2LON,DURNO,SP4,CPCDATE;
.
          MATCH     STRTURNO,DURNO
          GOTO      LODD1000 IF LESS        * Out of U/R range
.
          MATCH     DURNO,ENDURNO
          GOTO      LODD1000 IF LESS        * Out of U/R range
.
          PACK      KEY8,DADMNO
          CALL      RDMISS1                 * Read the admission file
          BRANCH    OVRCD,LODD1000
.
          PACK      KEY8,DADMNO
          CALL      RDPMVX11                * Read the visit extension file
          BRANCH    OVRCD,LODD1000
.
          MATCH     SP3,KIHOSPID                                      *I-160842
          IF        !@EQUAL & IBCNMHOS = 1
            MATCH     KIHOSPID,PMVXMHOS     * check if current Hospital
            GOTO      LODD1000 IF NOT EQUAL
          ENDIF
.
          CALL      CAIR0000                * Check discharge is in range
          BRANCH    EXIT,LODD1000
.
          IF        ASTAT=1 | ASTAT=5
            GOTO      LODD1000              * Ignore pre admitted or cancelled
          ENDIF
.
          CALL      CHKX0000                * Check for admin type exclusion
          BRANCH    EXIT,LODD1000
.
          CALL      GDSD0000                * Get discharge principal diagnosis
          CALL      GCTD0000                * Get current details
.
.         Get Planned Re-Admission for Current Admission number
.
           CALL      CPLRM000
           BRANCH    EXIT,LODD1000
.
. ----- Get A Previous Admission For Current U/R -----
.
          PACK      SAVKEY16,CURDSDTE,CURADMIN
          PACK      KEY24,AURNO,CURADDTE,CURADMIN
          CALL      RDAVISA2                * Position on the visit file
LODD2000  CALL      RDPVISA2                * Read the visit file
          BRANCH    OVRCD,LODD3000
.
          MATCH     AURNO,PVIURNO
          GOTO      LODD3000 IF NOT EQUAL   * Different U/R, exit
.
          MOVE      CURADMIN,DIM8
          MATCH     DIM8,PVIBILL
          GOTO      LODD2000 IF EQUAL       * Original admission
.
          COMPARE   "3",PVITYPE
          GOTO      LODD2000 IF NOT EQUAL   * Not an inpatient, get next admin
.
          PACK      KEY8,PVIBILL
          CALL      RDMISS1                 * Read the admission file
          BRANCH    OVRCD,LODD2000
.
          PACK      KEY8,PVIBILL
          CALL      RDPMVX11                * Read the visit extension file
          BRANCH    OVRCD,LODD2000
.         
          MATCH     SP3,KIHOSPID                                      *I-160842
          IF        !@EQUAL & IBCNMHOS = 1
            MATCH     KIHOSPID,PMVXMHOS     * check if current Hospital
            GOTO      LODD2000 IF NOT EQUAL
          ENDIF
.
          COMPARE   THREE,ASTAT
          GOTO      LODD2000 IF NOT EQUAL   * Admission must be discharged
.
          PACK      KEY8,AADMNO
          CALL      RDTEMP4                 * Check to see if previous admin
          COMPARE   ONE,OVRCD                 already exists in tempfile
          GOTO      LODD2000 IF NOT EQUAL   * Previous admission already exists
.
          PACK      KEY8,AURNO
          CALL      RDMAST1                 * Read the master file
          BRANCH    OVRCD,LODD2000
.
          PACK      KEY8,AADMNO
          CALL      RDDSCH1                 * Get previous discharge record
          BRANCH    OVRCD,LODD2000
.
          CALL      CAIR0000                * Check discharge is in range
          BRANCH    EXIT,LODD2000
.
          PACK      CDYSFDTE,DDATE          * Previous discharge date
          PACK      CDYSTDTE,CURADDTE       * Current admission date
          CALL      CALCDAYS                * Calculate diff between 2 date
          SUB       ONE,CDYSDAYS
          IF        CDYSDAYS<0 | CDYSDAYS>READMDAY
            GOTO      LODD2000              * Out of re admission range
          ENDIF
.
          MOVE      PURNO,URNO              * U/R number
          ADD       ONE,TMPCOUNT            * Tempfile counter
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,PATNAME        * Patients name
.
          CALL      GDSD0000                * Get discharge principal diagnosis
          CALL      GRSD0000                * Get previous details
.
.
.         Get Planned Re-Admission for Previous Admission number
.
          CALL      PPLRM000
          BRANCH    EXIT,LODD2000
.
          PACK      KEY20,URNO,PREADMIN,TMPCOUNT
          CALL      RATEMP1                 * Read the 1st tempfile
          IF        OVRCD=1
            CALL      WRTEMP1               * Write to the 1st tempfile
            DISPLAY   *P45:24,*V2LON,URNO;
          ENDIF
.
. ----- Reposition On Original Discharge -----
.
LODD3000  MOVE      SAVKEY16,KEY16
          CALL      RDDSCH2
          GOTO      LODD1000
.
LODD9999  RETURN
+
.******************************************************************************
.*                                 CAIR0000               Called by: LODD0000 *
.*                      Check If Admission Is In Range                        *
.******************************************************************************
CAIR0000  PACK      KEY30,DADMNO,ZED24
          CALL      RDSTRAN2                * Position on & read the transfer
          CALL      RDPTRAN2                  file
          BRANCH    OVRCD,CAIR9500
.
          MATCH     TADMN,DADMNO
          GOTO      CAIR9500 IF NOT EQUAL   * Different discharge number
.
          MATCH     ANSD,TMOVE
          GOTO      CAIR9500 IF NOT EQUAL   * Patient not discharged
.
          PACK      KEY6,TADOCT
          CALL      RDDOCT1                 * Read the doctors file
          BRANCH    OVRCD,CAIR9500
.
          MATCH     STRTDOCT,TADOCT
          GOTO      CAIR9500 IF LESS        * Not in doctor range
.
          MATCH     TADOCT,ENDDOCT
          GOTO      CAIR9500 IF LESS        * Not in doctor range
.
          IF        PTCNINRA=1 & PTCNADMT>0
            MOVE      SP3,INTENT
            LOAD      INTENT,PTCNADMT,DUSD1,DUSD2,DUSD3,DUSD4,DUSD5:
                                      AUSR6,AUSR7,PMVXIRAD
            MATCH     STRTINRA,INTENT
            GOTO      CAIR9500 IF LESS      * Not in intention range
.
            MATCH     INTENT,ENDINRA
            GOTO      CAIR9500 IF LESS      * Not in intention range
          ENDIF
          MOVE      ZERO,EXIT
          GOTO      CAIR9999
.
CAIR9500  MOVE      ONE,EXIT
CAIR9999  RETURN
+
.******************************************************************************
.*                                 CHKX0000              Called by: LODS0000  *
.*                     Check For Admission Type Exclusion         & LODD0000  *
.******************************************************************************
CHKX0000  PACK      KEY5,CODEA,ATYPE
          CALL      RDCODE1                 * Read the codes file
          BRANCH    OVRCD,CHKX9500
.
          MATCH     ANSX,TCINDC5
          GOTO      CHKX9500 IF EQUAL       * Exclude admission type
.
          MOVE      ZERO,EXIT
          GOTO      CHKX9999
.
CHKX9500  MOVE      ONE,EXIT
CHKX9999  RETURN
+
.******************************************************************************
.*                                 GADD0000               Called by: LODS0000 *
.*                     Get Admission Principal Diagnosis                      *
.******************************************************************************
GADD0000  MOVE      SP100,PRINCODE
          CLEAR     PRINCODE
          MATCH     SP9,AMBS
          IF        !@EQUAL
            STRIP     AMBS
            APPEND    AMBS,PRINCODE         * Admission MBS code
            APPEND    SP1,PRINCODE
          ENDIF
          APPEND    ADIAG1,PRINCODE         * Admission admitting diagnosis
          RESET     PRINCODE                * Principal diagnosis
GADD9999  RETURN
+
.******************************************************************************
.*                                 GCTD0000              Called by: LODS0000  *
.*                            Get Current Details                 & LODD0000  *
.******************************************************************************
GCTD0000  UNPACK    SP30,URNO,PATNAME
          UNPACK    SP100,CURADMIN,CURADDTE,CURCODE,CURATYPE,CURWARD:
                          CURITENT,CURDSDTE,CURDSDOC,CURDRG
          MOVE      AADMNO,CURADMIN         * Current admission number
          MOVE      ADATE,CURADDTE          * Current admission date
          MOVE      PRINCODE,CURCODE        * Current principal diagnosis
          IF        OPTION=2
            MOVE      ATYPE,CURATYPE        * Current admission type
            MOVE      TWARD,CURWARD         * Current ward
            MOVE      INTENT,CURITENT       * Current intention
            MOVE      DDATE,CURDSDTE        * Current discharge date
            MOVE      TADOCT,CURDSDOC       * Current discharge doctor
            MOVE      PTDSDDRG,CURDRG       * Current DRG
          ENDIF
GCTD9999  RETURN
+
.******************************************************************************
.*                                 GDSD0000               Called by: LODS0000 *
.*                     Get Discharge Principal Diagnosis           & LODD0000 *
.******************************************************************************
GDSD0000  MOVE      SP100,PRINCODE
          PACK      KEY16,AADMNO,SP20                                 *C-240107
          CALL      RSPTECD2                * Position on & read a patient
          CALL      RKPTECD2                  episode disease file record
          BRANCH    OVRCD,GDSD9999
.
          MATCH     AADMNO,PTEDADMN
          GOTO      GDSD9999 IF NOT EQUAL
.
          CLEAR     PRINCODE
          STRIP     PTEDCODE
          APPEND    PTEDCODE,PRINCODE
          PACK      KEY9,PTEDCODE
.
          MOVE      CURDSDTE,ICDRDDTE
          CALL      RDPTICD1
          IF        OVRCD<>1
            APPEND    SP1,PRINCODE
            APPEND    DDESC,PRINCODE
          ENDIF
          RESET     PRINCODE                * Principal diagnosis
GDSD9999  RETURN
+
.******************************************************************************
.*                                 GRSD0000              Called by: LODS0000  *
.*                           Get Previous Details                 & LODD0000  *
.******************************************************************************
GRSD0000  UNPACK    SP100,PREADMIN,PREADDTE,PRECODE,PREATYPE,PREWARD:
                          PREITENT,PREDSDTE,PREDSDOC,PREDRG
          MOVE      AADMNO,PREADMIN         * Previous admission number
          MOVE      ADATE,PREADDTE          * Previous admission date
          MOVE      PRINCODE,PRECODE        * Previous principal diagnosis
          IF        OPTION=2
            MOVE      ATYPE,PREATYPE        * Previous admission type
            MOVE      TWARD,PREWARD         * Previous ward
            MOVE      INTENT,PREITENT       * Previous intention
            MOVE      TADOCT,PREDSDOC       * Previous discharge doctor
            MOVE      PTDSDDRG,PREDRG       * Previous DRG
          ENDIF
          MOVE      DDATE,PREDSDTE          * Previous discharge date
GRSD9999  RETURN
+
.******************************************************************************
.*                                 CPLRM000               Called by: LODS0000 *
.*        Get Planned Re-Admission for current Admission number    & LODD0000 *
.******************************************************************************
CPLRM000  MOVE      SP70,CPLREADM
          PACK      KEY8,AADMNO,SP70
          CALL      RDPMVX11
          BRANCH    OVRCD,CPLRM100
.
          IF        PTCNHDPS=1
            MOVE      AUSR6,CPLREADM
          ELSE
            MOVE      PMVXUREA,CPLREADM
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      CPLRM999
CPLRM100  MOVE      ONE,EXIT
.
CPLRM999  RETURN
.******************************************************************************
.*                                 PPLRM000               Called by: LODS0000 *
.*        Get Planned Re-Admission for Previous Admission number   & LODD0000 *
.******************************************************************************
PPLRM000  MOVE      SP70,PPLREADM
          PACK      KEY8,AADMNO,SP70
          CALL      RDPMVX11
          BRANCH    OVRCD,PPLRM100
          MOVE      PMVXUREA,PPLREADM
          MOVE      ZERO,EXIT
          GOTO      PPLRM999
PPLRM100  MOVE      ONE,EXIT
.
PPLRM999  RETURN
+
.******************************************************************************
.*                                 KYHOSPID                                   *
.*                            Key in Hospital Id.                             *
.******************************************************************************
KYHOSPID  MOVE      ZERO,EXIT
.         UNPACK    SP30,KIHOSPID,KIHSNAME
          DISPLAY   *P1:CVERT,"Hospital Id.  : ";
KYHOS100  MOVE      SP6,DIM3A
          KEYIN     *PCCOL:CVERT,*V2LON,DIM3A;
.
.
          PACK      KEY3,DIM3A,SP10
KYHOS200  MATCH     SP3,KEY3
          GOTO      KYHOS300 IF EQUAL
.
          MATCH     QUEST,KEY3
          IF        @EQUAL
            CALL      GETSCR00
            DISPLAY   *P1:4,*EF,*P35:4,"Hospital Details";
            MOVE      "6",EVERT
            PACK      KEY3,SP70
            CALL      RSPTHSP1
KYHOS210    CALL      RKPTHSP1
            BRANCH    OVRCD,KYHOS290
            DISPLAY   *P1:EVERT,PTHSHOSP,SP4,PTHSNAME;
            ADD       ONE,EVERT
            IF        ECOL < 22
              GOTO      KYHOS210
            ENDIF
.
KYHOS290    DISPLAY   *P1:24,*EL,"Hospital ID : ",UNDLN3;
            KEYIN     *P15:24,*V2LON,DIM3A;
            PACK      KEY3,DIM3A,SP10
            CALL      PUTSCR00
          ENDIF
.
KYHOS300  UNPACK    SP70,PTHSHOSP,PTHSNAME,PTHSAPPR
          MOVE      "All Hospitals",PTHSNAME
          CALL      RDPTHSP1
          DISPLAY   *PCCOL:CVERT,*V2LON,KEY3,SP4,PTHSNAME
          GOTO      KYHOS999
.
KYHOS950  MOVE      ONE,EXIT
.
KYHOS999  RETURN
+
.******************************************************************************
.*                                 PRTS0000               Called by: ML0000   *
.*                           Print Summary Details                            *
.******************************************************************************
PRTS0000  MOVE      ZERO,EXIT
          MOVE      ZERO,PATCOUNT
          DISPLAY   *P1:24,*EL,"Printing : ";
          MOVE      ZERO,CPAGENO
          CALL      HEDS0000                * Print summary header
          PACK      KEY26,SP30
          CALL      RSTEMP2                 * Position on & read the 2nd
          CALL      RKTEMP2                   tempfile
          BRANCH    OVRCD,PRTS9000
.
          CALL      UND132                  * Print an underline
          PACK      KEY26,SP30
          CALL      RSTEMP2                 * Position on & read the 2nd
PRTS1000  CALL      RKTEMP2                   tempfile
          BRANCH    OVRCD,PRTS9000
.
          IF        CLNO>55
            CALL      UND132                * Print an underline
            CALL      HEDS0000              * Print summary header
            CALL      UND132                * Print an underline
          ENDIF
.
.         Replaced No.of days admission column with Planned Re-Admission
.         Replace  CDYSDAYS - No.of days column with CPLREADM - Planned Re-Admission for current admission- print
.
          UNPACK    CURADDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format current admission date
          MOVE      CURCODE,DIM25
          PRINT     *1,"|",*2,URNO,*10,"|",*11,PATNAME,*29,"|",*30,CURADMIN:
                    *38,"|",*39,CPDATE,*47,"|",*48,DIM25:
                    *73,"|",*74,CPLREADM,*79,"|",*80,PREADMIN;
          UNPACK    PREADDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format previous admission date
          PRINT     *88,"|",*89,CPDATE;
          UNPACK    PREDSDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format previous discharge date
          MOVE      PRECODE,DIM25
          PRINT     *97,"|",*98,CPDATE,*106,"|",*107,DIM25,*132,"|"
          ADD       ONE,CLNO
          CALL      PSBL0000                * Print summary blank line
          ADD       ONE,PATCOUNT
          DISPLAY   *P12:24,*V2LON,URNO;
          GOTO      PRTS1000
.
PRTS9000  CALL      UND132                  * Print an underline
          PRINT     *1,"Total Patients : ",PATCOUNT,*N,*N:
                    *1,"*** End of Report ***"
.
          DISPLAY   *P1:24,*EL,*B,"Report Generation Complete. ";
          CALL      HITENTER
PRTS9999  RETURN
+
.******************************************************************************
.*                                 HEDS0000               Called by: PRTS0000 *
.*                           Print Summary Heading                            *
.******************************************************************************
HEDS0000  CALL      HEAD132                 * Print main report heading
          IF        IBCNMHOS = 1
            PRINT     *48,KIHSNAME,*N       * Hospital Name           *I-160842
            ADD       "2",CLNO
          ENDIF
.
          PRINT     *40,"Starting Admission Date : ",DSPSTDTE,*N:
                    *40,"Ending Admission Date   : ",DSPEDDTE,*N,*N:
                    *40,"Number of Days Before Re-Admission : ",READMDAY,*N
          ADD       "5",CLNO
          CALL      UND132                  * Print an underline
          PRINT     *1,"|U/R",*10,"|Patient",*29,"|Current",*38,"|Current":
                    *47,"|Current Admission",*73,"|Plan",*79,"|Previous":
                    *88,"|Previous",*97,"|Previous",*106,"|Previous":
                    *132,"|"
          PRINT     *1,"|Number",*10,"|Name",*29,"|Admin No",*38,"|Adm Date":
                    *47,"|Admitting Diagnosis",*73,"|ReAdm",*79,"|Admin No":
                    *88,"|Adm Date",*97,"|Dis Date",*106,"|Principal Diagnosis":
                    *132,"|"
          ADD       "2",CLNO
HEDS9999  RETURN
+
.******************************************************************************
.*                                 PSBL0000               Called by: PRTS0000 *
.*                         Print Summary Blank Line                           *
.******************************************************************************
PSBL0000  IF        BLNKLINE=1
            PRINT     *1,"|",*10,"|",*29,"|",*38,"|",*47,"|",*73,"|",*79,"|":
                      *88,"|",*97,"|",*106,"|",*132,"|"
            ADD       ONE,CLNO
          ENDIF
PSBL9999  RETURN
+
.******************************************************************************
.*                                 PRTD0000               Called by: ML0000   *
.*                             Print The Report                               *
.******************************************************************************
PRTD0000  MOVE      ZERO,EXIT
          MOVE      ZERO,PATCOUNT
          DISPLAY   *P1:24,*EL,"Printing : ";
          MOVE      ZERO,CPAGENO
          CALL      HEDD0000
          BRANCH    SOPTION,PRTD1000,PRTD2000
. 
. ----- U/R Sequence -----
.
PRTD1000  PACK      KEY20,SP30
          CALL      RSTEMP1                 * Position on  & read the 1st
          CALL      RKTEMP1                   tempfile
          BRANCH    OVRCD,PRTD9000
.
          CALL      UND132                  * Print an underline
          PACK      KEY20,SP30
          CALL      RSTEMP1                 * Position on the 1st tempfile
          GOTO      PRTD3000
. 
. ----- Doctor Sequence -----
.
PRTD2000  PACK      KEY26,SP30
          CALL      RSTEMP3                 * Position on & read the 3rd
          CALL      RKTEMP3                   tempfile
          BRANCH    OVRCD,PRTD9000
.
          CALL      UND132                  * Print an underline
          PACK      KEY26,SP30
          CALL      RSTEMP3                 * Position on the 3rd tempfile
.
PRTD3000  BRANCH    SOPTION,PRTD4000,PRTD5000
.
. ----- U/R Sequence -----
.
PRTD4000  CALL      RKTEMP1                 * Read the 1st tempfile
          BRANCH    OVRCD,PRTD9000
          GOTO      PRTD6000
.
. ----- Doctor Sequence -----
.
PRTD5000  CALL      RKTEMP3                 * Read the 3rd tempfile
          BRANCH    OVRCD,PRTD9000
.
PRTD6000  IF        CLNO>55
            CALL      UND132                * Print an underline
            CALL      HEDD0000              * Print the report heading
            CALL      UND132                * Print an underline
          ENDIF
.
          MOVE      CDYSDAYS,FORM3          * Readmit days
          MOVE      PREDSDOC,DOCCODE
          CALL      FDOC0000                * Format previous discharge doctor
.
          MOVE      CURCODE,CDIM39
          MOVE      PRECODE,PDIM39
.
.         Replaced No.of days admission column with Planned Re-Admission
.         Replace  FORM3 - No.of days column with CPLREADM - Planned Re-Admission for Current Admission and
.                                                 PPLREADM - Planned Re-Admission for Previous Admission. - print
.
          UNPACK    PREADDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format previous admission date
          PRINT     *1,"|",*2,URNO,*10,"|",*12,PREADMIN,*20,"|",*22,CPDATE;
          UNPACK    PREDSDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format previous admission date
          PRINT     *30,"|",*32,CPDATE,*40,"|",*42,PREATYPE:
                    *45,"|",*47,PREWARD,*50,"|",*52,DOCDESC:
                    *74,"|",*75,PREDRG,*79,"|",*81,PDIM39,*120,"|",*121,PPLREADM:
                    *126,"|",*127,PREITENT,*132,"|"
          ADD       ONE,CLNO                * Increment counter
.
          MOVE      CURDSDOC,DOCCODE
          CALL      FDOC0000                * Format current discharge doctor
.
          UNPACK    CURADDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format current admission date
          PRINT     *1,"|",*10,"|",*12,CURADMIN,*20,"|",*22,CPDATE;
          UNPACK    CURDSDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format current discharge date
          PRINT     *30,"|",*32,CPDATE,*40,"|",*42,CURATYPE:
                    *45,"|",*47,CURWARD,*50,"|",*52,DOCDESC:
                    *74,"|",*75,CURDRG,*79,"|",*81,CDIM39:
                    *120,"|",*121,CPLREADM,*126,"|",*127,CURITENT,*132,"|"
.
          ADD       ONE,CLNO   
          CALL      PDBL0000                * Print detailed blank line
          ADD       ONE,PATCOUNT
          DISPLAY   *P12:24,*V2LON,URNO;
          GOTO      PRTD3000
.
PRTD9000  CALL      UND132                  * Print an underline
          PRINT     *1,"Total Patients : ",PATCOUNT,*N,*N:
                    *1,"*** End of Report ***"
.
          DISPLAY   *P1:24,*EL,*B,"Report Generation Complete. ";
          CALL      HITENTER
PRTD9999  RETURN
.******************************************************************************
.*                                 HEDD0000               Called by: PRTD0000 *
.*                         Print The Report Heading                           *
.******************************************************************************
HEDD0000  CALL      HEAD132                 * Print main report heading
          IF        IBCNMHOS = 1
            PRINT     *48,KIHSNAME,*N       * Hospital Name           *I-160842
            ADD       "2",CLNO
          ENDIF
.
          PRINT     *40,"Discharge Date From : ",DSPSTDTE," to ",DSPEDDTE,*N:
                    *40,"Doctor Code    From : ",DSPSTDOC,"   to ",DSPEDDOC,*N:
                    *40,"U/R Number     From : ",DSPSTUR," to ",DSPEDUR,*N;
          ADD       "3",CLNO
          IF        PTCNINRA=1 & PTCNADMT>0
          PRINT     *40,"Intent Readmit From : ",DSPSTINR,"   to ",DSPEDINR,*N;
            ADD       "1",CLNO
          ENDIF
          PRINT     *40,"Number of Days Before Re-Admission : ",READMDAY,*N
          ADD       "2",CLNO
          CALL      UND132                  * Print an underline
          PRINT     *1,"|U/R",*10,"|Admission",*20,"|Admission":
                    *30,"|Discharge":
                    *40,"|Admn",*45,"|Ward",*50,"|Discharge Doctor":
                    *74,"|DRG",*79,"|Principle",*120,"|Plan",*126,"|Inten":
                    *132,"|"
          PRINT     *1,"|Number",*10,"|Number",*20,"|Date",*30,"|Date":
                    *40,"|Type",*45,"|",*50,"|":
                    *74,"|",*79,"|Diagnosis",*120,"|ReAdm",*126,"|Re-Ad":
                    *132,"|"
          ADD       "2",CLNO
HEDD9999  RETURN
+
.******************************************************************************
.*                                 FDOC0000               Called by: PRTD0000 *
.*                          Format The Doctors Name                           *
.******************************************************************************
FDOC0000  MOVE      "  <<Missing Doctor>>  ",DOCDESC
          PACK      KEY6,DOCCODE
          CALL      RDDOCT1
          IF        OVRCD=0
            CLEAR     DOCDESC
            RESET     DGNAME
            MOVE      DGNAME,DIM1 
            PACK      DOCDESC,DIM1,DOT,DSNAME,SP20
          ENDIF
FDOC9999  RETURN
+
.******************************************************************************
.*                                 PDBL0000               Called by: PRTD0000 *
.*                         Print Detailed Blank Line                          *
.******************************************************************************
PDBL0000  IF        BLNKLINE=1
            PRINT     *1,"|",*10,"|",*20,"|",*30,"|",*40,"|",*45,"|",*50,"|":
                      *74,"|",*79,"|",*122,"|",*126,"|",*132,"|"
            ADD       ONE,CLNO
          ENDIF
PDBL9999  RETURN
+
.******************************************************************************
.*                                 OPNT0000               Called by: LODS0000 *
.*                             Open The Tempfile                   & LODD0000 *
.******************************************************************************
OPNT0000  CALL      KILL0000                * Delete the tempfile
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME,CMDLINE
          APPEND    FILELINE,CMDLINE
          RESET     CMDLINE
          MOVE      "   ",TASKID
.
          EXECUTE   CMDLINE,TASKID          * Create the tempfile
          OPEN      TEMP1,FNAME
          OPEN      TEMP2,FNAME
          OPEN      TEMP3,FNAME
          OPEN      TEMP4,FNAME
OPNT9999  RETURN
+
.******************************************************************************
.*                                 KILL0000               Called by: ML0000   *
.*                            Delete The Tempfile                  & OPNT0000 *
.******************************************************************************
KILL0000  CLOSE     TEMP1
          CLOSE     TEMP2
          CLOSE     TEMP3
          CLOSE     TEMP4
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
KILL9999  RETURN
+
.******************************************************************************
.*                     Routines To Keep PATCOMN1.PBL Happy                    *
.******************************************************************************
GETSVAR   RETURN
.
REDISPS   DISPLAY   *P1:3,*EF,*P1:4,"U/R Number : ";
          RETURN
+
.******************************************************************************
.*                       I/O Routines For The Tempfile                        *
.******************************************************************************
RATEMP1   RESET     KEY20
          MOVE      ZERO,OVRCD
          READ      TEMP1,KEY20;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTEMP1   RESET     KEY20
          READ      TEMP1,KEY20;;
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMP1;URNO,DTMPCONT,PATNAME,CURADMIN,CURADDTE:
                          CURCODE,CURATYPE,CURWARD,CURITENT,CURDSDTE:
                          CURDSDOC,CURDRG,CDYSDAYS,PREADMIN,PREADDTE:
                          PRECODE,PREATYPE,PREWARD,PREITENT,PREDSDTE:
                          PREDSDOC,PREDRG,PPLREADM,CPLREADM
          GOTO      OVERCOND IF OVER
          MOVE      DTMPCONT,TMPCOUNT
          RETURN
.
WRTEMP1   RESET     KEY20
          MOVE      TMPCOUNT,DTMPCONT
          WRITE     TEMP1,KEY20;URNO,DTMPCONT,PATNAME,CURADMIN,CURADDTE:
                                CURCODE,CURATYPE,CURWARD,CURITENT,CURDSDTE:
                                CURDSDOC,CURDRG,CDYSDAYS,PREADMIN,PREADDTE:
                                PRECODE,PREATYPE,PREWARD,PREITENT,PREDSDTE:
                                PREDSDOC,PREDRG,PPLREADM,CPLREADM
          RETURN
.
RSTEMP2   RESET     KEY26
          READ      TEMP2,KEY26;;
          RETURN
.
RKTEMP2   MOVE      ZERO,OVRCD
          READKS    TEMP2;URNO,DTMPCONT,PATNAME,CURADMIN,CURADDTE:
                          CURCODE,CURATYPE,CURWARD,CURITENT,CURDSDTE:
                          CURDSDOC,CURDRG,CDYSDAYS,PREADMIN,PREADDTE:
                          PRECODE,PREATYPE,PREWARD,PREITENT,PREDSDTE:
                          PREDSDOC,PREDRG,PPLREADM,CPLREADM
          GOTO      OVERCOND IF OVER
          MOVE      DTMPCONT,TMPCOUNT
          RETURN
.
RSTEMP3   RESET     KEY26
          READ      TEMP3,KEY26;;
          RETURN
.
RKTEMP3   MOVE      ZERO,OVRCD
          READKS    TEMP3;URNO,DTMPCONT,PATNAME,CURADMIN,CURADDTE:
                          CURCODE,CURATYPE,CURWARD,CURITENT,CURDSDTE:
                          CURDSDOC,CURDRG,CDYSDAYS,PREADMIN,PREADDTE:
                          PRECODE,PREATYPE,PREWARD,PREITENT,PREDSDTE:
                          PREDSDOC,PREDRG,PPLREADM,CPLREADM
          GOTO      OVERCOND IF OVER
          MOVE      DTMPCONT,TMPCOUNT
          RETURN
.
RDTEMP4   RESET     KEY8
          MOVE      ZERO,OVRCD
          READ      TEMP4,KEY8;URNO,DTMPCONT,PATNAME,CURADMIN,CURADDTE:
                               CURCODE,CURATYPE,CURWARD,CURITENT,CURDSDTE:
                               CURDSDOC,CURDRG,CDYSDAYS,PREADMIN,PREADDTE:
                               PRECODE,PREATYPE,PREWARD,PREITENT,PREDSDTE:
                               PREDSDOC,PREDRG,PPLREADM,CPLREADM
          GOTO      OVERCOND IF OVER
          MOVE      DTMPCONT,TMPCOUNT
          RETURN
OPEN0000
CLOS0000  RETURN
.
.==============================================================================
.
          INC       STD001IO
          INC       CALCAGE                 * Calculate patients age
          INC       CALCDAYS                * Calculate diff between dates
          INC       CLPATMAS                * Clear master variables
          INC       NZIBSRCH                * Surname search on the NZ database
          INC       PATALERT                * Patient alerts
          INC       PATCODKY                * Input code
          INC       PATCOMN1                * Patient standard library - KURN
          INC       PATDOCKY                * Input doctor code
          INC       PATSNDX                 * Surname search
          INC       PATSNX2                 * Sound ex redefine
          INC       PMIHEAD                 * Display basic patients details
          INC       TFILENAM                     * Generate Temp File Name
.
          INC       BOKRC1IO/INC            * Booking master file
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       NHIMASIO/INC            * NHI master file
          INC       NZHISIIO                * Routines for NZ database
          INC       OUTPREIO/INC            * Outpatients pre attendance file
          INC       PATALRIO/INC            * Patients alerts file
          INC       PATCODIO/INC            * Codes file
          INC       PATDO1IO/INC            * Doctors file
          INC       PATDSCIO/INC            * Discharge file
          INC       PATECDIO/INC            * Patient episode disease file
          INC       PATGSRIO/INC       * Patient Surname / Given Name
          INC       PATICDIO/INC            * ICD-9CM disease file
          INC       PATHSPIO/INC
          INC       PATMA1IO/INC            * Patient master file
          INC       PATMI1IO/INC            * Patient admission file
          INC       PATPR1IO/INC            * Patient pre admission file
          INC       PATTRNIO/INC            * Patient transfer file
          INC       PATVISIO/INC            * Visit file
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
          INC       WEBERRIO/INC                 * Web Server Error Logging
+
.


.******************************************************************************
.*                                                                            *
.*                        P R O G R A M   S U M M A R Y                       *
.*                        -------------   -------------                       *
.*                                                                            *
.*     PROGRAM NAME:    IBAINP52                                              *
.*                                                                            *
.*     FUNCTION:        PATIENT DAY STATISTICS by Claim Code                  *
.*                                                                            *
.*     DATE WRITTEN:    15/10/1998  Ported from 5.06 for Mersey               *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.*        V12.00.01 21.05.2025  DA Sarkies       TSK 0905641                  *
.*                  Updated for Alpha-Numeric Visit Number                    *
.*        V10.03.01 28/12/2012 Patrick Adair  CAR 261630                      *
.*                  Removed port number from temp file name                   *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V9.12.00  04/11/2009  Mike Laming   CAR 205980                      *
.*                  Ported from V6.17
.*        V6.07.B01 11/04/2000  Greg Horvat                                   *
.*                  Removed MATADAFD MATADBFD MATADBIO MATTRAFD & MATTRAIO    *
.*                      V6.05.02  08/12/1998  Nicole Harrington               *
.*                                Removed CCENTRY                             *
.*                      V6.05.01  03/12/1998  Nicole Harrington               *
.*                      Fixed printing of dates                               *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
.
          INC       IBACONFD/INC
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATDAYFD/INC
          INC       PATDSCFD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATTRNFD/INC     * for onleave date
          INC       HL7BMTFD/INC
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
+
.
.         Temporary File 1 Definition
.         -------------------------
.
PATMP1XX  IFILE SQL, FIXED=9, KEY="u1-8"
.
.NAME     TYPE    LENGTH     PHYSICAL     START     DESCRIPTION
.----     ----    ------     --------     -----     -----------
.AADMNO   DIM       8           8           1       Admission Number
.
.End of Record                              9
.
.
.         Temporary File 2 Definition
.         -------------------------
.
PSTIDZ1   IFILE     SQL, FIXED=33, KEY="U1-8,30-32,9-11"
PSTIDZ2   IFILE     SQL, FIXED=33, KEY="U30-32,9-11,1-8"
.
.NAME     TYPE   LENGTH   PHYSICAL  START LOC.   DESCRIPTION
.----     ----   ------   --------  ----------   -----------
ZDATE     DIM         8          8           1   DATE OF STATISTICS (CCYYMMDD)
ZTYPE     DIM         3          3           9   ADMISSION TYPE CODE
.
ZADMNS    FORM        6          4          12   NUMBER OF ADMISSIONS
ZDSCHS    FORM        6          4          16   NUMBER OF DISCHARGES
ZBDAYS    FORM        8          5          20   NUMBER OF BED DAYS
ZDCASES   FORM        7          5          25   NUMBER OF DAY CASES
ZCLAIM    DIM         3          3          30   Claim Code
.
.End of Record                              33
.
. Extra temp file variables
.
SZDATE    DIM       8        DATE OF STATISTICS (CCYYMMDD)
SZTYPE    DIM       3        ADMISSION TYPE CODE
SZCLAIM   DIM       3        CLAIM CODE
.
.
. PROGRAM VARIABLES
. -----------------
.
AADMNS    FORM      6
ABDAYS    FORM      8
ADCASES   FORM      7
ADSCHS    FORM      6
.
CDYSDAYS  FORM      6
CDYSFDTE  DIM       8
CDYSTDTE  DIM       8
CDYSYEAR  FORM      2
CMDLINE   DIM       80
.
DBLLINE   FORM      1                 * double line print flag
.                                            0 = print double line
.                                            1 = print single line
DDCLAIM   DIM       20
DDTYPE    DIM       20
DIM8      DIM       8
.
FDAY      DIM       2
FMON      DIM       2
FYEAR     DIM       2
FCENT     DIM       2
FORM82    FORM      8.2
FROMDATE  DIM       8        * from date in (ccyymmdd) format
FFDATE    DIM       10
FTDATE    DIM       10
.
IDAY1     FORM      2 
IDAY2     FORM      2
IMON1     FORM      2
IMON2     FORM      2
IYEAR1    FORM      2
IYEAR2    FORM      2
ICENT1    FORM      2
ICENT2    FORM      2
IDATEF    DIM       8
.
IDATET    DIM       8
JDAYS     FORM      3
JYEAR     FORM      2
.
LYR       FORM      1
.
NADMNS    FORM      6
NBDAYS    FORM      8
NDCASES   FORM      7
NDSCHS    FORM      6
.
OPCND     FORM      1
.
SDAYMON   DIM       4
STRTFLG   FORM      1
SYEAR     DIM       4
.
TADMNS    FORM      6
TBDAYS    FORM      8
TDAYMON   DIM       4
TDCASES   FORM      7
TDSCHS    FORM      6
TEMPFIL1  DIM       8
TEMPFIL2  DIM       8
TMPADMN   DIM       8
TODATE    DIM       8        * to date in (ccyymmdd) format
TYEAR     DIM       4
.
XDAY1     DIM       2
XDAY2     DIM       2
XMON1     DIM       2
XMON2     DIM       2
XYEAR1    DIM       2
XYEAR2    DIM       2
XCENT1    DIM       2
XCENT2    DIM       2
.
.
. PROGRAMS CONSTANTS
. ------------------
.
CODEA     INIT      "A "
CODECL    INIT      "CL"
DETHEAD   INIT      " - Detail"
ERASE     INIT      "iserase "
ISBUILD   INIT      "isbuild "
SUMHEAD   INIT      " - Summary"
UKEY1     INIT      " 9 U1-8"
UKEY2     INIT      " 33 U1-8,30-32,9-11 U30-32,9-11,1-8"
.
.
PRGID     INIT      "IBAINP52"
PRGNAM    INIT      "Patient Day Statistics by Claim Code"
VERSION   INIT      "V12.01.00"
.
.         Start of Program
.
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,SEVENTY9;*115,PTCNDGUP,*116,PTCNDGUA,*239,PTCNDGAD:
                                      *240,PTCNDGDS,*241,PTCNDGTR,*242,PTCNDGRG
          READ      CONTROLF,TEN5;*188,CMATRN
          MOVE      ONE,CNOUNDLN
.
          CALL      CREA0000                     * create temp file
.
.         Start of Program
.
START     MOVE      ZERO,CPAGENO
          MOVE      SP1,ANS
.
          DISPLAY   *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Detailed Report":
                    *P1:6,*V2LON,TWO,*HOFF," = Summary Report":
                    *P1:8,"Select Option :";
.
START1    KEYIN     *P17:8,*EL,*DV,UNDLN1,*P17:8,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      ENDIT IF EQUAL
.
          LOAD      CPHDROPT,OPTION,DETHEAD,SUMHEAD
.
          BRANCH    OPTION,KDDATE,KDDATE
          BEEP
          GOTO      START1
.
.         Keyin date range
.
KDDATE    DISPLAY   *P1:10,*EF,"From Date : ":
                    *P1:11,"To   Date : "
.
.         ** From Date
.
KADAT1    UNPACK    SP8,CDAY,CMON,CYEAR,CCENT
          MOVE      "13",CCOL
          MOVE      "10",CVERT
          DISPLAY   *P13:10,*EL,*P13:11,*EL
          READ      CONTROLF,TEN5;*188,CMATRN
.
          CALL      KEYDATE
          BRANCH    OVRCD,START
.
          PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",FROMDATE
          UNPACK    FROMDATE,XCENT1,XYEAR1,XMON1,XDAY1
.
.         ** To Date
.
KADAT2    MOVE      "11",CVERT
          UNPACK    SP8,CDAY,CMON,CYEAR,CCENT
          CALL      KEYDATE
          BRANCH    OVRCD,ENDAD2R
.
          PACK      TODATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE
          UNPACK    TODATE,XCENT2,XYEAR2,XMON2,XDAY2
          GOTO      CHTODT
.
ENDAD2R   MOVE      FROMDATE,TODATE
          UNPACK    TODATE,XCENT2,XYEAR2,XMON2,XDAY2
          DISPLAY   *P13:11,*EL,*V2LON,XDAY2,SLASH,XMON2,SLASH,XCENT2,XYEAR2
.
.         Check if date range is ok or not
.
CHTODT    PACK      XDATE,CCC,CYY,CMM,CDD
          REP       " 0",XDATE
          MATCH     TODATE,XDATE
          GOTO      INVDATS IF LESS
.
          MATCH     FROMDATE,TODATE        * 'To' date must be >= 'From' date
          GOTO      INVDATG IF LESS              
.
          MOVE      XDAY1,IDAY1
          MOVE      XMON1,IMON1
          MOVE      XYEAR1,IYEAR1
          MOVE      XCENT1,ICENT1
          MOVE      XDAY2,IDAY2
          MOVE      XMON2,IMON2
          MOVE      XYEAR2,IYEAR2
          MOVE      XCENT2,ICENT2
          GOTO      REKEY
.
INVDATS   DISPLAY   *P1:24,*EL,*B,"To Date Must be Less Than or ":
                                         "Equal to Today's Date. ";
          CALL      HITENTER
          GOTO      KADAT2
.
INVDATG   DISPLAY   *P1:24,*EL,*B,"To Date Must be Greater Than or ":
                                         "Equal to From Date. ";
          CALL      HITENTER
          GOTO      KADAT1
.
.         Accept date range ?
.
REKEY     CALL      POSTQST
          BRANCH    CEXIT,CORDATE,KADAT1,START
.
.         Calculate the number of days in the period
.
CORDATE   CALL      CLER0000                     * remove temp file records
          MOVE      FROMDATE,CDYSFDTE
          MOVE      TODATE,CDYSTDTE
          CALL      CALCDAYS
.
          MOVE      ZERO,ZADMNS
          MOVE      ZERO,ZDSCHS
          MOVE      ZERO,ZBDAYS
          MOVE      ZERO,ZDCASES
.
.         Data extraction
.
          CALL      DAYA0000        * load the temp file from the patdayaf file
.
          DISPLAY   *P1:24,*EL,*V2LON,"** ",*BLINKON,"Generating Report":
                    *HOFF,*V2LON," **",*HOFF:
                    *P35:24,"Admission :",*P60:24,"Scanning :";
.
.         Read through the indexed temp file and process each admission 
.         Position on the temp file 
.
          MOVE      SP8,KEY8
          CALL      RSTMPR1
NEXTADM   CALL      RKTMPR1
          BRANCH    OVRCD,READDA
.
          MOVE      TMPADMN,AADMNO
          MOVE      AADMNO,KEY8
          CALL      RDMISS1              * read the admissions file
          BRANCH    OVRCD,NEXTADM
.
          MATCH     ADATE,TODATE         * make sure admission is in date range
          GOTO      NEXTADM IF LESS
.
          DISPLAY   *P71:24,AADMNO;
          MOVE      SP8,DDATE
.
          MOVE      ACLAIM,ZCLAIM
.
          BRANCH    ASTAT,NEXTADM,CONTADM,CONTDIS,CONTADM,NEXTADM
.
CONTDIS   MOVE      AADMNO,KEY8
          CALL      RDDSCH1              * read discharge file
          BRANCH    OVRCD,NEXTADM
.
          MATCH     FROMDATE,DDATE       * make sure discharge is in date range
          GOTO      NEXTADM IF LESS        
.
.         We have a valid admission so extract details & write to summary file 
.
CONTADM   DISPLAY   *P47:24,*V2LON,AADMNO;
          CALL      TRANS
          GOTO      NEXTADM
+
.**********************************************************************
.*                            DAYA0000                                *
.*     Process patdayaf Data writing to temp indexed file patmp1xx    *
.**********************************************************************
.
DAYA0000  UNPACK    TODATE,CCENT,CYEAR,TDAYMON
          PACK      TYEAR,CCENT,CYEAR
          REP       " 0",TYEAR
.
          UNPACK    FROMDATE,CCENT,CYEAR,SDAYMON
          PACK      SYEAR,CCENT,CYEAR
          REP       " 0",SYEAR
.
          DISPLAY   *P1:24,*EL,"Processing admission:";
.
.------ close previous day file and set up the name of the next one ------
.
DAYA1000  CLOSE     PATDAYA1
          PACK      CFNAME,FILDAYA1,SYEAR
.
          TRAP      DAYA4900 IF IO        * trap if file does not exist
          OPEN      PATDAYA1,CFNAME
          TRAPCLR   IO
.
          PACK      KEY12,SDAYMON,SP10    * position on daily patient list file
          CALL      RSPTDAY1
.
.------ read through the patient daily list file ------
.
DAYA2000  CALL      RKPTDAY1
          BRANCH    OVRCD,DAYA5000
.
          MATCH     TYEAR,SYEAR            * see if the current file is the one
          GOTO      DAYA3000 IF NOT EQUAL    for the last year in the date range
.
          MATCH     PTDYDATE,TDAYMON       * if so, make sure the month and day
          GOTO      DAYA9999 IF LESS         are still in range
.
.------ Only need to write to the temporary file if the admission number -----
.------ is not already there ------
.
DAYA3000  MOVE      PTDYADMN,KEY8
          CALL      RDTMPR1                * read the temp file
          BRANCH    OVRCD,DAYA4000
          GOTO      DAYA2000
.
.------ write a new record to the temp file ------
.
DAYA4000  CALL      WRTMPR1
          DISPLAY   *P23:24,*EL,*V2LON,PTDYADMN;
          GOTO      DAYA2000
.
.------ need to pop the return address off the stack if I/O error trapped -----
.
DAYA4900  NORETURN
.
.------ either file does not exist or we have reached the end of the ------
.------ current file ------
.
DAYA5000  MATCH     TYEAR,SYEAR             * test if we have reached the end 
          GOTO      DAYA9999 IF NOT LESS      year in the date range
.
          MOVE      SYEAR,FORM4             * if not, increment the start year
          ADD       ONE,FORM4                 to the next year and process
          MOVE      FORM4,SYEAR               the file for this year
          REP       " 0",SYEAR
          MOVE      "0101",SDAYMON
.
          GOTO      DAYA1000
.
DAYA9999  RETURN
+
.**********************************************************************
.
.         Subroutine to process the transfer file for this person
.
TRANS     MOVE      FROMDATE,IDATEF
          MOVE      SP1,STMOVE
          MOVE      ATYPE,STATYPE
.
          PACK      KEY30,AADMNO,SP30
          CALL      RDSTRAN2
NEXTTRN   CALL      RDKTRAN2
          BRANCH    OVRCD OF ENDTRN
.
          MATCH     TADMN,AADMNO
          GOTO      ENDTRN IF NOT EQUAL
.
.         TMOVE means A=Admitted
.                     D=Discharged
.                     C=Changed Details
.                     L=Leave Hospital
.                     R=Return to Hospital
.
          MOVE      TDATE,IDATET
.
.         If this is an Admission or Return record, then just save the details
.
          CMATCH    ANSA,TMOVE
          GOTO      SAVETRN IF EQUAL
.
          CMATCH    ANSR,TMOVE
          GOTO      SAVETRN IF EQUAL
.
.         If this is not a Change record, then write/display a record
.
          CMATCH    ANSC,TMOVE
          GOTO      FNDTRN IF NOT EQUAL
.
.         If the Admission type has not changed, then ignore the record
.
          MATCH     TATYPE,STATYPE
          GOTO      NEXTTRN IF EQUAL
.
.         If this records date is greater than or equal to the starting date,
.         then generate an accomodation record.
.
FNDTRN    MATCH     IDATEF,IDATET
          CALL      CALC IF NOT LESS
.
.         If this is the discharge record, then we have finished
.
          CMATCH    ANSD,TMOVE
          RETURN    IF EQUAL
.
.         Save the Admission type
.
SAVETRN   MOVE      TATYPE,STATYPE
          MOVE      TMOVE,STMOVE
          MOVE      TDATE,IDATEF
.
.         Is this starting date less than the start date of the period?
.
          MATCH     FROMDATE,IDATEF
          GOTO      NEXTTRN IF NOT LESS
.
          MOVE      FROMDATE,IDATEF
          GOTO      NEXTTRN
.
.         End of this admissions records in transfer file. No Discharge record
.
ENDTRN    COMPARE   FOUR,ASTAT
          RETURN    IF EQUAL            * Last record was an "L" record
.
          MOVE      "99999999",IDATET     * Dummy Value
          MOVE      "99999999",TDATE      * Dummy Value
          MOVE      SP1,TMOVE             * Dummy Value
.
          MATCH     IDATEF,IDATET
          CALL      CALC IF NOT LESS
          RETURN
+
.
.         calculates the statistics for the date range specified - Option 1
.
CALC      MOVE      STATYPE,ZTYPE
.
          MATCH     IDATEF,IDATET
          GOTO      NEXTZ IF NOT EQUAL
.
.         The only same day periods we are interested in are those that
.         include the "A" record and/or the "D" record.
.
.         As a special case, we are only interested in the one with the
.         "D" record if this is a day case (eg A-C, C-D should ignore the
.         A-C for day cases).
.
          CMATCH    ANSD,TMOVE
          GOTO      NEXTZ IF EQUAL
.
          MATCH     ADATE,DDATE
          RETURN    IF EQUAL
.
          CMATCH    ANSA,STMOVE
          RETURN    IF NOT EQUAL
.
.         Loop over the date range, and for each day calculate the stats
.
NEXTZ     MATCH     IDATEF,TODATE
          RETURN    IF LESS
.
.         initialize the data values for this date
.
          MOVE      ZERO,NADMNS
          MOVE      ZERO,NDCASES
          MOVE      ZERO,NDSCHS
          MOVE      ONE,NBDAYS
.
.         Check if this is the last date in the period
.
          MATCH     IDATEF,TDATE
          GOTO      CNTAA IF NOT EQUAL
.
.         Check if this is the Discharge record
.
          CMATCH    ANSD,TMOVE
          GOTO      CNTAAD IF EQUAL
.
.         Check if this is an On-leave record or a Change record
.
          CMATCH    ANSC,TMOVE
          GOTO      CNTAAL IF EQUAL
.
          CMATCH    ANSL,TMOVE
          GOTO      CNTAAL IF EQUAL
          GOTO      CNTAA
.
.         Discharged today - not a bed day
.
CNTAAD    MOVE      ONE,NDSCHS
.
.         If we went on leave today, or if we changed admission type today
.         then don't count this as a bed day (for change in admission type,
.         today is the start of the next period).
.
CNTAAL    MOVE      ZERO,NBDAYS
.
.         Check if this is the admission date
.
CNTAA     UNPACK    ADATE,XCENT,XYEAR,XMON,XDAY
          PACK      XDATE,XCENT,XYEAR,XMON,XDAY
          REP       " 0",XDATE
.
          MATCH     XDATE,IDATEF
          GOTO      WRDDD IF NOT EQUAL
.
.         Patient admitted today. Check if they were also discharged today
.
          MATCH     ADATE,DDATE
          GOTO      CHKAMVE IF NOT EQUAL
.
.         Day case - Note test above which means that we are processing the
.                    "D" record. This guarentee's this code is only run once.
.
          MOVE      ONE,NDCASES
          MOVE      ZERO,NBDAYS
          MOVE      ONE,NADMNS
          GOTO      WRDDD
.
.         Make sure this is the period that starts with the A record
.         This is to allow for the case A-L-R all on the admission date
.
CHKAMVE   CMATCH    ANSA,STMOVE
          GOTO      WRDDD IF NOT EQUAL
.
          MOVE      ONE,NADMNS
.
.         Update statistics file
.
WRDDD     MOVE      IDATEF,ZDATE
          PACK      KEY14,ZDATE,ZCLAIM,ZTYPE
          CALL      RDZIND1
          BRANCH    OVRCD OF WRZZZ
.
          ADD       NDSCHS,ZDSCHS
          ADD       NDCASES,ZDCASES
          ADD       NADMNS,ZADMNS
          ADD       NBDAYS,ZBDAYS
          CALL      UPZIND1
          GOTO      CNTACCR
.
.         Write a new record to statistics file
.
WRZZZ     MOVE      NADMNS,ZADMNS
          MOVE      NDSCHS,ZDSCHS
          MOVE      NBDAYS,ZBDAYS
          MOVE      NDCASES,ZDCASES
          CALL      WRZIND1
.
.         Calculate the next date, and check if it is still in date range
.
CNTACCR   MATCH     IDATEF,IDATET
          RETURN    IF EQUAL
.
          CALL      CALCDTE
.
.         Check if this date is in range
.
          MATCH     IDATEF,IDATET
          RETURN    IF LESS
.
          GOTO      NEXTZ
.
.         Recalculate date as date plus 1 day.
.
CALCDTE   UNPACK    IDATEF,FCENT,FYEAR,FMON,FDAY
          MOVE      FDAY,DD
          MOVE      FMON,MM
          MOVE      FYEAR,YY
          MOVE      FCENT,CC
          CALL      DMYCON
.
.         Add one to the julian date, and convert back to yymmdd
.
          ADD       ONE,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON
.
          PACK      IDATEF,CC,YY,MM,DD
          REP       " 0",IDATEF
          UNPACK    IDATEF,FCENT,FYEAR,FMON,FDAY
          RETURN
+
.
.         Read and print data from temp indexed file
.
READDA    IF        OPTION = 2
            CALL      SUMM0000
            GOTO      START
          ELSE
            CALL      DETL0000
            GOTO      START
          ENDIF
+
.
.         Last line on the page
.
PAGEND    IF        OPTION=2 
            PRINT     *9,"*--------------------------------------------------":
                      "-------------------------------------------------------*"
          ELSE
            PRINT     *1,"*----------------------------------------------":
                         "-----------------------------------------------":
                         "-------------------------------*"
          ENDIF
          ADD       ONE,CLNO
          RETURN
.
.         Page Separator
.
PAGTOT    IF        OPTION=2 
            PRINT     *19,"|         |                      |----------------":
                      "-------------------------------------------|"
          ELSE
            PRINT     *1,"|                 |":
                         "                     |":
                          "                      |":
                          "----------------":
                          "---------------------------------------------|"
          ENDIF
          ADD       ONE,CLNO
          RETURN
.
.         Page Separator
.
PAGSEP    IF        OPTION=2 
            PRINT     *19,"|--------------------------------------------------":
                      "--------------------------------------------|"
          ELSE
            PRINT     *1,"|----------------------------------------------":
                         "-----------------------------------------------":
                         "-------------------------------|"
          ENDIF
          ADD       ONE,CLNO
          RETURN
.
.         Total Separator
.
PAGASK    IF        OPTION=2 
            PRINT     *9,"*==================================================":
                      "=======================================================*"
          ELSE
            PRINT     *1,"*==============================================":
                         "===============================================":
                         "===============================*"
          ENDIF
          ADD       ONE,CLNO
          RETURN
+
.****************************************************************************
.*                               DETL0000              Called by:           *
.*                  Process and print detail report                         *
.****************************************************************************
.
DETL0000  CALL      IBACLOCK
          DISPLAY   *P35:24,*EL,*P40:24,"Generating for";
          MOVE      ONE,DBLLINE                  * dont print double line
          MOVE      ONE,CNOUNDLN                 * just print heading
          CALL      ZEROT                        * initialise grand totals
          CALL      SHED0000                     * print page header
          CALL      ZCLM0000                     * zero claim totals
.
          MOVE      SP3,SZTYPE                   * initialise admission type
          MOVE      SP3,SZCLAIM                  * initialise claim type
          MOVE      SP8,SZDATE                   * initialise date
.
          MOVE      SP20,KEY14                  
          CALL      RDSZIND1                     * position in file
          CALL      RDKZIND1                     * read next record
          BRANCH    OVRCD,DETL9500               * eof - finished
.
.         First record found
.
          CALL      UTYP0000                     * update adm. type totals
.
          MOVE      ZTYPE,SZTYPE                 * save adm. type
          MOVE      ZCLAIM,SZCLAIM               * save claim type
          MOVE      ZDATE,SZDATE                 * save date
          CALL      GCLM0000                     * get claim code description
          CALL      GADM0000                     * get ad. type description
.
DETL0500  CALL      RDKZIND1                     * read next record
          BRANCH    OVRCD,DETL9000               * eof - finished
.
.         Check if date has changed
.
          MATCH     SZDATE,ZDATE                 * same date ?
          GOTO      DETL1000 IF NOT EQUAL        * no
.
          MATCH     SZCLAIM,ZCLAIM               * same claim code ?
          GOTO      DETL1000 IF NOT EQUAL        * no
.
          MATCH     SZTYPE,ZTYPE                 * same adm. type ?
          IF        !@EQUAL
            CALL      UCLM0000                   * update claim totals
            CALL      DTOT0000                   * print prev, adm. type total
            CALL      ZADM0000                   * zero adm. type totals
            CALL      GADM0000                   * get adm. type description
            MOVE      ZTYPE,SZTYPE
          ENDIF
          GOTO      DETL2000
.
DETL1000  CALL      UCLM0000                     * update claim totals
          CALL      UGRD0000                     * update grand totals
          CALL      DTOT0000                     * print prev. adm. type total
          CALL      LTOT0000                     * print prev. claim total
          CALL      ZCLM0000                     * zero claim totals
          CALL      GCLM0000                     * get claim type description
          CALL      GADM0000                     * get adm. type description
          MOVE      ZDATE,SZDATE
          MOVE      ZCLAIM,SZCLAIM
          MOVE      ZTYPE,SZTYPE
          MOVE      ZERO,STRTFLG
.
DETL2000  CALL      UTYP0000                     * update running totals
.
          GOTO      DETL0500                     * get next temp record
.
DETL9000  MOVE      ZERO,DBLLINE                 * print double line
          CALL      UCLM0000                     * update claim totals
          CALL      UGRD0000                     * update gran totals
          CALL      DTOT0000                     * print prev. adm. type total
          CALL      LTOT0000                     * print prev. claim total
.
.         Print grand totals
.
          PACK      DIM8,ICENT1,IYEAR1,IMON1,IDAY1
          UNPACK    DIM8,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,FFDATE
          REP       " 0",FFDATE
          PACK      DIM8,ICENT2,IYEAR2,IMON2,IDAY2
          UNPACK    DIM8,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,FTDATE
          REP       " 0",FTDATE
          PRINT     *1,"|  TOTAL ",FFDATE:
                    " to ",FTDATE:
                    *64,"| ",TBDAYS,*81,"| ",TADMNS:
                    *96,"| ",TDSCHS,*111,"| ",TDCASES,*126,"|"
.
.         Calculate daily average
.
          MOVE      TBDAYS,FORM82
          DIV       CDYSDAYS,FORM82
.
          PRINT     *1,"|  Daily Average",*64,"| ",FORM82,*81,"|",*96,"|":
                    *111,"|",*126,"|"
          
          CALL      PAGASK
.
DETL9500  CALL      ENDR0000                     * end of report details
.
DETL9999  RETURN
+
.****************************************************************************
.*                               SUMM0000              Called by:           *
.*                  Process and print summary report                        *
.****************************************************************************
.
SUMM0000  CALL      IBACLOCK                     * get current date/time
          MOVE      ONE,DBLLINE                  * dont print double line
          MOVE      ONE,CNOUNDLN                 * just print heading
          CALL      SHED0000                     * print summary header
          CALL      ZEROT                        * initialise grand totals
          CALL      ZCLM0000                     * zero claim/adm type totals
.
          MOVE      SP3,SZTYPE                   * initialise admission type
          MOVE      SP3,SZCLAIM                  * initialise claim type
.
          MOVE      SP20,KEY14                  
          CALL      RDSZIND2                     * position in file
          CALL      RDKZIND2                     * read next record
          BRANCH    OVRCD,SUMM9500               * eof - finished
.
.         First record found
.
          CALL      UTYP0000                     * update running totals
.
          MOVE      ZTYPE,SZTYPE                 * save adm. type
          MOVE      ZCLAIM,SZCLAIM               * save claim type
          CALL      GCLM0000                     * get claim code description
          CALL      GADM0000                     * get ad. type description
.
SUMM0500  CALL      RDKZIND2                     * read next record
          BRANCH    OVRCD,SUMM9000               * eof - finished
.
.         Check if claim code has changed
.
          MATCH     SZCLAIM,ZCLAIM               * same claim code ?
          IF        !@EQUAL
            CALL      UCLM0000                   * update claim totals
            CALL      UGRD0000                   * update grand totals
            CALL      TTOT0000                   * print prev. adm. type total
            CALL      CTOT0000                   * print prev. claim total
            CALL      ZCLM0000                   * zero claim totals
            CALL      GCLM0000                   * get claim type description
            CALL      GADM0000                   * get adm. type description
            MOVE      ZCLAIM,SZCLAIM
            MOVE      ZTYPE,SZTYPE
            MOVE      ZERO,STRTFLG
          ELSE
            MATCH     SZTYPE,ZTYPE               * same adm. type ?
            IF        !@EQUAL
              CALL      UCLM0000                 * update claim totals
              CALL      TTOT0000                 * print prev, adm. type total
              CALL      ZADM0000                 * zero admission totals
              CALL      GADM0000                 * get adm. type description
              MOVE      ZTYPE,SZTYPE
            ENDIF
          ENDIF
.
          CALL      UTYP0000                     * update adm. type counts
.
          GOTO      SUMM0500                     * get next temp record
.
SUMM9000  MOVE      ZERO,DBLLINE                 * print double line
          CALL      UCLM0000                     * update claim totals
          CALL      UGRD0000                     * update gran totals
          CALL      TTOT0000                     * print prev. adm. type total
          CALL      CTOT0000                     * print prev. claim total
.
.         Print grand totals
.
          PACK      DIM8,ICENT1,IYEAR1,IMON1,IDAY1
          UNPACK    DIM8,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,FFDATE
          REP       " 0",FFDATE
          PACK      DIM8,ICENT2,IYEAR2,IMON2,IDAY2
          UNPACK    DIM8,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,FTDATE
          REP       " 0",FTDATE
          PRINT     *9,"|  TOTAL ",FFDATE:
                    " to ",FTDATE:
                    *55,"| ",TBDAYS,*70,"| ",TADMNS:
                    *85,"| ",TDSCHS,*100,"| ",TDCASES,*115,"|"
.
.         Calculate daily average
.
          MOVE      TBDAYS,FORM82
          DIV       CDYSDAYS,FORM82
.
          PRINT     *9,"|  Daily Average",*55,"| ",FORM82,*70,"|",*85,"|":
                    *100,"|",*115,"|"
          
          CALL      PAGASK
.
SUMM9500  CALL      ENDR0000                     * end of report details
.
SUMM9999  RETURN
+
.****************************************************************************
.*                            ENDR0000                 Called by: DETL0000  *
.*                 Print end of report details                    SUMM0000  *
.****************************************************************************
.
ENDR0000  PRINT     *N,*N,"  Note: The Bed Days column does not include":
                          " day cases":
                    *N,*N,"*** End of Report ***"
.
ENDR9999  RETURN
+
.****************************************************************************
.*                           UCLM0000                  Called by: SUMM0000  *
.*                Update claim code totals                        DETL0000  *
.****************************************************************************
.
UCLM0000  ADD       AADMNS,NADMNS
          ADD       ADSCHS,NDSCHS
          ADD       ABDAYS,NBDAYS
          ADD       ADCASES,NDCASES
.
UCLM9999  RETURN
+
.****************************************************************************
.*                          UTYP0000                   Called by: DETL0000  *
.*              Update adm. type totals for date                  SUMM0000  *
.****************************************************************************
.
UTYP0000  ADD       ZADMNS,AADMNS                * update running totals
          ADD       ZDSCHS,ADSCHS
          ADD       ZBDAYS,ABDAYS
          ADD       ZDCASES,ADCASES
.
UTYP9999  RETURN
+
.****************************************************************************
.*                           UGRD0000                  Called by: SUMM0000  *
.*                      Update grand totals                       DETL0000  *
.****************************************************************************
.
UGRD0000  ADD       NADMNS,TADMNS                * update grand totals
          ADD       NDSCHS,TDSCHS
          ADD       NBDAYS,TBDAYS
          ADD       NDCASES,TDCASES
.
UGRD9999  RETURN
+
.****************************************************************************
.*                             GCLM0000                Called by: SUMM0000  *
.*                     Get claim code description                 DETL0000  *
.****************************************************************************
.
GCLM0000  MOVE      "Unknown Claim Code",TDESC   * initialise description
          PACK      KEY5,CODECL,ZCLAIM
          CALL      RDCODE1
          MOVE      TDESC,DDCLAIM                * load description
.
GCLM9999  RETURN
+
.****************************************************************************
.*                             GADM0000                Called by: SUMM0000  *
.*                     Get admission type description             DETL0000  *
.****************************************************************************
.
GADM0000  MOVE      "Unknown Admiss. Type",TDESC * initialise description
          PACK      KEY5,CODEA,ZTYPE
          CALL      RDCODE1
          MOVE      TDESC,DDTYPE                 * load description
.
GADM9999  RETURN
+
.****************************************************************************
.*                            SHED0000                 Called by: SUMM0000  *
.*                      Print report header                       DETL0000  *
.****************************************************************************
.
SHED0000  CALL      HEAD132
          PACK      DIM8,ICENT1,IYEAR1,IMON1,IDAY1
          UNPACK    DIM8,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,FFDATE
          REP       " 0",FFDATE
          PACK      DIM8,ICENT2,IYEAR2,IMON2,IDAY2
          UNPACK    DIM8,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,FTDATE
          REP       " 0",FTDATE
          PRINT     *N,*40,"Date From : ",FFDATE:
                       " to ",FTDATE,*N
          CALL      PAGEND
          IF        OPTION = 1
            PRINT     *1,"| Date",*19,"| Claim Code":
                      *41,"| Patient Class.",*64,"| Beds Days":
                      *81,"| Admissions",*96,"| Discharges":
                      *111,"| Day Cases",*126,"|"
          ELSE
            PRINT     *9,"| Claim Code",*32,"| Patient Class.",*55:
                      "| Beds Days",*70,"| Admissions",*85,"| Discharges":
                      *100,"| Day Cases",*115,"|"
          ENDIF
.
          MOVE      EIGHT,CLNO
.
SHED9999  RETURN
+
.****************************************************************************
.*                            TTOT0000                 Called by: SUMM0000  *
.*                  Print totals for previous admission type                *
.****************************************************************************
.
TTOT0000  COMPARE   FIFTY5,CLNO                  * page full ?
          IF        !@LESS
            CALL      PAGEND                     * print line
            CALL      SHED0000                   * yes
            MOVE      ZERO,STRTFLG 
          ENDIF
.
          IF        STRTFLG = 0
            CALL      PAGEND
            PRINT     *9,"|",*11,DDCLAIM;
          ELSE
            PRINT     *9,"|";
          ENDIF
.
          PRINT       *32,"|",*34,DDTYPE,*55,"|",*57,ABDAYS,*70,"|",*72,AADMNS:
                      *85,"|",*87,ADSCHS,*100,"|",*102,ADCASES,*115,"|"
.
          ADD         ONE,CLNO
          CALL        PAGEND
          MOVE        ONE,STRTFLG
.
TTOT9999  RETURN
+
.****************************************************************************
.*                            CTOT0000                 Called by: SUMM0000  *
.*                  Print totals for previous claim code                    *
.****************************************************************************
.
CTOT0000  COMPARE   FIFTY5,CLNO                  * page full ?
          IF        !@LESS
            CALL      PAGEND                     * yes - print line
            CALL      SHED0000                   * print page header
            MOVE      ZERO,STRTFLG
          ENDIF
.
          IF        STRTFLG = 0
            CALL      PAGEND
            PRINT     *9,"|",*11,DDCLAIM; 
          ELSE
            PRINT     *9,"|";
          ENDIF
.
          PRINT       *32,"|",*34,"TOTALS",*55,"|",*57,NBDAYS,*70,"|":
                      *72,NADMNS,*85,"|",*87,NDSCHS,*100,"|",*102,NDCASES:
                      *115,"|"
.
          ADD         ONE,CLNO
          IF          DBLLINE = 0
            COMPARE     FIFTY1,CLNO
            IF          @LESS
              CALL        PAGASK
            ELSE
              CALL        PAGEND                 * print line
              CALL        SHED0000               * print page header
            ENDIF
          ENDIF
.
CTOT9999  RETURN
+
.****************************************************************************
.*                             DTOT0000                Called by: DETL0000  *
.*                  Print totals for previous admission type                *
.****************************************************************************
.
DTOT0000  COMPARE   FIFTY5,CLNO                  * page full ?
          IF        !@LESS
            CALL      PAGEND                     * print line
            CALL      SHED0000                   * yes
            MOVE      ZERO,STRTFLG
          ENDIF
.
          IF        STRTFLG = 0
            CALL      PAGEND
            UNPACK    SZDATE,XCENT,XYEAR,XMON,XDAY
            MOVE      XDAY,CDAY
            MOVE      XMON,CMON
            MOVE      XYEAR,CYEAR
            MOVE      XCENT,CCENT
            CALL      PACDATE
            REP       " 0",CPCDATE
.
            DISPLAY   *P55:24,*V2LON,CPCDATE;
.
            PRINT     *1,"| ",CPCDATE:
                      *19,"| ",DDCLAIM;
          ELSE
            PRINT     *1,"|",*19,"|";
          ENDIF
.
          PRINT     *41,"| ",DDTYPE:
                    *64,"| ",ABDAYS,*81,"| ",AADMNS:
                    *96,"| ",ADSCHS,*111,"| ",ADCASES,*126,"|"
.
          ADD         ONE,CLNO
          MOVE        ONE,STRTFLG
.
DTOT9999  RETURN
+
.****************************************************************************
.*                            LTOT0000                 Called by: DETL0000  *
.*                  Print totals for previous claim code                    *
.****************************************************************************
.
LTOT0000  COMPARE   FIFTY5,CLNO                  * page full ?
          IF        !@LESS
            CALL      PAGEND                     * yes - print line
            CALL      SHED0000                   * print page header
            MOVE      ZERO,STRTFLG
          ELSE
            CALL      PAGTOT
          ENDIF
.
          IF        STRTFLG = 0
            CALL      PAGEND
            PRINT     *1,"| ",CPCDATE,*19,"|":
                      *22,DDCLAIM;
          ELSE
            PRINT     *1,"|",*19,"|";
          ENDIF
.
          PRINT     *41,"| TOTAL",*64,"| ",NBDAYS,*81,"| ",NADMNS:
                    *96,"| ",NDSCHS,*111,"| ",NDCASES,*126,"|"
.
          ADD         ONE,CLNO
          IF          DBLLINE = 0
            COMPARE     FIFTY1,CLNO
            IF          @LESS
              CALL        PAGASK
            ELSE
              CALL        PAGEND                 * print line
              CALL        SHED0000               * print page header
            ENDIF
          ENDIF
.
LTOT9999  RETURN
+
.****************************************************************************
.*                           ZCLM0000                  Called by: SUMM0000  *
.*                      Zero claim totals                         DETL0000  *
.****************************************************************************
.
ZCLM0000  MOVE      ZERO,NADMNS                  * initialise claim totals
          MOVE      ZERO,NDSCHS
          MOVE      ZERO,NBDAYS
          MOVE      ZERO,NDCASES
.
          CALL      ZADM0000                     * initialise adm. type totals
.
ZCLM9999  RETURN
+
.****************************************************************************
.*                           ZADM0000                  Called by: SUMM0000  *
.*                      Zero adm. type totals                     ZCLM0000  *
.*                                                                DETL0000  *
.****************************************************************************
.
ZADM0000  MOVE      ZERO,AADMNS                  * initialise adm. type totals
          MOVE      ZERO,ADSCHS
          MOVE      ZERO,ABDAYS
          MOVE      ZERO,ADCASES
.
ZADM9999  RETURN
+
.**************************************************************************
.*                              CREA0000               Called by:         *
.*             create a new temporary file                                *
.**************************************************************************
.
CREA0000  CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,TEMPFIL1
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,TEMPFIL2
.
          CALL      KILL0000                     * remove existing file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,TEMPFIL1,UKEY1
          EXECUTE   CMDLINE,TASKID               * create temporary index file
.
          OPEN      PATMP1XX,TEMPFIL1            * open temp index file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,TEMPFIL2,UKEY2
          EXECUTE   CMDLINE,TASKID               * create temporary index file
.
          OPEN      PSTIDZ1,TEMPFIL2            * open temp index file
          OPEN      PSTIDZ2,TEMPFIL2            * open temp index file
.
CREA9999  RETURN
+
.****************************************************************************
.*                              KILL0000               Called by:           *
.*               close and erase the temporary file                         *
.****************************************************************************
.
KILL0000  CLOSE     PATMP1XX                     * close file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ERASE,TEMPFIL1       * set file erase command
          EXECUTE   CMDLINE,TASKID               * erase temp file
.
          CLOSE     PSTIDZ1                      * close file
          CLOSE     PSTIDZ2                      * close file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ERASE,TEMPFIL2       * set file erase command
          EXECUTE   CMDLINE,TASKID               * erase temp file
.
KILL9999 RETURN
+
.**************************************************************************
.*                              CLER0000               Called by:         *
.*             Clear the temporary files of all records                   *
.**************************************************************************
.
CLER0000  MOVE      SP8,KEY8
          CALL      RSTMPR1
          CALL      RKTMPR1
          BRANCH    OVRCD,CLER5000
.
          MOVE      TMPADMN,KEY8
          CALL      DETMPR1
          GOTO      CLER0000
.
CLER5000  PACK      KEY14,SP20
          CALL      RDSZIND1
          CALL      RDKZIND1
          BRANCH    OVRCD,CLER9999
.
          PACK      KEY14,ZDATE,ZCLAIM,ZTYPE
          CALL      DEZIND1
          GOTO      CLER5000
.
CLER9999  RETURN
+
.
.         Initialize total variables
.
ZEROT     MOVE      ZERO,TADMNS
          MOVE      ZERO,TDSCHS
          MOVE      ZERO,TBDAYS
          MOVE      ZERO,TDCASES
          MOVE      ZERO,STRTFLG
          RETURN
+
.
.         Temp Indexed files I/O Routines
.
WRZIND1   MOVE      ZERO,OVRCD
          RESET     KEY14
          WRITE     PSTIDZ1,KEY14;ZDATE,ZTYPE,ZDSCHS,ZADMNS,ZBDAYS,ZDCASES:
                                 ZCLAIM
          GOTO      OVERCOND IF OVER
          RETURN
.
.
RDZIND1   MOVE      ZERO,OVRCD
          RESET     KEY14
          READ      PSTIDZ1,KEY14;ZDATE,ZTYPE,ZDSCHS,ZADMNS,ZBDAYS:
                                 ZDCASES,ZCLAIM
          GOTO      OVERCOND IF OVER
          RETURN
.
RDKZIND1  MOVE      ZERO,OVRCD
          READKS    PSTIDZ1;ZDATE,ZTYPE,ZDSCHS,ZADMNS,ZBDAYS,ZDCASES,ZCLAIM
          GOTO      OVERCOND IF OVER
          RETURN
.
RDSZIND1  MOVE      ZERO,OVRCD
          RESET     KEY14
          READ      PSTIDZ1,KEY14;;
          GOTO      OVERCOND IF OVER
          RETURN
.
DEZIND1   RESET     KEY14
          DELETE    PSTIDZ1,KEY14
          RETURN
.
UPZIND1   RESET     KEY14
          UPDATE    PSTIDZ1;ZDATE,ZTYPE,ZDSCHS,ZADMNS,ZBDAYS,ZDCASES,ZCLAIM
          RETURN
.
RDKZIND2  MOVE      ZERO,OVRCD
          READKS    PSTIDZ2;ZDATE,ZTYPE,ZDSCHS,ZADMNS,ZBDAYS,ZDCASES,ZCLAIM
          GOTO      OVERCOND IF OVER
          RETURN
.
RDSZIND2  MOVE      ZERO,OVRCD
          RESET     KEY14
          READ      PSTIDZ2,KEY14;;
          GOTO      OVERCOND IF OVER
          RETURN
+
.
RATMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READ      PATMP1XX,KEY8;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READ      PATMP1XX,KEY8;;
          RETURN
.
RDTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READ      PATMP1XX,KEY8;TMPADMN
          GOTO      OVERCOND IF OVER
          RETURN
.
RKTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READKS    PATMP1XX;TMPADMN
          GOTO      OVERCOND IF OVER
          RETURN
.
RPTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READKP    PATMP1XX;TMPADMN
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTMPR1   MOVE      ZERO,OVRCD
          RESET     KEY8
          WRITE     PATMP1XX,KEY8;KEY8
          RETURN
.
UPTMPR1   UPDATE    PATMP1XX;TMPADMN
          RETURN
.
DETMPR1   RESET     KEY8
          DELETE    PATMP1XX,KEY8
          RETURN
+
.
ENDIT     CALL      KILL0000
          CHAIN     PGM
          STOP
.
.==============================================================================
.
          INC       STD001IO
.
          INC       CALCDAYS
          INC       TFILENAM                     * Generate Temp File Name
.
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       PATDAYIO/INC
          INC       PATCODIO/INC
          INC       PATDSCIO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATTRNIO/INC
          INC       HL7BMTIO/INC
....      INC       HL7BMTTR
....      INC       NXTMESID
          INC       WEBERRIO/INC                 * Web Server Error Logging

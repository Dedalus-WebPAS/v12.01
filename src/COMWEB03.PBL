.------------------------------------------------------------------------------
. Program       : COMWEB03
.
. Function      : Category Security Maintenance Server for
.                 Report 1: Security Group Maintenance
.                 Report 2: Security Group Category Maintenance
.
. Modifications :
.      V11.03.01  25/11/2022  Bella Turco    TSK 0921957
.                 Added NOMORE00 & PTCNNRTD to CODCT0000 routine
.      V11.02.01  02/08/2022  Bella Turco    TSK 0921957
.                 Added NOMORE00 - Display "No more records" at end of list
.                 Added PTCNNRTD - Displays number of rows set by parameter 
.                 value
.      V10.04.01  31.03.2014  B.G.Ackland  CAR 299363
.                 Replace META Tag Redirect with Javascript in Common RDIREC00
.                 Routine
.------------------------------------------------------------------------------
.      V9.07.01   13/07/2006  Ebon Clements CAR 103365
.                 Added report 5 - category code security
.      V9.05.01   09/03/2006  Ebon Clements CAR 78533
.                 Mods for PATCODTM - Hospital specific category codes
.      V9.05.B01  30/01/2006  Ebon Clements CAR 93186
.                 Mods for REDIRECT length change. Recompiled for IBAWEBDF
.      V9.04.01   06/06/2005  Davin  CAR 62379
.                 Added option 4 - HIC Medicare Explanation Codes Maintenance
.      v9.03.00   14/04/2004  Davin  CAR 18702
.                 Created server
.------------------------------------------------------------------------------
.
.------------------------------------------------------------------------------
. FD Includes
.------------------------------------------------------------------------------
          INC       IBAWEBDF
          INC       WEBDATDF
          INC       RSHWEBDF
.
          INC       PATCOMM/INC             * IBA Control file
.
          INC       HICEMSFD/INC            * HIC Medicare Explanation Codes
          INC       IBACCSFD/INC            * Category Security Categories
          INC       IBACGSFD/INC            * Category Security Groups
          INC       IBACOSFD/INC            * Category Code Security
          INC       IBACUSFD/INC            * Category Security User IDs
          INC       PATCONFD/INC            * Patient Control file
.
.-----------------------------------------------------------------------------
. Variable Declarations
.-----------------------------------------------------------------------------
.
. CGI Parameters
. **************
.
NEXTPAGE  FORM      1         * Nextpage parameter
UPDTTYPE  DIM       1         * Update Type (A)dd,(U)pdate,(D)elete,Blank=disp
STARTKEY  DIM       3         * Starting Key for search
.
HCEMS001  DIM       3
HCEMS002  DIM       70
.
SCGRP001  DIM       3
SCGRP002  DIM       20
SCGRP003  DIM       1
SCGRP004  DIM       8
SCGRP005  DIM       8
SCGRP006  DIM       10
SCGRP007  DIM       8
SCGRP008  DIM       8
SCGRP009  DIM       10
.
SCCAT001  DIM       3
SCCAT002  DIM       2
SCCAT003  DIM       20
SCCAT004  DIM       8
SCCAT005  DIM       8
SCCAT006  DIM       10
.
SCUSR001  DIM       3
SCUSR002  DIM       10
SCUSR003  DIM       30
SCUSR004  DIM       8
SCUSR005  DIM       8
SCUSR006  DIM       10
.
SCCOD001  DIM       3
SCCOD002  DIM       2
SCCOD003  DIM       3
.
. Program Variables
. *****************
.
CATEDESC  DIM       20       * Current security group desc
CATGRY    DIM       2
CURRCATG  DIM       2        * Current category
CURRCODE  DIM       3        * Current security group
CURRUSER  DIM       10       * current user id
DISPFMDT  DIM       11
DISPTODT  DIM       11
GROUPCD   DIM       3
GROUPDSC  DIM       20
IBCCDESC  DIM       20       * category description (display)
IBCUNAME  DIM       30       * user name (display)
JAVSNAME  DIM       80
ENDNAME   DIM       80
STRTNAME  DIM       80
LINK2PRT  FORM      1
SAVCATG   DIM       2
SAVKEY3   DIM       3
USERCD    DIM       10
.
. Program Constants
. *****************
FIVE9     FORM      "59"
SEVEN9    FORM      "79"
NBSP      INIT      "&nbsp;"
+
.-----------------------------------------------------------------------------
.
PRGID     INIT      "COMWEB03"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "Category Security Maintenance"
+
.------------------------------------------------------------------------------
.
          CALL      WEBINT00                * Initialise Fifo Etc.
          CALL      INIT0000                * Open Files
.
MAIN1000  CALL      WEBRED00                * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO          * End Server Process if Option=999
          GOTO      MAIN9999 IF EQUAL
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200,MAIN1300,MAIN1400,MAIN1500
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "Invalid Option",WEBTITLE
          CALL      WEBERR00                * Output File & Write HTML Pg Header
          GOTO      MAIN1000
.
MAIN1100  CALL      SECGRP00                * Security Group Maintenance
          BRANCH    EXIT,MAIN1000
          CALL      NXTPG000                * next page
          GOTO      MAIN1000
.
MAIN1200  CALL      SECCAT00                * Security Group Category Maint.
          BRANCH    EXIT,MAIN1000
          CALL      NXTPG000                * next page
          GOTO      MAIN1000
.
MAIN1300  CALL      SECUSR00                * Security Group User ID Maint.
          BRANCH    EXIT,MAIN1000
          CALL      NXTPG000                * next page
          GOTO      MAIN1000
.
MAIN1400  CALL      HICEMS00                * HIC Medicare Expl. Codes Maint.
          BRANCH    EXIT,MAIN1000
          CALL      NXTPG000                * next page
          GOTO      MAIN1000
.
MAIN1500  CALL      SECCOD00                * Security Group Category Code Maint
          BRANCH    EXIT,MAIN1000  
          CALL      NXTPG000                * next page
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
+
.------------------------------------------------------------------------------
.  Next Page URL
.------------------------------------------------------------------------------
.
NXTPG000  IF        NEXTPAGE=0
            CALL      WINCLS00              * Refresh parent and close window
          ENDIF

          IF        NEXTPAGE=1
            CALL      RSAVED00              * Redirect URL
          ENDIF
.
          IF        NEXTPAGE=2
            CALL      GOBACK00              * Go Back 1 html page
          ENDIF
.
          IF        NEXTPAGE=3
            CALL      DAH20000              * Go Back 2 html pages
          ENDIF
.
NXTPG999  RETURN
.------------------------------------------------------------------------------
.  Goes back 1 page
.------------------------------------------------------------------------------
GOBACK00  CALL      OPENHTML
          BRANCH    EXIT,GOBACK99
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "    alert(#"",WEBTITLE,"#");":
                             "    self.close();":
                             "    opener.history.go(-1);":
                             "}":
                             "</script>"

          CALL      CLOSHTML
.
GOBACK99  RETURN
+
.------------------------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------------------------
INIT0000  OPEN      WEBSECA1,"websecaf"
          OPEN      WEBSECA3,"websecaf"
.
          OPEN      IBACCSA1,"ibaccsaf"
          OPEN      IBACCSA2,"ibaccsaf"
.
          OPEN      IBACGSA1,"ibacgsaf"
          OPEN      IBACGSA2,"ibacgsaf"
.
          OPEN      IBACUSA1,"ibacusaf"
          OPEN      IBACUSA2,"ibacusaf"
.
          OPEN      IBACOSA1,"ibacosaf"
          OPEN      IBACOSA2,"ibacosaf"
.
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
          OPEN      PATCODE3,"patcodes"
.
          OPEN      HICEMSA1,"hicemsaf"
          OPEN      HICEMSA2,"hicemsaf"
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,HUND25;*238,PTCNNRTD
.
          MOVE      ZERO,F2
          MOVE      PTCNNRTD,F2
          IF        F2 = 0
            MOVE      "10",PTCNNRTD
          ENDIF
.
          IF        IBCNMHOS=1
            OPEN      MLTCODA1,"mltcodaf"
            OPEN      MLTCODA2,"mltcodaf"
          ENDIF
.
INIT9999  RETURN
+
.------------------------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          MOVE      SP70,TEMPLATE
          MOVE      SP70,UPDTTYPE
          MOVE      ZERO,NEXTPAGE
          PACK      REDIRECT,SP70,SP70,SP70,SP70
          MOVE      ZERO,STARTNUM
          MOVE      ZERO,NORECORD
          MOVE      SP70,STARTKEY
.
          MOVE      Z70,HCEMS001
          MOVE      Z70,HCEMS002
.
          MOVE      Z70,SCGRP001
          MOVE      Z70,SCGRP002
          MOVE      Z70,SCGRP003
          MOVE      Z70,SCGRP004
          MOVE      Z70,SCGRP005
          MOVE      Z70,SCGRP006
          MOVE      Z70,SCGRP007
          MOVE      Z70,SCGRP008
          MOVE      Z70,SCGRP009
.
          MOVE      Z70,SCCAT001
          MOVE      Z70,SCCAT002
          MOVE      Z70,SCCAT003
          MOVE      Z70,SCCAT004
          MOVE      Z70,SCCAT005
          MOVE      Z70,SCCAT006
.
          MOVE      Z70,SCUSR001
          MOVE      Z70,SCUSR002
          MOVE      Z70,SCUSR003
          MOVE      Z70,SCUSR004
          MOVE      Z70,SCUSR005
          MOVE      Z70,SCUSR006.
.
          MOVE      Z70,SCCOD001
          MOVE      Z70,SCCOD002
          MOVE      Z70,SCCOD003
.
CLRPAR99  RETURN
+
.------------------------------------------------------------------------------
. Set Parameters
.------------------------------------------------------------------------------
.
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updttype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDTTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nextpage=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NEXTPAGE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startkey=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTKEY
            PACK      STARTKEY,STARTKEY,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "redirect=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REDIRECT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "norecord=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NORECORD
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startnum=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTNUM
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "scgrp",QRYNAME
          IF        @EQUAL
            CALL      STCGS000
            COMPARE   ZERO,EXIT
            GOTO      SETPAR99 IF EQUAL
          ENDIF
.
          MATCH     "sccat",QRYNAME
          IF        @EQUAL
            CALL      STCCS000
            COMPARE   ZERO,EXIT
            GOTO      SETPAR99 IF EQUAL
          ENDIF
.
          MATCH     "scusr",QRYNAME
          IF        @EQUAL
            CALL      STCUS000
            COMPARE   ZERO,EXIT
            GOTO      SETPAR99 IF EQUAL
          ENDIF
.
          MATCH     "sccod",QRYNAME
          IF        @EQUAL
            CALL      STCOD000
            COMPARE   ZERO,EXIT
            GOTO      SETPAR99 IF EQUAL
          ENDIF
.
          MATCH     "hcems001=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,HCEMS001
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "hcems002=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,HCEMS002
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
+
.-------------------------------------------------------------------------------
.   Set Parameters for Category Security Group Table
.-------------------------------------------------------------------------------
STCGS000  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,STCGS001,STCGS002,STCGS003,STCGS004,STCGS005:
                       STCGS006,STCGS007,STCGS008,STCGS009
.
          MOVE      ONE,EXIT
          GOTO      STCGS999
.
STCGS001  SQUEEZE   QRYDATA
          MOVE      QRYDATA,SCGRP001
          PACK      SCGRP001,SCGRP001,SP70
          GOTO      STCGS999
STCGS002  MOVE      QRYDATA,SCGRP002
          GOTO      STCGS999
STCGS003  MOVE      QRYDATA,SCGRP003
          GOTO      STCGS999
STCGS004  UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00            * Set CMON from MTHNAM3
          PACK      SCGRP004,CCENT,CYEAR,CMON,CDAY
          GOTO      STCGS999
STCGS005  MOVE      QRYDATA,SCGRP005
          GOTO      STCGS999
STCGS006  MOVE      QRYDATA,SCGRP006
          GOTO      STCGS999
STCGS007  UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00            * Set CMON from MTHNAM3
          PACK      SCGRP007,CCENT,CYEAR,CMON,CDAY
          GOTO      STCGS999
STCGS008  MOVE      QRYDATA,SCGRP008
          GOTO      STCGS999
STCGS009  MOVE      QRYDATA,SCGRP009
          GOTO      STCGS999
.
STCGS999  RETURN
+
.-------------------------------------------------------------------------------
.   Set Parameters for Security Group User IDs Table
.-------------------------------------------------------------------------------
STCCS000  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,STCCS001,STCCS002,STCCS003,STCCS004,STCCS005:
                       STCCS006
.
          MOVE      ONE,EXIT
          GOTO      STCCS999
.
STCCS001  SQUEEZE   QRYDATA
          MOVE      QRYDATA,SCCAT001
          PACK      SCCAT001,SCCAT001,SP70
          GOTO      STCCS999
STCCS002  MOVE      QRYDATA,SCCAT002
          GOTO      STCCS999
STCCS003  MOVE      QRYDATA,SCCAT003
          GOTO      STCCS999
STCCS004  UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00            * Set CMON from MTHNAM3
          PACK      SCCAT004,CCENT,CYEAR,CMON,CDAY
          GOTO      STCCS999
STCCS005  MOVE      QRYDATA,SCCAT005
          GOTO      STCCS999
STCCS006  MOVE      QRYDATA,SCCAT006
          GOTO      STCCS999
STCCS999  RETURN
+
.-------------------------------------------------------------------------------
.   Set Parameters for Security Group User IDs
.-------------------------------------------------------------------------------
STCUS000  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,STCUS001,STCUS002,STCUS003,STCUS004,STCUS005:
                       STCUS006
.
          MOVE      ONE,EXIT
          GOTO      STCUS999
.
STCUS001  SQUEEZE   QRYDATA
          MOVE      QRYDATA,SCUSR001
          PACK      SCUSR001,SCUSR001,SP70
          GOTO      STCUS999
STCUS002  PACK      SCUSR002,QRYDATA,SP70
          GOTO      STCUS999
STCUS003  MOVE      QRYDATA,SCUSR003
          GOTO      STCUS999
STCUS004  UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00            * Set CMON from MTHNAM3
          PACK      SCUSR004,CCENT,CYEAR,CMON,CDAY
          GOTO      STCUS999
STCUS005  MOVE      QRYDATA,SCUSR005
          GOTO      STCUS999
STCUS006  MOVE      QRYDATA,SCUSR006
          GOTO      STCUS999
STCUS999  RETURN
+
.-------------------------------------------------------------------------------
.   Set Parameters for Security Group Category Codes
.-------------------------------------------------------------------------------
STCOD000  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,STCOD001,STCOD002,STCOD003
.
          MOVE      ONE,EXIT
          GOTO      STCOD999
.
STCOD001  PACK      SCCOD001,QRYDATA,SP70
          GOTO      STCOD999
.
STCOD002  PACK      SCCOD002,QRYDATA,SP70
          GOTO      STCOD999
.
STCOD003  PACK      SCCOD003,QRYDATA,SP70
          GOTO      STCOD999
.
STCOD999  RETURN
+
.------------------------------------------------------------------------------
. Category Security Groups Maintenance
.------------------------------------------------------------------------------
.
SECGRP00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE            * Display formaction
          GOTO      SECGRP40 IF EQUAL
.
          MATCH     "A",UPDTTYPE            * add
          GOTO      SECGRP10 IF EQUAL
.
          MATCH     "U",UPDTTYPE            * update
          GOTO      SECGRP20 IF EQUAL
.
          MATCH     "D",UPDTTYPE            * delete
          GOTO      SECGRP30 IF EQUAL
.
          GOTO      SECGRP90
.
.
SECGRP10  RESET     SCGRP001                * add formaction
          PACK      KEY3,SCGRP001,SP70
          CALL      RDIBCGS1
          COMPARE   ZERO,OVRCD
          GOTO      SECGRP92 IF EQUAL
.
          CALL      GRPSAV00           * Moves input variables to file variables
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          MOVE      KEY8,IBCGCDAT                * System date
          CLOCK     TIME,IBCGCTIM                * System time
          MOVE      USERID,IBCGCUID              * web user id
          MOVE      SP70,IBCGUDAT
          MOVE      SP70,IBCGUTIM
          MOVE      SP70,IBCGUUID
          PACK      KEY3,SCGRP001,SP70
          CALL      WRIBCGS1           * Writes away the data
          MOVE      ZERO,EXIT
          GOTO      SECGRP99
.
SECGRP20  PACK      KEY3,SCGRP001,SP70   * Update Formaction
          CALL      RDIBCGS1
          BRANCH    OVRCD,SECGRP91
          CALL      GRPSAV00           * Moves input variables to file variables
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          MOVE      KEY8,IBCGUDAT                * System date
          CLOCK     TIME,IBCGUTIM                * System time
          MOVE      USERID,IBCGUUID              * web user id
          CALL      UPIBCGS1           * Writes away the data
          MOVE      ZERO,EXIT
          GOTO      SECGRP99
.
SECGRP30  PACK      KEY3,SCGRP001,SP70   * Delete Formaction
          CALL      RDIBCGS1
          BRANCH    OVRCD,SECGRP91
          PACK      KEY3,IBCGSGRP
          CALL      DEIBCGS1           * delete security group
.
          CALL      DEGRP000           * delete security group from other files
.
          MOVE      ZERO,EXIT
          GOTO      SECGRP99
.
.         ************ DISPLAY FORMACTION **********
.
SECGRP40  PACK      KEY3,SCGRP001,SP70
          CALL      RDIBCGS1
          IF        OVRCD=1
            CALL      CLRGRP00         * Clear sec.group file variables
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Category Security Groups Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00                * Output File & Write HTML Pg Header
          BRANCH    EXIT,SECGRP99
.
          CALL      WEBBOD00                * Process Web Body
.
          CALL      WEBEND00                * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      SECGRP99
.
.         ************ DISPLAY ERROR MESSAGE **********
.
SECGRP90  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SECGRP99
.
SECGRP91  MOVE      "Security Group does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SECGRP99
.
SECGRP92  MOVE      "Security Group exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SECGRP99
.
SECGRP99  RETURN
+
.--------------------------------------------------------------------
. Delete security group from ibaccsaf (categories) & ibacusaf (users)
.--------------------------------------------------------------------
DEGRP000  PACK      KEY5,SCGRP001,SP70
DEGRP100  CALL      RSIBCCS1
          CALL      RKIBCCS1                * loop through ibaccs
          BRANCH    OVRCD,DEGRP500
.
          MATCH     SCGRP001,IBCCSGRP
          GOTO      DEGRP500 IF NOT EQUAL   * check if same security group
.
          PACK      KEY5,IBCCSGRP,IBCCCATG
          CALL      DEIBCCS1                * delete record
          GOTO      DEGRP100
.
DEGRP500  PACK      KEY13,SCGRP001,SP70
DEGRP600  CALL      RSIBCUS1
          CALL      RKIBCUS1                * loop through ibacus
          BRANCH    OVRCD,DEGRP999
.
          MATCH     SCGRP001,IBCUSGRP
          GOTO      DEGRP999 IF NOT EQUAL   * check if same security group
.
          PACK      KEY13,IBCUSGRP,IBCUUSER
          CALL      DEIBCUS1                * delete record
          GOTO      DEGRP600
.
DEGRP999  RETURN
+
.------------------------------------------------------------
. Moves security group cgi variables into file variables
.------------------------------------------------------------
GRPSAV00  MATCH     SCGRP001,Z70
          IF        !@EQUAL
            MOVE      SCGRP001,KEY3
            SQUEEZE   KEY3
            PACK      IBCGSGRP,KEY3,SP70
          ENDIF
.
          MATCH     SCGRP002,Z70
          IF        !@EQUAL
            PACK      IBCGDESC,SCGRP002,SP70
          ENDIF
.
          MATCH     SCGRP003,Z70
          IF        !@EQUAL
            MOVE      SCGRP003,IBCGACTV
          ELSE
            MOVE      ZERO,IBCGACTV
          ENDIF
.
          MATCH     SCGRP004,Z70
          IF        !@EQUAL
            PACK      IBCGCDAT,SCGRP004,SP70
          ENDIF
.
          MATCH     SCGRP005,Z70
          IF        !@EQUAL
            PACK      IBCGCTIM,SCGRP005,SP70
          ENDIF
.
          MATCH     SCGRP006,Z70
          IF        !@EQUAL
            PACK      IBCGCUID,SCGRP006,SP70
          ENDIF
.
          MATCH     SCGRP007,Z70
          IF        !@EQUAL
            PACK      IBCGUDAT,SCGRP007,SP70
          ENDIF
.
          MATCH     SCGRP008,Z70
          IF        !@EQUAL
            PACK      IBCGUTIM,SCGRP008,SP70
          ENDIF
.
          MATCH     SCGRP009,Z70
          IF        !@EQUAL
            PACK      IBCGUUID,SCGRP009,SP70
          ENDIF
.
GRPSAV99  RETURN
+
.-----------------------------------------------------------
. Clear sec.group file variables
.-----------------------------------------------------------
CLRGRP00  PACK      IBCGSGRP,SP70
          PACK      IBCGDESC,SP70
          PACK      IBCGACTV,SP70
          PACK      IBCGCDAT,SP70
          PACK      IBCGCTIM,SP70
          PACK      IBCGCUID,SP70
          PACK      IBCGUDAT,SP70
          PACK      IBCGUTIM,SP70
          PACK      IBCGUUID,SP70
          PACK      IBCGSPAR,SP70
.
CLRGRP99  RETURN
+
.------------------------------------------------------------
. Security Group Category Records: Display,Add,Delete
.------------------------------------------------------------
SECCAT00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      SECCAT40 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      SECCAT10 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      SECCAT30 IF EQUAL
.
          GOTO      SECCAT93
.
SECCAT10  PACK      KEY5,SCCAT001,SCCAT002,SP70
          CALL      RDIBCCS1
          COMPARE   ZERO,OVRCD
          GOTO      SECCAT92 IF EQUAL
.
          CALL      CATSAV00      * Moves input variables to file variables
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          MOVE      KEY8,IBCCCDAT                * System date
          CLOCK     TIME,IBCCCTIM                * System time
          MOVE      USERID,IBCCCUID              * web user id
          PACK      KEY5,SCCAT001,SCCAT002,SP70
          CALL      WRIBCCS1             * Writes away the data
          MOVE      ZERO,EXIT
          GOTO      SECCAT99
.
SECCAT30  PACK      KEY5,SCCAT001,SCCAT002,SP70
          CALL      RDIBCCS1
          BRANCH    OVRCD,SECCAT91
          PACK      KEY5,IBCCSGRP,IBCCCATG
          CALL      DEIBCCS1             * delete security group category
          MOVE      ZERO,EXIT
          GOTO      SECCAT99
.
.         ************ DISPLAY FORMACTION **********
.
SECCAT40  PACK      CURRCODE,SP70
          PACK      CATEDESC,SP70
          PACK      KEY3,SCCAT001,SP70
          CALL      RDIBCGS1
.>>>>     BRANCH    OVRCD,SECCAT94     * read sec.group record
          IF        OVRCD<>1
            MOVE      IBCGSGRP,CURRCODE  * Current group
            MOVE      IBCGDESC,CATEDESC  * Current group desc.
          ENDIF
.
          MOVE      SCCAT002,CURRCATG  * Current category
.
          PACK      KEY5,SCCAT001,SCCAT002,SP70
          CALL      RDIBCCS1
          IF        OVRCD=1
            CALL      CLRCAT00         * Clear sec.group category file variables
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Category Security Groups Category Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00                * Output File & Write HTML Pg Header
          BRANCH    EXIT,SECCAT99
.
          CALL      WEBBOD00                * Process Web Body
.
          CALL      WEBEND00                * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      SECCAT99
.
.         ************ DISPLAY ERROR MESSAGE **********
.
SECCAT91  MOVE      "Category Record does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SECCAT99
.
SECCAT92  MOVE      "Category Record exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SECCAT99
.
SECCAT93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SECCAT99
.
SECCAT94  MOVE      "Invalid Group.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SECCAT99
.
SECCAT99  RETURN
+
.------------------------------------------------------------
. Moves security category cgi variables into file variables
.------------------------------------------------------------
CATSAV00  MATCH     SCCAT001,Z70
          IF        !@EQUAL
            MOVE      SCCAT001,KEY3
            SQUEEZE   KEY3
            PACK      IBCCSGRP,KEY3,SP70
          ENDIF
.
          MATCH     SCCAT002,Z70
          IF        !@EQUAL
            PACK      IBCCCATG,SCCAT002,SP70
          ENDIF
.
          MATCH     SCCAT004,Z70
          IF        !@EQUAL
            PACK      IBCCCDAT,SCCAT004,SP70
          ENDIF
.
          MATCH     SCCAT005,Z70
          IF        !@EQUAL
            PACK      IBCCCTIM,SCCAT005,SP70
          ENDIF
.
          MATCH     SCCAT006,Z70
          IF        !@EQUAL
            PACK      IBCCCUID,SCCAT006,SP70
          ENDIF
.
CATSAV99  RETURN
+
.-----------------------------------------------------------
. Clear sec.category file variables
.-----------------------------------------------------------
CLRCAT00  PACK      IBCCSGRP,SP70
          PACK      IBCCCATG,SP70
          PACK      IBCCCDAT,SP70
          PACK      IBCCCTIM,SP70
          PACK      IBCCCUID,SP70
          PACK      IBCCSPAR,SP70
.
CLRCAT99  RETURN
+
.------------------------------------------------------------
. Security Group User ID Records: Display,Add,Delete
.------------------------------------------------------------
SECUSR00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      SECUSR40 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      SECUSR10 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      SECUSR30 IF EQUAL
.
          GOTO      SECUSR93
.
SECUSR10  PACK      KEY13,SCUSR001,SCUSR002,SP70
          CALL      RDIBCUS1
          COMPARE   ZERO,OVRCD
          GOTO      SECUSR92 IF EQUAL
.
          CALL      USRSAV00      * Moves input variables to file variables
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          MOVE      KEY8,IBCUCDAT                * System date
          CLOCK     TIME,IBCUCTIM                * System time
          MOVE      USERID,IBCUCUID              * web user id
          PACK      KEY13,SCUSR001,SCUSR002,SP70
          CALL      WRIBCUS1             * Writes away the data
          MOVE      ZERO,EXIT
          GOTO      SECUSR99
.
SECUSR30  PACK      KEY13,SCUSR001,SCUSR002,SP70
          CALL      RDIBCUS1
          BRANCH    OVRCD,SECUSR91
          PACK      KEY13,IBCUSGRP,IBCUUSER
          CALL      DEIBCUS1             * delete security group user id
          MOVE      ZERO,EXIT
          GOTO      SECUSR99
.
.         ************ DISPLAY FORMACTION **********
.
SECUSR40  PACK      CURRCODE,SP70
          PACK      CATEDESC,SP70
          PACK      KEY3,SCUSR001,SP70
          CALL      RDIBCGS1
.>>>>     BRANCH    OVRCD,SECUSR94     * read sec.group record
          IF        OVRCD<>1
            MOVE      IBCGSGRP,CURRCODE  * Current group
            MOVE      IBCGDESC,CATEDESC  * Current group desc.
          ENDIF
.
          MOVE      SCUSR002,CURRUSER  * Current user
.
          PACK      KEY13,SCUSR001,SCUSR002,SP70
          CALL      RDIBCUS1
          IF        OVRCD=1
            CALL      CLRUSR00         * Clear sec.group user id file
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Category Security Groups Category Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00                * Output File & Write HTML Pg Header
          BRANCH    EXIT,SECUSR99
.
          CALL      WEBBOD00                * Process Web Body
.
          CALL      WEBEND00                * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      SECUSR99
.
.         ************ DISPLAY ERROR MESSAGE **********
.
SECUSR91  MOVE      "User ID Record does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SECUSR99
.
SECUSR92  MOVE      "User ID Record exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SECUSR99
.
SECUSR93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SECUSR99
.
SECUSR94  MOVE      "Invalid Group.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SECUSR99
.
SECUSR99  RETURN
+
.------------------------------------------------------------
. Moves security group user id cgis into file variables
.------------------------------------------------------------
USRSAV00  MATCH     SCUSR001,Z70
          IF        !@EQUAL
            MOVE      SCUSR001,KEY3
            SQUEEZE   KEY3
            PACK      IBCUSGRP,KEY3,SP70
          ENDIF
.
          MATCH     SCUSR002,Z70
          IF        !@EQUAL
            PACK      IBCUUSER,SCUSR002,SP70
          ENDIF
.
          MATCH     SCUSR004,Z70
          IF        !@EQUAL
            PACK      IBCUCDAT,SCUSR004,SP70
          ENDIF
.
          MATCH     SCUSR005,Z70
          IF        !@EQUAL
            PACK      IBCUCTIM,SCUSR005,SP70
          ENDIF
.
          MATCH     SCUSR006,Z70
          IF        !@EQUAL
            PACK      IBCUCUID,SCUSR006,SP70
          ENDIF
.
USRSAV99  RETURN
+
.-----------------------------------------------------------
. Clear sec.group user id variables
.-----------------------------------------------------------
CLRUSR00  PACK      IBCUSGRP,SP70
          PACK      IBCUUSER,SP70
          PACK      IBCUCDAT,SP70
          PACK      IBCUCTIM,SP70
          PACK      IBCUCUID,SP70
          PACK      IBCUSPAR,SP70
.
CLRUSR99  RETURN
+
.------------------------------------------------------------
. HIC Medicare Explanation Codes Maintenance
.------------------------------------------------------------
HICEMS00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE            * Display formaction
          GOTO      HICEMS40 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      HICEMS10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      HICEMS20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      HICEMS30 IF EQUAL
.
          GOTO      HICEMS93
.
.         ************ ADD FORMACTION ***********
.
HICEMS10  PACK      KEY3,HCEMS001,SP70
          CALL      RDHCEMS1
          COMPARE   ZERO,OVRCD
          GOTO      HICEMS92 IF EQUAL
.
          CALL      EMSSAV00      * Moves input variables to file variables
          PACK      KEY3,HCEMS001,SP70
          CALL      WRHCEMS1      * Writes away the data
.
          MOVE      ZERO,EXIT
          GOTO      HICEMS99
.
.         ************ UPDATE FORMACTION **********
.
HICEMS20  PACK      KEY3,HCEMS001,SP70
          CALL      RDHCEMS1
          BRANCH    OVRCD,HICEMS91
.
          CALL      EMSSAV00      * Moves input variables to file variables
          PACK      KEY3,HCEMS001,SP70
          CALL      UPHCEMS1      * Writes away the data
.
          MOVE      ZERO,EXIT
          GOTO      HICEMS99
.
.         ************ DELETE FORMACTION **********
.
HICEMS30  PACK      KEY3,HCEMS001,SP70
          CALL      RDHCEMS1
          BRANCH    OVRCD,HICEMS91
          CALL      DEHCEMS1
.
          MOVE      ZERO,EXIT
          GOTO      HICEMS99
.
.         ************ DISPLAY FORMACTION **********
.
HICEMS40  PACK      KEY3,HCEMS001,SP70
          CALL      RDHCEMS1
          IF        OVRCD=1
            CALL      CLREMS00    * Clear all the file variables
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "HIC Medicare Explanation Code Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,HICEMS99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      HICEMS99
.
HICEMS91  MOVE      "Medicare Explanation Code does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      HICEMS99
.
HICEMS92  MOVE      "Medicare Explanation Code exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      HICEMS99
.
HICEMS93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      HICEMS99
.
HICEMS99  RETURN
.-----------------------------------------------------------
. Initialize's file data (Medicare Explanation Code Maintenance)
.-----------------------------------------------------------
CLREMS00  MOVE      SP70,HCEMCODE
          MOVE      SP70,HCEMDESC
          MOVE      SP70,HCEMSPAR
CLREMS99  RETURN
+
.-----------------------------------------------------------------
. Moves cgi variables into file variables (Medicare Explanation Code Mainten.)
.-----------------------------------------------------------------
EMSSAV00  MATCH     HCEMS001,Z70
          IF        !@EQUAL
            PACK      HCEMCODE,HCEMS001,SP70
          ENDIF
          MATCH     HCEMS002,Z70
          IF        !@EQUAL
            PACK      HCEMDESC,HCEMS002,SP70
          ENDIF
EMSSAV99  RETURN
+
.------------------------------------------------------------------------------
. Process Fields called from WEBBOD00
.------------------------------------------------------------------------------
.
PROFLD00  BRANCH    REPORTNO,PROFLD10,PROFLD20,PROFLD30,PROFLD40,PROFLD50
.
          GOTO      PROFLD99
.
PROFLD10  BRANCH    FLDSETNO,PROFLD11
          GOTO      PROFLD99
.
PROFLD11  CALL      CSGS0000                * Category Security Groups
          GOTO      PROFLD99
.
PROFLD20  BRANCH    FLDSETNO,PROFLD21
          GOTO      PROFLD99
.
PROFLD21  CALL      CSCS0000                * Category Security Categories
          GOTO      PROFLD99
.
PROFLD30  BRANCH    FLDSETNO,PROFLD31
          GOTO      PROFLD99
.
PROFLD31  CALL      CSUS0000                * Category Security User IDs
          GOTO      PROFLD99
.
PROFLD40  BRANCH    FLDSETNO,PROFLD41
          GOTO      PROFLD99
.
PROFLD41  CALL      HMEC0000                * HIC Medicare Explanation Codes
          GOTO      PROFLD99
.
PROFLD50  BRANCH    FLDSETNO,PROFLD51
          GOTO      PROFLD99
.
PROFLD51  CALL      CSCO0000                * Category Security Codes
          GOTO      PROFLD99
.
PROFLD99  RETURN
+
.-----------------------------------------------------------
. Option 1: Category Security Groups
.-----------------------------------------------------------
CSGS0000  BRANCH    FLDITMNO,CSGS0100,CSGS0200,CSGS0300,CSGS0400,CSGS0500:
                             CSGS0600,CSGS0700,CSGS0800,CSGS0900,CSGS1000:
                             CSGS1100,CSGS1200,CSGS1300
.
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      CSGS9999
.
CSGS0100  WRITE     HTMLFILE;IBCGSGRP;
          GOTO      CSGS9999
.
CSGS0200  WRITE     HTMLFILE;IBCGDESC;
          GOTO      CSGS9999
.
CSGS0300  WRITE     HTMLFILE;IBCGACTV;
          GOTO      CSGS9999
.
CSGS0400  MATCH     IBCGCDAT,SP70
          GOTO      CSGS9999 IF EQUAL
          UNPACK    IBCGCDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          GOTO      CSGS9999
.
CSGS0500  WRITE     HTMLFILE;IBCGCTIM;
          GOTO      CSGS9999
.
CSGS0600  WRITE     HTMLFILE;IBCGCUID;
          GOTO      CSGS9999
.
CSGS0700  MATCH     IBCGUDAT,SP70
          GOTO      CSGS9999 IF EQUAL
          UNPACK    IBCGUDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          GOTO      CSGS9999
.
CSGS0800  WRITE     HTMLFILE;IBCGUTIM;
          GOTO      CSGS9999
.
CSGS0900  WRITE     HTMLFILE;IBCGUUID;
          GOTO      CSGS9999
.
CSGS1000  CALL      GRPGP000        * Table of Groups by Group
          GOTO      CSGS9999
.
CSGS1100  CALL      GRPDS000        * Table of Groups by Description
          GOTO      CSGS9999
.
CSGS1200  CALL      NEXTGR00        * next
          GOTO      CSGS9999
.
CSGS1300  CALL      PREVGR00        * previous
          GOTO      CSGS9999
.
.
.
CSGS9999  RETURN
+
.------------------------------------------------------------
. Table of Groups by Group
.------------------------------------------------------------
GRPGP000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          CALL      GRHED000              * Output Table Heading
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY3,STARTKEY,SP70
          CALL      RSIBCGS1
          CALL      RPIBCGS1
.
GRPGP100  CALL      RKIBCGS1
          BRANCH    OVRCD,GRPGP900
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      GRPGP100
          ENDIF
.
          CALL      GRROW000                * Display Group Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      GRPGP100 IF NOT EQUAL
          GOTO      GRPGP999
.
GRPGP900  CALL      NOMORE00     * Display No More Records Message
.
GRPGP999  RETURN
+
.------------------------------------------------------------
. Table of Groups by Description
.------------------------------------------------------------
GRPDS000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          CALL      GRHED000              * Output Table Heading
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY23,STARTKEY,SP70
          CALL      RSIBCGS2
          CALL      RPIBCGS2
.
GRPDS100  CALL      RKIBCGS2
          BRANCH    OVRCD,GRPDS900
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      GRPDS100
          ENDIF
.
          CALL      GRROW000                * Display Group Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      GRPDS100 IF NOT EQUAL
          GOTO      GRPDS999
.
GRPDS900  CALL      NOMORE00    * Display No More Records Message
.
GRPDS999  RETURN
+
.------------------------------------------------------------
. Security Group Maintenance (TABLE HEADING)
.------------------------------------------------------------
GRHED000  WRITE     HTMLFILE;"<tr>":
                             "<td class=HeadingCell width=20%>Security Group":
                             "</td>":
                             "<td class=HeadingCell width=70%>Description":
                             "</td>":
                             "<td class=HeadingCell width=10%>Status":
                             "</td>":
                             "</tr>"
.
GRHED999  RETURN
+
.------------------------------------------------------------
. Security Group User ID Maintenance (TABLE HEADING)
.------------------------------------------------------------
GUSRH000  WRITE     HTMLFILE;"<tr>":
                             "<td class=HeadingCell width=20%>Security Group":
                             "</td>":
                             "<td class=HeadingCell width=80%>Description":
                             "</td>":
                             "</tr>"
.
GUSRH999  RETURN
+
.------------------------------------------------------------
. Security Group Category Maintenance (TABLE HEADING)
.------------------------------------------------------------
GCATH000  WRITE     HTMLFILE;"<tr>":
                             "<td class=HeadingCell width=20%>Security Group":
                             "</td>":
                             "<td class=HeadingCell width=80%>Description":
                             "</td>":
                             "</tr>"
.
GCATH999  RETURN
+
.------------------------------------------------------------
. Security Group Maintenance (TABLE ROW)
.------------------------------------------------------------
GRROW000  MATCH     SP70,IBCGDESC
          IF        @EQUAL
            MOVE      "&nbsp;",IBCGDESC
          ENDIF
.
          MATCH   "1",IBCGACTV
          IF      @EQUAL
             MOVE    "Active  ",KEY8
          ELSE
             MOVE    "Inactive",KEY8
          ENDIF
.
GRROW030  WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:ShowDetail01":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&scgrp001=",IBCGSGRP,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             IBCGSGRP,"</td>":
                             "<td>",IBCGDESC,"</td>":
                             "<td>",KEY8,"</td>":
                             "</tr>"
.
GRROW999  RETURN
+
.------------------------------------------------------------
. Security Group User ID Maintenance (TABLE ROW)
.------------------------------------------------------------
GUSRR000  MATCH     SP70,GROUPDSC
          IF        @EQUAL
            MOVE      "&nbsp;",GROUPDSC
          ENDIF
.
GUSRR030  WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:ShowDetail03":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&scusr001=",IBCUSGRP:
                             "&scusr002=",IBCUUSER,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             IBCUSGRP,"</td>":
                             "<td>",GROUPDSC,"</td>":
                             "</tr>"
.
GUSRR999  RETURN
+
.------------------------------------------------------------
. Security Group Category Maintenance (TABLE ROW)
.------------------------------------------------------------
GCATR000  MATCH     SP70,GROUPDSC
          IF        @EQUAL
            MOVE      "&nbsp;",GROUPDSC
          ENDIF
.
GCATR030  WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:ShowDetail02":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&sccat001=",IBCCSGRP:
                             "&sccat002=",IBCCCATG:
                             "#"",COMMA,"#"",ONE,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             IBCCSGRP,"</td>":
                             "<td>",GROUPDSC,"</td>":
                             "</tr>"
.
GCATR999  RETURN
+
.-----------------------------------------------------------
. Link to Next Group
.-----------------------------------------------------------
NEXTGR00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
          REP       " +",STARTKEY
          REP       " +",CURRCODE
          REP       " +",CURRCATG
          REP       " +",CURRUSER
          REP       "'~",STARTKEY
.
          WRITE     HTMLFILE;"comweb03.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&startkey=",STARTKEY:
                                 "&sccat001=",CURRCODE:
                                 "&sccat002=",CURRCATG:
                                 "&scusr001=",CURRCODE:
                                 "&scusr002=",CURRUSER:
                                 "&sccod001=",CURRCODE:
                                 "&sccod002=",CURRCATG;
.
          REP       "+ ",STARTKEY
          REP       "+ ",CURRCODE
          REP       "+ ",CURRCATG
          REP       "+ ",CURRUSER
          REP       "~'",STARTKEY
.
NEXTGR99  RETURN
.-------------------------------------------------------------
. Link to Prev Group
.-------------------------------------------------------------
PREVGR00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3
          IF        F3<0
            MOVE      ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
          REP       " +",STARTKEY
          REP       " +",CURRCODE
          REP       " +",CURRCATG
          REP       " +",CURRUSER
          REP       "'~",STARTKEY
.
          WRITE     HTMLFILE;"comweb03.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&startkey=",STARTKEY:
                                 "&sccat001=",CURRCODE:
                                 "&sccat002=",CURRCATG:
                                 "&scusr001=",CURRCODE:
                                 "&scusr002=",CURRUSER:
                                 "&sccod001=",CURRCODE:
                                 "&sccod002=",CURRCATG;
.
          REP       "+ ",STARTKEY
          REP       "+ ",CURRCODE
          REP       "+ ",CURRCATG
          REP       "+ ",CURRUSER
          REP       "~'",STARTKEY
.
PREVGR99  RETURN
+
.-----------------------------------------------------------
. Option 2: Security Group Categories
.-----------------------------------------------------------
CSCS0000  BRANCH    FLDITMNO,CSCS0100,CSCS0200,CSCS0300,CSCS0400,CSCS0500:
                             CSCS0600,CSCS0700,CSCS0800,CSCS0900,CSCS1000:
                             CSCS1100,CSCS1200,CSCS1300,CSCS1400,CSCS1500
.
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      CSCS9999
.
CSCS0100  WRITE     HTMLFILE;IBCCSGRP;
          GOTO      CSCS9999
.
CSCS0200  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,CSCS0210,CSCS0220
          GOTO      CSCS9999
.
CSCS0210  WRITE     HTMLFILE;IBCCCATG;
          GOTO      CSCS9999
.
CSCS0220  PACK      IBCCDESC,SP70
          PACK      KEY5,IBCCCATG,SP2
          CALL      RDCODE1
          IF        OVRCD<>1
            PACK      IBCCDESC,TDESC,SP70
          ENDIF
          WRITE     HTMLFILE;IBCCDESC;
          GOTO      CSCS9999
.
CSCS0300
          GOTO      CSCS9999
.
CSCS0400  MATCH     IBCCCDAT,SP70
          GOTO      CSCS9999 IF EQUAL
          UNPACK    IBCCCDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          GOTO      CSCS9999
.
CSCS0500  WRITE     HTMLFILE;IBCCCTIM;
          GOTO      CSCS9999
.
CSCS0600  WRITE     HTMLFILE;IBCCCUID;
          GOTO      CSCS9999
.
CSCS0700  CALL      CATCT000        * Table of Categories by Category
          GOTO      CSCS9999
.
CSCS0800  CALL      GRCAT000        * Table of Groups against a Category
          GOTO      CSCS9999
.
CSCS0900  CALL      NEXTGR00        * next
          GOTO      CSCS9999
.
CSCS1000  CALL      PREVGR00        * previous
          GOTO      CSCS9999
.
CSCS1100  WRITE     HTMLFILE;CURRCODE;
          GOTO      CSCS9999
.
CSCS1200  WRITE     HTMLFILE;CATEDESC;
          GOTO      CSCS9999
.
CSCS1300  MOVE      SCCAT002,CATGRY
          CALL      CATLST00        * list of categories
          GOTO      CSCS9999
.
CSCS1400  WRITE     HTMLFILE;CURRCATG;
          GOTO      CSCS9999
.
CSCS1500  MOVE      SP70,GROUPCD
          CALL      GRPLST00        * list of security groups
          GOTO      CSCS9999
.
CSCS9999  RETURN
.
+
.------------------------------------------------------------
. Table of Categories by Category
.------------------------------------------------------------
CATCT000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          CALL      CTHED000      * Output Table Heading
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY5,SCCAT001,STARTKEY,SP70
          CALL      RSIBCCS1
          CALL      RPIBCCS1
.
CATCT100  CALL      RKIBCCS1
          BRANCH    OVRCD,CATCT900
.
          MATCH     SCCAT001,IBCCSGRP
          GOTO      CATCT900 IF NOT EQUAL
.
          PACK      IBCCDESC,SP70
          PACK      KEY5,IBCCCATG,SP2
          CALL      RDCODE1
          IF        OVRCD<>1
            PACK      IBCCDESC,TDESC,SP70
          ENDIF
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      CATCT100
          ENDIF
.
          CALL      CTROW000                * Display Category Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      CATCT100 IF NOT EQUAL
          GOTO      CATCT999
.
CATCT900  CALL      NOMORE00     * Display No More Records Message
.
CATCT999  RETURN
+
.------------------------------------------------------------
. Security Group Category Maintenance (TABLE HEADING)
.------------------------------------------------------------
CTHED000  WRITE     HTMLFILE;"<tr>":
                             "<td class=HeadingCell width=20%>Category":
                             "</td>":
                             "<td class=HeadingCell width=80%>Description":
                             "</td>":
                             "</tr>"
.
CTHED999  RETURN
+
.------------------------------------------------------------
. Security Group Category Maintenance (TABLE ROW)
.------------------------------------------------------------
CTROW000  MATCH     SP70,IBCCDESC
          IF        @EQUAL
            MOVE      "&nbsp;",IBCCDESC
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:ShowDetail02":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&sccat001=",IBCCSGRP:
                             "&sccat002=",IBCCCATG:
                             "#"",COMMA,"#"",ZERO,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             IBCCCATG,"</td>":
                             "<td>",IBCCDESC,"</td>":
                             "</tr>"
.
CTROW999  RETURN
+
.-----------------------------------------------------------
. Option 3: Security Group User IDs
.-----------------------------------------------------------
CSUS0000  BRANCH    FLDITMNO,CSUS0100,CSUS0200,CSUS0300,CSUS0400,CSUS0500:
                             CSUS0600,CSUS0700,CSUS0800,CSUS0900,CSUS1000:
                             CSUS1100,CSUS1200,CSUS1300,CSUS1400,CSUS1500
.
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      CSUS9999
.
CSUS0100  WRITE     HTMLFILE;IBCUSGRP;
          GOTO      CSUS9999
.
CSUS0200  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,CSUS0210,CSUS0220
          GOTO      CSUS9999
.
CSUS0210  WRITE     HTMLFILE;IBCUUSER;
          GOTO      CSUS9999
.
CSUS0220  PACK      IBCUNAME,SP70
          PACK      KEY10,IBCUUSER,SP70
          CALL      RDWBSE1
          IF        OVRCD<>1
            PACK      IBCUNAME,WBSENAM,SP70
          ENDIF
          WRITE     HTMLFILE;IBCUNAME;
          GOTO      CSUS9999
.
CSUS0300
          GOTO      CSUS9999
.
CSUS0400  MATCH     IBCUCDAT,SP70
          GOTO      CSUS9999 IF EQUAL
          UNPACK    IBCUCDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          GOTO      CSUS9999
.
CSUS0500  WRITE     HTMLFILE;IBCUCTIM;
          GOTO      CSUS9999
.
CSUS0600  WRITE     HTMLFILE;IBCUCUID;
          GOTO      CSUS9999
.
CSUS0700  CALL      USRUS000        * Table of Users by User
          GOTO      CSUS9999
.
CSUS0800  CALL      GRUSR000        * Table of Security Groups against User
          GOTO      CSUS9999
.
CSUS0900  CALL      NEXTGR00        * next
          GOTO      CSUS9999
.
CSUS1000  CALL      PREVGR00        * previous
          GOTO      CSUS9999
.
CSUS1100  WRITE     HTMLFILE;CURRCODE;
          GOTO      CSUS9999
.
CSUS1200  WRITE     HTMLFILE;CATEDESC;
          GOTO      CSUS9999
.
CSUS1300  MOVE      SCUSR002,USERCD
          CALL      USRLST00        * list of user ids
          GOTO      CSUS9999
.
CSUS1400  WRITE     HTMLFILE;CURRUSER;
          GOTO      CSUS9999
.
CSUS1500  MOVE      SP70,GROUPCD
          CALL      GRPLST00        * list of security groups
          GOTO      CSUS9999
.
CSUS9999  RETURN
+
.------------------------------------------------------------
. Table of Users by User ID
.------------------------------------------------------------
USRUS000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          CALL      USHED000      * Output Table Heading
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY13,SCUSR001,STARTKEY,SP70
          CALL      RSIBCUS1
          CALL      RPIBCUS1
.
USRUS100  CALL      RKIBCUS1
          BRANCH    OVRCD,USRUS900
.
          MATCH     SCUSR001,IBCUSGRP
          GOTO      USRUS900 IF NOT EQUAL
.
          PACK      IBCUNAME,SP70
          PACK      KEY10,IBCUUSER,SP70
          CALL      RDWBSE1
          IF        OVRCD<>1
            PACK      IBCUNAME,WBSENAM,SP70
          ENDIF
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      USRUS100
          ENDIF
.
          CALL      USROW000                * Display User ID Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      USRUS100 IF NOT EQUAL
          GOTO      USRUS999
.
USRUS900  CALL      NOMORE00    * Display No More Records Message
.
USRUS999  RETURN
+
.------------------------------------------------------------
. Table of Groups against a User ID
.------------------------------------------------------------
GRUSR000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          CALL      GUSRH000      * Output Table Heading
.
          IF        NORECORD=0
            MOVE      "13",NORECORD       * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY13,SCUSR002,STARTKEY,SP70
          CALL      RSIBCUS2
          CALL      RPIBCUS2
.
GRUSR100  CALL      RKIBCUS2
          BRANCH    OVRCD,GRUSR999
.
          MATCH     SCUSR002,IBCUUSER
          GOTO      GRUSR999 IF NOT EQUAL
.
          PACK      GROUPDSC,SP70
          PACK      KEY3,IBCUSGRP,SP70
          CALL      RDIBCGS1
          IF        OVRCD<>1
            PACK      GROUPDSC,IBCGDESC,SP70
          ENDIF
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      GRUSR100
          ENDIF
.
          CALL      GUSRR000                * Display Group Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      GRUSR100 IF NOT EQUAL
.
GRUSR999  RETURN
+
.------------------------------------------------------------
. Security Group User ID Maintenance (TABLE HEADING)
.------------------------------------------------------------
USHED000  WRITE     HTMLFILE;"<tr>":
                             "<td class=HeadingCell width=20%>User ID":
                             "</td>":
                             "<td class=HeadingCell width=80%>Username":
                             "</td>":
                             "</tr>"
.
USHED999  RETURN
+
.------------------------------------------------------------
. Security Group User ID Maintenance (TABLE ROW)
.------------------------------------------------------------
USROW000  MATCH     SP70,IBCUNAME
          IF        @EQUAL
            MOVE      "&nbsp;",IBCUNAME
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:ShowDetail03":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&scusr001=",IBCUSGRP:
                             "&scusr002=",IBCUUSER,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             IBCUUSER,"</td>":
                             "<td>",IBCUNAME,"</td>":
                             "</tr>"
.
USROW999  RETURN
+
.------------------------------------------------------------
. Table of Groups against a Category
.------------------------------------------------------------
GRCAT000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          CALL      GCATH000      * Output Table Heading
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY5,SCCAT002,STARTKEY,SP70
          CALL      RSIBCCS2
          CALL      RPIBCCS2
.
GRCAT100  CALL      RKIBCCS2
          BRANCH    OVRCD,GRCAT900
.
          MATCH     SCCAT002,IBCCCATG
          GOTO      GRCAT900 IF NOT EQUAL
.
          PACK      GROUPDSC,SP70
          PACK      KEY3,IBCCSGRP,SP70
          CALL      RDIBCGS1
          IF        OVRCD<>1
            PACK      GROUPDSC,IBCGDESC,SP70
          ENDIF
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      GRCAT100
          ENDIF
.
          CALL      GCATR000                * Display Group Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      GRCAT100 IF NOT EQUAL
          GOTO      GRCAT999
.
GRCAT900  CALL      NOMORE00      * Display No More Records Message
.
GRCAT999  RETURN
+
.------------------------------------------------------------
. List of Categories
.------------------------------------------------------------
CATLST00  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,CATLST10,CATLST20
          GOTO      CATLST99
.
CATLST10  PACK      CATGRY,CATGRY,SP70
          MATCH     SP70,CATGRY
          GOTO      CATLST99 IF EQUAL
          WRITE     HTMLFILE;CATGRY;
          GOTO      CATLST99
.
CATLST20  CALL      CATSEL00
          GOTO      CATLST99
.
CATLST99  RETURN
+
.------------------------------------------------------------
. Add Selection Options to Form (category value)
.------------------------------------------------------------
CATSEL00  MOVE      SP70,TDESC
          MOVE      SP70,SAVCATG
.
          PACK      PTCDKEY2,CATGRY,SP70
          CALL      RDSCODE2
CATSEL10  CALL      RDKCODE2
          BRANCH    OVRCD,CATSEL99
.
          MATCH     SAVCATG,TCODE
          GOTO      CATSEL10 IF EQUAL
.
          MATCH     SP70,ACODE
          GOTO      CATSEL10 IF NOT EQUAL
.
          MATCH     CATGRY,TCODE
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=",TCODE," selected>":
                               "(",TCODE,") ",TDESC,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=",TCODE,">":
                               "(",TCODE,") ",TDESC,"</option>"
          ENDIF
          MOVE      TCODE,SAVCATG      * save category
          GOTO      CATSEL10
.
CATSEL99  RETURN
+
.------------------------------------------------------------
. List of User IDs
.------------------------------------------------------------
USRLST00  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,USRLST10,USRLST20
          GOTO      USRLST99
.
USRLST10  PACK      USERCD,USERCD,SP70
          MATCH     SP70,USERCD
          GOTO      USRLST99 IF EQUAL
          WRITE     HTMLFILE;USERCD;
          GOTO      USRLST99
.
USRLST20  CALL      USRSEL00
          GOTO      USRLST99
.
USRLST99  RETURN
+
.------------------------------------------------------------
. Add Selection Options to Form (user id value)
.------------------------------------------------------------
USRSEL00  MOVE      SP70,WBSENAM
.
          PACK      KEY10,USERCD,SP70
          CALL      RSWBSE1
USRSEL10  CALL      RKWBSE1
          BRANCH    OVRCD,USRSEL99
.
          MATCH     USERCD,WBSEUID
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=",WBSEUID," selected>":
                               "(",WBSEUID,") ",WBSENAM,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=",WBSEUID,">":
                               "(",WBSEUID,") ",WBSENAM,"</option>"
          ENDIF
          GOTO      USRSEL10
.
USRSEL99  RETURN
+
.------------------------------------------------------------
. List of Security Groups
.------------------------------------------------------------
GRPLST00  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,GRPLST10,GRPLST20
          GOTO      GRPLST99
.
GRPLST10  PACK      GROUPCD,GROUPCD,SP70
          MATCH     SP70,GROUPCD
          GOTO      GRPLST99 IF EQUAL
          WRITE     HTMLFILE;GROUPCD;
          GOTO      GRPLST99
.
GRPLST20  CALL      GRPSEL00
          GOTO      GRPLST99
.
GRPLST99  RETURN
+
.------------------------------------------------------------
. Add Selection Options to Form (group value)
.------------------------------------------------------------
GRPSEL00  MOVE      SP70,IBCGDESC
.
          PACK      KEY3,GROUPCD,SP70
          CALL      RSIBCGS1
GRPSEL10  CALL      RKIBCGS1
          BRANCH    OVRCD,GRPSEL99
.
          MATCH     "1",IBCGACTV
          GOTO      GRPSEL10 IF NOT EQUAL   * only want active groups listed
.
          MATCH     GROUPCD,IBCGSGRP
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=",IBCGSGRP," selected>":
                               "(",IBCGSGRP,") ",IBCGDESC,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=",IBCGSGRP,">":
                               "(",IBCGSGRP,") ",IBCGDESC,"</option>"
          ENDIF
          GOTO      GRPSEL10
.
GRPSEL99  RETURN
+
.-----------------------------------------------------------
. Option 4: HIC Medicare Explanation Codes
.-----------------------------------------------------------
HMEC0000  BRANCH    FLDITMNO,HMEC0100,HMEC0200,HMEC0300,HMEC0400,HMEC0500:
                             HMEC0600
.
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      HMEC9999
.
HMEC0100  WRITE     HTMLFILE;HCEMCODE;
          GOTO      HMEC9999
.
HMEC0200  WRITE     HTMLFILE;HCEMDESC;
          GOTO      HMEC9999
.
HMEC0300  MOVE      ZERO,LINK2PRT
          CALL      HMCTB000      * Table of Codes
          GOTO      HMEC9999
.
HMEC0400  CALL      NEXTGR00      * Link to Next Group
          GOTO      HMEC9999
.
HMEC0500  CALL      PREVGR00      * Link to Last Group
          GOTO      HMEC9999
.
HMEC0600  MOVE      ZERO,LINK2PRT
          CALL      HMCTD000      * Table of Codes (by description)
          GOTO      HMEC9999
.
HMEC9999  RETURN
+
.------------------------------------------------------------
. Medicare Explanation Codes Table
.------------------------------------------------------------
HMCTB000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          CALL      HMHED000      * Output Table Heading
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY3,STARTKEY,HCEMS001,SP70
          CALL      RDHCEMS1
          IF        OVRCD=0
            CALL      RPHCEMS1
          ENDIF
HMCTB100  CALL      RKHCEMS1
          BRANCH    OVRCD,HMCTB900
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      HMCTB100
          ENDIF
.
          CALL      HMROWA00      *  Table Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      HMCTB100 IF NOT EQUAL
          GOTO      HMCTB999
.
HMCTB900  CALL      NOMORE00      * Display No More Records Message
.
HMCTB999  RETURN
.------------------------------------------------------------
. Medicare Explanation Codes Table - by description
.------------------------------------------------------------
HMCTD000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          CALL      HMHED000      * Output Table Heading
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY73,STARTKEY,HCEMS002,SP70
          CALL      RDHCEMS2
          IF        OVRCD=0
            CALL      RPHCEMS2
          ENDIF
HMCTD100  CALL      RKHCEMS2
          BRANCH    OVRCD,HMCTD900
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      HMCTD100
          ENDIF
.
          CALL      HMROWA00      *  Table Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      HMCTD100 IF NOT EQUAL
          GOTO      HMCTD999
.
HMCTD900  CALL      NOMORE00      * Display No More Records Message
.
HMCTD999  RETURN
.------------------------------------------------------------
. Medicare Explanation Codes Maintenance (TABLE HEADING)
.------------------------------------------------------------
HMHED000  WRITE     HTMLFILE;"<tr>":
                               "<td class=HeadingCell width=15%>":
                               "<b>Explanation Code</b></td>":
                               "<td class=HeadingCell>":
                               "<b>Description</b></td>":
                             "</tr>"
.
HMHED999  RETURN
+
.------------------------------------------------------------
. Medicare Explanation Codes Maintenance (TABLE ROW)
.------------------------------------------------------------
HMROWA00  MATCH     SP70,HCEMDESC
          IF        @EQUAL
            MOVE      "&nbsp;",HCEMDESC
          ENDIF
.
          BRANCH    LINK2PRT,HMROWA10
.
          WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:ShowDetail04":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&hcems001=",HCEMCODE,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             HCEMCODE,"</td>":
                             "<td>",HCEMDESC,"</td>":
                             "</tr>"
          GOTO      HMROWA99
.
HMROWA10  MOVE      HCEMDESC,JAVSNAME   * String to Convert
          CALL      JAVDES00         * Remove "'" replace with "%60"
.
          WRITE     HTMLFILE;"<tr valign=top>":
                              "<td><a href=#"":
                              "javascript:UpdateParent('",HCEMCODE:
                              "','",JAVSNAME,"')#">":
                              HCEMCODE,"</a></td><td>",HCEMDESC,"</td></tr>";
.
HMROWA99 RETURN
+
.------------------------------------------------------------
. Format Description/Name (for Javascript)
.*** format the surname and given name by changing any ' to %60
.---------------------------------------------------------------
JAVDES00  SCAN      "'",JAVSNAME             * find any apostrophies
          GOTO      JAVDES99 IF NOT EQUAL
.
          MOVE      JAVSNAME,ENDNAME
          REP       "' ",ENDNAME             * remove the apostrophy
.
          LENSET    JAVSNAME
          RESET     JAVSNAME                 * get all characters before '
          MOVE      JAVSNAME,STRTNAME
.
          REP       "'%",STRTNAME
          STRIP     STRTNAME
          PACK      JAVSNAME,STRTNAME,SIXTY,ENDNAME,SP70 * formatted string
.
JAVDES99  RETURN
.
.------------------------------------------------------------
. Security Group Category Code Records: Display,Add,Delete
.------------------------------------------------------------
SECCOD00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      SECCOD40 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      SECCOD10 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      SECCOD30 IF EQUAL
.
          GOTO      SECCOD93
.
SECCOD10  PACK      KEY8,SCCOD001,SCCOD002,SCCOD003,SP70
          CALL      RDIBCOS1
          COMPARE   ZERO,OVRCD
          GOTO      SECCOD92 IF EQUAL
.
          CALL      CODSAV00      * Moves input variables to file variables
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          MOVE      KEY8,IBCOCDAT                * System date
          CLOCK     TIME,IBCOCTIM                * System time
          MOVE      USERID,IBCOCUID              * web user id
.
          PACK      KEY8,SCCOD001,SCCOD002,SCCOD003,SP70
          CALL      WRIBCOS1             * Writes away the data
          MOVE      ZERO,EXIT
          GOTO      SECCOD99
.
SECCOD30  PACK      KEY8,SCCOD001,SCCOD002,SCCOD003,SP70
          CALL      RDIBCOS1
          BRANCH    OVRCD,SECCOD91
.
          PACK      KEY8,SCCOD001,SCCOD002,SCCOD003,SP70
          CALL      DEIBCOS1             * delete security group category code
          MOVE      ZERO,EXIT
          GOTO      SECCOD99
.
.         ************ DISPLAY FORMACTION **********
.
SECCOD40  PACK      CURRCODE,SP70
          PACK      CATEDESC,SP70
.
          PACK      KEY3,SCCOD001,SP70
          CALL      RDIBCGS1
          BRANCH    OVRCD,SECCOD94
.
          MOVE      IBCGSGRP,CURRCODE  * Current group
          MOVE      IBCGDESC,CATEDESC  * Current group desc.
.
          PACK      KEY5,SCCOD001,SCCOD002,SP70
          CALL      RDIBCCS1
          BRANCH    OVRCD,SECCOD94
.
          MOVE      IBCCCATG,CURRCATG  * Current category
.
          PACK      KEY8,SCCOD001,SCCOD002,SCCOD003,SP70
          CALL      RDIBCOS1
          IF        OVRCD=1
            CALL      CLRCOD00         * Clear sec.group category file variables
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Category Security Group Category Code Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00                * Output File & Write HTML Pg Header
          BRANCH    EXIT,SECCOD99
.
          CALL      WEBBOD00                * Process Web Body
.
          CALL      WEBEND00                * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      SECCOD99
.
.         ************ DISPLAY ERROR MESSAGE **********
.
SECCOD91  MOVE      "Category Record does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SECCOD99
.
SECCOD92  MOVE      "Category Record exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SECCOD99
.
SECCOD93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SECCOD99
.
SECCOD94  MOVE      "Invalid Group/Category.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SECCOD99
.
SECCOD99  RETURN
.
.------------------------------------------------------------
. Moves security category code cgi variables into file variables
.------------------------------------------------------------
CODSAV00  MATCH     SCCOD001,Z70
          IF        !@EQUAL
            PACK      IBCOSGRP,SCCOD001,SP70
          ENDIF
.
          MATCH     SCCOD002,Z70
          IF        !@EQUAL
            PACK      IBCOCATG,SCCOD002,SP70
          ENDIF
.
          MATCH     SCCOD003,Z70
          IF        !@EQUAL
            PACK      IBCOCATC,SCCOD003,SP70
          ENDIF
.
CODSAV99  RETURN
.
.-----------------------------------------------------------
. Clear sec.category code file variables
.-----------------------------------------------------------
CLRCOD00  PACK      IBCOSGRP,SP70
          PACK      IBCOCATG,SP70
          PACK      IBCOCATC,SP70
          PACK      IBCOCDAT,SP70
          PACK      IBCOCTIM,SP70
          PACK      IBCOCUID,SP70
          PACK      IBCOSPAR,SP70
.
CLRCOD99  RETURN
.
.-----------------------------------------------------------
. Option 5: Security Group Category Codes
.-----------------------------------------------------------
CSCO0000  BRANCH    FLDITMNO,CSCO0100,CSCO0200,CSCO0300,CSCO0400,CSCO0500:
                             CSCO0600,CSCO0700,CSCO0800,CSCO0900,CSCO1000:
                             CSCO1100,CSCO1200,CSCO1300,CSCO1400,CSCO1500:
                             CSCO1600
.
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      CSCO9999
.
CSCO0100  WRITE     HTMLFILE;IBCOSGRP;
          GOTO      CSCO9999
.
CSCO0200  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,CSCO0210,CSCO0220
          GOTO      CSCO9999
.
CSCO0210  WRITE     HTMLFILE;IBCOCATG;
          GOTO      CSCO9999
.
CSCO0220  PACK      IBCCDESC,SP70
          PACK      KEY5,IBCOCATG,SP2
          CALL      RDCODE1
          IF        OVRCD<>1
            PACK      IBCCDESC,TDESC,SP70
          ENDIF
          WRITE     HTMLFILE;IBCCDESC;
          GOTO      CSCO9999
.
CSCO0300  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,CSCO0310,CSCO0320
          GOTO      CSCO9999
.
CSCO0310  WRITE     HTMLFILE;IBCOCATC;
          GOTO      CSCO9999
.
CSCO0320  PACK      IBCCDESC,SP70
          PACK      KEY5,IBCOCATG,IBCOCATC,SP2
          CALL      RDCODE1
          IF        OVRCD<>1
            PACK      IBCCDESC,TDESC,SP70
          ENDIF
          WRITE     HTMLFILE;IBCCDESC;
          GOTO      CSCO9999
.
CSCO0400  MATCH     IBCOCDAT,SP70
          GOTO      CSCO9999 IF EQUAL
          UNPACK    IBCOCDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          GOTO      CSCO9999
.
CSCO0500  WRITE     HTMLFILE;IBCOCTIM;
          GOTO      CSCO9999
.
CSCO0600  WRITE     HTMLFILE;IBCOCUID;
          GOTO      CSCO9999
.
CSCO0700  CALL      CODCT000        * Table of Category Codes by Group/Category
          GOTO      CSCO9999
.
CSCO0800  
          GOTO      CSCO9999
.
CSCO0900  CALL      NEXTGR00        * next
          GOTO      CSCO9999
.
CSCO1000  CALL      PREVGR00        * previous
          GOTO      CSCO9999
.
CSCO1100  WRITE     HTMLFILE;CURRCODE;
          GOTO      CSCO9999
.
CSCO1200  WRITE     HTMLFILE;CATEDESC;
          gOTO      CSCO9999
.
CSCO1300  
          GOTO      CSCO9999
.
CSCO1400  WRITE     HTMLFILE;CURRCATG;
          GOTO      CSCO9999
.
CSCO1500  MOVE      SCCOD002,CATEGORY
          MOVE      IBCOCATC,CODE
          CALL      CODITM00         * list of category codes  
          GOTO      CSCO9999
.
CSCO1600  PACK      IBCCDESC,SP70
          PACK      KEY5,CURRCATG,SP2
          CALL      RDCODE1
          IF        OVRCD<>1
            PACK      IBCCDESC,TDESC,SP70
          ENDIF
          WRITE     HTMLFILE;IBCCDESC;
          GOTO      CSCO9999
.
CSCO9999  RETURN
.
.------------------------------------------------------------
. Table of Category Codes by Group/Category
.------------------------------------------------------------
CODCT000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          CALL      CDHED000      * Output Table Heading
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY8,SCCOD001,SCCOD002,STARTKEY,SP70
          CALL      RSIBCOS1
          CALL      RPIBCOS1
.
CODCT100  CALL      RKIBCOS1
          BRANCH    OVRCD,CODCT900
.
          MATCH     SCCOD001,IBCOSGRP
          GOTO      CODCT900 IF NOT EQUAL
.
          MATCH     SCCOD002,IBCOCATG
          GOTO      CODCT900 IF NOT EQUAL
.
          PACK      IBCCDESC,SP70
          PACK      KEY5,IBCOCATG,IBCOCATC,SP2
          CALL      RDCODE1
          IF        OVRCD<>1
            PACK      IBCCDESC,TDESC,SP70
          ENDIF
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      CODCT100
          ENDIF
.
          CALL      CDROW000                * Display Category Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      CODCT100 IF NOT EQUAL
          GOTO      CODCT999
.
CODCT900  CALL      NOMORE00     * Display No More Records Message
.
CODCT999  RETURN
+
.------------------------------------------------------------
. Security Group Category Maintenance (TABLE HEADING)
.------------------------------------------------------------
CDHED000  WRITE     HTMLFILE;"<tr>":
                             "<td class=HeadingCell width=20%>Code":
                             "</td>":
                             "<td class=HeadingCell width=80%>Description":
                             "</td>":
                             "</tr>"
.
CDHED999  RETURN
+
.------------------------------------------------------------
. Security Group Category Maintenance (TABLE ROW)
.------------------------------------------------------------
CDROW000  MATCH     SP70,IBCCDESC
          IF        @EQUAL
            MOVE      "&nbsp;",IBCCDESC
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:ShowDetail05":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&sccod001=",IBCOSGRP:
                             "&sccod002=",IBCOCATG:
                             "&sccod003=",IBCOCATC:
                             "#"",COMMA,"#"",ZERO,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             IBCOCATC,"</td>":
                             "<td>",IBCCDESC,"</td>":
                             "</tr>"
.
CDROW999  RETURN
.
.------------------------------------------------------------
. NOMORE00 - Display "No more records" at end of list
.------------------------------------------------------------
NOMORE00  WRITE     HTMLFILE;"<tr>":
                             "<td class=RedCell colspan=20 align=center>":
                             "No more records</td>"
.
NOMORE99  RETURN
.
.------------------------------------------------------------------------------
. I/O and Subroutine Includes
.------------------------------------------------------------------------------
.
          INC       IBAWEBCD
          INC       DAYOFWEK
          INC       RSHWEBCD
          INC       WEBDATCD
.
          INC       HICEMSIO/INC            * HIC Medicare Explanation Codes
          INC       IBACCSIO/INC            * Category Security Categories
          INC       IBACGSIO/INC            * Category Security Groups
          INC       IBACOSIO/INC            * Category Code Security
          INC       IBACUSIO/INC            * Category Security User IDs
+
.------------------------------------------------------------------------------
. End Of Program
.------------------------------------------------------------------------------

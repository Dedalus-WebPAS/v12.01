. *****************************************************************************
. *              P R O G R A M  S U M M A R Y                                 *
. *              -------------  -------------                                 *
. *                                                                           *
. *     PROGRAM NAME:     IBAHOS83                                            *
. *                                                                           *
. *     FUNCTION:         CUMULATIVE YTD STATISTICS REPORT                    *
. *                                                                           *
. *     DATE WRITTEN:     26/10/88                                            *
. *                                                                           *
. *     AUTHOR:           JENI TAN      (I.B.A)                               *
. *                                                                           *
. *     MODIFICATIONS:                                                        *
. *                                                                           *
. *       V10.13.01 05/12/2018  Don Nguyen       TSK 0838558                  *
. *                 Deleted temp file (PSTROL) after processing.              *
. *                 Removed PORT from temp filenames (FNAMER & FNAMET).       *
. *****************************************************************************
. *       V10.03.01 17/10/2012  Jill Parkinson CAR 273375                     *
. *                 Recompiled for DATECONV - first day of year calculation   *
. *       V9.02.01  24/10/2001  B.G.Ackland                                   *
. *                 Remove pi on Temp File                                    *
. *       V5.07.B01 14/11/1998  Davin                                         *
. *                 Mods for 507                                              *
. *       V5.04.01  02/05/1997  Steve Armstrong   SRF 642857                  *
. *                 Changed temp file name "pstidx" to standard format        *
. *****************************************************************************
. *                        V5.01 17.06.89 S.Barcham                           *
. *                             Compiled for new standbys                     *
. *                             SRF 101101                                    *
. *                       V5.02 30/06/89 DIG                                  *
. *                             Re-Compiled for changes to                    *
. *                    V5.01.01 03/07/89 K.Peak                               *
. *                             Changed Adm & U/R to 8 chrs                   *
. *                             10/07/89 Graeme Williams                      *
. *                             Allow for 13x4 week periods                   *
. *                    V5.01.02 29.09.89 S.Barcham                            *
. *                             Recompiled for 13*4 Weeks                     *
. *                             SRF 102510                                    *
. *                    V5.01.03 02.10.89 S.Barcham                            *
. *                             Fixed 13x4 week Periods                       *
. *                             SRF 102537 102568 102415                      *
. *                    V5.01.04 19/06/90 J.Tan                                *
. *                             Fixed calculate no of days                    *
. *                    V5.01.05 28/06/90 J.Tan                                *
. *                             Added separate bed days                       *
. *                    V5.01.06 02/06/93 J.Tan SRF 121660                     *
. *                             Added ward/bed history file                   *
. *                    V5.01.07 30/12/93 I.Rutt MTA                           *
. *                             Added include PATACDAY                        *
. *        V5.02.001 02/03/95 Paul Howells     SRF 135026                     *
. *                  Fixed the use of PATWBHFD file.                          *
. *        V5.03.01  10/04/1996  Steve Armstrong                              *
. *                  Mods to use WDACTVDY to calculate active bed days.       *
. *                  Mods to cater for changes to patmstsf.                   *
. *                  Mods to use PTCNH82W.                                    *
. *****************************************************************************
.
          INC       STD001FD
          INC       PATCOMM/INC
.
          INC       PATCODFD/INC    * Codes file
          INC       PATDRGFD/INC    * Period Range file
          INC       PATMSTFD/INC    * Monthly inpatient statistics file
          INC       PATWR1FD/INC    * Ward file
          INC       PATWBHFD/INC    * Ward/bed history file
          INC       PATBRNFD/INC    * New born file
          INC       OUTSITFD/INC    * Outpatients - site code file
          INC       OUTSTBFD/INC    * Statistics A&E Booking file
          INC       AAESTAFD/INC    * A&E Statistics file
          INC       OPRWARFD/INC    * Ward Statistics file
          INC       WDACTVVR/INC
.
          INC       IBASEQFD/INC 
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
+
.               WORK AREA
.
.-------------------------------------------------------------
.Name      Type     Length      Physical      Start
.----      ----     ------      --------      -----
.MSDCLS    DIM          3             3          1
.MSWCLS    DIM          3             3          4
.MSWARD    DIM          3             3          7
INBNUM     FORM         3             3         10
PADYTD     FORM         8             5         13   * adms for last year
CADYTD     FORM         8             5         18   * adms for current year
PBDYTD     FORM         8             5         23   * bed days for last year
CBDYTD     FORM         8             5         28   * bed days for current year
CABDAY     FORM         8             5         33
PABDAY     FORM         8             5         38
CHNGFLG    FORM         1             2         43
.
.  END OF RECORD                                45
.-------------------------------------------------------------
PSTROL   FILE      ASCII, FIXED=256
TEMPFILE IFILE     SQL, FIXED=45, KEY="U1-9"
.
ADPCEN   FORM      7.2
ASKFLAG  FORM      1              * asterisk flag
.                                      0 = flag printed
.                                      1 = flag not printed
BDPCEN   FORM      7.2
.
CURMTH   DIM       6
CURYEAR  DIM       4
CODECG   INIT      "CG"
CMDLINE  DIM       50
COUNT    FORM      2
CPERMTH  DIM       6
CFMTH    DIM       6      * Current year - from date (CCYYMM)
CTMTH    DIM       6      *              - to date (CCYYMM)
CURDYTD  FORM      8
CURSTY   FORM      7.2
CURTAV   FORM      8
CENDDT   DIM       8
CSTDT    DIM       8
.
DATEFR   DIM       8
DATETO   DIM       8
DAYYTD   FORM      8
DIM4     DIM       4
.
ENDMTH   DIM       6
EDLFIN   DIM       6
EDLFDT   DIM       8
ENDDAT   FORM      2
.
FNAMER   DIM       8
FNAMET   DIM       8
FORM8    FORM      8
FORM10   FORM      10
FINYEAR  DIM       4
FIRST    FORM      "0"
FROMDATE DIM       8
.
GTADPR   FORM      8
GTADCR   FORM      8
GTBDPR   FORM      8
GTBDCR   FORM      8
GTCABDY  FORM      8
GTPABDY  FORM      8
GTOPRE   FORM      8
GTOCUR   FORM      8
.
JYEAR    FORM     2
.
.
LFMTH    DIM      6        * Last year - from date
LTMTH    DIM      6        *           - to date
LNO      FORM     2
LYEAR    FORM     "366"
LASTAV   FORM      8
LENDDT   DIM       8
LSTDT    DIM       8
LSTYEAR  DIM       4
LASTYR   DIM       5
.
MTHNUM   FORM     2
NOPCEN   FORM     7.2
NUMPERS  FORM     2
.
OPCND    FORM     1
OYEAR    FORM     "365"
OUTFLG   FORM     "0"
OCCUR    FORM     7.2
OCPRE    FORM     7.2
.
PAGENO   FORM     3
PATAD    FORM     8
PATBD    FORM     8
PREFLG   FORM     "0"
PREDYTD  FORM     8
PRESTY   FORM     7.2
PTCNH82W FORM     1
PTOCBDAY FORM     1
.
SAVCLS   DIM      3
SAVOPRE  FORM     8
SAVOCUR  FORM     8
STMTH    DIM      6
STATUS   FORM     1
SMONTH   DIM      6
SELMTH   DIM      6
SVHNOCC  FORM     3 
.
THISYR   DIM      5
TIMEIS   DIM      8
TPREOBS  FORM     8
TCUROBS  FORM     8
TPREDSH  FORM     8
TCURDSH  FORM     8
TPRESEP  FORM     8          * Previous year separation bed days
TCURSEP  FORM     8          * Current year separation bed days
TOTPAT   FORM     8
TTHCUR   FORM     8
TTHPRE   FORM     8
TBRCUR   FORM     8
TBRPRE   FORM     8
TODATE   DIM      8
TOTLBEDS FORM     8
TOJULDD  FORM     3
TOJULYY  FORM     2
TOJULCC  FORM     2
TFADMN   FORM     8
TFBDAY   FORM     8
TFNONP   FORM     8
.
WARBEDAC FORM    8.2
WARTOTBD FORM     6
WCLDES   DIM     20
WKDATE   DIM      8
XYEAR1   DIM      2
XMON1    DIM      2
.
.
PRGID     INIT      "IBAHOS83"
PRGNAM    INIT      "Cumulative Ytd Statistics Report"
VERSION   INIT      "V12.01.00"
+
.        START OF PROGRAM
.
         CALL           DISPHEAD
.
         DISPLAY        *P56:24,"Opening patcodes";
         OPEN           PATCODE1,"patcodes"
.
         DISPLAY        *P64:24,"patdrgaf";
         OPEN           PATDRGA1,"patdrgaf"
         OPEN           PATDRGA3,"patdrgaf"
.
         DISPLAY        *P64:24,"patmstsf";
         OPEN           PATMSTS1,"patmstsf"
.
         DISPLAY        *P64:24,"patwr1af";
         OPEN      PATWR1A1,"patwr1af"
.
         DISPLAY        *P64:24,"patwbhaf";
         OPEN           PATWBHA1,"patwbhaf"
.
         DISPLAY        *P64:24,"patbrnsf";
         OPEN           PATBRNS1,"patbrnsf"
.
         DISPLAY        *P64:24,"outsitaf";
         OPEN           OUTSITA1,"outsitaf"
.
         DISPLAY        *P64:24,"aaestaaf";
         OPEN           AAESTAA1,"aaestaaf"
.
         DISPLAY        *P64:24,"oprwaraf";
         OPEN           OPRWARA1,"oprwaraf"
.
         DISPLAY        *P64:24,"controlf";
         OPEN           CONTROLF,"controlf"
.
         READ           CONTROLF,TWENTY;*250,PTCNH82W
         READ           CONTROLF,FIFTY9;*27,CPERMTH,*217,PTOCBDAY
         MOVE           ONE,CNOUNDLN
.
.        Get the current financial period
.
         PACK      CPTDATE,CCC,CYY,CMM,CDD
         REP       " 0",CPTDATE
         CALL      GPERD
         BRANCH    EXIT,NOCPER
.
         PACK      CURMTH,DRGYR,DRGNUM
         MOVE      DRGYR,FINYEAR
.
.        MOVE      PORT,DIM2
.        CLEAR     FNAMER
.        APPEND    "pstrol",FNAMER
.        APPEND    DIM2,FNAMER
.        RESET     FNAMER
.        REP       " 0" IN FNAMER
.
.        CLEAR     FNAMET
.        APPEND    "hos83a",FNAMET
.        APPEND    DIM2,FNAMET
.        RESET     FNAMET
.        REP       " 0" IN FNAMET
.
         CALL      TFILENAM
         MOVE      TFILNAME,FNAMER
         CALL      TFILENAM
         MOVE      TFILNAME,FNAMET
.
         GOTO      START
.
NOCPER   KEYIN     *P1:24,*EL,*V2LON,*B:
                   "** Error: Current Period not in Period File. ":
                   "Hit <ENTER> to exit ",*EOFF,ANS;
         GOTO      ENDIT
.
NOCYRP   KEYIN     *P1:24,*EL,*V2LON,*B:
                   "** Error: Current Year not in Period File. ":
                   "Hit <ENTER> to exit ",*EOFF,ANS;
         GOTO      ENDIT
+
.        START OF PROGRAM
.
.             SELECT OPTION
.
START   MOVE        ZERO,PAGENO
        DISPLAY     *P1:4,*EF,"  Keyin ",CPERMTH,"/Year :";
.
        CALL        KEYMN
        BRANCH      OVRCD,ENDIT
.
        PACK        SELMTH,DRGYR,DRGNUM
        MOVE        DRGTDTE,CENDDT
        MOVE        DRGTDTE,ACTVTDTE
        MOVE        DRGYR TO CURYEAR
.
        MATCH       DRGYR,FINYEAR
        GOTO        CHKMTH IF EQUAL
        GOTO        REKEY IF NOT LESS
        GOTO        INVDAT1
.
CHKMTH  MATCH       SELMTH,CURMTH
        GOTO        REKEY IF NOT LESS
.
INVDAT1 DISPLAY     *P1:24,*B,*EL,*V2LON,"** Date must be < ":
                    "current ",CPERMTH," **",*W2;
        GOTO        START
.
.        Check if dates are OK or not
.
REKEY   KEYIN       *P1:24,*EF,"Ok to continue (":
                    *V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
        PACK        ANS,ANS,SP1
        REP         UPPLOW,ANS
.
        MATCH       "N" TO ANS
        GOTO        START IF EQUAL
.
        MATCH       "Y" TO ANS
        GOTO        OKPROC IF EQUAL
        BEEP
        GOTO        REKEY
.
+
.....................................................................
.
.       Check the tempfile, remove if it exists
.
OKPROC  DISPLAY     *P1:24,*EL,*P10:24,"** Processing Report **",*W;
.
        CLOCK       TIME,CTIMEIS
.
.        Get the details of the start of the financial year
.
        PACK        KEY6,DRGYR,SP2
        CALL        RDSDRGA1
        CALL        RDKDRGA1
        BRANCH      OVRCD,NOCYRP
.
        MATCH       DRGYR,CURYEAR
        GOTO        NOCYRP IF NOT EQUAL
 
.       Fromdate of current financial year (start of financial year)
.
        PACK        CFMTH,DRGYR,DRGNUM
        MOVE        DRGFDTE,CSTDT
        MOVE        DRGFDTE,ACTVFDTE
.
.       Todate of current financial year
.
        MOVE        SELMTH,CTMTH
.
.....................................................................
.
.       Get the end of last financial year
.
        UNPACK      SELMTH INTO DRGYR,DRGNUM
.
        MOVE        DRGYR TO FORM4
        SUB         ONE FROM FORM4
        MOVE        FORM4 TO LSTYEAR
.
        CALL        RDPDRGA1
        BRANCH      OVRCD,NOLFYR
.
        MATCH       DRGYR,LSTYEAR
        GOTO        NOLFYR IF NOT EQUAL
.
        PACK        EDLFIN,DRGYR,DRGNUM
        MOVE        DRGTDTE,EDLFDT
.
.....................................................................
.
.       Todate of Last financial year (using the selected month)
.
        UNPACK      SELMTH,DRGYR,DRGNUM
        PACK        LTMTH,FORM4,DRGNUM
        REP         " 0",LTMTH
.
        MOVE        LTMTH,KEY6
        CALL        RDDRGA1
        BRANCH      OVRCD,NOLEYR
.
        MOVE        DRGTDTE,LENDDT
.
.       Fromdate of Last financial year (start of financial year)
.
        PACK        KEY6 WITH FORM4,SP2
        CALL        RDSDRGA1
        CALL        RDKDRGA1
        BRANCH      OVRCD,NOLSYR
.
        MATCH       DRGYR TO LSTYEAR
        GOTO        NOLSYR IF NOT EQUAL
.
        PACK        LFMTH WITH DRGYR,DRGNUM
        MOVE        DRGFDTE,LSTDT
        GOTO        CALDYTD
.
.....................................................................
.
NOLEYR  KEYIN       *P1:24,*EL,*V2LON,*B:
                    "** Cannot find this date for last year ** ":
                    *EOFF,ANS,*P1:24,*EL;
        GOTO        START
.
NOLSYR  KEYIN       *P1:24,*EL,*V2LON,*B:
                    "** Cannot find the start of last year in Period file ** ":
                    *EOFF,ANS,*P1:24,*EL;
        GOTO        START
.
NOLFYR  KEYIN       *P1:24,*EL,*V2LON,*B:
                    "** Cannot find the end of last year in Period file ** ":
                    *EOFF,ANS,*P1:24,*EL;
        GOTO        START
.
.       Calculate number of days YTD
.
CALDYTD MOVE        ZERO,PREDYTD        * Total no of days for last year
        MOVE        ZERO,CURDYTD        *         "            current year
.
        MOVE        LSTDT,DATEFR        * last year
        MOVE        LENDDT,DATETO
.
        CALL        CALDAY
        MOVE        DAYYTD,PREDYTD
.
        MOVE        CSTDT,DATEFR        * current year
        MOVE        CENDDT,DATETO
.
        CALL        CALDAY
        MOVE        DAYYTD,CURDYTD
.
.       Calculate the number of periods on report
.
        UNPACK      LTMTH,DRGYR,DRGNUM
        MOVE        DRGNUM,MTHNUM
.
        UNPACK      LFMTH,DRGYR,DRGNUM
        MOVE        DRGNUM,FORM2
        SUB         FORM2,MTHNUM
        ADD         ONE,MTHNUM
.
.       Calculate the number of periods in financial year
.
        UNPACK      EDLFIN,DRGYR,DRGNUM
        MOVE        DRGNUM,NUMPERS
        SUB         FORM2,NUMPERS
        ADD         ONE,NUMPERS
        GOTO        STPROC
.
.       Routine to calculate total number of days between two dates
.
CALDAY  UNPACK      DATETO,XCENT,XYEAR,XMON,XDAY
        MOVE        XCENT,CC
        MOVE        XYEAR,YY
        MOVE        XMON,MM
        MOVE        XDAY,DD
        CALL        DMYCON
        MOVE        JULDAY,DAYYTD
        MOVE        JULYR,JYEAR
.
        UNPACK      DATEFR,XCENT,XYEAR,XMON,XDAY
        MOVE        XCENT,CC
        MOVE        XYEAR,YY
        MOVE        XMON,MM
        MOVE        XDAY,DD
        CALL        DMYCON
.
        COMPARE     JULYR,JYEAR
        GOTO        CALD10 IF EQUAL
.
        ADD         "365",DAYYTD
        ADD         LEAPYR,DAYYTD
.
CALD10  SUB         JULDAY,DAYYTD
        RETURN
.
.       Loop over the summary file to collect the data we want
.
STPROC  MOVE        ZERO,TPREOBS
        MOVE        ZERO,TCUROBS
        MOVE        ZERO,TPREDSH
        MOVE        ZERO,TCURDSH
        MOVE        ZERO,TPRESEP
        MOVE        ZERO,TCURSEP
        MOVE        ZERO,TFADMN
        MOVE        ZERO,TFBDAY
        MOVE        ZERO,TFNONP
.
        CALL        KILLIDX
        CALL        CREAIDX                     * Creating tempfile
.
        OPEN        TEMPFILE,FNAMET
.
        DISPLAY     *P1:24,*EL,*P25:24,"Ward:",*W;
        MOVE        SP20,KEY15
        CALL        RDSMSTS1
NXTMST  CALL        RDKMSTS1
        BRANCH      OVRCD OF GENOUT
        MOVE        ZERO,PREFLG
.
.       Check if the month is in the range of last financial year
.
        MATCH       LFMTH,MSDATE
        GOTO        NXTMST IF LESS
.
.       Add up number of patients for each month
.
        MOVE        MSNPRAD,PATAD
        ADD         MSNPBAD,PATAD
        ADD         MSBPRAD,PATAD
        ADD         MSBPBAD,PATAD
.
.       Add up number of bed days for each month
.
        MOVE        MSNBDAY,PATBD
        ADD         MSBBDAY,PATBD
.
        MATCH       MSDATE,LTMTH
        GOTO        CHKPRE IF LESS
.
        MOVE        ONE,PREFLG            * Previous financial year
.
.       Accumulate the admissions and beddays for last year in the period
.
        ADD         PATAD,TFADMN
        ADD         PATBD,TFBDAY
        GOTO        CONTPRO
.
.       Check with the end of last financial year
.
CHKPRE  MATCH       MSDATE,EDLFIN
        GOTO        CHKCUR IF LESS
.
.       Accumulate the admissions and beddays for last year NOT in the period
.
        ADD         PATAD,TFADMN
        ADD         PATBD,TFBDAY
        GOTO        NXTMST
.
.       Check if the month is in the range of current financial year
.
CHKCUR  MATCH       CFMTH,MSDATE
        GOTO        NXTMST IF LESS
.
        MATCH       MSDATE,CTMTH
        GOTO        NXTMST IF LESS
.
CONTPRO DISPLAY     *P35:24,*V2LON,XMON,*HOFF,"/",*V2LON,XCENT,XYEAR:
                    *P45:24,MSWARD;
.
.       Check if we are printing zero stat wards
.
        IF           PTCNH82W = 0
          CALL         ZERO0000                  * check for zero stats
          COMPARE      ZERO,FORM10               * zero stat ward ?
          GOTO         NXTMST IF EQUAL           * yes - ignore
        ENDIF
.
        CALL         CHKREC             * check if record exists
        BRANCH       PREFLG OF CONT20
.
.       Update the current financial figures
.
        ADD          PATAD,CADYTD
        ADD          PATBD,CBDYTD
        ADD          MSACBDAY,CABDAY
        ADD          MSOBSTH,TCUROBS       * Total of obstertric theatre
        ADD          MSDSCH,TCURDSH        * Total no of discharges
        ADD          MSSEPBD,TCURSEP       * Total separation bed days
        GOTO         CONT30
.
.       Update the previous financial year
.
CONT20  ADD          PATAD,PADYTD
        ADD          PATBD,PBDYTD
        ADD          MSACBDAY,PABDAY
        ADD          MSOBSTH,TPREOBS       * Total of obstertric theatre
        ADD          MSDSCH,TPREDSH        * Total no of discharges
        ADD          MSSEPBD,TPRESEP       * Total separation bed days
.
CONT30  CALL         UPTEMP1
        GOTO         NXTMST
.
+
.       Generate outpatients
.
GENOUT  MOVE         SP3,MSWARD
        MOVE         SP3,MSDCLS
        MOVE         ZERO,TFNONP
        MOVE         ZERO,FIRST
.
        DISPLAY     *P1:24,*EL,*P25:24,"** Generating Outpatients ** ",*W:
                     *P25:24,*EL,"Ward Class :";
.
.       Loop through the site file
.
        MOVE         SP10,KEY6
        CALL         RDSSITA1
NXSITA  CALL         RDKSITA1
        BRANCH       OVRCD OF GENREP
.
        CLOSE        OUTSTBA1
        PACK         CFNAME WITH OSTFILE,FILSTBA1
        OPEN         OUTSTBA1,CFNAME
.
        PACK         KEY5 WITH CODECG,SP3
        CALL         RDCODE1
NXCODE  CALL         RDKCODE1
        BRANCH       OVRCD OF NXSITA
.
        MATCH        CODECG,TCODE
        GOTO         NXSITA IF NOT EQUAL
.
        MOVE         ACODE,MSWCLS
.
        DISPLAY      *P40:24,*V2LON,MSWCLS;
.
.    For each code -Accumulate the number of oupatients for last financial year
.       only upto the keyin month
.
        MOVE         LFMTH,SMONTH
        MOVE         LTMTH,ENDMTH
        MOVE         ONE,PREFLG
        CALL         CALOUT      
.
.       Accumulate the number of outpatients for current year
.
        MOVE         CFMTH,SMONTH
        MOVE         CTMTH,ENDMTH
        MOVE         ZERO,PREFLG
        CALL         CALOUT
.
.       Accumulate the number of outpatients for the whole last year
.
        MOVE         ONE,FIRST
        MOVE         LFMTH,SMONTH
        MOVE         EDLFIN,ENDMTH
        CALL         CALOUT
        MOVE         ZERO,FIRST
.
        GOTO         NXCODE
.
+
.       Calculate number of outpatients/accident & emergency
.
CALOUT  MOVE         ZERO,TOTPAT
        PACK         KEY11 WITH CODECG,MSWCLS,SMONTH
        CALL         RDSTAA1
        BRANCH       OVRCD OF CALO10
        MOVE         AESNUMB,TOTPAT
.
CALO10  PACK         KEY17 WITH OSTSITE,CODECG,MSWCLS,SMONTH
        CALL         RDSTBA1
        BRANCH       OVRCD OF CALO20
        ADD          OSBNUMB,TOTPAT
.
CALO20  BRANCH       FIRST OF CALO25
        COMPARE      ZERO,TOTPAT
        GOTO         CALO30 IF EQUAL
.
        CALL         CHKTMP
        GOTO         CALO30
.
.       Accumulate number of patients for the whole last year
.
CALO25  ADD          TOTPAT,TFNONP
.
.       Check the next month
.
CALO30  MOVE         SMONTH,KEY6
        CALL         RDDRGA1
        CALL         RDKDRGA1
        BRANCH       OVRCD,CALO99
.
        PACK         SMONTH,DRGYR,DRGNUM
.
        MATCH        SMONTH,ENDMTH              * Check with keyin month
        GOTO         CALOUT IF NOT LESS
CALO99  RETURN
.
+
.       Check with tempfile
.
CHKTMP  MOVE         SP3,MSWARD
        CALL         CHKREC
.
        BRANCH       PREFLG OF CHKT20
        ADD          TOTPAT,CADYTD            * accumulate the current year
        GOTO         CHKT30
.
CHKT20  ADD          TOTPAT,PADYTD            * accumulate the previous year
.
CHKT30  CALL         UPTEMP1
        RETURN
.
+
.       Routine if record exist in tempfile
.
CHKREC  PACK        KEY9 WITH MSDCLS,MSWCLS,MSWARD
        CALL        RDTEMP1
        COMPARE     ZERO,OVRCD
        RETURN      IF EQUAL
.
        CALL        INTTMP
        CALL        WRTEMP1                    * create a new blank record
        CALL        RDTEMP1
.
.       A new ward, get the number of beds in the ward
.
        MOVE        ZERO,INBNUM
        PACK        KEY6 WITH MSWARD,SP3
        CALL        RDWARD1
        COMPARE     ONE,OVRCD
        RETURN      IF EQUAL
.
        MOVE        ONE,ACTVWFLG                 * don't calculate weekend days
        PROC        WDACTVDY                     * get days ward was active
        COMPARE     ZERO,ACTVDAYS                * ward active during period ?
        IF          !@EQUAL
          MOVE        TOTBED,INBNUM              * yes update bed total
          MOVE        ACTVBCHG,CHNGFLG           * set change flag
        ENDIF
        RETURN
+
.       Initialize the temp variables
.
INTTMP  MOVE         ZERO,INBNUM
        MOVE         ZERO,PADYTD
        MOVE         ZERO,PBDYTD
        MOVE         ZERO,CADYTD
        MOVE         ZERO,CBDYTD
        MOVE         ZERO,CABDAY
        MOVE         ZERO,PABDAY
        RETURN
.
+
GENREP  DISPLAY     *P1:24,*EL,*P10:24,"** Generating Report **",*W;
.
        CLOSE       TEMPFILE
        TRAP        NOOPEN IF IO
        OPEN        TEMPFILE,FNAMET
        TRAPCLR     IO
.
        DISPLAY     *P1:12,*EF
        BRANCH      OPCND OF ENDP
.
        MOVE        "60",LNO
        CLOCK       TIME,TIMEIS
.
        MOVE        ZERO,FIRST
        MOVE        SP3,SAVCLS
        MOVE        ZERO,SAVOPRE
        MOVE        ZERO,SAVOCUR
        MOVE        ZERO,OUTFLG
        CALL        INTVAR
.
.       Non Inpatient details has a blank ward
.
        MOVE        ONE,ASKFLAG                  * set asterisk not printed
        DISPLAY     *P1:24,*EL,*P25:24,"Ward :";
        MOVE        SP10,KEY9
        CALL        RDSTEMP1
READLOP CALL        RDKTEMP1
        BRANCH      OVRCD OF PRGRT
.
        MATCH       SP3,MSWARD
        GOTO        SAVNINP IF EQUAL        * Non Inpatient details
.
        DISPLAY     *P37:24,*V2LON,MSWARD;
        COMPARE     "55",LNO
        CALL        HEAD IF NOT LESS
.
        PRINT       *1,"|";
        ADD         ONE,LNO
.
.       If this is the first record of inpatient, don't bother to check for
.       ward class with the SAVCLS, because ward class can be blank
.       and the SAVCLS is always blank at the start
.
        COMPARE     ZERO,FIRST             
        GOTO        READ10 IF EQUAL
.
        MATCH       SAVCLS,MSWCLS
        GOTO        READ30 IF EQUAL
.
READ10  MOVE        MSWCLS,SAVCLS                * a new ward class
.
        MOVE        "Unknown             ",WCLDES
        MATCH       SP3,MSWCLS
        GOTO        READ20 IF EQUAL
.
        PACK        KEY5 WITH CODECG,MSWCLS
        CALL        RDCODE1
        BRANCH      OVRCD OF READ20
.
        MOVE        TDESC,WCLDES
READ20  PRINT       *2,WCLDES;
.
.       Calculate the occupancy
.
READ30  MOVE        ZERO,ADPCEN
        COMPARE     ZERO,PADYTD
        GOTO        READ32 IF EQUAL
.
        MOVE        CADYTD,ADPCEN
        SUB         PADYTD,ADPCEN
        MULT        "100",ADPCEN
        DIV         PADYTD,ADPCEN      * admissions
.
READ32  MOVE        PREDYTD,FORM8
        MULT        INBNUM,FORM8
.
        MOVE        ZERO,OCPRE
        COMPARE     ZERO,FORM8
        GOTO        READ34 IF EQUAL
.
        COMPARE     ZERO,PABDAY
        GOTO        READ34 IF EQUAL
.
        ASSIGN      ((PBDYTD*100)/PABDAY),OCPRE
.
READ34  MOVE        CURDYTD,FORM8
        MULT        INBNUM,FORM8
.
        MOVE        ZERO,OCCUR
        COMPARE     ZERO,FORM8
        GOTO        READ36 IF EQUAL
.
        COMPARE     ZERO,CABDAY
        GOTO        READ36 IF EQUAL
.
        ASSIGN      ((CBDYTD*100)/CABDAY),OCCUR
.
READ36  MOVE        ZERO,BDPCEN
        COMPARE     ZERO,OCPRE
        GOTO        READ38 IF EQUAL
.
        MOVE        OCCUR,BDPCEN
        SUB         OCPRE,BDPCEN
        MULT        "100",BDPCEN
        DIV         OCPRE,BDPCEN      * occupancy
.
READ38  PACK        KEY6,MSWARD,SP6
        CALL        RDWARD1
        IF          CHNGFLG = 1
          MOVE        ZERO,ASKFLAG
          PRINT       *31,"*";
        ENDIF
        PRINT       *22,MSWARD,*28,INBNUM:
                    *34,"| ",PADYTD,*47,CADYTD,*57,ADPCEN:
                    *67,"| ",OCPRE,*80,OCCUR,*90,BDPCEN,*100,"|";
.
        ADD         PADYTD,GTADPR
        ADD         CADYTD,GTADCR
        ADD         PBDYTD,GTBDPR
        ADD         CBDYTD,GTBDCR
        ADD         CABDAY,GTCABDY
        ADD         PABDAY,GTPABDY
        ADD         INBNUM,TOTLBEDS
.
.       Check if there is any Non Inpatient details
.
        COMPARE     ZERO,OUTFLG
        GOTO        READ40 IF EQUAL
.
.       Print the Non Inpatient details on the first line of every ward class
.
        MOVE        ZERO,NOPCEN
        COMPARE     ZERO,SAVOPRE
        GOTO        READ39 IF EQUAL
.
        MOVE        SAVOCUR,NOPCEN
        SUB         SAVOPRE,NOPCEN
        MULT        "100",NOPCEN
        DIV         SAVOPRE,NOPCEN
.
READ39  PRINT       *101,SAVOPRE,*112,SAVOCUR,*122,NOPCEN;
.
        ADD         SAVOPRE,GTOPRE      * for the grand totals
        ADD         SAVOCUR,GTOCUR
.
        MOVE        ZERO,SAVOPRE
        MOVE        ZERO,SAVOCUR
        MOVE        ZERO,NOPCEN
        MOVE        ZERO,OUTFLG
.
READ40  PRINT       *132,"|"
        MOVE        ONE,FIRST
        GOTO        READLOP
.
.
.       Check if previously any Non Inpatient details
.
SAVNINP COMPARE     ZERO,OUTFLG
        GOTO        SAVN10 IF EQUAL
.
        MOVE        "Unknown             ",WCLDES
        MATCH       SP3,MSWCLS
        GOTO        SAVN05 IF EQUAL

        PACK        KEY5 WITH CODECG,SAVCLS
        CALL        RDCODE1
        BRANCH      OVRCD OF SAVN05
.
        MOVE        TDESC,WCLDES
.
.       Just print the Non Inpatient without Inpatient details
.
SAVN05  MOVE        ZERO,NOPCEN
        COMPARE     ZERO,SAVOPRE
        GOTO        SAVN06 IF EQUAL
.
        MOVE        SAVOCUR,NOPCEN
        SUB         SAVOPRE,NOPCEN
        MULT        "100",NOPCEN
        DIV         SAVOPRE,NOPCEN
.
SAVN06  PRINT       *1,"|",WCLDES,*22,"|",*28,"|",*34,"|",*67,"|":
                    *100,"| ",SAVOPRE,*112,SAVOCUR,*122,NOPCEN,*132,"|"
        ADD         ONE,LNO
.
        ADD         SAVOPRE,GTOPRE      * for the grand totals
        ADD         SAVOCUR,GTOCUR
.
        MOVE        ZERO,SAVOPRE
        MOVE        ZERO,SAVOCUR
        MOVE        ZERO,NOPCEN
        MOVE        ZERO,OUTFLG
.
.       Save the outpatient details
.
SAVN10  MOVE        PADYTD,SAVOPRE
        MOVE        CADYTD,SAVOCUR
        MOVE        ONE,OUTFLG
        GOTO        READLOP
.
.       Initialize the grand total variables
.
INTVAR  MOVE        ZERO,GTADPR
        MOVE        ZERO,GTADCR
        MOVE        ZERO,GTBDPR
        MOVE        ZERO,GTBDCR
        MOVE        ZERO,GTCABDY
        MOVE        ZERO,GTPABDY
        MOVE        ZERO,TOTLBEDS
        MOVE        ZERO,GTOPRE
        MOVE        ZERO,GTOCUR
        MOVE        ZERO,GTOPRE
        MOVE        ZERO,GTOCUR
        RETURN
.
+
.       Print the Grand Total
.
PRGRT   COMPARE     ZERO,PAGENO
        GOTO        NODATA IF EQUAL
.
.       Calculate the occupancy of the totals
.
        MOVE        PREDYTD,FORM8
        MULT        TOTLBEDS,FORM8
.
        MOVE        ZERO,OCPRE
        COMPARE     ZERO,FORM8
        GOTO        PRG10 IF EQUAL
.
        COMPARE     ZERO,GTPABDY
        GOTO        PRG10 IF EQUAL
.
        ASSIGN      ((GTBDPR*100)/GTPABDY),OCPRE
.
PRG10   MOVE        CURDYTD,FORM8
        MULT        TOTLBEDS,FORM8
.
        MOVE        ZERO,OCCUR
        COMPARE     ZERO,FORM8
        GOTO        PRG20 IF EQUAL
.
        ASSIGN      ((GTBDCR*100)/GTCABDY),OCCUR
.
PRG20   MOVE        ZERO,BDPCEN
        COMPARE     ZERO,OCPRE
        GOTO        PRG30 IF EQUAL
.
        MOVE        OCCUR,BDPCEN
        SUB         OCPRE,BDPCEN
        MULT        "100",BDPCEN
        DIV         OCPRE,BDPCEN       * occupancy
.
PRG30   MOVE        ZERO,ADPCEN
        COMPARE     ZERO,GTADPR
        GOTO        PRG40 IF EQUAL
.
        MOVE        GTADCR,ADPCEN
        SUB         GTADPR,ADPCEN
        MULT        "100",ADPCEN
        DIV         GTADPR,ADPCEN        * admissions
.
PRG40   MOVE        ZERO,NOPCEN
        COMPARE     ZERO,GTOPRE
        GOTO        PRG50 IF EQUAL
.
        MOVE        GTOCUR,NOPCEN
        SUB         GTOPRE,NOPCEN
        MULT        "100",NOPCEN
        DIV         GTOPRE,NOPCEN        * Non Inpatient
.
PRG50   CALL        PRTLINE
        PRINT       *1,"|   Totals (Includes Boarders)":
                    *34,"| ",GTADPR,*47,GTADCR,*57,ADPCEN:
                    *67,"| ",OCPRE,*80,OCCUR,*90,BDPCEN:
                    *100,"| ",GTOPRE,*112,GTOCUR,*122,NOPCEN,*132,"|"
        ADD         ONE,LNO
        IF          LNO < 53
          CALL        PRTLINE
        ENDIF
.
        COMPARE     "54",LNO
        CALL        HEAD IF NOT LESS
.
        UNPACK      LTMTH WITH DRGYR,DRGNUM
        PRINT       *1,"|",*34,"|",*56,"Fiscal ",DRGYR:
                    *67,"|",*89,"Fiscal ",DRGYR:
                    *100,"|",*121,"Fiscal ",DRGYR,*132,"|"
        ADD         ONE,LNO
.
.       Calculate monthly averages
.
        DIV         MTHNUM,GTADPR       * last year admissions
        DIV         MTHNUM,GTOPRE       * last year non inpatients
.
        MOVE        GTBDPR,LASTAV       * last year occupancy
        DIV         MTHNUM,LASTAV
.
        DIV         MTHNUM,GTADCR       * current year admissions
        DIV         MTHNUM,GTOCUR       * current year non inpatients
.
        MOVE        GTBDCR,CURTAV       * current year occupancy
        DIV         MTHNUM,CURTAV
.
.
.       Calculate fiscal of last year
.
        DIV         NUMPERS,TFADMN
        DIV         NUMPERS,TFBDAY
        DIV         NUMPERS,TFNONP
.
        PRINT       *1,"|    ",CPERMTH," Averages":
                    *34,"| ",GTADPR,*47,GTADCR,*57,TFADMN:
                    *67,"| ",LASTAV,*80,CURTAV,*90,TFBDAY:
                    *100,"| ",GTOPRE,*112,GTOCUR,*122,TFNONP,*132,"|"
        ADD         ONE,LNO
        IF          LNO < 52
          CALL        PRTLINE
        ENDIF
.
.
.       Get the number of theatre visits from outpatient files
.
        MOVE        ZERO,TTHPRE
        MOVE        ZERO,TTHCUR
        MOVE        SP10,KEY6
        CALL        RDSWARD1
NXWRD   CALL        RDKWARD1
        BRANCH      OVRCD OF GETBRN
.
        MATCH       SP3,WBED
        GOTO        NXWRD IF NOT EQUAL
.
.       Accumulate the number of theatre visits for last year
.
        MOVE        LFMTH,STMTH
        MOVE        LTMTH,ENDMTH
.
        CALL        GTHEA
        ADD         TOTPAT,TTHPRE
.
.       Accumulate number of theatre visits for current year
.
        MOVE        CFMTH,STMTH
        MOVE        CTMTH,ENDMTH
.
        CALL        GTHEA
        ADD         TOTPAT,TTHCUR
        GOTO        NXWRD
.
.       Routine to get number of theatre visit for each ward
.
GTHEA   MOVE        STMTH,SMONTH
        MOVE        ZERO,TOTPAT
GTH10   PACK        KEY9 WITH SMONTH,WWARD
        CALL        RDOPWAR1
        BRANCH      OVRCD OF GTH20
        ADD         OPWANCAS,TOTPAT
.
GTH20   MOVE        SMONTH,KEY6
        CALL        RDDRGA1
        CALL        RDKDRGA1
        BRANCH      OVRCD,GTH99
.
        PACK        SMONTH,DRGYR,DRGNUM
.
        MATCH       SMONTH,ENDMTH              * Check with keyin month
        GOTO        GTH10 IF NOT LESS
GTH99   RETURN
.
+
.       Get number of births
.
GETBRN  MOVE        ZERO,TBRPRE
        MOVE        ZERO,TBRCUR
.
        MOVE        LFMTH,STMTH
        MOVE        LTMTH,ENDMTH
        CALL        GETBTH
        MOVE        TOTPAT,TBRPRE         * number of newborns for last year
.
        MOVE        CFMTH,STMTH
        MOVE        CTMTH,ENDMTH
        CALL        GETBTH
        MOVE        TOTPAT,TBRCUR        * number of newborns for current year
        GOTO        PRTHEA
.
.       Routine to get number of births
.
GETBTH  MOVE        STMTH,SMONTH
        MOVE        ZERO,TOTPAT
.
GETB10  PACK        KEY6 WITH SMONTH
        CALL        RDBRNS1
        BRANCH      OVRCD OF GETB20
        ADD         BRADMN,TOTPAT
.
GETB20  MOVE        SMONTH,KEY6
        CALL        RDDRGA1
        CALL        RDKDRGA1
        BRANCH      OVRCD,GETB99
.
        PACK        SMONTH,DRGYR,DRGNUM
.
        MATCH       SMONTH,ENDMTH              * Check with keyin month
        GOTO        GETB10 IF NOT LESS
GETB99  RETURN
.
.
.       Print other service
.
PRTHEA   COMPARE    "53",LNO
         CALL       HEAD IF NOT LESS
.
         PRINT      *1,"|",*34,"| ",*67,"|",*100,"|",*132,"|":
                    *N,*1,"|   Other Service Indicators":
                    *34,"| Operations",*67,"| Average Bed Stay (Days)":
                    *100,"| Births",*132,"|"
         ADD        ONE,LNO
.
         UNPACK     LSTDT WITH XCENT,XYEAR,XMON,XDAY
         UNPACK     EDLFDT WITH CCENT,CYEAR,CMON,CDAY
.
         MATCH      XYEAR,CYEAR
         GOTO       PRTH10 IF EQUAL
.
         PACK       LASTYR,XYEAR,SLASH,CYEAR
.
         UNPACK     CSTDT WITH XCENT,XYEAR,XMON,XDAY
         MOVE       XYEAR,IYEAR
         ADD        ONE,IYEAR
         PACK       THISYR,XYEAR,SLASH,IYEAR
         REP        " 0",THISYR
.
         PRINT      *1,"|",*34,"|",*39,LASTYR:
                    *50,THISYR,*59,"   %":
                    *67,"|",*73,LASTYR:
                    *85,THISYR,*92,"   %":
                    *100,"|",*104,LASTYR:
                    *115,THISYR,*124,"   %",*132,"|"
         ADD        ONE,LNO
         GOTO       PRTH20
.
PRTH10   PRINT      *1,"|",*34,"|",*39,LSTYEAR,*50,FINYEAR,*59,"   %":
                    *67,"|",*73,LSTYEAR,*85,FINYEAR,*92,"   %":
                    *100,"|",*104,LSTYEAR,*115,FINYEAR:
                    *124,"   %",*132,"|"
         ADD        ONE,LNO
.
PRTH20   PRINT      *1,"|",*34,"|    Y.T.D",*47,"   Y.T.D",*59,"+ or (-)":
                    *67,"|     Y.T.D",*80,"     Y.T.D",*92,"+ or (-)":
                    *100,"|   Y.T.D",*112,"   Y.T.D",*124,"+ or (-)",*132,"|"
         ADD        ONE,LNO
         IF         LNO < 52
           CALL       PRTLIN1
         ENDIF
.
         MOVE       ZERO,ADPCEN
         COMPARE    ZERO,TTHPRE
         GOTO       PRTH30 IF EQUAL
.
         MOVE       TTHCUR,ADPCEN
         SUB        TTHPRE,ADPCEN
         MULT       "100",ADPCEN
         DIV        TTHPRE,ADPCEN          * percentage of Main theatre
.
PRTH30   MOVE       ZERO,BDPCEN
         COMPARE    ZERO,TPREOBS
         GOTO       PRTH40 IF EQUAL
.
         MOVE       TCUROBS,BDPCEN
         SUB        TPREOBS,BDPCEN
         MULT       "100",BDPCEN
         DIV        TPREOBS,BDPCEN         * percentage of obstetrics theatre
.
PRTH40   COMPARE    "53",LNO                     * page full ?
         CALL       HEAD IF NOT LESS             * yes
.
         PRINT      *1,"|",*5,"Main Theatres":
                    *34,"| ",TTHPRE,*47,TTHCUR,*57,ADPCEN:
                    *67,"|",*100,"|",*132,"|":
                    *N,*1,"|",*5,"Obstetrics Theatres":
                    *34,"| ",TPREOBS,*47,TCUROBS,*57,BDPCEN;
         ADD        TWO,LNO
.
.        Calculate totals operations
.
         MOVE       TTHPRE,GTADPR
         ADD        TPREOBS,GTADPR        * Last year
.
         MOVE       TTHCUR,GTADCR
         ADD        TCUROBS,GTADCR        * Current year
.
         MOVE       ZERO,NOPCEN
         COMPARE    ZERO,GTADPR
         GOTO       PRTS10 IF EQUAL
.
         MOVE       GTADCR,NOPCEN
         SUB        GTADPR,NOPCEN
         MULT       "100",NOPCEN
         DIV        GTADPR,NOPCEN
.
.        Calculate previous year Average length of stay
.
PRTS10   MOVE       ZERO,PRESTY
         COMPARE    ZERO,TPREDSH
         GOTO       PRTS20 IF EQUAL
.
.        Check if we are using separation bed days or occupied bed days to
.        calculate LOS
.
         BRANCH     PTOCBDAY,PRTS14
         MOVE       GTBDPR,PRESTY           * Use total occupied beddays
         DIV        TPREDSH,PRESTY
         GOTO       PRTS20
.
PRTS14   MOVE       TPRESEP,PRESTY          * Use total separation beddays
         DIV        TPREDSH,PRESTY
.
.        Calculate current year Average length of stay
.
PRTS20   MOVE       ZERO,CURSTY
         COMPARE    ZERO,TCURDSH
         GOTO       PRTS30 IF EQUAL
.
         BRANCH     PTOCBDAY,PRTS24
         MOVE       GTBDCR,CURSTY          * Use total occupied beddays
         DIV        TCURDSH,CURSTY         * to calculate LOS
         GOTO       PRTS30
.
PRTS24   MOVE       TCURSEP,CURSTY         * Use total separation beddays
         DIV        TCURDSH,CURSTY         * to calculate LOS
.
PRTS30   MOVE       ZERO,ADPCEN
         COMPARE    ZERO,PRESTY
         GOTO       PRTS40 IF EQUAL
.
         MOVE       CURSTY,ADPCEN
         SUB        PRESTY,ADPCEN
         MULT       "100",ADPCEN
         DIV        PRESTY,ADPCEN
.
.        Percentage of Births
.
PRTS40   MOVE       ZERO,BDPCEN
         COMPARE    ZERO,TBRPRE
         GOTO       PRTS50 IF EQUAL
.
         MOVE       TBRCUR,BDPCEN
         SUB        TBRPRE,BDPCEN
         MULT       "100",BDPCEN
         DIV        TBRPRE,BDPCEN
.
PRTS50   PRINT      *67,"|",PRESTY,*80,CURSTY,*90,ADPCEN:
                    *100,"|",TBRPRE,*112,TBRCUR,*122,BDPCEN,*132,"|":
                    *N,*1,"|",*5,"TOTAL Operations":
                    *34,"| ",GTADPR,*47,GTADCR,*57,NOPCEN:
                    *67,"|",*100,"|",*132,"|"
         ADD        ONE,LNO
         IF         LNO < 44
           CALL       PRTLINE 
         ENDIF
.
.
.           OVER CONDITION FOUND..PRINT LAST LINE OF -- AND DISPLAY MESSAGE
.
ENDP    COMPARE     ZERO,PAGENO
        GOTO        NODATA IF EQUAL
.
        COMPARE     "45",LNO
        CALL        HEAD IF NOT LESS
.
        PRINT       *N,*N,*1,"The Occupancy =      Occupied Bed Days",*53,"100":
                    *75,"The % + or - column = (YTD Current - YTD Last Year)":
                    *N,*17,"------------------------------ * ----------":
                    *97,"-----------------------------":
                    "  * 100":
                    *N,*17,"Number of Days Ward was Active   No of Beds":
                    *102,"YTD  Last Year":
               *N,*N,*1,"The Fiscal Year = Total for Last Year";
.
       BRANCH  PTOCBDAY,END10
       PRINT   *75,"The Average Bed Stay = Number of Occupied Bed Days YTD";
       GOTO    END20
.
END10  PRINT   *75,"The Average Bed Stay = Number of Separation Bed Days YTD";
.
END20  PRINT   *N,*19,"-------------------":
               *98,"-------------------------------":
               *N,*27,NUMPERS:
               *98,"Number of Discharges only YTD"
.
       IF      ASKFLAG = 0
         PRINT     *N,"* - Some wards changed number of `Beds' during period."
       ENDIF
.
        PRINT   *N,"*** End of Report ***"
.
        DISPLAY     *P1:24,*EL,*P40:24,*V2LON:
                    "** Report Generation Completed **",*W2;
.
         CALL       KILLIDX
         GOTO       START
.
NODATA  DISPLAY     *P20:24,*EL,*B,*V2LON:
                    "** No Available Data on File **",*W2;
        CALL        KILLIDX
        GOTO        START
.
NOOPEN  MOVE        ONE,OPCND
        DISPLAY     *P1:24,*B,*EL,"** TEMPORARY FILE ":
                                 FNAMET," NOT FOUND **",*W2;
        RETURN
.
+
.
.          LAST LINE ON THE CODE
.
PRTLINE   PRINT     *1,"*-----------------------------------------":
                       "-------------------------------------------":
                       "----------------------------------------------*"
          ADD       ONE,LNO
          RETURN
.
PRTLIN1   PRINT     *1,"|-----------------------------------------":
                       "-------------------------------------------":
                       "----------------------------------------------|"
          ADD       ONE,LNO
          RETURN
+
.
.
.                 HEADINGS FOR OUTPUT
.
HEAD      ADD       ONE,PAGENO
.
          COMPARE   ONE,PAGENO
          CALL      PRTLINE IF NOT EQUAL
.
          CALL      HEAD132
          UNPACK    CSTDT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *41,"Period ",DRGMDSC,"  ",CPCDATE;
.
          UNPACK    CENDDT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *70,"to ",CPCDATE,*N
.
          CALL      PRTLINE
          PRINT     *1,"|Inpatients",*34,"|",*67,"|":
                    *100,"|Non-Inpatient",*132,"|":
                    *N,*1,"|----------":
                    *34,"| Admissions",*67,"| Occupancy":
                    *100,"|-------------",*132,"|"
.
         UNPACK     LSTDT WITH XCENT,XYEAR,XMON,XDAY
         UNPACK     EDLFDT WITH CCENT,CYEAR,CMON,CDAY
.
         MATCH      XYEAR,CYEAR
         GOTO       HEAD10 IF EQUAL
.
         PACK       LASTYR,XYEAR,SLASH,CYEAR
.
         UNPACK     CSTDT WITH XCENT,XYEAR,XMON,XDAY
         MOVE       XYEAR,IYEAR
         ADD        ONE,IYEAR
         PACK       THISYR,XYEAR,SLASH,IYEAR
         REP        " 0",THISYR
.
         PRINT      *1,"|",*34,"|",*39,LASTYR:
                    *50,THISYR,*58,"   %":
                    *67,"|",*74,LASTYR:
                    *85,THISYR,*91,"   %":
                    *100,"|",*104,LASTYR:
                    *115,THISYR,*123,"   %",*132,"|"
         GOTO       HEAD20
.
HEAD10   PRINT      *1,"|",*34,"|",*39,LSTYEAR,*50,FINYEAR,*58,"   %":
                    *67,"|",*74,LSTYEAR,*85,FINYEAR,*91,"   %":
                    *100,"|",*104,LSTYEAR,*115,FINYEAR:
                    *123,"   %",*132,"|"
.
HEAD20   PRINT      *1,"|Ward Class",*22,"Ward",*28,"Beds":
                    *34,"|    Y.T.D",*49," Y.T.D",*58," + or (-)":
                    *67,"|",*73," Y.T.D",*84," Y.T.D",*91," + or (-)":
                    *100,"|   Y.T.D",*114," Y.T.D",*123," + or (-)",*132,"|"
          CALL      PRTLINE
          MOVE      TEN1,LNO
          RETURN
.
.         Subroutine to key in a valid month and year
.
KEYMN     MOVE      TWENTY5,CCOL
          MOVE      FOUR,CVERT
          MOVE      ZERO,CHIGHLT
          UNPACK    SP4,CYEAR,CMON
          MOVE      CCC,CCENT
.
          CALL      KEYPER
          BRANCH    OVRCD,KEYMN999
.
          PACK      KEY6,ICENT,IYEAR,IMON
          REP       " 0",KEY6
          CALL      RDDRGA3
          BRANCH    OVRCD,KEYMN900
.
          GOTO      KEYMN999
.
KEYMN900  DISPLAY   *P50:4,*V2LON,*B,"** Invalid Date **",*W2,*P50:4,*EL;
          GOTO      KEYMN
.
KEYMN999  RETURN
+
.****************************************************************************
.*                           ZERO0000                                       *
.*               See if this record has zero stats                          *
.****************************************************************************
.
ZERO0000  MOVE      MSNPRAD,FORM10
          ADD       MSNPBAD,FORM10
          ADD       MSTRIN,FORM10
          ADD       MSTROUT,FORM10
          ADD       MSDSCH,FORM10
          ADD       MSNBDAY,FORM10
          ADD       MSBBDAY,FORM10
          ADD       MSDEATH,FORM10
          ADD       MSBPRAD,FORM10
          ADD       MSBPBAD,FORM10
.
ZERO9999  RETURN
.
.       Deleting  AN INDEXED FILE BASED ON PORT
.
KILLIDX CLEAR         CMDLINE
        APPEND        "iserase ",CMDLINE
        APPEND        FNAMET,CMDLINE
        RESET         CMDLINE
        EXECUTE       CMDLINE,TASKID
        DISPLAY       *P1:8,*EF,*P1:8
.
        RETURN
.
CREAIDX PREP          PSTROL,FNAMER
        WRITE         PSTROL,SEQ;"isbuild ",FNAMET," 45 u1-9"
        WEOF          PSTROL,SEQ
.
        MOVE          ONE,STATUS
        DISPLAY       *P1:13,*EF,*P1:14
.
        CLEAR         CMDLINE
        APPEND        "sh ",CMDLINE
        APPEND        FNAMER,CMDLINE
        APPEND        ".txt",CMDLINE
        RESET         CMDLINE
.
        EXECUTE      CMDLINE,TASKID
        BRANCH       STATUS TO INDOK
.
        MOVE         ONE,OPCND
        DISPLAY      *P20:24,*B,*V2LON:
                     "** The Index File Not Created **",*W3;
.
INDOK   DISPLAY       *P1:13,*EF,*P1:14
        CLOSE         PSTROL,DELETE
        RETURN  
.
.
.          I/O Routine for tempfile (used for MMHS outpatient)
.
RDTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY9
          READ      TEMPFILE,KEY9;MSDCLS,MSWCLS,MSWARD,INBNUM:
                                  PADYTD,CADYTD,PBDYTD,CBDYTD,CABDAY,PABDAY:
                                  CHNGFLG
          GOTO      OVERCOND IF OVER
          RETURN
.
RDSTEMP1  RESET     KEY9
          READ      TEMPFILE,KEY9;;
          RETURN
.
RDKTEMP1  MOVE      ZERO,OVRCD
          READKS    TEMPFILE;MSDCLS,MSWCLS,MSWARD,INBNUM:
                                  PADYTD,CADYTD,PBDYTD,CBDYTD,CABDAY,PABDAY:
                                  CHNGFLG
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTEMP1   UPDATE    TEMPFILE;MSDCLS,MSWCLS,MSWARD,INBNUM:
                                  PADYTD,CADYTD,PBDYTD,CBDYTD,CABDAY,PABDAY:
                                  CHNGFLG
          RETURN
.
WRTEMP1   RESET     KEY9
          MOVE      ZERO,OVRCD
          READ      TEMPFILE,KEY9;ANS
          GOTO      OVERCOND IF NOT OVER
.
          WRITE     TEMPFILE,KEY9;KEY9,INBNUM:
                                  PADYTD,CADYTD,PBDYTD,CBDYTD,CABDAY,PABDAY:
                                  CHNGFLG
          RETURN
.
          INC       STD001IO
          INC       DATECONV
          INC       CALCDAYS
          INC       PATCOMN3
          INC       WDACTVDY
          INC       PATCODIO/INC
          INC       PATDRGIO/INC
          INC       PATMSTIO/INC
          INC       PATWBHIO/INC
          INC       PATWR1IO/INC
          INC       PATBRNIO/INC
          INC       OUTSITIO/INC
          INC       OUTSTBIO/INC
          INC       AAESTAIO/INC
          INC       OPRWARIO/INC
.
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC
          INC       TFILENAM 
.
ENDIT    CHAIN      PGM
         STOP

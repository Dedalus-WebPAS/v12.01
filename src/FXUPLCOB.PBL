.***************************************************************************
.* System    :   Inpatients                                                *
.* Program   :   FXUPLCOD                                                  *
.* Desc      :   Upload pmi country of birth from a text file              *
.***************************************************************************
.* Date      :   26/07/2006                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   This program will read a text file (uploadcb.txt) that    *
.*               contains the U/R number and a new country of birth and    *
.*               update the pmi                                            *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATMA1FD/INC
.
.Layout of uploadcb text file 
.----------------------------
COBTEXT   FILE      ASCII, FIXED=12
.
.Name     Type    Length  Pos  Description
.----     ----    ------  ---  -----------
CTXTURNO  DIM       8     1    U/R Number
CTXTCONT  DIM       3     9    Country of Birth
. 
. End of Record           12
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
RECNUM    FORM      8
.
PRGID     INIT      "FXUPLCOD"
PRGNAM    INIT      "Upload New Country of Birth to PMI"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      OPEN0000               * open uploadcb.txt
          BRANCH    EXIT,ML0100
.
          CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPEN0000             Called by: ML2000    *
.*  The routine is used to open the upload text file uploadcb.txt           *
.****************************************************************************
.
OPEN0000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      COBTEXT,"uploadcb"
          TRAPCLR   IO
          BRANCH    OVRCD,OPEN9000
.
          MOVE      ZERO,RECNUM
.
          MOVE      ZERO,EXIT
          GOTO      OPEN9999
.
OPEN9000  DISPLAY   *P1:24,*EL,*B,"uploadcb.txt does not exist ";
          CALL      HITENTER
          MOVE      ONE,EXIT
          GOTO      OPEN9999
.
OPEN9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Country of Birth Upload"
.
          DISPLAY   *P1:9,*EL,"NOTE  - Upload file name uploadcb.txt"
          DISPLAY   *P1:11,*EL,"FILE LAYOUT - U/R(8 Characters Right Justify)"
          DISPLAY   *P1:12,*EL,"              COB(3 Characters Left Justify) "
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  UNPACK    SP70,CTXTURNO,CTXTCONT
.
          READ      COBTEXT,SEQ;CTXTURNO,CTXTCONT     * Get UR and COB 
          GOTO      PROC9000 IF OVER
.
          MATCH     SP70,CTXTURNO                     * Skip if no U/R
          GOTO      PROC0000 IF EQUAL
.
          PACK      CTXTCONT,CTXTCONT,SP70
.
          MATCH     SP70,CTXTCONT                     * Skip if not COB
          GOTO      PROC0000 IF EQUAL
.
          PACK      KEY8,CTXTURNO,SP70
          CALL      RDMAST1                           * Read PMI
          BRANCH    OVRCD,PROC0000
.
          MOVE      CTXTCONT,PCONT                    * Update COB
          CALL      UPMAST1
.
          ADD       ONE,RECNUM                        * update record counter
.
          IF        (RECNUM%1000) = 0
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          GOTO      PROC0000
.
PROC9000  DISPLAY   *P1:24,*EL,*B,"Finish processing. ":
                                  "Records Updated ",RECNUM," ";
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PATMA1IO/INC

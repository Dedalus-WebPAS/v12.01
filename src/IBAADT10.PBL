.******************************************************************************
.* System         : Patient Billing                                           *
.* Program        : IBAADT10.PBL                                              *
.* Name           : Health Fund HosClaims Account Extract                     *
.******************************************************************************
.* Date           : 07/10/2002                                                *
.* Author         : Tonii                                                     *
.* Function       : Extract Health Fund HosClaims Account details             *
.*                  User is given 2 options upon entering program:            *
.*                     1. Create Extract                                      *
.*                     2. Re-extract Batch                                    *
.*                  Option 1 will loop through patedsaf table and extract any *
.*                           records that haven't yet been sent (ptesflag=0)  *
.*                  Option 2 will allow user to re-extract a batch via batch  *
.*                           number. No records which have been selected for  *
.*                           resubmission in IBAADT57 will be re-extracted.   *
.*                                                                            *
.* Modifications  :                                                           *
.*        V12.00.01 14/05/2025  J.Tan          TSK 0955096                    *
.*                  Added alpanumeric visit number generation                 *
.******************************************************************************
.*        V11.01.01 17/02/2021  Tracey Nguyen  TSK 0888639                    *
.*                  Recompiled for PATMMBFD changes                           *
.******************************************************************************
.*        V11.00.01 10/03/2020  J.Tan          TSK 0838262                    *
.*                  Added Effective from and to date to MBS Item file         *
.*        V10.08.01 26/08/2016  Don Nguyen     TSK 0312570                    *
.*                  Recompiled for NCGSEGMT (GETDRG)                          *
.*                  Added PMSEDGFD/IO and GETEFDRG                            *
.*        V10.06.01 15/04/2015  Ania P         CAR 261630                     *
.*                  Removed use of temporary file name                        *
.*        V10.05.01 18/09/2014  Davin          CAR 304204                     *
.*                  Recompiled for GETDRG - get drg version based on disc dte *
.*        V10.03.01 09/01/2012  Mike Laming    CAR 242229                     *
.*                  Recompiled for NCGSEGMT - Test "Source of Referral" = "1" *
.*        V10.02.02 31/10/2011  Mike Laming    CAR 250267                     *
.*                  Recompiled for NCGSEGMT - PATICUFD fields to Form 5       *
.*        V10.02.01 25/03/2011  Mike Laming    CAR 240107                     *
.*                  Recompiled for NCGSEGMT - Mods PATECDaf/PATECOaf Keys     *
.*        V10.01.01 21/12/2010  Mike Laming    CAR 233046                     *
.*                  Recompiled for NCGSEGMT - Mods to Misc.Charges PATMCHFD   *
.*        V9.12.05  27/01/2010  Steve Armstrong     CAR 217787                *
.*                  Recompiled for changes to NCGSEGMT - EPD segment.         *
.*        V9.12.04  27/01/2010  Steve Armstrong     CAR 201308                *
.*                  Recompiled for changes to NCGSEGMT - SVB segment.         *
.*        V9.12.03  22/12/2009  Steve Armstrong     CAR 212980                *
.*                  Recompiled for changes to NCGSEGMT - SVB & MIG segments.  *
.*        V9.12.02  14/12/2009  Steve Armstrong     CAR 201308                *
.*                  Recompiled for changes to NCGSEGMT - SVB segment.         *
.*        V9.12.01  24/07/2009  Peter Vela          CAR 202276                *
.*                  Lengthened CMDLINE to 80                                  *
.******************************************************************************
.*        V9.10.01  18/06/2008  Mike Laming         CAR 164567                *
.*                  Recompiled for NCGSEGMT - Add Service Type "O"            *
.******************************************************************************
.*        V9.09.10  23/01/2008  Steve Armstrong     CAR 159875                *
.*                  Recompiled for changes to NCGSEGMT.                       *
.*        V9.09.09  26/11/2007  Steve Armstrong     CAR 155764                *
.*                  Mods to ED040009 to send as right justified, zero filled. *
.*        V9.09.08  22/11/2007  Peter Vela          CAR 132264                *
.*                  Recompiled for PATCHCFD                                   *
.*        V9.09.07  15/11/2007  Steve Armstrong     CAR 154963                *
.*                  Recompiled for changes to NCGCLMVR & HSCLMSRV             *
.*        V9.09.06  13/11/2007  Steve Armstrong     CAR 153566                *
.*                  Fixed update of the original source record for a          *
.*                  resubmission, to use index 3 to do a direct read on the   *
.*                  original source record instead of using index 1 (as it    *
.*                  was using status "2" when the status could be "2", "3" or *
.*                  "4").                                                     *
.*        V9.09.05  26/10/2007  Steve Armstrong  CAR 147461 & 147455          *
.*                  Recompiled for changes to NCGSEGMT                        *
.*        V9.09.04  22/10/2007  Steve Armstrong     CAR 152842                *
.*                  Mods to use PTESADMN instead of ADMINO to load KEY8 in    *
.*                  multi-campus scenario when retrieving the pmsvx1af record *
.*                  in WRIT0000.                                              *
.*        V9.09.03  18/102/007  Steve Armstrong     CAR 151231                *
.*                  Recompiled for changes to NCGSEGMT.                       *
.*        V9.09.02  27/09/2007  Steve Armstrong     CAR 151231                *
.*                  Recompiled for changes to NCGSEGMT.                       *
.*        V9.09.01  14/08/2007  Mike Laming         CAR 146742                *
.*                  Mods to delete "fully credited" Invoices from "patedsaf"  *
.*                  14/08/2007  Mike Laming         CAR 146686 & 146815       *
.*                  Re-compiled for NCGSEGMT -                                *
.******************************************************************************
.*        V9.08.05  23/04/2007  J.Tan               CAR 136149                *
.*                  Mods to use default claim code from system parameter      *
.*        V9.08.04  19/03/2007  Mike Laming         CAR 125462                *
.*                  Added Health Fund selection & MultiHosp EDI File Prefix   *
.*        V9.08.03  09/03/2007  Steve Armstrong     CAR 93540                 *
.*                  Added call to WRTFR000 (Transfer segment)                 *
.*        V9.08.02  12/12/2006  Steve Armstrong     CAR 127885                *
.*                  Recompiled for changes to NCGSEGMT                        *
.*        V9.08.01  08/12/2006  Steve Armstrong     CAR 123343                *
.*                  Recompiled for changes to NCGSEGMT                        *
.******************************************************************************
.*        V9.07.04  07/09/2006  Steve Armstrong     CAR 112793                *
.*                  Recompiled for changes to NCGSEGMT                        *
.*        V9.07.03  17/08/2006  Davin S             CAR 117071                *
.*                  Recompiled for changes to NCGSEGMT                        *
.*        V9.07.02  17/07/2006  Steve Armstrong     CAR 112793                *
.*                  Recompiled for changes to NCGSEGMT                        *
.*        V9.07.01  12/07/2006  Sandra Barcham      CAR 112415                *
.*                  Recompiled for changes to NCGSEGMT & NCGCLMVR             *
.*                  08/05/2006  Peter Vela          CAR 104252                *
.*                  Recompiled for PATCERFD and PATCLCFD                      *
.******************************************************************************
.*        V9.05.B07 22/02/2006  Mike Laming         CAR 95392                 *
.*                  Recompiled for NCGSEGMT - add Provider No.warning         *
.*        V9.05.B06 03/02/2006  J.Tan               CAR 93540                 *
.*                  Recompiled for NCGSEGMT - TFR segment                     *
.*        V9.05.B05 20/01/2006  Steve Armstrong     CAR 92453                 *
.*                  Recompiled for NCGSEGMT.                                  *
.*                  Added OPEN on PATEDSA2.                                   *
.*        V9.05.B04 09/01/2006  Steve Armstrong     CAR 89871                 *
.*                  Recompiled for NCGSEGMT                                   *
.*        V9.05.B03 09/01/2006  Steve Armstrong     CAR 88319                 *
.*                  Recompiled for NCGSEGMT                                   *
.*        V9.05.B02 28/12/2005  Steve Armstrong     CAR 89871                 *
.*                  Recompiled for NCGSEGMT                                   *
.******************************************************************************
.*        V9.04.28  28/11/2005  Mike Laming         CAR 86417                 *
.*                  Add OPENs & IO Inc's for PATCERFD, PATCLCFD and PMSHCPFD  *
.*                  Recompiled for NCGRSEGMT - Add Certificates               *
.*        V9.04.27  22/11/2005  Steve Armstrong     CAR 85944                 *
.*                  Recompiled for NCGSEGMT - ANB changes                     *
.*        V9.04.26  02/11/2005  Mike Laming         CAR 82516                 *
.*                  Recompiled for NCGSEGMT - add test for Patient Portion Inv*
.*        V9.04.25  21/09/2005  Mike Laming         CAR 65745                 *
.*                  1) Change to delete "patedsaf" record if PINVTYP=2        *
.*                  (Patient portion of Split Inv.) in VALD0000               *
.*                  2) re-compile for NCGSEGMT                                *
.*        V9.04.24  31/08/2005  Steve Armstrong     CAR 74220                 *
.*                  Recompiled for changes to NCGSEGMT.                       *
.*        V9.04.23  30/08/2005  Steve Armstrong     CAR 74043                 *
.*                  Recompiled for changes to NCGSEGMT.                       *
.*        V9.04.22  11/08/2005  Steve Armstrong     CAR 71776                 *
.*                  Recompiled for changes to NCGSEGMT.                       *
.*        V9.04.21  05/08/2005  Steve Armstrong     CAR 70776                 *
.*                  Recompiled for changes to NCGSEGMT.                       *
.*        V9.04.20  21/07/2005  Steve Armstrong     CAR 65071                 *
.*                  Recompiled for changes to NCGSEGMT.                       *
.*                  18/07/2005  Steve Armstrong     CAR 67928                 *
.*                  Recompiled for changes to NCGSEGMT.                       *
.*        V9.04.19  06/07/2005  Steve Armstrong     CAR 66286 & 66447         *
.*                  Recompiled for changes to NCGSEGMT.                       *
.*        V9.04.18  20/06/2005  Steve Armstrong     CAR 62812                 *
.*                  Recompiled for changes to NCGSEGMT.                       *
.*        V9.04.17  10/06/2005  Steve Armstrong     CAR 62812                 *
.*                  Recompiled for changes to NCGSEGMT  & NCGCLMVR.           *
.*        V9.04.16  19/05/2005  Steve Armstrong     CAR 62085                 *
.*                  Recompiled for changes to NCGSEGMT                        *
.*        V9.04.15  05/04/2005  Steve Armstrong     CAR 60208                 *
.*                  Removed reference to patebiaf and recompile for NCGCLMVR  *
.*        V9.04.14  30/03/2005  Steve Armstrong     CAR 59974                 *
.*                  Fixed writing of patebhaf record to be done in PREP0000   *
.*                  instead of WADR0000 so that the variables don't get       *
.*                  overwritten by PBAT0000 during processing of the batch.   *
.*        V9.04.13  02/02/2005  Steve Armstrong     CAR 57600                 *
.*                  Recompiled for changes to NCGSEGMT                        *
.*        V9.04.12  18/01/2005  Steve Armstrong     CAR 56917                 *
.*                  Recompiled for changes to NCGSEGMT                        *
.*        V9.04.11  15/12/2004  Steve Armstrong     CAR 55922                 *
.*                  Recompiled for changes to NCGSEGMT                        *
.*        V9.04.10  21/10/2004  Steve Armstrong     CAR 54520                 *
.*                  Recompiled for changes to NCGSEGMT & NCGCLMVR             *
.*                  Also added OPEN on PATMI1A8 and PATLINK1                  *
.*                  Also added read on CHOSTYP, PTCNDFIN & PTCNDLKR           *
.*        V9.04.09  19/10/2004  Steve Armstrong     CAR 54427                 *
.*                  Recompiled for changes to NCGSEGMT                        *
.*        V9.04.08  13/10/2004  Steve Armstrong     CAR 54283                 *
.*                  Recompiled for changes to NCGSEGMT                        *
.*        V9.04.07  11/10/2004  Steve Armstrong     CAR 54208                 *
.*                  Recompiled for changes to NCGSEGMT                        *
.*        V9.04.06  11/10/2004  Steve Armstrong     CAR 54180                 *
.*                  Recompiled for changes to NCGSEGMT                        *
.*        V9.04.05  05/10/2004  Jill Habasque       CAR 53798                 *
.*                  Added checks for multi hospital                           *
.*        V9.04.04  23/09/2004  Steve Armstrong     CAR 53737                 *
.*                  Recompiled for changes to NCGSEGMT                        *
.*                  Mods to set same day stay days to "1" instead of "0"      *
.*                  by using NHOSDAYC (for NOINVDYS - 100005 & 100007)        *
.*        V9.04.03  16/09/2004  Steve Armstrong     CAR 53569 & 53557         *
.*                  Recompiled for changes to NCGSEGMT                        *
.*        V9.04.02  10/09/2004  Steve Armstrong     CAR 53402                 *
.*                  Recompiled for changes to NCGSEGMT                        *
.*                  Also fixed controlf read position for PTCNBTHN.           *
.*        V9.04.01  23/08/2004  Steve Armstrong     CAR 52899                 *
.*                  Recompiled for changes to NCGSEGMT                        *
.******************************************************************************
.*        V9.03.06  13/08/2004  Steve Armstrong     CAR 52656                 *
.*                  Recompiled for changes to NCGSEGMT                        *
.*        V9.03.05  03/08/2004  Steve Armstrong     CAR 52322                 *
.*                  Recompiled for changes to NCGSEGMT                        *
.*        V9.03.04  28/07/2004  Steve Armstrong     CAR 51477                 *
.*                  Recompiled for changes to NCGSEGMT & NCGCLMVR             *
.*                  26/07/2004  Steve Armstrong     CAR 52064                 *
.*                  Recompiled for changes to NCGSEGMT.                       *
.*                  26/07/2004  Steve Armstrong     CAR 50708                 *
.*                  Recompiled for changes to NCGSEGMT & NCGCLMVR             *
.*        V9.03.03  10/06/2004  Pat Dirito       CAR 50552                    *
.*                  Added correct positioning of control vars for 9.03        *
.*                  Added print error statements for web report in OPTN0000 & *
.*                  QUES0000 routines                                         *
.*        V9.03.02  08/06/2004  Pat Dirito       CAR 48876                    *
.*                  Re-ported program from 6.11                               *
.******************************************************************************
.*        V6.11.16  18/05/2004  Steve Armstrong  CAR 48876                    *
.*                  Mods to move NCG formatting routines into an external     *
.*                  common routine (NCGSEGMT).                                *
.*                  Also added flag (EXTRFLAG) setting to identify ADT10 when *
.*                  calling NCGSEGMT.                                         *
.*        V6.11.15  22/03/2004  Steve Armstrong  CAR 48453                    *
.*                  Mods to display status on the batch listing (re-extract). *
.*                  Also mods for changes to patedsaf status values.          *
.*                  Also mods to check for batch status at the start of the   *
.*                  program.                                                  *
.*        V6.11.14  29/03/2004  Steve Armstrong  CAR 48430                    *
.*                  Mods to allow for "No Anaesthetic" in 140012 ("N")        *
.*        V6.11.13  15/03/2004  Steve Armstrong  CAR 45665                    *
.*                  Mods to reset any records with a status of "1" (Error in  *
.*                  processing), back to "0" (Not Sent).                      *
.*                  Also mods to populate 170008 with the single amount charge*
.*                  and 170009 with the multiple amount (using TSERVS).       *
.*                  Also removed the check for disease morphology codes so    *
.*                  that all disease codes are sent (DMG).                    *
.*                  Also added check for same admission and invoice when      *
.*                  getting the previous claim details (WCID2050)             *
.*        V6.11.12  09/03/2004  Steve Armstrong  CAR 45665                    *
.*                  Fixed setting of principal service charge (140008) to     *
.*                  load when reading patmmbsf or patecoaf.                   *
.*                  Also initialised value of TCINDC1 prior to determining    *
.*                  anaesthetic type code (140012).                           *
.*        V6.11.11  05/03/2004  Steve Armstrong  CAR 45665                    *
.*                  Fixed setting of 110004 to be "D4" instead of "D3" when   *
.*                  TCFORM6 = "4".                                            *
.*                  Also padded out key 22 for read on patdtraf file in       *
.*                  WACD0000.                                                 *
.*        V6.11.10  24/02/2004  Steve Armstrong  CAR 45665                    *
.*                  Set TESTFLG to be "1" always (separate line for each      *
.*                  segment).                                                 *
.*                  Also added question for resubmission as part of re-       *
.*                  extraction.                                               *
.*        V6.11.09  11/02/2004  Steve Armstrong  CAR 45665                    *
.*                  Fixed field 110008 & 120007 to remove period from ICD     *
.*                  disease code.                                             *
.*                  Also added use of PTCNHCCC to check if using Admission    *
.*                  Type or Bed Type to determine if critical care stay.      *
.*        V6.11.08  27/01/2004  Steve Armstrong  CAR 45665                    *
.*                  Re-wrote WACD/WCCG routines to loop through patdtraf      *
.*                  instead of pattranf.                                      *
.*        V6.11.07  22/01/2004  Steve Armstrong  CAR 45665                    *
.*                  Modified counter for item 150003 to start from 1.         *
.*                  Fixed CID routine to restore current invoice after        *
.*                  checking contigous claim status.                          *
.*                  Fixed field 110013 & 120011 for same day patients.        *
.*                  Also fixed WACD and WCCG routines to restore tran record  *
.*                  for current admission number.                             *
.*        V6.11.06  20/01/2004  Steve Armstrong  CAR 45665                    *
.*                  Mods to use PTCNORIG to populate the .ADR originator      *
.*                  field instead of hardcoding.                              *
.*                  Also modified code to separate out first given name from  *
.*                  second initial (50010 & 50011) instead of using PGNAME    *
.*                  in 50010.                                                 *
.*                  Also removed trailing blanks from the Recipient,          *
.*                  originator & Subject fields in the .ADR file.             *
.*        V6.11.05  19/01/2004  Steve Armstrong  CAR 45665                    *
.*                  Mods to include the Medicare Card Number (#050004)        *
.*        V6.11.04  19/12/2003  Steve Armstrong  CAR 45665                    *
.*                  Mods to record the contiguous claim flag for each claim   *
.*                  sent.  Made further mods so when processing a new claim   *
.*                  (invoice), the program will check what the previous       *
.*                  claim's contiguous flag was set to.  This is required for *
.*                  40006 (previous claim processing) to determine if this is *
.*                  an additional claim.                                      *
.*        V6.11.03  18/12/2003  Steve Armstrong  CAR 45665                    *
.*                  Fixed coversion from dollars/cents to cents for 110013 &  *
.*                  120011.                                                   *
.*        V6.11.02  27/11/2003  Steve Armstrong  CAR 45665                    *
.*                - Fixed validation of Indicator 9 for Category BT so that   *
.*                  110013 and 120011 are populated - code was never reached  *
.*                  to populate these fields.                                 *
.*                - Fixed logic of WACD0000 and WCCG0000 - never reached      *
.*                  setting of TPATAMTI.                                      *
.*                - Also fixed setting of 110013 and 120011 to use key11      *
.*                  instead of key9 to set daily rate to value in cents.      *
.*                - Also fixed I*A in WCID0000 caused by EOF when reading     *
.*                  next record and subsequently not repositioning on         *
.*                  previous record.                                          *
.*                - Added GETSCR00/PUTSCR00 around "mv" of .txt and .ADR      *
.*                  files in WADR0000.  Unix error messages are appearing on  *
.*                  the screen in some circumstances (eg if files are         *
.*                  identical in source & destination directories for         *
.*                  re-extraction) and the screen display was scrolling up.   *
.*                - Mods to populate CID 40006 & 40010 so that resubmissions  *
.*                  can be processed.                                         *
.*                - Mods to process invoices for non-discharged patients.  As *
.*                  a result, had to re-write WCID0000 routine to check for   *
.*                  contiguous claim & set fields accordingly (including      *
.*                  previous claim details).                                  *
.*                - Fix bug in ACD segment to clear fields between each       *
.*                  record.                                                   *
.*                - Removed use of PTCNHCPB for determination of ICU - now    *
.*                  only uses CAT A (not Cat BT).                             *
.*        V6.11.01  27/11/2003  Steve Armstrong CAR 45665                     *
.*                  Mods to fix setting of key for deleting cancelled invoice *
.*                  then repositioning in file (VALD2000)                     *
.*                  Also fixed to check WRTCT is not "0" before writing a     *
.*                  trailer record (WRIT8000)                                 *
.******************************************************************************
.*        V6.10.03  25/03/2003  Tonii CAR 20135 (defect 9)                    *
.*                  Changed program name from "EDI" to "HOSClaims"            *
.*                  Mods to ACS segment, total accom. charge to include GST   *
.*                  Mod to write patebhaf rec in WADR0000, batch total not    *
.*                  getting picked up.                                        *
.*        V6.10.02  17/03/2003 Tonii  CAR 20135                               *
.*                  - Changed wording of message when no extracts are created *
.*                  - Mods to make sure only records which have been sent     *
.*                    (PTEHFLAG = 0) are displayed when using a '?' in Option2*
.*                  - Mods to not display cancelled invoices                  *
.*                  - WINS0000, read PATFUNDF table to get HF code/description*
.*                  - WPAT0000, mods to display 'POSTCODE'                    *
.*                  - WEPD0000, removed EOL at position 56                    *
.*                  - WDMG0000, fixed layout of segment                       *
.*        V6.10.01  21/11/2002 Tonii                                          *
.*                  Removed decimal point in element #110013 & #120011        *
.*                  20/11/2002 Tonii SRF 23277 (IBAADT22, replicated changes) *
.*                  Fixed U*R, increased the array for disease and operation  *
.*                  codes from 20 to 99                                       *
.*                  18/11/2002 Tonii SRF 22061 (IBAADT22, replicated changes) *
.*                  Mods to WACD0000 to use patdtraf to calculate the daily   *
.*                  rates and not values from pattranf                        *
.******************************************************************************

          INC       STD001FD
          INC       NCGCLMVR/INC
          INC       IBASEQFD/INC 
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
          INC       PMSEDGFD/INC       * Effective DRG Dates
.
.
.
ADRFILE1  FILE      ASCII,FIXED=256
EXTFILE1  FILE      ASCII,FIXED=256
TEMPFILE  IFILE     SQL,FIXED=9, KEY="U1-8"
.
.Name     Type   Length   Physical   Start   Description
.----     ----   ------   --------   -----   -----------
TMPURNO   DIM       8        8         1     UR number
.
. End of Record                        9
.
. ----- Program variables -----
.
ADRFNAME  DIM       127                     * Messenger ".ADR" filename
.
BATCHNUM  DIM       8
BSTATDES  DIM       14
BSTATDS1  INIT      "Sent          "
BSTATDS2  INIT      "Acknowledged  "
BSTATDS3  INIT      "Unacknowledged"
.
CNTRCEFR  FORM      1
CEFFDATE  DIM       8
CALLPOSN  FORM      1
COUNTR    FORM      2
CMDLINE   DIM       80
.
DIM5      DIM       5
DIM60     DIM       60
DIM7      DIM       7
.
.
HFUNDCOD  DIM       6                                                 *I-125462
HFUNDNAM  DIM       30                                                *I-125462
.
IBCNMHOS  FORM      1
INVNO     DIM       8                  
.
KEY21A    DIM       21
.
PAGENUMB  FORM      3
.
RECCNT    FORM      5
.                                              0 = No
.                                              1 = Yes
.
SAVHFUND  DIM       6
.
SELHFUND  FORM      1                                                 *I-125462
LEAVDATE  DIM       8
RETNDATE  DIM       8
SAVURNO   DIM       8                       * Save the urno
SKEY18    DIM       18
STATDESC  DIM       6
.
CFPAYCOD  DIM       3        * 3M Codefinder PAY Code
.
.
. Messenger ".ADR" File Constants
.
ADR       INIT      "/ADR"
ADRLN1    INIT      "ID="
ADRLN2    INIT      "Recipient="
ADRLN3    INIT      "Originator="
ADRLN4    INIT      "Subject="
ADRLN5    INIT      "File="
ADRLN6    INIT      "Type="
ADRLN7    INIT      "Patients="
.
FNAME     INIT      "adt10a"
.
HCLM      INIT      "HCLM"
. 
RET       INIT      015
STATUS2   INIT      "Sent  "
TILDA30   INIT      "~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~"
TXT       INIT      ".txt"
.
.
PRGID     INIT      "IBAADT10"
PRGNAM    INIT      "EDI Health Fund Claims Extract"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                   ML0000                                   *
.*                                 Main Module                                *
.******************************************************************************
ML0000    CALL      INIT0000                * Initialise variables & open files
          CALL      HSCLMSRV                * check & update claim statuses
          CALL      CTMP0000                * Create tempfile
          CALL      CREAT000                * create MSG tempfile
.
ML1000    CALL      OPTN0000                * Choose main option
          BRANCH    EXIT,ML9999             * EXIT
          BRANCH    OPTION,ML2000:          * Create Extract 
                           ML4000           * Re-extract Batch
.
ML2000    CALL      KHOS0000                * keyin Hospital Code (Mult.Hosp)
          BRANCH    EXIT,ML1000
ML2500    CALL      KHFND000                * keyin selected Health Fund
          BRANCH    EXIT,ML1000
          CALL      HEAD0000                * Print report header
          CALL      RERR0000                * reset any error records
          CALL      VALD0000                * Validate the EDI Summary file
          CALL      QUES0000                * Extract question
          BRANCH    EXIT,ML3000
.
          CALL      WRIT0000                * Write to the files
ML3000    CALL      TAIL0000                * Print report tail
.
          GOTO      ML1000
.
ML4000    CALL      GBAT0000                * Re-extract batch
          BRANCH    EXIT,ML1000
          CALL      DALL0000
.
          GOTO      ML4000
.
ML9999    CALL      KILL0000                * Kill tempfile
          CALL      KILLM000                * kill MSG tempfile
          CHAIN     PGM
+
.******************************************************************************
.*                                  INIT0000              Called by: ML0000   *
.*                       Initialise Variable & Open Files                     *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen heading
.
          DISPLAY   *P56:24,"Opening controlf"
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patchcof"
          OPEN      PATCHCO1,"patchcof"
.
          DISPLAY   *P64:24,"patdgwaf"
          OPEN      PATDGWA1,"patdgwaf"
.
          DISPLAY   *P64:24,"patdschf"
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"patdtraf"
          OPEN      PATDTRA1,"patdtraf"
          OPEN      PATDTRA2,"patdtraf"
          OPEN      PATDTRA4,"patdtraf"
.
          DISPLAY   *P64:24,"patebhaf"
          OPEN      PATEBHA1,"patebhaf"
          OPEN      PATEBHA2,"patebhaf"
.
          DISPLAY   *P64:24,"patecdaf"
          OPEN      PATECDA1,"patecdaf"
          OPEN      PATECDA2,"patecdaf"
.
          DISPLAY   *P64:24,"patecoaf"
          OPEN      PATECOA2,"patecoaf"
.
          DISPLAY   *P64:24,"patfn1af"
          OPEN      PATFN1A1,"patfn1af"
.
          DISPLAY   *P64:24,"patfx1af"
          OPEN      PATFX1A1,"patfx1af"
.
          DISPLAY   *P64:24,"pathdfaf"
          OPEN      PATHDFA1,"pathdfaf"
.
          DISPLAY   *P64:24,"pathgrpf"
          OPEN      PATHGRP1,"pathgrpf"
.
          DISPLAY   *P64:24,"pati10df"
          CALL      OPICD1
.
          DISPLAY   *P64:24,"pati10of"
          CALL      OPICO1
.
          DISPLAY   *P64:24,"paticuaf"
          OPEN      PATICUA1,"paticuaf"
.
          DISPLAY   *P64:24,"patinvrf"
          OPEN      PATINVR1,"patinvrf"
          OPEN      PATINVR3,"patinvrf"
.
          DISPLAY   *P64:24,"patitemn"
          OPEN      PATITEM1,"patitemn"
.
          DISPLAY   *P64:24,"patlinkf"
          OPEN      PATLINK1,"patlinkf"
.
          DISPLAY   *P64:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmchgf"
          OPEN      PATMCHG1,"patmchgf"
.
          DISPLAY   *P64:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A8,"patmi1af"
.
          DISPLAY   *P64:24,"patvadaf"
          OPEN      PATVADA1,"patvadaf"
.
          DISPLAY   *P64:24,"patdadaf"
          OPEN      PATDADA1,"patdadaf"
.
          DISPLAY   *P64:24,"pmsvx1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patmmbsf"
          OPEN      PATMMBS1,"patmmbsf"
          OPEN      PATMMBS2,"patmmbsf"
.
          DISPLAY   *P64:24,"pattranf"
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patwc1af"
          OPEN      PATWC1A1,"patwc1af"
.
          DISPLAY   *P64:24,"patwmabf"
          OPEN      PATWMAB1,"patwmabf"
.
          DISPLAY   *P64:24,"patwvetf"
          OPEN      PATWVET1,"patwvetf"
.
          DISPLAY   *P64:24,"patedsaf"
          OPEN      PATEDSA1,"patedsaf"
          OPEN      PATEDSA2,"patedsaf"
          OPEN      PATEDSA3,"patedsaf"
          OPEN      PATEDSA4,"patedsaf"
.
          DISPLAY   *P64:24,"patceraf"           * Certificates files
          OPEN      PATCERA1,"patceraf"
          OPEN      PATCLCA1,"patclcaf"
          OPEN      PMSHCPA1,"pmshcpaf"          * Health Care Provider
.
          DISPLAY   *P64:24,"pathspaf"
          OPEN      PATHSPA1,"pathspaf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TEN;*79,CAPPRVNO
          READ      CONTROLF,TEN3;*245,CHOSTYP
          READ      CONTROLF,TWENTY1;*45,PTCNDRSM
          READ      CONTROLF,SEVENTY7;*195,PTCNDLKR,*220,PTCNEDRG
          READ      CONTROLF,SEVENTY9;*82,PTCNDSCI
          READ      CONTROLF,EIGHTY8;*59,PTCNI10D
          READ      CONTROLF,HUND03;*43,PTCNDFIN
          READ      CONTROLF,HUND10;*115,PTCNUEDI,*116,PTCNEDIH,*118,PTCNEDIO:
                                    *178,PTCNORIG,*228,PTCNHCCC,*242,PTCNMBRL
.
          DISPLAY   *P64:24,"oprdetaf";
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OPRDETA2,"oprdetaf"
          TRAPCLR   IO
          MOVE      OVRCD,OPRFLG            * OPR flag
.
          DISPLAY   *P64:24,"pat3miaf";
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PAT3MIA1,"pat3miaf"
          TRAPCLR   IO
          MOVE      OVRCD,MAPFLG            * Map flag
.
          PACK      CURDTE8,CCC,CYY,CMM,CDD
          REP       " 0",CURDTE8
          CLOCK     TIME,CTIMEIS
          MOVE      ONE,CNEWDS
.
INIT2000  MOVE      ZERO,FIELD2
          WHILE     FIELD2<>15
            ADD       ONE,FIELD2
            PACK      TMPKEY2[FIELD2],SP30
          DO
.
          MOVE      ZERO,FIELD1            
          WHILE     FIELD1<>18
            ADD      ONE,FIELD1
            MOVE     SP8,TMPKEY1[FIELD1]
          DO
.
          CALL      TFILENAM
          MOVE      TFILNAME,TMPFILNM
.
          CALL      TFILENAM
          MOVE      TFILNAME,TEMPMSGF
.
.         Set the extract flag (EXTRFLAG) to identify the calling program
.         as IBAADT10 when NCGSEGMT is called.  This flag is required as
.         IBAADT10 uses PATEDSFD to record EDI sent, while IBAADT33 uses
.         PATINVFD.
.
INIT9000  MOVE      ZERO,EXTRFLAG
.
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000              Called by: ML0000   *
.*                                Select Option                               *
.******************************************************************************
OPTN0000  MOVE      ZERO,EXIT
          MOVE      ZERO,OPTION
          HLINE     *G37,2,55,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Create Extract ":
                    *P1:6,*V2LON,"2",*HOFF," = Re-extract Batch ":
                    *P1:8,"Select Option : ";
.
OPTN1000  KEYIN     *P17:8,*V2LON,*DV,UNDLN2,*P17:8,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN2000,OPTN2000
.
          BEEP
          GOTO      OPTN1000
.
OPTN2000  COMPARE   ONE,PTCNUEDI
          GOTO      OPTN2500 IF EQUAL
.
          DISPLAY   *P1:24,"Hospital not using the EDI Module. "; 
          PRINT     *N,*1,"Hospital not using the EDI Module. "; 
          CALL      HITEXIT
          GOTO      OPTN9500
.
OPTN2500  MATCH     SP8,PTCNEDIH
          IF        @EQUAL
            DISPLAY   *P1:24,*B,"EDI Hospital Code blank. ";
            PRINT     *N,*1,"EDI Hospital Code blank. ";
            CALL      HITENTER
            GOTO      OPTN9500
          ENDIF
.
          MATCH     SP70,PTCNEDIO
          IF        @EQUAL
            DISPLAY   *P1:24,*B,"EDI Extract Outbound Path blank. ";
            PRINT     *N,*1,"EDI Extract Outbound Path blank. ";
            CALL      HITENTER
            GOTO      OPTN9500
          ENDIF
.
          MATCH     SP70,PTCNORIG
          IF        @EQUAL
            DISPLAY   *P1:24,*B,"EDI Originator blank. ";
            PRINT     *N,*1,"EDI Originator blank. ";
            CALL      HITENTER
            GOTO      OPTN9500
          ENDIF
.
          BRANCH    OPTION,OPTN3000,OPTN4000
.
OPTN3000  DISPLAY   *P55:2,*V2LON,"- Create Extract ";
          MOVE      "- Create Extract",CPHDROPT
.
. initialise arrays used in option 1
.
          MOVE      ZERO,COUNT
          WHILE     COUNT<>99 
            ADD       ONE,COUNT
            PACK      ARRAY[COUNT],SP30
            PACK      RECARRY[COUNT],SP30
          DO
          GOTO      OPTN9000
.
OPTN4000  DISPLAY   *P55:2,*V2LON,"- Re-extract Batch ";
          MOVE      "- Re-extract Batch",CPHDROPT
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.******************************************************************************
.*                                  GBAT0000              Called by: ML0000   *
.*                       Re-extract previously extracted batch                *
.******************************************************************************
GBAT0000  KEYIN     *P1:3,*EF:
                    *P1:4,"Batch Number :":
                    *P16:4,*V2LON,*JR,BATCHNUM
.
          PACK      BATCHNUM,BATCHNUM,SP8
          MATCH     SP8,BATCHNUM                 * nothing input ?
          GOTO      GBAT9100 IF EQUAL            * yes - exit
.
          UNPACK    BATCHNUM,DIM7,ANS
          MATCH     QUEST,ANS                    * "?" input ?
          IF        @EQUAL
            CALL      DBAT0000                   * yes - display all batches
            GOTO      GBAT0000                   * get next batch number
          ENDIF
.
          TYPE      BATCHNUM                     * numeric input ?
          IF        !@EQUAL
            BEEP                                 * no - error
            GOTO      GBAT0000
          ENDIF
.
          MOVE      BATCHNUM,KEY8
          CALL      RDPTEBH1
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Batch number not on file.  ";
            CALL      HITENTER
            GOTO      GBAT0000
          ENDIF
.
GBAT9000  MOVE      ZERO,EXIT
          GOTO      GBAT9999
.
GBAT9100  MOVE      ONE,EXIT
GBAT9999  RETURN 
+
.***************************************************************************
.*                             DBAT0000               Called by: GBAT0000  *
.*                 Display all batches in reverse sequence                 *
.***************************************************************************
.
DBAT0000  CALL      BHED0000                     * display batch headings
.
          MOVE      ONE,CPAGENO                  * Initialise page number
          MOVE      FOUR,CVERT                   * Reset line number
          MOVE      ZERO,FIELD1                  * Reset record count
.
          PACK      KEY8,TILDA30
          CALL      RSPTEBH1                     * position in file
DBAT1000  CALL      RPPTEBH1                     * read previous record
          BRANCH    OVRCD,DBAT5000               * end of file
.
          ADD       ONE,CVERT                    * increment line count
.
          COMPARE   TWENTY3,CVERT                * page full ?
          GOTO      DBAT4000 IF NOT LESS         * yes
.
DBAT3000  ADD       ONE,FIELD1
.
          UNPACK    PTEHDTBC,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          UNPACK    PTEHTMBC,DIM5,ANS,ANS,ANS
.
.         Load the batch status
.
          MOVE      "Extracted     ",BSTATDES
          LOAD      BSTATDES,PTEHFLAG,BSTATDS1,BSTATDS2,BSTATDS3
.
.         Display the line item on the screen
.
          DISPLAY   *P1:CVERT,*V2LON,FIELD1,*HOFF,*P8:CVERT,PTEHBTHN:
                    *P20:CVERT,PTEHHFGP,*P34:CVERT,CPCDATE,*P47:CVERT,DIM5:
                    *P54:CVERT,BSTATDES
          MOVE      PTEHBTHN,TMPKEY1[FIELD1]
          GOTO      DBAT1000
.
.         new page needed
.
DBAT4000  BRANCH    CPAGENO,DBAT4500
.
.         in middle pages : Next & First options
.
DBAT4100  MOVE      "1",CALLPOSN                 * set call position
          CALL      KEYA0000                     * keyin response
          BRANCH    EXIT,DBAT9999:               * exit
                         DBAT7000:               * next
                         DBAT8000                * previous
          GOTO      DBAT6000                     * number entered
.
.         on first page : Next option
.
DBAT4500  MOVE      "2",CALLPOSN                 * set call position
          CALL      KEYA0000                     * keyin response
          BRANCH    EXIT,DBAT9999:               * exit
                         DBAT7000:               * next
                         DBAT5900                * previous
          GOTO      DBAT6000                     * number entered
.
.         end of data
.
DBAT5000  BRANCH    CPAGENO,DBAT5500
.
.         in middle pages : First option
.
DBAT5100  MOVE      "3",CALLPOSN                 * set call position
          CALL      KEYA0000                     * keyin response
          BRANCH    EXIT,DBAT9999:               * exit
                         DBAT5900:               * next
                         DBAT8000                * prev
          GOTO      DBAT6000                     * number entered
.
.         on first page : No options
.
DBAT5500  MOVE      "4",CALLPOSN                 * set call position
          CALL      KEYA0000                     * keyin response
          BRANCH    EXIT,DBAT9999:               * exit
                         DBAT5900:               * next
                         DBAT5900                * previous
          GOTO      DBAT6000                     * number entered
.
DBAT5900  BEEP
DBAT5910  BRANCH    CALLPOSN,DBAT4100,DBAT4500,DBAT5100,DBAT5500
.
.         number entered
.
DBAT6000  MOVE      TMPKEY1[FORM2],KEY8
          CALL      RDPTEBH1                     * record on file ?
          BRANCH    OVRCD,DBAT5900               * no - shouldn't happen
.
.         Display claim details for selected batch
.
          MOVE      TMPKEY1[FORM2],BATCHNUM
          CALL      DALL0000                     * display claim invoices
.
.         Resdisplay batch listing
.
          MOVE      TMPKEY1[1],KEY8              * redisplay screen
          CALL      RDPTEBH1                     * record on file ?
          BRANCH    OVRCD,DBAT0000               * no - shouldn't happen
.
          CALL      BHED0000                     * redisplay header
          MOVE      FIVE,CVERT                   * set scrren variables
          DISPLAY   *P1:CVERT,*EF
          MOVE      ZERO,FIELD1
          GOTO      DBAT3000
.
.         next screen selected
.
DBAT7000  MOVE      FIVE,CVERT
          ADD       ONE,CPAGENO
          DISPLAY   *P1:CVERT,*EF
          MOVE      ZERO,FIELD1
          GOTO      DBAT3000
.
.         previous screen selected
.
DBAT8000  MOVE      TMPKEY1[1],KEY8
          CALL      RDPTEBH1                     * reread first record on screen
          MOVE      ZERO,FORM2                   * initialise count
          WHILE     FORM2 <= 17
            CALL      RKPTEBH1                   * read next record
            BRANCH    OVRCD,DBAT0000             * eof - shouldn't happen
.
            ADD       ONE,FORM2                  * increment count
          DO
.
          MOVE      FIVE,CVERT                   * reset line position
          SUB       ONE,CPAGENO                  * decrement page number
          MOVE      ZERO,FIELD1                  * reset record counter
          DISPLAY   *P1:CVERT,*EF
          GOTO      DBAT3000
.
DBAT9999  RETURN
+
.*****************************************************************************
.*                           DALL0000                  Called by: MAIN0000   *
.*                  Display all the invoices for a batch          DBAT0000   *
.*****************************************************************************
.
DALL0000  PACK      KEY14,PTEHHFGP,ZERO4,SP20
          CALL      RDFUND1                      * HF on file ?
          IF        OVRCD = 1
            MOVE      "Unknown Health Fund",HFTABL
          ENDIF
.
          DISPLAY   *P1:4,*EF,"Batch Number:",*P16:4,*V2LON,PTEHBTHN,*HOFF:
                    *P1:5,"Health Fund  :",*P16:5,*V2LON,PTEHHFGP,*HOFF:
                    *P30:5,HFNAME:
                    *P70:6,"Batch":
                    *P6:7,*ULON,"Admission":
                    *P16:7,"Invoice ":
                    *P25:7,"U/R No. ":
                    *P34:7,"Invoice Amount ":
                    *P50:7,"Claim Status":
                    *P64:7,"Previous":
                    *P73:7,"Next    "
.
          MOVE      ONE,PAGENUMB                 * Initialise page number
          MOVE      SEVEN,EVERT                  * Reset line number
          MOVE      ZERO,FIELD2                  * Reset record count
.
          PACK      KEY24,BATCHNUM,SP30
          CALL      RSPTEDS3                     * Position for batch #
DALL1000  CALL      RKPTEDS3                     * read next record
          BRANCH    OVRCD,DALL5000               * end of file
.
          MATCH     PTESBATN,BATCHNUM            * same batch still ?
          GOTO      DALL5000 IF NOT EQUAL        * no - finished list
.
          MATCH     "00000000",PTESNBAT 
          GOTO      DALL1000 IF EQUAL
.
          ADD       ONE,EVERT                    * increment line count
.
          COMPARE   TWENTY3,EVERT                * page full ?
          GOTO      DALL4000 IF NOT LESS         * yes
.
DALL3000  ADD       ONE,FIELD2
.
          DISPLAY   *P7:EVERT,PTESADMN,*P16:EVERT,PTESINVN,*P25:EVERT,PTESURNO
.
          MOVE      PTESINVN,KEY8
          CALL      RDINV1
          IF        OVRCD = 1
            DISPLAY   *P42:EVERT,"Unknown"
          ELSE
            DISPLAY   *P38:EVERT,PINVAMT
          ENDIF
.
          MOVE      STATUS2,STATDESC
.
          DISPLAY   *P50:EVERT,STATDESC,*P64:EVERT,PTESPBAT,*P73:EVERT,PTESNBAT
.
          PACK      TMPKEY2[FIELD2],PTESBATN,PTESADMN,PTESINVN
          GOTO      DALL1000
.
.         new page needed
.
DALL4000  BRANCH    PAGENUMB,DALL4500
.
.         in middle pages : Next & First options
.
DALL4100  MOVE      "1",CALLPOSN                 * set call position
          CALL      KEYB0000                     * keyin response
          BRANCH    EXIT,DALL9999:               * exit
                         DALL7000:               * next
                         DALL8000                * previous
          GOTO      DALL6000                     * Re-extract entered
.
.         on first page : Next option
.
DALL4500  MOVE      "2",CALLPOSN                 * set call position
          CALL      KEYB0000                     * keyin response
          BRANCH    EXIT,DALL9999:               * exit
                         DALL7000:               * next
                         DALL5900                * previous
          GOTO      DALL6000                     * Re-extract entered
.
.         end of data
.
DALL5000  BRANCH    PAGENUMB,DALL5500
.
.         in middle pages : First option
.
DALL5100  MOVE      "3",CALLPOSN                 * set call position
          CALL      KEYB0000                     * keyin response
          BRANCH    EXIT,DALL9999:               * exit
                         DALL5900:               * next
                         DALL8000                * prev
          GOTO      DALL6000                     * Re-extract entered
.
.         on first page : No options
.
DALL5500  MOVE      "4",CALLPOSN                 * set call position
          CALL      KEYB0000                     * keyin response
          BRANCH    EXIT,DALL9999:               * exit
                         DALL5900:               * next
                         DALL5900                * previous
          GOTO      DALL6000                     * Re-extract entered
.
DALL5900  BEEP
DALL5910  BRANCH    CALLPOSN,DALL4100,DALL4500,DALL5100,DALL5500
.
.         Post re-extraction selected
.
DALL6000  CALL      REXT0000                     * call re-extraction
          DISPLAY   *P1:24,*EL,"Re-extraction finished. ";
          CALL      HITENTER
          GOTO      DALL9999
.
.         next screen selected
.
DALL7000  MOVE      EIGHT,EVERT
          ADD       ONE,PAGENUMB
          DISPLAY   *P1:EVERT,*EF
          MOVE      ZERO,FIELD2
          GOTO      DALL3000
.
.         previous screen selected
.
DALL8000  MOVE      TMPKEY2[1],KEY24
          CALL      RDPTEDS3                     * reread first record on screen
          MOVE      ZERO,FORM2                   * initialise count
          WHILE     FORM2 <= 14
            CALL      RPPTEDS3                   * read previous record
            BRANCH    OVRCD,DALL0000             * eof - shouldn't happen
.
            MATCH     PTESBATN,BATCHNUM          * same batch still ?
            GOTO      DALL0000 IF NOT EQUAL      * no - finished list
.
            ADD       ONE,FORM2                  * increment count
          DO
.
          MOVE      EIGHT,EVERT                  * reset line position
          SUB       ONE,PAGENUMB                 * decrement page number
          MOVE      ZERO,FIELD2                  * reset record counter
          DISPLAY   *P1:EVERT,*EF
          GOTO      DALL3000
.
DALL9999  RETURN
+
.*********************************************************************
.*                  KEYB0000                    Called by : DALL0000 *
.*        Keyin response to question                        DADM0000 *
.*        Returns : EXIT = 0    Re-extraction entered                *
.*                  EXIT = 1      Exit   entered                     *
.*                  EXIT = 2      Next   entered                     *
.*                  EXIT = 3      Prev   entered                     *
.*********************************************************************
.
KEYB0000  DISPLAY   *P1:24,*EL,"Re-(",*V2LON,ANSE,*HOFF,")xtract, e(":
                    *V2LON,ANSX,*HOFF,")it";
          MOVE      TEN9,CCOL
          IF        CALLPOSN = 1 | CALLPOSN = 2
            DISPLAY   ", (",*V2LON,ANSN,*HOFF,")ext";
            ADD       EIGHT,CCOL
          ENDIF
          IF        CALLPOSN = 1 | CALLPOSN = 3
            DISPLAY   ", p(",*V2LON,ANSR,*HOFF,")evious";
            ADD       TEN2,CCOL
          ENDIF
.
          DISPLAY   " :";
          ADD       FOUR,CCOL
.
KEYB1000  KEYIN     *PCCOL:24,*DV,UNDLN2:
                    *PCCOL:24,*V2LON,*JR,DIM2:
                    *PCCOL:24,*DV,DIM2
          PACK      DIM2,DIM2,SP2
          UNPACK    DIM2,ANS,ANS
          REP       UPPLOW,ANS
.
          MATCH     ANSE,ANS                * re-Extraction selected ?
          GOTO      KEYB1500 IF NOT EQUAL   * No 
.
KEYB1200  DISPLAY   *P1:24,*EL,"Re-extraction selected. Ok to Continue (":
                               *V2LON,ANSY,*HOFF,SLASH:
                               *V2LON,ANSN,*HOFF,") ? ";
.
          KEYIN     *P48:24,*V2LON,ANS
.
          REP       UPPLOW,ANS
          MATCH     ANSY,ANS
          IF        @EQUAL
KEYB1300    KEYIN     *P1:24,*EL,"Is batch a resubmission (",*V2LON,*DV,ANS:
                      *HOFF,*DV,SLASH,*V2LON,*DV,ANSN,*HOFF,") ? ":
                      *V2LON,ANS
            REP       UPPLOW,ANS
            MATCH     ANS,ANSY
            IF        @EQUAL
              MOVE      ONE,RESUBMN
              GOTO      KEYB9000
            ENDIF
.
            MATCH     ANS,ANSN
            IF        @EQUAL
              MOVE      ZERO,RESUBMN
              GOTO      KEYB9000
            ENDIF
            BEEP
            GOTO      KEYB1300
          ENDIF
.
          MATCH     ANSN,ANS
          GOTO      KEYB0000 IF EQUAL
. 
          BEEP
          GOTO      KEYB1200
.
KEYB1500  MATCH     ANSX,ANS                * eXit selected ?
          GOTO      KEYB9100 IF EQUAL       * yes
.
          MATCH     ANSN,ANS                * next page selected ?
          GOTO      KEYB9200 IF EQUAL       * yes
.
          MATCH     ANSR,ANS                * previous page selected ?
          GOTO      KEYB9300 IF EQUAL       * yes
.
KEYB2000  BEEP
          GOTO      KEYB1000
.
.         number entered directly
.
KEYB5000  MOVE      DIM2,FORM2
.
          COMPARE   FORM2,ZERO              * number < 1 ?
          GOTO      KEYB2000 IF NOT LESS    * yes - invalid
.
          COMPARE   FORM2,FIELD2            * number in range ?
          GOTO      KEYB2000 IF LESS        * no - too high
.
.         set the exit flag
.
KEYB9000  MOVE      ZERO,EXIT               * Re-extract selected
          GOTO      KEYB9999
.
KEYB9100  MOVE      ONE,EXIT                * exit  
          GOTO      KEYB9999
.
KEYB9200  MOVE      TWO,EXIT                * next
          GOTO      KEYB9999
.
KEYB9300  MOVE      THREE,EXIT              * previous
          GOTO      KEYB9999
.
KEYB9999  RETURN
+
.*****************************************************************************
.*                              BHED0000               Called by: DBAT0000   *
.*                Display the headings for the batch listing                 *
.*****************************************************************************
.
BHED0000  DISPLAY   *P1:4,*EF,*ULON,"Item":
                    *P8:4,"Batch No.":
                    *P20:4,"Health Fund":
                    *P34:4,"Date      ":
                    *P47:4,"Time ":
                    *P54:4,"Status"
.
BHED9999  RETURN
+
.*****************************************************************************
.*                  KEYA0000                           Called by : DBAT0000  *
.*        Keyin response to question                                         *
.*        Returns : EXIT = 0      Number entered                             *
.*                  EXIT = 1      Exit   entered                             *
.*                  EXIT = 2      Next   entered                             *
.*                  EXIT = 3      Prev   entered                             *
.*****************************************************************************
.
KEYA0000  DISPLAY   *P1:24,*EL,"Select item, e(",*V2LON,ANSX,*HOFF,")it";
          MOVE      TEN9,CCOL
          IF        CALLPOSN = 1 | CALLPOSN = 2
            DISPLAY   ", (",*V2LON,ANSN,*HOFF,")ext";
            ADD       EIGHT,CCOL
          ENDIF
          IF        CALLPOSN = 1 | CALLPOSN = 3
            DISPLAY   ", p(",*V2LON,ANSR,*HOFF,")evious";
            ADD       TEN2,CCOL
          ENDIF
.
          DISPLAY   " :  ";
          ADD       FOUR,CCOL
.
KEYA1000  KEYIN     *PCCOL:24,*DV,UNDLN2:
                    *PCCOL:24,*V2LON,*JR,DIM2:
                    *PCCOL:24,*DV,DIM2
          PACK      DIM2,DIM2,SP2
          UNPACK    DIM2,ANS,ANS
          REP       UPPLOW,ANS
.
          MATCH     ANSX,ANS                * exit selected ?
          GOTO      KEYA9100 IF EQUAL       * yes
.
          MATCH     ANSN,ANS                * next page selected ?
          GOTO      KEYA9200 IF EQUAL       * yes
.
          MATCH     ANSR,ANS                * previous page selected ?
          GOTO      KEYA9300 IF EQUAL       * yes
.
          TYPE      DIM2
          GOTO      KEYA5000 IF EQUAL       * a number entered
.
KEYA1500  BEEP
          GOTO      KEYA1000
.
.         number entered directly
.
KEYA5000  MOVE      DIM2,FORM2
.
          COMPARE   FORM2,ZERO              * number < 1 ?
          GOTO      KEYA1500 IF NOT LESS    * yes - invalid
.
          COMPARE   FORM2,FIELD1            * number in range ?
          GOTO      KEYA1500 IF LESS        * no - too high
.
.         set the exit flag
.
          MOVE      ZERO,EXIT               * number selected
          GOTO      KEYA9999
.
KEYA9100  MOVE      ONE,EXIT                * exit
          GOTO      KEYA9999
.
KEYA9200  MOVE      TWO,EXIT                * next
          GOTO      KEYA9999
.
KEYA9300  MOVE      THREE,EXIT              * previous
          GOTO      KEYA9999
.
KEYA9999  RETURN
+
.******************************************************************************
.*                                  REXT0000              Called by: DALL0000 *
.*                         Re-extract selected batch number                   *
.******************************************************************************
REXT0000  MOVE      ZERO,TMPCNT
          UNPACK    BATCHNUM,DIM2,BATCHNO
          MOVE      BATCHNUM,SAVBTHN
.
          PACK      KEY24,BATCHNUM,SP30 
REXT1000  CALL      RSPTEDS3
REXT1500  CALL      RKPTEDS3
          BRANCH    OVRCD,REXT3000
.
          MATCH     BATCHNUM,PTESBATN
          GOTO      REXT3000 IF NOT EQUAL
.
          MATCH     SP8,PTESNBAT          
          GOTO      REXT1500 IF NOT EQUAL  * Record selected for re-submission
.
          PACK      KEY32,PTESFLAG,PTESHFND,PTESADMN,PTESINVN,PTESBATN
          CALL      RDPTEDS1
          BRANCH    OVRCD,REXT1500
.
          MOVE      ZERO,PTESFLAG
          CALL      UPPTEDS1
          ADD       ONE,TMPCNT
.
          GOTO      REXT1500
.
REXT3000  CALL      WRIT0000
.         
REXT9999  RETURN
+
.******************************************************************************
.*                                  HEAD0000              Called by: ML0000   *
.*                             Print Report Header                            *
.******************************************************************************
HEAD0000  CALL      HEAD80                  * Print 80 column header
          IF        ONE,IBCNMHOS
            PRINT     *30,PTHSNAME          * Hospital Name (mult.hosp)
          ENDIF
          PRINT     *20,"extract for - ",HFUNDNAM
.
          CALL      UND80
          PRINT     *1,"| Inv No | Adm No | Error",*80,"|"
          CALL      UND80
          ADD       "7",CLNO
.
HEAD9000  MOVE      ZERO,EXIT
HEAD9999  RETURN
+
.******************************************************************************
.*                                  VALD0000              Called by: ML0000   *
.*                            Validate The Tempfile                           *
.******************************************************************************
VALD0000  MOVE      ONE,VALDFLG             * Validation flag - yes error check
.
          MOVE      ZERO,RECCNT             * Record counter for array
          MOVE      ZERO,REDCT
          MOVE      ZERO,ERRCT
          MOVE      ZERO,WARCT
          MOVE      ZERO,COUNT
          MOVE      ZEROVISN,SAVADMIN       * Clear the save admin no
          MOVE      SP8,SAVINVNO            * Clear the save invoice no
          MOVE      SP8,SAVINVN2            * Clear the save invoice no 2
          MOVE      ZERO,TOTAMTCT           * Clear the total amount counter
          MOVE      ZERO,TOTRECCT           * Clear the total record counter
          MOVE      ZERO,WRTCT
          DISPLAY   *P1:24,*EL,"Validating : ";
.
          PACK      KEY32,SP1,ZERO,SP30,SP10
          IF        SELHFUND = 1
            PACK      KEY32,SP1,ZERO,HFUNDCOD,SP30,SP10               *I-125462
          ENDIF
VALD0500  CALL      RSPTEDS1                * Position on & read patedsaf 
VALD1000  CALL      RKPTEDS1                  record
          BRANCH    OVRCD,VALD8000
.
          COMPARE   ZERO,PTESFLAG
          GOTO      VALD8000 IF NOT EQUAL   
.
          MATCH     SP8,PTESBATN          
          GOTO      VALD1000 IF NOT EQUAL
.                                                                     *I-125462
          IF        SELHFUND = 1
            MATCH     HFUNDCOD,PTESHFND
            GOTO      VALD1000 IF NOT EQUAL
          ENDIF
.
.
          PACK      SAVKEY32,PTESFLAG,PTESHFND,PTESADMN,PTESINVN,SP30
.
          MOVE      PTESADMN,ADMINO
          MOVE      PTESINVN,INVNO 
          MOVE      PTESFLAG,ERRFLG         * initialise error flag ERRFLG
          DISPLAY   *P15:24,*V2LON,INVNO,SP2,ADMINO;
.
          COMPARE   ONE,IBCNMHOS
          GOTO      VALD1500 IF NOT EQUAL
.                                           * Multi Hospital - identify current
.                                           * hospital summary records by 
.                                           * checking against Visit Extn.
          PACK      KEY8,ADMINO
          CALL      RDPMVX11                * Read a visit extension record
          IF        OVRCD=1
            MOVE      "Missing visit ext file record",ERRMSG
            CALL      ERMSG000              * Print an error message
          ENDIF
.
          MATCH     PMVXMHOS,PTHSHOSP
          GOTO      VALD1000 IF NOT EQUAL
.
VALD1500  IF        REDCT = 0
            MOVE      PTESHFND,SAVHFUND
          ELSE
            MATCH     PTESHFND,SAVHFUND
            IF        !@EQUAL
              ADD       ONE,COUNT
              PACK      RECARRY[COUNT],RECCNT
              MOVE      PTESHFND,SAVHFUND
              MOVE      ZERO,RECCNT
            ENDIF
          ENDIF
.
          ADD       ONE,REDCT
.
          PACK      KEY8,PTESINVN
          CALL      RDINV1                  * Read an invoice file record
          IF        OVRCD=1
            MOVE      "Missing invoice file record",ERRMSG
            CALL      ERMSG000              * Print an error message
          ELSE
.                                          *** CAR 20135 Ignore Cancelled Inv.
.                                          * ignore (split) Pat.Invoice C-65745
.                                          * ignore "fully credited" invoices
            MOVE      PTINCRST,FORM1       * fully credited' = 1
.
.           IF        PINVADM = 0 | PINVTYP = 2 | FORM1 = 1
.
            MATCH     ZEROVISN,PINVADM
            GOTO      VALD1600 IF EQUAL
.
            IF        PINVTYP = 2 | FORM1 = 1
VALD1600      PACK      KEY32,PTESFLAG,PTESHFND,PTESADMN,PTESINVN,PTESBATN
              CALL      DEPTEDS1           * Delete cancelled invoices
              SUB       ONE,REDCT
              CALL      RSPTEDS1           * reposition for next read
              GOTO      VALD1000
            ENDIF
          ENDIF
.
          MATCH     SAVADMIN,ADMINO
          GOTO      VALD2000 IF EQUAL       * Same admin no, dont get details
.
          PACK      KEY8,ADMINO
          CALL      RDMISS1                 * Read an admin file record
          IF        OVRCD=1
            MOVE      "Missing admission file record",ERRMSG
            CALL      ERMSG000              * Print an error message
          ENDIF
.
          CALL      CLPATDSC                * Clear disch file variables
          IF        ASTAT = 3
            PACK      KEY8,ADMINO
            CALL      RDDSCH1               * Read a disch file record
            IF        OVRCD=1
              MOVE      "Missing discharge file record",ERRMSG
              CALL      ERMSG000            * Print an error message
            ENDIF
          ENDIF
.
          PACK      KEY8,PINVUR
          CALL      RDMAST1                 * Read a master file record
          IF        OVRCD=1
            MOVE      "Missing master file record",ERRMSG
            CALL      ERMSG000              * Print an error message
          ENDIF
.
          MOVE      ADATE,INVFRDTE          * Invoice from date
          MOVE      ZERO,INVRECCT           * Clear the invoice record counter
          MOVE      ADMINO,SAVADMIN         * Save the admin no
.
VALD2000  MOVE      PINVDTE,INVTODTE        * Default to date to invoice date
          MATCH     SP8,DDATE
          GOTO      VALD3000 IF EQUAL       * Patient not discharged
.
          MATCH     DDATE,PINVDTE
          GOTO      VALD3000 IF LESS        * Invoice date < disch date
.
          MOVE      DDATE,INVTODTE          * Default to date to disch date
.
VALD3000  MOVE      ZERO,INVAMTCT           * Clear the invoice amount counter
          ADD       ONE,INVRECCT            * Increm the invoice record counter
          ADD       ONE,TOTRECCT            * Increment the total record counter
.
          MOVE      ZERO4,NOINVDYS          * No of invoice days
.
          READ      CONTROLF,TWENTY;*193,PTCNDCLM
          CALL      DCLM0000                * get default claim code
          CALL      WRCID000                * Write claim id details
          BRANCH    EXIT,VALD0500           * zero claim - ignore claim(deleted)
          CALL      WRPAT000                * Write patient details
          CALL      WREPD000                * Write episode details
          CALL      WRSVB000                * Write bungle/single value details
          CALL      WRANB000                * Write newborn baby details
          CALL      WRTFR000                * Write transfer details
          CALL      WRACS000                * Write accom summary details
          CALL      WRACD000                * Write accom details
          CALL      WRCCG000                * Write critical care details
          IF        LEAVDAY <> 0
            CALL      WRLPD000
          ENDIF
          CALL      WRPSG000                * Write principal services details
          CALL      WRMIG000                * Write misc services details
          CALL      WRDMG000                * Write DRG morbidity details
          CALL      WRCER000                * Write certificate details
.
          ADD       INVAMTCT,TOTAMTCT       * Increment the total amount counter
          MOVE      INVTODTE,INVFRDTE       * Invoice from date
.
          COMPARE   ONE,ERRFLG  
          IF        @EQUAL
            MOVE      ONE,PTESFLAG
            CALL      UPPTEDS1
            MOVE      SAVKEY32,KEY32
            GOTO      VALD0500
          ENDIF
.
          ADD       ONE,RECCNT
          GOTO      VALD1000
.
VALD8000  ADD       ONE,COUNT               * Write last health fund record
          PACK      RECARRY[COUNT],RECCNT
.
          CALL      WRTLR000                * Write trailer details
.
VALD9000  MOVE      ZERO,EXIT
VALD9999  RETURN
+
.******************************************************************************
.*                                  QUES0000              Called by: ML0000   *
.*                              Extract Question                              *
.******************************************************************************
QUES0000  DISPLAY   *P1:20,*EF,"Total Records Read     : ",*V2LON,REDCT,*HOFF:
                    *P1:21,"Total Number of Errors : ",*V2LON,ERRCT,*HOFF:
                    *P1:22,"Total Number Warnings  : ",*V2LON,WARCT,*HOFF;
.
          COMPARE   ZERO,REDCT
          GOTO      QUES1000 IF NOT EQUAL   * No data found
.
          DISPLAY   *P1:24,*EL,*B,"No data found. Extract not created.  ";
          PRINT     *N,*1,"No data found. Extract not created.  ";
          CALL      HITENTER
          GOTO      QUES9500
.
QUES1000  IF        ERRCT<>0
            DISPLAY   *P1:24,*EL,*B,"Errors found.  ";
          ENDIF
          DISPLAY   *P16:24,*EL,"Create extract files (",*V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ";
.
QUES2000  KEYIN     *P45:24,*V2LON,ANS;
          REP       UPPLOW,ANS
.
          MATCH     ANSY,ANS
          GOTO      QUES9000 IF EQUAL
.
          MATCH     ANSN,ANS
          GOTO      QUES3000 IF EQUAL
.
          BEEP
          GOTO      QUES2000
.
. Not doing extraction, reset status of records back to "0"
.
QUES3000  PACK      KEY32,SP1,ONE,SP30,SP10
          CALL      RSPTEDS1
QUES3500  CALL      RKPTEDS1
          BRANCH    OVRCD,QUES9500
.
          COMPARE   ONE,PTESFLAG
          GOTO      QUES9500 IF NOT EQUAL
.
          MOVE      ZERO,PTESFLAG
          CALL      UPPTEDS1
          GOTO      QUES3000
.
QUES9000  MOVE      ZERO,EXIT
          GOTO      QUES9999
.
QUES9500  MOVE      ONE,EXIT
QUES9999  RETURN
+
.******************************************************************************
.*                                  WRIT0000              Called by: ML0000   *
.*                             Write To The Files                             *
.******************************************************************************
WRIT0000  MOVE      ZERO,VALDFLG            * Validaion flag - no writing
.
          MOVE      ZERO,COUNT              * Clear array count
          MOVE      ZEROVISN,SAVADMIN       * Clear the save admin no
          MOVE      ZERO,TOTAMTCT           * Clear the total amount counter
          MOVE      ZERO,TOTRECCT           * Clear the total record counter
          MOVE      ZERO,WRTCT
          MOVE      SP20,SAVHFUND
          DISPLAY   *P1:23,*EF,"Processing Extract file [         ]":
                    *P1:24,"Writing : ";
.
WRIT0500  PACK      KEY32,SP1,ZERO,SP30,SP10
          CALL      RSPTEDS1                * Position on & read a tempfile
WRIT1000  CALL      RKPTEDS1                  record
          BRANCH    OVRCD,WRIT8000
.
          COMPARE   ZERO,PTESFLAG
          GOTO      WRIT8000 IF NOT EQUAL 
.
          IF        OPTION = 1              
            MATCH     SP8,PTESBATN          
            GOTO      WRIT1000 IF NOT EQUAL
.                                                                     *I-125462
            IF        SELHFUND = 1
              MATCH     HFUNDCOD,PTESHFND        * select reqd health fund
              GOTO      WRIT1000 IF NOT EQUAL
            ENDIF
          ELSE
            MATCH     BATCHNUM,PTESBATN     
            GOTO      WRIT1000 IF NOT EQUAL
          ENDIF
.
          COMPARE   ONE,IBCNMHOS
          GOTO      WRIT1100 IF NOT EQUAL
.
          PACK      KEY8,PTESADMN                * C-152842
          CALL      RDPMVX11                     * Read a visit extension record
          BRANCH    OVRCD,WRIT1000
.
          IF        OPTION = 1              
            MATCH     PMVXMHOS,PTHSHOSP
            GOTO      WRIT1000 IF NOT EQUAL
          ENDIF
.
WRIT1100  COMPARE   ZERO,WRTCT
          GOTO      WRIT1200 IF NOT EQUAL
.
          CALL      PREP0000
          BRANCH    EXIT,WRIT1000
.
          GOTO      WRIT1300
.
WRIT1200  MATCH     PTESHFND,SAVHFUND
          GOTO      WRIT1300 IF EQUAL
.
          MOVE      PTESHFND,SAVHFUND          * save new health fund
.
.     Write tail details for old health fund
          CALL      WADR0000
.
.     Create new extract file for different health fund
          CALL      PREP0000
          BRANCH    EXIT,WRIT1000
.
.         Keep a count of the number of patients (U/R's) processed in the batch
.         using the temp file.
.
WRIT1300  PACK      KEY8,PTESURNO
          CALL      RDTEMP1
          IF        OVRCD = 1
            MOVE      PTESURNO,TMPURNO
            CALL      WRTEMP1
            ADD       ONE,URCNT             * yes, add one to counter
          ENDIF
.
          MOVE      PTESHFND,SAVHFUND
          MOVE      PTESINVN,INVNO
          MOVE      PTESADMN,ADMINO
.
          ADD       ONE,WRTCT
          IF        WRTCT%1000=1
            DISPLAY   *P11:24,*V2LON,INVNO,SP2,ADMINO;
          ENDIF
.
          PACK      KEY8,INVNO
          CALL      RDINV1                  * Read an invoice file record
          BRANCH    OVRCD,WRIT1000
.
          MATCH     ZEROVISN,PINVADM
          GOTO      WRIT1000 IF EQUAL
.
          MATCH     SAVADMIN,ADMINO
          GOTO      WRIT2000 IF EQUAL       * Same admin no, dont get details
.
          PACK      KEY8,ADMINO
          CALL      RDMISS1                 * Read an admin file record
          BRANCH    OVRCD,WRIT1000
.
          CALL      CLPATDSC                * Clear disch file variables
          IF        ASTAT = 3
            PACK      KEY8,ADMINO
            CALL      RDDSCH1               * Read a disch file record
            BRANCH    OVRCD,WRIT1000
          ENDIF
.
          PACK      KEY8,PINVUR
          CALL      RDMAST1                 * Read a master file record
          BRANCH    OVRCD,WRIT1000
.
          MOVE      ADATE,INVFRDTE          * Invoice from date
          MOVE      ZERO,INVRECCT           * Clear the invoice record counter
          MOVE      ADMINO,SAVADMIN         * Save the admin no
.
WRIT2000  MOVE      PINVDTE,INVTODTE        * Default to date to invoice date
          MATCH     SP8,DDATE
          GOTO      WRIT3000 IF EQUAL       * Patient not discharged
.
          MATCH     DDATE,PINVDTE
          GOTO      WRIT3000 IF LESS        * Invoice date < disch date
.
          MOVE      DDATE,INVTODTE          * Default to date to disch date
.
WRIT3000  MOVE      ZERO,INVAMTCT           * Clear the invoice amount counter
          ADD       ONE,INVRECCT            * Increm the invoice record counter
          ADD       ONE,TOTRECCT            * Increment the total record counter
.
          MOVE      INVFRDTE,FROMDATE
          MOVE      INVTODTE,TODATE
          MOVE      ONE,NHOSDAYC            * set sameday to return LOS = 1
          CALL      NHOSPDAY                * Calculate no of days in hospital
          MOVE      NBRDAYS,NOINVDYS
          REP       " 0",NOINVDYS           * No of invoice days
.
          READ      CONTROLF,TWENTY;*193,PTCNDCLM
          CALL      DCLM0000                * get default claim code
          CALL      WRCID000                * Write claim id details
          CALL      WRPAT000                * Write patient details
          CALL      WREPD000                * Write episode details
          CALL      WRSVB000                * Write bungle/single value details
          CALL      WRANB000                * Write newborn baby details
          CALL      WRTFR000                * Write transfer details
          CALL      WRACS000                * Write accom summary details
          CALL      WRACD000                * Write accom details
          CALL      WRCCG000                * Write critical care details
          IF        LEAVDAY <> 0
            CALL      WRLPD000
          ENDIF
          CALL      WRPSG000                * Write principal services details
          CALL      WRMIG000                * Write misc services details
          CALL      WRDMG000                * Write DRG morbidity details
          CALL      WRCER000                * Write certificate details
.
          ADD       INVAMTCT,TOTAMTCT       * Increment the total amount counter
          MOVE      INVTODTE,INVFRDTE       * Invoice from date
.
WRIT4000  CALL      PROC0000                * Check/update patedsaf records
.
          GOTO      WRIT0500
.
WRIT8000  MOVE      PTESHFND,SAVHFUND       * save new health fund
          IF        WRTCT <> 0
            CALL      WADR0000              * write trailer details for last HF
          ENDIF
.
          BRANCH    OPTION,WRIT8500
          GOTO      WRIT9000
.
WRIT8500  DISPLAY   *P1:20,*EF,"Total records read      : ",*V2LON,REDCT,*HOFF:
                    *P1:21,"Total records extracted : ",*V2LON,WRTCT,*HOFF:
                    *P1:24,*HOFF,*B,"Extract finished.  ";
          CALL      HITENTER
.
WRIT9000  MOVE      ZERO,EXIT
.
WRIT9999  RETURN
+
.******************************************************************************
.*                                  PROC0000              Called by: WRIT0000 *
.*    Check if re-submission, Update records in patedsaf accordingly          *
.******************************************************************************
PROC0000  MOVE      SP10,SAVPRVBN
          BRANCH    OPTION,PROC1000
.
.--- Re-extraction, just update flag
.         
          MOVE      TWO,PTESFLAG
          MOVE      TOTRECCT,PTESCSNO       * save Claim Sequential No.
          MOVE      ED040005,PTESCCFL       * save contiguous claim flag
          CALL      UPPTEDS1
.
          GOTO      PROC9999
. 
PROC1000  PACK      KEY32,SP1,ZERO,PTESHFND,PTESADMN,PTESINVN,SP8
          CALL      RAPTEDS1
          IF        OVRCD <> 1
            MOVE      SAVBTHN,PTESBATN
            MOVE      TWO,PTESFLAG 
            MOVE      TOTRECCT,PTESCSNO     * save Claim Sequential No.
            MOVE      ED040005,PTESCCFL     * save contiguous claim flag
            CALL      UPPTEDS1              * update record as sent  
          ENDIF
.
          MATCH     SP8,PTESPBAT
          GOTO      PROC9999 IF EQUAL       * Not a resubmission
. 
          MOVE      PTESPBAT,SAVPRVBN       * Save previous batch number
.
.         Use index 3 to get the previous batch record for update (CAR 153566)
.
          PACK      KEY24,PTESPBAT,PTESADMN,PTESINVN
          CALL      RDPTEDS3
          BRANCH    OVRCD,PROC9999
.
.         Now position on the same record using index 1 (as there is no
.         UPPTEDS3), before updating
.
          PACK      KEY32,PTESFLAG,PTESHFND,PTESADMN,PTESINVN,PTESBATN
          CALL      RDPTEDS1
          BRANCH    OVRCD,PROC9999
.
          MOVE      SAVBTHN,PTESNBAT
          CALL      UPPTEDS1
.
PROC9999  RETURN
+
.******************************************************************************
.*                                  PREP0000              Called by: WRIT0000 *
.*      Prep extract files and reset variables for different health funds     *
.******************************************************************************
PREP0000  BRANCH    VALDFLG,PREP99999
.
          CALL      CLER0000                    * Clear tempfile
.
          BRANCH    OPTION,PREP1000
.
          GOTO      PREP2000
.
PREP1000  MOVE      " 88",PRXCODE               * System Lock Sector 88     
          CALL      GETSLK00                    * Get System Lock-Sector 88 
.
          READ      CONTROLF,NINTY8;*157,PTCNBTHN
          UNPACK    PTCNBTHN,DIM2,BATCHNO
.  
          MOVE      BATCHNO,FORM6A
.
          COMPARE   "999999",FORM6A 
          IF        @EQUAL
            MOVE      ZERO,PTCNBTHN       * Initialise the batch number
          ENDIF
.
          ADD       ONE,PTCNBTHN          * Increment next batch number
          WRITAB    CONTROLF,NINTY8;*157,PTCNBTHN
.
          CALL      RELSLK00                    * Release System Lock-Sector 88
.
          MOVE      FORM6A,SAVBTHN
          MOVE      FORM6A,BATCHNO        * new batch number
.
PREP2000  REP       " 0",BATCHNO
          PACK      FILENAME,PTCNEDIH,BATCHNO    * use default file prefix
.
          COMPARE   ONE,IBCNMHOS                 * check multi hospital
          GOTO      PREP3000 IF NOT EQUAL
.                                                * get mult.hosp. Hospital code
          MATCH     SP2,PTHSEDIP
          IF        @EQUAL
            PRINT     *1,"|",*19,"|Hospital Code ",PTHSHOSP," - ",PTHSNAME:
                      *N,*1,"|",*19,"| does not have an EDI File prefix"
            GOTO      PREP3000
          ENDIF
          PACK      FILENAME,PTHSEDIP,BATCHNO
.
PREP3000  PACK      EXTFNAME,FILENAME,TXT
          PREP      EXTFILE1,EXTFNAME
          PACK      ADRFNAME,FILENAME,ADR
          PREP      ADRFILE1,ADRFNAME
.
          DISPLAY   *P27:23,*V2LON,FILENAME,*HOFF
.
          ADD       ONE,COUNT
          IF        OPTION=1
            MOVE      SAVBTHN,PTEHBTHN
            PACK      PTEHHFGP,PTESHFND,SP10
            MOVE      SP8,PTEHSTDT
            MOVE      SP8,PTEHEDDT
            MOVE      ZERO,PTEHBHTL
            MOVE      RECARRY[COUNT],PTEHTRIB
            CALL      IBACLOCK
            PACK      PTEHDTBC,CCC,CYY,CMM,CDD
            REP       " 0",PTEHDTBC
            MOVE      CTIMEIS,PTEHTMBC
            MOVE      PASSCODE,PTEHOPER
            MOVE      ZERO,PTEHEETP
            MOVE      FILENAME,PTEHEFNM
            MOVE      ZERO,PTEHFLAG
            PACK      PTEHSPAR,SP30,SP10
            CALL      WRPTEBH1
          ENDIF
.
          PACK      ARRAY[COUNT],FILENAME
          MOVE      ZERO,URCNT
          MOVE      SP30,SAVURNO
          MOVE      ZERO,TOTAMTCT           * Clear the total amount counter
          MOVE      ZERO,TOTRECCT           * Clear the total record counter
          UNPACK    SP30,DIM2,DIM2A,DIM3,DIM1,DIM4
.
          PACK      KEY6,PTESHFND
          CALL      RDPTFX11
          BRANCH    OVRCD,PREP8000          * health fund missing
.
          CALL      WRHDR000                * Write header details
          CALL      WRPRO000                * Write provider details
          CALL      WRINS000                * Write insurer details
          GOTO      PREP9000
.
PREP8000  DISPLAY   *P1:24,*EL,"Health fund ",PTESHFND,"not found. No records":
                               " will be extracted. ";
          CALL      HITENTER
.
          DISPLAY   *P1:24,*EL,"Writing : ";
          PACK      KEY32,SP1,ZERO,PTESHFND,TILDA30
          CALL      RSPTEDS1
          MOVE      ONE,EXIT
          GOTO      PREP9999
.
PREP9000  MOVE      ZERO,EXIT
PREP9999  RETURN
+
.******************************************************************************
.*                                  WADR0000              Called by: WRIT0000 *
.*       Write ADR record details and trailer for individual health funds     *
.******************************************************************************
WADR0000  CALL      WRTLR000              * Write trailer details 
.
          WRITE     ADRFILE1,SEQ;ADRLN1,FILENAME,RET
.
.         Set up to write the Recipient address
.
          MOVE      PTFXADDR,DIM60
          STRIP     DIM60
          MOVELPTR  DIM60,CHARPOSN
.
          WRITE     ADRFILE1,SEQ;ADRLN2;
          MOVE      ZERO,COUNTR
          WHILE     COUNTR < CHARPOSN
            ADD       ONE,COUNTR
            CMOVE     DIM60,ANS
            IF        COUNTR = CHARPOSN
              WRITE     ADRFILE1,SEQ;ANS,RET
            ELSE
              WRITE     ADRFILE1,SEQ;ANS;
            ENDIF
            BUMP      DIM60
          DO
.
.         Set up to write the Originator
.
          MOVE      PTCNORIG,DIM60
          STRIP     DIM60
          MOVELPTR  DIM60,CHARPOSN
.
          WRITE     ADRFILE1,SEQ;ADRLN3;
          MOVE      ZERO,COUNTR
          WHILE     COUNTR < CHARPOSN
            ADD       ONE,COUNTR
            CMOVE     DIM60,ANS
            IF        COUNTR = CHARPOSN
              WRITE     ADRFILE1,SEQ;ANS,RET
            ELSE
              WRITE     ADRFILE1,SEQ;ANS;
            ENDIF
            BUMP      DIM60
          DO
.
.         Set up to write the Subject
.
          MOVE      PTFXSUBJ,DIM60
          STRIP     DIM60
          MOVELPTR  DIM60,CHARPOSN
.
          WRITE     ADRFILE1,SEQ;ADRLN4;
          MOVE      ZERO,COUNTR
          WHILE     COUNTR < CHARPOSN
            ADD       ONE,COUNTR
            CMOVE     DIM60,ANS
            IF        COUNTR = CHARPOSN
              WRITE     ADRFILE1,SEQ;ANS,RET
            ELSE
              WRITE     ADRFILE1,SEQ;ANS;
            ENDIF
            BUMP      DIM60
          DO
.
          WRITE     ADRFILE1,SEQ;ADRLN5,FILENAME,TXT,RET
          WRITE     ADRFILE1,SEQ;ADRLN6,HCLM,RET
          WRITE     ADRFILE1,SEQ;ADRLN7,URCNT,RET
.
          IF        OPTION = 1
            MOVE      SAVBTHN,KEY8
            CALL      RDPTEBH1
            IF        OVRCD = 1
              DISPLAY   *P1:24,"Error writing patebhaf record."
              GOTO      WADR1000
            ENDIF
.
            MOVE      TOTAMTCT,PTEHBHTL          * update batch total
            CALL      UPPTEBH1
          ENDIF
.
... Move extract files and ADR files to output path
.
WADR1000  MOVE      ZERO,HLEF
          CALL      GETSCR00
          STRIP     PTCNEDIO
          CLEAR     CMDLINE
          APPEND    "mv ",CMDLINE
          APPEND    FILENAME,CMDLINE
          APPEND    ".txt ",CMDLINE
          APPEND    PTCNEDIO,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CLEAR     CMDLINE
          APPEND    "mv ",CMDLINE
          APPEND    FILENAME,CMDLINE
          APPEND    ".ADR ",CMDLINE
          APPEND    PTCNEDIO,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          CALL      PUTSCR00
.
WADR9999  RETURN
+
.******************************************************************************
.*                                  TAIL0000              Called by: ML0000   *
.*                              Print Report Tail                             *
.******************************************************************************
TAIL0000  CALL      UND80
          PRINT     *N,*1,"Total Records Read      : ",REDCT:
                    *N,*1,"Total Number of Errors  : ",ERRCT:
                    *N,*1,"Total Number of Warnings: ",WARCT:
                    *N,*N,*1,"Total Records Extracted : ",WRTCT
.
          IF        REDCT =0
            PRINT     *N,*1,"No Extract file created"
            GOTO      TAIL2000
          ENDIF
          BRANCH    VALDFLG,TAIL9000
.
TAIL1000  PRINT     *N,*1,"Extract filename(s) created : "
          MOVE      ZERO,COUNT
TAIL1500  ADD       ONE,COUNT
          MATCH     SP8,ARRAY[COUNT]
          GOTO      TAIL2000 IF EQUAL
.
          PRINT     *1,ARRAY[COUNT]
          GOTO      TAIL1500
.
TAIL2000  PRINT     *N,*N,"*** End of Report ***"
.
          PACK      KEY32,SP1,ONE,SP30,SP10
          CALL      RSPTEDS1
TAIL2500  CALL      RKPTEDS1
          BRANCH    OVRCD,TAIL9000
.
          COMPARE   ONE,PTESFLAG
          GOTO      TAIL9000 IF NOT EQUAL
.
          MOVE      ZERO,PTESFLAG
          CALL      UPPTEDS1
          GOTO      TAIL2500
.
TAIL9000  MOVE      ZERO,EXIT
TAIL9999  RETURN
+
.*****************************************************************************
.*                            PBAT0000                                       *
.*             Get the claim details for the previous batch                  *
.* Requires: KEY8 - batch number for read on patebhaf (index 1)              *
.*           Current read on relevant patedsaf record                        *
.*****************************************************************************
.
PBAT0000    CALL      RDPTEBH1
            IF        OVRCD = 0
              UNPACK    SP30,DIM2,DIM2A,DIM3,DIM1
              UNPACK    PTEHTMBC,DIM2,DIM1,DIM2A,DIM3
              PACK      ED040007,DIM2,DIM2A
              REP       " 0",ED040007
.
              UNPACK    SP30,DIM2,DIM2A,DIM3,DIM1
              UNPACK    PTEHDTBC,DIM4,DIM2,DIM2A
              PACK      ED040008,DIM2A,DIM2,DIM4
              REP       " 0",ED040008
.
              UNPACK    PTEHBTHN,KEY2,KEY6       * C-155764
              MOVE      KEY6,ED040009
              REP       " 0",ED040009
              MOVE      PTESPCSN,ED040010
              REP       " 0",ED040010
            ENDIF
.
PBAT9999  RETURN
+
.*************************************************************************
.*                             CTMP0000            Called by:            *
.*                      create temporary file                            *
.*************************************************************************
CTMP0000  CALL      KILL0000
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    TMPFILNM,CMDLINE
          APPEND    " 9 U1-8",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPFILE,TMPFILNM
.
CTMP9999  RETURN
+
.*************************************************************************
.*                             KILL0000             Called by:           *
.*                   erase temporary file                                *
.*************************************************************************
KILL0000  CLOSE     TEMPFILE
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    TMPFILNM,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
KILL9999  RETURN
+
.*************************************************************************
.*                             CLER0000            Called by:            *
.*               close and erase the temporary file                      *
.*************************************************************************
.
CLER0000  PACK      KEY8,SP8
          CALL      RSTEMP1
          CALL      RKTEMP1
          BRANCH    OVRCD,CLER9999
.
          PACK      KEY8,TMPURNO
          CALL      DETEMP1
.
	  GOTO      CLER0000
.
CLER9999  RETURN
+
.*****************************************************************************
.*                              RERR0000           Called by:                *
.*             Reset error records back to zero status (Not Sent)            *
.*             just in case there were any problems with a previous          *
.*             batch and the error records were not set back to zero.        *
.*****************************************************************************
.
RERR0000  PACK      KEY32,SP1,ONE,SP30
          CALL      RSPTEDS1                     * position in file
          CALL      RKPTEDS1                     * read next record
          BRANCH    OVRCD,RERR9999               * eof - finished
.
          COMPARE   ONE,PTESFLAG                 * error record still ?
          GOTO      RERR9999 IF NOT EQUAL        * no -finished
.
          MOVE      ZERO,PTESFLAG                * yes - reset flag to "0"
          CALL      UPPTEDS1                     * update record
          GOTO      RERR0000                     * get next record
.
RERR9999  RETURN
+
.******************************************************************************
.*                                  KHOS0000              Called by: ML0000   *
.*                        Enter selected Hospital code                        *
.******************************************************************************
KHOS0000  COMPARE   ONE,IBCNMHOS
          GOTO      KHOS9999 IF NOT EQUAL
.
          MOVE      "10",EVERT
          DISPLAY   *P1:EVERT,*EL,"Hospital Id   : ";
          MOVE      TEN7,ECOL
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
.
          OPEN      PATHSPA1,"pathspaf"
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS3000,KHOS9500
.
KHOS2000  DISPLAY   *P25:EVERT,*V2LON,PTHSNAME;
          GOTO      KHOS9999
.
KHOS3000  DISPLAY   *P1:24,"Hospital Code not entered for Multi Hospital ":
                           "environment ",*W1;
          PRINT     *1,"Hospital Code not entered for Multi Hospital ":
                       "environment"
          MOVE      SP20,PTHSNAME                                     *I-125462
          MOVE      SP10,PTHSHOSP
.
KHOS9500  MOVE      ONE,EXIT
KHOS9999  RETURN
+
.******************************************************************************
.*                                  KHFND000              Called by: ML0000   *
.*                          Enter selected Health Fund                        *
.******************************************************************************
KHFND000  MOVE      ZERO,EXIT
          MOVE      ZERO,SELHFUND
          UNPACK    SP30,HFUNDCOD,HFUNDNAM
.
          MOVE      "17",ECOL
          MOVE      "12",EVERT
          MOVE      SP6,CKYIDEF6
          DISPLAY   *P1:EVERT,*EL,"Health Fund   : ";
          CALL      PATFNDKY
          BRANCH    EXIT,KHFND100,KHFND800,KHFND100
.
          MOVE      HCODE,HFUNDCOD
          MOVE      HFNAME,HFUNDNAM
          MOVE      ONE,SELHFUND
          GOTO      KHFND900
.
KHFND100  MOVE      "All Funds",HFUNDNAM
          GOTO      KHFND900
.
.
KHFND800  PRINT     *1,"Exitchar used for Health Fund ";
          MOVE      ONE,EXIT
          GOTO      KHFND999
.
KHFND900  DISPLAY   *PECOL:EVERT,*EL,*V2LON,HFUNDCOD,SP2,HFUNDNAM;
          MOVE      ZERO,EXIT
.
KHFND999  RETURN
+
.==============================================================================
.                Tempfile I/O
.==============================================================================
RSTEMP1   RESET     KEY8
          READ      TEMPFILE,KEY8;;
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMPFILE;TMPURNO
          GOTO      OVERCOND IF OVER
          RETURN
.
RPTEMP1   MOVE      ZERO,OVRCD
          READKP    TEMPFILE;TMPURNO
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY8 
          READ      TEMPFILE,KEY8;TMPURNO
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   RESET     KEY8
          WRITE     TEMPFILE,KEY8;TMPURNO
          RETURN
.
DETEMP1   RESET     KEY8
          DELETE    TEMPFILE,KEY8
          RETURN
+
.==============================================================================
.
          INC       STD001IO
.
          INC       CALCAGE                 * Calculate patients age
          INC       CALCDAYS                * Calculate diff between 2 dates
          INC       CLPATDSC                * Clear the disch variables
          INC       GETDRG                  * Get DRG & MDC codes
          INC       CHKCMXPT                * Check casemix funding
          INC       HSCLMSRV                * check claim statuses
          INC       NCGSEGMT                * format NCG fields
          INC       NHOSPDAY                * Calculate no of days in hospital
          INC       PATCODKY                * Keyin a codes file code
          INC       PATDEFCL                * routine DCLM0000
          INC       PATFNDKY                * Keyin a health fund
          INC       PATHSPKY                * Keyin a hospital code
          INC       PATGILOS                * Get intended LOS
          INC       PATM10T9                * Map ICD10 to ICD9 codes
          INC       PATMCHRD                * Miscellaneous Charges read routine
          INC       VALCONTR
          INC       VALCMXFN
          INC       GETCNEFF
          INC       TFILENAM 
          INC       GETEFDRG
          INC       PATITMRD
.
          INC       OPRDEAIO/INC            * Operation detail file
          INC       PAT3MIIO/INC            * ICD9 mapping file
          INC       PATCERIO/INC            * certificates
          INC       PATCLCIO/INC            * certificate line comments
          INC       PMSHCPIO/INC            * Health Care Provider
          INC       PATCMPIO/INC            * Complex mapping file
          INC       PATCODIO/INC            * Codes file
          INC       PATCHCIO/INC            * Code changes file
          INC       PATCMXIO/INC            * Casemix Funding file
          INC       PMSCIDIO/INC
          INC       PATCFAIO/INC
          INC       PATDSCIO/INC            * Disch file
          INC       PATDTRIO/INC            * DTR file
          INC       PATDGWIO/INC            * DTR file
          INC       PATEBHIO/INC            * EDI batch header file
          INC       PATECDIO/INC            * Patient episode disease file
          INC       PATECOIO/INC            * Patient episode operation file
          INC       PATFN1IO/INC            * Health fund file
          INC       PATFX1IO/INC            * Health fund extention
          INC       PATHDFIO/INC
          INC       PATHGPIO/INC            * Health fund group file
          INC       PATHSPIO/INC            * Hospital file
          INC       PATICDIO/INC            * ICD disease file
          INC       PATICOIO/INC            * ICD operation file
          INC       PATICUIO/INC            * ICU file
          INC       PATINVIO/INC            * Invoice file
          INC       PATITMIO/INC            * Item file
          INC       PATLINIO/INC            * U/R Link file
          INC       PATMA1IO/INC            * Master file
          INC       PATMCHIO/INC            * Miscellaneous charges file
          INC       PATMI1IO/INC            * Admin file
          INC       PMSVX1IO/INC            * Visit ext file
          INC       PATMMBIO/INC            * Patient MBS file
          INC       PATPGRIO/INC            * Patient grouper file
          INC       PATTRNIO/INC            * Transfer file
          INC       PATVADIO/INC            * Transfer source file
          INC       PATDADIO/INC            * Patient transfer destination file
          INC       PATWC1IO/INC            * WCO file
          INC       PATWMAIO/INC            * TAC file
          INC       PATWVEIO/INC            * DVA file
          INC       PATEDSIO/INC            * Patient EDI Summmary Table
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC
          INC       PMSEDGIO/INC       * Effective DRG Dates
+


.----------------------------------------------------------------------
.   Program       : GENERMKI
.   Modifications : 
.
.        V11.05.01 25/02/2025  Don Nguyen   TSK 0952080
.                  Recompiled for PATMKIPR
.        V11.02.01 07.10.2022  DA Sarkies   TSK 0909756
.                  Recompiled for PATMKIPR
.        V10.03.01 09/03/2012  J.Tan        CAR 259879
.                  Mods checking for Active/Inactive NZ Misc.charge
.        V10.01.01 01/12/2010  Mike Lmaing  CAR 233046
.                  Mods to change "Table" to Table Type for Misc.charge key
.        V10.00.01 30/04/2010 J.Tan         CAR 220887
.                  Mods checking for Active/Inactive of Misc.charge
.        V9.09.01  19/06/2007 Jill Habasque CAR 142154
.                  Recompiled for PATMKIPR
.        V9.08.02  21/11/2006 Jill Habasque CAR 120007
.                  Added nz private misc generation  
.        V9.08.01  14/11/2006 Ebon Clements CAR 120007
.                  Added nz private misc charges file
.        V9.04.02  22/09/2004 Lina Vo      CAR 54423
.                  Added web locking and un-locking to program
.        V9.04.01  27/09/2004 Linav Vo      CAR 53798
.                  Changed program to use PTHSDCLM instead of PTCNDCLM
.                  for Multi Hospital. 
.                  Changed to only write blank heath fund misc charges
.        V9.03.02  09/09/2003 Linav Vo      CAR 40497
.                  Changed size of index in patmchgf.  
.                  Changed program to write the first misc charge record to
.                  default misc charge keyword file disregarding effective date.
.        V9.03.01  07/08/2003 Tracey Nguyen CAR 41899
.                  Clears out all keywords records for the previous 
.                  default claim code, then rebuilds the keywords file 
.                  with the current default claim code. 
.
.----------------------------------------------------------------------
.
.----------------------------------------------------------------------
.  data includes
.----------------------------------------------------------------------
.
          INC       STDGENDF
          INC       IBACONFD/INC
          INC       NZPMCHFD/INC
          INC       PATCONFD/INC
          INC       PATHSPFD/INC
          INC       PATMCHFD/INC
          INC       PATMKIFD/INC
.
.----------------------------------------------------------------------
.  local variables
.----------------------------------------------------------------------
.
CURRDATE  DIM       8
DIM21     DIM       21                                                *I-233046
DIM26     DIM       26
DIM37     DIM       37
SAVKEY3   DIM       3
SAVKEY37  DIM       37
.
PRGID     INIT      "GENERMKI"
PRGNAM    INIT      "Generate patmkiaf"
VERSION   INIT      "V12.01.00"
.
.----------------------------------------------------------------------
.  program code
.----------------------------------------------------------------------
.
.---- initialise screen and confirm desire to execute program ----
.
ML0000    CALL      DISPHEAD
.
.---- open files ----
.
          OPEN      PATHSPA1,"pathspaf"
          OPEN      PATMCHG1,"patmchgf"
          OPEN      NZPMCHA1,"nzpmchaf"
          OPEN      PATMKIA1,"patmkiaf"
          OPEN      PATMKIA2,"patmkiaf"
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TEN9;*212,CFEETYP
          READ      CONTROLF,TWENTY;*193,PTCNDCLM
          MOVE      ZERO,SECTOR
.
.         Check if another terminal is using program
.
          MOVE      ZERO,EXIT
          READ      CONTROLF,SEVENTY9;*15,PTCNMCHM
.
.       check if this program is run by IBARSH (web program)
.
          MATCH     "IBARSH",PGM
          GOTO      ML0100 IF EQUAL
.
          PACK      DIM2,ZERO,ZERO
          MATCH     DIM2,PTCNMCHM
          GOTO      ML0100 IF EQUAL
.
          MATCH     PORT,PTCNMCHM
          GOTO      ML0100 IF EQUAL
.
          CALL      PRGLOCK
          GOTO      ML9999
.
ML0100    MOVE      PORT,PTCNMCHM
          WRITAB    CONTROLF,SEVENTY9;*15,PTCNMCHM
.
          DISPLAY   *P1:10,"This file generates Miscellaneous Keyword records."
          CALL      CONTQST
          SUB       ONE,CEXIT
          BRANCH    CEXIT,ML9999,ML9999
.
.
.  Clears out all keywords records for the previous default claim code
.
ML0500    PACK     KEY56,SP70                     
          CALL     RSPTMKI1                       
          CALL     RKPTMKI1                      
          BRANCH   OVRCD,ML0750             

          ADD       ONE,SECTOR              * display every 1000th record
          IF        SECTOR%1000=1
            DISPLAY   *P1:24,*V2LON,"Deleting.. ",SECTOR," - ",PTMKCODE,*EF;
          ENDIF
.
          PACK     KEY56,PTMKCODE,PTMKKKWD,SP70
          CALL     DEPTMKI1
          GOTO     ML0500
.
.  Rebuilds the keywords file with the current default claim code
.
ML0750    COMPARE   ONE,IBCNMHOS
          GOTO      ML2000 IF EQUAL
.
          IF        CFEETYP=5
            CALL      ADEX0000
            GOTO      ML9000
          ENDIF
.
          PACK      DIM26,SP70
          MOVE      ZERO,SECTOR
          PACK      KEY29,PTCNDCLM,SP70                               *C-233046
          CALL      RDSMCHG1                * Position read
ML1000    CALL      RDKMCHG1                * Read next record
          BRANCH    OVRCD,ML9000            * At end exit
.
          MATCH     PTCNDCLM,MCLMCOD        * Same claim code to default?
          GOTO      ML9000 IF NOT EQUAL     * No, then exit
.
          MATCH     SP10,MHFUND             * blank health fund          
          GOTO      ML9000 IF NOT EQUAL     * No, then exit
.
          MATCH     SP10,PTMCHFTT           * blank health Table Type *C-233046
          GOTO      ML9000 IF NOT EQUAL     * No, then exit
.
.         Check for Active item
.
          MATCH     "1",PTMCACTV            * Inactive?
          GOTO      ML1000 IF EQUAL         * skip
.
.         Only write one miscellaneous charge record and disregard effective
.         date.
.
....      PACK      KEY26,MCLMCOD,MHFUND,MHFTAB,MCHARGE,SP70
....      MATCH     DIM26,KEY26        * Check if same misc record
          PACK      KEY21,MCLMCOD,MHFUND,PTMCHFTT,MCHARGE,SP70        *C-233046
          MATCH     DIM21,KEY21        * Check if same misc record
          GOTO      ML1000 IF EQUAL
          MOVE      KEY21,DIM21
.
          ADD       ONE,SECTOR              * display every 100th record
          IF        SECTOR%100=1
            DISPLAY   *P1:24,*V2LON,SECTOR," - ",MCHARGE,*EF;
          ENDIF

          PROC      PATMKIUP                * Write to keywords file
          GOTO      ML1000                  * Loop back to read next record
.
.         Rebuild the keywords file for each multi hospital default claim code
.
ML2000    MOVE      ZERO,SECTOR
.
          PACK      KEY3,SP10
          CALL      RSPTHSP1
ML2100    CALL      RKPTHSP1
          BRANCH    OVRCD,ML8000
.
          PACK      SAVKEY3,PTHSHOSP,SP70
          MATCH     SP10,PTHSDCLM
          GOTO      ML2100 IF EQUAL
          PACK      PTCNDCLM,PTHSDCLM,SP70
.
.         Check if default claim code already written
.
          PACK     KEY56,PTHSDCLM,SP70                     
          CALL     RSPTMKI1                       
          CALL     RKPTMKI1                      
          IF       OVRCD=0             
            UNPACK   PTMKCODE,KEY3,KEY23
            MATCH    KEY3,PTHSDCLM           
            GOTO     ML2100 IF EQUAL        
          ENDIF
.
          IF        CFEETYP=5
            CALL      NZMKI000
            PACK      KEY3,SAVKEY3,SP70
            CALL      RDPTHSP1
            GOTO      ML2100
          ENDIF
.
          PACK      DIM26,SP70
          PACK      KEY29,PTHSDCLM,SP70                               *C-233046
          CALL      RDSMCHG1                * Position read
ML2200    CALL      RDKMCHG1                * Read next record
          BRANCH    OVRCD,ML2100            * At end exit
.
          MATCH     PTHSDCLM,MCLMCOD        * Same claim code to default?
          GOTO      ML2100 IF NOT EQUAL     * No, then exit
.
          MATCH     SP10,MHFUND             * blank health fund          
          GOTO      ML2100 IF NOT EQUAL     * No, then exit
.
....      MATCH     SP10,MHFTAB             * blank health fund          
          MATCH     SP10,PTMCHFTT           * blank h/f table type    *C-233046
          GOTO      ML2100 IF NOT EQUAL     * No, then exit
.
.         Check for Active item
.
          MATCH     "1",PTMCACTV            * Inactive?
          GOTO      ML2200 IF EQUAL         * skip
.
.         Only write one miscellaneous charge record and disregard effective
.         date.
.
          PACK      KEY21,MCLMCOD,MHFUND,PTMCHFTT,MCHARGE,SP70        *C-233046
          MATCH     DIM21,KEY21        * Check if same misc record
          GOTO      ML2200 IF EQUAL
          MOVE      KEY21,DIM21
.
          ADD       ONE,SECTOR              * display every 100th record
          IF        SECTOR%100=1
            DISPLAY   *P1:24,*V2LON,SECTOR," - ",KEY34,*EF;
          ENDIF
.
          PROC      PATMKIUP                * Write to keywords file
          GOTO      ML2200                  * Loop back to read next record
.
ML8000    CALL      ADEX0000  * Add extra records for hospital/contract specific
.
.---- exit program ----
ML9000    PACK      PTCNMCHM,ZERO,ZERO
          WRITAB    CONTROLF,SEVENTY9;*15,PTCNMCHM
.
          MATCH     "IBARSH",PGM
          IF        @EQUAL
            MOVE      SP10,PTCNWMCH
            WRITAB    CONTROLF,HUND10;*31,PTCNWMCH
          ENDIF
.
ML9999    CHAIN     PGM
+
NZMKI000  MOVE      ZERO,SECTOR
          PACK      KEY37,SP3,PTCNDCLM,SP70
          CALL      RSNZMCH1                * Position read
NZMKI100  CALL      RKNZMCH1                * Read next record
          BRANCH    OVRCD,NZMKI900          * At end exit
.
          MATCH     PTCNDCLM,NZMCCLMC       * Same claim code to default?
          GOTO      NZMKI900 IF NOT EQUAL   * No, then exit
.
          MATCH     SP10,NZMCCNTR           * blank health fund
          GOTO      NZMKI900 IF NOT EQUAL   * No, then exit
.
          MATCH     SP10,NZMCFTAB           * blank health fund
          GOTO      NZMKI900 IF NOT EQUAL   * No, then exit
.
.         Check for Active item
.
          MATCH     "1",NZMCACFL            * Inactive?
          GOTO      NZMKI100 IF EQUAL       * skip
.
.         Only write one miscellaneous charge record and disregard effective
.         date.
.
          PACK      KEY26,NZMCCLMC,NZMCCNTR,NZMCFTAB,NZMCMCHG,SP70
          MATCH     DIM26,KEY26        * Check if same misc record
          GOTO      NZMKI100 IF EQUAL
          MOVE      KEY26,DIM26
.
          ADD       ONE,SECTOR              * display every 100th record
          IF        SECTOR%100=1
            DISPLAY   *P1:24,*V2LON,SECTOR," - ",MCHARGE,*EF;
          ENDIF

          PROC      PATMKIUP                * Write to keywords file
          IF        IBCNMHOS=1
            PACK      SAVKEY3,PTHSHOSP,SP70
            CALL      RDPTHSP1
            MATCH     SP10,PTHSDCLM
            IF        !@EQUAL
              PACK      PTCNDCLM,PTHSDCLM
            ENDIF
          ENDIF
          GOTO      NZMKI100                * Loop back to read next record
.
NZMKI900
.
NZMKI999  RETURN
+
.----------------------------------------------------------------------
.  Loop through entire nzpmchaf and check all charge codes exist on patmkiaf
.----------------------------------------------------------------------
ADEX0000  PACK      KEY37,SP70
          CALL      RSNZMCH1
ADEX1000  CALL      RKNZMCH1
          BRANCH    OVRCD,ADEX9999
          PACK      SAVKEY37,NZMCHOSP,NZMCCLMC,NZMCCNTR,NZMCFTAB: * Validate Rec
                            NZMCMCHG,NZMCEFDT,SP70
.
.         Check for Active item
.
          MATCH     "1",NZMCACFL            * Inactive?
          GOTO      ADEX1000 IF EQUAL       * skip
.
          ADD       ONE,SECTOR              * display every 100th record
          IF        SECTOR%100=1
            DISPLAY   *P1:24,*V2LON,SECTOR," - ",MCHARGE,*EF;
          ENDIF
.
          PROC      PATMKIUP                * Write to keywords file
          PACK      KEY37,SAVKEY37,SP70
          CALL      RDNZMCH1
          GOTO      ADEX1000
.
ADEX9999  RETURN
.
.----------------------------------------------------------------------
.  code includes
.----------------------------------------------------------------------
.
          INC       NZPMCHIO/INC
          INC       PATHSPIO/INC
          INC       PATMCHIO/INC
          INC       PATMKIIO/INC
          INC       PATMKIPR
          INC       STDGENCD
.

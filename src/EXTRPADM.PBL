.*****************************************************************************
.* System    :   Patient Management System                                   *
.* Program   :   EXTRPADM                                                    *
.* Desc      :   Pre-admission Data Extraction Program                       *
.*****************************************************************************
.* Date      :   10/02/2015                                                  *
.* Author    :   Steve Armstrong                                             *
.* Function  :   This program will extract the pre-admission data into       *
.*               a pipe delimited file in a format that will feed into       *
.*               CONVPADM.                                                   *
.* Mods      :                                                               *
.*****************************************************************************
.*        V11.05.01  05.12.2024  DA Sarkies        TSK 0951086               *
.*                   Increased size of the variable to hold Length of Stay   *
.*****************************************************************************
.*        V11.04.01  05.02.2024  DA Sarkies        TSK 0936282               *
.*                   Increased size of the variable to hold healthfund number*
.*****************************************************************************
.*        V10.11.03  09/11/2017  Peter Vela       TSK 0847214                *
.*                   Recompiled for EXACCDAT                                 *
.*        V10.11.02  28/09/2017 Thanh T.           TSK 0821710               *
.*                   Audit Amission/outpatient visit comments                *
.*        V10.11.01  26/09/2017  Steve Armstrong   TSK 0845903               *
.*                   Added extraction of PADMPHPU, PADMAGNC & PADMOGNO       *
.*                   fields (CTAS).                                          *
.*****************************************************************************
.*        V10.08.01  27/10/2016  Steve Armstrong   TSK 0819758               *
.*                   Mods to extract ACC Comment data.                       *
.*                   Also added keyin of "From Date" for pre-admissions.     *
.*                   Also added check on admission time to add seconds if    *
.*                   not present.                                            *
.*****************************************************************************
.*        V10.06.00  10/02/2015  Steve Armstrong   CAR 313856                *
.*                   Created program for CRISP                               *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       ACCCMTFD/INC
          INC       IBAALVFD/INC
          INC       VISMDTFD/INC
          INC       VISMTXFD/INC
          INC       PATCONFD/INC
          INC       PATMI1FD/INC
          INC       PATWC1FD/INC
          INC       PMSVX1FD/INC
          INC       PMSWX1FD/INC
.
.         Pre-admission upload file layout - convpadm.txt
.         (as per CONVPADM)
.
PRADMEXT  FILE      HL7, FIXED=4096         * Pipe delimited upload file
.                                             4096 is maximum
.
.         Fields must be pipe delimited and in the following sequence.
.         Field lengths are irrelevant other than the fact that where
.         a field is longer than allowed, the extra data will be ignored.
.
.Name     Type    Length  Start  Description
.----     ----    ------  -----  -----------
PADMTYPE  DIM       1       1    I/P record type
.                                   0 = Pre-admission
.PIPE     DIM       1       2    Pipe Delimiter
PADMURNO  DIM       8       3    U/R Number                  (patmi1af.aurno)
.PIPE     DIM       1       11   Pipe Delimiter
PADMDATE  DIM       8       12   Admission Date (ccyymmdd)   (patmi1af.adate)
.PIPE     DIM       1       20   Pipe Delimiter
PADMTIME  DIM       8       21   Admission Time (hh:mm:ss)   (patmi1af.atime)
.PIPE     DIM       1       29   Pipe Delimiter
PADMWARD  DIM       3       30   Ward (patwr1af)             (patmi1af.ptmixwrd)
.PIPE     DIM       1       33   Pipe Delimiter
PADMWBED  DIM       3       34   Bed (patwr1af)              (patmi1af.ptmiebed)
.PIPE     DIM       1       37   Pipe Delimiter
PADMADOC  DIM       6       38   Attending Doctor (patdo1af) (patmi1af.adocta)
.PIPE     DIM       1       44   Pipe Delimiter
PADMSRCE  DIM       3       45   Referral Source  (Cat S)    (patmi1af.asrce)
.PIPE     DIM       1       48   Pipe Delimiter
PADMATYP  DIM       3       49   Admission Type   (Cat A)    (patmi1af.atype)
.PIPE     DIM       1       52   Pipe Delimiter
PADMCLSS  DIM       3       53   Admission Class  (Cat P)    (patmi1af.aclss)
.PIPE     DIM       1       56   Pipe Delimiter
PADMCARE  DIM       3       57   Care Class      (Cat CC)    (patmi1af.acare)
.PIPE     DIM       1       60   Pipe Delimiter
PADMHFND  DIM       6       61   Health Fund (patfn1af)      (patmi1af.afundh)
.PIPE     DIM       1       67   Pipe Delimiter
PADMHFTB  DIM       8       68   Health Fund Table (patfn1af)(patmi1af.afndtb)
.PIPE     DIM       1       76   Pipe Delimiter
PADMHFMN  DIM       19      77   Health Fund Membership No.  (pmsvx1af.pmvxfndm)
.                                                            (patmi1af.afndmm)
.PIPE     DIM       1       96   Pipe Delimiter
PADMDIAG  DIM       80      97   Admitting Diagnosis 1       (patmi1af.adiag1)
.PIPE     DIM       1      177   Pipe Delimiter
PADMDEPT  DIM       3      178   Department Code (Cat AC)    (patmi1af.aclssft)
.PIPE     DIM       1      181   Pipe Delimiter
PADMCOMP  DIM       3      182   Claim Code      (Cat CL)    (patmi1af.aclaim)
.PIPE     DIM       1      185   Pipe Delimiter
PADMDIET  DIM       3      186   Diet Code       (Cat DC)    (patmi1af.adiet)
.PIPE     DIM       1      189   Pipe Delimiter
PADMSTAY  DIM       3      190   Exp. Length of Stay (days)  (patmi1af.astay)
.PIPE     DIM       1      193   Pipe Delimiter
PADMDHOS  DIM       4      194   Days of Hosp'n (days)       (patmi1af.adyhosp)
.PIPE     DIM       1      197   Pipe Delimiter
PADMPCMX  DIM       9      199   Casemix Code (patcmcaf)     (patmi1af.ptmipcmx)
.PIPE     DIM       1      207   Pipe Delimiter
PADMGSTA  DIM       1      209   GST Applicable (Y/N/U)      (patmi1af.ptmigsta)
.PIPE     DIM       1      209   Pipe Delimiter
PADMCMXP  DIM       1      211   Casemix Payment (Y/N)       (patmi1af.ptmicmxp)
.PIPE     DIM       1      211   Pipe Delimiter
PADMRHC1  DIM       10     213   Referring HCP (pmshcpaf)    (pmsvx1af.pmvxrhc1)
.PIPE     DIM       1      222   Pipe Delimiter
PADMRH1G  DIM       10     224   Ref. HCP Practice (pmshcpaf)(pmsvx1af.pmvxrh1g)
.PIPE     DIM       1      233   Pipe Delimiter
PADMIDUS  DIM       3      235   Int. Stay Dur'n (Cat VI)    (pmsvx1af.pmvxidus)
.PIPE     DIM       1      237   Pipe Delimiter
PADMAPWD  DIM       3      239   Adm. Point Ward (Cat ap)    (pmsvx1af.pmvxapwd)
.PIPE     DIM       1      301   Pipe Delimiter
PADMPOWD  DIM       3      303   Post Op Ward    (patwr1af)  (pmsvx1af.pmvxpowd)
.PIPE     DIM       1      305   Pipe Delimiter
PADMPOBD  DIM       3      307   Post Op Bed     (patwr1af)  (pmsvx1af.pmvxpobd)
.PIPE     DIM       1      309   Pipe Delimiter
PADMMHLS  DIM       3      311   No longer in use
.PIPE     DIM       1      313   Pipe Delimiter
PADMEMPL  DIM       3      315   Empl. Status    (Cat KD)    (pmsvx1af.pmvxempl)
.PIPE     DIM       1      317   Pipe Delimiter
PADMPALC  DIM       3      319   Pall. Care      (Cat KG)    (pmsvx1af.pmvxpalc)
.PIPE     DIM       1      321   Pipe Delimiter
PADMUDIN  DIM       3      323   Disc. Intention (Cat KN)    (pmsvx1af.pmvxudin)
.PIPE     DIM       1      325   Pipe Delimiter
PADMUREA  DIM       3      327   Unpl. Readmiss. (Cat KO)    (pmsvx1af.pmvxurea)
.PIPE     DIM       1      329   Pipe Delimiter
PADMPACC  DIM       3      331   Pref. Accomm.   (Cat BP)    (pmsvx1af.pmvxpacc)
.PIPE     DIM       1      333   Pipe Delimiter
PADMPRGM  DIM       8      335   Program Code  (pmsprgaf)    (pmsvx1af.pmvxprgm)
.PIPE     DIM       1      342   Pipe Delimiter
PADMSLOC  DIM       3      344   Location SOR    (Cat SG)    (pmsvx1af.pmvxsloc)
.PIPE     DIM       1      346   Pipe Delimiter
PADMSCLI  DIM       3      348   Clinical SOR    (Cat SH)    (pmsvx1af.pmvxscli)
.PIPE     DIM       1      350   Pipe Delimiter
PADMSTRA  DIM       3      352   Transport SOR   (Cat SI)    (pmsvx1af.pmvxstra)
.PIPE     DIM       1      354   Pipe Delimiter
PADMMHOS  DIM       3      356   Hospital (pathspaf)         (pmsvx1af.pmvxmhos)
.PIPE     DIM       1      358   Pipe Delimiter
PADMAVIS  DIM      20      360   Alternate Visit Number      (ibaalvaf.ibavavis)
.PIPE     DIM       1      379   Pipe Delimiter
PADMPHPU  DIM       3      381   Principal Health Service    (patmi1af.ptmiphpu)
.                                Purchaser (Cat HP)
.PIPE     DIM       1      383   Pipe Delimeter
PADMAGNC  DIM       3      385   Health Agency (Cat HA)      (patmi1af.ptmiagnc)
.PIPE     DIM       1      387   Pipe Delimeter
PADMOGNO  DIM       8      389   Original Admission Number   (patmi1af.ptmiogno)
.
. End of Record            397
.
.
.         ACC Data Records
.
.         Fields must be pipe delimited and in the following sequence.
.         Field lengths are irrelevant other than the fact that where
.         a field is longer than allowed, the extra data will be ignored.
.
.PADMTYPE DIM       1      1      * I/P record type
.                                     1 = ACC Data
.PIPE     DIM       1      2      * Pipe Delimiter
.PADMURNO DIM       8      3      * U/R Number                        (ptwcurno)
.PIPE     DIM       1      11     * Pipe Delimiter
ACCDNAME  DIM       30     12     * Employer Name                     (wcename)
.PIPE     DIM       1      42     * Pipe Delimiter
ACCDADD1  DIM       35     43     * Employer Address Line 1           (wceadd1)
.PIPE     DIM       1      78     * Pipe Delimiter
ACCDADD2  DIM       35     79     * Employer Address Line 2           (wceadd2)
.PIPE     DIM       1      114    * Pipe Delimiter
ACCDADD3  DIM       35     115    * Employer Address Line 3           (wceadd3)
.PIPE     DIM       1      150    * Pipe Delimiter
ACCDADD4  DIM       35     151    * Employer Address Line 4           (wceadd4)
.PIPE     DIM       1      186    * Pipe Delimiter
ACCDPOST  DIM       8      187    * Employer Postcode                 (wcepost)
.PIPE     DIM       1      195    * Pipe Delimiter
ACCDTELP  DIM       20     196    * Employer Telephone                (wcetele)
.PIPE     DIM       1      216    * Pipe Delimiter
ACCDACDT  DIM       8      217    * Date of Accident (ccyymmdd)       (wcacdat)
.PIPE     DIM       1      225    * Pipe Delimiter
ACCDACPT  DIM       1      226    * Accepted by Ins. Co. (Y/N)        (wcaccpt)
.PIPE     DIM       1      227    * Pipe Delimiter
ACCDINSR  DIM       6      228    * Insurance Company Code            (wcicode)
.PIPE     DIM       1      234    * Pipe Delimiter
ACCDCLNO  DIM       20     235    * Insurance Claim Number            (wcclmno)
.PIPE     DIM       1      255    * Pipe Delimiter
ACCDCOM1  DIM       40     256    * Comments 1                        (wccomn1)
.PIPE     DIM       1      296    * Pipe Delimiter
ACCDCOM2  DIM       40     297    * Comments 2                        (wccomn2)
.PIPE     DIM       1      337    * Pipe Delimiter
ACCDALOC  DIM       50     338    * Accident Location                 (pmwxaloc)
.PIPE     DIM       1      388    * Pipe Delimiter
ACCDCINJ  DIM       3      389    * Cause of Injury (Cat IK)          (pmwxcinj)
.PIPE     DIM       1      392    * Pipe Delimiter
ACCDICOD  DIM       3      393    * Injury Code (Cat IJ)              (pmwxicod)
.PIPE     DIM       1      396    * Pipe Delimiter
ACCDCNAM  DIM       40     397    * Contact Name                      (pmwxcnam)
.PIPE     DIM       1      437    * Pipe Delimiter
ACCDCDTE  DIM       8      438    * Date Record Created (ccyymmdd)    (pmwxcdte)
.PIPE     DIM       1      446    * Pipe Delimiter
ACCDCTIM  DIM       8      447    * Time Record Created (hh:mm:ss)    (pmwxctim)
.PIPE     DIM       1      455    * Pipe Delimiter
ACCDWEBC  DIM       10     456    * WEB User ID Record Creator        (pmwxwebc)
.PIPE     DIM       1      466    * Pipe Delimiter
ACCDLUPD  DIM       8      467    * Last Update Date (ccyymmdd)       (pmwxlupd)
.PIPE     DIM       1      475    * Pipe Delimiter
ACCDLUPT  DIM       8      476    * Last Update Time (hh:mm:ss)       (pmwxlupt)
.PIPE     DIM       1      484    * Pipe Delimiter
ACCDWEBU  DIM       10     485    * WEB User ID Record Updator        (pmwxwebu)
.PIPE     DIM       1      495    * Pipe Delimiter
ACCDACCF  DIM       3      496    * Work Related (Cat IQ)             (pmwxaccf)
.PIPE     DIM       1      499    * Pipe Delimiter
ACCDPLIN  DIM       3      500    * Place Injury Occurred (Cat IP)    (pmwxplin)
.PIPE     DIM       1      503    * Pipe Delimiter
ACCDACTI  DIM       3      504    * Activity When Injured (Cat IO)    (pmwxacti)
.PIPE     DIM       1      507    * Pipe Delimiter
ACCDADTE  DIM       8      508    * ACC Decline Date (ccyymmdd)       (pmwxadte)
.PIPE     DIM       1      516    * Pipe Delimiter
ACCDATME  DIM       8      517    * Accident Time (hh:mm:ss)          (pmwxatme)
.PIPE     DIM       1      525    * Pipe Delimiter
ACCDACLC  DIM       3      526    * Accident Location (Cat IM)        (pmwxaclc)
.PIPE     DIM       1      529    * Pipe Delimiter
ACCDAINZ  DIM       1      530    * Accident in NZ (Y/N)              (pmwxainz)
.PIPE     DIM       1      531    * Pipe Delimiter
ACCDMOVV  DIM       1      532    * Involves Moving Vehicle           (pmwxmovv)
.                                    on Public Road (Y/N)
.PIPE     DIM       1      533    * Pipe Delimiter
ACCDSPTI  DIM       1      534    * Sporting Injury (Y/N)             (pmwxspti)
.PIPE     DIM       1      535    * Pipe Delimiter
ACCDSPRT  DIM       3      536    * Sport Name (Cat IB)               (pmwxsprt)
.PIPE     DIM       1      539    * Pipe Delimiter
ACCDRECI  DIM       1      540    * Recurring Injury Indicator (Y/N)  (pmwxreci)
.PIPE     DIM       1      541    * Pipe Delimiter
ACCDTRIC  DIM       1      542    * Treatment Injury Claim (Y/N)      (pmwxtric)
.PIPE     DIM       1      543    * Pipe Delimiter
ACCDESTA  DIM       3      544    * Employment Status (Cat IF)        (pmwxesta)
.PIPE     DIM       1      547    * Pipe Delimiter
ACCDPDDT  DIM       8      548    * Patient Declaration Date          (pmwxpddt)
.                                     (ccyymmdd)
.PIPE     DIM       1      556    * Pipe Delimiter
ACCDARG1  DIM       20     557    * Authorised Representative         (pmwxarg1)
.                                     Given Name 1
.PIPE     DIM       1      577    * Pipe Delimiter
ACCDARG2  DIM       20     578    * Authorised Representative         (pmwxarg2)
.                                     Given Name 2
.PIPE     DIM       1      598    * Pipe Delimiter
ACCDARSN  DIM       25     599    * Authorised Representative         (pmwxasrn)
.                                     Surname
.PIPE     DIM       1      624    * Pipe Delimiter
ACCDARTL  DIM       4      625    * Authorised Representative         (pmwxartl)
.                                     Title (pmstleaf)
.PIPE     DIM       1      629    * Pipe Delimiter
ACCDARRP  DIM       10     630    * Authorised Representative         (pmwxarrp)
.                                     Relation
.PIPE     DIM       1      640    * Pipe Delimiter
ACCDASST  DIM       1      641    * Assistance is Required (Y/N)      (pmwxasst)
.PIPE     DIM       1      642    * Pipe Delimiter
ACCDNEED  DIM       1      643    * Need to Call HP (Y/N)             (pmwxneed)
.PIPE     DIM       1      644    * Pipe Delimiter
ACCDCONT  DIM       1      645    * Continue Normal Hours of          (pmwxcont)
.                                     Work (Y/N)
.PIPE     DIM       1      646    * Pipe Delimiter
ACCDALTW  DIM       1      647    * Alternative Work Indicator (Y/N)  (pmwxaltw)
.PIPE     DIM       1      648    * Pipe Delimiter
ACCDTYPA  DIM       3      649    * Type of Alternative Work (Cat IH) (pmwxtypa)
.PIPE     DIM       1      652    * Pipe Delimiter
ACCDSALT  DIM       8      653    * Start Date of Alternative         (pmwxsalt)
.                                     Work (ccyymmdd)
.PIPE     DIM       1      661    * Pipe Delimiter
ACCDHPDA  DIM       2      662    * Hours per Day of Alternative      (pmwxhpda)
.                                     Work
.PIPE     DIM       1      664    * Pipe Delimiter
ACCDUNFD  DIM       3      665    * Unfit for Work for Duration       (pmwxunfd)
.PIPE     DIM       1      668    * Pipe Delimiter
ACCDUNFT  DIM       3      669    * Unfit for Work for Type (Cat UI)  (pmwxunft)
.PIPE     DIM       1      672    * Pipe Delimiter
ACCDFISD  DIM       8      673    * Full Incapacity Start Date        (pmwxfisd)
.                                     (ccyymmdd)
.PIPE     DIM       1      681    * Pipe Delimiter
ACCDRNWD  DIM       8      682    * Return to Normal Work Date        (pmwxrnwd)
.                                     (ccyymmdd)
.PIPE     DIM       1      690    * Pipe Delimiter
ACCDCSTA  DIM       1      691    * Claim Status                      (pmwxcsta)
.                                     0 = Existing
.                                     1 = Parked
.                                     2 = Completed
.PIPE     DIM       1      692    * Pipe Delimiter
ACCDTWRK  DIM       3      693    * Type of Work (Cat IX)             (pmwxtwrk)
.PIPE     DIM       1      696    * Pipe Delimiter
ACCDOEST  DIM       50     697    * Other Employment Status           (pmwxoest)
.PIPE     DIM       1      747    * Pipe Delimiter
ACCDTPRO  DIM       10     748    * HCP Treatment Provider            (pmwxtpro)
.PIPE     DIM       1      758    * Pipe Delimiter
ACCDECOD  DIM       6      759    * Employer Code (pateoraf)          (pmwxecod)
.PIPE     DIM       1      765    * Pipe Delimiter
ACCDTYP1  DIM       3      766    * Hard-coded Comment Type           (accmtype)
.                                     001 = Cause of Injury
.PIPE     DIM       1      769    * Pipe Delimiter
ACCDDAT1  DIM       500    770    * Cause of Injury Comments          (accmdata)
.PIPE     DIM       1     1270    * Pipe Delimiter
ACCDTYP2  DIM       3     1271    * Hard-coded Comment Type           (accmtype)
.                                     002 = Injury Comments
.PIPE     DIM       1     1274    * Pipe Delimiter
ACCDDAT2  DIM       500   1275    * Injury Comments                   (accmdata)
.PIPE     DIM       1     1775    * Pipe Delimiter
ACCDTYP3  DIM       3     1776    * Hard-coded Comment Type           (accmtype)
.                                     003 = Alternative Work Restrictions
.PIPE     DIM       1     1779    * Pipe Delimiter
ACCDDAT3  DIM       500   1780    * Alternative Work Restrictions Comments
.                                                                     (accmdata)
. End of Record           2280
.
. Visit comment header upload file layout
.
.Name     Type    Length Start   Description
.----     ----    ------ -----   -----------
.PADMTYPE DIM       1      1     Comment Header (2)
.PIPE     DIM       1      2     Pipe Delimiter
.PADMURNO DIM       8      3     U/R Number
.PIPE     DIM       1      11    Pipe Delimiter
CSMDVISN  DIM       8      12    U/R Number
.PIPE     DIM       1      20    Pipe Delimiter
CSMDTYPE  DIM       3      21    Comment Type
.PIPE     DIM       1      24    Pipe Delimiter
CSMDNOTE  DIM       6      25    Counter/Note Number
.PIPE     DIM       1      31    Pipe Delimiter
CSMDDATE  DIM       8      32    Date Record Created (ccyymmdd)
.PIPE     DIM       1      40    Pipe Delimiter
CSMDTIME  DIM       8      41    Time Record Created (hh:mm:ss)
.PIPE     DIM       1      49    Pipe Delimiter
CSMDUSER  DIM       10     50    User Who Created Record
.PIPE     DIM       1      60    Pipe Delimiter
CSMDOCCG  DIM       3      61    Occupational Group WEB User ID
.PIPE     DIM       1      64    Pipe Delimiter
CSMDDELT  DIM       1      65    Delete Indicator (0=No, 1=Yes)
.PIPE     DIM       1      66    Pipe Delimiter
CSMDDDAT  DIM       8      67    Date Record Deleted (ccyymmdd)
.PIPE     DIM       1      75    Pipe Delimiter
CSMDDTIM  DIM       8      76    Time Record Deleted (hh:mm:ss)
.PIPE     DIM       1      84    Pipe Delimiter
CSMDDUSE  DIM       10     85    User Who Deleted Record (websecaf)
.PIPE     DIM       1      95    Pipe Delimiter
CSMDDOCC  DIM       3      96    Occupational Grp WEB UserID Deleted
.
.End of Record             99
.
. Visit comment detail upload file layout
.
.Name     Type    Length Start   Description
.----     ----    ------ -----   -----------
.PADMTYPE DIM       1      1     Comment Details (3)
.PIPE     DIM       1      2     Pipe Delimiter
.PADMURNO DIM       8      3     U/R Number
.PIPE     DIM       1      11    Pipe Delimiter
CSMTVISN  DIM       8      12    U/R Number
.PIPE     DIM       1      20    Pipe Delimiter
CSMTTYPE  DIM       3      21    Comment Type
.PIPE     DIM       1      24    Pipe Delimiter
CSMTNOTE  DIM       6      25    Counter/Note Number
.PIPE     DIM       1      31    Pipe Delimiter
CSMTUNIQ  DIM       3      32    Line Number
.PIPE     DIM       1      35    Pipe Delimiter
CSMTCMNT  DIM       70     36    Comment
.
.End of Record            106
.
.
. LOCAL VARIABLE DEFINITION
. -------------------------
ACCCMTYP  DIM       3
ACCCOUNT  FORM      10
CURRDATE  DIM       8
CURRTIME  DIM       8
DIM500    DIM       500
EXTCOUNT  FORM      10
LINCOUNT  FORM      1
RECCOUNT  FORM      10
SAVECODE  DIM       3
SAVKEY33  DIM       33
STRTDATE  DIM       8
TYPCOUNT  FORM      1
VISNUMBR  DIM       8
EXTCNTHR  FORM      10
EXTCNTDT  FORM      10
PRCCNTHR  FORM      10
PRCCNTDT  FORM      10
.
.
.
. PROGRAM CONSTANTS
. -----------------
ACCTYPE1  INIT      "001"
ACCTYPE2  INIT      "002"
ACCTYPE3  INIT      "003"
PIPE      INIT      "|"
.
.
.-----------------------------------------------------------------------
PRGID     INIT      "EXTRPADM"
PRGNAM    INIT      "Preadmission Details Extraction"
VERSION   INIT      "V12.01.00"
.-----------------------------------------------------------------------
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000       * proceed with upload
.
MAIN1000  CALL      GDAT0000               * get start date for pre-admissions
          BRANCH    EXIT,MAIN0100          * nothing input
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN5000:        * yes
                          MAIN1000:        * no
                          MAIN0100         * cancel
.
MAIN5000  CALL      CFIL0000               * create file
          BRANCH    EXIT,MAIN0100          * file exists
.
          CALL      PROC0000               * process
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"ibaalvaf";
          OPEN      IBAALVA1,"ibaalvaf"
.
          DISPLAY   *P64:24,"acccmtaf";
          OPEN      ACCCMTA1,"acccmtaf"
.
          DISPLAY   *P64:24,"vismdtaf";
          OPEN      VISMDTA1,"vismdtaf"
          DISPLAY   *P64:24,"vismtxaf";
          OPEN      VISMTXA1,"vismtxaf"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A2,"patmi1af"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,EIGHTY;*250,PTCNHDPS
.
.         If NZ, open files for ACC and NBRS data
.
          IF        PTCNHDPS = 1
            DISPLAY   *P64:24,"patwc1af";
            OPEN      PATWC1A1,"patwc1af"
.
            DISPLAY   *P64:24,"pmswx1af";
            OPEN      PMSWX1A1,"pmswx1af"
          ENDIF
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run data extraction                     *
.*                        TRUE  (1)  Exit selected                           *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Preadmission Details Extraction"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run extraction
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.****************************************************************************
.*                             GDAT0000                Called by: MAIN0000  *
.*                       Get starting date                                  *
.* Returns: STRTDATE - starting date (ccyymmdd)                             *
.*          EXIT  0 = valid date entered                                    *
.*                1 = nothing entered                                       *
.****************************************************************************
.
GDAT0000  DISPLAY   *P1:11,*EF,"From Date :"
          MOVE      ONE,CCENTRY
          MOVE      ONE,CCANLDTE
          MOVE      TEN1,CVERT
          MOVE      TEN3,CCOL
.
          CALL      IBACLOCK                     * get current date
.
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          UNPACK    CURRDATE,CCENT,CYEAR,CMON,CDAY
          CALL      KEYDATE
          BRANCH    OVRCD,GDAT9100
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
.
.         Check that start date is not in the past
.
          MATCH     CURRDATE,STRTDATE
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Start Date cannot be in the past.  ";
            CALL      HITENTER
            GOTO      GDAT0000
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      GDAT9999
.
GDAT9100  MOVE      ONE,EXIT
.
GDAT9999  RETURN
+
.*****************************************************************************
.*                                  CFIL0000       Called by: MAIN0000       *
.*                         Create the extract file                           *
.*****************************************************************************
.
CFIL0000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PRADMEXT,"convpadm"     * extract file exists ?
          TRAPCLR   IO
          BRANCH    OVRCD,CFIL5000          * no - create it
.
CFIL1000  KEYIN     *P1:23,*B,*EF,"The extract file convpadm.txt already ":
                    "exists.":
                    *P1:24,"Overwrite it (",*V2LON,*DV,ANSY,*HOFF,"/":
                    *V2LON,*DV,ANSN,*HOFF,")?":
                    *P21:24,*V2LON,ANS
.
          REP       UPPLOW,ANS
          MATCH     ANSY,ANS                * "Y" entered ?
          GOTO      CFIL2000 IF EQUAL       * yes - continue
.
          MATCH     ANSN,ANS                * "N" entered ?
          GOTO      CFIL9100 IF EQUAL       * no
.
          BEEP
          GOTO      CFIL1000
.
.         Delete the existing file
.
CFIL2000  CLOSE     PRADMEXT,DELETE
.
.         Create a new extract file
.
CFIL5000  PREP      PRADMEXT,"convpadm"
.
          MOVE      ZERO,EXIT
          GOTO      CFIL9999
.
CFIL9100  MOVE      ONE,EXIT
.
CFIL9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*    Loop through the admission file and extract each preadmission record   *
.*****************************************************************************
.
PROC0000  MOVE      ZERO,RECCOUNT                * initialise record count
          MOVE      ZERO,EXTCOUNT                * initialise extract count
          MOVE      ZERO,ACCCOUNT                * initialise acc count
          MOVE      ZERO,PRCCNTHR
          MOVE      ZERO,PRCCNTDT
          MOVE      ZERO,EXTCNTHR
          MOVE      ZERO,EXTCNTDT
          DISPLAY   *P1:24,*EL,"Processing:";
.
          PACK      KEY9,ONE,SP9
          CALL      RDSMISS2                     * position at start of file
PROC0500  CALL      RDKMISS2                     * read next record
          BRANCH    OVRCD,PROC9000               * eof - finished
.
          COMPARE   ONE,ASTAT                    * preadmission still ?
          GOTO      PROC9000 IF NOT EQUAL        * no - finished
.
          MATCH     STRTDATE,ADATE               * valid date ?
          GOTO      PROC0500 IF LESS             * no - ignore record
.
          ADD       ONE,RECCOUNT                 * increment record counter
.
          DISPLAY   *P13:24,*V2LON,RECCOUNT
.
          CALL      CLPMSVX1                     * clear pmsvx1af variables
          MOVE      AADMNO,KEY8
          CALL      RDPMVX11
.
          MOVE      AURNO,PADMURNO
          SQUEEZE   PADMURNO
          MOVE      ADATE,PADMDATE
          STRIP     PADMDATE
          MOVE      ATIME,PADMTIME
          STRIP     PADMTIME
.
.         Check that the time is hh:mm:ss instead of hh:mm
.
          MOVELPTR  PADMTIME,FORM1
          IF        FORM1 = 5
            ENDSET    PADMTIME
            APPEND    ":00",PADMTIME
            RESET     PADMTIME
          ENDIF
.
          MOVE      PTMIXWRD,PADMWARD
          STRIP     PADMWARD
          MOVE      PTMIEBED,PADMWBED
          STRIP     PADMWBED
          MOVE      ADOCTA,PADMADOC
          STRIP     PADMADOC
          MOVE      ASRCE,PADMSRCE
          STRIP     PADMSRCE
          MOVE      ATYPE,PADMATYP
          STRIP     PADMATYP
          MOVE      ACLSS,PADMCLSS
          STRIP     PADMCLSS
          MOVE      ACARE,PADMCARE
          STRIP     PADMCARE
          MOVE      AFUNDH,PADMHFND
          STRIP     PADMHFND
          MOVE      AFNDTB,PADMHFTB
          STRIP     PADMHFTB
          MATCH     PMVXFNDM,SP20
          IF        @EQUAL
            MOVE      AFNDMM,PADMHFMN
          ELSE
            MOVE      PMVXFNDM,PADMHFMN
          ENDIF
          STRIP     PADMHFMN
          MOVE      ADIAG1,PADMDIAG
          STRIP     PADMDIAG
          MOVE      ACLSSFT,PADMDEPT
          STRIP     PADMDEPT
          MOVE      ACLAIM,PADMCOMP
          STRIP     PADMCOMP
          MOVE      ADIET,PADMDIET
          STRIP     PADMDIET
          MOVE      ASTAY,PADMSTAY
          SQUEEZE   PADMSTAY
          MOVE      ADYHOSP,PADMDHOS
          SQUEEZE   PADMDHOS
          MOVE      PTMIPCMX,PADMPCMX
          STRIP     PADMPCMX
.
          MATCH     SP1,PTMIGSTA
          IF        @EQUAL
            MOVE      ANSN,PADMGSTA
          ELSE
            MOVE      PTMIGSTA,PADMGSTA
          ENDIF
          SQUEEZE   PADMGSTA
.
          MATCH     SP1,PTMICMXP
          IF        @EQUAL
            MOVE      ANSN,PADMCMXP
          ELSE
            MOVE      PTMICMXP,PADMCMXP
          ENDIF
          SQUEEZE   PADMCMXP
.
          MOVE      PMVXRHC1,PADMRHC1
          STRIP     PADMRHC1
          MOVE      PMVXRH1G,PADMRH1G
          STRIP     PADMRH1G
          MOVE      PMVXIDUS,PADMIDUS
          STRIP     PADMIDUS
          MOVE      PMVXAPWD,PADMAPWD
          STRIP     PADMAPWD
          MOVE      PMVXPOWD,PADMPOWD
          STRIP     PADMPOWD
          MOVE      PMVXPOBD,PADMPOBD
          STRIP     PADMPOBD
          MOVE      SP3,PADMMHLS
          STRIP     PADMMHLS
          MOVE      PMVXEMPL,PADMEMPL
          STRIP     PADMEMPL
          MOVE      PMVXPALC,PADMPALC
          STRIP     PADMPALC
          MOVE      PMVXUDIN,PADMUDIN
          STRIP     PADMUDIN
          MOVE      PMVXUREA,PADMUREA
          STRIP     PADMUREA
          MOVE      PMVXPACC,PADMPACC
          STRIP     PADMPACC
          MOVE      PMVXPRGM,PADMPRGM
          STRIP     PADMPRGM
          MOVE      PMVXSLOC,PADMSLOC
          STRIP     PADMSLOC
          MOVE      PMVXSCLI,PADMSCLI
          STRIP     PADMSCLI
          MOVE      PMVXSTRA,PADMSTRA
          STRIP     PADMSTRA
          MOVE      PMVXMHOS,PADMMHOS
          STRIP     PADMMHOS
.
          CALL      GALV0000                     * get alternate visit number
.
          MOVE      PTMIPHPU,PADMPHPU
          STRIP     PADMPHPU
          MOVE      PTMIAGNC,PADMAGNC
          STRIP     PADMAGNC
          MOVE      PTMIOGNO,PADMOGNO
          STRIP     PADMOGNO
.
          MOVE      "0",PADMTYPE
.
          WRITE     PRADMEXT,SEQ;*+,PADMTYPE,PIPE,PADMURNO,PIPE,PADMDATE,PIPE:
                                    PADMTIME,PIPE,PADMWARD,PIPE,PADMWBED,PIPE:
                                    PADMADOC,PIPE,PADMSRCE,PIPE,PADMATYP,PIPE:
                                    PADMCLSS,PIPE,PADMCARE,PIPE,PADMHFND,PIPE:
                                    PADMHFTB,PIPE,PADMHFMN,PIPE,PADMDIAG,PIPE:
                                    PADMDEPT,PIPE,PADMCOMP,PIPE,PADMDIET,PIPE:
                                    PADMSTAY,PIPE,PADMDHOS,PIPE,PADMPCMX,PIPE:
                                    PADMGSTA,PIPE,PADMCMXP,PIPE,PADMRHC1,PIPE:
                                    PADMRH1G,PIPE,PADMIDUS,PIPE,PADMAPWD,PIPE:
                                    PADMPOWD,PIPE,PADMPOBD,PIPE,PADMMHLS,PIPE:
                                    PADMEMPL,PIPE,PADMPALC,PIPE,PADMUDIN,PIPE:
                                    PADMUREA,PIPE,PADMPACC,PIPE,PADMPRGM,PIPE:
                                    PADMSLOC,PIPE,PADMSCLI,PIPE,PADMSTRA,PIPE:
                                    PADMMHOS,PIPE,PADMAVIS,PIPE:
                                    PADMPHPU,PIPE,PADMAGNC,PIPE,PADMOGNO,*-
.
          ADD       ONE,EXTCOUNT                 * increment extract count
.
          CALL      EXCMTA00                     * get visit comments
.
.         Now get matching ACC data record for NZ sites only
.
          MOVE      AADMNO,VISNUMBR
          CALL      EXACCDAT                     * extract ACC data for NZ only
          BRANCH    EXIT,PROC0500:               * no ACC data
                         PROC0500                * not NZ
.
          MOVE      "1",PADMTYPE
.
          WRITE     PRADMEXT,SEQ;*+,PADMTYPE,PIPE,PADMURNO,PIPE:
                                    ACCDNAME,PIPE,ACCDADD1,PIPE,ACCDADD2,PIPE:
                                    ACCDADD3,PIPE,ACCDADD4,PIPE,ACCDPOST,PIPE:
                                    ACCDTELP,PIPE,ACCDACDT,PIPE,ACCDACPT,PIPE:
                                    ACCDINSR,PIPE,ACCDCLNO,PIPE,ACCDCOM1,PIPE:
                                    ACCDCOM2,PIPE,ACCDALOC,PIPE,ACCDCINJ,PIPE:
                                    ACCDICOD,PIPE,ACCDCNAM,PIPE,ACCDCDTE,PIPE:
                                    ACCDCTIM,PIPE,ACCDWEBC,PIPE,ACCDLUPD,PIPE:
                                    ACCDLUPT,PIPE,ACCDWEBU,PIPE,ACCDACCF,PIPE:
                                    ACCDPLIN,PIPE,ACCDACTI,PIPE,ACCDADTE,PIPE:
                                    ACCDATME,PIPE,ACCDACLC,PIPE,ACCDAINZ,PIPE:
                                    ACCDMOVV,PIPE,ACCDSPTI,PIPE,ACCDSPRT,PIPE:
                                    ACCDRECI,PIPE,ACCDTRIC,PIPE,ACCDESTA,PIPE:
                                    ACCDPDDT,PIPE,ACCDARG1,PIPE,ACCDARG2,PIPE:
                                    ACCDARSN,PIPE,ACCDARTL,PIPE,ACCDARRP,PIPE:
                                    ACCDASST,PIPE,ACCDNEED,PIPE,ACCDCONT,PIPE:
                                    ACCDALTW,PIPE,ACCDTYPA,PIPE,ACCDSALT,PIPE:
                                    ACCDHPDA,PIPE,ACCDUNFD,PIPE,ACCDUNFT,PIPE:
                                    ACCDFISD,PIPE,ACCDRNWD,PIPE,ACCDCSTA,PIPE:
                                    ACCDTWRK,PIPE,ACCDOEST,PIPE,ACCDTPRO,PIPE:
                                    ACCDECOD,PIPE,ACCDTYP1,PIPE,ACCDDAT1,PIPE:
                                    ACCDTYP2,PIPE,ACCDDAT2,PIPE,ACCDTYP3,PIPE:
                                    ACCDDAT3,*-
.
          ADD       ONE,ACCCOUNT                 * increment acc count
.
          GOTO      PROC0500                     * get next record
.
PROC9000  DISPLAY   *P1:19,*EF,"Extraction complete.":
                    *P1:20,*V2LON,EXTCOUNT,*HOFF," Pre-admission records ":
                                                 "extracted.  ";
.
          DISPLAY   *P1:21,*V2LON,ACCCOUNT,*HOFF," Pre-admission ACC records ":
                                                 "extracted.  ";
.
          DISPLAY   *P1:22,*V2LON,EXTCNTHR,*HOFF," Pre-admission Visit ":
                           "Comment Header records extracted.";
.
          DISPLAY   *P1:23,*V2LON,EXTCNTDT,*HOFF," Pre-admission Visit ":
                           "Comment Detail records extracted.";
.
          DISPLAY   *P1:24;
          CALL      HITENTER
.
PROC9999  RETURN
+
.*****************************************************************************
.*                               GALV0000              Called by: PROC0000   *
.*  Check if there is an alternate (legacy) visit id on file for the visit   *
.* Requires: AADMNO - visit number                                           *
.* Returns:  PADMAVIS - alternate visit id field                             *
.*****************************************************************************
.
GALV0000  MOVE      SP20,PADMAVIS                * initialise field
.
          MOVE      AADMNO,KEY8
          CALL      RDIBALV1                     * visit on file ?
          BRANCH    OVRCD,GALV9000               * no - finished
.
          MATCH     " 1",IBAVTYPE                * legacy visit number ?
          IF        !@EQUAL
            MOVE      IBAVAVIS,PADMAVIS          * yes
          ENDIF
.
GALV9000  STRIP     PADMAVIS
.
GALV9999  RETURN
.
.-----------------------------------------------------------------------
. Extract all the active visit comment details
.-----------------------------------------------------------------------
EXCMTA00  MOVE      AADMNO,KEY8
          PACK      KEY17,KEY8,SP70
          CALL      RSVSMDT1
EXCMTA10  CALL      RKVSMDT1
          BRANCH    OVRCD,EXCMTA99
.
          MATCH     KEY8,VSMDVISN
          GOTO      EXCMTA99 IF NOT EQUAL
.
          ADD       ONE,PRCCNTHR
.
          MATCH     "1",VSMDDELT            * is comment deleted
          GOTO      EXCMTA10 IF EQUAL
.
. Extract comment header
.
          MOVE      "2",PADMTYPE
          MOVE      VSMDVISN,CSMDVISN
          MOVE      VSMDTYPE,CSMDTYPE
          MOVE      VSMDNOTE,CSMDNOTE
          MOVE      VSMDDATE,CSMDDATE
          MOVE      VSMDTIME,CSMDTIME
          MOVE      VSMDUSER,CSMDUSER
          MOVE      VSMDOCCG,CSMDOCCG
          MOVE      VSMDDELT,CSMDDELT
          MOVE      VSMDDDAT,CSMDDDAT
          MOVE      VSMDDTIM,CSMDDTIM
          MOVE      VSMDDUSE,CSMDDUSE
          MOVE      VSMDDOCC,CSMDDOCC
.
          SQUEEZE   CSMDVISN
          SQUEEZE   CSMDTYPE
          SQUEEZE   CSMDNOTE
          SQUEEZE   CSMDDATE
          SQUEEZE   CSMDTIME
          SQUEEZE   CSMDUSER
          SQUEEZE   CSMDOCCG
          SQUEEZE   CSMDDELT
          SQUEEZE   CSMDDDAT
          SQUEEZE   CSMDDTIM
          SQUEEZE   CSMDDUSE
          SQUEEZE   CSMDDOCC
.
          WRITE     PRADMEXT,SEQ;*+,PADMTYPE,PIPE,PADMURNO,PIPE,CSMDVISN,PIPE:
                                    CSMDTYPE,PIPE,CSMDNOTE,PIPE,CSMDDATE,PIPE:
                                    CSMDTIME,PIPE,CSMDUSER,PIPE,CSMDOCCG,PIPE:
                                    CSMDDELT,PIPE,CSMDDDAT,PIPE,CSMDDTIM,PIPE:
                                    CSMDDUSE,PIPE,CSMDDOCC,*-
.
          ADD       ONE,EXTCNTHR
.
          CALL      EXCMXA00
.
          GOTO      EXCMTA10
.
EXCMTA99  RETURN
.
.---------------------------------------------------------------------
. Extract visit comment text details
.---------------------------------------------------------------------
EXCMXA00  PACK      KEY20,VSMDVISN,VSMDTYPE,VSMDNOTE,SP70
          CALL      RSVSMTX1
EXCMXA20  CALL      RKVSMTX1
          BRANCH    OVRCD,EXCMXA99
.
          MATCH     VSMDVISN,VSMTVISN
          GOTO      EXCMXA99 IF NOT EQUAL
.
          MATCH     VSMDTYPE,VSMTTYPE
          GOTO      EXCMXA99 IF NOT EQUAL
.
          MATCH     VSMDNOTE,VSMTNOTE
          GOTO      EXCMXA99 IF NOT EQUAL
.
          ADD       ONE,PRCCNTDT
.
. Extract comment details
.
          MOVE      "3",PADMTYPE
          MOVE      VSMTVISN,CSMTVISN
          MOVE      VSMTTYPE,CSMTTYPE
          MOVE      VSMTNOTE,CSMTNOTE
          MOVE      VSMTUNIQ,CSMTUNIQ
          MOVE      VSMTCMNT,CSMTCMNT
.
          SQUEEZE   CSMTVISN
          SQUEEZE   CSMTTYPE
          SQUEEZE   CSMTNOTE
          SQUEEZE   CSMTUNIQ
          STRIP     CSMTCMNT
.
          WRITE     PRADMEXT,SEQ;*+,PADMTYPE,PIPE,PADMURNO,PIPE,CSMTVISN,PIPE:
                                    CSMTTYPE,PIPE,CSMTNOTE,PIPE,CSMTUNIQ,PIPE:
                                    CSMTCMNT,*-
          ADD       ONE,EXTCNTDT
.
          GOTO      EXCMXA20
.
EXCMXA99  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       CLPMSVX1
          INC       CLPMSWX1
          INC       EXACCDAT
.
          INC       ACCCMTIO/INC
          INC       IBAALVIO/INC
          INC       VISMDTIO/INC
          INC       VISMTXIO/INC
          INC       PATMI1IO/INC
          INC       PATWC1IO/INC
          INC       PMSVX1IO/INC
          INC       PMSWX1IO/INC

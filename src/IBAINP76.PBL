.******************************************************************************
.* System         :  Patient Billing                                          *
.* Program        :  IBAINP76                                                 *
.* Description    :  Return To Theatre During An Admission Report             *
.******************************************************************************
.* Date           :  07/12/1994                                               *
.* Author         :  Greg Horvat                                              *
.* Function       :  This report asks for admission or discharge date ranges. *
.*                   It will then report patients who have at least 2 MBS     *
.*                   items on their account for 2 different theatre sessions. *
.*                                                                            *
.* Modifications  :                                                           *
.* V12.00.04  14/08/2025 Alina Bhari    TSK 0964492                           *
.*            Correctly displayed the discharge date when the a visit have    *
.*            multiple theatre case - PRNT1500                                *
.* V12.00.03  04/08/2025 Alina Bhari    TSK 0964492                           *
.*            Correctly displayed the admission date when the a visit have    *
.*            multiple theatre case - PRNT1500                                *
.* V12.00.02  03/06/2025  J.Tan          TSK 0955096                          *
.*            Added Alphanumeric visit number changes                         *
.* V12.00.01  30/05/2025 Jacob Jackson  TSK 0955096                           *
.*            Alphanumeric changes                                            *
.* V11.04.02  31/07/2024 Alina Bhari    TSK 0938569                           *
.*            Correctly Displayed data for multi theatre cases within single  *
.*            visit - GTHT5500                                                *
.* V11.04.01  03/07/2024 Alina Bhari    TSK 0938569                           *
.*            Displayed the data for second and any subsequent theatre cases  *
.*            that are within the same patient visit - PRNT1500                *
.* V10.04.01  15/06/2014 Jill Parkinson CAR 301639                            *
.*            Moved TFILENAM to INIT000                                       *
.* V10.03.01  28/12/2012 Patrick Adair                             CAR 261630 *
.*                       Removed port number from temp file name              *
.*        V10.00.01 15/10/2010  Steve Armstrong  CAR 231697                   *
.*                  Multiple changes including:                               *
.*                   - Changed I/O for Index 1 and Index 2 of temp file to    *
.*                     use KEY27 instead of KEY25.                            *
.*                   - Reduced PATNAME from 26 to 24 characters to fit column.*
.*                   - Fixed printing of blank line after last record on page.*
.*                   - Changed to print PIPE at position 132 instead of 133.  *
.*                   - Removed unused array variables.                        *
.*                   - Added routine to clear temp file before each report    *
.*                     is run.                                                *
.******************************************************************************
.*        V9.12.01  06/01/2010  Mike Laming      CAR 186955                   *
.*                  Mods to include Theatre Items not yet Billed              *
.*        V9.07.01  30/08/2006 Peter Vela        CAR 117286                   *
.*                  Fixed patient name display                                *
.*                  Increased array from 20 cells to 99 cells                 *
.*        V9.06.01  06/06/2006 Janet George      CAR 63052                    *
.*                  Added a column Unplanned Theatre to the report IBAINP76   *
.*        V9.04.02  21/12/2004 Henry Son         CAR 55666                    *
.*                  Fix printing by discharge date.                           *
.*        V9.04.01  23/08/2004  Lina Vo CAR 52901                             *
.*                  Added Multi-Hospital processing                           *
.*        V9.03.01  23/10/2002 J.Tan                                          *
.*                  Mods to use Misc.theatre item file                        *
.*        V5.08.02  25.10.2000 J.Tan                                          *
.*                  Recompiled for WEB Report Scheduler                       *
.*        V5.08.01  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.B01 15/03/2000  Greg Horvat                                   *
.*                  Removed any reference to the patient coding files         *
.*        V5.07.B03 29/01/1999  Nicole Harrington                             *
.*                  507 rebounds                                              *
.*        V5.07.B02 19/11/1998  Jill Habasque                                 *
.*                  Modifications to include centuries                        *
.*        V5.04.02  28/02/1997  Greg Horvat      SRF 618923                   *
.*                  Changed to right justify the theatre session variable so  *
.*                  OPR & PAT theatre sessions are the same                   *
.*        V5.04.01  06/09/1996  Greg Horvat      SRF 616758                   *
.*                  - Changed to print the attending doctor for each item     *
.*                  - Changed to print a blank after each printed admission   *
.*                  - Fixed problems with the tempfile                        *
.*        V6.03.B04 03/02/1995  Gabrielle                                     *
.*                  Changed Program Name                                      *
.*        V6.03.B03 02/02/1995  Gabrielle                                     *
.*                  Changed Program Name                                      *
.*        V6.03.B00 24/01/1995  Paul Howells                                  *
.*                  Fixed a few miscellaneous errors                          *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
.
          INC       IBACONFD/INC
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       PATCODFD/INC
          INC       PATCONFD/INC            * Inpatient Control file
          INC       PATDO1FD/INC            * Doctors file
          INC       PATDSCFD/INC            * Discharge file
          INC       PATDTRFD/INC            * Debtors file
          INC       PATHSPFD/INC
          INC       PATICDFD/INC            * ICD-9CM disease file
          INC       PATMA1FD/INC            * Master file
          INC       PATMI1FD/INC            * Admission file
          INC       PATTRNFD/INC            * Transfer file
          INC       PMSMTIFD/INC            * Misc.Theatre item file
          INC       PMSVX1FD/INC
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
. ----- Tempfile Definition -----
.
TEMP1     IFILE     SQL, FIXED=86, KEY="U44-51,1-8,9-16,17-19"
TEMP2     IFILE     SQL, FIXED=86, KEY="U62-69,1-8,9-16,17-19"
TEMP3     IFILE     SQL, FIXED=86, KEY="U1-8,9-16,17-19"
.
FILELIN1  INIT      " 86 U1-8,9-16,17-19 U44-51,1-8,9-16,17-19"
FILELIN2  INIT      " 86 U1-8,9-16,17-19 U62-69,1-8,9-16,17-19"
.
.Name     Type      Length   Physical   Start   Description
.----     ----      ------   --------   -----   -----------
URNO      DIM       8        8          1       U/R number
ADMISS    DIM       8        8          9       Admission number
DTMPCONT  DIM       3        3          17      Tempfile counter
PATNAME   DIM       24       24         20      Patient name
ADMDATE   DIM       8        8          44      Admission date (CCYYMMDD)
DSPADDTE  DIM       10       10         52      Display admission date
DSCDATE   DIM       8        8          62      Discharge date (CCYYMMDD)
DSPDSDTE  DIM       10       10         70      Display discharge date
.ADOCTA   DIM       6        6          80      Attending doctor
.
.End of Record                          86
.
.Redefine FORM fields from key
.-----------------------------
TMPCOUNT  FORM      3
.
.
. ----- Program Variables -----
.
ADOCTORS  DIM       12[99]                  * Attending doctor short descr.
.
BLNKLINE  FORM      1                       * Print a blank line flag
.
CMDLINE   DIM       80
CURDATE   DIM       8                       * Current date (CCYYMMDD)
.
DIM8      DIM       8
DIM17     DIM       17
DIM30     DIM       30
DSPEDDTE  DIM       10                      * Display ending date
DSPSTDTE  DIM       10                      * Display starting date
DSPTHDTE  DIM       10[99]                  * Display theatre date
.
ENDDATE   DIM       8                       * Ending date (CCYYMMDD)
.
FNAME     DIM       8                       * Tempfile name
.
MTIFLAG   FORM      1
OPTIONDS  DIM       15                      * Option description
PATCOUNT  FORM      3                       * Patient report counter
PATNAME1  DIM       20                      * Temp var for storing patient name
.
SAVSESSN  DIM       2                       * Save theatre session number
SAVTHDTE  DIM       8                       * Save theatre date
STRTDATE  DIM       8                       * Starting date (CCYYMMDD)
.
THEACONT  FORM      3                       * Theatre item counter
THEADATE  DIM       8                       * Theatre date
THEASESN  DIM       2[99]                   * Theatre Session No.
THEAITES  DIM       26[99]                  * Theatre item & short desc
.
. ----- Program Constants -----
.
SP60      INIT      "                              ":
                    "                              "
Z30       INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
.
PRGID     INIT      "IBAINP76"
PRGNAM    INIT      "Return To Theatre During An Admn. Report"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                  ML0000                                    *
.*                                Main Module                                 *
.******************************************************************************
ML0000    CALL      INIT0000                * Initialize variables & open files
ML1000    CALL      OPTN0000                * Choose option
          BRANCH    EXIT,ML9000
          BRANCH    OPTION,ML2000,ML2000
.                          Admin, Disch
.
ML2000    CALL      DATE0000                * Keyin the date range
          BRANCH    EXIT,ML1000
.
ML3000    CALL      BLNK0000                * Print a blank line (Y/N) ?
          BRANCH    EXIT,ML2000
.
          CALL      KHOS0000
          BRANCH    EXIT,ML2000 
.
          CALL      CONTQST                 * Ok to Continue (Y/N/C) ?
          BRANCH    CEXIT,ML4000,ML3000,ML1000
.                         Yes    No     Cancel
.
ML4000    PERFORM   OPTION,LADM0000,LDSC0000     * Load admin/disch details
          CALL      PRNT0000                * Print the report
          GOTO      ML1000
.
ML9000    CALL      KILL0000                * Delete tempfile
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                 INIT0000               Called by: ML0000   *
.*                     Initialize Variables & Open Files                      *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen header
.
          DISPLAY   *P56:24,"Opening patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"patdtraf";
          OPEN      PATDTRA1,"patdtraf"
.
          DISPLAY   *P64:24,"pmsmtiaf";
          OPEN      PMSMTIA2,"pmsmtiaf"
.
          DISPLAY   *P64:24,"paticddf";
          OPEN      PATICDD1,"paticddf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A3,"patmi1af"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAME
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          CLOCK     TIME,CTIMEIS
          MOVE      ONE,CNOUNDLN
          PACK      CURDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURDATE
.
INIT9999  RETURN
+
.******************************************************************************
.*                                 OPTN0000               Called by: ML0000   *
.*                               Choose Option                                *
.******************************************************************************
OPTN0000  MOVE      ZERO,OPTION
          HLINE     *G37,2,60,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = By Admission Date":
                    *P1:6,*V2LON,"2",*HOFF," = By Discharge Date":
                    *P1:8,*HOFF,"Select Option : ";
.
OPTN1000  KEYIN     *P17:8,*V2LON,OPTION;
          BRANCH    OPTION,OPTN2000,OPTN3000
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BEEP
          GOTO      OPTN1000
.
OPTN2000  MOVE      "Admission Date ",OPTIONDS
          GOTO      OPTN9000
.
OPTN3000  MOVE      "Discharge Date ",OPTIONDS
.
OPTN9000  DISPLAY   *P59:2,*EL,*V2LON," - By ",OPTIONDS;
          MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                 DATE0000               Called by: ML0000   *
.*                           Keyin The Date Range                             *
.******************************************************************************
DATE0000  MOVE      ONE,CCANLDTE            * Cancel default date
.
. ----- Enter Starting Date -----
.
DATE1000  DISPLAY   *P1:10,*EF,"Starting ",OPTIONDS,": ":
                    *P1:11,*EL,"Ending ",OPTIONDS,"  : ";
          MOVE      "10",CVERT
          MOVE      "27",CCOL
          MOVE      CCC,CCENT
          UNPACK    SP6,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin the starting date
          BRANCH    OVRCD,DATE9500
.
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDATE
          CALL      PACDATE                 * Pack the starting date
          MOVE      CPCDATE,DSPSTDTE
          REP       " 0",DSPSTDTE
.
. ----- Check If Starting Today Is Greater Than Today's Date -----
.
          MATCH     STRTDATE,CURDATE
          IF        @LESS
            DISPLAY    *P1:24,*EL,*B,"Keyed in date is greater than today's ":
                       "date.  ";
            CALL       HITENTER
            GOTO       DATE1000
          ENDIF
.
. ----- Enter Ending Date -----
.
DATE2000  MOVE      "11",CVERT
          MOVE      "27",CCOL
          CALL      KEYDATE                 * Keyin the ending date
          BRANCH    OVRCD,DATE1000
.
          PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDATE
          CALL      PACDATE                 * Pack the ending date
          MOVE      CPCDATE,DSPEDDTE
          REP       " 0",DSPEDDTE
.
. ----- Check If The Ending Today Is Greater Than Today's Date -----
.
          MATCH     ENDDATE,CURDATE
          IF        @LESS
            DISPLAY    *P1:24,*EL,*B,"Keyed in date is greater than today's ":
                       "date.  ";
            CALL       HITENTER
            GOTO       DATE2000
          ENDIF
.
. ----- Check That The Ending Date Is Greater Than The Starting Date -----
.
          MATCH     STRTDATE,ENDDATE
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Ending date must be greater than the ":
                      "starting date.  ";
            CALL      HITENTER
            GOTO      DATE2000
          ENDIF
          MOVE        ZERO,EXIT
          GOTO        DATE9999
.
DATE9500  MOVE      ONE,EXIT
DATE9999  RETURN
+
.******************************************************************************
.*                                 BLNK0000               Called by: ML0000   *
.*                         Print A Blank Line (Y/N) ?                         *
.******************************************************************************
BLNK0000  MOVE      ZERO,BLNKLINE
          MOVE      ANSN,ANS
          DISPLAY   *P1:13,*EL,"Print a blank line between each admission (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ?";
BLNK1000  KEYIN     *P51:13,*V2LON,*RV,ANS;
          REP       UPPLOW,ANS
.
          ENDSET    ANS
          APPEND    SP1,ANS
          RESET     ANS
.
          MATCH     SP1,ANS
          GOTO      BLNK9500 IF EQUAL       * Exit from question
.
          MATCH     ANSY,ANS
          IF        @EQUAL
            MOVE      ONE,BLNKLINE          * Blank line
            GOTO      BLNK9000
          ENDIF
.
          MATCH     ANSN,ANS
          IF        @EQUAL
            MOVE      ZERO,BLNKLINE         * No blank line
            GOTO      BLNK9000
          ENDIF
.
          BEEP
          GOTO      BLNK1000
.
BLNK9000  MOVE      ZERO,EXIT
          GOTO      BLNK9999
.
BLNK9500  MOVE      ONE,EXIT
BLNK9999  RETURN
+
.******************************************************************************
.*                                  KHOS0000              Called by: ML0000   *
.*                            Enter The Date Ranges                           *
.******************************************************************************
KHOS0000  COMPARE   ONE,IBCNMHOS
          GOTO      KHOS9999 IF NOT EQUAL
.
          DISPLAY   *P1:15,*EL,"Hospital Id : ";
          MOVE      TEN5,ECOL
          MOVE      TEN5,EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS2000,KHOS9500
          GOTO      KHOS3000
.
KHOS2000  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          MOVE      ZERO,EXIT
.
KHOS3000  CLOSE     PATHSPA1
.
          DISPLAY   *P20:15,*V2LON,PTHSNAME;
          GOTO      KHOS9999
.
KHOS9500  MOVE      ONE,EXIT
KHOS9999  RETURN
+
.******************************************************************************
.*                                 LADM0000               Called by: ML0000   *
.*                        Load The Admission Details                          *
.******************************************************************************
LADM0000  MOVE      ZERO,TMPCOUNT
          DISPLAY   *P1:24,*EL,"Scanning :",*P25:24,"Loading : ";
          CALL      OPEN0000                * Open the tempfile
          PACK      KEY16,STRTDATE,SP20
          CALL      RDSMISS3                * Position on the admission file
LADM1000  CALL      RDKMISS3                * Read the admission file
          BRANCH    OVRCD,LADM9999
.
          MATCH     ADATE,ENDDATE
          GOTO      LADM9999 IF LESS        * Out of date range, exit
.
          IF        ASTAT=1 | ASTAT=5
            GOTO      LADM1000              * Ignore pre-admitted or cancelled
          ENDIF                               patients
.
          COMPARE   ONE,IBCNMHOS
          GOTO      LADM1100 IF NOT EQUAL
.
          MATCH     SP10,PTHSHOSP
          GOTO      LADM1100 IF EQUAL
.
          MOVE      AADMNO,KEY8
          CALL      RDPMVX11
          BRANCH    OVRCD,LADM1000
.
          MATCH     PMVXMHOS,PTHSHOSP
          GOTO      LADM1000 IF NOT EQUAL
.
LADM1100  DISPLAY   *P12:24,*V2LON,AADMNO;
          PACK      KEY8,AURNO
          CALL      RDMAST1                 * Read the master file
          BRANCH    OVRCD,LADM1000
.
          MOVE      ZERO,MTIFLAG
          MOVE      ZERO,THEACONT           * Reset theatre item counter
          MOVE      SP8,SAVTHDTE
          MOVE      SP2,SAVSESSN
          PACK      KEY22,AADMNO,SP20
          CALL      RDSDTRN1                * Position on the DTR file
LADM2000  CALL      RDKDTRN1                * Read the DTR file
          BRANCH    OVRCD,LADM4000
.
          MATCH     AADMNO,TADMNO
          GOTO      LADM4000 IF NOT EQUAL   * Different admin, get next admin
.
          COMPARE   TWO,TRECTYPE
          GOTO      LADM2000 IF NOT EQUAL   * Not a theatre item, read next
.
          PACK      THEADATE,DTFCENT,DTFYEAR,DTFMONTH,DTFDAY
          REP       " 0",THEADATE
.
LADM3000  MOVE      ZERO,FORM2
          MOVE      TSESSION,KEY2
          SQUEEZE   KEY2
          MOVE      KEY2,FORM2
          MOVE      FORM2,KEY2              * Right justify tsession
.
          MATCH     SAVTHDTE,THEADATE
          IF        @EQUAL
            MATCH     SAVSESSN,KEY2
            GOTO      LADM3200 IF EQUAL     * Same session & session date
          ENDIF
.
          MOVE      THEADATE,SAVTHDTE
          MOVE      KEY2,SAVSESSN
          ADD       ONE,THEACONT            * Add one to theatre item counter
          IF        THEACONT <= 1
           GOTO      LADM3200              * Get next theatre item record
          ENDIF
          GOTO      LADM8000
.
LADM3200  BRANCH    MTIFLAG,LADM5000
          GOTO      LADM2000
.
.         Check with theatre items in pmsmtiaf file
.
LADM4000  MOVE      ONE,MTIFLAG
          MOVE      "2",PMMIRTYP
          PACK      KEY15,AADMNO,PMMIRTYP,SP20
          CALL      RSPMMTI2                * Position on the item file
LADM5000  CALL      RKPMMTI2
          BRANCH    OVRCD,LADM1000          * get next admission
.
          MATCH     PMMIVISN,DAADMNO
          GOTO      LADM1000 IF NOT EQUAL   * get next admission
          MATCH     "2",PMMIRTYP
          GOTO      LADM1000 IF NOT EQUAL   * get next admission
.
          MOVE      PMMITDAT,THEADATE
          MOVE      PMMISESS,TSESSION
          GOTO      LADM3000
.
LADM8000  CALL      CLER0000                * Clear the tempfile variables
          MOVE      PURNO,DIM8
          MOVE      DIM8,URNO
          MOVE      AADMNO,ADMISS
          ADD       ONE,TMPCOUNT
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME                 * Format patient name
          MOVE      PACFNAME,PATNAME
          MOVE      PATNAME,PATNAME1
          MOVE      ADATE,ADMDATE
          UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format the admission date
          MOVE      CPDATE,DSPADDTE
          PACK      KEY8,AADMNO
          CALL      RDDSCH1                 * Read the discharge file
          IF        OVRCD<>1
            MOVE      DDATE,DSCDATE
            UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE               * Format the discharge date
            MOVE      CPDATE,DSPDSDTE
          ENDIF
          PACK      KEY19,URNO,ADMISS,TMPCOUNT
          CALL      WRTEMP3                 * Write to the 3rd tempfile
          DISPLAY   *P35:24,*V2LON,AADMNO;
          GOTO      LADM1000
.
LADM9999  RETURN
+
.******************************************************************************
.*                                 LDSC0000               Called by: ML0000   *
.*                        Load The Discharge Details                          *
.******************************************************************************
LDSC0000  MOVE      ZERO,TMPCOUNT
          DISPLAY   *P1:24,*EL,"Scanning :",*P25:24,"Loading : ";
          CALL      OPEN0000                * Open the tempfile
          PACK      KEY16,STRTDATE,SP20
          CALL      RDSDSCH2                * Position on the discharge file
LDSC1000  CALL      RDKDSCH2                * Read the discharge file
          BRANCH    OVRCD,LDSC9999
.
          MATCH     DDATE,ENDDATE
          GOTO      LDSC9999 IF LESS        * Out of date range, exit
.
.         Multi-Hospital Processing
.
          COMPARE   ONE,IBCNMHOS
          GOTO      LDSC1100 IF NOT EQUAL
.
          MATCH     SP10,PTHSHOSP
          GOTO      LDSC1100 IF EQUAL
.
          MOVE      DADMNO,KEY8
          CALL      RDPMVX11
          BRANCH    OVRCD,LDSC1000
.
          MATCH     PMVXMHOS,PTHSHOSP
          GOTO      LDSC1000 IF NOT EQUAL
.
LDSC1100  PACK      KEY8,DADMNO
          CALL      RDMISS1                 * Read the admission file
          BRANCH    OVRCD,LDSC1000
.
          COMPARE   THREE,ASTAT
          GOTO      LDSC1000 IF NOT EQUAL  * Patient not discharged, get next
.
          DISPLAY   *P12:24,*V2LON,DADMNO;
          PACK      KEY8,DURNO
          CALL      RDMAST1                 * Read the master file
          BRANCH    OVRCD,LDSC1000
.
          MOVE      ZERO,MTIFLAG
          MOVE      ZERO,THEACONT           * Reset theatre item counter
          MOVE      SP6,SAVTHDTE
          MOVE      SP2,SAVSESSN
          PACK      KEY22,DADMNO,SP20
          CALL      RDSDTRN1                * Position on the DTR file
LDSC2000  CALL      RDKDTRN1                * Read the DTR file
          BRANCH    OVRCD,LDSC4000
.
          MATCH     DADMNO,TADMNO
          GOTO      LDSC4000 IF NOT EQUAL   * Different admin, get next admin
.
          COMPARE   TWO,TRECTYPE
          GOTO      LDSC2000 IF NOT EQUAL   * Not a theatre item, read next
.
          PACK      THEADATE,DTFCENT,DTFYEAR,DTFMONTH,DTFDAY
          REP       " 0",THEADATE
.
LDSC3000  MOVE      ZERO,FORM2
          MOVE      TSESSION,KEY2
          SQUEEZE   KEY2
          MOVE      KEY2,FORM2
          MOVE      FORM2,KEY2              * Right justify tsession
.
          MATCH     SAVTHDTE,THEADATE
          IF        @EQUAL
            MATCH     SAVSESSN,KEY2
            GOTO      LDSC3200 IF EQUAL     * Same session & session date
          ENDIF
.
          MOVE      THEADATE,SAVTHDTE
          MOVE      KEY2,SAVSESSN
          ADD       ONE,THEACONT            * Add one to theatre item counter
          IF        THEACONT<=1
            GOTO      LDSC3200              * Get next theatre item record
          ENDIF
          GOTO      LDSC8000                                           *I-55666
.
LDSC3200  BRANCH    MTIFLAG,LDSC5000
          GOTO      LDSC2000
.
.         Check with theatre items in pmsmtiaf file
.
LDSC4000  MOVE      ONE,MTIFLAG
          MOVE      "2",PMMIRTYP
          PACK      KEY15,DADMNO,PMMIRTYP,SP20
          CALL      RSPMMTI2                * Position on the item file
LDSC5000  CALL      RKPMMTI2                * Read the item file
          BRANCH    OVRCD,LDSC1000
.
          MATCH     PMMIVISN,DDADMNO
          GOTO      LDSC1000 IF NOT EQUAL
          MATCH     "2",PMMIRTYP
          GOTO      LDSC1000 IF NOT EQUAL
.
          MOVE      PMMITDAT,THEADATE
          MOVE      PMMISESS,TSESSION
          GOTO      LDSC3000
.
LDSC8000  CALL      CLER0000                * Clear the tempfile variables
          MOVE      PURNO,DIM8
          MOVE      DIM8,URNO
          MOVE      DADMNO,ADMISS
          ADD       ONE,TMPCOUNT
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME                 * Format patient name
          MOVE      PACFNAME,PATNAME
          MOVE      PATNAME,PATNAME1 
          MOVE      ADATE,ADMDATE
          UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format the admission date
          MOVE      CPDATE,DSPADDTE
          MOVE      DDATE,DSCDATE
          UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format the discharge date
          MOVE      CPDATE,DSPDSDTE
          PACK      KEY19,URNO,ADMISS,TMPCOUNT
          CALL      WRTEMP3                 * Write to the 3rd tempfile
          DISPLAY   *P35:24,*V2LON,DADMNO;
          GOTO      LDSC1000
.
LDSC9999  RETURN
+
.******************************************************************************
.*                                 PRNT0000               Called by: ML0000   *
.*                             Print The Report                               *
.******************************************************************************
PRNT0000  DISPLAY   *P1:24,*EL,"Printing : ";
.
          MOVE      ZERO,PATCOUNT           * initialise patient count
          MOVE      ZERO,CPAGENO            * initialise page count
          CALL      HEAD0000                * Print the report heading
.
          PACK      KEY27,SP30
          PERFORM   OPTION,RSTEMP1,RSTEMP2  * Position on & read the 1st/2nd
PRNT1000  PERFORM   OPTION,RKTEMP1,RKTEMP2    tempfiles
          BRANCH    OVRCD,PRNT9000
.
          IF        CLNO>55
            CALL      UND132                * Print an underline
            CALL      HEAD0000              * Print the report heading
          ELSE
            IF        PATCOUNT > 0
              CALL      PBLK0000             * Print a blank line
            ENDIF
          ENDIF
.
          CALL      GTHT0000                * Get theatre details for printing
.
          MOVE      DSPADDTE,KEY8
          PRINT     *1,"|",URNO,*10,"|",ADMISS,*19,"|",PATNAME:
                    *44,"|",KEY8;
.
          UNPACK    DSPDSDTE,KEY8
          PRINT     *53,"|",KEY8,*62,"|",ADOCTORS[1];
.
          MOVE      AADMNO,KEY8
          CALL      RDPMVX11
          IF        OVRCD = 1
            CALL      CLPMSVX1
          ENDIF
.
PRNT1500  MATCH     SP70,PMVXUVTH
          IF        @EQUAL
            PRINT     *75,"|",PMVXUVTH;
          ELSE
            PACK      KEY5,ANSK,ANSB,PMVXUVTH,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              PRINT     *75,"|",PMVXUVTH;
            ELSE
              MOVE      TDESC,DIM17
              PRINT     *75,"|",DIM17;
            ENDIF
          ENDIF
. 
          MOVE      DSPTHDTE[1],KEY8
          PRINT     *93,"|",KEY8,*102,"|",THEASESN[1],"|",THEAITES[1],*132,"|"
          ADD       ONE,CLNO
          ADD       ONE,PATCOUNT
          DISPLAY   *P12:24,*V2LON,AADMNO;
.
          MOVE      ONE,FORM2
          WHILE     FORM2 < THEACONT
            ADD       ONE,FORM2
            MOVE      DSPADDTE,KEY8
            PRINT     *1,"|",URNO,*10,"|",ADMISS,*19,"|",PATNAME:
                      *44,"|",KEY8;
            MOVE      DSPDSDTE,KEY8
            PRINT     *53,"|",KEY8,*62,"|",ADOCTORS[FORM2]:
                      *75,"|",DIM17:
                      *93,"|",KEY8,*102,"|",THEASESN[FORM2],"|",THEAITES[FORM2]:
                      *132,"|"
.            PRINT     *1,"|",*10,"|",*19,"|",*44,"|",*53,"|":
.                      *62,"|",ADOCTORS[FORM2],*75,"|":
.                    *93,"|",KEY8,*102,"|",THEASESN[FORM2],"|",THEAITES[FORM2]:
.                      *132,"|"
            ADD       ONE,CLNO
          DO
          GOTO      PRNT1000
.
PRNT9000  CALL      UND132                  * Print an underline
          PRINT     *1,"Total Patients : ",PATCOUNT,*N,*N:
                    *1,"*** End of Report ***"
.
          DISPLAY   *P1:24,*EL,*B,"Report Generation Complete.  ";
          CALL      HITENTER
PRNT9999  RETURN
+
.******************************************************************************
.*                                 CLER0000               Called by: LADM0000 *
.*                       Clear The Tempfile Variables              & LDSC0000 *
.******************************************************************************
CLER0000  UNPACK    SP60,URNO,ADMISS,PATNAME,DSPADDTE,DSPDSDTE
          MOVE      PATNAME,PATNAME1
CLER9999  RETURN
+
.******************************************************************************
.*                                 HEAD0000               Called by: PRNT0000 *
.*                         Print The Report Heading                           *
.******************************************************************************
HEAD0000  MOVE      "- By ",KEY5
          PACK      CPHDROPT,KEY5,OPTIONDS
          CALL      HEAD132                 * Print main report heading
.
          IF        IBCNMHOS =1
            PRINT     *40,"Hospital : ",PTHSNAME
            ADD       "1",CLNO
          ENDIF
.
          PRINT     *40,"Starting ",OPTIONDS,": ",DSPSTDTE,*N:
                    *40,"Ending ",OPTIONDS,"  : ",DSPEDDTE,*N
          ADD       "3",CLNO
          CALL      UND132                  * Print an underline
.
          PRINT     *1,"|U/R No",*10,"|Admn No",*19,"|Patient Name":
                    *44,"|Adm Date",*53,"|Dsc Date",*62,"|Attending Dr":
                    *75,"|Unplanned Theatre":
                    *93,"|ItemDate",*102,"|Sn|Theatre Item",*132,"|"
          ADD       "1",CLNO
          CALL      UND132                  * Print an underline
.
HEAD9999  RETURN
+
.******************************************************************************
.*                                 GTHT0000               Called by: PRNT0000 *
.*                     Get Theatre Details For Printing                       *
.******************************************************************************
GTHT0000  MOVE      ZERO,FORM2
          REPEAT
            ADD       ONE,FORM2
            MOVE      SP30,THEAITES[FORM2]
            MOVE      SP2,THEASESN[FORM2]
            MOVE      SP20,ADOCTORS[FORM2]
          UNTIL     FORM2 > 98
          MOVE      ZERO,THEACONT           * Reset theatre item counter
          MOVE      SP8,SAVTHDTE
          MOVE      SP2,SAVSESSN
.
          MOVE      ADMISS,AADMNO
          PACK      KEY22,AADMNO,SP20
          CALL      RDSDTRN1                * Position on the DTR file
GTHT1000  CALL      RDKDTRN1                * Read the DTR file
          BRANCH    OVRCD,GTHT5000
.
          MATCH     AADMNO,TADMNO
          GOTO      GTHT5000 IF NOT EQUAL   * Different admin, get next admin
.
          COMPARE   TWO,TRECTYPE
          GOTO      GTHT1000 IF NOT EQUAL   * Not a theatre item, read next
.
          PACK      THEADATE,DTFCENT,DTFYEAR,DTFMONTH,DTFDAY
          REP       " 0",THEADATE
          MOVE      ZERO,FORM2
          MOVE      TSESSION,KEY2
          SQUEEZE   KEY2
          MOVE      KEY2,FORM2
          MOVE      FORM2,KEY2              * Right justify tsession
.
          MATCH     SAVTHDTE,THEADATE
          IF        @EQUAL
            MATCH     SAVSESSN,KEY2
            GOTO      GTHT1000 IF EQUAL     * Same session & session date
          ENDIF
.
          MOVE      THEADATE,SAVTHDTE
          MOVE      KEY2,SAVSESSN
          ADD       ONE,THEACONT            * Add one to theatre item counter
          CALL      GADC0000                * Get the attending doctor
          PACK      DSPTHDTE[THEACONT],DTFDAY,SLASH,DTFMONTH,SLASH,DTFYEAR
          REP       " 0",DSPTHDTE[THEACONT]
          CLEAR     DIM30
          STRIP     TITEMNO
          APPEND    TITEMNO,DIM30
          APPEND    SP1,DIM30
          APPEND    TDDESC,DIM30
          APPEND    SP30,DIM30
          RESET     DIM30
          MOVE      TSESSION,THEASESN[THEACONT]
          PACK      THEAITES[THEACONT],DIM30
.
          GOTO      GTHT1000
.
.                                                             * start *I-186955
.                                           * find Theatre Items in "pmsmtiaf"
GTHT5000  MOVE      "2",KEY1
          MOVE      AADMNO,DAADMNO
          PACK      KEY15,DAADMNO,KEY1,SP20
          CALL      RSPMMTI2                * Position on the item file
GTHT5500  CALL      RKPMMTI2
          BRANCH    OVRCD,GTHT9999          * finished
.
          MATCH     PMMIVISN,DAADMNO
          GOTO      GTHT9999 IF NOT EQUAL   * finished admission
          MATCH     "2",PMMIRTYP
          GOTO      GTHT9999 IF NOT EQUAL   * get next admission
.
          MOVE      PMMITDAT,THEADATE
          MOVE      PMMISESS,KEY2
          SQUEEZE   KEY2
          MOVE      KEY2,FORM2
          MOVE      FORM2,KEY2              * Right justify pmmisess
          MOVE      THEADATE,SAVTHDTE
          MOVE      KEY2,SAVSESSN
.
          ADD       ONE,THEACONT            * Add one to theatre item counter
          CALL      GADC0000                * Get the attending doctor
          UNPACK    PMMITDAT,DTFCENT,DTFYEAR,DTFMONTH,DTFDAY
          PACK      DSPTHDTE[THEACONT],DTFDAY,SLASH,DTFMONTH,SLASH,DTFYEAR
          REP       " 0",DSPTHDTE[THEACONT]
          CLEAR     DIM30
          STRIP     PMMIITEM
          APPEND    PMMIITEM,DIM30
          APPEND    SP1,DIM30
          APPEND    PMMIDESC,DIM30
          APPEND    SP30,DIM30
          RESET     DIM30
          MOVE      PMMISESS,THEASESN[THEACONT]
          PACK      THEAITES[THEACONT],DIM30
.
...       GOTO      GTHT5500                * (only print 1 Item - drop thru)
          GOTO      GTHT5500                                                  
.                                                             * end   *I-186955
.
GTHT9999  RETURN
+
.******************************************************************************
.*                                 PBLK0000               Called by: PRNT0000 *
.*                            Print A Blank Line                              *
.******************************************************************************
PBLK0000  IF        BLNKLINE=1
            PRINT     *1,"|",*10,"|",*19,"|",*44,"|":
                      *53,"|",*62,"|",*75,"|",*93,"|",*102,"|",*132,"|"
            ADD       ONE,CLNO
          ENDIF
PBLK9999  RETURN
+
.******************************************************************************
.*                                 GADC0000               Called by: GTHT0000 *
.*                         Get The Attending Doctor                           *
.******************************************************************************
GADC0000  PACK      ADOCTORS[THEACONT],SP30
.
          PACK      KEY30,AADMNO,THEADATE,Z30
          CALL      RDSTRAN2                * Position on & read the transfer
          CALL      RDPTRAN2                  file
          BRANCH    OVRCD,GADC1000
.
          MATCH     AADMNO,TADMN
          GOTO      GADC1000 IF NOT EQUAL   * Different admission number
.
          MATCH     SP6,TADOCT
          GOTO      GADC1000 IF EQUAL       * Blank transfer attending doctor
.
          PACK      KEY6,TADOCT             * Transfer attending doctor
          GOTO      GADC2000
.
GADC1000  PACK      KEY6,ADOCTA             * Admission attending doctor
GADC2000  CALL      RDDOCT1                 * Read the doctors file
          BRANCH    OVRCD,GADC9999
.
          MOVE      DSNAME,PACSNAME
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME                 * Format doctors name
          PACK      ADOCTORS[THEACONT],PACFNAME  * Attending doctor short desc
.
GADC9999  RETURN
+
.******************************************************************************
.*                                 OPEN0000               Called by: LADM0000 *
.*                             Open The Tempfile                   & LDSC0000 *
.******************************************************************************
OPEN0000  CALL      KILL0000                * Delete the tempfile
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME,CMDLINE
          BRANCH    OPTION,OPEN1000,OPEN2000
.                          Admin    Disch
.
OPEN1000  APPEND    FILELIN1,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID          * Create the tempfile
          OPEN      TEMP1,FNAME
          OPEN      TEMP3,FNAME
          GOTO      OPEN9000
.
OPEN2000  APPEND    FILELIN2,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID          * Create the tempfile
          OPEN      TEMP2,FNAME
          OPEN      TEMP3,FNAME
.
OPEN9000  CALL      CTMP0000                * clear temp file
.
OPEN9999  RETURN
+
.******************************************************************************
.*                                 KILL0000               Called by: ML0000   *
.*                            Delete The Tempfile                  & OPEN0000 *
.******************************************************************************
KILL0000  CLOSE     TEMP1
          CLOSE     TEMP2
          CLOSE     TEMP3
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
KILL9999  RETURN
+
.*****************************************************************************
.*                              CTMP0000           Called by:                *
.*                      Clear all temp file records                          *
.*****************************************************************************
.
CTMP0000  MOVE      SP20,KEY19
          CALL      RSTEMP3                      * position at start of file
          CALL      RKTEMP3                      * read next record
          BRANCH    OVRCD,CTMP9999               * eof - finished
.
          PACK      KEY19,URNO,ADMISS,TMPCOUNT
          CALL      DETEMP3                      * delete record
          GOTO      CTMP0000                     * get next record
.
CTMP9999  RETURN
+
.******************************************************************************
.*                       I/O Routines For The Tempfile                        *
.******************************************************************************
.
.         Index 1
.
RSTEMP1   RESET     KEY27
          READ      TEMP1,KEY27;;
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMP1;URNO,ADMISS,DTMPCONT,PATNAME,ADMDATE,DSPADDTE:
                          DSCDATE,DSPDSDTE,ADOCTA
          GOTO      OVERCOND IF OVER
          MOVE      DTMPCONT,TMPCOUNT
          RETURN
.
.         Index 2
.
RSTEMP2   RESET     KEY27
          READ      TEMP2,KEY27;;
          RETURN
.
RKTEMP2   MOVE      ZERO,OVRCD
          READKS    TEMP2;URNO,ADMISS,DTMPCONT,PATNAME,ADMDATE,DSPADDTE:
                          DSCDATE,DSPDSDTE,ADOCTA
          GOTO      OVERCOND IF OVER
          MOVE      DTMPCONT,TMPCOUNT
          RETURN
.
.         Index 3
.
RSTEMP3   RESET     KEY19
          READ      TEMP3,KEY19;;
          RETURN
.
RKTEMP3   MOVE      ZERO,OVRCD
          READKS    TEMP3;URNO,ADMISS,DTMPCONT,PATNAME,ADMDATE,DSPADDTE:
                          DSCDATE,DSPDSDTE,ADOCTA
          GOTO      OVERCOND IF OVER
          MOVE      DTMPCONT,TMPCOUNT
          RETURN
.
WRTEMP3   RESET     KEY19
          MOVE      TMPCOUNT,DTMPCONT
          WRITE     TEMP3,KEY19;URNO,ADMISS,DTMPCONT,PATNAME,ADMDATE,DSPADDTE:
                                DSCDATE,DSPDSDTE,ADOCTA
          RETURN
.
DETEMP3   DELETE    TEMP3,KEY19
          RETURN
+
.==============================================================================
.
          INC       STD001IO
          INC       CLPMSVX1
          INC       PATHSPKY
          INC       TFILENAM                     * Generate Temp File Name
.
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       PATCODIO/INC
          INC       PATDO1IO/INC            * Doctors file
          INC       PATDSCIO/INC            * Discharge file
          INC       PATDTRIO/INC            * Debtors file
          INC       PATHSPIO/INC
          INC       PATICDIO/INC            * ICD-9CM disease file
          INC       PATMA1IO/INC            * Master file
          INC       PATMI1IO/INC            * Admission file
          INC       PATTRNIO/INC            * Transfer file
          INC       PMSMTIIO/INC            * Misc.Theatre item file
          INC       PMSVX1IO/INC
          INC       WEBERRIO/INC                 * Web Server Error Logging
+
.


.***************************************************************************
.* System    :   Patient Management System                                 *
.* Program   :   IBAADT17                                                  *
.* Desc      :   Sameday Fee Listing                                       *
.***************************************************************************
.* Date      :   31/07/96                                                  *
.* Author    :   J.Tan                                                     *
.* Mods.     :                                                             * 
.*        V10.01.02 08/02/2011  Davin          CAR 233055                  *
.*                  Removed option 2 (new files)                           *
.*        V10.01.01 28/01/2011  Ebon Clements  CAR 233050                  *
.*                  Recompiled for changes to PATCMXFD                     *
.*        V9.12.04  14/12/09 J.Tan        CAR 212419                       *
.*                  Mods to allow selecting Contract ID from New file      *
.*        V9.12.03  25/11/09 J.Tan        CAR 211002                       *
.*                  Mods for range of contract ID                          *
.*        V9.12.02  09/09/09 WeeLee Koo   CAR 205303                       *
.*                  Added user keyin for Contract ID (CNID0000)            *
.*        V9.12.01  04/09/05 J.Tan    CAR 205303                           *
.*                  Added Contract ID to casemix files                     *
.*        V9.04.01  03/08/05 J.Tan    CAR 62750                            *
.*                  Removed redefined amount variables                     *
.*        V9.03.02  23/09/2003  CAR 42191   Mike Laming                    *
.*                  Add Revenue & Invoice Breakdown report                 *
.*        V9.03.01  30/05/2003  Dean Cramer    CAR 38200                   *
.*                  Fixed web heading overlap when specific funds entered  *
.*        V9.02.01  30/04/2003  Dean Cramer    CAR 38200                   *
.*                  Fixed heading details for entered ranges               *
.*        V6.06.01  09/02/1999  Jill Habasque                              *
.*                  Fixed DISPHEAD display                                 *
.***************************************************************************
          INC       STD001FD
          INC       PATASDFD/INC  
          INC       PATLSDFD/INC  
          INC       PATCMXFD/INC
          INC       PATCODFD/INC
          INC       PATSRBFD/INC            * Sameday Revenue B'down   *I-90302
          INC       PMSSINFD/INC            * Sameday Invoice B'down   *I-90302
+
.**********************************************************************
.*                       GLOBAL VARIABLES                             *
.**********************************************************************
.
ACLAIM    DIM       3
AFUNDH    DIM       6
CODE      DIM       2
CONTID    DIM       6                       * Contract ID (db)
COUNT     FORM      2                                                  *I-90302
DIM15     DIM       15
DIM15A    DIM       15
DIM20     DIM       20                                                 *I-90302
DIM20A    DIM       20                                                 *I-90302
ECOL1     FORM      2
FOPTION   FORM      1
FORM8P2   FORM      8.2
FRCLM     DIM       5
FRMCONTR  DIM       6                       * Contract ID
OVRSRB    FORM      1                                                  *I-90302
OVRINV    FORM      1                                                  *I-90302
TOCLM     DIM       5
ENDCONTR  DIM       6                       * Contract ID
TEMPAMNT  DIM       15                                                 *I-90302
TMPAMNT   DIM       15                                                 *I-90302
STRCLM    DIM       3
STRING1   DIM       6                       * Contract ID
ENDCLM    DIM       3
STRHF     DIM       6
ENDHF     DIM       6
PDSFLAG   FORM      1
SAVCONTR  DIM       6
.
PRGNAM    INIT      "Sameday Master Listing"
PRGID     INIT      "IBAADT17"
VERSION   INIT      "V12.01.00"
.
.**********************************************************************
.*                           MAINLINE                                 *
.*                       Controlling Logic                            *
.**********************************************************************
ML0000    CALL      INIT0000            * initialization
.                                       * open files
.                                       * display header
.
ML1000    CALL      MOPT0000            * Display main options
          BRANCH    EXIT,ML9999
          GOTO      ML2000
.
ML1500    CALL      PUTSCR00            * Redisplay screen
ML2000    CALL      OPTN0000            * select option
          BRANCH    EXIT,ML1000
.
          CALL      GETSCR00            * Save current screen
          CALL      OPFL0000            * Open file
          BRANCH    EXIT,ML1500
.
          CALL      KCLM0000            * Keyin range of claim code
          BRANCH    EXIT,ML1500
          CALL      KFUN0000            * Keyin range of health fund
.
          CALL      CNID0000            * Select Contract ID   
          CALL      DOPT0000            * print full description ?
.
ML2400    CALL      CONTQST             * OK to continue ?
          BRANCH    CEXIT,ML3000,ML1500,ML9999
          BEEP
          GOTO      ML2400
.
ML3000    CALL      PRIN0000            * print all lumpsum charge details
          GOTO      ML1500
.
ML9999    CHAIN     PGM
+
.**********************************************************************
.*                           INIT0000                                 *
.*               Open files and display header                        *
.**********************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P50:24,"Opening ";
.
          DISPLAY   *P60:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P60:24,"patcmxaf";
          OPEN      PATCMXA2,"patcmxaf"
.
INIT9999  RETURN
+
.**********************************************************************
.*                           MOPT0000                                 *
.*                   Display the main option screen                   *
.**********************************************************************
MOPT0000  MOVE       FALSE,EXIT
          CALL       DISPHEAD
          DISPLAY    *P45:2:
                     *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                     *P1:5,*V2LON,ONE,*HOFF," = Daily Charge":
                     *P1:6,*V2LON,TWO,*HOFF," = Additional Charge":
                     *P1:7,*V2LON,THREE,*HOFF," = Revenue and ":       *I-90302
                           "Invoice Breakdown ";                       *I-90302
.
MOPT1000  KEYIN      *P1:9,*EL,"Please Select : ",*V2LON,FOPTION;      *C-90302
          COMPARE    ZERO,FOPTION
          GOTO       MOPT9000 IF EQUAL
.
          BRANCH     FOPTION,MOPT2000,MOPT3000,MOPT4000                *C-90302
          BEEP
          GOTO       MOPT1000
.
MOPT2000  DISPLAY    *P51:2,*V2LON,*+," - Daily Charge";
          GOTO       MOPT9999
.
MOPT3000  DISPLAY    *P51:2,*V2LON,*+," - Additional Charge";
          GOTO       MOPT9999
.
MOPT4000  DISPLAY    *P15:2,SP1,*+,PRGNAM,*V2LON,*+:                   *I-90302
                            " - Revenue and Invoice Breakdown";        *I-90302
          GOTO       MOPT9999                                          *I-90302
.
MOPT9000  MOVE       TRUE,EXIT
MOPT9999  RETURN
+
.**********************************************************************
.*                           OPTN0000                                 *
.*                   Display the main option screen                   *
.**********************************************************************
OPTN0000  MOVE       FALSE,EXIT
          DISPLAY    *P1:11,*EF,*V2LON,ZERO,*HOFF," = Exit":
                     *P1:12,*V2LON,ONE,*HOFF," = Code Sequence ";
.
OPTN1000  KEYIN      *P1:15,*EL,"Please Select : ",*V2LON,OPTION;
          COMPARE    ZERO,OPTION
          GOTO       OPTN9000 IF EQUAL
.
          BRANCH     OPTION,OPTN9999
          BEEP
          GOTO       OPTN1000
.
OPTN9000  MOVE       TRUE,EXIT
OPTN9999  RETURN
+
.**********************************************************************
.*                             OPFL0000                               *
.*                Open casemix funding file                           *
.**********************************************************************
OPFL0000  IF        FOPTION = 1
            OPEN      PATLSDA1,"patlsdaf"                           
          ELSE
            IF        FOPTION = 2
              OPEN      PATASDA1,"patasdaf"
            ELSE                                                     *I-90302
              OPEN      PATLSDA1,"patlsdaf"     * lump sum           *I-90302
              OPEN      PATSRBA1,"patsrbaf"     * revenue b'down     *I-90302
              OPEN      PMSSINA1,"pmssinaf"     * invoice b'down     *I-90302
            ENDIF                                                    *I-90302
          ENDIF
          MOVE      ZERO,EXIT
OPFL9999  RETURN
+
.**********************************************************************
.*                             KCLM0000                               *
.*                keyin range of claim codes                          *
.**********************************************************************
KCLM0000  MOVE       ZERO,EXIT
          UNPACK     SP10,FRCLM,TOCLM
          MOVE       SP20,STRCLM
          MOVE       SP20,ENDCLM
.
          DISPLAY    *P1:4,*EF,"From Claim Code   :":
                     *P1:5,"To   Claim Code   :";
.
          MOVE      "4",EVERT
          MOVE      "20",ECOL               * keyin position
          MOVE      "30",ECOL1              * Display description position
          MOVE      SP3,ACODE
          MOVE      "CL",CODE
          CALL      KCOD0000                * keyin code
          BRANCH    EXIT,KCLM1000,KCLM9000  * Nothing,exit entered
          MOVE      ACODE,STRCLM  
          MOVE      STRCLM,FRCLM
          GOTO      KCLM5000
.
KCLM1000  DISPLAY   *PECOL:EVERT,*V2LON,"Start",*HOFF;
          MOVE      "START",FRCLM
          MOVE      ZERO,EXIT
.
KCLM5000  MOVE      "5",EVERT
          MOVE      SP3,ACODE
          CALL      KCOD0000                * keyin code
          BRANCH    EXIT,KCLM6000,KCLM9000  * Nothing,exit entered
          MOVE      ACODE,ENDCLM  
          MOVE      ENDCLM,TOCLM
          GOTO      KCLM9999
.
KCLM6000  DISPLAY   *PECOL:EVERT,*V2LON,"End",*HOFF;
          MOVE      "END  ",TOCLM
          MOVE      ZERO,EXIT
          GOTO      KCLM9999
.
KCLM9000  MOVE      ONE,EXIT
KCLM9999  RETURN
+
.**********************************************************************
.*                             KFUN0000                               *
.*                Keyin range of health funds                         *
.**********************************************************************
KFUN0000  MOVE      SP6,STRHF
          KEYIN     *P1:7,*EL,"Starting Health Fund : ",*V2LON,STRHF
          ENDSET    STRHF
          APPEND    SP6,STRHF
          RESET     STRHF
.
          MATCH     SP6,STRHF
          IF        @EQUAL
            DISPLAY   *P24:7,*V2LON,"Start"
          ELSE
            DISPLAY   *P24:7,*V2LON,STRHF
          ENDIF
.
          MOVE      SP6,ENDHF
          KEYIN     *P1:8,*EL,"Ending   Health Fund : ",*V2LON,ENDHF
          ENDSET    ENDHF
          APPEND    SP6,ENDHF
          RESET     ENDHF
.
          MATCH     SP6,ENDHF
          IF        @EQUAL
            DISPLAY   *P24:8,*V2LON,"End"
            MOVE      "ZZZZZZ",ENDHF
          ELSE
            DISPLAY   *P24:8,*V2LON,ENDHF
          ENDIF
.
          MATCH     STRHF,ENDHF
          GOTO      KFUN9999 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Ending H.F. < Starting H.F. **";
          CALL      HITENTER
          GOTO      KFUN0000
.
KFUN9999  RETURN
+
.******************************************************************************
.*                                 CNID0000              Called By : ML0000   *
.*                          Select Contract ID                                *
.******************************************************************************
.
CNID0000  MOVE      SP6,FRMCONTR
          KEYIN     *P1:10,*EL,"Starting Contract ID : ",*V2LON,FRMCONTR
          ENDSET    FRMCONTR
          APPEND    SP70,FRMCONTR
          RESET     FRMCONTR
.
          MATCH     SP70,FRMCONTR
          IF        @EQUAL
            DISPLAY   *P23:12,*EL,"Start";
            GOTO      CNID2000
          ENDIF
.
.         PACK      KEY24,FRMCONTR,SP70
.         CALL      RSPTCMX2
.         CALL      RKPTCMX2
.         BRANCH    OVRCD,CNID9000
.
.         MATCH     FRMCONTR,PTCMCNID
.         GOTO      CNID9000 IF NOT EQUAL
.
CNID2000  MOVE      SP6,ENDCONTR
          KEYIN     *P1:12,*EL,"Ending Contract ID : ",*V2LON,ENDCONTR
          ENDSET    ENDCONTR
          APPEND    SP70,ENDCONTR
          RESET     ENDCONTR
.
          MATCH     SP70,ENDCONTR
          IF        @EQUAL
            DISPLAY   *P23:12,*EL,"End";
            MOVE      "ZZZZZZ",ENDCONTR
            GOTO      CNID9999
          ENDIF
.
.         PACK      KEY24,ENDCONTR,SP70
.         CALL      RSPTCMX2
.         CALL      RKPTCMX2
.         BRANCH    OVRCD,CNID9000
.
.         MATCH     ENDCONTR,PTCMCNID
.         GOTO      CNID9000 IF NOT EQUAL
.
          GOTO      CNID9999
.
CNID9000  DISPLAY   *P1:24,*EL,*B,"Invalid Contract ID. ";
          CALL      HITENTER
          GOTO      CNID0000
.
CNID9999  RETURN
.
.******************************************************************************
.*                                 DOPT0000              Called By : ML0000   *
.*                         Print full description ?                           *
.******************************************************************************
DOPT0000  MOVE      ZERO,PDSFLAG
          MOVE      "N",ANS
          KEYIN     *P1:12,*EL,"Print full description (Y/N) ? ",*RV,*V2LON,ANS;
          REP       "yYnN",ANS
          CMATCH    "N",ANS
          GOTO      DOPT9999 IF EQUAL
          MOVE      ONE,PDSFLAG
          CMATCH    "Y",ANS
          GOTO      DOPT9999 IF EQUAL
          BEEP
          GOTO      DOPT0000
.
DOPT9999  RETURN
+
.******************************************************************************
.*                                 KCOD0000              Called By : ML0000   *
.*                         Keyin codes                                        *
.******************************************************************************
KCOD0000  CALL      KEYINCOD
          BRANCH    EXIT,KCOD1000:          * Nothing entered
                         KCOD2000           * Exitchar entered
.
KCOD1000  DISPLAY   *PECOL1:EVERT,*V2LON,TDESC;
KCOD2000  RETURN
+
.**********************************************************************
.*                             PRIN0000                               *
.*                Read file and print all lumpsum details             *
.**********************************************************************
PRIN0000  MOVE       ZERO,CPAGENO
          CLOCK      TIME,CTIMEIS
          MOVE       "60",CLNO
          MOVE       SP70,SAVCONTR
.
          DISPLAY    *P10:24,"Printing Claim Code : ":
                     *P35:24,*EL,*V2LON,PTLSCLMN,*P42:24,"- ",PTLSHFND;
.
          CALL       RSTEMP1                       * position to start of file
PRIN1000  CALL       RKTEMP1 
          BRANCH     OVRCD,PRIN8000
.
          DISPLAY    *P35:24,*EL,*V2LON,PTLSCLMN,*P42:24,"- ",PTLSHFND;
.
          MATCH      SP70,FRMCONTR
          IF         !@EQUAL
            MATCH      FRMCONTR,CONTID
            GOTO       PRIN8000 IF LESS
          ENDIF
.
          MATCH      SP70,ENDCONTR
          IF         !@EQUAL
            MATCH      CONTID,ENDCONTR
            GOTO       PRIN8000 IF LESS
          ENDIF
.
          MATCH      SP70,STRCLM
          IF         !@EQUAL
            MATCH      STRCLM,ACLAIM
            GOTO       PRIN1000 IF LESS
          ENDIF
.
          MATCH      SP70,ENDCLM
          IF         !@EQUAL
            MATCH      ACLAIM,ENDCLM
            GOTO       PRIN1000 IF LESS
          ENDIF
.
          MATCH      SP70,STRHF
          IF         !@EQUAL
            MATCH      STRHF,AFUNDH
            GOTO       PRIN1000 IF LESS
          ENDIF
.
          MATCH     SP6,ENDHF
          IF        !@EQUAL
            MATCH     AFUNDH,ENDHF
            GOTO      PRIN1000 IF LESS
          ENDIF
.
          MATCH      CONTID,SAVCONTR
          GOTO       PRIN6000 IF EQUAL
.
          MATCH      SP70,SAVCONTR
          IF         !@EQUAL
            CALL       UND132
          ENDIF
          MOVE       CONTID,SAVCONTR
          CALL       HEAD0000
.
PRIN6000  COMPARE    ONE,FOPTION
          CALL       PREC0000 IF EQUAL              * print lumpsum Record  
.
          COMPARE    TWO,FOPTION
          CALL       PREA0000 IF EQUAL              * print additional record
.
          COMPARE    THREE,FOPTION                                     *I-90302
          CALL       PREB0000 IF EQUAL             * print Rev. & Inv. Brkdwn
.
          GOTO       PRIN1000
.
PRIN8000  COMPARE    ZERO,CPAGENO
          GOTO       PRIN9000 IF EQUAL
.
          CALL       UND132
          PRINT      *N,"*** END OF REPORT  ***";
          GOTO       PRIN9999
.
PRIN9000  DISPLAY   *P1:24,*EL,*B,"No Data Available. ";
          CALL      HITENTER
.
PRIN9999  RETURN
+
.**********************************************************************
.*                                HEAD0000                            *
.*                          Print page header                         *
.**********************************************************************
HEAD0000  CALL      HEAD132
.
          IF        FOPTION = 1
            PRINT     *1,"** Lumpsum Charge";
          ELSE
            IF        FOPTION = 2
              PRINT     *1,"** Additional Charge";
            ELSE                                                       *I-90302
              PRINT     *1,"** Revenue and Invoice Breakdown";         *I-90302
            ENDIF                                                      *I-90302
          ENDIF
.
          PRINT     *40,"Claim Code from  : ",FRCLM:
                    *65," to : ",TOCLM,*N;
.
          PRINT     *40,"Health Fund from :";
.
          MATCH     SP6,STRHF
          GOTO      HEAD0100 IF EQUAL
.
          PRINT     *59,STRHF;
          GOTO      HEAD0200
.
HEAD0100  PRINT     *59,"START";
.
HEAD0200  PRINT     *65," to : ";
          MATCH     "ZZZZZZ",ENDHF
          GOTO      HEAD0300 IF EQUAL
.
          PRINT     *71,ENDHF,*N;
          GOTO      HEAD0400
.
HEAD0300  PRINT     *71,"END",*N;
.
HEAD0400  PRINT     *40,"Contract from :";
.
          MATCH     SP6,FRMCONTR
          GOTO      HEAD0600 IF EQUAL
.
          PRINT     *59,FRMCONTR;
          GOTO      HEAD0700
.
HEAD0600  PRINT     *59,"START";
.
HEAD0700  PRINT     *65," to : ";
          MATCH     "ZZZZZZ",ENDCONTR
          GOTO      HEAD0800 IF EQUAL
.
          PRINT     *71,ENDCONTR,*N;
          GOTO      HEAD0900
.
HEAD0800  PRINT     *71,"END",*N;
.
HEAD0900  PRINT     *3,"Contract ID      : ",SAVCONTR,*N;
.
          CALL      UND132
.
          BRANCH    FOPTION,HEAD1000,HEAD2000,HEAD3000
.
.         Lumpsum
.
HEAD1000  MOVE      ZERO,OVRCD              * FOPTION = 1
          PRINT     *1,"| Claim Code":
                    *27,"| Health":
                    *38,"| Table Type":
                    *52,"| DRG":
                    *68,"|Description":
                    *104,"|        Lumpsum",*132,"|":
                    *N,*1,"|",*27,"| Fund",*38,"|":
                    *52,"| MBS Band",*68,"|":
                    *104,"|  Rebate",*116,"|  Patient",*132,"|"
          GOTO      HEAD9000
.
.         Additional
.
HEAD2000  MOVE      ZERO,OVRCD              * FOPTION = 2
          PRINT     *1,"| Claim Code":
                    *24,"| Health":
                    *36,"| Table":
                    *45,"| DRG":
                    *61,"| Bed":
                    *68,"|Description":
                    *107,"|        Amount ",*132,"|":
                    *N,*1,"|",*24,"| Fund",*36,"| Type":
                    *45,"| MBS Band ",*61,"| Type",*68,"|":
                    *107,"|  Rebate",*119,"|  Patient",*132,"|"
          GOTO      HEAD9000
.
.* ***************************************** Start of Addition         *I-90302
.         Revenue and Invoice Breakdown
.
HEAD3000  MOVE      ZERO,OVRCD              * FOPTION = 3
          PRINT     *1,"| Claim Code":
                    *25,"| Health":
                    *34,"| Table":
                    *42,"| Casemix":
                    *52,"|   Revenue Breakdown":
                    *76,"|    Revenue":
                    *92,"|   Invoice Breakdown":
                    *116,"|    Invoice":
                    *132,"|":
                    *N,*1,"|",*25,"| Fund",*34,"| Type",*42,"|  Code":
                    *52,"|",*76,"|    Amount",*92,"|":
                    *116,"|    Amount",*132,"|"
          GOTO      HEAD9000
.* ***************************************** Start of Addition         *I-90302
.
HEAD9000  ADD       FOUR,CLNO
          CALL      UND132
.
HEAD9999  RETURN
+
.**********************************************************************
.*                                PREC0000                             *
.*                   Routine to print lumpsum record                   *
.**********************************************************************
PREC0000  COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          MOVE      SP20,DIM15
          MOVE      "CL",TCODE
          PACK      KEY5,TCODE,PTLSCLMN
          CALL      RDCODE1
          PACK      DIM15,TDESC
.
          MOVE      SP20,DIM15A
          MOVE      "HT",TCODE
          PACK      KEY5,TCODE,PTLSTABT
          CALL      RDCODE1
          PACK      DIM15A,TDESC
.
          MOVE      PTLSLREB,FORM8P2
          PRINT     *1,"| ",PTLSCLMN," -",DIM15:
                    *27,"| ",PTLSHFND:
                    *38,"|   ",PTLSTABT:
                    *52,"| ",PTLSCASM:
                    *68,"|",PTLSDES1:
                    *104,"|",FORM8P2,*116,"|",PTLSLPAT,*132,"|"
          ADD       ONE,CLNO
.
          IF        PDSFLAG = 1
             PRINT     *1,"|",*27,"|",*38,"|":
                       *52,"|",*68,"|",PTLSDES2:
                       *104,"|",*116,"|",*132,"|"
             ADD       ONE,CLNO
          ENDIF
.
PREC9999   RETURN
+
.**********************************************************************
.*                                PREA0000                             *
.*                   Routine to print an additional charge record      *
.**********************************************************************
PREA0000  COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          MOVE      SP20,DIM15
          MOVE      "CL",TCODE
          PACK      KEY5,TCODE,PTADCLMN
          CALL      RDCODE1
          PACK      DIM15,TDESC
.
          MOVE      PTADDREB,FORM8P2
          PRINT     *1,"|",PTADCLMN," -",DIM15:
                    *24,"| ",PTADHFND:
                    *36,"|  ",PTADTABT:
                    *45,"| ",PTADCASM:
                    *61,"| ",PTADBTYP:
                    *68,"|",PTADDES1:
                    *107,"|",FORM8P2;
.
          MOVE      PTADDPAT,FORM8P2
          PRINT     *119,"|",FORM8P2,*132,"|"
          ADD       ONE,CLNO
.
          IF        PDSFLAG = 1
             PRINT     *1,"|",*24,"|",*36,"|":
                       *45,"|",*61,"|":
                       *68,"|",PTADDES2,*107,"|",*119,"|",*132,"|"
             ADD       ONE,CLNO
          ENDIF
.
PREA9999   RETURN
+
.* ****************************************** Start of Addition        *I-90302
.***********************************************************************
.*                                PREB0000                             *
.*                   Routine to print a record (Rev. & Inv. Brkdwn)    *
.***********************************************************************
PREB0000  COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          MOVE      ZERO,COUNT
          MOVE      ZERO,OVRSRB
          MOVE      ZERO,OVRINV
          UNPACK    SP30,DIM20,TEMPAMNT
          UNPACK    SP30,DIM20A,TMPAMNT
.
          PACK      KEY30,FRMCONTR,PTLSCLMN,PTLSHFND,PTLSTABT,PTLSCASM,SP10
          CALL      RSPMSIN1
          CALL      RSPTSRB1
.
PREB1000  BRANCH    OVRSRB,PREB1500         * if no more "patsrbaf"
.
          CALL      RKPTSRB1                * read next Revenue B'down
          IF        OVRCD = 1
            MOVE      ONE,OVRSRB
            UNPACK    SP30,PTSBCODE,PTSBFUND,PTSBTBLT,PTSBCSCD
          ENDIF
.
          CALL      VALD0000                * make sure code is valid
.
PREB1500  BRANCH    OVRINV,PREB2000         * if no more "pmssinaf"
.
          CALL      RKPMSIN1                * read next Invoice B'down
          IF        OVRCD = 1
            MOVE      ONE,OVRINV
            UNPACK    SP30,PMSICODE,PMSIFUND,PMSITBLT,PMSICSCD
          ENDIF
.
          CALL      VLCD0000                * make sure invoice code is valid
.
PREB2000  IF        OVRSRB = 1 & OVRINV = 1
            GOTO      PREB9000              * End of both B'downs
          ENDIF
.
          ADD       ONE,COUNT
          MOVE      SP20,DIM15
          MOVE      "CL",TCODE
          PACK      KEY5,TCODE,PTSBCODE
          CALL      RDCODE1
          PACK      DIM15,TDESC
.
          IF        COUNT = 1
            PRINT     *1,"| ",PTLSCLMN," -",DIM15:
                      *25,"| ",PTLSHFND:
                      *34,"| ",PTLSTABT:
                      *42,"| ",PTLSCASM:
                      *52,"| ",DIM20:
                      *76,"|",TEMPAMNT:
                      *92,"| ",DIM20A:
                      *116,"|",TMPAMNT:
                      *132,"|"
          ELSE
            PRINT       *1,"|",*25,"|",*34,"|",*42,"|":
                        *52,"| ",DIM20,*76,"|",TEMPAMNT:
                        *92,"| ",DIM20A,*116,"|",TMPAMNT:
                        *132,"|"
          ENDIF
.
          ADD       ONE,CLNO
          GOTO      PREB1000
.
.
.
PREB9000  COMPARE   ZERO,COUNT
          IF        !@EQUAL
            PRINT       *1,"|",*25,"|",*34,"|",*42,"|":
                        *52,"|",*76,"|",*92,"|",*116,"|",*132,"|"
            ADD       ONE,CLNO
          ENDIF
.
PREB9999  RETURN
+
.******************************************************************************
.*                             VALD0000                                       *
.*      Check if the Overnight Revenue Breakdown record is valid              *
.******************************************************************************
VALD0000  MOVE      ZERO,EXIT
.
          MATCH     PTSBCNID,FRMCONTR
          GOTO      VALD9500 IF NOT EQUAL
.
          MATCH     PTSBCODE,PTLSCLMN
          GOTO      VALD9500 IF NOT EQUAL
.
          MATCH     PTSBFUND,PTLSHFND
          GOTO      VALD9500 IF NOT EQUAL
.
          MATCH     PTSBTBLT,PTLSTABT
          GOTO      VALD9500 IF NOT EQUAL
.
          MATCH     PTSBCSCD,PTLSCASM
          GOTO      VALD9500 IF NOT EQUAL
.
.
VALD9000  MOVE      SP20,DIM20
          MOVE      "FI",TCODE
          PACK      KEY5,TCODE,PTSBBRCD      * Breakdown Code
          CALL      RDCODE1
          PACK      DIM20,TDESC,SP10
          MOVE      PTSBAMNT,TEMPAMNT
          MOVE      ZERO,EXIT
          GOTO      VALD9999
.
VALD9500  MOVE      SP20,DIM20
          MOVE      SP15,TEMPAMNT
          MOVE      ONE,OVRSRB              * end of this Rev. B'down
          MOVE      ONE,EXIT
.
VALD9999  RETURN
.
+
.******************************************************************************
.*                             VLCD0000                                       *
.*      Check if the Overnight Invoice Breakdown record is valid              *
.******************************************************************************
VLCD0000  MOVE      ZERO,EXIT
.
          MATCH     PMSICNID,FRMCONTR
          GOTO      VLCD9500 IF NOT EQUAL
.
          MATCH     PMSICODE,PTLSCLMN
          GOTO      VLCD9500 IF NOT EQUAL
.
          MATCH     PMSIFUND,PTLSHFND
          GOTO      VLCD9500 IF NOT EQUAL
.
          MATCH     PMSITBLT,PTLSTABT
          GOTO      VLCD9500 IF NOT EQUAL
.
          MATCH     PMSICSCD,PTLSCASM
          GOTO      VLCD9500 IF NOT EQUAL
.
VLCD9000  MOVE      SP20,DIM20A
          MOVE      "FI",TCODE
          PACK      KEY5,TCODE,PMSIBKCD      * Breakdown Code
          CALL      RDCODE1
          PACK      DIM20A,TDESC,SP10
          MOVE      PMSIAMNT,TMPAMNT
          MOVE      ZERO,EXIT
          GOTO      VLCD9999
.
VLCD9500  MOVE      SP20,DIM20A
          MOVE      SP15,TMPAMNT
          MOVE      ONE,OVRINV              * end of this Inv. B'down
          MOVE      ONE,EXIT
.
VLCD9999  RETURN
.* ****************************************** End of Addition          *I-90302
+
.**********************************************************************
.*                                I/O                                  *
.**********************************************************************
RSTEMP1   IF         FOPTION = 1 | FOPTION = 3
            MATCH      SP70,FRMCONTR
            IF         @EQUAL
              PACK       KEY27,SP70
            ELSE
              MATCH      SP3,STRCLM
              IF         @EQUAL
                PACK       KEY27,FRMCONTR,STRCLM,SP20
              ELSE
                PACK       KEY27,FRMCONTR,STRCLM,STRHF,SP20
              ENDIF
            ENDIF
            CALL       RSPTLSD1             * position read on lumpsum file
          ELSE
            MATCH      SP70,FRMCONTR
            IF         @EQUAL
              PACK       KEY30,SP70
            ELSE
              MATCH      SP3,STRCLM
              IF         @EQUAL
                PACK       KEY30,FRMCONTR,STRCLM,SP20
              ELSE
                PACK       KEY30,FRMCONTR,STRCLM,STRHF,SP20
              ENDIF
            ENDIF
            CALL       RSPTASD1             * position read on additional file
          ENDIF
          RETURN
.
RKTEMP1   IF         FOPTION = 1 | FOPTION = 3
            CALL       RKPTLSD1             * read next recod of lumpsum file
            MOVE       PTLSCLMN,ACLAIM
            MOVE       PTLSHFND,AFUNDH
            MOVE       PTLSCNID,CONTID
          ELSE
            CALL       RKPTASD1             * read next recod of additional file
            MOVE       PTADCLMN,ACLAIM
            MOVE       PTADHFND,AFUNDH
            MOVE       PTADCNID,CONTID
          ENDIF
          RETURN
+
.******************************************************************************
.
          INC       STD001IO
          INC       PATDSCOD
          INC       KEYINCOD
          INC       PATASDIO/INC
          INC       PATLSDIO/INC
          INC       PATCMXIO/INC
          INC       PATCODIO/INC
          INC       PATSRBIO/INC            * Sameday Revenue B'down   *I-90302
          INC       PMSSINIO/INC            * Sameday Invoice B'down   *I-90302

.**********************************************************************
.* System   : OUTPATIENT                                              *
.* Program  : IBAOUT29                                                *
.* Desc     : Outpatient/Inpatient Visit Report By Date               *
.**********************************************************************
.* Date     :  02/07/2009                                             *
.* Author   :  Ebon Clements                                          *
.*                                                                    *
.* Mods     :                                                         *
.*        V12.00.01 16/05/2025  J.Tan          TSK 0955096            *
.*                  Added alpanumeric visit number generation         *
.*        V10.08.01 15/06/2016  Ebon Clements TSK 0318672             *
.*                  Added count OP appointments by associated number  *
.*                  functionality to patient programs                 *
.*        V10.04.01 14/07/2014  Ebon Clements    CAR 261630           *
.*                  Removed port number from temp file name           *
.*        V10.03.01 24/01/2012  Peter McMullen   CAR 255297           *
.*                  Fetch HFund from PMI if not in OP Booking.        *
.*        V10.02.01 11/08/2011  Ebon Clements    CAR 244899           *
.*                  Fixed PRGID                                       *
.*        V10.00.01 28/06/2010  Ebon Clements    CAR 224792           *
.*                  Fixed open on TEMPFIL3                            *
.*        V9.12.00  02/07/2009  Ebon Clements    CAR 190939           *
.*                  Created program                                   *
.**********************************************************************
.
          INC       STD001FD
          INC       IBACONFD/INC     * Control file
          INC       IBASEQFD/INC
          INC       PATCODFD/INC     * Codes file
          INC       PATVISFD/INC     * Visit File
.
          INC       PATCONFD/INC     * Patient control file
          INC       PATFN1FD/INC     * Health fund
          INC       PATMA1FD/INC     * PMI
          INC       PATMI1FD/INC     * Admission file
          INC       PATHSPFD/INC     * Hospital file
          INC       PMSVX1FD/INC     * Visit Extn
          INC       OUTBOAFD/INC     * Clinic booking file
          INC       OUTBB1FD/INC     * Clinic session booking table
          INC       OUTCONFD/INC
          INC       OUTSITFD/INC     * Site file
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
.
.**********************************************************************
.*                         TEMPFILE DEFINITIONS                       *
.**********************************************************************
.
TEMPFIL1  IFILE     SQL, FIXED=61, KEY="U1-8,9-16"
TEMPFIL2  IFILE     SQL, FIXED=61, KEY="U33-35,36-41,1-8,60-61,17-24,25-32,9-16"
TEMPFIL3  IFILE     SQL, FIXED=61, KEY="U36-41,33-35,1-8,60-61,17-24,25-32,9-16"
.
.NAME     TYPE     LENGTH  PHYSICAL   START     DESCRIPTION
.----     ----     ------  --------   -----     -----------
TEMPURNO  DIM           8         8       1     U/R Number
TEMPVISN  DIM           8         8       9     Visit Number
TEMPDATE  DIM           8         8      17     Visit Date
TEMPTIME  DIM           8         8      25     Visit Time
TEMPCLAM  DIM           3         3      33     Claim Code (Cat CL)
TEMPFUND  DIM           6         6      36     Health Fund
TEMPSTYP  DIM           3         3      42     O/Patient Slot Type (Cat CV)
TEMPCLIN  DIM           6         6      45     Clinic Id
TEMPCTYP  DIM           6         6      51     Clinic Type
TEMPATYP  DIM           3         3      57     Admission Type (Cat A)
TEMPVTYP  DIM           1         1      60     Visit Type (Inp/Out)
.
.                        end of record   61    
.
ERASE     INIT      "iserase "
ISBUILD   INIT      "isbuild "
UKEY      INIT      " 61 U1-8,9-16 U33-35,36-41,1-8,60-61,17-24,25-32,9-16 ":
                    "U36-41,33-35,1-8,60-61,17-24,25-32,9-16"
ZERO8     INIT      "0000    "
.
CHKOUTNO  DIM       8
CMDLINE   DIM       127
CODE      DIM       2
REPFDATE  DIM       8
REPFFUND  DIM       6
REPTFUND  DIM       6
REPFCLAM  DIM       3
REPPCLAM  FORM      1
REPPFUND  FORM      1
REPVTYPE  FORM      1
REPTSORT  FORM      1
REPTCLAM  DIM       3
REPTDATE  DIM       8
.
SAVECLAM  DIM       3
SAVEFUND  DIM       6
SAVEURTY  DIM       9
.
TEMPFILE  DIM       8
.
PRFRDATE  DIM       10
PRTODATE  DIM       10
PRFRCLAM  DIM       20
PRTOCLAM  DIM       20
PRFRVTYP  DIM       25
PRFRFUND  DIM       40
PRTOFUND  DIM       40
PRTSTYPE  DIM       10
PRTATYPE  DIM       10
PRTNAME   DIM       30
PRTVTYPE  DIM       3
PRTVTIME  DIM       5
PRTVCNTR  FORM      6
VISCOUNT  FORM      6
.
PRGID     INIT      "IBAOUT29"
PRGNAM    INIT      "Visit Report By Date"
VERSION   INIT      "V12.01.00"
.
.**********************************************************************
.*                         MAINLINE                                   *
.**********************************************************************
.
ML0000    CALL      INIT0000        * Initialisation
.
ML1000    CALL      OPTN0000        * Select option
          BRANCH    EXIT,ML9000
.
          BRANCH    OPTION,ML2000
.
ML2000    CALL      KSRT0000        * Keyin sort order
          CALL      KCLM0000        * keyin claim code range
          CALL      KFUN0000        * keyin health fund range
          CALL      KCPG0000        * Keyin new page for each claim code
          CALL      KHPG0000        * Keyin new page for each health fund
          CALL      KDAT0000        * keyin date range
          CALL      KVTY0000        * Keyin visit type
          CALL      KHOS0000        * Keyin hospital code
.
          CALL      CONTQST         * Ok to continue
          BRANCH    CEXIT,ML3000,ML2000,ML9000    * 1=Yes, 2=No, 3=Cancel
.
ML3000    CALL      CREA0000        * Create tempfile
.
          CALL      LINP0000        * Load inpatient data
          CALL      LOUT0000        * Load outpatient data
          CALL      PRIN0000        * Print routine
          CALL      ENDP0000        * End of report
.
          CALL      KILL0000        * Remove tempfile
          GOTO      ML1000
.
ML9000    CHAIN     PGM
.
.**********************************************************************
.*                         INIT0000                                   *
.*                      Initialisation                                *
.**********************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA1,"patvisaf"
.
          DISPLAY   *P64:24,"patfn1af";
          OPEN      PATFN1A1,"patfn1af"
.
          DISPLAY   *P64:24,"pathspaf"
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P64:24,"patmi1af"
          OPEN      PATMI1A3,"patmi1af"
.
          DISPLAY   *P64:24,"pmsvx1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          PACK      CFNAME WITH UID,FILBB1A2        * OUTBB1FD
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTBB1A2,CFNAME
.
          PACK      CFNAME WITH UID,FILBOKA1        * OUTBOKFD
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTBOKA1,CFNAME
.
          PACK      CFNAME WITH UID,FILBOKA5        * OUTBOKFD
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTBOKA6,CFNAME
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,TEMPFILE
.
          DISPLAY   *P64:24,"outsitaf";
          OPEN      OUTSITA1,"outsitaf"
          MOVE      IDDD,KEY6
          CALL      RDSITA1
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,THIRTY2;*143,OTCNCASS
.
          CALL      IBACLOCK
.
          RETURN
.**************************************************************************
.*                              CREA0000                                  *
.*             create a new temporary file                                *
.**************************************************************************
.
CREA0000  CALL      KILL0000                     * remove existing file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,TEMPFILE,UKEY
          EXECUTE   CMDLINE,TASKID               * create temporary index file
.
          OPEN      TEMPFIL1,TEMPFILE            * open temp index 1 file
          OPEN      TEMPFIL2,TEMPFILE            * open temp index 2 file
          OPEN      TEMPFIL3,TEMPFILE            * open temp index 3 file
.
CREA9999  RETURN
.
.****************************************************************************
.*                              KILL0000                                    *
.*               close and erase the temporary file                         *
.****************************************************************************
.
KILL0000  CLOSE     TEMPFIL1                     * close file
          CLOSE     TEMPFIL2                     * close file
          CLOSE     TEMPFIL3                     * close file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ERASE,TEMPFILE       * set file erase command
          EXECUTE   CMDLINE,TASKID               * erase temp file
.
KILL9999 RETURN
.
.**********************************************************************
.*                      OPTN0000                                      *
.*                      Main Option Screen                            *
.**********************************************************************
.
OPTN0000  MOVE      FALSE,EXIT
          HLINE     *G37,2,53,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Visit Report By Date";
.
OPTN1000  DISPLAY   *P1:7,*EL,"Option : ";
          KEYIN     *P10:7,*V2LON,OPTION;
.
          BRANCH    OPTION,OPTN9999
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9000 IF EQUAL
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      TRUE,EXIT
.
OPTN9999  RETURN
.
.**********************************************************************
.*                      KSRT0000                                      *
.*                 Keyin sort order                                   *
.**********************************************************************
KSRT0000    MOVE       ZERO,REPTSORT
            KEYIN      *P1:9,*EL,"Report Option":
                       " : ",*P17:9,*V2LON,*RV,REPTSORT;
.
            COMPARE    ZERO,REPTSORT
            IF         @EQUAL
              DISPLAY    *P19:9,"Group By Claim Code"
              GOTO       KSRT9999
            ENDIF
.
            COMPARE    ONE,REPTSORT
            IF         @EQUAL
              DISPLAY    *P19:9,"Group By Health Fund"
              GOTO       KSRT9999
            ENDIF
.
            GOTO    KSRT0000
.
KSRT9999    RETURN
.
.**********************************************************************
.*                             KCLM0000                               *
.*                keyin range of claim codes                          *
.**********************************************************************
KCLM0000  MOVE       ZERO,EXIT
          MOVE       SP20,REPFCLAM
          MOVE       SP20,REPTCLAM
.         
          DISPLAY    *P1:10,*EF,"From Claim Code   :":
                         *P1:11,"To   Claim Code   :";
.                        
          MOVE      "10",EVERT
          MOVE      "21",ECOL               * keyin position
          PACK      CODE,ANSC,ANSL,SP70
          MOVE      SP3,ACODE               
          CALL      PATCODKY                * keyin code
          BRANCH    EXIT,KCLM2000,KCLM2000  
          MOVE      ACODE,REPFCLAM
          DISPLAY   *P25:10,*EL,TDESC
          MOVE      TDESC,PRFRCLAM
          GOTO      KCLM3000
.         
KCLM2000  DISPLAY   *P25:10,*EL,"Start"
          MOVE      "Start",PRFRCLAM
.
KCLM3000  MOVE      "11",EVERT
          MOVE      SP3,ACODE
          CALL      PATCODKY                * keyin code
          BRANCH    EXIT,KCLM4000,KCLM4000
          MOVE      ACODE,REPTCLAM
          DISPLAY   *P25:11,*EL,TDESC
          MOVE      TDESC,PRTOCLAM
          GOTO      KCLM9999
.
KCLM4000  DISPLAY   *P25:11,*EL,"Finish"
          MOVE      "Finish",PRTOCLAM
          GOTO      KCLM9999
.
KCLM9000  MOVE      ONE,EXIT
KCLM9999  RETURN
.
.**********************************************************************
.*                             KFUN0000                               *
.*                Keyin range of health funds                         *
.**********************************************************************
KFUN0000  MOVE      SP6,REPFFUND
          DISPLAY   *P24:12,*EL:
                    *P1:12,*EL,"From Health Fund  : ";
.         
          MOVE      TEN2,EVERT
          MOVE      TWENTY1,ECOL
          MOVE      ZERO,CKYIMAND
          CALL      PATFNDKY
.
          IF        EXIT<>0
            MOVE      "Start",HFNAME
          ENDIF
.
          MOVE      HCODE,REPFFUND
          MOVE      HFNAME,PRFRFUND
.
          DISPLAY   *P29:12,HFNAME;
.
          MOVE      SP6,REPTFUND
          DISPLAY   *P1:13,*EL,"To Health Fund    : ";
.
          MOVE      TEN3,EVERT
          MOVE      TWENTY1,ECOL
          MOVE      ZERO,CKYIMAND
          CALL      PATFNDKY
.
          IF        EXIT<>0
            MOVE      "Finish",HFNAME
          ENDIF
.
          MOVE      HCODE,REPTFUND
          MOVE      HFNAME,PRTOFUND
.
          DISPLAY   *P29:13,HFNAME;
.
          RESET     REPTFUND
          MATCH     SP6,REPTFUND
          GOTO      KFUN1000 IF NOT EQUAL
.
KFUN1000  ENDSET    REPTFUND
          APPEND    SP6,REPTFUND
          RESET     REPTFUND
.
          MATCH     REPFFUND,REPTFUND
          GOTO      KFUN9999 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** To H.F. < From H.F. **";
          CALL      HITENTER
          GOTO      KFUN0000
.
KFUN9999  RETURN
.
.**********************************************************************
.*                      KCPG0000                                      *
.*                 Keyin new page for each claim code                 *
.**********************************************************************
KCPG0000    MOVE       ZERO,REPPCLAM
            MOVE       "N",ANS
            KEYIN      *P1:14,*EL,"New Page for Each Claim Code":
                        "  : ",*P33:14,*V2LON,*RV,ANS;
            RESET      ANS
            REP        UPPLOW,ANS
.
            CMATCH     "Y",ANS
            GOTO       KCPG9999 IF NOT EQUAL
.
            MOVE       ONE,REPPCLAM
KCPG9999    RETURN
.
.**********************************************************************
.*                      KHPG0000                                      *
.*                 Keyin new page for each health fund                *
.**********************************************************************
KHPG0000    MOVE       ZERO,REPPFUND
            MOVE       "N",ANS
            KEYIN      *P1:15,*EL,"New Page for Each Health Fund":
                       " : ",*P33:15,*V2LON,*RV,ANS;
            RESET      ANS
            REP        UPPLOW,ANS
.
            CMATCH     "Y",ANS
            GOTO       KHPG9999 IF NOT EQUAL
.
            MOVE       ONE,REPPFUND
KHPG9999    RETURN
.
.******************************************************************************
.*                                  KDAT0000                                  *
.*                            Keyin The Date Range                            *
.******************************************************************************
KDAT0000  MOVE      "13",CCOL
          DISPLAY   *P1:16,"From Date : ",*EF:
                    *P1:17,"To Date   : ";
.
          MOVE      ZERO,CCANLDTE            * Blank cancel default
.
. ----- Keyin start date -----
.
KDAT1000  MOVE      "16",CVERT
          MOVE      CCC,CCENT
          MOVE      CYY,CYEAR
          MOVE      CMM,CMON
          MOVE      CDD,CDAY

          CALL      KEYDATE                 * Keyin date
          BRANCH    OVRCD,KDAT9500          * Nothing entered
          BRANCH    CQUEST,KDAT1000
.
          PACK      REPFDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",REPFDATE
.
. ----- Keyin end date -----
.
KDAT2000  MOVE      "17",CVERT
          UNPACK    REPFDATE,CCENT,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin date
          BRANCH    OVRCD,KDAT1000          * Nothing entered
          BRANCH    CQUEST,KDAT2000
.
          PACK      REPTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",REPTDATE
.
          MATCH     REPFDATE,REPTDATE
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"End date must after the start date.  ";
            CALL      HITENTER
            GOTO      KDAT2000
          ENDIF
.
          UNPACK    REPFDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,PRFRDATE         * formatted start date
.
          UNPACK    REPTDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,PRTODATE         * formatted start date
.
KDAT9000  MOVE      ZERO,EXIT
          GOTO      KDAT9999
.
KDAT9500  MOVE      ONE,EXIT
KDAT9999  RETURN
.
.
.**********************************************************************
.*                      KVTY0000                                      *
.*                 Keyin visit type                                   *
.**********************************************************************
KVTY0000    MOVE       ZERO,REPVTYPE
            MOVE       ZERO,ANS
            KEYIN      *P1:18,*EL,"Visit Type":
                       " : ",*P14:18,*V2LON,REPVTYPE;
.
            COMPARE    ZERO,REPVTYPE
            IF         @EQUAL
              DISPLAY    *P16:18,"Inpatient & Outpatient"
              MOVE       "Inpatient & Outpatient",PRFRVTYP
              GOTO       KVTY9999
            ENDIF
.
            COMPARE    ONE,REPVTYPE
            IF         @EQUAL
              DISPLAY    *P16:18,"Outpatient"
              MOVE       "Outpatient",PRFRVTYP
              GOTO       KVTY9999
            ENDIF
.
            COMPARE    TWO,REPVTYPE
            IF         @EQUAL
              DISPLAY    *P16:18,"Inpatient"
              MOVE       "Inpatient",PRFRVTYP
              GOTO       KVTY9999
            ENDIF
.
            GOTO    KVTY0000
.
KVTY9999    RETURN
.
.******************************************************************************
.*                                  KHOS0000                                  *
.*                            keyin hospital                                  *
.******************************************************************************
KHOS0000  COMPARE   ONE,IBCNMHOS
          GOTO      KHOS9999 IF NOT EQUAL
.
          MOVE      TEN9,CVERT
          DISPLAY   *P1:CVERT,*EL,"Hospital Id : ";
          MOVE      TEN5,ECOL
          MOVE      CVERT,EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ONE,CKYIMAND           * Mandatory
.
          OPEN      PATHSPA1,"pathspaf"
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS9999,KHOS9999
.
KHOS2000  DISPLAY   *P20:CVERT,PTHSNAME;
          GOTO      KHOS9999
.
KHOS9999  RETURN
.
.**********************************************************************
.*                      LOUT0000                                      *
.*                      Load outpatient data                          *
.**********************************************************************
LOUT0000  IF        REPVTYPE=2
            GOTO      LOUT9999              * Inpatients Only
          ENDIF
.
          PACK      KEY16,REPFDATE,SP70
          CALL      RDSBOKB2
LOUT1000  CALL      RDKBOKB2
          BRANCH    OVRCD,LOUT9999
.         
          MATCH     OBADATE,REPTDATE
          GOTO      LOUT9999 IF LESS
.         
          MOVE      OBAOUTNO,CHKOUTNO
          PACK      KEY36,OBAOUTNO,OBADATE,SP70
          CALL      RDSBOKA6
          CALL      RDKBOKA6
          BRANCH    OVRCD,LOUT1000
.
          MATCH     OBASITE,IDDD                      * Site Code
          GOTO      LOUT1000 IF NOT EQUAL
.
          MATCH     OBAOUTNO,CHKOUTNO
          GOTO      LOUT1000 IF NOT EQUAL
.
          COMPARE   FOUR,OBASTAT                      * Attended status
          GOTO      LOUT1000 IF NOT EQUAL
.
          PACK      KEY8,OBAOUTNO,SP70
          CALL      RDPMVX11
          BRANCH    OVRCD,LOUT1000
.
          IF        IBCNMHOS=1
            MATCH     PMVXMHOS,PTHSHOSP               * Hospital
            GOTO      LOUT1000 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,OTBBFUND
          IF        @EQUAL
            PACK      KEY8,OBAURNO,SP70
            CALL      RDMAST1
            IF        OVRCD=0
              MOVE      PFUNDH,OTBBFUND
            ENDIF
          ENDIF
. 
          MATCH     SP70,REPFFUND
          IF        !@EQUAL
            MATCH    REPFFUND,OTBBFUND                * H/F From
            GOTO     LOUT1000 IF LESS
          ENDIF
.
          MATCH     SP70,REPTFUND
          IF        !@EQUAL
            MATCH    OTBBFUND,REPTFUND                * H/F To
            GOTO     LOUT1000 IF LESS
          ENDIF
.
          MATCH     SP70,REPFCLAM
          IF        !@EQUAL
            MATCH    REPFCLAM,OBACOMP                 * From Claim Code
            GOTO     LOUT1000 IF LESS
          ENDIF
.
          MATCH     SP70,REPTCLAM
          IF        !@EQUAL
            MATCH    OBACOMP,REPTCLAM                  * To Claim Code
            GOTO     LOUT1000 IF LESS
          ENDIF
.
          MOVE      OBAURNO,TEMPURNO
          MOVE      OBAOUTNO,TEMPVISN
          MOVE      OBADATE,TEMPDATE
          MOVE      OBATIME,TEMPTIME
          MOVE      OBACOMP,TEMPCLAM
          MOVE      OTBBFUND,TEMPFUND
          MOVE      OBAVISIT,TEMPSTYP
          MOVE      OBACLIN,TEMPCLIN
          MOVE      OBACTYP,TEMPCTYP
          MOVE      SP70,TEMPATYP
          MOVE      TWO,TEMPVTYP
.
          PACK      KEY16,TEMPURNO,TEMPVISN,SP70
          CALL      RATEMP1
          IF        OVRCD=1
            CALL      WRTEMP1
          ENDIF
.
          GOTO      LOUT1000
.
LOUT9999  RETURN
.
.**********************************************************************
.*                      LINP0000                                      *
.*                      Load inpatient data                           *
.**********************************************************************
LINP0000  IF        REPVTYPE=1
            GOTO      LINP9999              * Outpatients Only
          ENDIF
.
          PACK      KEY16,REPFDATE
          CALL      RDSMISS3
LINP1000  CALL      RDKMISS3
          BRANCH    OVRCD,LINP9999
.
          MATCH     ADATE,REPTDATE
          GOTO      LINP9999 IF LESS
.
          BRANCH    ASTAT,LINP1000,LINP2000,LINP2000,LINP2000,LINP1000
          GOTO      LINP1000
.
LINP2000  PACK      KEY8,AADMNO,SP70
          CALL      RDPMVX11
          BRANCH    OVRCD,LINP1000
.
          IF        IBCNMHOS=1
            MATCH     PMVXMHOS,PTHSHOSP               * Hospital
            GOTO      LINP1000 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,REPFFUND
          IF        !@EQUAL
            MATCH    REPFFUND,AFUNDH                  * H/F From
            GOTO     LINP1000 IF LESS
          ENDIF
.
          MATCH     SP70,REPTFUND
          IF        !@EQUAL
            MATCH    AFUNDH,REPTFUND                  * H/F To
            GOTO     LINP1000 IF LESS
          ENDIF
.
          MATCH     SP70,REPFCLAM
          IF        !@EQUAL
            MATCH    REPFCLAM,ACLAIM                  * From Claim Code
            GOTO     LINP1000 IF LESS
          ENDIF
.
          MATCH     SP70,REPTCLAM
          IF        !@EQUAL
            MATCH    ACLAIM,REPTCLAM                  * To Claim Code
            GOTO     LINP1000 IF LESS
          ENDIF
.
          MOVE      AURNO,TEMPURNO
          MOVE      AADMNO,TEMPVISN
          MOVE      ADATE,TEMPDATE
          MOVE      ATIME,TEMPTIME
          MOVE      ACLAIM,TEMPCLAM
          MOVE      AFUNDH,TEMPFUND 
          MOVE      SP70,TEMPSTYP
          MOVE      SP70,TEMPCLIN
          MOVE      SP70,TEMPCTYP
          MOVE      ATYPE,TEMPATYP
          MOVE      THREE,TEMPVTYP
.
          PACK      KEY16,TEMPURNO,TEMPVISN,SP70
          CALL      RATEMP1
          IF        OVRCD=1
            CALL      WRTEMP1
          ENDIF
.
          GOTO      LINP1000
.
LINP9999  RETURN
.
.*********************************************************************
.         print a new page
.*********************************************************************
HEAD0000  CALL      HEAD132
.
          IF        IBCNMHOS=1
            PRINT     *40,"Hospital : ",PTHSNAME
            ADD       ONE,CLNO
          ENDIF
.
          PRINT     *40,"From Claim: ",PRFRCLAM," to ",PRTOCLAM
          PRINT     *40,"From H/Fund: ",PRFRFUND," to ",PRTOFUND
          PRINT     *40,"Date From : ",PRFRDATE," to ",PRTODATE
          PRINT     *40,"Visit : ",PRFRVTYP
.
          MATCH     "1",OTCNCASS
          IF        @EQUAL
            PRINT     *40,"Count by Assoc Number : Yes"
            ADD       ONE,CLNO
          ENDIF
.
          ADD       FOUR,CLNO
.
HEAD9999  RETURN
.
.*********************************************************************
.         print new sub heading
.*********************************************************************
SHED0000  CALL      UND132
.         
          PRINT     *1,"| Claim Code : ",TDESC,*132,"|"
          PRINT     *1,"| Health Fund : ",HFNAME,*132,"|"
.
          ADD       TWO,CLNO
.
SHED9999  RETURN
.
.*********************************************************************
.         print new colums heading
.*********************************************************************
CHED0000 CALL       UND132
         PRINT     *1,"| U/R",*10,"| Visit",*19,"| Date/Time",*36,"| Name":
                    *67,"|Claim",*73,"|Health",*80,"|Inp":
                    *84,"| Clinic Type":
                    *98,"| Visit Type",*113,"| Adm Type",*125,"|Visits":
                    *132,"|"
          PRINT     *1,"|",*10,"|",*19,"|",*36,"|",*67,"|Code":
                    *73,"|Fund",*80,"|Out",*84,"| Id":
                    *98,"|",*113,"|",*125,"|":
                    *132,"|"
          ADD       TWO,CLNO
          CALL      UND132
.         
CHED9999  RETURN
.
.**********************************************************************
.*                         PRIN0000                                   *
.*                        Print Data                                  *
.**********************************************************************
PRIN0000  CALL      HEAD0000
.
          MOVE      Z70,SAVECLAM
          MOVE      Z70,SAVEFUND
          MOVE      SP70,SAVEURTY
.
          IF        REPTSORT=0
            PACK      KEY36,SP70
          ELSE
            PACK      KEY39,SP70
          ENDIF
.
          IF        REPTSORT=0
            CALL      RDSTEMP2
          ELSE
            CALL      RDSTEMP3
          ENDIF
.
PRIN1000  IF        REPTSORT=0
            CALL      RDKTEMP2
          ELSE      
            CALL      RDKTEMP3
          ENDIF 
          BRANCH    OVRCD,PRIN9000
.
          PACK      KEY8,TEMPURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,PRIN1000
.
          PACK      PRTNAME,SP30,SP30
          MOVE      PTITL,PACTITLE
          MOVE      PGNAME,PACGNAME
          MOVE      PSNAME,PACSNAME
          MOVE      TWO,PACFRMT
          CALL      PACNAME   
          MOVE      PACFNAME,PRTNAME    
.
          UNPACK    TEMPDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      TEMPTIME,PRTVTIME
.
          MOVE      SP70,PRTVTYPE
          MOVE      ONE,VISCOUNT            * Visit/Slot counter
.
          MATCH     "2",TEMPVTYP
          IF        @EQUAL
            MOVE      "Out",PRTVTYPE
          ENDIF
.
          MATCH     "3",TEMPVTYP
          IF        @EQUAL
            MOVE      "Inp",PRTVTYPE
          ENDIF
.
          MOVE      SP70,PRTSTYPE
          MATCH     SP70,TEMPSTYP
          IF         !@EQUAL
            PACK      KEY5,ANSC,ANSV,TEMPSTYP,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      TEMPSTYP,TDESC
              MOVE      ZERO,TCFORM6
            ENDIF
            MOVE    TDESC,PRTSTYPE
.
            MATCH     "1",OTCNCASS     * Count OP bookings by associated number
            IF        @EQUAL
              MOVE      TCFORM6,VISCOUNT
            ENDIF
         ENDIF
.
          MOVE      SP70,PRTATYPE
          MATCH     SP70,TEMPATYP
          IF         !@EQUAL              
            PACK      KEY5,ANSA,SP1,TEMPATYP,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      TEMPATYP,TDESC
            ENDIF       
            MOVE    TDESC,PRTATYPE
          ENDIF
.
          PACK      KEY9,TEMPURNO,TEMPVTYP,SP70
          MATCH     SAVEURTY,KEY9
          IF        @EQUAL
            ADD       VISCOUNT,PRTVCNTR     * Visit total by U/R and Visit Type
          ELSE
            MATCH     SP70,SAVEURTY
            IF        !@EQUAL
              PRINT     *1,"|",*10,"|",*19,"|",*36,"|",*67,"|":
                        *73,"|",*80,"|",*84,"|":
                        *98,"|",*113,"|",*125,"|",PRTVCNTR:
                        *132,"|"
              ADD       ONE,CLNO
            ENDIF
            PACK      SAVEURTY,TEMPURNO,TEMPVTYP,SP70
            MOVE      VISCOUNT,PRTVCNTR
          ENDIF
.
          MATCH     SAVECLAM,TEMPCLAM
          IF        @EQUAL
            MATCH     SAVEFUND,TEMPFUND              * Claim Code or Health
            GOTO      PRIN5000 IF EQUAL              * fund sub heading changed
          ENDIF
.           
          PACK      KEY5,ANSC,ANSL,TEMPCLAM,SP70
          CALL      RDCODE1                          * Read Claim Code
          IF        OVRCD=1
            MOVE      TEMPCLAM,TDESC
          ENDIF
.
          PACK      KEY14,TEMPFUND,ZERO8,SP70
          CALL      RDFUND1                         * Read Health Fund
          IF        OVRCD=1
            MOVE      TEMPFUND,HFNAME
          ENDIF
.
          IF        REPPCLAM=1
            MATCH     Z70,SAVECLAM
            IF        !@EQUAL
              MATCH     TEMPCLAM,SAVECLAM
              IF        !@EQUAL
                CALL      HEAD0000
                GOTO      PRIN4000
              ENDIF
            ENDIF
          ENDIF
.
          IF        REPPFUND=1
            MATCH     Z70,SAVEFUND
            IF        !@EQUAL
              MATCH     TEMPFUND,SAVEFUND
              IF        !@EQUAL
                CALL      HEAD0000
                GOTO      PRIN4000
              ENDIF
            ENDIF
          ENDIF
.
PRIN4000  CALL      SHED0000     
.
          MOVE      TEMPCLAM,SAVECLAM
          MOVE      TEMPFUND,SAVEFUND
.
          CALL      CHED0000
.
PRIN5000  PRINT     *1,"|",TEMPURNO,*10,"|",TEMPVISN:
                    *19,"|",CPCDATE,SP1,PRTVTIME:
                    *36,"|",PRTNAME,*67,"|",TEMPCLAM:
                    *73,"|",TEMPFUND,*80,"|",PRTVTYPE:
                    *84,"|",TEMPCLIN,SLASH,TEMPCTYP,*98,"|",PRTSTYPE:
                    *113,"|",PRTATYPE,*125,"|",*132,"|"
.
          ADD       ONE,CLNO
          COMPARE   "55",CLNO
          IF        !@LESS
            CALL      HEAD0000
            CALL      CHED0000
          ENDIF
.
          GOTO      PRIN1000              * read next record
.
PRIN9000  MATCH     SP70,SAVEURTY
          IF        !@EQUAL
            PRINT     *1,"|",*10,"|",*19,"|",*36,"|",*67,"|":
                      *73,"|",*80,"|",*84,"|":
                      *98,"|",*113,"|",*125,"|",PRTVCNTR:
                      *132,"|"
          ENDIF 
.
PRIN9999  RETURN
.
.**********************************************************************
.*                           ENDP0000                                 *
.*                      Print the end of the report                   *
.**********************************************************************
ENDP0000  CALL      UND132                   * finish off report
.
          PRINT     *N,*50,"*** End of Report ***"
.
ENDP9999  RETURN
.
.**********************************************************************
.*              I/O SUBROUTINE FOR TEMPFILE                           *
.**********************************************************************
.
RDSTEMP1  MOVE      ZERO,OVRCD
          RESET     KEY16
          READ      TEMPFIL1,KEY16;;
          GOTO      OVERCOND IF OVER
          RETURN
.
RDKTEMP1  MOVE      ZERO,OVRCD
          READKS    TEMPFIL1;TEMPURNO,TEMPVISN,TEMPDATE,TEMPTIME:       
                             TEMPCLAM,TEMPFUND,TEMPSTYP,TEMPCLIN:
                             TEMPCTYP,TEMPATYP,TEMPVTYP
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   RESET     KEY16
          WRITE     TEMPFIL1,KEY16;TEMPURNO,TEMPVISN,TEMPDATE,TEMPTIME:
                                   TEMPCLAM,TEMPFUND,TEMPSTYP,TEMPCLIN:
                                   TEMPCTYP,TEMPATYP,TEMPVTYP
          RETURN
.
RATEMP1   RESET     KEY16
          MOVE      ZERO,OVRCD
          READ      TEMPFIL1,KEY16;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RDSTEMP2  MOVE      ZERO,OVRCD
          RESET     KEY42
          READ      TEMPFIL2,KEY42;;
          GOTO      OVERCOND IF OVER
          RETURN
.
RDKTEMP2  MOVE      ZERO,OVRCD
          READKS    TEMPFIL2;TEMPURNO,TEMPVISN,TEMPDATE,TEMPTIME:
                             TEMPCLAM,TEMPFUND,TEMPSTYP,TEMPCLIN:
                             TEMPCTYP,TEMPATYP,TEMPVTYP
          GOTO      OVERCOND IF OVER
          RETURN
.
RDSTEMP3  MOVE      ZERO,OVRCD
          RESET     KEY42
          READ      TEMPFIL3,KEY42;;
          GOTO      OVERCOND IF OVER
          RETURN
.
RDKTEMP3  MOVE      ZERO,OVRCD
          READKS    TEMPFIL3;TEMPURNO,TEMPVISN,TEMPDATE,TEMPTIME:
                             TEMPCLAM,TEMPFUND,TEMPSTYP,TEMPCLIN:
                             TEMPCTYP,TEMPATYP,TEMPVTYP
          GOTO      OVERCOND IF OVER
          RETURN
.
.**********************************************************************
.*              I/O INCLUDES FOR FILES AND GENERAL ROUTINES           *
.**********************************************************************
.
          INC       PATCODKY
          INC       PATFNDKY
          INC       PATHSPKY
          INC       TFILENAM
.
          INC       IBASEQIO/INC
          INC       PATCODIO/INC
          INC       PATHSPIO/INC
          INC       PATMA1IO/INC 
          INC       PATMI1IO/INC
          INC       PATFN1IO/INC
          INC       PATVISIO/INC
          INC       PMSVX1IO/INC
          INC       OUTBOAIO/INC                            
          INC       OUTBB1IO/INC
          INC       OUTSITIO/INC
          INC       WEBERRIO/INC
.
          INCLUDE   STD001IO
.

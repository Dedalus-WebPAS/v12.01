.******************************************************************************
.* System        : OPERATING ROOMS                                            *
.* Program       : IBAOPR90                                                   *
.* Name          : Operator Error Corrections                                 *
.******************************************************************************
.* Date          : 25/02/1993                                                 *
.* Author        : i chung                                                    *
.* Function      : Perform various tasks because of operator errors           *
.*                 (Adapted from Release 6)                                   *
.* Modifications :                                                            *
.******************************************************************************
.*        V11.02.02 18/04/2025  Alina Bhari      TSK 0965023                  *
.*                  Recompiled for changes to OPRSESIO - converted            *
.*                  form(OPSESUSP) to DIM (DOPSESUS) for read and write       *
.*        V12.00.01 16/05/2025  Ebon Clements    TSK 0955096                  *
.*                  Alphanueric visit number changes                          *
.*        V11.03.02 19/04/2023  Thanh T          TSK 0909393                  *
.*                  Changed BKDT0000 to setup KEY22 with OPDAHOSP.            *
.*                  Changed SELT0000 to use RSOPDEA6 instead of RSOPDEA1      *
.*        V11.03.01 20/03/2023  PJ Le Febour     TSK 0909393a                 *
.*                  Amended KEY19 to KEY22 for theatre index changes          *
.*                  OPEN    PMSVX1A1 "pmsvx1af" to correct I*C error          *
.*        V10.02.01 07/07/2011  Steve Armstrong  CAR 240722                   *
.*                  Added RDPMPX21 prior to calling PMIHEAD                   *
.******************************************************************************
.*        V5.08.01  21/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*                V5.07.B03  18/01/1999  Nicole Harrington 507 rebound 093    *
.*                           Mods to display century correctly                *
.*                V5.07.B02  13/01/1999  Jim Stathopoulos                     *
.*                           Fixed Global Compile Errors                      *
.*                 V5.07.00  29/10/1998  Jim Stathopoulos                     *
.*                           507 Changes                                      *
.******************************************************************************
.*                 V5.01.01  01/04/93 J.Tan   SRF 121502                      *
.*                           Fixed the billing no in inputing booking details *
.*                 V5.01.02  22/04/93 DIG                                     *
.*                           Re-Compiled for changes to PATALERT.             *
.*                 V5.01.03  25/08/93  ROD                                    *
.*                           Re-Compiled for NZHIS                            *
.*                 V5.01.04  13/01/1994  Steve Armstrong                      *
.*                           Fixed bugs for global recompile                  *
.*                 V5.01.05  17/05/1994  WHIRLPOOL    SRF 129175              *
.*                           Recompiled for OPDSPSHD                          *
.*                 V5.01.06  15/09/1994    Max White     Hawkes Bay Mods      *
.*                           Recompiled for OPR001FD.                         *
.*        V5.03.B2  18/05/1995  Greg Horvat      SRF 134879  (Rebound)        *
.*                  Recompiled for OPR001FD                                   *
.*        V5.03.01  08/05/96   Howellsy       5.04                            *
.*                  Added 2 Nurse Types & 2 Nurse Codes                       *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
          INC       PATNHIFD/INC                 * Include for NZHIS vars
          INC       PATCONFD/INC                 * Include for NZHIS vars
          INC       PATCOMM/INC
          INC       PATDHEAD/INC
          INC       PATCALAG/INC
          INC       NZHISIFD/INC                 * Include for NZHIS vars
.
          INC       BOKRC1FD/INC
.
          INC       PATALRFD/INC       * needed by PATALERT
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PATDSCFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATNIDFD/INC
          INC       PATNOBFD/INC
          INC       PATPR1FD/INC
          INC       PATGSRFD/INC
          INC       PATVISFD/INC
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
          INC       PATWR1FD/INC
.
          INC       OPR001FD/INC
          INC       OPRCLIFD/INC
          INC       OPRCONFD/INC
          INC       OPRDEAFD/INC
          INC       OPRDEBFD/INC
          INC       OPRNURFD/INC
          INC       OPRSESFD/INC
          INC       OUTPREFD/INC
+
.
. ------- Program Work Variables -------
.
ADMISS    DIM       8
ANAECODE  DIM       6
ANAEDESC  DIM       30
BJDAY     FORM      3
.
CJDAY     FORM      3
CURRITEM  FORM      2
.
DOCDESC   DIM       24
.
FRSTGNAM  DIM       40
FULLGNAM  DIM       80
ITEMS     DIM       40[24]
.
KITEM     DIM       2
.
NEWSESS   DIM       22
NMPNUMB   DIM       20
OPSTATUS  DIM       30
.
PATDESC   DIM       30
.
SCNDGNAM  DIM       40
SELITEM   FORM      2
SESSDATE  DIM       8
SESSION   DIM       19
SUBOPT    FORM      1
SURGCODE  DIM       6
SURGDESC  DIM       30
.
. ------- Program Constants -------
.
PRGID     INIT      "IBAOPR90"
PRGNAM    INIT      "Operator Error Corrections"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.* ML0000     : Mainline Code                                                 *
.******************************************************************************
ML0000    CALL      INIT0000                 * program initialisation
.
ML0100    CALL      OPTN0000                 * select option
          BRANCH    OPTION,ML1100            * Cancel Team Allocation
          GOTO      ML9999                   * Exit
.
. ------- Cancel Team Allocation -------
.
ML1100    CALL      SELB0000                 * select booking
          BRANCH    EXIT,ML0100
          CALL      CTMA0000                 * cancel Team Allocation
          GOTO      ML1100
.
. ------- Exit --------
.
ML9999    CHAIN     PGM
+
.******************************************************************************
.* INIT0000   : Program initialisation                                        *
.******************************************************************************
INIT0000  CALL      DISPHEAD                 * standard heading
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"bokrc1af";
          OPEN      BOKRC1A1,"bokrc1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patnobef";
          OPEN      PATNOBE1,"patnobef"
.
          DISPLAY   *P64:24,"patpramf";
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA1,"patvisaf"
          OPEN      PATVISA2,"patvisaf"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY   *P64:24,"oprcliaf";
          OPEN      OPRCLIA1,"oprcliaf"
.
          DISPLAY   *P64:24,"oprdetaf";
          OPEN      OPRDETA1,"oprdetaf"
          OPEN      OPRDETA2,"oprdetaf"
          OPEN      OPRDETA6,"oprdetaf"
.
          DISPLAY   *P64:24,"oprdetbf";
          OPEN      OPRDETB1,"oprdetbf"
.
          DISPLAY   *P64:24,"oprnuraf";
          OPEN      OPRNURA1,"oprnuraf"
.
          DISPLAY   *P64:24,"oprsesaf";
          OPEN      OPRSESA1,"oprsesaf"
          OPEN      OPRSESA7,"oprsesaf"
.
          READ      CONTROLF,TEN9;*75,CCNOTES
          READ      CONTROLF,FORTY;*61,OPCNAUDS,*81,OPCNODSC,*96,OPCNCDSC:
                                  *111,OPCNCLSU,*114,OPCNSESS,*224,OPCNUDM1
          READ      CONTROLF,FORTY2;*28,OPCNFULL
.
          DISPLAY   *P64:24,"patgsrnf";
          OPEN      PATGSRN2,"patgsrnf"
          OPEN      PATGSRN3,"patgsrnf"
          OPEN      PATGSRN4,"patgsrnf"
.
INIT2000  STRIP     OPCNCDSC
          STRIP     OPCNODSC
.
INIT9999  RETURN
+
.******************************************************************************
.* OPTN0000   : Select program option                                         *
.* Returns    : OPTION   0 = Exit, otherwise option selected                  *
.******************************************************************************
OPTN0000  MOVE      ZERO,SUBOPT              * clear sub-option
.
          HLINE     *G37,2,53,80
          DISPLAY   *P1:3,*EF:
                    *P2:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P2:5,*V2LON,ONE,*HOFF," = Cancel Team Allocation":
                    *P1:7,"Select Option : ";
.
OPTN1000  KEYIN     *P17:7,*EL,*DV,UNDLN1,*P17:7,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9999 IF EQUAL
          DISPLAY   *P17:7,*V2LON,OPTION;
.
          BRANCH    OPTION,OPTN2000
          BEEP
          GOTO      OPTN1000
.
.  ------ Valid Option Selected ------
.
OPTN2000  DISPLAY   *P53:2,*V2LON,"- Cancel Team Allocation ";
.
OPTN9999  RETURN
+
.******************************************************************************
.* SELB0000   : Select a booking                                              *
.* Parameters : SUBOPT   Last method chosen for selecting a booking           *
.*                       0 = None, Ask the operator                           *
.*                       1 = By U/R Number                                    *
.*                       2 = By Booking Number                                *
.*                       3 = By Session Date                                  *
.* Returns    : SUBOPT   Last method chosen for selecting a booking           *
.*              BOKRC1FD record or PATMI1FD record, as appropriate            *
.*              OPRDEAFD record                                               *
.*              EXIT     0 = Record found, 1 = No Record returned             *
.******************************************************************************
SELB0000  BRANCH    SUBOPT,SELB1000,SELB2000,SELB3000
.
. ------- Ask how they want to select the booking -------
.
SELB0100  CALL      SUBO0000
          BRANCH    SUBOPT,SELB1000,SELB2000,SELB3000
          MOVE      ONE,EXIT                 * nothing selected
          GOTO      SELB9999
.
. ------- Select by U/R Number -------
.
SELB1000  CALL      GETU0000                 * get the U/R Number
          BRANCH    EXIT,SELB0100            * nothing input
.
SELB1100  OPEN      BOKRC1A6,"bokrc1af"      * open booking file
          CALL      GTBU0000                 * get a booking for this U/R
          CLOSE     BOKRC1A6                 * close booking file
          BRANCH    EXIT,SELB1000            * nothing selected
.
          CALL      BOKHEAD                  * display standard heading
.
          CALL      GETT0000                 * get a theatre booking
          BRANCH    EXIT,SELB1100            * nothing selected
.
          CALL      BKDT0000                 * display booking details
          MOVE      ZERO,EXIT                * record found
          GOTO      SELB9999
.
. ------- Select by Booking Number -------
.
SELB2000  CALL      GETB0000                 * get the booking number
          BRANCH    EXIT,SELB0100            * nothing input
.
          CALL      BOKHEAD                  * display standard heading
.
          CALL      GETT0000                 * get a theatre booking
          BRANCH    EXIT,SELB2000            * nothing selected
.
          CALL      BKDT0000                 * display booking details
          MOVE      ZERO,EXIT                * record found
          GOTO      SELB9999
.
. ------- Select by Session Date -------
.
SELB3000  CALL      GETD0000                 * get the session date
          BRANCH    EXIT,SELB0100            * nothing input
.
SELB3100  CALL      GETS0000                 * get a theatre session
          BRANCH    EXIT,SELB3000:           * another date selected
                         SELB0100            * exit selected
.
          CALL      SESH0000                 * display the session header
.
          CALL      SELT0000                 * select booking from session
          BRANCH    EXIT,SELB3100            * nothing selected
.
          CALL      BOKHEAD                  * display standard heading
          CALL      BKDT0000                 * display booking details
          MOVE      ZERO,EXIT                * record found
.
SELB9999  RETURN
+
.******************************************************************************
.* SUBO0000   : Select sub-option                                             *
.* Returns    : SUBOPT   0 = Exit, otherwise option selected                  *
.******************************************************************************
SUBO0000  DISPLAY   *P1:4,*EF:
                    *P2:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P2:5,*V2LON,ONE,*HOFF," = By U/R Number":
                    *P2:6,*V2LON,TWO,*HOFF," = By Booking Number":
                    *P2:7,*V2LON,THREE,*HOFF," = By Session Date":
                    *P1:9,"Select Option : ";
.
SUBO1000  KEYIN     *P17:9,*EL,*DV,UNDLN1,*P17:9,*V2LON,SUBOPT;
.
          COMPARE   ZERO,SUBOPT
          GOTO      SUBO9999 IF EQUAL
          DISPLAY   *P17:9,*V2LON,SUBOPT;
.
          BRANCH    SUBOPT,SUBO9999,SUBO9999,SUBO9999
          BEEP
          GOTO      SUBO1000
.
SUBO9999  RETURN
+
.******************************************************************************
.* GETU0000   : Input a U/R Number                                            *
.* Returns    : PATMA1FD record                                               *
.*              EXIT     0 = U/R selected, 1 = Nothing input                  *
.******************************************************************************
GETU0000  CALL      REDISPS
          MOVE      FOUR,CVERT
          MOVE      TEN4,CCOL
          MOVE      TWO,SRCHOPT
          MOVE      ZERO,SRCHSYS
.
          OPEN      OUTPREA1,"outpreaf"
          CALL      KURN
          CLOSE     OUTPREA1
          BRANCH    EXIT,GETU9900
          BRANCH    OVRCD,GETU9000
.
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          BRANCH    OVRCD,GETU9000
.
          CALL      PMIHEAD
          MOVE      ZERO,EXIT
          GOTO      GETU9999
.
.         U/R Number not on file
.
GETU9000  DISPLAY   *P1:24,*EL,*B,*V2LON,"Invalid U/R Number.    ";
          CALL      HITENTER
          GOTO      GETU0000
.
.         Nothing input
.
GETU9900  MOVE      ONE,EXIT
.
GETU9999  RETURN
+
.******************************************************************************
.* REDISPS    : Redisplay routine for KURN                                    *
.******************************************************************************
REDISPS   DISPLAY   *P1:4,*EF,"U/R Number : ";
          RETURN
+
.******************************************************************************
.* GTBU0000   : Get a booking for this U/R Number                             *
.* Parameters : PURNO    U/R Number                                           *
.* Returns    : BOKRC1FD or PATMI1FD record, as appropriate                   *
.*              EXIT     0 = Record found, 1 = Nothing selected               *
.******************************************************************************
GTBU0000  DISPLAY   *P1:6,*EF:
                    *P4:7,*ULON,*V2LON," Booking",*P13:7," Adm Date ":
                    *P24:7,"Doctor                  ":
                    *P49:7,"Theatre Bookings                ";
          MOVE      EIGHT,CVERT
          MOVE      ZERO,CURRITEM           * current item number
          MOVE      ZERO,ISNEXT             * assume no next screen
          MOVE      ZERO,ISPREV             * assume no prev screen
.
.         Get the list of admissions for this U/R Number
.
          MOVE      PURNO,PVIURNO
          PACK      KEY24,PVIURNO,Z70
          CALL      RDSVISA2
GTBU1000  CALL      RDPVISA2
          BRANCH    OVRCD,GTBU4000
.
          MATCH     PURNO,PVIURNO
          GOTO      GTBU4000 IF NOT EQUAL
.
          COMPARE   THREE,PVITYPE           * Inpatient visit ?
          GOTO      GTBU1000 IF NOT EQUAL   * No, ignore
.
.         Found an item to be displayed
.
          COMPARE   TWENTY3,CVERT
          GOTO      GTBU2000 IF LESS
.
          MOVE      ONE,ISNEXT              * we have a next screen
          CALL      SELI0000                * select an item
          BRANCH    EXIT,GTBU8000:          * item selected
                         GTBU9000:          * exit selected
                         GTBU1500:          * next screen selected
                         GTBU3000           * previous screen selected
.
.         Next screen selected
.
GTBU1500  MOVE      EIGHT,CVERT             * position for next display
          MOVE      ZERO,CURRITEM           * current item number
          DISPLAY   *P1:CVERT,*EF;
          MOVE      ONE,ISPREV              * we have a previous screen
.
.         Display this item
.
GTBU2000  ADD       ONE,CURRITEM            * increment item count
          MOVE      PVIBILL,ITEMS[CURRITEM] * save key for this item
          CALL      DSPB0000                * display booking
          ADD       ONE,CVERT               * ready for next display
          GOTO      GTBU1000
.
.         Previous screen selected
.
GTBU3000  MOVE      ITEMS[1],KEY8           * key for first item on screen
          CALL      RDVISA1                 * read in record
          PACK      KEY24,PVIURNO,PVIDATE,PVIBILL
          CALL      RDVISA2
.
.         Loop backwards over one screen
.
          MOVE      EIGHT,FORM2              * duplicates CVERT in display loop
GTBU3100  CALL      RDKVISA2
          BRANCH    OVRCD,GTBU0000
.
          MATCH     PURNO,PVIURNO
          GOTO      GTBU0000 IF NOT EQUAL
.
          COMPARE   THREE,PVITYPE           * Inpatient visit ?
          GOTO      GTBU3100 IF NOT EQUAL   * No, ignore
.
          COMPARE   TWENTY3,FORM2           * read appropriate number of recs?
          GOTO      GTBU3200 IF NOT LESS    * finished looping backwards
          ADD       ONE,FORM2               * increment counter
          GOTO      GTBU3100
.
GTBU3200  MOVE      EIGHT,CVERT             * position for next display
          MOVE      ZERO,CURRITEM           * current item number
          DISPLAY   *P1:CVERT,*EF;
          GOTO      GTBU1000                * start display loop
.
. ------- Finished loop over the admissions. Now loop over any bookings -------
.
GTBU4000  MOVE      PURNO,BKREURNO
          PACK      KEY16,BKREURNO,Z70
          CALL      RSBKREC6
GTBU4100  CALL      RPBKREC6
          BRANCH    OVRCD,GTBU7500
.
          MATCH     PURNO,BKREURNO
          GOTO      GTBU7500 IF NOT EQUAL
.
          MOVE      BKREBOOK,KEY8
          CALL      RDVISA1                 * on visit file ?
          COMPARE   ZERO,OVRCD              * Yes, already processed
          GOTO      GTBU4100 IF EQUAL
.
.         Found an item to be displayed
.
          COMPARE   TWENTY3,CVERT
          GOTO      GTBU5000 IF LESS
.
          MOVE      ONE,ISNEXT              * we have a next screen
          CALL      SELI0000                * select an item
          BRANCH    EXIT,GTBU8000:          * item selected
                         GTBU9000:          * exit selected
                         GTBU4500:          * next screen selected
                         GTBU6000           * previous screen selected
.
.         Next screen selected
.
GTBU4500  MOVE      EIGHT,CVERT             * position for next display
          MOVE      ZERO,CURRITEM           * current item number
          DISPLAY   *P1:CVERT,*EF;
          MOVE      ONE,ISPREV              * we have a previous screen
.
.         Display this item
.
GTBU5000  ADD       ONE,CURRITEM            * increment item count
          MOVE      BKREBOOK,PVIBILL
          MOVE      BKREEDAT,PVIDATE
          MOVE      BKREADOC,PVIDOCT
.
          MOVE      PVIBILL,ITEMS[CURRITEM] * save key for this item
          CALL      DSPB0000                * display booking
          ADD       ONE,CVERT               * ready for next display
          GOTO      GTBU4100
.
.         Previous screen selected
.
GTBU6000  MOVE      EIGHT,FORM2             * duplicates CVERT in display loop
          MOVE      ITEMS[1],KEY8           * key for first item on screen
          CALL      RDVISA1                 * read in record
          BRANCH    OVRCD,GTBU6100          * not on visit file -> a booking
          PACK      KEY24,PVIURNO,PVIDATE,PVIBILL
          CALL      RDVISA2
          GOTO      GTBU3100                * loop backwards over visits
.
.         Loop backwards over one screen of bookings
.
GTBU6100  MOVE      PURNO,BKREURNO
          PACK      KEY16,BKREURNO,ITEMS[1]
          CALL      RDBKREC6
GTBU6200  CALL      RKBKREC6
          BRANCH    OVRCD,GTBU7000
.
          MATCH     PURNO,BKREURNO
          GOTO      GTBU7000 IF NOT EQUAL
.
          MOVE      BKREBOOK,KEY8
          CALL      RDVISA1                 * on visit file ?
          COMPARE   ZERO,OVRCD              * Yes, already processed
          GOTO      GTBU6200 IF EQUAL
.
          COMPARE   TWENTY3,FORM2           * read appropriate number of recs?
          GOTO      GTBU6300 IF NOT LESS    * finished looping backwards
          ADD       ONE,FORM2               * increment counter
          GOTO      GTBU6200
.
GTBU6300  MOVE      EIGHT,CVERT             * position for next display
          MOVE      ZERO,CURRITEM           * current item number
          DISPLAY   *P1:CVERT,*EF;
          GOTO      GTBU4100                * start display loop
.
. ------- Finished loop back over bookings, now go back over visit file -------
.
GTBU7000  MOVE      PURNO,PVIURNO
          PACK      KEY24,PVIURNO,SP20
          CALL      RDSVISA2
          GOTO      GTBU3100                * continue loop over visit file
.
. ------- Finished locating all data -------
.
GTBU7500  MOVE      ZERO,ISNEXT             * we do not have a next screen
          CALL      SELI0000                * select an item
          BRANCH    EXIT,GTBU8000:          * item selected
                         GTBU9000:          * exit selected
                         GTBU7500:          * next screen selected
                         GTBU6000           * previous screen selected
.
. ------- A visit has been selected -------
.
GTBU8000  MOVE      ITEMS[SELITEM],KEY8
          CALL      RDVISA1                 * on visit file ?
          BRANCH    OVRCD,GTBU8500          * no, a booking file record
.
          MOVE      PVIBILL,AADMNO
          MOVE      AADMNO,KEY8
          CALL      RDMISS1
.
          MOVE      SP8,DDATE
          MOVE      AADMNO,KEY8
          CALL      RDDSCH1
.
          LOAD      BKRESTAT,ASTAT,ONE,THREE,FOUR,THREE,TWO
          MOVE      ZERO,EXIT
          GOTO      GTBU9999
.
GTBU8500  MOVE      ITEMS[SELITEM],KEY8
          CALL      RDBKREC1
          MOVE      BKREBOOK,AADMNO
          MOVE      ZERO,EXIT
          GOTO      GTBU9999
.
. ------- Nothing selected --------
.
GTBU9000  MOVE      ONE,EXIT
.
GTBU9999  RETURN
+
.******************************************************************************
.* DSPB0000   : Display a single booking record                               *
.* Parameters : PVIBILL  Booking number                                       *
.*              PVIDATE  Visit date                                           *
.*              PVIDOCT  Doctor code                                          *
.*              CVERT    Line to display details on                           *
.*              CURRITEM Line number to be display                            *
.******************************************************************************
DSPB0000  UNPACK    PVIDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          MOVE      PVIDOCT,KEY6
          CALL      RDDOCT1
          IF        OVRCD = 0
            MOVE      DSNAME,PACSNAME
            MOVE      DGNAME,PACGNAME
            MOVE      DTITL,PACTITLE
            MOVE      TWO,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,DOCDESC
          ELSE
            MOVE      SP30,DOCDESC
          ENDIF
          DISPLAY   *P1:CVERT,*V2LON,CURRITEM,*HOFF,DOT:
                    *P4:CVERT,PVIBILL:
                    *P13:CVERT,CPCDATE:
                    *P24:CVERT,DOCDESC;
.
.         Get the dates of the first few theatre bookings for this patient
.
          MOVE      "49",CCOL
          MOVE      PVIBILL,ADMISS
          PACK      KEY31,PVIBILL,SP30
          CALL      RSOPDEA2
DSPB1000  CALL      RKOPDEA2
          BRANCH    OVRCD,DSPB9999
.
          MATCH     ADMISS,OPDAADMN
          GOTO      DSPB9999 IF NOT EQUAL
.
          UNPACK    OPDADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          COMPARE   "72",CCOL
          IF        @LESS
            DISPLAY   *PCCOL:CVERT,CPDATE;
            ADD       NINE,CCOL
          ELSE
            DISPLAY   *PCCOL:CVERT,"+";
            GOTO      DSPB9999
          ENDIF
          GOTO      DSPB1000
.
DSPB9999  RETURN
+
.******************************************************************************
.* GETT0000   : Get a Theatre Booking for a Visit                             *
.* Parameters : AADMNO   Booking number (may not have an admission record)    *
.* Returns    : OPRDEAFD record                                               *
.*              EXIT     0 = Record found, 1 = Nothing selected               *
.******************************************************************************
GETT0000  MOVE      AADMNO,PVIBILL
          MOVE      PVIBILL,ADMISS          * convert to a DIM for matches
          PACK      KEY31,PVIBILL,SP30
          CALL      RSOPDEA2
          CALL      RKOPDEA2                * get first booking
          BRANCH    OVRCD,GETT9100          * no bookings
.
          MATCH     ADMISS,OPDAADMN
          GOTO      GETT9100 IF NOT EQUAL   * no bookings for this visit
.
.         We have at least one booking. Check if we have more than one.
.
          CALL      RKOPDEA2
          BRANCH    OVRCD,GETT7000          * no more bookings
.
          MATCH     ADMISS,OPDAADMN
          GOTO      GETT7000 IF NOT EQUAL   * no more bookings for this visit
.
.         We have at least two bookings for this visit. Allow the operator
.         to choose which they wish to select.
.
          DISPLAY   *P1:7,*EF:
                    *P4:8,*ULON,"   Date   ",*P15:8,OPCNCDSC:
                    *P46:8,"Time ",*P52:8,"Status               ";
          MOVE      NINE,CVERT
          MOVE      ZERO,CURRITEM           * current item number
          MOVE      ZERO,ISNEXT             * assume no next screen
          MOVE      ZERO,ISPREV             * assume no prev screen
.
.         Get the list of theatre bookings for this admission number
.
          PACK      KEY31,BKREBOOK,SP30
          CALL      RSOPDEA2
GETT1000  CALL      RKOPDEA2
          BRANCH    OVRCD,GETT4000
.
          MATCH     ADMISS,OPDAADMN
          GOTO      GETT4000 IF NOT EQUAL
.
.         Found an item to be displayed
.
          COMPARE   TWENTY3,CVERT
          GOTO      GETT2000 IF LESS
.
          MOVE      ONE,ISNEXT              * we have a next screen
          CALL      SELI0000                * select an item
          BRANCH    EXIT,GETT5000:          * item selected
                         GETT9000:          * exit selected
                         GETT1500:          * next screen selected
                         GETT3000           * previous screen selected
.
.         Next screen selected
.
GETT1500  MOVE      NINE,CVERT              * position for next display
          MOVE      ZERO,CURRITEM           * current item number
          DISPLAY   *P1:CVERT,*EF;
          MOVE      ONE,ISPREV              * we have a previous screen
.
.         Display this item
.
GETT2000  ADD       ONE,CURRITEM            * increment item count
          PACK      ITEMS[CURRITEM],OPDAADMN,OPDASTAT,OPDADATE,OPDATIME:
                                    OPDACLIN,OPDACASE
          CALL      DSPT0000                * display theatre booking
          ADD       ONE,CVERT               * ready for next display
          GOTO      GETT1000
.
.         Previous screen selected
.
GETT3000  MOVE      ITEMS[1],KEY31          * key for first item on screen
          CALL      RDOPDEA2                * read in record
.
.         Loop backwards over one screen
.
          MOVE      NINE,FORM2              * duplicates CVERT in display loop
GETT3100  CALL      RPOPDEA2
          BRANCH    OVRCD,GETT0000
.
          MATCH     ADMISS,OPDAADMN
          GOTO      GETT0000 IF NOT EQUAL
.
          COMPARE   TWENTY3,FORM2           * read appropriate number of recs?
          GOTO      GETT3200 IF NOT LESS    * finished looping backwards
          ADD       ONE,FORM2               * increment counter
          GOTO      GETT3100
.
GETT3200  MOVE      NINE,CVERT              * position for next display
          MOVE      ZERO,CURRITEM           * current item number
          DISPLAY   *P1:CVERT,*EF;
          GOTO      GETT1000                * start display loop
.
. ------- Finished locating all data -------
.
GETT4000  MOVE      ZERO,ISNEXT             * we don't have a next screen
          CALL      SELI0000                * select an item
          BRANCH    EXIT,GETT5000:          * item selected
                         GETT9000:          * exit selected
                         GETT4000:          * next screen selected
                         GETT3000           * previous screen selected
.
. ------- A booking has been selected -------
.
GETT5000  MOVE      ITEMS[SELITEM],KEY31
          CALL      RDOPDEA2
          MOVE      ZERO,EXIT
          GOTO      GETT9999
.
.         Only one booking for this visit
.
GETT7000  MOVE      AADMNO,PVIBILL
          PACK      KEY31,PVIBILL,SP30
          CALL      RSOPDEA2
          CALL      RKOPDEA2                * get first booking
          MOVE      ZERO,EXIT               * booking selected
          GOTO      GETT9999
.
.         Exit selected
.
GETT9000  MOVE      ONE,EXIT
          GOTO      GETT9999
.
.         No bookings for this visit
.
GETT9100  DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "There are no theatre bookings for this visit.    ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
GETT9999  RETURN
+
.******************************************************************************
.* DSPT0000   : Display a single booking record                               *
.* Parameters : OPRDEAFD record                                               *
.******************************************************************************
DSPT0000  UNPACK    OPDADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          CALL      GSUR0000                * get clinic/surgeon description
.
          DISPLAY   *P1:CVERT,*V2LON,CURRITEM,*HOFF,DOT,*P4:CVERT,CPCDATE:
                    *P15:CVERT,OPCLDESC,*P46:CVERT,OPDAEXPS;
.
.         Get the status of this booking
.
          IF        OPDASTAT = 3
            MOVE      "Cancelled ",OPSTATUS
            MATCH     SP3,OPDACANC
            IF        !@EQUAL
              PACK      KEY5,ANSS,ANSC,OPDACANC
              CALL      RDCODE1
              IF        OVRCD = 0
                PACK      OPSTATUS,OPSTATUS,TDESC
              ELSE
                PACK      OPSTATUS,OPSTATUS,OPDACANC
              ENDIF
            ENDIF
          ELSE
            CLEAR     OPSTATUS
            IF        OPDATDUR = 0
              APPEND    "Booked for ",OPSTATUS
              APPEND    OPDAEXPD,OPSTATUS
              APPEND    " minutes",OPSTATUS
            ELSE
              APPEND    "Theatre time was ",OPSTATUS
              APPEND    OPDATDUR,OPSTATUS
              APPEND    " minutes",OPSTATUS
            ENDIF
            RESET     OPSTATUS
          ENDIF
.
          DISPLAY   *P52:CVERT,OPSTATUS;
.
DSPT9999  RETURN
+
.******************************************************************************
.* GSUR0000   : Get the surgeon/clinic description                            *
.* Parameters : OPDACLIN Clinic/Suregon code                                  *
.* Returns    : OPCLDESC Clinic description                                   *
.******************************************************************************
GSUR0000  IF        OPCNCLSU = 0
            MOVE      SP30,OPCLDOCT
            MOVE      SP6,OPCLDESC
            MOVE      OPDACLIN,KEY6
            CALL      RDOPCLI1
            MATCH     SP6,OPCLDOCT
            GOTO      GSUR9999 IF EQUAL
          ELSE
            MOVE      OPDACLIN,OPCLDOCT
          ENDIF
.
          MOVE      OPCLDOCT,KEY6
          CALL      RDDOCT1
          IF        OVRCD = 0
            MOVE      DSNAME,PACSNAME
            MOVE      DGNAME,PACGNAME
            MOVE      DTITL,PACTITLE
            MOVE      TWO,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,OPCLDESC
          ELSE
            MOVE      SP30,OPCLDESC
          ENDIF
.
GSUR9999  RETURN
+
.******************************************************************************
.* BKDT0000   : Display a standard header for a theatre booking               *
.* Parameters : OPRDEAFD record                                               *
.******************************************************************************
BKDT0000  CALL      GSUR0000                * get clinic/surgeon description
          SETLPTR   OPCLDESC,TWENTY4        * truncate
.
          PACK      KEY22,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN,SP70
          CALL      RDOPSES1
.
          MATCH     SP6,OPDATHET
.          GOTO      BKDT1000 IF NOT EQUAL
          IF        @EQUAL
            MOVE      OPSETHET,OPDATHET
          ENDIF
.
          UNPACK    OPDADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          DISPLAY   *P1:7,*EF,*+,OPCNCDSC:
                    *P26:7,"Date : ",*V2LON,CPCDATE,*HOFF:
                    *P44:7,"Time : ",*V2LON,OPSETIME," - ",OPSEENDT:
                    *P1:8,OPCLDESC,*HOFF:
                    *P26:8,"Case : ",*V2LON,OPDACASE,*HOFF:
                    *P44:8,OPCNODSC," : ",*V2LON,OPDATHET;
.
BKDT9999  RETURN
+
.******************************************************************************
.* SELI0000   : Select an item from the list displayed                        *
.* Parameters : ISNEXT   0 = No next screen. 1 = Have a next screen           *
.*              ISPREV   0 = No prev screen. 1 = Have a prev screen           *
.*              CURRITEM The number of items on the screen                    *
.* Returns    : SELITEM  Item number selected                                 *
.*              EXIT     1 = An item was selected                             *
.*                       2 = Exit was selected                                *
.*                       3 = Next screen was selected                         *
.*                       4 = Previous screen was selected                     *
.******************************************************************************
SELI0000  COMPARE   ZERO,CURRITEM
          GOTO      SELI9000 IF EQUAL         * nothing on the screen
.
          IF        ISNEXT = 1
            IF        ISPREV = 1
              DISPLAY   *P1:24,*EL,"Select item, (":
                        *V2LON,"E",*HOFF,")xit, (":
                        *V2LON,"N",*HOFF,")ext, (":
                        *V2LON,"P",*HOFF,")revious ? ";
              MOVE      "43",CCOL             * set keyin position
              MOVE      "E2N3P4",MASK         * set valid option mask
            ELSE
              DISPLAY   *P1:24,*EL,"Select item, (":
                        *V2LON,"E",*HOFF,")xit, (":
                        *V2LON,"N",*HOFF,")ext ? ";
              MOVE      "31",CCOL             * set keyin position
              MOVE      "E2N3",MASK           * set valid option mask
            ENDIF
          ELSE
            IF        ISPREV = 1
              DISPLAY   *P1:24,*EL,"Select item, (":
                        *V2LON,"E",*HOFF,")xit, (":
                        *V2LON,"P",*HOFF,")revious ? ";
              MOVE      "35",CCOL             * set keyin position
              MOVE      "E2P4",MASK           * set valid option mask
            ELSE
              DISPLAY   *P1:24,*EL,"Select item, (":
                        *V2LON,"E",*HOFF,")xit ? ";
              MOVE      "23",CCOL             * set keyin position
              MOVE      "E2",MASK             * set valid option mask
            ENDIF
          ENDIF
.
.         Get the selection
.
          KEYIN     *PCCOL:24,*DV,UNDLN2,*PCCOL:24,*V2LON,*JR,KITEM:
                    *PCCOL:24,*DV,KITEM;
.
          PACK      KITEM,KITEM,SP2
          REP       UPPLOW,KITEM
.
          TYPE      KITEM                     * was an item selected ?
          IF        @EQUAL
.
.         Numeric option (Item) selected
.
            MOVE      KITEM,SELITEM           * selected item
            IF        SELITEM < 1 | SELITEM > CURRITEM
              BEEP
              GOTO      SELI0000
            ENDIF
            MOVE      ONE,EXIT                * valid item selected
          ELSE
.
.         Letter option was selected
.
            REP       MASK,KITEM              * convert to a numeric
            TYPE      KITEM                   * valid selection ?
            IF        !@EQUAL
              BEEP
              GOTO      SELI0000
            ENDIF
            MOVE      KITEM,EXIT              * set exit status as appropriate
          ENDIF
          GOTO      SELI9999
.
.         Nothing on the screen
.
SELI9000  DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "There are no items to be selected.    ";
          CALL      HITENTER
          MOVE      TWO,EXIT                  * treat as an exit option
.
SELI9999  RETURN
+
.******************************************************************************
.* GETB0000   : Input a Booking Number                                        *
.* Returns    : PATMI1FD or BOKRC1FD record                                   *
.*              EXIT     0 = Booking record returned, 1 = Nothing input       *
.******************************************************************************
GETB0000  DISPLAY   *P1:4,*EF,"Booking No.: ";
.
          KEYIN     *P14:4,*DV,UNDLN8,*P14:4,*V2LON,BKREBOOK:
                    *P14:4,*DV,BKREBOOK;
.
          MATCH     ZEROVISN,BKREBOOK
          GOTO      GETB9000 IF EQUAL
.
          MOVE      BKREBOOK,KEY8
          CALL      RDVISA1                 * valid visit number ?
          BRANCH    OVRCD,GETB1000          * no, try booking file
.
          MOVE      BKREBOOK,KEY8
          CALL      RDMISS1                 * valid admission number ?
          BRANCH    OVRCD,GETB1000          * try booking file
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,GETB9100
.
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          BRANCH    OVRCD,GETB9100
.
          MOVE      SP8,DDATE
          MOVE      AADMNO,KEY8
          CALL      RDDSCH1
.
          LOAD      BKRESTAT,ASTAT,ONE,THREE,FOUR,THREE,TWO
          MOVE      ZERO,EXIT
          GOTO      GETB9999
.
.         Try the booking file
.
GETB1000  MOVE      BKREBOOK,KEY8
          CALL      RDBKREC1
          BRANCH    OVRCD,GETB9200
          MOVE      BKREBOOK,AADMNO
          MOVE      BKREURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,GETB9100
          MOVE      ZERO,EXIT
          GOTO      GETB9999
.
.         Nothing input
.
GETB9000  MOVE      ONE,EXIT
          GOTO      GETB9999
.
.         U/R Number not on file
.
GETB9100  DISPLAY   *P1:24,*EL,*B,*V2LON,"Missing U/R Number [",AURNO,"].    ";
          CALL      HITENTER
          GOTO      GETB0000
.
.         Booking number not on file
.
GETB9200  DISPLAY   *P1:24,*EL,*B,*V2LON,"Invalid Booking Number.    ";
          CALL      HITENTER
          GOTO      GETB0000
.
GETB9999  RETURN
+
.******************************************************************************
.* GETD0000   : Get a session date                                            *
.* Returns    : SESSDATE Session date                                         *
.*              EXIT     0 = Date returned, 1 = Nothing input                 *
.******************************************************************************
GETD0000  DISPLAY   *P1:4,*EF,*P1:4,"Session Date : ";
.
          MOVE      FOUR,CVERT
          MOVE      TEN6,CCOL
          MOVE      CCC,CCENT
          UNPACK    SP6,CYEAR,CMON,CDAY
.
          CALL      KEYDATE
          BRANCH    CQUEST,GETD0000
          BRANCH    OVRCD,GETD9000
.
          PACK      SESSDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",SESSDATE
          MOVE      ZERO,EXIT
          GOTO      GETD9999
.
GETD9000  MOVE      ONE,EXIT
.
GETD9999  RETURN
+
.******************************************************************************
.* GETS0000   : Get a session on the specified date                           *
.* Parameters : SESSDATE Session date                                         *
.* Returns    : OPRSESFD record                                               *
.*              EXIT     0 = session selected, 1 = Nothing selected           *
.******************************************************************************
GETS0000  PACK      KEY22,SESSDATE,SP30
          CALL      RSOPSES7
          CALL      RKOPSES7                * get first session
          BRANCH    OVRCD,GETS9100          * no sessions
.
          MATCH     SESSDATE,OPSEDATE
          GOTO      GETS9100 IF NOT EQUAL   * no sessions for this date
.
.         We have at least one session for this date.
.
          UNPACK    SESSDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          DISPLAY   *P1:4,*EF,"Session Date : ",*V2LON,CPCDATE,*HOFF:
                    *P4:6,*ULON,OPCNCDSC,*P35:6,"Session Time ":
                    *P50:6,"Status               ";
          MOVE      SEVEN,CVERT
          MOVE      ZERO,CURRITEM           * current item number
          MOVE      ZERO,ISNEXT             * assume no next screen
          MOVE      ZERO,ISPREV             * assume no prev screen
.
.         Get the list of sessions for this date
.
          PACK      KEY22,SESSDATE,SP20
          CALL      RSOPSES7
GETS1000  CALL      RKOPSES7
          BRANCH    OVRCD,GETS4000
.
          MATCH     SESSDATE,OPSEDATE
          GOTO      GETS4000 IF NOT EQUAL
.
.         Found an item to be displayed
.
          COMPARE   TWENTY3,CVERT
          GOTO      GETS2000 IF LESS
.
          MOVE      ONE,ISNEXT              * we have a next screen
          CALL      SELI0000                * select an item
          BRANCH    EXIT,GETS5000:          * item selected
                         GETS9000:          * exit selected
                         GETS1500:          * next screen selected
                         GETS3000           * previous screen selected
.
.         Next screen selected
.
GETS1500  MOVE      SEVEN,CVERT             * position for next display
          MOVE      ZERO,CURRITEM           * current item number
          DISPLAY   *P1:CVERT,*EF;
          MOVE      ONE,ISPREV              * we have a previous screen
.
.         Display this item
.
GETS2000  ADD       ONE,CURRITEM            * increment item count
          PACK      ITEMS[CURRITEM],OPSEDATE,OPSETIME,OPSECLIN,OPSEHOSP,SP70
          CALL      DSPS0000                * display session
          ADD       ONE,CVERT               * ready for next display
          GOTO      GETS1000
.
.         Previous screen selected
.
GETS3000  MOVE      ITEMS[1],KEY22          * key for first item on screen
          CALL      RDOPSES7                * read in record
.
.         Loop backwards over one screen
.
          MOVE      SEVEN,FORM2             * duplicates CVERT in display loop
GETS3100  CALL      RPOPSES7
          BRANCH    OVRCD,GETS0000
.
          MATCH     SESSDATE,OPSEDATE
          GOTO      GETS0000 IF NOT EQUAL
.
          COMPARE   TWENTY3,FORM2           * read appropriate number of recs?
          GOTO      GETS3200 IF NOT LESS    * finished looping backwards
          ADD       ONE,FORM2               * increment counter
          GOTO      GETS3100
.
GETS3200  MOVE      SEVEN,CVERT             * position for next display
          MOVE      ZERO,CURRITEM           * current item number
          DISPLAY   *P1:CVERT,*EF;
          GOTO      GETS1000                * start display loop
.
. ------- Finished locating all data -------
.
GETS4000  MOVE      ZERO,ISNEXT             * we do not have a next screen
          CALL      SELI0000                * select an item
          BRANCH    EXIT,GETS5000:          * item selected
                         GETS9000:          * exit selected
                         GETS4000:          * next screen selected
                         GETS3000           * previous screen selected
.
. ------- A session has been selected -------
.
GETS5000  MOVE      ITEMS[SELITEM],KEY22
          CALL      RDOPSES7
          MOVE      ZERO,EXIT
          GOTO      GETS9999
.
.         Exit selected
.
GETS9000  MOVE      ONE,EXIT
          GOTO      GETS9999
.
.         No sessions for this date
.
GETS9100  DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "There are no sessions for this date.    ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
GETS9999  RETURN
+
.******************************************************************************
.* DSPS0000   : Display a single session record                               *
.* Parameters : OPRSESFD record                                               *
.******************************************************************************
DSPS0000  MOVE      OPSECLIN,OPDACLIN
          CALL      GSUR0000                * get clinic/surgeon description
.
          DISPLAY   *P1:CVERT,*V2LON,CURRITEM,*HOFF,DOT:
                    *P4:CVERT,OPCLDESC:
                    *P35:CVERT,OPSETIME," - ",OPSEENDT;
.
.         Get the status of this session
.
          IF        OPSESTAT = 0
            MOVE      "Cancelled ",OPSTATUS
            MATCH     SP3,OPSECANC
            IF        !@EQUAL
              PACK      KEY5,ANSO,ANSC,OPSECANC
              CALL      RDCODE1
              IF        OVRCD = 0
                PACK      OPSTATUS,OPSTATUS,TDESC
              ELSE
                PACK      OPSTATUS,OPSTATUS,OPSECANC
              ENDIF
            ENDIF
          ELSE
            CLEAR     OPSTATUS
            IF        OPSESTAT = 1
              APPEND    "Booked -",OPSTATUS
            ELSE
              APPEND    "Extra  -",OPSTATUS
            ENDIF
            IF        OPSESUSP = 1
              APPEND    " Suspended",OPSTATUS
            ELSE
              APPEND    " Open",OPSTATUS
            ENDIF
            RESET     OPSTATUS
          ENDIF
.
          DISPLAY   *P50:CVERT,OPSTATUS;
.
DSPS9999  RETURN
+
.******************************************************************************
.* SESH0000   : Display the session header                                    *
.* Parameters : OPRSESFD record                                               *
.******************************************************************************
SESH0000  MOVE      OPSEDATE,SESNDATE
          MOVE      OPSETIME,SESNTIME
          MOVE      OPSECLIN,SESNCODE
          MOVE      SP3,CURBKTYP
          CALL      OPDSPSHD
SESH9999  RETURN
+
.******************************************************************************
.* SELT0000   : Select a booking in the specified session                     *
.* Parameters : OPRSESFD record                                               *
.* Returns    : OPRDEAFD record                                               *
.*              EXIT     0 = Record found, 1 = Nothing selected               *
.******************************************************************************
SELT0000  PACK      SESSION,OPSEDATE,OPSETIME,OPSECLIN,SP70
          PACK      KEY26,SESSION,SP20
          CALL      RSOPDEA6
          CALL      RKOPDEA6                * get first session
          BRANCH    OVRCD,SELT9100          * no sessions
.
          PACK      NEWSESS,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN
          MATCH     SESSION,NEWSESS
          GOTO      SELT9100 IF NOT EQUAL   * no sessions for this date
.
.         We have at least one booking for this session
.
          DISPLAY   *P1:8,*EF,*P4:9,*ULON,*V2LON,"Case":
                    *P9:9,"Patient's Name                ":
                    *P40:9," U/R No.",*P49:9," Booking":
                    *P58:9,"Start",*P64:9,"Dur.",*P69:9,"Status";
          MOVE      TEN,CVERT
          MOVE      ZERO,CURRITEM           * current item number
          MOVE      ZERO,ISNEXT             * assume no next screen
          MOVE      ZERO,ISPREV             * assume no prev screen
.
.         Get the list of bookings for this session
.
          PACK      KEY26,SESSION,SP20
          CALL      RSOPDEA6
SELT1000  CALL      RKOPDEA6
          BRANCH    OVRCD,SELT4000
.
          PACK      NEWSESS,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN
          MATCH     SESSION,NEWSESS
          GOTO      SELT4000 IF NOT EQUAL
.
.         Found an item to be displayed
.
          COMPARE   TWENTY3,CVERT
          GOTO      SELT2000 IF LESS
.
          MOVE      ONE,ISNEXT              * we have a next screen
SELT1100  CALL      SELI0000                * select an item
          BRANCH    EXIT,SELT5000:          * item selected
                         SELT9000:          * exit selected
                         SELT1500:          * next screen selected
                         SELT3000           * previous screen selected
.
.         Next screen selected
.
SELT1500  MOVE      TEN,CVERT               * position for next display
          MOVE      ZERO,CURRITEM           * current item number
          DISPLAY   *P1:CVERT,*EF;
          MOVE      ONE,ISPREV              * we have a previous screen
.
.         Display this item
.
SELT2000  ADD       ONE,CURRITEM            * increment item count
          PACK      ITEMS[CURRITEM],OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN:
                                    OPDASTAT,OPDACASE,SP70
          CALL      DSPC0000                * display session
          ADD       ONE,CVERT               * ready for next display
          GOTO      SELT1000
.
.         Previous screen selected
.
SELT3000  MOVE      ITEMS[1],KEY26          * key for first item on screen
          CALL      RDOPDEA1                * read in record
.
.         Loop backwards over one screen
.
          MOVE      TEN,FORM2               * duplicates CVERT in display loop
SELT3100  CALL      RPOPDEA1
          BRANCH    OVRCD,SELT0000
.
          PACK      NEWSESS,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN
          MATCH     SESSION,NEWSESS
          GOTO      SELT0000 IF NOT EQUAL
.
          COMPARE   TWENTY3,FORM2           * read appropriate number of recs?
          GOTO      SELT3200 IF NOT LESS    * finished looping backwards
          ADD       ONE,FORM2               * increment counter
          GOTO      SELT3100
.
SELT3200  MOVE      TEN,CVERT               * position for next display
          MOVE      ZERO,CURRITEM           * current item number
          DISPLAY   *P1:CVERT,*EF;
          GOTO      SELT1000                * start display loop
.
. ------- Finished locating all data -------
.
SELT4000  MOVE      ZERO,ISNEXT             * we do not have a next screen
          CALL      SELI0000                * select an item
          BRANCH    EXIT,SELT5000:          * item selected
                         SELT9000:          * exit selected
                         SELT4000:          * next screen selected
                         SELT3000           * previous screen selected
.
. ------- A booking has been selected -------
.
SELT5000  MOVE      ITEMS[SELITEM],KEY26
          CALL      RDOPDEA1
.
          MOVE      OPDAADMN,BKREBOOK
          MOVE      BKREBOOK,KEY8
          CALL      RDVISA1                 * valid visit number ?
          BRANCH    OVRCD,SELT5100          * no, try booking file
.
          MOVE      BKREBOOK,KEY8
          CALL      RDMISS1                 * valid Admission Number ?
          BRANCH    OVRCD,SELT5100          * try booking file
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,SELT7000
.
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          BRANCH    OVRCD,SELT7000
.
          MOVE      SP8,DDATE
          MOVE      AADMNO,KEY8
          CALL      RDDSCH1
.
          LOAD      BKRESTAT,ASTAT,ONE,THREE,FOUR,THREE,TWO
          MOVE      ZERO,EXIT
          GOTO      SELT9999
.
.         Try the booking file
.
SELT5100  MOVE      BKREBOOK,KEY8
          CALL      RDBKREC1
          BRANCH    OVRCD,SELT8000
          MOVE      BKREURNO,AADMNO
.
          MOVE      BKREURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,SELT7000
          MOVE      ZERO,EXIT
          GOTO      SELT9999
.
.         U/R Number not on file
.
SELT7000  DISPLAY   *P1:24,*EL,*B,*V2LON,"Missing U/R Number [",AURNO,"].    ";
          CALL      HITENTER
          BRANCH    ISNEXT,SELT1100
          GOTO      SELT4000
.
.         Booking number not on file
.
SELT8000  DISPLAY   *P1:24,*EL,*B,*V2LON,"Invalid Booking Number.    ";
          CALL      HITENTER
.
.         Exit selected
.
SELT9000  MOVE      ONE,EXIT
          GOTO      SELT9999
.
.         No bookings for this session
.
SELT9100  DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "There are no bookings for this session.    ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
SELT9999  RETURN
+
.******************************************************************************
.* DSPC0000   : Display a single theatre booking record                       *
.* Parameters : OPRDEAFD record                                               *
.******************************************************************************
DSPC0000  MOVE      OPDAADMN,KEY8
          CALL      RDVISA1
          IF        OVRCD = 1
            MOVE      OPDAADMN,KEY8
            CALL      RDBKREC1
            IF        OVRCD = 1
               MOVE      ZERO,PVIURNO
               MOVE      "Missing Booking Details",PATDESC
               GOTO      DSPC1000
            ENDIF
            MOVE      BKREURNO,PVIURNO
          ENDIF
.
          MOVE      PVIURNO,KEY8
          CALL      RDMAST1
          IF        OVRCD = 1
            MOVE      "Missing Patient Details",PATDESC
          ELSE
            MOVE      PSNAME,PACSNAME
            MOVE      PGNAME,PACGNAME
            MOVE      PTITL,PACTITLE
            MOVE      TWO,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,PATDESC
          ENDIF
.
.         Set record status
.
DSPC1000  IF        OPDASTAT = 3
            MOVE      "Cancelled",OPSTATUS
          ELSE
            IF        OPDATDUR = 0
              MOVE      "Booked",OPSTATUS
            ELSE
              MOVE      "Completed",OPSTATUS
            ENDIF
          ENDIF
.
          DISPLAY   *P1:CVERT,*V2LON,CURRITEM,*HOFF,DOT,*P4:CVERT,OPDACASE:
                    *P9:CVERT,PATDESC,*P39:CVERT,PVIURNO,*P49:CVERT,OPDAADMN:
                    *P58:CVERT,OPDAEXPS,*P64:CVERT,OPDAEXPD:
                    *P69:CVERT,*+,OPSTATUS;
.
DSPC9999  RETURN
+
.******************************************************************************
.* CTMA0000   : Cancel a Team allocated to a theatre booking                  *
.* Parameters : OPRDEAFD record                                               *
.******************************************************************************
CTMA0000  CALL      STMA0000                * select the Team to be cancelled
          BRANCH    EXIT,CTMA9100:          * no teams allocated to booking
                         CTMA9999           * exit selected
.
          CALL      DSTM0000                * display Team Allocation
          CALL      CTMQ0000                * ask if this is correct
          BRANCH    EXIT,CTMA0000           * no, return to select Team again
.
.         Cancel this Team Allocation
.
          PACK      KEY11,OPDBUNIQ,OPDBTEAM
          CALL      DEOPDEB1
          DISPLAY   *P1:24,*EL,*V2LON,"*** Team Allocation Cancelled ***    ";
          CALL      HITENTER
          GOTO      CTMA9999
.
.         Booking has no Team Allocation
.
CTMA9100  DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "This booking does not have any teams allocated.    ";
          CALL      HITENTER
.
CTMA9999  RETURN
+
.******************************************************************************
.* STMA0000   : Select an allocated Team for the current booking              *
.* Parameters : OPRDEAFD record                                               *
.* Returns    : OPRDEBFD record                                               *
.*              EXIT     0 = Team returned, 1 = No teams found                *
.******************************************************************************
STMA0000  PACK      KEY11,OPDAUNIQ,SP1
          CALL      RSOPDEB1
          CALL      RKOPDEB1
          BRANCH    OVRCD,STMA8000
.
          MATCH     OPDAUNIQ,OPDBUNIQ
          GOTO      STMA8000 IF NOT EQUAL
.
.         We have at least one Team. Ask the operator which Team they want.
.
          CALL      DTMS0000                * display teams
.
STMA1000  PACK      KEY11,OPDAUNIQ,SP1
          CALL      RSOPDEB1
          CALL      RKOPDEB1                * default to first Team
.
          DISPLAY   *P1:24,*EL,"Input the Team number to be cancelled : ":
                    *P68:24,"<",*V2LON,ANSE,*HOFF," to Exit>";
          KEYIN     *P41:24,*DV,OPDBTEAM,*P41:24,*V2LON,*RV,OPDBTEAM:
                    *P41:24,*DV,OPDBTEAM;
.
          TYPE      OPDBTEAM                * numeric input ?
          GOTO      STMA2000 IF EQUAL       * yes
          REP       UPPLOW,OPDBTEAM         * no
          MATCH     ANSE,OPDBTEAM
          GOTO      STMA9000 IF EQUAL
.
STMA2000  PACK      KEY11,OPDAUNIQ,OPDBTEAM
          CALL      RDOPDEB1
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,*V2LON,"Invalid Team number.    ";
            CALL      HITENTER
            GOTO      STMA1000
          ENDIF
          MOVE      ZERO,EXIT
          GOTO      STMA9999
.
.         No teams allocated for this booking
.
STMA8000  MOVE      ONE,EXIT
          GOTO      STMA9999
.
.         User wants to exit
.
STMA9000  MOVE      TWO,EXIT
.
STMA9999  RETURN
+
.******************************************************************************
.* DTMS0000   : Display the allocated teams for the current booking           *
.* Parameters : OPRDEAFD record                                               *
.******************************************************************************
DTMS0000  MOVE      TEN1,CVERT
          DISPLAY   *P1:9,*EF,*P4:10,*V2LON,*ULON,"Operating Surgeon",SP20:
                    *P42:10,"Anaesthetist",SP5,SP20;
.
          PACK      KEY11,OPDAUNIQ,SP1
          CALL      RSOPDEB1
DTMS1000  CALL      RKOPDEB1
          BRANCH    OVRCD,DTMS9999
.
          MATCH     OPDAUNIQ,OPDBUNIQ
          GOTO      DTMS9999 IF NOT EQUAL
.
.         Find the surgeon and anaesthetist, and display their name and jobs
.
          MOVE      SP30,SURGDESC
          MOVE      SP30,SURGCODE
          MOVE      SP30,ANAEDESC
          MOVE      SP30,ANAECODE
.
          MOVE      ONE,CLNO
          WHILE     CLNO <= 8
            LOAD      ACODE,CLNO,OPDBTYD1,OPDBTYD2,OPDBTYD3,OPDBTYD4:
                                 OPDBTYD5,OPDBTYD6,OPDBTYD7,OPDBTYD8
.
            MATCH     SP3,ACODE               * check if there is a code
            GOTO      DTMS3000 IF EQUAL       * no, do nothings
.
            PACK      KEY5,ANSO,ANSP,ACODE
            CALL      RDCODE1
            BRANCH    OVRCD,DTMS3000          * no code, ignore
.
.         Check for a surgeon
.
            CMATCH    ANSS,TCINDC1
            IF        @EQUAL
              MATCH     SP20,SURGDESC         * check if already have surgeon
              GOTO      DTMS3000 IF NOT EQUAL
              LOAD      KEY6,CLNO,OPDBDCC1,OPDBDCC2,OPDBDCC3,OPDBDCC4:
                                  OPDBDCC5,OPDBDCC6,OPDBDCC7,OPDBDCC8
              CALL      RDDOCT1
              BRANCH    OVRCD,DTMS3000
.
              MOVE      DSNAME,PACSNAME
              MOVE      DGNAME,PACGNAME
              MOVE      DTITL,PACTITLE
              MOVE      TWO,PACFRMT
              CALL      PACNAME
              MOVE      PACFNAME,SURGDESC
.
              MOVE      DCODE,SURGCODE
            ENDIF
.
.         Check for an anaesthetist
.
            CMATCH    ANSN,TCINDC1
            IF        @EQUAL
              MATCH     SP20,ANAEDESC       * check if already have anaesthetist
              GOTO      DTMS3000 IF NOT EQUAL
              LOAD      KEY6,CLNO,OPDBDCC1,OPDBDCC2,OPDBDCC3,OPDBDCC4:
                                  OPDBDCC5,OPDBDCC6,OPDBDCC7,OPDBDCC8
              CALL      RDDOCT1
              BRANCH    OVRCD,DTMS3000
.
              MOVE      DSNAME,PACSNAME
              MOVE      DGNAME,PACGNAME
              MOVE      DTITL,PACTITLE
              MOVE      TWO,PACFRMT
              CALL      PACNAME
              MOVE      PACFNAME,ANAEDESC
.
              MOVE      DCODE,ANAECODE
            ENDIF
.
DTMS3000    ADD       ONE,CLNO
          DO
.
.         Display Team number and details
.
DTMS4000  DISPLAY   *P1:CVERT,*V2LON,OPDBTEAM,*HOFF,DOT:
                    *P4:CVERT,*V2LON,SURGCODE,*HOFF,SP1,SURGDESC:
                    *P42:CVERT,*V2LON,ANAECODE,*HOFF,SP1,ANAEDESC;
          ADD       ONE,CVERT
          GOTO      DTMS1000
.
DTMS9999  RETURN
+
.******************************************************************************
.* DSTM0000   : Display the Team Allocated                                    *
.* Parameters : OPRDEBFD record                                               *
.******************************************************************************
DSTM0000  DISPLAY   *P1:9,*EF,*P26:9,"Team :   ",*V2LON,OPDBTEAM:
                    *P1:10,*ULON,"Operating Personnel ":
                    *P23:10,"Doctor",SP20,SP5:
                    *P55:10,"Supervisor Level",SP9,*HOFF:
                    *P2:11,*V2LON,"1.",*HOFF,*P21:11,COLON:
                    *P2:12,*V2LON,"2.",*HOFF,*P21:12,COLON:
                    *P2:13,*V2LON,"3.",*HOFF,*P21:13,COLON:
                    *P2:14,*V2LON,"4.",*HOFF,*P21:14,COLON:
                    *P2:15,*V2LON,"5.",*HOFF,*P21:15,COLON:
                    *P2:16,*V2LON,"6.",*HOFF,*P21:16,COLON:
                    *P2:17,*V2LON,"7.",*HOFF,*P21:17,COLON:
                    *P2:18,*V2LON,"8.",*HOFF,*P21:18,COLON:
                    *P1:19,*V2LON,*ULON,"Nursing Personnel":
                    *P23:19,"Nurse",SP20,*HOFF:
                    *P1:20,*V2LON," 9.",*HOFF,*P21:20,COLON:
                    *P1:21,*V2LON,"10.",*HOFF,*P21:21,COLON:
                    *P1:22,*V2LON,"11.",*HOFF,*P21:22,COLON:
                    *P1:23,*V2LON,"12.",*HOFF,*P21:23,COLON;
.
.         Loop over the eight possible doctor codes
.
          MOVE      TEN1,CVERT                * set screen position
          MOVE      ONE,CLNO                  * initialise doctor count
.
          WHILE     CLNO <= 8
            LOAD      ACODE,CLNO,OPDBTYD1,OPDBTYD2,OPDBTYD3,OPDBTYD4:
                                 OPDBTYD5,OPDBTYD6,OPDBTYD7,OPDBTYD8
.
            MATCH     SP3,ACODE               * check if there is a code
            GOTO      DSTM1200 IF EQUAL       * no, do nothing
.
            PACK      KEY5,ANSO,ANSP,ACODE
            CALL      RDCODE1
            IF        OVRCD = 0
              MOVE      TDESC,KEY15           * truncate doctor type code desc
            ELSE
              MOVE      "Missing Code",KEY15
            ENDIF
            DISPLAY   *P5:CVERT,KEY15;
.
            LOAD      KEY6,CLNO,OPDBDCC1,OPDBDCC2,OPDBDCC3,OPDBDCC4:
                                OPDBDCC5,OPDBDCC6,OPDBDCC7,OPDBDCC8
            CALL      RDDOCT1
            BRANCH    OVRCD,DSTM1200
.
            MOVE      DSNAME,PACSNAME
            MOVE      DGNAME,PACGNAME
            MOVE      DTITL,PACTITLE
            MOVE      TWO,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,KEY24
.
            DISPLAY   *P23:CVERT,*V2LON,KEY6,*HOFF," ",KEY24;
.
            LOAD      ACODE,CLNO,OPDBLVD1,OPDBLVD2,OPDBLVD3,OPDBLVD4:
                                 OPDBLVD5,OPDBLVD6,OPDBLVD7,OPDBLVD8
.
            MATCH     SP3,ACODE               * doctor requires supervision ?
            GOTO      DSTM1200 IF EQUAL       * yes
            MOVE      "Missing Code",TDESC    * no
            PACK      KEY5,ANSO,ANSS,ACODE
            CALL      RDCODE1                 * get supervision desc
            DISPLAY   *P55:CVERT,*V2LON,ACODE,*HOFF,SP2,TDESC;
.
.         Increment for next doctor
.
DSTM1200    ADD       ONE,CVERT
            ADD       ONE,CLNO                * set doctor count
          DO
.
.         Loop over the six possible nurse codes
.
          MOVE      TWENTY,CVERT              * set screen position
          MOVE      ONE,CLNO                  * initialise nurse count
.
          WHILE     CLNO <= 6
            LOAD      ACODE,CLNO,OPDBTYN1,OPDBTYN2,OPDBTYN3,OPDBTYN4:
                                 OPDBTYN5,OPDBTYN6
.
            MATCH     SP3,ACODE
            GOTO      DSTM2200 IF EQUAL
            PACK      KEY5,ANSO,ANSN,ACODE
            CALL      RDCODE1
            BRANCH    OVRCD,DSTM2200
            MOVE      TDESC,KEY15             * truncate nurse type
            DISPLAY   *P5:CVERT,KEY15;
.
            LOAD      KEY6,CLNO,OPDBNUC1,OPDBNUC2,OPDBNUC3,OPDBNUC4:
                                OPDBNUC5,OPDBNUC6
            CALL      RDOPNUR1
            BRANCH    OVRCD,DSTM2200
.
            MOVE      TWO,PACFRMT             * use surname, given name
            MOVE      OPNRSNAM,PACSNAME       * surname
            MOVE      OPNRGNAM,PACGNAME       * given names
            MOVE      SP10,PACTITLE
            CALL      PACNAME                 * pack up nurses name
            MOVE      PACFNAME,KEY18
.
            DISPLAY   *P23:CVERT,*V2LON,KEY6,*HOFF," ",KEY18;
.
DSTM2200    ADD       ONE,CVERT
            ADD       ONE,CLNO
          DO
.
.    Display user define info
.
          MOVE      TWENTY,CVERT
          MOVE      TEN3,FORM2                * set select item index
          MOVE      ZERO,CLNO                 * Set user define counter
.
DSTM3100  ADD       ONE,CLNO                  * increment user define counter
          LOAD      EXIT,CLNO,OPCNUTU1,OPCNUTU2,OPCNUTU3,OPCNUTU4
          COMPARE   ONE,EXIT                  * test if user define is ON
          GOTO      DSTM4000 IF NOT EQUAL
.
          LOAD      EXIT,CLNO,SIX,SEVEN,EIGHT,NINE
          PACK      KEY5,ANSQ,EXIT,SP3
          CALL      RDCODE1                   * get user define category desc
          BRANCH    OVRCD,DSTM4000
          MOVE      TDESC,KEY10               * truncate user define cat desc
          DISPLAY   *P49:CVERT,*V2LON,FORM2,".",*HOFF,*P53:CVERT,KEY10:
                    *P64:CVERT,":";
.
          LOAD      KEY3,CLNO,OPDBUSR1,OPDBUSR2,OPDBUSR3,OPDBUSR4
          MATCH     SP3,KEY3                  * test blank user define code
          GOTO      DSTM3200 IF EQUAL
          MOVE      SP20,TDESC
          PACK      KEY5,ANSQ,EXIT,KEY3
          CALL      RDCODE1                   * get user define code desc
          MOVE      TDESC,KEY10               * truncate user define code desc
          DISPLAY   *P66:CVERT,*V2LON,KEY3,*HOFF," ",KEY10;
.
DSTM3200  ADD       ONE,CVERT                 * increment line counter
          ADD       ONE,FORM2                 * increment screen index
.
DSTM4000  COMPARE   FOUR,CLNO                 * test if check all 4 user defines
          GOTO      DSTM3100 IF LESS
.
DSTM9999  RETURN
+
.******************************************************************************
.* CTMQ0000   : Ask if this is the Team to be cancelled                       *
.* Returns    : EXIT     0 = Okay, 1 = No                                     *
.******************************************************************************
CTMQ0000  DISPLAY   *P1:24,*EL,"Is this is the Team to be cancelled (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ? ";
.
CTMQ1000  KEYIN     *P45:24,*EL,*DV,UNDLN1,*P45:24,*V2LON,ANS:
                    *P45:24,*DV,ANS;
          REP       UPPLOW,ANS
.
          MATCH     ANSY,ANS
          GOTO      CTMQ2000 IF EQUAL
          MATCH     ANSN,ANS
          GOTO      CTMQ3000 IF EQUAL
          BEEP
          GOTO      CTMQ1000
.
CTMQ2000  MOVE      ZERO,EXIT
          GOTO      CTMQ9999
.
CTMQ3000  MOVE      ONE,EXIT
.
CTMQ9999  RETURN
+
.******************************************************************************
.                                 DOCNM000
.    Set up doctors name after a successful read on doctor file.
.****************************************************************************
DOCNM000  MOVE      TWO,PACFRMT             * title name surname of doctor
          MOVE      DSNAME,PACSNAME         * use pacdate to fix doctor's name
          MOVE      DGNAME,PACGNAME         * get first letter in given name
          MOVE      DTITL,PACTITLE
          CALL      PACNAME                 * pack up doctor's name
          MOVE      PACFNAME,DESC40
DOCNM999  RETURN
+
. ------------------------
. ------- OPRSESFD -------
. ------------------------
AUSES000  BRANCH    OPCNAUDS,AUSES999 
          PACK      TDESC,KEY19,SP1         * save key19 on entry
.
          COMPARE   FIVE,AUDIFLAG           * test to delete B audit
          GOTO      AUSES600 IF EQUAL
.
          COMPARE   THREE,AUDIFLAG          * is it an after change audit?
          GOTO      AUSES100 IF EQUAL       * Yes. So do not change time
.
          CALL      IBACLOCK                * get current date
          PACK      OPSEAUDD,CCC,CYY,CMM,CDD
          REP       " 0",OPSEAUDD
          MOVE      CTIMEIS,OPSEAUDT        * clock current time
.
AUSES100  MOVE      ONE,OPSEAUDS            * not printed
          MOVE      PASSCODE,OPSEAUDO       * get User ID
          MOVE      PORT,OPSEAUDP           * get Port number
.
          MOVE      AUDIFLAG,OPSEAUDR
          REPLACE   "1A2B3C4D",OPSEAUDR
          PACK      KEY19,OPSEAUDD,OPSEAUDT,OPSEAUDP,OPSEAUDR
          CALL      AWOPSES                 * write audit record
          GOTO      AUSES995
.
.         now delete B audit
.
AUSES600  PACK      KEY19,OPSEAUDD,OPSEAUDT,OPSEAUDP,ANSB
          CALL      ADOPSES                 * delete audit record
.
AUSES995  UNPACK    TDESC,KEY19,ANS         * restore the key19
.
AUSES999  RETURN
+
. ------- Dummy routine for PATCOMN1 (Surname searches) -------
.
GETSVAR   RETURN
+
.==============================================================================
.
          INC       STD001IO
          INC       BOKHEAD
          INC       OPDSPSHD
          INC       OPRSES01
          INC       PATALERT           * needed by PMIHEAD
          INC       PATCOMN1
          INC       PATHEAD
          INC       PATSNDX
          INC       PATSNX2
          INC       PMIHEAD
.
          INC       BOKRC1IO/INC
          INC       PATALRIO/INC       * needed by PATALERT
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATDSCIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATNIDIO/INC
          INC       PATNOBIO/INC
          INC       PATPR1IO/INC
          INC       PATGSRIO/INC
          INC       PATVISIO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
          INC       PATWR1IO/INC
.
          INC       OPRCLIIO/INC
          INC       OPRDEAIO/INC
          INC       OPRDEBIO/INC
          INC       OPRNURIO/INC
          INC       OPRSESIO/INC
.
          INC       OUTPREIO/INC
          INC       NZCOMPIO/INC
          INC       CALCAGE
AGECHK
CLPATMAS  RETURN
+

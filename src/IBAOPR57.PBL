.******************************************************************************
.* System   : Operating Room System                                           *
.* Program  : IBAOPR57                                                        *
.* Desc     : Anaesthetic Usage Report                                        *
.******************************************************************************
.* Date     :  02/01/1991                                                     *
.* Author   :  MMO ( Copied from 5.01 by Peter Eddey)                         *
.* Mods.    :                                                                 *
.*        V10.04.01 15/04/2014  J.Tan          CAR 261630                     *
.*                  Removed port number from temp file name                   *
.*        V5.01.01  08/02/91  ROD                                             *
.*                  Fixed printing of doctor codes and desc's.                *
.*                  15/02/91  ROD                                             *
.*                  Added to default to end date                              *
.*        V5.01.02  19/02/91  Graeme Williams                                 *
.*                  Print headings if no data found                           *
.*                  Print times no anaesthetic was used                       *
.*        V5.01.03  02/07/91  Justin Coates                                   *
.*                  Fixed paging problems                                     *
.*        V5.01.04  18/06/92  ROD    (your'e learning Lofty!)                 *
.*                  Changed to display 'No Anaesthetist'                      *
.*                  Display date range for period                             *
.*        V5.01.05  04/06/1993  Glenn Berry      SRF 120247                   *
.*                  Changed page breaks to line 60                            *
.******************************************************************************
.
          INC       STD001FD
          INC       IBASEQFD/INC            * Sequential Numbers File
          INC       TFILEVAR/INC            * Generate Temp File Name
          INC       WEBERRFD/INC            * Web Server Error Logging
.
          INC       OPRANEFD/INC
          INC       OPRCONFD/INC
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PATDRGFD/INC
+
.**********************************************************************
.*                       CONSTANTS                                    *
.**********************************************************************
CODEOA    INIT      "OA"
.
CODOPT    INIT      " - Doctor Code Seq."
NAMOPT    INIT      " - Doctor Name Seq."
EQUAL30   INIT      "=============================="
DASH30    INIT      "------------------------------"        
.
+
.**********************************************************************
.*                       GLOBAL VARIABLES                             *
.**********************************************************************
ANAES     DIM       6
ANAESCD   DIM       3
ANAESNAM  DIM       25
COUNT     FORM      3
CPTDATE   DIM       8
DCOUNT    DIM       3
DIM3      DIM       3
DISPOPTL  DIM       14
DOCNAME   DIM       20
DOCSURN   DIM       20
EDRGCNUM  DIM       2
EDRGMDSC  DIM       9
EDRGCYR   DIM       4
ENDDATE   DIM       6
ENDFLAG   FORM      1
ENMON     DIM       2
ENMONF    FORM      2
ENYEAR    DIM       2
GRANTOT   FORM      6
INDEX     FORM      1
MON       FORM      2
MOND      DIM       2
MONTH     FORM      2
MONVAR    FORM      5
NINETY    FORM      "90"
NUMBER    FORM      6
PERD      DIM       3
PERS      FORM      2
PRFDATE1  DIM       10
PRTDATE1  DIM       10
PRFDATE2  DIM       10
PRTDATE2  DIM       10
PRDOCT    DIM       6
PRTYPE    DIM       3
SDRGCNUM  DIM       2
SDRGMDSC  DIM       9
SDRGCYR   DIM       4
STDATE    DIM       6
STFDATE   FORM      6
SURGCODE  DIM       6
SURGNAME  DIM       40
SVANDATE  FORM      6
SVENDATE  FORM      6
SVSTDATE  FORM      6
TEMPDATE  DIM       6
XDOCT     DIM       6                   * doctor code
XTYPE     DIM       3
Z3        INIT      "ZZZ"
Z6        INIT      "ZZZZZZ"
.
OPTMPA1   IFILE     SQL, FIXED=82, KEY="U1-6,7-9"
OPTMPA2   IFILE     SQL, FIXED=82, KEY="U10-29,1-6,7-9"
.
CMDLINE   DIM       60
FNAME1    DIM       8
.
.    ----- tEMPFILE vars -----
.
.OPANDOCT DIM       6      6     1   DOCTOR CODE
.                                    zzzzzz = grand totals
.                                    
.OPANTYPE DIM       3      3     7   ANAES CODE
.                                    zzz = total for doctor 
.DOCNAME  DIM       20     20   10   DOCTOR SURNAME
.                                    zzzzzz = grand totals
MON1     FORM      5      4    30
MON2     FORM      5      4    34
MON3     FORM      5      4    38
MON4     FORM      5      4    42
MON5     FORM      5      4    46
MON6     FORM      5      4    50
MON7     FORM      5      4    54
MON8     FORM      5      4    58
MON9     FORM      5      4    62
MON10    FORM      5      4    66
MON11    FORM      5      4    70
MON12    FORM      5      4    74
MON13    FORM      5      4    78
.            End of record     82 
.
PRGID     INIT      "IBAOPR57"
PRGNAM    INIT      "Anaesthetic Usage Report"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                           MAINLINE                                 *
.**********************************************************************
          CALL      INIT0000               * initialization and open files
.
ML0100    CALL      OPTN0000               * select option         
          BRANCH    EXIT,ML9999
.
. ---- main processing ----
.
ML4000    CALL      CLR0000                * clear variables
.
          CALL      FRDT0000               * keyin from date 
          BRANCH    EXIT,ML0100
.
          CALL      TODT0000               * keyin to date
          BRANCH    EXIT,ML4000
.
          CALL      CONT0000               * ok to continue
          BRANCH    EXIT,ML0100,ML0100,ML9999
.
          CALL      TEMP0000               * create tempfile
.
          CALL      EXTR0000               * extract records accord to range
.
          BRANCH    COPTION,ML5000,ML6000
.
ML5000    CALL      PRNA0000               * print report doc code seq
          CALL      KILL0000               * kill tempfile
          GOTO      ML0100
.
ML6000    CALL      PRNB0000               * print report surn seq
          CALL      KILL0000               * kill tempfile
          GOTO      ML0100
.
ML9999    CHAIN     PGM          
+
.****************************************************************************
.*                                INIT0000                                  *
.*                             initialization                               *
.*  The initialization routine is used to display headings and open files.  *
.****************************************************************************
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P50:24,*EL,"Opening ":
                    *P60:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,FORTY;*81,OPCNODSC,*96,OPCNCDSC,*111,OPCNCLSU
.
          DISPLAY   *P60:24,"opranaef";
          OPEN      OPRANAE1,"opranaef"
.
          DISPLAY   *P60:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P60:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P60:24,"patdo1af";
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P60:24,"patdrgaf";
          OPEN      PATDRGA1,"patdrgaf"
.
          DISPLAY   *P60:24,"patdrgaf";
          OPEN      PATDRGA3,"patdrgaf"
.
          MOVE      ONE,CNOUNDLN
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,FNAME1
.
INIT9999  RETURN
+
.****************************************************************************
.*                               OPTN0000                                   *
.*                       select option                                      *
.****************************************************************************
.
OPTN0000  HLINE     *G37,2,52,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Doctor Code Sequence":
                    *P1:6,*V2LON,TWO,*HOFF," = Doctor Surname Sequence"
.
          KEYIN     *P1:10,*EL,"Select Option : _",*P17:10,*V2LON,COPTION
          COMPARE   ZERO,COPTION
          GOTO      OPTN9000 IF EQUAL
.
          BRANCH    COPTION,OPTN8000,OPTN8000
          BEEP
          GOTO      OPTN0000
.
OPTN8000  LOAD      DISPOPTL WITH COPTION OF CODOPT,NAMOPT
          MOVE      DISPOPTL,CPHDROPT              * printing heading field
          DISPLAY   *P52:2,*V2LON,*+,DISPOPTL,SP1
          MOVE      FALSE,EXIT
          GOTO      OPTN9999
.
OPTN9000  MOVE      TRUE,EXIT
.
OPTN9999  RETURN
+
.**********************************************************************
.*                             CLR0000                                *
.*                     Clear variables                                *
.**********************************************************************
CLR0000   MOVE      ZERO,COUNT
CLR1000   ADD       ONE,COUNT
          STORE     ZERO USING COUNT INTO MON1,MON2,MON3,MON4,MON5:
                    MON6,MON7,MON8,MON9,MON10,MON11,MON12,MON13
          COMPARE   TEN3,COUNT
          GOTO      CLR1000 IF LESS
.
CLR2000   MOVE      ZERO,NUMBER
.
CLR9999   RETURN
+
.**********************************************************************
.*                             FRDT0000                               *
.*                     Keyin from date (period)                       *
.**********************************************************************
FRDT0000  DISPLAY   *P1:13,*EF,"Start Date  : ":
                    *P1:14,"End Date    : "
.
          MOVE      TEN3,CVERT
          MOVE      TEN5,CCOL
          MOVE      TWENTY4,EVERT
          MOVE      ONE,ECOL
          MOVE      ZERO,CHIGHLT
          MOVE      CCC,CCENT
          MOVE      SP2,CYEAR
          MOVE      SP2,CMON
          MOVE      SP2,CDAY
.
          CALL      KEYPER
          BRANCH    OVRCD,FRDT8000
          BRANCH    CQUEST,FRDT8100
.
          PACK      KEY6,ICENT,IYEAR,IMON
          REP       " 0",KEY6
          CALL      RDDRGA3
          BRANCH    OVRCD,FRDT8100
.
          UNPACK    DRGFDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P27:13,CPCDATE
          MOVE      CPCDATE,PRFDATE1              * save date for printing
.
.....     UNPACK    DRGTDTE,CCENT,CYEAR,CMON,CDAY
.....     CALL      PACDATE
.....     DISPLAY   *P40:13,CPCDATE
.....     MOVE      CPCDATE,PRTDATE1              * save date for printing
.
          PACK      STDATE,DRGYR,DRGNUM    
.
          REP       " 0",STDATE
          MOVE      DRGMDSC,SDRGMDSC
          MOVE      DRGCNUM,SDRGCNUM
          MOVE      DRGCYR,SDRGCYR
          MOVE      FALSE,EXIT
          GOTO      FRDT9999
.
FRDT8000  MOVE      TRUE,EXIT
          GOTO      FRDT9999
.
FRDT8100  DISPLAY   *P1:24,*EL,*B,"Invalid period - ";
          CALL      HITENTER
          GOTO      FRDT0000
.
FRDT9999  RETURN
+
.**********************************************************************
.*                             TODT0000                               *
.*                     Keyin to date (period)                         *
.**********************************************************************
TODT0000  MOVE      TEN4,CVERT
          MOVE      TEN5,CCOL
          MOVE      TWENTY4,EVERT
          MOVE      ONE,ECOL
          MOVE      ZERO,CHIGHLT
          MOVE      ICENT,CCENT
          MOVE      IYEAR,CYEAR
          MOVE      IMON,CMON
          MOVE      SP2,CDAY
          REP       " 0",CMON
.
          CALL      KEYPER
          BRANCH    OVRCD,TODT8000
          BRANCH    CQUEST,TODT8100
.
          PACK      KEY6,ICENT,IYEAR,IMON
          REP       " 0",KEY6
          CALL      RDDRGA3
          BRANCH    OVRCD,TODT8100
....      UNPACK    DRGFDTE,CCENT,CYEAR,CMON,CDAY
....      CALL      PACDATE
....      DISPLAY   *P27:14,CPCDATE," - "
....      MOVE      CPCDATE,PRFDATE2              * save date for printing
.
          UNPACK    DRGTDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P27:14,CPCDATE
          MOVE      CPCDATE,PRTDATE2              * save date for printing
.
          PACK      ENDDATE,DRGYR,DRGNUM      
          REP       " 0",ENDDATE
.
          MOVE      DRGMDSC,EDRGMDSC
          MOVE      DRGCYR,EDRGCYR
          MOVE      DRGCNUM,EDRGCNUM
.
          MATCH     STDATE,ENDDATE
          GOTO      TODT8200 IF LESS
.
          MOVE      STDATE,KEY6        
          REP       " 0",KEY6
          CALL      CALP0000                 * calculate diff in periods
          COMPARE   TEN3,PERS
          GOTO      TODT8300 IF NOT LESS
.
          MOVE      FALSE,EXIT
          GOTO      TODT9999
.
TODT8000  MOVE      TRUE,EXIT
          GOTO      TODT9999
.
TODT8100  DISPLAY   *P1:24,*EL,*B,"Invalid period - ";
          CALL      HITENTER
          GOTO      TODT0000
.
TODT8200  DISPLAY   *P1:24,*EL,*B,"From date must not be after to date - ";
          CALL      HITENTER
          GOTO      TODT0000
.
TODT8300  DISPLAY   *P1:24,*EL,*B,"Must have less than 13 periods - "; 
          CALL      HITENTER
          GOTO      TODT0000
.
TODT9999  RETURN
+
.**********************************************************************
.*                             CALM0000                               *
.*                    Calculate diffs in months                       *
.**********************************************************************
CALM0000  CALL      RDSDRGA1
          MOVE      ZERO,MONTH
.
CALM1000  CALL      RDKDRGA1
          BRANCH    OVRCD,CALM9000
.
          PACK      TEMPDATE,DRGYR,DRGNUM
          REP       " 0",TEMPDATE
.
          ADD       ONE,MONTH
          MATCH     TEMPDATE,OPANDATE
          GOTO      CALM9999 IF LESS
.
          GOTO      CALM1000
.
CALM9000  ADD       ONE,MONTH
.
CALM9999  RETURN
+
.**********************************************************************
.*                             CALP0000                               *
.*                    Calculate diffs in periods                      *
.**********************************************************************
CALP0000  CALL      RDSDRGA1
          MOVE      ZERO,PERS
.
CALP1000  CALL      RDKDRGA1
          BRANCH    OVRCD,CALP9000
.
          PACK      TEMPDATE,DRGYR,DRGNUM
          REP       " 0",TEMPDATE
.
          ADD       ONE,PERS
          MATCH     TEMPDATE,ENDDATE
          GOTO      CALP9999 IF LESS
.
          GOTO      CALP1000
.
CALP9000  ADD       ONE,PERS
.
CALP9999  RETURN
+
.**********************************************************************
.*                             CONT0000                               *
.*                          Ok to Continue                            *
.**********************************************************************
CONT0000  MOVE      FALSE,EXIT
.
          CALL      CONTQST
          MOVE      ANS,FORM1
          BRANCH    FORM1,CONT9999,CONT9000,CONT9500
          BEEP
          GOTO      CONT0000                          * invalid reply
.
CONT9000  MOVE      TWO,EXIT
          GOTO      CONT9999
.
CONT9500  MOVE      THREE,EXIT
.
CONT9999  RETURN
+
.**********************************************************************
.*                             KILL0000                               *
.*                Kill tempfile                                       *
.**********************************************************************
KILL0000
.
. ----- Kill indexed tempfile -----
.
          CLOSE     OPTMPA1
          CLOSE     OPTMPA2
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME1,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
KILL9999  RETURN
+
.**********************************************************************
.*                             TEMP0000                               *
.*                Create tempfile                                     *
.**********************************************************************
TEMP0000  CALL      KILL0000                * kill tempfile
.
. ----- Create indexed tempfile -----
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME1,CMDLINE
          APPEND    " 82 ",CMDLINE
          APPEND    "U1-6,7-9 U10-29,1-6,7-9",CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
. ----- Open tempfile -----
.
          OPEN      OPTMPA1,FNAME1
          OPEN      OPTMPA2,FNAME1
.
TEMP9999  RETURN
+
.**********************************************************************
.*                             EXTR0000                               *
.*                Extract records according to selection              *
.**********************************************************************
EXTR0000  MOVE     ZERO,NUMBER
          DISPLAY  *P1:24,*EL,"Doctor code : ",*P30:24,"Anaesthetic code : ";
          PACK     KEY15,STDATE,SP10
          CALL     RSOPANE1
.
EXTR1000  CALL     RKOPANE1
          BRANCH   OVRCD,EXTR9999
.
          MATCH    OPANDATE,ENDDATE
          GOTO     EXTR9999 IF LESS
.
          DISPLAY  *P15:24,OPANDOCT,*P49:24,OPANTYPE;                   
.
          MOVE     "No Anaesthetist",DSNAME
          MATCH    SP6,OPANDOCT
          GOTO     EXTR1050 IF EQUAL        * no code
.
          MOVE     "Unknown Doctor Code",DSNAME
          MOVE     OPANDOCT,KEY6
          CALL     RDDOCT1
          GOTO     EXTR1100
.
EXTR1050  MOVE     "******",OPANDOCT
.
EXTR1100  MOVE     DSNAME,DOCSURN
.
          MOVE     STDATE,KEY6
          REP      " 0",KEY6
          CALL     CALM0000                * calc num of months
.
          PACK     KEY9,OPANDOCT,OPANTYPE
          CALL     UPDT0000
.
          PACK     KEY9,OPANDOCT,Z3
          CALL     UPDT0000
.
          PACK     DOCSURN,Z6,Z6,Z6,Z6
.
          PACK     KEY9,Z6,OPANTYPE
          CALL     UPDT0000
.
          PACK     KEY9,Z6,Z3
          CALL     UPDT0000
.
          GOTO     EXTR1000
.
EXTR9999  RETURN
+
.**********************************************************************
.*                             UPDT0000                               *
.*                Update  records according to selection              *
.**********************************************************************
UPDT0000  CALL     RDTMP
          BRANCH   OVRCD,UPDT5000
.
          LOAD     NUMBER,MONTH,MON1,MON2,MON3,MON4,MON5,MON6,MON7,MON8:
                                MON9,MON10,MON11,MON12,MON13
          ADD      OPANNUMB,NUMBER
          STORE    NUMBER,MONTH,MON1,MON2,MON3,MON4,MON5,MON6,MON7,MON8:
                                MON9,MON10,MON11,MON12,MON13
          CALL     UPTMP
          GOTO     UPDT9999
.
UPDT5000  MOVE     ZERO,MON1
          MOVE     ZERO,MON2
          MOVE     ZERO,MON3
          MOVE     ZERO,MON4
          MOVE     ZERO,MON5
          MOVE     ZERO,MON6
          MOVE     ZERO,MON7
          MOVE     ZERO,MON8
          MOVE     ZERO,MON9
          MOVE     ZERO,MON10
          MOVE     ZERO,MON11
          MOVE     ZERO,MON12
          MOVE     ZERO,MON13
.
          STORE    OPANNUMB,MONTH,MON1,MON2,MON3,MON4,MON5,MON6,MON7,MON8:
                                  MON9,MON10,MON11,MON12,MON13
          CALL     WRTMP
.
UPDT9999  RETURN
.**********************************************************************
.*                             PRNA0000                               *
.*                Print records from the tempfile                     *
.**********************************************************************
PRNA0000  CLOCK     TIME,CTIMEIS
          DISPLAY   *P1:24,*EL,*P50:24,"*** Printing records ***",*W
          MOVE      NINETY,CLNO
.
          MOVE      SP6,PRDOCT
          MOVE      SP3,PRTYPE
.
PRNA0100  MOVE      SP9,KEY9 
          CALL      RSTMP
.
PRNA1000  CALL      RKTMP
          BRANCH    OVRCD,PRNA8000
.
          CALL      PRNT0000                  * print the records
.
          GOTO      PRNA1000
.
PRNA8000  CALL      ENDP0000                  * last lines
.
PRNA9999  RETURN
+
.**********************************************************************
.*                             PRNB0000                               *
.*                Print records from the tempfile                     *
.**********************************************************************
PRNB0000  CLOCK     TIME,CTIMEIS
          DISPLAY   *P1:24,*EL,*P50:24,*EL,"*** Printing records ***",*W
          MOVE      NINETY,CLNO
.
          MOVE      SP6,PRDOCT
          MOVE      SP6,XDOCT
          MOVE      SP3,PRTYPE
.
PRNB0100  PACK      KEY29,SP20,SP10
          CALL      RSTMP2
.
PRNB1000  CALL      RKTMP2
          BRANCH    OVRCD,PRNB8000
.
          CALL      PRNT0000                  * print the records
          GOTO      PRNB1000
.
PRNB8000  CALL      ENDP0000                  * last lines
.
PRNB9999  RETURN
+
.**********************************************************************
.*                             PRNT0000                               *
.*                Print records from the tempfile                     *
.**********************************************************************
PRNT0000  COMPARE   "60",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          MOVE      MON1,NUMBER
          ADD       MON2,NUMBER
          ADD       MON3,NUMBER
          ADD       MON4,NUMBER
          ADD       MON5,NUMBER
          ADD       MON6,NUMBER
          ADD       MON7,NUMBER
          ADD       MON8,NUMBER
          ADD       MON9,NUMBER
          ADD       MON10,NUMBER
          ADD       MON11,NUMBER
          ADD       MON12,NUMBER
          ADD       MON13,NUMBER
.
. --- print '--''s ----
.
PRNT0050  MATCH     XDOCT,Z6
          GOTO      PRNT5000 IF EQUAL
.
PRNT0070  MATCH     PRDOCT,XDOCT
          CALL      HEDD0000 IF NOT EQUAL
.
          MATCH     XTYPE,Z3
          GOTO      PRNT2000 IF NOT EQUAL
.
. --- print '--''s ----
.
          PRINT     *1,DASH30;
          MOVE      ONE,COUNT
          MOVE      THIRTY1,CCOL
PRNT0100  PRINT     *CCOL,"------";
          ADD       SEVEN,CCOL
          ADD       ONE,COUNT
          COMPARE   COUNT,PERS                    * all pers yet ????
          GOTO      PRNT0100 IF NOT LESS 
          PRINT     *CCOL,"-------"                * total line
          ADD       ONE,CLNO
.
.     --- print line
.
          PRINT     *15,"       Total";
          MOVE      ONE,COUNT
          PRINT     *30;
PRNT0500  LOAD      MONVAR USING COUNT FROM MON1,MON2,MON3,MON4:
                    MON5,MON6,MON7,MON8,MON9,MON10,MON11,MON12,MON13
          PRINT     SP2,MONVAR;
          ADD       ONE,COUNT
          COMPARE   COUNT,PERS
          GOTO      PRNT0500 IF NOT LESS
          PRINT     SP2,NUMBER
          ADD       ONE,CLNO
.
. --- print '--''s ----
.
PRNT0700  
          PRINT     *1,EQUAL30;                 *Print's 30 "="
          MOVE      ONE,COUNT
          MOVE      THIRTY1,CCOL
PRNT0800  PRINT     *CCOL,"=======";
          ADD       SEVEN,CCOL
          ADD       ONE,COUNT
          COMPARE   COUNT,PERS                    * all pers yet ????
          GOTO      PRNT0800 IF NOT LESS 
          PRINT     *CCOL,"======="                * total line
          ADD       ONE,CLNO
          GOTO      PRNT9999
.
.    print normal line
.
PRNT2000  MOVE      "No Anaesthetic      ",TDESC
.
          MATCH     SP3,XTYPE
          GOTO      PRNT2050 IF EQUAL
.
          MOVE      "Unknown Anaes. Code",TDESC
          PACK      KEY5,CODEOA,XTYPE
          CALL      RDCODE1
.
PRNT2050  PRINT     *5,TDESC,SP5;
          MOVE      ONE,COUNT
.
PRNT2100  LOAD      MONVAR USING COUNT FROM MON1,MON2,MON3,MON4:
                    MON5,MON6,MON7,MON8,MON9,MON10,MON11,MON12,MON13
          PRINT     SP2,MONVAR;
          ADD       ONE,COUNT
          COMPARE   COUNT,PERS
          GOTO      PRNT2100 IF NOT LESS
          PRINT     SP2,NUMBER
          ADD       ONE,CLNO
          GOTO      PRNT9999
.
.    doctor code is = 'zzzzzz'  ( means we print totals )
.
PRNT5000  MATCH    PRDOCT,XDOCT
          GOTO     PRNT5010 IF EQUAL
          MOVE     ONE,ENDFLAG
          CALL     HEAD0000
.  
PRNT5010  MATCH     XTYPE,Z3
          GOTO      PRNT7000 IF EQUAL
.
          MOVE      XDOCT,PRDOCT
.
          MOVE      "No Anaesthetic      ",TDESC
          MATCH     SP3,XTYPE
          GOTO      PRNT5050 IF EQUAL
.
          MOVE      "Unknown Anaes. Code",TDESC
          PACK      KEY5,CODEOA,XTYPE
          CALL      RDCODE1
.
PRNT5050  PRINT     *5,TDESC,SP5;
          MOVE      ONE,COUNT
.
PRNT5100  LOAD      MONVAR USING COUNT FROM MON1,MON2,MON3,MON4:
                    MON5,MON6,MON7,MON8,MON9,MON10,MON11,MON12,MON13
          PRINT     SP2,MONVAR;
          ADD       ONE,COUNT
          COMPARE   COUNT,PERS
          GOTO      PRNT5100 IF NOT LESS
          PRINT     SP2,NUMBER
          ADD       ONE,CLNO
          GOTO      PRNT9999
.
PRNT7000  
.
. --- print '--''s ----
.
          PRINT     *1,DASH30;
          MOVE      ONE,COUNT
          MOVE      THIRTY1,CCOL
PRNT7100  PRINT     *CCOL,"------";
          ADD       SEVEN,CCOL
          ADD       ONE,COUNT
          COMPARE   COUNT,PERS                    * all pers yet ????
          GOTO      PRNT7100 IF NOT LESS 
          PRINT     *CCOL,"-------"
          ADD       ONE,CLNO
.
.     --- print line
.
          PRINT     *15,"Grand Total",SP4;
          MOVE      ONE,COUNT
PRNT7500  LOAD      MONVAR USING COUNT FROM MON1,MON2,MON3,MON4:
                    MON5,MON6,MON7,MON8,MON9,MON10,MON11,MON12,MON13
          PRINT     SP2,MONVAR;
          ADD       ONE,COUNT
          COMPARE   COUNT,PERS
          GOTO      PRNT7500 IF NOT LESS
          PRINT     SP2,NUMBER
          ADD       ONE,CLNO
.
. --- print '--''s ----
.
PRNT7700  
          PRINT     *1,EQUAL30;
          MOVE      ONE,COUNT
          MOVE      THIRTY1,CCOL
PRNT7800  PRINT     *CCOL,"=======";
          ADD       SEVEN,CCOL
          ADD       ONE,COUNT
          COMPARE   COUNT,PERS                    * all pers yet ????
          GOTO      PRNT7800 IF NOT LESS 
          PRINT     *CCOL,"======="                * total line
          ADD       ONE,CLNO
          GOTO      PRNT9999
.
PRNT9999  RETURN
+
.**********************************************************************
.*                             HEAD0000                               *
.*                Print heading                                       *
.**********************************************************************
HEAD0000  COMPARE   "60",CLNO
          GOTO      HEAD0100 IF NOT LESS
.
.         If there is room on the current page, then don't start a
.         new page for the report summary heading.
.
          BRANCH    ENDFLAG,HEAD0800
.
HEAD0100  MOVE      XDOCT,PRDOCT
          MOVE      XTYPE,PRTYPE
.
          CALL      HEAD132
.
          PRINT     *50,"From : ",*57,SDRGCNUM,SLASH,SDRGCYR:
                    SP2,PRFDATE1:
                    *N,*50,"To   : ",*57,EDRGCNUM,SLASH,EDRGCYR:
                    SP2,PRTDATE2,*N,*N
          ADD       "4",CLNO
          GOTO      HEAD1000
.
HEAD0800  PRINT     " "
          ADD       "1",CLNO
HEAD1000
          PRINT     *1,DASH30;
          MOVE      ONE,COUNT
          MOVE      THIRTY1,CCOL
HEAD2000  PRINT     *CCOL,"-------";
          ADD       SEVEN,CCOL
          ADD       ONE,COUNT
          COMPARE   COUNT,PERS                    * all pers yet ????
          GOTO      HEAD2000 IF NOT LESS 
          PRINT     *CCOL,"-------"
          ADD       "1",CLNO
.
          BRANCH    ENDFLAG,HEAD2500
          GOTO      HEAD2800
HEAD2500
          PRINT     *1,"Report Summary";
HEAD2800
          MOVE      THIRTY2,CCOL
          MOVE      STDATE,KEY6
          REP       " 0",KEY6
          CALL      RDDRGA1
          BRANCH    OVRCD,HEAD3000
          UNPACK    STDATE,CCENT,CYEAR,DRGNUM
          GOTO      HEAD5000
.
HEAD3000  CALL      RDKDRGA1
          BRANCH    OVRCD,HEAD7000
.
          PACK      TEMPDATE,DRGYR,DRGNUM
          REP       " 0",TEMPDATE
.
          MATCH     TEMPDATE,ENDDATE
          GOTO      HEAD7000 IF LESS
.
HEAD5000  UNPACK    DRGCYR,CCENT,CYEAR
HEAD6000  PRINT     *CCOL,DRGCNUM,SLASH,CYEAR;
          ADD       SEVEN,CCOL
          GOTO      HEAD3000
.
HEAD7000  PRINT     *CCOL," Total"
          ADD       "1",CLNO
          PRINT     *1,DASH30;            
.
          MOVE      ONE,COUNT
          MOVE      THIRTY1,CCOL
HEAD8000  PRINT     *CCOL,"------";
          ADD       SEVEN,CCOL
          ADD       ONE,COUNT
          COMPARE   COUNT,PERS                    * all pers yet ????
          GOTO      HEAD8000 IF NOT LESS 
          PRINT     *CCOL,"-------"
          ADD       "1",CLNO
.
          BRANCH    ENDFLAG,HEAD9999
          CALL      HEDD0000
.
HEAD9999  RETURN
+
.**********************************************************************
.*                             HEDD0000                               *
.*                print doctor heading                                *
.**********************************************************************
HEDD0000  MOVE      XDOCT,PRDOCT
          MOVE      XDOCT,KEY6                  * set up key
.
          MATCH     "ZZZZZZ",XDOCT
          GOTO      HEDD9999 IF EQUAL       * total so dont get name
.
          MATCH     "******",XDOCT
          GOTO      HEDD7100 IF EQUAL       * no code
.
          CALL      RDDOCT1
          BRANCH    OVRCD,HEDD7000              * not on file?
          MOVE      ONE,PACFRMT                 * type of format of name
          MOVE      DGNAME,PACGNAME
          MOVE      DSNAME,PACSNAME
          MOVE      DTITL,PACTITLE
          CALL      PACNAME
          PRINT     *1,XDOCT,*9,PACFNAME        * print formatted name
          ADD       "1",CLNO
          GOTO      HEDD9999
.
HEDD7000  PRINT     *1,XDOCT,*9,"<< Missing name >>"
          ADD       "1",CLNO
          GOTO      HEDD9999
.
HEDD7100  PRINT     *1,SP6,*9,"<< No Anaesthetist >>"
          ADD       "1",CLNO
.
HEDD9999  RETURN
+
.**********************************************************************
.*                             ENDP0000                               *
.*                last lines                                          *
.**********************************************************************
ENDP0000  MOVE      ONE,ENDFLAG
          PRINT     *N,"  ***  END OF REPORT  ***"
.
ENDP9999  RETURN
+
.****************************************************************************
.*                      TEMPFILE IO'S                                       *
.****************************************************************************
RDTMP     MOVE      ZERO,OVRCD
          RESET     KEY9
          READ      OPTMPA1,KEY9;XDOCT,XTYPE:
                           DOCSURN,MON1,MON2,MON3,MON4,MON5,MON6,MON7:
                           MON8,MON9,MON10,MON11,MON12,MON13
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTMP     MOVE      ZERO,OVRCD
          RESET     KEY9
          READ      OPTMPA1,KEY9;;
          RETURN
.
RKTMP     MOVE      ZERO,OVRCD
          READKS    OPTMPA1;XDOCT,XTYPE:
                           DOCSURN,MON1,MON2,MON3,MON4,MON5,MON6,MON7:
                           MON8,MON9,MON10,MON11,MON12,MON13
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTMP     RESET     KEY9
          WRITE     OPTMPA1,KEY9;KEY9:
                           DOCSURN,MON1,MON2,MON3,MON4,MON5,MON6,MON7:
                           MON8,MON9,MON10,MON11,MON12,MON13
          RETURN
.
UPTMP     UPDATE    OPTMPA1;XDOCT,XTYPE:
                           DOCSURN,MON1,MON2,MON3,MON4,MON5,MON6,MON7:
                           MON8,MON9,MON10,MON11,MON12,MON13
          RETURN
.
RSTMP2    MOVE      ZERO,OVRCD
          RESET     KEY29
          READ      OPTMPA2,KEY29;;
          RETURN
.
RKTMP2    MOVE      ZERO,OVRCD
          READKS    OPTMPA2;XDOCT,XTYPE:
                           DOCSURN,MON1,MON2,MON3,MON4,MON5,MON6,MON7:
                           MON8,MON9,MON10,MON11,MON12,MON13
          GOTO      OVERCOND IF OVER
          RETURN
.
.****************************************************************************
          INC       STD001IO
          INC       PATCOMN3
          INC       TFILENAM                * Generate Temp File Name
.
          INC       IBASEQIO/INC            * Sequential Numbers File
          INC       WEBERRIO/INC            * Web Server Error Logging
          INC       OPRANEIO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATDRGIO/INC

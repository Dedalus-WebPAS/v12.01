.******************************************************************************
.* System         : Medical Records Tracking                                  *
.* Program        : IBAMRT71.PBL                                              *
.* Name           : Medical Records Master File Generation                    *
.******************************************************************************
.* Date           : 04/03/1997                                                *
.* Author         : Darren Jones                                              *
.* Function       : Write mrtmasaf & mrthisaf records from patma1af           *
.* Modifications  :                                                           *
.*        V10.02.01 04/07/2011  Ebon Clements  CAR 240724                     *
.*                  Mods for MRT hospital/location - Added hospital to        *
.*                  location key                                              *
.*        V10.00.01 06/10/2010  Mike Laming      CAR 231158                   *
.*                  Change "clear MRT Master" to use module CLMRTMAS          *
.*        V9.09.01  21/02/2008  Ebon Clements    CAR 161950                   *
.*                  Fixed population of current location                      *
.*        V9.08.02  23/03/2007  Janet George     CAR 115701                   *
.*                  Added Multihospital checks. Added options for U/R Range   *
.*                  and by U/R Prefix. Also prints U/Rs with a different type *
.*                  of record                                                 *
.*        V9.08.01  25/01/2007  Peter Vela       CAR 127930                   *
.*                  Added MRMADTCR MRMATMCR before WRTMAS1                    *
.*        V9.04.01  29/04/2005  Peter Vela       CAR 55214                    *
.*                  Recompiled for MRTCONFD                                   *
.*        V9.03.01  18/12/2003  Peter Vela CAR 43134                          *
.*                  Recompiled for MRTCONFD                                   *
.*        V5.09.B01 01/02/2001  J.Tan                                         *
.*                  Changed for WEB                                           *
.*        V5.08.01  29/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.06.00  16/07/1998  Glenn Berry
.*                  Ported to 5.06
.******************************************************************************
+
          INC       STD001FD
.
          INC       IBACONFD/INC            * Control file
          INC       MRTCONFD/INC            * Control file
          INC       MRTHISFD/INC            * Tracking history file
          INC       MRTMASFD/INC            * MRT Master file
          INC       MRTVISFD/INC            * MRT Visit File
          INC       PATMA1FD/INC            * Patient Master file
          INC       PATHSPFD/INC            * Patient Hospital File
          INC       PMSPX2FD/INC            * Patient PMI Ext File 
.
. ----- Program Variables -----
.
URPREFIX  DIM       1                       * U/R Prefix
REDRCCNT  FORM      6                       * Read record counter
WRTRCCNT  FORM      6                       * Write record counter
FROMURNO  DIM       8
FROMURNF  FORM      8
FROMURTX  DIM       8                       * Keyin From UR Number 
TOOOURNO  DIM       8
TOOOURNF  FORM      8
TOOOURTX  DIM       8                       * Keyin To UR Number 
START     INIT      "Start     "
END       INIT      "End       "
SAVKEY20  DIM       20                       * Dummy Key variable
DIM20     DIM       20                       * Dummy Key variable
.
. ----- Program Constants -----
.
PRGID     INIT      "IBAMRT71"
PRGNAM    INIT      "Medical Records Master File Generation"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                   ML0000                                   *
.*                                 Main Module                                *
.******************************************************************************
ML0000    CALL      INIT0000                * Initialise variables & open files
.
ML1000    CALL      OPTN0000                * Choose main option
          BRANCH    OPTION,ML2000,ML1500,ML1700
          BRANCH    EXIT,ML9999
.
ML1500    CALL      RANG0000                * Keyin U/R Range
          BRANCH    EXIT,ML1000
          GOTO      ML2000
.
ML1700    CALL      PFXUR000                * Keyin U/R Prefix
          BRANCH    EXIT,ML1000
.
ML2000    CALL      WRIT0000                * Write new details
.
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                   INIT0000             Called by: ML0000   *
.*                       Initialise Variable & Open Files                     *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen heading
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"mrthisaf";
          OPEN      MRTHISA1,"mrthisaf"
.
          DISPLAY   *P64:24,"mrtmasaf";
          OPEN      MRTMASA1,"mrtmasaf"
.
          DISPLAY   *P64:24,"mrtvisaf";
          OPEN      MRTVISA1,"mrtvisaf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          READ      CONTROLF,SIXTY;*84,MRCNHLOC,MRCNRCTY,MRCNVOLM,*108,MRCNSTAT
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          IF        IBCNMHOS=1
            OPEN      PATHSPA1,"pathspaf" 
          ENDIF
.
INIT9000  MOVE      ZERO,EXIT
.
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000              Called by: ML0000   *
.*                                Choose Option                               *
.******************************************************************************
OPTN0000  MOVE      ZERO,OPTION
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Generate Master File Records":
                    *P1:6,*V2LON,"2",*HOFF," = By U/R Range":  
                    *P1:7,*V2LON,*HOFF,"(Note:Not to be used for prefixed":
                    *P34:7,*V2LON,*HOFF," U/Rs with differing ranges)":
                    *P1:8,*V2LON,"3",*HOFF," = By prefixed U/R":
                    *P1:10,"Select Option : ";
OPTN1000  KEYIN     *P17:10,*V2LON,*DV,UNDLN2,*P17:10,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN9000,OPTN9000,OPTN9000
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                              RANG0000                                      *
.*                     input report range/s screen                            *
.******************************************************************************
RANG0000  MOVE      ZERO,EXIT
.
          DISPLAY   *P1:10,*EF,"U/R No. - From : ",FROMURTX:
                    *P1:11,*EL,"        - To   : ",TOOOURTX
.
          MATCH     FROMURTX,START
          IF        @EQUAL
            MOVE      SP8,FROMURNO
          ENDIF
          MATCH     TOOOURTX,END
          IF        @EQUAL
            MOVE      SP8,TOOOURNO
          ENDIF
.
RANG1000  KEYIN     *P18:10,*EL,*V2LON,*JR,FROMURNF;
          MOVE      FROMURNF,FROMURNO
          MOVE      FROMURNF,FROMURTX
          COMPARE   ZERO,FROMURNF
          IF        @EQUAL | @EOS
            MOVE      SP8,FROMURNO
            MOVE      START,FROMURTX        * "Start"
            DISPLAY   *P18:10,*EL,FROMURTX
          ENDIF
.
.                                           * keyin "To U/R"
RANG2000  KEYIN     *P18:11,*EL,*V2LON,*JR,TOOOURNF;
          MOVE      TOOOURNF,TOOOURNO
          MOVE      TOOOURNF,TOOOURTX
          COMPARE   ZERO,TOOOURNF
          IF        @EQUAL | @EOS
            MOVE      "99999999",TOOOURNO
            MOVE      TOOOURNO,TOOOURNF
            MOVE      END,TOOOURTX          * "End   "
            DISPLAY   *P18:11,*EL,TOOOURTX
          ENDIF
.
RANG5000  MOVE      ZERO,EXIT
.
          CALL      CONTQST                 * Ok to Continue (Y/N/C) ?
          BRANCH    CEXIT,RANG9999,RANG1000,RANG9000
          GOTO      RANG5000
.
RANG9000  MOVE      ONE,EXIT
.
RANG9999  RETURN
+
.******************************************************************************
.*                                PFXUR000              Called by: ML1700     *
.*                 Keyin U/R Prefix if option 3 is selected                   *
.******************************************************************************
PFXUR000  MOVE      ZERO,EXIT
          MOVE      SP70,URPREFIX
          KEYIN     *P1:12,*EF,"U/R Prefix : ",URPREFIX 
.
          RESET     URPREFIX
          MATCH     SP70,URPREFIX
          GOTO      PFXUR900 IF EQUAL
.
PFXUR500  CALL      CONTQST                 * Ok to Continue (Y/N/C) ?
          BRANCH    CEXIT,PFXUR999,PFXUR000,PFXUR900
          GOTO      PFXUR500
.
PFXUR900  MOVE      ONE,EXIT
.
PFXUR999  RETURN
.******************************************************************************
.*                                   WRIT0000             Called by: ML0000   *
.*                               Write New Details                            *
.******************************************************************************
WRIT0000  MOVE      ZERO,CPAGENO 
          MOVE      ZERO,REDRCCNT           * Reset the read record counter
          MOVE      ZERO,WRTRCCNT           * Reset the write record counter
          DISPLAY   *P1:24,*EL,"Scanning : ",*P35:24,"Writing : ";
.
          CALL      PRNTH0000               * Print Header Info 
.
          IF        OPTION=2
            MATCH     FROMURTX,START
            IF        @EQUAL
              PACK      KEY8,SP70
              CALL      RDSMAST1
              GOTO      WRIT1000  
            ENDIF
            PACK      KEY8,FROMURTX,SP70
            CALL      RDMAST1
            IF        OVRCD<>1
              GOTO      WRIT1200
            ENDIF
          ELSE
            PACK      KEY8,SP70
          ENDIF
          CALL      RDSMAST1                * Position on & read a master file
WRIT1000  CALL      RDKMAST1
          BRANCH    OVRCD,WRIT8000
.
WRIT1200  IF        OPTION=2
            MATCH     TOOOURTX,END
            GOTO      WRIT1500 IF EQUAL
.
            MATCH     PURNO,TOOOURTX
            IF        @LESS
              GOTO      WRIT8000
            ENDIF
          ENDIF
. 
          IF        OPTION=3
            SCAN      URPREFIX,PURNO
            IF        !@EQUAL
              GOTO      WRIT1000
            ENDIF 
            RESET       PURNO
          ENDIF
.
WRIT1500  ADD       ONE,REDRCCNT            * Increment the read record counter
          IF        REDRCCNT%10=1
            DISPLAY   *P12:24,*V2LON,PURNO;
          ENDIF
.
.         Clear all file variables
.
          CALL      CLMRTMAS                                          *C-231158
.
. ----- Set up mrtmasaf records -----
.
          MOVE      PURNO,MRMAURNO
.
.         Multihospital Checks
.
          IF        IBCNMHOS=1
            PACK      KEY8,MRMAURNO,SP70
            CALL      RDPMPX21
            BRANCH    OVRCD,WRIT1000
.
            PACK      KEY3,PMPXIHOS,SP70
            CALL      RDPTHSP1                     
            BRANCH    OVRCD,WRIT1000
.
.       Get values from the hospital file if multihospital is enabled   
.
            MOVE      PTHSHHOS,MRMAHHOS            * Home Location Hospital
            MOVE      PTHSHLOC,MRMAHLOC            * Home Location
            MOVE      PTHSHHOS,MRMACHOS            * Current Location Hospital
            MOVE      MRMAHLOC,MRMACLOC            * Current Location
            MOVE      PTHSRCTY,MRMADOTY            * Document Type
            MOVE      PTHSSTAT,MRMASTAT            * Status
            SQUEEZE   PTHSVOLM
            MOVE      PTHSVOLM,MRMAVOLN            * Volume 
            MOVE      SP20,MRMACOMM                * Comments
          ELSE
            MOVE      SP70,MRMAHHOS                * Home Location Hospital
            MOVE      MRCNHLOC,MRMAHLOC            * Home Location 
            MOVE      SP70,MRMACHOS                * Current Location Hospital
            MOVE      MRMAHLOC,MRMACLOC            * Current Location
            MOVE      MRCNRCTY,MRMADOTY            * Document Type
            MOVE      MRCNSTAT,MRMASTAT            * Status
            SQUEEZE   MRCNVOLM
            MOVE      MRCNVOLM,MRMAVOLN            * Volume 
            MOVE      SP20,MRMACOMM                * Comments
          ENDIF
.
.Skip the record if no home location exists
.
          MATCH     SP70,MRMAHLOC
          GOTO      WRIT1000 IF EQUAL
.
.Skip the record if no document type exists
.
          MATCH     SP70,MRMADOTY
          GOTO      WRIT1000 IF EQUAL
.
.Skip the record if no volume exists
.
          COMPARE   ZERO,MRMAVOLN
          GOTO      WRIT1000 IF EQUAL
.
          PACK      KEY20,MRMAURNO,MRMAHHOS,MRMAHLOC,MRMADOTY,MRMAVOLN
          CALL      RAMRMAS1                * Position read a master file record
          COMPARE   ONE,OVRCD
          GOTO      WRIT1000 IF NOT EQUAL   * Record exists, get next UR
.
          READ      CONTROLF,SIXTY;*95,MRCNUKEY
          ADD       ONE,MRCNUKEY
          WRITAB    CONTROLF,SIXTY;*95,MRCNUKEY
          SUB       ONE,MRCNUKEY
.
          MOVE      MRCNUKEY,MRMAKEY
          CALL      IBACLOCK
          PACK      MRMADTCR,CCC,CYY,CMM,CDD
          REP       " 0",MRMADTCR
          MOVE      CTIMEIS,MRMATMCR
          MOVE      SP70,MRMACUID
          MOVE      SP70,MRMADTUP
          MOVE      SP70,MRMATMUP
          MOVE      SP70,MRMAUUID
          MOVE      SP20,MRMASPAR
.
          PACK      KEY20,MRMAURNO,MRMAHHOS,MRMAHLOC,MRMADOTY,MRMAVOLN
          CALL      WRTMAS1                 * Write a master file record
.
. ----- Set up mrthisaf records -----
.
          MOVE      MRMAKEY,MRHIKEY
.
          CALL      IBACLOCK                * Get the current date
          PACK      MRHIDATE,CCC,CYY,CMM,CDD
          REP       " 0",MRHIDATE
.
          CLOCK     TIME,MRHITIME
          REP       " 0",MRHITIME
.
          MOVE      MRMAHHOS,MRHIDHOS
          MOVE      MRMAHLOC,MRHILOC
          MOVE      SP6,MRHIRECV
          MOVE      SP4,MRHIREAS
          MOVE      SP6,MRHIOPER
          MOVE      SP8,MRHIDDUE
          MOVE      "2",MRHISTAT
          MOVE      SP2,MRHILOCK
          MOVE      SP20,MRHISPAR
.
          PACK      KEY26,MRHIKEY,MRHIDATE,MRHITIME
          CALL      RAMRHIS1                * Position read a history file rec
          COMPARE   ONE,OVRCD
          GOTO      WRIT1000 IF NOT EQUAL   * Record exists, get next
.
          PACK      KEY26,MRHIKEY,MRHIDATE,MRHITIME
          CALL      WRMRHIS1                 * Write a history file record
.
          ADD       ONE,WRTRCCNT            * Increment the write record counter
          IF        WRTRCCNT%10=1
            DISPLAY   *P45:24,*V2LON,PURNO;
          ENDIF
.
.         Check if any other record(other than default)
.         exists for the same UR
.
          CALL      CHEK0000
.
          GOTO      WRIT1000
.
WRIT8000  PRINT     *N,*1,"-----------------------------------":
                    *36,"------------------------------":
                    *66,"--"
          PRINT     *N,"***END OF REPORT***"
.
          DISPLAY   *P1:23,*EF,*V2LON,REDRCCNT,*HOFF," records read.  ":
                               *V2LON,WRTRCCNT,*HOFF," records written.  ":
                    *P1:24,*B;
.
          CALL      HITENTER
.
          MATCH     "IBARSH99",PGM
          IF        @EQUAL
            PRINT     *1,REDRCCNT," records read. ":
                      *N,*1,WRTRCCNT," records written. "
          ENDIF
.
WRIT9000  MOVE      ZERO,EXIT
.
WRIT9999  RETURN
.------------------------------------------------------------
.      Check if any other record exists for the same UR  
.------------------------------------------------------------
CHEK0000  PACK      SAVKEY20,MRMAURNO,MRMAHHOS,MRMAHLOC,MRMADOTY,MRMAVOLN,SP70
.
          PACK      KEY20,PURNO,SP70
          CALL      RSMRMAS1
CHEK1000  CALL      RKMRMAS1 
          BRANCH    OVRCD,CHEK9999
.
          MATCH     MRMAURNO,PURNO
          GOTO      CHEK9999 IF NOT EQUAL
.
          PACK      DIM20,MRMAURNO,MRMAHHOS,MRMAHLOC,MRMADOTY,MRMAVOLN,SP70
.
          MATCH     DIM20,SAVKEY20
          GOTO      CHEK1000 IF EQUAL
.
.         *** Print error ***
.
          CALL      PRNDT000
.
          COMPARE   "56",CLNO
          CALL      PRNTH000 IF NOT LESS
.
          GOTO      CHEK1000          
.
CHEK9999  RETURN
+
.------------------------------------------------------------
.      Print Header Details
.------------------------------------------------------------
PRNTH000  MOVE      ZERO,CLNO
          CLOCK     TIME,CTIMEIS
.
          CALL      HEAD80 
          IF        OPTION=2
            PRINT     *1,"U/R  - From : ",FROMURTX,*27,"To : ",TOOOURTX
          ENDIF
.
          IF        OPTION=3
            PRINT     *1,"Prefix: ",URPREFIX
          ENDIF
.
          PRINT     *1,"-----------------------------------":
                    *36,"------------------------------":
                    *66,"--"
.
          PRINT     *1,"|",*5,"UR Number",*17,"|":
                    *20,"Home Location",*35,"|":
                    *37,"Document Type",*55,"|":
                    *57,"Volume",*66,"|"
.
          PRINT     *1,"-----------------------------------":
                    *36,"------------------------------":
                    *66,"--"
.
PRNTH999  RETURN
+
.------------------------------------------------------------
.      Print Patient/Medical Records Details
.------------------------------------------------------------
PRNDT000  PRINT     *1,"|",*5,MRMAURNO,*17,"|":
                    *21,MRMAHHOS,MRMAHLOC,*35,"|":
                    *39,MRMADOTY,*55,"|":
                    *59,MRMAVOLN,*66,"|"
.
          ADD       ONE,CLNO
.
PRNDT999  RETURN
.==============================================================================
.
          INC       STD001IO
          INC       CLMRTMAS                                          *I-231158
.
          INC       MRTHISIO/INC            * Tracking history file
          INC       MRTMASIO/INC            * MRT Master file
          INC       PATMA1IO/INC            * Patient Master file
          INC       MRTVISIO/INC            * MRT Visit File
          INC       PATHSPIO/INC            * Patient Hospital File
          INC       PMSPX2IO/INC            * Patient PMI Ext File
+
.


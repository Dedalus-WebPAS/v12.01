.*****************************************************************************
.* System    :   Patient Management System                                   *
.* Program   :   FXPATMRG                                                    *
.* Desc      :   Check all merged records in the patient master file also    *
.*               have a corresponding record in the merge file.              *
.*****************************************************************************
.* Date      :   16/07/2014                                                  *
.* Author    :   Steve Armstrong                                             *
.* Function  :   This program will loop through the patient master file      *
.*               looking for merged records (PSTAT=1) which don't have a     *
.*               corresponding patmrgaf record.  For any such record found,  *
.*               a patmrgaf record will be created and any associated        *
.*               aliases on patgsrnf for the inactive U/R will be deleted.   *
.* Mods      :                                                               *
.*        V10.04.00 20/05/2014  Steve Armstrong  CAR 296667                  *
.*                  Program created.                                         *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATGSRFD/INC
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC
          INC       PATMRGFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
COUNTER   FORM      8             * new patmrgaf record count
CURRDATE  DIM       8             * current date (ccyymmdd)
.
HOSPCODE  DIM       3             * hospital code (pathspaf)
.
LASTDATE  DIM       8             * last status date (ccyymmdd)
LASTTIME  DIM       8             * last status time (hh:mm:ss)
.
RQSTDATE  DIM       8             * requested date (ccyymmdd)
.
.
.
PRGID     INIT      "FXPATMRG"
PRGNAM    INIT      "Fixit for Merged PMI Records"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000       * proceed with fixit
.
MAIN1000  CALL      HOSP0000               * get hospital
          BRANCH    EXIT,MAIN0100          * exitchar entered
.
MAIN2000  CALL      RDAT0000               * get requesting date
          BRANCH    EXIT,MAIN1000          * nothing entered
.
MAIN3000  CALL      SDAT0000               * get last status date
          BRANCH    EXIT,MAIN2000          * nothing entered
.
          CALL      STIM0000               * get last status time
          BRANCH    EXIT,MAIN3000          * nothing entered
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN5000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN5000  CALL      PROC0000               * process
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmrgaf";
          OPEN      PATMRGG1,"patmrgaf"
.
          DISPLAY   *P64:24,"patgsrnf";
          OPEN      PATGSRN1,"patgsrnf"
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run fixit                               *
.*                        TRUE  (1)  Exit option selected                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Merge Fixit"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               HOSP0000              Called by: MAIN0000   *
.*              Get the user to keyin the hospital code                      *
.*****************************************************************************
.
HOSP0000  DISPLAY   *P1:10,*EF,"Requesting Hospital:";

          MOVE      TWENTY2,ECOL                 * set screen coordinates
          MOVE      TEN,EVERT
          MOVE      SP3,CKYIDEF3                 * no default
          MOVE      ONE,CKYIMAND                 * mandatory
          MOVE      ZERO,CKYIMODE                * code must exist
          CALL      PATHSPKY                     * get hospital code
          BRANCH    EXIT,HOSP9999:               * irrelevant as mandatory
                         HOSP9100                * exitchar
.
          MOVE      KEY3,HOSPCODE
          DISPLAY   *P25:10,PTHSNAME;
          MOVE      ZERO,EXIT
          GOTO      HOSP9999
.
HOSP9100  MOVE      ONE,EXIT
.
HOSP9999  RETURN
+
.*****************************************************************************
.*                               RDAT0000              Called by: MAIN0000   *
.*              Get the user to keyin the requested date                     *
.*****************************************************************************
.
RDAT0000  DISPLAY   *P1:13,*EF,"Requested Date     :"
          MOVE      ONE,CCENTRY
          MOVE      ONE,CCANLDTE
          MOVE      ZERO,CDEFDTE
          MOVE      ZERO,CHIGHLT
          MOVE      TEN3,CVERT
          MOVE      TWENTY2,CCOL
.
          CALL      IBACLOCK                     * get current date and default
.
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
          UNPACK    CURRDATE,CCENT,CYEAR,CMON,CDAY
.
          CALL      KEYDATE
          BRANCH    OVRCD,RDAT9100
.
          PACK      RQSTDATE,CCENT,CYEAR,CMON,CDAY
          MATCH     RQSTDATE,CURRDATE            * date in future ?
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Date cannot be in the future.  ";
            CALL      HITENTER
            GOTO      RDAT0000
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      RDAT9999
.
RDAT9100  MOVE      ONE,EXIT
.
RDAT9999  RETURN
+
.*****************************************************************************
.*                               SDAT0000              Called by: MAIN0000   *
.*              Get the user to keyin the last status date                   *
.*****************************************************************************
.
SDAT0000  DISPLAY   *P1:15,*EF,"Last Status Date   :"
          MOVE      ONE,CCENTRY
          MOVE      ONE,CCANLDTE
          MOVE      ZERO,CDEFDTE
          MOVE      ZERO,CHIGHLT
          MOVE      TEN5,CVERT
          MOVE      TWENTY2,CCOL
.
          CALL      KEYDATE
          BRANCH    OVRCD,SDAT9100
.
          PACK      LASTDATE,CCENT,CYEAR,CMON,CDAY
          MATCH     LASTDATE,CURRDATE            * date in future ?
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Date cannot be in the future.  ";
            CALL      HITENTER
            GOTO      SDAT0000
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      SDAT9999
.
SDAT9100  MOVE      ONE,EXIT
.
SDAT9999  RETURN
+
.*****************************************************************************
.*                               STIM0000              Called by: MAIN0000   *
.*              Get the user to keyin the last status time                   *
.*****************************************************************************
.
STIM0000  DISPLAY   *P1:17,*EF,"Last Status Time   :"
.
          MOVE      ONE,CCANLDTE
          MOVE      ZERO,CDEFDTE
          MOVE      TEN7,CVERT
          MOVE      TWENTY2,CCOL
          MOVE      ZERO,CHIGHLT
          UNPACK    CTIMEIS,CHOUR,ANS,CMIN
.
          CALL      KEYTIME
          BRANCH    OVRCD,STIM9100
.
          PACK      LASTTIME,CHOUR,COLON,CMIN,COLON,ZERO,ZERO
.
          MOVE      ZERO,EXIT
          GOTO      STIM9999
.
STIM9100  MOVE      ONE,EXIT
.
STIM9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*      Loop through patma1af looking for merged records that don't have     *
.*      a corresponding record on patmrgaf                                   *
.*****************************************************************************
.
PROC0000  DISPLAY   *P1:22,*EF,"New patmrgaf count :":
                    *P1:24,"Processing:";
          MOVE      ZERO,COUNTER                 * initialise count
.
          MOVE      SP8,KEY8
          CALL      RDSMAST1                     * position at start of file
PROC0500  CALL      RDKMAST1                     * read next record
          BRANCH    OVRCD,PROC9000               * end of file
.
          DISPLAY   *P13:24,*V2LON,PURNO
.
          COMPARE   ONE,PSTAT                    * merged record ?
          GOTO      PROC0500 IF NOT EQUAL        * no - get next record
.
          MATCH     PPSNAME,SP20                 * "merged to" U/R blank ?
          GOTO      PROC0500 IF EQUAL            * yes - get next record
.
          PACK      KEY17,THREE,PURNO,PPSNAME
          CALL      RDPTMRG1                     * merge record found ?
          BRANCH    OVRCD,PROC1000               * no - write new record
.
          GOTO      PROC0500                     * get next record
.
PROC1000  MOVE      PURNO,PTMROLUR
          MOVE      PPSNAME,PTMRNWUR
          MOVE      RQSTDATE,PTMRRDTE
          MOVE      HOSPCODE,PTMRRHOS
          MOVE      SP20,PTMRAPPR
          MOVE      THREE,PTMRSTAT
          MOVE      LASTDATE,PTMRSDTE
          MOVE      LASTTIME,PTMRSTIM
          MOVE      SP2,PTMRLOCK
          MOVE      SP1,PTMRSENT
          MOVE      "FXPATMRG",PTMRUSER
          MOVE      SP30,PTMRFILL
.
          CALL      WRPTMRG1                     * write new patmrgaf record
          ADD       ONE,COUNTER                  * increment record count
          DISPLAY   *P22:22,*V2LON,COUNTER
.
          CALL      RGSR0000                     * remove patgsrnf records
          GOTO      PROC0500                     * get next record
.
PROC9000  DISPLAY   *P1:24,*EL,"Fixit completed.  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
.*****************************************************************************
.*                               RGSR0000              Called by: PROC0000   *
.*            Remove any patgsrnf records for a given U/R                    *
.* Requires: PURNO - Patient U/R Number                                      *
.*****************************************************************************
.
RGSR0000  PACK      KEY115,PURNO,SP70,SP70
          CALL      RSPTGSR1                     * position on U/R
          CALL      RKPTGSR1                     * read next record
          BRANCH    OVRCD,RGSR9999               * end of file
.
          MATCH     PURNO,GSRURNO                * same U/R still ?
          GOTO      RGSR9999 IF NOT EQUAL        * no - finished
.
          PACK      KEY115,GSRURNO,GSRBILL,GSRSNAM,GSRGNAM,PTGSGNM2:
                           GSRDOB,GSRSEX
          CALL      DEPTGSR1                     * delete record
          GOTO      RGSR0000                     * get next record
.
RGSR9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PATHSPKY
.
          INC       PATGSRIO/INC
          INC       PATHSPIO/INC
          INC       PATMA1IO/INC
          INC       PATMRGIO/INC

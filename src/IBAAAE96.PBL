.******************************************************************************
.*                                                                            *
.*                        P R O G R A M   S U M M A R Y                       *
.*                        -------------   -------------                       *
.*                                                                            *
.*     PROGRAM NAME:     IBAAAE96                                             *
.*                                                                            *
.*     FUNCTION:         AUDIT REPORT                                         *
.*                                                                            *
.*     DATE WRITTEN:     10/05/1988                                           *
.*                                                                            *
.*     AUTHOR:           MALCOLM HAYSE                                        *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.*                    V10.04.01 15/04/2014  J.Tan          CAR 261630         *
.*                              Removed port number from temp file name       *
.*                    V9.03.01  20/11/2003  Steve Armstrong  CAR 44631        *
.*                              Removed PI command                            *
.******************************************************************************
.*                       V5.01  21/04/89 K.Peak           SRF 100081          *
.*                              Added CONTROLF                                *
.*                       V5.02  31/05/89 K.Peak           SRF 100824          *
.*                              Fixed Audit Busy                              *
.*                    V5.01.02  06/06/90 D.Di.Paola       SRF 103773          *
.*                              Recompiled for changes to AAECONFD            *
.*                    V5.01.03  07/07/92 i chung          SRF 115614          *
.*                              Fixed audits with data not printing.          *
.*                              Added option to clear audit(s) after printing.*
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
          INC       AAEAUXFD/INC
          INC       AAECONFD/INC
+
.
.         WORK AREA
.
CMDLINE   DIM       50
CNOPORT   FORM      2
.
DANSX     DIM       14
DIM3      DIM       3
DIM15     DIM       15
DIM27     DIM       27
.
ENDOPT    FORM      1
FIELD     FORM      2
FILENAM   DIM       40
FILES     FILE      ASCII, FIXED=256
FNAMEA    DIM       8
.
INDEX     FORM      2
.
ONAME     DIM       30
OPERCODE  DIM       4
PDESC     DIM       18
.
SEC       FORM      1
.
.         CONSTANTS
.
.
.
PDESCA    INIT      "Detail file        "
PDESCB    INIT      "Audit B (Not Used) "
PDESCC    INIT      "Charge File        "
PDESCD    INIT      "Audit D (Not Used) "
PDESCE    INIT      "Audit E (Not used) "
PDESCF    INIT      "Audit F (Not used) "
PDESCG    INIT      "Audit G (Not used) "
PDESCH    INIT      "Audit H (Not used) "
PDESCI    INIT      "Audit I (Not Used) "
PDESCJ    INIT      "Audit J (Not used) "
PDESCK    INIT      "Audit K (Not used) "
PDESCL    INIT      "Audit L (Not used) "
PDESCM    INIT      "Misc. Charges      "
PDESCN    INIT      "Audit N (Not used) "
PDESCO    INIT      "Audit O (Not used) "
PDESCP    INIT      "Audit P (Not used) "
PDESCQ    INIT      "Audit Q (Not used) "
PDESCR    INIT      "Audit R (Not Used) "
PDESCS    INIT      "Supervisor Correctn"
PDESCT    INIT      "Audit T (Not Used) "
PDESCU    INIT      "Audit U (Not used) "
PDESCV    INIT      "Audit V (Not used) "
PDESCW    INIT      "Waiting File       "
PDESCX    INIT      "Audit X (Not used) "
PDESCY    INIT      "Audit Y (Not used) "
PDESCZ    INIT      "Audit Z (Not used) "
.
SEC1      FORM      "00001"
.
PRGID     INIT      "IBAAAE96"
PRGNAM    INIT      "Audit Reports"
VERSION   INIT      "V12.01.00"
+
.
.         START OF PROGRAM
.
          CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening controlf";   
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,TEN;*138,CNOPORT
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,FNAMEA
.
.         MAIN      MENU
.
START     DISPLAY   *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Print Audit Reports":
                    *P1:7,"Select Option : ";
.
BEGIN     KEYIN     *P17:7,*EL,*DV,UNDLN1,*P17:7,*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
          BRANCH    OPTION OF START0
          DISPLAY   *P35:7,*B,*V2LON,"** Invalid Option **",*W2;
          GOTO      BEGIN
.
START0    READ      CONTROLF,THIRTY;*51,HEAUDA,HEAUDB,HEAUDC,HEAUDD,HEAUDE:
                                        HEAUDF,HEAUDG,HEAUDH,HEAUDI,HEAUDJ:
                                        HEAUDK,HEAUDL,HEAUDM,HEAUDN,HEAUDO:
                                        HEAUDP,HEAUDQ,HEAUDR,HEAUDS,HEAUDT:
                                        HEAUDU,HEAUDV,HEAUDW,HEAUDX,HEAUDY:
                                        HEAUDZ
.
          DISPLAY   *P1:3,*EF:
                    *P1:4," 1. ",PDESCA,":":
                    *P1:5," 2. ",PDESCB,":":
                    *P1:6," 3. ",PDESCC,":":
                    *P1:7," 4. ",PDESCD,":":
                    *P1:8," 5. ",PDESCE,":":
                    *P1:9," 6. ",PDESCF,":":
                    *P1:10," 7. ",PDESCG,":":
                    *P1:11," 8. ",PDESCH,":":
                    *P1:12," 9. ",PDESCI,":":
                    *P1:13,"10. ",PDESCJ,":":
                    *P1:14,"11. ",PDESCK,":":
                    *P1:15,"12. ",PDESCL,":":
                    *P1:16,"13. ",PDESCM,":":
                    *P41:4,"14. ",PDESCN,":":
                    *P41:5,"15. ",PDESCO,":":
                    *P41:6,"16. ",PDESCP,":":
                    *P41:7,"17. ",PDESCQ,":":
                    *P41:8,"18. ",PDESCR,":":
                    *P41:9,"19. ",PDESCS,":":
                    *P41:10,"20. ",PDESCT,":":
                    *P41:11,"21. ",PDESCU,":":
                    *P41:12,"22. ",PDESCV,":":
                    *P41:13,"23. ",PDESCW,":":
                    *P41:14,"24. ",PDESCX,":":
                    *P41:15,"25. ",PDESCY,":":
                    *P41:16,"26. ",PDESCZ,":";
.
          MOVE      ZERO,INDEX
          MOVE      THREE,CVERT
          MOVE      "26",CCOL
.
AUDLOOP1  ADD       ONE,INDEX
          ADD       ONE,CVERT
          COMPARE   "27",INDEX
          GOTO      START1 IF EQUAL
.
          COMPARE   "17",CVERT
          GOTO      AUDPRNT1 IF LESS
.
          MOVE      FOUR,CVERT
          MOVE      "66",CCOL
.
AUDPRNT1  LOAD      FIELD,INDEX,HEAUDA,HEAUDB,HEAUDC,HEAUDD,HEAUDE,HEAUDF:
                                HEAUDG,HEAUDH,HEAUDI,HEAUDJ,HEAUDK,HEAUDL:
                                HEAUDM,HEAUDN,HEAUDO,HEAUDP,HEAUDQ,HEAUDR:
                                HEAUDS,HEAUDT,HEAUDU,HEAUDV,HEAUDW,HEAUDX:
                                HEAUDY,HEAUDZ
          MOVE      DYES,DANSX
          LOAD      DANSX,FIELD,DNO
          DISPLAY   *PCCOL:CVERT,*V2LON,DANSX;
          GOTO      AUDLOOP1
.
START1    KEYIN     *P1:24,*EL," Select Audit Report (":
                    *V2LON,"1-26",*HOFF,") or (":
                    *V2LON,*DV,ANSA,*HOFF,")ll ? ",*V2LON,DIM2;
          RESET     DIM2
          GOTO      START IF EOS
.
          TYPE      DIM2                    * numeric input ?
          GOTO      SINGAUDT IF EQUAL       * yes
.
          CMATCH    ANSA,DIM2               * "All" selected ?
          GOTO      ALLAUDIT IF EQUAL       * yes
.
INVFLD    DISPLAY   *P50:24,*B,*V2LON,"** Invalid Selection **",*W2;
          GOTO      START1
.
INVAVL    DISPLAY   *P50:24,*B,*V2LON,"** Not Available **",*W2;
          GOTO      START1
.
NODATA    DISPLAY   *P50:24,*B,*V2LON,"** Empty Audit(s) **",*W2;
          GOTO      START1
.
.         Print Single Audit
.
SINGAUDT  MOVE      DIM2,INDEX
          COMPARE   ZERO,INDEX
          GOTO      INVFLD IF EQUAL
          GOTO      INVFLD IF LESS
.
          COMPARE   INDEX,TWENTY6
          GOTO      INVFLD IF LESS
.
          LOAD      FIELD,INDEX,HEAUDA,HEAUDB,HEAUDC,HEAUDD,HEAUDE,HEAUDF:
                                HEAUDG,HEAUDH,HEAUDI,HEAUDJ,HEAUDK,HEAUDL:
                                HEAUDM,HEAUDN,HEAUDO,HEAUDP,HEAUDQ,HEAUDR:
                                HEAUDS,HEAUDT,HEAUDU,HEAUDV,HEAUDW,HEAUDX:
                                HEAUDY,HEAUDZ
.         1 = NO
.
          BRANCH    FIELD OF INVAVL
.
          MOVE      ONE,OPTION         *** DUMMY SET
          DISPLAY   *P1:23,*EL,"Audit : ",*P13:23,"Terminal :":
                    *P65:23,"Rec No : ",*P50:24,*V2LON,"** ",*BLINKON:
                    "Retrieving Audit ",INDEX,*HOFF,*V2LON," **";
.
          LOAD      AUTYPE,INDEX,ANSA,ANSB,ANSC,ANSD,ANSE,ANSF,ANSG,ANSH,ANSI:
                                 ANSJ,ANSK,ANSL,ANSM,ANSN,ANSO,ANSP,ANSQ,ANSR:
                                 ANSS,ANST,ANSU,ANSV,ANSW,ANSX,ANSY,ANSZ
.
          REP     "AaBbCcDdEeFfGgHhIiJjKkLlMmNnOoPpQqRrSsTtUuVvWwXxYyZz",AUTYPE
          GOTO      TERMINAL
.
.         Print All Audits
.
ALLAUDIT  MOVE      TWO,OPTION
          DISPLAY   *P1:23,*EL,"Audit : ",*P13:23,"Terminal :":
                    *P65:23,"Rec No : ";
.
          MOVE      ZERO,INDEX
          MOVE      ZERO,CPAGENO
.
NEXTALL   ADD       ONE,INDEX
          COMPARE   "27",INDEX
          GOTO      ENDAUDIT IF EQUAL
.
          DISPLAY   *P50:24,*V2LON,"** ",*BLINKON,"Retrieving Audit ",INDEX:
                    *HOFF,*V2LON," **";
.
          LOAD      FIELD,INDEX,HEAUDA,HEAUDB,HEAUDC,HEAUDD,HEAUDE,HEAUDF:
                                HEAUDG,HEAUDH,HEAUDI,HEAUDJ,HEAUDK,HEAUDL:
                                HEAUDM,HEAUDN,HEAUDO,HEAUDP,HEAUDQ,HEAUDR:
                                HEAUDS,HEAUDT,HEAUDU,HEAUDV,HEAUDW,HEAUDX:
                                HEAUDY,HEAUDZ
.
          BRANCH    FIELD OF NEXTALL
.
          LOAD      AUTYPE,INDEX,ANSA,ANSB,ANSC,ANSD,ANSE,ANSF,ANSG,ANSH,ANSI:
                                 ANSJ,ANSK,ANSL,ANSM,ANSN,ANSO,ANSP,ANSQ,ANSR:
                                 ANSS,ANST,ANSU,ANSV,ANSW,ANSX,ANSY,ANSZ
.
          REP     "AaBbCcDdEeFfGgHhIiJjKkLlMmNnOoPpQqRrSsTtUuVvWwXxYyZz",AUTYPE
.
TERMINAL  CLEAR     CMDLINE
          APPEND    "search_dpath '",CMDLINE
          APPEND    AUAAEAU,CMDLINE
          APPEND    AUTYPE,CMDLINE
          APPEND    "*.txt' > ",CMDLINE
          APPEND    FNAMEA,CMDLINE
          APPEND    ".txt",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          MOVE      SP1,AUMODE
          DISPLAY   *P49:24,*EL,*P9:23,AUTYPE;
.
          MOVE      ZERO,CPAGENO
          MOVE      "60",CLNO          *** NEW PAGE FOR EACH AUDIT TYPE
.
          OPEN      FILES,FNAMEA
NEXTTERM  READ      FILES,SEQ;FILENAM
          GOTO      ENDTERM IF OVER
          DISPLAY   *P24:23,FILENAM;
.
          ENDSET    FILENAM
NXTTERM1  CMATCH    ".",FILENAM
          GOTO      NXTTERM2 IF EQUAL
          BUMP      FILENAM BY -1
          GOTO      NXTTERM1 IF NOT EOS
.
NXTTERM2  BUMP      FILENAM BY -2
          MOVE      FILENAM,DIM2
          RESET     FILENAM
          MOVE      DIM2,AUPORT
.
          CALL      PRNTAUDX
          BRANCH    OVRCD OF NEXTTERM,NEXTTERM
.
          DISPLAY   *P24:23,*V2LON,FILENAM;
.
          LOAD      PDESC,INDEX,PDESCA,PDESCB,PDESCC,PDESCD,PDESCE,PDESCF:
                                PDESCG,PDESCH,PDESCI,PDESCJ,PDESCK,PDESCL:
                                PDESCM,PDESCN,PDESCO,PDESCP,PDESCQ,PDESCR:
                                PDESCS,PDESCT,PDESCU,PDESCV,PDESCW,PDESCX:
                                PDESCY,PDESCZ
.
          CALL      PRNTDA             *** PRINTS ALL AAE DATA FOR THIS TERMINAL
          GOTO      NEXTTERM
.
ENDTERM   BRANCH    OPTION OF ENDAUDIT,NEXTALL
.
.         WE HAVE AN AUDIT AND IT IS OPEN
.
PRNTDA    MOVE      ZERO,AUNEXT
.
NEXTA     ADD       ONE,AUNEXT
.
          COMPARE   AUNEXT,AUORIG
.         GOTO      ENDAUDIT IF EQUAL
          RETURN    IF EQUAL
.
          CALL      READAUDX
.
          CALL      DISPDA
          DISPLAY   *P74:23,*V2LON,AUNEXT;
          GOTO      NEXTA
.
.         PRINTING HAS FINISHED
.
ENDAUDIT  MOVE      TWO,ENDOPT              * initialize to free audit 
.
          COMPARE   ZERO,CPAGENO            * empty audit(s) ?
          GOTO      FREEAUD IF EQUAL        * yes
          CALL      CLYN0000                * no - ask whether to clear audits
.
FREEAUD   CALL      CLFR0000                * clear or free the audit file(s)
.
          COMPARE   ZERO,CPAGENO
          GOTO      NODATA IF EQUAL
.
          PRINT     *N,"  ***  End of Report  ***"
.
          DISPLAY   *P1:24,*EL,*P24:24,*V2LON:
                    "** Report Generation Completed **",*W2;
          BRANCH    OPTION,START1
          GOTO      START
+
.****************************************************************************
.*                                CLYN0000                                  *
.*                   ask if files are to be CLeared (Y/N)                   *
.****************************************************************************
CLYN0000  MOVE      ZERO,ENDOPT
          KEYIN     *P1:24,*EL,"Do you want the Audit file(s) cleared (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
          MATCH     "Y",ANS
          GOTO      CLYN8000 IF EQUAL          * "Yes" 
          MATCH     "N",ANS
          GOTO      CLYN9000 IF EQUAL          * "No"
          BEEP
          GOTO      CLYN0000                   * invalid - re-enter 
.
CLYN8000  MOVE      ONE,ENDOPT                 * "1" = Clear Audit
          GOTO      CLYN9999
.
CLYN9000  MOVE      TWO,ENDOPT                 * "2" = Free Audit
.
CLYN9999  RETURN
+
.****************************************************************************
.
.         PRINT ACTUAL DATA
.
DISPDA    COMPARE   FIFTY5,CLNO
          CALL      HEAD IF NOT LESS
.
          CMATCH    ANSI,AUMODE
          GOTO      DISPDA1 IF EQUAL
.
          CMATCH    ANSC,AUMODE
          GOTO      DISPDA2 IF EQUAL
.
          CMATCH    ANSD,AUMODE
          GOTO      DISPDA3 IF EQUAL
.
          PRINT     *1,AUDATE,*10,AUTIME,*19,AUPASSCD,*24,SP1,AUPORT:
                    *29,AUITEM,*51,AUDESC,*72,"Unknown",*82,AUCONBEF
          ADD       ONE,CLNO
          RETURN
.
DISPDA1   PRINT     *1,AUDATE,*10,AUTIME,*19,AUPASSCD,*24,SP1,AUPORT:
                    *29,AUITEM,*51,AUDESC,*72,"Inserted",*82,AUCONBEF
          ADD       ONE,CLNO
          RETURN
.
DISPDA2   PRINT     *1,AUDATE,*10,AUTIME,*19,AUPASSCD,*24,SP1,AUPORT:
                    *29,AUITEM,*51,AUDESC,*72,"Chgd From",*82,AUCONBEF:
                    *N,*72,"Chgd To",*82,AUCONAFT
          ADD       TWO,CLNO
          RETURN
.
DISPDA3   PRINT     *1,AUDATE,*10,AUTIME,*19,AUPASSCD,*24,SP1,AUPORT:
                    *29,AUITEM,*51,AUDESC,*72,"Deleted",*82,AUCONBEF
          ADD       ONE,CLNO
          RETURN
.
.         HEADINGS FOR OUTPUT
.
HEAD      ADD       ONE,CPAGENO
.
          CLOCK     TIME,CTIMEIS
          PRINT     *F,PRGID,*35,CNAME,*118,"DATE : ",CDATE:
                    *N,VERSION,*35,"AUDIT REPORT FOR - ",PDESC:
                              *118,"TIME : ",CTIMEIS:
                    *N,*118,"PAGE : ",SP5,CPAGENO:
                    *N,*1,"  Date",*10,"  Time",*19,"User",*24,"Port":
                      *29,"Record",*51,"Field",*72,"Operation",*82,"Data":
                    *N,*1,"--------",*10,"--------",*19,"----",*24,"----":
                      *29,"--------------------",*51,"--------------------":
                      *72,"---------":
                      *82,"--------------------------------------------------"
          MOVE      FIVE,CLNO
          RETURN
.
PAGEND    PRINT     *1,"----------------------------------------":
                       "----------------------------------------":
                       "-------------------------------"
          ADD       ONE,CLNO
          RETURN
+
.****************************************************************************
.*                                 CLFR0000                                 *
.*        Loop over all the existing ports for the particular file or all   *
.*           files and if not in use or printed then clear it/them...       *
.****************************************************************************
CLFR0000  BRANCH    ENDOPT,CLFR0500            * "1" = clear audit
          DISPLAY   *P1:23,*EF,"Releasing Audit: ";
          GOTO      CLFR1000
.
CLFR0500  DISPLAY   *P1:23,*EF,"Clearing Audit : ";
.
CLFR1000  BRANCH    OPTION,CLFR3000            * Option 1 - Single Audit
          MOVE      ZERO,INDEX                 * Option 2 - All Audits
.
CLFR2000  ADD       ONE,INDEX
          COMPARE   "27",INDEX
          GOTO      CLFR9000 IF EQUAL
.
          LOAD      FIELD,INDEX,HEAUDA,HEAUDB,HEAUDC,HEAUDD,HEAUDE,HEAUDF:
                                HEAUDG,HEAUDH,HEAUDI,HEAUDJ,HEAUDK,HEAUDL:
                                HEAUDM,HEAUDN,HEAUDO,HEAUDP,HEAUDQ,HEAUDR:
                                HEAUDS,HEAUDT,HEAUDU,HEAUDV,HEAUDW,HEAUDX:
                                HEAUDY,HEAUDZ
.
          BRANCH    FIELD OF CLFR2000          * "1" = No
.
          LOAD      AUTYPE,INDEX,ANSA,ANSB,ANSC,ANSD,ANSE:ANSF,ANSG,ANSH,ANSI:
                                 ANSJ,ANSK,ANSL,ANSM,ANSN,ANSO,ANSP,ANSQ,ANSR:
                                 ANSS,ANST,ANSU,ANSV,ANSW,ANSX,ANSY,ANSZ
.
          REP     "AaBbCcDdEeFfGgHhIiJjKkLlMmNnOoPpQqRrSsTtUuVvWwXxYyZz",AUTYPE
.
CLFR3000  OPEN      FILES,FNAMEA
.
CLFR4000  READ      FILES,SEQ;FILENAM
          GOTO      CLFR8000 IF OVER
          DISPLAY   *P65:23,*EL;
          ENDSET    FILENAM
.
CLFR4100  CMATCH    ".",FILENAM
          GOTO      CLFR4200 IF EQUAL
          BUMP      FILENAM BY -1
          GOTO      CLFR4100 IF NOT EOS
.
CLFR4200  BUMP      FILENAM BY -2
          MOVE      FILENAM,DIM2
          RESET     FILENAM
.
          MOVE      DIM2,AUPORT
          PACK      AUFILE,AUAAEAU,AUTYPE,AUPORT
          REP       " 0",AUFILE
          DISPLAY   *P19:23,*V2LON,AUFILE;
.
          TRAP      CLFR7000 IF IO
          OPEN      AAEAUDXX,AUFILE
          TRAPCLR   IO
.
          MOVE      "AUX",PRXCODE                * set to lock file AAEAUXFD
          CALL      GETSLK00                     * lock file
          READ      AAEAUDXX,ZERO;AUNEXT,AUFLAG
          COMPARE   TWO,AUFLAG
          IF        !@EQUAL
            CALL      RELSLK00                   * release file lock
            GOTO      CLFR4000
          ENDIF
.
          BRANCH    ENDOPT,CLFR5000,CLFR6000   * "1"= Clear, "2"= Free
.
.-------- clear audit -----
.
CLFR5000  CLOSE     AAEAUDXX
          PREPARE   AAEAUDXX,AUFILE
          CLOSE     AAEAUDXX
          CALL      RELSLK00                     * release file lock
          DISPLAY   *P65:23,"** Cleared **",*W1;
          GOTO      CLFR4000
.
.-------- Free audit ------
.
CLFR6000  MOVE      ZERO,AUFLAG
          WRITE     AAEAUDXX,ZERO;AUNEXT,AUFLAG
          CLOSE     AAEAUDXX
          CALL      RELSLK00                     * release file lock
          GOTO      CLFR4000
.
CLFR7000  NORETURN
          GOTO      CLFR4000
.
CLFR8000  BRANCH    OPTION,CLFR9000,CLFR2000
.
CLFR9000  DISPLAY   *P1:23,*EL;
.
CLFR9999  RETURN
+
.****************************************************************************
.
ENDIT     CHAIN     PGM
          STOP
+
.==============================================================================
.
          INC       STD001IO
          INC       TFILENAM                * Generate Temp File Name
.
          INC       IBASEQIO/INC            * Sequential Numbers File
          INC       WEBERRIO/INC            * Web Server Error Logging
          INC       AAEAUXIO/INC
+

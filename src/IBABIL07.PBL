.***************************************************************************
.* System    :   Billing                                                   *
.* Program   :   IBABIL07                                                  *
.* Desc      :   Delete old free format estimates                          *
.***************************************************************************
.* Date      :   07/07/2008                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   This program will delete old free format estaimes that    *
.*           :   are greater than 120 days old.                            *
.***************************************************************************
.* Mods      :   V9.10.00  07/07/2008  Ebon Clements  CAR 160213           *
.*           :             Created program                                 *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PMSFEDFD/INC    
          INC       PMSFESFD/INC    
          INC       PMSFENFD/INC    
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
EXPIRYDT  DATE      8
.
PRGID     INIT      "IBABIL07"
PRGNAM    INIT      "Delete old free format estimates"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P64:24,"pmsfedaf";
          OPEN      PMSFEDA2,"pmsfedaf"
.
          DISPLAY   *P64:24,"pmsfesaf";
          OPEN      PMSFESA1,"pmsfesaf"
.
          DISPLAY   *P64:24,"pmsfenaf";
          OPEN      PMSFENA1,"pmsfenaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Delete free format estimates ":
                                           "greater than 120 days old"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  CALL      IBACLOCK
          PACK      EXPIRYDT,CCC,CYY,CMM,CDD,SP10
          REP       " 0",EXPIRYDT
          SUB       HUND20,EXPIRYDT
.
          PACK      KEY20,SP70
          CALL      RSPMFED2
PROC1000  CALL      RKPMFED2
          BRANCH    OVRCD,PROC9999
.
          MATCH     SP70,PMFDURNO
          GOTO      PROC9999 IF NOT EQUAL
.
          MATCH     SP70,PMFDCDAT
          IF        @EQUAL
            MATCH     EXPIRYDT,PMFDADAT
            GOTO      PROC1000 IF NOT LESS
          ELSE
            MATCH     EXPIRYDT,PMFDCDAT
            GOTO      PROC1000 IF NOT LESS
          ENDIF
.
          PACK      KEY12,PMFDQUOT,SP70
          CALL      DEPMFEN1
.
          CALL      DFES0000
.
          PACK      KEY20,PMFDURNO,PMFDQUOT,SP70
          CALL      DEPMFED2
.
          GOTO      PROC1000
.
PROC9999  RETURN
+
.**************************************************************************
.*                               DFES0000              Called by: PROC0000*
.*                                                                        *
.**************************************************************************
DFES0000  PACK      KEY17,PMFDQUOT,SP70
          CALL      RSPMFES1
DFES1000  CALL      RKPMFES1
          BRANCH    OVRCD,DFES9999
.
          MATCH     PMFSQUOT,PMFDQUOT
          GOTO      DFES9999 IF NOT EQUAL
.
          PACK      KEY17,PMFSQUOT,PMFSUNIQ,SP70
          CALL      DEPMFES1
.
          GOTO      DFES1000
.
DFES9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PMSFEDIO/INC    
          INC       PMSFESIO/INC    
          INC       PMSFENIO/INC    

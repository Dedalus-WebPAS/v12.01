.******************************************************************************
.*    Program      : IBAMEH55                                                 *
.*    Description  : Admission/ReAdmission/Replacement Report                 *
.*                                                                            *
.*    Author       : ROD    (12/12/91)                                        *
.*    Function     : List all MH Admissions and returns from holiday or       *
.*                   trial leave on the specified date.                       *
.*                                                                            *
.*    Modifications:                                                          *
.*        V12.00.01 16/05/2025  Ebon Clements   TSK 0955096                   *
.*                  Alphanueric visit number changes                          *
.*       V10.05.01  01/01/2015  Ania P                      CAR 261630        *
.*                  Removed use of PORT for temp file naming                  *
.*        V10.01.01 10/02/2011  Jill Parkinson CAR 233088                     *
.*                  Added pmsvx1af                                            *
.*        V10.00.01 31/03/2010  Mike Laming  CAR 217575                       *
.*                  Recompiled for changes to MEHDIAFD                        *
.******************************************************************************
.*        V9.02.00  26/10/2002  Lina Vo     SRF 22733                         *
.*                  Ported to V9                                              *
.******************************************************************************
.*        V5.08.01  23/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund table variables to 8 chars            *
.*        V5.08.B01 30/12/1999  Steve Armstrong                               *
.*                  Fixed global recompile bugs                               *
.******************************************************************************
.*        V5.07.00  27/10/1998  Davin                                         *
.*                  Mods for 507                                              *
.*        V5.05.01  22/09/1997  Steve Armstrong                               *
.*                  Mods for AXIS V to be a free text description.            *
.*                  (MEHRVDIA)                                                *
.******************************************************************************
.*                   V5.01.002  24/06/92  ROD                                 *
.*                              Changed to use day file                       *
.*                   V5.01.003  29/07/92  ROD               SRF 116179        *
.*                              Use Leave type on "R" tran record             *
.*                              30/07/92  ROD                                 *
.*                              Print Adm/Repl as MHVIATYP not report date    *
.*        V5.01.004 26/05/1994 Ian Rutt                                       *
.*                  Fixed Global Recompile Bugs                               * 
.*        V5.04.01  07/11/1996  Howellsy                                      *
.*                  Added Review Time & Legal Status Time.                    *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
          INC       MEHVI1FD/INC
          INC       MEHDIAFD/INC
          INC       MEHLEGFD/INC
          INC       MEHDS1FD/INC
          INC       PATDHEAD/INC
          INC       PATTRNFD/INC
          INC       PATMA1FD/INC
          INC       PATCONFD/INC
          INC       PATVISFD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATDSCFD/INC
          INC       PATCODFD/INC
          INC       PATHOSFD/INC
          INC       PATCALAG/INC
          INC       PATDAYFD/INC
          INC       IBASEQFD/INC  
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
.
BJDAY     FORM      3
CJDAY     FORM      3
CMDLINE   DIM       80
CMONDAY   DIM       4
CURRDATE  DIM       8
DAYFILE   DIM       8
DRDATE    DIM       10
DSADATE   DIM       10
DSDDATE   DIM       10
DSADMD    DIM       8
DSDOB     DIM       10
FNDTL     FORM      1
OVHEAD    FORM      1
RDATE     DIM       8
SAMEDIS   FORM      1
SAVADMNO  DIM       8
SAVKEY30  DIM       30
SAVLTYP   DIM       3
SAVTDATE  DIM       8
XXNAME    DIM       36
XXADD     DIM       36
XXPOST    DIM       4 
XXAGE     FORM      3
XXSEX     DIM       1
XXRACE    DIM       1
XXMARIT   DIM       1
XXLSC     DIM       3
XXLSD     DIM       20
XXREFC    DIM       3 
XXREFD    DIM       20
XXDIA1C   DIM       9 
XXDIA1D   DIM       30
XXDIA2C   DIM       9 
XXDIA2D   DIM       30
XXHOSP    DIM       30
ZED20     INIT      "zzzzzzzzzzzzzzzzzzzz"
.
. temp file
.
MEHTMPA1  IFILE     ASCII, FIXED=17, KEY="U1-8"
XXURNO    DIM       8
XXADMN    DIM       8
.
PRGID     INIT      "IBAMEH55"
PRGNAM    INIT      "Hss Prs984 - (re)admission/replacement/transfer Report"
VERSION   INIT      "V12.01.00"
.
.****************************************************************************
.* ML0000  :  Mainline code   (1 of the great all time mainlines)           *
.****************************************************************************
ML0000    CALL      INIT0000                      * initialisation
          CALL      CRET0000                      * create/erase temp file
.
ML1000    CALL      MENU0000                      * get menu options
          BRANCH    EXIT,ML9999                   * Exit selected
.
ML2000    CALL      DATE0000                      * Get Date
          BRANCH    EXIT,ML1000                   * nothing input
.
          CALL      CONTQST                       * Ask Continue?
          BRANCH    CEXIT,ML5000,ML2000,ML1000    * Yes, No, Cancel
.
ML5000    CALL      LOAD0000                      * load data into temp file
          CALL      RPRT0000                      * Print Report
          CALL      CRET0000                      * create/erase temp file
          GOTO      ML2000
.
ML9999    CHAIN     PGM
+
.****************************************************************************
.* INIT0000  :  Initialisation                                              *
.****************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P60:24,"controlf"
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P60:24,"patdschf"
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P60:24,"patvisaf"
          OPEN      PATVISA2,"patvisaf"
.
          DISPLAY   *P60:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P60:24,"mehdiaaf"
          OPEN      MEHDIAA1,"mehdiaaf"
.
          DISPLAY   *P60:24,"mehds1af"
          OPEN      MEHDS1A1,"mehds1af"
.
          DISPLAY   *P60:24,"mehlegaf"
          OPEN      MEHLEGA1,"mehlegaf"
.
          DISPLAY   *P60:24,"mehvi1af"
          OPEN      MEHVI1A1,"mehvi1af"
.
          DISPLAY   *P60:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P60:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P60:24,"pattranf"
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P60:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P60:24,"pathospf"
          OPEN      PATHOSP2,"pathospf"
.
.     get hospital name
.
          READ      CONTROLF,TEN;*244,CHOSPNUM
          MOVE      SP30,HOSDESC
          PACK      KEY2,CHOSPNUM
          CALL      RDHOSP2
          MOVE      HOSDESC,XXHOSP
          CLOSE     PATHOSP2
.
          PACK      CURRDATE,CCC,CYY,CMM,CDD          * get current date
          REP       " 0",CURRDATE
.
          CALL      TFILENAM 
.
INIT9999  RETURN
+
.****************************************************************************
.* MENU0000  :  Main Menu                                                   *
.****************************************************************************
MENU0000  DISPLAY   *P1:4,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = By U/R Number"
.
MENU1000  KEYIN     *P1:7,"Select Option ? ":
                    *P17:7,*DV,UNDLN1:
                    *P17:7,*V2LON,OPTION
.
          IF        OPTION = 0
                      MOVE   ONE,EXIT
                      GOTO   MENU9999
          ENDIF
.
          MOVE      ZERO,EXIT
          BRANCH    OPTION,MENU9999
          BEEP
          GOTO      MENU1000
.
MENU9999  RETURN
+
.****************************************************************************
.* DATE0000  :  Get the Report date                                         *
.****************************************************************************
DATE0000  DISPLAY   *P1:24,*EL,*P1:9,"Enter the report date : "
.
DATE1000  MOVE      NINE,CVERT
          MOVE      "25",CCOL
          MOVE      CCC,CCENT
          UNPACK    SP10,CYEAR,CMON,CDAY
          MOVE      ONE,CDEFDTE
.
          CALL      KEYDATE
          BRANCH    CQUEST,DATE1000
          BRANCH    OVRCD,DATE9000
.
          PACK      RDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",RDATE
.
          MATCH     CURRDATE,RDATE
          GOTO      DATE8000 IF LESS
          GOTO      DATE8000 IF EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"Date must not be into future.  ";
          CALL      HITENTER
          GOTO      DATE1000
.
DATE8000  CALL      PACDATE
          MOVE      CPCDATE,DRDATE                 * display var
.
. now open the day file for the keyed in date
.
          PACK      CMONDAY,CMON,CDAY
          REP       " 0",CMONDAY
        
          PACK      DAYFILE,FILDAYA1,CCENT,CYEAR
          REP       " 0",DAYFILE
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATDAYA1,DAYFILE
          TRAPCLR   IO
          IF        OVRCD=1
            DISPLAY   *P1:24,*EL,*B,"Day file not set up.  ";
            CALL      HITENTER
            DISPLAY   *P1:24,*EL
            GOTO      DATE1000
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      DATE9999
.
DATE9000  MOVE      ONE,EXIT
.
DATE9999  RETURN
+
.***************************************************************************
.*  LOAD0000  :  Load data into temp file                                  *
.***************************************************************************
LOAD0000  DISPLAY   *P30:24,"Loading... [        ]";
.
          PACK      KEY12,CMONDAY,SP10
          CALL      RSPTDAY1
LOAD1000  CALL      RKPTDAY1
          BRANCH    OVRCD,LOAD9999
.
          MATCH     CMONDAY,PTDYDATE              * still same date?
          GOTO      LOAD9999 IF NOT EQUAL
.
          DISPLAY   *P42:24,PTDYADMN;
.
          PACK      KEY8,PTDYADMN                 * mental health patient?
          CALL      RDMHVIS1
          BRANCH    OVRCD,LOAD1000
.
          PACK      KEY8,PTDYADMN                 * get U/R Number
          CALL      RDMISS1
          BRANCH    OVRCD,LOAD1000                * ignore
.
          MOVE      AURNO,XXURNO
          PACK      KEY8,XXURNO
          CALL      RATEMP1
          IF        OVRCD=1
            MOVE      MHVIADMN,XXADMN
            CALL      WRTEMP1
          ENDIF
          GOTO      LOAD1000
.
LOAD9999  RETURN
+
.***************************************************************************
.*  RPRT0000  :  Print report                                              *
.***************************************************************************
RPRT0000  DISPLAY   *P1:24,*EL,*P30:24,"Printing... [        ]";
.
          CALL      IBACLOCK                      * initialise a few things
          MOVE      ZERO,CPAGENO
          MOVE      ONE,CNOUNDLN
          MOVE      TEN,CLNO                      * line counter
.
          CALL      HEAD132                       * Print 3 line header
          PRINT     *40,"Report Date : ",DRDATE,*N
          CALL      PTHD0000                      * Print table headings
.
. Loop thru temp file for each UR found
.
          PACK      KEY8,SP8
          CALL      RSTEMP1
RPRT0500  CALL      RKTEMP1
          BRANCH    OVRCD,RPRT9000
.
          PACK      KEY8,XXADMN                   * get mental health details
          CALL      RDMHVIS1
          BRANCH    OVRCD,RPRT0500
.
          CALL      IDAT0000                      * init temp vars
.
. check for Admission/Leave status
.
          PACK      KEY30,MHVIADMN,RDATE,SP30
          CALL      RDSTRAN2
RPRT2000  CALL      RDKTRAN2
          BRANCH    OVRCD,RPRT0500
.
          MATCH     MHVIADMN,TADMN                * same admission number?
          GOTO      RPRT0500 IF NOT EQUAL
.
          MATCH     RDATE,TDATE                   * same date?
          GOTO      RPRT0500 IF NOT EQUAL
.
          MATCH     ANSA,TMOVE                    * Admission?
          GOTO      RPRT5000 IF EQUAL
          MATCH     ANSC,TMOVE                    * Change record? Ignore
          GOTO      RPRT2000 IF EQUAL
          MATCH     ANSD,TMOVE                    * Discharge record? Ignore
          GOTO      RPRT2000 IF EQUAL
          MATCH     ANSL,TMOVE                    * Leave record? Ignore
          GOTO      RPRT2000 IF EQUAL
.
. We have a Return From Leave so make sure Leave is 'Trial' or 'Holiday' leave
.
          PACK      KEY5,ANST,ANSL,PTTRLTYP       * check if Trial/Holiday leave
          CALL      RDCODE1
          BRANCH    OVRCD,RPRT2000
          MATCH     ANST,TCINDC1                  * Trial Leave?
          GOTO      RPRT2500 IF EQUAL
          MATCH     ANSH,TCINDC1                  * Holiday Leave?
          GOTO      RPRT2000 IF NOT EQUAL
.
. make sure patient was not discharged on the day they returned from leave
.
RPRT2500  MOVE      ZERO,SAMEDIS                  * no discharge rec found
          PACK      SAVKEY30,TADMN,TDATE,TTIME,TWARD,TBED    * save curr record
RPRT3000  CALL      RDKTRAN2
          BRANCH    OVRCD,RPRT4000
.
          MATCH     MHVIADMN,TADMN                * same admission number?
          GOTO      RPRT4000 IF NOT EQUAL
.
          MATCH     ANSD,TMOVE                    * ignore if no leave record
          GOTO      RPRT3000 IF NOT EQUAL
.
          MATCH     TDATE,RDATE                   * Return & leave on same date?
          GOTO      RPRT4000 IF NOT EQUAL
.
          MOVE      ONE,SAMEDIS                   * disch and return on same day
.
RPRT4000  MOVE      SAVKEY30,KEY30                * restore key
          CALL      RDTRAN2
          BRANCH    SAMEDIS,RPRT2000              * try next record
.
. we have a record to print so set up details for report
.
RPRT5000  COMPARE   "55",CLNO                     * need a new page?
          GOTO      RPRT5100 IF LESS
.
. set up for new page
.
          CALL      HEAD132
          PRINT     *40,"Report Date : ",DRDATE
          CALL      PTHD0000                      * print table headings
          MOVE      TEN,CLNO
.
RPRT5100  PACK      KEY8,MHVIADMN
          CALL      RDMISS1
          BRANCH    OVRCD,RPRT0500                * ignore if no admission rec
          MOVE      AADMNO,SAVADMNO               * save admission no.
.
          PACK      KEY8,AURNO
          CALL      RDMAST1
          BRANCH    OVRCD,RPRT0500                * ignore if no master rec
.
.      patients name
.
          STRIP     PSNAME
          PACK      XXNAME,PSNAME,COMMA,SP1,PGNAME
.
.      patients address
.
          PACK      XXADD,PADD1,SP20,SP20
          STRIP     XXADD
          ENDSET    XXADD
          APPEND    SP1,XXADD
          APPEND    PADD2,XXADD
          RESET     XXADD
          STRIP     XXADD
          ENDSET    XXADD
          APPEND    SP1,XXADD
          APPEND    PSUBRB,XXADD
          RESET     XXADD
.
.      patients Date of Birth
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DSDOB
.
.      patients age at report date
.
          MOVE      RDATE,AGEDATE
          CALL      CALCAGE
          MOVE      PSAGEYRS,XXAGE
.
.      race (HDP equiv PUSR1)
.
          MOVE      SP4,THCSCOD
          MATCH     SP3,PUSR1
          IF        @EQUAL
            MOVE      SP1,XXRACE
          ELSE
            PACK      KEY5,ANSP,ONE,PUSR1
            CALL      RDCODE1
            MOVE      THCSCOD,XXRACE                * Race
         ENDIF
.
.      Marital Status (HDP equiv PMSTAT)
.
          MOVE      SP4,THCSCOD
          MATCH     SP3,PMSTAT
          IF        @EQUAL
            MOVE      SP1,XXMARIT
          ELSE
            PACK      KEY5,ANSM,SP1,PMSTAT
            CALL      RDCODE1
            MOVE      THCSCOD,XXMARIT               * Marital Status
          ENDIF
.
. Get Legal status (HDP equivalent) at current date
.
          MOVE      SP3,XXLSC
          MOVE      SP20,XXLSD
          MOVE      SP4,THCSCOD
          MOVE      SP20,TDESC
          PACK      KEY21,MHVIADMN,ZED20
          CALL      RSMHLEG1
LOAD5200  CALL      RPMHLEG1
          BRANCH    OVRCD,LOAD5250
.
          MATCH     MHVIADMN,MHLEADMN             * same admission no.?
          GOTO      LOAD5250 IF NOT EQUAL
.
          MATCH     MHLEDATE,RDATE                * <= report date?
          GOTO      LOAD5200 IF LESS
.
.      Legal Status (HDP equiv)
.
          PACK      KEY5,ANSL,ANSS,MHLESTAT
          CALL      RDCODE1
          MOVE      THCSCOD,XXLSC               * Legal Status code
          MOVE      TDESC,XXLSD                 * Legal status description
.
.      Referral Source (HDP equivalent)
.
LOAD5250  MOVE      SP4,THCSCOD
          MOVE      SP20,TDESC
          MATCH     SP3,ASRCE
          IF        @EQUAL
            MOVE      SP3,XXREFC
            MOVE      SP20,XXREFD
          ELSE
            PACK      KEY5,ANSS,SP1,ASRCE
            CALL      RDCODE1
            MOVE      THCSCOD,XXREFC                * Legal Ref. Source code
            MOVE      TDESC,XXREFD                  * Legal Ref. Source desc
          ENDIF
.
.      ICD9 Diagnosis (Get diagnosis at time of report date)
.
          MOVE      SP9,XXDIA1C
          MOVE      SP9,XXDIA2C
          MOVE      SP30,XXDIA1D
          MOVE      SP30,XXDIA2D
.
          PACK      KEY16,MHVIADMN,ZED20  
          CALL      RSMHDIA1
RPRT5300  CALL      RPMHDIA1
          BRANCH    OVRCD,RPRT5500
.
          MATCH     MHVIADMN,MHDIADMN             * same admission no.?
          GOTO      RPRT5500 IF NOT EQUAL
.
. the following match was required in the specs but was taken out so the
. report would look like the PAXUS version.  Get a clu PAXUS
.
......    MATCH     MHDIDATE,RDATE
......    GOTO      RPRT5300 IF LESS
.
          MOVE      MHDIICD1,XXDIA1C
          MOVE      MHDIDES1,XXDIA1D
          MOVE      MHDIICD2,XXDIA2C
          MOVE      MHDIDES2,XXDIA2D
.
. Print details
.
RPRT5500  PRINT     *1,PURNO,*11,XXNAME,*50,DSDOB,*59,XXAGE,*64,PSEX,*69,XXRACE:
                    *73,XXMARIT,*78,MHVIATYP,*87,XXLSC," - ",XXLSD:
                    *N,*11,XXADD,*87,XXREFC," - ",XXREFD:
                    *N,*11,"Domicile Code: ",PPOST,*64,"Diagnosis A: ",XXDIA1D:
                    SP2,"I.C.D. Code : ",XXDIA1C:
                    *N,*64,"Diagnosis B: ",XXDIA2D,SP2,"I.C.D. Code : ",XXDIA2C
.
          CALL      OADM0000     * get/print ALL other Admissions for this pat.
.
          DISPLAY   *P43:24,MHVIADMN;
          GOTO      RPRT2000
.
. we have finished printing patient data so print end of report details
.
RPRT9000  PRINT     *N,"*** End of Report ***"
          DISPLAY   *P1:24,*EL
.
RPRT9999  RETURN
+
.**************************************************************************
.*  PTHD0000  :  Print Table Headings                                     *
.**************************************************************************
PTHD0000  PRINT     *1," U/R No.  Patient Name",*50,"D.O.B.",*59,"Age Sex ":
                    "Race  M  Adm/Repl   Legal Status":
                    *N,*11,"Address",*87,"Referral Source"
.
PTHD9999  RETURN
+
.**************************************************************************
.*  OADM0000  :  Get ALL other Admissions for the current patient         *
.**************************************************************************
OADM0000  MOVE      ZERO,OVHEAD                   * print header flag
.
          PACK      KEY24,PURNO,SP20,SP4
          CALL      RDSVISA2
OADM0500  CALL      RDKVISA2
          BRANCH    OVRCD,OADM9000
.
          MATCH     PURNO,PVIURNO                 * same U/R Number?
          GOTO      OADM9000 IF NOT EQUAL
.
          COMPARE   THREE,PVITYPE                 * inpatient?
          GOTO      OADM0500 IF NOT EQUAL
.
. now check if a Mental Health patient
.
          PACK      KEY8,PVIBILL
          CALL      RDMHVIS1
          BRANCH    OVRCD,OADM0500
.
          MATCH     SAVADMNO,MHVIADMN             * is this the current patient?
          GOTO      OADM0500 IF EQUAL
.
          COMPARE   "55",CLNO                     * need a new page?
          GOTO      OADM1000 IF LESS
.
. set up for new page
.
          CALL      HEAD132
          PRINT     *40,"Report Date : ",DRDATE
          CALL      PTHD0000                      * print table headings
          MOVE      TEN,CLNO
.
. we have a new visit so get details for this visit
.
OADM1000  MOVE      SP8,ADATE
          PACK      KEY8,MHVIADMN                 * get Admission date
          CALL      RDMISS1
.
          MOVE      SP8,DDATE
          PACK      KEY8,MHVIADMN                 * get Discharge date
          CALL      RDDSCH1
.
. Print Record (another visit)
.
          BRANCH    OVHEAD,OADM7000               * print header?
          PRINT     *11,"Other Admissions:  Hospital",*61,"Adm Date":
                    *73,"Disch Date"
          MOVE      ONE,OVHEAD                    * printed header flag
.
OADM7000  UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DSADATE                * display var (Admis date)
.
          UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DSDDATE                * display var (Disch date)
.
          PRINT     *29,XXHOSP,*61,DSADATE,*73,DSDDATE
          ADD       ONE,CLNO
          GOTO      OADM0500                      * get next visit
.
OADM9000  PRINT     *N;
.
OADM9999  RETURN
+
.*************************************************************************
.*  IDAT0000  :  initialise vars                                         *
.*************************************************************************
IDAT0000  UNPACK    SP70,XXNAME,XXADD
          UNPACK    SP70,XXPOST,XXSEX,XXRACE,XXMARIT,XXLSC,XXLSD:
                         XXREFC,XXREFD
          UNPACK    SP70,XXDIA1C,XXDIA1D,XXDIA2C
          UNPACK    SP70,XXDIA2D
.
          MOVE      ZERO,XXAGE
.
IDAT9999  RETURN
+
.*************************************************************************
.*  KILL0000  :  erase temp file                                         *
.*************************************************************************
KILL0000  CLOSE     MEHTMPA1
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    TFILNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
KILL9999  RETURN
+
.*************************************************************************
.*                             CRET0000            Called by:            *
.*                      create temporary file                            *
.*************************************************************************
CRET0000  CALL      KILL0000 
          CLEAR     CMDLINE 
          APPEND    "isbuild ",CMDLINE
          APPEND    TFILNAME,CMDLINE
          APPEND    " 17 U1-8",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      MEHTMPA1,TFILNAME
.
CRET9999  RETURN
+
.***************************************************************************
.*  Temp file I/O's                                                        *
.***************************************************************************
RATEMP1   RESET     KEY8
          MOVE      ZERO,OVRCD
          READ      MEHTMPA1,KEY8;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTEMP1   RESET     KEY8
          READ      MEHTMPA1,KEY8;;
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    MEHTMPA1;XXURNO,XXADMN
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   RESET     KEY8
          WRITE     MEHTMPA1,KEY8;XXURNO,XXADMN
          RETURN
.
.
          INC       STD001IO
          INC       MEHVI1IO/INC
          INC       MEHLEGIO/INC
          INC       MEHDIAIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATCODIO/INC
          INC       MEHDS1IO/INC
          INC       PATHOSIO/INC
          INC       PATDSCIO/INC
          INC       PATVISIO/INC
          INC       PATTRNIO/INC
          INC       PATDAYIO/INC
          INC       CALCAGE
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC

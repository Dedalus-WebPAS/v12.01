.*****************************************************************************
.* System    :   Patient Management                                          *
.* Program   :   FX230961                                                    *
.* Desc      :   Fixit for Assessments where Excess Amount exceeds Claimed   *
.*               Amount.                                                     *
.*****************************************************************************
.* Date      :   29/09/2010                                                  *
.* Author    :   Steve Armstrong                                             *
.* Function  :   This program will check for records where the status is     *
.*               "Complete Reported" but the Excess on the Assessment report *
.*               is equal to or greater than the claimed amount.  Any        *
.*               matching records found will have their status updated to    *
.*               "Closed".                                                   *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       IBACONFD/INC
          INC       PATCONFD/INC
          INC       PATFN1FD/INC
          INC       PATFX1FD/INC
          INC       PATHSPFD/INC
          INC       PMSEAHFD/INC
          INC       PMSECAFD/INC
          INC       PMSECTFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
COUNTER   FORM      10
.
DIM3      DIM       3
DIM7      DIM       7
DIM10     DIM       10
DIM20     DIM       20
.
FORM7P2   FORM      7.2
.
SAVKEY35  DIM       35
.
.
.
PRGID     INIT      "FX230961"
PRGNAM    INIT      "Fixit for Assessments where Excess exceeds Claim"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000       * proceed with fixit
.
MAIN1000  CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN2000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN2000  CALL      PROC0000               * process
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"pmsectaf";
          OPEN      PMSECTA1,"pmsectaf"
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P64:24,"pmseahaf";
          OPEN      PMSEAHA2,"pmseahaf"
.
          DISPLAY   *P64:24,"pmsecaaf";
          OPEN      PMSECAA1,"pmsecaaf"
.
          DISPLAY   *P64:24,"patfn1af";
          OPEN      PATFN1A1,"patfn1af"
.
          DISPLAY   *P64:24,"patfx1af";
          OPEN      PATFX1A1,"patfx1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TEN3;*194,CVETINS
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run fixit                               *
.*                        TRUE  (1)  Exit option selected                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*           Check for records where the status is "Complete Reported"       *
.*           but the Excess on the Assessment report is equal to or          *
.*           greater than the claimed amount.  Any matching records found    *
.*           will have their status updated to "Closed".                     *
.*****************************************************************************
.
PROC0000  DISPLAY   *P1:24,*EL,"Processing:";
          MOVE      ZERO,COUNTER                 * initialise counter
.
          MOVE      SP3,PTHSHOSP                 * initialise hospital code
          COMPARE   ZERO,IBCNMHOS                * using multi-hospital ?
          GOTO      PROC1000 IF EQUAL            * no
.
.         Using multi-hospital, so loop through pmsectaf for each
.         hospital code.
.
          MOVE      SP3,KEY3
          CALL      RSPTHSP1                     * position at start of file
PROC0500  CALL      RKPTHSP1                     * read next record
          BRANCH    OVRCD,PROC9000               * eof - finished
.
.         Loop through pmsectaf for the hospital code and a status of
.         Complete Reported (13).
.
PROC1000  PACK      KEY35,PTHSHOSP,ONE,THREE,SP30,SP5
PROC1500  CALL      RSPMECT1                     * position on comp. reported
PROC2000  CALL      RKPMECT1                     * read next record
          BRANCH    OVRCD,PROC8000               * eof - finished
.
          COMPARE   TEN3,PMECFLAG                * same status still ?
          GOTO      PROC8000 IF NOT EQUAL        * no - finished
.
          DISPLAY   *P13:24,*EL,*V2LON,PMECHOSP,PMECFLAG,PMECHFND,PMECADMN:
                                       PMECINVN,PMECBATN
.
          MOVE      CVETINS,DIM3                 * load DVA participant code
          BRANCH    PMECEETP,PROC4000            * DVA claim
.
.         A HF claim record has been found with a status of Complete Reported,
.         so determine the participant code from the health fund.
.
          PACK      KEY14,PMECHFND,ZERO,ZERO,ZERO,ZERO,SP20
          CALL      RDFUND1                      * HF on file ?
          BRANCH    OVRCD,PROC2000               * no - ignore record
.
          MOVE      PMECHFND,KEY6
          CALL      RDPTFX11                     * HF extension on file ?
          BRANCH    OVRCD,PROC2000               * no - ignore record
.
          MATCH     SP3,PTFXECLP                 * blank participant code ?
          IF        @EQUAL
            MOVE      PTHFHGRP,DIM3              * yes - use HF Group
          ELSE
            MOVE      PTFXECLP,DIM3              * no - use eclipse part.
          ENDIF
.
PROC4000  MOVE      PMECINVN,DIM20               * load a/c reference id
          SQUEEZE   DIM20
.         PACK      DIM20,DIM20,SP20
.
.         Now check if there is a final Assessment Report for the claim.
.
          PACK      KEY62,PMECTRID,DIM3,DIM20,Z70
          CALL      RSPMEAH2                     * position on trans. id
PROC5000  CALL      RPPMEAH2                     * read previous record
          BRANCH    OVRCD,PROC2000               * eof - no assessment report
.
          MATCH     PMECTRID,PMAHFTID            * same claim trans. id ?
          GOTO      PROC2000 IF NOT EQUAL        * no - no assessment report
.
          MATCH     DIM3,PMAHFBID                * same fund brand. id ?
          GOTO      PROC2000 IF NOT EQUAL        * no - no assessment report
.
          MATCH     DIM20,PMAHARID               * same invoice number ?
          GOTO      PROC2000 IF NOT EQUAL        * no - no assessment report
.
          MATCH     ANSA,PMAHCFAC                * assessment code "A" ?
          GOTO      PROC5000 IF NOT EQUAL        * no - get next pmseahaf record
.
.         A final Assessment Report for the claim was found, so check if
.         the excess amount is less than the claim amount.
.
          UNPACK    PMAHEXAM,DIM7,DIM2           * convert excess to FORM field
          PACK      DIM10,DIM7,DOT,DIM2
          MOVE      DIM10,FORM7P2
          COMPARE   PMECAMTC,FORM7P2             * excess < claim amount ?
          GOTO      PROC2000 IF LESS             * yes  - ignore record
.
.         The excess amount is equal to or greater than the total claim amount,
.         so we expect that a Remittance Report will not be returned, so
.         we need to update the status of the record to closed.
.
          ADD       ONE,COUNTER                  * increment counter
          PACK      SAVKEY35,PMECHOSP,PMECFLAG,PMECHFND,PMECADMN,PMECINVN:
                             PMECBATN
          MOVE      TEN5,PMECFLAG                * set status to "Closed"
          CALL      UPPMECT1                     * update pmsectaf record
.
.         Finally, write an EDI History record (pmsecaaf)
.
          MOVE      PMECINVN,PMEAINVN
          CALL      IBACLOCK
          PACK      PMEADATE,CCC,CYY,CMM,CDD
          REP       " 0",PMEADATE
          MOVE      CTIMEIS,PMEATIME
          MOVE      PMECBATN,PMEABATN
          MOVE      PMECFLAG,PMEASTAT
          MOVE      " 5",PMEATYPE
          MOVE      "FX230961  ",PMEAOPER
          MOVE      PMECTRID,PMEATRID
          MOVE      SP4,PMEAEROR
          MOVE      SP70,PMEAEROT
          MOVE      SP30,PMEASPAR
.
PROC6000  PACK      KEY26,PMEAINVN,PMEADATE,PMEATIME,PMEATYPE
          CALL      RAPMECA1
          IF        OVRCD = 1
            CALL      WRPMECA1
          ELSE
            CALL      IBACLOCK
            PACK      PMEADATE,CCC,CYY,CMM,CDD
            REP       " 0",PMEADATE
            MOVE      CTIMEIS,PMEATIME
            GOTO      PROC6000
          ENDIF
.
          MOVE      SAVKEY35,KEY35
          GOTO      PROC1500                     * get next pmsectaf record
.
.         We have finished processing the records for a hospital
.
PROC8000  BRANCH    IBCNMHOS,PROC0500            * using multi-hospital
.
PROC9000  DISPLAY   *P1:24,*EL,*V2LON,COUNTER,*HOFF," records updated.  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PATFN1IO/INC
          INC       PATFX1IO/INC
          INC       PATHSPIO/INC
          INC       PMSEAHIO/INC
          INC       PMSECAIO/INC
          INC       PMSECTIO/INC

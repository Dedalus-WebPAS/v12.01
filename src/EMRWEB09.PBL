.----------------------------------------------------------------------
. Program       : EMRWEB09
.
. Function      : Emergency Table Maintenance
.
. Modifications : 
.
.-----------------------------------------------------------------------------
. V12.00.02 20/06/2025  Ebon Clements   TSK 0961623
.           Fixed visit number allocation for ED events - ADDEMV00
. V12.00.01 15/05/2025  Nikitha B       TSK 0955096
.           Alphanumeric changes.
.           13/05/2025  Ebon Clements   TSK 0955096
.           Added alpanumeric visit number generation - ADDEMV00 - GENANVIS
. V11.05.01 02/12/2024 Ebon Clements   TSK 0951038
.           Retain EXIT value after NEWVIS30 DELE0000 call
. V11.04.01 20/09/2024 J.Tan           TSK 0950577
.           Recompiled for PATIPERH - Fixed issue with Hold Invoice audit
. V11.03.02 04/10/2023  J.Tan          TASK 0938230
.           Recompiled for ABFAECCL - Emergency ABF
. V11.03.01 05/04/2023 Bella Turco     TSK 0911166
.           Recompiled for PATIPERH - Hold Invoice Audit
. V11.02.07 18/07/2022 J.Tan           TSK 0906596
.           Mods for Claim code Indic2=N (NWAUFLAG)
. V11.02.06 29/06/2022 J.Tan           TSK 0920712
.           Recompiled for ABFAECCL - I*D on emrvcdaf file
. V11.02.05 31/04/2022 J.Tan           TSK 0915626
.           Recompiled for AAECHRG - check if ABF Invoice has been raised
. V11.02.04 19/05/2022 J.Tan           TSK 0837128
.           Recompiled for PRFAINSR - to use Cat CL indic 24
. V11.02.03 26/04/2022 J.Tan           TSK 0837128
.           Recompiled for WEBEMRCM - Default PRFA based on Claim type
. V11.02.02 01/03/2022  Alina Bhari   TSK 0900613
.           Recompiled for PATMASTM
.           Aboriginality Icon dislayed on Patient Banner Banner if INDC 3=D
. V11.02.01 09/02/2022  Thanh T       TSK 0905641
.           Recompiled as OUTMA1FD (PATMASTM) changes
. V11.01.05 08/11/2021 J.Tan           TSK 0880410
.           Recompiled for PATIPERH - added Hospital and System
. V11.01.04 17/11/2021  J.Tan          TSK 0913621
.           Recompiled for ABFAEINV - Mod for Multiple NEP Values
. V11.01.03 28/10/2021 J.Tan           TSK 0913056
.           Recompiled for ABFAECCL-ADMFLAG and CCAR0000(Clinical Care)
. V11.01.02 13/10/2021 J.Tan    TSK 0905305
.           Recompiled for ABFAECCL - Adjustment type 068 & 069
. V11.01.01 09/08/2021  J.Tan           TSK 0905305
.           Recompiled for AAECHRG/ABFAEINV - AE Care class for 2021/2022
. V11.00.07 12/01/2021  Ebon Clements  TSK 0901440
.           Fixed deposit file read comdepaf index 2 key size - KEY26
. V11.00.06 09/12/2020  Thanh T        TSK 0872840
.           Added PMSCCDFD/PMSCCDIO as PMSPX2TM changes
. V11.00.05 22/10/2020  J.Tan           TSK 0898866
.           Recompiled for PATIPERH - on hold invoice(Contract for Eff.Adm.from)
. V11.00.04 01/07/2020 J.Tan           TSK 0886178
.           Recompiled for ABFAEINV - write to invoice pending details
. V11.00.03 29/06/2020  J.Tan           TSK 0893767
.           Recompiled for ABFAEINV - Changed CALCFLAG to 01/01/2021
. V11.00.02 20/04/2020 J.Tan           TSK 0890733
.           Recompiled for ABFAEINV - ABF new 2020 calculation
. V11.00.01 22/04/2020  Ebon Clements    TSK 0850105      
.           Clear PMCUTSTA and PMCUTLOC before write to pmscuraf
. V10.15.02 13/02/2020  J.Tan          Task 0888319
.           Mod to call CHGAE000 at the end of process
. V10.15.01 28/01/2019  J.Tan            TSK 0857392
.           Added FNAMEP for ABF
. V10.14.03 19/09/2019  J.Tan           TSK 0857392
.           Recompiled for ABF EMR invoice
. V10.14.02 20/08/2019  J.Tan           TSK 0857392
.           Recompiled for ABF invoice
. V10.14.01 05/08/2019  J.Tan           TSK 0857392
.           Recompiled for ABF invoice
. V10.13.02 24/10/2018  Steve Armstrong TSK 0863512
.           Recompiled for changes to DGCLICCE & DGCLICAE.
.           Also added read of pmsvx1af prior to above DGCLI.. calls
.           where it was previously missing.
. V10.13.01 08/08/2018  Peter Vela       Task 0852438
.           Recompiled for WEBEMRVR
.------------------------------------------------------------------------------
. V10.11.03 28/12/2017  Ania P           TSK 0848601
.           Recompiled for changes to DGCLICAE
. V10.11.02 27/11/2017  Thanh Tieu       Task 0821710
.           Recompiled as PATMASTM changed
. V10.11.01 30/07/2017  Thanh T.         TSK 0821710
.           Audit admission/outpatient/UR comments. changed PATMASTM/PMSPX2TM.
.------------------------------------------------------------------------------
. V10.06.01 12/03/2015  Steve Armstrong  CAR 314108
.           Removed definition of PTCGDATE as PATCGPFD is no longer
.           in use.
.----------------------------------------------------------------------
. V10.04.02 07/04/2014  Peter Vela      CAR 286258
.           Recompiled for PATMASTM
. V10.04.01 31.03.2014  B.G.Ackland  CAR 299363
.           Replace META Tag Redirect with Javascript in Common RDIREC00
.           Routine
.----------------------------------------------------------------------
. V10.03.06 10/09/2013  Steve Armstrong  CAR 291089
.           Recompiled for changes to PMSQVIFD.
. V10.03.05 18/07/2013  Steve Armstrong  CAR 268961
.           Recompiled for changes to PMSQVIFD.
. V10.03.04 15/10/2012  Mike Laming      CAR 265541
.           Added test for Workers Comp. in VALCLM00
. V10.03.03 15/05/2012 J.Tan               CAR 255708
.           Recompiled for AAECHRG - Mods checking for Hold Invoices From Date
. V10.03.02 16/01/2012  Sandra Barcham  CAR 256563
.           Recompiled for PATMASTM
. V10.03.01 17/11/2011  Mike Laming     CAR 240184
.           Changes to IBAPOSTF Post Code table - added State to Keys
. V10.02.03 27/10/2011  Steve Armstrong CAR 251323
.           Recompiled for PMSQVIFD changes
. V10.02.02 26/09/2011  Steve Armstrong   CAR 249584
.           Added read of pmsvx1af record prior to calls to DGCLICEC.
.           Recompiled for changes to DGCLICEC.
. V10.02.01 22/06/2011  Steve Armstrong   CAR 240722
.           Mods to cater for changes to PMSCURFD:
.                - PMCUSURN & PMCUGNAM extended to 40 chars.
.                - PMCUGNM2 added.
.----------------------------------------------------------------------
. V10.01.02 19/01/2011  Davin            CAR 236197
.           Added broadcast triggers (dgclicae/cec) for add/update to emrvisaf
. V10.01.01 06/12/2010  Ebon Clements     CAR 233927
.           Added reason for hold update to invoice pending
. V10.00.01 18/03/2010  Steve Armstrong  CAR 135505
.           Added HL7TRGID & HL7INCLD setting for all broadcast messages
.----------------------------------------------------------------------
. V9.12.01  08/01/2010  J.Tan          CAR 206736
.           Recompiled for AAECHRG - only checking for facility fee if HESYST<>0
. V9.11.02  07/04/2009  Peter Vela     CAR 183976
.           Move spaces to PMVXPILC before WRPMVX11
. V9.11.01  17/02/2009  Peter Vela     CAR 183976
.           Move spaces to PMVXPOEC before WRPMVX11
. V9.10.01  05/05/2008  J.Tan          CAR 162313
.           Added Deposit status to comdepaf
. V9.09.02  13/11/2007  Peter Vela        CAR 151199
.           Added status 4 cancelled emergency visits  
. V9.09.01  10/08/2007  Steve Armstrong   CAR 145650
.           Added missing call to DGCLICCE when deleting an emergency visit
.----------------------------------------------------------------------
. V9.08.01  31/01/2007  Peter Vela    CAR 127930
.           Recompled for MRTCONFD
.----------------------------------------------------------------------
. V9.05.02  31/03/2006  Ebon Clements CAR 78527
.           Added hospital to current patients
. V9.05.01  09/03/2006  Ebon Clements CAR 78533
.           Mods for PATCODTM - Hospital specific category codes
. V9.05.B01 30/01/2006  Ebon Clements CAR 93186
.           Mods for REDIRECT length change. Recompiled for IBAWEBDF
.----------------------------------------------------------------------
. V9.04.04  01/06/2005  Peter Vela       CAR 60166
.           Recompiled for EMRCONFD
. V9.04.03  29/04/2005  Peter Vela       CAR 55214
.           Recompiled for MRTCONFD
. V9.04.02  01/03/2005  Peter Vela    CAR 57547
.           Recompiled for PATVADFD and PATVADIO
. V9.04.01  27/09/2004  Ebon Clements            CAR 53266
.           Multi hospital changes for ADDVIS00 - medical record link
.----------------------------------------------------------------------
. V9.03.10  18/06/2004 Ebon Clements  CAR 50825
.           Recompiled for AAECHRG
.           07/06/2004 Peter Vela CAR 49016
.           2004 Statutory Changes - recompiled for PMSPX2TM
. V9.03.09  26/05/2004 J.Tan CAR 50123
.           Recompiled for AAECHRG 
. V9.03.08  26/03/2004 Peter Vela    CAR 13038
.           Recompiled for PATDOCCD - Doctor Name format Parameter
. V9.03.07  23/01/2004 Ebon Clements CAR 38490 and 46979
.           Recompiled for MRTVISCD - Auto create medical records
. V9.03.06  18/12/2003  Peter Vela    CAR 43134
.           Recompiled for MRTCONFD
. V9.03.05  11/12/2003  Peter Vela    CAR 40355
.           Recompiled for PATNAMCD
.           15/12/2003  Ebon Clements CAR 45481
.           Mods for PMSCURFD file change - added outpatient site
. V9.03.04  24/10/2003  J.Tan   CAR 44172
.           Recompiled for AAEDTRFD 
. V9.03.03  24/10/2003  Steve Armstrong    CAR 38016
.           Mods to use standard CLEMRVIS instead of local CLEMRDET
. V9.03.02  03/10/2003 Peter Vela     CAR 41114
.           Recompiled for WEBEMRCM
. V9.03.01  28/05/2003  Lina Vo       CAR 37504              
.           Write SLA/ASGC code from ibapostf to visit extension table for
.           emergency events.
.----------------------------------------------------------------------
. V9.02.08  16/01/2003  Jill Habasque SRF 35097              
.           Fixed pack of key for patvisaf
. V9.02.07  06/07/2002  Jill Habasque SRF 19273              
.           Added given name and UR to current patients              
. V9.02.06  22/06/2002  Pat Dirito               SRF 18736
.           Fixed U*U caused by option 1. Created Routine DISEMR00
. V9.02.05  23/05/2002  Bronko Sosic 
.           Added open of patwr1af
. V9.02.04  20/02/2002  Jill Habasque
.           Added open of outsitaf
. V9.02.03  12/02/2002  Bronko Sosic
.           Added a move for ADMISSNO to EMVIADMN
. V9.02.02  08/02/2002  Pat Dirito
.           Recompiled for MRTVISCD         
. V9.02.01  30/01/2002  Jill Habasque
.           Added error for overseas claim code/resident status
. V9.02.00  21/01/2002  Jill Habasque
.           Created Program
.
.----------------------------------------------------------------------
          INC       IBAWEBDF    
          INC       TFILEVAR/INC
.
          INC       PATCOMM/INC
          INC       PMSPX2TD/INC
          INC       PMSHCGTD/INC
          INC       PMSHCPTD/INC
          INC       WEBDATDF
          INC       WEBEMRVR/INC
          INC       ABFVARS/INC
.
          INC       PATFINFD/INC
          INC       AAEDE1FD/INC
          INC       AAEDEBFD/INC
          INC       AAEDI1FD/INC
          INC       AAEDTRFD/INC
          INC       APSMASFD/INC
          INC       COMDEPFD/INC
          INC       EMRCONFD/INC
          INC       EMRINCFD/INC 
          INC       EMRSECFD/INC
          INC       EMRVISFD/INC
          INC       IBAPOSFD/INC
          INC       MEHLERFD/INC
          INC       MRTCONFD/INC
          INC       MRTMASFD/INC
          INC       MRTVISFD/INC
          INC       NHIETHFD/INC
          INC       NHIMASFD/INC
          INC       OPRNURFD/INC
          INC       OUTSITFD/INC
          INC       PATALRFD/INC
          INC       PATAM1FD/INC
          INC       PATCALAG/INC
          INC       PATCFAFD/INC
          INC       PMSUCHFD/INC
          INC       PMSUCDFD/INC
          INC       PATMASTD/INC
          INC       PATCONFD/INC
          INC       PATDHEAD/INC
          INC       PATDO1FD/INC
          INC       PATDSCFD/INC
          INC       PATFN1FD/INC
          INC       PATONHFD/INC
          INC       PATFX1FD/INC
          INC       PATFHIFD/INC
          INC       PATIN1FD/INC
          INC       PATINVFD/INC
          INC       PATIPEFD/INC
          INC       PATMA1FD/INC
          INC       PATNIDFD/INC
          INC       PATONLFD/INC
.
          INC       PMSCCDFD/INC            * Concession Card Details
          INC       PMSBRQFD/INC
          INC       PMSCURFD/INC
          INC       PMSPX2FD/INC
          INC       PMSHCGFD/INC
          INC       PMSHCPFD/INC
          INC       PATMI1FD/INC
          INC       PATPR1FD/INC
          INC       PATRE1FD/INC
          INC       PATVADFD/INC
          INC       PATVISFD/INC
          INC       PMSVX1FD/INC
          INC       PATWC1FD/INC
          INC       PATWMAFD/INC
          INC       PATWVEFD/INC
          INC       PATWR1FD/INC
          INC       PMSMTIFD/INC
          INC       PATHSPFD/INC
          INC       PATTRNFD/INC
          INC       MRTLOCFD/INC
          INC       EMRSITFD/INC
.
.
. CGI Parameters
.
EMEVT001  DIM       8              * Attendance Date
EMEVT002  DIM       8              * Attendance Time
EMEVT003  DIM       10             * Referring HCp
EMEVT004  DIM       3              * Claim Code
EMEVT005  DIM       3              * Visit type
EMEVT006  DIM       3              * Cancellation code
EMEVT007  DIM       8              * Discharge date
EMEVT008  DIM       8              * Discharge time
EMEVT009  DIM       3              * Discharge destination
NEXTPAGE  DIM       2              * Next page
UPDTTYPE  DIM       1              * Update Type - A=Add,U=Update,D=Delete
UPDATETY  DIM       1              * Update Type for Comments  (HPS01I)
UPDATEKY  DIM       22             
.
. Local Variable Definition
.
HESYST    FORM      1
AECNCHRS  FORM      1
AECNFTYP  FORM      1
CALLPOSN  FORM      1
CHGR      FORM      1
CLAIMCOD  DIM       3
COUNTER   FORM      3
VISITTYP  DIM       3
DOCTCODE  DIM       6
MODE1     DIM       1
MHFUND    DIM       6
NMPNUMB   DIM       20
FNAMEN    DIM       8
FNAMEP    DIM       8
FORM8     FORM      8
FORM12D2  FORM      12.2
INVDFLAG  FORM      "0"
SEC1      FORM      "00005"
SELCDATE  DIM       8
SP12      INIT      "            "
VCODE     DIM       5
PENDSDAT  DIM       8
PENDADAT  DIM       8
PENDDDAT  DIM       8
PENDFUND  DIM       6
PENDCLAM  DIM       3
PENDHOSP  DIM       3
PENDSYST  DIM       2
RTNFLG    FORM      1
.
. Following fields are used in AAECHRG/ABFAEINV (CHGAE000)
.
FROMDATE  DIM       8
TODATE    DIM       8
CALCFLAG  FORM      1
NODAYS    FORM      8
NWAUFLAG  FORM      "0"
CEFFDATE  DIM       8
CNTRCEFR  FORM      1
CONTRCID  DIM       6
NOFEE     FORM      "0"
PROGFLAG  FORM      1
SAVCONTR  DIM       6
WEBFLAG   FORM      1
NETTOT    FORM      12.2
GROSSTOT  FORM      12.2
LSTRATE   FORM      12.2
LINETOT   FORM      12.2
LINEGST   FORM      12.2
CNEXTINV  FORM      8
DFDATE    DIM       8
RECFLAG   FORM      1
FGSTFLAG  FORM      1
XLINETOT  FORM      6.2
XLINEGST  FORM      6.2
.
ANSCA     INIT      "CA"
ANSCM     INIT      "CM"
ZERO8     INIT      "00000000"
.
.------------------------------------------------------------------------------
PRGID     INIT      "EMRWEB09"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "Create Emergency Details"
.------------------------------------------------------------------------------
.
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
.------------------------------------------------------------------------------
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "Invalid Option",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  CALL      DISEMR00              * Display 
          GOTO      MAIN1000
.
MAIN1200  CALL      NEWVIS00              * add/update/delete emr record
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
.------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------
INIT0000  OPEN      AAEDE1A1,"aaede1af"
          OPEN      AAEDETB1,"aaedetbf"
          OPEN      AAEDI1A1,"aaedi1af"
          OPEN      AAEDTRE1,"aaedtref"
          OPEN      COMDEPA1,"comdepaf"
          OPEN      COMDEPA2,"comdepaf"
          OPEN      EMRINCA1,"emrincaf"     * Incomplete visit table
          OPEN      EMRSECA1,"emrsecaf"
          OPEN      EMRVISA1,"emrvisaf"
          OPEN      IBAPOST1,"ibapostf"
          OPEN      MRTMASA1,"mrtmasaf"
          OPEN      MRTMASA2,"mrtmasaf"
          OPEN      MRTVISA1,"mrtvisaf"
          OPEN      PATCFAA1,"patcfaaf"
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATFX1A1,"patfx1af"
          OPEN      PATIPEN1,"patipenf"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PATINVR3,"patinvrf"
          OPEN      PMSCURA1,"pmscuraf"
          OPEN      PMSPX2A1,"pmspx2af"
          OPEN      PATVADA1,"patvadaf"
          OPEN      PATVISA1,"patvisaf"
          OPEN      PATRE1A1,"patre1af"
.
          OPEN      PMSCCDA1,"pmsccdaf"
          OPEN      PMSHCPA1,"pmshcpaf"
          OPEN      PMSHCGA1,"pmshcgaf"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
          OPEN      PATWC1A1,"patwc1af"
          OPEN      PATWMAB1,"patwmabf"
          OPEN      PATWVET1,"patwvetf"
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATWR1A4,"patwr1af"
          OPEN      OUTSITA1,"outsitaf"
          OPEN      PMSMTIA1,"pmsmtiaf"
          OPEN      PATHSPA1,"pathspaf"
          OPEN      MRTLOCA1,"mrtlocaf"
          OPEN      EMRSITA1,"emrsitaf"
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TEN;*196,CGENRUR,*211,CAUDK,*213,CAUDM:
                                 *244,CHOSPNUM
          READ      CONTROLF,TEN3;*188,CMABINS,*194,CVETINS
          READ      CONTROLF,TEN8;*246,CMAXAGE
          READ      CONTROLF,TEN9;*208,CPMIAD,*209,CSRESP
          READ      CONTROLF,TWENTY;*14,PTPCLRES
          READ      CONTROLF,TWENTY2;*53,STCNPEMR
          READ      CONTROLF,THIRTY;*95,HESYST:
                                    *196,AECNCHRS,*197,AEALERT,*200,AECNFTYP
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          IF        IBCNMHOS=1
            OPEN      MLTCODA1,"mltcodaf"
            OPEN      MLTCODA2,"mltcodaf"
          ENDIF
.
...  used in ABFAECCL (CHGAE000,AAECHRG)
.
          CALL      TFILENAM                * Get file name calculating Exponent
          MOVE      TFILNAME,FNAMEN
.
          BRANCH    CAUDM,INIT9999
          OPEN      PATAM1A1,"patam1af"
.
INIT9999  RETURN
.------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          MOVE      SP70,TEMPLATE
          MOVE      SP70,UPDTTYPE
          PACK      REDIRECT,SP70,SP70,SP70,SP70
          MOVE      ZERO,NORECORD
          MOVE      ZERO,STARTNUM
          MOVE      SP70,UPDATETY
          MOVE      SP70,NEXTPAGE
          MOVE      SP70,URNUMBER
          MOVE      SP70,ADMISSNO
          MOVE      SP70,EMEVT001
          MOVE      SP70,EMEVT002
          MOVE      SP70,EMEVT003
          MOVE      SP70,EMEVT004
          MOVE      SP70,EMEVT005
          MOVE      SP70,EMEVT006
          MOVE      SP70,EMEVT007
          MOVE      SP70,EMEVT008
          MOVE      SP70,EMEVT009
.
          RETURN
.------------------------------------------------------------
. Set Parameters
.------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "redirect=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REDIRECT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updttype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDTTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updatety=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDATETY
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "urnumber=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,URNUMBER
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "admissno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ADMISSNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nextpage=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NEXTPAGE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "norecord=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NORECORD
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startnum=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTNUM
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "emevt",QRYNAME
          IF        @EQUAL
            CALL      SEEVT000
            COMPARE   ZERO,EXIT
            GOTO      SETPAR99 IF EQUAL
          ENDIF
.
.
SETPAR99  RETURN
+
.------------------------------------------------------------
. Emergency Event Details
.------------------------------------------------------------
SEEVT000  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,SEEVT010,SEEVT020,SEEVT030,SEEVT040,SEEVT050:
                       SEEVT060,SEEVT070,SEEVT080,SEEVT090
. 
          MOVE      ONE,EXIT
          GOTO      SEEVT999
.
SEEVT010  UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00            * Set CMON from MTHNAM3
          PACK      EMEVT001,CCENT,CYEAR,CMON,CDAY
          GOTO      SEEVT999
.
SEEVT020  MOVE      QRYDATA,EMEVT002   * attendance time
          GOTO      SEEVT999
.
SEEVT030  MOVE      QRYDATA,EMEVT003   * Referring HCP
          GOTO      SEEVT999
.
SEEVT040  MOVE      QRYDATA,EMEVT004   * Claim Code
          GOTO      SEEVT999
.
SEEVT050  MOVE      QRYDATA,EMEVT005   * Visit Type
          GOTO      SEEVT999
.
SEEVT060  MOVE      QRYDATA,EMEVT006   * Cancellation code
          GOTO      SEEVT999
.
SEEVT070  UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00            * Set CMON from MTHNAM3
          PACK      EMEVT007,CCENT,CYEAR,CMON,CDAY
          GOTO      SEEVT999            * discharge date
.
SEEVT080  MOVE      QRYDATA,EMEVT008
          GOTO      SEEVT999            * discharge time
.
SEEVT090  MOVE      QRYDATA,EMEVT009
          GOTO      SEEVT999            * discharge destination
.
SEEVT999  RETURN
.------------------------------------------------------------
. Display Info               
.------------------------------------------------------------
DISEMR00  PACK      KEY8,URNUMBER,SP70   
          CALL      RDMAST1
          BRANCH    OVRCD,DISEMR90
.
          CALL      RDPMPX21
          CALL      CLEMRVIS
.
          CLEAR     WEBTITLE
          MOVE      "Add Emergency Event",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00    * Create Output File and Write HTML Page Header
          BRANCH    EXIT,DISEMR99
          CALL      WEBBOD00    * Process Web Body
          CALL      WEBEND00    * Process End of HTML File
.
          GOTO      DISEMR99
.
DISEMR90  MOVE      "Can't Find Master Details",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      DISEMR99
.
DISEMR99  RETURN
.------------------------------------------------------------
. Create New Emergency Visit
.------------------------------------------------------------
NEWVIS00  PACK      KEY8,URNUMBER,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,NEWVIS90
          CALL      RDPMPX21
          PACK      KEY8,ADMISSNO,SP70
          CALL      RDEMVIS1
          IF        OVRCD=1
            CALL      CLEMRVIS
          ENDIF
.
          MOVE      "Add Emergency Event",WEBTITLE
.
          MATCH     SP70,UPDTTYPE
          GOTO      NEWVIS70 IF EQUAL
.
          MATCH     "A",UPDTTYPE
          GOTO      NEWVIS10 IF EQUAL
.
          MATCH     "U",UPDTTYPE
          GOTO      NEWVIS20 IF EQUAL
.
          MATCH     "D",UPDTTYPE
          GOTO      NEWVIS30 IF EQUAL
.
          GOTO      NEWVIS95
.
NEWVIS10  CALL      ADDEMV00         * write emr record
          BRANCH    EXIT,NEWVIS99
          CALL      UPVS000
.
          MOVE      ZERO,EXIT
          GOTO      NEWVIS99
.
NEWVIS20  CALL      UPDEMV00         * update emr details
          BRANCH    EXIT,NEWVIS99
          CALL      UPVS000
.
          MOVE      ZERO,EXIT
          GOTO      NEWVIS99
.
NEWVIS30  CALL      DELE0000         * delete emr details
          GOTO      NEWVIS99
.
NEWVIS70  PACK      KEY8,URNUMBER,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,NEWVIS90
          CALL      RDPMPX21
.
          PACK      KEY8,ADMISSNO,SP70
          CALL      RDEMVIS1
          IF        OVRCD=1
            CALL      CLEMRVIS
          ENDIF
.
          CALL      WEBHED00    * Create Output File and Write HTML Page Header
          BRANCH    EXIT,NEWVIS99
          CALL      WEBBOD00    * Process Web Body
          CALL      WEBEND00    * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      NEWVIS99
.
NEWVIS90  MOVE      "Can't Find Master Details",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      NEWVIS99
.
NEWVIS95  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      NEWVIS99
.
NEWVIS99  RETURN
.------------------------------------------------------------
. Process Fields 
.------------------------------------------------------------
PROFLD00  BRANCH    REPORTNO,PROFLD10,PROFLD10
          GOTO      PROFLD99
.
PROFLD10  BRANCH    FLDSETNO,PROFLD11,PROFLD12
          GOTO      PROFLD99
.
PROFLD11  CALL      MASF0000
          GOTO      PROFLD99
.
PROFLD12  CALL      EMER0000
          GOTO      PROFLD99
.
PROFLD99  RETURN
+
.------------------------------------------------------------
. Output emergency fields
.------------------------------------------------------------
EMER0000  BRANCH   FLDITMNO,EMER0010,EMER0020,EMER0030,EMER0040:
                            EMER0050,EMER0060,EMER0070,EMER0080:
                            EMER0090
.
EMER0010  MATCH     EMVIDATE,SP70
          GOTO      EMER9999 IF EQUAL
          UNPACK    EMVIDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          GOTO      EMER9999
.
EMER0020  WRITE     HTMLFILE;EMVITIME
          GOTO      EMER9999
.
EMER0030  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,EMER0035
          PACK      KEY10,EMVIDOCT,SP70
          CALL      RDPMHCP1
          BRANCH    OVRCD,EMER9999
          MOVE      PMHCTITL,FMTTITLE
          MOVE      PMHCGNAM,FMTGNAME
          MOVE      PMHCSNAM,FMTSNAME
          CALL      DOCNM000
          WRITE     HTMLFILE;DOCFNAME;
          GOTO      EMER9999
.
EMER0035  WRITE     HTMLFILE;EMVIDOCT;
          GOTO      EMER9999
.
EMER0040  MOVE      "CL",CATEGORY
          PACK      CODE,EMVICOMP,SP70
          CALL      CODITM00
          GOTO      EMER9999
.
EMER0050  GOTO      EMER9999
.
EMER0060  GOTO      EMER9999
.
EMER0070  MATCH     EMVIDDAT,SP70
          GOTO      EMER9999 IF EQUAL
          UNPACK    EMVIDDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          GOTO      EMER9999
.
EMER0080  WRITE     HTMLFILE;EMVIDTIM
          GOTO      EMER9999
.
EMER0090  MOVE      "ED",CATEGORY
          PACK      CODE,EMVIDSTA,SP70
          CALL      CODITM00
          GOTO      EMER9999
.
EMER9999  RETURN
+
.------------------------------------------------------------
. Write emrvisaf record
.------------------------------------------------------------
ADDEMV00  MOVE      ZERO,EXIT
          CALL      CLEMRVIS
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,HUND30;*75,PTCNUANV      * Using AN Visit Numbers
.
          MATCH     "1",PTCNUANV
          IF        @EQUAL
            CALL      GANV0000                * Get next AN visit number
            MOVE      PTCNNXTV,EMVIADMN
          ELSE
            MOVE      " 10",PRXCODE   * System Lock Sector 10
            CALL      GETSLK00        * Get System Lock of Sector 10
            READ      CONTROLF,TEN;*1,FORM8V        * get next visit number
            ADD       ONE,FORM8V
            WRITAB    CONTROLF,TEN;*1,FORM8V
            CALL      RELSLK00        * Release System Lock of Sector 10
            SUB       ONE,FORM8V
            MOVE      FORM8V,EMVIADMN
          ENDIF
.
.         Make sure that the new visit number does not exist already
.
          MOVE      EMVIADMN,PVIBILL
          MOVE      PVIBILL,KEY8
          CALL      RDAVISA1             * visit already on file?
          COMPARE   ZERO,OVRCD
          GOTO      ADDEMV00 IF EQUAL    * yes - get next number
.
          MOVE      PURNO,EMVIURNO       * load U/E
          MOVE      ONE,EMVISTAT         * set for current patient
          MOVE      PASSCODE,EMVIOPR1    * set operator
          MOVE      EMVIADMN,ADMISSNO    * Set visit number
.
          CALL      IBACLOCK
.
          CALL      MVEMR000
          MOVE      PURNO,EMVIURNO       * load U/E
.
          CALL      VALCLM00             * validate claim code/resident status
          BRANCH    EXIT,ADDEMV99
.
          PACK      EMVIDATE,EMVIDATE,SP70
          MATCH     SP70,EMVIDATE
          IF        @EQUAL
            PACK      EMVIDATE,CCC,CYY,CMM,CDD
            REP       " 0",EMVIDATE
          ENDIF
.
          PACK      EMVITIME,EMVITIME,SP70
          MATCH     SP70,EMVITIME
          IF        @EQUAL
            PACK      EMVITIME,CTIMEIS
          ENDIF
.
          PACK      EMVITRDT,EMVIDATE
          PACK      EMVITRTM,EMVITIME
          MOVE      WBSEESC,EMVISITE
.
          PACK      KEY3,EMVISITE,SP70
          CALL      RDEMSIT1
          IF        OVRCD<>1
            PACK      EMSTHSNO,SP70
          ENDIF
.
          CALL      WREMVIS1             * write new emergency record
.
.         Broadcast registration message
.         Moved below code into UPVS000 after write/update of pmsvx1af
.
.         CALL      PMIGTNID               * get national id for dgate write
.         MOVE      NMPNUMB,PTNINMPI
.         MOVE      THREE,HL7TRGID
.         MOVE      SP8,HL7INCLD
.         PROC      DGCLICAE               * send emr visit message
.
          CALL      WRCPAT00               * write to current patient table
          MOVE      ZERO,EXIT
.
ADDEMV99  RETURN
+
.
.***********************************************************************
.*    Validate the claim code/resident status                          *
.***********************************************************************
VALCLM00  PACK      KEY5,ANSC,ANSL,EMVICOMP,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,VALCLM90
.                                                        * used in VALCLM50
          PACK      KEY5,TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5,SP70 *I-265541
.
          MATCH     "1",TCINDC1
          GOTO      VALCLM50 IF NOT EQUAL
.
          PACK      KEY5,ANST,SP1,PTYPE,SP70
          CALL      RDCODE1
          MATCH     "2",TCINDC1
          IF        !@EQUAL
            MOVE      "ERROR: Claim code cannot be OVERSEAS/INELIGIBLE",WEBTITLE
            CALL      WEBERR00
            MOVE      ONE,EXIT
          ENDIF
          GOTO      VALCLM99
.
VALCLM50  PACK      KEY5,ANST,SP1,PTYPE,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,VALCLM95
.
          MATCH     "2",TCINDC1
          IF        @EQUAL
            SCAN      ANSM,KEY5             * test for Motor Accident Board
            GOTO      VALCLM99 IF EQUAL
            RESET     KEY5                                            *I-265541
            SCAN      ANSW,KEY5             * Test for Workers Comp.
            GOTO      VALCLM99 IF EQUAL
.
            MOVE      "ERROR: Claim Code must be OVERSEAS/INELIGIBLE",WEBTITLE
            CALL      WEBERR00
            MOVE      ONE,EXIT
          ENDIF
          GOTO      VALCLM99
.
VALCLM90  MOVE      "Invalid Claim code",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      VALCLM99
.
VALCLM95  MOVE      "Invalid Resident Status",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      VALCLM99
.
VALCLM99  RETURN
.
.------------------------------------------------------------
. Write patvisaf record
.------------------------------------------------------------
UPVS000   MOVE      ZERO,EXIT
          MOVE      EMVIURNO,PVIURNO
          MOVE      EMVIDATE,PVIDATE
          MOVE      EMVIADMN,PVIBILL
          MOVE      ONE,PVITYPE
          MOVE      EMVIDOCT,PVIDOCT
          MOVE      ONE,PVITRAN
          MOVE      SP30,PVISPAR
          MOVE      SP10,PVISITE
          MOVE      SP2,PVILOCK
.
          PACK      KEY8,PVIBILL,SP70
          CALL      RDAVISA1
          IF        OVRCD=1
            CALL      WRVISA1              * write visit record
          ELSE
            CALL      UPVISA1
          ENDIF
.
          PACK      KEY3,EMVISITE,SP70
          CALL      RDEMSIT1
          IF        OVRCD<>1
            PACK      PMVXMHOS,EMSTHSNO,SP70
          ENDIF
.
          MOVE      EMVIADMN,PMVXVISN
          MOVE      EMVIDATE,PMVXVSDT
          PACK      PMVXCDTE,CCC,CYY,CMM,CDD
          REP       " 0",PMVXCDTE           * Date
          CLOCK     TIME,PMVXCTIM

          MOVE      EMVIDOCT,PMVXDOCA
          MOVE      USERID,PMVXWEBC
          MOVE      SP20,PMVXSPAR
          MOVE      PPOST,PMVXPOST
          CALL      GTASGC00                * SLA/ASGC code
.
          MOVE      PMVXVISN,KEY8
          CALL      RAPMVX11
          IF        OVRCD=1
            MOVE      SP70,PMVXPOEC
            MOVE      SP70,PMVXPGID
            MOVE      SP70,PMVXPILC
            CALL      WRPMVX11              * write visit extension record
          ELSE
            CALL      UPPMVX11
          ENDIF
.
.         Broadcast registration message
.         (Moved here from ADDEMV00 as we need pmsvx1af record)
.
          MATCH     "A",UPDTTYPE
          IF        @EQUAL
            CALL      PMIGTNID             * get national id for dgate write
            MOVE      NMPNUMB,PTNINMPI
            MOVE      THREE,HL7TRGID
            MOVE      SP8,HL7INCLD
            PROC      DGCLICAE             * send emr visit message
          ENDIF
.
          CALL      ADDVIS00             * Addes record to MRTVISFD
.
.         Write a new record to the old A&E files
.
          CALL      CLDT0000             * clear a&e files
          CALL      UPDTA000             * update aae detail fields
.
          PACK      KEY8,ADMISSNO
          CALL      RDADETA1
          IF        OVRCD=1
            CALL      WRDETA1              * write aae details record
          ELSE
            CALL      UPDETA1
          ENDIF
.
.         If a discharge date has been entered, then create an aae discharge
.         record.  (This probably won't happen but the code is included
.         just in case).
.
          CALL      CLDS0000
          MATCH     SP8,EMVIDDAT
          IF        !@EQUAL
            CALL      UPDSA000           * clear aae discharge fields
            PACK      KEY8,ADMISSNO,SP70
            CALL      RDADISA1
            IF        OVRCD=1
              CALL      WRDISA1
            ELSE
              CALL      UPDISA1
            ENDIF
          ENDIF
.
          CALL      PRAUP000
.
          CALL      CLAMC000             * write claim details
.
          CALL      CHGAE000
.
UPVS999  RETURN
+
.------------------------------------------------------------
. Get SLA/ASGC code from ibapostf
.------------------------------------------------------------
GTASGC00  MOVE      PPOST,D8
          SQUEEZE   D8
          MOVE      D8,F4
          MOVE      F4,D4
          REP       " 0",D4
          PACK      PPOST,D4,SP10
          PACK      KEY56,PPOST,PSUBRB,SP10,PADD4,SP70                *C-240184
          CALL      RDIBPOS1
          IF        OVRCD=0
            MOVE      IBPOASGC,PMVXASGC
          ELSE                                                        *I-240184
            PACK      KEY56,PPOST,PSUBRB,SP70
            CALL      RSIBPOS1
            CALL      RKIBPOS1
            BRANCH    OVRCD,GTASGC99
            MATCH     PPOST,IBPOPCOD
            IF        @EQUAL
              MATCH     PSUBRB,IBPOSUBR
              IF        @EQUAL
                MOVE      IBPOASGC,PMVXASGC
              ENDIF
            ENDIF
          ENDIF
.
GTASGC99  RETURN
.------------------------------------------------------------
. Update emergency details
.------------------------------------------------------------
UPDEMV00  MOVE      ZERO,EXIT
          PACK      KEY8,ADMISSNO,SP70
          CALL      RDEMVIS1
          BRANCH    OVRCD,UPDEMV85
.
          COMPARE   FOUR,EMVISTAT
          GOTO      UPDEMV86 IF EQUAL
.
          CALL      MVEMR000
.
          CALL      VALCLM00             * validate claim code/resident status
          BRANCH    EXIT,UPDEMV90
.
          PACK      KEY8,ADMISSNO,SP70
          CALL      RAEMVIS1
          BRANCH    OVRCD,UPDEMV85
.
          COMPARE   FOUR,EMVISTAT
          GOTO      UPDEMV86 IF EQUAL
.
          CALL      UPEMVIS1
.
          MOVE      EMVIADMN,KEY8
          CALL      RDPMVX11
          BRANCH    OVRCD,UPDEMV99
.
.         Broadcast update message
.
          CALL      PMIGTNID               * get national id for dgate write
          MOVE      NMPNUMB,PTNINMPI
          MOVE      FOUR,HL7TRGID
          MOVE      SP8,HL7INCLD
          PROC      DGCLICEC                * send update emr details
.
          GOTO      UPDEMV99
.
UPDEMV85  MOVE      "ERROR: Missing emergency visit record.",WEBTITLE
          CALL      WEBERR00
          GOTO      UPDEMV90
.
UPDEMV86  MOVE      "ERROR: Cancelled emergency visit record.",WEBTITLE
          CALL      WEBERR00
.
UPDEMV90  MOVE      ONE,EXIT
.
UPDEMV99  RETURN
+
.------------------------------------------------------------
. Delete emergency details
.------------------------------------------------------------
.DELE0000  MOVE      ADMISSNO,KEY8
.          RJUSTIFY  KEY8
.          CALL      RLEMVIS1                       * read and lock record
.          BRANCH    OVRCD,DELE9800:                * not found
.                          DELE9500                 * record already locked
.
.DELE1000  CALL      PMIGTNID                 * get national id for dgate write
.          MOVE      NMPNUMB,PTNINMPI
.          MOVE      ONE,HL7TRGID
.          MOVE      SP8,HL7INCLD
.          PROC      DGCLICCE                       * send cancel emr details
.
.          MOVE      EMVIADMN,KEY8
.          CALL      DEEMVIS1                       * delete emr visit record
..
.DELE1050  MOVE      EMVIADMN,KEY8
.          CALL      RLPTVIS1                       * read and lock record
.          BRANCH    OVRCD,DELE1100:                * not on file
.                          DELE9400                 * record locked
.
.          MOVE      PVIBILL,KEY8
.          CALL      DEVISA1                        * delete patient visit record
.
.DELE1100  MOVE      EMVIADMN,KEY8
.          CALL      RDDETA1
.          BRANCH    OVRCD,DELE1200
.
.          MOVE      ADANUMB,KEY8
.          CALL      DEDETA1                        * delete aae details record
.
.DELE1200  MOVE      EMVIADMN,KEY8
.          CALL      RDDISA1
.          BRANCH    OVRCD,DELE1300
.
.          MOVE      ADSNUMB,KEY8
.          CALL      DEDISA1                        * delete aae discharge rec.
.
.DELE1300  PACK      KEY10,EMVIADMN,SP10
.          CALL      RDSDETB1
.          CALL      RDKDETB1
.          IF        OVRCD = 0
.            MATCH     EMVIADMN,ADBNUMB             * same admission still?
.            GOTO      DELE1400 IF NOT EQUAL        * no - finished this file
.            PACK      KEY10,ADBNUMB,ADBCLNO
.            CALL      DEDETB1                      * delete visit comments
.            GOTO      DELE1300
.          ENDIF
.
.DELE1400  MOVE      EMVIADMN,KEY8
.          CALL      RDWCOM1
.          BRANCH    OVRCD,DELE1500
.
.          MOVE      WCADMNO,KEY8
.          CALL      DEWCOM1                        * delete workers comp record
.
.DELE1500  MOVE      EMVIADMN,KEY8
.          CALL      RDWVET1
.          BRANCH    OVRCD,DELE1600
.
.          MOVE      VADMNO,KEY8
.          CALL      DEWVET1                        * delete VA record
.
.DELE1600  MOVE      EMVIADMN,KEY8
.          CALL      RDWMAB1
.          BRANCH    OVRCD,DELE1700
.
.          MOVE      MADMNO,KEY8
.          CALL      DEWMAB1                        * delete motor acc. record
.
.DELE1700  MOVE      EMVIADMN,KEY8
.          CALL      RDRESP1
.          BRANCH    OVRCD,DELE9900
.
.          MOVE      PKADMN,KEY8
.          CALL      DERESP1                         * delete PRA record
.
.          MOVE      ZERO,EXIT
.          GOTO      DELE9900
.
.DELE9000  CALL      ULEMVIS1                        * unlock visit record
.          GOTO      DELE9600
.
.DELE9400  APPEND    "Patient visit record locked",WEBTITLE
.          RESET     WEBTITLE
.          CALL      WEBERR00
.          GOTO      DELE9600
.
.DELE9500  MOVE      "Emergency visit record locked",WEBTITLE
.          CALL      WEBERR00
.
.DELE9600  MOVE      ONE,EXIT                        * set for no refresh
.          GOTO      DELE9999
.
.DELE9800  MOVE      "Record not found",WEBTITLE
.          CALL      WEBERR00
.          MOVE      ONE,EXIT
.          GOTO      DELE9999
.
.DELE9900  MOVE      ZERO,EXIT                         * set for refresh
.
.DELE9999  RETURN
+
.------------------------------------------------------------
.         DELE0000  Delete emergency event
.------------------------------------------------------------
DELE0000  CALL      CHKD0000                 * Check for deposits
          BRANCH    EXIT,DELE9850   
.         
          PACK      KEY14,ADMISSNO,SP20
          CALL      RSPMMTI1
          CALL      RKPMMTI1
          BRANCH    OVRCD,DELE0100
.
          MATCH     PMMIVISN,ADMISSNO
          GOTO      DELE9100 IF EQUAL
.
DELE0100  PACK      KEY16,ADMISSNO,SP20
          CALL      RSPTINV3
          CALL      RKPTINV3
          BRANCH    OVRCD,DELE0200
.
          MATCH     DPINVADM,ADMISSNO
          GOTO      DELE0200 IF NOT EQUAL
.
          MATCH     "1",PTINCRST
          GOTO      DELE9200 IF NOT EQUAL
.
DELE0200  MOVE      ADMISSNO,KEY8
          CALL      RLEMVIS1                       * read and lock record
          BRANCH    OVRCD,DELE9800:                * not found
                          DELE9500                 * record already locked
.
          COMPARE   FOUR,EMVISTAT
          GOTO      DELE9300 IF EQUAL
.
.         Broadcast cancel registration message
.
          MOVE      EMVIURNO,KEY8              * first - get the PMI details
          CALL      RDMAST1
          BRANCH    OVRCD,DELE0700
.
          MOVE      EMVIURNO,KEY8
          CALL      RDPMPX21
          BRANCH    OVRCD,DELE0700
.
          MOVE      EMVIADMN,KEY8
          CALL      RDPMVX11                   * get visit extension record
          BRANCH    OVRCD,DELE0700
.
          CALL      PMIGTNID                   * get national id for dgate write
          MOVE      NMPNUMB,PTNINMPI
          MOVE      TWO,HL7TRGID
          MOVE      SP8,HL7INCLD
          PROC      DGCLICCE                       * send cancel emr details
.
DELE0700  MOVE      EMVIADMN,KEY8
          MOVE      FOUR,EMVISTAT
          MOVE      SP70,EMVILOCN
          MOVE      SP70,EMVIPRLO
          MOVE      SP70,EMVIPRTM
          CALL      UPEMVIS1
.
.         MOVE      EMVIADMN,KEY8
.         CALL      RLEMUNK1
.         BRANCH    OVRCD,DELE1800:
.                         DELE9300
.         CALL      DEEMUNK1
.
DELE1800  MOVE      EMVIADMN,KEY8
          CALL      RDEMINC1
          BRANCH    OVRCD,DELE1900
.
          MOVE      EMIPADMN,KEY8
          CALL      DEEMINC1
.
DELE1900  CALL      ULEMVIS1
          MOVE      ZERO,EXIT
          GOTO      DELE9900
.
DELE9100  CLEAR     WEBTITLE
          APPEND    "Items exist for this visit.",WEBTITLE
          RESET     WEBTITLE
          CALL      WEBERR00
          GOTO      DELE9600
.
DELE9200  CLEAR     WEBTITLE
          APPEND    "Visit has been invoiced. ",WEBTITLE
          APPEND    "Invoice not fully credited.",WEBTITLE
          RESET     WEBTITLE
          CALL      WEBERR00
          GOTO      DELE9600
.
DELE9300  CLEAR     WEBTITLE
          APPEND    "Visit has already been cancelled.",WEBTITLE
          RESET     WEBTITLE
          CALL      WEBERR00
          GOTO      DELE9600
.
DELE9500  MOVE      "Emergency visit record locked",WEBTITLE
          CALL      WEBERR00
.
DELE9600  MOVE      ONE,EXIT                        * set for no refresh
          GOTO      DELE9999
.
DELE9800  MOVE      "Record not found",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      DELE9999
.
DELE9850  MOVE      "Deposits Exist - Visit not deleted",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      DELE9999
.
DELE9900  MOVE      ZERO,EXIT                         * set for refresh
.         CALL      WRTHIS00
.         CALL      DELAUD00
          GOTO      DELE9999
.
DELE9999  RETURN

.------------------------------------------------------------
.         CLDT0000  Clear old aae details file variables
.------------------------------------------------------------
CLDT0000  MOVE      SP8,ADADATE
          MOVE      SP8,ADATIME
          MOVE      SP3,ADACOMP
          MOVE      SP3,ADACLASS
          MOVE      SP3,ADASRC
          MOVE      SP3,ADAINSUR
          MOVE      SP3,ADASIT
          MOVE      SP3,ADAMODE
          MOVE      SP1,ADAFILL
          MOVE      SP3,ADAPREV
          MOVE      SP6,ADADOCT
          MOVE      SP5,ADASEEN
          MOVE      ZERO,ADAADMIT
          MOVE      SP2,ADALOCK
          MOVE      SP30,ADAEMPL
          PACK      ADAADD1,SP30,SP10
          PACK      ADAADD2,SP30,SP10
          PACK      ADASUBR,SP30,SP10
          MOVE      SP30,ADAPOST
          MOVE      SP30,ADATELE
          MOVE      SP30,ADACONT
.
          MOVE      SP3,ADAUSR1
          MOVE      SP3,ADAUSR2
          MOVE      SP3,ADAUSR3
          MOVE      SP3,ADAUSR4
          MOVE      SP3,ADAUSR5
          PACK      ADADIAG,SP30,SP30
          MOVE      SP5,ADAWAIT
          MOVE      SP8,ADASDTE
.
          MOVE      SP5,AEDARDOC
.
CLDT9999  RETURN
.------------------------------------------------------------
.         CLDS0000  Clear old aae discharge variables
.------------------------------------------------------------
CLDS0000  MOVE      SP8,ADSDATE
          MOVE      SP5,ADSTIME
          MOVE      SP7,ADSDIAG
          MOVE      SP3,ADSDISC
          PACK      ADSSSES,SP30,SP30,SP20
.
CLDS9999  RETURN
.------------------------------------------------------------
. Move cgi variables to emrvisaf fields
.------------------------------------------------------------
MVEMR000  MOVE      EMEVT001,EMVIDATE
          MOVE      EMEVT002,EMVITIME
          MOVE      EMEVT003,EMVIDOCT
          MOVE      EMEVT004,EMVICOMP
          MOVE      EMEVT007,EMVIDDAT
          MOVE      EMEVT008,EMVIDTIM
          MOVE      EMEVT009,EMVIDSTA
          MOVE      ADMISSNO,EMVIADMN
.
MVEMR999  RETURN
+
.------------------------------------------------------------
. Write to the current patient list
.------------------------------------------------------------
WRCPAT00  CALL      CALRAN00
.
          MATCH     STRTDATE,EMVIDATE        * before start date
          GOTO      WRCPAT99 IF LESS
.
          MATCH     EMVIDATE,LASTDATE        * after end date
          GOTO      WRCPAT99 IF LESS
.
          MOVE      DEMVIADM,PMCUVISN
          MOVE      EMVIDATE,PMCUDATE
          MOVE      PTMASNAM,PMCUSURN
          MOVE      PMPXFNAM,PMCUGNAM
          MOVE      PMPXSNAM,PMCUGNM2
          MOVE      PURNO,PMCUURNO
          MOVE      "1",PMCUSYST
          MOVE      SP70,PMCULOCN
          MOVE      SP70,PMCUOSIT
          MOVE      EMSTHSNO,PMCUHOSP
          MOVE      SP70,PMCUTSTA
          MOVE      SP70,PMCUTLOC
.
          PACK      KEY8,PMCUVISN,SP70
          CALL      RAPMCUR1
          IF        OVRCD=1
            CALL      WRPMCUR1
          ENDIF
.
WRCPAT99  RETURN
+
.------------------------------------------------------------
. Calculate the date range for current patient records
.------------------------------------------------------------
CALRAN00  READ      CONTROLF,HUND01;*49,CWEBSDAY  * no of days for search
          MOVE      SP70,STRTDATE
          MOVE      SP70,LASTDATE
.
. ---  calculate starting date ---
.
          CALL      IBACLOCK
          CALL      DMYCON
          SUB       CWEBSDAY,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON
          PACK      STRTDATE,CC,YY,MM,DD
          REP       " 0",STRTDATE
.
. ---  calculate last date ---
.
          CALL      IBACLOCK
          CALL      DMYCON
          ADD       CWEBSDAY,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON
          PACK      LASTDATE,CC,YY,MM,DD
          REP       " 0",LASTDATE
.
CALRAN99  RETURN
+
.------------------------------------------------------------
. Output Next Page Based on CGI Parameter nextpage
.------------------------------------------------------------
NXTPAG00  MOVE      ZERO,F1
          MOVE      NEXTPAGE,F1
          BRANCH    F1,NXTPAG10,NXTPAG20
.
          CALL      WINCLS00
          GOTO      NXTPAG99
.
NXTPAG10  CALL      RDIREC00
          GOTO      NXTPAG99
.
NXTPAG20  CALL      GOBACK00
          GOTO      NXTPAG99
.
NXTPAG99  RETURN
.-------------------------------------------------------------------------------
.  Goes back x number of pages
.-------------------------------------------------------------------------------
GOBACK00  CALL      OPENHTML
          BRANCH    EXIT,GOBACK99
          MOVE      NEXTPAGE,F1
          SUB       TWO,F1
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "    alert(#"",WEBTITLE,"#");":
.                            "    self.close();":
                             "    window.history.go(-",F1,");":
                             "}":
                             "</script>"
          CALL      CLOSHTML
GOBACK99  RETURN
.------------------------------------------------------------
PATCALF
GHSP0000
PRLN0000
NXTTRAN1
WAAE0000
VISERR    RETURN    * Dummy routine for AAECHRG
.------------------------------------------------------------
.**********************************************************************
.*  CHKD0000  - Check if there are any deposits for this visit if so  *
.*              display an error                                      *
.**********************************************************************
CHKD0000  MOVE      ZERO,EXIT
          MOVE      "0",CMDESTAT
          PACK      KEY26,CMDESTAT,ADMISSNO,SP70
          CALL      RSCMDEP2
CHKD1000  CALL      RKCMDEP2
          BRANCH    OVRCD,CHKD9999
.
          MATCH     "0",CMDESTAT
          GOTO      CHKD9999 IF NOT EQUAL
.
          MATCH     ADMISSNO,CMDEADMN            * Same outpatient booking
          GOTO      CHKD9999 IF NOT EQUAL
.
CHKD9000  MOVE      ONE,EXIT
          GOTO      CHKD9999

CHKD9999  RETURN
.
          INC       IBAWEBCD
          INC       TFILENAM
.
          INC       CALCAGE
          INC       AAECHRG
          INC       CLEMRVIS
          INC       CLMRTMAS
          INC       CLNHIMAS
          INC       CLPATRE1
          INC       DAYOFWEK
          INC       DGCLICAE
          INC       DGCLICEC
          INC       DGCLICCE
          INC       GENANVIS
          INC       PATDOCCD
          INC       PATIPERH
          INC       PATMASTM
          INC       PMSPX2TM
          INC       PATDOCTM
          INC       PATMSXTM
          INC       MRTVISCD
          INC       NAMSTRCD
          INC       WEBDATCD
          INC       WEBEMRCM
          INC       PATNAMCD
          INC       PMIGTNID
          INC       PMSHCGTM
          INC       PMSHCPTM
          INC       ABFAEINV
          INC       ABFAECCL
          INC       PRFAINSR
.
          INC       AAEDE1IO/INC
          INC       AAEDEBIO/INC
          INC       AAEDI1IO/INC
          INC       AAEDTRIO/INC
          INC       APSMASIO/INC
          INC       COMDEPIO/INC
          INC       EMRINCIO/INC            * Incomplete visit table
          INC       EMRSECIO/INC
          INC       EMRVISIO/INC
          INC       IBAPOSIO/INC
          INC       MRTMASIO/INC
          INC       MRTVISIO/INC
          INC       NHIETHIO/INC
          INC       NHIMASIO/INC
          INC       OPRNURIO/INC
          INC       OUTSITIO/INC
          INC       PATALRIO/INC
          INC       PATAM1IO/INC
          INC       PATCFAIO/INC
          INC       PMSUCHIO/INC
          INC       PMSUCDIO/INC
          INC       PATDO1IO/INC
          INC       PATFN1IO/INC
          INC       PATFX1IO/INC
          INC       PATFHIIO/INC
          INC       PATIN1IO/INC
          INC       PATINVIO/INC
          INC       PATIPEIO/INC
          INC       PATMA1IO/INC
.
          INC       PMSCCDIO/INC            * Concession Card Details
          INC       PMSCURIO/INC
          INC       PMSPX2IO/INC
          INC       PMSHCGIO/INC
          INC       PMSHCPIO/INC
          INC       PATMI1IO/INC
          INC       PATNIDIO/INC
          INC       PATPR1IO/INC
          INC       PATRE1IO/INC
          INC       PATVADIO/INC
          INC       PATVISIO/INC
          INC       PMSVX1IO/INC
          INC       PATWC1IO/INC
          INC       PATWMAIO/INC
          INC       PATWVEIO/INC
          INC       PATWR1IO/INC
          INC       PMSMTIIO/INC
          INC       PATHSPIO/INC
          INC       PATONHIO/INC
          INC       MRTLOCIO/INC
          INC       EMRSITIO/INC
. 

.*****************************************************************************
.*    Program       : IBAOUT82  -  QM08 Outpatients First Attendances        *
.*    Author        : ROD                                                    *
.*    Date          : 18/01/94                                               *
.*    Modifications :                                                        *
.*                                                                           *
.* V12.00.01 30/05/2025  Don Nguyen    TSK 0955096                           *
.*           Alphanumeric visit number changes                               *
.*****************************************************************************
.* V10.05.01 18/12/2014  Ebon Clements     CAR 261630                        *
.*           Removed port number from temp file name                         *
.* V10.04.01 22/01/2014  Don Nguyen    CAR 295972                            *
.*           Changed KEY16 to KEY24 for IO calls on patpa1af                 *
.*****************************************************************************
.* V9.03.01  11/09/2003  Sandra Barcham    43010                             *
.*           Changed open OUTPREA1 to NOT use site prefix                    *
.*****************************************************************************
.* V9.02.01  31/01/2003  Tracey Nguyen SRF 22709                             *
.*           Changed open OUTPREA1 to use site prefix                        *
.* V9.02.00  Ported from r5.10                                               *
.*****************************************************************************
.* V5.08.01  29/08/2000  Caleb Sharman                                       *
.*           Changed Health Fund variables to be 8 chars                     *
.* V5.07.B01 02/11/1998  Jim Stathopoulos                                    *
.*           507 Changes                                                     *
.*****************************************************************************
.*           V5.04.01 20/12/1996  Howellsy          EOC & ACC                *
.*                    Recompiled for OUTRF1FD                                *
.*           V5.01.02 09/08/1994  Paul Howells     SRF 441025                *
.*                    Include GP Referred DNA's in Wait columns.             *
.*                    Include Patient Cancellations in Wait columns.         *
.*           V5.01.03 02/03/1995  Max White        SRF 441207 / Quote 440053 *
.*                    1. Allow report to be produced by either District of   *
.*                       Residence or Purchaser sequence.                    *
.*                    2. Include Patients not seen at end of quarter figures *
.*                       in accordance with DSCN 73/94/P56.                  *
.*           V5.01.04 22/03/1995  Max White        SRF 441207 / Quote 440053 *
.*                    1. Use correct District of Residence as at             *
.*                       clinic date.                                        *
.*                    2. Add explanatory notes at end of report.             *
.*           V5.01.05 07/04/1995  Helen Webster    SRF 441263                *
.*                    Problem with MOVE ONE to XXTRADA,changes to ADD ONE    *
.*****************************************************************************
.
          INC       STDGENDF
.
.
          INC       IBASEQFD/INC
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
          INC       OUTBOCFD/INC
          INC       OUTCANFD/INC
          INC       OUTCONFD/INC
          INC       OUTOSFFD/INC
          INC       OUTPREFD/INC
          INC       OUTRAPFD/INC
          INC       OUTRF1FD/INC
          INC       OUTRSHFD/INC
.
          INC       PATCODFD/INC
          INC       PATDRGFD/INC
          INC       PATMA1FD/INC
          INC       PATPA1FD/INC
          INC       PATVISFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
ALL       INIT      "ALL                                           "
CANDATE   DIM       8
CDYSDAYS  FORM      6
CDYSFDTE  DIM       8
CDYSTDTE  DIM       8
CDYSYEAR  FORM      2
CLCDAYS   FORM      1
CMDLINE   DIM       70
CODE      DIM       2
CPTDATE   DIM       8
DNADATE   DIM       8
DOFR      DIM       3
DOFRDATE  DIM       8
EDATE     DIM       8
EDOR      DIM       3
EDORDESC  DIM       46
EPUR      DIM       5
EPURDESC  DIM       46
FINISH    INIT      "Finish"
FNAME     DIM       8
FORM3     FORM      3
FROMDATE  DIM       8
NOTSEEN   FORM      1             * 0 = calculate wait to 1st attendance
                                  * 1 = calculate wait to end of period
NOWEEKS   FORM      4
PRTFDESC  DIM       20
PRTFPER   DIM       7
PRTTDESC  DIM       20
PRTTPER   DIM       7
PRVRANGE  DIM       46
RECSREAD  FORM      8             * No. of records read
SAVFPER   DIM       6
SAVINDIC  DIM       1
SAVKEY28  DIM       28
SAVKEY8   DIM       8
SAVOUTNO  DIM       8
SAVRBKN   DIM       8
SAVSTAT   FORM      1
SAVTPER   DIM       6
SDATE     DIM       8
SDOR      DIM       3
SDORDESC  DIM       46
SPUR      DIM       5
SPURDESC  DIM       46
START     INIT      "Start"
SUMMARY   FORM      1
TODATE    DIM       8
.
TOTCOL1   FORM      6
TOTCOL2   FORM      6
TOTCOL3   FORM      6
TOTCOL4   FORM      6
TOTCOL5   FORM      6
TOTCOL6   FORM      6
TOTCOL7   FORM      6
TOTCOL8   FORM      6
TOTCOL9   FORM      6
TOTCOL10  FORM      6
TOTCOL11  FORM      6
TOTCOL12  FORM      6
.
TOVRCD    FORM      1
ZDOR      INIT      "zzz"
ZPUR      INIT      "zzzzz"
Z30       INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
.
. Temp file definition
.
TEMPFILE  IFILE     SQL, FIXED=119, KEY="U1-46,47-50"
XXRANGE   DIM       46     1      * DoR/Purchaser Range
XXHDPEQ   DIM       4     47      * Specialty (HDP Equivalent)
XXSPECD   DIM       20    51      * Specialty description
XXRRQGP   FORM      6     71      * No. Referral requests from GP
XXRRQOT   FORM      6     75      * No. Referral requests from Other
XXTRASN   FORM      6     89      * Total No. referral 1st atendances Seen
XXTRADA   FORM      6     82      * Total No. referral 1st atendances DNA's
XXLWA     FORM      6     87      * Length Wait 0-3 weeks   - 1st attendance
XXLWB     FORM      6     91      * Length Wait 4-12 weeks  - 1st attendance
XXLWC     FORM      6     95      * Length Wait 13-25 weeks - 1st attendance
XXLWD     FORM      6     99      * Length Wait 26+ weeks   - 1st attendance
XXLWE     FORM      6     102     * Length Wait 0-3 weeks   - not yet seen  
XXLWF     FORM      6     107     * Length Wait 4-12 weeks  - not yet seen  
XXLWG     FORM      6     111     * Length Wait 13-25 weeks - not yet seen  
XXLWH     FORM      6     115     * Length Wait 26+ weeks   - not yet seen  
.       End of record     119
.
PRGID     INIT      "IBAOUT82"
PRGNAM    INIT      "QM08 - Outpatients First Attendances"
VERSION   INIT      "V12.01.00"
+
.***********************************************************************
.*  ML0000  :  Mainline code                                           *
.***********************************************************************
ML0000    CALL      INIT0000                      * initialisation
.
ML0500    CALL      MENU0000                      * Program Menu
          BRANCH    EXIT,ML9999
.
ML1000    CALL      DSCR0000                      * Display Selection Screen
.
          CALL      KPER0000                      * Keyin period range
          BRANCH    EXIT,ML0500
.
          IF        MOPTION = 1
            CALL      KDOR0000                    * Keyin District
            BRANCH    EXIT,ML0500
          ENDIF
.
          CALL      CONTQST                       * Ok to Continue
          BRANCH    CEXIT,ML5000,ML1000,ML0500
.
ML5000    CALL      LOAD0000                      * load data into temp file
          CALL      RPRT0000                      * print report (from temp fil)
          GOTO      ML0500
.
ML9999    CHAIN     PGM
+
.***********************************************************************
.*  INIT0000  :  Initialisation                                        *
.***********************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P47:24,*EL,"Opening"
          DISPLAY   *P55:24,*EL,"controlf"
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,THIRTY1;*174,OTCNULET
.
.
          DISPLAY   *P55:24,*EL,"patdrgaf"
          OPEN      PATDRGA3,"patdrgaf"
.
          DISPLAY   *P55:24,*EL,"patcodes"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
.
          DISPLAY   *P55:24,*EL,"outbokaf"
          PACK      CFNAME,UID,FILBOKA1
          OPEN      OUTBOKA1,CFNAME
          OPEN      OUTBOKA3,CFNAME
.
          DISPLAY   *P55:24,*EL,"outbb1af"
          PACK      CFNAME,UID,FILBB1A1
          CALL      OPOTBB11
.
          DISPLAY   *P55:24,*EL,"outbokcf"
          PACK      CFNAME,UID,FILBOKC1     * Get the name of the BOKC1 file
          OPEN      OUTBOKC1,CFNAME
          OPEN      OUTBOKC2,CFNAME
.
          DISPLAY   *P55:24,*EL,"outrshaf"
          PACK      CFNAME,UID,FILRSHA1     * Get the name of the RSHA1 file
          OPEN      OUTRSHA1,CFNAME
.
          DISPLAY   *P55:24,*EL,"outcanaf"
          PACK      CFNAME,UID,FILCANA5     * Get the name of the CANA5 file
          OPEN      OUTCANA5,CFNAME
.
          DISPLAY   *P55:24,*EL,"outrapaf"
          PACK      CFNAME,UID,FILRAPA1     * Get the name of the RAPA1 file
          OPEN      OUTRAPA1,CFNAME
          PACK      CFNAME,UID,FILRAPA2     * Get the name of the RAPA2 file
          OPEN      OUTRAPA2,CFNAME
.
          DISPLAY   *P55:24,"outpreaf";
          OPEN      OUTPREA1,"outpreaf"
.
          DISPLAY   *P55:24,*EL,"outosfaf"
          OPEN      OUTOSFA3,"outosfaf"
.
          DISPLAY   *P55:24,*EL,"patvisaf"
          OPEN      PATVISA1,"patvisaf"
.
          IF        OTCNULET = 1
            DISPLAY   *P55:24,*EL,"outrf1af"
            OPEN      OUTRF1A6,"outrf1af"
          ENDIF
.
          DISPLAY   *P55:24,*EL,"patma1af"
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P55:24,*EL,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
          DISPLAY   *P55:24,*EL,"patpadaf";
          OPEN      PATPA1A1,"patpa1af"
.
          CALL      TFILENAM * Get temp file name in create
          MOVE      TFILNAME,FNAME
.
          MOVE      ZERO,RECSREAD                 * Initialise record counter
.
INIT9999  RETURN
+
.*****************************************************************************
.*                              MENU0000                                     *
.*                        Program Menu Routine                               *
.*****************************************************************************
MENU0000  MOVE      ZERO,EXIT
.
          HLINE     *G37,2,58,80
.
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = by District of Residence":
                    *P1:6,*V2LON,"2",*HOFF," = by Purchaser":
                    *P1:8,"Select Option ? "

MENU1000  KEYIN     *P17:8,*V2LON,MOPTION:
                    *P17:8,*DV,MOPTION
.
          BRANCH    MOPTION,MENU9999,MENU9999
.
          COMPARE   ZERO,MOPTION
          GOTO      MENU9000 IF EQUAL
.
          BEEP
          GOTO      MENU1000
.
MENU9000  MOVE      ONE,EXIT
.
MENU9999  RETURN
+                  
.*****************************************************************************
.*                              DSCR0000                                     *
.*                    display the Keyin Screen                               *
.*****************************************************************************
DSCR0000  IF        MOPTION = 1
            DISPLAY   *P58:2,*V2LON,"- by District "
          ELSE
            DISPLAY   *P58:2,*V2LON,"- by Purchaser "
          ENDIF
.
          DISPLAY   *P1:10,*EF:
                    *P1:10,"   Period From : ":
                    *P1:11,"   Period To   : ";
.
          IF        MOPTION = 1
            DISPLAY   *P1:13," District From : ":
                      *P1:14," District To   : " 
          ELSE
            DISPLAY   *P1:13,"Purchaser From : ":
                      *P1:14,"Purchaser To   : " 
          ENDIF
.
DSCR9999  RETURN
+
.*************************************************************************
.*                             KPER0000            Called by: ML0000     *
.*                         keyin a period                                *
.*************************************************************************
KPER0000
.
. set up defaults for routine KEYPER (PATCOMN3)
.
KPER1000  MOVE      "10",CVERT
          MOVE      "18",CCOL
          MOVE      "24",EVERT
          MOVE      "1",ECOL
          MOVE      ZERO,CHIGHLT
          UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
.
          CALL      KEYPER                        * keyin a period
          BRANCH    CQUEST,KPER1000               * dont allow "?"
          BRANCH    OVRCD,KPER9000                * nothing input
.
          PACK      KEY6,ICENT,IYEAR,IMON
          REP       " 0",KEY6
          CALL      RDDRGA3                       * on file?
          BRANCH    OVRCD,KPER7000
.
          PACK      SAVFPER,ICENT,IYEAR,IMON      * save the period keyed in
          REP       " 0",SAVFPER
.
          MOVE      DRGFDTE,FROMDATE
          PACK      PRTFPER,IMON,SLASH,ICENT,IYEAR * format period for printing
          REP       " 0",PRTFPER
          MOVE      DRGLDSC,PRTFDESC              * save period desc for print
          DISPLAY   *P28:10,DRGLDSC               * display description
.
. now keyin Period TO
.
KPER5000  MOVE      "11",CVERT
          MOVE      "18",CCOL
          MOVE      "24",EVERT
          MOVE      "1",ECOL
          MOVE      ZERO,CHIGHLT
.
          MOVE      IMON,FORM3
          COMPARE   TEN1,FORM3
          GOTO      KPER5100 IF NOT LESS
.
          ADD       TWO,FORM3
          MOVE      FORM3,IMON
KPER5100  PACK      KEY8,ICENT,IYEAR,IMON,SP2
          UNPACK    KEY8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",CMON
.
          CALL      KEYPER                        * keyin a period
          BRANCH    CQUEST,KPER5000               * dont allow "?"
.
          PACK      KEY6,ICENT,IYEAR,IMON
          REP       " 0",KEY6
          CALL      RDDRGA3                       * on file?
          BRANCH    OVRCD,KPER7000
.
          PACK      SAVTPER,ICENT,IYEAR,IMON      * save the period keyed in
          REP       " 0",SAVTPER
.
          MOVE      DRGTDTE,TODATE
          PACK      PRTTPER,IMON,SLASH,ICENT,IYEAR * format period for printing
          REP       " 0",PRTTPER
          MOVE      DRGLDSC,PRTTDESC               * save period desc for print
          DISPLAY   *P28:11,DRGLDSC                * display description
.
          MATCH     SAVFPER,SAVTPER
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Invalid range.  ";
            CALL      HITENTER
            GOTO      KPER0000
          ENDIF
          GOTO      KPER8500
.
KPER7000  DISPLAY   *P1:24,*EL,*B,"Invalid Period.  ";
          CALL      HITENTER
          GOTO      KPER0000
.
KPER8500  MOVE      ZERO,EXIT                     * period entered
          GOTO      KPER9999
.
KPER9000  MOVE      ONE,EXIT                      * nothing input or Cancel
.
KPER9999  RETURN
+
.*****************************************************************************
.*                              KDOR0000                                     *
.*                   Keyin the District of Residence Range                   *
.*****************************************************************************
.
. --- District of Residence From ---
.
KDOR0000  MOVE      ZERO,EXIT
.
          MOVE      ZERO,CKYIMAND
          MOVE      TEN3,EVERT
          MOVE      "18",ECOL
          DISPLAY   *PECOL:EVERT,SP6
.
          MOVE      ONE,CNEWDS
          MOVE      SP20,CKYIDEF3
          PACK      CODE,ANSD,ANSA
          CALL      PATCODKY
          IF        EXIT = 2
            MOVE      ONE,EXIT
            GOTO      KDOR9999
          ENDIF
.
          IF        EXIT = 1
            DISPLAY   *PECOL:EVERT,*EL,*V2LON,START
            MOVE      SP3,SDOR
            PACK      SDORDESC,START,SP30,SP30
            GOTO      KDOR5000
          ENDIF
.
          MOVE      ACODE,SDOR
          DISPLAY   *P28:EVERT,TDESC
          PACK      SDORDESC,SDOR,SP2,TDESC
.
. --- District of Residence To ---
.
KDOR5000  MOVE      ZERO,EXIT
.
          MOVE      TEN4,EVERT
          DISPLAY   *PECOL:EVERT,*EL
          CALL      PATCODKY
          IF        EXIT = 2
            MOVE      ONE,EXIT
            GOTO      KDOR9999
          ENDIF
.
          IF        EXIT = 1
            DISPLAY   *PECOL:EVERT,*EL,*V2LON,FINISH
            MOVE      ZDOR,EDOR
            MOVE      ZERO,EXIT
            PACK      EDORDESC,FINISH,SP30,SP30
            GOTO      KDOR8000
          ENDIF
.
          MOVE      ACODE,EDOR
          DISPLAY   *P28:EVERT,TDESC
          PACK      EDORDESC,EDOR,SP2,TDESC
.
          MATCH     SDOR,EDOR
          IF        @LESS
            DISPLAY   *P1:24,*EL,"District Codes Must be in Sequence. ";
            CALL      HITENTER
            GOTO      KDOR0000
          ENDIF
.
KDOR8000  MOVE      ZERO,SUMMARY
.
          MATCH     SP3,SDOR
          GOTO      KDOR9999 IF NOT EQUAL
.
          MATCH     ZDOR,EDOR
          GOTO      KDOR9999 IF NOT EQUAL
.
          MOVE      ONE,SUMMARY                   * Printing all of range
.
KDOR9999  RETURN
+
.***********************************************************************
.*  GREF0000  :  Get Referral Date from outbokcf                       *
.***********************************************************************
GREF0000
          PACK      KEY16,FROMDATE,SP20
          CALL      RSOTBOC2
GREF1000  CALL      RKOTBOC2
          BRANCH    OVRCD,GREF9999
.
          ADD       ONE,RECSREAD
          IF        (RECSREAD % 10) = 0
            DISPLAY   *P60:24,OBAOUTNO
          ENDIF
.
          MATCH     OTBCRDAT,TODATE
          GOTO      GREF9999 IF LESS
.
.         get the boka record for this booking No.
.
          PACK      KEY8,OBAOUTNO
          CALL      RDPREA1
          IF        OVRCD=0
            PACK      KEY28,OPRSITE,OPRCLIN,OPRDATE,OPRSTRT,OPRSLOT
            CALL      RDBOKA1
            BRANCH    OVRCD,GREF1000
          ELSE
            PACK      KEY8,OBAOUTNO
            CALL      RDVISA1
            BRANCH    OVRCD,GREF1000
.
            PACK      KEY36,PVIURNO,PVIDATE,SP30
            CALL      RDSBOKA3
GREF2000    CALL      RDKBOKA3
            BRANCH    OVRCD,GREF1000
.
            MATCH     PVIURNO,OBAURNO
            GOTO      GREF1000 IF NOT EQUAL
            MATCH     PVIDATE,OBADATE
            GOTO      GREF1000 IF NOT EQUAL
.
            MATCH     PVIBILL,OBAOUTNO
            GOTO      GREF2000 IF NOT EQUAL
          ENDIF
.
.         now check if it is a NEW appointment/attendance
.
          PACK      KEY5,ANSC,ANSV,OBAVISIT
          CALL      RDCODE1
          BRANCH    OVRCD,GREF1000
.
          MATCH     "N",TCINDC1
          GOTO      GREF1000 IF NOT EQUAL
.
.         now check DoR or Purchaser
.
          IF        SUMMARY = 1
            MOVE      ALL,XXRANGE                 * Printing all of range
            GOTO      GREF5000
          ENDIF
.
          IF        MOPTION = 1
            CALL      CDOR0000                    * Check District of Residence
            BRANCH    EXIT,GREF1000
          ENDIF
.
. We have a problem with DNA's as there is a record in outbokcf for the 
. DNA'd booking as well as a record for the Reappointment. Although in theory
. there is only one referral. Therefore only use the original booking.
.
. Only DNA reappointment's can have a New visit type
.
GREF5000  PACK      KEY16,OBAOUTNO,SP20
          CALL      RSOTRAP2
          CALL      RKOTRAP2
          IF        OVRCD = 0
            MATCH     OBAOUTNO,OTRPRBKN       * same reappointment
            GOTO      GREF1000 IF EQUAL
          ENDIF
.
.         get specialty from bokb record
.
          PACK      KEY8,OBAOUTNO
          CALL      RDBOKB1
          BRANCH    OVRCD,GREF1000
.
          PACK      KEY5,ANSS,ANSR,OBASRCR
          CALL      RDCODE1
          BRANCH    OVRCD,GREF1000
          MOVE      TCINDC1,SAVINDIC
.
          PACK      KEY5,ANSA,SP1,OBACLASS
          CALL      RDCODE1
          BRANCH    OVRCD,GREF1000
.
          MOVE      THCSCOD,XXHDPEQ               * HDP Equivalent
          MOVE      TDESC,XXSPECD                 * Specialty description
.
.         now post a record to the temp file
.
          CALL      IREC0000                      * initialise record totals
.
          PACK      KEY50,XXRANGE,XXHDPEQ
          CALL      RDTEMP1
          MOVE      OVRCD,TOVRCD
.
          MATCH     "G",SAVINDIC                  * referred by GP?
          IF        @EQUAL
            ADD       ONE,XXRRQGP                 * referred by GP
          ELSE
            ADD       ONE,XXRRQOT                 * referred Other
          ENDIF
.
. write/update temp file
.
          IF        TOVRCD=1
            CALL      WRTEMP1
          ELSE
            CALL      UPTEMP1
          ENDIF
          GOTO      GREF1000                      * get next record
.
GREF9999  RETURN
+
.***********************************************************************
.*  GLET0000  :  Get Referral Date from referral file                  *
.***********************************************************************
GLET0000
          PACK      KEY16,FROMDATE,SP20
          CALL      RSOTRFL6
GLET1000  CALL      RKOTRFL6
          BRANCH    OVRCD,GLET9999
.
          ADD       ONE,RECSREAD
          IF        (RECSREAD % 10) = 0
            DISPLAY   *P60:24,OTRLOUTN
          ENDIF
.
          MATCH     OTRLDATE,TODATE
          GOTO      GLET9999 IF LESS
.
.         get the boka record for this booking No.
.
          PACK      KEY8,OTRLOUTN
          CALL      RDPREA1
          IF        OVRCD=0
            PACK      KEY28,OPRSITE,OPRCLIN,OPRDATE,OPRSTRT,OPRSLOT
            CALL      RDBOKA1
            BRANCH    OVRCD,GLET1000
          ELSE
            PACK      KEY8,OTRLOUTN
            CALL      RDVISA1
            BRANCH    OVRCD,GLET1000
.
            PACK      KEY36,PVIURNO,PVIDATE,SP30
            CALL      RDSBOKA3
GLET2000    CALL      RDKBOKA3
            BRANCH    OVRCD,GLET1000
.
            MATCH     PVIURNO,OBAURNO
            GOTO      GLET1000 IF NOT EQUAL
            MATCH     PVIDATE,OBADATE
            GOTO      GLET1000 IF NOT EQUAL
.
            MATCH     PVIBILL,OBAOUTNO
            GOTO      GLET2000 IF NOT EQUAL
          ENDIF
.
.         now check if it is a NEW appointment/attendance
.
          PACK      KEY5,ANSC,ANSV,OBAVISIT
          CALL      RDCODE1
          BRANCH    OVRCD,GLET1000
.
          MATCH     "N",TCINDC1
          GOTO      GLET1000 IF NOT EQUAL
.
.         now check DoR or Purchaser
.
          IF        SUMMARY = 1
            MOVE      ALL,XXRANGE                 * Printing all of range
            GOTO      GLET5000
          ENDIF
.
          IF        MOPTION = 1
            CALL      CDOR0000                    * Check District of Residence
            BRANCH    EXIT,GLET1000
          ENDIF
.
.         get specialty from bokb record
.
GLET5000  PACK      KEY8,OBAOUTNO
          CALL      RDBOKB1
          BRANCH    OVRCD,GLET1000
.
          PACK      KEY5,ANSS,ANSR,OBASRCR
          CALL      RDCODE1
          BRANCH    OVRCD,GLET1000
          MOVE      TCINDC1,SAVINDIC
.
          PACK      KEY5,ANSA,SP1,OBACLASS
          CALL      RDCODE1
          BRANCH    OVRCD,GLET1000
.
          MOVE      THCSCOD,XXHDPEQ               * HDP Equivalent
          MOVE      TDESC,XXSPECD                 * Specialty description
.
.         now post a record to the temp file
.
          CALL      IREC0000                      * initialise record totals
.
          PACK      KEY50,XXRANGE,XXHDPEQ
          CALL      RDTEMP1
          MOVE      OVRCD,TOVRCD
.
          MATCH     "G",SAVINDIC                  * referred by GP?
          IF        @EQUAL
            ADD       ONE,XXRRQGP                 * referred by GP
          ELSE
            ADD       ONE,XXRRQOT                 * referred Other
          ENDIF
.
. write/update temp file
.
          IF        TOVRCD=1
            CALL      WRTEMP1
          ELSE
            CALL      UPTEMP1
          ENDIF
          GOTO      GLET1000                      * get next record
.
GLET9999  RETURN
.***********************************************************************
.*  LOAD0000  :  Load data into temp file                              *
.*                                                                     *
.*  1. Get all referral requests in the specified period.              *
.*  2. Get all attendances in the period.                              *
.*     - outbokaf attendances & DNA's                                  *
.*  3. Get all patients still waiting at end of period.                *
.*     - outbokaf bookings, attendances & DNA's who were waiting at    *
.*       the end of the period. NOTE: These figures will exclude       *
.*       any patients who were waiting at the end of the period,       *
.*       and cancelled before the report was run.                      *
.*       There is not enough information in outcanaf to include these, *
.*       and there SHOULD only be a negligable number of them.         *
.***********************************************************************
LOAD0000  DISPLAY   *P1:24,*EL,*P30:24,"Loading...";
.
          CALL      CRET0000                      * create temp file
.
. --- firstly get all referral requests in the specified period ---
.
          DISPLAY   *P42:24,"Appointments"
          IF        OTCNULET = 1
            CALL      GLET0000                    * using referral letters file
          ELSE
            CALL      GREF0000                    * using outbokcf     
          ENDIF
.
. --- secondly, get all attendances in the period ---
.
          MOVE      ZERO,NOTSEEN                  * get 1st attendance figures
.
LOAD1000  DISPLAY   *P42:24,*EL,"Attendances"
          PACK      KEY34,FROMDATE,SP30
          CALL      RSOTOSF3
LOAD2000  CALL      RKOTOSF3
          BRANCH    OVRCD,LOAD9999
.
          ADD       ONE,RECSREAD
          IF        (RECSREAD % 10) = 0
            DISPLAY   *P54:24,OTOSSESS,SP1,OTOSDATE
          ENDIF
.
. --- finally, get all patients not yet seen at end of period
.
          COMPARE   ONE,NOTSEEN
          GOTO      LOAD2900 IF EQUAL             * already getting not yet seen
.
          MATCH     OTOSDATE,TODATE
          IF        @LESS
            MOVE      ONE,NOTSEEN                 * get not yet seen figures  
            DISPLAY   *P42:24,*EL,"Waiting"
          ENDIF
.
. --- loop thru boka file to get bookings/attendances in period ---
. 
LOAD2900  PACK      KEY28,OTOSSITE,OTOSCLID,OTOSDATE,OTOSTIME,SP30
          CALL      RDSBOKA1
LOAD3000  CALL      RDKBOKA1
          BRANCH    OVRCD,LOAD2000
.
          ADD       ONE,RECSREAD
          IF        (RECSREAD % 10) = 0
            DISPLAY   *P71:24,OBAOUTNO;
          ENDIF
.
          PACK      KEY25,OBASITE,OBACLIN,OBADATE,OBASTRT
          MATCH     KEY25,KEY28
          GOTO      LOAD2000 IF NOT EQUAL
.
. --- must be booked/attended/DNA'd in period ---
.
          COMPARE   ZERO,NOTSEEN                  * attendance waiting times?
          GOTO      LOAD31000 IF EQUAL
.
          COMPARE   ONE,OBASTAT                   * booked ?
          GOTO      LOAD3500 IF EQUAL
.
LOAD3100  COMPARE   FOUR,OBASTAT                  * attended/DNA'd ?
          GOTO      LOAD3000 IF LESS 
.
LOAD3500  MOVE      ZERO,CLCDAYS
.
. --- must have been referred ---
.
          PACK      KEY8,OBAOUTNO                 * check if a referral
          CALL      RDOTBOC1
          BRANCH    OVRCD,LOAD3000
.
. --- must have been a NEW attendance ---
.
          PACK      KEY5,ANSC,ANSV,OBAVISIT
          CALL      RDCODE1
          BRANCH    OVRCD,LOAD3000
.
          MATCH     "N",TCINDC1
          GOTO      LOAD3000 IF NOT EQUAL
.
. --- now check DoR or Purchaser ---
.
          IF        SUMMARY = 1
            MOVE      ALL,XXRANGE                 * Printing all of range
            GOTO      LOAD3900
          ENDIF
.
          IF        MOPTION = 1
            CALL      CDOR0000                    * Check District of Residence
            BRANCH    EXIT,LOAD3000
          ENDIF
.
. --- get specialty from bokb record ---
.
LOAD3900  PACK      KEY8,OBAOUTNO
          CALL      RDBOKB1
          BRANCH    OVRCD,LOAD3000
.
          PACK      KEY5,ANSA,SP1,OBACLASS
          CALL      RDCODE1
          BRANCH    OVRCD,LOAD3000
.
          MOVE      THCSCOD,XXHDPEQ               * HDP Equivalent
          MOVE      TDESC,XXSPECD                 * Specialty description
.
. --- now post a record to the temp file ---
.
          CALL      IREC0000                      * initialise record totals
.
          PACK      KEY50,XXRANGE,XXHDPEQ
          CALL      RDTEMP1
          MOVE      OVRCD,TOVRCD
.
. --- to report waiting days, the booking must be referred by a GP ---
.
          PACK      KEY5,ANSS,ANSR,OBASRCR
          CALL      RDCODE1
          IF        OVRCD = 0
            MATCH     ANSG,TCINDC1              * must be GP referral
            IF        @EQUAL
              MOVE      ONE,CLCDAYS             * calc days wait
            ENDIF
          ENDIF
.
          COMPARE   ONE,NOTSEEN                   * not yet seen figures ?
          GOTO      LOAD4000 IF EQUAL
....
.... --- for Seen & DNA columns the referral date must be in period ---
....
...          MATCH     FROMDATE,OTBCRDAT
...          GOTO      LOAD4000 IF LESS
....
...          MATCH     OTBCRDAT,TODATE
...          GOTO      LOAD4000 IF LESS
.
. ******* DNA ****************************
.
          COMPARE   FIVE,OBASTAT                * DNA?
          IF        @EQUAL
            ADD       ONE,XXTRADA
            GOTO      LOAD9000
          ENDIF
.
. ******* Attended ************************
.
          ADD       ONE,XXTRASN                 * Attendance
.
. --- set the dates the patient has been waiting ---
.
LOAD4000  IF        NOTSEEN = 0
            MOVE      OBADATE,EDATE             * end date = booking date 
          ELSE
            MOVE      TODATE,EDATE              * end date = period end date 
          ENDIF
.
          CALL      GDNA0000                    * Check if DNA  
.
          CALL      GCAN0000                    * Check if Patient Cancellation
.
. The idea is to get the hospitals waiting times down, so use the latest date
.         
          MATCH     DNADATE,CANDATE
          IF        @LESS
            MATCH     DNADATE,OTBCRDAT
            IF        @LESS
              MOVE      DNADATE,SDATE
            ELSE
              MOVE      OTBCRDAT,SDATE          * referral date = start wait
            ENDIF
          ELSE
            MATCH     CANDATE,OTBCRDAT
            IF        @LESS
              MOVE      CANDATE,SDATE
            ELSE
              MOVE      OTBCRDAT,SDATE          * referral date = start wait
            ENDIF
          ENDIF
.
. --- Ensure not yet seen figures only include those who were waiting ---
.     at the end of the period
.
          COMPARE   ZERO,NOTSEEN                  * getting attendance waits ?
          GOTO      LOAD7900 IF EQUAL             * yes, continue
.
          MATCH     SDATE,TODATE                  * started waiting after period
          GOTO      LOAD3000 IF LESS              * yes, ignore
.
LOAD7900  MATCH     SP8,SDATE
          GOTO      LOAD9000 IF EQUAL
.
. --- write/update temp file ---
.
LOAD8000  IF        CLCDAYS = ONE
            CALL      CDAY0000                    * calc days wait
          ENDIF
.
LOAD9000  IF        TOVRCD=1
            CALL      WRTEMP1
          ELSE
            CALL      UPTEMP1
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      LOAD3000                      * get next record
.
LOAD9999  RETURN
+
.***********************************************************************
.*  GCAN0000  :  Check if Patient Cancelled the Booking                *
.***********************************************************************
GCAN0000  MOVE      SP8,CANDATE
          PACK      KEY11,OBAOUTNO,SP20
          CALL      RDSRSHA1
GCAN1000  CALL      RDKRSHA1
          BRANCH    OVRCD,GCAN9999
.
          MATCH     OBAOUTNO,OPRSOUTN
          GOTO      GCAN9999 IF NOT EQUAL
.
          MATCH     OBACLIN,OPRSNCLI
          GOTO      GCAN1000 IF NOT EQUAL
.
          MATCH     OBADATE,OPRSNDAT
          GOTO      GCAN1000 IF NOT EQUAL
.
          MATCH     OBASTRT,OPRSNSTR 
          GOTO      GCAN1000 IF NOT EQUAL
. 
          COMPARE   OBASLOT,OPRSSLOT 
          GOTO      GCAN1000 IF NOT EQUAL
.
          MATCH     OBATIME,OPRSNTIM
          GOTO      GCAN1000 IF NOT EQUAL
.
          PACK      KEY5,ANSR,ANSR,OPRSREAS       * Reschedule Reason
          CALL      RDCODE1
          BRANCH    OVRCD,GCAN1000
.
          MATCH     "P",TCINDC1                   * Patient Cancellation?
          GOTO      GCAN1000 IF NOT EQUAL
.
. We have a patient cancellation
.
          MATCH     OPRSDATE,FROMDATE             * before period
          GOTO      GCAN9999 IF LESS
.
          MOVE      OPRSDATE,CANDATE
.
.
GCAN9999  RETURN
.***********************************************************************
.*  GDNA0000  :  Get the End Date for DNA's                            *
.***********************************************************************
.
. Check if the attendance was originally DNA'd. 
.
GDNA0000
          MOVE      OBAOUTNO,SAVRBKN
          MOVE      OBAOUTNO,SAVOUTNO
          MOVE      SP8,DNADATE
          PACK      KEY16,OBAOUTNO,SP20
GDNA1000  CALL      RSOTRAP2
          CALL      RKOTRAP2
          BRANCH    OVRCD,GDNA9000
.
          MATCH     SAVRBKN,OTRPRBKN       * same reappointment
          GOTO      GDNA9000 IF NOT EQUAL
.
. patient attendance is a reappointment
.
. check to see if original booking was DNA'd
.
          PACK      KEY8,OTRPOBKN,SP10
          CALL      RDBOKB1
          BRANCH    OVRCD,GDNA5000
.
          PACK      KEY5,ANSO,ANSZ,OTBBOUTC
          CALL      RDCODE1
          BRANCH    OVRCD,GDNA5000
.
          MATCH     "1",TCINDC1
          GOTO      GDNA5000 IF NOT EQUAL
.
          MATCH     FROMDATE,OBADATE         * before period
          GOTO      GDNA5000 IF LESS
.
          MOVE      OBADATE,DNADATE
.
          MOVE      ZERO,EXIT
          GOTO      GDNA9000
.
GDNA5000  PACK      KEY16,OTRPOBKN,SP20
          MOVE      OTRPOBKN,SAVRBKN
          GOTO      GDNA1000
.
GDNA9000  PACK      KEY8,SAVOUTNO,SP20
          CALL      RDBOKB1                 * restore file posy
.
GDNA9999  RETURN
.***********************************************************************
.*  CDAY0000  :  Calculate Days Wait                                   *
.*               Parameters : SDATE - start date                       *
.*                            EDATE - to date                          *
.***********************************************************************
CDAY0000
.
            MOVE      SDATE,CDYSFDTE
            MOVE      EDATE,CDYSTDTE
            CALL      CALCDAYS
.
            SUBTRACT  ONE,CDYSDAYS                * get difference between dates
.
            ASSIGN    (CDYSDAYS/7),NOWEEKS        * get No. weeks wait
.
            IF        NOWEEKS>=26
              IF        NOTSEEN = 0
                ADD       ONE,XXLWD               * 26+ to 1st attendance
              ELSE
                ADD       ONE,XXLWH               * 26+ not yet seen
              ENDIF
              GOTO      CDAY9500
            ENDIF
.
            IF        NOWEEKS>=13
              IF        NOTSEEN = 0
                ADD       ONE,XXLWC               * 13-25 to 1st attendance
              ELSE
                ADD       ONE,XXLWG               * 13-25 not yet seen
              ENDIF
              GOTO      CDAY9500
            ENDIF
.
            IF        NOWEEKS>=4
              IF        NOTSEEN = 0
                ADD       ONE,XXLWB               * 4-12 to 1st attendance
              ELSE
                ADD       ONE,XXLWF               * 4-12 not yet seen
              ENDIF
            ELSE
              IF        NOTSEEN = 0
                ADD       ONE,XXLWA               * 0-3 to 1st attendance
              ELSE
                ADD       ONE,XXLWE               * 0-3 not yet seen
              ENDIF
            ENDIF
.
CDAY9000  MOVE      ZERO,EXIT
          GOTO      CDAY9999
.
CDAY9500  MOVE      ONE,EXIT
          GOTO      CDAY9999
.
CDAY9999  RETURN
+
.****************************************************************************
.*                        CDOR0000                                          *
.*     Check To make sure District of Residence matches if MOPTION = 1      *
.****************************************************************************
CDOR0000  MOVE      ZERO,EXIT
.
          MOVE      OBAURNO,KEY8                  * Read Master Patient File
          CALL      RDMAST1
          BRANCH    OVRCD,CDOR1000                * Record Not Found
.
.------   Get current district of residence for outpatient attendance
.
          CALL      GDOR0000
.
.------   Is patient within the District of Residence range ?
.
          MATCH     SDOR,DOFR
          GOTO      CDOR1000 IF LESS
.
          MATCH     DOFR,EDOR
          GOTO      CDOR1000 IF LESS
.
          PACK      KEY5,ANSD,ANSA,DOFR
          CALL      RDCODE1
          BRANCH    OVRCD,CDOR1000
.
          PACK      XXRANGE,ACODE,SP2,TDESC,SP30,SP30
.
          GOTO      CDOR9999                 * Valid Record
.
CDOR1000  MOVE      ONE,EXIT
.
CDOR9999  RETURN
+
.******************************************************************************
.* GDOR0000   : Get the district of residence                                 *
.* Called by  : CDOR0000                                                      *
.* Parameters : PTMXDOFR - District of Residence Code                         *
.*              OBADATE  - Outpatient Clinic Date                             *
.* Returns    : DOFR     - District of residence (blank if none found)        *
.******************************************************************************
GDOR0000  
.
. get district of residence current at outpatient attendance date
.
          MOVE      OBADATE,DOFRDATE        * reformat to YYMMDD format
.
. see if district has been changed - patpa1af
.
          MOVE      SP3,DOFR
          PACK      KEY24,OBAURNO,Z30
          CALL      RDSPADD1
GDOR1000  CALL      RDPPADD1
          BRANCH    OVRCD,GDOR4000
.
          MATCH     OBAURNO,PAURNO
          GOTO      GDOR4000 IF NOT EQUAL
.
          MATCH     PADATE,DOFRDATE
          IF        @LESS
            MOVE      PTPADOR,DOFR
            GOTO      GDOR1000
          ENDIF
.
GDOR4000  MATCH     SP3,DOFR                 * changed district found ?
          GOTO      GDOR9999 IF NOT EQUAL    * yes, use this one
.
. use current district of residence (from PMI)
.
          MOVE      PTMXDOFR,DOFR
.
GDOR9999  RETURN
+
.***********************************************************************
.*  RPRT0000  :  Print Report                                          *
.***********************************************************************
RPRT0000  DISPLAY   *P1:24,*EL,*P35:24,"Printing...";
.
          CALL      IBACLOCK
          MOVE      ZERO,CPAGENO
          MOVE      ONE,CNOUNDLN
          PACK      PRVRANGE,SP30,SP30
.
          CALL      ITOT0000                      * initialise totals
.
          PACK      KEY50,SP30,SP30
          CALL      RSTEMP1  
RPRT0500  CALL      RKTEMP1                       * get next record
          BRANCH    OVRCD,RPRT9000                * no more records
.
. check if district or purchaser has changed
.
          MATCH     PRVRANGE,XXRANGE
          IF        !@EQUAL
            CALL      PTOT0000                    * print group totals
            MOVE      SIXTY,CLNO                  * force new page
          ENDIF
.
. check if we need a new page
.
          COMPARE   "52",CLNO
          GOTO      RPRT5000 IF LESS
.
. we need a new page
.
          CALL      PAGE0000                      * print page header
          MOVE      XXRANGE,PRVRANGE              * save DoR/Purchaser
.
. print a line
.
RPRT5000  PRINT     *1,"| ",XXSPECD,SP1,XXHDPEQ:
                    *30,"|",*33,XXRRQGP,SP2,XXRRQOT:
                    *49,"|",*52,XXTRASN,SP2,XXTRADA:
                    *68,"|",*71,XXLWA,SP1,XXLWB,SP1,XXLWC,SP1,XXLWD:
                    *100,"|",*103,XXLWE,SP1,XXLWF,SP1,XXLWG,SP1,XXLWH:
                    *132,"|"
.
          ADD       ONE,CLNO
.
. increment totals
.
          ADD       XXRRQGP,TOTCOL1
          ADD       XXRRQOT,TOTCOL2
          ADD       XXTRASN,TOTCOL3
          ADD       XXTRADA,TOTCOL4
          ADD       XXLWA,TOTCOL5
          ADD       XXLWB,TOTCOL6
          ADD       XXLWC,TOTCOL7
          ADD       XXLWD,TOTCOL8
          ADD       XXLWE,TOTCOL9
          ADD       XXLWF,TOTCOL10
          ADD       XXLWG,TOTCOL11
          ADD       XXLWH,TOTCOL12
.  
          GOTO      RPRT0500
.
. no more records
.
RPRT9000  COMPARE   ZERO,CPAGENO
          GOTO      RPTR9500 IF NOT EQUAL
.
          MOVE      ALL,XXRANGE
          CALL      PAGE0000                      * print page header
.
RPTR9500  CALL      PTOT0000                      * print totals
.
          COMPARE   "48",CLNO
          GOTO      RPTR9600 IF LESS
.
          CALL      PAGE0000
.
RPTR9600  PRINT     *N,*1,"Notes: (1). No. of referral requests include ":
                          "all requests for a first outpatient attendance ":
                          "that were referred during the period.":
                    *N,*1,"       (2). No. referral attendances include all ":
                          "attendances and DNA's for first outpatient ":
                          "appointments during the period.":
                    *N,*1,"       (3). Length of wait totals include GP ":
                          "written referral first attendances during ":
                          "the period.":
                    *N,*1,"       (4). No. not yet seen totals include GP ":
                          "written referral first attendance requests ":
                          "still waiting at the end of the period."
          PRINT     *N,*1,"*** End of report ***"
.
          CALL      KILL0000                      * erase temp file
.
RPRT9999  RETURN
+
.*************************************************************************
.*  ITOT0000  :  initialise totals                                       *
.*************************************************************************
ITOT0000  MOVE      ZERO,TOTCOL1
          MOVE      ZERO,TOTCOL2
          MOVE      ZERO,TOTCOL3
          MOVE      ZERO,TOTCOL4
          MOVE      ZERO,TOTCOL5
          MOVE      ZERO,TOTCOL6
          MOVE      ZERO,TOTCOL7
          MOVE      ZERO,TOTCOL8
          MOVE      ZERO,TOTCOL9
          MOVE      ZERO,TOTCOL10
          MOVE      ZERO,TOTCOL11
          MOVE      ZERO,TOTCOL12
.
ITOT9999  RETURN
+
.*************************************************************************
.*  IREC0000  :  initialise record area totals                           *
.*************************************************************************
IREC0000  MOVE      ZERO,XXRRQGP
          MOVE      ZERO,XXRRQOT
          MOVE      ZERO,XXTRASN
          MOVE      ZERO,XXTRADA
          MOVE      ZERO,XXLWA
          MOVE      ZERO,XXLWB
          MOVE      ZERO,XXLWC
          MOVE      ZERO,XXLWD
          MOVE      ZERO,XXLWE
          MOVE      ZERO,XXLWF
          MOVE      ZERO,XXLWG
          MOVE      ZERO,XXLWH
.
IREC9999  RETURN
+
.*************************************************************************
.*  PAGE0000  :  Print a new page                                        *
.*************************************************************************
PAGE0000  CALL      HEAD132
.
          STRIP     PRTFPER
          STRIP     PRTFDESC
          STRIP     PRTTPER
          STRIP     PRTTDESC
.
          IF        MOPTION = 1
            STRIP     SDORDESC
            STRIP     EDORDESC
          ELSE
            STRIP     SPURDESC
            STRIP     EPURDESC
          ENDIF
.
          PRINT     *40,*+,"Period From : ",PRTFPER,SP2,PRTFDESC," To ":
                                            PRTTPER,SP2,PRTTDESC
.
          IF        MOPTION = 1
            PRINT     *25,*+,"District of Residence From : ",SDORDESC," To ":
                                                             EDORDESC,*N
          ELSE
            PRINT     *37,*+,"Purchaser From : ",SPURDESC," To ":
                                                 EPURDESC,*N
          ENDIF
.
          IF        MOPTION = 1
            PRINT     *1,"District of Residence : ",XXRANGE,*N 
          ELSE
            PRINT     *1,"Purchaser : ",XXRANGE,*N 
          ENDIF
.
          CALL      UND132
          PRINT     *1,"|":
                    *30,"|  No. Referral":
                    *49,"|  No. Referral":
                    *68,"|  Length of wait from referral":
                    *100,"| No. not yet seen at end of qtr":
                    *132,"|":
                    *N:
                    *1,"|",*11,"Specialty":
                    *30,"|  requests for 1st":
                    *49,"|  1st attendances":
                    *68,"|  request to 1st outpatient":
                    *100,"| who have been waiting (weeks)":
                    *132,"|":
                    *N:
                    *1,"|",*11,"Function":
                    *30,"|  Outpat. appoint.":
                    *49,"|  during quarter":
                    *68,"|  attendance (weeks)":
                    *100,"|":
                    *132,"|":
                    *N:
                    *1,"|",*30,"|-----------------------------------------":
                    "-------------------------------------------------------":
                    "-----*":
                    *N:
                    *1,"|":
                    *30,"|",*37,"GP",*42,"Other":
                    *49,"|",*54,"Seen",*61,"DNA's":
                    *68,"|",*74,"0-3   4-12  13-25    26+":
                    *100,"|",*106,"0-3   4-12  13-25    26+":
                    *132,"|"
          CALL      UND132
.
          MOVE      "13",CLNO
.
PAGE9999  RETURN
+
.*************************************************************************
.*  PTOT0000  :  Print District/Purchaser Totals                         *
.*************************************************************************
PTOT0000  COMPARE   ZERO,CPAGENO
          GOTO      PTOT9999 IF EQUAL             * no totals first time through
.
          CALL      UND132
          PRINT     *1,"| ","FINAL TOTAL          999":
                    *30,"|",*33,TOTCOL1,SP2,TOTCOL2:
                    *49,"|",*52,TOTCOL3,SP2,TOTCOL4:
                    *68,"|",*71,TOTCOL5,SP1,TOTCOL6,SP1,TOTCOL7,SP1,TOTCOL8:
                    *100,"|",*103,TOTCOL9,SP1,TOTCOL10,SP1:
                    TOTCOL11,SP1,TOTCOL12:
                    *132,"|"
          CALL      UND132
.
          CALL      ITOT0000                      * Initialise Totals
.
PTOT9999  RETURN
+
.*************************************************************************
.*  Temp file IO's                                                       *
.*************************************************************************
RSTEMP1   RESET     KEY50
          READ      TEMPFILE,KEY50;;
          RETURN
.
RDTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY50
          READ      TEMPFILE,KEY50;XXRANGE,XXHDPEQ,XXSPECD:
                                   XXRRQGP,XXRRQOT,XXTRASN:
                                   XXTRADA,XXLWA,XXLWB,XXLWC,XXLWD:
                                           XXLWE,XXLWF,XXLWG,XXLWH
          GOTO      OVERCOND IF OVER
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMPFILE;XXRANGE,XXHDPEQ,XXSPECD,XXRRQGP,XXRRQOT,XXTRASN:
                             XXTRADA,XXLWA,XXLWB,XXLWC,XXLWD:
                                     XXLWE,XXLWF,XXLWG,XXLWH
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   RESET     KEY50
          WRITE     TEMPFILE,KEY50;XXRANGE,XXHDPEQ,XXSPECD:
                                   XXRRQGP,XXRRQOT,XXTRASN:
                                   XXTRADA,XXLWA,XXLWB,XXLWC,XXLWD:
                                   XXLWE,XXLWF,XXLWG,XXLWH
          RETURN
.
UPTEMP1   UPDATE    TEMPFILE;XXRANGE,XXHDPEQ,XXSPECD,XXRRQGP,XXRRQOT,XXTRASN:
                             XXTRADA,XXLWA,XXLWB,XXLWC,XXLWD:
                                     XXLWE,XXLWF,XXLWG,XXLWH
          RETURN
+
.*************************************************************************
.*  KILL0000  :  erase temporary file                                    *
.*************************************************************************
KILL0000  CLOSE     TEMPFILE
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
KILL9999  RETURN
+
.*************************************************************************
.*  CRET0000  :  create temporary file                                   *
.*************************************************************************
CRET0000  CALL      KILL0000 
          CLEAR     CMDLINE 
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME,CMDLINE
          APPEND    " 119 U1-46,47-50",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPFILE,FNAME
.
CRET9999  RETURN
.
          INC       STDGENCD
.
          INC       IBASEQIO/INC
          INC       OUTBOCIO/INC
          INC       OUTCANIO/INC
          INC       OUTBOAIO/INC
          INC       OUTBB1IO/INC
          INC       OUTOSFIO/INC
          INC       OUTPREIO/INC
          INC       OUTRF1IO/INC
          INC       OUTRAPIO/INC
          INC       OUTRSHIO/INC
.
          INC       PATCODKY
          INC       PATCOMN3
          INC       PATDRGIO/INC
          INC       PATCODIO/INC
          INC       PATMA1IO/INC
          INC       PATPA1IO/INC
          INC       PATVISIO/INC
          INC       TFILENAM
          INC       WEBERRIO/INC
.
          INC       CALCDAYS
................................................................................

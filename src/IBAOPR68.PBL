.******************************************************************************
.* System   : THEATRE                                                         *
.* Program  : IBAOPR68                                                        *
.* Desc.    : Theatre Statistics Report                                       *
.******************************************************************************
.* Author   : Paul Duncan                                                     *
.* Date     : 14/02/1990                                                      *
.* Mods.    :                                                                 *
.*                                                                            *
.*        V10.04.01  23/06/2014   J.Tan           CAR 261630                  *
.*                   Removed port number from temp file name                  *
.*         V9.09.01  26/02/2008  Mike Laming    CAR 142531                    *
.*                   Ported from V6.15 - replace version ported from V5.03    *
.*         V6.09.01  05/07/2001  Steve Downing  SRF 10944                     *
.*                   Fixed column 1 overwriting to column 2                   *
.*         V6.05.01  24/11/1998  Nicole Harrington                            *
.*                   Removed CCENTRY                                          * 
.*            V5.00  18/12/90 R. Knowles                                      *
.*                   Converted to Version 5                                   *
.*            V5.01  03/01/91 Graeme Williams                                 *
.*                   Re-written for new OPRORUFD and to allow for the idea of *
.*                   sessions no longer tied to theatres                      *
.*                   08/01/91 Peter Eddey                                     *
.*                   Graeme did bugger all, found it to hard so gave it to me *
.*                   to re-write.                                             *
.*            V5.02  04/02/91 ROD                                             *
.*                   Fixed calculation of Grand Total % Utilised              *
.*            V5.03  28/05/91 i chung            SRF 108688                   *
.*                   Modify to include UNKNOWN theatre                        *
.*                   25/06/91 i chung            SRF 108688                   *
.*                   Fix so % Utilised will remain 0 if Total Time Avail is 0 *
.*         6.00.00   14/05/92 ROD                SRF 114601                   *
.*                   Fixed summary screen (Someone stuffed up bad!)           *
.*         6.03.B1   22/03/95 ROD                                             *
.*                   Theatre Workshop mods                                    *
.*                                                                            *
.******************************************************************************
.
.$$$$$
.
.  Theatre Statistics Report - IBAOPR68
.  ------------------------------------
.
.  - Initialization
.
.       - Display Header
.       - Open Files
.          oproruaf (OPRORU1)
.       - Read CONTROLF for OPCLCNSU (clinic/surgeon flag)
.
.  - Processing Options
.
.       - 0 = Exit
.       - 1 = Operating Room/Theatre Statistics
.             - enter date range required (GTDT0000)
.             - only summary page required ?
.             - O.K. to Continue ?
.               Yes    - loop thru oproruaf file printing all data in date range
.               No     - ask for date range again
.               Cancel - exit option and return to program menu
.
.  - END
.
.$$$$$$
+
          INC       STD001FD
          INC       IBASEQFD/INC            * Sequential Numbers File
          INC       TFILEVAR/INC            * Generate Temp File Name
          INC       WEBERRFD/INC            * Web Server Error Logging
.
          INC       OPRCONFD/INC
          INC       OPROPRFD/INC
          INC       OPRORUFD/INC
          INC       PATCONFD/INC
          INC       PATDRGFD/INC
+
.***********************************************************************
.*                          VARIABLES                                  *
.***********************************************************************
CMDLINE   DIM       70
CPTDATE   DIM       8
.
DESCFR    DIM       40
DESCTO    DIM       40
DIM7      DIM       7
.
EFINPERD  DIM       4
EFINCNUM  DIM       2
ENDP      DIM       6
ENDPDESC  DIM       15
ENDPERI   DIM       6
.
FORM3P2   FORM      3.2
FORM7P4   FORM      7.4
.
OPRTEMP1  DIM       8
OPRTEMPB  DIM       8
.
PERCUSED  FORM      3.2
.
SAVEPERI  DIM       6
SFINCNUM  DIM       2
SFINPERD  DIM       4
SRTPDESC  DIM       15
STRTPERI  DIM       6
SUMMARYF  FORM      1
.
TIMEUSED  FORM      7
TODAY     DIM       8
TOTAVAIL  FORM      7 
TOTBCASE  FORM      6   
TOTBOPR   FORM      6
TOTBTIME  FORM      7  
TOTECASE  FORM      6 
TOTEOPR   FORM      6
TOTETIME  FORM      7 
TOTPERCU  FORM      7.5
TOTTCASE  FORM      6 
TOTTOPR   FORM      6
TOTTTIME  FORM      7 
.
TEMPFIL1  IFILE     SQL, FIXED=57, KEY="u1-6,7-12"
.
XTUSED    FORM      7
.
.Name     Type     Size    Physical   Start       Description
.----     ----     ----    --------   -----       -----------
.OPOUDATE  DIM       6         6         1         Operating Period Date
XROOM     DIM       6         6         7         Operating room code
XBCASE    FORM      6         4         13        Number of booked cases
XBTIME    FORM      7         5         17        Total time for booked cases
XTCASE    FORM      6         4         22        Number of trans. cases
XTTIME    FORM      7         5         26        Total time for trans. cases
XECASE    FORM      6         4         31        Number of extra cases
XETIME    FORM      7         5         35        Total time for extra cases
XAVAIL    FORM      7         5         40        Total time theatre available
XBPROC    FORM      6         4         45        No. Booked Procedures
XTPROC    FORM      6         4         49        No. Transferred Procedures
XEPROC    FORM      6         4         53        No. Extra Procedures
. End of record                         57
.
. Temp file for Summary screen (cause Floater stuffed up)
TEMPFILB  IFILE     SQL, FIXED=51, KEY="U1-6"
.
XXROOM    DIM       6         6         1         Operating room code
XXBCASE   FORM      6         4         7         Number of booked cases
XXBTIME   FORM      7         5         11        Total time for booked cases
XXTCASE   FORM      6         4         16        Number of trans. cases
XXTTIME   FORM      7         5         20        Total time for trans. cases
XXECASE   FORM      6         4         25        Number of extra cases
XXETIME   FORM      7         5         29        Total time for extra cases
XXAVAIL   FORM      7         5         34        Total time theatre available
XXBPROC   FORM      6         4         39        No. Booked Procedures
XXTPROC   FORM      6         4         43        No. Transferred Procedures
XXEPROC   FORM      6         4         47        No. Extra Procedures
.                                       51
PRGNAM    DIM       50
PRGID     INIT      "IBAOPR68"
PRGNAM1   INIT      " STATISTICS"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                            MAIN LINE                               *
.**********************************************************************
ML0000    CALL      INIT0000            
.
ML1000    CALL      OPTN0000                * Get the option
          BRANCH    EXIT,ML9999             * exit?
.
.*** Theatre Option ***
.
ML2000    CALL      TODY0000
          CALL      SCRN0000
          CALL      KSPR0000                * keyin start period
          BRANCH    EXIT,ML1000 
.
          CALL      KEPR0000                * keyin ending period 
.
          CALL      SUMM0000                * Summary only ?
.
          CALL      CONTQST
          BRANCH    CEXIT,ML2200:           * (Y)es selected
                          ML2000:           * (N)o selected
                          ML1000            * (C)ancel selected
.
ML2200    CALL      CREA0000
          CALL      PRNT0000                * produce printout
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.**********************************************************************
.*                             INIT0000                                *
.*           Display head, open file and read control file             *
.***********************************************************************
INIT0000  MOVE      ONE,CNOUNDLN
.
          DISPLAY   *ES,PRGID,*P20:1,CNAME,*P70:1,CDATE:
                    *P33:24,"Opening opropraf";
          OPEN      OPROPRA1,"opropraf"
.
          DISPLAY   *P41:24,"oproruaf"
          OPEN      OPRORUA1,"oproruaf"
.
          DISPLAY   *P41:24,"patdrgaf"
          OPEN      PATDRGA1,"patdrgaf"
          OPEN      PATDRGA3,"patdrgaf"
.
          DISPLAY   *P41:24,"controlf"
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,FORTY;*81,OPCNODSC
          READ      CONTROLF,FIFTY9;*27,CPERMTH
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,OPRTEMP1
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,OPRTEMPB
.
.      -- remove blanks from op room desc --
.
          ENDSET    OPCNODSC
.
INIT1000  CMATCH    SP1,OPCNODSC
          GOTO      INIT2000 IF NOT EQUAL
.
          BUMP      OPCNODSC BY -1
          GOTO      INIT1000 IF NOT EOS
.
INIT2000  LENSET    OPCNODSC
          RESET     OPCNODSC
.
          PACK      PRGNAM,OPCNODSC,PRGNAM1
          RESET     PRGNAM
          REP       UPPLOW,PRGNAM
.
          CALL      DISPHEAD
.
INIT9999  RETURN
+
.**********************************************************************
.*                              CREA0000                              *
.*                   Create the Indexed Temp File                     *
.**********************************************************************
CREA0000  CALL      KILL0000
.
.------ Build new indexed temp file ------
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    OPRTEMP1,CMDLINE
          APPEND    " 57 u1-6,7-12",CMDLINE
          APPEND    SP30,CMDLINE
          APPEND    SP30,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPFIL1,OPRTEMP1
.
. do the same for the summary temp file
.
          CALL      KILS0000                       * kill existing temp file
.  
.------ Build new indexed temp file ------
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    OPRTEMPB,CMDLINE
          APPEND    " 51 u1-6",CMDLINE
          APPEND    SP30,CMDLINE
          APPEND    SP30,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPFILB,OPRTEMPB
.
CREA9999  RETURN
+
.********************************************************************
.*                        TODY0000                                  *
.*                 Format todays date                               *
.********************************************************************
TODY0000  CALL      IBACLOCK
          PACK      TODAY,CCC,CYY,CMM,CDD
          REP       " 0",TODAY 
TODY9999  RETURN
+
.**********************************************************************
.*                         SCRN0000                                   *
.*                  Display the date prompt screen                    *
.********************************************************************** 
SCRN0000  PACK      CPERMTH,CPERMTH,SP20
          DISPLAY   *P1:8,*EF:
                    *P1:11,"Starting ",CPERMTH," :":
                    *P1:13,"Ending   ",CPERMTH," :";
                    
SCRN9999  RETURN
+
.**********************************************************************
.*                         KSPR0000                                   *
.*                Keyin the starting period                           *
.**********************************************************************
KSPR0000  CLEAR     SFINPERD
          CLEAR     SFINCNUM
          MOVE      ZERO,EXIT
.
          MOVE      TEN1,CVERT
          MOVE      TEN9,CCOL
          MOVE      ZERO,CHIGHLT
          MOVE      TEN9,CCENT
          MOVE      SP2,CYEAR
          MOVE      SP2,CMON
.
          CALL      KEYPER
          BRANCH    CQUEST,KSPR0000
          BRANCH    OVRCD,KSPR9000
.
          PACK      STRTPERI,ICENT,IYEAR,IMON
          REP       " 0",STRTPERI
.
          PACK      KEY6,STRTPERI
          CALL      RDDRGA3
          BRANCH    OVRCD,KSPR6000
.
          MATCH     DRGTDTE,TODAY
          GOTO      KSPR7000 IF LESS
.
          MOVE      DRGYR,SFINPERD 
          MOVE      DRGNUM,SFINCNUM
          MOVE      DRGMDSC,SRTPDESC
          DISPLAY   *P32:CVERT,*EL,SRTPDESC;
          GOTO      KSPR9999
KSPR6000
          DISPLAY   *P1:24,*EL,*B,*V2LON,CPERMTH," is not on file.   ";
          CALL      HITENTER
          GOTO      KSPR0000
KSPR7000
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "Ending date for this ",CPERMTH," is > today's date.   ";
          CALL      HITENTER
          GOTO      KSPR0000
KSPR9000  
          MOVE      ONE,EXIT
.
KSPR9999  RETURN
+
.**********************************************************************
.*                         KEPR0000                                   *
.*                  Keyin the Ending period                           *
.**********************************************************************
KEPR0000  CLEAR     EFINPERD
          CLEAR     EFINCNUM
.
          MOVE      TEN3,CVERT
          MOVE      TEN9,CCOL
          MOVE      ZERO,CHIGHLT
          UNPACK    STRTPERI,CCENT,CYEAR,CMON
.
          CALL      KEYPER
          BRANCH    CQUEST,KEPR0000
          BRANCH    OVRCD,KEPR0000
.
          PACK      ENDPERI,ICENT,IYEAR,IMON
          REP       " 0",ENDPERI
.
          PACK      KEY6,ENDPERI
          CALL      RDDRGA3
          BRANCH    OVRCD,KEPR6000
.
          MATCH     DRGTDTE,TODAY
          GOTO      KEPR7000 IF LESS
.
          MATCH     STRTPERI,ENDPERI
          GOTO      KEPR8000 IF LESS
.
          MOVE      DRGYR,EFINPERD 
          MOVE      DRGNUM,EFINCNUM
          MOVE      DRGMDSC,ENDPDESC
          DISPLAY   *P32:CVERT,*EL,ENDPDESC;
          GOTO      KEPR9999
KEPR6000
          DISPLAY   *P1:24,*EL,*B,*V2LON,CPERMTH," is not on file.   ";
          CALL      HITENTER
          GOTO      KEPR0000
KEPR7000
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "Ending date for this ",CPERMTH," is > today's date.   ";
          CALL      HITENTER
          GOTO      KEPR0000
KEPR8000
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "Ending ",CPERMTH," must be > or equal to Starting ":
                              CPERMTH,".  ";
          CALL      HITENTER
          GOTO      KEPR0000
.
KEPR9999  RETURN
+
.**********************************************************************
.*                        SUMM0000                                    *
.*                Ask if summary page only required                   *
.**********************************************************************
SUMM0000  MOVE      ZERO,SUMMARYF
          DISPLAY   *P1:16,*EL,"Do you want just the summary page (Y/N) ? ";
.
SUMM1000  KEYIN     *P43:16,*DV,UNDLN1,*P43:16,*V2LON,ANS:
                    *P43:16,*DV,ANS;
.
          REP       UPPLOW,ANS
          MATCH     ANSN,ANS
          GOTO      SUMM2000 IF NOT EQUAL
.
          MOVE      ZERO,SUMMARYF
          DISPLAY   *P43:16,*V2LON,"No";
          GOTO      SUMM9999
SUMM2000
          MATCH     ANSY,ANS
          GOTO      SUMM9000 IF NOT EQUAL
.
          MOVE      ONE,SUMMARYF
          DISPLAY   *P43:16,*V2LON,"Yes";
          GOTO      SUMM9999
SUMM9000
          BEEP 
          GOTO      SUMM1000
.
SUMM9999  RETURN
+
.**********************************************************************
.*                         DATE0000                                   *
.**********************************************************************
DATE0000  UNPACK    STRTPERI,CCENT,CYEAR,CMON
          CLEAR     DIM7
          PACK      DIM7,CMON,SLASH,CCENT,CYEAR
          PRINT     *N,*40,"From : ",DIM7,SP5,SRTPDESC
.
          UNPACK    ENDPERI,CCENT,CYEAR,CMON
          CLEAR     DIM7
          PACK      DIM7,CMON,SLASH,CCENT,CYEAR
          PRINT     *N,*40,"To   : ",DIM7,SP5,ENDPDESC
          ADD       FOUR,CLNO
.
DATE9999  RETURN
+
.**********************************************************************
.*                         PRNT0000                                   *
.*                    Print the report                                *
.**********************************************************************
PRNT0000  MOVE      ZERO,CPAGENO
          MOVE      ZERO,CNOUNDLN
          CALL      CLPT0000             * clear period totals
.
          PACK      ENDP,EFINPERD,EFINCNUM
.
.         get the first period in range
.
          PACK      KEY12,SFINPERD,SFINCNUM,SP10
          CALL      RDOPORU1
          BRANCH    OVRCD,PRNT1000
          GOTO      PRNT1500
.
PRNT1000  PACK      KEY12,SFINPERD,SFINCNUM,SP10
          CALL      RSOPORU1
          CALL      RKOPORU1
          BRANCH    OVRCD,PRNT9000
.
PRNT1500  MOVE      OPOUDATE,SAVEPERI
.
          BRANCH    SUMMARYF,PRNT2000
          DISPLAY   *P1:24,*EL,*P28:24,"Printing Period : ";
.
          CALL      HEAD132              * print page headings
          CALL      DATE0000
          CALL      HEAD0000             * print report headings
          GOTO      PRNT3500
.
PRNT2000  DISPLAY   *P1:24,*EL,*P29:24,*V2LON,"*** ",*BLINKON:
                           "Printing Summary",*HOFF,*V2LON," ***";
          GOTO      PRNT3500
.
.         start processing loop
.
PRNT3000  CALL      RKOPORU1
          BRANCH    OVRCD,PRNT8000
.
PRNT3500  MATCH     OPOUDATE,ENDP        * test if within range
          GOTO      PRNT8000 IF LESS
.
          CALL      WTMP0000             * write to the tempfile
          BRANCH    SUMMARYF,PRNT3000    * if only summary required return
.
          DISPLAY   *P46:24,*EL,*V2LON,DIM7;
.
          MATCH     SAVEPERI,OPOUDATE
          GOTO      PRNT6000 IF NOT EQUAL
.
PRNT4000  CALL      CPER0000             * calculate percent used & totals etc.
          CALL      PLIN0000             * print the line
.
          COMPARE   CLNO,FIFTY8          * test if page is full
          GOTO      PRNT5000 IF LESS
          GOTO      PRNT3000
.
PRNT5000  CALL      HEAD132
          CALL      DATE0000
          CALL      HEAD0000
          GOTO      PRNT3000
.
PRNT6000  MOVE      OPOUDATE,SAVEPERI
          CALL      PPTL0000             * print period totals
          CALL      CLPT0000             * clear period totals
          CALL      HEAD0000             
          GOTO      PRNT4000
PRNT8000
          BRANCH    SUMMARYF,PRNT8500 
          CALL      PPTL0000             * print the last period totals
          MOVE      ONE,SUMMARYF
PRNT8500
          CALL      PSUM0000             * print the summary page
          GOTO      PRNT9800
.
PRNT9000  DISPLAY   *P1:24,*EL,*B,*V2LON,*P21:24:
                    "** No Statistics for PERIOD specified **",*W2;
.
PRNT9800  CALL      KILL0000
          CALL      KILS0000                       * kill existing temp file
PRNT9999  RETURN
+
.*********************************************************************
.*                        WTMP0000                                   *
.*                 Write / update the tempfile                       *
.*********************************************************************
WTMP0000  PACK      KEY12,OPOUDATE,OPOUROOM
          CALL      RDOPTP1              * test if record exists
          BRANCH    OVRCD,WTMP5000
.
.         record exists so update the values
.
          ADD       OPOUBCAS,XBCASE
          ADD       OPOUBOPR,XBPROC
          ADD       OPOUBTIM,XBTIME
          ADD       OPOUTCAS,XTCASE
          ADD       OPOUTOPR,XTPROC
          ADD       OPOUTTIM,XTTIME
          ADD       OPOUECAS,XECASE
          ADD       OPOUEOPR,XEPROC
          ADD       OPOUETIM,XETIME
          ADD       OPOUAVAL,XAVAIL
          CALL      UPOPTP1
          GOTO      WTMP9999
.
.         record didn't exist so write it
.
WTMP5000  MOVE      OPOUBCAS,XBCASE
          MOVE      OPOUBOPR,XBPROC
          MOVE      OPOUBTIM,XBTIME
          MOVE      OPOUTCAS,XTCASE
          MOVE      OPOUTOPR,XTPROC
          MOVE      OPOUTTIM,XTTIME
          MOVE      OPOUECAS,XECASE
          MOVE      OPOUEOPR,XEPROC
          MOVE      OPOUETIM,XETIME
          MOVE      OPOUAVAL,XAVAIL
          MOVE      OPOUROOM,XROOM
          CALL      WROPTP1
.
WTMP9999  RETURN
+
.*********************************************************************
.*                         CPER0000                                  *
.*                 Calculate the percentage used                     *
.*********************************************************************
CPER0000  BRANCH    SUMMARYF,CPER5000
.
          ADD       OPOUBCAS,TOTBCASE
          ADD       OPOUBOPR,TOTBOPR
          ADD       OPOUBTIM,TOTBTIME
          ADD       OPOUTCAS,TOTTCASE
          ADD       OPOUTOPR,TOTTOPR
          ADD       OPOUTTIM,TOTTTIME
          ADD       OPOUECAS,TOTECASE
          ADD       OPOUEOPR,TOTEOPR
          ADD       OPOUETIM,TOTETIME
          ADD       OPOUAVAL,TOTAVAIL
.
.         calculate time used
.
          MOVE      ZERO,XTUSED
          ADD       OPOUBTIM,XTUSED
          ADD       OPOUTTIM,XTUSED
          ADD       OPOUETIM,XTUSED
.
.         calculate percentage used
.
          MOVE      ZERO,PERCUSED
          COMPARE   ZERO,OPOUAVAL
          GOTO      CPER9999 IF EQUAL
.
          MOVE      ZERO,FORM7P4
          MOVE      XTUSED,FORM7P4
          DIV       OPOUAVAL,FORM7P4
          MULT      HUNDRED,FORM7P4    
          MOVE      FORM7P4,PERCUSED
          GOTO      CPER9999
.
.*******   Calculations for grand summary page *********
.
CPER5000  ADD       XBCASE,TOTBCASE
          ADD       XBPROC,TOTBOPR
          ADD       XBTIME,TOTBTIME
          ADD       XTCASE,TOTTCASE
          ADD       XTPROC,TOTTOPR
          ADD       XTTIME,TOTTTIME
          ADD       XECASE,TOTECASE
          ADD       XEPROC,TOTEOPR
          ADD       XETIME,TOTETIME
          ADD       XAVAIL,TOTAVAIL
.
.         calculate time used
.
          MOVE      ZERO,XTUSED
          ADD       XBTIME,XTUSED
          ADD       XTTIME,XTUSED
          ADD       XETIME,XTUSED
.
.         calculate percentage used
.
          MOVE      ZERO,PERCUSED
          COMPARE   ZERO,XAVAIL
          GOTO      CPER9999 IF EQUAL
.
          MOVE      ZERO,FORM7P4
          MOVE      XTUSED,FORM7P4
          DIV       XAVAIL,FORM7P4
          MULT      HUNDRED,FORM7P4    
          MOVE      FORM7P4,PERCUSED
.
CPER9999  RETURN
+
.*********************************************************************
.*                         CLPT0000                                  *
.*                clear the period total                             *
.*********************************************************************
CLPT0000  MOVE      ZERO,TOTBCASE  
          MOVE      ZERO,TOTBOPR
          MOVE      ZERO,TOTBTIME
          MOVE      ZERO,TOTTCASE 
          MOVE      ZERO,TOTTOPR
          MOVE      ZERO,TOTTTIME
          MOVE      ZERO,TOTECASE
          MOVE      ZERO,TOTEOPR
          MOVE      ZERO,TOTETIME 
          MOVE      ZERO,TOTAVAIL 
          MOVE      ZERO,TOTPERCU
.
CLPT9999  RETURN
+
.**********************************************************************
.*                              KILL0000                              *
.*                  Kill the Tempfile if it Already Exists            *
.**********************************************************************
KILL0000  CLEAR     CMDLINE
          CLOSE     TEMPFIL1
          APPEND    "iserase ",CMDLINE
          APPEND    OPRTEMP1,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
          MOVE      ZERO,XAVAIL
          MOVE      ZERO,XBCASE
          MOVE      ZERO,XBPROC
          MOVE      ZERO,XBTIME
          MOVE      ZERO,XTCASE
          MOVE      ZERO,XTPROC
          MOVE      ZERO,XTTIME
          MOVE      ZERO,XECASE
          MOVE      ZERO,XEPROC
          MOVE      ZERO,XETIME
.
KILL9999  RETURN
+
.**********************************************************************
.*  KILS0000  :  Kill summary temp file                               *
.**********************************************************************
KILS0000  CLEAR     CMDLINE
          CLOSE     TEMPFILB
          APPEND    "iserase ",CMDLINE
          APPEND    OPRTEMPB,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
KILS9999  RETURN
+
.************************************************************************
.*                              OPTN0000                                *
.*                  Select the option to be run                         *
.************************************************************************
OPTN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = ",*+,OPCNODSC," Statistics":
                    *P1:7,"Select Option :";
.
OPTN1000  KEYIN     *P17:7,*EL,*DV,UNDLN1,*P17:7,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9000 IF EQUAL
          DISPLAY   *P17:7,*EL,*V2LON,OPTION;
.
          BRANCH    OPTION,OPTN8000
          BEEP                              * invalid selection 
          GOTO      OPTN1000
.
OPTN8000  MOVE      ZERO,EXIT               * don't exit
          GOTO      OPTN9999
.
OPTN9000  MOVE      ONE,EXIT                * chose exit
.
OPTN9999  RETURN
.***********************************************************************
.*                         HEAD0000                                    *
.*            Print headings for new page                              *
.***********************************************************************
HEAD0000  BRANCH    SUMMARYF,HEAD1000
          UNPACK    OPOUDATE,DRGCYR,DRGCNUM   * defaults, just in case
.
          MOVE      OPOUDATE,KEY6
          CALL      RDDRGA1
          PACK      DIM7,DRGCNUM,SLASH,DRGCYR
.
          PRINT     *N,*2,DIM7
          GOTO      HEAD2000
HEAD1000
          PRINT     *N,*2,"Grand Summary"
.
HEAD2000  CALL      UND132
          PRINT     "|",*23,"|---------- Booked ---------|------- Transferred":
                    " -------|--------- Extra ----------|  Total  |  Total |":
                    *132,"|"
.
          PRINT     "|",*23,"| No. of | No. of |   Time  | No. of | No. of ":
                    "|  Time  | No. of | No. of |  Time  |   Time  ":
                    "|  Time  |   %",*132,"|"
.
          PRINT     "| ",OPCNODSC,*23,"|  Cases | Proc's |   Used  |  Cases ":
                    "| Proc's |  Used  |  Cases | Proc's |  Used  ":
                    "|   Used  |  Avail | Utils |"
          CALL      UND132
          ADD       SEVEN,CLNO
.
HEAD9999  RETURN
+
.**********************************************************************
.*                        PPTL0000                                    *
.*                   Print the period totals                          *
.**********************************************************************
PPTL0000  MOVE      ZERO,TIMEUSED            * calc total time used
          ADD       TOTBTIME,TIMEUSED
          ADD       TOTTTIME,TIMEUSED
          ADD       TOTETIME,TIMEUSED
.
.         calculate Grand Total % Utilised
.
          MOVE      TIMEUSED,TOTPERCU
          DIV       TOTAVAIL,TOTPERCU
          MULT      "100",TOTPERCU
          MOVE      TOTPERCU,FORM3P2
.
          CALL      UND132
          PRINT     "|",*5,"Grand Total",*23,"| ",TOTBCASE," | ",TOTBOPR:
                    " | ",TOTBTIME," | ",TOTTCASE," | ",TOTTOPR," |":
                    TOTTTIME," | ",TOTECASE," | ",TOTEOPR," |",TOTETIME," | ":
                    TIMEUSED," |",TOTAVAIL," |",FORM3P2,*132,"|"
          CALL      UND132
          ADD       THREE,CLNO
.
PPTL9999  RETURN
+
.**********************************************************************
.*                        PLIN0000                                    *
.*                   Print the line                                   *
.**********************************************************************
PLIN0000  BRANCH    SUMMARYF,PLIN5000
.
          MOVE      "UNKNOWN",OPRMDESC
          MATCH     SP6,OPOUROOM
          GOTO      PLIN1000 IF EQUAL
          CLEAR     OPRMDESC
          MOVE      OPOUROOM,KEY6
          CALL      RDOPOPR1
.
PLIN1000  MOVE      OPRMDESC,KEY20
          PRINT     "|",SP1,KEY20,*23,"| ",OPOUBCAS," | ",OPOUBOPR:
                    " | ",OPOUBTIM," | ",OPOUTCAS," | ",OPOUTOPR," |":
                    OPOUTTIM," | ",OPOUECAS," | ",OPOUEOPR," |",OPOUETIM," | ":
                    XTUSED," |",OPOUAVAL," |",PERCUSED,*132,"|"
          GOTO      PLIN9000
.
PLIN5000  MOVE      "UNKNOWN",OPRMDESC
          MATCH     SP6,XROOM
          GOTO      PLIN5500 IF EQUAL
          CLEAR     OPRMDESC
          MOVE      XROOM,KEY6
          CALL      RDOPOPR1
.
PLIN5500  MOVE      OPRMDESC,KEY20
          PRINT     "|",SP1,KEY20,*23,"| ",XBCASE," | ",XBPROC:
                    " | ",XBTIME," | ",XTCASE," | ",XTPROC," |":
                    XTTIME," | ",XECASE," | ",XEPROC," |",XETIME," | ":
                    XTUSED," |",XAVAIL," |",PERCUSED,*132,"|"
.
PLIN9000  ADD       ONE,CLNO
.
PLIN9999  RETURN
+
.**********************************************************************
.*  LOAD0000  :  Load data from 1st temp file into Summary temp file  *
.**********************************************************************
LOAD0000  MOVE      SP20,KEY12
.
. loop thru temp file containing the data and put all theatres together
.
          CALL      RSOPTP1 
LOAD1000  CALL      RKOPTP1 
          BRANCH    OVRCD,LOAD9999
.
. check if record already exists
.
          MOVE      ZERO,XXBCASE
          MOVE      ZERO,XXBPROC
          MOVE      ZERO,XXBTIME
          MOVE      ZERO,XXTCASE
          MOVE      ZERO,XXTPROC
          MOVE      ZERO,XXTTIME
          MOVE      ZERO,XXECASE
          MOVE      ZERO,XXEPROC
          MOVE      ZERO,XXETIME
          MOVE      ZERO,XXAVAIL
.
          MOVE      XROOM,XXROOM
          MOVE      XXROOM,KEY6
          CALL      RDSUTP1
          BRANCH    OVRCD,LOAD3000
.
. theatre already exists so update record
.
          ADD       XBCASE,XXBCASE
          ADD       XBPROC,XXBPROC
          ADD       XBTIME,XXBTIME
          ADD       XTCASE,XXTCASE
          ADD       XTPROC,XXTPROC
          ADD       XTTIME,XXTTIME
          ADD       XECASE,XXECASE
          ADD       XEPROC,XXEPROC
          ADD       XETIME,XXETIME
          ADD       XAVAIL,XXAVAIL
.
          CALL      UPSUTP1
          GOTO      LOAD1000
.
. theatre doesnt exist so write a new record
.
LOAD3000  MOVE      XBCASE,XXBCASE
          MOVE      XBPROC,XXBPROC
          MOVE      XBTIME,XXBTIME
          MOVE      XTCASE,XXTCASE
          MOVE      XTPROC,XXTPROC
          MOVE      XTTIME,XXTTIME
          MOVE      XECASE,XXECASE
          MOVE      XEPROC,XXEPROC
          MOVE      XETIME,XXETIME
          MOVE      XAVAIL,XXAVAIL
.
          MOVE      XXROOM,KEY6
          CALL      WRSUTP1
          GOTO      LOAD1000
.
LOAD9999  RETURN
+         
.**********************************************************************
.*                         PSUM0000                                   *
.*                    Print the summary page                          *
.**********************************************************************
PSUM0000  CALL      LOAD0000             * load data into Summary temp file
.
          MOVE      ONE,CNOUNDLN
          CALL      CLPT0000             * clear period totals
.
          CALL      HEAD132              * print page headings
          MOVE      ZERO,CNOUNDLN
          CALL      DATE0000
          CALL      HEAD0000             * print report headings
.
          MOVE      SP6,KEY6
          CALL      RDSUTP1
          BRANCH    OVRCD,PSUM0900
          GOTO      PSUM1500
.
.         start processing loop
.
PSUM0900  MOVE      SP6,KEY6
          CALL      RSSUTP1 
PSUM1000  CALL      RKSUTP1 
          BRANCH    OVRCD,PSUM8000
.
PSUM1500  MOVE      XXROOM,XROOM
          MOVE      XXBCASE,XBCASE
          MOVE      XXBPROC,XBPROC
          MOVE      XXBTIME,XBTIME
          MOVE      XXTCASE,XTCASE
          MOVE      XXTPROC,XTPROC
          MOVE      XXTTIME,XTTIME
          MOVE      XXECASE,XECASE
          MOVE      XXEPROC,XEPROC
          MOVE      XXETIME,XETIME
          MOVE      XXAVAIL,XAVAIL
.
          CALL      CPER0000             * calculate percent used & totals etc.
          CALL      PLIN0000             * print the line
.
          COMPARE   CLNO,FIFTY8          * test if page is full
          GOTO      PSUM5000 IF LESS
          GOTO      PSUM1000
.
PSUM5000  CALL      HEAD132
          CALL      DATE0000
          CALL      HEAD0000
          GOTO      PSUM1000
.
PSUM8000  CALL      PPTL0000             * print the last period totals
          COMPARE   "48",CLNO
          GOTO      PSUM8500 IF LESS
          CALL      HEAD132
          CALL      DATE0000
          CALL      HEAD0000
.
PSUM8500  PRINT     *N,*N,"*** End of Report ***",*N,*N,*N
          CALL      PDES0000             * print coloumn descriptions
.
PSUM9999  RETURN
+
.**********************************************************************
.*                        PDES0000                                    *
.*                Print the column descriptions                       *
.**********************************************************************
PDES0000  PRINT     *5,"Number of Booked Cases":
                    *43,"= Number of cases seen in the theatre booked ":
                          "for their session",*N:
                    *5,"Number of Booked Procedures":
                    *43,"= Number of MBS procedure codes for cases seen in the":
                          " theatre booked for their session",*N:
                    *5,"Booked Time Used (in minutes)":
                    *43,"= Duration in theatre for each case seen in ":
                          "the theatre booked for their session",*N:
                    *5,"Number of Transferred Cases":
                    *43,"= Number of cases not seen in the theatre ":
                          "booked for their session",*N:
                    *5,"Number of Transferred Procedures":
                    *43,"= Number of MBS procedure codes for cases Not seen ":
                          "in the theatre booked for their session",*N:
                    *5,"Transferred Time Used (in minutes)":
                    *43,"= Duration in theatre for each case not seen ":
                          "in the theatre booked for their session",*N:
                    *5,"Number of Extra Cases":
                    *43,"= Number of cases seen in extra sessions",*N:
                    *5,"Number of Extra Procedures":
                    *43,"= Number of MBS Procedure codes for cases ":
                        "seen in extra sessions",*N:
                    *5,"Extra Time Used (in minutes)":
                    *43,"= Duration in theatre for case seen in extra ":
                          "sessions",*N:
                    *5,"Total Time Used (in minutes)":
                    *43,"= Total Booked Time + Total Transferred Time ":
                          "+ Total Extra Time Used",*N:
                    *5,"Total Time Available (in minutes)":
                    *43,"= Total theatre time that was available",*N:
                    *5,"Percentage Utilised":
                    *43,"= Total Time Used / Total Time Available * 100"
.
PDES9999  RETURN
+
.**********************************************************************
.*                         I/O                                        *
.*             I/O routines for temp file                             *
.**********************************************************************
RSOPTP1   MOVE      ZERO,OVRCD
          RESET     KEY12
          READ      TEMPFIL1,KEY12;;
          RETURN
.
RDOPTP1   MOVE      ZERO,OVRCD
          RESET     KEY12
          READ      TEMPFIL1,KEY12;OPOUDATE,XROOM,XBCASE,XBTIME,XTCASE,XTTIME:
                                   XECASE,XETIME,XAVAIL,XBPROC,XTPROC,XEPROC
          GOTO      OVERCOND IF OVER
          RETURN
.
RKOPTP1   MOVE      ZERO,OVRCD
          RESET     KEY12
          READKS    TEMPFIL1;OPOUDATE,XROOM,XBCASE,XBTIME,XTCASE,XTTIME:
                             XECASE,XETIME,XAVAIL,XBPROC,XTPROC,XEPROC
          GOTO      OVERCOND IF OVER
          RETURN
.
WROPTP1   MOVE      ZERO,OVRCD
          RESET     KEY12
          WRITE     TEMPFIL1,KEY12;KEY12,XBCASE,XBTIME,XTCASE,XTTIME:
                                  XECASE,XETIME,XAVAIL,XBPROC,XTPROC,XEPROC
          RETURN
.
UPOPTP1   UPDATE    TEMPFIL1;OPOUDATE,XROOM,XBCASE,XBTIME,XTCASE,XTTIME:
                             XECASE,XETIME,XAVAIL,XBPROC,XTPROC,XEPROC
          RETURN
.
. IO routines for summary temp file
.
RSSUTP1   MOVE      ZERO,OVRCD
          RESET     KEY6
          READ      TEMPFILB,KEY6;;
          RETURN
.
RDSUTP1   MOVE      ZERO,OVRCD
          RESET     KEY6
          READ      TEMPFILB,KEY6;XXROOM,XXBCASE,XXBTIME,XXTCASE,XXTTIME:
                                  XXECASE,XXETIME,XXAVAIL,XXBPROC,XXTPROC:
                                  XXEPROC
          GOTO      OVERCOND IF OVER
          RETURN
.
RKSUTP1   MOVE      ZERO,OVRCD
          RESET     KEY6
          READKS    TEMPFILB;XXROOM,XXBCASE,XXBTIME,XXTCASE,XXTTIME:
                             XXECASE,XXETIME,XXAVAIL,XXBPROC,XXTPROC:
                             XXEPROC
          GOTO      OVERCOND IF OVER
          RETURN
.
WRSUTP1   MOVE      ZERO,OVRCD
          RESET     KEY6
          WRITE     TEMPFILB,KEY6;KEY6,XXBCASE,XXBTIME,XXTCASE,XXTTIME:
                                  XXECASE,XXETIME,XXAVAIL,XXBPROC,XXTPROC:
                                  XXEPROC
          RETURN
.
UPSUTP1   UPDATE    TEMPFILB;XXROOM,XXBCASE,XXBTIME,XXTCASE,XXTTIME:
                             XXECASE,XXETIME,XXAVAIL,XXBPROC,XXTPROC:
                             XXEPROC
          RETURN
+
.==============================================================================
.
          INC       STD001IO
          INC       PATCOMN3
          INC       TFILENAM                * Generate Temp File Name
          INC       IBASEQIO/INC            * Sequential Numbers File
          INC       WEBERRIO/INC            * Web Server Error Logging
.
          INC       OPROPRIO/INC
          INC       OPRORUIO/INC
          INC       PATDRGIO/INC

.*****************************************************************************
.*              P R O G R A M  S U M M A R Y                                 *
.*              -------------  -------------                                 *
.*                                                                           *
.*     PROGRAM NAME:     IBAHOS82                                            *
.*                                                                           *
.*     FUNCTION:         MONTHLY INPATIENT STATISTICS                        *
.*                                                                           *
.*     DATE WRITTEN:     07/10/88                                            *
.*                                                                           *
.*     AUTHOR:           J.TAN                                               *
.*                                                                           *
.*     MODIFICATIONS:                                                        *
.*     V10.03.02 25/02/2013  Ania P         CAR 281021                       *
.*               Removed unnecessary modification of constants.              *
.*     V10.03.01 17/10/2012  Jill Parkinson CAR 273375                       *
.*               Recompiled for DATECONV - first day of year calculation     *
.*     V9.12.02  29/06/2009  Mike Laming   CAR 197009                        *
.*               Add Option for Date Range - see DATRNG00 (not month range)  *
.*     V9.12.01  09/06/2009  Jill Habasque CAR                               *
.*               Added births definition                                     *
.*     V9.02.05  12/08/2002  Dean Cramer - srf 19445                         *
.*               Make so screen clear and return to 1st prompt when done.    *
.*     V9.02.04  23/07/2002  Dean Cramer - srf 19445                         *
.*               Temporarily remove the By Unit option until balancing       *
.*               problem can be solved.                                      *
.*     V9.02.03  18/01/2002  Dean Cramer                                     *
.*               Make so when only part current period processes dates on    *
.*               print heading are representative of correct range           *
.*     V9.02.02  13/01/2002  Dean Cramer                                     *
.*               Reinstate pre change version until change spec resolved     *
.*     V9.02.01  24/08/2001  Dean Cramer                                     *
.*               Reinstate pre change version until change spec resolved     *
.*     V5.07.B02 28/01/1999  Nicole Harrington                               *
.*               507 rebounds                                                *
.*     V5.07.B01 14/11/1998  Davin                                           *
.*               Mods for 507                                                *
.*****************************************************************************
.*     V5.03.02  08/02/1996  Steve Armstrong  SRF 641143                     *
.*               Mods to use WDACTVDY to calculate active ward days.         *
.*               Mods to only display wards active in the period.            *
.*               Mods to calculate hospital total for daily average on the   *
.*               fly.                                                        *
.*               Mods to calculate Av. % Occupancy using active bed days.    *
.*     V5.03.01  23/08/1995    MATT   SRF : 640652                           *
.*               Recompiled for PATACDAY                                     *
.*****************************************************************************
. *                       V5.01 29/04/89 J.Tan  (I.B.A)    *
. *                             Fixed length of stay       *
. *                             SRF 3978                   *
. *                       V5.02 17.06.89 S.Barcham         *
. *                             Compiled for new standbys  *
. *                             SRF 101101                 *
. *                       V5.03 19/06/89 J.Tan             *
. *                             Added Private boarders to  *
. *                             Public Boarders            *
. *                             SRF 101062                 *
. *                       V5.04 28/06/89 J.Tan             *
. *                             Mods Length of stay again  *
. *                             SRF 101263                 *
. *                    V5.01.01 04/07/89 K.Peak            *
. *                             Changed Adm & U/R to 8 chrs*
. *                             10/07/89 Graeme Williams   *
. *                             Allow for 13x4 week periods*
. *                    V5.01.02 29.09.89 S.Barcham         *
. *                             Recompiled for 13 * 4 Weeks*
. *                             SRF 103509                 *
. *                    V5.01.03 02/10/89 K.Peak            *
. *                             Changed date to CPERMTH    *
. *                             SRF 102534                 *
. *                    V5.01.04 07/02/90 J.Tan   SRF 103500*
. *                             Fixed Average calculation  *
. *                    V5.01.05 10/05/90 J.Tan   SRF 104921*
. *                             Fixed page break           *
. *                             Mods to keyin the period   *
. *                    V5.01.06 28/06/90 J.Tan             *
. *                             Added separate bed days    *
. *                    V5.01.07 10/08/90 J.Tan             *
. *                             Fixed page break           *
. *                    V5.01.08 22/10/90 Peter Eddey       *
. *                             changed program to use the *
. *                         new ward stat's file OPRWARFD  *
. *                             instead of THEWARFD        *
. *                    V5.01.09 16/05/91 J.Tan             *
. *                             Fixed keyin dates          *
. *                    V5.01.10 22/06/92 J.Tan   SRF 114792*
. *                             Fixed ALOS                 *
. *                    V5.01.11 02/06/93 J.Tan   SRF 121660*
. *                             Added ward/bed history file*
. *                    V5.01.12 30/12/93 I.Rutt  MTA       *
. *                             Added new include PATACDAY ********************
. *        V5.02.001 02/03/95 Paul Howells     SRF 135026                     *
. *                  Fixed the use of PATWBHFD file.                          *
. *****************************************************************************
.
          INC           STD001FD
          INC           PATCOMM/INC
          INC           PATMSDFD/INC        * Monthly Inpatient Statistics Dept
          INC           PATMSTFD/INC        * Monthly Inpatient Statistics Ward
          INC           PATWR1FD/INC        * Ward/bed file
          INC           PATWBHFD/INC        * Ward/bed history file
          INC           PATCODFD/INC        * Codes file
          INC           PATBRNFD/INC        * New born file
          INC           PATCSTFD/INC        * Catchment summary file
          INC           PATDRGFD/INC        * Period Date Range file
          INC           OPRWARFD/INC        * Ward statistics file
          INC           WDACTVVR/INC        * for WDACTVDY
.
+
.               VARIABLES DEFINITIONS
.
AVDAY     FORM      6.2
AVSTAY    FORM      6.2
.
CENDDT    DIM       8
CURMTH    DIM       6
CODEAC    INIT      "AC"
CODECG    INIT      "CG"
CODECH    INIT      "CH"
CPERMTH   DIM       6
.
DIM4      DIM       4
.
ENDDAT    FORM      2
ENDMTH    DIM       6
.
FORM4B    FORM      4
FORM6     FORM      6
FORM8     FORM      8
FORM8A    FORM      8
FORM10    FORM      10
FDAY      DIM       2
FYR       DIM       4
FMTH      DIM       2
.
GTWAN     FORM      8     * Total no of operations/separation bed days
GTADM     FORM      8     * Total no of admissions
GTRIN     FORM      8     * Total no of trans in
GTDSH     FORM      8     * Total no of discharges
GTROUT    FORM      8     * Total no of trans out 
GTLEAVE   FORM      8     * Total no of beddays on leave
GTAAE     FORM      8     * Total no of Accident Emergency
GTDEATH   FORM      8     * Total no of deaths
GTBOARD   FORM      8     * Total no of boarders
GTNPRAD   FORM      8     * Total no of admissions (private not baorders)
GTNPBAD   FORM      8     *           "            (public not boarders)
GTBPRAD   FORM      8     *           "            (private boarders)
GTBPBAD   FORM      8     *           "            (public  boarders)
GTBDAY    FORM      8     * Total no of beddays
GTTBED    FORM      8
GTTHEA    FORM      8
GTTWOC    FORM      8
.
GTAVDAY   FORM      6.2
GTBEDAY   FORM      8
GTPCENT   FORM      6.2
GTAVST    FORM      6.2
.
LYEAR     FORM      "366"
OYEAR     FORM      "365"
OPTTYP    FORM      2
PCENTG    FORM      6.2
PTCNALOS  FORM      1
PTCNH82W  FORM      1
PTOCBDAY  FORM      1
PRFRDATE  DIM       8
.
SELMTH    DIM       6
SMONTH    DIM       6
SAVUNT    DIM       3
SAVWRD    DIM       3
SNOMTH    FORM      2
SVHNOCC   FORM      3
SVSTDATE  DIM       8
.
TOTPAT    FORM      8
TOTBDAY   FORM      8
TOJULDD   FORM      3
TOJULYY   FORM      2
TOJULCC   FORM      2 
TOTPB     FORM      8
TOTPR     FORM      8
TOTCST    FORM      8
TDAY      DIM       2
TYR       DIM       4
TMTH      DIM       2
.
UNDES     DIM       20
UTAAE     FORM      8
UTDEATH   FORM      8     * Total no of deaths
UTADM     FORM      8     * Unit Total no of admissions
UTRIN     FORM      8     * Unit Total no of trans in
UTDSH     FORM      8     * Unit Total no of discharges
UTROUT    FORM      8     * Unit Total no of trans out 
UTAVDAY   FORM      6.2
UTPCENT   FORM      6.2
UTLEAVE   FORM      8     * Total no of beddays on leave
UTAVST    FORM      6.2
UTTBED    FORM      8
UTTHEA    FORM      8
UTBDAY    FORM      8     * Unit Total no of beddays
UTWAN     FORM      8     * Unit Total no of operations/separation bed days
UTBEDAY   FORM      8
UTBOARD   FORM      8     * Total no of boarders
UTNPRAD   FORM      8     * Total no of admissions (private not baorders)
UTNPBAD   FORM      8     *           "            (public not boarders)
UTBPRAD   FORM      8     *           "            (private boarders)
UTBPBAD   FORM      8     *           "            (public  boarders)
.
VERT      FORM      2
.
WRDES     DIM       20
WARBEDAC  FORM      8.2
WARTOTBD  FORM      6
WKDATE    DIM       8
WKDAY     DIM       2
WKMTH     DIM       2
WKYR      DIM       4
.
MDESC     DIM      10
ZEIGHT    INIT      "ZZZZZZZZ"
.
PRGID     INIT      "IBAHOS82"
PRGNAM    INIT      "Monthly Inpatient Statistics"
VERSION   INIT      "V12.01.00"
+
.         START OF PROGRAM
.
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patmsdaf";
          OPEN      PATMSDA1,"patmsdaf"
.
          DISPLAY   *P64:24,"patmstsf";
          OPEN      PATMSTS1,"patmstsf"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY   *P64:24,"patwbhaf";
          OPEN      PATWBHA1,"patwbhaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdrgaf";
          OPEN      PATDRGA1,"patdrgaf"
          OPEN      PATDRGA3,"patdrgaf"
.
          DISPLAY   *P64:24,"patbrnsf";
          OPEN      PATBRNS1,"patbrnsf"
.
          DISPLAY   *P64:24,"patcstsf";
          OPEN      PATCSTS1,"patcstsf"
.
          DISPLAY   *P64:24,"oprwaraf";
          OPEN      OPRWARA1,"oprwaraf"
.
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,TWENTY;*250,PTCNH82W
          READ      CONTROLF,FIFTY9;*27,CPERMTH,*217,PTOCBDAY
          READ      CONTROLF,EIGHTY;*242,PTCNALOS
          PACK      CPTDATE WITH CCC,CYY,CMM,CDD
          REP       " 0",CPTDATE
.
INITIT    CALL      GPERD
          BRANCH    EXIT,NOCPER
.
          PACK      CURMTH,DRGYR,DRGNUM
          GOTO      START
.
NOCPER    KEYIN     *P1:24,*EL,*V2LON,*B:
                    "** Error: Current Date not in Period File. ":
                    "Hit <ENTER> to exit ",*EOFF,ANS;
          GOTO      ENDIT
+
.
.        START OF PROGRAM
.
START   DISPLAY     *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = By Ward"
....                    *P1:6,*V2LON,TWO,*HOFF," = By Unit"
.
KEYTYP  KEYIN       *P1:8,"Option : ",*EL,*V2LON,OPTTYP
        COMPARE     ZERO,OPTTYP
        GOTO        ENDIT IF EQUAL
.
        BRANCH      OPTTYP OF STPER,STPER
        BEEP
        GOTO        KEYTYP
.
STPER   DISPLAY     *P1:11,*V2LON,ONE,*HOFF," = Single ",CPERMTH:
                    *P1:12,*V2LON,TWO,*HOFF," = Range of ",CPERMTH:
                    *P1:13,*V2LON,THREE,*HOFF," = Date Range"
.
KEYOPT  DISPLAY     *P1:15,*EF
        KEYIN       *P1:15,"Option : ",*EL,*V2LON,OPTION
        COMPARE     ZERO,OPTION
        GOTO        KEYTYP IF EQUAL
.
        MOVE        SP6,SELMTH
        MOVE        SP6,ENDMTH
BRNOPT  BRANCH      OPTION OF SNGDAT,RNGDAT,DATRNG00
        BEEP
        GOTO        KEYOPT
.
.
.       Keyin single date (MM/YY)
.
SNGDAT  DISPLAY     *P1:17,"  Keyin ",CPERMTH,"/Year :";
        GOTO        KADAT1
.
.
RNGDAT  DISPLAY     *P1:17,"Keyin From ",CPERMTH,"/Year :":
                    *P1:18,"Keyin To   ",CPERMTH,"/Year :"
.
KADAT1  MOVE        TEN7,CVERT
        CALL        KEYMN
        BRANCH      OVRCD,KEYOPT
.
.
        PACK        SELMTH WITH DRGYR,DRGNUM
        REP         " 0",SELMTH
.
        MATCH       SELMTH,CURMTH
        GOTO        INVDTE0 IF LESS
.
        IF          OPTION = 1
          MOVE        DRGFDTE,ACTVFDTE
          MOVE        DRGTDTE,PRFRDATE
        ENDIF
        MOVE        DRGFDTE,SVSTDATE
        MOVE        DRGTDTE,CENDDT
.
        BRANCH      OPTION OF REKEY
.
.
.       Keyin todate (MM/YY)
.
KADAT2  MOVE        TEN8,CVERT
        CALL        KEYMN
        BRANCH      OVRCD,KADAT1
.
        MOVE        DRGTDTE,PRFRDATE 
        PACK        ENDMTH WITH DRGYR,DRGNUM
        REP         " 0",ENDMTH
.
        MATCH       ENDMTH,CURMTH
        GOTO        INVDTE0 IF LESS
.
        MATCH       SELMTH,ENDMTH
        GOTO        INVDTE1 IF LESS
        GOTO        REKEY
.
INVDTE0 DISPLAY     *P1:24,*B,*EL,*V2LON,"** Date must be < ":
                    "current Date **",*W2,*P1:24,*EL;
.
        GOTO        BRNOPT
.
INVDTE1 DISPLAY     *P1:24,*B,*EL,*V2LON,"** From Date must be <= ":
                    "To Date **",*W2;
        GOTO        KADAT2
.
.
.        Check if dates are OK or not
.
REKEY   DISPLAY     *P1:24,*EF,CPERMTH;
        KEYIN       " ok (",*V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
        REP         "yYnN",ANS
        MATCH       "N" TO ANS
        GOTO        BRNOPT IF EQUAL
.
        MATCH       "Y" TO ANS
        GOTO        OKPROC IF EQUAL
        BEEP
        GOTO        REKEY
.
.* ****************************************** start of addition       *I-197009
.******************************************************************************
.*                         DATRNG00                                           *
.*                       Keyin Dates                                          *
.******************************************************************************
DATRNG00  MOVE      ZERO,EXIT
.                                           * keyin from date
          DISPLAY   *P1:17,"Start Date :":
                    *P1:18,"End Date   :"
          UNPACK    SP6,CYEAR,CMON,CDAY     * set up defaults
          MOVE      TEN4,CCOL               * column for keyin
          MOVE      TEN7,CVERT              * row for keyin
          MOVE      THIRTY,ECOL             * column for error
          MOVE      TEN7,EVERT              * row for error
          MOVE      ZERO,CHIGHLT            * 0 = highlight 1 = no highlight
          MOVE      ONE,CDEFDTE             * 0 = check all fields for default
.                                           * 1 = check day only for default
          CALL      KEYDATE
          BRANCH    OVRCD,DATRNG80          * no date keyed in
.
          PACK      SVSTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",SVSTDATE
.
.                                           * keyin to date
DATRNG20  UNPACK    SP6,CYEAR,CMON,CDAY     * set up defaults
          MOVE      TEN4,CCOL               * column for keyin
          MOVE      TEN8,CVERT              * row for keyin
          MOVE      THIRTY,ECOL             * column for error
          MOVE      TEN8,EVERT              * row for error
.                                           * 1 = check day only for default
          CALL      KEYDATE
          BRANCH    OVRCD,DATRNG00
.
DAT3000   PACK      PRFRDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",PRFRDATE
.
          MATCH     SVSTDATE,PRFRDATE
          GOTO      DATRNG90 IF NOT LESS
.
          DISPLAY   *P40:13,*V2LON,*B:
                    "** Ending Date > Starting Date ** ";
          CALL      HITENTER
          DISPLAY   *P40:13,*EL
          GOTO      DATRNG20
.
DATRNG80  MOVE      ONE,EXIT
          GOTO      STPER
.
DATRNG90  MOVE      SVSTDATE,DRGFDTE
          MOVE      PRFRDATE,DRGTDTE
          GOTO      OKPROC2
.
.* ******************************************   end of addition       *I-197009
.
.
.       Continue processing
.
OKPROC   DISPLAY    *P1:24,*EL,*P10:24,*V2LON:
                    "** Generating Report **",*W2;
.
         CLOCK      TIME,CTIMEIS
         CALL       IBACLOCK
.
....     MOVE       DRGTDTE,TODATE
         MOVE       DRGTDTE,ACTVTDTE
         MOVE       ZERO,CPAGENO
         MOVE       SELMTH,SMONTH
         CLOCK      TIME,CTIMEIS
         BRANCH     OPTION,GENREP
.
         MOVE       SMONTH,KEY6
         CALL       RDDRGA1
OKPROC2  MOVE       DRGFDTE,ACTVFDTE
         MOVE       DRGTDTE,ACTVTDTE
.
         DISPLAY    *P1:24,*EL,*P18:24,CPERMTH,"/Year :":
                    *P50:24,"Ward :";
.
GENREP   CALL       HEAD
         MOVE       ZERO,ASTFLAG
         MOVE       SP3,SAVWRD
         MOVE       ZERO,TOTBED
         CALL       INITGT
         CALL       INITUT
.
         UNPACK     SMONTH,XCENT,XYEAR,XMON
         DISPLAY    *P50:24,*V2LON,XMON,*HOFF,SLASH,*V2LON,XCENT,XYEAR;
.
.
         BRANCH     OPTTYP,STARTW,STARTU
.
.*******************************************************************************
.                   Ward Processing                                            *
.*******************************************************************************
STARTW   MOVE       SP6,KEY6
         CALL       RDSWARD1                     * position at start of wards
         CALL       RDKWARD1                     * read next record
         COMPARE    ZERO,OVRCD                   * record on file ?
         GOTO       NEWWARD IF EQUAL             * yes
ZZZ      MOVE       ZERO,CPAGENO                 * no
         GOTO       CHKEND 
.
STARTU   MOVE       SP20,KEY12
         MOVE       "~~~",SAVUNT
         CALL       RSPTMSD1
NEXTMSD  CALL       RKPTMSD1
         COMPARE    ZERO,OVRCD                   * record on file ?
         GOTO       CHKDATE IF EQUAL             * yes
         CALL       UNTOT
         GOTO       CHKEND
.
CHKDATE  MATCH      PTMDDATE,SMONTH
         GOTO       NEXTMSD IF NOT EQUAL
.
GETUD    MOVE       "Dept. not in Codes.",UNDES
         PACK       KEY5,CODEAC,PTMDDEPT
         CALL       RDCODE1                      * position at start of unit
         BRANCH     OVRCD,NODESC                 * record on file ?
.
         MOVE       TDESC,UNDES
.
NODESC   MOVE       PTMDWARD,SAVWRD
         PACK       KEY6,PTMDWARD,SP6
         CALL       RDWARD1                      * position at start of wards
         BRANCH     OVRCD,NEXTMSD                * record on file ?
.
NEWWARD  DISPLAY    *P72:24,*V2LON,WWARD;
         MOVE       WWARD,SAVWRD
         MOVE       WBDESC,WRDES
.
         MOVE       ONE,ACTVWFLG                 * don't get weekend days
         PROC       WDACTVDY                     * get active days
.
ENDOFWRD CALL       RDKWARD1                     * read next record
         IF         OVRCD = 1
           IF         ACTVDAYS = 0
             GOTO       CHKEND
           ENDIF
           GOTO       NEWWRD
         ENDIF
         MATCH      SAVWRD,WWARD
         GOTO       ENDOFWRD IF EQUAL
.
NEWWRD   COMPARE    ZERO,ACTVDAYS                * ward active ?
         GOTO       NEWWRD9 IF EQUAL             * no - ignore
         IF         PTCNH82W = 1
           ADD        ACTVBDAY,GTBEDAY
           ADD        ACTVBDAY,UTBEDAY
         ENDIF
.
         MOVE       TOTBED,FORM4
         ADD        TOTBED,FORM4B
         CALL       RDPWARD1
.
         GOTO       GETMST
.
NEWWRD9  BRANCH     OPTTYP,NEWWARD,NEXTMSD
.
.        Get monthly statistics for ward
.
GETMST   PACK       KEY15,WWARD,SP20
         CALL       RDSMSTS1
NEXTMST  CALL       RDKMSTS1
         BRANCH     OVRCD OF MSTEND
.
         MATCH      MSDATE,SMONTH
         GOTO       NEXTMST IF NOT EQUAL
.
         MATCH      "~~~",SAVUNT
         IF         @EQUAL
           CALL       UNHEAD
         ENDIF
.
         COMPARE    OPTTYP,TWO
         IF         @EQUAL
           MATCH      PTMDDEPT,SAVUNT
           IF         !@EQUAL
             CALL       UNTOT
             CALL       UNHEAD
           ENDIF
         ENDIF
.
         MATCH      SAVWRD,MSWARD
         GOTO       MSTEND IF NOT EQUAL
.
         IF         PTCNH82W = 0
           CALL       ZERO0000                   * see if blank record
           COMPARE    ZERO,FORM10                * blank record ?
           GOTO       MSTEND IF EQUAL            * yes
         ENDIF
.
.
         COMPARE    SIXTY,CLNO
         CALL       HEAD IF NOT LESS
.
         CALL       PRTWRDDT
.
.        Check if we are printing separation bed days ?
.
         MOVE       ZERO,OPWANCAS
         BRANCH     PTOCBDAY,NXTM10
.
.        No, get the operation main theatre
.
         PACK       KEY9 WITH MSDATE,MSWARD
         CALL       RDOPWAR1
         GOTO       NXTM20
.
.        Yes, print it on the last column
.
NXTM10   MOVE       MSSEPBD,OPWANCAS
.
NXTM20   GOTO       SAMEWRD
.
MSTEND   IF         PTCNH82W = 1
           ADD        TOTBED,GTTBED
           ADD        TOTBED,UTTBED
           GOTO       WRDDETLS
         ENDIF
.
NEXTWRD  CALL       RDKWARD1
         BRANCH     OVRCD,CHKEND
         GOTO       NEWWRD9
.
WRDDETLS COMPARE    SIXTY,CLNO
         CALL       HEAD IF NOT LESS
         CALL       PRTWRDDT
         CALL       INITVAR
         MOVE       ZERO,OPWANCAS
         MOVE       ZERO,MSTRIN
         MOVE       ZERO,MSDSCH
         MOVE       ZERO,MSTROUT
         CALL       PRTWRDST
         GOTO       NEXTWRD
.
.        Print Ward details
.
PRTWRDDT PRINT      *1,"|",WRDES," ",WWARD,*27,"|",FORM4;
         IF         ACTVBCHG = 1
           PRINT      "*";
           MOVE       ONE,ASTFLAG
         ENDIF
         RETURN
.
SAMEWRD  MOVE       MSBBDAY,TOTBDAY
         ADD        MSNBDAY,TOTBDAY      * Total number of bed days
.
         MOVE       MSNPRAD,TOTPAT
         ADD        MSNPBAD,TOTPAT
         ADD        MSBPRAD,TOTPAT
         ADD        MSBPBAD,TOTPAT       * Total number of patients
.
         COMPARE    ZERO,ACTVDAYS
         GOTO       NOAVDAY IF EQUAL
.
         ASSIGN     (TOTBDAY/ACTVDAYS),AVDAY
         ADD        AVDAY,GTAVDAY
         ADD        AVDAY,UTAVDAY 
.
NOAVDAY  MOVE       ZERO,PCENTG
         COMPARE    ZERO,ACTVBDAY
         GOTO       NOPCENT IF EQUAL
.
         COMPARE    ZERO,TOTBDAY
         GOTO       NOPCENT IF EQUAL
.
         ASSIGN     ((TOTBDAY*100)/ACTVBDAY),PCENTG
.
NOPCENT  MOVE       ZERO,AVSTAY
         MOVE       MSDSCH,FORM8        * to calculate the Av. length of stay
         ADD        MSTROUT,FORM8
.
         BRANCH     PTOCBDAY,NOPC100
         COMPARE    ZERO,FORM8
         GOTO       NOAVST IF EQUAL
.
         MOVE       TOTBDAY,AVSTAY
         IF         FORM8 > 0
           DIV        FORM8,AVSTAY
         ENDIF
         GOTO       NOAVST
.
NOPC100  MOVE       MSSEPBD,AVSTAY
         IF         FORM8 > 0
           DIV        FORM8,AVSTAY        * Average length of stay
         ENDIF
.
NOAVST   CALL       PRTWRDST
.
         ADD        OPWANCAS,GTWAN
         ADD        OPWANCAS,UTWAN
         IF         PTCNH82W = 0
           ADD        ACTVBDAY,GTBEDAY
           ADD        ACTVBDAY,UTBEDAY 
         ENDIF
         ADD        TOTPAT,GTADM
         ADD        TOTPAT,UTADM 
         ADD        MSTRIN,GTRIN
         ADD        MSTRIN,UTRIN
         ADD        MSDSCH,GTDSH
         ADD        MSDSCH,UTDSH
         ADD        MSTROUT,GTROUT
         ADD        MSTROUT,UTROUT
         ADD        TOTBDAY,GTBDAY
         ADD        TOTBDAY,UTBDAY
         ADD        TOTBED,GTTBED
         ADD        TOTBED,UTTBED
.
         ADD        MSLVBD,GTLEAVE
         ADD        MSLVBD,UTLEAVE
         ADD        MSAEADM,GTAAE
         ADD        MSAEADM,UTAAE
         ADD        MSDEATH,GTDEATH
         ADD        MSDEATH,UTDEATH
.
         ADD        MSBBDAY,GTBOARD
         ADD        MSBBDAY,UTBOARD
         ADD        MSNPRAD,GTNPRAD
         ADD        MSNPRAD,UTNPRAD
         ADD        MSNPBAD,GTNPBAD
         ADD        MSNPBAD,UTNPBAD
         ADD        MSBPRAD,GTBPRAD
         ADD        MSBPRAD,UTBPRAD
         ADD        MSBPBAD,GTBPBAD
         ADD        MSBPBAD,UTBPBAD
.
NXTMST   CALL       RDKMSTS1
         BRANCH     OVRCD,NEXTWRD
.
         MATCH      MSDATE,SMONTH
         GOTO       NXTMST IF NOT EQUAL
.
         MATCH      SAVWRD,MSWARD
         GOTO       NEXTWRD IF NOT EQUAL
         PRINT      *1,"|";
         CALL       INITVAR
         GOTO       SAMEWRD
.
PRTWRDST COMPARE    SIXTY,CLNO
         GOTO       PRTWDST1 IF LESS
         CALL       HEAD
         CALL       PRTWRDDT
.
PRTWDST1 PRINT      *33,"|",TOTPAT:
                    *44,"|",MSTRIN,*55,"|",MSDSCH,*66,"|",MSTROUT:
                    *77,"|",AVDAY,*88,"|",PCENTG,"%",*99,"|",AVSTAY:
                    *110,"|",TOTBDAY,*121,"|   ",OPWANCAS,*132,"|"
         ADD        ONE,CLNO
         RETURN
.
CHKEND   CALL       PRTLINE
         COMPARE    ZERO TO CPAGENO
         GOTO       CHK10 IF EQUAL
.
         MOVE       ZERO,UTPCENT
         COMPARE    ZERO,UTBDAY
         GOTO       NOUTP IF EQUAL
.
         COMPARE    ZERO,UTBEDAY
         GOTO       NOUTP IF EQUAL
.
         ASSIGN     ((UTBDAY*100)/UTBEDAY),UTPCENT
.
NOUTP    MOVE       ZERO,UTAVST
         MOVE       ZERO,FORM8A
         MOVE       UTDSH,FORM8A
         BRANCH     PTCNALOS,DSCHONLU      * Calc ALOS using Discharges only
.
         ADD        UTROUT,FORM8A          * Total number of disch + tran.out
.
DSCHONLU BRANCH     PTOCBDAY,CALLOSU
         COMPARE    ZERO,FORM8A
         GOTO       CAL100U IF EQUAL
.
         MOVE       UTBDAY,UTAVST         * Use the occupied bed days to calc.
         DIV        FORM8A,UTAVST         * Average length of stay
         GOTO       CAL100U
.
CALLOSU  MOVE       UTWAN,UTAVST           * Use the separation bed days
         DIV        FORM8A,UTAVST          * to calculate Av.LOS
.
CAL100U  ADD        UTAAE,UTTHEA
         MOVE       UTTBED,FORM4B
.
         MOVE       ZERO,GTPCENT
         COMPARE    ZERO,GTBDAY
         GOTO       NOGTP IF EQUAL
.
         COMPARE    ZERO,GTBEDAY
         GOTO       NOGTP IF EQUAL
.
         ASSIGN     ((GTBDAY*100)/GTBEDAY),GTPCENT
.
NOGTP    MOVE       ZERO,GTAVST
         MOVE       ZERO,FORM8
         MOVE       GTDSH,FORM8
         BRANCH     PTCNALOS,DSCHONLY      * Calc ALOS using Discharges only
.
         ADD        GTROUT,FORM8           * Total number of disch + tran.out
.
DSCHONLY BRANCH     PTOCBDAY,CALLOS
         COMPARE    ZERO,FORM8
         GOTO       CAL100 IF EQUAL
.
         MOVE       GTBDAY,GTAVST         * Use the occupied bed days to calc.
         DIV        FORM8,GTAVST          * Average length of stay
         GOTO       CAL100
.
CALLOS   MOVE       GTWAN,GTAVST           * Use the separation bed days
         DIV        FORM8,GTAVST           * to calculate Av.LOS
.
CAL100   ADD        GTAAE,GTTHEA
         MOVE       GTTBED,FORM4
.
.        Printing the end of report
.
         PRINT     *1,"| Hospital Total ",*27,"|",FORM4;
         IF        ASTFLAG = 1
           PRINT     *32,"*";
         ENDIF
         PRINT     *33,"|",GTADM:
                   *44,"|",GTRIN,*55,"|",GTDSH,*66,"|",GTROUT:
                   *77,"|",GTAVDAY,*88,"|",GTPCENT,"%",*99,"|",GTAVST:
                   *110,"|",GTBDAY,*121,"| ",GTWAN,*132,"|"
         ADD       ONE,CLNO
.
         COMPARE   "55",CLNO
         CALL      HEAD IF NOT LESS
.
.        Get the number of new born 
.
         MOVE      ZERO,BRADMN
         PACK      KEY6,SMONTH
         CALL      RDBRNS1
.
         CALL      PRTLINE
.
         PRINT     *N,*1,"Births = ",BRADMN:
                   *75,"Trial Leave (Bed Days) =",*100,GTLEAVE:
                   *114,"A & E = ",GTAAE:
                   *N,*1,"Deaths = ",GTDEATH;
.
         BRANCH    PTOCBDAY,PRT100
         PRINT     *75,"   Boarders (Bed Days) =",*100,GTBOARD:
                   *114,"Total = ",GTTHEA;
.
PRT100   PRINT     " "
         ADD       FOUR,CLNO
.
         CALL      PRTLINE
.
.        Add Private Boarders to Public. As Private boarder is always zero
.
         ADD       GTBPRAD,GTBPBAD
         MOVE      ZERO,GTBPRAD
.
         MOVE      GTNPBAD,TOTPB      * total public patients
         ADD       GTBPBAD,TOTPB
.
         MOVE      GTNPRAD,TOTPR      * total private patients
         ADD       GTBPRAD,TOTPR
.
         COMPARE   "55",CLNO
         CALL      HEAD IF NOT LESS
.
         PRINT     *68,"Patients",*81,"Boarders",*94,"   Total":
                   *N,*1,"Total Hospital Patients Admitted:":
                   *68,GTNPBAD,*81,GTBPBAD,*94,TOTPB:
                   *N,*1,"Total Private  Patients Admitted:":
                   *68,GTNPRAD,*81,GTBPRAD,*94,TOTPR
         ADD       THREE,CLNO
.
         COMPARE   "55",CLNO
         CALL      HEAD IF NOT LESS
.
.        Printing total inpatients based on catchment code
.
         CALL      PRTLINE
         PRINT     *N,*1,"Total Inpatient Admitted From ";
         ADD       TWO,CLNO
.
         MOVE      SP10,KEY9
         CALL      RDSCSTS1
NXTCST   CALL      RDKCSTS1
         BRANCH    OVRCD OF CHK10
.
         MATCH     CCCYYMM,SMONTH
         GOTO      NXTCST IF NOT EQUAL
.
         MOVE      "UNALLOCATED CODES",TDESC
         MATCH     SP3,CCCODE
         GOTO      PCATCH IF EQUAL
.
         PACK      KEY5 WITH CODECH,CCCODE
         CALL      RDCODE1
         COMPARE   ZERO,OVRCD
         GOTO      PCATCH IF EQUAL
.
         MOVE      "UNKNOWN CODES",TDESC
.
PCATCH   MOVE      CCADMN,TOTPAT
         ADD       CCSAMED,TOTPAT
.
         MOVE      TOTPAT,TOTCST
         ADD       CCBOARD,TOTCST
.
         PRINT     *35,TDESC,*68,TOTPAT,*81,CCBOARD,*94,TOTCST
         ADD       ONE,CLNO
         COMPARE   "55",CLNO
         CALL      HEAD IF NOT LESS
.
         GOTO      NXTCST
.
.
CHK10    BRANCH    OPTION OF ENDREP
         PRINT     *N;
         CALL      PRTLINE
.
.        Check the range of date
.
         CALL      CNXTM
         MATCH     "999999",SMONTH
         GOTO      GENREP IF NOT EQUAL
.
ENDREP   COMPARE   ZERO TO CPAGENO
         GOTO      END10 IF NOT EQUAL
.
         CALL      HEAD
         PRINT     *N,"  ***  End of Report  ***"
         DISPLAY   *P10:24,*EL,*V2LON:
                   "** No Report Generated **",*W2;
         GOTO      BRNOPT
.
END10    COMPARE   "47",CLNO
         GOTO      END15 IF LESS
         CALL      HEAD
         GOTO      END17
.
END15    CALL      PRTLINE
.
END17    PRINT   *N,*N,*N:
                 *1,"The Number of Births = No. of Registrations Made ":
                 "Using Input Newborn Funcion ":
                 *N,*N,*1,"The Daily Average =             Occupied Bed Days":
                 *60,"The Average Percentage Occupancy = Daily Average":
                 *N,*21,"-------------------------------------":
                 *95,"--------------------- * 100":
                 *N,*1,"                    No. of Days Ward was Active in ":
                 "Period",*95,"Number of Beds Active";
.
         BRANCH   PTOCBDAY,END20
         PRINT    *N,*N,*1,"The Average Length of Stay =  Occupied Bed Days";
         GOTO     END30
.
END20    PRINT   *N,*N,*1,"The Average Length of Stay =  Separation Bed Days";
.
END30    PRINT      *60,"Discharges = Discharges + Deaths":
                    *N,*1,"                              ---------------------"
          IF        PTCNALOS=1
            PRINT   *1,"                              No of Discharges";
          ELSE
            PRINT   *1,"                              No of Disch.+Tran.out";
          ENDIF
.
.        SA - Removed the line below as the beds column has always been based
.        upon the no of beds for occupancy stats (WOCCBED).
.
.        PRINT     *N,*N,*1,"The column headed `Beds' contains the number":
.                           " of registered beds and is not used in the ":
.                           "occupancy statistical calculation";
.
         IF         ASTFLAG = 1
           PRINT      *N,*N,*1,"* - Some wards changed number of `Beds' during":
                      " period.";
         ENDIF
         PRINT     *N,*N,"*** End of Report ***"
.
         DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                   "** Report Generation Completed **",*W2;
         GOTO      INITIT
.
.
.       Get the next month
.
CNXTM   CALL       RDKDRGA1             * Get the next period
        BRANCH     OVRCD,CNXTM90        * No More periods
.
        PACK       SMONTH,DRGYR,DRGNUM  * New period
.
        MATCH      SMONTH,ENDMTH        * Period within range requested ?
        GOTO       CNXTM90 IF LESS
        MOVE       DRGTDTE,CENDDT
        MOVE       DRGFDTE,ACTVFDTE
        MOVE       DRGTDTE,ACTVTDTE
        IF         ASTFLAG = 1
          COMPARE    FIFTY5,CLNO
          CALL       HEAD IF NOT LESS
.
          PRINT      *N,*1,"* - Some wards changed number of `Beds' during ":
                     "period."
        ENDIF
        RETURN
.
CNXTM90 MOVE       "999999",SMONTH      * Force termination of loop
        RETURN
.
.*******************************************************************************
.         Heading Routines                                                     *
.*******************************************************************************
UNHEAD   COMPARE    SIXTY,CLNO
         CALL       HEAD IF NOT LESS
         CALL       PRTUNTDT
         MOVE       PTMDDEPT,SAVUNT
UNHEAD8  RETURN
.
.        Print Unit details
.
PRTUNTDT PRINT      *1,"|",UNDES," ",PTMDDEPT,*27,"|":
                    *33,"|":
                    *44,"|",*55,"|",*66,"|":
                    *77,"|",*88,"|",*99,"|":
                    *110,"|",*121,"|   ",*132,"|"
         IF         ACTVBCHG = 1
           PRINT      "*";
           MOVE       ONE,ASTFLAG
         ENDIF
         RETURN
.
HEAD      COMPARE   ZERO,CPAGENO
          CALL      PRTLINE IF NOT EQUAL
.
          MOVE      SP20,DRGMDSC
          MOVE      SP20,DRGCYR
          MOVE      SMONTH,KEY6
          CALL      RDDRGA1
.
          MOVE     ONE,CNOUNDLN 
          CALL     IBACLOCK
          CALL     HEAD132
          MOVE     THREE,CLNO 
.
          UNPACK    SVSTDATE,FYR,FMTH,FDAY
          UNPACK    PRFRDATE,TYR,TMTH,TDAY
          UNPACK    CDATE,WKDAY,D1,WKMTH,D1,WKYR
          PACK      WKDATE,WKYR,WKMTH,WKDAY
          MATCH     PRFRDATE,WKDATE
          GOTO      HEAD1 IF NOT LESS 
.
          MOVE      WKDAY,TDAY
.
HEAD1     PRINT     *1,"Inpatients",*40," *** ":
                    FDAY,SLASH,FMTH,SLASH,FYR:
                    " to ",TDAY,SLASH,TMTH,SLASH,TYR," ***" 
.
          CALL      PRTLINE
.
          PRINT     *1,"|",*27,"|",*33,"|",*41,"Arrivals":
                    *55,"|",*61,"Departures":
                    *77,"|   Daily",*88,"| Average",*99,"|  Average":
                    *110,"| Occupied";
.
          BRANCH    PTOCBDAY,HEAD10
          PRINT     *121,"|Operation",*132,"|";
          GOTO      HEAD20
.
HEAD10    PRINT     *121,"|Separation",*132,"|";
.
HEAD20    PRINT     *N,*1,"|   Ward",*27,"|Beds ":
                    *33,"|---------------------":
                    *55,"|---------------------",*77,"|  Average":
                    *88,"|Percentage",*99,"|   Length":
                    *110,"|   Bed";
.
          BRANCH    PTOCBDAY,HEAD30
          PRINT     *121,"|  Main",*132,"|";
          GOTO      HEAD40
.
HEAD30    PRINT     *121,"|   Bed",*132,"|";
.
HEAD40    PRINT     *N,*1,"|",*27,"|",*33,"|   Admit",*44,"| Trans In":
                    *55,"|   Disch",*66,"| Trans Out",*77,"|",*88,"|Occupancy":
                    *99,"|  Of Stay",*110,"|   Days";
.
          BRANCH    PTOCBDAY,HEAD50
          PRINT     *121,"| Theatre",*132,"|"
          GOTO      HEAD60
.
HEAD50    PRINT     *121,"|   Days",*132,"|"
.
HEAD60    CALL      PRTLINE
          MOVE      TEN,CLNO
          RETURN
.
.
PRTLINE   PRINT    *1,"*--------------------------------------------------":
                      "--------------------------------------------------":
                      "------------------------------*"
          ADD      ONE,CLNO
          RETURN
.
.*******************************************************************************
.         Unit Totals                                                          *
.*******************************************************************************
UNTOT    MOVE       ZERO,UTPCENT
         COMPARE    ZERO,UTBDAY
         GOTO       UNTOT1 IF EQUAL
.
         COMPARE    ZERO,UTBEDAY
         GOTO       UNTOT1 IF EQUAL
.
         ASSIGN     ((UTBDAY*100)/UTBEDAY),UTPCENT
.
UNTOT1   MOVE       ZERO,UTAVST
         MOVE       ZERO,FORM8A
         MOVE       UTDSH,FORM8A
         BRANCH     PTCNALOS,UNTOT2        * Calc ALOS using Discharges only
.
         ADD        UTROUT,FORM8A          * Total number of disch + tran.out
.
UNTOT2   BRANCH     PTOCBDAY,UNTOT3 
         COMPARE    ZERO,FORM8A
         GOTO       UNTOT4 IF EQUAL
.
         MOVE       UTBDAY,UTAVST         * Use the occupied bed days to calc.
         DIV        FORM8A,UTAVST         * Average length of stay
         GOTO       UNTOT4 
.
UNTOT3   MOVE       UTWAN,UTAVST           * Use the separation bed days
         DIV        FORM8A,UTAVST          * to calculate Av.LOS
.
UNTOT4   ADD        UTAAE,UTTHEA
         MOVE       UTTBED,FORM4B
.
.
         CALL      PRTLINE
         COMPARE   "55",CLNO
         CALL      HEAD IF NOT LESS
.
         PRINT     *1,"| Unit Total ",*27,"|",FORM4B;
         IF        ASTFLAG = 1
           PRINT     *32,"*";
         ENDIF
         PRINT     *33,"|",UTADM:
                   *44,"|",UTRIN,*55,"|",UTDSH,*66,"|",UTROUT:
                   *77,"|",UTAVDAY,*88,"|",UTPCENT,"%",*99,"|",UTAVST:
                   *110,"|",UTBDAY,*121,"| ",UTWAN,*132,"|"
         ADD       ONE,CLNO
.
         CALL      PRTLINE
.
         COMPARE   "55",CLNO
         CALL      HEAD IF NOT LESS
         CALL      INITUT
UNTOT9   RETURN
.
.
.         Initialize the variables
.
INITVAR   MOVE     ZERO,TOTPAT
          MOVE     ZERO,AVDAY
          MOVE     ZERO,PCENTG
          MOVE     ZERO,AVSTAY
          MOVE     ZERO,TOTBDAY
          RETURN     
.
INITGT    MOVE     ZERO,GTADM
          MOVE     ZERO,GTRIN
          MOVE     ZERO,GTDSH
          MOVE     ZERO,GTROUT
          MOVE     ZERO,GTAVDAY
          MOVE     ZERO,GTPCENT
          MOVE     ZERO,GTAVST
          MOVE     ZERO,GTBDAY
          MOVE     ZERO,GTTHEA
          MOVE     ZERO,GTTWOC 
          MOVE     ZERO,GTLEAVE
          MOVE     ZERO,GTAAE
          MOVE     ZERO,GTDEATH
          MOVE     ZERO,GTBOARD
          MOVE     ZERO,GTNPRAD
          MOVE     ZERO,GTNPBAD
          MOVE     ZERO,GTBPRAD
          MOVE     ZERO,GTBPBAD
          MOVE     ZERO,GTWAN
          MOVE     ZERO,GTBEDAY
          MOVE     ZERO,GTTBED
          RETURN     
.
INITUT    MOVE     ZERO,UTADM
          MOVE     ZERO,UTRIN
          MOVE     ZERO,UTDSH
          MOVE     ZERO,UTROUT
          MOVE     ZERO,UTAVDAY
          MOVE     ZERO,UTPCENT
          MOVE     ZERO,UTAVST
          MOVE     ZERO,UTBDAY
          MOVE     ZERO,UTTHEA
          MOVE     ZERO,UTLEAVE
          MOVE     ZERO,UTAAE
          MOVE     ZERO,UTDEATH
          MOVE     ZERO,UTBOARD
          MOVE     ZERO,UTNPRAD
          MOVE     ZERO,UTNPBAD
          MOVE     ZERO,UTBPRAD
          MOVE     ZERO,UTBPBAD
          MOVE     ZERO,UTWAN
          MOVE     ZERO,UTBEDAY
          MOVE     ZERO,UTTBED
          MOVE     ZERO,FORM4B
          RETURN     
.
.
.
.         Subroutine to key in a valid month and year
.
KEYMN     MOVE     TWENTY6,CCOL
          UNPACK   SP4,CYEAR,CMON
          MOVE     CCC,CCENT
.
          MOVE     ZERO,CHIGHLT
          CALL     KEYPER
          BRANCH   OVRCD,KEYMN999
.
          PACK     KEY6,ICENT,IYEAR,IMON
          REP      " 0",KEY6
          CALL     RDDRGA3
          BRANCH   OVRCD,KEYMN900
.
          DISPLAY  *P50:CVERT,*V2LON,DRGLDSC
.
          GOTO     KEYMN999
.
KEYMN900  DISPLAY  *P50:CVERT,*V2LON,*B,*EL:
                   "** Invalid Date ** ",*W2,*P50:CVERT,*EL;
          GOTO     KEYMN
.
KEYMN999  RETURN
+
.****************************************************************************
.*                           ZERO0000                                       *
.*               See if this record has zero stats                          *
.****************************************************************************
.
ZERO0000  MOVE      MSNPRAD,FORM10
          ADD       MSNPBAD,FORM10
          ADD       MSTRIN,FORM10
          ADD       MSTROUT,FORM10
          ADD       MSDSCH,FORM10
          ADD       MSNBDAY,FORM10
          ADD       MSBBDAY,FORM10
          ADD       MSDEATH,FORM10
          ADD       MSBPRAD,FORM10
          ADD       MSBPBAD,FORM10
          ADD       MSLVBD,FORM10
          ADD       MSAEADM,FORM10
          ADD       MSOBSTH,FORM10
          ADD       MSOUTTH,FORM10
          ADD       MSNOPAT,FORM10
.
ZERO9999  RETURN
+
.
         INCLUDE    PATDRGIO/INC
.
         INC       PATMSDIO/INC
.
         INC       PATMSTIO/INC
.
         INC       PATWR1IO/INC
.
         INCLUDE    PATWBHIO/INC
.
         INC       PATCODIO/INC
.
         INC       PATBRNIO/INC
.
         INC       PATCSTIO/INC
.
         INCLUDE    OPRWARIO/INC
.
         INCLUDE    CALCDAYS
         INC        DATECONV
         INCLUDE    PATCOMN3
         INC        WDACTVDY
         INCLUDE    STD001IO
.
ENDIT    CHAIN      PGM
         STOP

.******************************************************************************
.* System   : WAITING LIST                                                    *
.* Program  : IBAWAT19                                                        *
.* Desc     : Unscheduled Patients Report                                     *
.******************************************************************************
.* Author   : K.Peak                                                          *
.* Date     : 21/12/1989                                                      *
.* Mods.    :                                                                 *
.******************************************************************************
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899                         *
.*           Recompiled as WATTR1FD changes                                   *
.******************************************************************************
.*        V11.00.01 10/03/2020  Jill Parkinson Task 0879163                   *
.*                  Added SEXDSCIO include                                    *
.*        V10.05.01 05/01/2015  Ebon Clements CAR 261630                      *
.*                  Removed port number from temp file name                   *
.*        V9.04.01  01/03/2005  Peter Vela    CAR 57547                       *
.*                  Recompiled for PATVADFD and PATVADIO                      *
.*        V9.03.01  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2      *
.*                  - Add Sex Description routine SEXDSC00 with Code "R" -    *
.*                  "Intersex" added                                          *
.*        V5.08.02  01/09/2000  Tonii                                         *
.*                  Mods to accept Unknown and Indeterminate as valid patient *
.*                  sex description                                           *
.*        V5.08.01  29/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.07.02  27/10/1999 J Habasque                                     *
.*                  Recompiled for changes to WATPROFD - Active/Inactive codes*
.*        V5.07.01  16/08/1999 J.Tan                                          *
.*                  Added transfer destination description                    *
.*            V5.07.B03 12/01/1999  Nicole Harrington  5.07 rebounds Error 32 *
.*                      Fixed printing of header date                         *
.*            V5.07.B02 23/11/1998  Nicole Harrington                         *
.*                      5.07 changes                                          *
.******************************************************************************
.*            V5.01.121 13/11/90 i chung           SRF 106963                 *
.*                      Changed category for removal reasons from "BC" to "WR"*
.*        V5.01.122 02.03.93 Neeriem "I Hate Support" Dye (I.B.A.)            *
.*                  Reformat print for new address lengths                    *
.*        V5.01.123 26/05/1994 Ian Rutt                                       *
.*                  Fixed Global Recompile Bugs                               * 
.*        V5.04.01  25/06/1997  Howellsy         505                          *
.*                  Use PATDOCKY instead of PATDOCDS                          *
.*                                                                            *
.******************************************************************************
.
.$$$$$
.    Waiting List System Unscheduled Patients Report (IBAWAT19)
.    ----------------------------------------------------------
.
.    - Initialisation
.
.    - Display Header
.            - Open Files
.               PATCODE1
.               PATDO1A1
.               PATDO1A2
.               PATMA1A1
.               WATPROA1
.               WATTR1A3
.
.    - Clear Input Variables
.
.    - Display Screen Format
.
.    - Input Date Range 
.
.    - Input Unit & Doctor Range 
.
.    - Read through appropriate file
.
.    - Print Report
.
.    - END
.$$$$$
.
          INC       STD001FD
          INC       IBASEQFD/INC
          INC       PATCODFD/INC           * Patient Codes File
          INC       PATDO1FD/INC           * Patient Doctor File
          INC       PATMA1FD/INC           * Patient Master File
          INC       TFILEVAR/INC
          INC       WATPROFD/INC           * Waiting List Procedure File
          INC       WATRTDFD/INC           * Transfer destination file
          INC       WATTR1FD/INC           * Waiting List Treatment File
          INC       WEBERRFD/INC
+
.**********************************************************************
.*                           CONSTANTS                                *
.**********************************************************************
.
CODETP    INIT      "TP"                           * priority
CODEWR    INIT      "WR"                           * removal reason
CODEWU    INIT      "WU"                           * cliinic/unit
+
.**********************************************************************
.*                       GLOBAL VARIABLES                             *
.**********************************************************************
.
ALL       INIT      "ALL                 "
ADDRESS   DIM       50              * patients address
.
BTDTE     DIM       10              * patients birthdate
CODE      DIM       5
CMDLINE   DIM       50
CANDESC   DIM       20              * cancel description
COL       FORM      2
.
DIM3      DIM       3               * used in patdsdoc
DOCCODE   DIM       6
DSEX      DIM       8               * sex description                  *C-49016
.....DSEXF     INIT      "FEMALE "                                      D-49016
.....DSEXM     INIT      "MALE   "                                      D-49016
.....DSEXI     INIT      "INDETER"                                      D-49016
.....DSEXU     INIT      "UNKNOWN"                                      D-49016
DESCOPT   DIM       18              * option description
DESCOPT1  INIT      " - U/R SEQUENCE   "  
DESCOPT2  INIT      " - DOCTOR SEQUENCE"
.
ENDATE    DIM       8               * keyin todate
.
FSTDDD    FORM      2               * fromdate
FSTMON    FORM      2               * fromdate
FSTYRR    FORM      2               * fromdate
FSTCCC    FORM      2
FROMDATE  DIM       8               * fromdate for key
.
.
LNO       FORM      2
LSTDAY    FORM      2               * todate
LSTMON    FORM      2               * todate
LSTYRR    FORM      2               * todate
LSTCCC    FORM      2
.
PTCNUADT  FORM      1
PTCNDCQS  FORM      1
PFDATE    DIM       10              * print fromdate
PTDATE    DIM       10              * print todate
PNAME     DIM       50              * patients name
PTYDESC   DIM       20              * priority description
.
SCODE     DIM       3               * from unit code
SDOCT     DIM       6               * from doctor code 
SCDESC    DIM       20              * unit description
SDDESC    DIM       20              * doctor surname
STARTD    DIM       8               * keyin fromdate
.
TODATE    DIM       8               * todate for key
.
UNIQUE    FORM      8
UNTDESC   DIM       20              * unit description
VERT      FORM      2
.
WDTE1     DIM       10              * added date to waiting list
WDTE2     DIM       10              * review date to waiting list
WDESC35   DIM       35              * procedure code description
WATTMP    DIM       8
.
XCC       DIM       2
XCC2      DIM       2
XDD       DIM       2
XDD1      DIM       2
XDD2      DIM       2
XMM       DIM       2
XMM1      DIM       2
XMM2      DIM       2
XYY       DIM       2
XYY1      DIM       2
XYY2      DIM       2
.----------------------------------------------------------
.         Temporary File Layout Variables
.
SORTSTR   DIM       50
SORT1     INIT      "u9-16,1-8"  * u/r sequence
SORT2     INIT      "u17-22,1-8" * doctor sequence
WATTMPXX  IFILE SQL, FIXED=269, KEY=SORTSTR
.
.Name     Type      Length      Physical      Start
.----     ----      ------      --------      -----
DUNIQUE   DIM            8             8          1
.WMURNO   DIM            8             8          9
.WMDOCTOR DIM            6             6         17
.PNAME    DIM           50            50         23
.ADDRESS  DIM           50            50         73
.PTELEP   DIM           20            20        123
.PTELEB   DIM           20            20        143
.PSEX     DIM            3             3        163
.PBDATE   DIM            8             8        166
.WPDESC   DIM           50            50        174
.WMUNIT   DIM            3             3        222
.WPTIME   FORM           5             4        228
.WMDATE1  DIM            8             8        236
.WMDATE2  DIM            8             8        244
.WMPTY    DIM            3             3        252
.WMREMOVE DIM            3             3        255
.WMCODE   DIM            9             9        258
.DWMCNT   DIM            2             2        267
.
.End of Record                                  269
.----------------------------------------------------------
.
.
PRGID     INIT      "IBAWAT19"
PRGNAM    INIT      "Unscheduled Patients Report"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                           MAINLINE                                 *
.**********************************************************************
.
          CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      CLR0000                * clear input variables
.
          CALL      OPTN0000               * select option         
          BRANCH    EXIT,ML9999
.
          CALL      DOPT0000               * display option selected
.
          CALL      DSC0000                * display screen   
          CALL      DAT0000                * select date range
          CALL      RNGE0000               * select unit & doctor ranges
.
          CALL      CONT0000               * ok to continue
          BRANCH    EXIT,ML1000,ML0100,ML9999
.
ML1000    CALL      CREA0000               * create temporary file
          MOVE      ONE,UNIQUE
.
          CALL      CODE0000               * sort data 
          BRANCH    EXIT,ML9999
.
          CALL      PRNT0000               * print report
          CALL      END0000                * end of report
          GOTO      ML0100
.
ML9999    CLOSE     WATTMPXX               * close temporary file
          CALL      KILL0000               * remove temporary file
          CHAIN     PGM                    * chain back to program
+
.**********************************************************************
.*                             INIT0000                               *
.*                      Initialisation  Routine                       *
.**********************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"watproaf";
          OPEN      WATPROA1,"watproaf"
.
          DISPLAY   *P64:24,"wattr1af";
          OPEN      WATTR1A3,"wattr1af"
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,FIFTY9;*220,PTCNUADT
.
          CALL      TFILENAM * Get temp file name in create
          MOVE      TFILNAME,WATTMP
.
          RETURN
+
.**********************************************************************
.*                             CLR0000                                *
.*                 Routine to Clear Local Variables                   *
.**********************************************************************
.
CLR0000   MOVE      ZERO,CPAGENO
          MOVE      "60",LNO
.
. ----- Clear the Procedure File Variables -----
.
          MOVE      SP8,WMDATE1
          MOVE      SP8,WMDATE2
          MOVE      SP3,WMREMOVE
          MOVE      SP6,WMDOCTOR
          MOVE      SP3,WMPTY    
.
. ----- Clear the Treatment File Variables -----
.
          PACK      WPDESC,SP20,SP20,SP20
          MOVE      ZERO,WPTIME
          RETURN
+
.**********************************************************************
.*                             OPTN0000                               *
.*                          Select Option                             *
.**********************************************************************
.
OPTN0000  MOVE      FALSE,EXIT
.
          HLINE     *G37,2,54,80
          DISPLAY   *P1:3,*EF:
                    *P2:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P2:5,*V2LON,ONE,*HOFF,". U/R Sequence":
                    *P2:6,*V2LON,TWO,*HOFF,". Doctor Sequence"
.
OPTN1000  DISPLAY   *P1:8,*EL,"Option : "
          KEYIN     *P10:8,*V2LON,OPTION
.
          BRANCH    OPTION,OPTN9999,OPTN9999
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9000 IF EQUAL
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      TRUE,EXIT
OPTN9999  RETURN
+
.**********************************************************************
.*                             DOPT0000                               *
.*                          Display Option                            *
.**********************************************************************
.
DOPT0000  BRANCH    OPTION,DOPT1000,DOPT2000
.
DOPT1000  DISPLAY   *P54:2,*V2LON,"- U/R Sequence "
          MOVE      SORT1,SORTSTR
          GOTO      DOPT9999
.
DOPT2000  DISPLAY   *P54:2,*V2LON,"- Doctor Sequence "
          MOVE      SORT2,SORTSTR
.
DOPT9999  RETURN
+
.**********************************************************************
.*                         DSC0000                                    *
.*                      Display Screen                                *
.**********************************************************************
.
DSC0000   DISPLAY   *P1:10,*EL,"From Date : ":
                    *P1:12,*EL,"To   Date : ":
                    *P1:14,"Unit      : ":
                    *P1:16,"Doctor    : "
.
          RETURN
+
.**********************************************************************
.*                         DAT0000                                    *
.*                       Keyin Dates                                  *
.**********************************************************************
.
.         keyin from date
.
DAT0000   MOVE      FALSE,EXIT
          UNPACK    SP8,CCENT,CYEAR,CMON,CDAY  * set up defaults
          MOVE      "13",CCOL            * column for keyin
          MOVE      "10",CVERT           * row for keyin
          MOVE      "30",ECOL            * column for error
          MOVE      "10",EVERT           * row for error
          MOVE      ZERO,CHIGHLT         * 0 = highlight 1 = no highlight
          MOVE      ONE,CDEFDTE          * 0 = check all fields for default
.                                        * 1 = check day only for default
          CALL      KEYDATE
          BRANCH    OVRCD,DAT9000        * no date keyed in
.
          MOVE      CDAY,FSTDDD
          MOVE      CMON,FSTMON
          MOVE      CYEAR,FSTYRR
          MOVE      CCENT,FSTCCC
.
          MOVE      FSTDDD,LSTDAY
          MOVE      FSTMON,LSTMON
          MOVE      FSTYRR,LSTYRR
          MOVE      FSTCCC,LSTCCC
.
          PACK      STARTD WITH FSTCCC,FSTYRR,FSTMON,FSTDDD
          PACK      FROMDATE WITH FSTCCC,FSTYRR,FSTMON,FSTDDD
          PACK      PFDATE WITH FSTDDD,SLASH,FSTMON,SLASH,FSTCCC,FSTYRR
          REP       " 0",STARTD
          REP       " 0",FROMDATE
          REP       " 0",PFDATE
          RESET     STARTD
          RESET     FROMDATE
          RESET     PFDATE
.
.         keyin to date
.
DAT1000   UNPACK    SP6,CYEAR,CMON,CDAY  * set up defaults
          MOVE      "13",CCOL            * column for keyin
          MOVE      "12",CVERT           * row for keyin
          MOVE      "30",ECOL            * column for error
          MOVE      "12",EVERT           * row for error
          MOVE      ZERO,CHIGHLT         * 0 = highlight 1 = no highlight
          MOVE      ONE,CDEFDTE          * 0 = check all fields for default
.                                        * 1 = check day only for default
          CALL      KEYDATE
          BRANCH    OVRCD,DAT2000        * no date keyed in
.
          MOVE      CDAY,FSTDDD
          MOVE      CMON,FSTMON
          MOVE      CYEAR,FSTYRR
          MOVE      CCENT,FSTCCC
          GOTO      DAT3000
.
DAT2000   MOVE      CDAY,FSTDDD
          MOVE      CMON,FSTMON
          MOVE      CYEAR,FSTYRR
          MOVE      CCENT,FSTCCC
          DISPLAY   *P29:12,*V2LON,CDD,SLASH,CMM,SLASH,CCC,CYY
.
DAT3000   MOVE      FSTDDD,LSTDAY
          MOVE      FSTMON,LSTMON
          MOVE      FSTYRR,LSTYRR
          MOVE      FSTCCC,LSTCCC
.
          PACK      ENDATE WITH LSTCCC,LSTYRR,LSTMON,LSTDAY
          PACK      TODATE WITH LSTCCC,LSTYRR,LSTMON,LSTDAY
          PACK      PTDATE WITH LSTDAY,SLASH,LSTMON,SLASH,LSTCCC,LSTYRR
          REP       " 0",ENDATE
          REP       " 0",TODATE
          REP       " 0",PTDATE
          RESET     ENDATE
          RESET     TODATE
          RESET     PTDATE
.
.         match dates
.
          MATCH     STARTD,ENDATE
          RETURN    IF NOT LESS
.
          DISPLAY   *P30:12,*V2LON,*B:
                    "** Ending Date > Starting Date **",*W,*P30:12,*EL
          GOTO      DAT1000
.
DAT9000   MOVE      TRUE,EXIT
DAT9999   RETURN
+
.**********************************************************************
.*                             RNGE0000                               *
.*                          Select Range                              *
.**********************************************************************
.
RNGE0000  MOVE      SP3,SCODE
          MOVE      SP6,SDOCT
          MOVE      SP20,SCDESC
          MOVE      SP20,SDDESC
.
RNGE1000  KEYIN     *P13:14,*EL,*V2LON,SCODE
.
          ENDSET    SCODE
          APPEND    SP3,SCODE
          RESET     SCODE
.
          MATCH     QUEST,SCODE
          GOTO      RNGE2000 IF NOT EQUAL
          MOVE      SP3,SCODE
          MOVE      CODEWU,CODE
          CALL      PATCODDS
          CALL      RDSP0000
          GOTO      RNGE1000
.
RNGE2000  MATCH     SP6,SCODE
          GOTO      RNGE3000 IF NOT EQUAL
          MOVE      ALL,SCDESC                * begin at start of unit codes
          GOTO      RNGE4000
.
RNGE3000  MOVE      SP20,SCDESC
          PACK      KEY5,CODEWU,WMUNIT
          CALL      RDCODE1 
          BRANCH    OVRCD,RNGE5000
          MOVE      TDESC,SCDESC
.
RNGE4000  DISPLAY   *P24:14,SCDESC
          GOTO      RNGE6000
.
RNGE5000  DISPLAY   *P1:24,*EL,"Code Does Not Exist - Hit <",*V2LON,"ENTER":
                    *HOFF,"> to Continue";
          KEYIN     ANS;
          DISPLAY   *P1:24,*EL
          GOTO      RNGE0000
.
RNGE6000  MOVE      "13",ECOL
          MOVE      "16",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          CALL      PATDOCKY
          BRANCH    EXIT,RNGE7000,RNGE6000
.
          MOVE      DCODE,SDOCT
          GOTO      RNGE8000
.
RNGE7000  MOVE      ALL,SDDESC                * finish at end of unit codes
          MOVE      SP10,SDOCT
          GOTO      RNGE9000
.
RNGE8000  MOVE      SP20,SDDESC
          MOVE      SP1,ANS
          MOVE      DGNAME,ANS
          PACK      SDDESC,ANS,DOT,DSNAME        * unit description
.
RNGE9000  DISPLAY   *P24:16,SDDESC
          GOTO      RNGE9999
.
RNGE9999  RETURN
+
.**********************************************************************
.*                             CONT0000                               *
.*                          Ok to Continue                            *
.**********************************************************************
.
CONT0000  MOVE      FALSE,EXIT
.
          CALL      CONTQST
          MOVE      ANS,FORM1
          BRANCH    FORM1,CONT9999,CONT9000,CONT9500
.
CONT9000  MOVE      TWO,EXIT
          GOTO      CONT9999
.
CONT9500  MOVE      THREE,EXIT
CONT9999  RETURN
+
.**********************************************************************
.*                             CODE0000                               *
.*                     Sort Data by Code Sequence                     *
.**********************************************************************
.
CODE0000  MOVE      FALSE,EXIT
          DISPLAY   *P1:24,*EL,*P20:24,"U/R Number : "
.
          PACK      KEY27,FROMDATE,SP20
          CALL      RDSTREA3
CODE1000  CALL      RDKTREA3
          BRANCH    OVRCD,CODE9999
.
          MATCH     WMDATE1,TODATE
          GOTO      CODE9999 IF LESS
.
          MATCH     SP3,SCODE
          GOTO      CODE2000 IF EQUAL
.
          MATCH     WMUNIT,SCODE
          GOTO      CODE1000 IF NOT EQUAL
.
CODE2000  MATCH     SP3,SDOCT
          GOTO      CODE3000 IF EQUAL
.
          MATCH     WMDOCTOR,SDOCT
          GOTO      CODE1000 IF NOT EQUAL
.
CODE3000  DISPLAY   *P33:24,*V2LON,WMURNO
.
          MOVE      WMCODE,KEY9
          CALL      RDPROA1
.
          MOVE      WMURNO,KEY8
          CALL      RDMAST1
.
          CALL      NAME0000
          CALL      ADDR0000
          BRANCH    OPTION,CODE7000,CODE8000
.
CODE7000  PACK      KEY16,WMURNO,UNIQUE
          CALL      WRTMP1
          GOTO      CODE1000
.
CODE8000  PACK      KEY14,WMDOCTOR,UNIQUE
          CALL      WRTMP1
          GOTO      CODE1000
.
CODE9999  RETURN
+
.**************************************************************************
.*                         PRNT0000                                       *
.*                        Print Data                                      *
.**************************************************************************
.
PRNT0000  BRANCH    OPTION,PRNT1000,PRNT2000
.
PRNT1000  MOVE      SP20,KEY16
          GOTO      PRNT3000
.
PRNT2000  MOVE      SP20,KEY14
.
PRNT3000  CALL      RDSTMP1
PRNT3500  CALL      RDKTMP1
          BRANCH    OVRCD,PRNT9999
.
          COMPARE   "55",LNO
          CALL      HEAD0000 IF NOT LESS
.
.* ****************************************** Start of Changes         *C-49016
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          MOVE      DSEXDESC,DSEX
.* ******************************************   End of Changes         *C-49016
.
PRNT4500  UNPACK    WMDATE1,XCC,XYY,XMM,XDD
          PACK      WDTE1,XDD,SLASH,XMM,SLASH,XCC,XYY
.
          UNPACK    PBDATE,XCENT,XYY1,XMM1,XDD1
          PACK      BTDTE,XDD1,SLASH,XMM1,SLASH,XCENT,XYY1
.
          UNPACK    WMDATE2,XCC2,XYY2,XMM2,XDD2
          PACK      WDTE2,XDD2,SLASH,XMM2,SLASH,XCC2,XYY2
.
          MATCH     SP3,WMREMOVE
          GOTO      PRNT5000 IF EQUAL
.
          MOVE      SP20,CANDESC
          PACK      KEY5,CODEWR,WMREMOVE
          CALL      RDCODE1
          BRANCH    OVRCD,PRNT5000
          MOVE      TDESC,CANDESC
.
          PACK      KEY19,WMURNO,WMCODE,DWMCNT
          PROC      DTRNDEST               * to get the transfer destination
          MOVE      TDESC,CANDESC
.
PRNT5000  MATCH     SP3,WMPTY
          GOTO      PRNT5500 IF EQUAL
.
          MOVE      SP20,PTYDESC
          PACK      KEY5,CODETP,WMPTY
          CALL      RDCODE1
          BRANCH    OVRCD,PRNT5500
          MOVE      TDESC,PTYDESC
.
PRNT5500  MATCH     SP3,WMUNIT
          GOTO      PRNT6000 IF EQUAL
.
          MOVE      SP20,UNTDESC
          PACK      KEY5,CODEWU,WMUNIT
          CALL      RDCODE1
          BRANCH    OVRCD,PRNT6000
          MOVE      TDESC,UNTDESC
.
PRNT6000  MOVE      WPDESC,WDESC35
.
          PRINT     *1,"!",WMURNO,*12,PNAME,*64,"! ",WDESC35:
                    *101,"! Added  : ",WDTE1,*132,"!":
                    *N,*1,"!",*12,ADDRESS,*64,"! ":
                    *101,"! Review : ",WDTE2,*132,"!":
                    *N,*1,"!",*12,"Home ",PTELEP,SP2,"Bus ",PTELEB:
                    *64,"! Unit : ",UNTDESC:
                    *101,"! Pty    : ",PTYDESC,*132,"!":
                    *N,*1,"!",*12,"Sex  : ",DSEX,*39,"D.O.B.   : ",BTDTE:
                    *64,"! Stay : ",WMSTAY:
                    *75,"Duration : ",WPTIME,*101,"! Cancel : ",CANDESC,*132,"!"
.
          ADD       FOUR,LNO
          CALL      PGE0000
          GOTO      PRNT3500
.
PRNT9999  RETURN
+
.**************************************************************************
.*                         END0000                                        *
.*                      End of Report                                     *
.**************************************************************************
.
END0000   COMPARE   ZERO,CPAGENO
          GOTO      END1000 IF NOT EQUAL
.
.         No Data Found
.
          DISPLAY   *P1:24,*EL,*P20:24,*V2LON:
                    "** No Record Selected **",*W
          CALL      HEAD0000
          PRINT     *N,*N,"** END OF REPORT **"
          GOTO      END9999
.
END1000   PRINT     *N;
          PRINT     *N,*N,"** END OF REPORT **"
          DISPLAY   *P1:24,*EL,*P20:24,*V2LON:
                    "** Report Generation Completed **",*W
.
END9999   DISPLAY   *P1:14,*EF
          CLOSE     WATTMPXX
          CALL      KILL0000               * remove temporary file
          RETURN
+
.**************************************************************************
.*                         HEAD0000                                       *
.*                     Print Heading                                      *
.**************************************************************************
.
HEAD0000  CLOCK     TIME,CTIMEIS
          CALL      IBACLOCK
.
          LOAD      DESCOPT,OPTION,DESCOPT1,DESCOPT2
          MOVE      DESCOPT,CPHDROPT
.
          CALL      HEAD132
.
          PRINT     *45,"From : ",PFDATE,*70,"Unit   : ",SCODE,SP6,SCDESC,*N:
                    *45,"To   : ",PTDATE,*70,"Doctor : ",SDOCT,SP3,SDDESC,*N
.
          CALL      PGE0000
.
          PRINT     *1,"! U/R No",*12,"Personal Details":
                    *64,"! Medical Details",*101,"! Waiting List Details":
                    *132,"!"
.
          MOVE      TEN,LNO
          CALL      PGE0000
.
          RETURN
+
.**********************************************************************
.*                             PGE0000                                *
.*                     Print Page Separator                           *
.**********************************************************************
.
PGE0000   PRINT     "*---------------------------------------":
                    "----------------------------------------":
                    "----------------------------------------":
                    "-----------*" 
          ADD       ONE,LNO
.
          RETURN
+
.**********************************************************************
.*                             RDSP0000                               *
.*                        Redisplay Screen                            *
.**********************************************************************
.
RDSP0000  
          HLINE     *G37,2,54,80
          DISPLAY   *P1:3,*EF:
                    *P2:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P2:5,*V2LON,ONE,*HOFF,". Unit Sequence":
                    *P2:6,*V2LON,TWO,*HOFF,". Doctor Sequence":
                    *P1:8,*EL,"Option : ",*P10:8,*V2LON,OPTION
.
          CALL      DSC0000                    * display screen format
.
          DISPLAY   *P13:10,*V2LON,PFDATE:
                    *P13:12,PTDATE:
                    *P13:14,SCODE,*HOFF,*P24:14,SCDESC:
                    *P13:16,*V2LON,SDOCT,*HOFF,*P24:16,SDDESC
.
          RETURN
+
.**********************************************************************
.*                             CREA0000                               *
.*                     Create Temporary File                          *
.**********************************************************************
.
CREA0000  CLOSE     WATTMPXX
          CALL      KILL0000               * remove temporary file
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    WATTMP,CMDLINE
          APPEND    " 269 ",CMDLINE
          APPEND    SORTSTR,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      WATTMPXX,WATTMP
          RETURN
+
.**********************************************************************
.*                             KILL0000                               *
.*                     Remove Temporary File                          *
.**********************************************************************
.
KILL0000  PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    WATTMP,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          RETURN
+
.**********************************************************************
.*                             NAME0000                               *
.*                     Pack Patients Name                             *
.**********************************************************************
.
NAME0000 
          STRIP     PSNAME
          STRIP     PGNAME
          STRIP     PTITL
          PACK      PNAME,SP30,SP30
          CLEAR     PNAME
          APPEND    PSNAME,PNAME
          APPEND    ",",PNAME
          APPEND    SP1,PNAME
          APPEND    PTITL,PNAME
          APPEND    SP1,PNAME
          APPEND    PGNAME,PNAME
          RESET     PNAME
.
          RETURN
+
.**********************************************************************
.*                             ADDR0000                               *
.*                     Pack Patients Address                          *
.**********************************************************************
.
ADDR0000
          PACK      ADDRESS,SP70
          CLEAR     ADDRESS
.
          MATCH     SP70,PADD1
          IF        !@EQUAL
            STRIP     PADD1
            APPEND    PADD1,ADDRESS
            APPEND    ",",ADDRESS
          ENDIF
.
          MATCH     SP70,PADD2
          IF        !@EQUAL
            STRIP     PADD2
            APPEND    PADD2,ADDRESS
            APPEND    ",",ADDRESS
          ENDIF
.
          MATCH     SP70,PSUBRB
          IF        !@EQUAL
            STRIP     PSUBRB
            APPEND    PSUBRB,ADDRESS
            APPEND    ",",ADDRESS
          ENDIF
.
          MATCH     SP70,PADD4
          IF        !@EQUAL
            STRIP     PADD4
            APPEND    PADD4,ADDRESS
            APPEND    " ",ADDRESS
          ENDIF
.
          MATCH     SP70,PPOST
          IF        !@EQUAL
            SQUEEZE   PPOST
            APPEND    PPOST,ADDRESS
          ENDIF
.
          RESET     ADDRESS
.
          RETURN
+
.********************************************************************** 
.*              Temporary File Layouts                                *
.**********************************************************************
.
RDSTMP1   MOVE      ZERO,OVRCD
          BRANCH    OPTION,RSTMP1,RSTMP2
.
RSTMP1    RESET     KEY16
          READ      WATTMPXX,KEY16;;
          RETURN
.
RSTMP2    RESET     KEY14
          READ      WATTMPXX,KEY14;;
          RETURN
.
RDKTMP1   MOVE      ZERO,OVRCD
          READKS    WATTMPXX;DUNIQUE,WMURNO,WMDOCTOR,PNAME,ADDRESS:
                             PTELEP,PTELEB,PSEX,PBDATE,WPDESC,WMUNIT:
                             WPTIME,WMDATE1,WMDATE2,WMPTY,WMREMOVE
          MOVE      DUNIQUE,UNIQUE
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTMP1    MOVE      UNIQUE,DUNIQUE
          BRANCH    OPTION,WTMP1,WTMP2
.
WTMP1     RESET     KEY16
          WRITE     WATTMPXX,KEY16;DUNIQUE,WMURNO,WMDOCTOR,PNAME,ADDRESS:
                             PTELEP,PTELEB,PSEX,PBDATE,WPDESC,WMUNIT:
                             WPTIME,WMDATE1,WMDATE2,WMPTY,WMREMOVE:
                             WMCODE,DWMCNT
          ADD       ONE,UNIQUE
          RETURN
.
WTMP2     RESET     KEY14
          WRITE     WATTMPXX,KEY14;DUNIQUE,WMURNO,WMDOCTOR,PNAME,ADDRESS:
                             PTELEP,PTELEB,PSEX,PBDATE,WPDESC,WMUNIT:
                             WPTIME,WMDATE1,WMDATE2,WMPTY,WMREMOVE:
                             WMCODE,DWMCNT
          ADD       ONE,UNIQUE
          RETURN
.
.********************************************************************** 
.*              I/O INCLUDES FOR FILES AND GENERAL ROUTINES           *
.**********************************************************************
.
         INC       STD001IO
         INC       PATCODKY                * patient codes file routine
         INC       PATDOCKY                * patient doctor file routine
         INC       TFILENAM
         INC       SEXDSCIO
         INC       TRANDEST                * transfer destination
.
         INC       IBASEQIO/INC
         INC       PATCODIO/INC            * patient i/o for codes
         INC       PATDO1IO/INC            * patient i/o for doctors
         INC       PATMA1IO/INC            * patient i/o for master details
         INC       WATRTDIO/INC            * Transfer destination file
         INC       WATPROIO/INC            * w/list procedure file
         INC       WATTR1IO/INC            * w/list treatment file
         INC       WEBERRIO/INC

. **********************************************************
. *              P R O G R A M  S U M M A R Y              *
. *              -------------  -------------              *
. *                                                        *
. *     PROGRAM NAME:     FIXPRI33                         *
. *                                                        *
. *     FUNCTION:         PP OUTSTANDING DEBTORS REPORT    *
. *                                                        *
. *     DATE WRITTEN:     26/08/2014                       *
. *                                                        *
. *     AUTHOR:           J.Tan                            *
. *                                                        *
. *     MODIFICATIONS:                                     *
. *                                                        *
. *     V10.13.01 26/07/2018  J.Tan            TSK 0848506 *
. *               Changed PP Doctor from DIM6 to DIM10     *
. *                                                        *
. **********************************************************
.
          INC       STD001FD
          INC       IBASEQFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
          INC       IBACONFD/INC
          INC       IBAPASFD/INC
          INC       IBASECFD/INC
          INC       PATCODFD/INC
          INC       PRIINVFD/INC
          INC       PRIHREFD/INC
          INC       PATMA1FD/INC
.
.        EXTRA   VARIABLES
.
OUTDFILE  FILE      ASCII, FIXED=256
.
.Name     Type   Length   Start   Description
.----     ----   ------   -----   -----------
.TMPURNO   DIM        8        1   U/R Number
TMPCLMC   DIM        3        9   Claim Code
TMPADAT   DIM        8       12   Accident Date
TMPINVN   DIM        8       20   Invoice Number
TMPINVD   DIM        8       28   Invoice Date
TMPINVM   FORM      12.2     36   Invoice Amount
TMPMPRA   DIM        6       51   Medical Practice
TMPSDOC   DIM       10       57   Service Doctor
TMPPIND   DIM        3       67   Patient Indicator (Cat GI)
.EOR                70
.
WORKFILE  IFILE     SQL, FIXED=129, KEY="U1-8"
.
. ----- Tempfile consists of following vars -----
.
. Name         Type      Length     Physical     Start   Description
. ----         ----      ------     --------     -----   -----------
.PRINNUMB      DIM         8          8             1
.PRINDEBT      DIM         8          8             9
WSNAME         DIM        20         20            17
WGNAME         DIM         1          1            37
.PRINITOT      FORM       12.2        8            38
.PRINPAMT      FORM       12.2        8            46
.PRINHAMT      FORM       12.2        8            54
.PRINIAMT      FORM       12.2        8            62
.PRINMAMT      FORM       12.2        8            70
.PRINVAMT      FORM       12.2        8            78
.PRINOTHA      FORM       12.2        8            86
WBALNC         FORM       12.2        8            94
.PRINCLAM      DIM         3          3           102
.PRINPRAC      DIM         6          6           105
.PRINDOCT      DIM        10         10           111
.PRINDATE      DIM         8          8           121
. EOR                                             129
.
CMDLINE   DIM       80
FNAMED    INIT      "priosdeb"
FNAMET    DIM       8
FORM12P2  FORM      12.2
.
COUNT     FORM      2
CODE      DIM       2
CODECL    INIT      "CL"
.
DIM8      DIM       8
DIM24     DIM       24
.
FROMDATE  DIM       8
.
PNAME     DIM       22
TODATE    DIM       8
.
LN55      FORM      "55"
.
TBALNC    FORM      12.2
XDAY1     DIM       2
XMON1     DIM       2
XYEAR1    DIM       2
XCENT1    DIM       2
.
PRGID     INIT      "FIXPRI33"
PRGNAM    INIT      "PP Outstanding Debtors Report"
VERSION   INIT      "V12.01.00"
.
.         START OF PROGRAM
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
MAIN1000  CALL      KOPT0000
          BRANCH    EXIT,MAIN9999
.
          CALL      KDAT0000
          BRANCH    EXIT,MAIN1000
.
          CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN2000,MAIN1000,MAIN9999 * Yes, no, cancel
.
MAIN2000  
          CALL      PROC0000                      * processing
          CALL      PRNT0000                      * print
.
MAIN9999  CHAIN     PGM
          STOP
+
.********************************************************************
.                INIT0000                                           *
.********************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening priinvof";
          OPEN      PRIINVO1,"priinvof"
.
          DISPLAY   *P64:24,"prihreff";
          OPEN      PRIHREF1,"prihreff"
.
          DISPLAY   *P64:24,"priinvof";
          OPEN      PRIINVO2,"priinvof"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMET
.
INIT9999  RETURN
.
.********************************************************************
.         Option
.********************************************************************
KOPT0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Print Outstanding Debts"
.
          KEYIN     *P1:8,*EL,"Please Select : ",*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      KOPT9000 IF EQUAL
.
          BRANCH    OPTION OF KOPT1000
          DISPLAY   *P35:8,*EL,*B,*V2LON,"** Invalid Selection **",*W2;
          GOTO      KOPT0000
.
KOPT1000  MOVE      ZERO,EXIT
          GOTO      KOPT9999
.
KOPT9000  MOVE      ONE,EXIT
KOPT9999  RETURN
.
.********************************************************************
.         Keyin from date
.********************************************************************
KDAT0000  DISPLAY   *P1:12,*EF,"From Date : ":
                        *P1:14,"To Date   :"
.
KDAT1000  MOVE      TEN2,CVERT
          MOVE      TEN5,CCOL
          MOVE      ZERO,CHIGHLT
          MOVE      ZERO,CDEFDTE
.
          PACK      FROMDATE,CCC,CYY,CMM,CDD
          UNPACK    FROMDATE,CCENT,CYEAR,CMON,CDAY
.
          MOVE      "45",ECOL           * Error message
          MOVE      CVERT,EVERT
          CALL      KEYDATE
          BRANCH    OVRCD OF KDAT9200
.
          PACK      FROMDATE WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",FROMDATE
.
.         Keyin todate
.
          MOVE      TEN4,CVERT
          MOVE      TEN5,CCOL
.
          MOVE      "45",ECOL          * Error message
          MOVE      CVERT,EVERT
          CALL      KEYDATE
          BRANCH    OVRCD OF KDAT1000
.
          PACK      TODATE WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE
.
          MATCH     FROMDATE,TODATE
          GOTO      KDAT9000 IF LESS
.
          MOVE      ZERO,EXIT
          GOTO      KDAT9999
.
KDAT9000  DISPLAY   *P1:24,*EL,*P20:24,*V2LON,*B:
                    "** Fromdate must be less than Todate **",*W3;
          GOTO      KDAT1000
.
KDAT9200  MOVE      ONE,EXIT
KDAT9999  RETURN
+
.********************************************************************
.         Processing
.********************************************************************
PROC0000  CALL      CREA0000
.
          DISPLAY   *P1:20,*EL,*P10:20,*V2LON:
                    "** Process Outstanding Debtors **"
.
          DISPLAY   *P1:24,*EL,*P10:24:
                    "Processing Invoice Number :",*P50:24,"Scanning :"
.
          PACK      KEY16,FROMDATE,SP20
          CALL      RSPRIN2 
PROC1000  CALL      RKPRIN2
          BRANCH    OVRCD OF PROC9999
.
          MATCH     PRINDATE,TODATE
          GOTO      PROC9999 IF LESS
.
          DISPLAY   *P61:24,PRINNUMB;
.
          MOVE      PRINITOT,WBALNC
          ADD       PRINPAMT,WBALNC
          ADD       PRINHAMT,WBALNC
          ADD       PRINIAMT,WBALNC
          ADD       PRINMAMT,WBALNC
          ADD       PRINVAMT,WBALNC
          ADD       PRINOTHA,WBALNC
          ADD       PRINJAMT,WBALNC
          ADD       PRINCNTT,WBALNC
          IF        IBCNUGST=2
            ADD       PRINGSTJ,WBALNC
          ENDIF
.
          COMPARE   ZERO,WBALNC
          GOTO      PROC1000 IF EQUAL
.
          DISPLAY   *P38:24,*V2LON,PRINNUMB;
.
          PACK      PSNAME WITH SP10,SP10
          PACK      PGNAME WITH SP10,SP10,SP10
.
          MOVE      PRINDEBT,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF PROC7000
.
          MOVE      PGNAME,WGNAME
          MOVE      PSNAME,WSNAME
.
          PACK      KEY8,PRINNUMB,SP70
          CALL      WRWORK1
.
.         Read patient referral to get patient indicator
.
          PACK      KEY24,DPRINUNQ,PRINPRAC,PRINDOCT,SP70
          PACK      KEY27,KEY24,SP70
          CALL      RSPRHR1
          CALL      RKPRHR1
          BRANCH    OVRCD,PROC2000
.
          PACK      DIM24,PRHRUNIQ,PRHRPRAC,PRHRDOCT,SP70
          MATCH     KEY24,DIM24
          GOTO      PROC3000 IF EQUAL
.
PROC2000  MOVE      SP70,PRHRPIND
.
PROC3000  CALL      WRTOUTD
          GOTO      PROC1000
.
PROC7000  DISPLAY   *P1:23,*EL,*B,"U/R No.: ",*V2LON,PRINDEBT:
                    "  *** Master File Missing ":
                    "- Contact IBA *** ",*W2;
          GOTO      PROC1000
.
PROC9999  RETURN
.
.********************************************************************
.      PRINT OUTSTANDING DEBTOR REPORT
.********************************************************************
PRNT0000  DISPLAY   *P1:20,*EL,*P10:20,*V2LON:
                    "** Generating Outstanding Debtor Report **";
.
          DISPLAY   *P1:24,*EL,*P10:24:
                    "Printing Patient Name :"
.
          MOVE      ZERO,CPAGENO
          MOVE      "99",CLNO
          MOVE      ZERO,TBALNC
.
          PACK      KEY8,SP70
          CALL      RDSWORK1
PRNT1000  CALL      RDKWORK1
          BRANCH    OVRCD OF PRNT2000
. 
          COMPARE   "55",CLNO
          CALL      PRTH0000 IF NOT LESS
.
          UNPACK    PRINDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *1,"|",PRINDEBT,*12,"|",PRINNUMB,*25,"|",WBALNC:
                    *45,"|  ",PRINCLAM,*55,"| ",PRINPRAC,*65,"| ",PRINDOCT:
                    *75,"|",CPCDATE,*100,"|" 
.
          ADD       WBALNC,TBALNC
          ADD       ONE,CLNO
          GOTO      PRNT1000
.
PRNT2000  COMPARE   ZERO,CPAGENO
          GOTO      PRNT8000 IF EQUAL
.
          CALL      PRLN0000
          PRINT     *1,"| Total ",*25,"|",TBALNC,*100,"|"
          CALL      PRLN0000
.
          DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** Generation Completed Successfully **"
.
          CALL      DELW0000
          GOTO      PRNT9000
.
PRNT8000  DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** No Outstanding Debtors **"
.
PRNT9000  CLOSE        OUTDFILE
PRNT9999  RETURN
.
.*******************************************************************************
.       Print head
.*******************************************************************************
PRTH0000  UNPACK    FROMDATE,XCENT,XYEAR,XMON,XDAY
          UNPACK    TODATE,XCENT1,XYEAR1,XMON1,XDAY1
.
          MOVE      ONE,CNOUNDLN
          MOVE      SP70,CPHDROPT
          CALL      IBACLOCK
          CALL      HEAD132
.
          PRINT      *N,*40,"From Date : ",XDAY,SLASH,XMON,SLASH,XCENT,XYEAR:
                     " to ",XDAY1,SLASH,XMON1,SLASH,XCENT1,XYEAR1
.
          CALL      PRLN0000
          PRINT     *1,"| U/R No.",*12,"| Invoice No.",*25,"| Outstanding Amt":
                    *45,"| Claim ",*55,"| Med.Prac.",*65,"| Doctor":
                    *75,"| Inv.Date",*100,"|" 
          CALL      PRLN0000
          MOVE      SIX,CLNO
.
PRTH9999  RETURN
+
.*******************************************************************************
.        Print line
.*******************************************************************************
PRLN0000  PRINT     "*---------------------------":
                    "----------------------------":
                    "----------------------------":
                    "---------------*"
PRLN9999 RETURN
+
.*******************************************************************************
.
RDSWORK1 RESET     KEY8
         READ      WORKFILE,KEY8;;
         RETURN  
.
RDWORK1  MOVE      ZERO,OVRCD
         RESET     KEY8
         READ      WORKFILE,KEY8;PRINNUMB,PRINDEBT,WSNAME,WGNAME,PRINITOT:
                                 PRINPAMT,PRINHAMT,PRINIAMT,PRINMAMT,PRINVAMT:
                                 PRINOTHA,WBALNC,PRINCLAM,PRINPRAC,PRINDOCT:
                                 PRINDATE
         GOTO      OVERCOND IF OVER
         RETURN  
.
RDKWORK1 MOVE      ZERO,OVRCD
         READKS    WORKFILE;PRINNUMB,PRINDEBT,WSNAME,WGNAME,PRINITOT:
                            PRINPAMT,PRINHAMT,PRINIAMT,PRINMAMT,PRINVAMT:
                            PRINOTHA,WBALNC,PRINCLAM,PRINPRAC,PRINDOCT:
                            PRINDATE
         GOTO      OVERCOND IF OVER
         RETURN  
.
WRWORK1  RESET     KEY8
         MOVE      ZERO,OVRCD
         WRITE     WORKFILE,KEY8;PRINNUMB,PRINDEBT,WSNAME,WGNAME,PRINITOT:
                            PRINPAMT,PRINHAMT,PRINIAMT,PRINMAMT,PRINVAMT:
                            PRINOTHA,WBALNC,PRINCLAM,PRINPRAC,PRINDOCT:
                            PRINDATE
         RETURN  
.
UPWORK1  UPDATE    WORKFILE;PRINNUMB,PRINDEBT,WSNAME,WGNAME,PRINITOT:
                            PRINPAMT,PRINHAMT,PRINIAMT,PRINMAMT,PRINVAMT:
                            PRINOTHA,WBALNC,PRINCLAM,PRINPRAC,PRINDOCT:
                            PRINDATE
         RETURN  
.
.*******************************************************************************
.        Create tempfile
.*******************************************************************************
CREA0000  CALL      DELW0000
.
          PACK      CMDLINE,SP70,SP70
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMET,CMDLINE
          APPEND    " 129 UC1-8",CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      WORKFILE,FNAMET
          PREP      OUTDFILE,FNAMED
CREA9999  RETURN
.
.*******************************************************************************
.         Delete tempfile
.*******************************************************************************
DELW0000  CLOSE      WORKFILE
          CLEAR      CMDLINE
          APPEND     "iserase ",CMDLINE
          APPEND     FNAMET,CMDLINE
          RESET      CMDLINE
          EXECUTE    CMDLINE,TASKID
DELW9999  RETURN
+
.*******************************************************************************
WRTOUTD   WRITE     OUTDFILE,SEQ;PRINDEBT,PRINCLAM,PRINADTE,PRINNUMB:
                                 PRINDATE,WBALNC,PRINPRAC,PRINDOCT,PRHRPIND
         RETURN
.
.
         INC       STD001IO
         INC       PATCODKY
         INC       TFILENAM
         INC       IBASEQIO/INC
         INC       WEBERRIO/INC
.
         INC       PATCODIO/INC
         INC       PATMA1IO/INC
         INC       PRIINVIO/INC
         INC       PRIHREIO/INC
         INC       IBAPASIO/INC
         INC       IBASECIO/INC
.
ENDIT    CHAIN     PGM
         STOP
.

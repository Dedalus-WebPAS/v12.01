.*****************************************************************************
.*              P R O G R A M  S U M M A R Y                                 *
.*              -------------  -------------                                 *
.*                                                                           *
.*     PROGRAM NAME:     IBAHMS51                                            *
.*                                                                           *
.*     FUNCTION:         Weekly Bed Planner Report                           *
.*                                                                           *
.*     DATE WRITTEN:     20/10/2008                                          *
.*                                                                           *
.*     AUTHOR:           J.TAN                                               *
.*                                                                           *
.*     MODIFICATIONS:                                                        *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*                                                                           *
. *****************************************************************************
.
          INC           STD001FD
          INC           PATCOMM/INC
          INC           PATBMDFD/INC
          INC           PATMA1FD/INC
          INC           PATMI1FD/INC
          INC           PMSVX1FD/INC
          INC           PATWR1FD/INC
          INC           BOKRC1FD/INC
+
.               VARIABLES DEFINITIONS
.
CMDLINE   DIM       60
DATE8     DATE      8
DIM8      DIM       8
ENDDATE   DIM       8
.
HEAD1     DIM       10
HEAD2     DIM       10
HEAD3     DIM       10
HEAD4     DIM       10
HEAD5     DIM       10
HEAD6     DIM       10
HEAD7     DIM       10
.
STRTDATE  DIM       8
SELWARD   DIM       3
SAVBED    DIM       3
SAVUNQ    DIM       2
.
DETCOL1   DIM       17
DETCOL2   DIM       17
DETCOL3   DIM       17
DETCOL4   DIM       17
DETCOL5   DIM       17
DETCOL6   DIM       17
DETCOL7   DIM       17
DATEARRAY DIM       8[9]
.
FNAME     DIM       8
.
TEMP1     IFILE     SQL, FIXED=24, KEY="U1-3,4-4,5-6"
TEMP2     IFILE     SQL, FIXED=24, KEY="U1-3,5-6,4-4"
.
.Name     Type     Size    Physical   Start       Description
.----     ----     ----    --------   -----       -----------
XBED      DIM       3         3         1         Bed
XCOL      DIM       1         1         4         Colum no.
XUNQ      DIM       2         2         5         Uniq no.
XPAT      DIM       17        17        7         Patient name and doctor
.EOR                24
.
DOCTORC   DIM       6
PNAME     DIM       10
PREVBED   DIM       3
.
PRGID     INIT      "IBAHMS51"
PRGNAM    INIT      "Weekly Bed Planner Report"
VERSION   INIT      "V12.01.00"
+
.****************************************************************************
.*                            MAIN0000                                      *
.*                      Controlling Logic (Mainline code)                   *
.****************************************************************************
MAIN0000  CALL      INIT0000
.
          MOVE      ZERO,OPTION
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Weekly Bed planner":
                    *P1:6,*V2LON,TWO,*HOFF," = Create Export file for Excel"
MAIN1000  KEYIN     *P1:8,*EF,"Option : ",*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      MAIN9999 IF EQUAL
.
          BRANCH    OPTION,MAIN2000,MAIN2000
          BEEP
          GOTO      MAIN0000
.
MAIN2000  CALL      KDAT0000         * keyin date
          BRANCH    EXIT,MAIN9999
.
          CALL      KWRD0000         * keyin range of ward
          BRANCH    EXIT,MAIN2000
.
          CALL      CONTQST          * Ok to continue ?
          BRANCH    CEXIT,MAIN3000,MAIN2000,MAIN9999
          BEEP
          GOTO      MAIN2000
.
MAIN3000  CALL      PROC0000         * Process report
          CALL      PRNT0000         * Print report
          CALL      KILL0000         * Make sure the files are gone
          GOTO      MAIN1000
.
MAIN9999  CHAIN     PGM
.
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
INIT0000  CALL      DISPHEAD
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      BOKRC1A1,"bokrc1af"
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATWR1A3,"patwr1af"
          OPEN      PATBMDA1,"patbmdaf"
.
          MOVE      "pathms51",FNAME
INIT9999  RETURN
+
.****************************************************************************
.*                            Keyin Start date                              *
.****************************************************************************
KDAT0000  DISPLAY   *P1:17,*EF,"Keyin Start Date :";
          MOVE      TEN7,CVERT
          MOVE      TWENTY1,CCOL
          MOVE      CCC,CCENT
          UNPACK    SP6,CYEAR,CMON,CDAY     * No defaults
          CALL      KEYDATE                 * Keyin date
          BRANCH    OVRCD,KDAT9000          * Nothing entered
.
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDATE
.
.         Work out date range
.
          CALL      LDAT0000             * Setup the dates variable
.
          MOVE      ZERO,EXIT
          GOTO      KDAT9999
.
KDAT9000  MOVE      ONE,EXIT
KDAT9999  RETURN
+
.****************************************************************************
.*                            Keyin Range of Ward                           *
.****************************************************************************
KWRD0000  DISPLAY   *P1:19,"Ward : ";
          MOVE      SP70,SELWARD
.
          MOVE      TEN9,EVERT
          MOVE      TWENTY1,ECOL
          CALL      PATWRDKY
          BRANCH    EXIT,KWRD9000,KWRD9000
.
          DISPLAY   *P40:19,*EL,*V2LON,WBDESC
          MOVE      WWARD,SELWARD
          MOVE      ZERO,EXIT
          GOTO      KWRD9999
.
KWRD9000  MOVE      ONE,EXIT
KWRD9999  RETURN
+
.****************************************************************************
.*               Continue processing                                        *
.****************************************************************************
PROC0000  DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** Generating Report **",*W2;
.
          CALL      CRET0000              * Creating tempfile
.
.         Ward/Bed Details
.
PROC1000  PACK      KEY6,SELWARD,SP70
          CALL      RDWARD1
PROC2000  CALL      RDKWARD1
          BRANCH    OVRCD,PROC9999
          MATCH     SP70,WBED
          GOTO      PROC2000 IF EQUAL      * Ward record
.
          MATCH     WWARD,SELWARD
          GOTO      PROC9999 IF NOT EQUAL
.
          BRANCH    WACTIVE,PROC2000       * skip Inactive Beds
.
          CALL      BMAN0000               * Read bed management details
.
.         Check if any record written for the bed, if not a new record
.
          PACK      KEY6,WBED,SP70
          CALL      RDSTEMP1
          CALL      RDKTEMP1
          IF        OVRCD=0
            MATCH     XBED,WBED
            GOTO      PROC2000 IF EQUAL    * got a record for the bed
          ENDIF
.
          MOVE      WBED,XBED
          MOVE      "1",XCOL
          MOVE      " 1",XUNQ
          MOVE      SP70,XPAT
          PACK      KEY6,XBED,XCOL,XUNQ,SP70
          CALL      WRTEMP1
          GOTO      PROC2000
.
PROC9999  RETURN
.+
.****************************************************************************
.         Read bed management details for the ward/bed
.****************************************************************************
BMAN0000  PACK      KEY30,WWARD,WBED,STRTDATE,SP70
          CALL      RSPTBMD1
BMAN2000  CALL      RKPTBMD1
          BRANCH    OVRCD,BMAN9999
.
          MATCH     WWARD,PTBDWARD
          GOTO      BMAN9999 IF NOT EQUAL
.
          MATCH     WBED,PTBDWBED
          GOTO      BMAN9999 IF NOT EQUAL
.
          MATCH     PTBDDATE,ENDDATE
          GOTO      BMAN9999 IF LESS
.
          MATCH     "4",PTBDSTAT
          GOTO      BMAN2000 IF EQUAL   * Discharged
.
          CALL      PDOC0000            * Populate Patient name and doctor code
          GOTO      BMAN2000
.
BMAN9999  RETURN
+
.****************************************************************************
.         Populate Patient name and doctor code
.****************************************************************************
PDOC0000  MOVE      SP70,DIM8
          MOVE      SP70,DOCTORC
          PACK      KEY8,PTBDADMN,SP70
          CALL      RDBKREC1
          IF        OVRCD=0
            PACK      DOCTORC,BKREADOC,SP70
            MOVE      BKREURNO,DIM8
          ENDIF
          PACK      KEY8,PTBDADMN,SP70
          CALL      RDMISS1
          IF        OVRCD=0
            PACK      DOCTORC,ADOCTA,SP70
            MOVE      AURNO,DIM8
          ENDIF
          MOVE      DIM8,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,PDOC9999
.
          CALL      SNAM0000                  * Set patient name
          PACK      KEY17,PNAME,SP1,DOCTORC,SP70
.
          DAYSEP    STRTDATE,PTBDDATE,F1
          MOVE      ONE,FORM1
          LOAD      FORM1,F1,TWO,THREE,FOUR,FIVE,SIX,SEVEN
.
          MOVE      FORM1,KEY1                * column no
          MOVE      ZERO,FORM2
          PACK      KEY6,PTBDWBED,KEY1,Z70
          CALL      RDSTEMP1
          CALL      RDPTEMP1
          BRANCH    OVRCD,PDOC2000
.
          MATCH     PTBDWBED,XBED
          GOTO      PDOC2000 IF NOT EQUAL
          MATCH     KEY1,XCOL
          GOTO      PDOC2000 IF NOT EQUAL
          MOVE      XUNQ,FORM2
.
PDOC2000  ADD       ONE,FORM2
          UNPACK    SP70,XBED,XCOL,XUNQ,XPAT
          MOVE      PTBDWBED,XBED
          MOVE      FORM1,XCOL                * column no
          MOVE      FORM2,XUNQ                * unique no
          PACK      KEY6,XBED,XCOL,XUNQ,SP70
          CALL      RDATEMP1
          IF        OVRCD=1
            MOVE      KEY17,XPAT
            CALL      WRTEMP1
          ENDIF
PDOC9999  RETURN
+
.****************************************************************************
.         Setup patient name
.****************************************************************************
SNAM0000  REP       ", ",PSNAME
          CLEAR     PNAME
          APPEND    PSNAME,PNAME
.
.         Get the first no blank surname
.
SNAM2000  CMATCH    SP1,PNAME
          GOTO      SNAM3000 IF NOT EQUAL
.
          BUMP      PNAME,-1
          GOTO      SNAM2000 IF NOT EOS
.
.         No Surname name, name will be just Given name
.
          APPEND    PGNAME,PNAME
          GOTO      SNAM9000
.
SNAM3000  MOVE      PGNAME,ANS
          CMATCH    DOT,PNAME
          GOTO      SNAM4000 IF EQUAL
.
          APPEND    DOT,PNAME
SNAM4000  APPEND    ANS,PNAME
.
SNAM9000  APPEND    SP70,PNAME
          RESET     PNAME
SNAM9999  RETURN
+
.****************************************************************************
.         Print report
.****************************************************************************
PRNT0000  MOVE      ZERO,CPAGENO
          CLOCK     TIME,CTIMEIS
          MOVE      "99",CLNO
.
          IF        OPTION<>1
            MOVE      SP70,PREVBED
            CALL      HEAD0000
          ENDIF
          UNPACK    SP70,SAVBED,SAVUNQ
          UNPACK    SP70,DETCOL1,DETCOL2,DETCOL3,DETCOL4
          UNPACK    SP70,DETCOL5,DETCOL6,DETCOL7
          MOVE      SP70,KEY6
          CALL      RDSTEMP2
PRNT1000  CALL      RDKTEMP2
          BRANCH    OVRCD,PRNT8000
.
          MATCH     SP70,SAVBED
          GOTO      PRNT6000 IF EQUAL
.
          MATCH     SAVBED,XBED
          GOTO      PRNT4000 IF NOT EQUAL 
.
          MATCH     SAVUNQ,XUNQ
          GOTO      PRNT4000 IF NOT EQUAL
.
          GOTO      PRNT7000
.
.         Print details for each uniq.no or line no for each bed
.
PRNT4000  IF        OPTION=1
            COMPARE   SIXTY,CLNO
            CALL      HEAD0000 IF NOT LESS
.
            MATCH     " 1",SAVUNQ
            IF        !@EQUAL
              PRINT     *1,"|",SP3," |";
            ENDIF
.
            PRINT     *7,DETCOL1,"|",*25,DETCOL2,"|",*43,DETCOL3,"|":
                      *61,DETCOL4,"|",*79,DETCOL5,"|",*97,DETCOL6,"|":
                      *115,DETCOL7,"|"
            ADD       ONE,CLNO
            CALL      UND132
          ELSE
            MATCH     " 1",SAVUNQ
            IF        !@EQUAL
              PRINT     "<tr><td></td>";
            ELSE
              PRINT     "<tr><td>",PREVBED,"</td>";
            ENDIF

            PRINT     "<td>",DETCOL1,"</td>":
                      "<td>",DETCOL2,"</td>":
                      "<td>",DETCOL3,"</td>":
                      "<td>",DETCOL4,"</td>":
                      "<td>",DETCOL5,"</td>":
                      "<td>",DETCOL6,"</td>":
                      "<td>",DETCOL7,"</td></tr>"
            ADD       ONE,CLNO
          ENDIF
.
PRNT6000  MATCH     SAVBED,XBED
          IF        !@EQUAL
            IF        OPTION=1
              COMPARE   SIXTY,CLNO
              CALL      HEAD0000 IF NOT LESS
              PRINT     *1,"|",XBED," |";
            ELSE
              MOVE      XBED,PREVBED
            ENDIF
          ENDIF
.
          UNPACK    SP70,DETCOL1,DETCOL2,DETCOL3,DETCOL4,DETCOL5,DETCOL6,DETCOL7
          MOVE      XUNQ,SAVUNQ
          MOVE      XBED,SAVBED
.
PRNT7000  MOVE      ZERO,FORM1
          MOVE      XCOL,FORM1
          STORE     XPAT,FORM1,DETCOL1,DETCOL2,DETCOL3,DETCOL4:
                               DETCOL5,DETCOL6,DETCOL7
          GOTO      PRNT1000
.
PRNT8000  IF        OPTION=1
            COMPARE   SIXTY,CLNO
            CALL      HEAD0000 IF NOT LESS
            PRINT     *1,"|",SAVBED," |":
                      *7,DETCOL1,"|",*25,DETCOL2,"|",*43,DETCOL3,"|":
                      *61,DETCOL4,"|",*79,DETCOL5,"|",*97,DETCOL6,"|":
                      *115,DETCOL7,"|"
            CALL      UND132
            PRINT     *N,"  ***  End of Report  ***"
          ELSE
            PRINT     "<tr><td>",SAVBED,"</td>":
                      "<td>",DETCOL1,"</td>":
                      "<td>",DETCOL2,"</td>":
                      "<td>",DETCOL3,"</td>":
                      "<td>",DETCOL4,"</td>":
                      "<td>",DETCOL5,"</td>":
                      "<td>",DETCOL6,"</td>":
                      "<td>",DETCOL7,"</td></tr>"
          ENDIF
.
PRNT9999  RETURN
+
.****************************************************************************
.*               Setup the dates variable
.****************************************************************************
LDAT0000  MOVE      STRTDATE,DATE8
          MOVE      DATE8,DATEARRY[1]
          ADD       ONE,DATE8
          MOVE      DATE8,DATEARRY[2]
          ADD       ONE,DATE8
          MOVE      DATE8,DATEARRY[3]
          ADD       ONE,DATE8
          MOVE      DATE8,DATEARRY[4]
          ADD       ONE,DATE8
          MOVE      DATE8,DATEARRY[5]
          ADD       ONE,DATE8
          MOVE      DATE8,DATEARRY[6]
          ADD       ONE,DATE8
          MOVE      DATE8,DATEARRY[7]
          MOVE      DATE8,ENDDATE
LDAT9999  RETURN
+
.****************************************************************************
.*               HEAD0000 - Header                                          *
.****************************************************************************
HEAD0000  UNPACK    DATEARRY[1],CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,HEAD1
.
          UNPACK    DATEARRY[2],CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,HEAD2
.
          UNPACK    DATEARRY[3],CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,HEAD3
.
          UNPACK    DATEARRY[4],CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,HEAD4
.
          UNPACK    DATEARRY[5],CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,HEAD5
.
          UNPACK    DATEARRY[6],CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,HEAD6
.
          UNPACK    DATEARRY[7],CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,HEAD7
.
          BRANCH    OPTION,HEAD2000
.
          PRINT     "<table border=1><tr><td width=10%></td>":
                    "<td>",HEAD1,"</td>":
                    "<td>",HEAD2,"</td>":
                    "<td>",HEAD3,"</td>":
                    "<td>",HEAD4,"</td>":
                    "<td>",HEAD5,"</td>":
                    "<td>",HEAD6,"</td>":
                    "<td>",HEAD7,"</td></tr>"
          GOTO      HEAD9999
.
HEAD2000  CALL      IBACLOCK
          CALL      HEAD132
.
          UNPACK    STRTDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *40,"Date Range from: ",CPCDATE;
.
          UNPACK    ENDDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *70," to : ",CPCDATE
.
          CALL      UND132
          PRINT     *1,"|Bed |":
                    *10,HEAD1,"    |":
                    *28,HEAD2,"    |":
                    *46,HEAD3,"    |":
                    *64,HEAD4,"    |":
                    *82,HEAD5,"    |":
                    *100,HEAD6,"    |":
                    *118,HEAD7,"    |"
          ADD       TWO,CLNO 
          CALL      UND132
HEAD9999  RETURN
+
.****************************************************************************
.         Create the tempfile
.****************************************************************************
CRET0000  CALL      KILL0000                 * Make sure the files are gone
.
          CLEAR     CMDLINE                  * Clear the command line
          APPEND    "isbuild ",CMDLINE       * Set up the command to
          APPEND    FNAME,CMDLINE
          APPEND    " 24 U1-3,4-4,5-6 U1-3,5-6,4-4",CMDLINE   * index file.
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID           * Create the 1st file
          OPEN      TEMP1,FNAME
          OPEN      TEMP2,FNAME
CRET9999  RETURN
.
.****************************************************************************
.         Kill tempfile
.****************************************************************************
KILL0000  CLOSE     TEMP1
          CLEAR     CMDLINE                  * Clear the command line
          APPEND    "iserase ",CMDLINE       * Set up the command to
          APPEND    FNAME,CMDLINE            * delete temporary file
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID           * Delete file
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      TEMP1,FNAME
          TRAPCLR   IO
          BRANCH    OVRCD,KILL9999           * file doesn't exit
.
          MOVE      SP70,KEY6
          CALL      RDSTEMP1
KILL2000  CALL      RDKTEMP1
          BRANCH    OVRCD,KILL9999
.
          PACK      KEY6,XBED,XCOL,XUNQ,SP70
          MATCH     SP70,KEY6
          GOTO      KILL9999 IF EQUAL
          CALL      DETEMP1
          GOTO      KILL2000
.
KILL9999  RETURN
+
. *****************************************************************************
. * I/O routines for the three index files                                    *
. *****************************************************************************
RDSTEMP1  READ      TEMP1,KEY6;;
          RETURN
.
RDKTEMP1  MOVE      ZERO,OVRCD
          READKS    TEMP1;XBED,XCOL,XUNQ,XPAT
          GOTO      OVERCOND IF OVER
          RETURN
.
RDATEMP1 RESET     KEY6
         MOVE      ZERO,OVRCD
         READ      TEMP1,KEY6;ANS
         GOTO      OVERCOND IF OVER
         RETURN
.
RDTEMP1   MOVE      ZERO,OVRCD
          READ      TEMP1,KEY6;XBED,XCOL,XUNQ,XPAT
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   WRITE     TEMP1,KEY6;XBED,XCOL,XUNQ,XPAT
          RETURN
.
UPTEMP1   UPDATE    TEMP1;XBED,XCOL,XUNQ,XPAT
          RETURN
.
DETEMP1   MOVE      ZERO,OVRCD
          RESET     KEY6
          DELETE    TEMP1,KEY6
          RETURN
.
RDPTEMP1  MOVE      ZERO,OVRCD
          READKP    TEMP1;XBED,XCOL,XUNQ,XPAT
          GOTO      OVERCOND IF OVER
          RETURN
.
RDSTEMP2  READ      TEMP2,KEY6;;
          RETURN
.
RDKTEMP2  MOVE      ZERO,OVRCD
          READKS    TEMP2;XBED,XCOL,XUNQ,XPAT
          GOTO      OVERCOND IF OVER
          RETURN
.
.****************************************************************************
          INC       PATWRDKY
          INC       PATBMDIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATWR1IO/INC
          INC       BOKRC1IO/INC
.
          INCLUDE   STD001IO
.
ENDIT    CHAIN      PGM
         STOP

.***************************************************************************
.* System    :   Patient Management System                                 *
.* Program   :   FXUPNAME                                                  *
.***************************************************************************
.* Date      :   17/06/2009                                                *
.* Author    :   Jill Habasque                                             *
.* Mods      :                                                             *
.*             V12.00.01 02/06/2025  Peter Vela        TSK 0955096         *
.*                       Alphanumeric changes                              *
.*             V10.02.02 13/07/2011  Steve Armstrong   CAR 240722          *
.*                       Further mods to cater for second given name as    *
.*                       part of PATGSRFD keys                             *
.*             V10.02.01 22/06/2011  Steve Armstrong   CAR 240722          *
.*                       Mods to cater for changes to PATGSRFD:            *
.*                            - GSRSNAM & GSRGNAM extended to 30 chars.    *
.*                            - PTGSGNM2 added.                            *
.*                            - all indexes changed in length.             *
.***************************************************************************
.*              V9.10.01  17/06/2009  Jill Habasque CAR 197395             *
.*                        Created program - Update names as uppercase      *
.***************************************************************************
.
          INC       STD001FD
.
.
. FILE DESCRIPTION INCLUDES
. -------------------------
.
          INC       PATMA1FD/INC
          INC       PATGSRFD/INC
          INC       PMSPX2FD/INC
.
.
PRGID     INIT      "FXUPNAME"
PRGNAM    INIT      "Update names with uppercase"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select FROM and TO
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
.
          CALL      PROC0000               * process records for update
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                     * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"          * patient master file
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"          * patient master file
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"          * patient master file
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patgsrnf";
          OPEN      PATGSRN1,"patgsrnf"          * patient surname file
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Valid option selected                  *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Update patient names"
.
          DISPLAY   *P1:11,*EF,*V2LON,"WARNING: PLEASE BACKUP patmastf BEFORE RUNNING THIS PROGRAM."
.
OPTN1000  KEYIN     *P1:7,*EL,"Select option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION
          GOTO      OPTN9500 IF EQUAL                 * exit
.
          BRANCH    COPTION,OPTN2000
.
          BEEP
          GOTO      OPTN1000
.
OPTN2000  CALL      CONTQST                          * Ok to continue ?
          BRANCH    CEXIT,OPTN9000,OPTN1000,OPTN9500 * Yes, no, cancel
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.****************************************************************************
.*                                PROC0000            Called by: ML0000     *
.*              read through master file                                    *
.****************************************************************************
PROC0000  DISPLAY   *P1:24,*EL,"Processing U/R Number: ";
          MOVE      SP10,KEY8
          CALL      RDSMAST1                     * position at start of file
PROC1000  CALL      RDKMAST1                     * read next record
          BRANCH    OVRCD,PROC9000               * end of file
.
          MOVE      PURNO,KEY8
          CALL      RDPMPX21                     * extension record found ?
          BRANCH    OVRCD,PROC1000               * no
.
          DISPLAY   *P30:24,*EL,*V2LON,PURNO;
.
          REP       UPPLOW,PTMASNAM
          REP       UPPLOW,PMPXFNAM
          REP       UPPLOW,PMPXSNAM
.
          CALL      UPMAST1
.
          MOVE      PTMASNAM,GSRSNAM    * Set up the Surname
          MOVE      PMPXFNAM,GSRGNAM    * Set up the First Given Name
          MOVE      PMPXSNAM,PTGSGNM2   * Set up the Second Given Name
          MOVE      PURNO,GSRURNO       * Set up the U/R Number
          MOVE      PBDATE,GSRDOB       * Set up the DOB
          MOVE      PSEX,GSRSEX         * Set up the Sex
          MOVE      ZEROVISN,GSRBILL    * Set up the billing number
.
          PACK      KEY115,GSRURNO,GSRBILL,GSRSNAM,GSRGNAM,PTGSGNM2,GSRDOB:
                           GSRSEX
          CALL      RDPTGSR1            * Check if this record exists
          BRANCH    OVRCD,PROC1000      * Old record not found
          CALL      DEPTGSR1
.
          GOTO      PROC1000
.
PROC9000  DISPLAY   *P1:20,*EL,"Processing master file complete.  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       PATMA1IO/INC
          INC       PATGSRIO/INC
          INC       PMSPX2IO/INC

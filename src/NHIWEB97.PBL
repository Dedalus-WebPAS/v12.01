.----------------------------------------------------------------------
. Program       : NHIWEB97
.
. Function      : NHI/MWS Table Maintenance
.
. Modifications : 
.----------------------------------------------------------------------
. V11.03.01 10/01/2023 Bella Turco      TSK 0921957
.           Added NOMORE00 - Display "No more records" at end of list
.           Added PTCNNRTD - Displays number of rows set by parameter value
. V10.14.01 19/02/2019  Ebon Clements TSK 0867703
.           Changed NHI ethnicity file reads to use KEY5
. V10.05.01 21/08/2014  Don Nguyen          CAR 303878
.                       Modified DMHED000 & DMROW000 to display
.                       'District Health Board'.
.                       Added NHDMC003 - District Health Board (Cat dh)
.----------------------------------------------------------------------
. V10.04.01 31.03.2014  B.G.Ackland  CAR 299363
.           Replace META Tag Redirect with Javascript in Common RDIREC00
.           Routine
.----------------------------------------------------------------------
.                V10.03.01 16/07/2013  Jill Parkinson CAR 287923
.                          Added include of PATCONFD
.----------------------------------------------------------------------
.                 V9.05.01 09/03/2006  Ebon Clements CAR 78533
.                          Mods for PATCODTM - Hospital specific category codes
.                 V9.05.B01 30/01/2006  Ebon Clements CAR 93186
.                          Mods for REDIRECT length change.
.                          Recompiled for IBAWEBDF
.----------------------------------------------------------------------
.                 V9.04.01 04/10/2005 Ebon Clements CAR 78351
.                          Fixed spelling of Domicile
.----------------------------------------------------------------------
.                 V9.03.02 12/11/2003 Jill Habasque CAR 42705
.                          Added upcase of domicile description
.                 V9.03.01 10/11/2003 Jill Habasque CAR 42705
.                          Added domicile code search
.                 V5.07.04 27/03/2000 Bronko Sosic
.                          Added startkey for GOTO button
.                 V5.07.03 10/03/2000 Pat Dirito
.                          Update javascript ShowDetail with report instance
.                 V5.07.02 20.12.1999 Katie Nelson 
.                          Recompiled for IBAWEBDF/IBAWEBCD
.                 V5.07.01 28/09/1999 B.G.Ackland
.                          Use HeadingCell
.                 V5.07.00 02/07/1999 Pat Dirito
.                          Created Program 
.----------------------------------------------------------------------
          INC       IBAWEBDF    
          INC       WEBDATDF
.
          INC       PATCONFD/INC
          INC       NHIRELFD/INC   * Relationship Codes             
          INC       NHIETHFD/INC   * Ethnicity Codes             
          INC       NHIDCDFD/INC   * Donor Codes             
          INC       NHIDMCFD/INC   * Domicile Code Masterfile             
.
.----------------------------------------------------------------------
. CGI Parameters
.----------------------------------------------------------------------
NEXTPAGE  DIM       1
UPDTTYPE  DIM       1     * Update Type
STARTKEY  DIM       10      * Starting Key
DOMICKEY  DIM       20      * Starting Key for domicile search
DISPDESC  DIM       50
.
NHREL001  DIM       2     * Relationship Code
NHREL002  DIM       25    * Relationship Description
NHETH001  DIM       5     * Ethnic Group Code 
NHETH002  DIM       25    * Ethnic Group Description
NHETH003  DIM       3     * Map to IBA Code (Category P1)
NHDCD001  DIM       1     * Donor Code
NHDCD002  DIM       25    * Donor Decription
NHDCD003  DIM       3     * IBA Donor Code 
NHDMC001  DIM       4     * Domicile Code
NHDMC002  DIM       50    * Domicile Description  
NHDMC003  DIM       3     * District Health Board (Category dh)
.----------------------------------------------------------------------
. Program Variables
.----------------------------------------------------------------------
CATdh     INIT      "dh"
.
.----------------------------------------------------------------------
PRGID     INIT      "NHIWEB97"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "NHI/MWS Table Maintenance"
.----------------------------------------------------------------------
.
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200,MAIN1300,MAIN1400
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "Invalid Option",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  CALL      NHIREL00        * Relationship Codes Maintenance
          BRANCH    EXIT,MAIN1000
          MOVE      "Update Complete",WEBTITLE
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1200  CALL      NHIETH00        * Ethnicity Codes Maintenance 
          BRANCH    EXIT,MAIN1000
          MOVE      "Update Complete",WEBTITLE
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1300  CALL      NHIDNR00        * Donor Codes Maintenance
          BRANCH    EXIT,MAIN1000
          MOVE      "Update Complete",WEBTITLE
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1400  CALL      NHIDOM00        * Domicile Codes Maintenance
          BRANCH    EXIT,MAIN1000
          MOVE      "Update Complete",WEBTITLE
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
.------------------------------------------------------------
. Output Next Page Based on CGI Parameter nextpage
.------------------------------------------------------------
NXTPAG00  MOVE      ZERO,F1
          MOVE      NEXTPAGE,F1
          BRANCH    F1,NXTPAG10,NXTPAG20,NXTPAG30,NXTPAG40
.
          CALL      WINCLS00
          GOTO      NXTPAG99
.
NXTPAG10  CALL      RDIREC00
          GOTO      NXTPAG99
.
NXTPAG20  CALL      GOBACK00
          GOTO      NXTPAG99
.
NXTPAG30  CALL      GOBACK00
          GOTO      NXTPAG99
.
NXTPAG40  CALL      GOBACK00
          GOTO      NXTPAG99
.
NXTPAG99  RETURN
.-------------------------------------------------------------------------------
.  Goes back x number of pages
.-------------------------------------------------------------------------------
GOBACK00  CALL      OPENHTML
          BRANCH    EXIT,GOBACK99
          MOVE      NEXTPAGE,F1
          SUB       TWO,F1
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "    alert(#"",WEBTITLE,"#");":
.                            "    self.close();":
                             "    window.history.go(-",F1,");":
                             "}":
                             "</script>"
          CALL      CLOSHTML
GOBACK99  RETURN
.------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------
INIT0000  OPEN      NHIRELA1,"nhirelaf"   * Relationship Codes
          OPEN      NHIETHA1,"nhiethaf"   * Ethnicity Codes
          OPEN      NHIDCDA1,"nhidcdaf"   * Donor Codes
          OPEN      NHIDMCA1,"nhidmcaf"   * Domicile Codes
          OPEN      NHIDMCA2,"nhidmcaf"   * Domicile Codes
          OPEN      PATCODE1,"patcodes"   * patient Codes
          OPEN      PATCODE2,"patcodes"   * patient Codes
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,HUND25;*238,PTCNNRTD
.
          MOVE      ZERO,F2
          MOVE      PTCNNRTD,F2
          IF        F2 = 0
            MOVE      "10",PTCNNRTD
          ENDIF
.
          IF        IBCNMHOS=1
            OPEN      MLTCODA1,"mltcodaf"
            OPEN      MLTCODA2,"mltcodaf"
          ENDIF
.
          RETURN
.------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          MOVE      SP70,TEMPLATE
          MOVE      ZERO,NORECORD
          MOVE      ZERO,STARTNUM
          MOVE      SP70,NEXTPAGE
          PACK      REDIRECT,SP70,SP70,SP70,SP70
          MOVE      SP70,UPDTTYPE
          MOVE      SP70,STARTKEY
          MOVE      SP70,DOMICKEY
.
          MOVE      SP70,NHREL001
          MOVE      SP70,NHREL002
          MOVE      SP70,NHETH001
          MOVE      SP70,NHETH002
          MOVE      SP70,NHDCD001
          MOVE      SP70,NHDCD002
          MOVE      SP70,NHDMC001
          MOVE      SP70,NHDMC002
          MOVE      SP70,NHDMC003
.
          RETURN
.------------------------------------------------------------
. Set Parameters
.------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startkey=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTKEY
            PACK      STARTKEY,STARTKEY,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "domickey=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,DOMICKEY
            PACK      DOMICKEY,DOMICKEY,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nextpage=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NEXTPAGE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "redirect=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REDIRECT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "norecord=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NORECORD
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startnum=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTNUM
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updttype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDTTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nhrel001=",QRYNAME
          IF        @EQUAL
            SQUEEZE   QRYDATA
            MOVE      QRYDATA,NHREL001
            PACK      NHREL001,NHREL001,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nhrel002=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NHREL002
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nheth001=",QRYNAME
          IF        @EQUAL
            SQUEEZE   QRYDATA
            MOVE      QRYDATA,NHETH001
            PACK      NHETH001,NHETH001,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nheth002=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NHETH002
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nhdcd001=",QRYNAME
          IF        @EQUAL
            SQUEEZE   QRYDATA
            MOVE      QRYDATA,NHDCD001
            PACK      NHDCD001,NHDCD001,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nhdcd002=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NHDCD002
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nhdmc001=",QRYNAME
          IF        @EQUAL
            SQUEEZE   QRYDATA
            MOVE      QRYDATA,NHDMC001
            PACK      NHDMC001,NHDMC001,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nhdmc002=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NHDMC002
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nhdmc003=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NHDMC003
            GOTO      SETPAR99
          ENDIF
SETPAR99  RETURN
.------------------------------------------------------------
. Relationship Codes Maintenance: Display,Add,Update,Delete
.------------------------------------------------------------
NHIREL00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      NHIREL40 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      NHIREL10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      NHIREL20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      NHIREL30 IF EQUAL
.
          GOTO      NHIREL93
.
NHIREL10  CALL      CHKREL00  * Checks mandatory fields & checks for blank
          BRANCH    EXIT,NHIREL99
          PACK      KEY2,NHREL001,SP70  * ADD FORMACTION
          CALL      RDNHREL1
          COMPARE   ZERO,OVRCD
          GOTO      NHIREL92 IF EQUAL
          CALL      RELSAV00      * Moves input variables to file variables
          CALL      WRNHREL1      * Writes away the data
NHIREL15  MOVE      ZERO,EXIT
          GOTO      NHIREL99
.
NHIREL20  PACK      KEY2,NHREL001,SP70  * UPDATE FORMACTION
          CALL      RDNHREL1
          BRANCH    OVRCD,NHIREL91
          CALL      CHKREL00      * Checks mandatory fields
          BRANCH    EXIT,NHIREL99
          CALL      RELSAV00      * Moves input variables to file variables
          CALL      UPNHREL1       * Writes away the data
          MOVE      ZERO,EXIT
          GOTO      NHIREL99
.
NHIREL30  PACK      KEY2,NHREL001,SP70   * DELETE FORMACTION
          CALL      RDNHREL1
          BRANCH    OVRCD,NHIREL91
          CALL      DENHREL1       * Deletes the data
          MOVE      ZERO,EXIT
          GOTO      NHIREL99
.
NHIREL40  PACK      KEY2,NHREL001,SP70 
          CALL      RDNHREL1
          IF        OVRCD=1
            CALL      CLRREL00    * Clear all the file variables
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Relationship Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,NHIREL99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      NHIREL99
.
NHIREL91  MOVE      "Relationship Code does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      NHIREL99
.
NHIREL92  MOVE      "Relationship Code exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      NHIREL99
.
NHIREL93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      NHIREL99
.
NHIREL99  RETURN
.-----------------------------------------------------------
. Initialize's file data
.-----------------------------------------------------------
CLRREL00  MOVE      SP70,NHRERCD
          MOVE      SP70,NHREDES
          MOVE      SP70,NHRESPA 
.
CLRREL99  RETURN
.------------------------------------------------------------
. Moves cgi variables into file variables
.------------------------------------------------------------
RELSAV00  MOVE      NHREL001,KEY2
          SQUEEZE   KEY2
          MOVE      KEY2,NHRERCD
          MOVE      NHREL002,NHREDES
          MOVE      SP70,NHRESPA 
.
RELSAV99  RETURN
.------------------------------------------------------------------
. * Checks mandatory fields and checks for blank
.------------------------------------------------------------------
CHKREL00  RESET     NHREL001
          MATCH     SP70,NHREL001
          GOTO      CHKREL90 IF EQUAL
.
          RESET     NHREL002
          MATCH     SP70,NHREL002
          GOTO      CHKREL91 IF EQUAL
.
          GOTO      CHKREL98
.
CHKREL90  MOVE      "Relationship Code is mandatory!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKREL99
.
CHKREL91  MOVE      "Relation Description is mandatory!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKREL99
.
CHKREL98  MOVE      ZERO,EXIT
CHKREL99  RETURN
.------------------------------------------------------------
. Ethnic Codes Maintenance: Display,Add,Update,Delete
.------------------------------------------------------------
NHIETH00  RESET     NHETH001 
          RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      NHIETH40 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      NHIETH10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      NHIETH20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      NHIETH30 IF EQUAL
.
          GOTO      NHIETH93
.
NHIETH10  CALL      CHKETH00  * Checks mandatory fields & checks for blank
          BRANCH    EXIT,NHIETH99
          PACK      KEY5,NHETH001,SP70  * ADD FORMACTION
          CALL      RDNHETH1
          COMPARE   ZERO,OVRCD
          GOTO      NHIETH92 IF EQUAL
          CALL      ETHSAV00      * Moves input variables to file variables
          CALL      WRNHETH1      * Writes away the data
          MOVE      ZERO,EXIT
          GOTO      NHIETH99
.
NHIETH20  PACK      KEY5,NHETH001,SP70  * UPDATE FORMACTION
          CALL      RDNHETH1
          BRANCH    OVRCD,NHIETH91
          CALL      CHKETH00      * Checks mandatory fields
          BRANCH    EXIT,NHIETH99
          CALL      ETHSAV00      * Moves input variables to file variables
          CALL      UPNHETH1      * Writes away the data
          MOVE      ZERO,EXIT
          GOTO      NHIETH99
.
NHIETH30  PACK      KEY5,NHETH001,SP70   * DELETE FORMACTION
          CALL      RDNHETH1
          BRANCH    OVRCD,NHIETH91
          CALL      DENHETH1      * Deletes the data
          MOVE      ZERO,EXIT
          GOTO      NHIETH99
.
NHIETH40  PACK      KEY5,NHETH001,SP70  
          CALL      RDNHETH1
          IF        OVRCD=1
            CALL      CLRETH00    * Clear all the file variables
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Ethnicity Codes Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,NHIETH99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      NHIETH99
.
NHIETH91  MOVE      "Ethnic Group Code does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      NHIETH99
.
NHIETH92  MOVE      "Ethnic Group Code exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      NHIETH99
.
NHIETH93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      NHIETH99
.
NHIETH99  RETURN
.-----------------------------------------------------------
. Initialize's file data
.-----------------------------------------------------------
CLRETH00  MOVE      SP70,NHETECD
          MOVE      SP70,NHETDES
          MOVE      SP70,NHETIBA
          MOVE      SP70,NHETLEV
          MOVE      SP70,NHETSPA  
.
CLRETH99  RETURN
.------------------------------------------------------------
. Moves cgi variables into file variables
.------------------------------------------------------------
ETHSAV00  MOVE      NHETH001,KEY2
          SQUEEZE   KEY2
          MOVE      KEY2,NHETECD
          MOVE      NHETH002,NHETDES
          MOVE      SP70,NHETIBA
          MOVE      SP70,NHETLEV
          MOVE      SP70,NHRESPA 
.
ETHSAV99  RETURN
.------------------------------------------------------------------
. * Checks mandatory fields and checks for blank
.------------------------------------------------------------------
CHKETH00  RESET     NHETH001
          MATCH     SP70,NHETH001
          GOTO      CHKETH90 IF EQUAL
.
          RESET     NHETH002
          MATCH     SP70,NHETH002
          GOTO      CHKETH91 IF EQUAL
.
          GOTO      CHKETH98
.
CHKETH90  MOVE      "Ethnic Group Code is mandatory!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKETH99
.
CHKETH91  MOVE      "Ethnic Group Code Description is mandatory!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKETH99
.
CHKETH98  MOVE      ZERO,EXIT
CHKETH99  RETURN
.------------------------------------------------------------
. Donor Codes Maintenance: Display,Add,Update,Delete
.------------------------------------------------------------
NHIDNR00  RESET     NHDCD001 
          RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      NHIDNR40 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      NHIDNR10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      NHIDNR20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      NHIDNR30 IF EQUAL
.
          GOTO      NHIDNR93
.
NHIDNR10  CALL      CHKDNR00  * Checks mandatory fields & checks for blank
          BRANCH    EXIT,NHIDNR99
          PACK      KEY1,NHDCD001,SP70  * ADD FORMACTION
          CALL      RDNHDCD1
          COMPARE   ZERO,OVRCD
          GOTO      NHIDNR92 IF EQUAL
          CALL      DNRSAV00      * Moves input variables to file variables
          CALL      WRNHDCD1      * Writes away the data
          MOVE      ZERO,EXIT
          GOTO      NHIDNR99
.
NHIDNR20  PACK      KEY1,NHDCD001,SP70  * UPDATE FORMACTION
          CALL      RDNHDCD1
          BRANCH    OVRCD,NHIDNR91
          CALL      CHKDNR00      * Checks mandatory fields
          BRANCH    EXIT,NHIDNR99
          CALL      DNRSAV00      * Moves input variables to file variables
          CALL      UPNHDCD1      * Writes away the data
          MOVE      ZERO,EXIT
          GOTO      NHIDNR99
.
NHIDNR30  PACK      KEY1,NHDCD001,SP70   * DELETE FORMACTION
          CALL      RDNHDCD1
          BRANCH    OVRCD,NHIDNR91
          CALL      DENHDCD1      * Deletes the data
          MOVE      ZERO,EXIT
          GOTO      NHIDNR99
.
NHIDNR40  PACK      KEY1,NHDCD001,SP70  
          CALL      RDNHDCD1
          IF        OVRCD=1
            CALL      CLRDNR00    * Clear all the file variables
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Donor Codes Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,NHIDNR99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      NHIDNR99
.
NHIDNR91  MOVE      "Donor Code does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      NHIDNR99
.
NHIDNR92  MOVE      "Donor Code exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      NHIDNR99
.
NHIDNR93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      NHIDNR99
.
NHIDNR99  RETURN
.-----------------------------------------------------------
. Initialize's file data
.-----------------------------------------------------------
CLRDNR00  MOVE      SP70,NHDCDCD
          MOVE      SP70,NHDCDES
          MOVE      SP70,NHDCIBA
          MOVE      SP70,NHDCSPA
.
CLRDNR99  RETURN
.------------------------------------------------------------
. Moves cgi variables into file variables
.------------------------------------------------------------
DNRSAV00  MOVE      NHDCD001,KEY1
          SQUEEZE   KEY1
          MOVE      KEY1,NHDCDCD
          MOVE      NHDCD002,NHDCDES
          MOVE      SP70,NHDCIBA
          MOVE      SP70,NHDCSPA 
.
DNRSAV99  RETURN
.------------------------------------------------------------------
. * Checks mandatory fields and checks for blank
.------------------------------------------------------------------
CHKDNR00  RESET     NHDCD001
          MATCH     SP70,NHDCD001
          GOTO      CHKDNR90 IF EQUAL
.
          RESET     NHDCD002
          MATCH     SP70,NHDCD002
          GOTO      CHKDNR91 IF EQUAL
.
          GOTO      CHKDNR98
.
CHKDNR90  MOVE      "Donor Code is mandatory!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKDNR99
.
CHKDNR91  MOVE      "Donor Code Description is mandatory!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKDNR99
.
CHKDNR98  MOVE      ZERO,EXIT
CHKDNR99  RETURN
.------------------------------------------------------------
. Domicile Codes Maintenance: Display,Add,Update,Delete
.------------------------------------------------------------
NHIDOM00  RESET     NHDCD001 
          RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      NHIDOM80 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      NHIDOM10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      NHIDOM20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      NHIDOM30 IF EQUAL
.
          MATCH     "S",UPDTTYPE    * Add formaction
          GOTO      NHIDOM40 IF EQUAL
.
          GOTO      NHIDOM93
.
NHIDOM10  CALL      CHKDOM00  * Checks mandatory fields & checks for blank
          BRANCH    EXIT,NHIDOM99
          PACK      KEY4,NHDMC001,SP70  * ADD FORMACTION
          CALL      RDNHDM1
          COMPARE   ZERO,OVRCD
          GOTO      NHIDOM92 IF EQUAL
          CALL      DOMSAV00      * Moves input variables to file variables
          REP       UPPLOW,NHDMDES
          CALL      WRNHDM1       * Writes away the data
          MOVE      ZERO,EXIT
          GOTO      NHIDOM99
.
NHIDOM20  PACK      KEY4,NHDMC001,SP70  * UPDATE FORMACTION
          CALL      RDNHDM1
          BRANCH    OVRCD,NHIDOM91
          CALL      CHKDOM00      * Checks mandatory fields
          BRANCH    EXIT,NHIDOM99
          CALL      DOMSAV00      * Moves input variables to file variables
          REP       UPPLOW,NHDMDES
          CALL      UPNHDM1       * Writes away the data
          MOVE      ZERO,EXIT
          GOTO      NHIDOM99
.
NHIDOM30  PACK      KEY4,NHDMC001,SP70   * DELETE FORMACTION
          CALL      RDNHDM1
          BRANCH    OVRCD,NHIDOM91
          CALL      DENHDM1       * Deletes the data
          MOVE      ZERO,EXIT
          GOTO      NHIDOM99
.
NHIDOM40  CLEAR     WEBTITLE
          MOVE      "Domicile Code Search",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,NHIDOM99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      NHIDOM99
.
NHIDOM80  PACK      KEY4,NHDMC001,SP70  
          CALL      RDNHDM1
          IF        OVRCD=1
            CALL      CLRDOM00    * Clear all the file variables
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Domicile Codes Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,NHIDOM99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      NHIDOM99
.
NHIDOM91  MOVE      "Domicile Code does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      NHIDOM99
.
NHIDOM92  MOVE      "Domicile Code exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      NHIDOM99
.
NHIDOM93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      NHIDOM99
.
NHIDOM99  RETURN
.-----------------------------------------------------------
. Initialize's file data
.-----------------------------------------------------------
CLRDOM00  MOVE      SP70,NHDMDOM
          MOVE      SP70,NHDMDES
          MOVE      SP70,NHDMDHB
          MOVE      SP70,NHDMSPA
.
CLRDOM99  RETURN
.------------------------------------------------------------
. Moves cgi variables into file variables
.------------------------------------------------------------
DOMSAV00  MOVE      NHDMC001,KEY4
          SQUEEZE   KEY4
          MOVE      KEY4,NHDMDOM
          MOVE      NHDMC002,NHDMDES
          MOVE      NHDMC003,NHDMDHB
          MOVE      SP70,NHDMSPA 
.
DOMSAV99  RETURN
.------------------------------------------------------------------
. * Checks mandatory fields and checks for blank
.------------------------------------------------------------------
CHKDOM00  RESET     NHDMC001
          MATCH     SP70,NHDMC001
          GOTO      CHKDOM90 IF EQUAL
.
          RESET     NHDMC002
          MATCH     SP70,NHDMC002
          GOTO      CHKDOM91 IF EQUAL
.
          GOTO      CHKDOM98
.
CHKDOM90  MOVE      "Domicile Code is mandatory!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKDOM99
.
CHKDOM91  MOVE      "Domicile Code Description is mandatory!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKDOM99
.
CHKDOM98  MOVE      ZERO,EXIT
CHKDOM99  RETURN
.------------------------------------------------------------
. Process Fields 
.------------------------------------------------------------
PROFLD00  BRANCH    REPORTNO,PROFLD10,PROFLD20,PROFLD30,PROFLD40
          GOTO      PROFLD99
.
PROFLD10  CALL      NZRL0000    * Relationship Codes Maintenance
          GOTO      PROFLD99 
.
PROFLD20  CALL      NZEC0000    * Ethnicity Codes Maintenance
          GOTO      PROFLD99 
.
PROFLD30  CALL      NZDN0000    * Donor Codes Maintenace
          GOTO      PROFLD99 
.
PROFLD40  CALL      NZDM0000    * Domicile Codes Maintenace
          GOTO      PROFLD99 
.
PROFLD99  RETURN
.------------------------------------------------------------
. Relationship Codes
.------------------------------------------------------------
NZRL0000  BRANCH    FLDITMNO,NZRL0100,NZRL0200,NZRL0300,NZRL0400,NZRL0500
          GOTO      NZRL9999
.
NZRL0100  WRITE     HTMLFILE;NHRERCD;
          GOTO      NZRL9999
.
NZRL0200  WRITE     HTMLFILE;NHREDES; 
          GOTO      NZRL9999
.
NZRL0300  CALL      RLTAB000   * Table of Relationship Codes
          GOTO      NZRL9999
.
NZRL0400  CALL      NEXTNH00
          GOTO      NZRL9999
.
NZRL0500  CALL      PREVNH00
          GOTO      NZRL9999
.
NZRL9999  RETURN
.------------------------------------------------------------
. Ethnic Group Codes
.------------------------------------------------------------
NZEC0000  BRANCH    FLDITMNO,NZEC0100,NZEC0200,NZEC0300,NZEC0400,NZEC0500
          GOTO      NZEC9999
.
NZEC0100  WRITE     HTMLFILE;NHETECD;
          GOTO      NZEC9999
.
NZEC0200  WRITE     HTMLFILE;NHETDES;
          GOTO      NZEC9999
.
NZEC0300  CALL      ECTAB000   * Table of Ethnic Group Codes
          GOTO      NZEC9999
.
NZEC0400  CALL      NEXTNH00
          GOTO      NZEC9999
.
NZEC0500  CALL      PREVNH00
          GOTO      NZEC9999
.
NZEC9999  RETURN
.------------------------------------------------------------
. Donor Codes 
.------------------------------------------------------------
NZDN0000  BRANCH    FLDITMNO,NZDN0100,NZDN0200,NZDN0300,NZDN0400,NZDN0500
          GOTO      NZDN9999
.
NZDN0100  WRITE     HTMLFILE;NHDCDCD;
          GOTO      NZDN9999
.
NZDN0200  WRITE     HTMLFILE;NHDCDES;
          GOTO      NZDN9999
.
NZDN0300  CALL      DNTAB000   * Table of Donor Codes
          GOTO      NZDN9999
.
NZDN0400  CALL      NEXTNH00
          GOTO      NZDN9999
.
NZDN0500  CALL      PREVNH00
          GOTO      NZDN9999
.
NZDN9999  RETURN
.------------------------------------------------------------
. Domicile Codes
.------------------------------------------------------------
NZDM0000  BRANCH    FLDITMNO,NZDM0100,NZDM0200,NZDM0300,NZDM0400,NZDM0500:
                             NZDM0600,NZDM0700,NZDM0800,NZDM0900
          GOTO      NZDM9999
.
NZDM0100  WRITE     HTMLFILE;NHDMDOM;
          GOTO      NZDM9999
.
NZDM0200  WRITE     HTMLFILE;NHDMDES;
          GOTO      NZDM9999
.
NZDM0300  CALL      DMTAB000   * Table of Domicile Codes
          GOTO      NZDM9999
.
NZDM0400  CALL      NEXTNH00
          GOTO      NZDM9999
.
NZDM0500  CALL      PREVNH00
          GOTO      NZDM9999
.
NZDM0600  CALL      DSRCH000
          GOTO      NZDM9999
.
NZDM0700  CALL      NXTGRP00
          GOTO      NZDM9999
.
NZDM0800  CALL      PRVGRP00
          GOTO      NZDM9999
.
NZDM0900  MOVE      NHDMDHB,CODE
          MOVE      "dh",CATEGORY
          CALL      CODITM00
          GOTO      NZDM9999
.
NZDM9999  RETURN
.*****************************************************************************
.*                           Domicile Code Search                            *
.*****************************************************************************
DSRCH000  CALL      PHHD0000                * Output Table Heading
          IF        NORECORD=0
            MOVE      "10",NORECORD         * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY54,DOMICKEY,SP70
          CALL      RSNHDM2
DSRCH010  CALL      RKNHDM2 
          BRANCH    OVRCD,DSRCH999
          STRIP     DOMICKEY
          MATCH     NHDMDES,DOMICKEY
          GOTO      DSRCH999 IF NOT EQUAL
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      DSRCH010
          ENDIF
.
          WRITE     HTMLFILE;"<tr valign=top>":
                             "<td><a href=#"":
                         "javascript:updateParent('",NHDMDOM,"')#">":
                         NHDMDOM,"</a>":
                         "</td><td>",NHDMDES,"</td>";
.
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      DSRCH010 IF NOT EQUAL
.
DSRCH999  RETURN
.
.-----------------------------------------------------------
. Link to Next Group option 1
.-----------------------------------------------------------
NXTGRP00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      RECNUMB,KEY5
          REP       " +",KEY5
          MOVE      REPORTNO,F1
.
          WRITE     HTMLFILE;"nhiweb97.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY5:
                                 "&norecord=",KEY2:
                                 "&domickey=",DOMICKEY;
.
NXTGRP99  RETURN
.
.-------------------------------------------------------------
. Link to Prev Group 
.-------------------------------------------------------------
PRVGRP00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F5                      * C CAR 41918
          IF        F5<0
            MOVE      ZERO,F5
          ENDIF
          MOVE      F5,KEY5
          REP       " +",KEY5                                 * end C CAR 41918
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
.
          WRITE     HTMLFILE;"nhiweb97.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY5:           * C CAR 41918
                                 "&norecord=",KEY2:
                                 "&domickey=",DOMICKEY;
.
PRVGRP99  RETURN
.
.*****************************************************************************
.*                  Domicile Code Search(Table Heading Formation)            *
.*****************************************************************************
PHHD0000  WRITE     HTMLFILE;"<tr align=center>":
                               "<td class=HeadingCell colspan=6>":
                               "You Searched for ":
                               DOMICKEY,"</td>":
                               "</tr>":
                               "<tr>":
                               "<td class=HeadingCell width=25%>":
                               "<b>Domicile Code</b></td>":
                               "<td class=HeadingCell width=75%>":
                               "<b>Description</b></td>":
                             "</tr>"
.
PHHD9999  RETURN
.
.-----------------------------------------------------------
. Link to Next Group
.-----------------------------------------------------------
NEXTNH00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
.
          WRITE     HTMLFILE;"nhiweb97.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2;
.
NEXTNH99  RETURN
.-------------------------------------------------------------
. Link to Prev Group
.-------------------------------------------------------------
PREVNH00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3
          IF        F3<0
            MOVE      ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
.
          WRITE     HTMLFILE;"nhiweb97.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2;
.
PREVNH99  RETURN
.------------------------------------------------------------
. Table of Relationship Codes
.------------------------------------------------------------
RLTAB000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          CALL      RLHED000      * Output Table Heading
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY2,STARTKEY,SP70
          CALL      RSNHREL1
          IF        OVRCD=0
            CALL      RPNHREL1
          ENDIF
RLTAB100  CALL      RKNHREL1
          BRANCH    OVRCD,RLTAB900
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      RLTAB100
          ENDIF
.
          CALL      RLROW000      * Display Relationship Code Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      RLTAB100 IF NOT EQUAL
          GOTO      RLTAB999
.
RLTAB900  CALL      NOMORE00      * Display No More Records Message
.
RLTAB999  RETURN
.------------------------------------------------------------
. Relationship Codes (TABLE HEADING)
.------------------------------------------------------------
RLHED000  WRITE     HTMLFILE;"<tr>":
                               "<td class=HeadingCell><b>Code</b></td>":
                               "<td class=HeadingCell><b>Description</b></td>":
                             "</tr>"
.
RLHED999  RETURN
.------------------------------------------------------------
. Relationship Code (TABLE ROW)
.------------------------------------------------------------
RLROW000  WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:ShowDetail01":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&nhrel001=",NHRERCD,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             NHRERCD,"</td>":
                             "<td>",NHREDES,"</td>":
                             "</tr>"
.
RLROW999 RETURN
.------------------------------------------------------------
. Table of Ethnic Group Codes 
.------------------------------------------------------------
ECTAB000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          CALL      ECHED000      * Output Table Heading
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY5,STARTKEY,SP70
          CALL      RSNHETH1
          IF        OVRCD=0
            CALL      RPNHETH1
          ENDIF 
ECTAB100  CALL      RKNHETH1
          BRANCH    OVRCD,ECTAB900
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      ECTAB100
          ENDIF
.
          CALL      ECROW000      * Display Ethnic Group Code Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      ECTAB100 IF NOT EQUAL
          GOTO      ECTAB999
.
ECTAB900  CALL      NOMORE00      * Display No More Records Message
.
ECTAB999  RETURN
.------------------------------------------------------------
. Ethnic Group Code (TABLE HEADING)
.------------------------------------------------------------
ECHED000  WRITE     HTMLFILE;"<tr>":
                               "<td class=HeadingCell><b>Code</b></td>":
                               "<td class=HeadingCell><b>Description</b></td>":
                             "</tr>"
.
ECHED999  RETURN
.------------------------------------------------------------
. Ethnic Group Code (TABLE ROW)
.------------------------------------------------------------
ECROW000  WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:ShowDetail02":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&nheth001=",NHETECD,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             NHETECD,"</td>":
                             "<td>",NHETDES,"</td>":
                             "</tr>"
.
ECROW999 RETURN
.------------------------------------------------------------
. Table of Donor Codes 
.------------------------------------------------------------
DNTAB000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          CALL      DNHED000      * Output Table Heading
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY1,STARTKEY,SP70
          CALL      RSNHDCD1
          IF        OVRCD=0
            CALL      RPNHDCD1
          ENDIF
DNTAB100  CALL      RKNHDCD1
          BRANCH    OVRCD,DNTAB900
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      DNTAB100
          ENDIF
.
          CALL      DNROW000      * Display Donor Code Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      DNTAB100 IF NOT EQUAL
          GOTO      DNTAB999
.
DNTAB900  CALL      NOMORE00      * Display No More Records Message
.
DNTAB999  RETURN
.------------------------------------------------------------
. Donor Codes (TABLE HEADING)
.------------------------------------------------------------
DNHED000  WRITE     HTMLFILE;"<tr>":
                               "<td class=HeadingCell><b>Code</b></td>":
                               "<td class=HeadingCell><b>Description</b></td>":
                             "</tr>"
.
DNHED999  RETURN
.------------------------------------------------------------
. Donor Codes (TABLE ROW)
.------------------------------------------------------------
DNROW000  WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:ShowDetail03":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&nhdcd001=",NHDCDCD,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             NHDCDCD,"</td>":
                             "<td>",NHDCDES,"</td>":
                             "</tr>"
.
DNROW999 RETURN
.------------------------------------------------------------
. Table of Domicile Codes 
.------------------------------------------------------------
DMTAB000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          CALL      DMHED000      * Output Table Heading
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY4,STARTKEY,SP70
          CALL      RSNHDM1
          IF        OVRCD=0
            CALL      RPNHDM1
          ENDIF
DMTAB100  CALL      RKNHDM1
          BRANCH    OVRCD,DMTAB900
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      DMTAB100
          ENDIF
.
          CALL      DMROW000      * Display Domicile Code Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      DMTAB100 IF NOT EQUAL
          GOTO      DMTAB999
.
DMTAB900  CALL      NOMORE00      * Display No More Records Message
.
DMTAB999  RETURN
.------------------------------------------------------------
. Domicile Codes (TABLE HEADING)
.------------------------------------------------------------
DMHED000  WRITE     HTMLFILE;"<tr>":
                               "<td class=HeadingCell width=25%><b>Code</b></td>":
                               "<td class=HeadingCell><b>Description</b></td>":
                               "<td class=HeadingCell width=25%><b>District Health Board</b></td>":
                             "</tr>"
.
DMHED999  RETURN
.------------------------------------------------------------
. Domicile Codes (TABLE ROW)
.------------------------------------------------------------
DMROW000  MOVE      SP70,DISPDESC
          MATCH     SP70,NHDMDHB
          GOTO      DMROW010 IF EQUAL
.
          PACK      KEY5,CATdh,NHDMDHB,SP70
          CALL      RDCODE1
          IF        OVRCD=0
            MOVE      PTCDLDES,DISPDESC
          ENDIF
.
DMROW010  WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:ShowDetail04":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&nhdmc001=",NHDMDOM,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             NHDMDOM,"</td>":
                             "<td>&nbsp;",NHDMDES,"</td>":
                             "<td>&nbsp;",DISPDESC,"</td>":
                             "</tr>"
.
DMROW999  RETURN
.------------------------------------------------------------
. NOMORE00 - Display "No more records" at end of list
.------------------------------------------------------------
NOMORE00  WRITE     HTMLFILE;"<tr>":
                             "<td class=RedCell colspan=20 align=center>":
                             "No more records</td>"
.
NOMORE99  RETURN
.
.-------------------------------------------------------------
.
          INC       NHIRELIO/INC   * Relationship Codes 
          INC       NHIETHIO/INC   * Ethnicity Codes
          INC       NHIDCDIO/INC   * Donor Codes
          INC       NHIDMCIO/INC   * Domicile Code Masterfile
.
          INC       DAYOFWEK   
          INC       IBAWEBCD
          INC       WEBDATCD



.***************************************************************************
.* System    :   Inpatients                                                *
.* Program   :   Auto Cancel Prior Pre-Admission                           *
.* Desc      :   This program will automatically cancel outstanding        *
.*           :   pre-admission date that is more than X days past          *
.***************************************************************************
.* Date      :   15/04/2022                                                *
.* Author    :   Jill Parkinson                                            *
.* Function  :   This program will automatically cancel prior pre-admission*
.* Modifications  :                                                        *
.*                                                                         *
.* V12.00.01 15/05/2025 Nikitha B         TSK 0955096                      *
.*           Alphanumeric changes                                          *
.* V11.03.01 28/07/2023  Ebon Clements    TSK 0935527                      *
.*           Recompiled for OPRQUEFD - Theatre booking hospital            *
.* V11.02.00 26/04/2022  Jill Parkinson and Alina Bhari   TSK 0912792      *
.*           Created program                                               *
.***************************************************************************
.
          INC       STD001FD
          INC       BOKSTAFD/INC
          INC       BOKRC1FD/INC
          INC       BOKRX1FD/INC
          INC       EOCLNKFD/INC
          INC       EMRVISFD/INC
          INC       OPRDEAFD/INC
          INC       MEHDIAFD/INC
          INC       MEHCJAFD/INC
          INC       MEHLEGFD/INC
          INC       MEHVI1FD/INC
          INC       MRTVISFD/INC
          INC       NHIMASFD/INC
          INC       PATDRGFD/INC
          INC       PATASFFD/INC
          INC       PATDSCFD/INC
          INC       PATTRNFD/INC
          INC       PATHSPFD/INC      * Hospital File
          INC       PATCODFD/INC      * Category Code
          INC       PMSCURFD/INC
          INC       IBACONFD/INC
	  INC       OPRCONFD/INC
          INC       PATCONFD/INC
          INC       MEHCONFD/INC
          INC       WATCONFD/INC
          INC       PATBMDFD/INC
          INC       PMSBRQFD/INC
          INC       PATONLFD/INC
          INC       PATRE1FD/INC
          INC       PATWR1FD/INC
          INC       PATMA1FD/INC
          INC       PATGSRFD/INC
          INC       PATDADFD/INC
          INC       PATPR1FD/INC
          INC       PMSAIDFD/INC
          INC       DISMASFD/INC
          INC       DISPATFD/INC
          INC       DISPTLFD/INC
          INC       PATNIDFD/INC
          INC       PATPNIFD/INC
          INC       PMSPX2FD/INC
          INC       PMSQVIFD/INC
          INC       PATMI1FD/INC
          INC       PATWVEFD/INC
          INC       PATWMAFD/INC
          INC       PATWC1FD/INC
          INC       PMSWX1FD/INC
          INC       PMSVX1FD/INC
          INC       PMSCTCFD/INC
          INC       PATUNAFD/INC
          INC       PATVISFD/INC
          INC       RCPBNKFD/INC
          INC       RCPDTEFD/INC
          INC       COMDEPFD/INC
          INC       WATESNFD/INC
          INC       WATCATFD/INC
          INC       WATCHAFD/INC
          INC       WATTR1FD/INC
          INC       WATHISFD/INC
          INC       WATPROFD/INC
          INC       WATTX1FD/INC
          INC       WEBSECFD/INC
          INC       VISINTFD/INC
          INC       VISIAUFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
ADMISSNO  DIM       8
ADDWLFLG  FORM      1
AUDIFLAG  FORM      1
CALDAYS   FORM      4
CBOOK     FORM      1
CHKGRPIN  FORM      1
CMDLINE   DIM       127       * Command line
CODE      DIM       3
CURDATE   DIM       8         * Current date
CNCLCODE  DIM       3         * Category actual code
CTGRCODE  DIM       2         * Tcode, Type of code
CPADM001  DIM       3         * Booking Cancellation Reason CAT BC
CPADM002  DIM       1         * Return to Waiting list? (Y/N)
CPADM003  DIM       3         * Removal Reason
CPADM004  DIM       3         * Reason for Change of Schedule Adm
CPADM006  DIM       3         * Theatre Booking Cancellation Reas
CPADM007  DIM       3         * Booking Cancellation Reason CAT BE
CPADM008  DIM       8         * Removal Date
CPADM009  DIM       8         * Removal Date Time
CPTDATE   DIM       8
CURRDATE  DATE      8         * current date
CALCDATE  DATE      8         * date calculated
DAYPRIOR  FORM      3
DIM9      DIM       9
DISPTDAT  DIM       10        * Print to date
DISPFDAT  DIM       10        * Print from date
DIM11     DIM       11
DIM25     DIM       25
DIM90     DIM       90
ERRORFLG  FORM      1
FADATE    FORM      8               * Prior Pre-admission date
FCURDATE  FORM      8               * current date
FILTHOSP  DIM       3
KEEPBKTM  DIM       1
HOUR      FORM      2
HBWAIT    FORM      1
INTAUDTY  DIM       1
MIN       DIM       2
MINUTES   FORM      6
MHLRCODE  DIM       3
NBRDAYS   FORM      6
NMPNUMB   DIM       20
NEXTPAGE  FORM      1 
PREVSTAT  FORM      1
PREVSLOT  FORM      3
PTMIS001  DIM       8
PTMIS002  DIM       8
PTMIS003  DIM       3
TOTALLIN  FORM      1         * Print total line
VISFRDAT  DIM       8         * From date
VISTODAT  DIM       8         * To Date
RECCOUNT  FORM      8
REPTONLY  DIM       1         * Report only
REPONLYD  DIM       3         * Report only description
REDIRECT  DIM       200
SAVADMN   DIM       8
SAVBOOK   DIM       8
SAVEBRQN  DIM       8
SAVKEY31  DIM       31
SAVTDEPT  DIM       3
SAVTXWRD  DIM       3
SAVDISKY  DIM       17
URNUMBER  DIM       8
XDATTIME  DIM       14     * Date/Time (ccyymmddhhmmss)
WATHI002  DIM       8
ADDSECND  FORM      1
DIM8      DIM       8
HOURD     DIM       2
HOURTM    DIM       2
MINTIME   DIM       2
SEC       DIM       2
SECTIME   DIM       2
UPDATETY  FORM      1
USERID    DIM       10
CATcz     INIT      "cz"
DELESN    INIT      "DELETE"
ZERO6     INIT      "000000"
WEBTITLE  DIM       100
.
PRGID     INIT      "IBAPAT64"
PRGNAM    INIT      "Auto Cancel Prior Preadmissions"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      HOSP0000               * Get Hospital Code if Required
          CALL      DAYX0000               * Get Days prior
          CALL      CNCL0000               * Get Cancellation Reason
          CALL      KREP0000               * Keyin Report Only
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      PATHSPA1,"pathspaf"
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TEN5;*187,CBOOK
          READ      CONTROLF,SIXTY9;*43,MHCNAUDA,*69,MHCNUSE
          READ      CONTROLF,THIRTY7;*241,WTCNUSBC
          CALL      IBACLOCK
.
          READ      CONTROLF,TWENTY7;*46,HBWAIT
          OPEN      BOKBSTA1,"bokstaaf"
          OPEN      BOKRC1A1,"bokrc1af"
          OPEN      BOKRX1A1,"bokrx1af"
          OPEN      EOCLNKA1,"eoclnkaf"
          OPEN      EMRVISA1,"emrvisaf"
          OPEN      EMRVISA3,"emrvisaf"
          READ      CONTROLF,SIXTY9;*43,MHCNAUDA,*69,MHCNUSE,*80,MHCNUDSA
          IF        MHCNUSE=1
            OPEN      MEHDIAA1,"mehdiaaf"
            OPEN      MEHCJAA1,"mehcjaaf"
            OPEN      MEHLEGA1,"mehlegaf"
            OPEN      MEHVI1A1,"mehvi1af"
          ENDIF
          IF        MHCNAUDA=0
            OPEN      MEHAUVI1,"mehauvi1"
          ENDIF
          OPEN      MRTVISA1,"mrtvisaf"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATTRAN1,"pattranf"
          OPEN      PATDRGA1,"patdrgaf"
          OPEN      PATGSRN1,"patgsrnf"
          OPEN      PATBMDA1,"patbmdaf"
          OPEN      PATBMDA2,"patbmdaf"
          OPEN      PMSBRQA1,"pmsbrqaf"
          OPEN      PMSBRQA2,"pmsbrqaf"
          OPEN      PATASFA1,"patasfaf"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PATWC1A1,"patwc1af"
          OPEN      PATWVET1,"patwvetf"
          OPEN      PATWMAB1,"patwmabf"
          OPEN      PMSWX1A1,"pmswx1af"
          OPEN      PMSCTCA1,"pmsctcaf"
          OPEN      PATNIDA1,"patnidaf"
          OPEN      PATGSRN1,"patgsrnf"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
          OPEN      PATDADA1,"patdadaf"
          OPEN      PMSAIDA1,"pmsaidaf"
          OPEN      DISMASA1,"dismasaf"
          OPEN      DISPATA1,"dispataf"
          OPEN      DISPTLA1,"disptlaf"
          OPEN      PMSPX2A1,"pmspx2af"
          OPEN      PMSCURA1,"pmscuraf"
          OPEN      PATVISA1,"patvisaf"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      VISINTA1,"visintaf"
          OPEN      VISIAUA1,"visiauaf"
          OPEN      WATESNA5,"watesnaf"
          OPEN      WATCATA1,"watcataf"
          OPEN      WATCHAA1,"watchaaf"
          OPEN      WATTR1A1,"wattr1af"
          OPEN      WATHISA1,"wathisaf"
          OPEN      WATPROA1,"watproaf"
          OPEN      WATTX1A1,"wattx1af"
          OPEN      WEBSECA3,"websecaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
PROC0000  CALL      HEAD0000                * Print heading
          MOVE      ZERO,RECCOUNT
.
          PACK      KEY9,ONE,SP70
          CALL      RDSMISS2
PROC1000  CALL      RDKMISS2
          BRANCH    OVRCD,PROC9000
.
          COMPARE   "1",ASTAT
          GOTO      PROC9000 IF NOT EQUAL
.
          MATCH     ADATE,CALCDATE
          GOTO      PROC1000 IF LESS
.
          ADD       ONE,RECCOUNT
          IF        RECCOUNT>999
            PRINT     *1,"*********** Limit of 1000 records reached *******"
            GOTO      PROC9000
          ENDIF
.
          MOVE      DAADMNO,ADMISSNO
          MATCH     ANSN,REPTONLY
          IF        @EQUAL
            MOVE      ZERO,ERRORFLG
            OPEN      BOKRC1A1,"bokrc1af"
            CALL      CPADM000
            IF        ERRORFLG=1
.   there was an error, so this record has already been printed 
              GOTO      PROC1000
            ENDIF
          ENDIF
.
          CALL      PRIN0000                * Print report
          GOTO      PROC1000
.
PROC9000  CALL      UND132
          PRINT     *1,"Records : ",RECCOUNT
          PRINT     *1,"***** End of Report ****"
.
PROC9999  RETURN
+
.*********************************************************************
. PRIN0000 - Print the report
.*********************************************************************
PRIN0000  UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *1,"| ",AURNO,*12,"| ",CPCDATE:
                    *30,"| ",DAADMNO,*40,"|",*132,"|" 
.
PRIN9999  RETURN
+
.*********************************************************************
. WEBERR0 - Print the error report
.*********************************************************************
WEBERR00  UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      SP70,DIM90
          UNPACK    WEBTITLE,DIM90
          PRINT     *1,"| ",AURNO,*12,"| ",CPCDATE:
                    *30,"| ",DAADMNO,*40,"|",DIM90,*132,"|" 
          MOVE      ONE,ERRORFLG
.
WEBERR99  RETURN
.*********************************************************************
. HEAD0000 - Print heading
.*********************************************************************
HEAD0000  MOVE      ONE,CNOUNDLN
          CALL      HEAD132
.
          IF        IBCNMHOS = 1
            PRINT     *40,"Hospital                    :",*71,PTHSNAME
          ENDIF
          UNPACK    CALCDATE,CCENT,CYEAR,CMON,CDAY
          PRINT     *40,"Prior Pre-Admission Date      :",*71:
                     CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          PRINT     *40,"Cancellation Reason           :",*71,TDESC
          PRINT     *40,"Report Only :",*71,REPTONLY
          PRINT     *1," "
          CALL      UND132
          PRINT     *1,"| UR NO",*12,"| Admit Date ":
                    *30,"|Visit No",*40,"| Error Conditions",*132,"|"
          CALL      UND132
          IF        IBCNMHOS = 1
            MOVE      11,CLNO
          ELSE
            MOVE      10,CLNO
          ENDIF
.
HEAD9999  RETURN
+
.*********************************************************************
.*        HOSP0000                               Called by ML0000    *
.*        Get hospital code                                          *
.*********************************************************************
HOSP0000  IF        IBCNMHOS = 1
            DISPLAY   *P1:10,*EL,"Hospital Id : ";
            MOVE      "25",ECOL
            MOVE      "10",EVERT
            MOVE      SP3,CKYIDEF3
            MOVE      ZERO,CKYIMAND
HOSP1100    CALL      PATHSPKY
            IF        EXIT = ONE
              MOVE      SP70,FILTHOSP
              MOVE      "All Hospitals",PTHSNAME
              MOVE      SP70,PTHSHOSP
              DISPLAY   *P29:10,*EL,PTHSNAME,*W2;
            ELSE
              IF        EXIT = 2
                MOVE      SP70,FILTHOSP
                GOTO      HOSP9400
              ELSE
                MOVE      KEY3,FILTHOSP
                DISPLAY   *P29:10,*EL,PTHSNAME,*W2;
              ENDIF
            ENDIF
         ENDIF
.
HOSP1199  MOVE      ZERO,EXIT
          GOTO      HOSP9999
.
HOSP9400  DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid Hospital ID ** ";
          CALL      HITENTER
          GOTO      HOSP0000
.
HOSP9999  RETURN
+
.**********************************************************************
.* DAYX0000 - Number of days prior to cancel                          *
.**********************************************************************
DAYX0000  DISPLAY   *P1:11,*EL:
                    *P1:11,"Days Prior to cancel : ";
.
DAYX0500  MOVE      TWENTY,DAYPRIOR
          DISPLAY   *P25:11,UNDLN1;
          KEYIN     *P25:11,*V2LON,DAYPRIOR;
.
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          SUB       DAYPRIOR,CURRDATE
          MOVE      CURRDATE,CALCDATE
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          DISPLAY   *P25:16,"Date From ",CALCDATE;
.
DAYX9999  RETURN
+

.**********************************************************************
.* KREP0000 - Keyin report only                                       *
.**********************************************************************
KREP0000  DISPLAY   *P1:13,*EL:
                    *P1:13,"Report Only : ";
.
.-------- keyin slot type to delete  -----
.
KREP0500  MOVE      SP70,REPTONLY
          DISPLAY   *P25:13,UNDLN1;
          KEYIN     *P25:13,*V2LON,REPTONLY;
.
          PACK      REPTONLY,REPTONLY,SP70
.
          MATCH     SP70,REPTONLY
          IF        @EQUAL
            MOVE      "Y",REPTONLY * Default to report only
          ENDIF
.
          MATCH     "N",REPTONLY   * Run and delete
          IF        @EQUAL
            MOVE      "No ",REPONLYD
            DISPLAY   *P29:13,*EL,REPONLYD;
            GOTO      KREP9999
          ENDIF

          MATCH     "Y",REPTONLY   * Report only
          IF        @EQUAL
            MOVE      "Yes",REPONLYD
            DISPLAY   *P29:13,*EL,REPONLYD;
            GOTO      KREP9999
          ENDIF
.
          DISPLAY   *P1:13,*EL,*B,"Invalid report only option - Hit <":
                              *V2LON,"ENTER",*HOFF,"> To Continue ";
          KEYIN     ANS;
          DISPLAY  *P1:13,*EL;
          GOTO      KREP0500
.
KREP9999  RETURN
+
.**********************************************************************
.* CNCL0000 - Cancellation Reason                                     *
.**********************************************************************
.
CNCL0000  MOVE      SP70,CNCLCODE
          MOVE      SP70,CTGRCODE
.          
          DISPLAY   *P1:12,*EL:
                    *P1:12,"Cancellation Reason : ";
CNCL1000  MOVE      "PC",CODE
          MOVE      SP3,CKYIDEF3
          MOVE      ONE,CKYIMAND
          MOVE      "25",ECOL
          MOVE      "12",EVERT
          CALL      PATCODKY
          BRANCH    EXIT,CNCL1000
.
          MOVE      ACODE,CNCLCODE
          MOVE      ACODE,CPADM001
.
CNCL9999  RETURN
+
.
.*******************************************************************************
. RMDIS000 - Remove disaster details when cancelling preadmission
.*******************************************************************************
..  First, delete the disaster link record if there is one
RMDIS000  PACK     SAVDISKY,SP70
          PACK     KEY25,URNUMBER,SP70
          CALL     RSDSPTL1
RMDIS100  CALL     RKDSPTL1
          BRANCH   OVRCD,RMDIS999
.
          MATCH    DSPLURNO,URNUMBER
          GOTO     RMDIS999 IF NOT EQUAL
.
          MATCH    DSPLVISN,DAADMNO
          GOTO     RMDIS100 IF NOT EQUAL
.
          PACK     KEY25,DSPLURNO,DSPLCODE,DSPLVISN,SP70
          CALL     DEDSPTL1
          PACK     SAVDISKY,DSPLURNO,DSPLCODE,SP70
.
..  If this was the only disaster link record, delete the patient level record
.
          PACK     KEY25,URNUMBER,SP70
          CALL     RSDSPTL1
          CALL     RKDSPTL1
          BRANCH   OVRCD,RMDIS200
.
          MATCH    DSPLURNO,URNUMBER
          GOTO     RMDIS200 IF NOT EQUAL
          GOTO     RMDIS999
.
RMDIS200  PACK     KEY17,SAVDISKY,SP70
          CALL     DEDSPAT1
          CALL     DELALT00
.
RMDIS999  RETURN
+
.---------------------------------------------------------------------
. DELALT00 - Delete alternate ID record for disaster patients
.---------------------------------------------------------------------
DELALT00  UNPACK    SAVDISKY,DIM8,DIM9
          PACK      KEY9,DIM9,SP70
          CALL      RDDSMAS1
          BRANCH    OVRCD,DELALT99
.
          MATCH     SP70,DSMAAICD
          GOTO      DELALT99 IF EQUAL
          GOTO      DELALT99 IF EOS
.
          PACK      KEY31,DIM8,DSMAAICD,SP70
          CALL      RSPMAID1
          CALL      RKPMAID1
          BRANCH    OVRCD,DELALT99
.
          MATCH     PMAIURNO,DIM8
          GOTO      DELALT99 IF NOT EQUAL
.
          MATCH     PMAITYPE,DSMAAICD
          GOTO      DELALT99 IF NOT EQUAL
.
          PACK      KEY31,PMAIURNO,PMAITYPE,PMAIALID,SP70
          CALL      DEPMAID1
.
DELALT99  RETURN
+
.*******************************************************************************
. INTDEL00 - Delete Interpreter Table Record
. Input  - DAADMNO
.          AURNO
.*******************************************************************************
.
INTDEL00  PACK      KEY8,AURNO,SP70
          CALL      RDPMPX21
          BRANCH    OVRCD,INTDEL99
.
          MATCH     "1",PMPXINTR
          GOTO      INTDEL99 IF NOT EQUAL
.
          PACK      KEY8,DAADMNO
          CALL      RDVSINT1
          IF        OVRCD=0
            CALL      DEVSINT1           * Delete record from Int.Table(Cancel)
            MOVE      "D",INTAUDTY
            CALL      INTAUD00           * Add record to Int.Audit after Delete
          ENDIF
          MOVE      ZERO,EXIT
.
INTDEL99  RETURN
+
.*******************************************************************************
. INTAUD00 - Update Interpreter Audit Table
.  Input - CASEKEYS  Key To Booking
.          INTAUDTY  Audit Type
.*******************************************************************************
.
INTAUD00  CALL      CLVISIAU
          PACK      VSIACDAT,CCC,CYY,CMM,CDD
          REP       " 0",VSIACDAT
          MOVE      CTIMEIS,VSIACTIM
          MOVE      INTAUDTY,VSIARTYP
.
          MOVE      VSINVISN,VSIAVISN
          MOVE      VSINDATE,VSIADATE
          MOVE      VSINTIME,VSIATIME
.....     MOVE      "3",VSIATYPE                                        D-40489
          MOVE      VSINTYPE,VSIATYPE                                  *I-40489
          MOVE      VSINSUBK,VSIASUBK
          MOVE      VSINWUID,VSIAWUID
          MOVE      PMPXLNG1,VSIALNG1
          MOVE      PMPXLNG2,VSIALNG2
          MOVE      PMPXLNG3,VSIALNG3
          MOVE      VSINNOTF,VSIANOTF
          MOVE      VSINCONF,VSIACONF
.
          PACK      KEY25,VSIACDAT,VSIACTIM,VSIARTYP,VSIAVISN,SP70
          CALL      RAVSIAU1
          IF        OVRCD=0
            CALL      UPVSIAU1
          ELSE
            CALL      WRVSIAU1
          ENDIF
          MOVE      ZERO,EXIT
.
INTAUD99  RETURN
+

INTINP00  RETURN
+

. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       PATHSPKY
          INC       PREADCAN
          INC       BOKSTAIO/INC
          INC       BOKRC1IO/INC
          INC       BOKRX1IO/INC
          INC       EOCLNKIO/INC
          INC       EMRVISIO/INC
          INC       OPRDEAIO/INC
          INC       MEHDIAIO/INC
          INC       MEHLEGIO/INC
          INC       MEHCJAIO/INC
          INC       MEHVI1IO/INC
          INC       MRTVISIO/INC
          INC       NHIMASIO/INC
          INC       RCPBNKIO/INC
          INC       RCPDTEIO/INC
          INC       COMDEPIO/INC
          INC       PATBMDIO/INC
          INC       PATASFIO/INC
          INC       PMSBRQIO/INC
          INC       PATDRGIO/INC
          INC       PATMI1IO/INC
          INC       PMSCURIO/INC
          INC       PATWVEIO/INC
          INC       PATWMAIO/INC
          INC       PATWC1IO/INC
          INC       PMSWX1IO/INC
          INC       PMSCTCIO/INC
          INC       PMSVX1IO/INC
          INC       PATHSPIO/INC      * Hospital File
          INC       PATCODIO/INC      * Category Code
          INC       PATGSRIO/INC
          INC       PATMA1IO/INC
          INC       PATPR1IO/INC
          INC       PATDADIO/INC
          INC       DISMASIO/INC
          INC       DISPATIO/INC
          INC       DISPTLIO/INC
          INC       PATNIDIO/INC
          INC       PATPNIIO/INC
          INC       PMSAIDIO/INC
          INC       PMSPX2IO/INC
          INC       PATUNAIO/INC
          INC       PATVISIO/INC
          INC       PATDSCIO/INC
          INC       PATTRNIO/INC
          INC       WATESNIO/INC
          INC       WATCATIO/INC
          INC       WATCHAIO/INC
          INC       WATHISIO/INC
          INC       WATPROIO/INC
          INC       WATTR1IO/INC
          INC       WATTX1IO/INC
          INC       WEBSECIO/INC
          INC       VISINTIO/INC
          INC       VISIAUIO/INC
.
          INC       BEDBDPRO 
          INC       PATCODKY 
          INC       CLWATTX1 
          INC       CLVISIAU 
          INC       CLPMSPX2
          INC       DGCLIUWL 
          INC       DGCLIRWL 
          INC       DGCLIWLD 
          INC       DGCLICPA 
          INC       DGCLICCB 
          INC       DGCLIS14 
          INC       EOCDELEV 
          INC       GENBOKNO 
          INC       GENANVIS
          INC       MHAUDVIS 
          INC       PATCOMN3 
          INC       PMIGTNID 
          INC       UPEADSTS 
          INC       WLHISSUB

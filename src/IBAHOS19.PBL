. *****************************************************************************
. *              P R O G R A M  S U M M A R Y                                 *
. *              -------------  -------------                                 *
. *                                                                           *
. *     PROGRAM NAME:     IBAHOS19                                            *
. *                                                                           *
. *     FUNCTION:         CURRENT ADMISSIONS BY CODE                          *
. *                                                                           *
. *     DATE WRITTEN:     20/05/88                                            *
. *                                                                           *
. *     AUTHOR:           KARIN PEAK   (I.B.A)                                *
. *                                                                           *
. *     MODIFICATIONS:                                                        *
. *                                                                           *
. *       V12.00.01 22/05/2025  Alina Bhari   TSK 0955096                     *
. *                 Added alpanumeric visit number generation -AADMNO         *
. *       V10.13.01 05/12/2018  Don Nguyen    TSK 0838558                     *
. *                 Deleted temp file (PATROL) after processing               *
. *       V10.10.01 31/03/2017  Ania P        CAR 261630                      *
. *                 Removed use of PORT                                       *
. *       V10.03.01 19/04/2012  Mike Laming   CAR 263541                      *
. *                 Moved "OPEN PMSVX1A1" to "OPEN PATMI1A1" as now read in   *
. *                 PATMI1IO                                                  *
. *       V9.08.01  28/03/2007  Neelkamal Pyla CAR 108502                     *
. *                 Added code to filter the records based on Hospital Id for *
. *                 Multi-Hospital sites, displayed Hospital Id in the header *
. *       V5.08.01  28/08/2000  Caleb Sharman                                 *
. *                 Changed Health Fund variables to be 8 chars               *
. *                   V5.07.B03 28/01/1999  N Harrington                      *
. *                             507 rebound                                   *
. *                   V5.07.B02 05/01/1998  J Habasque                        *
. *                             SRF 645223 Fixed display                      *
. *                   V5.07.B01 09/11/1998  Davin                             *
. *                             Mods for 507                                  *
. *****************************************************************************
. *                       V5.01 06.05.89 S.Barcham                            *
. *                             Fix 06476 I * D                               *
. *                             SRF100463                                     *
. *                       V5.02 17/05/89 S.O'Gorman                           *
. *                             Allow for 8 Digit U.R's &                     *
. *                             Admission Numbers                             *
. *                             Include STD001                                *
. *                       V5.03 27/05/89 J.Tan                                *
. *                             Fixed Mother Adm. to form8                    *
. *                             (PATMI1FD).     SRF 100750                    *
. *                       V5.04 06/06/89 J.Tan                                *
. *                             Mods Locking master file                      *
. *                             (PATIOMAS)     SRF 100809                     *
. *                    V5.01.01 05/07/89 M.Ohleiter                           *
. *                             Changes for 8 char ur and                     *
. *                             admiss no, standards - 5.01                   *
. *                   V5.01.121 27/11/90 Glen Hobby                           *
. *                            Recompiled for changes to                      *
. *                            PATMX1FD                                       *
. *                   V5.01.122 5/5/92                                        *
. *                            Removed hard coded desc's                      *
. *****************************************************************************
.
          INC           STD001FD
.
          INC       PATMI1FD/INC
          INC       PATMA1FD/INC
          INC       PMSVX1FD/INC
          INC       PATHSPFD/INC 
          INC       PATCODFD/INC
          INC       IBASEQFD/INC 
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
+
.
.         IBACONFD Variable
.
IBCNMHOS  FORM      1
.
.               WORK AREA
.
.--------------------------------------------------------------
.Name      Type      Length     Physical     Start
.----      ----      ------     --------     -----
CLTYP      DIM           3            3         1
DUNIQUE    DIM           8            8         4
CLDES      DIM          20           20        12
.AWARD     DIM           3            3        32
.ABED      DIM           3            3        35
.AURNO     DIM           8            8        38
.AADMNO    DIM           8            6        46
.ADATE     DIM           8            8        52
.PSNAME    DIM          20           20        60
.PGNAME    DIM          25           25        80
.
.  END OF RECORD                              105
.
.  redefine form fields for key
.  ----------------------------
.Name      Type      Length     Start
.----      ----      ------     -----
UNIQUE     FORM          8         1
.--------------------------------------------------------------
.
.
PATTMP          IFILE     SQL, FIXED=105, KEY="U1-11"
PATROL          FILE      ASCII, FIXED=256
.
FNAMER          DIM     8
FNAMET          DIM     8
.
FIELD           FORM    1
.
CUSR1           FORM     1
CUSR2           FORM     1
CUSRDS1         DIM     12
CUSRDS2         DIM     12
.
.
.      EXTRA VARIABLES
.
OPCND           FORM    1
STATUS          FORM    1
CMDLINE         DIM     50
HOSCODE         DIM     3
DALPHA          DIM     6
DNAME           DIM     30
DESC12          DIM       12
CLDESC          DIM       20
ATDESC          DIM       20
ACDESC          DIM       20
CODE            DIM     2
.
EQUAL           INIT    " = "
CODECL          INIT    "CL"
CODEA           INIT    "A "
CODEP           INIT    "P "
CODEU1          INIT    "U1"
CODEU2          INIT    "U2"
.
PRGID     INIT      "IBAHOS19"
PRGNAM    INIT      "Current Admissions By Code"
VERSION   INIT      "V12.01.00"
+
.         START OF PROGRAM
.
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,*EL,"Opening patmi1af";
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"     * now read in PATMI1IO    *I-263541
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
OPFIL     DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TEN3;*216,CUSR1,CUSRDS1,CUSR2,CUSRDS2
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          IF        IBCNMHOS = 1
            DISPLAY   *P64:24,"pathspaf";
            OPEN      PATHSPA1,"pathspaf"
          ENDIF
.
          CALL      TFILENAM
          PACK      FNAMER,TFILNAME
.
          CALL      TFILENAM
          PACK      FNAMET,TFILNAME
.
+
.
.        START OF PROGRAM
.
          IF        IBCNMHOS = 1
            DISPLAY   *P1:5,*EL,"Hospital Id : ";
            MOVE      TEN5,ECOL
            MOVE      FIVE,EVERT
            MOVE      SP3,CKYIDEF3
            MOVE      ONE,CKYIMAND
START       CALL      PATHSPKY
            BRANCH    EXIT,START,ENDIT
            PACK      HOSCODE,KEY3,SP70
            DISPLAY   *P22:5,*EL,PTHSNAME,*W2;
          ENDIF
.
.        SELECT OPTION
. 
SOPT   MOVE       THREE,FIELD
       MOVE       SEVEN,CVERT
.
. get categories descriptions
.
       MOVE       "Code CL: Missing",TDESC
       PACK       KEY5,ANSC,ANSL,SP3
       CALL       RDCODE1
       MOVE       TDESC,CLDESC
.
       MOVE       "Code A: Missing",TDESC
       PACK       KEY5,ANSA,SP4
       CALL       RDCODE1
       MOVE       TDESC,ATDESC
.
       MOVE       "Code P: Missing",TDESC
       PACK       KEY5,ANSP,SP4
       CALL       RDCODE1
       MOVE       TDESC,ACDESC
.
       STRIP      CLDESC
       STRIP      ATDESC
       STRIP      ACDESC
.
       HLINE      *G37,2,53,80
       DISPLAY    *P1:3,*EF:
                  *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                  *P1:5,*V2LON,ONE,*HOFF," = ",*+,CLDESC," Sequence":
                  *P1:6,*V2LON,TWO,*HOFF," = ",*+,ATDESC," Sequence":
                  *P1:7,*V2LON,THREE,*HOFF," = ",*+,ACDESC," Sequence"
.
USRFLD COMPARE    ONE,CUSR1
       GOTO       DSPUR2 IF NOT EQUAL
       ADD        ONE,CVERT
       ADD        ONE,FIELD
       DISPLAY    *P1:CVERT,*V2LON,FIELD,*HOFF,EQUAL,CUSRDS1
.
DSPUR2 COMPARE    ONE,CUSR2
       GOTO       KEYOPT IF NOT EQUAL
       ADD        ONE,CVERT
       ADD        ONE,FIELD
       DISPLAY    *P1:CVERT,*V2LON,FIELD,*HOFF,EQUAL,CUSRDS2
.
.      FIND OUT WHICH OPTIONS TO DISPLAY
.
KEYOPT KEYIN      *P1:11,"Option : ",*EL,*V2LON,OPTION
.
        COMPARE   ZERO TO OPTION
        GOTO      ENDIT IF EQUAL
.
        BRANCH    CUSR1 OF BRF1
        COMPARE   ONE,CUSR2
        GOTO      BRUF1 IF EQUAL
        GOTO      BRUF3
.
BRF1    COMPARE   ZERO,CUSR2
        GOTO      BRUF2 IF EQUAL
.
BRNCH   BRANCH    OPTION OF CLMCOD,ADMTYP,ADMCLS,USR1,USR2
        BEEP
        GOTO      SOPT
.
BRUF1   BRANCH    OPTION OF CLMCOD,ADMTYP,ADMCLS,USR2
        BEEP    
        GOTO      SOPT
.
BRUF2   BRANCH    OPTION OF CLMCOD,ADMTYP,ADMCLS,USR1
        BEEP    
        GOTO      SOPT
.
BRUF3   BRANCH    OPTION OF CLMCOD,ADMTYP,ADMCLS
        BEEP    
        GOTO      SOPT
.
.      ERROR MESSAGES
.
NOCLM  DISPLAY   *P20:24,*B,*EL,*V2LON:
                  "** No Current Admissions by Claim Code **",*W3;
        GOTO      SOPT 
.
NOADTY  DISPLAY   *P20:24,*B,*EL,*V2LON:
                  "** No Current Admissions by Admission Type **",*W3;
        GOTO      SOPT 
.
NOADCL  DISPLAY   *P20:24,*B,*EL,*V2LON:
                  "** No Current Admissions by Admission Class **",*W3;
        GOTO      SOPT
.
NOUDF1  DISPLAY   *P20:24,*B,*EL,*V2LON:
                  "** No Current Admissions by ",CUSRDS1," **",*W3;
        GOTO      SOPT
.
NOUDF2  DISPLAY   *P20:24,*B,*EL,*V2LON:
                  "** No Current Admissions by ",CUSRDS2," **",*W3;
        GOTO      SOPT
.
.       DISPLAY THE CHOICE SELECTED
.
CLMCOD  DISPLAY  *P53:2,*V2LON,"- ",*+,CLDESC," Sequence ",*P1:3,*EF
        GOTO     ADMNNO
.
ADMTYP  DISPLAY  *P53:2,*V2LON,"- ",*+,ATDESC," Sequence ",*P1:3,*EF
        GOTO     ADMNNO
.
ADMCLS  DISPLAY  *P53:2,*V2LON,"- ",*+,ACDESC," Sequence ",*P1:3,*EF
        GOTO     ADMNNO
.
USR1    DISPLAY  *P53:2,*V2LON,"- ",CUSRDS1," Sequence ",*P1:3,*EF
        GOTO     ADMNNO
.
USR2    DISPLAY  *P53:2,*V2LON,"- ",CUSRDS2," Sequence ",*P1:3,*EF
.
.          GET ONLY CURRENT ADMISSIONS..IE ASTAT=2
.
ADMNNO  CALL     KILLIDX
        CALL     CREAIDX
.
        OPEN     PATTMP,FNAMET
        MOVE     ONE TO UNIQUE
.
        PACK     KEY9 WITH TWO,SP8
        CALL     RDSMISS2
        MOVE     "60",CLNO
        DISPLAY  *P1:24,*EL,*P20:24,*V2LON:
                 "** Scanning ADMISSIONS **",*W2;
.
        DISPLAY  *P20:24,*EL,"Admission :";
.
NEXTA   CALL     RDKMISS2
        BRANCH   OVRCD OF SORTF
.
        COMPARE  TWO,ASTAT
        GOTO     SORTF IF NOT EQUAL
.
        DISPLAY  *P32:24,*V2LON,AADMNO;
        MOVE     AURNO,KEY8
        CALL     RDMAST1
.
        IF        IBCNMHOS = 1
          PACK      KEY8,DAADMNO,SP10
          CALL      RDPMVX11
          BRANCH    OVRCD,NEXTA
.
          MATCH     PMVXMHOS,HOSCODE
          GOTO      NEXTA IF NOT EQUAL     
.
        ENDIF
.
.       Load in the variable to be reported on
.
        LOAD     CLTYP USING OPTION OF ACLAIM,ATYPE,ACLSS,AUSR1,AUSR2
.
        MATCH    SP3,CLTYP
        GOTO     NEXTA IF EQUAL
.
        PACK     KEY11 WITH CLTYP,UNIQUE
        CALL     WRTEMPD
        ADD      ONE,UNIQUE
        GOTO     NEXTA
.
SORTF   DISPLAY  *P1:14,*EF
        MOVE     ZERO,CPAGENO
        MOVE     "60",CLNO
.
.       DISPLAY THE CURRENT ADMN FOR CHOSEN SEQ
.
        LOAD      CODE USING OPTION OF CODECL,CODEA,CODEP,CODEU1,CODEU2
.
        DISPLAY   *P1:24,*EL,*P19:24,"Code : ";
.
        MOVE      SP20,KEY11
        CALL      RDSSORTD
NEXTCC  CALL      RDKSORTD
        BRANCH    OVRCD OF ENDP
.
        PACK      KEY5 WITH CODE,CLTYP
        CALL      RDCODE1
        BRANCH    OVRCD OF INVCOD
        MOVE      TDESC,CLDES
        DISPLAY   *P34:24,*V2LON,CLTYP,*P39:24,CLDES;
.
        UNPACK    ADATE,XCENT,XYEAR,XMON,XDAY
        CALL      DISPDA
        GOTO      NEXTCC
+
INVCOD  DISPLAY   *P32:22,*EL,*B,*V2LON:
                  "Invalid Code ",CLTYP,*W2,*P32:22,*EL
.
        GOTO      NEXTCC
.
.           OVER CONDITION FOUND..PRINT LAST LINE OF -- AND DISPLAY MESSAGE
.
ENDP     COMPARE      ZERO,CPAGENO
         GOTO         NOREC IF EQUAL
.
         GOTO         SEPLN
.
NOREC    BRANCH       OPTION OF NCLM,NTYP,NCLS,NUF1,NUF2
.
NCLM     GOTO         NOCLM IF EQUAL
.
NTYP     GOTO         NOADTY IF EQUAL
.
NCLS     GOTO         NOADCL IF EQUAL
.
NUF1     GOTO         NOUDF1 IF EQUAL
.
NUF2     GOTO         NOUDF2 IF EQUAL
.
SEPLN    CALL         PAGEND
         PRINT        *N,"  ***  END OF REPORT  ***"
.
         CLOSE        PATTMP
         CALL         KILLIDX
.
         DISPLAY      *P20:24,*EL,*B,*V2LON:
                      "** Report Generation Completed **",*W3;
         GOTO         SOPT
+
.
.     PRINT ACTUAL DATA 
.
DISPDA  COMPARE   "57",CLNO
        CALL      HEAD IF NOT LESS
.
        PRINT     *1,"! ",*6,CLTYP,*12,CLDES:
                  *41,"!",AURNO,*50,"! ",PSNAME,*71," ",PGNAME:
                  *99,"! ",AWARD,"/",ABED:
                  *109,"! ",AADMNO:
                  *120,"! ",XDAY,"/",XMON,"/",XCENT,XYEAR,*132,"!":
                  *N,*1,"!",*41,"!",*50,"!":
                  *99,"!",*109,"!",*120,"!",*132,"!"
.
        ADD       TWO,CLNO
        RETURN
+
.
.          LAST LINE ON THE PAGE
.
PAGEND    PRINT     "*---------------------------------------------------":
                    "--------------------------------------------------------":
                    "-----------------------*"
          ADD       ONE,CLNO
          RETURN
+
.
.                 HEADINGS FOR OUTPUT
.
HEAD      MOVE      EIGHT,CLNO
          COMPARE   ZERO,CPAGENO
          CALL      PAGEND IF NOT EQUAL
.
          MOVE      ONE,CNOUNDLN
          CALL      IBACLOCK
          CALL      HEAD132
.
          BRANCH    OPTION OF PRNTA,PRNTB,PRNTC,PRNTD,PRNTE
.
PRNTA     PRINT     *N,*40,"*** (",*+,CLDESC," - SEQUENCE) ***":
                    *N,*87,"HOSPITAL ID : ",PTHSNAME,*N;
          GOTO      PRTCONT
.
PRNTB     PRINT     *N,*40,"*** (",*+,ATDESC," - SEQUENCE) ***":
                    *N,*87,"HOSPITAL ID : ",PTHSNAME,*N;
          GOTO      PRTCONT
.
PRNTC     PRINT     *N,*40,"*** (",*+,ACDESC," - SEQUENCE) ***":
                    *N,*87,"HOSPITAL ID : ",PTHSNAME,*N;
          GOTO      PRTCONT
.
PRNTD     PRINT     *N,*40,"*** (",CUSRDS1," - SEQUENCE) ***":
                    *N,*87,"HOSPITAL ID : ",PTHSNAME,*N;
          GOTO      PRTCONT
.
PRNTE     PRINT     *N,*40,"*** (",CUSRDS2," - SEQUENCE) ***":
                    *N,*87,"HOSPITAL ID : ",PTHSNAME,*N;
.
PRTCONT   CALL      PAGEND
.
          BRANCH    OPTION OF HDCLM,HDTYP,HDCLS,HDUF1,HDUF2
.
HDCLM     PRINT     *1,"! ",CLDESC:
                    *41,"! U/R no",*50,"! Patients Name":
                    *99,"! Ward/Bed",*109,"! Admn No":
                    *120,"! Admn Date",*132,"!"
.
           GOTO     PRTLINE
.

HDTYP     PRINT     *1,"! ",ATDESC:
                    *41,"! U/R no",*50,"! Patients Name":
                    *99,"! Ward/Bed",*109,"! Admn No":
                    *120,"! Admn Date",*132,"!"
.
           GOTO     PRTLINE
.
HDCLS   PRINT       *1,"! ",ACDESC:
                    *41,"! U/R no",*50,"! Patients Name":
                    *99,"! Ward/Bed",*109,"! Admn No":
                    *120,"! Admn Date",*132,"!"
.
          GOTO     PRTLINE
.
HDUF1     PRINT     *1,"! ",CUSRDS1:
                    *41,"! U/R no",*50,"! Patients Name":
                    *99,"! Ward/Bed",*109,"! Admn No":
                    *120,"! Admn Date",*132,"!"
.
          GOTO     PRTLINE
.
HDUF2     PRINT     *1,"! ",CUSRDS2:
                    *41,"! U/R no",*50,"! Patients Name":
                    *99,"! Ward/Bed",*109,"! Admn No":
                    *120,"! Admn Date",*132,"!"
.
PRTLINE   CALL     PAGEND
          RETURN
+
.         Deleting  AN INDEXED FILE BASED ON PORT
.
KILLIDX   DISPLAY       *P1:9,*EF;
.
          CLEAR         CMDLINE
          APPEND        "iserase ",CMDLINE
          APPEND        FNAMET,CMDLINE
          RESET         CMDLINE
          EXECUTE       CMDLINE,TASKID
.
          DISPLAY       *P1:15,*EF;
          RETURN
.
CREAIDX   PREP      PATROL,FNAMER
          WRITE     PATROL,SEQ;"isbuild ",FNAMET," 105 u1-11"
          WEOF      PATROL,SEQ
.
          MOVE      ONE,STATUS
          DISPLAY   *P1:13,*EF,*P1:14;
.
          CLEAR     CMDLINE
          APPEND    "sh ",CMDLINE
          APPEND    FNAMER,CMDLINE
          APPEND    ".txt",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          BRANCH    STATUS TO INDOK
.
          CLOSE     PATROL,DELETE
.
          MOVE      ONE,OPCND
          DISPLAY   *P20:24,*B,*V2LON:
                    "** The Index File Not Created **",*W3;
.
INDOK     DISPLAY       *P1:13,*EF,*P1:14
          RETURN  
.
+
WRTEMPD  MOVE       UNIQUE,DUNIQUE
         WRITE      PATTMP,KEY11;CLTYP,DUNIQUE,CLDES,AWARD,ABED:
                                 AURNO,AADMNO,ADATE:
                                 PSNAME,PGNAME
         RETURN
.
RDSSORTD READ       PATTMP,KEY11;;
         RETURN
.
RDKSORTD MOVE       ZERO,OVRCD
         READKS     PATTMP;CLTYP,DUNIQUE,CLDES,AWARD,ABED:
                                 AURNO,AADMNO,ADATE:
                                 PSNAME,PGNAME
         GOTO       OVERCOND IF OVER
         MOVE       DUNIQUE,UNIQUE
         RETURN
.
         INC        STD001IO
         INC        TFILENAM
         INC        PATHSPKY
.
         INC        PATCODIO/INC
         INC        PATMA1IO/INC
         INC        PATMI1IO/INC
         INC        PMSVX1IO/INC
         INC        PATHSPIO/INC
         INC        IBASEQIO/INC 
         INC        WEBERRIO/INC
.
ENDIT    CHAIN      PGM
         STOP

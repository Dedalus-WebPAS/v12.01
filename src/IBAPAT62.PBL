. *                       P R O G R A M  S U M M A R Y                    *
. *                       -------------  -------------                    *
. *                                                                       *
. *     PROGRAM NAME:     IBAPAT62                                        *
. *                                                                       *
. *     FUNCTION:         PATIENT ADMISSION AUDIT PRINTING                *
. *                                                                       *
. *     DATE WRITTEN:     04/07/85                                        *
. *                                                                       *
. *     AUTHOR:           MALCOLM HAYSE (I.B.A.)                          *
. *                                                                       *
. *     MODIFICATIONS:                                                    *
. *       V11.04.03 03.06.2024  Ebon Clements   TSK 0936282               *
. *                 Added  code to add * if Health Fund Number greater    *
. *                 than 10 characters                                    *
. *       V11.04.02 20.05.2024  Ebon Clements   TSK 0936282               *
. *                 Pack SAFNDMM with SP70 prior to unpack                *
. *       V11.04.01 05.02.2024  DA Sarkies      TSK 0936282               *
. *                 Added  code to add * if Health Fund Number greater    *
. *                 than 10 characters                                    *
. *       V5.08.01  28/08/2000  Caleb Sharman                             *
. *                  Changed Health Fund variables to be 8 chars          *
. *       V5.07.01  22/10/1999  Andrew Peel  SRF 632186                   *
. *                 Recompiled for PATAA1FD and PATIOADA - F * T          *
. *       V5.07.B02 25/01/1999  Nicole Harrington 507 rebound 137         *
. *                 Mods to use HEAD132                                   *
.                   09/11/1998  Glenn Berry      5.07 changes             *
.**************************************************************************
. *        V5.05.01  03/11/1997  Steve Armstrong                          *
. *                  Recompiled for changes to PATAA1FD                   *
. *                  Also mods to sector count                            *
. *************************************************************************
. *                       V5.01.02 06/07/89 J. Tan            SRF 101423  *
. *                                Fix print layout.                      *
. *                       V5.01.03 20/07/90 i. chung          SRF 105697  *
. *                                Print separate record for admission    *
. *                                and pre-admission                      *
. *                       V5.01.04 17/05/91 D.Di.Paola        SRF 108572  *
. *                                Added print for Department (SACLSSFT)  *
. *        V5.01.05  24.02.93 Neeriem "I Hate Support" Dye (I.B.A.)       *
. *                  Reformat print to look beautiful for new fields      *
. *        V5.01.06  24.03.93 Neeriem "I Hate Support" Dye (I.B.A.)       *
. *                  Remove Adm & Crn Fields                              *
. *                  Recompile for CRAAUD  (wasn't working for new file)  *
. *        V5.01.07  05/10/93 Whirlpool  srf 4403389                      *
. *                  recompiled for bug in PATIOADA                       *
. *                                                                       *
. *************************************************************************
.
          INC       STD001FD
.
          INC       PATMI1FD/INC
          INC       PATAA1FD/INC
+
.                 WORK AREA
.
ADESC     DIM       11
.
ADD       DIM       2
AMM       DIM       2
AYY       DIM       2
ACC       DIM       2
.
DIM1A     DIM       1
DIM5      DIM       5
DIM6      DIM       6
DIM9      DIM       9
DIM10     DIM       10
.
XDAY1     DIM       2
XMON1     DIM       2
XYEAR1    DIM       2
.
SEC1      FORM      "00001" 
.
ENDSEC    FORM      5
ACTION    DIM       1
OPERCODE  DIM       4
.
SEC       FORM      1
.
.      EXTRA VARIABLES
.
INSEC     DIM       6
TIMEIS    DIM       8
TIMENOW   DIM       8
.
FIELD     FORM      2
STAR      INIT      "*"
.
PRGID     INIT      "IBAPAT62"
PRGNAM    INIT      "Admissions Audit File Listing"
VERSION   INIT      "V12.01.00"
+
.
.         START OF PROGRAM
.
          DISPLAY   *ES;
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening pataa1af";
          OPEN      PATAUAA1,"pataa1af"
          DISPLAY   *P1:24,*EL;
+
.           MAIN        MENU
.
.             SELECT OPTION
.
STARTX    MOVE      ZERO,CPAGENO
          MOVE      " ",ANS
.
START     KEYIN     *P1:5,*EL," Do you want to Print ":
                    "the Admission Audit Listing (":
                        *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF:
                        ") ? ",*V2LON,ANS
.
          MATCH     "N",ANS
          GOTO      ENDIT IF EQUAL
.
          MATCH     "Y",ANS
          GOTO      PRNTPG IF EQUAL
.
          BEEP
          GOTO      START
+
.
.        SUBROUTINE TO PRINT PAGES ONTO PRINTER
.
PRNTPG    CALL      RDSAAUD
          MOVE      SECTOR,ENDSEC
          COMPARE   SEC1,ENDSEC
          GOTO      NOADMN IF EQUAL
.
.      START AT FIRST SECTOR IN AUDIT TRAIL
.
          MOVE      SEC1,SECTOR
.
          MOVE      "60",CLNO
          DISPLAY   *P10:24,*EL,"U/R Number : ";
.
NEXTA     COMPARE   ENDSEC,SECTOR
          GOTO      ENDP IF NOT LESS
.
          CALL      RDAAUD
.
          CALL      DISPDA
          DISPLAY   *P35:24,*V2LON,SAURNO,"  ",ADESC
          ADD       ONE,SECTOR
          GOTO      NEXTA
.
NOADMN    DISPLAY   *P10:24,*V2LON:
                    "** Patient Admission Audit File is Empty **",*W3;
          GOTO      ENDIT
+
.           PRINTING HAS FINISHED 
.
ENDP      PRINT     *N,*N,"  ***  END OF REPORT  ***"
          DISPLAY   *P10:24,*EL,*V2LON:
                    "** Report Generation Completed **",*W2;
.
.         Clear the audit file
.
          CALL      CRAAUD
          DISPLAY   *P10:24,*EL,*V2LON:
                    "** Audit File Reset **",*W2;
          GOTO      ENDIT
+
.****************************************************************************
.*                               DISPDA                                     *
.*        PRINT ACTUAL DATA                                                 *
.****************************************************************************
.
DISPDA    COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
. 
.         PRINT DATA TO PRINTER
.
          CALL      ACTDES
.
          UNPACK    SADATE,XCENT,XYEAR,XMON,XDAY
.
          MOVE      "U" TO DIM1
          LOAD      DIM1 USING SAALERG OF ANSY,ANSN
.
          MOVE      "U" TO DIM1A
          LOAD      DIM1A USING SAVISIT OF ANSY,ANSN
          REP       " 0",ADD
          REP       " 0",AMM
          REP       " 0",AYY
          PACK      SAFNDMM,SAFNDMM,SP70
          UNPACK    SAFNDMM,DIM10,DIM9
          MATCH     SP70,DIM9
          IF        !@EQUAL
            MOVE      SAFNDMM,DIM9
            PACK      DIM10,DIM9,STAR
          ENDIF
.
          PACK      SAFNDTB,SAFNDTB,SP70
          UNPACK    SAFNDTB,DIM6,DIM2
          MATCH     SP70,DIM2
          IF        !@EQUAL
            MOVE      SAFNDTB,DIM5
            PACK      DIM6,DIM5,STAR
          ENDIF
.
          PRINT     *1,"|",ADD,"/",AMM,"/",AYY,*10,"|",TIMEIS:
                    *19,"|",OPERCODE,*24,"|",ADESC:
                    *36,"|",SAADMNO:
                    *45,"|",SAMBS,*55,"|",SAFUNDH," ",DIM6:
                    *69,"| ",SASRCE,*75,"| ",SACLSS,*82,"|",SPTMSRFG,SLASH:
                    SPTMSGPP:
                    *103,"| ",DIM1A,*108,"|",SADISC:
                    *115,"|",SASTAY:
                    *120,"|",SAUSR1," ",SAUSR4,*128,"|",SPTMSMGI:
                    *132,"|":
                    *N,"|",XDAY,"/",XMON,"/",XYEAR,*10,"|",SATIME:
                    *19,"|",*24,"|":
                    *36,"|",SAURNO:
                    *45,"|",SADOCTT:
                    *55,"|",DIM10:
                    *69,"| ",SATYPE,*75,"| ",SACARE,*82,"|",SPTMSFHA:
                    *103,"| ",DIM1,*108,"|",SAALLOW,*115,"| ",SADYHOSP:
                    *120,"|",SAUSR2," ",SAUSR4,*128,"|",*132,"|":
                    *N,"|",*10,"|",*19,"|",*24,"|":
                    *36,"|",SADOCTA:
                    *45,"|",SADOCTR:
                    *55,"|",SAWARD,SLASH,SABED:
                    *69,"| ",SACLAIM,*75,"| ",SADIET,*82,"|",SPTMSECR:
                    *103,"| ",SACLSSFT,*108,"|",SAALLOW,*115,"| ",SADYHOSP:
                    *120,"|",SAUSR3," ",SAUSR6,*128,"|",*132,"|"
          ADD       THREE,CLNO
          CALL      PAGEND
.
          RETURN
+
.****************************************************************************
.*                            ACTDES                                        *
.*          ACTION MOVE DESCRIPTION IN                                      *
.****************************************************************************
.
ACTDES    MATCH     "A",ACTION
          GOTO      ADD IF EQUAL
.
          MATCH     "B",ACTION
          GOTO      BEF IF EQUAL
.
          MATCH     "C",ACTION
          GOTO      CHG IF EQUAL
.
          MATCH     "D",ACTION
          GOTO      UNA IF EQUAL
.
ADD       COMPARE   TWO,SASTAT
          GOTO      ADD1 IF EQUAL
          MOVE      "PRE-ADMISSN",ADESC
          GOTO      ADD2
.
ADD1      MOVE      "ADMISSION  ",ADESC
ADD2      RETURN
.
BEF       MOVE      "CHANGED FR ",ADESC
          RETURN
.
CHG       MOVE      "CHANGED TO ",ADESC
          RETURN
.
UNA       MOVE      "UNADMITTED ",ADESC
.
          RETURN
+
.****************************************************************************
.*                               PAGEND                                     *
.*         LAST LINE ON THE ADMN                                            *
.****************************************************************************
.
PAGEND    PRINT     *1,"*-------------------------------":
                        "------------------------------":
                        "------------------------------":
                        "---------------------------------------*"
          ADD       ONE,CLNO
          RETURN
+
.****************************************************************************
.*                              HEAD                                        *
.*                HEADINGS FOR OUTPUT                                       *
.****************************************************************************
.
HEAD      MOVE      ONE,CNOUNDLN
          CALL      IBACLOCK
          CALL      HEAD132
.
          PRINT     *N,*1,"*---------------------------------":
                    "----------------------------------------":
                    "--------------------------------------------":
                    "-------------*":
                    *N,"|  Date",*10,"|  Time",*19,"|Oper":
                    *24,"|Action",*36,"|Adm No":
                    *45,"|Admt MBS",*55,"|Fund Table":
                    *69,"|A.Src",*75,"|Class",*82,"|Reffer GP  Prac Code":
                    *103,"|Vist",*108,"|Discnt",*115,"|Stay":
                    *120,"|Usr Def",*128,"|Man",*132,"|":
                    *N,"|Adm Date",*10,"|Adm Time",*19,"|",*24,"|":
                    *36,"|U/R No":
                    *45,"|T.Doct",*55,"|Membership":
                    *69,"|A.Typ",*75,"|Care.C",*82,"|GPFH Approv No.":
                    *103,"|Algy",*108,"|Allow",*115,"|Prev":
                    *120,"|123 456",*128,"|Int",*132,"|":
                    *N,"|",*10,"|",*19,"|",*24,"|":
                    *36,"|A.Doct":
                    *45,"|R.Doct",*55,"|Ward/Bed":
                    *69,"|Clm",*75,"|Diet",*82,"|ECR  Approv No.":
                    *103,"|Opt",*108,"|",*115,"|":
                    *120,"|",*128,"|   ",*132,"|"
          CALL      PAGEND
          MOVE      SIX,CLNO
          MOVE      SP1,ANS
          RETURN
+
.****************************************************************************
.*        I/O Includes                                                      *
.****************************************************************************
.
          INC       STD001IO
.
          INC       PATAA1IO/INC
.
ENDIT    CHAIN      PGM
         STOP

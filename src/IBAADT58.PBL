.******************************************************************************
.*                                                                            *
.*                        P R O G R A M   S U M M A R Y                       *
.*                        -------------   -------------                       *
.*                                                                            *
.*     PROGRAM NAME:     IBAADT58                                             *
.*                                                                            *
.*     FUNCTION:         Ward Tranfer Extract for ACT Health Department       *
.*                                                                            *
.*     DATE WRITTEN:     28/10/2004                                           *
.*                                                                            *
.*     AUTHOR:           Lina Vo                                              *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.*                                                                            *
.*        V12.00.01 30/05/2025  Don Nguyen    TSK 0955096                     *
.*                  Alphanumeric visit number changes                         *
.*        V10.13.01 05/12/2018  Don Nguyen    TSK 0838558                     *
.*                  Deleted temp file (WORK1) on program exit                 *
.*        V9.10.02  05/12/2008  Ian Farnell   CAR  140056                     *
.*                  Changed FILE1000 so filename consistant c/ other extract  *
.*        V9.11.01  02/12/2008  Jill Habasque CAR  140056                     *
.*                  Added XHINDIC to MVEXT000                                 *
.*        V9.09.01  09/08/2007  Jill Habasque CAR  147508                     *
.*                  Mods to send "D" records as well as ward transfers        *
.*        V9.08.01  11/05/2007  Ebon Clements CAR 139615                      *
.*                  Changed to only report ward transfers                     *
.*        V9.04.02  20/01/2005  Lina Vo       CAR 56664                       *
.*                  Make XHINDIC equal the Hospital's Health Department code  *
.*        V9.04.01  16/12/2004  Lina Vo       CAR 55914                       *
.*                  Make ACT PIN same as PMI                                  *
.*        V9.04.00  28/10/2004  Lina Vo       CAR 54698                       *
.*                  Created new                                               *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
.
          INC       IBACONFD/INC
          INC       PATCONFD/INC
          INC       PATCODFD/INC
          INC       PATDSCFD/INC
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATTRNFD/INC
          INC       PATVISFD/INC
          INC       PATWR1FD/INC
          INC       PMSVX1FD/INC
+
.
.         Transmission file layout
.
.
WORK1     FILE      ASCII, FIXED=64
.
.NAME     TYPE    LENGTH    START   DESCRIPTION
.----     ----    ------    -----   -----------
XHINDIC   DIM       2         1   * Hospital identification
XPINDIC   DIM       8         3   * Patient Identification ACT Number
XPATSNO   DIM       8        11   * Patient Identification
XEPSDNO   DIM       8        19   * Episode number
XWARDID   DIM       4        27   * Ward Indentifier          
XWARDNM   DIM      20        31   * Ward Name
XENTDTE   DIM       8        51   * Date the Patient enters Ward
XENTTME   DIM       4        59   * Time the Patient enters Ward
XTRNTYP   DIM       1        63   * Transfer Type    
.
.end of record               64
.
.         WORK AREA
.
CDYSYEAR  FORM      2
CATD      INIT      "D "
CODEFLG   FORM      1
CODED     DIM       3
CATT      INIT      "T "
CDYSDAYS  FORM      6
CDYSFDTE  DIM       8
CDYSTDTE  DIM       8
CMDLINE   DIM       100
COUNT     FORM      2
DIM1A     DIM       1
DIM2A     DIM       2
DIM3      DIM       3
DIM4      DIM       4
ERRFLG    FORM      5
ERRMSG    DIM       40
ENDDATE   DIM       8
SAVEWARD  DIM       3
WDATE1    DIM       8
WDATE2    DIM       8
MCOUNT    FORM      1
RETNDATE  DIM       8
.
FORM6     FORM      6
FILENAME  DIM       8
FROMDATE  DIM       8                  * From Date in CCYYMMDD format
PENDDATE  DIM       8
FSTCODE   FORM      1
TPSYFLG   FORM      1
TODATE    DIM       8
SAVADMN   FORM      8
STODATE   DIM       8
STRDATE   DIM       8
Z20       INIT      "ZZZZZZZZZZZZZZZZZZZ"
.
PRGID     INIT      "IBAADT58"
PRGNAM    INIT      "Ward Transfer Extract for ACT HDP"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                           MAINLINE                                 *
.**********************************************************************
ML0000    CALL      INIT0000                * initialization and open files
ML1000    CALL      OPTN0000                * select options
          BRANCH    OPTION,ML2000,ML2000
          GOTO      ML9999
.
ML2000    CALL      KHOS0000                * Keyin Hospital 
          BRANCH    EXIT,ML1000
.
ML3000    CALL      KEYD0000                * Keyin date range
          BRANCH    EXIT,ML2000
.
ML4000    CALL      FILE0000                * Keyin the filename
          BRANCH    EXIT,ML3000
.
ML5000    CALL      CONTQST                 * Ok to Continue (Y/N/C) ?
          BRANCH    CEXIT,ML6000,ML5000,ML1000
.                         Yes    No     Cancel
.
ML6000    CALL      PROC0000                * Processing discharged patients
          BRANCH    EXIT,ML1000
          CALL      ENDP0000                * End of processing
.
ML9999    PREP      WORK1,FILENAME
          CLOSE     WORK1,DELETE
          CHAIN     PGM
          STOP
+
.******************************************************************************
.*                                  INIT0000                                  *
.*                      Initialization routine                                *
.******************************************************************************
INIT0000  CALL      DISPHEAD
          CALL      IBACLOCK
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA2,"patvisaf"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TEN;*54,CFILENO
          READ      CONTROLF,TEN3;*213,CHCSCOD,*245,CHOSTYP
.
          SQUEEZE   CFILENO
          MOVE      CFILENO,XHINDIC         * Hospital code
.
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000                                  *
.*                      Main options                                          *
.******************************************************************************
OPTN0000  HLINE     *G37,2,57,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Processing the Run":
                    *P1:7,"Select Option : ";
.
OPTN1000  KEYIN     *P17:7,*EL,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9000 IF EQUAL
.
          MOVE      ZERO,EXIT
          BRANCH    OPTION,OPTN9999
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.******************************************************************************
.*                                  KHOS0000                                  *
.*                      Keyin hospital ID                                     *
.******************************************************************************
KHOS0000  MOVE       ZERO,EXIT
          COMPARE    ONE,IBCNMHOS
          GOTO       KHOS9999 IF NOT EQUAL
.
          MOVE       SP70,PTHSHOSP
          DISPLAY    *P1:9,*EL,"Hospital Id : ";
          MOVE       TEN5,ECOL
          MOVE       NINE,EVERT
          MOVE       SP3,CKYIDEF3
          MOVE       ONE,CKYIMAND           * Mandatory
          OPEN       PATHSPA1,"pathspaf"
.
          CALL       PATHSPKY
          BRANCH     EXIT,KHOS9000,KHOS9000
.
KHOS3000  DISPLAY   *P20:9,*V2LON,PTHSNAME;
          SQUEEZE    PTHSHDPC
          PACK       XHINDIC,PTHSHDPC,SP10  * Health department code
          MOVE       ZERO,EXIT
          GOTO       KHOS9999
.
KHOS9000  MOVE       ONE,EXIT
KHOS9999  RETURN
+
.******************************************************************************
.*                                  KEYD0000                                  *
.*                      Keyin date range                                      *
.******************************************************************************
KEYD0000  DISPLAY   *P1:11,*EL,"Start Date : ":
                    *P1:13,*EL,"End   Date : ";
          MOVE      "10",CVERT
.
          MOVE      "11",CVERT
          MOVE      "15",CCOL
          UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          MOVE      ZERO,CHIGHLT
          MOVE      ZERO,CDEFDTE
          CALL      KEYDATE
          BRANCH    OVRCD,KEYD9500
          PACK      STRDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRDATE
.
          MOVE      "13",CVERT
          MOVE      "15",CCOL
          UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          MOVE      ZERO,CHIGHLT
          MOVE      ZERO,CDEFDTE
          CALL      KEYDATE
          BRANCH    OVRCD,KEYD9500
.
          PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDATE
.
.        The new ending date should be greater than the Last Ending Date
.
          MOVE      ZERO,EXIT
          MATCH     ENDDATE,STRDATE
          GOTO      KEYD9999 IF EQUAL
          GOTO      KEYD9999 IF LESS
.
          UNPACK    STRDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P1:24,*EL,*B,*V2LON,"End Date must be greater than ":
                    "Start date ",CPCDATE,".  ";
          CALL      HITENTER
          GOTO      KEYD0000
.
KEYD9500  MOVE      ONE,EXIT
KEYD9999  RETURN
+
.******************************************************************************
.*                                  FILE0000              Called by: ML0000   *
.*                            Get Extract Filename                            *
.******************************************************************************
FILE0000  MOVE      "15",CVERT
          DISPLAY   *P1:CVERT,*EL,"Extract File Name : ";
.
FILE1000  MOVE      "WLF",KEY3
          UNPACK    ENDDATE,CCENT,CYEAR,CMON,CDAY
          PACK      FILENAME,KEY3,CDAY,CMON,SP10
          KEYIN     *P21:CVERT,*EL,*V2LON,*RV,FILENAME;
.
          ENDSET    FILENAME
          APPEND    SP8,FILENAME
          RESET     FILENAME
.
          CMATCH    EXITCHAR,FILENAME
          GOTO      FILE9500 IF EQUAL       * Exit
.
          MATCH     SP8,FILENAME
          GOTO      FILE9500 IF EQUAL       * Exit
.
          STRIP     FILENAME
          SQUEEZE   FILENAME
          DISPLAY   *P21:CVERT,*EL,*V2LON,*+,FILENAME,".txt";
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      WORK1,FILENAME
          TRAPCLR   IO
          BRANCH    OVRCD,FILE9000          * File doesnt exist
          CLOSE     WORK1
.
..FILE2000  KEYIN     *P1:24,*B,*EL,"An extraction file with this name":
..                    " already exists. Overwrite it (",*V2LON,*DV,ANSY:
..                    *HOFF,"/",*V2LON,*DV,ANSN,*HOFF,") ? ",*V2LON,ANS;
..          REP       UPPLOW,ANS
..          MATCH     ANSY,ANS
..          GOTO      FILE9000 IF EQUAL       * Overwrite file
.
..          MATCH     ANSN,ANS
..          GOTO      FILE1000 IF EQUAL       * Keyin filename again
.
..          BEEP
..          GOTO      FILE2000
.
FILE9000  MOVE      ZERO,EXIT
          GOTO      FILE9999
.
FILE9500  MOVE      ONE,EXIT
FILE9999  RETURN
+
.******************************************************************************
.*                                  PROC0000                                  *
.*                      Processing discharges patients                        *
.******************************************************************************
PROC0000  MOVE      ZERO,CPAGENO
.
          CLOSE     WORK1
          PREP      WORK1,FILENAME
.
PROC0700  DISPLAY   *P1:24,*EL,*P20:24,*V2LON,"** Processing Discharges **",*W:
                    *P20:24,*EL,"Discharged Admission No. : ";
.
          MOVE      ENDDATE,STODATE
          PACK      KEY16,STRDATE,SP8
          CALL      RDSDSCH2
PROC1000  CALL      RDKDSCH2
          BRANCH    OVRCD OF PROC9000
.
          MATCH     DDATE,STODATE
          GOTO      PROC9000 IF LESS
.
          DISPLAY   *P50:24,*V2LON,DADMNO;
.
          MOVE      DURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF PROC1000
.
          MOVE      DADMNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD OF PROC1000
.
          MOVE      DADMNO,KEY8
          CALL      RDPMVX11
          BRANCH    OVRCD OF PROC1000
.
          IF        IBCNMHOS=1
            MATCH     PMVXMHOS,PTHSHOSP
            GOTO      PROC1000 IF NOT EQUAL
          ENDIF
.
          CALL      INTEXT00
          CALL      WRIT0000
          GOTO      PROC1000
.
PROC9000  MOVE      ZERO,EXIT
PROC9999  RETURN
+
.******************************************************************************
.*                                  INTEXT00              Called by: PROC0000 *
.*                         Initialise variables                               *
.******************************************************************************
INTEXT00  UNPACK   SP70,XPINDIC,XPATSNO
          UNPACK   SP70,XEPSDNO,XWARDID,XWARDNM
          UNPACK   SP70,XENTDTE,XENTTME,XTRNTYP
.
INTEXT99  RETURN
+
.******************************************************************************
.*                                  WRIT0000              Called by: PROC0000 *
.*                      Write the record details                              *
.******************************************************************************
WRIT0000  MOVE      DURNO,XPINDIC         * Patient Identification ACT Number
          MOVE      DURNO,XPATSNO         * Patient Identification
          MOVE      AADMNO,XEPSDNO        * Admission number
.
          MOVE      SP70,SAVEWARD         * Clear save ward
.
          PACK      KEY30,AADMNO,SP20,SP10
          CALL      RDSTRAN2
WRIT1000  CALL      RDKTRAN2
          BRANCH    OVRCD,WRIT9999
.
          MATCH     AADMNO,TADMN            * same Admission No. ?
          GOTO      WRIT9999 IF NOT EQUAL
.
          MATCH     ANSD,TMOVE
          IF        !@EQUAL
            MATCH     SAVEWARD,TWARD          * Only report changes of ward
            GOTO      WRIT1000 IF EQUAL
          ENDIF
.
          MOVE      TWARD,SAVEWARD 
.
          MOVE      TWARD,XWARDID
          PACK      KEY6,TWARD,SP10
          CALL      RDWARD1
          IF        OVRCD=0
            MOVE      WBDESC,XWARDNM
          ELSE
            MOVE      "Unknown Ward        ",XWARDNM
          ENDIF
.
          UNPACK    TDATE,XCENT,XYEAR,XMON,XDAY
          PACK      XENTDTE,XDAY,XMON,XCENT,XYEAR
          REP       " 0",XENTDTE
.
          UNPACK    TTIME,XHOUR,DIM1,XMIN
          PACK      XENTTME,XHOUR,XMIN
          REP       " 0",XENTTME
.
          MOVE      TMOVE,XTRNTYP
.
          WRITE     WORK1,SEQ;XHINDIC,XPINDIC,XPATSNO,XEPSDNO,XWARDID:
                              XWARDNM,XENTDTE,XENTTME,XTRNTYP
.
          GOTO      WRIT1000
.
WRIT9999  RETURN
+
.******************************************************************************
.*                                  ENDP0000                                  *
.*                             End of process                                 *
.******************************************************************************
ENDP0000  DISPLAY   *P1:24,*EL,*P19:24,*V2LON:
                    "** Transmission Data Processing Completed **",*W;
.
          CALL      MVEXT000
.
ENDP9999  RETURN
+
.******************************************************************************
.*                                  MVEXT000                                  *
.*                         Move Extract to Web Directory                      *
.******************************************************************************
MVEXT000  CLEAR     CMDLINE                 * header file
          APPEND    "ibaadt58.us1 ",CMDLINE
          APPEND    FILENAME,CMDLINE
          APPEND    ".txt ",CMDLINE
          APPEND    XHINDIC,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
MVEXT999  RETURN
.==============================================================================
.
          INC       STD001IO
          INC       PATHSPKY
.
          INC       PATCODIO/INC
          INC       PATDSCIO/INC
          INC       PATHSPIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATTRNIO/INC
          INC       PATVISIO/INC
          INC       PATWR1IO/INC
          INC       PMSVX1IO/INC
+

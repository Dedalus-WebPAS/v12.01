.******************************************************************************
.* System         : Theatre System                                            *
.* Program        : IBAOPR29                                                  *
.* Name           : Preadmissions/Bookings for a day Report                   *
.******************************************************************************
.* Date           : 26/05/2006                                                *
.* Author         : J.Tan                                                     *
.* Function       : Print all Preadmissions/Booking for a selected day        *
.* Modifications  :                                                           *
.******************************************************************************
.*       V12.00.01 28/05/2025  PJ Le Febour      TSK 0955096 AlphaNum visitnum*
.*                 replace   MOVE OPDAADMN,FORM8 to MOVE  OPDAADMN,DIM8VISN   *
.*                           COMPARE BKREBOK,FORM8 to MATCH BKREBOK,DIM8VISN  *
.*       V11.03.02 19/04/2023  Thanh T           TSK 0909393                  *
.*                 Changed PRNT1800 to setup KEY22 with OPDAHOSP              *
.*       V11.03.01 20/03/2023  PJ Le Febour      TSK 0909393                  *
.*                 Amended KEY19 to KEY22 for theatre index changes           *
.*       V10.04.02 24/06/2014  J.Tan             CAR 261630                   *
.*                 Removed port number from temp file name                    *
.*       V10.04.01 06/02/2014 Ania P             CAR 285587                   *
.*                 Reformatted Layout                                         *
.*       V10.01.01 04/12/2012 Saroeun Soeur      CAR 273540                   *
.*                 changed temp file to match new fd length of BKRXUFF1       *
.*        V9.09.01 26/11/2007  Steve Armstrong   CAR 154817                   *
.*                 Fixed printing of 2nd line of theatre comments for         *
.*                 pre-admissions (OPDAUNIQ not being cleared).               *
.******************************************************************************
.*        V9.07.04 17/07/06 J.Tan      CAR 112247                             *
.*                 Fixed booking details for pre-admissions                   *
.*        V9.07.03 03/07/06 Peter Vela CAR 103656                             *
.*                 Fixed missing operation description                        *
.*        V9.07.02 29/06/06 J.Tan      CAR 103697                             *
.*                 Mods to include preadmission without booking               *
.*        V9.07.01 23/06/06 Peter Vela CAR 103656                             *
.*                 Changed Session column to display Cat TU OPSESTYP          *
.*                 Changed Comments column to display from oprdedaf comment   *
.*                 type 009 Theatre Session Comments                          *
.******************************************************************************
.*        V9.06.01 05/06/06 J.Tan  CAR 103697                                 *
.*                 Mods for Accom.Request and Adm.class                       *
.*                                                                            *
.******************************************************************************
+
          INC       STD001FD
          INC       PATCALAG/INC
          INC       IBASEQFD/INC            * Sequential Numbers File
          INC       TFILEVAR/INC            * Generate Temp File Name
          INC       WEBERRFD/INC            * Web Server Error Logging
.
          INC       BOKDIAFD/INC            * Booking diagnosis file
          INC       BOKRC1FD/INC            * Booking master file
          INC       BOKRX1FD/INC            * Booking master file
          INC       OPRDEAFD/INC            * Operation detail file
          INC       OPRDEDFD/INC            * Operation Comments File
          INC       PATCODFD/INC            * Patient codes file
          INC       PATCONFD/INC            * Control File
          INC       PATDHEAD/INC            
          INC       PATDO1FD/INC            * Patient doctors file
          INC       PATMA1FD/INC            * Patient master file
          INC       PATMI1FD/INC            * Patient admission file
          INC       PATPR1FD/INC            * Patient pre admission file
          INC       PATWR1FD/INC            * Patient ward file
          INC       PMSVX1FD/INC            * Visit extension file
.
          INC       OPRSESFD/INC            * Session file
          INC       OPRCLIFD/INC            * Clinic file
.
. ----- Tempfile Definition -----
.
TEMP1     IFILE     SQL, FIXED=317, KEY="U297-317,1-8"
TEMP2     IFILE     SQL, FIXED=317, KEY="U9-14,1-8"
FILELINE  INIT      " 317 U297-317,1-8 U9-14,1-8"
.
.Name     Type      Length   Physical   Start   Description
.----     ----      ------   --------   -----   -----------
DBKREBOK  DIM       8        8          1       Booking number
.OPDACLIN DIM       6        6          9       Clinic/Surgeon
.BKREADOC DIM       6        6          15      Attending Doctor Code
.BKREURNO DIM       8        8          21      U/R number
PATNAME   DIM       22       22         29      Patient name
PATAGE    DIM       3        3          51      Patient age
.PPOST    DIM       8        8          54      Postcode
.BKRXUFF1 DIM       80       80         62      Operation/Procedure
.BKRECLMT DIM       3        3          142     Claim type (cat CL)
.PFUNDH   DIM       6        6          145     Health Fund
.PFNDTB   DIM       8        8          151     Health Fund Table
.BKREACCM DIM       3        3          159     Accommodation (Cat BP)
.BKRECLSS DIM       3        3          162     Admission class (cat P)
.BKRXOPRD DIM       3        3          165     Operation period (cat SP)
.OPDADATE DIM       8        8          168     Session date
.OPDATIME DIM       5        5          176     Start Time
.OPDACOMM DIM       40       40         181     Comments
.OPDDDATA DIM       70       70         221     Theatre Comments
.OPDATHET DIM       6        6          291     Operating Room Code
DOCSNAME  DIM       20       20         297     Treating doctor surname
.
.End of Record                          317
.
. Redefine FORM fields from KEY
. -----------------------------
.BKREBOOK DIM       8        8          1       Booking number
.
.
. ----- Program Variables -----
.
ATTENDOC  DIM       19       20         185     Attending Doctor
ANAESDOC  DIM       19
BJDAY     FORM      3
.
CJDAY     FORM      3
CMDLINE   DIM       80
CODE      DIM       2
.
FNAME     DIM       8                       * Filename
.
LOOPFLAG  FORM      1
OPCNCLSU  FORM      1                       * Using Clinic or Surgeon ?
PATCOUNT  FORM      4                       * Patient counter
.
SPATNAME  DIM       21                      * Truncated PATNAME
SELDATE   DIM       8                       * selected date (ccyymmdd)
SAVSESSN  DIM       22
SAVCLIN   DIM       6 
.
. ----- Program Constants -----
.
CODEBP    INIT      "BP"
CODEOY    INIT      "OY"
.
SP100     INIT      "                                                  ":
                    "                                                  "
ADM       INIT      "ADM"
BOK       INIT      "BOK"
CAN       INIT      "CAN"
CODECL    INIT      "CL"
DSC       INIT      "DSC"
PRE       INIT      "PRE"
UNKNOWN   INIT      "Unknown"
.
PRGID     INIT      "IBAOPR29"
PRGNAM    INIT      "Preadmissions/Bookings for a Day Report"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                   ML0000                                   *
.*                                 Main Module                                *
.******************************************************************************
ML0000    CALL      INIT0000                * Initialise variables & open files
ML1000    CALL      OPTN0000                * Choose main option
          BRANCH    EXIT,ML9000
.
ML3000    CALL      DATE0000                * Keyin the sch'd admin date range
          BRANCH    EXIT,ML1000
.
          CALL      CONTQST                 * Ok to Continue (Y/N/C) ?
          BRANCH    CEXIT,ML4000,ML3000,ML1000
.                         Yes    No     Cancel
.
ML4000    CALL      LOAD0000                * Load the tempfile
          CALL      PRNT0000                * Print the report
          GOTO      ML1000
.
ML9000    CALL      KILL0000
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                  INIT0000              Called By: ML0000   *
.*                      Initialise Variables & Open Files                     *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen header
.
          DISPLAY   *P56:24,"Opening bokdiaaf"
          OPEN      BOKDIAA1,"bokdiaaf"
.
          DISPLAY   *P64:24,"bokrc1af"
          OPEN      BOKRC1A1,"bokrc1af"
          OPEN      BOKRC1A4,"bokrc1af"
.
          DISPLAY   *P64:24,"bokrx1af"
          OPEN      BOKRX1A1,"bokrx1af"
.
          DISPLAY   *P64:24,"oprdetaf"
          OPEN      OPRDETA2,"oprdetaf"
.
          DISPLAY   *P64:24,"oprdedaf"
          OPEN      OPRDEDA1,"oprdedaf"
.
          DISPLAY   *P64:24,"oprsesaf"
          OPEN      OPRSESA1,"oprsesaf"
          OPEN      OPRSESA7,"oprsesaf"
.
          DISPLAY   *P64:24,"oprcliaf"
          OPEN      OPRCLIA1,"oprcliaf"
.
          DISPLAY   *P64:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P64:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patpramf"
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
.
          DISPLAY   *P64:24,"patwr1af"
          OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY   *P64:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A3,"patmi1af"
.
          DISPLAY   *P64:24,"pmsvx1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"controlf"
          DISPLAY   *P64:24,"controlf"
          OPEN      CONTROLF,"controlf"
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,FNAME
.
          READ      CONTROLF,FORTY;*111,OPCNCLSU
          CALL      IBACLOCK                * Get the date & time
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000              Called By: ML0000   *
.*                             Choose Main Option                             *
.******************************************************************************
OPTN0000  MOVE      ZERO,OPTION
          DISPLAY   *P1:4,*EF,*V2LON,"0",*HOFF," = Exit":
                   *P1:5,*V2LON,"1",*HOFF," = By Attending Doctor":
                   *P1:6,*V2LON,"2",*HOFF," = By Session":
                    *P1:8,"Select Option : ";
OPTN1000  KEYIN     *P17:8,*V2LON,*DV,UNDLN1,*P17:8,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN9000,OPTN9000
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                 DATE0000               Called by: ML0000   *
.*                             Keyin Date Range                               *
.******************************************************************************
DATE0000  MOVE      "29",CCOL
          MOVE      ONE,CCANLDTE            * Cancel default date
.
. ----- Enter Starting Date -----
.
DATE1000  MOVE      "12",CVERT
          DISPLAY   *P1:CVERT,*EF,"Keyin Date : ";
          MOVE      CCC,CCENT
          UNPACK    SP6,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin the starting date
          BRANCH    OVRCD,DATE9500
.
          PACK      SELDATE,CCENT,CYEAR,CMON,CDAY  
          REP       " 0",SELDATE
          CALL      PACDATE                 * Format the starting date
          MOVE      ZERO,EXIT
          GOTO      DATE9999
.
DATE9500  MOVE      ONE,EXIT
DATE9999  RETURN
+
.******************************************************************************
.*                                  LOAD0000              Called by: ML0000   *
.*                              Load The Tempfile                             *
.******************************************************************************
LOAD0000  CALL      OPEN0000                * Open the tempfile
          DISPLAY   *P1:24,*EL,"Scanning : ",*P35:24,"Loading : ";
          MOVE      ZERO,LOOPFLAG
          PACK      KEY16,SELDATE,SP20
          CALL      RSBKREC4                * Position on & read the booking
LOAD1000  CALL      RKBKREC4                  master file
          BRANCH    OVRCD,LOAD5000
.
          MATCH     BKREEDAT,SELDATE
          GOTO      LOAD5000 IF NOT EQUAL   * not same date
.
          IF        BKRESTAT>0
            GOTO      LOAD1000 
          ENDIF
.
          MOVE      BKREBOOK,KEY8
          CALL      RDBKRX11
          BRANCH    OVRCD,LOAD1000          * missing booking extension record
.
          IF        BKRESTAT=3 | BKRESTAT=1
            PACK      KEY8,BKREBOOK
            CALL      RDMISS1 
            IF        OVRCD=0
	      MOVE      ADOCTA,BKREADOC
              MOVE      ACLAIM,BKRECLMT
              MOVE      ACLSS,BKRECLSS
            ENDIF
          ENDIF
.
LOAD2000  DISPLAY   *P12:24,*V2LON,BKREBOOK
.
          PACK      KEY31,BKREBOOK,SP30
          CALL      RSOPDEA2                * Position on & read the operation
          CALL      RKOPDEA2                  detail file
          BRANCH    OVRCD,LOAD3000
.
          MOVE      OPDAADMN,DIM8VISN
          MATCH     BKREBOOK,DIM8VISN
          GOTO      LOAD4000 IF EQUAL  
.
LOAD3000  UNPACK    SP70,OPDADATE,OPDATIME,OPDACLIN
          UNPACK    SP70,OPDACOMM,OPDDDATA,OPDATHET
          MOVE      SP10,OPDAUNIQ           * I-154817
.
LOAD4000  MOVE      BKREURNO,PURNO
          PACK      KEY8,PURNO
          CALL      RDMAST1                 * Read the master file
          IF        OVRCD=1
            MOVE      BKREBOOK,PURNO
            PACK      KEY8,PURNO
            CALL      RDPRAM1               * Read the preadmission file
            BRANCH    OVRCD,LOAD9000
            MOVE      "       0",BKREURNO   * Clear the U/R number
          ENDIF
.
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME                 * Format patient name
          MOVE      PACFNAME,PATNAME        * Patient name
.
          PACK      AGEDATE,CCC,CYY,CMM,CDD
          CALL      CALCAGE                 * Calculate age as of today
          MOVE      PSAGEYRS,PATAGE         * Patient age
.
          PACK      KEY8,BKREBOOK
          CALL      RDPMVX11
          IF        OVRCD=0
            MOVE      PMVXPACC,BKREACCM     * Preferred accommodation
          ENDIF
.
          MATCH     SP10,OPDAUNIQ           * I-154817
          IF        !@EQUAL
            PACK      KEY16,OPDAUNIQ,ZERO,ZERO,NINE,SP1,SP1,ONE,SP70
            CALL      RDOPDED1
            IF        OVRCD=1
              MOVE      SP70,OPDDDATA
            ENDIF
          ENDIF
.
          PACK      DOCSNAME,BKREADOC,SP70
          PACK      KEY6,BKREADOC
          CALL      RDDOCT1                 * Read the doctors file
          IF        OVRCD<>1
            MOVE      DSNAME,DOCSNAME
          ENDIF
.
          PACK      KEY28,DOCSNAME,BKREBOOK,SP70
          CALL      RATEMP1                 * Position on the tempfile
          IF        OVRCD=1
            CALL      WRTEMP1               * Write to the tempfile
            DISPLAY   *P45:24,*V2LON,BKREBOOK;
          ENDIF
          GOTO      LOAD9000
.
LOAD5000  MOVE      ONE,LOOPFLAG
          PACK      KEY16,SELDATE,SP70
          CALL      RDSMISS3
LOAD6000  CALL      RDKMISS3
          BRANCH    OVRCD,LOAD9999     
.
          MATCH     ADATE,SELDATE
          GOTO      LOAD9999 IF NOT EQUAL     * not same date
.
          COMPARE   "1",ASTAT                 * pre-admissions only
          GOTO      LOAD6000 IF NOT EQUAL
.
          UNPACK    SP70,BKRXUFF1,BKRXOPRD
          PACK      KEY8,AADMNO,SP70
          CALL      RDBKRX11
.
          MOVE      AADMNO,BKREBOOK
          MOVE      AURNO,BKREURNO
	  MOVE      ADOCTA,BKREADOC
          MOVE      ADATE,BKREEDAT
          MOVE      ACLAIM,BKRECLMT
          MOVE      ACLSS,BKRECLSS
          UNPACK    SP70,BKREACCM
          GOTO      LOAD2000
.
LOAD9000  BRANCH    LOOPFLAG,LOAD6000
          GOTO      LOAD1000
.
LOAD9999  RETURN
+
.******************************************************************************
.*                                  PRNT0000              Called by: ML0000   *
.*                              Print The Report                              *
.******************************************************************************
PRNT0000  MOVE      ZERO,EXIT
          MOVE      ZERO,PATCOUNT
          DISPLAY   *P1:24,*EL,"Printing : ";
          MOVE      ZERO,CPAGENO            * Initialise page number
          CALL      HEAD0000                * Print the report header
          UNPACK    SP70,KEY22,SAVSESSN,SAVCLIN
          PACK      KEY14,SP70
          PACK      KEY28,SP70
          CALL      RSTEMPA                 * Position on & read the 2nd
PRNT1000  CALL      RKTEMPA                   tempfile
          BRANCH    OVRCD,PRNT9000
.
          BRANCH    OPTION,PRNT1800         * print by attending doctor
.
          MATCH     SP70,OPDACLIN
          GOTO      PRNT1800 IF EQUAL
.
.         If printing by Session, we need to print session intructions
.
          MATCH     SP70,SAVCLIN 
          IF        @EQUAL
            MOVE      OPDACLIN,SAVCLIN
          ELSE
            MATCH     OPDACLIN,SAVCLIN
            GOTO      PRNT1800 IF EQUAL
            CALL      SINT0000              * Print special session instruction
            CALL      UND132                * Print an underline
            MOVE      OPDACLIN,SAVCLIN
          ENDIF
.
PRNT1800  PACK      SAVSESSN,OPDADATE,OPDATIME,OPDACLIN,OPDAHOSP,SP70
          COMPARE   FIFTY8,CLNO
          CALL      HEAD0000 IF NOT LESS    * Print the report heading 
.
          PACK      KEY6,BKREADOC
          CALL      RDDOCT1                 * Read the doctors file
          IF        OVRCD<>1
            MOVE      DSNAME,PACSNAME
            MOVE      DGNAME,PACGNAME
            MOVE      DTITL,PACTITLE
            MOVE      TWO,PACFRMT
            CALL      PACNAME               * Format doctors name
            MOVE      PACFNAME,ATTENDOC     * Doctors name
          ENDIF
.
.         MOVE      SP70,KEY12
.         MATCH     SP70,BKRXOPRD
.         IF        !@EQUAL
.           MOVE      "SP",TCODE
.           PACK      KEY5,TCODE,BKRXOPRD
.           CALL      RDCODE1
.           IF        OVRCD=0  
.             UNPACK    TDESC,KEY12
.           ENDIF
.         ENDIF
.
          MOVE      SP70,ANAESDOC
          PACK      KEY22,OPDADATE,OPDATIME,OPDACLIN,OPDAHOSP,SP70
          CALL      RDOPSES7
          BRANCH    OVRCD,PRNT2000
          MOVE      OPSEANAE,KEY6
          CALL      RDDOCT1
          IF        OVRCD<>1
            MOVE      DSNAME,PACSNAME
            MOVE      DGNAME,PACGNAME
            MOVE      DTITL,PACTITLE
            MOVE      TWO,PACFRMT
            CALL      PACNAME               * Format doctors name
            MOVE      PACFNAME,ANAESDOC     * Doctors name
          ENDIF
.
.         MOVE      SP70,KEY12
.         MATCH     SP70,OPSESTYP
.         IF        !@EQUAL
.           MOVE      "TU",TCODE
.           PACK      KEY5,TCODE,OPSESTYP
.           CALL      RDCODE1
.           IF        OVRCD=0
.             UNPACK    TDESC,KEY12
.           ENDIF
.         ENDIF
.
PRNT2000  UNPACK    OPDATHET,KEY6 
          UNPACK    BKRXUFF1,KEY55
          MOVE      PATNAME,SPATNAME         * Truncate
          UNPACK    OPDDDATA,KEY30

          PRINT     *1,"|",BKREURNO,*11,SPATNAME,*33,PATAGE:
                    *37,KEY55,*113,ATTENDOC,*132,"|"                *80,BKREACCM,*85,BKRECLSS,*90,PPOST,*98,KEY6:
.
          MATCH     "       0",BKREURNO
          IF        @EQUAL
            PRINT     *11,"Booking No: ",BKREBOOK;
          ENDIF
.
          PRINT     *1,"|",*37,KEY30,*68,BKRECLMT,*72,PFUNDH,*79,PFNDTB:
                    *88,BKREACCM,*93,BKRECLSS,*97,PPOST,*106,KEY6:
                    *113,ANAESDOC,*132,"|"
.
          ADD       TWO,CLNO
          CALL      UND132                  * Print an underline
          ADD       ONE,PATCOUNT
          DISPLAY   *P12:24,*V2LON,BKREBOOK;
          GOTO      PRNT1000
.
PRNT9000  BRANCH    OPTION,PRNT9200
.
          MATCH     SP70,SAVSESSN
          GOTO      PRNT9200 IF EQUAL     * No patient for the session
          CALL      SINT0000              * Print special session instruction
.
PRNT9200  PRINT     *1,"Total Preadmissions/Bookings for the day: ":
                    PATCOUNT,*N,*N:
                    *1,"*** End of Report ***"
.
          DISPLAY   *P1:24,*EL,"Report Generation Complete. ";
PRNT9999  RETURN
+
.******************************************************************************
.*                                  HEAD0000              Called by: PRNT0000 *
.*                          Print The Report Heading                          *
.******************************************************************************
HEAD0000  MOVE      "By Attending Doctor",CPHDROPT
          IF        OPTION<>1
            MOVE      "By Session",CPHDROPT
          ENDIF
          CLOCK     TIME,CTIMEIS
          CALL      HEAD132                 * Print main report heading
.
          UNPACK    SELDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *40,"Preadmissions/Bookings for : ",CPCDATE
          ADD       ONE,CLNO
          CALL      UND132                  * Print an underline
          PRINT     *1,"|U/R No.",*11,"Patient Name",*33,"Age":
                    *37,"Operation",*68,"Clm",*72,"H/Fund",*79,"Table":
                    *88,"Acm",*93,"Adm",*97,"Post",*106,"Sessn":
                    *113,"Attending Doctor",*132,"|"
.
          PRINT     *1,"|",*37,"Comments",*68,"Typ",*88,"Req":
                    *93,"Cls",*97,"Code",*113,"Anaesthetist",*132,"|"
.
          ADD       "2",CLNO
          CALL      UND132                  * Print an underline
HEAD9999  RETURN
+
************************************************************************
.         Print session special instructions
************************************************************************
SINT0000  UNPACK    SAVSESSN,KEY8,KEY5,KEY6
.
          MOVE      SP20,KEY22
          BRANCH    OPCNCLSU,SINT2000       * using doctor file
          CALL      RDOPCLI1
          BRANCH    OVRCD,SINT2000          * invalid clinic code
.
          MOVE      OPCLDESC,KEY22          * set up description
          MATCH     SP6,OPCLDOCT
          GOTO      SINT4000 IF EQUAL
.
          MOVE      OPCLDOCT,KEY6           * Use doctor code
SINT2000  CALL      RDDOCT1
          BRANCH    OVRCD,SINT4000          * invalid doctor code
          MOVE      DTITL,PACTITLE
          MOVE      DGNAME,PACGNAME
          MOVE      DSNAME,PACSNAME
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,KEY22          * set up doctors name
.
SINT4000  MATCH     SP70,KEY22
          IF        @EQUAL
            PACK      KEY22,KEY6,SP70
          ENDIF
          MOVE      SAVSESSN,KEY22
          CALL      RDOPSES7
          IF        OVRCD=1
            PACK      OPSESPI1,SP70         * Special instructions
            PACK      OPSESPI2,SP70
          ENDIF     
          COMPARE   FIFTY2,CLNO
          CALL      HEAD0000 IF NOT LESS    * Print the report heading 
.
          PRINT     *2,"Special Instructions for ":
                    KEY22,":":
                    *52,OPSESPI1:
                    *N,*52,OPSESPI2,*N
          ADD       TWO,CLNO
SINT9999  RETURN
+         
.******************************************************************************
.*                                  OPEN0000              Called by: LOAD0000 *
.*                              Open The Tempfile                             *
.******************************************************************************
OPEN0000  CALL      KILL0000                * Delete the tempfile
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME,CMDLINE
          APPEND    FILELINE,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID          * Create the tempfile
          OPEN      TEMP1,FNAME
          OPEN      TEMP2,FNAME
.
          CALL      CLET0000             * Clear tempfile
.
OPEN9999  RETURN
+
.******************************************************************************
.*                                  KILL0000              Called by: ML0000   *
.*                             Delete The Tempfile                 & LOAD0000 *
.******************************************************************************
KILL0000  CLOSE     TEMP1
          CLOSE     TEMP2
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
KILL9999  RETURN
+
.------------------------------------------------------------------------------
.         Clear tempfile
.------------------------------------------------------------------------------
CLET0000  PACK      KEY28,SP70
          CALL      RSTEMP1
          CALL      RKTEMP1
          BRANCH    OVRCD,CLET9999
.
          PACK      KEY28,DOCSNAME,BKREBOOK,SP70
          CALL      DETEMP1
          GOTO      CLET0000
.
CLET9999  RETURN
.
.******************************************************************************
.*                         I/O Routines For The Tempfile                      *
.******************************************************************************
RATEMP1   RESET     KEY28
          MOVE      ZERO,OVRCD
          READ      TEMP1,KEY28;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   RESET     KEY28
          MOVE      BKREBOOK,DBKREBOK
          WRITE     TEMP1,KEY28;DBKREBOK,OPDACLIN,BKREADOC:
                                BKREURNO,PATNAME,PATAGE,PPOST:
                                BKRXUFF1,BKRECLMT,PFUNDH,PFNDTB,BKREACCM:
                                BKRECLSS,BKRXOPRD,OPDADATE,OPDATIME:
                                OPDACOMM,OPDDDATA,OPDATHET,DOCSNAME
          RETURN
.
RSTEMP1   RESET     KEY28
          READ      TEMP1,KEY28;;
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMP1;DBKREBOK,OPDACLIN,BKREADOC:
                          BKREURNO,PATNAME,PATAGE,PPOST:
                          BKRXUFF1,BKRECLMT,PFUNDH,PFNDTB,BKREACCM:
                          BKRECLSS,BKRXOPRD,OPDADATE,OPDATIME:
                          OPDACOMM,OPDDDATA,OPDATHET,DOCSNAME
          GOTO      OVERCOND IF OVER
          MOVE      DBKREBOK,BKREBOOK
          RETURN
.
DETEMP1   RESET     KEY28
          DELETE    TEMP1,KEY28
          RETURN
.
RSTEMP2   RESET     KEY14
          READ      TEMP2,KEY14;;
          RETURN
.
RKTEMP2   MOVE      ZERO,OVRCD
          READKS    TEMP2;DBKREBOK,OPDACLIN,BKREADOC:
                          BKREURNO,PATNAME,PATAGE,PPOST:
                          BKRXUFF1,BKRECLMT,PFUNDH,PFNDTB,BKREACCM:
                          BKRECLSS,BKRXOPRD,OPDADATE,OPDATIME:
                          OPDACOMM,OPDDDATA,OPDATHET,DOCSNAME
          GOTO      OVERCOND IF OVER
          MOVE      DBKREBOK,BKREBOOK
          RETURN
.
RSTEMPA   IF        OPTION=1
            CALL      RSTEMP1
          ELSE
            CALL      RSTEMP2
          ENDIF
          RETURN
.
RKTEMPA   IF        OPTION=1
            CALL      RKTEMP1
          ELSE
            CALL      RKTEMP2
          ENDIF
          RETURN
+
.==============================================================================
.
          INC       STD001IO
.
          INC       CALCAGE                 * Calculate patients age
          INC       KEYINCOD                * Input code
          INC       PATDSCOD                * "?" on codes
          INC       TFILENAM                * Generate Temp File Name
          INC       IBASEQIO/INC            * Sequential Numbers File
          INC       WEBERRIO/INC            * Web Server Error Logging
.
          INC       BOKDIAIO/INC            * Booking diagnosis file
          INC       BOKRC1IO/INC            * Booking master file
          INC       BOKRX1IO/INC            * Booking master file
          INC       OPRDEAIO/INC            * Operation detail file
          INC       OPRDEDIO/INC            * Operation Comments File
          INC       PATCODIO/INC            * Patient codes file
          INC       PATDO1IO/INC            * Patient doctors file
          INC       PATMA1IO/INC            * Patient master file
          INC       PATMI1IO/INC            * Patient admission file
          INC       PATPR1IO/INC            * Patient pre admission file
          INC       PATWR1IO/INC            * Patient ward file
          INC       PMSVX1IO/INC            * Visit extension file
.
          INC       OPRSESIO/INC            * Session file
          INC       OPRCLIIO/INC            * Clinic file
+
.

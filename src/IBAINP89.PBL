.***************************************************************************
.* System    :   Patient                                                   *
.* Program   :   IBAINP89                                                  *
.* Desc      :   South Australian Cancer Register Extract                  *
.***************************************************************************
.* Date      :   05/02/2015                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   This program extracts data in the format required for the *
.*               south australian cancer register                          *
.* Modifications  :                                                        *
.* V12.00.01 03/06/2025  Nikitha Bhaskar    TSK 0959300                    *
.*           SA Cancer Registration Extract changes 2025-26                *
.* V11.04.01 12/04/2024  Alina Bhari        TSK 0944494                    *
.*           Removed the unecessary fields(EXTRALI1,EXTRALI2,EXTRALI3,     *
.*           EXTRALI4,EXTRDOBA,EXTRPTUA,EXTRRACE,EXTROCUP)in Extract       *
.*           Re-sequence Data Elements in Extract (EXTRHOST in between     *
.*           EXTRSDAT and EXTRSNAT,EXTRLATP between EXTRPSIT and EXTRDTXT) *
.*           -WREX0000                                                     *
.* V10.13.01 04/10/2018  Richa Phogat       TSK 0863567                    *
.*           Deleted Mandatory check for Patient Occupation Description    *
.*           under MAND0000                                                *
.* V10.12.01 29/01/2018  Ken Bell           TAK 0850321                    *
.*           Added Secondary Cancer Site from 01/01/2018                   *
.* V10.06.02 17/09/2015  Ebon Clements      CAR 319224                     *
.*           Change to report referring doctor from PMVXRHC1 not ADOCTR    *
.* V10.06.01 28/04/2015  Ebon Clements      CAR 298488                     *
.*           Fixed update of PTCDDTEX date extracted                       *
.* V10.05.00 05/02/2015  Ebon Clements      CAR 298488                     *
.*           Created program                                               *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       IBACONFD/INC
          INC       IBASEQFD/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATCRDFD/INC
          INC       PATCRGFD/INC
          INC       PATDADFD/INC
          INC       PATDO1FD/INC
          INC       PATDSCFD/INC
          INC       PATGSRFD/INC
          INC       PMSHCPFD/INC
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATICDFD/INC
          INC       PMSPX2FD/INC
          INC       PATVADFD/INC
          INC       PMSVX1FD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
JAN       INIT      "JANUARY"
FEB       INIT      "FEBRUARY"
MAR       INIT      "MARCH"
APR       INIT      "APRIL"
MAY       INIT      "MAY"
JUN       INIT      "JUNE"
JUL       INIT      "JULY"
AUG       INIT      "AUGUST"
SEP       INIT      "SEPTEMBER"
OCT       INIT      "OCTOBER"
NOV       INIT      "NOVEMBER"
DEC       INIT      "DECEMBER"
JULY2025  INIT      "20250701"
.
ACC       DIM       2
ADD       DIM       2
ADMDATE   DIM       10
AMM       DIM       2
AYY       DIM       2
CMDLINE   DIM       227
DIAGDESC  DIM       50
DIM9      DIM       9
DOB       DIM       10
DOYR2025  FORM      1
ENDDATE   DIM       8
EXTCOUNT  FORM      8
FILENAME  DIM       8
FIELDNAM  DIM       40
FNAMEA    DIM       8
FNAMEE    DIM       8
FROMDATE  DIM       10
HOSPCODE  DIM       3
MANDFLAG  FORM      1
MONTH3    DIM       3
PDESSTAT  DIM       1
PRNTDATE  DIM       10
PRINTFLG  FORM      1
PRNTHEAD  FORM      1
STRTDATE  DIM       8
TODATE    DIM       10
TOTALEXT  FORM      8
TOTALERR  FORM      8
PIPE      INIT      "|"
.
TEMPAFIL  IFILE SQL, FIXED=38, KEY="U1-8"
.
. NAME    TYPE   LENGTH   PHYSICAL  START LOC    DESCRIPTION
. ----    ----   ------   --------  ---------    --------------
TEMPADMN  DIM       8       8          1
TEMPSPAR  DIM       30      30         8
.
TEMPEFIL  IFILE SQL, FIXED=38, KEY="U1-8"
.
. NAME    TYPE   LENGTH   PHYSICAL  START LOC    DESCRIPTION
. ----    ----   ------   --------  ---------    --------------
ERROADMN  DIM       8       8          1
ERROSPAR  DIM       30      30         8
.
. CANCER EXTRACT FILE LAYOUT
. -------------------------
.
EXTCANFL  FILE      ASCII, FIXED=4096    * Extract File Pipe Delimited
.
.         TRANSMISSION FILE DEFINITION    * before 01/07/2025
.
. Field   Type     Size     Field Number - Description
. -----   ----     ----     ----------------
.EXTRHOSP  DIM       4       1 - Hospital File Number
.EXTRURNO  DIM       8       2 - Patient U/R Number
.EXTRSNAM  DIM       40      3 - Patient Surname
.EXTRGNM1  DIM       40      4 - Patient Given Name 1
.EXTRGNM2  DIM       40      5 - Patient Given Name 2
.EXTRALI1  DIM       80      6 - Patient Alias 1
EXTRALI2  DIM       80      7 - Patient Alias 2
EXTRALI3  DIM       80      8 - Patient Alias 3
EXTRALI4  DIM       80      9 - Patient Alias 4
.EXTRGEND  DIM       1       10 - Patient Sex
EXTRPDOB  DIM       8       11 - Patient Date of Birth
EXTRDOBA  DIM       1       12 - Patient Date of Birth Accuracy
EXTRADD1  DIM       35      13 - Patient Address Line 1
EXTRADD2  DIM       35      14 - Patient Address Line 2
.EXTRSUBR  DIM       35      15 - Patient Suburb
.EXTRPOST  DIM       4       16 - Patient Post Code
EXTRPTUA  DIM       1       17 - Patient Type of usual Accommodation
.EXTRPCOB  DIM       4       18 - Patient Country of Birth
EXTRRACE  DIM       1       19 - Patient Race
.EXTRABRG  DIM       1       20 - Patient Indigenous Status
.EXTROCUP  DIM       1       21 - Patient Occupation Code
.EXTROCUD  DIM       20      22 - Patient Occupation Description
EXTRTDSN  DIM       20      23 - Treating Doctor Surname
EXTRTDG1  DIM       25      24 - Treating Doctor Given Name 1
EXTRTDG2  DIM       25      25 - Treating Doctor Given Name 2
EXTRTDA1  DIM       35      26 - Treating Doctor Address 
EXTRTDA2  DIM       35      27 - Treating Doctor Suplementary Address 
EXTRTDA3  DIM       35      28 - Treating Doctor Suburb
EXTRTDPC  DIM       4       29 - Treating Doctor Post Code
EXTRTDPN  DIM       20      30 - Treating Doctor Phone Number
.EXTRHOST  DIM       5       31 - Transferrred To Hospital
EXTRGPSN  DIM       20      32 - GP Surname
EXTRGPG1  DIM       25      33 - GP Given Name 1
EXTRGPG2  DIM       25      34 - GP Given Name 2
EXTRGPA1  DIM       35      35 - GP Address
EXTRGPA2  DIM       35      36 - GP Suplementary Address
EXTRGPA3  DIM       35      37 - GP Suburb
EXTRGPPC  DIM       4       38 - GP Post Code
EXTRGPPN  DIM       20      39 - GP Phone Number
EXTRADAT  DIM       8       40 - Patient Admission Date
EXTRSDAT  DIM       8       41 - Patient Separation Date
EXTRSNAT  DIM       1       42 - Patient Separation Nature
EXTRDIAD  DIM       8       43 - Patient Diagnosis Date
.EXTRLATP  DIM       1       44 - Laterality of Primary Tumour
.EXTRPSIT  DIM       9       45 - Primary Cancer Site
.EXTRDTXT  DIM       100     46 - ICD Diagnosis Text
.EXTRSSIT  DIM       9       47 - Second Cancer Site - New for 2018
.EXTRSTXT  DIM       100     48 - ICD Secondary Text - New for 2018
.EXTRLABN  DIM       20      49 - Laboratory Name
.EXTRLTNM  DIM       10      50 - Laboratory Test Number
.EXTRPATD  DIM       50      51 - Pathology Description
.
.
.         TRANSMISSION FILE DEFINITION    * after 01/07/2025
.
. Field   Type     Size     Field Number - Description
. -----   ----     ----     ----------------
EXTRHOSP  DIM       4       1 - Hospital File Number
EXTRURNO  DIM       8       2 - Patient U/R Number
EXTRSNAM  DIM       40      3 - Patient Surname
EXTRGNM1  DIM       40      4 - Patient Given Name 1
EXTRGNM2  DIM       40      5 - Patient Given Name 2
EXTRALI1  DIM       80      6 - Patient Alias 1
EXTRGEND  DIM       1       7 - Patient Sex
EXTRADOB  DIM       10      8 - Patient Date of Birth
EXTRFADD  DIM       70      9 - Patient Address Line 1 and 2
EXTRSUBR  DIM       35      10 - Patient Suburb
EXTRPOST  DIM       4       11 - Patient Post Code
EXTRPCOB  DIM       4       12 - Patient Country of Birth
EXTRABRG  DIM       1       13 - Patient Indigenous Status
EXTRVTLS  DIM       1       14 - Vital Status 
EXTROCUP  DIM       2       15 - Patient Occupation ID
EXTROCUD  DIM       20      16 - Patient Occupation Description
EXTRADDT  DIM       10      17 - Patient Admission Date
EXTRSDDT  DIM       10      18 - Patient Separation Date
EXTRHOST  DIM       5       19 - Transferrred To Hospital
EXTRICDP  DIM       8       20 - ICD Version Primary Site
EXTRDIDT  DIM       10      21 - Patient Diagnosis Date
EXTRPSIT  DIM       9       22 - Primary Cancer Site
EXTRDTXT  DIM       100     23 - Primary Cancer ICD Diagnosis Text
EXTRSSIT  DIM       9       24 - Second Cancer Site - New for 2018
EXTRSTXT  DIM       100     25 - Second Cancer ICD Secondary Text - New for 2018
EXTRLATP  DIM       1       26 - Laterality of Primary Tumour
EXTRTMUR  DIM       10      27 - Cancer Stage Tumour
EXTRNODE  DIM       10      28 - Cancer Stage Node
EXTRMETA  DIM       10      29 - Cancer Stage Metastasis
EXTRLABN  DIM       20      31 - Laboratory Name
EXTRLTNM  DIM       10      32 - Laboratory Test Number
EXTRPATD  DIM       50      33 - Pathology Description
.
.
PRGID     INIT      "IBAINP89"
PRGNAM    INIT      "SA Cancer Register Extract"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
.
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      KDAT0000               * Keyin extract date range
          BRANCH    EXIT,ML0100
.
          CALL      KHOS0000               * Keyin hospital id 
          BRANCH    EXIT,ML0100            * if multi hospital
.
ML5000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P58:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A4,"patmi1af"
.
          DISPLAY   *P58:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P58:24,"patcrdaf"
          OPEN      PATCRDA1,"patcrdaf"
.
          DISPLAY   *P58:24,"patdadaf"
          OPEN      PATDADA1,"patdadaf"
.
          DISPLAY   *P58:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P58:24,"patdschf"
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P58:24,"patgsrnf"
          OPEN      PATGSRN1,"patgsrnf"
.
          DISPLAY   *P58:24,"patvadaf"
          OPEN      PATVADA1,"patvadaf"
.
          DISPLAY   *P58:24,"pmschpaf"
          OPEN      PMSHCPA1,"pmshcpaf"
.
          DISPLAY   *P58:24,"pmsvx1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P58:24,"patcrgaf"
          OPEN      PATCRGA1,"patcrgaf"
.
          DISPLAY   *P58:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P58:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P58:24,"pmspx2af"
          OPEN      PMSPX2A1,"pmspx2af"

          DISPLAY   *P58:24,"paticddf"
          CALL      OPICD1
.
          DISPLAY   *P58:24,"controlf"
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TEN;*54,CFILENO
          READ      CONTROLF,TWENTY;*91,PTCNHSEM
          READ      CONTROLF,TWENTY1;*216,PTCNDTRF
          READ      CONTROLF,HUND30;*86,PTCNGDX3
          READ      CONTROLF,HUND28;*231,PTCNGDX2
          READ      CONTROLF,HUND17;*220,PTCNGDX1
.
          CALL      IBACLOCK
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Cancer Extract by Date Range"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.                                   KDAT0000
.                    Routine to get the from and to dates.
.**************************************************************************
KDAT0000  MOVE      ZERO,EXIT
          DISPLAY   *P1:9,*EF,*P1:9,"From Date : ":
                    *P1:10,"To Date   : ";
.
          MOVE      CCC,CCENT
          MOVE      CYY,CYEAR
          MOVE      CMM,CMON
          MOVE      CDD,CDAY
          MOVE      ONE,CCANLDTE
          MOVE      ONE,CDEFDTE
KDAT1000  MOVE      TEN3,CCOL
          MOVE      NINE,CVERT
          CALL      KEYDATE
          BRANCH    OVRCD,KDAT9100
.
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDATE
          CALL      PACDATE
          PACK      FROMDATE,CPCDATE
          REP       " 0",FROMDATE
.
          MOVE      CCC,CCENT
          MOVE      CYY,CYEAR
          MOVE      CMM,CMON
          MOVE      CDD,CDAY
KDAT2000  MOVE      TEN3,CCOL
          MOVE      TEN,CVERT
          CALL      KEYDATE
          BRANCH    OVRCD,KDAT2000
.
          PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDATE
          CALL      PACDATE
          PACK      TODATE,CPCDATE
          REP       " 0",TODATE
.
          MATCH      STRTDATE,ENDDATE
          GOTO       KDAT9000 IF NOT LESS
.
          DISPLAY    *P1:24,*EL,*B,"2nd Range Must Be Greater Than 1st ":
                       "Range - Hit <",*V2LON,"ENTER",*HOFF,"> To Continue ";
          KEYIN      ANS;
          DISPLAY    *P1:24,*EL;
          GOTO       KDAT0000
.
KDAT9000  MOVE      ZERO,EXIT
          GOTO      KDAT9999
.
KDAT9100  MOVE      ONE,EXIT
          GOTO      KDAT9999
.
KDAT9999  RETURN
+
.***********************************************************************
.*  KHOS0000  :  Keyin hospital                                        *
.***********************************************************************
KHOS0000  COMPARE   ZERO,IBCNMHOS           * Exit if not multi hospital
          GOTO      KHOS9999 IF EQUAL
.
          DISPLAY   *P1:12,*EF,"Hospital ID : ";
.
          MOVE      "12",EVERT
          MOVE      "15",ECOL
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS0000,KHOS9000
.
          MOVE      KEY3,HOSPCODE
          DISPLAY   *P19:12,PTHSNAME
          MOVE      ZERO,EXIT
          GOTO      KHOS9999
.
KHOS9000  MOVE      ONE,EXIT                      * EXITCHAR
.
KHOS9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
PROC0000  CALL      CREA0000           * Create extract text text file
          CALL      HEAD0000           * Print report heading
.
          MOVE      ZERO,TOTALEXT      * Total extracted
          MOVE      ZERO,TOTALERR      * Total errors
.
          IF        PTCNHSEM <> 1
            CALL      NCOD0000         * Load patients by coding date
          ELSE
            CALL      NDIS0000         * Load patients by discharge date
          ENDIF
.
          MOVE      ZERO,DOYR2025
          SCAN      "PreJulyTest",REPTNAME
          IF        @EQUAL
            MATCH     "20250101",STRTDATE
          ELSE
            MATCH     "20250701",STRTDATE
          ENDIF
.
          IF        !@LESS
            MOVE      ONE,DOYR2025
            MOVE      "20250101",JULY2025
          ENDIF
          RESET     REPTNAME
.
          PACK      KEY8,SP70
          CALL      RSTEMP1
PROC1000  CALL      RKTEMP1            * Loop temp file of patients
          BRANCH    OVRCD,PROC9000
.
          PACK      KEY25,TEMPADMN,SP70
          CALL      RSPTCRD1
PROC1200  CALL      RKPTCRD1
          BRANCH    OVRCD,PROC1000
.
          MATCH     TEMPADMN,DPTCDDAD
          GOTO      PROC1000 IF NOT EQUAL
.
          CALL      CLEX0000           * Clear extract file fields
.   
          CALL      DATA0000           * Populate extract field values
          BRANCH    EXIT,PROC1200
.
          MOVE      ZERO,PRINTFLG
          CALL      MAND0000           * Check mandatory fields 
          BRANCH    EXIT,PROC1200
.
          CALL      WREX0000           * Write extract record
          CALL      PLIN0000           * Print report line
.
          PACK      PTCDDTEX,CCC,CYY,CMM,CDD,SP10   * set date extracted
          REP       " 0",PTCDDTEX
          CALL      UPPTCRD1 
.
          GOTO      PROC1200
.
PROC9000  CALL      PERR0000           * Print errors and report totals
.
          CALL      MVEX0000           * Move file to web extract directory
.
PROC9999  RETURN
+
.**************************************************************************
.*                                CREA0000
.*                       Create the extract text file    
.*        AAAAAMMYY.txt = AAAAA = Hospital Number, MM = Month and YY = year
.**************************************************************************
CREA0000  CALL      IBACLOCK
          MOVE      CFILENO,D4
.
          IF        IBCNMHOS=1
            PACK      D4,PTHSHSID,SP70
          ENDIF
          REP       " 0",D4 
.
          UNPACK    ENDDATE,ACC,AYY,AMM,ADD
.
          PACK      FILENAME,D4,AMM,AYY,SP70
          PREP      EXTCANFL,FILENAME            * Create esctract file
.
          MOVE      ZERO,EXTCOUNT                * Extracted recourd count
.
          CALL      TFILENAM                     * Get temp file name 
          MOVE      TFILNAME,FNAMEA
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMEA,CMDLINE
          APPEND    " 38 U1-8",CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPAFIL,FNAMEA              * Create temp file
.
          CALL      TFILENAM                     * Get temp file name
          MOVE      TFILNAME,FNAMEE
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMEE,CMDLINE
          APPEND    " 38 U1-8",CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPEFIL,FNAMEE              * Create error temp file
.
CREA9999  RETURN
+
.**************************************************************************
.*                               CLEX0000                                 *
.*                       Clear extract file fields                        *
.**************************************************************************
CLEX0000  MOVE      SP70,EXTRHOSP
          MOVE      SP70,EXTRURNO
          MOVE      SP70,EXTRSNAM
          MOVE      SP70,EXTRGNM1
          MOVE      SP70,EXTRGNM2
          MOVE      SP70,EXTRALI1
          MOVE      SP70,EXTRALI2
          MOVE      SP70,EXTRALI3
          MOVE      SP70,EXTRALI4
          MOVE      SP70,EXTRGEND
          MOVE      SP70,EXTRPDOB
          MOVE      SP70,EXTRDOBA
          MOVE      SP70,EXTRADD1
          MOVE      SP70,EXTRADD2
          MOVE      SP70,EXTRSUBR
          MOVE      SP70,EXTRPOST
          MOVE      SP70,EXTRPTUA
          MOVE      SP70,EXTRPCOB
          MOVE      SP70,EXTRRACE
          MOVE      SP70,EXTRABRG
          MOVE      SP70,EXTROCUP
          MOVE      SP70,EXTROCUD
          MOVE      SP70,EXTRTDSN
          MOVE      SP70,EXTRTDG1
          MOVE      SP70,EXTRTDG2
          MOVE      SP70,EXTRTDA1
          MOVE      SP70,EXTRTDA2
          MOVE      SP70,EXTRTDA3
          MOVE      SP70,EXTRTDPC
          MOVE      SP70,EXTRTDPN
          MOVE      SP70,EXTRHOST
          MOVE      SP70,EXTRGPSN
          MOVE      SP70,EXTRGPG1
          MOVE      SP70,EXTRGPG2
          MOVE      SP70,EXTRGPA1
          MOVE      SP70,EXTRGPA2
          MOVE      SP70,EXTRGPA3
          MOVE      SP70,EXTRGPPC
          MOVE      SP70,EXTRGPPN
          MOVE      SP70,EXTRADAT
          MOVE      SP70,EXTRSDAT
          MOVE      SP70,EXTRSNAT
          MOVE      SP70,EXTRDIAD
          MOVE      SP70,EXTRLATP
          MOVE      SP70,EXTRPSIT
          PACK      EXTRDTXT,SP70,SP70
          MOVE      SP70,EXTRSSIT
          PACK      EXTRSTXT,SP70,SP70
          MOVE      SP70,EXTRLABN
          MOVE      SP70,EXTRLTNM
          MOVE      SP70,EXTRPATD
          MOVE      SP70,EXTRADOB
          MOVE      SP70,EXTRFADD
          MOVE      SP70,EXTRVTLS
          MOVE      SP70,EXTRICDP
          MOVE      SP70,EXTRADDT
          MOVE      SP70,EXTRSDDT
          MOVE      SP70,EXTRDIDT
          MOVE      SP70,EXTRTMUR
          MOVE      SP70,EXTRNODE
          MOVE      SP70,EXTRMETA
.
CLEX9999  RETURN
+
.**************************************************************************
.*                               DATA0000                                 *
.*                       Populate extract field values                    *
.**************************************************************************
DATA0000  IF        IBCNMHOS=1
            MOVE      PTHSHDPC,EXTRHOSP        * Hospital File Number
          ELSE
            MOVE      CFILENO,EXTRHOSP         * Hospital File Number
          ENDIF
.
          PACK      KEY8,TEMPADMN,SP70
          CALL      RDMISS1                    * Read admission
          BRANCH    OVRCD,DATA9000
.
          PACK      KEY8,TEMPADMN,SP70
          CALL      RDPMVX11                   * Read visit erxtension file
          BRANCH    OVRCD,DATA9000
.
          PACK      KEY8,TEMPADMN,SP70
          CALL      RDDSCH1                    * Read discharge
          IF        OVRCD = 1
            CALL      CLPATDSC
          ENDIF
.
          PACK      KEY8,TEMPADMN,SP10
          CALL      RDPTCRG1                   * Get cancer registation record
          BRANCH    OVRCD,DATA9000
.
          PACK      KEY8,PTCRURNO,SP70
          CALL      RDMAST1                    * Read PMI
          BRANCH    OVRCD,DATA9000
.
          PACK      KEY8,PTCRURNO,SP70
          CALL      RDPMPX21                   * Read PMI extension file 2
          BRANCH    OVRCD,DATA9000
.
          MOVE      PTCRURNO,EXTRURNO          * Patient U/R Number
          MOVE      PTMASNAM,EXTRSNAM          * Patient Surname
          MOVE      PMPXFNAM,EXTRGNM1          * Patient Given Name 1
          MOVE      PMPXSNAM,EXTRGNM2          * Patient Given Name 2
.
          CALL      GALIS000                   * Get alias
.
          MOVE      PSEX,EXTRGEND              * Patient Sex
.
          IF        DOYR2025 = ONE
            UNPACK    PBDATE,ACC,AYY,AMM,ADD   * Date of Birth
            PACK      EXTRADOB,ADD,SLASH,AMM,SLASH,ACC,AYY
          ELSE
            MOVE      PBDATE,EXTRPDOB           * Patient Date of Birth
          ENDIF
.
          MATCH      "1",PMPXEDOB
          IF         @EQUAL
            MOVE       TWO,EXTRDOBA             * Patient Date of Birth Accuracy
          ELSE
            MOVE       ONE,EXTRDOBA
          ENDIF
.
          MOVE      PADD1,EXTRADD1             * Patient Address Line 1
          MOVE      PADD2,EXTRADD2             * Patient Address Line 2
          IF        DOYR2025 = ONE
            PACK      EXTRFADD,PADD1,SP1,PADD2,SP70
          ENDIF
          MOVE      PSUBRB,EXTRSUBR            * Patient Suburb
          MOVE      PPOST,EXTRPOST             * Patient Post Code
.
          MATCH     SP70,PMVXUSAC
          IF        !@EQUAL
            PACK      KEY5,ANSK,ANSP,PMVXUSAC,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              PACK      EXTRPTUA,THCSCOD       *  Type of usual Accommodation
            ENDIF
          ENDIF
.
          MATCH     SP70,PCONT
          IF        !@EQUAL
            PACK      KEY5,ANSC,SP1,PCONT,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              PACK      EXTRPCOB,THCSCOD       *  Country of Birth
            ENDIF
          ENDIF
. 
          MOVE      SP70,EXTRRACE              * Patient Race
.
          MATCH     SP70,PMVXABRG
          IF        !@EQUAL
            PACK      KEY5,ANSV,ANSA,PMVXABRG,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              UNPACK      THCSCOD,D1,EXTRABRG  *  Indigenous Status
            ENDIF
          ENDIF
.
          MOVE      SP70,EXTROCUP              * Occupation Code
.
          IF        DOYR2025 = 1
            MOVE      POCCUP,EXTROCUD          * Occupation Description
          ELSE
            MATCH     SP70,PMVXEMPL
            IF        !@EQUAL
              PACK      KEY5,ANSK,ANSD,PMVXEMPL,SP70
              CALL      RDCODE1
              IF        OVRCD<>1
                MOVE        TDESC,EXTROCUD     * Occupation Description
              ENDIF
            ENDIF
          ENDIF
.
          MATCH     SP70,ADOCTA                * Attending doctor
          GOTO      DATA1000 IF EQUAL
.
          PACK      KEY6,ADOCTA,SP70
          CALL      RDDOCT1
          BRANCH    OVRCD,DATA1000
.
          MOVE      DSNAME,EXTRTDSN        * Treating Doctor Surname
          MOVE      DGNAME,EXTRTDG1        * Treating Doctor Given Name 1
          MOVE      SP70,EXTRTDG2          * Treating Doctor Given Name 2
.
          MOVE      DSADD1,EXTRTDA1       * Treating Doctor Address
          MOVE      DSADD2,EXTRTDA2       * Treating Doctor Suplementary Address
          MOVE      DSADD3,EXTRTDA3       * Treating Doctor Suburb
          MOVE      DSPOST,EXTRTDPC       * Treating Doctor Post Code
          MOVE      DTELES,EXTRTDPN       * Treating Doctor Phone Number
.
DATA1000  PACK      KEY8,TEMPADMN,SP70
          CALL      RDPTDAD1
          BRANCH    OVRCD,DATA1500
.
          PACK      KEY5,PTDADCTS,SP70   
          CALL      RDPTVAD1
          BRANCH    OVRCD,DATA1500
.
          MOVE      PTVANHSC,EXTRHOST     * Transferrred To Hospital
.
DATA1500  MATCH     SP70,PMVXRHC1         * GP / Referring doctor
          GOTO      DATA2000 IF EQUAL
.
          PACK      KEY10,PMVXRHC1,SP70
          CALL      RDPMHCP1
          BRANCH    OVRCD,DATA2000
.
          MOVE      PMHCSNAM,EXTRGPSN        * GP Surname
          MOVE      PMHCGNAM,EXTRGPG1        * GP Given Name 1
          MOVE      SP70,EXTRGPG2            * GP Given Name 2
.
          MOVE      PMHCADR1,EXTRGPA1        * GP Address
          MOVE      PMHCADR2,EXTRGPA2        * GP Suplementary Address
          MOVE      PMHCADR3,EXTRGPA3        * GP Suburb
          MOVE      PMHCPOST,EXTRGPPC        * GP Post Code
          MOVE      PMHCSTEL,EXTRGPPN        * GP Phone Number
.
DATA2000  IF        DOYR2025 = ONE
            GOTO      DATA2100
          ELSE
            GOTO      DATA2200
          ENDIF
.
DATA2100  UNPACK    ADATE,ACC,AYY,AMM,ADD  
          PACK      EXTRADDT,ADD,SLASH,AMM,SLASH,ACC,AYY 
.                                          * Patient Admission Date with slash
          UNPACK    DDATE,ACC,AYY,AMM,ADD  
          PACK      EXTRSDDT,ADD,SLASH,AMM,SLASH,ACC,AYY 
.                                          * Patient Separation Date with slash
          GOTO      DATA3000         
.
DATA2200  MOVE      ADATE,EXTRADAT         * Patient Admission Date
          MOVE      DDATE,EXTRSDAT         * Patient Separation Date
.
          IF        PTCNDTRF=1
            PACK      KEY5,ANSD,SP1,DSTAT
          ELSE
            PACK      KEY5,ANSD,ANSD,DDEST
          ENDIF
          CALL      RDCODE1
          BRANCH    OVRCD,DATA3000
.
          PACK      EXTRSNAT,THCSCOD         * Patient Separation Nature
.
DATA3000  IF        DOYR2025 = ONE
            UNPACK    PTCDDDTE,ACC,AYY,AMM,ADD
            PACK      EXTRDIDT,ADD,SLASH,AMM,SLASH,ACC,AYY
.                                            * Patient Diagnosis Date wit slash
          ELSE
            MOVE      PTCDDDTE,EXTRDIAD      * Patient Diagnosis Date
          ENDIF
.
DATA3500  MATCH     SP70,PTCDLATR
          IF        !@EQUAL
            PACK      KEY5,ANSL,ANSY,PTCDLATR,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              PACK      EXTRLATP,THCSCOD     * Laterality of Primary Tumour
            ENDIF
          ENDIF
.
          MOVE      PTCDPRMM,EXTRPSIT        * Primary Cancer Site
.
          MATCH     SP70,DDATE
          IF        !@EQUAL
            MOVE      DDATE,ICDRDDTE
          ELSE
            MOVE      SP70,ICDRDDTE
          ENDIF
.
          PACK      KEY9,PTCDPRMM,SP70
          CALL      RDPTICD1
          IF        OVRCD=0
            MOVE      DDESC,EXTRDTXT         * ICD Diagnosis Text
          ENDIF
.
          MOVE      PTCDMET1,EXTRSSIT        * Second Cancer Site
.
          PACK      KEY9,PTCDMET1
          CALL      RDPTICD1
          IF        OVRCD=0
            MOVE      DDESC,EXTRSTXT         * ICD Second Site Text
          ENDIF
.
          MATCH     SP70,PTCDRPLB
          IF        !@EQUAL
            PACK      KEY5,ANSL,ANSB,PTCDRPLB,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              MOVE     TDESC,EXTRLABN        * Laboratory Name
            ENDIF
          ENDIF
.
          MOVE      PTCDBOL5,EXTRLTNM        * Laboratory Test Number
          MOVE      PTCDOTDG,EXTRPATD        * Pathology Description
.
          COMPARE   DOYR2025,ONE
          GOTO      DATA8000 IF NOT EQUAL
.
.         Below is for 2025 SA Stat Changes
.
          IF        PCEASE=1
            MATCH     PDECDTE,DDATE          * Date of Death & Discharge Date
            IF        @LESS | @EQUAL
              MOVE      ZERO,EXTRVTLS        * Vital Status - Dead 
            ELSE
              MOVE      ONE,EXTRVTLS         * Vital Status - Alive
            ENDIF
          ELSE
            MOVE      ONE,EXTRVTLS           * Vital Status - Alive
          ENDIF
.
          MATCH     DDATE,PTCNGDX3        * ICD10 Ed.13          *I-0956210
          IF        @LESS | @EQUAL
            MOVE      "ICD10V13",EXTRICDP 
            GOTO      DATA4000
          ENDIF
.
          MATCH     DDATE,PTCNGDX2        * ICD10 Ed.12          *I-0956210
          IF        @LESS | @EQUAL
            MOVE      "ICD10V12",EXTRICDP
            GOTO      DATA4000
          ENDIF
.
          MATCH     DDATE,PTCNGDX1        * ICD10 Ed.11          *I-0956210
          IF        @LESS | @EQUAL
            MOVE      "ICD10V11",EXTRICDP  
            GOTO      DATA4000
          ENDIF
.
DATA4000  MOVE      PTCDCSGT,EXTRTMUR      * Cancer Stage - Tumour 
          MOVE      PTCDCSGN,EXTRNODE      * Cancer Stage - Node
          MOVE      PTCDCSGM,EXTRMETA      * Cancer Stage - Metastasis
.
DATA8000  MOVE      ZERO,EXIT
          GOTO      DATA9999
.
DATA9000  MOVE      ONE,EXIT
          GOTO      DATA9999
.
DATA9999  RETURN
+
.**************************************************************************
.*                                GALIS000
.                  Get & Write Former/Alias Names details
.**************************************************************************
GALIS000  MOVE      ZERO,FORM2
          PACK      KEY8,PURNO
.
          PACK      KEY115,KEY8,SP70,SP70
          CALL      RSPTGSR1
GALIS100  CALL      RKPTGSR1
          BRANCH    OVRCD,GALIS999
.
GALIS400  MATCH     KEY8,GSRURNO            * check U/R
          GOTO      GALIS999 IF NOT EQUAL
.
          MATCH     GSRSNAM,PTMASNAM        * check surname
          GOTO      GALIS500 IF NOT EQUAL
.
          MATCH     GSRGNAM,PMPXFNAM        * check first given name
          GOTO      GALIS500 IF NOT EQUAL
.
          MATCH     PTGSGNM2,PMPXSNAM       * check second given name
          GOTO      GALIS500 IF NOT EQUAL
.
          MOVE      ZERO,GSRURNO
          GOTO      GALIS100
.                                           * same UR No. found
GALIS500  ADD       ONE,FORM2
          BRANCH    FORM2,GALIS510,GALIS520,GALIS530,GALIS540
          GOTO      GALIS999
.
GALIS510  STRIP     GSRGNAM
          PACK      EXTRALI1,GSRGNAM,SP1,GSRSNAM,SP20   * 1st alias
          GOTO      GALIS100
.
GALIS520  STRIP     GSRGNAM
          PACK      EXTRALI2,GSRGNAM,SP1,GSRSNAM,SP20   * 2st alias
          GOTO      GALIS100
.
GALIS530  STRIP     GSRGNAM
          PACK      EXTRALI3,GSRGNAM,SP1,GSRSNAM,SP20   * 3st alias
          GOTO      GALIS999
.
GALIS540  STRIP     GSRGNAM
          PACK      EXTRALI4,GSRGNAM,SP1,GSRSNAM,SP20   * 4st alias
          GOTO      GALIS999
.
GALIS999  RETURN
+
.**************************************************************************
.*                               WREX0000                                 *
.*                       Write extract file record                        *
.**************************************************************************
WREX0000  STRIP     EXTRHOSP
          STRIP     EXTRURNO
          STRIP     EXTRSNAM
          STRIP     EXTRGNM1
          STRIP     EXTRGNM2
          STRIP     EXTRALI1
          STRIP     EXTRALI2
          STRIP     EXTRALI3
          STRIP     EXTRALI4
          STRIP     EXTRGEND
          STRIP     EXTRPDOB
          STRIP     EXTRDOBA
          STRIP     EXTRADD1
          STRIP     EXTRADD2
          STRIP     EXTRSUBR
          STRIP     EXTRPOST
          STRIP     EXTRPTUA
          STRIP     EXTRPCOB
          STRIP     EXTRRACE
          STRIP     EXTRABRG
          STRIP     EXTROCUP
          STRIP     EXTROCUD
          STRIP     EXTRTDSN
          STRIP     EXTRTDG1
          STRIP     EXTRTDG2
          STRIP     EXTRTDA1
          STRIP     EXTRTDA2
          STRIP     EXTRTDA3
          STRIP     EXTRTDPC
          STRIP     EXTRTDPN
          STRIP     EXTRHOST
          STRIP     EXTRGPSN
          STRIP     EXTRGPG1
          STRIP     EXTRGPG2
          STRIP     EXTRGPA1
          STRIP     EXTRGPA2
          STRIP     EXTRGPA3
          STRIP     EXTRGPPC
          STRIP     EXTRGPPN
          STRIP     EXTRADAT
          STRIP     EXTRSDAT
          STRIP     EXTRSNAT
          STRIP     EXTRDIAD
          STRIP     EXTRLATP
          STRIP     EXTRPSIT
          STRIP     EXTRDTXT
          STRIP     EXTRSSIT
          STRIP     EXTRSTXT
          STRIP     EXTRLABN
          STRIP     EXTRLTNM
          STRIP     EXTRPATD
          STRIP     EXTRADOB
          STRIP     EXTRFADD
          STRIP     EXTRVTLS
          STRIP     EXTRICDP
          STRIP     EXTRADDT
          STRIP     EXTRSDDT
          STRIP     EXTRDIDT
          STRIP     EXTRTMUR
          STRIP     EXTRNODE
          STRIP     EXTRMETA
.
          ADD       ONE,EXTCOUNT
.
          COMPARE   DOYR2025,ONE
          GOTO      WREX2000 IF EQUAL
.          
          WRITE     EXTCANFL,SEQ;*+,EXTRHOSP,PIPE,EXTRURNO,PIPE,EXTRSNAM,PIPE:
.                                    EXTRGNM1,PIPE,EXTRGNM2,PIPE,EXTRALI1,PIPE:
                                    EXTRGNM1,PIPE,EXTRGNM2,PIPE:
.                                    EXTRALI2,PIPE,EXTRALI3,PIPE,EXTRALI4,PIPE:
.                                    EXTRGEND,PIPE,EXTRPDOB,PIPE,EXTRDOBA,PIPE:
                                    EXTRGEND,PIPE,EXTRPDOB,PIPE:
                                    EXTRADD1,PIPE,EXTRADD2,PIPE,EXTRSUBR,PIPE:
.                                    EXTRPOST,PIPE,EXTRPTUA,PIPE,EXTRPCOB,PIPE:
                                    EXTRPOST,PIPE,EXTRPCOB,PIPE:
.                                    EXTRRACE,PIPE,EXTRABRG,PIPE,EXTROCUP,PIPE:
                                    EXTRABRG,PIPE:
                                    EXTROCUD,PIPE,EXTRTDSN,PIPE,EXTRTDG1,PIPE:
                                    EXTRTDG2,PIPE,EXTRTDA1,PIPE,EXTRTDA2,PIPE:
                                    EXTRTDA3,PIPE,EXTRTDPC,PIPE,EXTRTDPN,PIPE:
.                                    EXTRHOST,PIPE,EXTRGPSN,PIPE,EXTRGPG1,PIPE:
                                    EXTRGPSN,PIPE,EXTRGPG1,PIPE:
                                    EXTRGPG2,PIPE,EXTRGPA1,PIPE,EXTRGPA2,PIPE:
                                    EXTRGPA3,PIPE,EXTRGPPC,PIPE,EXTRGPPN,PIPE:
.                                    EXTRADAT,PIPE,EXTRSDAT,PIPE,EXTRSNAT,PIPE:
                                    EXTRADAT,PIPE:
                                    EXTRSDAT,PIPE,EXTRHOST,PIPE,EXTRSNAT,PIPE:
.                                    EXTRDIAD,PIPE,EXTRLATP,PIPE,EXTRPSIT,PIPE:
                                    EXTRDIAD,PIPE,EXTRPSIT,PIPE,EXTRDTXT,PIPE:
                                    EXTRLATP,PIPE,EXTRSSIT,PIPE,EXTRSTXT,PIPE:
                                    EXTRLABN,PIPE,EXTRLTNM,PIPE,EXTRPATD
.
          GOTO      WREX9999
.
WREX2000  WRITE     EXTCANFL,SEQ;*+,EXTRHOSP,PIPE,EXTRURNO,PIPE,EXTRSNAM,PIPE:
                                    EXTRGNM1,PIPE,EXTRGNM2,PIPE,EXTRALI1,PIPE:
                                    EXTRGEND,PIPE,EXTRADOB,PIPE,EXTRFADD,PIPE:
                                    EXTRSUBR,PIPE,EXTRPOST,PIPE,EXTRPCOB,PIPE:
                                    EXTRABRG,PIPE,EXTRVTLS,PIPE,EXTROCUP,PIPE:
                                    EXTROCUD,PIPE,EXTRADDT,PIPE,EXTRSDDT,PIPE:
                                    EXTRHOST,PIPE,EXTRICDP,PIPE,EXTRDIDT,PIPE:
                                    EXTRPSIT,PIPE,EXTRDTXT,PIPE,EXTRSSIT,PIPE:
                                    EXTRSTXT,PIPE,EXTRLATP,PIPE,EXTRTMUR,PIPE:
                                    EXTRNODE,PIPE,EXTRMETA,PIPE,EXTRLABN,PIPE:
                                    EXTRLTNM,PIPE,EXTRPATD,PIPE
.
WREX9999  RETURN
+
.**************************************************************************
.*                                MVEX0000
.*                       Move to web extracts directory
.*                       or delete empty extract file
.**************************************************************************
MVEX0000  IF        EXTCOUNT=0
            CLOSE     EXTCANFL,DELETE         * Delete empty extract file
          ELSE
            CLEAR     CMDLINE                 * cancer details file
            APPEND    "ibainp89.us1 ",CMDLINE
            APPEND    FILENAME,CMDLINE
            APPEND    ".txt ",CMDLINE
            RESET     CMDLINE
            EXECUTE   CMDLINE,TASKID
          ENDIF
.
          CLOSE     TEMPAFIL
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMEA,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
          CLOSE     TEMPEFIL
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMEE,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
MVEX9999  RETURN
+
.**************************************************************************
.*                               MAND0000                                 *
.*                       Check mandatory fields are populated             *
.**************************************************************************
MAND0000  MOVE      ZERO,MANDFLAG
.
          MATCH     SP70,EXTRHOSP       * Hospital File Number
          IF        @EQUAL
            IF        PRINTFLG=1
              MOVE     "Hospital File Number",FIELDNAM
              CALL     PRTE0000       
            ENDIF
            MOVE      ONE,MANDFLAG
          ENDIF
.
          MATCH     SP70,EXTRURNO       * Patient U/R Number
          IF        @EQUAL
            IF        PRINTFLG=1
              MOVE     "Patient U/R Number",FIELDNAM
              CALL     PRTE0000       
            ENDIF
            MOVE      ONE,MANDFLAG
          ENDIF
.
          MATCH     SP70,EXTRSNAM       * Patient Surname
          IF        @EQUAL
            IF        PRINTFLG=1
              MOVE     "Patient Surname",FIELDNAM
              CALL     PRTE0000       
            ENDIF
            MOVE      ONE,MANDFLAG
          ENDIF
.
          MATCH     SP70,EXTRGNM1       * Patient Given Name 1
          IF        @EQUAL
            MOVE      ONE,MANDFLAG
            IF        PRINTFLG=1
              MOVE     "Patient Given Name",FIELDNAM
              CALL     PRTE0000       
            ENDIF
          ENDIF
.
          MATCH     SP70,EXTRGEND       * Patient Sex
          IF        @EQUAL
            MOVE      ONE,MANDFLAG
            IF        PRINTFLG=1
              MOVE     "Patient Sex",FIELDNAM
              CALL     PRTE0000       
            ENDIF
          ENDIF
.
          IF        DOYR2025 = ONE
            MATCH     SP70,EXTRADOB       * Patient Date of Birth 
            IF        @EQUAL
              MOVE      ONE,MANDFLAG
              IF        PRINTFLG=1
                MOVE     "Patient Date of Birth",FIELDNAM
                CALL     PRTE0000
              ENDIF
            ENDIF
          ELSE
            MATCH     SP70,EXTRPDOB       * Patient Date of Birth
            IF        @EQUAL
              MOVE      ONE,MANDFLAG
              IF        PRINTFLG=1
                MOVE     "Patient Date of Birth",FIELDNAM
                CALL     PRTE0000
              ENDIF
            ENDIF
          ENDIF
.
          IF        DOYR2025 = ONE
            MATCH     SP70,EXTRFADD       * Patient Full Address
            IF        @EQUAL
              MOVE      ONE,MANDFLAG
              IF        PRINTFLG=1
                MOVE     "Patient Address is ",FIELDNAM
                CALL     PRTE0000
              ENDIF
            ENDIF
          ELSE
            MATCH     SP70,EXTRADD1       * Patient Address Line 1
            IF        @EQUAL
              MOVE      ONE,MANDFLAG
              IF        PRINTFLG=1
                MOVE     "Patient Address Line 1",FIELDNAM
                CALL     PRTE0000
              ENDIF
            ENDIF
          ENDIF
.
          MATCH     SP70,EXTRSUBR       * Patient Suburb
          IF        @EQUAL
            MOVE      ONE,MANDFLAG
            IF        PRINTFLG=1
              MOVE     "Patient Suburb",FIELDNAM
              CALL     PRTE0000       
            ENDIF
          ENDIF
.
          MATCH     SP70,EXTRPOST       * Patient Postcode
          IF        @EQUAL
            MOVE      ONE,MANDFLAG
            IF        PRINTFLG=1
              MOVE     "Patient Postcode",FIELDNAM
              CALL     PRTE0000
            ENDIF
          ENDIF
.
          MATCH     SP70,EXTRPCOB       * Patient Country of Birth
          IF        @EQUAL
            MOVE      ONE,MANDFLAG
            IF        PRINTFLG=1
              MOVE     "Patient Country of Birth",FIELDNAM
              CALL     PRTE0000       
            ENDIF
          ENDIF
.
          MATCH     SP70,EXTRABRG       * Patient Indigenous Status
          IF        @EQUAL
            MOVE      ONE,MANDFLAG
            IF        PRINTFLG=1
              MOVE     "Patient Indigenous Status",FIELDNAM
              CALL     PRTE0000       
            ENDIF
          ENDIF
.
          COMPARE   DOYR2025,ONE
          IF        @EQUAL
            MATCH     SP70,EXTRVTLS       * Vital Status
            IF        @EQUAL
              MOVE      ONE,MANDFLAG
              IF        PRINTFLG=1
                MOVE     "Vital Status",FIELDNAM
                CALL     PRTE0000
              ENDIF
            ENDIF
          ENDIF
.
          COMPARE   DOYR2025,ONE
          IF        !@EQUAL
            MATCH     SP70,EXTRTDSN       * Treating Doctor Surname
            IF        @EQUAL
              MOVE      ONE,MANDFLAG
              IF        PRINTFLG=1
                MOVE     "Treating Doctor Surname",FIELDNAM
                CALL     PRTE0000       
              ENDIF
            ENDIF
.
            MATCH     SP70,EXTRTDG1       * Treating Doctor Given Name 1
            IF        @EQUAL
              MOVE      ONE,MANDFLAG
              IF        PRINTFLG=1
                MOVE     "Treating Doctor Given Name",FIELDNAM
                CALL     PRTE0000       
              ENDIF
            ENDIF
          ENDIF
.
          COMPARE   DOYR2025,ONE
          IF        @EQUAL
            MATCH     SP70,EXTRADDT       * Patient Admission Date
            IF        @EQUAL
              MOVE      ONE,MANDFLAG
              IF        PRINTFLG=1
                MOVE     "Patient Admission Date",FIELDNAM
                CALL     PRTE0000
              ENDIF
            ENDIF
          ELSE
            MATCH     SP70,EXTRADAT       * Patient Admission Date
            IF        @EQUAL
              MOVE      ONE,MANDFLAG
              IF        PRINTFLG=1
                MOVE     "Patient Admission Date",FIELDNAM
                CALL     PRTE0000       
              ENDIF
            ENDIF
          ENDIF
.         
          COMPARE   DOYR2025,ONE
          IF        @EQUAL
            MATCH     SP70,EXTRSDDT       * Patient Separation Date
            IF        @EQUAL
              MOVE      ONE,MANDFLAG
              IF        PRINTFLG=1
                MOVE     "Patient Separation Date",FIELDNAM
                CALL     PRTE0000       
              ENDIF
            ENDIF
          ELSE
            MATCH     SP70,EXTRSDAT       * Patient Separation Date
            IF        @EQUAL
              MOVE      ONE,MANDFLAG
              IF        PRINTFLG=1
                MOVE     "Patient Separation Date",FIELDNAM
                CALL     PRTE0000
              ENDIF
            ENDIF
          ENDIF
.
          COMPARE   DOYR2025,ONE
          IF        !@EQUAL
            MATCH     SP70,EXTRSNAT       * Patient Separation Nature
            IF        @EQUAL
              MOVE      ONE,MANDFLAG
              IF        PRINTFLG=1
                MOVE     "Patient Separation Nature",FIELDNAM
                CALL     PRTE0000       
              ENDIF
            ENDIF
          ENDIF
.
          MATCH     SP70,EXTRPSIT       * Primary Cancer Site
          IF        @EQUAL
            MOVE      ONE,MANDFLAG
            IF        PRINTFLG=1
              MOVE     "Primary Cancer Site",FIELDNAM
              CALL     PRTE0000       
            ENDIF
          ENDIF
.
          COMPARE   DOYR2025,ONE
          IF        !@EQUAL
            MATCH     SP70,EXTRLABN       * Laboratory Name
            IF        @EQUAL
              MOVE      ONE,MANDFLAG
              IF        PRINTFLG=1
                MOVE     "Laboratory Name",FIELDNAM
                CALL     PRTE0000       
              ENDIF
            ENDIF
          ENDIF
.
          MATCH     SP70,EXTRLTNM       * Laboratory Test Number
          IF        @EQUAL
            MOVE      ONE,MANDFLAG
            IF        PRINTFLG=1
              MOVE     "Laboratory Test Number",FIELDNAM
              CALL     PRTE0000       
            ENDIF
          ENDIF
.
          COMPARE   ONE,MANDFLAG
          GOTO      MAND9100 IF NOT EQUAL
.
MAND9000  BRANCH    PRINTFLG,MAND9100            * Printing errors
.
          MOVE      TEMPADMN,ERROADMN
          MOVE      SP70,ERROSPAR
.
          PACK      KEY8,ERROADMN,SP70
          CALL      RDTEME1
          IF        OVRCD=1
            CALL      WRTEME1                    * Write admission to error file
          ENDIF
.
          MOVE      ONE,EXIT
          GOTO      MAND9999
.
MAND9100  MOVE      ZERO,EXIT
          GOTO      MAND9999
.
MAND9999  RETURN
+
.**************************************************************************
.*                               PRTE0000                                 *
.*                      Print error line for report                       *
.**************************************************************************
PRTE0000  COMPARE   "52",CLNO
          IF        !@LESS
            CALL      HEAD0000 
            CALL      EHED0000           * Print error heading
          ENDIF
.
          PRINT     *1,"| Admission ",TEMPADMN:
                    " is missing a value in the mandatory field ":
                    FIELDNAM,*132,"|"
.
          ADD       ONE,CLNO
          ADD       ONE,TOTALERR       * Total patients with errors
.
PRTE9999  RETURN
+
.**************************************************************************
.*                               NCOD0000                                 *
.*                      Load patient by coding date                       *
.**************************************************************************
NCOD0000  MOVE      SP30,TEMPSPAR
.
          PACK      KEY16,STRTDATE,SP20
          CALL      RDSMISS4
NCOD1000  CALL      RDKMISS4
          BRANCH    OVRCD,NCOD9999
.
          MATCH     ACODDTE,ENDDATE         * In Date range
          GOTO      NCOD9999 IF LESS
.
          PACK      KEY8,AADMNO,SP10
          CALL      RDPTCRG1                * Get cancer registation record
          BRANCH    OVRCD,NCOD1000
.                                           * found a wanted Cancer Reg.
          IF        IBCNMHOS=1
            PACK      KEY8,AADMNO,SP70
            CALL      RDPMVX11
            BRANCH    OVRCD,NCOD1000
            MATCH     HOSPCODE,PMVXMHOS
            GOTO      NCOD1000 IF NOT EQUAL
          ENDIF

          MOVE      AADMNO,TEMPADMN
.
          PACK      KEY8,TEMPADMN,SP70
          CALL      RDTEMP1
          IF        OVRCD=1
            CALL      WRTEMP1
          ENDIF
          GOTO      NCOD1000
.
NCOD9999  RETURN
+
.**************************************************************************
.*                               NDIS0000                                 *
.*                      Load patients by discharge date                   *
.**************************************************************************
NDIS0000  MOVE      SP30,TEMPSPAR
.
          PACK      KEY16,STRTDATE,SP10
          CALL      RDSDSCH2
NDIS1000  CALL      RDKDSCH2                * Loop over discharges
          BRANCH    OVRCD,NDIS9999
.
          MATCH     DDATE,ENDDATE           * In date range
          GOTO      NDIS9999 IF LESS
.
          PACK      KEY8,DADMNO,SP70
          CALL      RDPTCRG1
          BRANCH    OVRCD,NDIS1000
.
          IF        IBCNMHOS=1
            PACK      KEY8,DADMNO,SP70
            CALL      RDPMVX11
            BRANCH    OVRCD,NDIS1000
            MATCH     HOSPCODE,PMVXMHOS
            GOTO      NDIS1000 IF NOT EQUAL
          ENDIF
.                                           * found a wanted Cancer Reg.
          MOVE      DADMNO,TEMPADMN
.
          PACK      KEY8,TEMPADMN,SP70
          CALL      RDTEMP1
          IF        OVRCD=1
            CALL      WRTEMP1
          ENDIF
          GOTO      NDIS1000
.
NDIS9999  RETURN
+
.**************************************************************************
.*                               HEAD0000                                 *
.*                      Print report heading                              *
.**************************************************************************
HEAD0000  CALL      HEAD132
.
          PRINT     *30,"South Australian Cancer Register"
.
          IF        PTCNHSEM <> 1
            PRINT     *30,"From Coding Date : ",FROMDATE," To : ",TODATE
          ELSE
            PRINT     *30,"From Discharge Date : ",FROMDATE," To : ",TODATE
          ENDIF
.
          IF        IBCNMHOS =1
            PRINT     *30,"Hospital : ",PTHSNAME
            MOVE      SIX,CLNO
          ELSE
            MOVE      FIVE,CLNO
          ENDIF
.
          CALL      UND132
.
HEAD9999  RETURN
+
.**************************************************************************
.*                               EHED0000                                 *
.*                      Print error heading                               *
.**************************************************************************
EHED0000  IF       CLNO>10
            CALL      UND132
          ENDIF
.
          PRINT     *1,"| South Australian Cancer Registration Errors":
                    *132,"|"
          ADD       ONE,CLNO
.
          CALL      UND132
.
EHED9999  RETURN
+
.**************************************************************************
.*                               PLIN0000                                 *
.*                      Print report line                                 *
.**************************************************************************
PLIN0000  ADD       ONE,TOTALEXT               * Total extracted
.
          ADD       ONE,CLNO
          COMPARE   "52",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PACK      KEY9,PTCDPRMM,SP70
          CALL      RDPTICD1
          IF        OVRCD=0
            MOVE      DDESC,DIAGDESC           * Primary site text description
          ELSE
            MOVE      SP70,DIAGDESC
          ENDIF
.
          MOVE      TWO,PACFRMT
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          CALL      PACNAME
          MOVE      PACFNAME,KEY28
.
          UNPACK    PBDATE,ACC,AYY,AMM,ADD   * Date of Birth
          MOVE      AMM,F2
          LOAD      MONTH3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DOB,ADD,SLASH,AMM,SLASH,ACC,AYY
.
          UNPACK    ADATE,ACC,AYY,AMM,ADD
          MOVE      AMM,F2
          LOAD      MONTH3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      ADMDATE,ADD,SLASH,AMM,SLASH,ACC,AYY
.
          IF        PTCNHSEM <> 1
            MOVE     "Coding",DIM9
            UNPACK    ACODDTE,ACC,AYY,AMM,ADD
            MOVE      AMM,F2
            LOAD      MONTH3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      PRNTDATE,ADD,SLASH,AMM,SLASH,ACC,AYY
          ELSE
            MOVE     "Discharge",DIM9
            UNPACK    DDATE,ACC,AYY,AMM,ADD
            MOVE      AMM,F2
            LOAD      MONTH3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      PRNTDATE,ADD,SLASH,AMM,SLASH,ACC,AYY
          ENDIF
.
          PRINT     *1,"| U/R Number",*18,": ",PURNO,*54,"| Admission No.":
                    *76,": ",AADMNO,*132,"|"
          PRINT     *1,"| Name",*18,": ",KEY28,*54,"| Admission Date ":
                    *76,": ",ADMDATE,*132,"|"
          PRINT     *1,"| Date of Birth",*18,": ",DOB,*54,"| ",DIM9," Date":
                    *76,": ",PRNTDATE,*132,"|"
          PRINT     *1,"| Sex",*18,": ",PSEX,*54,"| Diagnosis":
                    *76,": ",DIAGDESC,*132,"|"
. 
          ADD       FOUR,CLNO
          CALL      UND132
.
PLIN9999  RETURN
+
.**************************************************************************
.*                               PERR0000                                 *
.*                      Print errors and report totals                    *
.**************************************************************************
PERR0000  MOVE      ZERO,PRNTHEAD       * Set flag to print error heading
.
          PACK      KEY8,SP70
          CALL      RSTEME1
PERR1000  CALL      RKTEME1
          BRANCH    OVRCD,PERR5000
.
          IF        PRNTHEAD=0
            CALL      EHED0000           * Print error heading
            MOVE      ONE,PRNTHEAD
          ENDIF
.
          MOVE      ERROADMN,TEMPADMN
.
          PACK      KEY25,TEMPADMN,SP70
          CALL      RSPTCRD1
PERR1200  CALL      RKPTCRD1
          BRANCH    OVRCD,PERR1000
.
          MATCH     TEMPADMN,DPTCDDAD
          GOTO      PERR1000 IF NOT EQUAL
.
          CALL      CLEX0000           * Clear extract file fields
.
          CALL      DATA0000           * Populate extract field values
          BRANCH    EXIT,PERR1200
.
          MOVE      ONE,PRINTFLG
          CALL      MAND0000          
.
          GOTO      PERR1200
.
PERR5000  CALL      UND132
          PRINT     *1," Number of Cancer Registrations : ",TOTALEXT
          PRINT     *1," Number of Errors               : ",TOTALERR
.
          PRINT     *N,*1,"*** End of Report ***"
.
PERR9999  RETURN
+
.******************************************************************************
.*                             I/O for Temporary files                        *
.******************************************************************************
RSTEMP1   RESET     KEY8
          READ      TEMPAFIL,KEY8;;
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMPAFIL;TEMPADMN,TEMPSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READ      TEMPAFIL,KEY8;TEMPADMN,TEMPSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   WRITE     TEMPAFIL,KEY8;TEMPADMN,TEMPSPAR
          RETURN
.
RSTEME1   RESET     KEY8
          READ      TEMPEFIL,KEY8;;
          RETURN
.
RKTEME1   MOVE      ZERO,OVRCD
          READKS    TEMPEFIL;ERROADMN,ERROSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTEME1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READ      TEMPEFIL,KEY8;ERROADMN,ERROSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEME1   WRITE     TEMPEFIL,KEY8;ERROADMN,ERROSPAR
          RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       CLPATDSC
          INC       PATHSPKY
          INC       STD001IO
          INC       TFILENAM
.
          INC       IBASEQIO/INC
          INC       PATCODIO/INC
          INC       PATCRDIO/INC
          INC       PATCRGIO/INC
          INC       PATDADIO/INC
          INC       PATDO1IO/INC
          INC       PATDSCIO/INC
          INC       PATGSRIO/INC
          INC       PMSHCPIO/INC
          INC       PATHSPIO/INC
          INC       PATICDIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATVADIO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
          INC       WEBERRIO/INC

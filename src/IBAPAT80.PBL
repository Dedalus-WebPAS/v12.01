.******************************************************************************
.*                                                                            *
.*                        P R O G R A M   S U M M A R Y                       *
.*                        -------------   -------------                       *
.*                                                                            *
.*     PROGRAM NAME:     IBAPAT80                                             *
.*                                                                            *
.*     FUNCTION:         PRINT DOCTOR LABELS                                  *
.*                                                                            *
.*     DATE WRITTEN:     23/07/1986                                           *
.*                                                                            *
.*     AUTHOR:           MALCOLM HAYSE                                        *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.*        V10.02.01 04/04/2011 Jill Parkinson CAR 239574                      *
.*                  Removed open of ibaprntf                                  *
.*        V9.08.01  07/06/2007  Mike Laming   CAR 125736                      *
.*                  Changes to use Doctor Address Line 4                      *
.*        V9.03.00  28/07/2004  Pat Dirito       CAR 50673                    *
.*                  Ported from V6.12                                         *
.******************************************************************************
.*       V6.09.01   14/03/2002	Raghunandan Surve,HPS - SRF14810              *
.*                  Fixed label printing by doctor type                       *
.*                  Fixed display problems                                    *
.*............................................................................*
.*       V6.09.B02  16/02/2001	 Tonii  609 rebounds                          *
.*                  - Added ? search for Doctor Type                          *
.*                  - Added more space for Displaying of Doctor Surname       *
.******************************************************************************
.*                       W2.01  10/09/86 Graeme Williams                      *
.*                              Added printer selection Menu                  *
.*                       W2.02  11/01/87 M. Hayse                             *
.*                              For Doctor Type option also print if DRTYPE2, *
.*                              3, 4, and 5                                   *
.*                       W2.03  22/01/87 M. Hayse                             *
.*                              Stop Display Defaults indicator               *
.*                       W2.04  29/01/87 Graeme Williams                      *
.*                              Fixed label spacing down page                 *
.*                       W2.05  25/02/87 M. Hayse                             *
.*                              Epworth Labels added                          *
.*                       W2.06  02/03/87 M. Hayse                             *
.*                              New size of labels                            *
.*                       W2.07  30/04/87 Graeme Williams                      *
.*                              Allow non-numeric doctor codes                *
.*                       W2.08  09/07/87 J. Tan                               *
.*                              Mods to give option on number of labels to    *
.*                              print                                         *
.*                       W2.09  28/08/87 J. Tan                               *
.*                              Option Primary Type only                      *
.*                       W2.10  09/09/87 Graeme Williams                      *
.*                              Use new printer system                        *
.*                       W2.11  07/10/87 Graeme Williams                      *
.*                              Define no. of labels from CONTRLOF            *
.*                       W2.12  28/10/87 J. Tan                               *
.*                              Added Doctor Label size from CONTROLF         *
.*                       W2.13  12/01/88 Graeme Williams                      *
.*                              Fixed third label line-up.                    *
.*                              Batch labels for option 2.                    *
.*                       W2.14  01/02/88 Graeme Williams                      *
.*                              Added CDOCLLN facility                        *
.*                       W2.15  25/03/88 P. Lo                                *
.*                              Move postcode 2 characters to the left        *
.*                       W2.16  26/05/88 K. Peak                              *
.*                              Added option to print Doctor labels in        *
.*                              surname sequence                              *
.*                       W4.01  09/11/88 K. Peak                              *
.*                              Print 2nd init. of patient for St. Andrews    *
.*                              (Melbourne)                                   *
.*                       V4.02  29/12/88 J. Tan           SRF 5277            *
.*                              Print the Initial of 2nd given name           *
.*                                                                            *
.*        V5.51     28/11/1989  S. Barcham        SRF 102600                  *
.*                  Only print labels for active doctors                      *
.*        V5.52     25/05/1990  D. Di.Paola       SRF 105112                  *
.*                  Only print for active as well as active outside hospital  *
.*                  doctors                                                   *
.*        V5.53     07/06/1991  i chung           SRF 108765                  *
.*                  Fixed missing 2nd given name initial                      *
.*        V5.54     07/08/1991  i chung           SRF 109924                  *
.*                  Called SELPRINT and added OPNPRINT & CLSPRINT statements  *
.*                  to overcome printer problem                               *
.*        V6.00.00  28/10/1991  i chung           SRF 111176                  *
.*                  Fixed incorrect no. of labels across page when repeating  *
.*                  Option 3 - Doctor Type                                    *
.*        V6.00.01  29/01/1993  i chung           SRF 118441                  *
.*                  Allow to print inactive doctors as well for Opts 3,4, & 5 *
.*        V6.02.01  10/01/1994  Graeme Williams   SRF 131746                  *
.*                  Added question to say if inactive doctors are wanted      *
.*        V6.02.02  13/01/1995  Paul Howells      SRF 131746                  *
.*                  Added question to say if active outside doctors are wanted*
.*        V6.03.01  08/05/1995  ROD               SRF 136349                  *
.*                  Fixed above mod (going into infinite loop)                *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
.
          INC       IBAPRNFD/INC
.
          INC       PATCODFD/INC
          INC       PATDO1FD/INC 
+
.
. ------ <CONTROLF Variables Declaration> ------
.
CDOCLAB   FORM      1             * Number of labels across page
CDOCLLN   FORM      1             * Number of lines on a label
CDOCSLB   FORM      1             * Label Size use for doctor label
CNDOCLB   FORM      2             * Default number of doctor labels
CTYPLAB   FORM      1             * Label Type in hospital
.
. ------ <Variables Declaration> ------
.
ACTOTFLG  FORM      1
ANS1      DIM       1
.
CODE      DIM       3
COUNTER   FORM      1
.
DOCCODE   DIM       6
DIM3      DIM       3
DIM30     DIM       30
DNAME1    DIM       33
DNAME2    DIM       33
DNAME3    DIM       33
DSADD11   DIM       30
DSADD12   DIM       30
DSADD13   DIM       30
DSADD21   DIM       30
DSADD22   DIM       30
DSADD23   DIM       30
DSADD31   DIM       30
DSADD32   DIM       30
DSADD33   DIM       30
DSADD41   DIM       15                                                *I-125736
DSADD42   DIM       15
DSADD43   DIM       15
DSPOST1   DIM       4
DSPOST2   DIM       4
DSPOST3   DIM       4
DTSRCH    DIM       3
.
ENDNAM    DIM       20
FORM3     FORM      3             * Number of labels to print
.
LABLEFT   FORM      3
NOVALID   FORM      1
PRIMFLG   DIM       1
.
SEC       FORM      2
STARTNM   DIM       20
STATFLG   FORM      1             * Doc.'s Status Flag: 0=Active Only
.                                                       1=Both Active & Inactive
VERT      FORM      2
WDOCLAB   FORM      1             * Number of labels across page
.
. ------ <Constants Declaration> ------
.
CODEDT    INIT      "DT"
X99       FORM      "9999"
XXX       INIT      "XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX"
.
PRGID     INIT      "IBAPAT80"
PRGNAM    INIT      "PRINT DOCTOR LABELS"
VERSION   INIT      "V12.01.00"
+
.
.         DISPLAY SCREEN HEADER AND OPEN FILES
.
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening ibacodef";
          OPEN      IBACODE1,"ibacodef"
          CLOSE     IBACODE1
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.            
.            READ CONTROL FILE
.
          READ      CONTROLF,TEN;*148,CDOCLAB,*193,CTYPLAB
          READ      CONTROLF,TEN3;*201,CNDOCLB,*212,CDOCSLB
          READ      CONTROLF,TEN4;*232,CDOCLLN
.
          COMPARE   ZERO,CDOCLLN
          GOTO      DSPHD IF NOT EQUAL
          MOVE      EIGHT,CDOCLLN
.
DSPHD     SUB       FIVE,CDOCLLN            * Allow for lines always printed
.
          COMPARE   ZERO,CDOCLAB
          GOTO      NXTTST IF NOT EQUAL
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** No Label information in System Parameter **    ";
          CALL      HITENTER
          GOTO      ENDIT
.
NXTTST    COMPARE   FOUR,CDOCLAB
          GOTO      SELPRTR IF LESS
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Too many labels ACROSS page in System Parameter **  ";
          CALL      HITENTER
          GOTO      ENDIT
.
SELPRTR   CALL      SELPRINT                * select output to printer/spoolfile
.
.         MAIN  MENU
.
BEGIN     CALL      MAINSCRN
          GOTO      START
.
MAINSCRN  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Line-up Print":
                    *P1:6,*V2LON,TWO,*HOFF," = Doctor Code":
                    *P1:7,*V2LON,THREE,*HOFF," = Doctor Type   ":
                    *P1:8,*V2LON,FOUR,*HOFF," = All Doctors":
                    *P1:9,*V2LON,FIVE,*HOFF," = Surname Sequence":
                    *P1:11,"Select Option :";
          RETURN
+
.
.         VALIDATE OPTION SELECTION AND INITIALIZATION
.
START     KEYIN     *P17:11,*EF,*DV,UNDLN1,*P17:11,*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
          DISPLAY   *P17:11,*EL,*V2LON,OPTION;
.
          MOVE      CDOCLAB TO WDOCLAB
          BRANCH    OPTION OF LINEUP,KDOCT,KTYPE,KALLDOCT,KSURSEQ
          BEEP
          GOTO      START
+
.
.         LINE-UP PRINT
.
LINEUP    MOVE      XXX,DSNAME
          MOVE      XXX,DSADD11
          MOVE      XXX,DSADD21
          MOVE      XXX,DSADD31
          MOVE      XXX,DSADD41                                       *I-125736
          MOVE      XXX,DSADD12
          MOVE      XXX,DSADD22
          MOVE      XXX,DSADD32
          MOVE      XXX,DSADD42                                       *I-125736
          MOVE      XXX,DSADD13
          MOVE      XXX,DSADD23
          MOVE      XXX,DSADD33
          MOVE      XXX,DSADD43                                       *I-125736
          MOVE      XXX,DIM3 
          MOVE      X99,DSPOST1
          MOVE      X99,DSPOST2
          MOVE      X99,DSPOST3
          MOVE      XXX,ANS
          PACK      DNAME1 WITH DIM3,SP1,ANS,DOT,SP1,DSNAME
          MOVE      DNAME1,DNAME2
          MOVE      DNAME1,DNAME3
.
          DISPLAY   *P1:24,*EL,*P26:24,*V2LON:
                    "** Generating Line-up Print **",*W2;
.
          CALL      OPNPRINT
          CALL      GETSCR00 
          CALL      PRTDOCT
          CALL      CLSPRINT
          CALL      PUTSCR00
.
          DISPLAY   *P1:24,*EL,*P26:24,*V2LON:
                    "** Line-up Print Completed **",*W2;
          GOTO      START
+
.
.         Print labels for selected doctor codes
.
KDOCT     DISPLAY   *P1:15,*EF,"Doctor Code   :":
                    *P50:15,"( ",*V2LON,"?",*HOFF," for Doctors scan )";
          KEYIN     *P17:15,*DV,UNDLN6,*P17:15,*V2LON,DCODE;
          RESET     DCODE
          GOTO      START IF EOS  
.
          ENDSET    DCODE
          APPEND    SP6,DCODE
          RESET     DCODE
.
          MATCH     SP6,DCODE
          GOTO      START IF EQUAL
          DISPLAY   *P17:15,*EL,*V2LON,DCODE;
.
          CMATCH    QUEST,DCODE
          GOTO      KDOCT1 IF NOT EQUAL
          CALL      PATDOCDS
          MOVE      SP6,DCODE
          CALL      MAINSCRN
          DISPLAY   *P17:11,*V2LON,OPTION;
          GOTO      KDOCT
.
KDOCT1    MOVE      DCODE,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD OF INVLDOC
.
.         Find the end of the doctors title
.
          ENDSET    DTITL
.
NEXT70    CMATCH    SP1,DTITL
          GOTO      NEXT71 IF NOT EQUAL
          BUMP      DTITL,-1
          GOTO      NEXT71 IF EOS
          GOTO      NEXT70
.
NEXT71    LENSET    DTITL
          RESET     DTITL
.
          MOVE      DGNAME,ANS
          COMPARE   THREE,CTYPLAB
          GOTO      NEXT72 IF NOT EQUAL
.
          CALL      GIVNAM
          MATCH     SP1,ANS1
          GOTO      NEXT72 IF EQUAL
.
.         Display the initial of second given name
.
          DISPLAY   *P30:15,DTITL,SP1,ANS,DOT,SP1,ANS1,DOT,SP1,DSNAME;
          GOTO      REKEY1
.
NEXT72    DISPLAY   *P30:15,DTITL,SP1,ANS,DOT,SP1,DSNAME;
.
REKEY1    CALL      CONTQST
          BRANCH    CEXIT,PRNTD,KDOCT,START
          GOTO      REKEY1
.
.         Get the number of labels to print
.
PRNTD     MOVE      CDOCLAB TO WDOCLAB
          MOVE      ZERO,COUNTER
.
          CALL      KEYLAB
          BRANCH    NOVALID TO KDOCT
          COMPARE   WDOCLAB,FORM3
          GOTO      PRNTD1 IF NOT LESS
          MOVE      FORM3,WDOCLAB
.
.         Put the labels into the buffer
. 
PRNTD1    MOVE      FORM3,LABLEFT
          DISPLAY   *P1:24,*EL,*P28:24,*V2LON,"** Generating Label(s) **",*W;
          CALL      OPNPRINT
.
PRNTD2    CALL      SAVEPRT
          SUB       ONE,LABLEFT
.
          COMPARE   ZERO,LABLEFT
          GOTO      PRNTD2 IF NOT EQUAL
          DISPLAY   *P1:24,*EL,*P24:24,*V2LON:
                    "** Labels Generation Completed **",*W;
          CALL      GETSCR00
          CALL      CLSPRINT
          CALL      PUTSCR00
          GOTO      KDOCT
.
.         Invalid Doctor
.
INVLDOC   DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid Doctor Code **     ";
          CALL      HITENTER
          GOTO      KDOCT
.
.         Print labels for doctors with a particular Doctor Type
.
KTYPE     MOVE      CODEDT,CODE
          MOVE      "17",ECOL
          MOVE      "15",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          DISPLAY   *P1:15,*EF,"Doctor Type   : ";
          CALL      PATCODKY
          BRANCH    EXIT,START,START
          DISPLAY   *P25:15,TDESC;
.
.KTYPE     MOVE      SP3,DTSRCH
.          KEYIN     *P1:15,*EF,"Doctor Type   : ___",*P17:15,*V2LON,DTSRCH;
.          ENDSET    DTSRCH
.          APPEND    SP3 TO DTSRCH
.          RESET     DTSRCH
..
.          MATCH     SP3 TO DTSRCH
.          GOTO      START IF EQUAL
.          DISPLAY   *P17:15,*EL,*V2LON,DTSRCH;
..
.          MOVE      SP25,TDESC
.          PACK      KEY5 WITH CODEDT,DTSRCH
.          CALL      RDCODE1
.          BRANCH    OVRCD OF NOTYPE
.          DISPLAY   *P25:15,TDESC;
          GOTO      KPRIM
.
NOTYPE    DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid Doctor Type **     ";
          CALL      HITENTER
          GOTO      KTYPE
.
KPRIM     KEYIN     *P1:17,*EL,"Primary Doctor Type Only (",*V2LON,"Y":
                           *HOFF,"/",*V2LON,"N",*HOFF,") ? ",*V2LON,PRIMFLG;
          RESET     PRIMFLG
          GOTO      KPRIM1 IF EOS
.
          MATCH     "Y",PRIMFLG
          GOTO      REKEY2 IF EQUAL
.
          MATCH     "N",PRIMFLG
          GOTO      REKEY2 IF EQUAL
.
KPRIM1    DISPLAY   *P1:24,*EL,*B,*V2LON,"** You must answer (Y/N) **     ";
          CALL      HITENTER
          GOTO      KPRIM 
.
REKEY2    CALL      CONTQST
          BRANCH    CEXIT,PRNTT,KTYPE,START
          GOTO      REKEY2
.
PRNTT     CALL      CHKSTAT
.
          MOVE      CDOCLAB TO WDOCLAB
          MOVE      ZERO,COUNTER
.
          CALL      KEYLAB
          BRANCH    NOVALID TO BEGIN
.
          DISPLAY   *P1:24,*EL,*V2LON,"** Generating Labels **",*HOFF:
                    *P38:24,"Doctor :",*P60:24,"Scanning :";
          CALL      OPNPRINT
.
          MOVE      SP6,KEY6
          CALL      RDSDOCT1
.
NEXTT     MOVE      FORM3,LABLEFT
          CALL      RDKDOCT1
          BRANCH    OVRCD OF ENDT
.
          DISPLAY   *P71:24,DCODE;
          BRANCH    STATFLG,NEXTTA          * print both active & inactive docs.
.
.         we are only concerned with active doctors
.
          COMPARE   ONE TO DRSTAT           * 0 = Active
          GOTO      NEXTT IF EQUAL            1 = Non-active
.                                             2 = Active outside hospital
.
NEXTTA    BRANCH    ACTOTFLG,NEXTTB         * active outside hospital
          IF        DRSTAT = 2
            GOTO      NEXTT
          ENDIF
.
NEXTTB    PACK      DTSRCH,ACODE,SP6
          MATCH     DTSRCH,DRTYPE
          GOTO      PRNTTYP IF EQUAL
.
.         Get additional Doctor Types    (M.H. 12/1/87)
.
          MATCH     "Y",PRIMFLG
          GOTO      NEXTT IF EQUAL
.
          MOVE      DCODE,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD OF NEXTT
.
          MATCH     DTSRCH,DRTYPE2
          GOTO      PRNTTYP IF EQUAL
.
          MATCH     DTSRCH,DRTYPE3
          GOTO      PRNTTYP IF EQUAL
.
          MATCH     DTSRCH,DRTYPE4
          GOTO      PRNTTYP IF EQUAL
.
          MATCH     DTSRCH,DRTYPE5
          GOTO      NEXTT IF NOT EQUAL
.
PRNTTYP   DISPLAY   *P47:24,*V2LON,DCODE;
.
          CALL      SAVEPRT  
          SUB       ONE FROM LABLEFT
.
          COMPARE   ZERO TO LABLEFT
          GOTO      PRNTTYP IF NOT EQUAL
          GOTO      NEXTT
.
ENDT      CALL      GETSCR00
          CALL      PRTLST
          CALL      CLSPRINT
          CALL      PUTSCR00
          GOTO      KTYPE
+
.
.         Determine whether to print inactive doctors as well
.
CHKSTAT   DISPLAY   *P1:19,*EF,"Print active doctors only (",*V2LON,"Y":
                           *HOFF,"/",*V2LON,"N",*HOFF,") ? ";
.
CHKSTAT1  KEYIN     *P35:19,*EL,*V2LON,ANS;
          RESET     ANS
          GOTO      CHKSTAT2 IF NOT EOS
          MOVE      "Y",ANS
          GOTO      CHKSTAT3
.
CHKSTAT2  REP       UPPLOW,ANS
.
CHKSTAT3  DISPLAY   *P35:19,*V2LON,ANS;
.
          MATCH     "Y",ANS
          GOTO      CHKSTAT4 IF EQUAL
          MATCH     "N",ANS
          GOTO      CHKSTAT5 IF EQUAL
          BEEP
          GOTO      CHKSTAT1
.
CHKSTAT4  MOVE      ZERO,STATFLG
          GOTO      CHKSTAT9
.
CHKSTAT5  MOVE      ONE,STATFLG
.
CHKSTAT9  
+
.             Suppress active outside doctors ?
.
ACTOTDOC  DISPLAY   *P1:20,*EL,"Suppress the printing of active outside doctors (":
                           *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ? ";
.
ACTOTDO1  MOVE      ZERO,ACTOTFLG
          KEYIN     *P57:20,*EL,"_",*P57:20,*V2LON,ANS;
          RESET     ANS
          GOTO      ACTOTDO3 IF EOS
.
          REP       UPPLOW,ANS
.
          MATCH     "Y",ANS
          GOTO      ACTOTDO3 IF EQUAL
          MATCH     "N",ANS
          GOTO      ACTOTDO2 IF EQUAL
          BEEP
          GOTO      ACTOTDO1
.
ACTOTDO2  DISPLAY   *P57:20,*V2LON,"N";
          MOVE      ONE,ACTOTFLG
          GOTO      ACTOTDO4
.
ACTOTDO3  DISPLAY   *P57:20,*V2LON,"Y";
.
.
ACTOTDO4  RETURN
+
.
.         Print labels for all doctors
.
KALLDOCT  CALL      CHKSTAT
.
          MOVE      ZERO,COUNTER
          CALL      KEYLAB
          BRANCH    NOVALID TO START
.
          DISPLAY   *P1:24,*EL,*V2LON,"** ",*BLINKON,"Generating Labels",*HOFF:
                    *V2LON," **",*HOFF,*P55:24,"Doctor  :";
          CALL      OPNPRINT
.
          MOVE      SP6,KEY6
          CALL      RDSDOCT1
.
NEXTD     CALL      RDKDOCT1
          BRANCH    OVRCD OF ENDP
.
          DISPLAY   *P66:24,*V2LON,DCODE;
          BRANCH    STATFLG,NEXTDA          * print both active & inactive docs.
.
.         we are only concerned with active doctors
.
          COMPARE   ONE TO DRSTAT           * 0 = Active
          GOTO      NEXTD IF EQUAL            1 = Non-active
.                                             2 = Active outside hospital
NEXTDA    BRANCH    ACTOTFLG,NEXTDB         * active outside hospital
          IF        DRSTAT = 2
            GOTO      NEXTD
          ENDIF
.
NEXTDB    MOVE      FORM3,LABLEFT
.
NEXT10    CALL      SAVEPRT  
          SUB       ONE FROM LABLEFT
.
          COMPARE   ZERO TO LABLEFT
          GOTO      NEXT10 IF NOT EQUAL
          GOTO      NEXTD
.
ENDP      CALL      GETSCR00 
          CALL      PRTLST
          CALL      CLSPRINT
          CALL      PUTSCR00 
          GOTO      START
+
.
.         Print labels for surname sequence
.
KSURSEQ   MOVE      SP30 TO STARTNM
.
          DISPLAY   *P1:15,*EF,"Surname From  : ":
                    *P1:17,"Surname To    : ";
.
          KEYIN     *P17:15,*EL,*DV,UNDLN20,*P17:15,*V2LON,STARTNM;
          CMATCH    QUEST,STARTNM
          GOTO      NOSUR1 IF EQUAL
.         
          RESET     STARTNM
          GOTO      KSURSEQ1 IF EOS
          DISPLAY   *P17:15,*EL,*V2LON,STARTNM;
          GOTO      KSURSEQ2
.
KSURSEQ1  MOVE      SP30,STARTNM
          DISPLAY   *P17:15,*EL,*V2LON,"Start";
.
KSURSEQ2  KEYIN     *P17:17,*EL,*DV,UNDLN20,*P17:17,*V2LON,ENDNAM;
          CMATCH    QUEST,ENDNAM 
          GOTO      NOSUR2 IF EQUAL
.         
          RESET     ENDNAM
          GOTO      KSURSEQ3 IF EOS
          DISPLAY   *P17:17,*EL,*V2LON,ENDNAM;
.
          MATCH     STARTNM TO ENDNAM
          GOTO      REKEY3 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Surname To must be > or equal to Surname From **   ";
          CALL      HITENTER
          GOTO      KSURSEQ2
.
KSURSEQ3  MOVE      "zzzzzz" TO ENDNAM
          DISPLAY   *P17:17,*EL,*V2LON,"Finish";
.
REKEY3    CALL      CONTQST
          BRANCH    CEXIT,PRNSQ,KSURSEQ,START
          GOTO      REKEY3
.
PRNSQ     CALL      CHKSTAT
.
          MOVE      ZERO,COUNTER
          CALL      KEYLAB
          BRANCH    NOVALID TO START
.
          DISPLAY   *P1:24,*EL,*V2LON,"** ",*BLINKON,"Generating Labels",*HOFF:
                    *V2LON," **",*HOFF,*P45:24,"Surname :";
          CALL      OPNPRINT
.
          PACK      KEY26 WITH STARTNM,SP30
          CALL      RDSDOCT2
.
NEXSQ     CALL      RDKDOCT2
          BRANCH    OVRCD OF ENDSQ
.
          DISPLAY   *P55:24,*V2LON,DSNAME;
          BRANCH    STATFLG,NEXSQA          * print both active & inactive docs.
.
.         we are only concerned with active doctors
.
          COMPARE   ONE TO DRSTAT           * 0 = Active
          GOTO      NEXSQ IF EQUAL            1 = Non-active
.                                             2 = Active outside hospital
NEXSQA    BRANCH    ACTOTFLG,NEXSQB         * active outside hospital
          IF        DRSTAT = 2
            GOTO      NEXSQ
          ENDIF
.
NEXSQB    MOVE      FORM3,LABLEFT
.
          MATCH     ENDNAM,DSNAME
          GOTO      PRNTSEQ IF EQUAL
          GOTO      PRNTSEQ IF LESS
          GOTO      ENDSQ IF NOT LESS
.
PRNTSEQ   CALL      SAVEPRT
          SUB       ONE FROM LABLEFT
.
          COMPARE   ZERO TO LABLEFT
          GOTO      PRNTSEQ IF NOT EQUAL
          GOTO      NEXSQ
.
.         Invalid Surname
.
NOSUR1    DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid Surname **     ";
          CALL      HITENTER
          GOTO      KSURSEQ
.
NOSUR2    DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid Surname **     ";
          CALL      HITENTER
          GOTO      KSURSEQ2
.
ENDSQ     CALL      GETSCR00 
          CALL      PRTLST
          CALL      CLSPRINT
          CALL      PUTSCR00
          GOTO      START
+
.         Put the current doctor into the print buffer
.
SAVEPRT   ADD       ONE,COUNTER
          MOVE      DGNAME,ANS
.
          BRANCH    CTYPLAB OF SAVPRT,SAVPRT,SAVPRT1,SAVPRT,SAVPRT
.
SAVPRT    MOVE      SP1,ANS1
          GOTO      SAVPRT2
.
SAVPRT1   CALL      GIVNAM
.
SAVPRT2   ENDSET    DTITL
.
NEXT80    CMATCH    SP1,DTITL
          GOTO      NEXT81 IF NOT EQUAL
          BUMP      DTITL BY -1
          GOTO      NEXT80 IF NOT EOS
.
NEXT81    LENSET    DTITL
          RESET     DTITL
.
          BRANCH    COUNTER OF SAVE1,SAVE2,SAVE3
.
.         Print the initial of second given name
.
SAVE1     MATCH     SP1,ANS1
          GOTO      SAVE1A IF EQUAL
.
          PACK      DNAME1 WITH DTITL,SP1,ANS,DOT,SP1,ANS1,DOT,SP1,DSNAME
          GOTO      SAVE1B
.
SAVE1A    PACK      DNAME1 WITH DTITL,SP1,ANS,DOT,SP1,DSNAME
.
SAVE1B    MOVE      DSADD1 TO DSADD11
          MOVE      DSADD2 TO DSADD21
          MOVE      DSADD3 TO DSADD31
          MOVE      DSADD4 TO DSADD41                                 *I-125736
          MOVE      DSPOST TO DSPOST1
          GOTO      PRTOK
.
SAVE2     MATCH     SP1,ANS1
          GOTO      SAVE2A IF EQUAL
.
          PACK      DNAME2 WITH DTITL,SP1,ANS,DOT,SP1,ANS1,DOT,SP1,DSNAME
          GOTO      SAVE2B
.
SAVE2A    PACK      DNAME2 WITH DTITL,SP1,ANS,DOT,SP1,DSNAME
.
SAVE2B    MOVE      DSADD1 TO DSADD12
          MOVE      DSADD2 TO DSADD22
          MOVE      DSADD3 TO DSADD32
          MOVE      DSADD4 TO DSADD42
          MOVE      DSPOST TO DSPOST2
          GOTO      PRTOK
.
SAVE3     MATCH     SP1,ANS1
          GOTO      SAVE3A IF EQUAL
.
          PACK      DNAME3 WITH DTITL,SP1,ANS,DOT,SP1,ANS1,DOT,SP1,DSNAME
          GOTO      SAVE3B
.
SAVE3A    PACK      DNAME3 WITH DTITL,SP1,ANS,DOT,SP1,DSNAME
.
SAVE3B    MOVE      DSADD1 TO DSADD13
          MOVE      DSADD2 TO DSADD23
          MOVE      DSADD3 TO DSADD33
          MOVE      DSADD4 TO DSADD43
          MOVE      DSPOST TO DSPOST3
.
PRTOK     COMPARE   COUNTER,WDOCLAB
          RETURN    IF NOT EQUAL
          CALL      PRTDOCT
          RETURN
+
.
PRTLST    COMPARE   ZERO,COUNTER
          RETURN    IF EQUAL
.
          MOVE      COUNTER,WDOCLAB
          CALL      PRTDOCT
          DISPLAY   *P1:24,*EL,*P24:24,*V2LON:
                    "** Labels Generation Completed **",*W2;
          RETURN
+
.
GIVNAM    MOVE      SP1,ANS1
          MOVE      SP30,DIM30
          MOVE      DGNAME,DIM30
.
          MATCH     SP30,DIM30
          RETURN    IF EQUAL
          RESET     DIM30
.
GIVNAM1   BUMP      DIM30,1
          RETURN    IF EOS
          CMATCH    SP1,DIM30
          GOTO      GIVNAM1 IF NOT EQUAL
.
GIVNAM2   BUMP      DIM30,1
          RETURN    IF EOS
          CMATCH    SP1,DIM30
          GOTO      GIVNAM2 IF EQUAL
          CMOVE     DIM30,ANS1
          RETURN
+
.
PRTDOCT   BRANCH    WDOCLAB OF LABEL1,LABEL2,LABEL3
          RETURN
.
LABEL1    PRINT     *N,*1,DNAME1:
                    *N,*1,DSADD11:
                    *N,*1,DSADD21:
                    *N,*1,DSADD31:
                    *N,*1,DSADD41,DSPOST1,*N
          GOTO      PRTLBEND
.
LABEL2    COMPARE   ONE,CDOCSLB
          GOTO      LABEL22 IF NOT EQUAL
.
          PRINT     *N,*1,DNAME1,*37,DNAME2,*N:
                    *1,DSADD11,*37,DSADD12,*N:
                    *1,DSADD21,*37,DSADD22,*N:
                    *1,DSADD31,*37,DSADD32,*N:
                    *1,DSADD41,DSPOST1,*37,DSADD42,DSPOST2,*N
          GOTO      PRTLBEND
.
LABEL22   PRINT     *N,*1,DNAME1,*42,DNAME2,*N:
                    *1,DSADD11,*42,DSADD12,*N:
                    *1,DSADD21,*42,DSADD22,*N:
                    *1,DSADD31,*42,DSADD32,*N:
                    *1,DSADD41,DSPOST1,*42,DSADD42,DSPOST2,*N
          GOTO      PRTLBEND
.
LABEL3    COMPARE   ONE,CDOCSLB
          GOTO      LABEL32 IF NOT EQUAL
.
          PRINT     *N,*1,DNAME1,*37,DNAME2,*73,DNAME3,*N:
                    *1,DSADD11,*37,DSADD12,*73,DSADD13,*N:
                    *1,DSADD21,*37,DSADD22,*73,DSADD23,*N:
                    *1,DSADD31,*37,DSADD32,*73,DSADD33,*N:
                    *1,DSADD41,DSPOST1:
                    *37,DSADD42,DSPOST2,*73,DSADD43,DSPOST3,*N
          GOTO      PRTLBEND
.
LABEL32   PRINT     *N,*1,DNAME1,*42,DNAME2,*84,DNAME3,*N:
                    *1,DSADD11,*42,DSADD12,*84,DSADD13,*N:
                    *1,DSADD21,*42,DSADD22,*84,DSADD23,*N:
                    *1,DSADD31,*42,DSADD32,*84,DSADD33,*N:
                    *1,DSADD41,DSPOST1:
                    *42,DSADD42,DSPOST2,*84,DSADD43,DSPOST3,*N
.
PRTLBEND  MOVE      ZERO,COUNTER
.
          BRANCH    CDOCLLN OF LEND1,LEND2,LEND3,LEND4
          RETURN
.
LEND1
...       PRINT     *N;                                                D-125736
          RETURN
.
LEND2     PRINT     *N;                     * was 2 x "*N"            *C-125736
          RETURN
.
LEND3     PRINT     *N,*N;                  * was 3 x "*N"            *C-125736
          RETURN
.
LEND4     PRINT     *N,*N,*N;               * was 4 x "*N"            *C-125736
          RETURN
+
.
.         Check the number of labels to be printed
.
KEYLAB    MOVE      ZERO TO NOVALID
          MOVE      CNDOCLB TO FORM3
.
KEYLAB1   KEYIN     *P1:24,*EL,"Printing ",*V2LON,*DV,CNDOCLB,*HOFF:
                    " labels per doctor (",*V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
.
          MATCH     "N",ANS
          GOTO      KEYLAB2 IF EQUAL
          MATCH     "Y",ANS
          RETURN    IF EQUAL
          BEEP
          GOTO      KEYLAB1
.
KEYLAB2   KEYIN     *P1:24,*EL,"Enter the number of labels to be printed : ":
                    *V2LON,FORM3;
.
          COMPARE   ONE,FORM3
          RETURN    IF NOT LESS
          MOVE      ONE TO NOVALID
          RETURN
+
.
ENDIT     CHAIN     PGM
          STOP
+
.==============================================================================
.
          INCLUDE   STD001IO
          INCLUDE   IBAPRINT
.
          INCLUDE   PATDOCDS
          INCLUDE   PATCODKY
.  
          INC       IBAPRNIO/INC
.
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
+

.
. *************************************************************************** 
. * System    :   OUTPATIENTS                                               * 
. * Program   :   IBAOUT46                                                  * 
. * Desc      :   A & E Referral Source Report                              * 
. *************************************************************************** 
. * Date      :   21/03/89                                                  * 
. * Author    :   Eric Phan                                                 * 
. * Function  :   This program prints all patients grouped into sources     *
. *               of referral for a date range.                             *
. *       V12.00.01  23/05/2025  Bella Turco     TSK 0955096                *
. *                  Alphanumeric changes                                   *
. *       V10.05.01  15/07/2014  Ebon Clements   CAR 261630                 *
. *                  Removed port number from temp file name                *
. *       V5.08.01  29/08/2000  Caleb Sharman                               *
. *                 Changed Health Fund variables to be 8 chas              *
. *           V5.08.B01 13/03/2000  Steve Armstrong   5.08 Mods             *
. *                     Mods for changes to OUTCLIFD (effective date)       *
. ***************************************************************************
. *           V5.07.B02 09/12/1998  Jim Stathopoulos                        *
. *                     Fixed Generated Date                                *
. *           V5.07.B01 30/10/1998  Jim Stathopoulos                        *
. *                     507 Changes                                         *
. ***************************************************************************
. *               V5.01 13/04/89 J.Tan   (I.B.A.)                           *
. *                     Mods Names order to be surname,given name,title     *
. *                     SRF 100096                                          *
. *               V5.02 22/04/89 J.Tan    (I.B.A.)                          *
. *                     Mods in surname alphabetical order                  *
. *                     SRF 12                                              *
. *               V5.03 03.06.89 S.Barcham              SRF 100751          *
. *                     Split OUTBOK & OUTMAS includes                      *
. *               V5.04 05.06.89 S.Barcham              SRF 100809          *
. *                     Recompiled for locking in PATIOMAS                  *
. *               V5.05 24.06.89 S.Barcham                                  *
. *                     Recompiled for OUTBB1FD                             *
. *            V5.01.01 16/07/89 K.Peak                                     *
. *                     Changed Adm & U/R to 8 characters                   *
. *            V5.01.02 15/09/89 K.Peak                                     *
. *                     Fixed I * L 00702                                   *
. *            V5.01.03 17/01/90 K.Peak                                     *
. *                     Fixed I * L 07274   SRF 103655                      *
. *            V5.01.04 18/01/90 K.Peak                                     *
. *                     Fixed I * D 07267 & the printing of A&E date        *
. *                     SRF 103700                                          *
. *            V5.01.05 11/07/90 Paul Duncan                                *
. *                     recomp for OUTBOA & OUTBOB audits                   *
. *            V5.01.121 27/11/90 Glen Hobby                                *
. *                      Recompiled for changes to                          *
. *                      PATMX1FD                                           *
. *       V5.01.122 24/06/1993  Graeme Williams            SRF 123217       *
. *                 Correct relevant opens to use site prefix, not "out"    *
. *       V5.01.123 13/01/1994  Sandra Barcham                              *
. *                 fixed global recompile errors                           *
. ***************************************************************************
.
            INC       STD001FD
.
            INC       AAEDE1FD/INC
            INC       AAEDEBFD/INC
            INC       IBASEQFD/INC
            INC       OUTBOAFD/INC
            INC       OUTBB1FD/INC
            INC       OUTCONFD/INC
            INC       OUTCLIFD/INC
            INC       OUTSITFD/INC
            INC       PATCODFD/INC
            INC       PATMA1FD/INC

            INC       PATPR1FD/INC
            INC       PATDO1FD/INC
            INC       TFILEVAR/INC
            INC       WEBERRFD/INC
.
DCENT       DIM       2
DYEAR       DIM       2
DMONTH      DIM       2
DDAY        DIM       2
.
CMDLINE     DIM       50
CODES       INIT      "S "   * Source of Referral
CODECL      INIT      "CL"   * Compensation Eligibility
.
DIM10       DIM       10
.
FIRSTREC    FORM      1    * 1=first record  0=not first record
FROMDATE    DIM       8
.
.
.
OUTTM1      DIM       8
OUTTM1XX    IFILE     SQL, FIXED=114, KEY="u1-6,7-26,27-34"
.
PATDESC     DIM       20
PATADDR     DIM       44
PNTITLE     DIM       4
PNGNAME     DIM       25
PNSNAME     DIM       30
PNFNAME     DIM       20
.
SAVCLIN     DIM       6
SAAENUMB    DIM       8
SAAEDATE    DIM       8
SAAEDAT1    DIM       10
SAAEDAT2    DIM       8
.
.--------------------------------------------------------------------------
.           Temporary File Layout
.
.Name       Type      Length      Physical      Start
.----       ----      ------      --------      -----
.OBACLIN    DIM            6             6          1
.PATDESC    DIM           20            20          7
.DOBAOUTN   DIM            8             8         27
.OBAURNO    DIM            8             8         35
.SAAENUMB   DIM            8             8         43
.SAAEDATE   DIM            8             8         51
.OBACOMP    DIM            3             3         59
.PATADDR    DIM           44            44         62
.DCENT      DIM            2             2        105
.DYEAR      DIM            2             2        108
.DMONTH     DIM            2             2        110
.DDAY       DIM            2             2        112
.
.End of Record                                    114
.--------------------------------------------------------------------------
Z20       INIT      "zzzzzzzzzzzzzzzzzzzz"
.
PRGNAM    INIT      "A & E Referral Source Report"
PRGID       INIT      "IBAOUT46"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              MAINLINE                                  *
.*                            Controlling Logic                           *
.**************************************************************************
.
            CALL      INIT0000               * initialization and open files
.
ML0100      CALL      OPTN0000               * select options
            BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
.
            CALL      PROC0000               * Process DTRAN file
            BRANCH    FIRSTREC,ML9500        * If set, no records found
.
            CALL      PRT0000                * Print processed records
.
            GOTO      ML0100
.
.           No records found in processing pass ...
.
ML9500      DISPLAY   *P30:24,*EL,*B,*V2LON,"*** No records found ***",*W;
            GOTO      ML0100
.
.--------------------------------------------------------------------------
.          The end ...
.--------------------------------------------------------------------------
.
ML9999     CALL       KILL1000
           CHAIN      PGM                    * chain back to program
+
.------------------------------------------------------------------------------
.          Create temp file - if it exists then delete first
.------------------------------------------------------------------------------
.
CREA0000   
           CLOSE      OUTTM1XX
           CALL       KILL1000         * Remove OUTTM1xx
           PACK       CMDLINE,SP30,SP30
           CLEAR      CMDLINE
           APPEND     "isbuild ",CMDLINE
           APPEND     OUTTM1,CMDLINE
           APPEND     " 114 u1-6,7-26,27-34",CMDLINE
           RESET      CMDLINE
           EXECUTE    CMDLINE,TASKID
           OPEN       OUTTM1XX,OUTTM1
.
           RETURN
.
.          Delete 1st index - OUTTM1xx
.
KILL1000   CLOSE      OUTTM1XX
           PACK       CMDLINE,SP30,SP30
           CLEAR      CMDLINE
           APPEND     "iserase ",CMDLINE
           APPEND     OUTTM1,CMDLINE
           RESET      CMDLINE
           EXECUTE    CMDLINE,TASKID
           RETURN
+
.****************************************************************************
.*                                INIT0000                                  *
.*                             initialization                               *
.****************************************************************************
.
INIT0000   CALL       DISPHEAD
.
           DISPLAY    *P56:24,"Opening controlf";
           OPEN       CONTROLF,"controlf"
           READ       CONTROLF,THIRTY1;*98,HOAAESCD
           CLOSE      CONTROLF
.
           MOVE       UID,OSTFILE
.
           DISPLAY    *P64:24,"aaede1af";
           OPEN      AAEDE1A2,"aaede1af"
.
           DISPLAY    *P64:24,"patcodes";
           OPEN       PATCODE1,"patcodes"
.
           DISPLAY    *P64:24,"patdo1af";
           OPEN      PATDO1A1,"patdo1af"
.
           DISPLAY    *P64:24,"patma1af";
           OPEN      PATMA1A1,"patma1af"
.
           DISPLAY    *P64:24,"patmx1af";
           OPEN      PATMX1A1,"patmx1af"
.
           DISPLAY    *P64:24,"patpramf";
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
.
           PACK       CFNAME,UID,FILBOKA1
           DISPLAY    *P64:24,CFNAME;
           OPEN       OUTBOKA1,CFNAME
.
           PACK       CFNAME,UID,FILBB1A1
           DISPLAY    *P64:24,CFNAME;
           CALL       OPOTBB11
.
           PACK       CFNAME,UID,FILCLIA1
           DISPLAY    *P64:24,CFNAME;
           OPEN       OUTCLIA1,CFNAME
.
           CALL      TFILENAM                * Get temp file name
           MOVE      TFILNAME,OUTTM1
.
INIT9999   DISPLAY    *P1:24,*EL
           RETURN
+
.****************************************************************************
.*                                OPTN0000                                  *
.*                        get user options or exit                          *
.****************************************************************************
.
OPTN0000   MOVE       ONE,EXIT 
           DISPLAY    *P1:3,*EF: 
                      *P1:4,*V2LON,ZERO,*HOFF,". Exit": 
                      *P1:5,*V2LON,ONE,*HOFF,". Print Report" 
OPTN0500   KEYIN      *P1:7,*EL,"Please Select : ",*V2LON,OPTION 
.
           COMPARE    ZERO,OPTION
           GOTO       OPTN9999 IF EQUAL
           MOVE       ZERO,EXIT
           BRANCH     OPTION,OPTN2000
           BEEP
           GOTO       OPTN0500
.
.----------------------------------------------------------------------------
.          Keyin dates
.----------------------------------------------------------------------------
.
OPTN2000   UNPACK     CDATE,CDAY,ANS,CMON,ANS,CCENT,CYEAR
           MOVE       ZERO,CHIGHLT
           MOVE       TWENTY2,CCOL
           MOVE       TEN,CVERT
.
           DISPLAY    *P1:10,*EF,"Generate Report for "
           CALL       KEYDATE
           PACK       FROMDATE,CCENT,CYEAR,CMON,CDAY
           REP        " 0",FROMDATE
.
.----------------------------------------------------------------------------
.          Proceed ?
.----------------------------------------------------------------------------
.
OPTN4100   MOVE       SP1,ANS
           KEYIN      *P1:24,"Ok to proceed (":
                      *V2LON,*DV,ANSY,*HOFF,*DV,SLASH:
                      *V2LON,*DV,ANSN,*HOFF,") ? ",*V2LON,ANS
           CMATCH     ANSY,ANS
           GOTO       OPTN9999 IF EQUAL
           CMATCH     ANSN,ANS
           GOTO       OPTN0000 IF EQUAL
           BEEP
           GOTO       OPTN4100
.
OPTN9999   RETURN
+
.**************************************************************************
.*                              PROC0000                                  *
.*                        Process Transaction File                        *
.**************************************************************************
.
PROC0000    CALL      CREA0000               * Create/Reset temporary file
.
            MOVE      ONE,FIRSTREC           * Haven't read 1st record yet
            DISPLAY   *P1:12,*EF,*V2LON,"*** Processing Records ***",*HOFF:
                      *P1:14,"U/R Number        :":
                      *P1:15,"Outpatient Number :":
                      *P1:16,"A & E Number      :":
                      *P1:17,"A & E Date        :"
.
            PACK      KEY20,IDDD,SP20
PROC1000    CALL      RDSCLIA1
            CALL      RDKCLIA1
            BRANCH    OVRCD,PROC9000
.
            MATCH     IDDD,OCASITE
            GOTO      PROC9000 IF NOT EQUAL
.
.           Get all patients in this clinic for this date
.
            PACK      KEY28,OCASITE,OCACLIN,FROMDATE,SP30
            CALL      RDSBOKA1
.
PROC2000    CALL      RDKBOKA1
            BRANCH    OVRCD TO PROC9000     * No more booking records - exit
            MATCH     OBASITE,OCASITE
            GOTO      PROC9000 IF NOT EQUAL * Different site - exit
            MATCH     OBADATE,FROMDATE
            GOTO      PROC2500 IF NOT EQUAL * Different date - get next clinic
            BRANCH    OBASTAT,PROC3000      * Use only booked slots
            GOTO      PROC2000              * Ignore all others
.
PROC2500    PACK      KEY20,IDDD,OCACLIN,Z20
            GOTO      PROC1000
.
.           Check that Source of Referral is correct
.
PROC3000    PACK      KEY8,OBAOUTNO
            CALL      RDBOKB1
            BRANCH    OVRCD,PROC2000
            MATCH     OBASRCR,HOAAESCD
            GOTO      PROC2000 IF NOT EQUAL
.
.           Get last A&E visit from A&E details file
.
            MOVE      ZEROVISN,ADANUMB
            MOVE      SP8,SAAENUMB
            MOVE      SP8,ADADATE
            PACK      KEY16,OBAURNO,SP20
            CALL      RDSDETA2
PROC4000    CALL      RDKDETA2
            BRANCH    OVRCD,PROC4500
            MATCH     ADAURNO,OBAURNO
            GOTO      PROC4500 IF NOT EQUAL
            MOVE      ADANUMB,SAAENUMB       * Save relevant details
            MOVE      ADADATE,SAAEDATE
            GOTO      PROC4000
.
PROC4500    MATCH     SP8,SAAENUMB
            GOTO      PROC5000 IF NOT EQUAL
            MOVE      SP8,SAAENUMB       * Save relevant details
            MOVE      SP8,SAAEDATE
.
PROC5000    UNPACK    ADADATE,XCENT,XYEAR,XMON,XDAY
            DISPLAY   *P21:14,*V2LON,OBAURNO,*P21:15,OBAOUTNO:
                      *P21:16,ADANUMB,*P21:17,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
            MOVE      ZERO,FIRSTREC           * Got at least 1 record
.
            MATCH     SP8,OBAURNO
            GOTO      PROC7000 IF EQUAL      * No U/R 
.
.           Got U/R - read PATMA1A1
.
            MOVE      "UNKNOWN U/R NUMBER  ",PATDESC
            PACK      PATADDR,SP30,SP30
            MOVE      OBAURNO,KEY8
            CALL      RDMAST1
            BRANCH    OVRCD,PROC8000
            MOVE      PTITL,PNTITLE
            MOVE      PGNAME,PNGNAME
            MOVE      PSNAME,PNSNAME
            CALL      PATN0000
            PACK      PATDESC,PNFNAME,SP20   
            CALL      PACKADDP
.
            UNPACK    PBDATE,DCENT,DYEAR,DMONTH,DDAY
            GOTO      PROC8000
.
.           No U/R - read OUTBOKC1
.
PROC7000    MOVE      "UNKNOWN OUTP. NUMBER",PATDESC
            PACK      PATADDR,SP30,SP30
            MOVE      OBAOUTNO,KEY8
            CALL      RDPRAM1
            BRANCH    OVRCD,PROC8000
            MOVE      PTITL,PNTITLE
            MOVE      PGNAME,PNGNAME
            MOVE      PSNAME,PNSNAME
            CALL      PATN0000
            PACK      PATDESC,PNFNAME,SP20   
            CALL      PACKADDO
.
            UNPACK    PBDATE,DCENT,DYEAR,DMONTH,DDAY
.
PROC8000    PACK      KEY34,OBACLIN,PATDESC,OBAOUTNO
            CALL      RDATMP1
            BRANCH    OVRCD,PROC8500
            DISPLAY   *P1:23,*EL,*V2LON,"* ERROR *",*HOFF:
                      " Duplicate Outpatient - KEY34=[",*V2LON,KEY34,"]":
                      *HOFF 
            KEYIN     "  Please ring IBA ... ",ANS;
            GOTO      PROC2000
.
PROC8500    CALL      WRTMP1
            GOTO      PROC2000
.
PROC9000    RETURN
+
.**************************************************************************
.*                              PRT0000                                   *
.*                          Print Processed File                          *
.**************************************************************************
.
PRT0000     MOVE      "80",CLNO
            MOVE      ZERO,CPAGENO
            MOVE      SP10,SAVCLIN
.
            DISPLAY   *P1:12,*EF,*V2LON,"*** Printing Records ***",*HOFF:
                      *P1:14,"U/R Number        :":
                      *P1:15,"Outpatient Number :":
                      *P1:16,"A & E Number      :":
                      *P1:17,"A & E Date        :"
.
            PACK      KEY34,SP20,SP20
            CALL      RDSTMP1
.
PRT1000     CALL      RDKTMP1
            BRANCH    OVRCD,PRT9000         * No more records
.
            MOVE      SP10,SAAEDAT1
            MATCH     SP8,SAAEDATE
            GOTO      PRT1200 IF EQUAL
.
            UNPACK    SAAEDATE,XCENT,XYEAR,XMON,XDAY
            PACK      SAAEDAT1,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
.
PRT1200     DISPLAY   *P21:14,*V2LON,OBAURNO,*P21:15,OBAOUTNO:
                      *P21:16,SAAENUMB,*P21:17,SAAEDAT1
.
            MATCH     OBACLIN,SAVCLIN
            GOTO      PRT2000 IF EQUAL
.
            PACK      KEY20,IDDD,OBACLIN,SAAEDATE
            CALL      RDCLIA1
            IF        OVRCD = 1
              CALL      RDPCLIA1
              IF        OVRCD = 0
                MATCH     IDDD,OCASITE
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ELSE
                  MATCH     OBACLIN,OCACLIN
                  IF        !@EQUAL
                    MOVE      ONE,OVRCD
                  ENDIF
                ENDIF
              ENDIF
            ENDIF
            IF        OVRCD = 1
              MOVE      "Unknown clinic",OCADESC
              PACK      OCADESC,OCADESC,SP30
              GOTO      PRT1500
            ENDIF
.
            MATCH     SP6,OCADOCT
            GOTO      PRT1500 IF EQUAL
            PACK      KEY6,OCADOCT
            CALL      RDDOCT1
            MOVE      DTITL,PNTITLE
            MOVE      DGNAME,PNGNAME
            MOVE      DSNAME,PNSNAME
            CALL      PACK0000
            PACK      OCADESC,PNFNAME
.
PRT1500     CALL      PRNT5000              * Print page heading
            MOVE      OBACLIN,SAVCLIN
.
PRT2000     COMPARE   CLNO,FIFTY5
            GOTO      PRT1500 IF LESS
.
            MOVE      SP8,SAAEDAT2
            MATCH     SP8,SAAEDATE
            GOTO      PRT2500 IF EQUAL
.
            UNPACK    SAAEDATE,XCENT,XYEAR,XMON,XDAY
            PACK      SAAEDAT2,XDAY,SLASH,XMON,SLASH,XYEAR
.
PRT2500     UNPACK    SP30,DIM10,TDESC
            MATCH     SP3,OBACOMP
            GOTO      PRT3000 IF EQUAL
            PACK      KEY5,CODECL,OBACOMP
            CALL      RDCODE1
            MOVE      TDESC,DIM10
PRT3000     CALL      PRNT1000              * Print item
.
            GOTO      PRT1000               * Read next record
.
PRT9000     CALL      PRNT4000
            CALL      PRNT6000
            RETURN
+
.****************************************************************************
.*                                                                          *
.*                    Printing subroutines                                  *
.*                                                                          *
.****************************************************************************
.          Print Item 
.----------------------------------------------------------------------------
.
PRNT1000  PRINT    *1,"|",*3,OBAURNO,*12,"|",*14,OBAOUTNO,*23,"|",*25,PATDESC:
                   *46,"|",*48,DDAY,SLASH,DMONTH,SLASH,DYEAR,*57,"|":
                   *59,PATADDR,*104,"|",*106,SAAEDAT2,*115,"|":
                   *117,OBACOMP,SP1,DIM10,*132,"|"
          ADD      ONE,CLNO
          RETURN
.
.----------------------------------------------------------------------------
.         Print bottom of page
.----------------------------------------------------------------------------
.
PRNT4000  PRINT      *1,"|",*12,"|",*23,"|",*46,"|",*57,"|":
                     *104,"|",*115,"|",*132,"|":
                     *N,"*-------------------------------------------":
                        "--------------------------------------------":
                        "-------------------------------------------*"
          RETURN
.
.----------------------------------------------------------------------------
.         Print page heading
.----------------------------------------------------------------------------
.
PRNT5000  ADD        ONE,CPAGENO
          CLOCK      TIME,CTIMEIS
.
          COMPARE    ONE,CPAGENO
          GOTO       PRNT5100 IF EQUAL
.
          CALL       PRNT4000        * Print page separator
.
PRNT5100  PACK       KEY12,IDDD,OBACLIN
          PRINT      *F,PRGID,*40,CNAME,*118,"DATE : ",CDATE:
                     *N,VERSION,*40,PRGNAM,*118,"TIME : ",CTIMEIS:
                     *N,*40,"DATE REQUIRED : ";
          UNPACK     FROMDATE,CCENT,CYEAR,CMON,CDAY
          PRINT      CDAY,SLASH,CMON,SLASH,CCENT,CYEAR:
                        *118,"PAGE :      ",CPAGENO:
                     *N,*N,"CLINIC ID : ",OCACLIN,*25,OCADESC:
                     *N,"*-------------------------------------------":
                         "--------------------------------------------":
                         "-------------------------------------------*":
                      *N,"| U/R ##",*12,"| OUTPAT ##",*23,"| PATIENT NAME":
                         *46,"| D.O.B.",*57,"| ADDRESS":
                         *104,"| A&E DATE",*115,"|   COMP.ELIG.",*132,"|":
                      *N,"*-------------------------------------------":
                         "--------------------------------------------":
                         "-------------------------------------------*"
           MOVE       EIGHT,CLNO
           RETURN
.
.----------------------------------------------------------------------------
.          Print tail of report
.----------------------------------------------------------------------------
.
PRNT6000   PRINT      *N,*N,"*** END OF REPORT ***"
           RETURN
+
.------------------------------------------------------------------------------
.         Format a name string in the form TITLE GIVEN NAME SURNAME
.         Requires : IBACOMM.PBL
.                    Title in PNTITLE (DIM 4)
.                    Given name in PNGNAME (DIM 30)
.                    Surname in PNSNAME (DIM 30)
.         Output   : Formatted name in PNFNAME (DIM 30)
.------------------------------------------------------------------------------
PACK0000  MOVE      SP30,PNFNAME
          RESET     PNSNAME
          RESET     PNGNAME
          RESET     PNTITLE
          MATCH     SP4,PNTITLE
          GOTO      PACK2000 IF EQUAL
          RESET     PNFNAME,0
          APPEND    PNTITLE,PNFNAME
          RESET     PNFNAME,30
PACK1000  CMATCH    SP1,PNFNAME
          GOTO      PACK2000 IF NOT EQUAL
          BUMP      PNFNAME,-1
          GOTO      PACK1000
PACK2000  APPEND    SP1,PNFNAME
          APPEND    PNGNAME,PNFNAME
          RESET     PNFNAME,30
PACK3000  CMATCH    SP1,PNFNAME
          GOTO      PACK4000 IF NOT EQUAL
          BUMP      PNFNAME,-1
          GOTO      PACK3000
PACK4000  APPEND    SP1,PNFNAME
          APPEND    PNSNAME,PNFNAME
          RESET     PNFNAME
          RETURN
.
.------------------------------------------------------------------------------
.         Format a name string in the form SURNAME GIVEN NAME TITLE
.------------------------------------------------------------------------------
.
PATN0000 MOVE       SP20,PNFNAME
         MOVE       PNSNAME,PNFNAME
         ENDSET     PNFNAME
         CALL       BUMPNM
         LENSET     PNFNAME
         APPEND     ", ",PNFNAME
         APPEND     PNGNAME,PNFNAME
         ENDSET     PNFNAME
         CALL       BUMPNM
         LENSET     PNFNAME
         APPEND     ", ",PNFNAME
         APPEND     PNTITLE,PNFNAME
         CALL       BUMPNM
         LENSET     PNFNAME
         RESET      PNFNAME
         CMATCH     PNFNAME," "
         RETURN     IF NOT EQUAL
         MOVE       SP20,PNFNAME
         RETURN
.
BUMPNM   CMATCH     SP1,PNFNAME
         RETURN     IF NOT EQUAL
         BUMP       PNFNAME BY -1
         RETURN     IF EOS
         GOTO       BUMPNM
.
+
.------------------------------------------------------------------------------
.         Formats inpatients address string
.------------------------------------------------------------------------------
.
PACKADDP  PACK      PATADDR,SP30,SP30
          MATCH     SP20,PADD1
          GOTO      PACP2000 IF EQUAL
          RESET     PATADDR,0
          APPEND    PADD1,PATADDR
          RESET     PATADDR,30
PACP1000  CMATCH    SP1,PATADDR
          GOTO      PACP2000 IF NOT EQUAL
          BUMP      PATADDR,-1
          GOTO      PACP1000
PACP2000  APPEND    COMMA,PATADDR
          MATCH     SP20,PADD2
          GOTO      PACP4000 IF EQUAL
          APPEND    PADD2,PATADDR
          RESET     PATADDR,30
PACP3000  CMATCH    SP1,PATADDR
          GOTO      PACP4000 IF NOT EQUAL
          BUMP      PATADDR,-1
          GOTO      PACP3000
PACP4000  APPEND    COMMA,PATADDR
          MATCH     SP20,PSUBRB
          GOTO      PACP6000 IF EQUAL
          APPEND    PSUBRB,PATADDR
          RESET     PATADDR,30
PACP5000  CMATCH    SP1,PATADDR
          GOTO      PACP6000 IF NOT EQUAL
          BUMP      PATADDR,-1
          GOTO      PACP5000
PACP6000  APPEND    SP1,PATADDR
          APPEND    PPOST,PATADDR
          RESET     PATADDR
          RETURN
+
.------------------------------------------------------------------------------
.         Formats outpatients address string
.------------------------------------------------------------------------------
.
PACKADDO  PACK      PATADDR,SP30,SP30
          MATCH     SP20,PADD1
          GOTO      PACO2000 IF EQUAL
          RESET     PATADDR,0
          APPEND    PADD1,PATADDR
          RESET     PATADDR,30
PACO1000  CMATCH    SP1,PATADDR
          GOTO      PACO2000 IF NOT EQUAL
          BUMP      PATADDR,-1
          GOTO      PACO1000
PACO2000  APPEND    COMMA,PATADDR
          MATCH     SP20,PADD2
          GOTO      PACO4000 IF EQUAL
          APPEND    PADD2,PATADDR
          RESET     PATADDR,30
PACO3000  CMATCH    SP1,PATADDR
          GOTO      PACO4000 IF NOT EQUAL
          BUMP      PATADDR,-1
          GOTO      PACO3000
PACO4000  APPEND    COMMA,PATADDR
          MATCH     SP20,PSUBRB
          GOTO      PACO6000 IF EQUAL
          APPEND    PSUBRB,PATADDR
          RESET     PATADDR,30
PACO5000  CMATCH    SP1,PATADDR
          GOTO      PACO6000 IF NOT EQUAL
          BUMP      PATADDR,-1
          GOTO      PACO5000
PACO6000  APPEND    SP1,PATADDR
          APPEND    PPOST,PATADDR
          RESET     PATADDR
          RETURN
.
.
. =========================================================================
.          I/O routines for temp file
. =========================================================================
.
RDATMP1    MOVE       ZERO,OVRCD
           RESET      KEY34
           READ       OUTTM1XX,KEY34;ANS
           GOTO       OVERCOND IF OVER
           RETURN  
.
RDSTMP1    RESET      KEY34
           READ       OUTTM1XX,KEY34;;
           RETURN  
.
RDKTMP1    MOVE       ZERO,OVRCD
           READKS     OUTTM1XX;OBACLIN,PATDESC,DOBAOUTN,OBAURNO:
                               SAAENUMB,SAAEDATE,OBACOMP:
                               PATADDR,DCENT,DYEAR,DMONTH,DDAY
           GOTO       OVERCOND IF OVER
           MOVE       DOBAOUTN,OBAOUTNO
           RETURN  
.
WRTMP1     MOVE       ZERO,OVRCD
           RESET      KEY34
           MOVE       OBAOUTNO,DOBAOUTN
           WRITE      OUTTM1XX,KEY34;OBACLIN,PATDESC,DOBAOUTN,OBAURNO:
                                     SAAENUMB,SAAEDATE,OBACOMP:
                                     PATADDR,DCENT,DYEAR,DMONTH,DDAY
           GOTO       OVERCOND IF OVER
           RETURN  
+
. =========================================================================
.          Includes for all file I/O would be included here.
.          Includes for all procedural routines would be included here.
. =========================================================================
.
           INC        STD001IO
           INC       TFILENAM
           INC       AAEDE1IO/INC
           INC       IBASEQIO/INC
           INC       PATCODIO/INC
           INC       PATDO1IO/INC
           INC       PATMA1IO/INC
           INC       PATPR1IO/INC
           INC       OUTBOAIO/INC
           INC       OUTBB1IO/INC
           INC       OUTCLIIO/INC
           INC       WEBERRIO/INC
.

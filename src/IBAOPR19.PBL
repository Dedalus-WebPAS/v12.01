.------------------------------------------------------------------------------
. System    :   OPR - Theatre                                             
. Program   :   IBAOPR19                                                  
. Desc      :   OPR Recovery adverse event report                      
.------------------------------------------------------------------------------
. Date      :   08/07/2010                                                
. Author    :   Saroeun J. Soeur                                          
. Function  :   print a list of all patients with recovery adverse events
.               
. Mods      :                                                             
.------------------------------------------------------------------------------
.
.  V11.03.01   20/03/2023  PJ Le Febour  TSK 0909393
.                          GENR0050 amended PACK KEY19 to KEY22
.                                           call RSOPSES1 to RSOPSES7
.                                           call RKOPSES1 to RKOPSES7
.  V10.06.01   06/07/2015  Peter Vela    CAR 314529
.                          Fixed OPCNCLSU functionality in GENR2000
.  V10.04.01   23/06/2014  J.Tan         CAR 261630
.                          Removed port number from temp file name
.  V10.03.01   13.01.2012  Peter McMullen CAR 258455
.                          fix pain score after 45 description
.  V10.02.02   28.06.2011  Saroeun Soeur CAR 245267
.                          jumpt to GENR3200
.  V10.02.01   17.06.2011  Saroeun Soeur CAR 240742
.                          fixed index for tmp files
.  V10.00.02   20.09.2010  Saroeun Soeur CAR 218727
.                          changed report to match template change
.  V10.00.00   29.07.2010  Saroeun Soeur CAR 218727
.                          created new program to print a list of patients
.                          with recovery adverse events                     
.------------------------------------------------------------------------------
.  FD INCLUDES
.------------------------------------------------------------------------------
.
                    INC       STD001FD                * std library
                    INC       IBASEQFD/INC            * Sequential Numbers File
                    INC       TFILEVAR/INC            * Generate Temp File Name
                    INC       WEBERRFD/INC            * Web Server Error Logging
.
                    INC       PATCODFD/INC            * patcodes file
                    INC       OPRSESFD/INC            * theatre session file
                    INC       PATVISFD/INC            * patient admiss. file
                    INC       PMSHCPFD/INC            * hcp file
                    INC       PATMA1FD/INC            * patient master file
                    INC       OPRAAEFD/INC            * recovery adv. evt file
                    INC       OPRCLIFD/INC            * clinic file
+
.------------------------------------------------------------------------------
.         Temp. File - store all the records that match the users
.                      criteria
.------------------------------------------------------------------------------
.
TREPORT1  IFILE     SQL, FIXED=89, KEY="U88-88,85-85,86-87,77-84,41-48,69-76"
+
. Name          Type      Length     Physical     Start   Description
. ----          ----      ------     --------     -----   -----------
.
TMPSURGN        DIM        20           20          1       Surgeon
TMPANAES        DIM        20           20         21       Anaesthetist
TMPURNUM        DIM         8            8         41       UR number
TMPSNAME        DIM        10           10         49       Patient Surname
TMPFNAME        DIM        10           10         59       Patient First name
TMPADMIS        DIM         8            8         69       Admission number
TMPTHEAD        DIM         8            8         77       Theatre Date
.
TMPEVENT        DIM         1            1         85       Event Type   
TMPEVTDT        DIM         2            2         86       Event Detail 
TMPADEVT        DIM         1            1         88       Adverse Event?
.
.                                       Total:     89     
+ 
.------------------------------------------------------------------------------
.         Local variables
.------------------------------------------------------------------------------
.
ADVEREVT  DIM       1         * Adverse Event?
ANAESTHC  DIM       10        * anaesthetist doctor code
SURGEONC  DIM       10        * surgeon doctor code
USERID    DIM       10
URNUMBR   DIM       8
GENEVENT  DIM       1
PAINMANG  DIM       1
RESPFUNC  DIM       1
CARDFUNC  DIM       1
BODYTEMP  DIM       1
OTHEEVNT  DIM       1
TEMP      DIM       1
EVENT     DIM       1
TMPCNT    FORM      1
EVNTD     DIM       39
NOEVNT    DIM       1
.
FADMDTE   DIM       8         * from date
TADMDTE   DIM       8         * to date
TTODATE   DIM       8         * date
FROMDATE  DIM       11        * from date
STRTDATE  DIM       11        * start date
.
YYYY      DIM       4         
SPECIAL   FORM      1         * Special print flag
DMM       DIM       2
DDD       DIM       2
DIM3      DIM       3
DIM3A     DIM       3
DIM3B     DIM       3
DIM3C     DIM       3
DIM3D     DIM       3
DIM3E     DIM       3
DIM3F     DIM       3
DIM10     DIM       10
DIM100    DIM      100
F2CODE    FORM      2
F1CODE    FORM      2
F1CATE    FORM      1
F2CATE    FORM      1
FCODE     FORM      2
COUNTER   FORM      1
SURCODE   DIM       10
DOCTYPE   DIM       3
ANACODE   DIM       10 
OPCNCLSU  FORM      1
CWEBDNAM  FORM      1
DISPNAME  DIM       60
CWEBDNAI  DIM       1
NAME35    DIM       35
FIRSTREC  FORM      1
OPCND     FORM      1
CMDLINE   DIM       90
FNAME     DIM       8
STATUS    FORM      1
TEMANA    DIM       10
TEMDOC    DIM       10
DOCFNAME  DIM       50
FMTTITLE  DIM       10
FMTSNAME  DIM       35
FMTGNAME  DIM       35
DIM20     DIM       20
DIM20B    DIM       20
+
CATEGCNT  FORM      1
CCOUNT    FORM      2
CODEGCNT  FORM      2
+
.------------------------------------------------------------------------------
.         Constant variables
.------------------------------------------------------------------------------
.
TWELVE    FORM      "12"
MAXCLNO   FORM      "38"      * Maximum Line Number
FSLASH    INIT      "/"
TILDA8    INIT      "~~~~~~~~"
CATOP     INIT      "OP"
DOWNCASE  INIT      "AaBbCcDdEeFfGgHhIiJjKkLlMmNnOoPpQqRrSsTtUuVvWwXxYyZz"
NA        INIT      "N/A"
ALL       INIT      "ALL"     
ELEVEN    FORM      "11"
TILDA26   INIT      "~~~~~~~~~~~~~~~~~~~~~~~~~~~"
+
.------------------------------------------------------------------------------
.
.------------------------------------------------------------------------------
.
GENEDESC  INIT      "General Event? (Y/N)                  :"
PAINDESC  INIT      "Pain Management? (Y/N)                :"
RESPDESC  INIT      "Respiratory Function? (Y/N)           :"
CARDDESC  INIT      "Cardiac / Circulatory Function? (Y/N) :"
BODYDESC  INIT      "Body Temperature? (Y/N)               :"
OTHEDESC  INIT      "Other Event? (Y/N)                    :"
.
TXT01_00  INIT      "Unrepsonsive > 30"                            * OPAAGEUR
TXT02_00  INIT      "Agitated confused/hallucinations"             * OPAAGEAC
TXT03_00  INIT      "PONV"                                         * OPAAGEP1
TXT04_00  INIT      "PONV requiring treatment with PACU protocol"  * OPAAGEP2
.
TXT05_00  INIT      "Reviewed by an anaesthetist to treat severe pain that"
TXT06_00  INIT      "Pain score >= 8 on waking"   
TXT07_00  INIT      "Pain score >= 8 after 45 min"
TXT37_00  INIT      "Severe Pain in PACU requiring treatment with PACU pain protocol"
.
TXT08_00  INIT      "Persistent sat < 95%"
TXT09_00  INIT      "Tachypnea"
TXT10_00  INIT      "Bradypnea"
TXT11_00  INIT      "Respiratory distress requiring tracheal intubation or insertion of laryngeal mask in recovery period"
TXT12_00  INIT      "Respiratory depression"
TXT13_00  INIT      "Laryngospasm"
TXT14_00  INIT      "Bronchospasm"
TXT15_00  INIT      "Aspiration"
TXT16_00  INIT      "Ventilated / Bagged"
.TXT17_00  INIT      "Reintubated"
TXT18_00  INIT      "Respirtory or cardiac arrest requiring intervention"
.
TXT19_00  INIT      "Intervention of anaesthetist required for circulatory"
TXT20_00  INIT      "Chest pain"
TXT21_00  INIT      "ECG Ischaemia"
TXT22_00  INIT      "New arrhythmia"
TXT23_00  INIT      "Hypotension"
TXT24_00  INIT      "Hypertension"
TXT25_00  INIT      "Bradycardia"
TXT26_00  INIT      "Tachycardia"
.
TXT27_00  INIT      "Death"
TXT28_00  INIT      "Drug error"
TXT29_00  INIT      "Anaphylaxis"
TXT30_00  INIT      "Seizures"
TXT31_00  INIT      "Unplanned transfer to ICU"
TXT32_00  INIT      "Inadequate reversal of neuromuscular blockade"
TXT33_00  INIT      "Unpredicted numbness / weakness or other neurological"
TXT34_00  INIT      "Unplanned Stay in PACU > 2hrs* for medical reasons"
TXT35_00  INIT      "Review by an anaesthetist for other reasons"
.
TXT36_00  INIT      "Body temp > 39C"
.TXT37_00  INIT      "Body temp < 35.5C"
TXT38_00  INIT      "Body temp < 35C recorded in PACU"
+
STR01_00  INIT      "General Event"
STR02_00  INIT      "Pain Management"
STR03_00  INIT      "Respiratory Function"
STR04_00  INIT      "Cardiac / Circulatory Function"
STR05_00  INIT      "Body Temperature"
STR06_00  INIT      "Other Event"
STR07_00  INIT      "No Events"

+
.------------------------------------------------------------------------------
.         Constant program variables
.------------------------------------------------------------------------------
.
PRGID     INIT      "IBAOPR19"
PRGNAM    INIT      "Recovery Anaesthetist Adverse Event Report"
VERSION   INIT      "V12.01.00"
+
.------------------------------------------------------------------------------
.         MAIN0000 
.------------------------------------------------------------------------------
.
MAIN0000  CALL      INIT0000            * Initialise
.
          CALL      GETC0000            * Get Search Criteria
          BRANCH    EXIT,MAIN9999
.
          CALL      GENR0000            * Generate Report
.
          CALL      PRNR0000            * Print Report
          CALL      KILL0000            * Kill temp file
.
MAIN9999  CHAIN     PGM
+
.------------------------------------------------------------------------------
.         Initialise 
.------------------------------------------------------------------------------
.
INIT0000  CALL      DISPHEAD             * header
.
          OPEN      OPRAAE11,"opraaeaf"; * open files
          OPEN      OPRSESA1,"oprsesaf";
          OPEN      OPRSESA7,"oprsesaf";
          OPEN      PATVISA1,"patvisaf";
          OPEN      PMSHCPA1,"pmshcpaf";
          OPEN      PATMA1A1,"patma1af";
          OPEN      PATMX1A1,"patmx1af";
          OPEN      PATCODE1,"patcodes";
          OPEN      OPRCLIA1,"oprcliaf";
          OPEN      CONTROLF,"controlf";
.
          READ      CONTROLF,FORTY;*111,OPCNCLSU  * read control parameter
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,FNAME
.
INIT9999  RETURN
+
.------------------------------------------------------------------------------
.         Remove temp file
.------------------------------------------------------------------------------
.
KILL0000  CLOSE     TREPORT1
          MOVE      ZERO,STATUS
.
          DISPLAY   *P1:24,*EL,*P28:24,*V2LON,"** Deleting Index File **";
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
KILL9999  RETURN
+
.------------------------------------------------------------------------------
.         create temp file
.------------------------------------------------------------------------------
.
CRET0000  MOVE      ZERO,STATUS
.
          CALL      KILL0000
.
          PACK      CMDLINE,SP70,SP70
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME,CMDLINE
          APPEND    " 89 U88-88,85-85,86-87,77-84,41-48,69-76 ",CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          MOVE      ONE,STATUS
.
          MOVE      ZERO,OPCND
          TRAP      CRET8000 IF IO
          OPEN      TREPORT1,FNAME
          TRAPCLR   IO
.
          CALL      CLET0000             * Clear tempfile
.
          GOTO      CRET9999
.
CRET8000  MOVE      ONE,OPCND
          DISPLAY   *P1:24,*EL,*B,"** Temporary Index File ",*V2LON,FNAME:
                               *HOFF," not found **   ";
          CALL      HITENTER
          TRAPCLR   IO
.
CRET9999  RETURN
+
.------------------------------------------------------------------------------
.         Clear tempfile
.------------------------------------------------------------------------------
CLET0000  PACK      KEY28,SP70
          CALL      RDSREPR1
          CALL      RDKREPR1
          BRANCH    OVRCD,CLET9999
.
          PACK  KEY28,TMPADEVT,TMPEVENT,TMPEVTDT,TMPTHEAD,TMPURNUM,TMPADMIS,SP70
          CALL      DELREPR1
          GOTO      CLET0000
.
CLET9999  RETURN
.
.------------------------------------------------------------------------------
.         Print report
.------------------------------------------------------------------------------
.
PRNR0000  MOVE      TRUE,FIRSTREC
.
          PACK      KEY28,SP70
          CALL      RDSREPR1                 * position read
PRNR1000  CALL      RDKREPR1                 * read next
          BRANCH    OVRCD,PRNR9000
.
          CALL      CHPG0000                 * Check If Need new Page
.
          IF        FIRSTREC = TRUE
            CALL      PAGE0000               * Setup Patient Heading Stuff
            CALL      FIRP0000
            MOVE      "9",CLNO
            CALL      HEAD0000               * Setup Patient Heading Stuff
            MOVE      FALSE,FIRSTREC
          ENDIF
.
          CALL      PRNA0000                   
.
          GOTO      PRNR1000
.
PRNR9000  IF        FIRSTREC=TRUE
            CALL      PAGE0000               * New Page
          ENDIF
.         
          PRINT     *N,"**** End of Report ****"
          ADD       "2",CLNO
          DISPLAY   *P1:24,*EL,"Print Complete.   ";
          CALL      HITENTER
.
PRNR9999  RETURN
+
.------------------------------------------------------------------------------
.         Print report data
.------------------------------------------------------------------------------
.
PRNA0000  UNPACK    TMPTHEAD,YYYY,DMM,DDD
          PACK      DIM10,DDD,FSLASH,DMM,FSLASH,YYYY
.
          MATCH     "1",TMPADEVT
          IF        @EQUAL
            MOVE      "Yes",DIM3
          ELSE
            MOVE      "No",DIM3
          ENDIF
.
          MOVE      SP70,DIM20
          MOVE      TMPEVENT,F1CATE
          LOAD      DIM20,F1CATE,STR01_00,STR02_00,STR03_00,STR04_00,STR05_00:
                                 STR06_00,STR07_00
.
          MOVE      TMPEVTDT,F1CODE
.
          BRANCH    F1CATE,PRNA1000,PRNA2000,PRNA3000,PRNA4000,PRNA5000:
                           PRNA6000,PRNA7100

PRNA1000  LOAD      DIM100,F1CODE,TXT01_00,TXT02_00,TXT03_00,TXT04_00
          GOTO      PRNA7000
.
PRNA2000  LOAD      DIM100,F1CODE,TXT05_00,TXT06_00,TXT07_00,TXT37_00
          GOTO      PRNA7000
.
PRNA3000  LOAD      DIM100,F1CODE,TXT08_00,TXT09_00,TXT10_00,TXT11_00,TXT12_00:
                                 TXT13_00,TXT14_00,TXT15_00,TXT16_00,TXT18_00
          GOTO      PRNA7000
.
PRNA4000  LOAD      DIM100,F1CODE,TXT19_00,TXT20_00,TXT21_00,TXT22_00,TXT23_00:  
                                 TXT24_00,TXT25_00,TXT26_00
.
          GOTO      PRNA7000

PRNA5000  LOAD      DIM100,F1CODE,TXT36_00,TXT38_00                     * boy temp
.
          GOTO      PRNA7000
.
PRNA6000  LOAD      DIM100,F1CODE,TXT27_00,TXT28_00,TXT29_00,TXT30_00,TXT31_00:  * other
                                 TXT32_00,TXT33_00,TXT34_00,TXT35_00
.
          GOTO      PRNA7000
.
PRNA7000  COMPARE   F1CATE,F2CATE
          IF        @EQUAL   
            COMPARE   F1CODE,F2CODE
            IF        !@EQUAL
              PRINT     *001,"| ":
                        *011,"  ",DIM20," - ",DIM100:
                        *132,"|"
              CALL      UND132
            ENDIF         
          ELSE
            PRINT     *001,"| ":
                      *011,"  ",DIM20," - ",DIM100:
                      *132,"|"
            CALL      UND132
          ENDIF
          GOTO      PRNA7200
.
PRNA7100  COMPARE   F1CATE,F2CATE
          IF        !@EQUAL
            PRINT     *001,"| ":
                      *011,"  ",DIM20:
                      *132,"|"
            CALL      UND132
          ENDIF
.
PRNA7200  MOVE      F1CATE,F2CATE
          MOVE      F1CODE,F2CODE
.
PRNA8000  PRINT     *001,"| ",DIM3: 
                    *011,"| ":
                    *027,"| ",TMPSURGN:
                    *049,"| ",TMPANAES:
                    *071,"| ",TMPURNUM:
                    *082,"| ",TMPADMIS:
                    *092,"| ",TMPSNAME:
                    *106,"| ",TMPFNAME:
                    *118,"| ",DIM10:
                    *132,"|"
.
            CALL       UND132
.
PRNA9999  RETURN
+
.---------------------------------------------------------------------------
.         check if a new page is needed
.---------------------------------------------------------------------------
.
CHPG0000  IF        CLNO >= MAXCLNO
            CALL      PAGE0000
            CALL      HEAD0000               * print header details
            MOVE      "45",MAXCLNO
          ENDIF
.
CHPG9999  RETURN
+
.---------------------------------------------------------------------------
.         Print report header
.---------------------------------------------------------------------------
.
HEAD0000  CALL      UND132
.
          PRINT     *001,"| Adverse":
                    *011,"| Event Type":
                    *027,"| Surgeon":
                    *049,"| Anaesthetist":
                    *071,"| Patient":
                    *082,"| Visit":
                    *092,"| Patient":
                    *106,"| Patient":
                    *118,"| Date of":
                    *132,"|"
.
          ADD       ONE,CLNO
.
          PRINT     *001,"| Event?":
                    *011,"| ":
                    *027,"| ":
                    *049,"| ":
                    *071,"| URN":
                    *082,"| ":
                    *092,"| Surname":
                    *106,"| Name":
                    *118,"| Visit":
                    *132,"|"
.
          CALL      UND132
.
          ADD       "5",CLNO
          MOVE      FALSE,SPECIAL
.
HEAD9999  RETURN
+
.------------------------------------------------------------------------------
.
.------------------------------------------------------------------------------
FIRP0000  UNPACK    FADMDTE,YYYY,DMM,DDD
          PACK      FROMDATE,DDD,FSLASH,DMM,FSLASH,YYYY

          UNPACK    TADMDTE,YYYY,DMM,DDD
          PACK      STRTDATE,DDD,FSLASH,DMM,FSLASH,YYYY
.
          MATCH     SURGEONC,SP70
          IF        @EQUAL
            MOVE      ALL,DIM20
          ELSE
            PACK      KEY10,SURGEONC,SP70
            CALL      RDPMHCP1
            IF        OVRCD=1
              MOVE      NA,DIM20
            ELSE
               MOVE      PMHCTITL,PACTITLE
               MOVE      PMHCGNAM,PACGNAME
               MOVE      PMHCSNAM,PACSNAME
               MOVE      TWO,PACFRMT             * title name surname of doctor
               CALL      PACNAME                 * pack up doctor's name
               MOVE      PACFNAME,DIM20
            ENDIF
          ENDIF

          MATCH     ANAESTHC,SP70
          IF        @EQUAL
            MOVE      ALL,DIM20B
          ELSE
            PACK      KEY10,ANAESTHC,SP70
            CALL      RDPMHCP1
            IF        OVRCD=1
              MOVE      NA,DIM20B
            ELSE
               MOVE      PMHCTITL,PACTITLE
               MOVE      PMHCGNAM,PACGNAME
               MOVE      PMHCSNAM,PACSNAME
               MOVE      TWO,PACFRMT             * title name surname of doctor
               CALL      PACNAME                 * pack up doctor's name
               MOVE      PACFNAME,DIM20B
            ENDIF
          ENDIF
.
          MATCH     URNUMBR,SP70
          IF        @EQUAL
            MOVE      ALL,URNUMBR
          ENDIF
.
          MATCH     ADVEREVT,SP70
          IF        @EQUAL
            MOVE      ALL,DIM3
          ENDIF
.
          MATCH     "1",ADVEREVT
          IF        @EQUAL
            MOVE      YES,DIM3
          ENDIF
.
          MATCH     "0",ADVEREVT
          IF        @EQUAL
            MOVE      NO,DIM3
          ENDIF
.
          MATCH     GENEVENT,SP70
          IF        @EQUAL
            MOVE      ALL,DIM3A
          ENDIF
.
          MATCH     "1",GENEVENT
          IF        @EQUAL
            MOVE      YES,DIM3A
          ENDIF
.
          MATCH     "0",GENEVENT
          IF        @EQUAL
            MOVE      NO,DIM3A
          ENDIF
.
          MATCH     PAINMANG,SP70
          IF        @EQUAL
            MOVE      ALL,DIM3B
          ENDIF
.
          MATCH     "1",PAINMANG
          IF        @EQUAL
            MOVE      YES,DIM3B
          ENDIF
.
          MATCH     "0",PAINMANG
          IF        @EQUAL
            MOVE      NO,DIM3B
          ENDIF
.
          MATCH     RESPFUNC,SP70
          IF        @EQUAL
            MOVE      ALL,DIM3C
          ENDIF
.
          MATCH     "1",RESPFUNC
          IF        @EQUAL
            MOVE      YES,DIM3C
          ENDIF
.
          MATCH     "0",RESPFUNC
          IF        @EQUAL
            MOVE      NO,DIM3C
          ENDIF
.
          MATCH     CARDFUNC,SP70
          IF        @EQUAL
            MOVE      ALL,DIM3D
          ENDIF
.
          MATCH     "1",CARDFUNC
          IF        @EQUAL
            MOVE      YES,DIM3D
          ENDIF
.
          MATCH     "0",CARDFUNC
          IF        @EQUAL
            MOVE      NO,DIM3D
          ENDIF
.
          MATCH     BODYTEMP,SP70
          IF        @EQUAL
            MOVE      ALL,DIM3E
          ENDIF
.
          MATCH     "1",BODYTEMP
          IF        @EQUAL
            MOVE      YES,DIM3E
          ENDIF
.
          MATCH     "0",BODYTEMP
          IF        @EQUAL
            MOVE      NO,DIM3E
          ENDIF
.
          MATCH     OTHEEVNT,SP70
          IF        @EQUAL
            MOVE      ALL,DIM3F
          ENDIF
.
          MATCH     "1",OTHEEVNT
          IF        @EQUAL
            MOVE      YES,DIM3F
          ENDIF
.
          MATCH     "0",OTHEEVNT
          IF        @EQUAL
            MOVE      NO,DIM3F
          ENDIF
.          
          PRINT      *N,*2,"Date Range           :",FROMDATE," - ",STRTDATE:
                     *N,*2,"UR Number            :",URNUMBR:
                     *N,*2,"Surgeon              :",DIM20:
                     *N,*2,"Anaesthetist         :",DIM20B:
                     *N,*2,"Adverse Event        :",DIM3:
                     *N,*2,"General Event        :",DIM3A:
                     *N,*2,"Pain Management      :",DIM3B:
                     *N,*2,"Respiratory Function :",DIM3C:
                     *N,*2,"Cardiac / Circulatory:",DIM3D:
                     *N,*2,"Body Temperature     :",DIM3E:
                     *N,*2,"Other Event          :",DIM3F:
                     *N,*2,"Run by               :",USERID
.
FIRP9999  RETURN
.------------------------------------------------------------------------------
.         Print selection criteria 
.------------------------------------------------------------------------------
.
PAGE0000  CLOCK     TIME,CTIMEIS
          CALL      HEAD132
          MOVE      "9",CLNO
.
PAGE9999  RETURN
+
.------------------------------------------------------------------------------
.
.------------------------------------------------------------------------------
.
CLVR0000  MOVE      SP70,TMPADEVT       * clear variables
          MOVE      SP70,TMPEVENT
          MOVE      SP70,TMPTHEAD
          MOVE      SP70,TMPADMIS
          MOVE      SP70,TMPURNUM
          MOVE      SP70,TMPSNAME
          MOVE      SP70,TMPFNAME
.
          MOVE      SP70,TEMANA
          MOVE      SP70,TEMDOC
          MOVE      SP70,DOCTYPE
          MOVE      SP70,SURCODE
          MOVE      SP70,ANACODE
.
CLVR9999  RETURN
+
.-----------------------------------------------------
.         Generate the temp file data        
.-----------------------------------------------------
.
GENR0000  CALL      CRET0000             * create temp file
          MOVE      ONE,CATEGCNT
.
GENR0050  PACK      KEY22,FADMDTE,SP70
          CALL      RSOPSES7
GENR1000  CALL      RKOPSES7
          BRANCH    OVRCD,GENR9999
.
          CALL      CLVR0000
.
          MATCH     TADMDTE,OPSEDATE
          GOTO      GENR2000 IF EQUAL
.          
          MATCH     TADMDTE,OPSEDATE              
          GOTO      GENR9999 IF NOT LESS
.
GENR2000  PACK      TMPSURGN,SP70,SP70
          COMPARE   ZERO,OPCNCLSU
          IF        @EQUAL
            PACK      KEY6,OPSECLIN,SP70
            CALL      RDOPCLI1
            IF        OVRCD=0
              MOVE    OPCLDESC,TMPSURGN
              PACK    KEY10,OPCLDOCT,SP70
              CALL    RDPMHCP1
              IF      OVRCD=0
                MOVE    PMHCTITL,PACTITLE
                MOVE    PMHCGNAM,PACGNAME
                MOVE    PMHCSNAM,PACSNAME
                MOVE    TWO,PACFRMT             * title name surname of doctor
                CALL    PACNAME                 * pack up doctor's name
                MOVE    PACFNAME,TMPSURGN
              ENDIF
            ELSE
              PACK    KEY10,OPSECLIN,SP70
              CALL    RDPMHCP1
              IF      OVRCD=0
                MOVE    PMHCTITL,PACTITLE         
                MOVE    PMHCGNAM,PACGNAME
                MOVE    PMHCSNAM,PACSNAME
                MOVE    TWO,PACFRMT             * title name surname of doctor
                CALL    PACNAME                 * pack up doctor's name
                MOVE    PACFNAME,TMPSURGN
              ENDIF
            ENDIF
          ELSE
            PACK    KEY10,OPSECLIN,SP70
            CALL    RDPMHCP1
            IF      OVRCD=0
              MOVE    PMHCTITL,PACTITLE
              MOVE    PMHCGNAM,PACGNAME
              MOVE    PMHCSNAM,PACSNAME
              MOVE    TWO,PACFRMT             * title name surname of doctor
              CALL    PACNAME                 * pack up doctor's name
              MOVE    PACFNAME,TMPSURGN
            ENDIF
          ENDIF
.
          MATCH     SURGEONC,SP70
          IF        !@EQUAL
            MATCH     KEY10,SURGEONC
            IF        @EQUAL
              GOTO      GENR3000
            ELSE
              GOTO      GENR1000
            ENDIF
          ENDIF
.
GENR3000  PACK      KEY10,OPSEANAE,SP70
          CALL      RDPMHCP1
          IF        OVRCD<>1
            MOVE      PMHCTITL,PACTITLE            * I CAR 13038
            MOVE      PMHCGNAM,PACGNAME
            MOVE      PMHCSNAM,PACSNAME
            MOVE      TWO,PACFRMT             * title name surname of doctor
            CALL      PACNAME                 * pack up doctor's name
            MOVE      PACFNAME,TMPANAES
          ENDIF
.
          MATCH     ANAESTHC,SP70
          IF        !@EQUAL
            MATCH     OPSEANAE,ANAESTHC
            IF        @EQUAL
              GOTO      GENR3100
            ELSE
              GOTO      GENR1000
            ENDIF
          ENDIF
.
GENR3100  PACK      KEY30,OPSEDATE,OPSETIME,OPSECLIN,SP70
          CALL      RSOPAAE1
GENR3200  CALL      RKOPAAE1
          BRANCH    OVRCD,GENR1000
.
          CALL      CLVR0000
.
          MATCH     OPAADATE,OPSEDATE
          GOTO      GENR1000 IF NOT EQUAL
.
          MATCH     OPAATIME,OPSETIME
          GOTO      GENR1000 IF NOT EQUAL
.
          MATCH     OPAACLIN,OPSECLIN
          GOTO      GENR1000 IF NOT EQUAL
.
          MATCH     ADVEREVT,SP70
          GOTO      GENR4000 IF EQUAL
.
GENR3500  MATCH     ADVEREVT,OPAAADVE
          GOTO      GENR3200 IF NOT EQUAL
.
GENR4000  PACK      KEY8,OPAAADMN,SP70
          CALL      RDPTVIS1
          BRANCH    OVRCD,GENR3200
.
          PACK      KEY8,PVIURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,GENR3200
.
          MATCH     URNUMBR,SP70
          IF        !@EQUAL
            MATCH     PVIURNO,URNUMBR
            IF        @EQUAL
              GOTO      GENR8000
            ELSE
              GOTO      GENR3200
            ENDIF
          ENDIF
.
GENR8000  MOVE      ONE,CCOUNT
GENR8100  PERFORM   CATEGCNT,CHKG0000,CHPM0000,CHRF0000,CHCC0000,CHBT0000:
                             CHOT0000,CHNO0000
.
          BRANCH    CEXIT,GENR9100,GENR3200
.
GENR9000  MOVE      CATEGCNT,TMPEVENT
          MOVE      CODEGCNT,TMPEVTDT
          MOVE      OPAAADVE,TMPADEVT
          MOVE      OPSEDATE,TMPTHEAD
          MOVE      OPAAADMN,TMPADMIS
          MOVE      PVIURNO,TMPURNUM
          MOVE      PSNAME,TMPSNAME
          MOVE      PGNAME,TMPFNAME
.
          PACK      KEY28,OPAAADVE,CATEGCNT,CODEGCNT,OPSEDATE,PVIURNO,OPAAADMN,SP70
          CALL      WRTREPR1
.
GENR9100  ADD       ONE,CCOUNT
.
          GOTO      GENR8100
.
GENR9999  COMPARE   EIGHT,CATEGCNT
          IF        @LESS
            ADD       ONE,CATEGCNT
            GOTO      GENR0050
          ENDIF
.
          RETURN
+
.-----------------------------------------------------
.         CHNO0000
.-----------------------------------------------------
.
CHNO0000  MOVE      ZERO,CEXIT
.
          COMPARE   TWO,CCOUNT
          IF        @LESS

            MATCH     "0",OPAAADVE
            IF        @EQUAL
              MOVE      CCOUNT,CODEGCNT
              GOTO      CHNO9999
            ENDIF          
          ENDIF
.
CHNO8000  MOVE      TWO,CEXIT
.
CHNO9999  RETURN
+
.-----------------------------------------------------
.         CHOT0000
.-----------------------------------------------------
.
CHOT0000  MOVE      ZERO,CEXIT
.
          MATCH     SP70,OTHEEVNT
          GOTO      CHOT8000 IF EQUAL
.
          MATCH     "1",OTHEEVNT
          IF        @EQUAL
CHOT1000    COMPARE   NINE,CCOUNT       * check general event
            IF        @LESS
              LOAD    TEMP,CCOUNT,OPAAORDE,OPAAORDT,OPAAORAN,OPAAORSE,OPAAORUN:
                                  OPAAORIR,OPAAORNB,OPAAORST
.
              MATCH   "1",TEMP          * is there a general event?
              IF      @EQUAL
                MOVE    CCOUNT,CODEGCNT
                GOTO    CHOT9999          * there is so leave
              ENDIF
              GOTO  CHOT8000
            ENDIF                       * no more
          ENDIF
.
          MOVE      TWO,CEXIT
          GOTO      CHOT9999
.
CHOT8000  MOVE      ONE,CEXIT
.
CHOT9999  RETURN
+

.-----------------------------------------------------
.         CHBT0000
.-----------------------------------------------------
.
CHBT0000  MOVE      ZERO,CEXIT
.
          MATCH     SP70,BODYTEMP
          GOTO      CHBT8000 IF EQUAL
.
          MATCH     "1",BODYTEMP
          IF        @EQUAL
CHBT1000    COMPARE   THREE,CCOUNT       * check general event
            IF        @LESS
              LOAD    TEMP,CCOUNT,OPAABTB1,OPAABTB3
.
              MATCH   "1",TEMP          * is there a general event?
              IF      @EQUAL
                MOVE    CCOUNT,CODEGCNT
                GOTO    CHBT9999          * there is so leave
              ENDIF
              GOTO  CHBT8000
            ENDIF                       * no more
          ENDIF
.
          MOVE      TWO,CEXIT
          GOTO      CHBT9999
.
CHBT8000  MOVE      ONE,CEXIT
.
CHBT9999  RETURN
+
.-----------------------------------------------------
.         CHCC0000
.-----------------------------------------------------
.
CHCC0000  MOVE      ZERO,CEXIT
.
          MATCH     SP70,CARDFUNC
          GOTO      CHCC8000 IF EQUAL
.
          MATCH     "1",CARDFUNC
          IF        @EQUAL
CHCC1000    COMPARE   NINE,CCOUNT       * check general event
            IF        @LESS
              LOAD    TEMP,CCOUNT,OPAACCIA,OPAACCCP,OPAACCEC,OPAACCNA,OPAACCHP:
                                  OPAACCHR,OPAACCBR,OPAACCTA
.
              MATCH   "1",TEMP          * is there a general event?
              IF      @EQUAL
                MOVE    CCOUNT,CODEGCNT
                GOTO    CHCC9999          * there is so leave
              ENDIF
              GOTO  CHCC8000
            ENDIF                       * no more
          ENDIF
.
          MOVE      TWO,CEXIT
          GOTO      CHCC9999
.
CHCC8000  MOVE      ONE,CEXIT
.
CHCC9999  RETURN
+
.-----------------------------------------------------
.         CHKG0000
.-----------------------------------------------------
.
CHRF0000  MOVE      ZERO,CEXIT
.
          MATCH     SP70,RESPFUNC
          GOTO      CHRF8000 IF EQUAL
.
          MATCH     "1",RESPFUNC
          IF        @EQUAL
CHRF1000    COMPARE   TWELVE,CCOUNT       * check general event
            IF        @LESS
              LOAD    TEMP,CCOUNT,OPAARFPS,OPAARFTA,OPAARFBR,OPAARFRD,OPAARFDE:
                                  OPAARFLA,OPAARFBO,OPAARFAS,OPAARFVB,OPAARFRE:
                                  OPAARFRC
.
              MATCH   "1",TEMP          * is there a general event?
              IF      @EQUAL
                MOVE    CCOUNT,CODEGCNT
                GOTO    CHRF9999          * there is so leave
              ENDIF
              GOTO  CHRF8000
            ENDIF                       * no more
          ENDIF
.          
          MOVE      TWO,CEXIT
          GOTO      CHRF9999
.
CHRF8000  MOVE      ONE,CEXIT
.
CHRF9999  RETURN
+
.-----------------------------------------------------
.         CHKG0000
.-----------------------------------------------------
.
CHPM0000  MOVE      ZERO,CEXIT
.
          MATCH     SP70,PAINMANG
          GOTO      CHPM8000 IF EQUAL
.
          MATCH     "1",PAINMANG
          IF        @EQUAL
CHPM1000    COMPARE   FIVE,CCOUNT       * check general event
            IF        @LESS
              LOAD    TEMP,CCOUNT,OPAAPMRA,OPAAPMP1,OPAAPMP2,OPAABTB2
.
              MATCH   "1",TEMP          * is there a general event?
              IF      !@LESS
                MOVE    CCOUNT,CODEGCNT
                GOTO    CHPM9999          * there is so leave
              ENDIF
              GOTO  CHPM8000
            ENDIF                       * no more
          ENDIF
          MOVE      TWO,CEXIT
          GOTO      CHPM9999
.
CHPM8000  MOVE      ONE,CEXIT
.
CHPM9999  RETURN
+
.-----------------------------------------------------
.         CHKG0000
.-----------------------------------------------------
.
CHKG0000  MOVE      ZERO,CEXIT
.
          MATCH     SP70,GENEVENT
          GOTO      CHKG8000 IF EQUAL
.
          MATCH     "1",GENEVENT
          IF        @EQUAL
CHKG1000    COMPARE   FIVE,CCOUNT       * check general event
            IF        @LESS
              LOAD    TEMP,CCOUNT,OPAAGEUR,OPAAGEAC,OPAAGEP1,OPAAGEP2
              MATCH   "1",TEMP          * is there a general event?
              IF      @EQUAL
                MOVE    CCOUNT,CODEGCNT
                GOTO    CHKG9999          * there is so leave
              ENDIF
              GOTO  CHKG8000
            ENDIF                       * no more
          ENDIF
.
          MOVE      TWO,CEXIT
          GOTO      CHKG9999
.
CHKG8000  MOVE      ONE,CEXIT
.
CHKG9999  RETURN
+
.-----------------------------------------------------
.         GETC0000
.-----------------------------------------------------
.
GETC0000  CALL      KDAT0000            * keyin date
          BRANCH    EXIT,GETC9999
.
          CALL      KSUR0000            * keyin surgeon
          CALL      KANA0000            * keyin anaesthetist
          CALL      KDIF0000            * keyin difficult intubation
          CALL      URNU0000            * keyin ur number
          CALL      GTEV0000            * get events
          CALL      USER0000
          CALL      CONTQST
          MOVE      ZERO,EXIT
          BRANCH    CEXIT,GETC9999,GETC0000,GETC9000
.
GETC9000  MOVE      ONE,EXIT
.
GETC9999  RETURN
+
.------------------------------------------------------
.         
.------------------------------------------------------
GTEV0000  MOVE      ZERO,TMPCNT
.
GTEV0050  COMPARE   SIX,TMPCNT
          IF        @LESS
            ADD       ONE,TMPCNT

            LOAD      EVNTD,TMPCNT,GENEDESC,PAINDESC,RESPDESC,CARDDESC,BODYDESC:
                                   OTHEDESC
.
            CALL      EVNT0000
.
            BRANCH    TMPCNT,GTEV1000,GTEV2000,GTEV3000,GTEV4000,GTEV5000:
                             GTEV6000
.
          ENDIF
.
          GOTO      GTEV9999
.
GTEV1000  MOVE      EVENT,GENEVENT
          GOTO      GTEV0050
.
GTEV2000  MOVE      EVENT,PAINMANG
          GOTO      GTEV0050

GTEV3000  MOVE      EVENT,RESPFUNC
          GOTO      GTEV0050

GTEV4000  MOVE      EVENT,CARDFUNC
          GOTO      GTEV0050

GTEV5000  MOVE      EVENT,BODYTEMP
          GOTO      GTEV0050

GTEV6000  MOVE      EVENT,OTHEEVNT
          GOTO      GTEV0050
.  
GTEV9999  RETURN
.------------------------------------------------------------------------------
.
.------------------------------------------------------------------------------
.
KDIF0000  KEYIN     *P1:CVERT,*EF,"Adverse Event?  (Y/N): ",*V2LON:
                    ADVEREVT
.
          ENDSET    ADVEREVT
          APPEND    SP70,ADVEREVT
          RESET     ADVEREVT
.
          MATCH     SP70,ADVEREVT            * Test for spaces
          IF        @EQUAL
            PACK      ADVEREVT,SP70
            DISPLAY   *P1:CVERT,*EF,"Adverse Event? (Y/N): ",*V2LON:
                                "ALL"
            MOVE      SP70,ADVEREVT
            GOTO      KDIF9999
          ENDIF
.          
          REP       "yYnN",ADVEREVT
.
          MATCH     ADVEREVT,ANSY
          IF        @EQUAL
            MOVE      "1",ADVEREVT
            GOTO      KDIF9999
          ENDIF
.
          MOVE      "0",ADVEREVT
.
KDIF9999  ADD       ONE,CVERT
          RETURN
+
.------------------------------------------------------------------------------
.
.------------------------------------------------------------------------------
.
EVNT0000  KEYIN     *P1:CVERT,*EF,*DV,EVNTD,*V2LON:
                    EVENT
.
          ENDSET    EVENT
          APPEND    SP70,EVENT
          RESET     EVENT
.
          MATCH     SP70,EVENT            * Test for spaces
          IF        @EQUAL
            PACK      EVENT,SP70
            DISPLAY   *P1:CVERT,*EF,EVNTD,*V2LON,"Y"
                               
            MOVE      "1",EVENT
            GOTO      EVNT9999
          ENDIF
.
          REP       "yYnN",EVENT
.
          MATCH     EVENT,ANSY
          IF        @EQUAL
            MOVE      "1",EVENT
            GOTO      EVNT9999
          ENDIF
.
          MOVE      "0",EVENT
.
EVNT9999  ADD       ONE,CVERT
          RETURN
+
.------------------------------------------------------------------------------
.
.------------------------------------------------------------------------------
.
KANA0000  KEYIN     *P1:CVERT,*EF,"Anaesthetist Code  : ",*V2LON:
                    ANAESTHC
.
          ENDSET    ANAESTHC
          APPEND    SP70,ANAESTHC
          RESET     ANAESTHC
.
          MATCH     SP70,ANAESTHC            * Test for spaces
          IF        @EQUAL
            PACK      ANAESTHC,ALL,SP70
            DISPLAY   *P1:CVERT,*EF,"Anaesthetist Code  : ",*V2LON:
                                ANAESTHC
            MOVE      SP70,ANAESTHC
            GOTO      KANA9999
          ENDIF
.
KANA9999  ADD       ONE,CVERT
          RETURN
+
.------------------------------------------------------
.         User name
.-----------------------------------------------------
.
URNU0000  KEYIN     *P1:CVERT,*EF,"UR Number  : ",*V2LON:
                    URNUMBR
.
          ENDSET    URNUMBR
          APPEND    SP70,URNUMBR
          RESET     URNUMBR
.
          MATCH     SP70,URNUMBR            * Test for spaces
          IF        @EQUAL
            PACK      URNUMBR,ALL,SP70
            DISPLAY   *P1:CVERT,*EF,"UR Number  : ",*V2LON:
                                URNUMBR
            MOVE      SP70,URNUMBR
            GOTO      URNU9999
          ENDIF
.
URNU9999  ADD       ONE,CVERT
          RETURN
+
.------------------------------------------------------
.         User name
.-----------------------------------------------------
.
USER0000  KEYIN     *P1:CVERT,*EF,"User ID  : ",*V2LON:
                    USERID
.
          ENDSET    USERID
          APPEND    SP70,USERID
          RESET     USERID
.
          MATCH     SP70,USERID            * Test for spaces
          IF        @EQUAL
            GOTO      USER0000
          ENDIF
.
          DISPLAY   *P1:CVERT,*EF,"User ID  : ",*V2LON:
                              USERID

USER9999  ADD       ONE,CVERT
          RETURN
+
.------------------------------------------------------
.
.-----------------------------------------------------
.
KSUR0000  KEYIN     *P1:4,*EF,"Surgeon Code  : ",*V2LON:
                    SURGEONC
.
          ENDSET    SURGEONC
          APPEND    SP6,SURGEONC
          RESET     SURGEONC
.
          MOVE      FIVE,CVERT
          MATCH     SP6,SURGEONC            * Test for spaces
          IF        @EQUAL
            PACK      SURGEONC,ALL,SP70
            DISPLAY   *P1:4,*EF,"Surgeon Code  : ",*V2LON:
                            SURGEONC
            MOVE      SP70,SURGEONC
            GOTO      KSUR9999
          ENDIF
.
KSUR9999  RETURN
+
.------------------------------------------------------
.
.------------------------------------------------------
.
KDAT0000  DISPLAY   *P1:4,*EF,"From Date :":
                        *P1:6,"To Date   :";
.
KDAT1000  MOVE      FOUR,CVERT
          MOVE      TWENTY,CCOL
          MOVE      ONE,CCENTRY
          MOVE      CCC,CCENT
          MOVE      ZERO,CDAY
          UNPACK    SP6,CYEAR,CMON,CDAY
          CALL      KEYDATE                     * Keyin todate
          BRANCH    OVRCD,KDAT9000
.
          PACK      FADMDTE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",FADMDTE
.
          MOVE      SIX,CVERT
          CALL      KEYDATE                     * Keyin fromdate
          BRANCH    OVRCD,KDAT1000
.
          PACK      TADMDTE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",TADMDTE
.
          MATCH     FADMDTE,TADMDTE             * match date range
          GOTO      KDAT2000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid date range entered **    ";
          CALL      HITENTER
          GOTO      KDAT0000
.
KDAT2000  CALL      CONTQST
          MOVE      ZERO,EXIT
          BRANCH    CEXIT,KDAT3000,KDAT1000,KDAT9000
.
.------ yes selected ------
.
KDAT3000  UNPACK    TADMDTE,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,CC
          MOVE      CYEAR,YY
          MOVE      CMON,MM
          MOVE      CDAY,DD
          CALL      DMYCON                  * convert to julian format
.
          ADD       ONE,JULDAY              * add one day to todate
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
.
          CALL      JULCON                  * convert back to DDMMYY format
.
          PACK      TTODATE,CC,YY,MM,DD
          REP       " 0",TTODATE
.
          GOTO      KDAT9999
.
KDAT9000  MOVE      ONE,EXIT
KDAT9999  RETURN
+
.------------------------------------------------------------------------------
.
.------------------------------------------------------------------------------
.
WRTREPR1  RESET     KEY28
          WRITE     TREPORT1,KEY28;TMPSURGN,TMPANAES,TMPURNUM,TMPSNAME:
                                  TMPFNAME,TMPADMIS,TMPTHEAD,TMPEVENT:
                                  TMPEVTDT,TMPADEVT
          RETURN
.
RDKREPR1  MOVE      ZERO,OVRCD
          READKS    TREPORT1;TMPSURGN,TMPANAES,TMPURNUM,TMPSNAME:
                             TMPFNAME,TMPADMIS,TMPTHEAD,TMPEVENT:
                             TMPEVTDT,TMPADEVT
          GOTO      OVERCOND IF OVER
          RETURN
.
RDSREPR1  RESET     KEY28
          READ      TREPORT1,KEY28;;
          RETURN
.
DELREPR1  RESET     KEY28
          DELETE    TREPORT1,KEY28
          RETURN
+
.------------------------------------------------------------------------------
.
.------------------------------------------------------------------------------
          INC       STD001IO
          INC       TFILENAM                * Generate Temp File Name
          INC       IBASEQIO/INC            * Sequential Numbers File
          INC       WEBERRIO/INC            * Web Server Error Logging
.
          INC       OPRSESIO/INC
          INC       PATVISIO/INC
          INC       PATMA1IO/INC
          INC       PMSHCPIO/INC
          INC       OPRAAEIO/INC
          INC       PATCODIO/INC
          INC       OPRCLIIO/INC



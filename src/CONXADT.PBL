.******************************************************************************
.* System   : Inpatients                                                      *
.* Program  : CONXADT                                                         *
.* Desc     : Remove Admission/Discharge/Transfer data added by Conversion   *
.******************************************************************************
.* Date     : 12/10/2001                                                      *
.* Author   : Mike Laming                                                     *
.* Mods     :                                                                 *
.*                                                                            *
.* V12.00.01 22/05/2025  Alina Bhari   TSK 0955096                            *
.*                       Added alpanumeric visit number generation -UPDTADMN  *
.******************************************************************************
.
          INC       STD001FD
          INC       MRTPDSFD/INC                 * DSC Header
          INC       MRTPSHFD/INC                 * DSC History
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATDADFD/INC                 * Transfer Destination
          INC       PATDSCFD/INC                 * Discharge Details
          INC       PATICUFD/INC                 * Intensive Care Unit
          INC       PATRE1FD/INC                 * Person Respon. for Account
          INC       PATMA1FD/INC                 * Patient Master
          INC       PATMI1FD/INC                 * Admissions
          INC       PATTRNFD/INC                 * Transfer Details
          INC       PATVISFD/INC                 * Visit Details
          INC       PATASFFD/INC                 * Special Funding Arrange.  @@
          INC       PATPGRFD/INC                 * Grouper Details           @@
          INC       PMSVX1FD/INC                 * Visit Extension           @@
.
.******************************************************************************
.*                            TEMPORARY FILES                                 *
.******************************************************************************
.
. File definition for admissions
. ------------------------------
ADT       FILE      ASCII, FIXED=901                                   *C-90207
.
.Name     Type   Length  Start loc.  Description
.----     ----   ------  ----------  -----------
TMPUR     DIM       8         1      U/R Number
TMPADMIS  FORM      8         9      Admission Number 
TMPADATE  DIM       8        17      Admission Date (ccyymmdd)
TMPATIME  DIM       8        25      Admission Time (hh:mm:ss)
TMPWARD   DIM       3        33      Ward
TMPBED    DIM       3        36      Bed
TMPRDOC   DIM      22        39      Referring Doctor
TMPADOC   DIM       6        61      Attending Doctor
TMPLDOC   DIM      30        67      Local Doctor
TMPASRCE  DIM       3        97      Admission Source (Cat S )
TMPATYPE  DIM       3        100     Admission Type (Cat A )
TMPACLSS  DIM       3        103     Admission Class (Cat P )
TMPADIAG  DIM     100        106     Admitting Diagnosis 
TMPCLAIM  DIM       3        206     Claim Code (Cat CL)
TMPCARE   DIM       3        209     Care Class (Cat CC)
TMPLSSET  DIM       3        212     Speciality/Unit                         @@
TMPHFUND  DIM       6        215     Health Fund
TMPHFTBL  DIM       8        221     Health Fund Table
TMPMEMB   DIM      10        229     Membership Number
TMPRELIG  FORM      1        239     Religious Visit 1=Yes, 2=No
TMPAUSR1  DIM       3        240     Admission user defined code 1 (Cat U1)
TMPAUSR2  DIM       3        243     Admission user defined code 2 (Cat U2)
TMPAUSR3  DIM       3        246     Admission user defined code 3 (Cat U3)
TMPAUSR4  DIM       3        249     Admission user defined code 4 (Cat U4)
TMPAUSR5  DIM       3        252     Admission user defined code 5 (Cat U5)
TMPAUSR6  DIM       3        255     Admission user defined code 6 (Cat U6)
TMPAUSR7  DIM       3        258     Admission user defined code 7 (Cat U7)
TMPAUSR8  DIM       3        261     Admission user defined code 8 (Cat U8)
TMPAUSR9  DIM       3        264     Admission user defined code 9 (Cat U9)
TMPAUSR0  DIM       3        267     Admission user defined code 0 (Cat U0)
TMPADMWT  DIM       4        270     Admission weight
TMPCDCMP  DIM       8        274     Date Coding Complete (ccyymmdd)
TMPDDATE  DIM       8        282     Discharge Date (ccyymmdd)
TMPDTIME  DIM       8        290     Discharge Time (hh:mm:ss)
TMPDSTAT  DIM       3        298     Discharge Status (Cat D )
TMPDDEST  DIM       3        301     Discharge Destination (Cat DD)
TMPDIAG   DIM      100       304     Discharge Diagnosis
TMPTRAN   DIM       5        404     Discharge Transfer Destination
TMPSSREF  DIM       4        409     Separation Referral                     @@
TMPDUSR1  DIM       3        413     Discharge user defined code 1 (Cat D1)
TMPDUSR2  DIM       3        416     Discharge user defined code 2 (Cat D2)
TMPDUSR3  DIM       3        419     Discharge user defined code 3 (Cat D3)
TMPDUSR4  DIM       3        422     Discharge user defined code 4 (Cat D4)
TMPDUSR5  DIM       3        425     Discharge user defined code 5 (Cat D5)
TMPGEPNO  DIM       2        428     Episode no                              @@ 
TMPGSDRG  DIM       4        430     State DRG Code                          @@ 
TMPGSMDC  DIM       4        434     State MDC Code                          @@ 
TMPSEVIX  DIM       1        438     DRG Severity Index
TMPGRVER  DIM       3        439     3M Grouper Version
TMPGWFIX  FORM   12.4        442     WIES value fixed                
TMPGWVAR  FORM   12.4        459     WIES value variable   
TMPGWTOL  FORM   12.4        476     WIES value total  
TMPGSWGT  FORM    6.4        493     State weight 
TMPGSALS  FORM    4.2        504     State average LOS 
TMPGWWTU  FORM    6.4        511     WIES weight used 
TMPGWWTD  DIM      13        522     WIES weight description 
TMPICUH   FORM      4        535     ICU hours
TMPHOMV   FORM      4        539     Hours on mech ventilation
TMPPRANM  DIM      20        543     P.R.A Name
TMPPRAA1  DIM      35        563     P.R.A address line 1
TMPPRAA2  DIM      35        598     P.R.A address line 2
TMPPRAA3  DIM      35        633     P.R.A address line 3
TMPPRAA4  DIM      35        668     P.R.A address line 4
TMPPRAPC  DIM       8        703     P.R.A post code
TMPPRAPP  DIM      20        711     P.R.A home phone
TMPPRABP  DIM      20        731     P.R.A business phone
TMPPRARE  DIM      10        751     P.R.A Relationship
TMPPDDAT  DIM       8        761     Post discharge date (ccyymmdd)
TMPPDTIM  DIM       8        769     Post discharge time (hh:mm:ss)
TMPPDDOC  DIM       6        777     Post discharge doctor
TMPPDSTA  DIM       3        783     Post discharge status (Cat CP)
. ------------------------------------------------------- Following in PMSVX1FD
TMPXABRG  DIM       3        786     Aboriginality        (PRS/2) (Cat VA)
TMPXCADM  DIM       3        789     Admission Criterion  (PRS/2) (Cat VB
TMPXIRAD  DIM       3        792     Intent to Readmit    (PRS/2) (Cat VR)
TMPXIDUS  DIM       3        795     Intend Stay Duration (PRS/2) Cat VI
TMPXCRAV  DIM       3        798     Carer Availability   (PRS/2) (Cat VK)
TMPXAPWD  DIM       3        801     Admitting Point Ward (patwr1af)
TMPXAPBD  DIM       3        804     Admitting Point Bed (patwr1af)
TMPXPOWD  DIM       3        807     Post-Op Ward (patwr1af)
TMPXPOBD  DIM       3        810     Post-Op Bed (patwr1af)
TMPXPSTY  DIM       1        813     Parent Staying (0=NO,1=YES)
TMPXVALW  DIM       1        814     Visitor Allowed (0=NO,1=YES)
TMPXPALW  DIM       1        815     Phone Calls Allowed? (0=NO,1=YES)
TMPXINDC  DIM       1        816     Intended Day Case (0=NO,1=YES)
TMPXMDAV  DIM       3        817     Mode of Arrival (Cat VM)
TMPXFRMS  DIM       3        820     Forms (Cat VF)
TMPXCSNG  DIM       3        823     Coder Snags (Cat VN)
TMPCHNCU  FORM      4        826     HOURS IN NCU 
TMPCUCCU  FORM      4        830     HOURS IN CRITICAL CARE (CCU)
TMPXPWGT  FORM      6        834     Patient Weight  
TMPXPHGT  FORM      6        840     Patient Height 
TMPXDHWR  DIM       8        846     Date Height/Weight Record. ccyymmdd
. ------------------------------------------------------- Following in PATASFFD
TMPFTYPE  DIM       1        854     SFA type
.                                     A - admission
.                                     L - on leave
TMPFDATE  DIM       8        855     SFA date(ccyymmdd leave only)
TMPFTIME  DIM       8        863     SFA time(leave only)
TMPFCODE  DIM       1        871     SFA code 
TMPFCTYP  DIM       1        872     Contract type
TMPFCROL  DIM       1        873     Contract role
TMPFCSID  DIM       5        874     Contract/spoke id
. -------------------------------------------------------
TMPXRCCT  DIM       3        879     Crit. Care Xfer Reason (PRS/2) (VJ) 
TMPAADTS  DIM       5        882     Admission Transfer Source 
TMPXPOST  DIM       8        887     Post Code 
TMPDWARD  DIM       3        895     Discharge Ward                    *I-90207
TMPDBED   DIM       3        898     Discharge Bed                     *I-90207
.
.End of Record               901
.
. File definition for transfers 
. -----------------------------
ADTTRAN   FILE      ASCII, FIXED=61                                          @@
.
.Name     Type   Length  Start loc.  Description
.----     ----   ------  ----------  -----------
XURNO     DIM       8         1      U/R no
XADMNO    FORM      8         9      Admission no
XTRNDATE  DIM       8        17      Transfer date (ccyymmdd)
XTRNTIME  DIM       8        25      Transfer time (hh:mm:ss)
XRECTY    DIM       1        33      Record Type - C=Transfer/Change         @@
.                                                  L=Going on Leave          @@
.                                                  R=Return from Leave       @@
XATYPE    DIM       3        34      Admission type (Cat A )
XUNIT     DIM       3        37      Unit/Department                         @@
XWARD     DIM       3        40      Ward
XBED      DIM       3        43      Bed
XADOCT    DIM       6        46      Attending doctor
XRATE     FORM      6.2      52      Rate
.
.End of Record               61

+
.******************************************************************************
.*                              GLOBAL VARIABLES                              *
.******************************************************************************
ADTFLAG   FORM      1
BYPASMSG  FORM      1                                                  *I-90213
.
CDYSDAYS  FORM      6
CDYSFDTE  DIM       8
CDYSTDTE  DIM       8
CDYSYEAR  FORM      2
COUNT     FORM      7
COUNTIN   FORM      7
COUNTREJ  FORM      7
COUNTERR  FORM      7
COUNTAUP  FORM      7
COUNTAWR  FORM      7
COUNTDUP  FORM      7
COUNTDWR  FORM      7
COUNTGUP  FORM      7
COUNTGWR  FORM      7
COUNTIUP  FORM      7
COUNTIWR  FORM      7
COUNTPUP  FORM      7
COUNTPWR  FORM      7
COUNTSUP  FORM      7
COUNTSWR  FORM      7
COUNTTDE  FORM      7                                                  *I-90209
COUNTTUP  FORM      7
COUNTTWR  FORM      7
COUNTUUP  FORM      7
COUNTUWR  FORM      7
COUNTVUP  FORM      7
COUNTVWR  FORM      7
COUNTWUP  FORM      7
COUNTWWR  FORM      7
.
DATAERR   FORM      1                                                  *I-90216
DIM3      DIM       3
DIM5      DIM       5
DIM8      DIM       8
DIM10     DIM       10
.
ERRORNO   FORM      2                                                  *I-90209
ERRTRAN   FORM      1                                                  *I-90209
FORM6     FORM      6
KEY127    DIM       127
LSTADMNO  DIM       8                       * Previous Admission No.   *I-90204
.
PDATE     DIM       10
.
REMVCRAP  FORM      1                       * Remove rubbish Transfer  *I-90207
REMVDONE  FORM      1                       * Remove rubbish Transfer  *I-90207
.
TEMP      FILE      ASCII,FIXED=256
TRNFLAG   FORM      1
UPDTADMN  FORM      1
UPDTREQD  FORM      1
UPDTEXTN  FORM      1
UPDTTRAN  FORM      1
.
+
.******************************************************************************
.*                                 CONSTANTS                                  *
.******************************************************************************
SP08      INIT      "        "
SP16      INIT      "                "
SP127     INIT      "                                          ":
                    "                                          ":
                    "                                           "
ZED30     INIT      "~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~"
.
PRGID     INIT      "CONXADT"
PRGNAM    INIT      "Remove Admission/Discharge/Transfer Data"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                 MAINLINE                                   *
.*                           Controlling Logic                                *
.******************************************************************************
ML0000    CALL      INIT0000                * Initialise variables
          BRANCH    EXIT,ML9999
.
ML1000    CALL      OPTN0000                * Select option
          BRANCH    EXIT,ML9000
.
          CALL      OPEN0000                * Open file
          CALL      PROC0000                * process the details
.
ML9000    CLOSE     TEMP,DELETE
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                  INIT0000                                  *
.*                            Initialise Variables                            *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * display screen header
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO          * see if conversion file exists
          OPEN      ADT,"convadt"
          MOVE      OVRCD,ADTFLAG
          TRAPCLR   IO
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO          * see if conversion file exists
          OPEN      ADTTRAN,"convtran"
          MOVE      OVRCD,TRNFLAG
          TRAPCLR   IO
.
          IF        ADTFLAG=1 & TRNFLAG=1
            DISPLAY   *P1:24,*EL,*B,"Data files not found.  ";
            CALL      HITENTER
            GOTO      INIT9500
          ENDIF
.
INIT9000  MOVE      ZERO,EXIT
          GOTO      INIT9999
.
INIT9500  MOVE      ONE,EXIT
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000                                  *
.*                                Select Option                               *
.******************************************************************************
OPTN0000  MOVE      FALSE,EXIT
          DISPLAY   *P1:4,"This program removes all data in the ":
                    "convadt & convtran files";
          KEYIN     *P1:24,*EL,"Ok to Continue (",*V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          MATCH     ANSY,ANS
          GOTO      OPTN9000 IF EQUAL
.
          MATCH     ANSN,ANS
          GOTO      OPTN9500 IF EQUAL
.
          BEEP
          GOTO      OPTN0000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                  OPEN0000                                  *
.*                                 Open Files                                 *
.******************************************************************************
OPEN0000  DISPLAY   *P1:24,*EL,*P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"mrtpdsaf";
          OPEN      MRTPDSA1,"mrtpdsaf"
.
          DISPLAY   *P64:24,"mrtpshaf";
          OPEN      MRTPSHA1,"mrtpshaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdadaf";
          OPEN      PATDADA1,"patdadaf"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATDSCH2,"patdschf"                                *I-90209
.
          DISPLAY   *P64:24,"paticuaf";
          OPEN      PATICUA1,"paticuaf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"patre1af";
          OPEN      PATRE1A1,"patre1af"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN1,"pattranf"
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA1,"patvisaf"
.                                        Special Funding Arrangement Codes
          DISPLAY   *P64:24,"patasfaf";
          OPEN      PATASFA1,"patasfaf"
.                                        Patient Grouper Details
          DISPLAY   *P64:24,"patpgraf";
          OPEN      PATPGRA1,"patpgraf"
.                                        Visit Extension Table
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
OPEN9999  RETURN
+
.******************************************************************************
.*                                  PROC0000                                  *
.*                   Routine to process the convadt.txt file                  *
.******************************************************************************
PROC0000  CLOCK     TIME,CTIMEIS
          MOVE      ZERO,CLNO
          MOVE      ZERO,COUNT
          MOVE      ZERO,COUNTTDE
          CALL      TOTL0000                * Reset Total Counts       *I-90206
          MOVE      SP4,PASSCODE                                       *I-90206
.
          CALL      HEAD0000                * print the error report header
.
          PRINT     *1,"|",*14,"|",*30,"| ":                           *I-90202
                    *50,"Errors on 'convadt.txt'",*132,"|"             *I-90202
          ADD       ONE,CLNO                                           *I-90206
.
          DISPLAY   *P1:6,"Started : ",*V2LON,CTIMEIS,*HOFF:
                    *P30:6,"Finished : ":
                    *P1:8,"Processing U/R       : ":
                    *P1:9,"Admission No.        : ":
                    *P1:10,"Record count         : ";
.
          BRANCH    ADTFLAG,PROC5000
.
.------ read the next record from the convadt file ------
.
PROC1000  MOVE      ZERO,DATAERR                                     
          TRAP      FERR0000 IF FORMAT                               
          READ      ADT,SEQ;TMPUR,TMPADMIS,TMPADATE,TMPATIME,TMPWARD:
                            TMPBED,TMPRDOC,TMPADOC,TMPLDOC,TMPASRCE:
                            TMPATYPE,TMPACLSS,TMPADIAG,TMPCLAIM,TMPCARE:
                            TMPLSSET,TMPHFUND,TMPHFTBL,TMPMEMB,TMPRELIG:
                            TMPAUSR1,TMPAUSR2,TMPAUSR3,TMPAUSR4,TMPAUSR5:
                            TMPAUSR6,TMPAUSR7,TMPAUSR8,TMPAUSR9,TMPAUSR0:
                            TMPADMWT,TMPCDCMP,TMPDDATE,TMPDTIME,TMPDSTAT:
                            TMPDDEST,TMPDIAG,TMPTRAN,TMPSSREF:
                            TMPDUSR1,TMPDUSR2,TMPDUSR3,TMPDUSR4,TMPDUSR5:
                            TMPGEPNO,TMPGSDRG,TMPGSMDC,TMPSEVIX,TMPGRVER:
                            TMPGWFIX,TMPGWVAR,TMPGWTOL,TMPGSWGT,TMPGSALS:
                            TMPGWWTU,TMPGWWTD,TMPICUH,TMPHOMV,TMPPRANM:
                            TMPPRAA1,TMPPRAA2,TMPPRAA3,TMPPRAA4,TMPPRAPC:
                            TMPPRAPP,TMPPRABP,TMPPRARE,TMPPDDAT,TMPPDTIM:
                            TMPPDDOC,TMPPDSTA,TMPXABRG,TMPXCADM,TMPXIRAD:
                            TMPXIDUS,TMPXCRAV,TMPXAPWD,TMPXAPBD,TMPXPOWD:
                            TMPXPOBD,TMPXPSTY,TMPXVALW,TMPXPALW,TMPXINDC:
                            TMPXMDAV,TMPXFRMS,TMPXCSNG,TMPCHNCU,TMPCUCCU:
                            TMPXPWGT,TMPXPHGT,TMPXDHWR,TMPFTYPE,TMPFDATE:
                            TMPFTIME,TMPFCODE,TMPFCTYP,TMPFCROL,TMPFCSID:
                            TMPXRCCT,TMPAADTS,TMPXPOST,TMPDWARD,TMPDBED
.
          TRAPCLR   FORMAT
.
          GOTO      PROC5000 IF OVER
          BRANCH    DATAERR,PROC1000        * next record if data error
.
          MOVE      TMPADMIS,DIM8           * Strip Leading Zeros      *I-90202
          MOVE      DIM8,TMPADMIS           * By moving to DIM & Back  *I-90202
          REP       " 0",TMPADATE
.
          PACK      DIM10,TMPDWARD,TMPDBED,SP10  * Fix NULL Ward & Bed  I-90210
          UNPACK    DIM10,TMPDWARD,TMPDBED                             *I-90210
.
          ADD       ONE,COUNT
          ADD       ONE,COUNTIN                                        *I-90206
          IF        COUNT%100=1
            DISPLAY   *P24:8,*V2LON,TMPUR:
                      *P24:9,TMPADMIS:
                      *P24:10,COUNT;
          ENDIF
.
. check if patient exists on master file
.
          PACK      KEY8,TMPUR
          CALL      RDMAST1                 * read the patient master file
          IF        OVRCD=1
. ---       PRINT     *1,"|",*4,TMPUR,*14,"|",*19,TMPADMIS,*30,"| ":
. ---                 "UR not found on PMI Master file",*132,"|"       *I-90202
. ---       CALL      CPAG0000              * check to see if we need new page
            ADD       ONE,COUNTREJ                                     *I-90206
            GOTO      PROC1000
          ENDIF
.
. ------- Check visit file -------
.
          MOVE      ZERO,UPDTREQD                                          * @@
          PACK      KEY8,TMPADMIS
          CALL      RDVISA1                 * read the patient visit file
          IF        OVRCD = 0
            CALL      DEPTVIS1
            ADD       ONE,COUNTVUP                             
          ENDIF
.
.------ write the visit extension file --------
.
          MOVE      ZERO,UPDTEXTN 
          PACK      KEY8,TMPADMIS
          CALL      RDPMVX11                * read the visit extension file
          IF        OVRCD = 0
            CALL      DEPMVX11                * update visit extension
          ENDIF
.
.
.------ check the admission file ------
.
PROC2000  MOVE      ZERO,UPDTADMN
          PACK      KEY8,TMPADMIS
          CALL      RDMISS1                 * Read admission file
.
          IF        OVRCD=0
            CALL      DEMISS1               * update the admissions file
            ADD       ONE,COUNTAUP                                  
          ENDIF
.
.
. ------- Check transfer file -------
.
PROC2700  MOVE      ZERO,UPDTREQD
          PACK      KEY30,TMPWARD,TMPBED,TMPADATE,TMPATIME,TMPADMIS
          CALL      RDTRAN1                 * read the transfer file
          IF        OVRCD = 0
            CALL      DETRAN1                 * update transfer record
            ADD       ONE,COUNTTUP                                   
          ENDIF
          PACK      KEY30,TMPWARD,TMPBED,SP08,TMPATIME,TMPADMIS
          CALL      RDTRAN1                 * read the transfer file
          IF        OVRCD = 0
            CALL      DETRAN1                 * update transfer record
            ADD       ONE,COUNTTUP                                   
          ENDIF
.
. ----- Write an ICU file record -----
.
PROC3000  COMPARE   ZERO,TMPICUH
          GOTO      PROC3100 IF NOT EQUAL   * ICU hours
.
          COMPARE   ZERO,TMPHOMV
          GOTO      PROC3200 IF EQUAL       * Hours on mech vent       *I-90205
.
PROC3100  MOVE      ZERO,PTICUINT
          MOVE      ZERO,PTICUMEC
          PACK      KEY8,TMPADMIS
          CALL      RDPTICU1                * Read an ICU file record
.
          IF        OVRCD=0
            CALL      DEPTICU1              * Update an ICU file record
            ADD       ONE,COUNTIUP                                     *I-90206
          ENDIF
.
. ----- Special Funding Arrangements -----
.
PROC3200  MATCH     SP8,TMPFDATE
          GOTO      PROC3300 IF EQUAL
.
          PACK      KEY25,TMPADMIS,TMPFTYPE,TMPFDATE,TMPFTIME
          CALL      RDPTASF1                * Read Special Funding Arr. file
.
          IF        OVRCD=0
            CALL      DEPTASF1
            ADD       ONE,COUNTSUP                                     *I-90206
          ENDIF
.
. ------- Write Grouper Details -------
.
PROC3300  MATCH     SP2,TMPGEPNO
          GOTO      PROC3500 IF EQUAL
.
          MOVE      TMPGEPNO,FORM2          * Remove leading zero      *I-90206
          MOVE      FORM2,TMPGEPNO                                     *I-90206
.
          PACK      KEY13,TMPADMIS,TMPGEPNO,TMPGRVER
          CALL      RDPTPGR1                * Read Grouper Details
.
          IF        OVRCD=0
            CALL      DEPTPGR1
            ADD       ONE,COUNTGUP                                     *I-90206
          ENDIF
. --------------------------- End of Addition for RCH/SVHM ---------------- @@
.
. --- check person responsible for account ---
.
PROC3500  MATCH     SP20,TMPPRANM
          GOTO      PROC4000 IF EQUAL       * No PRA
.
          PACK      KEY8,TMPADMIS
          CALL      RDRESP1
.
          IF        OVRCD=0
            CALL      DERESP1
            ADD       ONE,COUNTPUP                                     *I-90206
          ENDIF
.
.
PROC4000
          REP       " 0",TMPDDATE
          PACK      KEY8,TMPADMIS
          CALL      RDDSCH1                 * Is there a discharged record ?
          IF        OVRCD=0
            CALL      DEDSCH1               * update the discharge file
            ADD       ONE,COUNTDUP                                     *I-90206
          ENDIF
.
          PACK      KEY30,TMPDWARD,TMPDBED,TMPDDATE,TMPDTIME,TMPADMIS  *I-90207
          CALL      RDTRAN1                 * read the transfer file
          IF        OVRCD = 0
            CALL      DETRAN1                 * write to the transfer file
            ADD       ONE,COUNTTUP
          ENDIF
          PACK      KEY30,TMPWARD,TMPBED,TMPDDATE,TMPDTIME,TMPADMIS
          CALL      RDTRAN1                 * read the transfer file
          IF        OVRCD = 0
            CALL      DETRAN1                 * write to the transfer file
            ADD       ONE,COUNTTUP
          ENDIF
.
. ----- Transfer Destination File -----
.
PROC4500  MATCH     SP5,TMPTRAN
          GOTO      PROC4600 IF EQUAL       * Discharge transfer code
.
          PACK      KEY8,TMPADMIS
          CALL      RDPTDAD1
.
          IF        OVRCD=0
            CALL      DEPTDAD1
            ADD       ONE,COUNTUUP                                     *I-90206
          ENDIF
.
. ----- Post Discharge Tracking -----
.
PROC4600  PACK      KEY8,TMPADMIS
          CALL      RDMRPDS1                * Read a post dsc header file rec
.
          IF        OVRCD=0
            CALL      DEMRPDS1              * Update a post dsc header file rec
            ADD       ONE,COUNTWUP                                     *I-90206
          ENDIF
.
. ----- Post Discharge Tracking History -----
.
          PACK      KEY21,TMPADMIS,TMPPDDAT,TMPPDTIM
          CALL      RDMRPSH1                * Read a post dsc history file rec
.
          IF        OVRCD=0
            CALL      DEMRPSH1              * Update a post dsc history file rec
          ENDIF
.
          GOTO      PROC1000
.
. - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
.
. --- Process the Transfers ---
.
PROC5000  CALL      TOTL2000                                           *I-90206
          BRANCH    TRNFLAG,PROC9000
.
          PRINT     *1,"|",*14,"|",*30,"| ":                           *I-90202
                    *50,"Errors on 'convtran.txt'",*132,"|"            *I-90202
          ADD       TWO,CLNO                                           *I-90206
          MOVE      ZERO,COUNT                                         *I-90213
.
PROC5500  MOVE      ZERO,DATAERR                                       *I-90216
          TRAP      FERR2000 IF FORMAT                                 *I-90216
          READ      ADTTRAN,SEQ;XURNO,XADMNO,XTRNDATE,XTRNTIME,XRECTY:
                                XATYPE,XUNIT,XWARD,XBED,XADOCT,XRATE
          TRAPCLR   FORMAT                                             *I-90216
.
          GOTO      PROC9000 IF OVER
          BRANCH    DATAERR,PROC5500        * next record if data error I-90216
.
          ADD       ONE,COUNT                                          *I-90213
          ADD       ONE,COUNTIN                                        *I-90206
          IF        COUNT%100=1
            DISPLAY   *P24:8,*V2LON,XURNO:                             *I-90213
                      *P24:9,XADMNO:                                   *I-90213
                      *P24:10,COUNT;                                   *I-90213
          ENDIF                                                        *I-90213
          MOVE      ZERO,BYPASMSG                                      *I-90213
.
          MOVE      XADMNO,DIM8             * Strip Leading Zeros      *I-90202
          MOVE      DIM8,XADMNO             * By moving to DIM & back  *I-90202
.
.                                           ----------------------------------
.                                           See if the Transfer exists -
.                                           If found, update it
.                                           If not found, Check "A" and "D"
PROC5900  MOVE      ZERO,OVRCD                                         *C-90209
          MOVE      ZERO,ERRTRAN                                       *I-90209
          MOVE      ZERO,UPDTTRAN                                      *I-90209
          PACK      KEY30,XWARD,XBED,XTRNDATE,XTRNTIME,XADMNO          *I-90209
          CALL      RDTRAN1                 * re-read Transfer file    *I-90209
          IF        OVRCD = 0
            CALL      DETRAN1                                          *I-90209
            ADD       ONE,COUNTTUP                                     *I-90209
          ENDIF                                                        *I-90209
.
          GOTO      PROC5500                                           *I-90204
.
.
.------ we have finished processing the convadt file ------
.
PROC9000  BRANCH    TRNFLAG,PROC9200                                   *I-90204
          CALL      TOTL4000                * Print final record counts I-90206
.
PROC9200  CLOCK     TIME,CTIMEIS                                       *C-90204
          DISPLAY   *P41:6,*V2LON,CTIMEIS,*P24:10,COUNT,*HOFF:
                    *P1:24,*EL,*B,"Conversion Finished.  ";
          CALL      HITENTER
          CALL      UND132
          PRINT     *N,*N,"*** End of Report ***"
.
PROC9999  RETURN
+
.******************************************************************************
.*                                    HEAD0000                                *
.*                  Routine to print the error report page header             *
.******************************************************************************
HEAD0000  CALL      HEAD132                 * print the page header
.
          CALL      UND132
          PRINT     *1,"| U/R Number | Admission No. | Error Message",*132,"|"
          ADD       ONE,CLNO
          CALL      UND132
.
HEAD9999  RETURN
+
.******************************************************************************
.*                                     CPAG0000                               *
.*                         See if we need to paginate                         *
.******************************************************************************
CPAG0000  ADD       ONE,CLNO
          IF        CLNO>=58
            CALL      HEAD0000              * print the page header
          ENDIF
CPAG9999  RETURN
+
.******************************************************************************
.*                                     FERR0000                               *
.*               Print TRAP data errors in "convadt.txt" & "convtran.txt"     *
.******************************************************************************
.
FERR0000  MOVE      ONE,DATAERR
          PRINT     *1,"|",*14,"|",*30,"| ","Data error at UR No. ",TMPUR:
                    " Admission ",TMPADMIS," - Record No :",COUNTIN:
                    " - Record bypassed",*132,"|"
          READ      ADT,SEQ;TMPUR           * Bypass rest of record
          GOTO      FERR9999
.
FERR2000  MOVE      ONE,DATAERR
          PRINT     *1,"|",*14,"|",*30,"| ","Data error at UR No. ",XURNO:
                    " Admission ",XADMNO," - Record No :",COUNTIN:
                    " - Record bypassed",*132,"|"
          READ      ADTTRAN,SEQ;XURNO       * Bypass rest of record
.
FERR9999  RETURN
+
. ----------------------------------------- Start of Addition --------- I-90206
.******************************************************************************
.*                                     TOTL0000                               *
.*                         Print the Record Counts                            *
.******************************************************************************
.
TOTL0000  MOVE      ZERO,COUNTIN        
          MOVE      ZERO,COUNTREJ        
          MOVE      ZERO,COUNTERR            
          MOVE      ZERO,COUNTAWR              
          MOVE      ZERO,COUNTAUP                  
          MOVE      ZERO,COUNTDWR                     
          MOVE      ZERO,COUNTDUP                        
          MOVE      ZERO,COUNTGWR                           
          MOVE      ZERO,COUNTGUP                               
          MOVE      ZERO,COUNTIWR      
          MOVE      ZERO,COUNTIUP        
          MOVE      ZERO,COUNTPWR          
          MOVE      ZERO,COUNTPUP            
          MOVE      ZERO,COUNTSWR               
          MOVE      ZERO,COUNTSUP                  
          MOVE      ZERO,COUNTTWR                    
          MOVE      ZERO,COUNTTUP                      
          MOVE      ZERO,COUNTUWR                         
          MOVE      ZERO,COUNTUUP                           
          MOVE      ZERO,COUNTVWR                             
          MOVE      ZERO,COUNTVUP                               
          MOVE      ZERO,COUNTWWR                                 
          MOVE      ZERO,COUNTWUP                                  
.
          GOTO      TOTL9999
.
TOTL2000  PRINT     *1,"|",*14,"|",*30,"| ",*50:
                    "Records read from 'convadt.txt'     :",COUNTIN:
                    *132,"|",*N:                                   
                    *1,"|",*14,"|",*30,"| ",*50:                   
                    "        rejected                    :",COUNTREJ:
                    *132,"|",*N:                                  
                    *1,"|",*14,"|",*30,"| ",*50:                  
                    "        in error (not checked)      :",COUNTERR:
                    *132,"|",*N:                                 
                    *1,"|",*14,"|",*30,"| ",*50:               
                    "Records deleted -  Admissions       :",COUNTAUP:
                    *132,"|",*N:                                  
                    *1,"|",*14,"|",*30,"| ",*50:  
                    "Records deleted -  Visit Mast.      :",COUNTVUP:
                    *132,"|",*N:                 
                    *1,"|",*14,"|",*30,"| ",*50:        
                    "Records deleted -  Grouper Details  :",COUNTGUP:
                    *132,"|",*N:                             
                    *1,"|",*14,"|",*30,"| ",*50:        
                    "Records deleted -  Discharge        :",COUNTDUP:
                    *132,"|",*N:                             
                    *1,"|",*14,"|",*30,"| ",*50:        
                    "Records deleted -  Dischrg Tracking :",COUNTWUP:
                    *132,"|",*N:                             
                    *1,"|",*14,"|",*30,"| ",*50:        
                    "Records deleted -  Transfers        :",COUNTTUP:
                    *132,"|",*N:                             
                    *1,"|",*14,"|",*30,"| ",*50:        
                    "Records deleted -  Transfer Destin. :",COUNTUUP:
                    *132,"|",*N
          ADD       TEN,CLNO 
          ADD       TEN,CLNO 
.
          MOVE      ZERO,COUNTIN                           
          MOVE      ZERO,COUNTREJ                           
          MOVE      ZERO,COUNTERR                                
          MOVE      ZERO,COUNTTWR                                
          MOVE      ZERO,COUNTTUP                                
.
          GOTO      TOTL9999
.
TOTL4000  PRINT     *1,"|",*14,"|",*30,"| ",*50:
                    "Records read from 'convtran.txt'    :",COUNTIN:
                    *132,"|",*N:                                   
                    *1,"|",*14,"|",*30,"| ",*50:        
                    "Records deleted from Transfers      :",COUNTTUP:
                    *132,"|",*N
          ADD       FIVE,CLNO 
.
TOTL9999  RETURN
.==============================================================================
.
          INC       STD001IO
          INC       CALCDAYS                * Calculate diff between 2 dates
          INC       CLPATDSC                * Clear disch file variables
          INC       CLPATMIS                * Clear admin file variables
.
          INC       MRTPDSIO/INC
          INC       MRTPSHIO/INC
          INC       PATCODIO/INC
          INC       PATDADIO/INC
          INC       PATDSCIO/INC
          INC       PATICUIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATTRNIO/INC
          INC       PATVISIO/INC
          INC       PATRE1IO/INC
          INC       PATASFIO/INC                                           * @@
          INC       PATPGRIO/INC                                           * @@
          INC       PMSVX1IO/INC                                           * @@
+
.


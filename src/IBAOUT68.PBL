.**********************************************************************
.* System   : OUTPATIENT                                              *
.* Program  : IBAOUT68                                                *
.* Desc     : DVA VACS Report                                         *
.**********************************************************************
.* Date     :  18/07/2001                                             *
.* Author   :  Ebon Clements                                          *
.*                                                                    *
.* Mods     :                                                         *
.*                                                                    *
.*        V12.00.01 30/05/2025  Don Nguyen   TSK 0955096              *
.*                  Alphanumeric visit number changes                 *
.*        V10.05.01 15/07/2014 Ebon Clements CAR 261630               *
.*                  Removed port number from temp file name           *
.*        V9.05.B01 27/10/2005 Ebon Clements CAR 79261                *
.*                  Exclude patients with an outcome of DNA           *
.*        V9.04.01  16/09/2005 J.Tan         CAR 47834                *
.*                  Add option to print total for each clinic type    *
.*        V9.03.03  17/10/2003 Ebon Clements CAR 43674                *
.*                  Removed site prefix from OUTVACFD                 *
.*        V9.03.02  04/07/2003 Ebon Clements CAR 41117                *
.*                  Changed so a booking is a VACS attendance if      *
.*                  indicator 9 is a (P)ublic or (2) = Public RCH     *
.*                  for Cat CL - OBACOMP or if OBACOMP is blank       *
.*                  then indicator 1 is not a (P)rivate for           *
.*                  Cat CI - OTBBCIND                                 *
.*        V9.03.01  11/05/2003 Ebon Clements CAR 39658                *
.*                  Changed so a booking is a VACS attendance if      *
.*                  indicator 9 is a (P)ublic for Cat CL - OBACOMP or *
.*                  if OBACOMP is blank then indicator 1 is not a     *
.*                  (P)rivate for Cat CI - OTBBCIND                   *
.*        V9.02.09  02/12/2002 Lina Vo       22709                    *
.*                  Changed INIT0000 to open outpatient files with    *
.*                  site prefix.                                      *
.*        V9.02.08  17/06/2002 Ebon Clements 18724                    *
.*                  Changed to use OBACOMP to select DVA patients     *
.*        V9.02.07  27/05/2002 Ebon Clements                          *
.*                  Changed OUBB0000 to only count attended booking   *
.*        V9.02.06  15/05/2002 Ebon Clements                          *
.*                  Changed the tempfile to use PORT not CPORT        *
.*        V9.02.05  13/03/2002 Ebon Clements                          *
.*                  Fixed problem with todate check                   *
.*        V9.02.04  14/02/2002 Ebon Clements                          *
.*                  Added outpatient events to VACS report            *
.*        V9.02.03  02/11/2001 Ebon Clements                          *
.*                  Changed so a booking is a VACS attendance if      *
.*                  indicator 1 is not a P for Cat CI - OTBBCIND      *
.*        V9.02.02  02/11/2001 Ebon Clements                          *
.*                  Added details for VACS weights                    *
.*        V9.02.01  11/09/2001 Mike Laming                            *
.*                  Add code to print VACS Code Description (Cat CQ)  *
.*        V9.00.B00  18/7/2001 Ebon Clements                          *
.*                  Created program                                   *
.**********************************************************************
.
          INC       STD001FD
          INC       IBASEQFD/INC
          INC       PATCODFD/INC     * codes file
          INC       PATVISFD/INC     * Visit File
.
          INC       OUTBOAFD/INC     * clinic booking file
          INC       OUTBB1FD/INC     * Clinic session booking table
          INC       OUTCLIFD/INC     * clinic file
          INC       OUTCTYFD/INC     * clinic type file
          INC       OUTSITFD/INC     * site file
          INC       PATDO1FD/INC     * Doctor file
          INC       OUTVACFD/INC     * Outpatient VACS file
          INC       PMSEVTFD/INC     * Event File
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
+
.**********************************************************************
.*                         TEMPFILE DEFINITIONS                       *
.**********************************************************************
.
TEMPFIL1  IFILE     SQL, FIXED=33, KEY="U1-3,4-9,10-15"
.
.NAME     TYPE     LENGTH  PHYSICAL   START     DESCRIPTION
.----     ----     ------  --------   -----     -----------
XXVACS    DIM           3         3       1     VACS Code (Category CQ)
XXCTYP    DIM           6         6       4     Clinic Type 
XXCLIN    DIM           6         6      10     Cliinic Id
XXTATT    FORM          8         5      16     Total DVA Attendances 
XXTVAC    FORM          8         5      21     Total VACS Attendances
XXTWGH    FORM          8.4       7      26     Total VACS Weight
.
.                  end of record   33
.
ERASE     INIT      "iserase "
FNAME     INIT      "tmpout"          * set up for temp file name
ISBUILD   INIT      "isbuild "
TEMPFILE  DIM       8
UKEY      INIT      " 33 U1-3,4-9,10-15"    * set up for record length & key
.
CHKOUTNO  DIM       8
CURRDATE  DIM       8
CMDLINE   DIM       50
COUNTER   FORM      8
FROMDATE  DIM       8
FNAMEI    DIM       8
SITE      DIM       6        * site code
TODATE    DIM       8
PRTTCTYP  FORM      1
PRTODATE  DIM       10       * Display date from
PRFRDATE  DIM       10       * Display date to
PRINCTYP  DIM       19       * Clinic Type description for print
PRINCLID  DIM       19       * Clinic ID Description  
SAIND     DIM       1        * Save Indicator for future date message
SAVVACS   DIM       3        * Save VACS code
SAVCTYP   DIM       6        * Save clinic type
VACSFLAG  FORM      1        * VACS Flag
DVAFLAG   FORM      1        * DVA Flag    
WTDESC    DIM       20       * Total Line VACS Description             *I-90201
WEIGHT    FORM      4.4      * VACS Weight
YYTVAC    FORM      8        * Grand total VAC Attendances
YYTATT    FORM      8        * Grand total DVA Attendances
YYTWGH    FORM      8.4      * Grand total Weighted Attendances
PRVACS    DIM       3        * Print VACS code
.
PRGID     INIT      "IBAOUT68"
PRGNAM    INIT      "DVA VACS Report"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                         MAINLINE                                   *
.**********************************************************************
.
ML0000    CALL      INIT0000        * initialisation
.
ML1000    CALL      OPTN0000        * select option
          BRANCH    EXIT,ML9000
.
          BRANCH    OPTION,ML2000
.
ML2000    CALL      DATE0000        * keyin date range
          CALL      TCTY0000        * Print total of each clinic type
.
          CALL      CONTQST         * ok to continue
          BRANCH    CEXIT,ML3000,ML2000,ML9000    * 1=Yes, 2=No, 3=Cancel
.
ML3000    CALL      CREA0000        * create tempfile
          CALL      OUBB0000        * process data
          CALL      PMEV0000        * process outpatient event data
          CALL      PRIN0000        * print routine
          CALL      ENDP0000        * end of report
          CALL      KILL0000        * remove tempfile
          GOTO      ML1000
.
ML9000    CHAIN     PGM
+
.**********************************************************************
.*                         INIT0000                                   *
.*                      Initialisation                                *
.**********************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P64:24,"patcodes";             * Codes File
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patvisaf";             * Visit File
          OPEN      PATVISA1,"patvisaf"
.
          DISPLAY   *P64:24,"patdo1af";             * Doctors file
          OPEN      PATDO1A1,"patdo1af"
.
          OPEN      OUTVACA1,"outvacaf"             * VACS file
.
          DISPLAY   *P64:24,"pmsevtaf";             * Event file
          OPEN      PMSEVTA4,"pmsevtaf"
.
          PACK      CFNAME WITH UID,FILBB1A2        * OUTBB1FD
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTBB1A2,CFNAME
.
          PACK      CFNAME WITH UID,FILBOKA1        * OUTBOKFD
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTBOKA2,CFNAME
.
          PACK      CFNAME WITH UID,FILBOKA5        * OUTBOKFD
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTBOKA6,CFNAME
.
          PACK      CFNAME WITH UID,FILCLIA1        * OUTCLIFD
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTCLIA1,CFNAME
.
          PACK      CFNAME WITH UID,FILCTYA1        * OUTCTYFD
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTCTYA1,CFNAME
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,TEMPFILE
.
          DISPLAY   *P64:24,"outsitaf";
          OPEN      OUTSITA1,"outsitaf"
          MOVE      IDDD,SITE
          MOVE      IDDD,KEY6
          CALL      RDSITA1
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          RETURN
.**************************************************************************
.*                              CREA0000               Called by:         *
.*             create a new temporary file                                *
.**************************************************************************
.
CREA0000  CALL      KILL0000                     * remove existing file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,TEMPFILE,UKEY
          EXECUTE   CMDLINE,TASKID               * create temporary index file
.
          OPEN      TEMPFIL1,TEMPFILE            * open temp index file
.
CREA9999  RETURN
+
.****************************************************************************
.*                              KILL0000               Called by:           *
.*               close and erase the temporary file                         *
.****************************************************************************
.
KILL0000  CLOSE     TEMPFIL1                     * close file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ERASE,TEMPFILE       * set file erase command
          EXECUTE   CMDLINE,TASKID               * erase temp file
.
KILL9999 RETURN
+
.***************************************************************************
.*                               DATE0000                                  *
.*                          accept date range                              *
.***************************************************************************
.
.---------- keyin first date range -------
.
DATE0000    MOVE       FALSE,EXIT
            DISPLAY    *P1:9,*EF,"Outpatient Date From :":
                          *P1:10,"Outpatient Date To   :";
.
            MOVE      "9",CVERT
            MOVE      "9",EVERT
            CALL      IBACLOCK 
            PACK      CURRDATE,CCC,CYY,CMM,CDD
            REP       " 0",CURRDATE
            MOVE      CCC,CCENT
            MOVE      CYY,CYEAR
            MOVE      CMM,CMON
            MOVE      CDD,CDAY
            CALL      KEYDATE                          * enter the from date
            PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY   * move to form field
            REP       " 0",FROMDATE                    * from date
.
            MOVE       "10",CVERT
            MOVE       "10",EVERT
            CALL      KEYDATE                          * enter the from date
            PACK      TODATE,CCENT,CYEAR,CMON,CDAY     * move to form field
            REP       " 0",FROMDATE                    * from date
.
            MATCH      FROMDATE,TODATE
            GOTO       DATE9999 IF NOT LESS
.
            DISPLAY    *P1:24,*EL,*B,"2nd Range Must Be Greater Than 1st ":
                       "Range - Hit <",*V2LON,"ENTER",*HOFF,"> To Continue ";
            KEYIN      ANS;
            DISPLAY    *P1:24,*EL;
            GOTO       DATE0000
.
DATE9999    RETURN
+
.**********************************************************************
.*                      TCTY0000                                      *
.*                 Print total for each clinic type                   *
.**********************************************************************
TCTY0000    MOVE       ZERO,PRTTCTYP
            MOVE       "N",ANS
            KEYIN      *P1:15,*EL,"Do you want to print total for each Clinic":
                       " Type ? ",*DV,*RV,ANS:
                       *P51:15,*V2LON,ANS;
            RESET      ANS
            CMATCH     "Y",ANS
            GOTO       TCTY9999 IF NOT EQUAL
            MOVE       ONE,PRTTCTYP
TCTY9999    RETURN
+
.**********************************************************************
.*                      OUBB0000                                      *
.*                      Process Data                                  *
.**********************************************************************
OUBB0000  PACK      KEY16,FROMDATE,SP70
          CALL      RDSBOKB2
OUBB1000  CALL      RDKBOKB2
          BRANCH    OVRCD,OUBB9999
.
          MATCH     OBADATE,TODATE
          GOTO      OUBB9999 IF LESS
.
          MATCH     SP70,OTBBOUTC           * Check outcome 
          GOTO      OUBB1100 IF EQUAL
.
          PACK      KEY5,ANSO,ANSZ,OTBBOUTC,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,OUBB1100
.
          MATCH     "1",TCINDC1             * Skip if outcome is DNA
          GOTO      OUBB1000 IF EQUAL
.
OUBB1100  MOVE      OBAOUTNO,CHKOUTNO
          PACK      KEY36,OBAOUTNO,OBADATE,SP70 * Only count attended patients
          CALL      RDSBOKA6
OUBB1200  CALL      RDKBOKA6
          BRANCH    OVRCD,OUBB1000
.
          MATCH     OBAOUTNO,CHKOUTNO
          GOTO      OUBB1000 IF NOT EQUAL
.
          COMPARE   FOUR,OBASTAT                * Attended status
          GOTO      OUBB1200 IF NOT EQUAL
.
          MOVE      ZERO,VACSFLAG               * Check if VACS patient
          MOVE      ZERO,DVAFLAG                * Check if DVA patient
.
          MATCH     SP70,OBACOMP
          GOTO      OUBB1400 IF EQUAL
.
          PACK      KEY5,ANSC,ANSL,OBACOMP,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,OUBB1000
.
          MATCH     ANSP,TCINDC9               * Yes it is a VACS Patient
          IF        @EQUAL
            MOVE      ONE,VACSFLAG
          ENDIF
.
          MATCH     "2",TCINDC9                * Yes it is a VACS Patient
          IF        @EQUAL
            MOVE      ONE,VACSFLAG
          ENDIF
.
          GOTO      OUBB1500
.
OUBB1400  MATCH     SP70,OTBBCIND
          GOTO      OUBB1500 IF EQUAL
.
          PACK      KEY5,ANSC,ANSI,OTBBCIND,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,OUBB1000
.
          MATCH     ANSP,TCINDC1                * Yes it is a VACS Patient
          IF        !@EQUAL
            MOVE      ONE,VACSFLAG
          ENDIF
.
OUBB1500  MATCH     SP70,OBACOMP
          GOTO      OUBB1600 IF EQUAL
.
          PACK      KEY5,ANSC,ANSL,OBACOMP,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,OUBB1000
.
          MATCH     ANSV,TCINDC1                * Yes it is a DVA Patient
          IF        @EQUAL
            MOVE      ONE,DVAFLAG
          ENDIF
.
          MATCH     ANSV,TCINDC2                * Yes it is a DVA Patient
          IF        @EQUAL
            MOVE      ONE,DVAFLAG
          ENDIF
.
          MATCH     ANSV,TCINDC3                * Yes it is a DVA Patient
          IF        @EQUAL
            MOVE      ONE,DVAFLAG
          ENDIF
.
          MATCH     ANSV,TCINDC4                * Yes it is a DVA Patient
          IF        @EQUAL
            MOVE      ONE,DVAFLAG
          ENDIF
.
          MATCH     ANSV,TCINDC5                * Yes it is a DVA Patient
          IF        @EQUAL
            MOVE      ONE,DVAFLAG
          ENDIF
.
OUBB1600  PACK      KEY12,IDDD,OBBCTYP         * Read Clinic to get VACS Code
          CALL      RDCTYA1
          BRANCH    OVRCD,OUBB1000
.
          PACK      KEY8,OBAOUTNO              * Read visit to get Clinic ID
          CALL      RDVISA1
          BRANCH    OVRCD,OUBB1000
.
          COMPARE   ZERO,DVAFLAG               * Skip if not DVA or VACS
          IF        @EQUAL
            COMPARE   ZERO,VACSFLAG
            GOTO      OUBB1000 IF EQUAL
          ENDIF
.
          MATCH     OBADATE,CURRDATE
          GOTO      OUBB2000 IF LESS
.
          GOTO      OUBB2500
.
OUBB2000  MOVE      "1",SAIND
.
OUBB2500  MOVE      ZERO,WEIGHT                 * Get VACS weight for OBADATE
          PACK      KEY11,OCTVACS,OBADATE,Z70
          CALL      RSOTVAC1
          CALL      RPOTVAC1
          BRANCH    OVRCD,OUBB3000
.
          MATCH     OTVACODE,OCTVACS
          GOTO      OUBB3000 IF NOT EQUAL
.
          MOVE      OTVAWGHT,WEIGHT
.
OUBB3000  PACK      KEY15,OCTVACS,OBBCTYP,PVIDOCT  * Store Each VACS Code 
          CALL      RDTEMP1
          IF        OVRCD = 1
            MOVE      OCTVACS,XXVACS
            MOVE      OBBCTYP,XXCTYP
            MOVE      PVIDOCT,XXCLIN
            MOVE      ZERO,XXTATT
            IF        DVAFLAG = 1
              MOVE      ONE,XXTATT   
            ENDIF
            MOVE      ZERO,XXTVAC
            MOVE      ZERO,XXTWGH
            IF        VACSFLAG = 1
              MOVE      ONE,XXTVAC   
              MOVE      WEIGHT,XXTWGH
            ENDIF
            CALL      WRTEMP1
          ELSE
            IF        DVAFLAG = 1
              ADD       ONE,XXTATT   
            ENDIF
            IF        VACSFLAG = 1
              ADD       ONE,XXTVAC   
              ADD       WEIGHT,XXTWGH
            ENDIF
            CALL      UPTEMP1
          ENDIF
.
          PACK      KEY15,OCTVACS,Z70       * Store Total For Each VACS Code 
          CALL      RDTEMP1
          IF        OVRCD = 1
            MOVE      OCTVACS,XXVACS
            MOVE      Z70,XXCTYP
            MOVE      Z70,XXCLIN
            MOVE      ZERO,XXTATT
            IF        DVAFLAG = 1
              MOVE      ONE,XXTATT   
            ENDIF
            MOVE      ZERO,XXTVAC
            MOVE      ZERO,XXTWGH
            IF        VACSFLAG = 1
              MOVE      ONE,XXTVAC   
              ADD       WEIGHT,XXTWGH
            ENDIF
            CALL      WRTEMP1
          ELSE
            IF        DVAFLAG = 1
              ADD       ONE,XXTATT   
            ENDIF
            IF        VACSFLAG = 1
              ADD       ONE,XXTVAC   
              ADD       WEIGHT,XXTWGH
            ENDIF
            CALL      UPTEMP1
          ENDIF
.
          PACK      KEY15,Z70               * Store Grand Total
          CALL      RDTEMP1
          IF        OVRCD = 1
            MOVE      Z70,XXVACS
            MOVE      Z70,XXCTYP
            MOVE      Z70,XXCLIN
            MOVE      ZERO,XXTATT
            IF        DVAFLAG = 1
              MOVE      ONE,XXTATT   
            ENDIF
            MOVE      ZERO,XXTVAC
            MOVE      ZERO,XXTWGH
            IF        VACSFLAG = 1
              MOVE      ONE,XXTVAC   
              ADD       WEIGHT,XXTWGH
            ENDIF
            CALL      WRTEMP1
          ELSE
            IF        DVAFLAG = 1
              ADD       ONE,XXTATT   
            ENDIF
            IF        VACSFLAG = 1
              ADD       ONE,XXTVAC   
              ADD       WEIGHT,XXTWGH
            ENDIF
            CALL      UPTEMP1
          ENDIF
.
          GOTO        OUBB1000
.
OUBB9999  RETURN
+
.**********************************************************************
.*                      PMEV0000                                      *
.*                      Process Data                                  *
.**********************************************************************
PMEV0000  PACK      KEY16,FROMDATE,SP70
          CALL      RSPMEVT4
PMEV1000  CALL      RKPMEVT4
          BRANCH    OVRCD,PMEV9999
.
          MATCH     PMEVADTE,TODATE
          GOTO      PMEV9999 IF LESS
.
          PACK      KEY5,ANSE,ANSV,PMEVEVNT,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,PMEV1000
.
          MATCH     ANSO,TCINDC1                * Outpatient events only
          GOTO      PMEV1000 IF NOT EQUAL
.
          MOVE      ZERO,DVAFLAG
          MOVE      ZERO,VACSFLAG
.
          PACK      KEY12,IDDD,PMEVCTYP         * Read Clinic to get VACS Code
          CALL      RDCTYA1
          BRANCH    OVRCD,PMEV1000
.
          MATCH     SP70,OCTVACS
          IF        !@EQUAL
            MOVE      ONE,VACSFLAG
          ENDIF
.
          MATCH     SP70,PMEVCCOD
          GOTO      PMEV1900 IF EQUAL
.
          PACK      KEY5,ANSC,ANSL,PMEVCCOD,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,PMEV1000
.
          MATCH     ANSV,TCINDC1                * Yes it is a DVA Patient
          IF        @EQUAL
            MOVE      ONE,DVAFLAG
          ENDIF
.
          MATCH     ANSV,TCINDC2                * Yes it is a DVA Patient
          IF        @EQUAL
            MOVE      ONE,DVAFLAG
          ENDIF
.
          MATCH     ANSV,TCINDC3                * Yes it is a DVA Patient
          IF        @EQUAL
            MOVE      ONE,DVAFLAG
          ENDIF
.
          MATCH     ANSV,TCINDC4                * Yes it is a DVA Patient
          IF        @EQUAL
            MOVE      ONE,DVAFLAG
          ENDIF
.
PMEV1900  COMPARE   ZERO,DVAFLAG               * Skip if not DVA or VACS
          IF        @EQUAL
            COMPARE   ZERO,VACSFLAG
            GOTO      PMEV1000 IF EQUAL
          ENDIF
. 
          MATCH     PMEVADTE,CURRDATE
          GOTO      PMEV2000 IF LESS
.
          GOTO      PMEV2500
.
PMEV2000  MOVE      "1",SAIND
.
PMEV2500  MOVE      ZERO,WEIGHT                 * Get VACS weight for PMEVADTE
          PACK      KEY11,OCTVACS,PMEVADTE,Z70
          CALL      RSOTVAC1
          CALL      RPOTVAC1
          BRANCH    OVRCD,PMEV3000
.
          MATCH     OTVACODE,OCTVACS
          GOTO      PMEV3000 IF NOT EQUAL
.
          MOVE      OTVAWGHT,WEIGHT
.
PMEV3000  PACK      KEY15,OCTVACS,PMEVCTYP,PMEVCLID,SP70  * Store Each VACS Code
          CALL      RDTEMP1
          IF        OVRCD = 1
            MOVE      OCTVACS,XXVACS
            MOVE      PMEVCTYP,XXCTYP
            MOVE      PMEVCLID,XXCLIN
            MOVE      ZERO,XXTATT
            IF        DVAFLAG = 1
              MOVE      ONE,XXTATT
            ENDIF
            MOVE      ZERO,XXTVAC
            MOVE      ZERO,XXTWGH
            IF        VACSFLAG = 1
              MOVE      ONE,XXTVAC
              MOVE      WEIGHT,XXTWGH
            ENDIF
            CALL      WRTEMP1
          ELSE
            IF        DVAFLAG = 1
              ADD       ONE,XXTATT
            ENDIF
            IF        VACSFLAG = 1
              ADD       ONE,XXTVAC
              ADD       WEIGHT,XXTWGH
            ENDIF
            CALL      UPTEMP1
          ENDIF
.
          PACK      KEY15,OCTVACS,Z70       * Store Total For Each VACS Code
          CALL      RDTEMP1
          IF        OVRCD = 1
            MOVE      OCTVACS,XXVACS
            MOVE      Z70,XXCTYP
            MOVE      Z70,XXCLIN
            MOVE      ZERO,XXTATT
            IF        DVAFLAG = 1
              MOVE      ONE,XXTATT
            ENDIF
            MOVE      ZERO,XXTVAC
            MOVE      ZERO,XXTWGH
            IF        VACSFLAG = 1
              MOVE      ONE,XXTVAC
              ADD       WEIGHT,XXTWGH
            ENDIF
            CALL      WRTEMP1
          ELSE
            IF        DVAFLAG = 1
              ADD       ONE,XXTATT
            ENDIF
            IF        VACSFLAG = 1
              ADD       ONE,XXTVAC
              ADD       WEIGHT,XXTWGH
            ENDIF
            CALL      UPTEMP1
          ENDIF
.
          PACK      KEY15,Z70               * Store Grand Total
          CALL      RDTEMP1
          IF        OVRCD = 1
            MOVE      Z70,XXVACS
            MOVE      Z70,XXCTYP
            MOVE      Z70,XXCLIN
            MOVE      ZERO,XXTATT
            IF        DVAFLAG = 1
              MOVE      ONE,XXTATT
            ENDIF
            MOVE      ZERO,XXTVAC
            MOVE      ZERO,XXTWGH
            IF        VACSFLAG = 1
              MOVE      ONE,XXTVAC
              ADD       WEIGHT,XXTWGH
            ENDIF
            CALL      WRTEMP1
          ELSE
            IF        DVAFLAG = 1
              ADD       ONE,XXTATT
            ENDIF
            IF        VACSFLAG = 1
              ADD       ONE,XXTVAC
              ADD       WEIGHT,XXTWGH
            ENDIF
            CALL      UPTEMP1
          ENDIF
.
          GOTO        PMEV1000
.
PMEV9999  RETURN
+
.**********************************************************************
.*                           ENDP0000                                 *
.*                      Print the end of the report                   *
.**********************************************************************
ENDP0000  CALL      UND132                   * finish off report
.
          PRINT     *N,*50,"*** End of Report ***"
ENDP9999  RETURN
+
.**********************************************************************
.*                         PRIN0000                                   *
.*                        Print Data                                  *
.**********************************************************************
.
PRIN0000  CALL      HEAD0000
.
          MOVE      ZERO,COUNTER
          MOVE      SP70,SAVVACS
          PACK      KEY15,SP70
          CALL      RDSTEMP1
PRIN1000  CALL      RDKTEMP1
          BRANCH    OVRCD,PRIN9999
.
          MATCH     Z70,XXVACS
          GOTO      PRIN5000 IF EQUAL       * Print Grand Total
          ADD       ONE,COUNTER
.
          COMPARE    ONE,PRTTCTYP
          GOTO       PRIN1200 IF NOT EQUAL
.
          COMPARE   ONE,COUNTER
          IF        !@EQUAL
            MATCH     SAVCTYP,XXCTYP              * Print total for clinic type
            IF        !@EQUAL
              PRINT     *1,"|",*8,"|",*10,"Total":
                        *40,"|",*87,"|",*89,YYTATT,*101,"|":
                        *103,YYTVAC,*115,"|",*117,YYTWGH,*132,"|"
              PRINT     *1,"|",*8,"|",*40,"|",*87,"|",*101,"|",*115,"|",*132,"|"
              MOVE      ZERO,YYTATT
              MOVE      ZERO,YYTVAC
              MOVE      ZERO,YYTWGH
            ENDIF
          ENDIF
          MOVE      XXCTYP,SAVCTYP
.
.-------- print detail line -----
.
PRIN1200  MATCH     Z70,XXCTYP             * Print Total for each VACS Code
          IF        @EQUAL
            MOVE      SP70,WTDESC           * Clear Description        *I-90201
            PACK      KEY5,ANSC,ANSQ,XXVACS                            *I-90201
            CALL      RDCODE1                                          *I-90201
            COMPARE   ZERO,OVRCD                                       *I-90201
            MOVE      TDESC,WTDESC IF EQUAL                            *I-90201
.
            CALL      UND132
            ADD       ONE,CLNO
            PRINT     *1,"|",*3,"Total Code ",XXVACS:
                      " - ",WTDESC:                                    *I-90201
                      *87,"|",*89,XXTATT,*101,"|":
                      *103,XXTVAC,*115,"|",*117,XXTWGH,*132,"|"
            CALL      UND132
            ADD       ONE,CLNO
            MOVE      ZERO,COUNTER                                     *I-90201
            GOTO      PRIN1000
          ENDIF
.
          MOVE      SP70,PRINCTYP
          PACK      KEY12,IDDD,XXCTYP,SP70    * Read Clinic to get VACS Code
          CALL      RDCTYA1
          BRANCH    OVRCD,PRIN1500
          MOVE      OCTDESC,PRINCTYP
.
PRIN1500  MOVE      SP70,PRINCLID
          PACK      KEY20,IDDD,XXCLIN,Z70
          CALL      RDSCLIA1
          CALL      RDPCLIA1
          BRANCH    OVRCD,PRIN2000
. 
          MATCH     OCASITE,IDDD
          GOTO      PRIN2000 IF NOT EQUAL
          MATCH     OCACLIN,XXCLIN
          GOTO      PRIN2000 IF NOT EQUAL
.
          MOVE      OCADESC,PRINCLID
.
PRIN2000  MOVE      XXVACS,PRVACS
          MATCH     SAVVACS,XXVACS
          IF        @EQUAL
            MOVE     SP70,PRVACS
          ENDIF
.
          PRINT     *1,"|",*3,PRVACS,*8,"|",*10,PRINCTYP:
                      *40,"|",*42,PRINCLID,*87,"|",*89,XXTATT:
                      *101,"|",*103,XXTVAC,*115,"|",*117,XXTWGH,*132,"|"
.
          ADD       XXTATT,YYTATT
          ADD       XXTVAC,YYTVAC
          ADD       XXTWGH,YYTWGH
          MOVE      XXVACS,SAVVACS
.
          ADD       ONE,CLNO
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          GOTO      PRIN1000              * read next record
.
PRIN5000  CALL      UND132
          ADD       ONE,CLNO
          PRINT     *1,"|",*3,"Grand Total":
                    *87,"|",*89,XXTATT,*101,"|":
                    *103,XXTVAC,*115,"|",*117,XXTWGH,*132,"|"
.
          MATCH     "1",SAIND
          GOTO      PRIN9999 IF NOT EQUAL
.
          PRINT     *1,"|",*132,"|"
          PRINT     *1,"| Report ran into the future. May ultimately, not be ":
                    *54,"accurate.",*132,"|"
          PRINT     *1,"|",*132,"|"
.
.
PRIN9999  RETURN
+
.**********************************************************************
.*                      OPTN0000                                      *
.*                      Main Option Screen                            *
.**********************************************************************
.
OPTN0000  MOVE      "0",SAIND
          MOVE      FALSE,EXIT
          HLINE     *G37,2,53,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Outpatient DVACS Report";
.
OPTN1000  DISPLAY   *P1:7,*EL,"Option : ";
          KEYIN     *P10:7,*V2LON,OPTION;
.
          BRANCH    OPTION,OPTN9999
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9000 IF EQUAL
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      TRUE,EXIT
.
OPTN9999  RETURN
+
.*********************************************************************
.         print a new page
.*********************************************************************
HEAD0000  CALL      HEAD132
.
          UNPACK    FROMDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,PRFRDATE         * formatted start date
          UNPACK    TODATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,PRTODATE         * formatted start date

          PRINT     *20,"From : ",PRFRDATE," to ",PRTODATE
.
          CALL      UND132
          PRINT     *1,"| VACS ",*8,"| Clinic Type",*40,"| Clinic ID.":
                    *87,"| DVA ",*101,"| VACS ":
                    *115,"| Total Weighted",*132,"|"
          PRINT     *1,"| Code ",*8,"|",*40,"|":
                    *87,"| Attendances",*101,"| Attendances":
                    *115,"| Attendances",*132,"|"
.
          CALL      UND132
.
HEAD9999  RETURN
+
.**********************************************************************
.*              I/O SUBROUTINE FOR TEMPFILE                           *
.**********************************************************************
.
RDSTEMP1  MOVE      ZERO,OVRCD
          RESET     KEY15
          READ      TEMPFIL1,KEY15;;
          GOTO      OVERCOND IF OVER
          RETURN
.
RDKTEMP1  MOVE      ZERO,OVRCD
          READKS    TEMPFIL1;XXVACS,XXCTYP,XXCLIN,XXTATT,XXTVAC,XXTWGH
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   RESET     KEY15
          WRITE     TEMPFIL1,KEY15;KEY15,XXTATT,XXTVAC,XXTWGH
          RETURN
.
RDTEMP1   RESET     KEY15
          MOVE      ZERO,OVRCD
          READ      TEMPFIL1,KEY15;XXVACS,XXCTYP,XXCLIN,XXTATT,XXTVAC,XXTWGH
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTEMP1   RESET     KEY15
          UPDATE    TEMPFIL1;XXVACS,XXCTYP,XXCLIN,XXTATT,XXTVAC,XXTWGH
          RETURN
.
.**********************************************************************
.*              I/O INCLUDES FOR FILES AND GENERAL ROUTINES           *
.**********************************************************************
.
          INC       IBASEQIO/INC
          INC       PATCODIO/INC
          INC       PATVISIO/INC
          INC       OUTBOAIO/INC
          INC       OUTBB1IO/INC
          INC       OUTCLIIO/INC
          INC       OUTCTYIO/INC
          INC       OUTSITIO/INC
          INC       PATDO1IO/INC
          INC       OUTVACIO/INC
          INC       PMSEVTIO/INC
          INC       WEBERRIO/INC
.
          INCLUDE   STD001IO
          INC       TFILENAM
.

.******************************************************************************
.*    Program      : FX299117                                                 *
.*    Description  : Loop through pmsvx1af and update all records on oprdetaf *
.*                   IFF pmvxatcp=1 (where pmvxvisn=opdaadmn); i.e. set the   *
.*                   'All charges posted' (OPDAACPD) to '1'.                  *
.*                                                                            *
.*    Author       : Don Nguyen                                               *
.*    Date         : 21/07/2015                                               *
.*                                                                            *
.*    Modifications:                                                          *
.*        V10.06.00  21/07/2015  Don Nguyen           CAR 299117              *
.*                   Created program                                          *
.******************************************************************************
.
          INC       STD001FD
.
          INC       PMSVX1FD/INC
          INC       OPRDEAFD/INC
.
FINDFILE  FILE      ASCII, FIXED=256
.
. LOCAL VARIABLES
. ---------------
DISNUM    FORM      5
RECNUM    FORM      8
RECNDISP  DIM       8
UPDTCNTR  FORM      8
UPDTDISP  DIM       8
.
.
. PROGRAM CONSTANTS
. -----------------
.
.
PRGNAM    INIT      "FIXIT for oprdetaf - update opdaacpd=1 for pmvxatcp=1    "
PRGID     INIT      "FX299117"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                           MAIN0000                                         *
.*                  Program Mainline                                          *
.******************************************************************************
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
          CALL      OPTN0000                      * get option
          BRANCH    COPTION,MAIN1000              * run fixit
          GOTO      MAIN9999                      * exit selected
.
MAIN1000  CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN5000:               * yes
                          MAIN0000:               * no
                          MAIN9999                * cancel
.
.         Running fixit
.
MAIN5000  CALL      PROC0000                      * read old records and write
.
MAIN9999  CHAIN     PGM
+
.******************************************************************************
.*                           INIT0000                 Called by: MAIN0000     *
.*        Display heading and check that the files needed exists & open them  *
.******************************************************************************
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
          MOVE      SP70,RECNDISP
          MOVE      ZERO,UPDTCNTR
          MOVE      SP70,UPDTDISP
          MOVE      ZERO,DISNUM
.
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      OPRDETA2,"oprdetaf"
.
INIT9999  RETURN
+
.******************************************************************************
.*                           OPTN0000                 Called by: MAIN0000     *
.*                  Get user options or exit                                  *
.*    Returns:  EXIT    = FALSE (0)  Ok to proceed                            *
.*                        TRUE  (1)  Exit option selected                     *
.*              COPTION - option selected                                     *
.******************************************************************************
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ":
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.******************************************************************************
.*                           PROC0000                 Called by: MAIN0000     *
.*        Loop through pmsvx1af and check if PMVXATCP=1; update oprdetaf      *
.*        accordingly for all records with matching visit number.             *
.******************************************************************************
PROC0000  DISPLAY   *P1:20,*EF,"Record No. : ";
.
          PACK      KEY8,SP70
          CALL      RSPMVX11                      * position at start
PROC1000  CALL      RKPMVX11                      * read next record
          BRANCH    OVRCD,PROC2000                * no more records
.
          MATCH     "1",PMVXATCP
          GOTO      PROC1000 IF NOT EQUAL
.
.         Update all oprdetaf records for this visit
.
          PACK      KEY31,PMVXVISN,SP70
          CALL      RSOPDEA2
PROC1100  CALL      RKOPDEA2
          BRANCH    OVRCD,PROC1000
.
          MATCH     PMVXVISN,OPDAADMN
          GOTO      PROC1000 IF NOT EQUAL
.
          ADD       ONE,DISNUM              * Display counter
          ADD       ONE,RECNUM              * update record counter
.
          IF        DISNUM = 10000  | RECNUM = 1
            MOVE      ZERO,DISNUM
          ENDIF
.
          MOVE      RECNUM,RECNDISP
          LJUSTIFY  RECNDISP
          PACK      RECNDISP,RECNDISP,SP70
          STRIP     RECNDISP
          DISPLAY   *P15:20,*V2LON,*+,RECNDISP,*-,*HOFF;
.
          MOVE      ONE,OPDAACPD            * All charges posted = 1
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          CALL      UPOPDEA2
          ADD       ONE,UPDTCNTR            * update counter for records updated
          TRAPCLR   IO
.
          GOTO      PROC1100                * get next oprdetaf record
.
PROC2000  CALL      IBACLOCK
          REP       " 0",CDATE
          MOVE      UPDTCNTR,UPDTDISP
          LJUSTIFY  UPDTDISP
          PACK      UPDTDISP,UPDTDISP,SP70
          STRIP     UPDTDISP
          DISPLAY   *P1:22,*EF,"Ended  : ",CDATE,SP1,CTIMEIS," ... ":
                    *P35:22,*V2LON,*+,UPDTDISP,*-,*HOFF," / ":
                    *V2LON,*+,RECNDISP,*-,*HOFF," records updated !!":
                    *P1:24;
          CALL      HITENTER
.
          GOTO      PROC9999 
.
PROC9999  RETURN
+
.------------------------------------------------------------------------------
.
          INC       STD001IO
.
          INC       PMSVX1IO/INC
          INC       OPRDEAIO/INC

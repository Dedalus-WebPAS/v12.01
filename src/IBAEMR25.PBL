.***************************************************************************
.* System    :   Emergency                                                 *
.* Program   :   IBAEMR25                                                  *
.* Desc      :   Emergency Attendance / Admission by Health Fund           *
.***************************************************************************
.* Date      :   29/09/2010                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   This program will print the emergency attendance /        *
.*           :   admission by health fund report for a given date range    *
.* Modifications  :                                                        *
.* V10.03.01  22/01/2013 Patrick Adair                             CAR 261630 *
.*                       Removed port number from temp file name              *
.*        V10.01.01 10/02/2011  Jill Parkinson CAR 233088                  *
.*                  Added pmsvx1af                                         *
.*        V10.00.00 29/09/2010  Ebon Clements CAR 230579                   *
.*                  Created program                                        *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       EMRVISFD/INC     
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       PATCONFD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATFN1FD/INC 
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
.         Temporary File Definition
.         -------------------------
.
REPTEMP1  IFILE SQL, FIXED=41, KEY="U1-6,7-14"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
TEMPFUND  DIM       6      6        1     Health Fund
TEMPDATE  DIM       8      8        7     Date (CCYYMMDD)
TEMPNATT  FORM      3      3        15    Number Attended    
TEMPNADM  FORM      3      3        18    Number Attended and Admitted
TEMPSPAR  DIM       20     20       21    Spare
.
.End of Record                      41
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
ADMITTED  FORM      1
CMDLINE   DIM       127
CURRDATE  DIM       8
CURYEARM  DIM       6
ENDDFLAG  FORM      1
MONTHNAM  DIM       9
REPFFUND  DIM       6
REPTDATE  DIM       6
REPTFUND  DIM       6
PRFRFUND  DIM       40
PRTOFUND  DIM       40
PRNTFUND  DIM       6
SVPRFUND  DIM       6
TEMPFILE  DIM       8
TOTALATT  FORM      6
TOTALADM  FORM      6
ZERO8     INIT      "0000    "
.
ATTEND01  FORM      3
ATTEND02  FORM      3
ATTEND03  FORM      3
ATTEND04  FORM      3
ATTEND05  FORM      3
ATTEND06  FORM      3
ATTEND07  FORM      3
ATTEND08  FORM      3
ATTEND09  FORM      3
ATTEND10  FORM      3
ATTEND11  FORM      3
ATTEND12  FORM      3
ATTEND13  FORM      3
ATTEND14  FORM      3
ATTEND15  FORM      3
ATTEND16  FORM      3
ATTEND17  FORM      3
ATTEND18  FORM      3
ATTEND19  FORM      3
ATTEND20  FORM      3
ATTEND21  FORM      3
ATTEND22  FORM      3
ATTEND23  FORM      3
ATTEND24  FORM      3
ATTEND25  FORM      3
ATTEND26  FORM      3
ATTEND27  FORM      3
ATTEND28  FORM      3
ATTEND29  FORM      3
ATTEND30  FORM      3
ATTEND31  FORM      3
.
ADMITT01  FORM      3
ADMITT02  FORM      3
ADMITT03  FORM      3
ADMITT04  FORM      3
ADMITT05  FORM      3
ADMITT06  FORM      3
ADMITT07  FORM      3
ADMITT08  FORM      3
ADMITT09  FORM      3
ADMITT10  FORM      3
ADMITT11  FORM      3
ADMITT12  FORM      3
ADMITT13  FORM      3
ADMITT14  FORM      3
ADMITT15  FORM      3
ADMITT16  FORM      3
ADMITT17  FORM      3
ADMITT18  FORM      3
ADMITT19  FORM      3
ADMITT20  FORM      3
ADMITT21  FORM      3
ADMITT22  FORM      3
ADMITT23  FORM      3
ADMITT24  FORM      3
ADMITT25  FORM      3
ADMITT26  FORM      3
ADMITT27  FORM      3
ADMITT28  FORM      3
ADMITT29  FORM      3
ADMITT30  FORM      3
ADMITT31  FORM      3
.
JAN      INIT       "January"
FEB      INIT       "February"
MAR      INIT       "March"
APR      INIT       "April"
MAY      INIT       "May"
JUN      INIT       "June"
JUL      INIT       "July"
AUG      INIT       "August"
SEP      INIT       "September"
OCT      INIT       "October"
NOV      INIT       "November"
DEC      INIT       "December"
.
ERASE     INIT      "iserase "
ISBUILD   INIT      "isbuild "
UKEY      INIT      " 41 U1-6,7-14"     * set up for record length & key
.
PRGID     INIT      "IBAEMR25"
PRGNAM    INIT      "Emergency Attendance/Admission by Health Fund"
VERSION   INIT      "V12.01.00"
.
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
.
          CALL      KMTH0000               * Keyin the date range month/year
          BRANCH    EXIT,ML9999
.
          CALL      KFUN0000               * keyin health fund range
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      CREA0000               * create report tempfile
          CALL      LOAD0000               * load tempfile with visit statistics
          CALL      PRNT0000               * process
          CALL      KILL0000               * delete report tempfile
.
ML9999    CHAIN     PGM                    * chain back to program
.
.****************************************************************************
.*                                INIT0000                                  *
.*                             initialisation                               *
.****************************************************************************
INIT0000  CALL      DISPHEAD                  * display heading
.
          CALL      IBACLOCK
          PACK      CURYEARM,CCC,CYY,CMM
          REP       " 0",CURYEARM
.
          DISPLAY   *P64:24,"emrvisaf";
          OPEN      EMRVISA4,"emrvisaf" 
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af" 
          OPEN      PMSVX1A1,"pmsvx1af" 
.
          DISPLAY   *P64:24,"patfn1af";
          OPEN      PATFN1A1,"patfn1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TEN9;*212,CFEETYP
.
INIT9999  RETURN
.
.****************************************************************************
.*                                OPTN0000                                  *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
.
.
.*****************************************************************************
.*  Keyin the Month and year                                                 *
.*****************************************************************************
KMTH0000  DISPLAY   *P1:9,*EF:
                    *P1:9,"Month : "
.
          MOVE      NINE,CCOL
          MOVE      NINE,CVERT
          MOVE      ONE,CDEFDTE
          MOVE      ONE,CCANLDTE
          MOVE      ZERO,CHIGHLT
          UNPACK    CURYEARM,CCENT,CYEAR,CMON
.
          DISPLAY   *P9:9,*EL
          CALL      KEYMONTH
          BRANCH    OVRCD,KMTH9000
.
          PACK      REPTDATE,CCENT,CYEAR,CMON
          REP       " 0",REPTDATE
.
          MATCH     REPTDATE,CURYEARM
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Date cannot be into the future. ";
            CALL      HITENTER
            GOTO      KMTH0000
          ENDIF
.
          GOTO      KMTH9999
.
KMTH9000  MOVE      ONE,EXIT
.
KMTH9999  RETURN
.
.**********************************************************************
.*                             KFUN0000                               *
.*                Keyin range of health funds                         *
.**********************************************************************
KFUN0000  MOVE      SP6,REPFFUND
          DISPLAY   *P24:11,*EL:
                    *P1:11,*EL,"From Health Fund  : ";
.
          MOVE      TEN1,EVERT
          MOVE      TWENTY1,ECOL
          MOVE      ZERO,CKYIMAND
          CALL      PATFNDKY
.
          IF        EXIT<>0
            MOVE      "Start",HFNAME
          ENDIF
.
          MOVE      HCODE,REPFFUND
          MOVE      HFNAME,PRFRFUND
.
          DISPLAY   *P29:11,HFNAME;
.
          MOVE      SP6,REPTFUND
          DISPLAY   *P1:12,*EL,"To Health Fund    : ";
.
          MOVE      TEN2,EVERT
          MOVE      TWENTY1,ECOL
          MOVE      ZERO,CKYIMAND
          CALL      PATFNDKY
.
          IF        EXIT<>0
            MOVE      "Finish",HFNAME
          ENDIF
.
          MOVE      HCODE,REPTFUND
          MOVE      HFNAME,PRTOFUND
.
          DISPLAY   *P29:12,HFNAME;
.
          RESET     REPTFUND
          MATCH     SP6,REPTFUND
          GOTO      KFUN1000 IF NOT EQUAL
.
KFUN1000  ENDSET    REPTFUND
          APPEND    SP6,REPTFUND
          RESET     REPTFUND
.
          MATCH     REPFFUND,REPTFUND
          GOTO      KFUN9999 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** To H.F. < From H.F. **";
          CALL      HITENTER
          GOTO      KFUN0000
.
KFUN9999  RETURN
.
.**************************************************************************
.*                              CREA0000                                  *
.*             create a new temporary file                                *
.**************************************************************************
CREA0000  CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,TEMPFILE
.
          CALL      KILL0000                     * remove existing file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,TEMPFILE,UKEY
          EXECUTE   CMDLINE,TASKID               * create temporary index file
.         
          OPEN      REPTEMP1,TEMPFILE            * open temp index file
.         
CREA9999  RETURN
.
.****************************************************************************
.  close and erase the temporary file                                       *
.****************************************************************************
KILL0000  CLOSE     REPTEMP1                     * close file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ERASE,TEMPFILE       * set file erase command
          EXECUTE   CMDLINE,TASKID               * erase temp file
.
KILL9999 RETURN
.
.**************************************************************************
.*                               LOAD0000                                 *
.* Load temp file with emergency visit health fund statistics             *
.**************************************************************************
LOAD0000  PACK      KEY24,REPTDATE,SP70
          CALL      RSEMVIS4
LOAD1000  CALL      RKEMVIS4                * Loop emergency visits for month
          BRANCH    OVRCD,LOAD9999
.
          MOVE      EMVIDATE,KEY6
          MATCH     KEY6,REPTDATE           * Check visit year and month
          GOTO      LOAD9999 IF LESS
.
          COMPARE   FOUR,EMVISTAT           * Ignore cancelled ED visits
          GOTO      LOAD1000 IF EQUAL
.
          MATCH     SP70,REPFFUND           * Health fund from
          IF        !@EQUAL
            MATCH     REPFFUND,EMVIFUND
            GOTO      LOAD1000 IF LESS
          ENDIF
.        
          MATCH     SP70,REPTFUND           * Health fund to
          IF        !@EQUAL
            MATCH     EMVIFUND,REPFFUND
            GOTO      LOAD1000 IF LESS
          ENDIF
.        
          MOVE      ZERO,ADMITTED           * IP admission No
.
          PACK      KEY8,EMVIPADM,SP70
          CALL      RDPTMIS1                * Read linked IP admission
          BRANCH    OVRCD,LOAD2000
.
          IF        ASTAT=2 | ASTAT=3 | ASTAT=4
            MOVE      ONE,ADMITTED          * Current, discharged, on leave
          ENDIF                             * IP admission Yes
.
LOAD2000  MOVE      EMVIFUND,TEMPFUND
          MOVE      EMVIDATE,TEMPDATE
          MOVE      SP70,TEMPSPAR
.
          PACK      KEY14,TEMPFUND,TEMPDATE,SP70
          CALL      RDTEMP1
          IF        OVRCD=1
            MOVE      ONE,TEMPNATT
            MOVE      ADMITTED,TEMPNADM
            CALL      WRTEMP1
          ELSE
            ADD       ONE,TEMPNATT
            ADD       ADMITTED,TEMPNADM
            CALL      UPTEMP1
          ENDIF
.
          GOTO      LOAD1000
.
LOAD9999  RETURN
.
.**************************************************************************
.*                               PRNT0000                                 *
.* Print report                                                           *
.**************************************************************************
PRNT0000  CALL      HEAD0000                * Print Heading
.
          MATCH     SP70,REPFFUND
          IF        !@EQUAL
            MOVE      REPFFUND,PRNTFUND
          ELSE
            MOVE      SP70,PRNTFUND
          ENDIF
.
PRNT1000  MATCH     SP70,PRNTFUND
          IF        @EQUAL
            MOVE      "Blank",HFNAME
          ELSE
            PACK      KEY14,PRNTFUND,ZERO8,SP70
            CALL      RDFUND1                         * Read Health Fund
            IF        OVRCD=1
              MOVE      PRNTFUND,HFNAME
            ENDIF
          ENDIF
.
          MOVE      PRNTFUND,SVPRFUND       * Save print code for description
          CALL      GTOT0000                * Get totals for print line
.
          CALL      UND132
          PRINT     *1,"|",SVPRFUND,"| 01| 02| 03| 04| 05":
                       "| 06| 07| 08| 09| 10| 11| 12| 13| 14| 15":
                       "| 16| 17| 18| 19| 20| 21| 22| 23| 24| 25":
                       "| 26| 27| 28| 29| 30| 31|"
          ADD       ONE,CLNO
          CALL      UND132
                
.
          PRINT     *1,"|Attend|",ATTEND01,"|",ATTEND02,"|",ATTEND03:
                       "|",ATTEND04,"|",ATTEND05,"|",ATTEND06,"|",ATTEND07:
                       "|",ATTEND08,"|",ATTEND09,"|",ATTEND10,"|",ATTEND11:
                       "|",ATTEND12,"|",ATTEND13,"|",ATTEND14,"|",ATTEND15:
                       "|",ATTEND16,"|",ATTEND17,"|",ATTEND18,"|",ATTEND19:
                       "|",ATTEND20,"|",ATTEND21,"|",ATTEND22,"|",ATTEND23:
                       "|",ATTEND24,"|",ATTEND25,"|",ATTEND26,"|",ATTEND27:
                       "|",ATTEND28,"|",ATTEND29,"|",ATTEND30,"|",ATTEND31,"|"
.
          PRINT     *1,"|Admit |",ADMITT01,"|",ADMITT02,"|",ADMITT03:
                       "|",ADMITT04,"|",ADMITT05,"|",ADMITT06,"|",ADMITT07:
                       "|",ADMITT08,"|",ADMITT09,"|",ADMITT10,"|",ADMITT11:
                       "|",ADMITT12,"|",ADMITT13,"|",ADMITT14,"|",ADMITT15:
                       "|",ADMITT16,"|",ADMITT17,"|",ADMITT18,"|",ADMITT19:
                       "|",ADMITT20,"|",ADMITT21,"|",ADMITT22,"|",ADMITT23:
                       "|",ADMITT24,"|",ADMITT25,"|",ADMITT26,"|",ADMITT27:
                       "|",ADMITT28,"|",ADMITT29,"|",ADMITT30,"|",ADMITT31,"|"
                
.
          PRINT     *1,"|",HFNAME,*76,"| Total Attendances: ",TOTALATT:
                       *104,"|   Total Admissions: ",TOTALADM,"|"
.
          ADD       THREE,CLNO
          CALL      UND132
.
          COMPARE   "50",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          COMPARE   ONE,ENDDFLAG            * Report finished
          GOTO      PRNT1000 IF NOT EQUAL
.
PRNT9000  PRINT      *1,"The attend row will show the total number of ":
                        "patients attending ED for the day for a ":
                        "particular health fund"
.
          PRINT      *1,"The admit row will show the total number of ":
                        "patients attending ED for the day, how many where ":
                        "subsequently admitted to hospital"
.
          PRINT      *1,"As the subsequent admission may not be on the ":
                        "same day as the day of attendance, this report ":
                        "will not reconcile to Inpatient reports"
          PRINT      *1,"** END OF REPORT **"
.
PRNT9999  RETURN
.
.*********************************************************************
. Get totals for print line                                          *
.*********************************************************************
GTOT0000  CALL      CLRT0000                * Clear Total vars
.
          PACK      KEY14,PRNTFUND,SP70
          CALL      RSTEMP1
GTOT1000  CALL      RKTEMP1
          BRANCH    OVRCD,GTOT9000
.         
          MATCH     TEMPFUND,PRNTFUND
          IF        !@EQUAL
            MOVE      TEMPFUND,PRNTFUND
            GOTO      GTOT8000 
          ENDIF
.
          UNPACK    TEMPDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      ZERO,FORM2
          MOVE      CDAY,FORM2
.
          STORE     TEMPNATT,FORM2,ATTEND01,ATTEND02,ATTEND03,ATTEND04,ATTEND05:
                                   ATTEND06,ATTEND07,ATTEND08,ATTEND09,ATTEND10:
                                   ATTEND11,ATTEND12,ATTEND13,ATTEND14,ATTEND15:
                                   ATTEND16,ATTEND17,ATTEND18,ATTEND19,ATTEND20:
                                   ATTEND21,ATTEND22,ATTEND23,ATTEND24,ATTEND25:
                                   ATTEND26,ATTEND27,ATTEND28,ATTEND29,ATTEND30:
                                   ATTEND31
          ADD       TEMPNATT,TOTALATT
.         
          STORE     TEMPNADM,FORM2,ADMITT01,ADMITT02,ADMITT03,ADMITT04,ADMITT05:
                                   ADMITT06,ADMITT07,ADMITT08,ADMITT09,ADMITT10:
                                   ADMITT11,ADMITT12,ADMITT13,ADMITT14,ADMITT15:
                                   ADMITT16,ADMITT17,ADMITT18,ADMITT19,ADMITT20:
                                   ADMITT21,ADMITT22,ADMITT23,ADMITT24,ADMITT25:
                                   ADMITT26,ADMITT27,ADMITT28,ADMITT29,ADMITT30:
                                   ADMITT31
          ADD       TEMPNADM,TOTALADM
          GOTO      GTOT1000
.
GTOT8000  MOVE      ZERO,ENDDFLAG
          GOTO      GTOT9999
.
GTOT9000  MOVE      ONE,ENDDFLAG                 * Report Finished
          GOTO      GTOT9999
.
GTOT9999  RETURN
.
.*********************************************************************
. Clear print total vars                                             *
.*********************************************************************
CLRT0000  MOVE      ZERO,ATTEND01
          MOVE      ZERO,ATTEND02
          MOVE      ZERO,ATTEND03
          MOVE      ZERO,ATTEND04
          MOVE      ZERO,ATTEND05
          MOVE      ZERO,ATTEND06
          MOVE      ZERO,ATTEND07
          MOVE      ZERO,ATTEND08
          MOVE      ZERO,ATTEND09
          MOVE      ZERO,ATTEND10
          MOVE      ZERO,ATTEND11
          MOVE      ZERO,ATTEND12
          MOVE      ZERO,ATTEND13
          MOVE      ZERO,ATTEND14
          MOVE      ZERO,ATTEND15
          MOVE      ZERO,ATTEND16
          MOVE      ZERO,ATTEND17
          MOVE      ZERO,ATTEND18
          MOVE      ZERO,ATTEND19
          MOVE      ZERO,ATTEND20
          MOVE      ZERO,ATTEND21
          MOVE      ZERO,ATTEND22
          MOVE      ZERO,ATTEND23
          MOVE      ZERO,ATTEND24
          MOVE      ZERO,ATTEND25
          MOVE      ZERO,ATTEND26
          MOVE      ZERO,ATTEND27
          MOVE      ZERO,ATTEND28
          MOVE      ZERO,ATTEND29
          MOVE      ZERO,ATTEND30
          MOVE      ZERO,ATTEND31
          MOVE      ZERO,TOTALATT
.
          MOVE      ZERO,ADMITT01
          MOVE      ZERO,ADMITT02
          MOVE      ZERO,ADMITT03
          MOVE      ZERO,ADMITT04
          MOVE      ZERO,ADMITT05
          MOVE      ZERO,ADMITT06
          MOVE      ZERO,ADMITT07
          MOVE      ZERO,ADMITT08
          MOVE      ZERO,ADMITT09
          MOVE      ZERO,ADMITT10
          MOVE      ZERO,ADMITT11
          MOVE      ZERO,ADMITT12
          MOVE      ZERO,ADMITT13
          MOVE      ZERO,ADMITT14
          MOVE      ZERO,ADMITT15
          MOVE      ZERO,ADMITT16
          MOVE      ZERO,ADMITT17
          MOVE      ZERO,ADMITT18
          MOVE      ZERO,ADMITT19
          MOVE      ZERO,ADMITT20
          MOVE      ZERO,ADMITT21
          MOVE      ZERO,ADMITT22
          MOVE      ZERO,ADMITT23
          MOVE      ZERO,ADMITT24
          MOVE      ZERO,ADMITT25
          MOVE      ZERO,ADMITT26
          MOVE      ZERO,ADMITT27
          MOVE      ZERO,ADMITT28
          MOVE      ZERO,ADMITT29
          MOVE      ZERO,ADMITT30
          MOVE      ZERO,ADMITT31
          MOVE      ZERO,TOTALADM
.
CLRT9999  RETURN
.
.*********************************************************************
. Print a new page
.*********************************************************************
HEAD0000  CALL      HEAD132
.
          UNPACK    REPTDATE,CCENT,CYEAR,CMON
          MOVE      CMON,F2
          LOAD      MONTHNAM,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
.
          PRINT     *40,"Month Range : ",MONTHNAM," / ",CCENT,CYEAR
.
          STRIP     PRFRFUND
          STRIP     PRTOFUND
.
          PRINT     *40,*+,"From H/Fund: ",PRFRFUND," to ",PRTOFUND
          ADD       TWO,CLNO
.
HEAD9999  RETURN
.
.****************************************************************************
.*        IO ROUTINES FOR TEMPORARY FILE                                    *
.****************************************************************************
RSTEMP1   READ      REPTEMP1,KEY14;;
          RETURN
.
RDTEMP1   MOVE      ZERO,OVRCD
          READ      REPTEMP1,KEY14;TEMPFUND,TEMPDATE,TEMPNATT:
                                   TEMPNADM,TEMPSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    REPTEMP1;TEMPFUND,TEMPDATE,TEMPNATT:
                                   TEMPNADM,TEMPSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   WRITE     REPTEMP1,KEY14;TEMPFUND,TEMPDATE,TEMPNATT:
                                   TEMPNADM,TEMPSPAR
          RETURN
.
UPTEMP1   UPDATE    REPTEMP1;TEMPFUND,TEMPDATE,TEMPNATT:
                                   TEMPNADM,TEMPSPAR
          RETURN
.
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       PATFNDKY
          INC       TFILENAM                     * Generate Temp File Name
.
          INC       EMRVISIO/INC
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       PATFN1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       WEBERRIO/INC                 * Web Server Error Logging

******************************************************************************
.* System         :  Patient Billing                                          *
.* Program        :  IBAADT97                                                 *
.* Description    :  Upload Casemix Codes                                     *
.******************************************************************************
.* Date           :  18/10/2019                                               *
.* Author         :  Peter Vela                                               *
.* Function       :  Upload Casemix Code from ASCII file                      *
.* Modifications  :                                                           *
.*         V10.15.05 27/11/2019 Peter Vela     Task 0826834                   *
.*                   Fixed blank and inactive error messages                  *
.*                   Fixed report format                                      *
.*         V10.15.04 07/11/2019 Peter Vela     Task 0826834                   *
.*                   Fixed display of Include Inactive Items keyin Yes/No     *
.*                   in PRHD1000                                              *
.*                   Fixed error display in PERR0000                          *
.*         V10.15.03 04/11/2019 Peter Vela     Task 0826834                   *
.*                   Added blank line validation in POST1000                  * 
.*         V10.15.02 22/10/2019 Peter Vela     Task 0826834                   *
.*                   Fixed validations in VFLD0000                            *
.*         V10.15.01 18/10/2019 Peter Vela     Task 0826834                   *
.*                   Created new upload program                               *
.******************************************************************************
.
          INC       STD001FD
.
          INC       IBACONFD/INC
          INC       PATCMCFD/INC            * Casemix Codes Table
.
.         Temporary file for Bed fees records which
.         are loaded from the upload file provided by the customer (PROC0000)
.
.         Casemix Codes Upload file
.
UPCMCTXT  FILE       HL7, FIXED=1000        * Upload file for casemix codes
.
.Name     Type     Size     Start
.----     ----     ----     -----
XCACODE   DIM         9     Casemix Code
XCADES1   DIM         30    Description 1
XCADES2   DIM         30    Description 2
XCAELOS   FORM        4     Expected Length of Stay
XCAACTV   FORM        1     Active (1=Yes)
.
.End of Record
.
. ----- Program Variables -----
.
ADDCOUNT  FORM      8
CMDLINE   DIM       127
DIM9      DIM       9
ERRCOUNT  FORM      8
ERRORMSG  DIM       30
FNAMEI    DIM       150
KYINACTV  DIM       1
LINECNTR  FORM      8
PATHNAME  DIM       150
SAVKEY20  DIM       20
USERID    DIM       10
UPDCOUNT  FORM      8
VALDPASS  FORM      1
.
XFLD0001  DIM       30
XFLD0002  DIM       30
XFLD0003  DIM       30
XFLD0004  DIM       30
XFLD0005  DIM       30
.
PRGID     INIT      "IBAADT97"
PRGNAM    INIT      "Upload & Update Casemix Codes"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                  ML0000                                    *
.*                                Main Module                                 *
.******************************************************************************
ML0000    CALL      INIT0000                * Initailize variables & open files
          CALL      KOPT0000                * Keyin Upload or Update option
          BRANCH    EXIT,ML9999
.
          BRANCH    COPTION,ML1000
          GOTO      ML9999
.
ML1000    CALL      KFLD0000                * Keyin fields
          BRANCH    EXIT,ML0000   
.
          CALL      KASC0000                * Keyin ascii file
          BRANCH    EXIT,ML9900
.
          CALL      KPTH0000                * Keyin pathname
          BRANCH    EXIT,ML0000
.
.          CALL      OPTN0000                * Choose options
.          BRANCH    EXIT,ML9999
.
.ML1100    CALL      SCRD0000                * Display screen
.
.          CALL      KFLD0000                * Keyin fields
.          BRANCH    EXIT,ML0000
.
.          CALL      KPTH0000                * Keyin pathname
.          BRANCH    EXIT,ML0000
.
.          CALL      SFLD0000
.          BRANCH    EXIT,ML1100
.
          CALL      PRHD0000                * Print header
.
          CALL      CTOT0000                * Clear total vars
.
          MOVE      ONE,VALDPASS            * Validation Pass Set to Yes
          CALL      POST0000         
.
          IF        ERRCOUNT=0
            MOVE      ZERO,VALDPASS         * If no errors proceed         
            CALL      POST0000              * with Updating file
          ENDIF
.
          CALL      PTOT0000                * Print report totals
.
          GOTO      ML9999
.
ML9900    CALL      HEAD132
          PRINT     *N,"Failed to open upload file. "
.
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                 INIT0000                                   *
.*                     Initialize Variables & Open Files                      *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen heading
.
          CALL      IBACLOCK
.
          DISPLAY   *P56:24,"Opening patcmcaf";
          OPEN      PATCMCA1,"patcmcaf"
.
INIT9999  RETURN
+
.******************************************************************************
.*                                 KOPT0000                                   *
.*                            Choose Upload or Update files                   *
.******************************************************************************
KOPT0000  MOVE      ZERO,EXIT
          MOVE      ZERO,COPTION
          DISPLAY   *P1:6,*EF:
                    *P1:6,*V2LON,"0",*HOFF," = Exit":
                    *P1:7,*V2LON,"1",*HOFF," = Upload Casemix Codes";
          DISPLAY   *P1:9,"Select Option : ";
.
KOPT1000  KEYIN     *P17:9,*V2LON,COPTION;
          BRANCH    COPTION,KOPT9999
.
          COMPARE   ZERO,COPTION
          GOTO      KOPT9000 IF EQUAL
          BEEP
          GOTO      KOPT1000
.
KOPT9000  MOVE      ONE,EXIT
KOPT9999  RETURN
+
.******************************************************************************
.*                                 OPTN0000                                   *
.*                            Choose HF group or HF code                      *
.******************************************************************************
OPTN0000  MOVE      ZERO,EXIT
          MOVE      ZERO,OPTION
          DISPLAY   *P1:6,*EF:
                    *P1:6,*V2LON,"0",*HOFF," = Exit":
                    *P1:7,*V2LON,"1",*HOFF," = Include Inactive Items";
          DISPLAY   *P1:10,"Select Option : ";
.
OPTN1000  KEYIN     *P17:10,*V2LON,OPTION;
          BRANCH    OPTION,OPTN9999
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9000 IF EQUAL
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                 KASC0000                                   *
.*                            Keyin ASCI file                                 *
.******************************************************************************
KASC0000  MOVE      ZERO,EXIT
          PACK      FNAMEI WITH SP70,SP70,SP70
.
.         DISPLAY   *P19:13,*EF,"Casemix Codes Load File";
.
          KEYIN     *P1:14,*EL,"Keyin Path & File Name : ",*V2LON,FNAMEI
.
          PACK      FNAMEI,FNAMEI,SP70,SP70,SP70
.
          MATCH     SP70,FNAMEI
          GOTO      KASC9000 IF EQUAL
.
          SCAN      ".txt",FNAMEI
          IF        !@EQUAL
            SQUEEZE   FNAMEI
            ENDSET    FNAMEI
            APPEND    ".txt",FNAMEI      * add ".txt" if there is a path
            APPEND    SP70,FNAMEI
            RESET     FNAMEI
          ENDIF
          RESET     FNAMEI
.
          DISPLAY   *P26:14,*V2LON,*EL,FNAMEI;
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      UPCMCTXT,FNAMEI
          TRAPCLR   IO
          BRANCH    OVRCD,KASC8000
.
          CALL      EXCUS000
          MOVE      ZERO,EXIT
          GOTO      KASC9999
.
KASC8000  DISPLAY   *P1:24,*EL,*B,"File not found.  ";
          CALL      HITENTER
          GOTO      KASC0000
.
KASC9000  MOVE      ONE,EXIT
.
KASC9999  RETURN
+
.******************************************************************************
.*                                 KFLD0000                                   *
.*                            Keyin Fields                                    *
.******************************************************************************
KFLD0000  CALL      IVAR0000                * Initialise variables
.
          CALL      KINA0000                * Keyin Include Inactive Items
          BRANCH    EXIT,KFLD9999
.
          MOVE      ZERO,EXIT
.
KFLD9999  RETURN
+
.******************************************************************************
.*                                 IVAR0000                                   *
.*                            Initialise variable                             *
.******************************************************************************
IVAR0000  MOVE      SP70,KYINACTV
.
IVAR9999  RETURN
+
.******************************************************************************
.*                                 SCRD0000                                   *
.*                            Screen display                                  *
.******************************************************************************
SCRD0000  DISPLAY   *P1:11,*EF:
                    *P1:11," 1. Include Inactive It:":
                    *P1:12," 2. Keyin Path name    :";
.
SCRD9999  RETURN
+
.******************************************************************************
.*                                 KINA0000                                   *
.*                            Keyin Include Inactive Item                     *
.******************************************************************************
KINA0000  KEYIN     *P1:11,*EL,"Keyin Include Inactive Items: ",*V2LON,KYINACTV;
          ENDSET     KYINACTV
          APPEND     SP70,KYINACTV
          RESET      KYINACTV
.
          MATCH      SP70,KYINACTV
          GOTO       KINA9000 IF EQUAL
.
          TYPE       KYINACTV
          GOTO       KINA9000 IF NOT EQUAL 
.
          MOVE       ZERO,EXIT
          GOTO       KINA9999
.
KINA9000  MOVE       ONE,EXIT
KINA9999  RETURN
+
.******************************************************************************
.*                                  KPTH0000                                  *
.*                     Keyin path name                                        *
.******************************************************************************
KPTH0000  PACK      PATHNAME,SP70,SP70,SP70
          KEYIN     *P26:16,*EL,*V2LON,PATHNAME;
          ENDSET    PATHNAME
          APPEND    SP70,PATHNAME
          APPEND    SP70,PATHNAME
          APPEND    SP70,PATHNAME
          RESET     PATHNAME
.
.         PACK      PATHNAME,PATHNAME,SP70,SP70,SP70
          MATCH     SP70,PATHNAME
          GOTO      KPTH9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KPTH9999
.
KPTH9500  MOVE      ONE,EXIT
KPTH9999  RETURN
+
.******************************************************************************
.*                                  SFLD0000                                  *
.*                        Select Field                                        *
.******************************************************************************
SFLD0000  CALL      MAINQST                 * Select Item, (P)ost, (C)ancel ?
.
          COMPARE   ZERO,CCITEM
          GOTO      SFLD9999 IF EQUAL
          GOTO      SFLD1000 IF LESS
.
          MOVE      ZERO,EXIT
          PERFORM   CCITEM,KINA0000:             * Keyin Include Inactive Items
                           KPTH0000              * Keyin pathname
.
          BRANCH    EXIT,SFLD9999
          GOTO      SFLD0000
.
SFLD1000  MOVE      ONE,EXIT
SFLD9999  RETURN
+
.******************************************************************************
.*                                  PRHD0000                                  *
.*                        Print the selection details                         *
.******************************************************************************
PRHD0000  CALL      HEAD132
.
          PRINT     *N,"Upload Casemix Fees"
.
          PRINT     *N,"Date     : ",CDATE:
                    *N,"Time     : ",CTIMEIS
.                   *N,"User ID  : ",USERID
.
          PRINT     *N,"Upload Casemix Codes Load file         : ":
                    *N,"     ",PATHNAME
.
          BRANCH    COPTION,PRHD1000
          GOTO      PRHD9999
.
PRHD1000  MATCH     "1",KYINACTV
          IF        @EQUAL 
            PRINT     *N,"Include Inactive Items: Yes"
          ELSE
            PRINT     *N,"Include Inactive Items: No"
          ENDIF
.
          CALL      UND132
.
          PRINT     *1,"| Line",*12,"| Casemix Code ",*30,"| Error":
                    *132,"|"
          CALL      UND132
          GOTO      PRHD9999
.
PRHD9999  RETURN
+
.******************************************************************************
.*                                  POST0000                                  *
.*                        Generate Fees of a health fund                      *
.******************************************************************************
POST0000  MOVE      ZERO,LINECNTR
.
          OPEN      UPCMCTXT,FNAMEI
.
.         CALL      HITENTER
POST1000  READ      UPCMCTXT,SEQ;XFLD0001,XFLD0002,XFLD0003,XFLD0004,XFLD0005
          GOTO      POST9000 IF OVER
.
          PACK      XFLD0001,XFLD0001,SP70
          PACK      XFLD0002,XFLD0002,SP70
          PACK      XFLD0003,XFLD0003,SP70
          PACK      XFLD0004,XFLD0004,SP70
          PACK      XFLD0005,XFLD0005,SP70
.
          MATCH     SP70,XFLD0001
          GOTO      POST1010 IF NOT EQUAL
          MATCH     SP70,XFLD0002
          GOTO      POST1010 IF NOT EQUAL
          MATCH     SP70,XFLD0003
          GOTO      POST1010 IF NOT EQUAL
          MATCH     SP70,XFLD0004
          GOTO      POST1010 IF NOT EQUAL
          MATCH     SP70,XFLD0005
          GOTO      POST1010 IF NOT EQUAL
.
          GOTO      POST1000
.
POST1010  ADD       ONE,LINECNTR            * Text file line counter for error
.
          IF        VALDPASS=1
            CALL      VFLD0000                * Validation pass so
            BRANCH    EXIT,POST1000           * validate fields
          ENDIF
.
          PACK      XCACODE,XFLD0001,SP70
          PACK      XCADES1,XFLD0002,SP70
          PACK      XCADES2,XFLD0003,SP70
.
          PACK      KEY4,SP70
          PACK      KEY4,XFLD0004,SP70
          SQUEEZE   KEY4
          MOVE      ZERO,XCAELOS
          MOVE      KEY4,XCAELOS  
.
          PACK      KEY1,SP70
          PACK      KEY1,XFLD0005,SP70
          SQUEEZE   KEY1
          MOVE      ZERO,XCAACTV
          MOVE      KEY1,XCAACTV
.
          MATCH     "1",KYINACTV
          IF        !@EQUAL
            COMPARE   ZERO,XCAACTV
            GOTO      POST1000 IF EQUAL
          ENDIF
.
.         as per Upload file
.
POST3800  CALL      LCMC0000
          GOTO      POST1000
.
POST9000  CLOSE     UPCMCTXT
.
POST9999  RETURN
+
.******************************************************************************
.*                                  LCMC0000                                  *
.*                             Write casemix codes record                     *
.******************************************************************************
LCMC0000  UNPACK    SP70,PTCACODE,PTCADES1,PTCADES2
          MOVE      ZERO,PTCAELOS
          MOVE      ZERO,PTCAACTV
.
          PACK      KEY9,XCACODE,SP70
          CALL      RDPTCMC1
          IF        OVRCD=1
            MOVE      XCACODE,PTCACODE
            MOVE      XCADES1,PTCADES1
            MOVE      XCADES2,PTCADES2
            MOVE      XCAELOS,PTCAELOS
            MOVE      XCAACTV,PTCAACTV
            UNPACK    SP70,PTCSPARE
.
            IF        VALDPASS=0
              ADD       ONE,ADDCOUNT          * Total records added
              CALL      WRPTCMC1              * Write if not validation pass
            ENDIF
.
          ELSE
            MOVE      XCADES1,PTCADES1
            MOVE      XCADES2,PTCADES2
            MOVE      XCAELOS,PTCAELOS
            MOVE      XCAACTV,PTCAACTV
.
            IF        VALDPASS=0
              ADD       ONE,UPDCOUNT         * Total records updated
              CALL      UPPTCMC1             * Update if not validation pass
            ENDIF                         
.
          ENDIF
          MOVE      ZERO,EXIT
          GOTO      LCMC9999
.
LCMC9000  MOVE      ONE,EXIT
LCMC9999  RETURN
+
.******************************************************************************

.         Execute us1 command before processing
.******************************************************************************
EXCUS000  CLEAR     CMDLINE
          APPEND    "ibaadt97.us1 ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
EXCUS999  RETURN
+
.******************************************************************************
. Validate bed fee input text file fields
.******************************************************************************
VFLD0000  MOVE      ZERO,EXIT
.
          MATCH     SP70,XFLD0001
          IF        @EQUAL
            MOVE      "Blank Casemix Code",ERRORMSG
            CALL      PERR0000
            MOVE      ONE,EXIT
          ENDIF
.
          MATCH     SP70,XFLD0002
          IF        @EQUAL
            MOVE      "Blank Description 1",ERRORMSG
            CALL      PERR0000
            MOVE      ONE,EXIT
          ENDIF
.
.         MATCH     SP70,XFLD0003
.         IF        @EQUAL
.           MOVE      "Blank Description 2",ERRORMSG
.           CALL      PERR0000
.           MOVE      ONE,EXIT
.         ENDIF
.
          MATCH     SP70,XFLD0004
          IF        @EQUAL
            MOVE      "Blank or Invalid Expected LOS",ERRORMSG
            CALL      PERR0000
            MOVE      ONE,EXIT
            GOTO      VFLD1000
          ENDIF
.
          SQUEEZE   XFLD0004
          TYPE      XFLD0004
          IF        !@EQUAL | @EOS
            MOVE      "Blank or Invalid Expected LOS",ERRORMSG
            CALL      PERR0000
            MOVE      ONE,EXIT
          ELSE           
.
.           PACK      KEY4,SP70
.           PACK      KEY4,XFLD0004,SP70
.           SQUEEZE   KEY4
.           MOVE      KEY4,FORM4
.
.           COMPARE   ZERO,FORM4
.           IF        @EQUAL
.             MOVE      "Zero Expected LOS",ERRORMSG
.             CALL      PERR0000
.             MOVE      ONE,EXIT
.           ENDIF
.
          ENDIF
.
VFLD1000  MATCH     SP70,XFLD0005
          IF        @EQUAL
            MOVE      "Blank or Invalid Active Ind",ERRORMSG
            CALL      PERR0000
            MOVE      ONE,EXIT
            GOTO      VFLD9999
          ENDIF
.
          SQUEEZE   XFLD0005
          TYPE      XFLD0005
          IF        !@EQUAL | @EOS
            MOVE      "Blank or Invalid Active Ind",ERRORMSG
            CALL      PERR0000
            MOVE      ONE,EXIT
          ELSE
.
            MOVE      ZERO,FORM1
            PACK      KEY1,SP70
            PACK      KEY1,XFLD0005,SP70
            SQUEEZE   KEY1
            MOVE      KEY1,FORM1
.
            IF        FORM1 <> 0 & FORM1 <> 1
              MOVE      "Active Indicator must be 0/1",ERRORMSG
              CALL      PERR0000
              MOVE      ONE,EXIT 
            ENDIF
.
          ENDIF
.
VFLD9999  RETURN
+
.******************************************************************************
. Print error line
.******************************************************************************
PERR0000  ADD       ONE,ERRCOUNT            * Total error count
.
          MOVE      XFLD0001,DIM9
.
          PRINT     *1,"|",LINECNTR,*12,"| ",DIM9,*30,"| ":
                    ERRORMSG:
                    *132,"|"
.
PERR9999  RETURN
+
.******************************************************************************
. Clear total vars
.******************************************************************************
CTOT0000  MOVE      ZERO,ERRCOUNT
          MOVE      ZERO,ADDCOUNT
          MOVE      ZERO,UPDCOUNT
.
CTOT999   RETURN
+
.******************************************************************************
. Print total line
.******************************************************************************
PTOT0000  CALL      UND132
.
          PRINT     *1,"| Total Errors  : ",ERRCOUNT,*132,"|"
          PRINT     *1,"| Total Added   : ",ADDCOUNT,*132,"|"
          PRINT     *1,"| Total Updated : ",UPDCOUNT,*132,"|"
.
          CALL      UND132
.
PTOT9999  RETURN
.
          INC       STD001IO
.
          INC       PATCMCIO/INC
.

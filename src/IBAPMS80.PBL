.******************************************************************************
.* System   :  PMS                                                            *
.* Program  :  IBAPMS80                                                       *
.* Desc     :  Program to reset Patient Alerts flag                           *
.******************************************************************************
.* Date     :  20/03/2017                                                     *
.* Author   :  Don Nguyen                                                     *
.* Mods.    :                                                                 *
.*----------------------------------------------------------------------------*
.* V11.02.03  26/10/2022  Jacob Jackson     TSK 0923819                       *
.*            Add new scheduled run variable, so alert time can increment     *
.*            when report is running repeatedly                               *
.* V11.02.02  26/08/2022  Davin             TSK 0913316                       *
.*            Trigger hl7 message after updating alert end date (PRCU0090)    *
.* V11.02.01  24/08/2022  Alina Bhari       TSK 0913316                       *
.*            Recorded the updated date(PTALUDAT),time(PTALUTIM) and web user *
.*            id as "AutoEndPgm" when scheduled through                       *
.*            "Auto-End Selected Alert Code" - PRCU0060                       *
.* V11.00.01  21/09/2020  Thanh T           TSK 0878035                       *
.*            Added option2 for Auto-End Selected Alert Code                  * 
.*----------------------------------------------------------------------------*
.* V10.11.03  09/10/2017  Ania P            TSK 0842969                       *
.*            Fixed date logic in CHKALE10                                    *
.* V10.11.02  26/09/2017  Ania P            TSK 0842969                       *
.*            Removed hardcoded value in TODAY (date)                         *
.* V10.11.01  11/09/2017  Ania P            TSK 0842969                       * 
.*            Merged ALERTFIX.PBL/CHKALR00 into CHKALE00                      *
.* V10.10.02  13/04/2017  Davin             TSK 0818645                       *
.*            Changed to always check inactive date after end date (CHKALE00) *
.* V10.10.01  12/04/2017  Davin             TSK 0818645                       *
.*            Changed ptcncalr to read/write position 136                     *
.* V10.10.00  20/03/2017  Don Nguyen        TSK 0818645                       *
.*            Created program                                                 *
.******************************************************************************
.
          INC       STD001FD
          INC       IBACONFD/INC
.
          INC       NHIMASFD/INC
          INC       PATALRFD/INC       * Patient Alerts
          INC       PATCODFD/INC       * Codes file
          INC       PATCONFD/INC       * Control file
          INC       PATMA1FD/INC       * Master file
          INC       PATNIDFD/INC
          INC       PMSPX2FD/INC
          INC       WEBSECFD/INC
+
.**********************************************************************
.*                           VARIABLES                                *
.**********************************************************************
.
RECDPROC  FORM      5        * Number of Alert records processed (patalrtf)
RECDUPDT  FORM      5        * Number of Alert flags cleared (patmx1af records)
TODAY     DIM       8        * Current Date (ccyymmdd)
URNUMBER  DIM       8        * Current UR Number being processed
WORKDATE  DATE      8
WORKTIME  DIM       8
DIM8      DIM       8
NEWSIN1   DIM       1
NEWSIN7   DIM       1
NEWSIN8   DIM       1
NEWSIN9   DIM       1
NEWSN11   DIM       1
SAVKEY16  DIM       16
SAVKEY24  DIM       24
SAVIND5   DIM       1
SAVPURNO  DIM       8
FILTCATG  DIM       2
FILTCODE  DIM       3
FILTSTRD  DIM       8
FILSTRDT  DATE      8
FILSTRMO  DIM       8
FISCHDAT  DIM       8
CDYSDAYS  FORM      6
CDYSFDTE  DIM       8
CDYSTDTE  DIM       8 
CDYSYEAR  FORM      2
MTHNAM3   DIM       3
PATNAME   DIM       50                        * Patient Name
CATGDESC  DIM       20
CODELDSC  DIM       50
STRDATED  DIM       11
ENDDATED  DIM       11
NOFUPDT   FORM      5
NMPNUMB   DIM       20
.
SP16      INIT      "                "
JAN       INIT      "January"
FEB       INIT      "February"
MAR       INIT      "March"
APR       INIT      "April"
MAY       INIT      "May"
JUN       INIT      "June"
JUL       INIT      "July"
AUG       INIT      "August"
SEP       INIT      "September"
OCT       INIT      "October"
NOV       INIT      "November"
DEC       INIT      "December"
.
PRGNAM01  INIT      "Reset Patient Alert Flags (Expired Alerts) "
PRGNAM02  INIT      "Auto-End Selected Alert Code"
.
.---------------------------------------------------------------------- 
PRGID     INIT      "IBAPMS80"
PRGNAM    INIT      "Reset Patient Alert Flags (Expired Alerts) "
VERSION   INIT      "V12.01.00"
+
.
.**********************************************************************
.*                           MAINLINE                                 *
.**********************************************************************
.
ML0000    CALL      INIT0000                  * Initialisation 
.
ML1000    CALL      OPTN0000                  * Select option
          BRANCH    OPTION,ML2000,ML3000
          GOTO      ML9999                    * 0=Exit
.
ML2000    CALL      CONTQST                   * OK to continue?
          BRANCH    CEXIT,ML2500,ML2000,ML1000
.                         Yes    No     Cancel
.
ML2500    CALL      PROC0000                  * Process Patient Alerts
          DISPLAY   *P1:21,*EF,"Number of Alert records processed: ",RECDPROC
          DISPLAY   *P1:22,"Number of Alert flags cleared: ",*P36:22,RECDUPDT
          DISPLAY   *P1:23,*EF
          GOTO      ML1000 
.
ML3000    CALL      KCATG000                 * Keyin alert category
          CALL      KCATC000                 * Keyin alert code
          CALL      KSTDAT00                 * Keyin alert start date
          CALL      KSCDAT00                 * Keyin scheduled start date
.
          CALL      CONTQST                   * OK to continue?
          BRANCH    CEXIT,ML3500,ML3000,ML1000
.                         Yes    No     Cancel
.
ML3500    CALL      PRCU0000                  * Process Auto Clear Alerts
          DISPLAY   *P1:21,*EF,"Number of Alert records processed: ",RECDPROC
          DISPLAY   *P1:22,"Number of Alert flags cleared: ",*P36:22,RECDUPDT
          DISPLAY   *P1:23,*EF
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.**********************************************************************
.*                           INIT0000                                 *
.*                     Initialise variables                           *
.**********************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P57:24,"Opening controlf"
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P65:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P65:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P65:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P65:24,"patalrtf";
          OPEN      PATALRT1,"patalrtf"
.
          DISPLAY   *P65:24,"patalrtf";
          OPEN      PATALRT1,"patalrtf"
          OPEN      PATALRT2,"patalrtf"
.
          DISPLAY   *P65:24,"patalrtf";
          OPEN      PATALRT3,"patalrtf"
.
          DISPLAY   *P65:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
INIT2000  PACK      TODAY,CCC,CYY,CMM,CDD     * todays date
          REP       " 0",TODAY
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,HUND24;*136,PTCNCALR     * Last Run Date
.
          MOVE      ZERO,RECDUPDT
          MOVE      ZERO,RECDPROC
.
INIT9999  RETURN
+
.********************************************************************** 
.*                        Main Option Screen                          *
.**********************************************************************
OPTN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF:
                          " = Reset Patient Alert Flags (Expired Alerts)":
                    *P1:6,*V2LON,TWO,*HOFF:
                          " = Auto-End Selected Alert Code"
.
OPTN1000  KEYIN     *P1:8,"Select Option : ",*EF,*DV,UNDLN1:
                    *P17:8,*V2LON,OPTION
.
          COMPARE   ZERO,OPTION                  * exit ?
          GOTO      OPTN9999 IF EQUAL            * yes
.
          BRANCH    OPTION,OPTN9999,OPTN9999
          BEEP
          GOTO      OPTN1000
.
OPTN9999  RETURN
.
.------------------------------------------------------------------------------
. Keyin the alert category
.------------------------------------------------------------------------------
KCATG000  MOVE      SP70,FILTCATG
          DISPLAY   *P1:10,*V2LON,*HOFF,"Alert Category: "
          KEYIN     *P19:10,*V2LON,FILTCATG
.
KCATG999  RETURN
.
.------------------------------------------------------------------------------
. Keyin the alert code
.------------------------------------------------------------------------------
KCATC000  MOVE      SP70,FILTCODE
          DISPLAY   *P1:12,*V2LON,*HOFF,"Alert Code: "
          KEYIN     *P19:12,*V2LON,FILTCODE
.
KCATC999  RETURN
.
.------------------------------------------------------------------------------
. Keyin end date
.------------------------------------------------------------------------------
KSTDAT00  MOVE      TWENTY8,FILTSTRD
          DISPLAY   *P1:14,*V2LON,*HOFF,"Alert Start Date: "
          KEYIN     *P19:14,*V2LON,FILTSTRD
.
KSTDAT99  RETURN
.
.------------------------------------------------------------------------------
. First scheduled date
.------------------------------------------------------------------------------
KSCDAT00  MOVE      TWENTY8,FISCHDAT
          DISPLAY   *P1:16,*V2LON,*HOFF,"First Scheduled Run: "
          KEYIN     *P19:16,*V2LON,FISCHDAT     
.
KSCDAT99  RETURN
+
.****************************************************************************** 
.*        Process (clear) Patient Alert flags if there are no active Alerts   *
.******************************************************************************
PROC0000  MOVE      SP70,URNUMBER
          MOVE      ZERO,RECDUPDT
          MOVE      ZERO,RECDPROC
          MOVE      PRGNAM01,PRGNAM
.
.
..........Check Alert End Dates
.
          MATCH     "00000000",PTCNCALR
          IF        @EQUAL
            MOVE      SP8,PTCNCALR
          ENDIF
.
          PACK      KEY24,TODAY,Z70
          CALL      RSPTALR2
PROC1100  CALL      RPPTALR2
          BRANCH    OVRCD,PROC2000
.
          MATCH     PTCNCALR,PTALEDAT       * End Date >= Last Run Date?
          GOTO      PROC2000 IF LESS        * Skip Alerts check if not
.
          MATCH     URNUMBER,PTALURNO       * Same UR being processed?
          GOTO      PROC1100 IF EQUAL
.
          ADD       ONE,RECDPROC
          MOVE      PTALURNO,URNUMBER
.
          PACK      KEY8,URNUMBER,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,PROC1100
.
          PACK      KEY8,URNUMBER,SP70
          CALL      RDPMPX21
          BRANCH    OVRCD,PROC1100
.
          CALL      CHKALE00                * Check if any ACTIVE Alerts for UR?
          GOTO      PROC1100
.
.
..........Check Alert Inactive Dates
.
PROC2000  MOVE      SP70,URNUMBER
.
          PACK      KEY24,TODAY,Z70
          CALL      RSPTALR3
PROC2100  CALL      RPPTALR3
          BRANCH    OVRCD,PROC9000
.
          MATCH     PTCNCALR,PTALDTIN       * Inactive Date >= Last Run Date?
          GOTO      PROC9000 IF LESS        * Skip Alerts check if not
.
          MATCH     URNUMBER,PTALURNO       * Same UR being processed?
          GOTO      PROC2100 IF EQUAL
.
          ADD       ONE,RECDPROC
          MOVE      PTALURNO,URNUMBER
.
          PACK      KEY8,URNUMBER,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,PROC1100
.
          PACK      KEY8,URNUMBER,SP70
          CALL      RDPMPX21
          BRANCH    OVRCD,PROC1100
.
          CALL      CHKALE00                * Check if any ACTIVE Alerts for UR?
          GOTO      PROC2100
.
PROC9000  WRITAB    CONTROLF,HUND24;*136,TODAY     * Save Last Run Date
.
PROC9999  RETURN
+
.****************************************************************************** 
.*        Check for any active Alerts for the URNUMBER & TODAY specified      *
.*                                                                            *
.*        Returns:                                                            *
.*          EXIT = 0 - No active alerts for UR                                *
.*                 1 - Active alert exists;                                   *
.*                        End/Inactive Date is blank                          *
.*                     OR End/Inactive Date is after (greater than) TODAY     *
.******************************************************************************
CHKALE00  MOVE      SP70,PTMXSIN1                * 1=has alert 2=has security alert
          MOVE      SP70,PMPXSIN7                * Alert exists with TCINDC5 = "M"
          MOVE      SP70,PMPXSIN8                * Alert exists with TCINDC5 = "B"
          MOVE      SP70,PMPXSIN9                * Alert exists with TCINDC5 = "R"
          MOVE      SP70,PMPXSN11                * Alert exists with TCINDC5 = "C"
          MOVE      SP70,NEWSIN1
          MOVE      SP70,NEWSIN7
          MOVE      SP70,NEWSIN8
          MOVE      SP70,NEWSIN9
          MOVE      SP70,NEWSN11
.
          PACK      KEY16,URNUMBER,SP70
          CALL      RSPTALR1
CHKALE10  CALL      RKPTALR1
          BRANCH    OVRCD,CHKALE90
.
          MATCH     URNUMBER,PTALURNO
          GOTO      CHKALE90 IF NOT EQUAL
.
          PACK      KEY5,PTALCATG,PTALCODE,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,CHKALE10
.
          PACK      KEY16,PTALEDAT,PTALDTIN
          MATCH     SP16,KEY16
          IF        @EQUAL
            GOTO      CHKALE20                   * Both blank/active. Set flags.
          ENDIF
.
          MATCH     SP8,PTALEDAT
          IF        !@EQUAL
            MATCH     PTALEDAT,TODAY
            GOTO      CHKALE10 IF NOT LESS       * End Date < Today. Inactive
          ENDIF
.
          MATCH     SP8,PTALDTIN
          IF        !@EQUAL
            MATCH     PTALDTIN,TODAY
            GOTO      CHKALE10 IF NOT LESS       * Inactive Date < Today. Inactive
          ENDIF
.
.         Got here, both dates > today so ok, keep going
.
.
CHKALE20  MATCH     "M",TCINDC5
          IF        @EQUAL
            MOVE      "1",PMPXSIN7
            GOTO      CHKALE10
          ENDIF
          MATCH     "B",TCINDC5
          IF        @EQUAL
            MOVE      "1",PMPXSIN8
            GOTO      CHKALE10
          ENDIF
          MATCH     "R",TCINDC5
          IF        @EQUAL
            MOVE      "1",PMPXSIN9
            GOTO      CHKALE10
          ENDIF
          MATCH     "C",TCINDC5
          IF        @EQUAL
            MOVE      "1",PMPXSN11
            GOTO      CHKALE10
          ENDIF
.
          REP       "S2 1",TCINDC4
.
          MATCH     "2",TCINDC4                  * security alert? Yes->stop
          IF        @EQUAL
            PACK      PTMXSIN1,TCINDC4
            GOTO      CHKALE90
          ENDIF
.
          MATCH     PTMXSIN1,TCINDC4
          IF        @LESS
            PACK      PTMXSIN1,TCINDC4
          ENDIF
.
          MATCH     SP70,PTMXSIN1
          IF        @EQUAL
            PACK      PTMXSIN1,TCINDC4
          ENDIF
.
          GOTO      CHKALE10
.
CHKALE90  CALL      UPMAST1
          CALL      UPPMPX21
          ADD       ONE,RECDUPDT
          DISPLAY   *P1:24,*EF,"Cleared Alerts flag for UR: ",URNUMBER
.
CHKALE99  RETURN
+
.------------------------------------------------------------------------------
. Process Auto-End Selected Alert Code Report
.------------------------------------------------------------------------------
PRCU0000  DISPLAY   *P1:24,"Processing Auto-End Selected Alert Code Report:"
          MOVE      PRGNAM02,PRGNAM
.
          CALL      IBACLOCK
          PACK      WORKDATE,CCC,CYY,CMM,CDD     * Set up current date
          REP       " 0",WORKDATE
          MOVE      CTIMEIS,WORKTIME
.
          PACK      KEY5,FILTCATG,SP70
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE      SP70,TDESC
          ENDIF
          MOVE      TDESC,CATGDESC
.
          PACK      KEY5,FILTCATG,FILTCODE,SP70
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE      SP70,TDESC
          ENDIF
          MOVE      PTCDLDES,CODELDSC 
.
          MOVE      FISCHDAT,CDYSFDTE
          MOVE      TODAY,CDYSTDTE
          CALL      CALCDAYS
          MOVE      FILTSTRD,FILSTRDT
          SUB       ONE,FILSTRDT
          ADD       CDYSDAYS,FILSTRDT
          MOVE      FILSTRDT,FILSTRMO
.
          UNPACK    FILSTRMO,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      STRDATED,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          UNPACK    WORKDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      ENDDATED,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          CALL      PHED0000
.
          PACK      KEY5,FILTCATG,FILTCODE,SP70
          CALL      RDCODE1
          IF        OVRCD = 1
            MOVE      SP70,TCINDC5
          ENDIF
          MOVE      TCINDC5,SAVIND5
.
          PACK      KEY24,SP16,FILTCATG,FILTCODE,SP70
PRCU0010  CALL      RSPTALR2                   * Positional read
PRCU0020  CALL      RKPTALR2                   * Read next record
          BRANCH    OVRCD,PRCU9000             * End of file
.
          DISPLAY   *P20:24,*EL,PTALURNO,SP1,PTALCATG,SP1,PTALCODE
.
          MATCH     SP70,PTALEDAT
          GOTO      PRCU9000 IF NOT EQUAL
.
          MATCH     FILTCATG,PTALCATG
          GOTO      PRCU0020 IF NOT EQUAL
.
          MATCH     FILTCODE,PTALCODE
          GOTO      PRCU0020 IF NOT EQUAL
.
          MATCH     PTALDATE,FILSTRMO
          IF        @LESS
            GOTO      PRCU0020
          ENDIF
.
          PACK      KEY8,PTALURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,PRCU0020
.
          PACK      KEY8,PTALURNO,SP70
          CALL      RDPMPX21
          BRANCH    OVRCD,PRCU0020
.
. Update patient alert end date
.
PRCU0060  PACK      SAVKEY24,PTALEDAT,PTALURNO,PTALCATG,PTALCODE,SP70  
          MOVE      PTALURNO,SAVPURNO 
          PACK      KEY16,PTALURNO,PTALCATG,PTALCODE,PTALCNTR,SP70
          MOVE      KEY16,SAVKEY16 
          CALL      RDPTALR1
.
          MOVE      WORKDATE,PTALEDAT
          MOVE      WORKDATE,PTALUDAT
          MOVE      CTIMEIS,PTALUTIM 
          MOVE      "AutoEndPgm",PTALUUID 
          CALL      UPPTALR1
          ADD       ONE,NOFUPDT
          CALL      PRTALR00
.
          MATCH     SP70,SAVIND5
          GOTO      PRCU0080 IF EQUAL        
.
. clear PMPXSIN7, PMPXSIN8, PMPXSIN9 or PMPXSIN11 flags
.
          CALL      CALTFL00    
.
. clear PTMXSIN1 flag if no active
. 
PRCU0080  CALL      CALTST00                     
.
PRCU0090  CALL      PMIGTNID                   * get national id for dgate write
          MOVE      NMPNUMB,PTNINMPI
          MOVE      ONE,HL7TRGID
          MOVE      SP8,HL7INCLD
          PROC      DGCLICUP                     * trigger PMI update message
.
          MOVE      SAVKEY24,KEY24  
          GOTO      PRCU0010 
.
PRCU9000  CALL      PEND0000                     * Print End of report
.
PRCU9999  RETURN
.
.------------------------------------------------------------------------------
. Clear patient alert status (PTMXSIN1) if no active 
.------------------------------------------------------------------------------
CALTST00  MOVE      SAVPURNO,KEY8
          PACK      KEY16,KEY8,SP70
          CALL      RSPTALR1                   * Positional read
CALTST20  CALL      RKPTALR1                   * Read next record
          BRANCH    OVRCD,CALTST80             * End of file
.
          MATCH     KEY8,PTALURNO
          GOTO      CALTST80 IF NOT EQUAL
.
          MATCH     SP70,PTALEDAT
          GOTO      CALTST20 IF NOT EQUAL
.
          MATCH     SP70,PTALDTIN
          GOTO      CALTST20 IF NOT EQUAL
.
. active found. Both PTALEDAT and PTALDTIN are blank, no nothing 
.
          GOTO      CALTST99
.
CALTST80  MOVE      SP70,PTMXSIN1              * Clear status
          CALL      UPMAST1 
.
CALTST99  RETURN
.
.------------------------------------------------------------------------------
. Check if any other active with the same indicator 5 as selected alert
. category/code indicator 5, If it is, do nothing. Otherwise, clear PMPXSIN7,
. PMPXSIN8, PMPXSIN9 or PMPXSN11 depending on if indicator 5 is "M", "B", "R"
. or "C" respectively
.------------------------------------------------------------------------------
CALTFL00  MOVE      PTALURNO,KEY8
          PACK      KEY16,KEY8,SP70
          CALL      RSPTALR1                   * Positional read
CALTFL20  CALL      RKPTALR1                   * Read next record
          BRANCH    OVRCD,CALTFL80             * End of file
.
          MATCH     KEY8,PTALURNO
          GOTO      CALTFL80 IF NOT EQUAL
.
          MATCH     SP70,PTALEDAT
          GOTO      CALTFL20 IF NOT EQUAL
.
          MATCH     SP70,PTALDTIN
          GOTO      CALTFL20 IF NOT EQUAL
.
. active found. Both PTALEDAT and PTALDTIN are blank
.
          PACK      KEY5,PTALCATG,PTALCODE,SP70
          CALL      RDCODE1
          IF        OVRCD = 1
            MOVE      SP70,TCINDC5
          ENDIF
.
          MATCH     TCINDC5,SAVIND5            * same ind 5     
          GOTO      CALTFL99 IF EQUAL
.
          GOTO      CALTFL20
.
CALTFL80  MATCH     ANSM,SAVIND5
          IF        @EQUAL
            MOVE      SP70,PMPXSIN7            * Clear Medical Alert
            GOTO      CALTFL82
          ENDIF
.
          MATCH     ANSB,SAVIND5
          IF        @EQUAL
            MOVE      SP70,PMPXSIN8            * Clear Micro Alert  
            GOTO      CALTFL82
          ENDIF
.
          MATCH     ANSR,SAVIND5
          IF        @EQUAL
            MOVE      SP70,PMPXSIN9            * Clear Risk Alert
            GOTO      CALTFL82
          ENDIF
.
          MATCH     ANSC,SAVIND5
          IF        @EQUAL
            MOVE      SP70,PMPXSN11            * Clear Chronic Condition Alert 
            GOTO      CALTFL82
          ENDIF
.
          GOTO      CALTFL99 
.
CALTFL82  CALL      UPPMPX21
.
CALTFL99  RETURN
.
.------------------------------------------------------------------------------
. Print the header of the report
.------------------------------------------------------------------------------
PHED0000  CALL      HEAD132
.
          PRINT     *40,"Selected Category - H1-H9 : ",*68,FILTCATG,SP2,CATGDESC
          PRINT     *40,"Selected Code             : ",*68,FILTCODE,SP1,CODELDSC
          PRINT     *40,"Selected Alert Start Date : ",*68,STRDATED
.
          PRINT     *40," "
          CALL      PRTUND00 
.
          PRINT     *1,"|",*5,"Patient":
                    *14,"|",*16,"Name":
                    *67,"|",*69,"Alert Start Date":
                    *86,"|",*88,"End Date":
                    *105,"|"
          CALL      PRTUND00
.
          MOVE      TEN,CLNO
.
PHED9999  RETURN
.
PRTUND00  PRINT    "*---------------------------------------":
                   "----------------------------------------":
                   "------------------------*"
          ADD      ONE,CLNO
.
PRTUND99  RETURN 
.
.------------------------------------------------------------------------------
. Print the details line
.------------------------------------------------------------------------------
PRTALR00  ADD       ONE,CLNO
          IF        CLNO >= 54
            CALL      PRTUND00
            CALL      PHED0000
            ADD       ONE,CLNO
          ENDIF
.
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,PATNAME        * Patients name
.
          UNPACK    PTALDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      KEY11,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
. 
          PRINT     *1,"|",*4,PTALURNO:
                    *14,"|",*16,PATNAME:
                    *67,"|",*69,KEY11:
                    *86,"|",*88,ENDDATED:
                    *105,"|"
.
PRTALR99  RETURN
.
.------------------------------------------------------------------------------
. Print the end of the report
.------------------------------------------------------------------------------
PEND0000  CALL      PRTUND00
          PRINT     *N,*1,"No. of records updated: ",NOFUPDT
          PRINT     *N,"**** End of Report ****"
.
PEND9999  RETURN
+
.********************************************************************** 
.*              I/O INCLUDES FOR FILES AND GENERAL ROUTINES           *
.**********************************************************************
.
          INC       STD001IO
          INC       DGCLICUP
          INC       PMIGTNID
.
          INC       NHIMASIO/INC
          INC       PATALRIO/INC       * Patient Alerts
          INC       PATCODIO/INC       * Codes file
          INC       PATMA1IO/INC       * Master file
          INC       PATNIDIO/INC
          INC       PMSPX2IO/INC
          INC       WEBSECIO/INC
.
          INC       CALCDAYS
.
................................................................................

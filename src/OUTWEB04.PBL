.---------------------------------------------------------------------------
. Program       : OUTWEB04
.
. Function      : Outpatient Common Maintenance
.
. Modifications :
.---------------------------------------------------------------------------
. V11.03.01 05/12/2022  Bella Turco    TSK 0921957
.           Added NOMORE00 - Display "No more records" at end of list
.           Added PTCNNRTD - Displays number of rows set by parameter value
. V11.02.01 10/02/2022  Thanh T          TSK 0905641
.           Recompiled as OUTHEDFD changes
. V11.00.04 29/06/2020  Ebon Clements  TSK 0893340
.           Added ListIcon class to MaintenanceIcon outputs
. V11.00.03 20/03/2020  Peter Vela     TSK 0889143
.           Fixed OUTLET IO calls
. V11.00.02 18/03/2020  Peter Vela     TSK 0889143
.           Recompiled for OUTLETFD
. V11.00.01 02/03/2020  Tracey Nguyen      TSK 0823386
.           Added OTSTACTV, OTSTSPAR, OTSIT012
.---------------------------------------------------------------------------
. V10.07.03 16/12/2015  Ebon Clements  CAR 294154
.           Added SMS message notifications to letter maintenance
. V10.07.02 21.10.2015  Ebon Clements  CAR 320819
.           Added active/inactive indicator to OP letter maintenance
. V10.07.01 12.10.2015  Ebon Clements  CAR 316629
.           Added hospital to holiday file key  - OUTPHOFD
. V10.04.01 31.03.2014  B.G.Ackland  CAR 299363
.           Replace META Tag Redirect with Javascript in Common RDIREC00
.           Routine
.---------------------------------------------------------------------------
. V10.03.01 25/02/2013 Ania P         CAR 281021
.           Removed unnecessary modification of constants.
.---------------------------------------------------------------------------
. V10.02.01 07/10/2011  Ebon Clements CAR 252448
.           Limit letter lines to 99 to fix array out of bounds - GLTEXT00
.---------------------------------------------------------------------------
. V9.12.01  28/07/2009  Ebon Clements CAR 
.           Revised outpatient system wide comments
.---------------------------------------------------------------------------
. V9.05.01  09/03/2006  Ebon Clements CAR 78533
.           Mods for PATCODTM - Hospital specific category codes
. V9.05.B01 30/01/2006  Ebon Clements CAR 93186
.           Mods for REDIRECT length change. Recompiled for IBAWEBDF
. V9.04.03  22.05.2005 J.Tan     CAR 60889
.           Added tag for item schedule fee
. V9.04.02  22/02/2005 J.Tan          CAR 57097
.           Commented System type field from Site
. V9.04.01  01/02/2005 Ebon Clements  CAR 56707
.           Added report 6 - slot type default charges
. V9.03.05  09/02/2004 Sylvek Litewka CAR 47000
.           Added processing for new field outletrf.OTLTTYPE (Letter Type).
. V9.03.04  17/10/2003 Ebon Clements  CAR 43674
.           Removed site prefix from OUTVACFD
. V9.03.03  30/09/2003 Ebon Clements  CAR 43674
.           Close files before opening in OPNOUT00
. V9.03.02  12/09/2003  Sandra Barcham      43010
.           Removed file prefix from opens of outpreaf & outphoaf
. V9.03.01  15/05/2003  Sylvek Litewka  CAR 39071
.           Modified procedure SUSSES00 to populate new 
.           "outmspaf" fields OTMSRFDT and OTMSRTDT.
.---------------------------------------------------------------------------
. V9.02.07  05/12/2002  Sylvek Litewka  SRF 34195
.           Modified CHKCOD00 procedure to remove 'Mandatory'
.           validation for "Charge Flag" field (OTSIT007).
.           29/11/2002  Lina Vo         SRF 22709
.           Changed program to open outpatient files with 
.           site prefix.
. V9.02.06  15/11/2002  Sylvek Litewka  SRF 22721
.           Added processing for displaying and capturing
.           the details of Site Specific Category Codes file
.           (outsipaf.dat).
. V9.02.05  31/10/2002  Sylvek Litewka  SRF 22712
.           Added processing for Outpatients Extra Templates
.           file.
. V9.02.04  20/05/2002 Ebon Clements 17795
.           Changed suspend sessions to change all appointments
.           to suspended and not remove the session master
. V9.02.03  08/03/2002 Ebon Clements
.           Added suspend session functions to public holidays
. V9.02.02  02/10/2001 Ebon Clements
.           Added VACS code maintenance
. V9.02.01  04/09/2001 Ebon Clements
.           Added outpatient letter maintenance 
. V9.00.B01 HPS Banglore
.           Added System Wide Comments Option
.----------------------------------------------------------------------
. V5.08.01  Bronko Sosic
.           Removed check from Site Code for checkbox
.----------------------------------------------------------------------
.                 V5.07.05 25/03/2000 Bronko Sosic
.                          Added startkey for GOTO button
.                 V5.07.04 28/02/2000 Pat Dirito  
.                          Added javascript instances with reportno's
.                 V5.07.03 12/01/2000 Bronko Sosic
.                          Added REP " 0" after CYY
.                 V5.07.02 20/12/1999 Katie Nelson 
.                          Recompiled for IBAWEBDF/IBAWEBCD
.                 V5.07.01 03/11/1999 Katie Nelson 
.                          Recompiled for WEBDATCD/DAYOFWEK
.                 V5.07.00 22/07/1999 Pat Dirito
.                          Created Program 
.----------------------------------------------------------------------
          INC       IBAWEBDF    
          INC       WEBDATDF
.
          INC       MLTSECFD/INC    * Hospital security
          INC       OUTSITFD/INC    * Outpatients - Site Codes File
          INC       OUTPHOFD/INC    * Outpatients - Public Holiday File 
          INC       PATIRNFD/INC    * Invoice Range File 
          INC       OUTCMNFD/INC    * Comments File
          INC       OUTLETFD/INC    * Outpatient Letters
          INC       OUTHEDFD/INC    * Outpatient slot template header
          INC       OUTVACFD/INC    * Outpatient VACS codes
          INC       OUTSESFD/INC    * Outpatient Session File
          INC       OUTMSPFD/INC    * Outpatient Suspensin File
          INC       OUTBOAFD/INC    * Outpatient Clinic Booking File A
          INC       OUTTXSFD/INC    * Outpatient Extra Templates File FD
          INC       OUTTXSTD/INC    * Outpatient Extra Templates File TD
          INC       OUTSIPFD/INC    * Outpatient Site Specific Categories - FD
          INC       OUTSIPTD/INC    * Outpatient Site Specific Categories - TD
          INC       OUTDCHFD/INC    * Outpatient slot type default charges
          INC       PATCONFD/INC
          INC       PATHSPFD/INC    * Hospital File
          INC       PRIITMFD/INC    * Private Practice Items
.
.----------------------------------------------------------------------
. CGI Parameters
.----------------------------------------------------------------------
ACTIDESC  DIM       8     * Active or Inactive Description
NEXTPAGE  DIM       1
UPDTTYPE  DIM       1     * Update Type
SMSSDESC  DIM       3
STARTKEY  DIM       10    * Starting Key
.
OTVAC001  DIM       3     * VACS Code
OTVAC002  DIM       8     * Effective Date (CCYYMMDD)
OTVAC003  DIM       9     * VACS Weight
OTVAC004  DIM       15    * Amount
.
OTDCH001  DIM       3     * Slot Type - Category CV
OTDCH002  DIM       1     * Registrar (Y/N) 0-No 1-Yes
OTDCH003  DIM       3     * Item Type 0=MBS 1=AMA
OTDCH004  DIM       9     * CMBS Code
OTDCH005  DIM       3     * MBS Subitem Number
.
OTSIT001  DIM       6     * Site Code (TO COMMON IDDD)
OTSIT002  DIM       30    * Site Code Description
OTSIT003  DIM       8     * Password
OTSIT004  DIM       3     * File Prefix (TO COMMON UID)
OTSIT005  DIM       3     * Outpatient Department (Cat O)
OTSIT006  DIM       3     * Invoice Range to be used
OTSIT007  DIM       1     * Charging used for this site?
OTSIT008  DIM       2     * Source of referral Category
OTSIT009  DIM       3     * Max. Days for Booking screen
OTSIT010  DIM       2     * Screen Id for extra Booking Screen
OTSIT011  DIM       2     * Screen Id for extra Attendee screen  
OTSIT012  DIM       1     * Active
.
OTPHO001  DIM       4     * Calendar Year
OTPHO002  DIM       8     * Date (DD MM CCYY) - Formatted
OTPHO003  DIM       30    * Public Holiday Description
OTPHO004  DIM       3     * Suspend session reason CAT CE
OTPHO005  DIM       3     * Hospital (pathspaf)
OTPHO006  DIM       3     * New Hospital for Update (pathspaf)
.
OTLEEND   FORM      3
OTLECOMM  DIM       70[999]                Comments
.
OTLET001  DIM       3     * Letter Number
OTLET002  DIM       70    * Line of letter
OTLET003  DIM       2     * Letter bottom margin
OTLET004  DIM       2     * Letter top margin
OTLET005  DIM       3     * Physical paper length (no. of lines)
OTLET006  DIM       2     * Letter left margin
OTLET007  DIM       8     * Mail Merge File Name
OTLET008  DIM       1     * Letter Type, R = Referral Letter
OTLET009  DIM       1     * Active/Inactive
OTLET010  DIM       1     * Communication Method
OTLET011  DIM       1     * Process Confirmation
.
LETTNUMB  FORM      3
.
BYDIM     DIM       2
DATECMN   DIM       9 
ENDATE    DIM       11
DIM3      DIM       3
CMDAY     DIM       2
CMMON     DIM       3
CMYEAR    DIM       4
CMNCOUNT  FORM      3
CMNODATE  DIM       16
CMNTDATE  DIM       16
CONFDESC  DIM       3
.------------------------(HPS blr code  starts here)------------------------
.
OTCMNKEY  DIM       3     * Key For Comment
OTCMNYRR  DIM       4     * Comment Year
OTCMN001  DIM       9     * Comment Valid To Date
OTCMN002  DIM       11    * Comment Valis From Date
OTCMN003  DIM       60    * Comment One
OTCMN004  DIM       60    * Comment Two
OTCMN005  DIM       60    * Comment Three
OTCMN006  DIM       60    * Comment Four
OTCMN007  DIM       6     * Site Code
OTCMN008  DIM       6     * Clinic Type
OTCMN009  DIM       8     * Entered Date (System Generated)
OTCMN010  DIM       8     * Entered Time (System Generated)
OTCMN011  DIM       10    * Entered By (System Gener. WEB User ID)
OTCMN012  DIM       80    * Spare Variable
.
.------------------------(HPS blr Code Ends Here)---------------------------
.---------------------------------------------------------------------------
.             Program Variables
.---------------------------------------------------------------------------
PBYEAR    DIM       4     * Public Holiday Year
PBYR      FORM      4     * Public Holiday Year 
DATE      DIM       11    * Formatted Public Holiday Date
PBDATE    DIM       4     * Month & Day 
.
FILTACTV  DIM       1
FROMDATE  DIM       11    * System Comments From Date
TODATE    DIM       11    * System Comments To Date
INPUT     DIM       30    * System Inputs
.
UNQCOUNT  DIM       19     * Unique Count
CMNYR     DIM       4      * System Wide Comments Year
CMNYEAR   FORM      4      * System Wide Comments Year
CMNTYEAR  DIM       4      * YEAR
CURDUM4   FORM      1      * HPS01I
CURDUM5   FORM      3
CURDUM6   FORM      3
SERVPROG  DIM       8      * Stores Server Program name
SPECCHAR  INIT      "%"
SESSKEYS  DIM       25
SAVKEY25  DIM       25
CFILEPRE  DIM       3   * Current Open File Prefix for Outpatient Files
.
.----------------------------------------------------------------------
PRGID     INIT      "OUTWEB04"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "Outpatient Common Maintenance"
.----------------------------------------------------------------------
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          CALL      OUTFIL00       * Check Correct Files Are Open & Valid Site
          BRANCH    EXIT,MAIN1000
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200,MAIN1300,MAIN1400:
                             MAIN1500,MAIN1600
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "Invalid Option",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  CALL      OUTCOD00        * Outpatient Site Code Maintenance
          BRANCH    EXIT,MAIN1000
          MOVE      "Update Complete",WEBTITLE
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1200  CALL      OUTHOL00        * Public Holiday Maintenance 
          BRANCH    EXIT,MAIN1000
          MOVE      "Update Complete",WEBTITLE
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1300  CALL      OUTCMN00        * Outpatient Comment Maintenance(hps)
          BRANCH    EXIT,MAIN1000
          MOVE      "Update Complete",WEBTITLE
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1400  CALL      OUTLET00           * Outpatient Letter Maintenance
          GOTO      MAIN1000
.
MAIN1500  CALL      OUTVAC00           * Outpatient VACS Code Maintenance
          BRANCH    EXIT,MAIN1000
          MOVE      "Update Complete",WEBTITLE
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1600  CALL      OUTDEF00           * Outpatient Slot Type Default Charges
          BRANCH    EXIT,MAIN1000
          MOVE      "Update Complete",WEBTITLE
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
.------------------------------------------------------------
. Output Next Page Based on CGI Parameter nextpage
.------------------------------------------------------------
NXTPAG00  MOVE      ZERO,F1
          MOVE      NEXTPAGE,F1
          BRANCH    F1,NXTPAG10,NXTPAG20,NXTPAG30,NXTPAG40
.
          CALL      WINCLS00
          GOTO      NXTPAG99
.
NXTPAG10  CALL      RDIREC00
          GOTO      NXTPAG99
.
NXTPAG20  CALL      GOBACK00
          GOTO      NXTPAG99
.
NXTPAG30  CALL      GOBACK00
          GOTO      NXTPAG99
.
NXTPAG40  CALL      GOBACK00
          GOTO      NXTPAG99
.
NXTPAG99  RETURN
.-------------------------------------------------------------------------------
.  Goes back x number of pages
.-------------------------------------------------------------------------------
GOBACK00  CALL      OPENHTML
          BRANCH    EXIT,GOBACK99
          MOVE      NEXTPAGE,F1
          SUB       TWO,F1
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "    alert(#"",WEBTITLE,"#");":
.                            "    self.close();":
                             "    window.history.go(-",F1,");":
                             "}":
                             "</script>"
          CALL      CLOSHTML
.
GOBACK99  RETURN
.------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------
INIT0000  OPEN      OUTSITA1,"outsitaf"   * Outpatient Site Code
          OPEN      OUTLETR1,"outletrf"   * Outpatient Letters
          OPEN      OUTLETR2,"outletrf"   * Outpatient Letters
          OPEN      OUTTXSA1,"outtxsaf"   * Outpatient Extra Templates file
          OPEN      OUTSIPA1,"outsipaf"   * Outpatient Site Specific Categories
          OPEN      OUTVACA1,"outvacaf"   * Outpatient VACS codes
          OPEN      OUTDCHA1,"outdchaf"   * Outpatient slot default charges
          OPEN      PRIITEM1,"priitemf"   * Private Practice Items
.
          OPEN      PATIRNG1,"patirnge"   * Invoice Range File
          OPEN      PATCODE1,"patcodes"   * Codes Table File 
          OPEN      PATCODE2,"patcodes"   * Codes Table File  
          OPEN      PATHSPA1,"pathspaf"   * Hospital File
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,HUND25;*238,PTCNNRTD
.
          MOVE      ZERO,F2
          MOVE      PTCNNRTD,F2
          IF        F2 = 0
            MOVE      "10",PTCNNRTD
          ENDIF
.
          IF        IBCNMHOS=1
            OPEN      MLTCODA1,"mltcodaf"
            OPEN      MLTCODA2,"mltcodaf"
            OPEN      MLTSECA1,"mltsecaf"
          ENDIF
.
          MOVE      "by",BYDIM
.
          MOVE      "out",CFILEPRE       * Save Current File Prefix
          CALL      OPNOUT00             * Open Outpatient Files
.
          RETURN
.*******************************************************************************
.                     Check Correct Files are Open for System
.*******************************************************************************
OUTFIL00  MOVE      WBSESIT,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD,OUTFIL90
          MATCH     OSTFILE,CFILEPRE         * Save Current File Prefix
          IF        !@EQUAL
            MOVE      OSTFILE,CFILEPRE       * Save Current File Prefix
            CALL      OPNOUT00               * Open Outpatient Files
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      OUTFIL99
.
OUTFIL90  MOVE      "Outpatient Site Invalid",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
OUTFIL99  RETURN
.
.*******************************************************************************
.                    Open Outpateint Files
.*******************************************************************************
OPNOUT00  PACK      CFNAME,CFILEPRE,FILBOKA1  * Get the name of the BOKA1 file
          CLOSE     OUTBOKA1
          OPEN      OUTBOKA1,CFNAME           * Open the file
.
          PACK      CFNAME,CFILEPRE,FILSESA1  * Get the name of the SESA1 file
          CLOSE     OUTSESA1
          OPEN      OUTSESA1,CFNAME           * Open the file
.
          PACK      CFNAME,CFILEPRE,FILSESA2  * Get the name of the SESA2 file
          CLOSE     OUTSESA2
          OPEN      OUTSESA2,CFNAME           * Open the file
.
          OPEN      OUTPHOA1,"outphoaf"       * Public Holiday
.
          PACK      CFNAME,CFILEPRE,FILCMNA1  * Get the name of the CMNA1 file
          CLOSE     OUTCMNA1
          OPEN      OUTCMNA1,CFNAME           * Outpatient Comments File 
.
          PACK      CFNAME,CFILEPRE,FILMSPA1  * Get the name of the MSPA1 file
          CLOSE     OUTMSPA1
          OPEN      OUTMSPA1,CFNAME           * Outpatient Suspension file
.
          PACK      CFNAME,CFILEPRE,FILHEDA1  * Get the name of the HEDA1 file
          CLOSE     OUTHEDA1
          OPEN      OUTHEDA1,CFNAME           * Open the file
.
          RETURN
.------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          MOVE      SP70,TEMPLATE
          MOVE      ZERO,NORECORD
          MOVE      ZERO,STARTNUM
          MOVE      SP70,STARTKEY
          MOVE      SP70,NEXTPAGE
          PACK      REDIRECT,SP70,SP70,SP70,SP70
          MOVE      SP70,UPDTTYPE
          MOVE      SP70,PBYEAR
          MOVE      SP70,CMNYR
.
          MOVE      SP70,OTSIT001
          MOVE      SP70,OTSIT002
          MOVE      SP70,OTSIT003
          MOVE      SP70,OTSIT004
          MOVE      SP70,OTSIT005
          MOVE      SP70,OTSIT006
          MOVE      SP70,OTSIT007
          MOVE      SP70,OTSIT008
          MOVE      SP70,OTSIT009
          MOVE      SP70,OTSIT010
          MOVE      SP70,OTSIT011
          MOVE      SP70,OTSIT012
          MOVE      SP70,UNQCOUNT
.
          MOVE      SP70,OTPHO001
          MOVE      SP70,OTPHO002
          MOVE      SP70,OTPHO003
          MOVE      SP70,OTPHO004
          MOVE      SP70,OTPHO005
          MOVE      SP70,OTPHO006
.
          MOVE      SP70,OTCMN001
          MOVE      SP70,OTCMN002
          MOVE      SP70,OTCMN003
          MOVE      SP70,OTCMN004
          MOVE      SP70,OTCMN005
          MOVE      SP70,OTCMN006
          MOVE      SP70,OTCMN007
          MOVE      SP70,OTCMN008
          MOVE      SP70,OTCMN009
          MOVE      SP70,OTCMN010
          MOVE      SP70,OTCMN011
          MOVE      SP70,OTCMN012
          MOVE      SP70,CMNTYEAR
.
          MOVE      Z70,OTLET001
          MOVE      Z70,OTLET002
          MOVE      Z70,OTLET003
          MOVE      Z70,OTLET004
          MOVE      Z70,OTLET005
          MOVE      Z70,OTLET006
          MOVE      Z70,OTLET007
          MOVE      Z70,OTLET008
          MOVE      Z70,OTLET009
          MOVE      Z70,OTLET010
          MOVE      Z70,OTLET011
          MOVE      ZERO,OTLEEND
.
          MOVE      Z70,OTVAC001
          MOVE      Z70,OTVAC002
          MOVE      Z70,OTVAC003
          MOVE      Z70,OTVAC004
.
          MOVE      SP70,OTDCH001
          MOVE      SP70,OTDCH002
          MOVE      SP70,OTDCH003
          MOVE      SP70,OTDCH004
          MOVE      SP70,OTDCH005
.
          MOVE      SP70,FILTACTV
.
          CALL      CPOTTX00           * Outpatients Extra Templates file
          CALL      CPOTSP00           * Outpatients Site Specific Categories
.
          RETURN
.--------------------------------------------------------------------------
.       Set Parameters
.--------------------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nextpage=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NEXTPAGE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "redirect=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REDIRECT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "norecord=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NORECORD
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startkey=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTKEY
            PACK      STARTKEY,STARTKEY,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startnum=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTNUM
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updttype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDTTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "unqcount=",QRYNAME          
          IF        @EQUAL
            MOVE      QRYDATA,UNQCOUNT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "cmntyear=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,CMNTYEAR
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "otsit",QRYNAME
          IF        @EQUAL
            CALL      STCOD000
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "otpho",QRYNAME
          IF        @EQUAL
            CALL      PBHOL000
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "otcmn",QRYNAME
          IF        @EQUAL
            CALL      SYCMN000
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "otlet",QRYNAME
          IF        @EQUAL
            CALL      STLET000
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "otlecomm=",QRYNAME
          IF        @EQUAL
            CALL      GLTEXT00
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "otvac",QRYNAME
          IF        @EQUAL
            CALL      STVAC000
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "otdch",QRYNAME
          IF        @EQUAL
            CALL      STDCH000
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "ottxs",QRYNAME
          IF        @EQUAL
            CALL      SPTXS000
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "outsi",QRYNAME
          IF        @EQUAL
            CALL      SPSIP000
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtactv=",QRYNAME
          IF        @EQUAL
            PACK      FILTACTV,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
.
.-------------------------------------------------------------------------------
.  Set Parameters for Option 1: Outpatient Site Code Maintenance 
.-------------------------------------------------------------------------------
STCOD000  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,STCOD001,STCOD002,STCOD003,STCOD004,STCOD005:
                       STCOD006,STCOD007,STCOD008,STCOD009,STCOD010:
                       STCOD011,STCOD012
.
          MOVE      ONE,EXIT
          GOTO      STCOD999
.
STCOD001  SQUEEZE   QRYDATA
          MOVE      QRYDATA,OTSIT001
          PACK      OTSIT001,OTSIT001,SP70
          GOTO      STCOD999
STCOD002  MOVE      QRYDATA,OTSIT002
          GOTO      STCOD999
STCOD003  MOVE      QRYDATA,OTSIT003
          GOTO      STCOD999
STCOD004  MOVE      QRYDATA,OTSIT004
          GOTO      STCOD999
STCOD005  MOVE      QRYDATA,OTSIT005
          GOTO      STCOD999
STCOD006  MOVE      QRYDATA,OTSIT006
          GOTO      STCOD999
STCOD007  MOVE      QRYDATA,OTSIT007
          GOTO      STCOD999
STCOD008  MOVE      QRYDATA,OTSIT008
          GOTO      STCOD999
STCOD009  MOVE      QRYDATA,OTSIT009
          GOTO      STCOD999
STCOD010  MOVE      QRYDATA,OTSIT010
          GOTO      STCOD999
STCOD011  MOVE      QRYDATA,OTSIT011
          GOTO      STCOD999
STCOD012  MOVE      QRYDATA,OTSIT012
          GOTO      STCOD999
.
STCOD999  RETURN
.-------------------------------------------------------------------------------
.  Set Parameters for Option 2: Public Holiday Maintenance 
.-------------------------------------------------------------------------------
PBHOL000  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,PBHOL001,PBHOL002,PBHOL003,PBHOL004,PBHOL005:
                       PBHOL006
.
          MOVE      ONE,EXIT
          GOTO      PBHOL999
.
PBHOL001  MOVE      QRYDATA,OTPHO001
          GOTO      PBHOL999
.
PBHOL002  PACK      QRYDATA,QRYDATA,SP30
          UNPACK    QRYDATA,KEY8,KEY3 * Check for formatted or unformatted date
          MATCH     SP70,KEY3
          IF        @EQUAL
            MOVE      QRYDATA,OTPHO002 * Do not change date format = (CCYYMMDD)
          ELSE 
            MOVE      QRYDATA,KEY11   * Change Date as format is (DD MON CCYY) 
            PACK      KEY11,KEY11,SP70
            MATCH     SP70,KEY11
            IF        !@EQUAL
              UNPACK    QRYDATA,CDAY,D1,MTHNAM3,D1,CCENT,CYEAR
              CALL      SETMTH00
              PACK      OTPHO002,CCENT,CYEAR,CMON,CDAY
              REP       " 0",OTPHO002
            ENDIF
          ENDIF 
          GOTO      PBHOL999
.
PBHOL003  MOVE      QRYDATA,OTPHO003
          GOTO      PBHOL999
.
PBHOL004  MOVE      QRYDATA,OTPHO004
          GOTO      PBHOL999
.
PBHOL005  MOVE      QRYDATA,OTPHO005
          GOTO      PBHOL999
.
PBHOL006  MOVE      QRYDATA,OTPHO006
          GOTO      PBHOL999
.
PBHOL999  RETURN
.------------------------------------------------------------
. Outpatient Site Code Maintenance: Display,Add,Update,Delete
.------------------------------------------------------------
OUTCOD00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      OUTCOD50 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      OUTCOD10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      OUTCOD20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      OUTCOD30 IF EQUAL
.
          MATCH     "C",UPDTTYPE    * Update Site Specific Categories file
          GOTO      OUTCOD40 IF EQUAL
.
          GOTO      OUTCOD93
.
OUTCOD10  CALL      CHKCOD00  * Checks mandatory fields & checks for blank
          BRANCH    EXIT,OUTCOD99
          PACK      KEY6,OTSIT001,SP70  * ADD FORMACTION
          CALL      RDSITA1
          COMPARE   ZERO,OVRCD
          GOTO      OUTCOD92 IF EQUAL
          CALL      CODSAV00      * Moves input variables to file variables
          CALL      WRSITA1       * Writes away the data
          CALL      INOTTX00      * Write to Extra Templates File
          MOVE      ZERO,EXIT
          GOTO      OUTCOD99

OUTCOD20  PACK      KEY6,OTSIT001,SP70  * UPDATE FORMACTION
          CALL      RDSITA1
          BRANCH    OVRCD,OUTCOD91
          CALL      CHKCOD00      * Checks mandatory fields
          BRANCH    EXIT,OUTCOD99
          CALL      CODSAV00      * Moves input variables to file variables
          CALL      UPSITA1       * Writes away the data
          CALL      UPOTTX00      * Update Extra Templates File
          MOVE      ZERO,EXIT
          GOTO      OUTCOD99
.
OUTCOD30  PACK      KEY6,OTSIT001,SP70   * DELETE FORMACTION
          CALL      RDSITA1
          BRANCH    OVRCD,OUTCOD91
          CALL      DESITA1       * Deletes the data
          CALL      DLOTTX00      * Delete Extra Templates File
          CALL      DLOTSP00      * Delete Site Specific Categories File
          MOVE      ZERO,EXIT
          GOTO      OUTCOD99
.
OUTCOD40  PACK      KEY6,OTSIT001,SP70   * UPDATE SITE SPECIFIC CATEGORIES
          CALL      RDOTSIP1
          IF        OVRCD=1
            CALL      CLOTSP00
          ENDIF
.
          CALL      MVOTSP00      * Moves input variables to file variables
          MOVE      OTSIT001,OTSPSITE
.
          CALL      RAOTSIP1      * Does record exist?
          IF        OVRCD=1
            CALL      WROTSIP1    * No, write record
          ELSE
            CALL      UPOTSIP1    * Yes, update record
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      OUTCOD99
.
OUTCOD50  PACK      KEY6,OTSIT001,SP70 
          CALL      RDSITA1
          IF        OVRCD=1
            CALL      CLRCOD00    * Clear all the file variables
          ENDIF
.
          PACK      KEY12,OTSIT001,SP70
          CALL      RDOTTXS1      * Read Extra Template file for Site Code
          IF        OVRCD=1
            CALL      CLOTTX00    * Clear all the file variables
          ENDIF
.
          PACK      KEY6,OTSIT001,SP70
          CALL      RDOTSIP1      * Read Site Specific file for Site Code
          IF        OVRCD=1
            CALL      CLOTSP00    * Clear all the file variables
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Site Code Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,OUTCOD99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      OUTCOD99
.
OUTCOD91  MOVE      "Site Code does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      OUTCOD99
.
OUTCOD92  MOVE      "Site Code exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      OUTCOD99
.
OUTCOD93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      OUTCOD99
.
OUTCOD99  RETURN
.-----------------------------------------------------------
. Insert into Extra Templates File
.-----------------------------------------------------------
INOTTX00  MATCH     SP70,OTTXS003           * Is booking template populated?
          GOTO      INOTTX10 IF NOT EQUAL
          MATCH     SP70,OTTXS006           * Is attendance template populated?
          GOTO      INOTTX10 IF NOT EQUAL
.
          GOTO      INOTTX99                
.
INOTTX10  PACK      KEY12,OSTSITE,SP70
          CALL      RAOTTXS1
          COMPARE   ZERO,OVRCD
          GOTO      INOTTX90 IF EQUAL
.
          CALL      MVOTTX00          * Move input vars to Extra Templ. file
          MOVE      OSTSITE,OTTXSITE  * Set site code	
          CALL      WROTTXS1          * Write Extra Templates file record
.
          GOTO      INOTTX99
.
INOTTX90  MOVE      "Extra Templates exist for this Site Code!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      INOTTX99
.
INOTTX99  RETURN
.-----------------------------------------------------------
. Update Extra Templates File
.-----------------------------------------------------------
UPOTTX00  PACK      KEY12,OSTSITE,SP70
          CALL      RDOTTXS1
          BRANCH    OVRCD,UPOTTX20          * Record not found
.
          MATCH     SP70,OTTXS003           * Is booking template populated?
          GOTO      UPOTTX10 IF NOT EQUAL
          MATCH     SP70,OTTXS006           * Is attendance template populated?
          GOTO      UPOTTX10 IF NOT EQUAL
.
.         Record exists and extra template fields have been blanked out.
.         Therefore delete this record.
.
          CALL      DEOTTXS1               
          GOTO      UPOTTX99
.
.         Record exists and extra template fields are populated - update record
.
UPOTTX10  CALL      MVOTTX00           * Move input vars to Extra Templ. file
          MOVE      OSTSITE,OTTXSITE   * Set site code
          CALL      UPOTTXS1           * Update Extra Templates file record     
          GOTO      UPOTTX99
.
UPOTTX20  MATCH     SP70,OTTXS003           * Is booking template populated?
          GOTO      UPOTTX30 IF NOT EQUAL
          MATCH     SP70,OTTXS006           * Is attendance template populated?
          GOTO      UPOTTX30 IF NOT EQUAL
.
.         Record does not exist and extra template fields are blank
.
          GOTO      UPOTTX99
.
.         Record does not exist and extra template fields are populated.
.         Insert record.
.
UPOTTX30  CALL      MVOTTX00           * Move input vars to Extra Templ. file
          MOVE      OSTSITE,OTTXSITE   * Set site code
          PACK      KEY12,OTTXSITE,SP70
          CALL      WROTTXS1           * Update Extra Templates file record
.
UPOTTX99  RETURN
.-----------------------------------------------------------
. Delete Extra Templates File
.-----------------------------------------------------------
DLOTTX00  PACK      KEY12,OSTSITE,SP70
          CALL      RDOTTXS1
          BRANCH    OVRCD,DLOTTX99          * Record not found
.
          CALL      DEOTTXS1
.
DLOTTX99  RETURN
.-----------------------------------------------------------
. Delete Site Specific Categories File
.-----------------------------------------------------------
DLOTSP00  PACK      KEY6,OSTSITE,SP70
          CALL      RDOTSIP1
          BRANCH    OVRCD,DLOTSP99          * Record not found
.
          CALL      DEOTSIP1
.
DLOTSP99  RETURN
.-----------------------------------------------------------
. Initialize's file data
.-----------------------------------------------------------
CLRCOD00  MOVE      SP70,OSTSITE   
          MOVE      SP70,OSTDESC   
          MOVE      SP70,OSTPASS   
          MOVE      SP70,OSTFILE   
          MOVE      SP70,OSTSYST   
          MOVE      SP70,OSTIRNG   
          MOVE      SP70,OSTCHRG   
          MOVE      SP70,OSTCATG   
          MOVE      SP70,OSTMAXD  
          MOVE      SP70,OTSTXBOK  
          MOVE      SP70,OTSTXATT
          MOVE      SP70,OTSTACTV
          MOVE      SP70,OTSTSPAR
.
CLRCOD99  RETURN
.------------------------------------------------------------
. Moves cgi variables into file variables
.------------------------------------------------------------
CODSAV00  MOVE      OTSIT001,KEY6
          SQUEEZE   KEY6
          MOVE      KEY6,OSTSITE
          MOVE      OTSIT002,OSTDESC
          MOVE      OTSIT003,OSTPASS
          MOVE      OTSIT004,OSTFILE
          MOVE      OTSIT005,OSTSYST
          MOVE      OTSIT006,OSTIRNG 
.
          MATCH     "C",OTSIT007
          IF        @EQUAL 
            MOVE      "C",OSTCHRG
          ELSE
            MOVE      " ",OSTCHRG
          ENDIF         
.
          MOVE      OTSIT008,OSTCATG 
          MOVE      OTSIT009,OSTMAXD
          MOVE      SP70,OTSTXBOK
          MOVE      SP70,OTSTXATT
          MOVE      OTSIT012,OTSTACTV
          MOVE      SP70,OTSTSPAR
.
          
.
CODSAV99  RETURN
.------------------------------------------------------------------
. * Checks mandatory fields and checks for blank
.------------------------------------------------------------------
CHKCOD00  RESET     OTSIT001
          MATCH     SP70,OTSIT001
          GOTO      CHKCOD90 IF EQUAL
.
          RESET     OTSIT004
          MATCH     SP70,OTSIT004
          GOTO      CHKCOD91 IF EQUAL
.
.         RESET     OTSIT005
.         MATCH     SP70,OTSIT005
.         GOTO      CHKCOD92 IF EQUAL
.
          RESET     OTSIT006
          MATCH     SP70,OTSIT006
          GOTO      CHKCOD93 IF EQUAL
.
..          RESET     OTSIT007
..          MATCH     SP70,OTSIT007
..          GOTO      CHKCOD94 IF EQUAL
.
          RESET     OTSIT008
          MATCH     SP70,OTSIT008
          GOTO      CHKCOD95 IF EQUAL
.
          RESET     OTSIT009
          MATCH     SP70,OTSIT009
          GOTO      CHKCOD96 IF EQUAL
.
          MATCH     SP70,OTTXS003
          IF        !@EQUAL
            MOVE      OTTXS003,SERVPROG
            REP       UPPLOW,SERVPROG
            PACK      KEY13,SERVPROG,OTTXS004,OTTXS005,SP70
            CALL      RAWBTP1
            BRANCH    OVRCD,CHKCOD80
          ENDIF
.
          MATCH     SP70,OTTXS006
          IF        !@EQUAL
            MOVE      OTTXS006,SERVPROG
            REP       UPPLOW,SERVPROG
            PACK      KEY13,SERVPROG,OTTXS007,OTTXS008,SP70
            CALL      RAWBTP1
            BRANCH    OVRCD,CHKCOD81
          ENDIF
.
          GOTO      CHKCOD98
.
CHKCOD80  MOVE      "Invalid Booking Screen!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKCOD99
.
CHKCOD81  MOVE      "Invalid Attendance Screen!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKCOD99
.
CHKCOD90  MOVE      "Site Code is mandatory!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKCOD99
.
CHKCOD91  MOVE      "File Prefix is mandatory!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKCOD99
.
CHKCOD92  MOVE      "System Type is mandatory!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKCOD99
.
CHKCOD93  MOVE      "Invoice Range is mandatory!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKCOD99
.
CHKCOD94  MOVE      "Charging Flag is mandatory!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKCOD99
.
CHKCOD95  MOVE      "Referral Category is mandatory!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKCOD99
.
CHKCOD96  MOVE      "Max Days to search is mandatory!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKCOD99
.
CHKCOD98  MOVE      ZERO,EXIT
CHKCOD99  RETURN
.------------------------------------------------------------
. Public Holiday Maintenance: Display,Add,Update,Delete
.------------------------------------------------------------
OUTHOL00  RESET     OTPHO001 
          RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      OUTHOL70 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      OUTHOL10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      OUTHOL20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      OUTHOL30 IF EQUAL
.
          MATCH     "S",UPDTTYPE    * Suspend all session for public holiday
          GOTO      OUTHOL40 IF EQUAL
.
          GOTO      OUTHOL93
.
OUTHOL10  CALL      CHKHOL00  * Checks mandatory fields & checks for blank
          BRANCH    EXIT,OUTHOL99
.
          PACK      KEY11,OTPHO002,OTPHO005,SP70  * ADD FORMACTION
          CALL      RDPHOA1
          COMPARE   ZERO,OVRCD
          GOTO      OUTHOL92 IF EQUAL
.
          CALL      HOLSAV00      * Moves input variables to file variables
.
          CALL      WRPHOA1       * Writes away the data
          MOVE      ZERO,EXIT
          GOTO      OUTHOL99
.
OUTHOL20  PACK      KEY11,OTPHO002,OTPHO005,SP70  * UPDATE FORMACTION
          CALL      RDPHOA1
          BRANCH    OVRCD,OUTHOL91
.
          MATCH     OTPHO005,OTPHO006             * Change of hospital
          IF        !@EQUAL
            PACK      KEY11,OTPHO002,OTPHO006,SP70  * UPDATE FORMACTION
            CALL      RDAPHOA1
            COMPARE   ONE,OVRCD
            GOTO      OUTHOL92 IF NOT EQUAL
.
            PACK      KEY11,OTPHO002,OTPHO005,SP70  * UPDATE FORMACTION
            CALL      RDPHOA1
            BRANCH    OVRCD,OUTHOL91
.
            MOVE      OTPHO006,OTPHO005             * New hospital code
          ENDIF
.
          CALL      CHKHOL00      * Checks mandatory fields
          BRANCH    EXIT,OUTHOL99
.
          CALL      HOLSAV00      * Moves input variables to file variables
.
          CALL      UPPHOA1       * Writes away the data
          MOVE      ZERO,EXIT
          GOTO      OUTHOL99
.
OUTHOL30  PACK      KEY11,OTPHO002,OTPHO005,SP70   * DELETE FORMACTION
          CALL      RDPHOA1
          BRANCH    OVRCD,OUTHOL91
.
          CALL      DEPHOA1      * Deletes the data
          MOVE      ZERO,EXIT
          GOTO      OUTHOL99
.
OUTHOL40  PACK      KEY11,OTPHO002,OTPHO005,SP70  * UPDATE FORMACTION
          CALL      RDPHOA1
          BRANCH    OVRCD,OUTHOL91
.
          CALL      SUSSES00            * Suspend all sessions for holiday
.
          MOVE      ZERO,EXIT
          GOTO      OUTHOL99
.
OUTHOL70  PACK      KEY11,OTPHO002,OTPHO005,SP70  
          CALL      RDPHOA1
          IF        OVRCD=1
            CALL      CLRHOL00    * Clear all the file variables
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Public Holiday Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,OUTHOL99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      OUTHOL99
.
OUTHOL91  MOVE      "Public Holiday Date does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      OUTHOL99
.
OUTHOL92  MOVE      "Public Holiday Date exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      OUTHOL99
.
OUTHOL93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      OUTHOL99
.
OUTHOL99  RETURN
.-----------------------------------------------------------
. Initialize's file data
.-----------------------------------------------------------
CLRHOL00  MOVE      SP70,OPHDATE
          MOVE      SP70,OPHDESC
          MOVE      SP70,OPHHOSP
          MOVE      SP70,OPHSPARE
.
CLRHOL99  RETURN
.------------------------------------------------------------
. Moves cgi variables into file variables
.------------------------------------------------------------
HOLSAV00  MOVE      OTPHO002,OPHDATE  * Pack with Year Month & Day
          MOVE      OTPHO003,OPHDESC
          MOVE      OTPHO005,OPHHOSP
          MOVE      SP70,OPHSPARE
.
HOLSAV99  RETURN
.------------------------------------------------------------------
. * Checks mandatory fields and checks for blank
.------------------------------------------------------------------
CHKHOL00  RESET     OTPHO002
          MATCH     SP70,OTPHO002
          GOTO      CHKHOL90 IF EQUAL
.
          RESET     OTPHO003
          MATCH     SP70,OTPHO003
          GOTO      CHKHOL91 IF EQUAL
.
          GOTO      CHKHOL98
.
CHKHOL90  MOVE      "Public Holiday Date is mandatory!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKHOL99
.
CHKHOL91  MOVE      "Public Holiday Description is mandatory!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKHOL99
.
CHKHOL98  MOVE      ZERO,EXIT
CHKHOL99  RETURN
.------------------------------------------------------------
. Process Fields 
.------------------------------------------------------------
PROFLD00  BRANCH    REPORTNO,PROFLD10,PROFLD20,PROFLD30,PROFLD40:
                             PROFLD50,PROFLD60
          GOTO      PROFLD99
.
PROFLD10  BRANCH    FLDSETNO,PROFLD11,PROFLD12,PROFLD13
.
PROFLD11  CALL      OTST0000    * Outpatient Site Code Maintenance
          GOTO      PROFLD99 
.
PROFLD12  CALL      OTTX0000    * Outpatient Extra Templates
          GOTO      PROFLD99
.
PROFLD13  CALL      OTSP0000    * Outpatient Site Specific Categories
          GOTO      PROFLD99
.
PROFLD20  CALL      OTPH0000    * Public Holiday Maintenance
          GOTO      PROFLD99 
.
PROFLD30  CALL      OTCM0000    * System Comments Maintenance (hps)
          GOTO      PROFLD99
.
PROFLD40  CALL      LETF0000    * Letter Details
          GOTO      PROFLD99
.
PROFLD50  CALL      OTVA0000    * Outpatient VACS code file
          GOTO      PROFLD99
.
PROFLD60  CALL      OTDC0000    * Outpatient slot default charges file
          GOTO      PROFLD99
.
PROFLD99  RETURN
.------------------------------------------------------------
. Outpatient Site Code
.------------------------------------------------------------
OTST0000  BRANCH    FLDITMNO,OTST0100,OTST0200,OTST0300,OTST0400,OTST0500:
                             OTST0600,OTST0700,OTST0800,OTST0900,OTST1000:
                             OTST1100,OTST1200,OTST1300,OTST1400
          GOTO      OTST9999
.
OTST0100  WRITE     HTMLFILE;OSTSITE;
          GOTO      OTST9999
.
OTST0200  WRITE     HTMLFILE;OSTDESC; 
          GOTO      OTST9999
.
OTST0300  WRITE     HTMLFILE;OSTPASS; 
          GOTO      OTST9999
.
OTST0400  WRITE     HTMLFILE;OSTFILE; 
          GOTO      OTST9999
.
OTST0500  MOVE      "O ",CATEGORY
          MOVE      OSTSYST,CODE
          CALL      CODITM00
          GOTO      OTST9999
.
OTST0600  CALL      INVRAN00        * Invoice Range Selection List 
          GOTO      OTST9999
.
OTST0700  MATCH     "C",OSTCHRG 
          IF        @EQUAL 
            MOVE      "C",KEY1
          ELSE
            MOVE      " ",KEY1
          ENDIF
          WRITE     HTMLFILE;KEY1; 
          GOTO      OTST9999
.
OTST0800  WRITE     HTMLFILE;OSTCATG;     * Referal Category 
          GOTO      OTST9999
.
OTST0900  WRITE     HTMLFILE;OSTMAXD; 
          GOTO      OTST9999
.
OTST1000  CALL      OTTAB000   * Table of Outpatient Site Code
          GOTO      OTST9999
.
OTST1100  CALL      NEXTGR00   * Next set of Site Codes 
          GOTO      OTST9999
.
OTST1200  CALL      PREVGR00   * Previous set of Site Codes 
          GOTO      OTST9999
.
OTST1300  PACK      KEY5,OSTCATG,SP70     * Display Category Description
          CALL      RDSCODE1
          IF        OVRCD=1
            CALL      RDPCODE1  
          ENDIF
OTST1350  CALL      RDKCODE1
          MATCH     OSTCATG,TCODE
          GOTO      OTST9999 IF NOT EQUAL
          MATCH     SP70,ACODE
          GOTO      OTST9999 IF NOT EQUAL
          WRITE     HTMLFILE;TDESC; 
          GOTO      OTST1350
.
OTST1400  WRITE     HTMLFILE;OTSTACTV;
          GOTO      OTST9999
.
OTST9999  RETURN
.------------------------------------------------------------
. Invoice Range Selection List
.------------------------------------------------------------
INVRAN00  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          MATCH     SP70,OSTIRNG
          GOTO      INVRAN20 IF EQUAL
          PACK      KEY3,OSTIRNG
          CALL      RDIRNG1
          BRANCH    OVRCD,INVRAN20
          BRANCH    F1,INVRAN10,INVRAN20
          WRITE     HTMLFILE;IRANGE;
          GOTO      INVRAN99
.
INVRAN10  WRITE     HTMLFILE;IRDESC;
          GOTO      INVRAN99
.
INVRAN20  CALL      ISEL0000
.
INVRAN99  RETURN
.------------------------------------------------------------
. Invoice Range Selection
.------------------------------------------------------------
ISEL0000  PACK      KEY3,SP70
          CALL      RDSIRNG1
ISEL0100  CALL      RDKIRNG1
          BRANCH    OVRCD,ISEL9999
.
          MOVE      IRDESC,KEY40
          PACK      KEY5,QUOTE,IRANGE,QUOTE
          MATCH     OSTIRNG,IRANGE
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=",KEY5," selected>":
                               KEY40,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=",KEY5,">",KEY40,"</option>"
          ENDIF
          GOTO      ISEL0100
.
ISEL9999  RETURN
.------------------------------------------------------------
. Public Holiday Maintenance
.------------------------------------------------------------
OTPH0000  BRANCH    FLDITMNO,OTPH0100,OTPH0200,OTPH0300,OTPH0400,OTPH0500:
                             OTPH0600,OTPH0700
          GOTO      OTPH9999
.
OTPH0100  CALL      SETYR000 * Sets current year Depending Next or Prev Page   
          GOTO      OTPH9999
.
OTPH0200  UNPACK    OPHDATE,CCENT,CYEAR,CMON,CDAY   * Date in (DD MON CCYY)
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR 
          WRITE     HTMLFILE;DATE;      
          GOTO      OTPH9999
.
OTPH0300  WRITE     HTMLFILE;OPHDESC;
          GOTO      OTPH9999
.
OTPH0400  CALL      HLTAB000   * Table of Public Holiday Maintenance
          GOTO      OTPH9999
.
OTPH0500  CALL      NEXTYR00   * Next set of Public Holidays
          GOTO      OTPH9999
.
OTPH0600  CALL      PREVYR00   * Prev set of Public Holidays 
          GOTO      OTPH9999
.
OTPH0700  UNPACK    DIM127,D1,KEY1,DIM127    * Outpatient Site
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,OTPH0701,OTPH0702
.
          WRITE     HTMLFILE;OPHHOSP;
          GOTO      OTPH9999
.
OTPH0701  PACK      KEY3,OPHHOSP,SP70
          CALL      RDPTHSP1
          IF        OVRCD=1
            MOVE      OPHHOSP,PTHSNAME
          ENDIF
          WRITE     HTMLFILE;PTHSNAME;
          GOTO      OTPH9999
.
OTPH0702  CALL      LHSL0000            
          GOTO      OTPH9999
.
OTPH9999  RETURN
.
.------------------------------------------------------------
. Leave Hospital Selection List
.------------------------------------------------------------
LHSL0000  PACK      KEY3,SP70
          CALL      RSPTHSP1
LHSL1000  CALL      RKPTHSP1
          BRANCH    OVRCD,LHSL9999
.
          MOVE      PTHSNAME,KEY50
          PACK      KEY5,QUOTE,PTHSHOSP,QUOTE
          MATCH     OPHHOSP,PTHSHOSP
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=",KEY5," selected>":
                               KEY50,"</option>"
          ELSE
            IF        IBCNMHOS=1
              PACK      KEY13,USERID,SP70                * All hospital access
              CALL      RDMLSEC1
              IF        OVRCD=1
                PACK      KEY13,USERID,PTHSHOSP,SP70     * This hospital access
                CALL      RDMLSEC1
                BRANCH    OVRCD,LHSL1000
              ENDIF
            ENDIF
            WRITE     HTMLFILE;"<option value=",KEY5,">",KEY50,"</option>"
          ENDIF
          GOTO      LHSL1000
.
LHSL9999  RETURN
.
.-----------------------------------------------------------
. Sets Year to current or to next or prev
.-----------------------------------------------------------
SETYR000  RESET     OTPHO001
          MATCH     SP70,OTPHO001   
          GOTO      SETYR100 IF NOT EQUAL
.
          CALL      IBACLOCK
          PACK      OTPHO001,CCC,CYY 
          REP       " 0",OTPHO001
          WRITE     HTMLFILE;OTPHO001; 
          GOTO      SETYR999 
.
SETYR100  WRITE     HTMLFILE;OTPHO001;  * Year set to Next or Previous Year
.
SETYR999  RETURN
.-----------------------------------------------------------
. Link to Next Group
.-----------------------------------------------------------
NEXTGR00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
.
          WRITE     HTMLFILE;"outweb04.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2;
.
NEXTGR99  RETURN
.-------------------------------------------------------------
. Link to Prev Group
.-------------------------------------------------------------
PREVGR00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3
          IF        F3<0
            MOVE      ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
.
          WRITE     HTMLFILE;"outweb04.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2;
.
PREVGR99  RETURN
.-----------------------------------------------------------
. Link to Next Years Public Holidays 
.-----------------------------------------------------------
NEXTYR00  UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
          MOVE      PBYEAR,PBYR
          ASSIGN    PBYR+ONE,F4
          MOVE      F4,KEY4
.
          WRITE     HTMLFILE;"outweb04.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&otpho001=",KEY4;
.
NEXTYR99  RETURN
.-------------------------------------------------------------
. Link to Prev Years Public Holidays 
.-------------------------------------------------------------
PREVYR00  UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
          MOVE      PBYEAR,PBYR
          ASSIGN    PBYR-ONE,F4
          MOVE      F4,KEY4
.
          WRITE     HTMLFILE;"outweb04.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&otpho001=",KEY4;
.
PREVYR99  RETURN
.------------------------------------------------------------
. Table of Outpatient Site Code
.------------------------------------------------------------
OTTAB000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          CALL      OTHED000      * Output Table Heading
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY6,STARTKEY,SP70
          CALL      RDSSITA1
          IF        OVRCD=0
            CALL      RDPSITA1
          ENDIF
OTTAB100  CALL      RDKSITA1
          BRANCH    OVRCD,OTTAB900
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      OTTAB100
          ENDIF
.
          CALL      OTROW000      * Display Site Code Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      OTTAB100 IF NOT EQUAL
          GOTO      OTTAB999
.
OTTAB900  CALL      NOMORE00      * Display No More Records Message
.
OTTAB999  RETURN
.------------------------------------------------------------
. Outpatient Site Code (TABLE HEADING)
.------------------------------------------------------------
OTHED000  WRITE     HTMLFILE;"<tr>":
                               "<td class=HeadingCell width=20%>":
                               "<b>Code</b></td>":
                               "<td class=HeadingCell width=40%>":
                               "<b>Description</b></td>":
                               "<td class=HeadingCell width=20%>":
                               "<b>File Prefix</b></td>":
                             "</tr>"
.
OTHED999  RETURN
.------------------------------------------------------------
. Site Code Code (TABLE ROW)
.------------------------------------------------------------
OTROW000  MATCH     SP70,OSTDESC 
          IF        @EQUAL 
             MOVE     "&nbsp;",OSTDESC
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:ShowDetail01":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&otsit001=",OSTSITE,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif":
                             " class=ListIcon></a>":
                             OSTSITE,"</td>":
                             "<td>",OSTDESC,"</td><td>",OSTFILE,"</td>":
                             "</tr>"
.
OTROW999 RETURN
.------------------------------------------------------------
. Table of Public Holidays 
.------------------------------------------------------------
HLTAB000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          CALL      HLHED000      * Output Table Heading
.
          RESET     OTPHO001 
          MATCH     SP70,OTPHO001
          IF        @EQUAL  
            CALL      IBACLOCK  
            PACK      OTPHO001,CCC,CYY   * Set to current year
            REP       " 0",OTPHO001
          ENDIF
          MOVE      OTPHO001,PBYEAR      * Variable set to Prev & Next Years 
.
          PACK      KEY11,SP70
          CALL      RDSPHOA1
HLTAB100  CALL      RDKPHOA1
          BRANCH    OVRCD,HLTAB999
.
          UNPACK    OPHDATE,KEY4,KEY5
          MATCH     OTPHO001,KEY4
          GOTO      HLTAB100 IF NOT EQUAL
.
          CALL      HLROW000      * Display Public Holidays Row
          GOTO      HLTAB100 
.
HLTAB999  RETURN
.------------------------------------------------------------
. Public Holidays (TABLE HEADING)
.------------------------------------------------------------
HLHED000  WRITE     HTMLFILE;"<tr>":
                               "<td class=HeadingCell width=20%>":
                               "<b>Date</b></td>":
                               "<td class=HeadingCell width=40%>":
                               "<b>Public Holiday</b></td>";
.
          IF        IBCNMHOS=1
            WRITE     HTMLFILE;"<td class=HeadingCell width=20%>":
                               "<b>Hospital</b></td>";
          ENDIF
.
          WRITE     HTMLFILE;"</tr>"
.
HLHED999  RETURN
.------------------------------------------------------------
. Public Holidays (TABLE ROW)
.------------------------------------------------------------
HLROW000  UNPACK    OPHDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:ShowDetail02":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&otpho002=",OPHDATE:
                             "&otpho005=",OPHHOSP,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif ":
                             "class=ListIcon></a>":
                             DATE,"</td>":
                             "<td>",OPHDESC,"</td>";
.
          IF        IBCNMHOS=1
            PACK      KEY3,OPHHOSP,SP70
            CALL      RDPTHSP1
            IF        OVRCD=1
              MOVE      SP70,PTHSNAME
            ENDIF
            WRITE     HTMLFILE;"<td>",PTHSNAME,"&nbsp;</td>";
          ENDIF
.
          WRITE     HTMLFILE;"</tr>"
.
HLROW999  RETURN
.
.---------------------------------------------------------------------------
. Outpatient Comments Maintenance: Display,Add,Update,Delete  (hps)
.---------------------------------------------------------------------------
OUTCMN00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      OUTCMN40 IF EQUAL
.
          MATCH     "A",UPDTTYPE            * Add formaction
          GOTO      OUTCMN10 IF EQUAL
.
          MATCH     "U",UPDTTYPE            * Update Formaction
          GOTO      OUTCMN20 IF EQUAL
.
          MATCH     "D",UPDTTYPE            * Delete Formaction
          GOTO      OUTCMN30 IF EQUAL
.
          GOTO      OUTCMN93                   
.
OUTCMN10  CALL      CMNCNT00                * Generate Unique Comment No
          CALL      CMNSAV00                * Assign Values To File Variables
.
          PACK      KEY19,OTCMTDAT,OTCMFDAT,OTCMCONO,SP70
          CALL      RDPCMNA1
          COMPARE   ZERO,OVRCD
          GOTO      OUTCMN92 IF EQUAL
.
          CALL      WRPCMNA1                * Write Call
.
          MOVE      ZERO,EXIT
          GOTO      OUTCMN99
.
OUTCMN20  PACK      KEY19,OTCMN001,OTCMN002,UNQCOUNT,SP70
          CALL      RDPCMNA1
          BRANCH    OVRCD,OUTCMN94
.
          CALL      CMNSAV00                 * Assign Values To File Variables
.
          CALL      UPPCMNA1                 * Update Call
.
          MOVE      ZERO,EXIT
          GOTO      OUTCMN99
.
OUTCMN30  PACK      KEY19,OTCMN001,OTCMN002,UNQCOUNT,SP70
          CALL      RDPCMNA1 
          BRANCH    OVRCD,OUTCMN94
.
          CALL      DEPCMNA1                 * Delete Call
.
          MOVE      ZERO,EXIT
          GOTO      OUTCMN99
.
OUTCMN40  PACK      KEY19,UNQCOUNT,SP70
          CALL      RDPCMNA1 
          IF        OVRCD=1
            CALL      CLRCMN00              * Clear Variables
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Outpatient System Wide Comments",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,OUTCMN99
          CALL      WEBBOD00                * Process Web Body
          CALL      WEBEND00                * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      OUTCMN99
.
OUTCMN92  MOVE      "System Comments already Exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      OUTCMN99
.
OUTCMN93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      OUTCMN99
.
OUTCMN94  MOVE      "Invalid Record.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      OUTCMN99
.
OUTCMN99  RETURN
.
.--------------------------------------------------------------------
.              Capturing System Date and UserID
.--------------------------------------------------------------------
CMNDAT00  CALL     IBACLOCK                 * Calling Standard Routine
          PACK     OTCMCDAT,CCC,CYY,CMM,CDD
          REP      " 0",OTCMCDAT
          MOVE     CTIMEIS,OTCMCTIM
          REP      " 0",OTCMCTIM
          MOVE     USERID,OTCMCUID
.
CMNDAT99  RETURN
.
.-------------------------------------------------------------------------
.             OutPatient System Wide Comments- Field Item
.-------------------------------------------------------------------------
.
OTCM0000  BRANCH    FLDITMNO,OTCM0100,OTCM0200,OTCM0300:
                             OTCM0400,OTCM0500,OTCM0600:
                             OTCM0700,OTCM0800,OTCM0900:
                             OTCM1000,OTCM1100,OTCM1200:
                             OTCM1300,OTCM1400,OTCM1500:
                             OTCM1600,OTCM1700,OTCM1800
          GOTO      OTCM9999
.
OTCM0100  UNPACK    OTCMTDAT,CCENT,CYEAR,CMON,CDAY  
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      KEY11,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          WRITE     HTMLFILE;KEY11;         * To Date
          GOTO      OTCM9999
.
OTCM0200  UNPACK    OTCMFDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      KEY11,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          WRITE     HTMLFILE;KEY11;         * From Date
          GOTO      OTCM9999
.
OTCM0300  WRITE     HTMLFILE;OTCMCOM1;      * Comment One
          GOTO      OTCM9999
.
OTCM0400  WRITE     HTMLFILE;OTCMCOM2;      * Comment Two
          GOTO      OTCM9999
.
OTCM0500  WRITE     HTMLFILE;OTCMCOM3;      * Comment Three
          GOTO      OTCM9999
.
OTCM0600  WRITE     HTMLFILE;OTCMCOM4;      * Comment Four
          GOTO      OTCM9999
.
OTCM0700  WRITE     HTMLFILE;OTCMSITE;      * Site Code
          GOTO      OTCM9999
.
OTCM0800  WRITE     HTMLFILE;OTCMCLIN;      * Clinic Type
          GOTO      OTCM9999
.
OTCM0900  WRITE     HTMLFILE;OTCMCONO;      * Unique ID
          GOTO      OTCM9999
.
OTCM1000  WRITE     HTMLFILE;OTCMCUID;      * UserID
          GOTO      OTCM9999
.
OTCM1100  WRITE     HTMLFILE;OTCMCDAT;      * Current Date
          GOTO      OTCM9999
.
OTCM1200  WRITE     HTMLFILE;OTCMCTIM;      * Current Time
          GOTO      OTCM9999
.
OTCM1300  CALL      CMTAB000                * Comment Table
          GOTO      OTCM9999
.
OTCM1400  
          GOTO      OTCM9999
.
OTCM1500  CALL      SITS0000                * Site code selection list
          GOTO      OTCM9999
.
OTCM1600  CALL      CMYR0000                * Comments Year
          GOTO      OTCM9999  
.
OTCM1700  CALL      NXYR0000                * Next Year
          GOTO      OTCM9999
.
OTCM1800  CALL      PVYR0000                * Previous Year
          GOTO      OTCM9999
.
OTCM9999  RETURN
.
.------------------------------------------------------------------------
. Site code selection list for the current users file prefix
.------------------------------------------------------------------------
SITS0000  PACK      KEY6,SP70
          CALL      RDSSITA1
SITS1000  CALL      RDKSITA1
          BRANCH    OVRCD,SITS9999
.
          MATCH     SP70,OSTSITE
          GOTO      SITS1000 IF EQUAL
.
          MATCH     CFILEPRE,OSTFILE
          GOTO      SITS1000 IF NOT EQUAL
.
          PACK      KEY10,QUOTE,OSTSITE,QUOTE
          MATCH     OTCMSITE,OSTSITE
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=",KEY10," selected>":
                               OSTDESC,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=",KEY10,">",OSTDESC,"</option>"
          ENDIF
          GOTO      SITS1000
.
SITS9999  RETURN
.------------------------------------------------------------------------
. Sets Year to current or to next or prev
.------------------------------------------------------------------------
.
CMYR0000  RESET     CMNTYEAR
          MOVE      CMNTYEAR,OTCMN001
          MATCH     SP70,CMNTYEAR
          GOTO      CMYR1000 IF NOT EQUAL
.
          CALL      IBACLOCK
          PACK      OTCMN001,CCC,CYY
          REP       " 0",OTCMN001
          WRITE     HTMLFILE;OTCMN001;
          GOTO      SETYR999
.
CMYR1000  WRITE     HTMLFILE;OTCMN001;  * Year set to Next or Previous Year
.
CMYR9999  RETURN
.-------------------------------------------------------------------------
. Link to Next Year Comments
.-------------------------------------------------------------------------
NXYR0000  UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
          MOVE      CMNTYEAR,CMNYEAR
          ASSIGN    CMNYEAR+ONE,F4
          MOVE      F4,KEY4
.
          WRITE     HTMLFILE;"outweb04.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&cmntyear=",KEY4;
.
NXYR9999  RETURN
.--------------------------------------------------------------------------
. Link to Prev Years Comments
.--------------------------------------------------------------------------
PVYR0000  UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
          MOVE      CMNTYEAR,CMNYEAR
          ASSIGN    CMNYEAR-ONE,F4
          MOVE      F4,KEY4
.
          WRITE     HTMLFILE;"outweb04.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&cmntyear=",KEY4;
.
PVYR9999  RETURN
.------------------------------------------------------------------------
. Clear File Variables of Comment File
.------------------------------------------------------------------------
CLRCMN00  MOVE      SP70,OTCMTDAT     * Comment Valid to Date (CCYYMMDD)
          MOVE      SP70,OTCMFDAT     * Comment Valid from Date (CCYYMMDD)
          MOVE      SP70,OTCMCONO     * Comment No. (Generated Unique Count)
          MOVE      SP70,OTCMCOM1     * Comment Line 1
          MOVE      SP70,OTCMCOM2     * Comment Line 2
          MOVE      SP70,OTCMCOM3     * Comment Line 3
          MOVE      SP70,OTCMCOM4     * Comment Line 4
          MOVE      SP70,OTCMSITE     * Site Code (Blank for All Sites)
          MOVE      SP70,OTCMCLIN     * Clinic Type (Blank for All Clinics)
          MOVE      SP70,OTCMCDAT     * Entered Date (System Generated)
          MOVE      SP70,OTCMCTIM     * Entered Time (System Generated)
          MOVE      SP70,OTCMCUID     * Entered By (System Gener. WEB User ID)
          MOVE      SP70,OTCMSPAR     * Spare Variable
.
CLRCMN99  RETURN
.
.--------------------------------------------------------------------------
.   Comments Table
.--------------------------------------------------------------------------
.
CMTAB000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
          CALL      CMHED000                * Table Header
          RESET     CMNTYEAR
          CALL      CMNCNT00                * Getting Count
.         MOVE      ZERO,RECNUMB            * Counter Initailized
          MATCH     SP70,CMNTYEAR           
          IF        @EQUAL
            CALL      IBACLOCK
            PACK      CMNTYEAR,CCC,CYY      * Set to current year
            REP       " 0",CMNTYEAR
          ENDIF  
          MOVE      CMNTYEAR,CMNYR        
          PACK      KEY19,CMNYR,SP70        * Create Key
          CALL      RSPCMNA1                * Positional Read
          MOVE      ZERO,OVRCD
CMTAB200  CALL      RKPCMNA1                * Sequential Read
          BRANCH    OVRCD,CMTAB999 
          UNPACK    OTCMTDAT,KEY4,KEY5
          MATCH     CMNTYEAR,KEY4
          GOTO      CMTAB999 IF NOT EQUAL
.           ADD       ONE,RECNUMB
.         IF        RECNUMB>=CMNCOUNT
.           GOTO      CMTAB999
.          ENDIF           
          CALL      CMTDAT00                * Formating To Date
          CALL      CMFDAT00                * Formating From Date
          CALL      CMDDAT00                * Date Formating
          PACK      INPUT,ENDATE,SP3,BYDIM,SP3,OTCMCUID
          CALL      CMROW000                * Table Row
          GOTO      CMTAB200
.
CMTAB999  RETURN
.
.----------------------------------------------------------------------------
.                           Date Formating(dd/mon/yyyy)
.----------------------------------------------------------------------------
CMDDAT00  UNPACK    OTCMCDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      ENDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
CMDDAT99  RETURN
.
CMTDAT00  UNPACK    OTCMTDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      TODATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
CMTDAT99  RETURN
.
CMFDAT00  UNPACK    OTCMFDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      FROMDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
CMFDAT99  RETURN
.
.----------------------------------------------------------------------------
.                         Table Head
.----------------------------------------------------------------------------
.
CMHED000  WRITE     HTMLFILE;"<tr>":
                               "<td class=HeadingCell width=20%>":
                               "<b>From Date</b></td>":
                               "<td class=HeadingCell width=20%>":
                               "<b>To Date</b></td>":
                               "<td class=HeadingCell width=40%>":
                               "<b>Comment</b></td>":
                               "<td class=HeadingCell width=20%>":
                               "<b>Input</b></td>": 
                             "</tr>"
.
CMHED999  RETURN
.
.----------------------------------------------------------------------------
.              System Wide Comments (TABLE ROW)
.----------------------------------------------------------------------------
.
CMROW000  PACK      UNQCOUNT,OTCMTDAT,OTCMFDAT,OTCMCONO
          WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:ShowDetail01":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&unqcount=",UNQCOUNT,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif":
                             " class=ListIcon></a>":
                             FROMDATE,"</td>":
                             "<td>",TODATE,"</td>":
                             "<td>",OTCMCOM1,SP1,OTCMCOM2,SP1,OTCMCOM3:
                             SP1,OTCMCOM4,"</td>":
                             "<td>",INPUT,"</td>":
                             "</tr>"
.
CMROW999 RETURN
.
.-----------------------------------------------------------------------------
.  Set Parameters for Option 3: Outpatient System Wide Comments
.-----------------------------------------------------------------------------
.
SYCMN000  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,SYCMN001,SYCMN002,SYCMN003,SYCMN004,SYCMN005:
                       SYCMN006,SYCMN007,SYCMN008
.
          MOVE      ONE,EXIT
          GOTO      STCOD999
.
SYCMN001  UNPACK    QRYDATA,CDAY,D1,MTHNAM3,D1,CCENT,CYEAR
          CALL      SETMTH00
          PACK      OTCMN001,CCENT,CYEAR,CMON,CDAY 
          REP       " 0",OTCMN001
          GOTO      SYCMN999
.         
SYCMN002  UNPACK    QRYDATA,CDAY,D1,MTHNAM3,D1,CCENT,CYEAR
          CALL      SETMTH00
          PACK      OTCMN002,CCENT,CYEAR,CMON,CDAY 
          REP       " 0",OTCMN002
          GOTO      SYCMN999
.
SYCMN003  MOVE      QRYDATA,OTCMN003
          GOTO      SYCMN999
.
SYCMN004  MOVE      QRYDATA,OTCMN004
          GOTO      SYCMN999
.
SYCMN005  MOVE      QRYDATA,OTCMN005
          GOTO      SYCMN999
.
SYCMN006  MOVE      QRYDATA,OTCMN006
          GOTO      SYCMN999
.
SYCMN007  MOVE      QRYDATA,OTCMN007
          GOTO      SYCMN999
.
SYCMN008  MOVE      QRYDATA,OTCMN008
          GOTO      SYCMN999
.
SYCMN999  RETURN
.
.----------------------------------------------------------------------
.
CMNSAV00  MOVE      OTCMN001,OTCMTDAT       * To Date
          MOVE      OTCMN002,OTCMFDAT       * From Date
          MOVE      OTCMN003,OTCMCOM1       * Comment One
          MOVE      OTCMN004,OTCMCOM2       * Comment Two
          MOVE      OTCMN005,OTCMCOM3       * Comment Three
          MOVE      OTCMN006,OTCMCOM4       * Comment Four
          MOVE      OTCMN007,OTCMSITE       * Site Code
          MOVE      OTCMN008,OTCMCLIN       * Clinic Code
          MOVE      OTCMN009,OTCMCDAT       * Current Date
          MOVE      OTCMN010,OTCMCTIM       * Current Time
          MOVE      OTCMN011,OTCMCUID       * UserID
          CALL      CMNDAT00                * Capture System Date and UserID
.
CMNSAV99  RETURN        
.
.----------------------------------------------------------------------
.                     Generating The Unique Count
.----------------------------------------------------------------------
.
CMNCNT00  PACK      KEY19,SP70
          MOVE      ZERO,CURDUM5
          MOVE      ZERO,CURDUM4
          CALL      RSPCMNA1
CMNCNT10  CALL      RKPCMNA1
          BRANCH    OVRCD,CMNCNT90
          MOVE      ONE,CURDUM4
          MOVE      OTCMCONO,CURDUM6
          IF        CURDUM6>CURDUM5
            MOVE      CURDUM6,CURDUM5
          ENDIF
          GOTO      CMNCNT10
.
CMNCNT20  ADD       ONE,CURDUM5
          MOVE      CURDUM5,OTCMCONO  
          GOTO      CMNCNT99
.
CMNCNT90  COMPARE   ONE,CURDUM4
          GOTO      CMNCNT20 IF EQUAL
          MOVE      "001",OTCMCONO
.
CMNCNT99  RETURN
.------------------------------------------------------------
. Outpatient Letters Details
.------------------------------------------------------------
OUTLET00  CALL      CLOUTLET
          MATCH     SP70,UPDTTYPE      * Display
          GOTO      OUTLET50 IF EQUAL
.
          MATCH     "A",UPDTTYPE       * Add New
          GOTO      OUTLET10 IF EQUAL

          MATCH     "U",UPDTTYPE       * Update
          GOTO      OUTLET20 IF EQUAL

          MATCH     "D",UPDTTYPE       * Delete
          GOTO      OUTLET30 IF EQUAL
.
          MOVE      "Invalid Update Type",WEBTITLE
          CALL      WEBERR00
          GOTO      OUTLET99
.
OUTLET10  CALL      ADDLET00
          GOTO      OUTLET99
.
OUTLET20  CALL      UPDLET00
          GOTO      OUTLET99
.
OUTLET30  CALL      DELLET00
          GOTO      OUTLET99
.
OUTLET50  SQUEEZE   OTLET001
          MOVE      OTLET001,F3
          MOVE      F3,KEY3
          MOVE      ZERO,F3
          PACK      KEY6,KEY3,F3
          CALL      RDOTLET1
          IF        OVRCD=1
            CALL      CLOUTLET
          ENDIF
          MOVE      "Outpatient Letters",WEBTITLE
          CALL      WEBHED00
          BRANCH    EXIT,OUTLET99
          CALL      WEBBOD00
          CALL      WEBEND00
.
OUTLET99  RETURN
.-------------------------------------------------------------------------------
.  Set Parameters
.-------------------------------------------------------------------------------
STLET000  UNPACK    QRYNAME,KEY5,KEY3
          MOVE      ZERO,F3
          MOVE      KEY3,F3
          STORE     QRYDATA,F3,OTLET001,OTLET002,OTLET003,OTLET004,OTLET005:
                               OTLET006,OTLET007,OTLET008,OTLET009,OTLET010:
                               OTLET011
          RETURN
.------------------------------------------------------------------------------
. Clear letter file
.------------------------------------------------------------------------------
CLOUTLET  MOVE      ZERO,OTLTLTNO
          MOVE      ZERO,OTLTLINO
          MOVE      SP70,OTLTTEXT
          MOVE      ZERO,OTLTLVAR
          MOVE      ZERO,OTLTPBOT
          MOVE      ZERO,OTLTPTOP
          MOVE      ZERO,OTLTPPAG
          MOVE      ZERO,OTLTPTAB
          MOVE      SP70,OTLTMMFN
          MOVE      SP70,OTLTTYPE
          MOVE      SP70,OTLTACTV
          MOVE      SP70,OTLTCOMM
          MOVE      SP70,OTLTCONF
          MOVE      SP70,OTLTSPAR
          RETURN
.------------------------------------------------------------
. Add Letter
.------------------------------------------------------------
ADDLET00  CALL      CLOUTLET
          SQUEEZE   OTLET001
          MOVE      OTLET001,OTLTLTNO
          MOVE      ZERO,OTLTLINO
          PACK      KEY6,OTLTLTNO,OTLTLINO,SP70
          CALL      RDOTLET1
          IF        OVRCD=1
            CALL      MVLET000
            CALL      WROTLET1
            CALL      LETDET00  * Update Letter Details
            CALL      WINCLS00
          ELSE
            MOVE      "Letter Already on File",WEBTITLE
            CALL      WEBERR00
          ENDIF
          RETURN
.------------------------------------------------------------
. Update Extract
.------------------------------------------------------------
UPDLET00  CALL      CLOUTLET
          SQUEEZE   OTLET001
          MOVE      OTLET001,OTLTLTNO
          MOVE      ZERO,OTLTLINO
          PACK      KEY6,OTLTLTNO,OTLTLINO,SP70
          CALL      RDOTLET1
          IF        OVRCD=0
            CALL      MVLET000
            CALL      UPOTLET1
            CALL      LETDET00  * Update Letter Details
            CALL      WINCLS00
          ELSE
            MOVE      "Letter Not On File",WEBTITLE
            CALL      WEBERR00
          ENDIF
          RETURN
.------------------------------------------------------------
. Delete Extract
.------------------------------------------------------------
DELLET00  SQUEEZE   OTLET001
          MOVE      OTLET001,OTLTLTNO
          MOVE      ZERO,OTLTLINO
          PACK      KEY6,OTLTLTNO,OTLTLINO,SP70
          CALL      RDOTLET1
          IF        OVRCD=0
            CALL      DEOTLET1
            CALL      LETDET00
            CALL      WINCLS00
          ELSE
            MOVE      "Letter Not On File",WEBTITLE
            CALL      WEBERR00
          ENDIF
          RETURN
.------------------------------------------------------------
. Move in Letter Details
.------------------------------------------------------------
MVLET000  MATCH     Z70,OTLET001
          IF        !@EQUAL
            SQUEEZE    OTLET001
            MOVE       OTLET001,OTLTLTNO
          ENDIF
          MATCH     Z70,OTLET002
          IF        !@EQUAL
            MOVE       OTLET002,OTLTTEXT
          ENDIF
          MATCH     Z70,OTLET003
          IF        !@EQUAL
            SQUEEZE    OTLET003
            MOVE       OTLET003,OTLTPBOT
          ENDIF
          MATCH     Z70,OTLET004
          IF        !@EQUAL
            SQUEEZE    OTLET004
            MOVE       OTLET004,OTLTPTOP
          ENDIF
          MATCH     Z70,OTLET005
          IF        !@EQUAL
            SQUEEZE    OTLET005
            MOVE       OTLET005,OTLTPPAG
          ENDIF
          MATCH     Z70,OTLET006
          IF        !@EQUAL
            SQUEEZE    OTLET006
            MOVE       OTLET006,OTLTPTAB
          ENDIF
          MATCH     Z70,OTLET007
          IF        !@EQUAL
            MOVE       OTLET007,OTLTMMFN
          ENDIF
          MATCH     Z70,OTLET008
          IF        !@EQUAL
            MOVE       OTLET008,OTLTTYPE
          ENDIF
          MATCH     Z70,OTLET009
          IF        !@EQUAL
            MOVE       OTLET009,OTLTACTV
          ENDIF
          MATCH     Z70,OTLET010
          IF        !@EQUAL
            MOVE       OTLET010,OTLTCOMM
          ENDIF
          MATCH     Z70,OTLET011
          IF        !@EQUAL
            MOVE       OTLET011,OTLTCONF
          ENDIF
.
MVLET999  RETURN
.
.------------------------------------------------------------
. LETDET000                  
.------------------------------------------------------------
LETDET00  SQUEEZE   OTLET001
          MOVE      OTLET001,OTLTLTNO
          MOVE      OTLET001,LETTNUMB
          MOVE      ZERO,OTLTLINO
          PACK      KEY6,OTLTLTNO,OTLTLINO,SP70
          CALL      RDOTLET1
LETDET10  CALL      RKOTLET1
          BRANCH    OVRCD,LETDET20
          COMPARE   OTLTLTNO,LETTNUMB
          GOTO      LETDET20 IF NOT EQUAL
          PACK      KEY6,OTLTLTNO,OTLTLINO,SP70
          CALL      DEOTLET1
          CALL      RSOTLET1
          GOTO      LETDET10
.
LETDET20  MATCH     "D",UPDTTYPE
          GOTO      LETDET99 IF EQUAL
          COMPARE   ZERO,OTLEEND
          GOTO      LETDET99 IF EQUAL
          CALL      CLOUTLET
          MOVE      ZERO,F3
LETDET30  ADD       ONE,F3
          MOVE      LETTNUMB,OTLTLTNO
          MOVE      F3,OTLTLINO
          PACK      KEY6,OTLTLTNO,OTLTLINO,SP70
          CALL      RDOTLET1
          IF        OVRCD=0
            MOVE      ZERO,OTLTLVAR
            SCAN      SPECCHAR,OTLECOMM[F3]
            IF        @EQUAL
              MOVE      ONE,OTLTLVAR
            ENDIF
            RESET     OTLECOMM[F3]
            MOVE      OTLECOMM[F3],OTLTTEXT
            CALL      UPOTLET1
          ELSE
            MOVE      ZERO,OTLTLVAR
            SCAN      SPECCHAR,OTLECOMM[F3]
            IF        @EQUAL
              MOVE      ONE,OTLTLVAR
            ENDIF
            RESET     OTLECOMM[F3]
            MOVE      OTLECOMM[F3],OTLTTEXT
            CALL      WROTLET1
          ENDIF
          COMPARE   F3,OTLEEND
          GOTO      LETDET30 IF NOT EQUAL
LETDET99  RETURN
.------------------------------------------------------------
. Get Text
.------------------------------------------------------------
GLTEXT00  ADD       ONE,OTLEEND
          MOVE      QRYDATA,OTLECOMM[OTLEEND]
          RESET     OTLECOMM[OTLEEND]
.
GLTEXT10  READ      CONFFILE,SEQ;QRYNAME,QRYDATA * Read CONFFILE sequentially
          GOTO      GLTEXT99 IF OVER             * If end of file
          MATCH     SP70,QRYNAME                 * If next CGI parameter
          GOTO      GLTEXT80 IF NOT EQUAL
          ADD       ONE,OTLEEND                    * Increment Array Counter
          PACK      OTLECOMM[OTLEEND],QRYDATA,SP70 * Input Note to Array
.
          COMPARE   "999",OTLEEND                   * Max 999 lines 
          GOTO      GLTEXT99 IF EQUAL
.
          GOTO      GLTEXT10
.
GLTEXT80  MOVE      ONE,TEXTAREA                 * Flag for Next CGI Parameter
GLTEXT99  RETURN
.------------------------------------------------------------
. Letter Variables
.------------------------------------------------------------
LETF0000  BRANCH    FLDITMNO,LETF0100,LETF0200,LETF0300,LETF0400,LETF0500:
                             LETF0600,LETF0700,LETF0800,LETF0900,LETF1000:
                             LETF1100,LETF1200,LETF1300,LETF1400
          WRITE     HTMLFILE;"Invalid Tag";
          GOTO      LETF9999
.
LETF0100  COMPARE   ZERO,OTLTLTNO
          GOTO      LETF0110 IF EQUAL
          WRITE     HTMLFILE;OTLTLTNO;
          GOTO      LETF9999
.
LETF0110  MOVE      ZERO,F3
          PACK      KEY6,F3,Z70
          CALL      RSOTLET2
          CALL      RPOTLET2
          IF        OVRCD=1
            WRITE     HTMLFILE;"  1";
          ELSE
            ADD       ONE,OTLTLTNO
            WRITE     HTMLFILE;OTLTLTNO;
            CALL      CLOUTLET
          ENDIF
          GOTO      LETF9999
.
LETF0200  MATCH     SP70,OTLTTEXT
          GOTO      LETF9999 IF EQUAL
          WRITE     HTMLFILE;OTLTTEXT;
          GOTO      LETF9999
.
LETF0300  WRITE     HTMLFILE;OTLTPBOT;
          GOTO      LETF9999
.
LETF0400  WRITE     HTMLFILE;OTLTPTOP;
          GOTO      LETF9999
.
LETF0500  WRITE     HTMLFILE;OTLTPPAG;
          GOTO      LETF9999
.
LETF0600  WRITE     HTMLFILE;OTLTPTAB;
          GOTO      LETF9999
.
LETF0700  WRITE     HTMLFILE;OTLTMMFN;
          GOTO      LETF9999
.
LETF0800  CALL      LETTAB00
          GOTO      LETF9999
.
LETF0900  SQUEEZE   OTLET001
          MOVE      ZERO,LETTNUMB
          MOVE      OTLET001,LETTNUMB
          COMPARE   ZERO,LETTNUMB
          GOTO      LETF9999 IF EQUAL
          PACK      KEY6,LETTNUMB,SP1,SP1,ZERO
          CALL      RDOTLET1
LETF0910  CALL      RKOTLET1
          BRANCH    OVRCD,LETF0990
          COMPARE   LETTNUMB,OTLTLTNO
          GOTO      LETF0990 IF NOT EQUAL
.
          STRIP     OTLTTEXT
          MOVELPTR  OTLTTEXT,F3
          IF        F3>0
            SFORMAT   VAR,F3
          ELSE
            SFORMAT   VAR,1
          ENDIF
          MOVE      OTLTTEXT,VAR
          WRITE     HTMLFILE;VAR
          SFORMAT   VAR,127
.
          GOTO      LETF0910
LETF0990  PACK      KEY6,LETTNUMB,SP1,SP1,ZERO
          CALL      RDOTLET1
          GOTO      LETF9999
.
LETF1000  WRITE     HTMLFILE;OTLTTYPE;
          GOTO      LETF9999
.
LETF1100  WRITE     HTMLFILE;OTLTACTV;
          GOTO      LETF9999
.
LETF1200  WRITE     HTMLFILE;FILTACTV;
          GOTO      LETF9999
.
LETF1300  WRITE     HTMLFILE;OTLTCOMM;
          GOTO      LETF9999
.
LETF1400  WRITE     HTMLFILE;OTLTCONF;
          GOTO      LETF9999
.
LETF9999  RETURN
.------------------------------------------------------------
. Output Table of Letter Headers Details
.------------------------------------------------------------
LETTAB00  MOVE      ZERO,F3
          PACK      KEY6,F3,SP70
          CALL      RSOTLET2
LETTAB10  CALL      RKOTLET2
          BRANCH    OVRCD,LETTAB99
          COMPARE   ZERO,OTLTLINO
          GOTO      LETTAB99 IF NOT EQUAL
.
          MATCH     "0",FILTACTV            * Active Only
          IF        @EQUAL
            MATCH     "1",OTLTACTV
            GOTO      LETTAB10 IF EQUAL
          ENDIF
.
          MATCH     "1",FILTACTV            * Inactive Only
          IF        @EQUAL
            MATCH     "1",OTLTACTV
            GOTO      LETTAB10 IF NOT EQUAL
          ENDIF
.
          CLEAR     HTMLLINK
          APPEND    "javascript:UpdateLetter('",HTMLLINK
          APPEND    OTLTLTNO,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
          MATCH     "1",OTLTACTV
          IF        @EQUAL
            MOVE    "Inactive",ACTIDESC
          ELSE
            MOVE    "Active",ACTIDESC
          ENDIF
.
          MATCH     "1",OTLTCOMM
          IF        @EQUAL
            MOVE    DYES,SMSSDESC
          ELSE
            MOVE    DNO,SMSSDESC
          ENDIF
.
          MATCH     "1",OTLTCONF
          IF        @EQUAL
            MOVE    DYES,CONFDESC
          ELSE
            MOVE    DNO,CONFDESC
          ENDIF
.
          WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":
                             "#"",OTLTLTNO,"#",":
                             "#"",OTLTTEXT,"#",":
                             "#"",OTLTPBOT,"#",":
                             "#"",OTLTPTOP,"#",":
                             "#"",OTLTPPAG,"#",":
                             "#"",OTLTPTAB,"#",":
                             "#"",OTLTMMFN,"#",":
                             "#"",ACTIDESC,"#",":
                             "#"",SMSSDESC,"#",":
                             "#"",CONFDESC,"#");"
.
          GOTO      LETTAB10
.
LETTAB99  RETURN
.
.-------------------------------------------------------------------------------
.  Set Parameters for VAC cdes file
.-------------------------------------------------------------------------------
STVAC000  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,STVAC001,STVAC002,STVAC003,STVAC004
.
          GOTO      STVAC999
.
STVAC001  SQUEEZE   QRYDATA
          MOVE      QRYDATA,OTVAC001
          PACK      OTVAC001,OTVAC001,SP70
          GOTO      STVAC999
.
STVAC002  PACK      QRYDATA,QRYDATA,SP30
          UNPACK    QRYDATA,KEY8,KEY3 * Check for formatted or unformatted date
          MATCH     SP70,KEY3
          IF        @EQUAL
            MOVE      QRYDATA,OTVAC002 * Do not change date format = (CCYYMMDD)
          ELSE
            MOVE      QRYDATA,KEY11   * Change Date as format is (DD MON CCYY)
            PACK      KEY11,KEY11,SP70
            MATCH     SP70,KEY11
            IF        !@EQUAL
              UNPACK    QRYDATA,CDAY,D1,MTHNAM3,D1,CCENT,CYEAR
              CALL      SETMTH00
              PACK      OTVAC002,CCENT,CYEAR,CMON,CDAY
              REP       " 0",OTVAC002
            ENDIF
          ENDIF
          GOTO      STVAC999
.
STVAC003  MOVE      QRYDATA,OTVAC003
          GOTO      STVAC999
.
STVAC004  MOVE      QRYDATA,OTVAC004
          GOTO      STVAC999
.
STVAC999  RETURN
.-------------------------------------------------------------------
. Output Fields from the outpatient VACS code file
.-------------------------------------------------------------------
OTVA0000  BRANCH    FLDITMNO,OTVA0100,OTVA0200,OTVA0300,OTVA0400:
                             OTVA0500,OTVA0600,OTVA0700
.
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      OTVA9999
.
OTVA0100  MOVE      "CQ",CATEGORY
          MOVE      OTVACODE,CODE
          CALL      CODITM00 
          GOTO      OTVA9999
.
OTVA0200  MATCH     SP70,OTVADATE
          GOTO      OTVA9999 IF EQUAL
          UNPACK    OTVADATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          GOTO      OTVA9999
.
OTVA0300  WRITE     HTMLFILE;OTVAWGHT
          GOTO      OTVA9999
.
OTVA0400  WRITE     HTMLFILE;OTVAAMNT;
          GOTO      OTVA9999
.
OTVA0500  CALL      VATAB000   * Table of VACS Codes
          GOTO      OTVA9999
.
OTVA0600  CALL      NXTGRV00   * Next set of VACS Codes
          GOTO      OTVA9999
.
OTVA0700  CALL      PRVGRV00   * Previous set of VACS Code
          GOTO      OTVA9999
.
OTVA9999  RETURN
.
.------------------------------------------------------------
. Outpatient VAC Maintenance: Display,Add,Update,Delete
.------------------------------------------------------------
OUTVAC00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      OUTVAC40 IF EQUAL
.
          MATCH     "A",UPDTTYPE            * Add formaction
          GOTO      OUTVAC10 IF EQUAL
.
          MATCH     "U",UPDTTYPE            * Update formaction
          GOTO      OUTVAC20 IF EQUAL
.
          MATCH     "D",UPDTTYPE            * Delete formaction
          GOTO      OUTVAC30 IF EQUAL
.
          GOTO      OUTVAC93
.
.         ************ ADD FORMACTION **************
.
OUTVAC10  MATCH     SP70,OTVAC001
          GOTO      OUTVAC94 IF EQUAL
.
          MATCH     SP70,OTVAC002
          GOTO      OUTVAC95 IF EQUAL
.
          PACK      KEY11,OTVAC001,OTVAC002,SP70      * Duplicate VACS Codes ?
          CALL      RAOTVAC1
          COMPARE   ZERO,OVRCD
          GOTO      OUTVAC92 IF EQUAL
.
          CALL      CLROTV00      * Clear all the file variables
          CALL      OTVSAV00      * Moves input variables to file variables
.
          CALL      WROTVAC1      * Writes away the data
          MOVE      ZERO,EXIT
          GOTO      OUTVAC99
.
.         ************ UPDATE FORMACTION ************
.
OUTVAC20  MATCH     SP70,OTVAC001
          GOTO      OUTVAC94 IF EQUAL
.
          MATCH     SP70,OTVAC002
          GOTO      OUTVAC95 IF EQUAL
.
          PACK      KEY11,OTVAC001,OTVAC002,SP70
          CALL      RDOTVAC1
          BRANCH    OVRCD,OUTVAC91
.
          CALL      OTVSAV00      * Moves input variables to file variables
.
          CALL      UPOTVAC1      * Writes away the data
          MOVE      ZERO,EXIT
          GOTO      OUTVAC99
.
.         ************ DELETE FORMACTION **********
.
OUTVAC30  PACK      KEY11,OTVAC001,OTVAC002,SP70
          CALL      RDOTVAC1
          BRANCH    OVRCD,OUTVAC91
.
          CALL      DEOTVAC1      * Deletes the data
          MOVE      ZERO,EXIT
          GOTO      OUTVAC99
.
.         ************ DISPLAY FORMACTION **********
.
OUTVAC40  PACK      KEY11,OTVAC001,OTVAC002,SP70
          CALL      RDOTVAC1
          IF        OVRCD=1
            CALL      CLROTV00    * Clear all the file variables
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "VACS Code Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,OUTVAC99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      OUTVAC99
.
OUTVAC91  MOVE      "VACS Code does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      OUTVAC99
.
OUTVAC92  MOVE      "VACS Code and effective date exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      OUTVAC99
.
OUTVAC93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      OUTVAC99
.
OUTVAC94  MOVE      "VACS code is a required field.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      OUTVAC99
.
OUTVAC95  MOVE      "Effective date is a required field.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      OUTVAC99
.
OUTVAC99  RETURN
.
.------------------------------------------------------------
. Moves cgi variables into file variables
.------------------------------------------------------------
OTVSAV00  MATCH     Z70,OTVAC001
          IF        !@EQUAL
            MOVE      OTVAC001,OTVACODE
          ENDIF
.
          MATCH     Z70,OTVAC002
          IF        !@EQUAL
            MOVE      OTVAC002,OTVADATE
          ENDIF
.
          MATCH     Z70,OTVAC003
          IF        !@EQUAL
            MOVE      OTVAC003,OTVAWGHT
          ENDIF
.
          MATCH     Z70,OTVAC004
          IF        !@EQUAL
            MOVE      OTVAC004,OTVAAMNT
          ELSE
            MOVE      ZERO,OTVAAMNT
          ENDIF
.
          MOVE      SP70,OTVASPAR
.
OTVSAV99  RETURN
.
.------------------------------------------------------------
. Moves cgi variables into file variables
.------------------------------------------------------------
CLROTV00  MOVE      SP70,OTVACODE
          MOVE      SP70,OTVADATE
          MOVE      ZERO,OTVAWGHT
          MOVE      ZERO,OTVAAMNT
          MOVE      SP70,OTVASPAR
.
CLROTV99  RETURN
.
.------------------------------------------------------------
. Table of Outpatient Clinics : Report 2
.------------------------------------------------------------
VATAB000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY11,STARTKEY,SP70
          CALL      RAOTVAC1
          IF        OVRCD=0
            CALL      RPOTVAC1
          ENDIF
VATAB100  CALL      RKOTVAC1
          BRANCH    OVRCD,VATAB900
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      VATAB100
          ENDIF
.
          CALL      VAROW000      * Display Clinic Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      VATAB100 IF NOT EQUAL
          GOTO      VATAB999
.
VATAB900  CALL      NOMORE00      * Display No More Records Message
.
VATAB999  RETURN
.------------------------------------------------------------
. Clinic (TABLE ROW)
.------------------------------------------------------------
VAROW000  UNPACK    OTVADATE,CCENT,CYEAR,CMON,CDAY   * To Date in (DD MON CCYY)
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      KEY12,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          PACK      KEY5,ANSC,ANSQ,OTVACODE
          CALL      RDCODE1
          IF        OVRCD = 1
            MOVE     "&nbsp;",TDESC
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:ShowDetail05":
                          "(#"",LINKPRG,"#.pbl":
                          "?reportno=",LINKREP:
                          "&template=",LINKTEM:
                          "&otvac001=",OTVACODE:
                          "&otvac002=",OTVADATE,"#")'>":
                          "<img src=../images/MaintenanceIcon.gif":
                          " class=ListIcon></a>":
                          OTVACODE,"</td>":
                          "<td>",KEY12,"</td>":
                          "<td>",TDESC,"</td><td align=right>",OTVAWGHT,"</td>":
                          "<td align=right>",OTVAAMNT,"</td>":
                          "</tr>"
.
          GOTO      VAROW999
.
VAROW999 RETURN
.
.-----------------------------------------------------------
. Link to Next Group
.-----------------------------------------------------------
NXTGRV00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
.
          REPLACE   " +",OTVACODE
          REPLACE   " +",OTVADATE
          WRITE     HTMLFILE;"outweb04.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&otvac001=",OTVACODE:
                                 "&otvac002=",OTVADATE;
          REPLACE   "+ ",OTVACODE
          REPLACE   "+ ",OTVADATE
.
NXTGRV99  RETURN
.-------------------------------------------------------------
. Link to Prev Group
.-------------------------------------------------------------
PRVGRV00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3
          IF        F3<0
            MOVE      ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
.
          REPLACE   " +",OTVACODE
          REPLACE   " +",OTVADATE
          WRITE     HTMLFILE;"outweb04.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&otvac001=",OTVACODE:
                                 "&otvac002=",OTVADATE;
          REPLACE   "+ ",OTVACODE
          REPLACE   "+ ",OTVADATE
.
PRVGRV99  RETURN
.
.------------------------------------------------------------
. Suspend all sessions for the users site for this holiday
.------------------------------------------------------------
SUSSES00  PACK      KEY25,OPHDATE,SP70
          CALL      RDSSESA2
SUSSES05  CALL      RDKSESA2
          BRANCH    OVRCD,SUSSES99
.
          MATCH     OSEDATE,OPHDATE                 * Only for this holiday
          GOTO      SUSSES99 IF NOT EQUAL
.
          MATCH     OSESITE,WBSESIT                 * Only for users site
          GOTO      SUSSES05 IF NOT EQUAL
.
          COMPARE   ONE,IBCNMHOS                    * Multi hospital
          GOTO      SESSES08 IF NOT EQUAL
.
          MATCH     SP70,OPHHOSP                    * Hospital Specific
          GOTO      SESSES08 IF EQUAL
.
          PACK      KEY28,OSESITE,OSECLIN,OSEDAY,OSESTRT,OSESSHNO,OSESSDAT,SP70
          CALL      RDOTHED1
          BRANCH    OVRCD,SUSSES05
.
          MATCH     OPHHOSP,OTHEHOSP                * Same hospital 
          GOTO      SUSSES05 IF NOT EQUAL          
.
SESSES08  PACK      SAVKEY25,OSEDATE,OSESTRT,OSESITE,OSECLIN,SP70
          PACK      SESSKEYS,OSESITE,OSECLIN,OSEDATE,OSESTRT,SP70
.
          PACK      KEY24,OSESITE,OSECLIN,OSEDAY,Z70
          CALL      RSOTMSP1
          CALL      RPOTMSP1
          BRANCH    OVRCD,SUSSES10
          MATCH     OSESITE,OTMSSITE
          GOTO      SUSSES10 IF NOT EQUAL
          MATCH     OSECLIN,OTMSCLIN
          GOTO      SUSSES10 IF NOT EQUAL
          MOVE      OSEDAY,ANS
          MATCH     ANS,OTMSDOWK
          GOTO      SUSSES10 IF NOT EQUAL
.
          MOVE      OTMSSUSP,F3
          ADD       ONE,F3
          GOTO      SUSSES20
.
SUSSES10  MOVE      ONE,F3
.
SUSSES20  MOVE      OSESITE,OTMSSITE
          MOVE      OSECLIN,OTMSCLIN
          MOVE      OSEDAY,OTMSDOWK
          MOVE      OSESTRT,OTMSSTIM
          MOVE      F3,OTMSSUSP
          MOVE      OSEDATE,OTMSFDAT
          MOVE      OSEDATE,OTMSTDAT
          MOVE      OSEDATE,OTMSRFDT        * Set Susp Range From Date
          MOVE      OSEDATE,OTMSRTDT        * Set Susp Range To Date
          MOVE      OTPHO004,OTMSREAS
          PACK      OTMSEDAT,CCC,CYY,CMM,CDD
          REP       " 0",OTMSEDAT
          CLOCK     TIME,OTMSETIM
          MOVE      USERID,OTMSEUID
          PACK      KEY24,OTMSSITE,OTMSCLIN,OTMSDOWK,OTMSSUSP,OTMSFDAT
          CALL      RDOTMSP1
          IF        OVRCD=0
            ADD       ONE,F3
            GOTO      SUSSES20
          ENDIF
          CALL      WROTMSP1
.
          CALL      SLTSUS00
.
          MOVE      SAVKEY25,KEY25
          CALL      RDSSESA2             
          GOTO      SUSSES05
.
SUSSES99  RETURN
.--------------------------------------------------------------------
. Change the Status(DOBASTAT) in the Outboaaf table
.--------------------------------------------------------------------
SLTSUS00  PACK      KEY28,SESSKEYS,SP70
          CALL      RDSBOKA1
SLTSUS10  CALL      RDKBOKA1
          BRANCH    OVRCD,SLTSUS99
          PACK      KEY25,OBASITE,OBACLIN,OBADATE,OBASTRT
          MATCH     SESSKEYS,KEY25
          GOTO      SLTSUS99 IF NOT EQUAL   * different sessio
.
          IF        (OBASTAT=1|OBASTAT=0)
            PACK      KEY28,OBASITE,OBACLIN,OBADATE,OBASTRT,OBASLOT
            CALL      RLOTBOA1
            MOVE      "6",OBASTAT
            CALL      UPBOKA1
            CALL      UUOTBOA1
            GOTO      SLTSUS10
          ENDIF
.
          COMPARE   FOUR,OBASTAT
          GOTO      SLTSUS10 IF EQUAL
.
          COMPARE   FIVE,OBASTAT
          GOTO      SLTSUS10 IF EQUAL
.
          GOTO      SLTSUS10
.
SLTSUS99  RETURN
.
.------------------------------------------------------------
. Outpatient Slot Type Default Charges -  Display,Add,Update,Delete
.------------------------------------------------------------
OUTDEF00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      OUTDEF40 IF EQUAL
.
          MATCH     "A",UPDTTYPE            * Add formaction
          GOTO      OUTDEF10 IF EQUAL
.
          MATCH     "U",UPDTTYPE            * Update formaction
          GOTO      OUTDEF20 IF EQUAL
.
          MATCH     "D",UPDTTYPE            * Delete formaction
          GOTO      OUTDEF30 IF EQUAL
.
          GOTO      OUTDEF93
.
.         ************ ADD FORMACTION **************
.
OUTDEF10  MATCH     SP70,OTDCH001
          GOTO      OUTDEF94 IF EQUAL
.
          MATCH     SP70,OTDCH002
          GOTO      OUTDEF94 IF EQUAL
.
          PACK      KEY4,OTDCH001,OTDCH002,SP70      * Duplicate Charge Codes ?
          CALL      RAOTDCH1
          COMPARE   ZERO,OVRCD
          GOTO      OUTDEF92 IF EQUAL
.
          CALL      CLOTDC00      * Clear all the file variables
          CALL      OTDSAV00      * Moves input variables to file variables
.
          PACK      OTDCDCRT,CCC,CYY,CMM,CDD
          REP       " 0",OTDCDCRT
          CLOCK     TIME,OTDCTCRT
          MOVE      WBSEUID,OTDCWCRT
.
          CALL      WROTDCH1      * Writes away the data
          MOVE      ZERO,EXIT
          GOTO      OUTDEF99
.
.         ************ UPDATE FORMACTION ************
.
OUTDEF20  MATCH     SP70,OTDCH001
          GOTO      OUTDEF94 IF EQUAL
.
          MATCH     SP70,OTDCH002
          GOTO      OUTDEF94 IF EQUAL
.
          PACK      KEY4,OTDCH001,OTDCH002,SP70
          CALL      RDOTDCH1
          BRANCH    OVRCD,OUTDEF91
.
          CALL      OTDSAV00      * Moves input variables to file variables
.
          PACK      OTDCDUPD,CCC,CYY,CMM,CDD
          REP       " 0",OTDCDUPD
          CLOCK     TIME,OTDCTUPD
          MOVE      WBSEUID,OTDCWUPD
.
          CALL      UPOTDCH1      * Writes away the data
          MOVE      ZERO,EXIT
          GOTO      OUTDEF99
.
.         ************ DELETE FORMACTION **********
.
OUTDEF30  PACK      KEY4,OTDCH001,OTDCH002,SP70
          CALL      RDOTDCH1
          BRANCH    OVRCD,OUTDEF91
.
          CALL      DEOTDCH1      * Deletes the data
          MOVE      ZERO,EXIT
          GOTO      OUTDEF99
.
.         ************ DISPLAY FORMACTION **********
.
OUTDEF40  PACK      KEY4,OTDCH001,OTDCH002,SP70
          CALL      RDOTDCH1
          IF        OVRCD=1
            CALL      CLOTDC00    * Clear all the file variables
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Slot Type Default Charges Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,OUTDEF99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      OUTDEF99
.
OUTDEF91  MOVE      "Default Charge does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      OUTDEF99
.
OUTDEF92  CLEAR     WEBTITLE
          APPEND    "Default Charge exists for this slot ",WEBTITLE
          APPEND    "type/registrar",WEBTITLE
          RESET     WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      OUTDEF99
.
OUTDEF93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      OUTDEF99
.
OUTDEF94  MOVE      "Slot Type/Registrar are mandatory fields.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      OUTDEF99
.
OUTDEF99  RETURN
.
.-------------------------------------------------------------------------------
.  Set Parameters for outdchaf file
.-------------------------------------------------------------------------------
STDCH000  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,STDCH001,STDCH002,STDCH003,STDCH004,STDCH005
.
          GOTO      STDCH999
.
STDCH001  PACK      OTDCH001,QRYDATA,SP70
          GOTO      STDCH999
.
STDCH002  PACK      OTDCH002,QRYDATA,SP70
          GOTO      STDCH999
.
STDCH003  MOVE      QRYDATA,OTDCH003
          GOTO      STDCH999
.
STDCH004  MOVE      QRYDATA,OTDCH004
          GOTO      STDCH999
.
STDCH005  MOVE      QRYDATA,OTDCH005
          GOTO      STDCH999
.
STDCH999  RETURN
.
.------------------------------------------------------------
. Moves cgi variables into file variables
.------------------------------------------------------------
OTDSAV00  MOVE      OTDCH001,OTDCSLTY
          MOVE      OTDCH002,OTDCREGR
.
          MATCH     "MBS",OTDCH003
          IF        @EQUAL
            MOVE      " 0",OTDCIFLG
          ELSE
            MOVE      " 1",OTDCIFLG
          ENDIF
.
          MOVE      OTDCH004,OTDCITMN
          MOVE      OTDCH005,OTDCSUBN
.
OTDSAV99  RETURN
.
.------------------------------------------------------------
. Clear outdchaf file variables
.------------------------------------------------------------
CLOTDC00  MOVE      SP70,OTDCSLTY
          MOVE      SP70,OTDCREGR
          MOVE      SP70,OTDCIFLG
          MOVE      SP70,OTDCITMN
          MOVE      SP70,OTDCSUBN
          MOVE      SP70,OTDCWCRT
          MOVE      SP70,OTDCDCRT
          MOVE      SP70,OTDCTCRT
          MOVE      SP70,OTDCWUPD
          MOVE      SP70,OTDCDUPD
          MOVE      SP70,OTDCTUPD
          MOVE      SP70,OTDCSPAR
.
CLOTDC99  RETURN
.
.-------------------------------------------------------------------
. Output Fields from the default charges file
.-------------------------------------------------------------------
OTDC0000  BRANCH    FLDITMNO,OTDC0100,OTDC0200,OTDC0300,OTDC0400:
                             OTDC0500,OTDC0600,OTDC0700,OTDC0800:
                             OTDC0900,OTDC1000,OTDC1100,OTDC1200:
                             OTDC1300,OTDC1400,OTDC1500
.
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      OTDC9999
.
OTDC0100  MOVE      "CV",CATEGORY
          MOVE      OTDCSLTY,CODE
          CALL      CODITM00
          GOTO      OTDC9999
.
OTDC0200  WRITE     HTMLFILE;OTDCREGR;
          GOTO      OTDC9999
.
OTDC0300  MATCH     " 0",OTDCIFLG
          IF        @EQUAL
            WRITE     HTMLFILE;"MBS";     * MBS Code
          ELSE
            WRITE     HTMLFILE;"AMA";     * AMA Code
          ENDIF
          GOTO      OTDC9999
.
OTDC0400  WRITE     HTMLFILE;OTDCITMN;
          GOTO      OTDC9999
.
OTDC0500  WRITE     HTMLFILE;OTDCSUBN;
          GOTO      OTDC9999
.
OTDC0600  MATCH     SP70,OTDCWCRT
          GOTO      OTDC9999 IF EQUAL
.
          PACK      KEY10,OTDCWCRT
          CALL      RDWBSE1
          IF        OVRCD=1
            MOVE      OTDCWCRT,WBSENAM
          ENDIF
          WRITE     HTMLFILE;WBSENAM; 
          GOTO      OTDC9999
.
OTDC0700  MATCH     SP70,OTDCDCRT
          GOTO      OTDC9999 IF EQUAL
.
          UNPACK    OTDCDCRT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      OTDC9999
.
OTDC0800  WRITE     HTMLFILE;OTDCTCRT;
          GOTO      OTDC9999
.
OTDC0900  MATCH     SP70,OTDCWUPD
          GOTO      OTDC9999 IF EQUAL
.
          PACK      KEY10,OTDCWUPD
          CALL      RDWBSE1
          IF        OVRCD=1
            MOVE      OTDCWUPD,WBSENAM
          ENDIF
          WRITE     HTMLFILE;WBSENAM;
          GOTO      OTDC9999
.
OTDC1000  MATCH     SP70,OTDCDUPD
          GOTO      OTDC9999 IF EQUAL
.
          UNPACK    OTDCDUPD,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      OTDC9999
.
OTDC1100  WRITE     HTMLFILE;OTDCTUPD;
          GOTO      OTDC9999
.
OTDC1200  CALL      DFTAB000   * Table of VACS Codes
          GOTO      OTDC9999
.
OTDC1300  CALL      NXTGRD00   * Next set of default charges
          GOTO      OTDC9999
.
OTDC1400  CALL      PRVGRD00   * Previous set of default charges
          GOTO      OTDC9999
.
OTDC1500  MOVE      OTDCIFLG,FORM2
          PACK      KEY22,FORM2,OTDCITMN,OTDCSUBN,Z70 * Get current record
          CALL      RSPRIT1
          CALL      RPPRIT1
          BRANCH    OVRCD,OTDC9999
.
          COMPARE   FORM2,PRITFLAG           * Item Type
          GOTO      OTDC9999 IF NOT EQUAL
.
          MATCH     OTDCITMN,PRITITMN        * AMA/MBS Item
          GOTO      OTDC9999 IF NOT EQUAL
.
          MATCH     OTDCSUBN,PRITSUBN        * Sub Item
          GOTO      OTDC9999 IF NOT EQUAL
.
          UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,OTDC1510
          WRITE     HTMLFILE;PRITDESC;       * AMA/MBS Description
          GOTO      OTDC9999
.
OTDC1510  WRITE     HTMLFILE;PRITSFEE;       * Schedule fees
          GOTO      OTDC9999
.
OTDC9999  RETURN
.
.-----------------------------------------------------------
. Link to Next Group
.-----------------------------------------------------------
NXTGRD00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
.
          WRITE     HTMLFILE;"outweb04.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&startkey=",STARTKEY;
.
NXTGRD99  RETURN
.
.-------------------------------------------------------------
. Link to Prev Group
.-------------------------------------------------------------
PRVGRD00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3
          IF        F3<0
            MOVE      ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
.
          WRITE     HTMLFILE;"outweb04.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&startkey=",STARTKEY;
.
PRVGRD99  RETURN
.
.------------------------------------------------------------
. Table of slot type default charges : Report 6
.------------------------------------------------------------
DFTAB000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY4,STARTKEY,SP70
          CALL      RAOTDCH1
          IF        OVRCD=0
            CALL      RPOTDCH1
          ENDIF
DFTAB100  CALL      RKOTDCH1
          BRANCH    OVRCD,DFTAB900
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      DFTAB100
          ENDIF
.
          CALL      DFROW000      * Display Clinic Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      DFTAB100 IF NOT EQUAL
          GOTO      DFTAB999
.
DFTAB900  CALL      NOMORE00      * Display No More Records Message
.
DFTAB999  RETURN
.------------------------------------------------------------
. Clinic (TABLE ROW)
.------------------------------------------------------------
DFROW000  UNPACK    OTVADATE,CCENT,CYEAR,CMON,CDAY   * To Date in (DD MON CCYY)
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      KEY12,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          PACK      KEY5,ANSC,ANSV,OTDCSLTY
          CALL      RDCODE1
          IF        OVRCD = 1
            MOVE     "&nbsp;",TDESC
          ENDIF
.
          MATCH     "1",OTDCREGR
          IF        @EQUAL
            MOVE      "Yes",KEY3
          ELSE
            MOVE      "No ",KEY3
          ENDIF
.
          MATCH     " 0",OTDCIFLG
          IF        @EQUAL
            MOVE      "MBS",DIM3
          ELSE
            MOVE      "AMA",DIM3
          ENDIF
.
          MOVE      OTDCIFLG,FORM2
          PACK      KEY22,FORM2,OTDCITMN,OTDCSUBN,Z70 * Get current record
          CALL      RSPRIT1
          CALL      RPPRIT1
          BRANCH    OVRCD,DFROW700
.
          COMPARE   FORM2,PRITFLAG           * Item Type
          GOTO      DFROW700 IF NOT EQUAL
.
          MATCH     OTDCITMN,PRITITMN        * AMA/MBS Item
          GOTO      DFROW700 IF NOT EQUAL
.
          MATCH     OTDCSUBN,PRITSUBN        * Sub Item
          GOTO      DFROW700 IF NOT EQUAL
.
          GOTO      DFROW800
.
DFROW700  MOVE      SP70,PRITDESC
.
DFROW800  WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:ShowDetail06":
                          "(#"",LINKPRG,"#.pbl":
                          "?reportno=",LINKREP:
                          "&template=",LINKTEM:
                          "&otdch001=",OTDCSLTY:
                          "&otdch002=",OTDCREGR,"#")'>":
                          "<img src=../images/MaintenanceIcon.gif ":
                          "class=ListIcon></a>":
                          TDESC,"&nbsp;</td>":
                          "<td>",KEY3,"&nbsp;</td>":
                          "<td>",DIM3,"&nbsp;</td>":
                          "<td>",OTDCITMN,"&nbsp;</td>":
                          "<td>",PRITDESC,"&nbsp;</td>":
                          "</tr>"
.
DFROW999 RETURN
.------------------------------------------------------------
. NOMORE00 - Display "No more records" at end of list
.------------------------------------------------------------
NOMORE00  WRITE     HTMLFILE;"<tr>":
                             "<td class=RedCell colspan=20 align=center>":
                             "No more records</td>"
.
NOMORE99  RETURN
+
.------------------------------------------------------------------------------
.
          INC       MLTSECIO/INC    * Hospital security
          INC       OUTSITIO/INC   * Outpatients - Site Codes File
          INC       OUTPHOIO/INC   * Outpatients - Public Holiday File
          INC       PATIRNIO/INC   * Invoice Range File 
          INC       OUTCMNIO/INC   * Outpatients - Comments File
          INC       OUTLETIO/INC   * Outpatient Letters
          INC       OUTVACIO/INC   * Outpatient VACS codes
          INC       OUTHEDIO/INC   * Outpatient slot template header
          INC       OUTSESIO/INC   * Outpatient Session File
          INC       OUTMSPIO/INC   * Outpatient Suspensin File
          INC       OUTBOAIO/INC   * Outpatient Clinic Booking File A
          INC       OUTTXSIO/INC   * Outpatient Extra Templates File
          INC       OUTSIPIO/INC   * Outpatient Site Specific Categories File
          INC       OUTDCHIO/INC   * Outpatient slot type default charges
          INC       PATHSPIO/INC   * Hospital File
          INC       PRIITMIO/INC   * Private Practice Items
.
          INC       DAYOFWEK   
          INC       IBAWEBCD
          INC       WEBDATCD
          INC       OUTTXSTM
          INC       OUTSIPTM

.******************************************************************************
.*                                                                            *
.*                        P R O G R A M   S U M M A R Y                       *
.*                        -------------   -------------                       *
.*                                                                            *
.*     PROGRAM NAME:     IBAHOS07                                             *
.*                                                                            *
.*     FUNCTION:         HEALTH FUND AGED TRIAL BALANCE                       *
.*                                                                            *
.*     DATE WRITTEN:     10/10/1988                                           *
.*                                                                            *
.*     AUTHOR:           JENI TAN                                             *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.*                                                                            *
.*       V12.00.01  16/05/2025  J.Tan          TSK 0955096                    *
.*                  Added alpanumeric visit number generation                 *
.*       V11.04.03  03.06.2024  Ebon Clements TSK 0936282                     *
.*                  Added code to add * if Health Fund Number greater         *
.*                  than 10 characters                                        *
.*       V11.04.02  20/05/2024  J.Tan         TSK 0936282                     *
.*                  Fixed tempfile with FORM fields                           *
.*       V11.04.01  05.02.2024  DA Sarkies    TSK 0936282                     *
.*                  Changed positioning of Health Fund number on printouts    *
.******************************************************************************
.*       V10.13.02  06/12/2013 J.Tan         TSK 0859743                      *
.*                  Mod for OTRECTYP=6 for Theatre item                       *
.*       V10.13.01  05/12/2018  Don Nguyen    TSK 0838558                     *
.*                  Deleted temp file (TEMPSEQ) after processing              *
.******************************************************************************
.*       V10.11.02  27/11/2017  Thanh T.      TSK 0821710                     *
.*                  Recompiled as PATMISTD changed                            *
.*       V10.11.01  07/09/2017  Thanh T.      TSK 0821710                     *
.*                  Audit Amission/outpatient visit comments                  *
.******************************************************************************
.*       V10.05.01  01/01/2015  Ania P        CAR 261630                      *
.*                  Removed use of PORT for temp file naming                  *
.*       V10.01.01  10/03/2011 Sandra Barcham CAR 233772                      *
.*                  Use Effective date not transaction date for cash          *
.*        V9.12.01  31/08/2009  Mike Laming   CAR 198325                      *
.*                  Add logic for PTINGSTJ when checking Balance at           *
.*        V9.03.03  20/02/2008 Sandra Barcham CAR 160240                      *
.*                  Fix procedures being counted twice                        *
.*        V9.09.02  01/10/2007 Sandra Barcham CAR 151159                      *
.*                  Fix AAEDTR loop                                           *
.*        V9.09.01  13/07/2007  Mike Laming   CAR 141675                      *
.*                  Change Error messages to warnings at NOPREAD & NOCANAD    *
.*        V9.04.05  03/08/05 J.Tan    CAR 62750                               *
.*                  Removed redefined amount variables                        *
.*        V9.04.04  18/07/2005  J.Tan  CAR 61343                              *
.*                  Mods checking for credit notes                            *
.*        V9.04.03  18/10/2004  Lina Vo CAR 54252                             *
.*                  Fixed report to display Claim Code range and Health fund  *
.*                  range                                                     *
.*        V9.04.02  30/08/2004  J.Tan  CAR 53075                              *
.*                  Mods to use HF from master rec.if HF from adm.record blank*
.*        V9.04.01  23/08/2004 J.Tan   CAR 52835                              *
.*                  Mods for multi hospital                                   *
.*        V9.03.01  14/08/2003  Lina Vo   CAR 41196                           *
.*                  Recompiled for big changes in PATSELFN.                   *
.*        V9.02.02  02/04/2003  Dean Cramer                                   *
.*                  Tidied entry options for better web front end             *
.*        V9.02.01  15/10/2000  J.Tan                                         *
.*                  Fixed F*I                                                 *
.*        V5.08.05  30/10/2000  Steve Armstrong  srf 6233                     *
.*                  Fixed prinout of HF name to not print trailing blanks     *
.*        V5.08.04  27/10/2000  Dean Cramer   SRF 6710                        *
.*                  Recomplied for PATFNDDS via PATFNDKY                      *
.*                  Changed for health fund table code size increase to       *
.*                  8 characters                                              *
.*        V5.08.03  16/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.02  06/06/2000  Jill Habasque                                 *
.*                  Changed from key10 to key14 for the health fund file      *
.*        V5.08.01  08/05/2000 J.Tan    SRF 638791                            *
.*                  Fixed o/s amount with payment > one hundred thousand      *
.*        V5.08.01B 28/02/2000  J.Tan                                         *
.*                  Mods to use effective date instead of last changed date   *
.*        V5.07.02  30/09/1999  Greg Horvat                                   *
.*                  Changed to use DAYSEP & fixed other Y2K date calc bugs    *
.*        V5.07.01  27/07/1999  Steve Downing                                 *
.*                  Included century in invoice date calculations             *
.*                  Y/N options accept upper & lower case                     *
.*       V5.07.B02  28/01/1999  Nicole Harrington 507 rebounds                *
.*                  Mods to add ? Health Fund search and print centuries      *
.*       V5.07.B01  17/11/1998  Jim Stathopoulos                              *
.*                  507 Changes                                               *
.****************************************************************************** 
.*           V3.01  28/09/88  J.Tan                                           *
.*                  Line-up printout & use DTRA1                              *
.*           V3.02  31/01/1989  K.Peak                                        *
.*                  Added PATSELFN                                            *
.*           V5.01  13/04/1989  Graeme Williams   SRF 5645                    *
.*                  New index added to Dtrans                                 *
.*           V5.02  02/05/89  J.Tan      SRF 100366                           *
.*                  Fixed PATSELFN options                                    *
.*           V5.03  05/05/1989  Graeme Williams   SRF 4219                    *
.*                  Added PINVCADM to Invoice file                            *
.*           V5.04  16/05/1989  Karin Peak                                    *
.*                  Split AAEDETFD into DEA & B                               *
.*           V5.05  17/05/1989  S.O'Gorman                                    *
.*                  Allow for 8-digit U/R's & Admission Numbers.              *
.*                  Include STD001.                                           *
.*           V5.06  24/05/1989  DIG                                           *
.*                  Fixed U* error for 90+ report                             *
.*           V5.07  27/05/89  J.Tan      SRF 100750                           *
.*                  Fixed Mother Adm. to FORM 8 (PATMI1FD)                    *
.*           V5.08  03/06/1989  S.Barcham         SRF 100751                  *
.*                  Split OUTBOK & OUTMAS Includes                            *
.*           V5.09  06/06/89  J.Tan      SRF 100809                           *
.*                  Mods Locking master file (PATIOMAS)                       *
.*           V5.10  24/06/1989  S.Barcham                                     *
.*                  Recompiled for OUTBB1FD                                   *
.*           V5.11  30/06/1989  DIG                                           *
.*                  Recompiled for changes to OUTSITFD                        *
.*        V5.01.01  04/07/1989  M.Ohleiter                                    *
.*                  Changes for 8-characters U/R & Admission No - Rel. 5.01   *
.*        V5.01.02  20/11/89  J.Tan      SRF 102252                           *
.*                  Added other financial option in PATSELFN                  *
.*        V5.01.03  23/05/90  J.Tan      SRF 104901                           *
.*                  Mods PATFN1FD                                             *
.*        V5.01.04  13/06/90  J.Tan      SRF 105204                           *
.*                  Fixed balance owing                                       *
.*        V5.01.05  11/07/1990  Paul Duncan                                   *
.*                  Recompiled for OUTBOA & OUTBOB                            *
.*        V5.01.121 27/11/1990  Glen Hobby                                    *
.*                  Recompiled for changes to PATMX1FD                        *
.*        V5.01.122 12/06/1992  Graeme Williams   SRF 114865                  *
.*                  Changes for 9 character miscellaneous items               *
.*        V5.01.123 08/03/93  Neeriem "I Hate Support" Dye                    *
.*                  Modify to use standard routine KEYDATE                    *
.*        V5.01.124 06/04/1993  i chung           SRF 118792                  *
.*                  Fixed missing "0" Claim Type if not using Start to Finish *
.*                  range.                                                    *
.*                  Added "?" facility for Claim Type.                        *
.*        V5.03.B1  06/06/95  Paul Howells                  SRF 136735        *
.*                  Recompiled for PATSELFN                                   *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
          INC       IBASECFD/INC
          INC       IBAPASFD/INC
          INC       IBACONFD/INC
          INC       PATSELDT/INC
.
          INC       PMSCNOFD/INC
          INC       AAEDE1FD/INC
          INC       AAEDEBFD/INC
          INC       AAEDTRFD/INC
          INC       EMRVISFD/INC
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
          INC       OUTDTRFD/INC
          INC       OUTSITFD/INC
.
          INC       VISMDTFD/INC
          INC       VISMTXFD/INC
          INC       PATMISTD/INC
          INC       PATCODFD/INC
          INC       PATDSCFD/INC
          INC       PATDTRFD/INC
          INC       PATFN1FD/INC
          INC       PATINVFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATREMFD/INC
          INC       PATVISFD/INC
          INC       PATHSPFD/INC
          INC       PMSEVTFD/INC
          INC       PMSVX1FD/INC
          INC       IBASEQFD/INC  
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
+
.
.         TEMP FILES DECLARATION
.
TEMPSEQ   FILE      SQL, FIXED=260
.
TEMPFILE  IFILE     SQL, FIXED=259, KEY="U1-3,4-9,10-29,30-54,55-62,63-70" 
.
. ------  Temp file variables for "full report"  ------
.
. Name    Type    Length   Physical   Start       Description
. ----    ----    ------   --------   -----       -----------
.PINVCLM  DIM       3         3         1         -|
.AFUNDH   DIM       6         6         4          |
.PSNAME   DIM       20        20        10         >  key 70
.PGNAME   DIM       25        25        30         |
XDAADMNO  DIM       8         8         55         |
DUNIQUE   DIM       8         8         63        _|
.AURNO    DIM       8         8         71
.PINVNO   DIM       8         8         79
TPINVREB  DIM      15        15         87
TPINVHFD  DIM      15        15        102
XOWING    DIM       15       15         117
XTOTCURR  DIM       15       15         132
XTOT30    DIM       15       15         147
XTOT60    DIM       15       15         162
XTOT90    DIM       15       15         177
PADDING   DIM       67       67         192
.End of Record                          259
.
.  Redefined variables
.
OWING   FORM      8.2       6         103
TOTCURR FORM      8.2       6         109
TOT30   FORM      8.2       6         115
TOT60   FORM      8.2       6         121
TOT90   FORM      8.2       6         127
.
. ------  Temp file variables for "not full report"  ------
.
. Name    Type    Length   Physical   Start       Description
. ----    ----    ------   --------   -----       -----------
.PINVCLM  DIM       3         3         1         -|
.AFUNDH   DIM       6         6         4          |
.PSNAME   DIM       20        20        10         >  key 70
.PGNAME   DIM       25        25        30         |
.XDAADMNO DIM       8         8         55         |
.DUNIQUE  DIM       8         8         63        _|
.AURNO    DIM       8         8         71 
.PINVNO   DIM       8         8         79
.TPINVREB  DIM      15        15         87
.TPINVHFD  DIM      15        15         102
.XOWING   DIM      15        15         117
.PINVAMT  DIM      15        15         132
.AFNDTB   DIM       8         8         147
.AFNDMM   DIM       19        19        155
.PCLINE   DIM       70        70        174
.REMLSTL  DIM       3         3         244
.LSTLDAT  DIM       8         8         247
.PTITL    DIM       4         4         255
.End of Record                          259
+
.
.         PROGRAM VARIABLES
.
CATBLAY   FORM      1
CFEETYP   FORM      1
CJRNREP   FORM      1
CLDESC    DIM       20
CLFLAG    FORM      1
CLMFUND   DIM       9
CMDLINE   DIM       80
CODE      DIM       2
COTHSFN   FORM      1
COUNT     FORM      3
CT30      FORM      8.2
CT60      FORM      8.2
CT90      FORM      8.2
CTCURR    FORM      8.2
CTHF      FORM      8.2
CTREB     FORM      8.2
CTOWING   FORM      8.2
CURSITE   DIM       6
CURSYS    FORM      1
.
DALERG    DIM       3 
DANS      DIM       3
DAYSINYR  FORM      5
DHOSP     DIM       3
DILLN     DIM       3
DIM6      DIM       6
DIM8      DIM       8
DIM9A     DIM       9
DIM9      DIM       9
DIM10     DIM       10
DIM14     DIM       14
DIM15     DIM       15
DIM30     DIM       30
DIM50     DIM       50
DIM60     DIM       60
DIM70     DIM       70
DOUTXTRA  DIM       30
DSYST     DIM       30
.
EDCLM     DIM       3
EDHF      DIM       6
ENDFLAG   FORM      1
ENDNAM    DIM       6
.
FORM82    FORM      8.2
FORM12P2  FORM      12.2
FIRST     FORM      1
FNAMET    DIM       8
FRCLM     DIM       5                  * From Claim Code output
FRHF      DIM       6                  * From Health Fund output
FROMDATE  DIM       8                  * From Date in CCYYMMDD format
FRSURN    DIM       6                  * From Surname output
.
HDCLM     FORM      1
HEADFLG   FORM      1
HFFLAG    FORM      1
HT30      FORM      8.2
HT60      FORM      8.2
HT90      FORM      8.2
HTCURR    FORM      8.2
HTREB     FORM      8.2
HTHF      FORM      8.2
HTOWING   FORM      8.2
.
LSTLDAT   DIM       8
NBRDAYS   FORM      7
.
OPCND     FORM      1
OPTION1   FORM      1
.
PDIM8     DIM       8
PNAME     DIM       22                 * PGNAME.PSNAME
.
QUESTFLG  FORM      1                  * "?" option flag
ROW       FORM      2
.
SAVADMNO  DIM       8
SAVAMT    FORM      8.2
SAVCLM    DIM       3
SAVFUND   DIM       9                  * contains PINVCLM,AFUNDH
SEC       FORM      2
SEPCLM    FORM      1
SEPFUND   FORM      1
STAR      INIT      "*"
STARTNM   DIM       6
STATUS    FORM      1
STCLM     DIM       3
STHF      DIM       6
STOTPD    FORM      12.2
.
TDATE     DIM       8                  * actual Translated Date (CCYYMMDD)
TIMEIS    DIM       8
TMPPAID   FORM      12.2
TOCLM     DIM       6                  * To Claim Code output
TOHF      DIM       6                  * To Health Fund output
TOSURN    DIM       6                  * To Surname output
TOTONLY   FORM      1
TTOT30    FORM      8.2
TTOT60    FORM      8.2
TTOT90    FORM      8.2
TTOTCURR  FORM      8.2
TTOTREB   FORM      8.2
TTOTHF    FORM      8.2
TTOWING   FORM      8.2
.
UNIQUE    FORM      8
.
.         PROGRAM CONSTANTS
.
BD        INIT      "B-"
BEGIN     FORM      "0"
CODECL    INIT      "CL"
.
DSYST0    INIT      "CONSOLIDATED REPORT           "
DSYST1    INIT      "ACCIDENT & EMERGENCY REPORT   "
DSYST2    INIT      "OUTPATIENTS REPORT            "
DSYST3    INIT      "INPATIENTS REPORT             "
DSYST4    INIT      "OTHER FINANCIAL REPORT        "
DOUTALL   INIT      "ALL SITES                     "
DOUTSIT   INIT      "SITE : "
DOUTDEP   INIT      "DEPT : "
HD        INIT      "H-"
KEYREST   INIT      " 1 1"
.
FINISH    INIT      "Finish"
START     INIT      "Start"
.
SP68      INIT      "                                                   ":
                    "                   "
FIVE9     FORM      "59"
NINETY1   FORM      "91"
THREE65   FORM      "365"
THREE66   FORM      "366"
.
ZERO4     FORM      "0000"
ZERO00    FORM      "0.00"
ZEROSP    INIT      "0  "
.
.-----------------------------------------------------------------------
PRGID     INIT      "IBAHOS07"
PRGNAM    INIT      "Health Fund Aged Trial Balance"
VERSION   INIT      "V12.01.00"
.-----------------------------------------------------------------------
+
.
.         DISPLAY SCREEN HEADER AND OPEN FILES
.
          CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening aaede1af";
          OPEN      AAEDE1A1,"aaede1af"
.
          DISPLAY   *P64:24,"aaedtref";
          OPEN      AAEDTRE1,"aaedtref"
.
          DISPLAY   *P64:24,"emrvisaf";
          OPEN      EMRVISA1,"emrvisaf"
.
          DISPLAY   *P64:24,"outsitaf";
          OPEN      OUTSITA1,"outsitaf"
.
          DISPLAY   *P64:24,"vismdtaf";
          OPEN      VISMDTA1,"vismdtaf"
          DISPLAY   *P64:24,"vismtxaf";
          OPEN      VISMTXA1,"vismtxaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"patdtraf";
          OPEN      PATDTRA1,"patdtraf"
.
          DISPLAY   *P64:24,"patfn1af";
          OPEN      PATFN1A1,"patfn1af"
.
          DISPLAY   *P64:24,"patinvrf";
          OPEN      PATINVR4,"patinvrf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"pmscnoaf";
          OPEN      PMSCNOA3,"pmscnoaf"
.
          DISPLAY   *P64:24,"patremdf";
          OPEN      PATREMD1,"patremdf"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA1,"patvisaf"
.
          DISPLAY   *P64:24,"pmsevtaf";
          OPEN      PMSEVTA1,"pmsevtaf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS,*85,IBCNUGST
          READ      CONTROLF,TEN8;*255,CJRNREP
.
          MOVE      ONE,CNEWDS              * set "?" facility to new format
+
.
.         START OF PROGRAM
.
START     HLINE     *G37,2,55,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Patient Surname Sequence":
                    *P1:7,"Select Option : ";
.
KEYSEQ    KEYIN     *P17:7,*EL,*V2LON,OPTION;
.
          MOVE      ZERO,COUNT
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
          BRANCH    OPTION OF SURNAM
          BEEP
          GOTO      KEYSEQ
.
SURNAM    HLINE     *G37,2,55,80
          DISPLAY   *P55:2,*V2LON,"- Surname Sequence ",*HOFF,*P1:3,*EF:
                    *P1:4,"Surname From : ":
                    *P1:5,"Surname To   : ";
.
KEYSTTN   MOVE      SP6 TO STARTNM
          KEYIN     *P16:5,*EF,*P16:4,*EL,*V2LON,STARTNM;
          ENDSET    STARTNM
          APPEND    SP6,STARTNM
          RESET     STARTNM
.
          MATCH     "Start",STARTNM
          GOTO      KEYSTTN0 IF EQUAL
          MATCH     SP6,STARTNM
          GOTO      KEYSTTN1 IF NOT EQUAL
KEYSTTN0  MOVE      START,FRSURN            * set for print output
          MOVE      SP6,STARTNM
          DISPLAY   *P16:4,*V2LON,START;
          GOTO      KEYENDN
.
KEYSTTN1  MOVE      STARTNM,FRSURN
.
KEYENDN   MOVE      SP6 TO ENDNAM
          KEYIN     *P16:5,*EL,*V2LON,ENDNAM;
          ENDSET    ENDNAM
          APPEND    SP6,ENDNAM
          RESET     ENDNAM
.
          MATCH     "Finish",ENDNAM
          GOTO      KEYENDN0 IF EQUAL
          MATCH     SP6,ENDNAM
          GOTO      KEYENDN1 IF NOT EQUAL
KEYENDN0  MOVE      "zzzzzz" TO ENDNAM
          MOVE      FINISH,TOSURN           * set for print output
          DISPLAY   *P16:5,*EL,*V2LON,FINISH;
          GOTO      OK
.
KEYENDN1  MOVE      ENDNAM,TOSURN           * set for print output
.
          MATCH     STARTNM TO ENDNAM
          GOTO      OK IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Surname to must be > or equal to surname ":
                                  "from.  ";
          CALL      HITENTER
          GOTO      KEYENDN
.
OK        KEYIN     *P1:7,*EL,"OK (",*V2LON,"Y",*HOFF,"/",*V2LON,"N":
                    *HOFF,") ? ",*V2LON,ANS
          RESET     ANS
          GOTO      START IF EOS
          REP       UPPLOW,ANS
          DISPLAY   *P12:7,*V2LON,ANS;
.
          MATCH     "Y" TO ANS
          GOTO      KEYSCLM IF EQUAL
          MATCH     "N" TO ANS
          GOTO      KEYSTTN IF EQUAL
          BEEP
          GOTO      OK
.
.         Keyin Starting Claim Type
.
KEYSCLM   MOVE      "24",ECOL
          MOVE      "7",EVERT
          MOVE      "CL",CODE
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND
          DISPLAY   *P1:EVERT,*EF,"Starting Claim Type  : ";
          CALL      PATCODKY
          BRANCH    EXIT,KEYSCLM1,START
.
          MOVE      ACODE,STCLM
          MOVE      ACODE,FRCLM
          DISPLAY   *PECOL:EVERT,*EL,*V2LON,STCLM,*HOFF,SP2,TDESC;
          GOTO      KEYECLM
.
KEYSCLM1  MOVE      SP3,STCLM
          MOVE      START,FRCLM
          DISPLAY   *PECOL:EVERT,*EL,*V2LON,START;
.
.         Keyin Ending Claim Type
.
KEYECLM   MOVE      "24",ECOL
          MOVE      "8",EVERT
          MOVE      "CL",CODE
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND
          DISPLAY   *P1:EVERT,"Ending Claim Type    : ";
          CALL      PATCODKY
          BRANCH    EXIT,KEYECLM1,START
.
          MOVE      ACODE,EDCLM
          MOVE      ACODE,TOCLM
          DISPLAY   *PECOL:EVERT,*EL,*V2LON,EDCLM,*HOFF,SP2,TDESC;
          GOTO      KEYECLM2
.
KEYECLM1  MOVE      "zzz",EDCLM
          MOVE      FINISH,TOCLM
          DISPLAY   *PECOL:EVERT,*EL,*V2LON,FINISH;
.
KEYECLM2  MATCH     STCLM,EDCLM
          GOTO      KEYALL IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Ending claim type must be >= starting ":
                                  "claim type.  ";
          CALL      HITENTER
          GOTO      KEYECLM
+
.
.         Ask if each claim type is to be on a separate page
.
KEYALL    MOVE     SP1,ANS
          KEYIN    *P1:9,*EL,"Do you want each Claim Code on a separate page (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
          RESET     ANS
          REP       UPPLOW,ANS
.
          MATCH     SP1,ANS
          GOTO      SEPCLMN IF EOS
          GOTO      SEPCLMN IF EQUAL
.
          MATCH     "Y",ANS
          GOTO      SEPCLMY IF EQUAL
.
          MATCH     "N",ANS
          GOTO      SEPCLMN IF EQUAL
.
          BEEP
          GOTO      KEYALL
.
SEPCLMY   MOVE      ONE,SEPCLM
          DISPLAY   *P56:9,*V2LON,ANSY;
          GOTO      KEYSHF
.
SEPCLMN   MOVE      TWO,SEPCLM
          DISPLAY   *P56:9,*V2LON,ANSN;
.
.         Keyin Starting Health Fund
.
KEYSHF    MOVE      "24",ECOL
          MOVE      "10",EVERT
          MOVE      SP6,CKYIDEF6
          MOVE      ZERO,CKYIMAND
          DISPLAY   *P1:EVERT,*EF,"Starting Health Fund : ";
          CALL      PATFNDKY
          BRANCH    EXIT,KEYSHF1,START,KEYSHF1
.
          MOVE      HCODE,STHF
          MOVE      HCODE,FRHF
          DISPLAY   *PECOL:EVERT,*EL,*V2LON,STHF,*HOFF,SP2,TDESC;
          GOTO      KEYEHF
.
KEYSHF1   MOVE      SP6,STHF
          MOVE      START,FRHF
          DISPLAY   *PECOL:EVERT,*EL,*V2LON,START;
.
.         Keyin Ending Health Fund
.
KEYEHF    MOVE      "24",ECOL
          MOVE      "11",EVERT
          MOVE      SP6,CKYIDEF6
          MOVE      ZERO,CKYIMAND
          DISPLAY   *P1:EVERT,"Ending Health Fund   : ";
          CALL      PATFNDKY
          BRANCH    EXIT,KEYEHF1,START,KEYEHF1
.
          MOVE      HCODE,EDHF
          MOVE      HCODE,TOHF
          DISPLAY   *PECOL:EVERT,*EL,*V2LON,EDHF,*HOFF,SP2,TDESC;
          GOTO      KEYEHF2
.
KEYEHF1   MOVE      "zzzzzz",EDHF
          MOVE      FINISH,TOHF
          DISPLAY   *PECOL:EVERT,*EL,*V2LON,FINISH;
.
KEYEHF2   MATCH     STHF,EDHF
          GOTO      KEYFND IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Ending health fund must be >= starting ":
                                  "health fund.  ";
          CALL      HITENTER
          GOTO      KEYEHF
.
.         check if each Health Fund is to be on a separate page
.
KEYFND    MOVE      SP1,ANS
          KEYIN     *P1:12,*EL,"Do you want each Health Fund on a separate":
                    " page (",*V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ? ":
                    *V2LON,ANS;
          RESET     ANS
          REP       UPPLOW,ANS
.
          MATCH     SP1,ANS
          GOTO      SEPFNDN IF EOS
          GOTO      SEPFNDN IF EQUAL
.
          MATCH     "Y",ANS
          GOTO      SEPFNDY IF EQUAL
.
          MATCH     "N",ANS
          GOTO      SEPFNDN IF EQUAL
.
          BEEP
          GOTO      KEYFND
.
SEPFNDY   MOVE      ONE,SEPFUND
          DISPLAY   *P57:12,*V2LON,ANSY;
          GOTO      KADATE
.
SEPFNDN   MOVE      TWO,SEPFUND
          DISPLAY   *P57:12,*V2LON,ANSN;
.
KADATE    DISPLAY   *P1:13,*EF,"Keyin As At Date : ";
.  
          UNPACK    SP6,CDAY,CMON,CYEAR
          MOVE      CCC,CCENT
          MOVE      "13",CVERT
          MOVE      "20",CCOL
          MOVE      "1",CDEFDTE
          REPEAT
            CALL      KEYDATE
            BRANCH    OVRCD,START
          UNTIL     CQUEST=0
.
          PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",FROMDATE
.
CHKTOTS   MOVE      SP1,ANS
          KEYIN     *P1:14,*EL,"Totals Only to be printed (":
                    *V2LON,"Y",*HOFF,*DV,SLASH:
                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
          RESET     ANS
          REP       UPPLOW,ANS
.
          MATCH     SP1,ANS
          GOTO      CHKTOTY IF EOS
          GOTO      CHKTOTY IF EQUAL
.
          MATCH     "Y",ANS
          GOTO      CHKTOTY IF EQUAL
.
          MATCH     "N",ANS
          GOTO      CHKTOTN IF EQUAL
.
          BEEP
          GOTO      CHKTOTS
.
CHKTOTN   MOVE      ZERO,TOTONLY
          DISPLAY   *P35:14,*V2LON,ANSN;
          GOTO      KEYHOSP
.
CHKTOTY   MOVE      ONE,TOTONLY
          DISPLAY   *P35:14,*V2LON,ANSY;
.
KEYHOSP   COMPARE    ONE,IBCNMHOS
          GOTO       KEYOPT IF NOT EQUAL
.
          DISPLAY    *P1:15,*EL,"Hospital Id : ";
          MOVE       TEN5,ECOL
          MOVE       "15",EVERT
          MOVE       SP3,CKYIDEF3
          MOVE       ZERO,CKYIMAND           * Not Mandatory
          OPEN       PATHSPA1,"pathspaf"
.
          CALL       PATHSPKY
          BRANCH     EXIT,ALLHOSP,START
          GOTO       KEYOPT
.
ALLHOSP   MOVE       "All Hospitals",PTHSNAME
          MOVE       SP10,PTHSHOSP
.
.         check if dates are okay or not
.
KEYOPT    DISPLAY   *P1:16,*EF,*V2LON,ZERO,*HOFF," = Exit":
                         *P1:17,*V2LON,ONE,*HOFF," = Full Report":
                         *P1:18,*V2LON,TWO,*HOFF," = Current Only":
                       *P1:19,*V2LON,THREE,*HOFF," = 30 Days Only":
                        *P1:20,*V2LON,FOUR,*HOFF," = 60 Days Only":
                        *P1:21,*V2LON,FIVE,*HOFF," = Over 90 Days":
                    *P1:23,"Select Option : ";
.
KEYOPT1   KEYIN     *P17:23,*EL,*V2LON,OPTION1;
.
          COMPARE   ZERO TO OPTION1
          GOTO      START IF EQUAL
.
          BRANCH    OPTION1 TO OPTFULL,OPTCUR,OPT30,OPT60,OPT90  
          BEEP
          GOTO      KEYOPT1
.
OPTFULL   DISPLAY   *P65:2,*V2LON,"- Full Report ";
          GOTO      KPATSEL
.
OPTCUR    DISPLAY   *P65:2,*V2LON,"- Current ";
          MOVE      "CURRENT  " TO DIM9
          GOTO      KPATSEL
.
OPT30     DISPLAY   *P65:2,*V2LON,"- 30 Days ";
          MOVE      "30 DAYS  " TO DIM9
          GOTO      KPATSEL
.
OPT60     DISPLAY   *P65:2,*V2LON,"- 60 Days ";
          MOVE      "60 DAYS  " TO DIM9
          GOTO      KPATSEL
.
OPT90     DISPLAY   *P65:2,*V2LON,"- 90+ Days ";
          MOVE      "90+ DAYS " TO DIM9
.
.         get the Financial options
.
KPATSEL   CALL      PATSELFN
          BRANCH    EXIT,SURNAM
.
.         set up heading variables
.
          MOVE      DSYST0,DSYST
          LOAD      DSYST,FSTSYS,DSYST1,DSYST2,DSYST3,DSYST4
.
          MOVE      SP30,DOUTXTRA
.
          COMPARE   TWO,FSTSYS
          GOTO      REKEY IF NOT EQUAL
.
          MATCH     SP6,FSTSITE
          GOTO      SINGSITE IF NOT EQUAL
.
          MATCH     SP3,FSTDEPT
          GOTO      SINGDEPT IF NOT EQUAL
          MOVE      DOUTALL,DOUTXTRA
          GOTO      REKEY
.
SINGSITE  PACK      DOUTXTRA,DOUTSIT,FSTSITE,SP2,OSTDESC  * Desc already read in
          GOTO      REKEY
.
SINGDEPT  PACK      DOUTXTRA,DOUTDEP,FSTDEPT,SP2,TDESC    * Desc already read in
.
REKEY     CALL      CONTQST
          BRANCH    CEXIT,CHKCNT,SURNAM,SURNAM
.
.         Subroutine to get the starting Claim Code
.
GETSCLM   MOVE      SP3,ACODE
          PACK      KEY5 WITH CODECL,STCLM
          CALL      RDCODE1
          COMPARE   ZERO,OVRCD
          RETURN    IF EQUAL
.
          CALL      RDSCODE1
          CALL      RDKCODE1
          RETURN
+
.
CHKCNT    CALL      TFILENAM
          MOVE      TFILNAME,FNAMET
.
.         check if previously exists..if so delete it 
.
          CALL      KILLIDZ
.
NOEXIND   PREP      TEMPSEQ,FNAMET
          CLOSE     TEMPSEQ
          PREP      TEMPSEQ,FNAMET
.
          MOVE      SIXTY,CLNO
          MOVE      ZERO,CPAGENO
          MOVE      ZERO,HDCLM
          MOVE      ZERO,HEADFLG
.
          MOVE      ZEROVISN TO SAVADMNO
          MOVE      ZERO TO TOTCURR
          MOVE      ZERO TO TOT30
          MOVE      ZERO TO TOT60
          MOVE      ZERO TO TOT90
          MOVE      ZERO TO OWING
          MOVE      ZERO TO UNIQUE
          MOVE      SP70,TPINVREB
          MOVE      SP70,TPINVHFD
          MOVE      SP70,XOWING
          MOVE      SP70,XTOTCURR
          MOVE      SP70,XTOT30
          MOVE      SP70,XTOT60
          MOVE      SP70,XTOT90
.
          CLOCK     TIME,TIMEIS
.
.         Initialise the key variables for the loop over the invoice file
.
          MOVE      FSTSYS,CURSYS
          MOVE      FSTSITE,CURSITE
.
.         read in first Claim details
.
          CALL      GETSCLM
.
          DISPLAY   *P1:24,*EL,*P5:24,"Claim : ",*P20:24,"Surname : ":
                              *P50:24,"Admission Number :";
.
.         Initialize the key for the fourth index of the Invoice file
.
REPACK    PACK      KEY32,CURSYS,CURSITE,ACODE,STARTNM,SP30
          CALL      RDSINV4
URLOOP    CALL      RDKINV4
          BRANCH    OVRCD TO SORTD
.
          MATCH     ZEROVISN,PINVADM
          GOTO      URLOOP IF EQUAL         * cancelled Invoice
          MOVE      ZERO,SAVAMT
.
.         display the Invoice details
.
          DISPLAY   *P13:24,*V2LON,PINVCLM,*P31:24,PINALPHA,*P70:24,PINVADM;
.
.         check for a change in System
.
          COMPARE   PINVSYS,CURSYS
          GOTO      CHKSITE IF EQUAL
.
.         go to the next System
.
NEXTSYS   ADD       ONE,CURSYS
.
.         New System.  Check if we are outside the request range
.
          COMPARE   CURSYS,FEDSYS
          GOTO      SORTD IF LESS
          MOVE      FSTSITE,CURSITE        * Initialise for reposition
          CALL      GETSCLM
          GOTO      REPACK
.
.         same System, check for a change in Site
.
CHKSITE   MATCH     PINVSITE,CURSITE
          GOTO      CHKCLM IF EQUAL
.
.         go to the next Site Code
.
NEXTSITE  MOVE      CURSITE,KEY6
          CALL      RDSSITA1
NEXTSITL  CALL      RDKSITA1
          BRANCH    OVRCD,NEXTSYS
.
.         New Site.  Check if we are outside the request range
.
          MATCH     OSTSITE,FEDSITE
          GOTO      NEXTSYS IF LESS
.
.         Check for a valid department
.
          MATCH     FSTDEPT,OSTSYST
          GOTO      NEXTSITL IF LESS
.
          MATCH     OSTSYST,FEDDEPT
          GOTO      NEXTSITL IF LESS
.
.         initialise for reposition
.
          MOVE      OSTSITE,CURSITE
          CALL      GETSCLM
          GOTO      REPACK
.
.         same System/Site.  Check for a new Claim Code
.
CHKCLM    MATCH     PINVCLM,ACODE
          GOTO      CHKSUR IF EQUAL
.
.         go to the next Claim Code
.
NEXTCLM   CALL      RDKCODE1
          BRANCH    OVRCD,NEXTSITE
.
          MATCH     CODECL,TCODE
          GOTO      NEXTSITE IF NOT EQUAL
.
          MATCH     ACODE,EDCLM
          GOTO      NEXTSITE IF LESS
          GOTO      REPACK                  * Initialise the reposition
.
.         same System/Site/Claim Code.  Check for a surname in the valid range
.
CHKSUR    MATCH     STARTNM,PINALPHA
          GOTO      REPACK IF LESS
.
          MATCH     PINALPHA,ENDNAM
          GOTO      NEXTCLM IF LESS
.
.         check for a invalid Invoice date
.
          MATCH     PINVDTE,FROMDATE        * Invoice after cut-off date
          GOTO      URLOOP IF LESS
.
          MATCH     PINVBLCD,FROMDATE
          GOTO      URLOOP IF NOT LESS
.
          IF          IBCNMHOS=1
            MATCH       SP10,PTHSHOSP
            IF          !@EQUAL
              PACK        KEY8,PINVADM,SP8
              CALL        RDPMVX11
              BRANCH      OVRCD,URLOOP
              MATCH       PMVXMHOS,PTHSHOSP
              GOTO        URLOOP IF NOT EQUAL
            ENDIF
          ENDIF
.
.         determine if this Invoice is fully paid or not
.
URLP2000  MOVE      ZERO,TMPPAID
          MOVE      PINVAMT,SAVAMT
          PACK      KEY22 WITH PINVADM,PINVNO,SP6
.
.         Loop over the appropriate DTRAN file
.
          BRANCH    PINVSYS OF AAEDTR,OTHDTR,INPDTR,OTHDTR,PYEND1
.
.         Inpatient Invoice
.
INPDTR    PACK      KEY22 WITH PINVADM,PINVNO,SP6
          CALL      RDSDTRN1
.
PYLOOP1   CALL      RDKDTRN1
          BRANCH    OVRCD OF PYEND1
.
.         check for a change in Admission Number
.
          MATCH     TREF,PINVNO
          GOTO      PYEND1 IF NOT EQUAL
.
.         check the range of date
.
          BRANCH    TRECTYPE,PYLOOP1,PYLOOP1,PYLOOP1,PYLOOP1,PYCSH1,PYJRN1
          GOTO      PYLOOP1
.
.         If the Transaction date is used for journals, then treat as cash
.         otherwise use the last modification date as the journal date for
.         determining if the journal was done before or after cutoff date
.
PYJRN1    BRANCH    CJRNREP,PYCSH1
.
          MATCH     PTDTEFFD,FROMDATE
          GOTO      PYLOOP1 IF LESS
          GOTO      MPAY00
.
.         Use the transaction date to determine if this transaction was done
.         before or after the cutoff date.
.
PYCSH1    MATCH     PTDTEFFD,FROMDATE
          GOTO      PYLOOP1 IF LESS
.
MPAY00
.         check for the Transaction Type
.
...       MATCH     "DB",TTYPE
...       GOTO      PYLOOP1 IF EQUAL
.
...       MATCH     "GS",TTYPE
...       GOTO      PYLOOP1 IF EQUAL
.
          MULT      SEQ,TPATAMTT
          ADD       TPATAMTT,TMPPAID
.                                                                     *I-198325
          IF        TRECTYPE = 6
            MULT      SEQ,PTDTGSTM
            ADD       PTDTGSTM,TMPPAID
          ENDIF
          GOTO      PYLOOP1
.
.         Other Financial Invoice
.
OTHDTR    MOVE      "out",OSTFILE
          MOVE      PVISITE,KEY6
          CALL      RDSITA1
.
.         Outpatient Invoice
.
          PACK      CFNAME,OSTFILE,FILDTRO1
          OPEN      OUTDTRO1,CFNAME
.
          CALL      RDSDTRO1
.
PYLOOP2   CALL      RDKDTRO1
          BRANCH    OVRCD OF ENDDTRO
.
          MATCH     OTINVNO,PINVNO
          GOTO      ENDDTRO IF NOT EQUAL
.
          BRANCH    OTRECTYP,PYLOOP2,PYCSH2,PYJRN2
          COMPARE   SIX,OTRECTYP
          GOTO      PYLOOP2 IF EQUAL      * Theatre item
.
.         If the Transaction date is used for journals, then treat as cash
.         otherwise use the last modification date as the journal date for
.         determining if the journal was done before or after cutoff date
.
PYJRN2    BRANCH    CJRNREP,PYCSH2
.
          MATCH     OTDTEFFD,FROMDATE
          GOTO      PYLOOP2 IF LESS
          MULT      SEQ,OTPATAMT
          ADD       OTPATAMT,TMPPAID
.                                                                     *I-198325
          MULT      SEQ,OTDTGSTM
          ADD       OTDTGSTM,TMPPAID
          GOTO      PYLOOP2
.
.         Use the transaction date to determine if this transaction was done
.         before or after the cutoff date.
.
PYCSH2    MATCH     OTDDATE,FROMDATE
          GOTO      PYLOOP2 IF LESS
          MULT      SEQ,OTPATAMT
          ADD       OTPATAMT,TMPPAID
          GOTO      PYLOOP2
.
ENDDTRO   CLOSE     OUTDTRO1
          GOTO      PYEND1
.
.         A & E  Invoice
.
AAEDTR    CALL      RDSDTRE1
.
PYLOOP3   CALL      RDKDTRE1
          BRANCH    OVRCD OF PYEND1
.
          MATCH     ATINVNO,PINVNO
          GOTO      PYEND1 IF NOT EQUAL
.
          BRANCH    ATRECTYP,PYLOOP3,PYCSH3,PYJRN3,PYCSH3,PYLOOP3
.
.         If the Transaction date is used for journals, then treat as cash
.         otherwise use the last modification date as the journal date for
.         determining if the journal was done before or after cutoff date
.
PYJRN3    BRANCH    CJRNREP,PYCSH3
.
          MATCH     ATDTEFFD,FROMDATE
          GOTO      PYLOOP3 IF LESS
          MULT      SEQ,ATPATAMT
          ADD       ATPATAMT,TMPPAID
.                                                                     *I-198325
          MULT      SEQ,ATDTGSTM
          ADD       ATDTGSTM,TMPPAID
          GOTO      PYLOOP3
.
.         Use the transaction date to determine if this transaction was done
.         before or after the cutoff date.
.
PYCSH3    MATCH     ATDDATE,FROMDATE
          GOTO      PYLOOP3 IF LESS
          MULT      SEQ,ATPATAMT
          ADD       ATPATAMT,TMPPAID
          GOTO      PYLOOP3
.
.         Add the total payments to the invoice total to get the outstanding amt
.
PYEND1    CALL      CRDN0000              * Check credit note
          ADD       FORM82,PINVAMT
          SUB       TMPPAID,PINVAMT
.
          COMPARE   ZERO00 TO PINVAMT
          GOTO      URLOOP IF EQUAL
.
          MATCH     PINVADM TO SAVADMNO
          GOTO      URLOOP3 IF EQUAL
.
.         read the admission and master files
.
          MOVE      PINVUR TO KEY8
          CALL      RDMAST1
          BRANCH    OVRCD TO NOMAST
.
          BRANCH    PINVSYS OF AAEDET,OUTDET,INPDET,OTHDET,URLOOP3
.
.         Inpatient details
.
INPDET    MOVE      PINVADM TO KEY8
          CALL      RDMISS1
          BRANCH    OVRCD TO INVADM00
.
.         If Health Fund from admission is blank, use HF from master record
.
          MATCH     SP70,AFUNDH
          IF        @EQUAL
            MOVE      PFUNDH,AFUNDH
            MOVE      PFNDTB,AFNDTB
            MOVE      PFNDMM,AFNDMM
          ENDIF
.
          COMPARE   ONE TO ASTAT
          GOTO      NOPREAD IF EQUAL
          COMPARE   FIVE TO ASTAT
          GOTO      NOCANAD IF EQUAL
.
          MOVE      AADMNO TO KEY8
          CALL      RDDSCH1
          GOTO      URLOOP3
.
.         Outpatient details
.
OUTDET    MOVE      PINVADM TO OBAOUTNO
          MOVE      OBAOUTNO,KEY8
          CALL      RDVISA1
          BRANCH    OVRCD OF INVADM00
.
          MOVE      "out",OSTFILE
          MOVE      PVISITE,KEY6
          CALL      RDSITA1
.
          PACK      CFNAME WITH OSTFILE,FILBB1A1
          CALL      OPOTBB11
.
          MOVE      OBAOUTNO,KEY8
          CALL      RDBOKB1
          CLOSE     OUTBB1A1
          BRANCH    OVRCD TO INVADM00
.
          MOVE      OBACLASS,ATYPE
          MOVE      OBACOMP,ACLAIM
          MOVE      PINVUR,AURNO
          MOVE      PINVADM,AADMNO
          MOVE      PFUNDH,AFUNDH
          MOVE      PFNDTB,AFNDTB
          MOVE      PFNDMM,AFNDMM
          GOTO      URLOOP3
.
.         A & E  details
.
AAEDET    MOVE      PINVADM TO ADANUMB
          MOVE      ADANUMB,KEY8
          CALL      RDVISA1
          BRANCH    OVRCD,INVADM00
.
          MATCH     " 9",PTVITYPE
          GOTO      AAEDET20 IF EQUAL
.
          CALL      RDEMVIS1
          BRANCH    OVRCD,INVADM00
.
          MOVE      EMVICLAS,ATYPE
          MOVE      EMVICOMP,ACLAIM
          GOTO      AAEDET30
.
AAEDET20  CALL      RDPMEVT1
          BRANCH    OVRCD,INVADM00
.
          MOVE      PMEVADMT,ATYPE
          MOVE      PMEVCCOD,ACLAIM
.
AAEDET30  MOVE      PINVUR,AURNO
          MOVE      PINVADM,AADMNO
          MOVE      PFUNDH,AFUNDH
          MOVE      PFNDTB,AFNDTB
          MOVE      PFNDMM,AFNDMM
          GOTO      URLOOP3
.
.         Other Financial details
.
OTHDET    MOVE      SP3,ATYPE
          MOVE      PINVCLM,ACLAIM
          MOVE      PINVUR,AURNO
          MOVE      PINVADM,AADMNO
          MOVE      PFUNDH,AFUNDH
          MOVE      PFNDTB,AFNDTB
          MOVE      PFNDMM,AFNDMM
.
.         check the range of Health Fund
.
URLOOP3   MATCH     STHF,AFUNDH
          GOTO      URLOOP IF LESS
.
          MATCH     AFUNDH,EDHF
          GOTO      URLOOP IF LESS
.
          MOVE      ZERO,NBRDAYS
          DAYSEP    PINVDTE,FROMDATE,NBRDAYS
.
          COMPARE   ZERO,NBRDAYS
          GOTO      URLOOP IF LESS
.
          BRANCH    OPTION1 TO URFULL,URCUR,UR30,UR60,UR90       
.
.         check the no. of days for full report
.
URFULL
.
.         0-30 days
.
          COMPARE   "31",NBRDAYS
          GOTO      URF30 IF NOT LESS
.
          ADD       PINVAMT TO TOTCURR
          CALL      WRTEFUL
          GOTO      URLOOP
.
.         31-60 days
.
URF30     COMPARE   "61",NBRDAYS
          GOTO      URF60 IF NOT LESS
.
          ADD       PINVAMT TO TOT30
          CALL      WRTEFUL
          GOTO      URLOOP
.
.         61-90 days
.
URF60     COMPARE   "91",NBRDAYS
          GOTO      URF90A IF NOT LESS
.
          ADD       PINVAMT TO TOT60
          CALL      WRTEFUL
          GOTO      URLOOP
.
.         Over 90 days
.
URF90A    ADD       PINVAMT TO TOT90
          CALL      WRTEFUL
          GOTO      URLOOP
+
.         check for current report
.
URCUR     COMPARE   "31",NBRDAYS
          CALL      WRTEDAY IF LESS    
          GOTO      URLOOP
.
.         Check for 30 day  report
.
UR30      COMPARE   "31",NBRDAYS
          GOTO      URLOOP IF LESS
.
          COMPARE   "61",NBRDAYS
          CALL      WRTEDAY IF LESS    
          GOTO      URLOOP
.
.         Check for 60 day report
.
UR60      COMPARE   "61",NBRDAYS
          GOTO      URLOOP IF LESS
.
          COMPARE   "91",NBRDAYS
          CALL      WRTEDAY IF LESS
          GOTO      URLOOP
.
.         check for 90 day report
.
UR90      COMPARE   "91",NBRDAYS
          CALL      WRTEDAY IF NOT LESS
          GOTO      URLOOP
.         CALL      WRTEDAY IF LESS
.         GOTO      URLOOP
.
.         Error Messages
.
INVADM00  DISPLAY   *P1:20,*EL,*B,*V2LON,*P10:20,"** Invoice ",PINVNO:
                    " has invalid Admission Number ",PINVADM," **",*W;
.
          COMPARE   "54" TO CLNO
          CALL      HEAD IF NOT LESS
          PRINT     *1,"** Invoice ",PINVNO," has invalid Admission Number ":
                       PINVADM,".  Contact I.B.A. Immediately **",*N
          ADD       TWO,CLNO
          MOVE      ZEROVISN,SAVADMNO
          GOTO      URLOOP
.
NOPREAD   DISPLAY   *P1:20,*EL,*B,*V2LON,*P8:20,"** Invoice ",PINVNO:
                    " allocated to Pre-admission Number ",PINVADM," **";
.
          COMPARE   "54" TO CLNO
          CALL      HEAD IF NOT LESS
          PRINT     *1,"** Invoice ",PINVNO," allocated on cancelled ":
                    " Admission Number ",PINVADM,*N
          ADD       TWO,CLNO
          MOVE      ZEROVISN,SAVADMNO
          GOTO      URLOOP
.
NOCANAD   DISPLAY   *P1:20,*EL,*B,*V2LON,*P9:20,"** Invoice ",PINVNO:
                    " has cancelled Admission Number ",PINVADM," **";
.
          COMPARE   "54" TO CLNO
          CALL      HEAD IF NOT LESS
          PRINT     *1,"** Invoice ",PINVNO," on cancelled Admission Number ":
                    PINVADM,*N
          ADD       TWO,CLNO
          MOVE      ZEROVISN,SAVADMNO
          GOTO      URLOOP
.
NOMAST    DISPLAY   *P1:20,*EL,*B,*V2LON,*P11:20,"** Invoice ",PINVNO:
                    " missing master file record ",AURNO," **";
.
          COMPARE   "54" TO CLNO
          CALL      HEAD IF NOT LESS
          PRINT     *1,"** Invoice ",PINVNO," missing master file record ":
                    AURNO,".  Contact I.B.A. Immediately **",*N
          ADD       TWO,CLNO
          MOVE      ZEROVISN,SAVADMNO
          GOTO      URLOOP
.
.         write to temporary file for the full report
.
WRTEFUL   ADD       ONE TO UNIQUE                                   
.
          MOVE      PINVPATA,STOTPD
          ADD       PINVHFDA,STOTPD
          ADD       PINVOTHA,STOTPD
.
          MOVE      TOTCURR,OWING
          ADD       TOT30,OWING
          ADD       TOT60,OWING
          ADD       TOT90,OWING
.
.         MULT      SEQ,STOTPD
.         MULT      SEQ,PINVJOUR
.
.         Calculate balance outstanding
.
.         MOVE      SAVAMT,OWING
.         SUB       STOTPD,OWING
.         SUB       PINVJOUR,OWING
.
WRTFT     MOVE      AADMNO,XDAADMNO
          MOVE      UNIQUE,DUNIQUE
          MOVE      OWING,FORM12P2
          MOVE      FORM12P2,XOWING
          MOVE      TOTCURR,FORM12P2
          MOVE      FORM12P2,XTOTCURR
          MOVE      TOT30,FORM12P2
          MOVE      FORM12P2,XTOT30
          MOVE      TOT60,FORM12P2
          MOVE      FORM12P2,XTOT60
          MOVE      TOT90,FORM12P2
          MOVE      FORM12P2,XTOT90
          MOVE      PINVREB,TPINVREB
          MOVE      PINVHFDA,TPINVHFD
.
          WRITE     TEMPSEQ,SEQ;PINVCLM,AFUNDH,PSNAME,PGNAME,XDAADMNO,DUNIQUE:
                                AURNO,PINVNO,TPINVREB,TPINVHFD,XOWING,XTOTCURR:
                                XTOT30,XTOT60,XTOT90,PADDING
.
          MOVE      ZERO TO TOTCURR
          MOVE      ZERO TO TOT30
          MOVE      ZERO TO TOT60
          MOVE      ZERO TO TOT90
          MOVE      ZERO TO OWING
          RETURN
+
.         write to temporary file for not a full report
.
WRTEDAY   ADD       ONE TO UNIQUE
.
          MOVE      PINVPATA,STOTPD
          ADD       PINVHFDA,STOTPD
          ADD       PINVOTHA,STOTPD
.
          MOVE      PINVAMT,OWING
.
.         MULT      SEQ,STOTPD
.         MULT      SEQ,PINVJOUR
.
.         Calculate balance outstanding
.
.         MOVE      SAVAMT,OWING
.         SUB       STOTPD,OWING
.         SUB       PINVJOUR,OWING
.
          PACK      VSMTCMNT,SP70,SP70
          CALL      VSCMNT00 
.
          MOVE      SP3,REMLSTL
          MOVE      SP8,LSTLDAT
          MOVE      AADMNO,KEY8
          CALL      RDREMD1
          BRANCH    OVRCD OF WRTTDY
.
          MATCH     SP8,REMLDTE
          GOTO      WRTTDY IF EQUAL
.
          UNPACK    REMLDTE,CCENT,CYEAR,CMON,CDAY
          MOVE      REMLDTE,LSTLDAT
          REP       " 0",LSTLDAT
.
WRTTDY    MOVE      AADMNO,XDAADMNO
          MOVE      UNIQUE,DUNIQUE
.
          MOVE      OWING,FORM12P2
          MOVE      FORM12P2,XOWING
          MOVE      SP70,DIM15
          MOVE      PINVAMT,DIM15
          MOVE      PINVREB,TPINVREB
          MOVE      PINVHFDA,TPINVHFD
          UNPACK    VSMTCMNT,DIM70
.
          WRITE     TEMPSEQ,SEQ;PINVCLM,AFUNDH,PSNAME,PGNAME,XDAADMNO,DUNIQUE:
                                AURNO,PINVNO,TPINVREB,TPINVHFD,XOWING,DIM15:
                                AFNDTB,AFNDMM,DIM70,REMLSTL,LSTLDAT,PTITL
          RETURN
.----------------------------------------------------------------------
. Retrieve the 1st comment text line of the latest active visit
. comment details
.----------------------------------------------------------------------
VSCMNT00  MOVE      AADMNO,KEY8 
          PACK      KEY17,AADMNO,VSCMTTYP,Z70      * Key for start of comment
          CALL      RSVSMDT1
VSCMNT20  CALL      RPVSMDT1
          BRANCH    OVRCD,VSCMNT99
.
          MATCH     KEY8,VSMDVISN
          GOTO      VSCMNT99 IF NOT EQUAL
.
          MATCH     VSCMTTYP,VSMDTYPE
          GOTO      VSCMNT99 IF NOT EQUAL
.
          MATCH     "1",VSMDDELT          * is comment deleted
          GOTO      VSCMNT20 IF EQUAL
.
. Retrieve the comment text line details
.
          PACK      KEY20,VSMDVISN,VSMDTYPE,VSMDNOTE,SP70
          CALL      RSVSMTX1
          CALL      RKVSMTX1
          BRANCH    OVRCD,VSCMNT99
.
          MATCH     VSMDVISN,VSMTVISN
          GOTO      VSCMNT99 IF NOT EQUAL
.
          MATCH     VSMDTYPE,VSMTTYPE
          GOTO      VSCMNT99 IF NOT EQUAL
.
          MATCH     VSMDNOTE,VSMTNOTE
          GOTO      VSCMNT99 IF NOT EQUAL
.
VSCMNT99  RETURN
+
.         SORT THE DATA FILE
.
SORTD     MOVE      ZERO,STATUS
          DISPLAY   *P1:24,*EL,*V2LON,*P30:24:
                    "***  Processing Statistics  ***"
          WEOF      TEMPSEQ,SEQ
          CLOSE     TEMPSEQ
.
          CLOSE     TEMPFILE
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMET,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMET,CMDLINE
          APPEND    " 259 ",CMDLINE
          APPEND    "U1-3,4-9,10-29,30-54,55-62,63-70",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          MOVE      ZERO TO OPCND
.
          CALL      DREC0000                * Delete records
.
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "isbload ",CMDLINE
          APPEND    FNAMET,CMDLINE
          APPEND    ".txt ",CMDLINE
          APPEND    FNAMET,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
          MOVE      ONE,STATUS
          MOVE      ZERO TO OPCND
          BRANCH    STATUS OF SORTFI
          MOVE      ONE,OPCND
          DISPLAY   *P1:24,*EL,*B,*V2LON,*P22:24:
                    "***  The Statistics file is EMPTY  ***",*W;
          GOTO      ENDP
.
NOOPEN    MOVE      ONE,OPCND
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Temporary Statistics file ",FNAMET," not found **   ";
          CALL      HITENTER
          RETURN
+
.
DREC0000  OPEN      TEMPFILE,FNAMET
DREC1000  RESET     KEY70
          READ      TEMPFILE,SP70;;
          IF        OPTION1=1
            READKS    TEMPFILE;PINVCLM,AFUNDH,PSNAME,PGNAME,XDAADMNO,DUNIQUE:
                               AURNO,PINVNO,PINVREB,PINVHFDA,XOWING,XTOTCURR:
                               XTOT30,XTOT60,XTOT90,PADDING
          ELSE
            READKS    TEMPFILE;PINVCLM,AFUNDH,PSNAME,PGNAME,XDAADMNO,DUNIQUE:
                            AURNO,PINVNO,PINVREB,PINVHFDA,XOWING,DIM15,AFNDTB:
                            AFNDMM,DIM70,REMLSTL,LSTLDAT,PTITL
          ENDIF
          GOTO      DREC9000 IF OVER
          UNPACK    DIM70,VSMTCMNT
          MOVE      XOWING,OWING
          MOVE      XTOTCURR,TOTCURR
          MOVE      XTOT30,TOT30
          MOVE      XTOT60,TOT60
          MOVE      XTOT90,TOT90
          MOVE      ZERO,PINVAMT
          MOVE      DIM15,PINVAMT
.
          PACK      KEY70,PINVCLM,AFUNDH,PSNAME,PGNAME,XDAADMNO,DUNIQUE
          RESET     KEY70
          DELETE    TEMPFILE,KEY70
          GOTO      DREC1000
.
DREC9000  CLOSE     TEMPFILE
DREC9999  RETURN
+
.
SORTFI    CLOSE     TEMPFILE
          TRAP      NOOPEN IF IO
          OPEN      TEMPFILE,FNAMET
          TRAPCLR   IO
.
          COMPARE   ONE TO OPCND
          GOTO      ENDP IF EQUAL
.
          MOVE      ZERO TO TTOTREB
          MOVE      ZERO TO TTOTHF
          MOVE      ZERO TO TTOWING
          MOVE      ZERO TO TTOTCURR
          MOVE      ZERO TO TTOT30
          MOVE      ZERO TO TTOT60
          MOVE      ZERO TO TTOT90
          MOVE      ZERO TO CTCURR
          MOVE      ZERO TO CT30
          MOVE      ZERO TO CT60
          MOVE      ZERO TO CT90
          MOVE      ZERO TO HTCURR
          MOVE      ZERO TO HT30
          MOVE      ZERO TO HT60
          MOVE      ZERO TO HT90
.
          MOVE      ZEROVISN,SAVADMNO
          MOVE      SP30,HFNAME
          MOVE      SP3,SAVCLM
          MOVE      SP10,SAVFUND   
          MOVE      SIXTY,CLNO
          MOVE      ZERO,BEGIN
.
          DISPLAY   *P1:24,*EL,*P20:24,"Admission Number : ",*P48:24,"Name : ";
.
          READ      TEMPFILE,SP70;;
.    
          BRANCH    OPTION1 TO FULLREP,DAYREP,DAYREP,DAYREP,DAYREP
.
.         Looping the temporary file for full report
.
FULLREP   READKS    TEMPFILE;PINVCLM,AFUNDH,PSNAME,PGNAME,XDAADMNO,DUNIQUE:
                             AURNO,PINVNO,TPINVREB,TPINVHFD,XOWING,XTOTCURR:
                             XTOT30,XTOT60,XTOT90,PADDING
          GOTO      DAYEND IF OVER
          MOVE      XDAADMNO,AADMNO
          MOVE      DUNIQUE,UNIQUE
.
          MOVE      XOWING,FORM12P2
          MOVE      FORM12P2,OWING
          MOVE      XTOTCURR,FORM12P2
          MOVE      FORM12P2,TOTCURR
          MOVE      XTOT30,FORM12P2
          MOVE      FORM12P2,TOT30
          MOVE      XTOT60,FORM12P2
          MOVE      FORM12P2,TOT60
          MOVE      XTOT90,FORM12P2
          MOVE      FORM12P2,TOT90
          MOVE      TPINVREB,PINVREB
          MOVE      TPINVHFD,PINVHFDA
.
          MOVE      PGNAME TO ANS
          PACK      PNAME WITH ANS,DOT,PSNAME
          RESET     PNAME
.
          DISPLAY   *P39:24,*V2LON,AADMNO,*P55:24,PNAME;
          CALL      PRINTFUL
          GOTO      FULLREP
.
.         Looping the temporary file for not a full report
.
DAYREP    READKS    TEMPFILE;PINVCLM,AFUNDH,PSNAME,PGNAME,XDAADMNO,DUNIQUE:
                            AURNO,PINVNO,TPINVREB,TPINVHFD,XOWING,DIM15,AFNDTB:
                            AFNDMM,DIM70,REMLSTL,LSTLDAT,PTITL
          GOTO      DAYEND IF OVER
          UNPACK    DIM70,VSMTCMNT
          MOVE      XDAADMNO,AADMNO
          MOVE      DUNIQUE,UNIQUE
          MOVE      XOWING,FORM12P2
          MOVE      FORM12P2,OWING
          MOVE      DIM15,FORM12P2
          MOVE      FORM12P2,PINVAMT
          MOVE      TPINVREB,PINVREB
          MOVE      TPINVHFD,PINVHFDA
.
          DISPLAY   *P39:24,*V2LON,AADMNO,*P55:24,PSNAME;
.
          COMPARE   ZERO,BEGIN
          GOTO      DAYR3 IF EQUAL
          MOVE      ZERO,HFFLAG
          MOVE      ZERO,CLFLAG
.
          PACK      CLMFUND WITH PINVCLM,AFUNDH
          MATCH     CLMFUND TO SAVFUND
          GOTO      DAYR1 IF EQUAL
.
.         Print the total of health fund
.
          MOVE      ONE,HFFLAG
          CALL      HFTOT
.
DAYR1     MATCH     PINVCLM TO SAVCLM
          GOTO      DAYR2 IF EQUAL
.
.         Print the total of claim
.
          MOVE      ONE,CLFLAG
          CALL      CLMTOT
.
.         check if the Claim Code and Health Fund heading need to be printed
.
DAYR2     COMPARE   ONE,CLFLAG
          CALL      CLTITLE IF EQUAL
.
          COMPARE   ONE,HFFLAG
          CALL      HFTITLE IF EQUAL
.
DAYR3     PACK      DIM60 WITH SP30,SP30
          MOVE      VSMTCMNT TO DIM50
.
          COMPARE   "54" TO CLNO
          CALL      HEAD IF NOT LESS
.
          BRANCH    TOTONLY OF DAYR5
.
          MATCH     AADMNO,SAVADMNO
          GOTO      DAYR4 IF EQUAL
.
          UNPACK    LSTLDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          REP       " 0",CPCDATE
.
          PACK      AFNDMM,AFNDMM,SP70
          UNPACK    AFNDMM,DIM10,DIM9A
          MATCH     SP70,DIM9A
          IF        !@EQUAL
            MOVE      AFNDMM,DIM9A
            PACK      DIM10,DIM9A,STAR
          ENDIF
.
          PRINT      *1,AADMNO,*10,PSNAME,*35,AFNDTB,*44,DIM10,*65,REMLSTL:
                    *69,CPCDATE,*78,DIM50:
                    *N,*9,PTITL," ",PGNAME;
          ADD       ONE,CLNO
.
DAYR4     MOVE      PINVREB,FORM82
          PRINT     *71,PINVNO,*84,FORM82;
          MOVE      PINVHFDA,FORM82
          PRINT     *96,FORM82;
.
          MOVE      PINVAMT,FORM82
          PRINT     *107,OWING,*119,FORM82
          ADD       ONE,CLNO
.
DAYR5     MOVE      PINVCLM TO SAVCLM
          PACK      SAVFUND WITH PINVCLM,AFUNDH
          MOVE      AADMNO,SAVADMNO
.
.         Overall Total
.
          ADD       PINVREB,TTOTREB
          ADD       PINVHFDA,TTOTHF
          ADD       OWING,TTOWING
          ADD       PINVAMT,TTOTCURR
.
.         Total for each Claim
.
          ADD       PINVREB,CTREB
          ADD       PINVHFDA,CTHF
          ADD       OWING,CTOWING
          ADD       PINVAMT,CTCURR
.
.         Total for each Health Fund
.
          ADD       PINVREB,HTREB
          ADD       PINVHFDA,HTHF
          ADD       OWING,HTOWING
          ADD       PINVAMT,HTCURR
          GOTO      DAYREP
.
.         Overall Total
.
DAYEND    COMPARE   "48" TO CLNO
          CALL      HEAD IF NOT LESS
.
          CALL      HFTOT
          CALL      CLMTOT
.
          BRANCH    OPTION1 OF DAYEND1
.
          PRINT     *N,*83,"-----------",*95,"-----------":
                      *107,"-----------",*119,"-----------":
                    *N,*13,"OVERALL TOTAL :",*82,"$",*83,TTOTREB,*95,TTOTHF:
                      *107,TTOWING,*119,TTOTCURR:
                    *N,*83,"-----------",*95,"-----------":
                      *107,"-----------",*119,"-----------"
          GOTO      ENDP
.
DAYEND1   PRINT     *N,*45,"-----------",*57,"------------",*70,"------------":
                       *84,"-----------",*96,"------------",*109,"-----------":
                      *121,"------------":
                    *N,*13,"OVERALL TOTAL :",*44,"$",*45,TTOTREB,*58,TTOTHF:
                    *71,TTOWING,*84,TTOTCURR,*97,TTOT30,*109,TTOT60,*122,TTOT90:
                    *N,*45,"-----------",*57,"------------",*70,"------------":
                       *84,"-----------",*96,"------------",*109,"-----------":
                      *121,"------------"
          GOTO      ENDP
+
.         OVER Condition found..print last line of -- and display message
.
ENDP      COMPARE   ZERO TO CPAGENO
          GOTO      END10 IF NOT EQUAL
.
          CALL      HEAD
          PRINT     *N,"***  End of Report  ***"
          DISPLAY   *P10:24,*EL,*V2LON:
                    "***  No Report Generated  ***",*W2;
          GOTO      DELFILE
.
END10     PRINT     *N,"***  End of Report  ***"
.
          DISPLAY   *P1:24,*EL,*V2LON,*P22:24:
                    "***  Report Generation Completed  ***"
.
DELFILE   CALL      KILLIDZ
          GOTO      START
+
.         Print Full Report
.
PRINTFUL  COMPARE   ZERO,BEGIN
          GOTO      PRINTF3 IF EQUAL
          MOVE      ZERO,HFFLAG
          MOVE      ZERO,CLFLAG
.
          PACK      CLMFUND WITH PINVCLM,AFUNDH
          MATCH     CLMFUND TO SAVFUND
          GOTO      PRINTF1 IF EQUAL
.
.         print the total of Health Fund
.
          MOVE      ONE,HFFLAG
          CALL      HFTOT
.
PRINTF1   MATCH     PINVCLM TO SAVCLM
          GOTO      PRINTF2 IF EQUAL
          MOVE      ONE,CLFLAG              * print the total of Claim Code
          CALL      CLMTOT
.
.         check if the Claim Code and Health Fund heading need to be printed
.
PRINTF2   COMPARE   ONE,CLFLAG
          CALL      CLTITLE IF EQUAL
.
          COMPARE   ONE,HFFLAG
          CALL      HFTITLE IF EQUAL
.
PRINTF3   COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
.
          BRANCH    TOTONLY OF PRINTF5
.
          MATCH     AADMNO,SAVADMNO
          GOTO      PRINTF4 IF EQUAL
          PRINT     *1,AADMNO,*10,PNAME;
.
PRINTF4   MOVE      PINVREB,FORM82
          PRINT     *33,PINVNO,*46,FORM82,*59,PINVHFDA,*71,OWING:
                    *85,TOTCURR,*98,TOT30,*110,TOT60,*123,TOT90
          ADD       ONE,CLNO
.
.         Overall Total
.
PRINTF5   ADD       PINVREB TO TTOTREB
          ADD       PINVHFDA TO TTOTHF
          ADD       OWING TO TTOWING
          ADD       TOTCURR TO TTOTCURR
          ADD       TOT30 TO TTOT30
          ADD       TOT60 TO TTOT60
          ADD       TOT90 TO TTOT90
.
.         Total of each Claim Code
.
          ADD       PINVREB TO CTREB
          ADD       PINVHFDA TO CTHF
          ADD       OWING TO CTOWING
          ADD       TOTCURR TO CTCURR
          ADD       TOT30 TO CT30
          ADD       TOT60 TO CT60
          ADD       TOT90 TO CT90
.
.         Total of each Health Fund
.
          ADD       PINVREB TO HTREB
          ADD       PINVHFDA TO HTHF
          ADD       OWING TO HTOWING
          ADD       TOTCURR TO HTCURR
          ADD       TOT30 TO HT30
          ADD       TOT60 TO HT60
          ADD       TOT90 TO HT90
.
          MOVE      PINVCLM TO SAVCLM
          PACK      SAVFUND WITH PINVCLM,AFUNDH
          MOVE      AADMNO,SAVADMNO
          RETURN
+
.         HEADINGS FOR OUTPUT
.
HEAD      MOVE      ONE,CNOUNDLN
          CALL      IBACLOCK
          CALL      HEAD132
.
          IF        IBCNMHOS =1
            PRINT     *40,"Hospital : ",PTHSHOSP,"    ",PTHSNAME
            MOVE      FOUR,CLNO
          ELSE
            MOVE      THREE,CLNO
          ENDIF
.
          PRINT     *N,*40,"PATIENT DEBTORS - HEALTH FUND AGED TRIAL BALANCE":
                           "  (Surname Sequence)";
.
          BRANCH    HEADFLG OF HEAD1
.
          PRINT     *N,*40,"Starting Surname : ",FRSURN,*88,DSYST:
                    *N,*40,"Ending Surname   : ",TOSURN,*88,DOUTXTRA:
                    *N,*40,"Claim Range      : ",*+,FRCLM," to ",TOCLM,*-:
                    *N,*40,"Health Fund Range: ",*+,FRHF," to ",TOHF;
          ADD       FOUR,CLNO
          MOVE      ONE,HEADFLG
.
HEAD1     UNPACK    FROMDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          REP       " 0",CPCDATE
.
          COMPARE   ONE,OPTION1
          GOTO      PRTCONTE IF NOT EQUAL
.
          PRINT     *N,*40,"AS AT : ",CPCDATE:
                    *N,*N,*N,*45,"-------Fund Claim-------",*75,"Balance":
                    *N,"Adm.No",*10,"Patient Name",*33,"Inv. No.":
                    *51,"Claim",*65,"Paid",*77,"Owing",*88,"Current":
                    *101,"30 Days",*113,"60 Days",*125,"90+ Days":
                    *N,"--------",*10,"----------------------",*33,"--------":
                    *51,"-----",*65,"----",*77,"-----",*88,"-------":
                    *101,"-------",*113,"-------",*125,"--------"
          ADD       SIX,CLNO
          GOTO      PRTCONT1
.
.         Heading for not a full report
.
PRTCONTE  PRINT     *N,*N,*40,"DEBTORS OUTSTANDING FOR ",DIM9:
                    *N,*40,"AS AT : ",CPCDATE
.
          PRINT     *N,*N,"Adm.",*35,"H.F.",*44,"H.F.",*56,"Last Letter":
                      *83,"------Fund Claim-------",*111,"Balance",*121,DIM9:
                    *N,"Number",*10,"Patient Name",*35,"Table",*44,"Membership":
                    *56,"Code sent",*71,"Inv. No.",*89,"Claim",*102,"Paid":
                    *113,"Owing",*119,"Outstanding":
                    *N,"--------",*10,"-----------------------",*35,"------":
                    *44,"----------",*56,"-----------",*71,"--------":
                    *89,"-----",*102,"----",*113,"-----",*119,"-----------"
          ADD       EIGHT,CLNO
.
PRTCONT1  COMPARE   ZERO,BEGIN
          RETURN    IF NOT EQUAL
          CALL      PRCLM
          CALL      PRHF
.
PRTBEG    MOVE      ONE,BEGIN
          RETURN
+
.         Sub-heading for Claim Code
.
CLTITLE   COMPARE   ONE,SEPCLM
          GOTO      PRCLM IF NOT EQUAL
          MOVE      SIXTY,CLNO
          MOVE      ONE,HDCLM
.
PRCLM     COMPARE   "50",CLNO
          CALL      HEAD IF NOT LESS
          MOVE      SP30,TDESC
          MOVE      SP30,CLDESC
.
          MATCH     SP3,PINVCLM
          GOTO      NOCLM1 IF EQUAL
.
          PACK      KEY5 WITH CODECL,PINVCLM
          CALL      RDCODE1
          COMPARE   ZERO,OVRCD
          GOTO      CLT10 IF EQUAL
.
          CLEAR     TDESC
          APPEND    "UNKNOWN CLAIM CODE  ",TDESC
          APPEND    AFUNDH,TDESC
          APPEND    SP30,TDESC
          RESET     TDESC
          GOTO      CLT10
.
NOCLM1    MOVE      "NO CLAIM CODE   ",TDESC
.
CLT10     PRINT     *1,"Claim Type : ",PINVCLM," ",TDESC
          ADD       ONE,CLNO
.
          MOVE      TDESC,CLDESC
          MOVE      PINVCLM,SAVCLM
          RETURN
+
.         Subroutine to print the claim totals for each Claim Type
.
CLMTOT    COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
.
          BRANCH    OPTION1 TO CLMTOT0
.
          PRINT     *83,"-----------",*95,"-----------":
                    *107,"-----------",*119,"-----------":
                    *N,*13,"CLAIM TOTAL (",CLDESC,") :",*82,"$",*83,CTREB:
                    *95,CTHF,*107,CTOWING,*119,CTCURR:
                    *N,*83,"-----------",*95,"-----------":
                    *107,"-----------",*119,"-----------",*N
          ADD       FOUR,CLNO
          GOTO      ZERCLM
.
.         for Full report
.
CLMTOT0   PRINT     *N,*45,"-----------",*57,"------------",*70,"------------":
                       *84,"-----------",*96,"------------",*109,"-----------":
                      *121,"------------":
                    *N,*8,"CLAIM TOTAL (",CLDESC,") :",*44,"$",*45,CTREB:
                   *58,CTHF,*71,CTOWING,*84,CTCURR,*97,CT30,*109,CT60,*122,CT90:
                    *N,*45,"-----------",*57,"------------",*70,"------------":
                       *84,"-----------",*96,"------------",*109,"-----------":
                      *121,"------------",*N
          ADD       FIVE,CLNO
.
ZERCLM    MOVE      ZERO TO CTREB
          MOVE      ZERO TO CTHF
          MOVE      ZERO TO CTOWING
          MOVE      ZERO TO CTCURR
          MOVE      ZERO TO CT30
          MOVE      ZERO TO CT60
          MOVE      ZERO TO CT90
          MOVE      PINVCLM,SAVCLM
          RETURN
+
.         print Health Fund Total
.
HFTOT     COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
.
          MOVE      HFNAME,DIM30
.
          BRANCH    OPTION1 TO HFTOT0
.
          PRINT     *N,*83,"-----------",*95,"-----------":
                      *107,"-----------",*119,"-----------":
                    *N,*13,"TOTAL (",DIM30,") :",*82,"$",*83,HTREB:
                       *95,HTHF,*107,HTOWING,*119,HTCURR:
                    *N,*83,"-----------",*95,"-----------":
                      *107,"-----------",*119,"-----------",*N
          GOTO      ZERHF
.
.         print the Health Fund Total
.
HFTOT0    PRINT     *N,*45,"-----------",*57,"------------",*70,"------------":
                       *84,"-----------",*96,"------------",*109,"-----------":
                      *121,"------------":
                    *N,*5,"TOTAL (",DIM30,"):",*44,"$",*45,HTREB,*58,HTHF:
                       *71,HTOWING,*84,HTCURR,*97,HT30,*109,HT60,*122,HT90:
                    *N,*45,"-----------",*57,"------------",*70,"------------":
                       *84,"-----------",*96,"------------",*109,"-----------":
                      *121,"------------",*N
.
ZERHF     MOVE      ZERO TO HTREB
          MOVE      ZERO TO HTHF
          MOVE      ZERO TO HTOWING
          MOVE      ZERO TO HTCURR
          MOVE      ZERO TO HT30
          MOVE      ZERO TO HT60
          MOVE      ZERO TO HT90
          ADD       FIVE,CLNO
          PACK      SAVFUND WITH PINVCLM,AFUNDH
          RETURN
+
.         Subroutine to print the Health Fund subtitle
.
HFTITLE   COMPARE   ONE,SEPFUND
          GOTO      PRHF IF NOT EQUAL
.
          BRANCH    HDCLM OF PRHF
          MOVE      SIXTY,CLNO
.
PRHF      MOVE      ZERO,HDCLM
          COMPARE   "50",CLNO
          CALL      HEAD IF NOT LESS
.
          MATCH     SP6,AFUNDH
          GOTO      NOFUND1 IF EQUAL
.
          PACK      KEY14 WITH AFUNDH,ZERO,ZERO,ZERO,ZERO,SP10
          CALL      RDFUND1
          COMPARE   ZERO,OVRCD
          GOTO      PRTSHF2 IF EQUAL
.
          CLEAR     HFNAME
          APPEND    "UNKNOWN HEALTH FUND - ",HFNAME
          APPEND    AFUNDH,HFNAME
          APPEND    SP30,HFNAME
          RESET     HFNAME
          GOTO      PRTSHF2
.
NOFUND1   MOVE      "NO HEALTH FUND                ",HFNAME
.
PRTSHF2   COMPARE   ONE,TOTONLY
          GOTO      ENDHFTTL IF EQUAL
.
          PRINT     *1,HFNAME," (",AFUNDH,")":
                    *N,"---------------------------------------"
          ADD       TWO,CLNO
.
ENDHFTTL  PACK      SAVFUND WITH PINVCLM,AFUNDH
          RETURN
+
.******************************************************************************
.*        CRDN0000 - Credit for credit note                                   *
.******************************************************************************
CRDN0000  MOVE      ZERO,FORM82
          PACK      KEY16,PINVNO,SP70
          CALL      RSPMCNO3
CRDN1000  CALL      RKPMCNO3
          BRANCH    OVRCD,CRDN9999
          MATCH     PMCNINVN,PINVNO         * Same invoice number?
          GOTO      CRDN9999 IF NOT EQUAL   * No
.
          MATCH     PMCNDATE,FROMDATE
          GOTO      CRDN1000 IF LESS
          ADD       PMCNAMNT,FORM82
          GOTO      CRDN1000
.
CRDN9999  RETURN
.
.
.         deleting a temp indexed file based on Port
.
KILLIDZ   PACK      FNAMET,FNAMET,SP70
          MATCH     SP70,FNAMET
          RETURN    IF EQUAL
.
          DISPLAY   *P1:24,*EL,*V2LON,*P25:24:
                    "***  Deleting Indexed File  ***"
.
          MOVE      ZERO,STATUS
          CLOSE     TEMPFILE
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMET,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PREP      TEMPSEQ,FNAMET
          CLOSE     TEMPSEQ,DELETE
          RETURN
+
.
ENDIT     CALL      KILLIDZ
          CHAIN     PGM
+
.
.==============================================================================
.
          INC       STD001IO
.
          INC       PATHSPKY
          INC       PATCODKY
          INC       PATFNDKY
          INC       PATSELFN
.
          INC       PMSCNOIO/INC
          INC       AAEDE1IO/INC
          INC       AAEDTRIO/INC
          INC       EMRVISIO/INC
          INC       IBAPASIO/INC
          INC       IBASECIO/INC
          INC       OUTBB1IO/INC
          INC       OUTDTRIO/INC
          INC       OUTSITIO/INC
          INC       VISMDTIO/INC
          INC       VISMTXIO/INC
          INC       PATCODIO/INC
          INC       PATDSCIO/INC
          INC       PATDTRIO/INC
          INC       PATFN1IO/INC
          INC       PATINVIO/INC     
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATREMIO/INC
          INC       PATVISIO/INC
          INC       PATHSPIO/INC
          INC       PMSEVTIO/INC
          INC       PMSVX1IO/INC
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC
+
.


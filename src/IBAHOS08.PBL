.******************************************************************************
.*              P R O G R A M  S U M M A R Y                                  *
.*              -------------  -------------                                  *
.*                                                                            *
.*     PROGRAM NAME:     IBAHOS08                                             *
.*                                                                            *
.*     FUNCTION:         HEALTH FUND CLAIMS REPORT                            *
.*                                                                            *
.*     DATE WRITTEN:     29/04/88                                             *
.*                                                                            *
.*     AUTHOR:           Karin Peak      (I.B.A)                              *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.*----------------------------------------------------------------------------*
.* V12.00.02  11/07/2025 J.Tan          TSK 0961623                           *
.*            Fixed printing overflow underline on report                     *
.* V12.00.01  19/05/2025 Nikitha B      TSK 0955096                           *
.*            Alphanumeric changes                                            *
.* V11.04.02  20.05.2024 Ebon Clements  TSK 0936282                            *
.*            Increased length of temp file PATWORK1 for AFNDMM                *
.* V11.04.01  05.02.2024 DA Sarkies     TSK 0936282                            *
.*            Added code to note if a Health Fund number exceeds 9             *
.* V10.04.01  15/06/2014 Jill Parkinson CAR 301639                             *
.*            Moved TFILENAM to INIT000 and extra call to DTMP000 on exit      *
.* V10.03.01  27/12/2012 Patrick Adair                              CAR 261630 *
.*                       Removed port number from temp file name               *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V9.03.01  03/06/2003    Dean Cramer  CAR  38200                     *
.*                  Fix where work file has wrong field lengths that makes    *
.*                  report omit data                                          *
.*        V9.02.02  24/04/2003    Dean Cramer  CAR  38200                     *
.*                  Fix print overlaps for web                                *
.*        V9.02.01  19/03/2003    Dean Cramer  CAR  36516                     *
.*                  Port from 6.10 and alter to run on 9.02                   *
.*        V6.09.02  08.Mar.2002   Glenn Saunders   Mayne Defect #17           *
.*                  Fix report to display the increased fields from v6.09.01  *
.*                  correctly.                                                *
.*----------------------------------------------------------------------------*
.*        V6.09.01  14/11/2001  Tonii SRF 13489                               *
.*                  Mods to accommodate new length of TPATAMTT,TPATAMTG,      *
.*                  TPATAMTH,TPATAMTI,TPATAMTP,TREBATE, all now FORM 8.2      *
.******************************************************************************
.*        V6.07.B03 06/04/2000  Steve Downing                                 *
.*                  Fixed zeroes in From/To dates                             *
.*        V6.07.B02 23/02/2000  Steve Downing                                 *
.*                  Fixed century error in discharge date                     *
.******************************************************************************
.*        V6.06.01  10/01/2000  Steve Armstrong                               *
.*                  Removed reference to CSTRTYR (not being used)             *
. *       V6.06.00  18/03/1999  Davin                                         *
. *                 Mods for 606                                              *
. *****************************************************************************
. *       V6.03.01  16/09/1996  Greg Horvat      SRF 616262                   *
. *                 Changed to use the claim code as a selection criteria for *
. *                 the report                                                *
. *                                                                           *
. * ************************************************************************* *
. *                                                                           *
. *       V5.00     31/01/89 K.Peak  (I.B.A)                                  *
. *                 Added PATSELFN                                            *
. *       V5.01     13/02/89 K.Peak  (I.B.A)                                  *
. *                 INTERIM Invoices                                          *
. *       V5.02     28/03/89 J.Tan   (I.B.A)                                  *
. *                 Fixed overflow error                                      *
. *       V5.03     13/04/89 Graeme Williams   SRF 5645                       *
. *                 New index added to Dtrans                                 *
. *       V5.04     05/05/89 Graeme Williams   SRF 4219                       *
. *                 Added PINVCADM to inv file                                *
. *       V5.05     17/05/89 DIG                                              *
. *                 Changed to standard and mod'd print for 8 digit admission *
. *       V5.06     18/05/89 J.Tan          SRF 100526                        *
. *                 Fixed AAE & OUT patients                                  *
. *       V5.07     27/05/89 J.Tan                                            *
. *                 Fixed Mother Adm. to form8 (PATMISFD).     SRF 100750     *
. *       V5.08     06/06/89 J.Tan                                            *
. *                 Mods Locking master file  (PATIOMAS)     SRF 100809       *
. *       V5.09     13/06/89 J.Tan                                            *
. *                 Mods to store last Claims for each system. SRF 143        *
. *       V5.10     22/06/89 J.Tan      SRF 101185                            *
. *                 Removed PATSELFN                                          *
. *       V5.51     20/02/90 D.Di.Paola       SRF 103995                      *
. *                 Fixed exit point when enter "N" for Ok To Continue...     *
. *       V5.52     22/02/90 J.Tan  SRF 100626                                *
. *                 Mods to print fund rebate                                 *
. *       V5.53     21/05/90 J.Tan  SRF 104901                                *
. *                 Extend patfundf file                                      *
. *       V5.54     06/12/90 DWJ                                              *
. *                 Recompile for MBS extension                               *
. *       V5.55     15/07/1992    Justin Coates  SRF 115738                   *
. *                 Added reset option                                        *
. *                                                                           *
. *       V6.00     07/03/91 ROD                                              *
. *                 Release 6. Health fund table now 8 chars                  *
. *       V6.01.01  30/03/94 J.Tan    SRF 128627                              *
. *                 Mods to use health fund from admission file               *
. *****************************************************************************
.
          INC       STD001FD
          INC       IBASECFD/INC
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       PATMA1FD/INC
          INC       PATDTRFD/INC
          INC       PATINVFD/INC
          INC       PATTRNFD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATFN1FD/INC
          INC       PATDSCFD/INC
          INC       PATCODFD/INC
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
.               WORK AREA
.
PATWORK1  IFILE     SQL, FIXED=240, KEY="U1-42"
.
.Name       Type      Length     Physical     Start
.----       ----      ------     --------     -----
.AFUNDH     DIM           6            6         1
.PSNAME     DIM          20           20         7
XDPINVADM   DIM           8            8        27
.PINVNO     DIM           8            8        35
.PGNAME     DIM          25           25        43
PNAME       DIM          17           17        68
.PADD1      DIM          35           35        85   
.PADD2      DIM          35           35        120
.PSUBRB     DIM          35           35        155
.PPOST      DIM           8            8        190
.AFNDMM     DIM          19           19        198
ADSTAT      DIM           7            7        217
.ADATE      DIM           8            8        224
.DDATE      DIM           8            8        232
.
.  END OF RECORD                                240
.
.  redefine form fields for key
.  ----------------------------
.Name       Type      Length     Start
.----       ----      ------     -----
.PINVADM    DIM          8         1
.--------------------------------------------------------------
.
CLMCODE   DIM       3
CLMDESC   DIM       20
.
INTFRM    DIM       8
INTTOD    DIM       8
.
PINVACCM  FORM      8.2
PINVACRB  FORM      8.2
PINVTHET  FORM      8.2
PINVTHRB  FORM      8.2
PINVMISC  FORM      8.2
PINVMIRB  FORM      8.2
PINVTOTL  FORM      8.2
PINVTORB  FORM      8.2
.
TINVACCM  FORM      8.2
TINVACRB  FORM      8.2
TINVTHET  FORM      8.2
TINVTHRB  FORM      8.2
TINVMISC  FORM      8.2
TINVMIRB  FORM      8.2
TINVTOTL  FORM      8.2
TINVTORB  FORM      8.2
.
BEGIN     FORM      1
ERRFLAG   FORM      1
OVRFLG    FORM      1
TRNFLG    FORM      1
.
XMON1     DIM       2
XDAY1     DIM       2
XYEAR1    DIM       2
XCENT1    DIM       2
.
XMON2     DIM       2
XDAY2     DIM       2
XYEAR2    DIM       2
XCENT2    DIM       2
.
DIM5      DIM       5 
DIM9      DIM       9 
DIM10     DIM       10
DIM20     DIM       20
DIM25A    DIM       25
DIM25B    DIM       25
DIM30     DIM       30
.
PSTROL    FILE      ASCII, FIXED=256
.
CLHFASS   FORM      8
CODE      DIM       2
CURSYS    FORM      1
.
OPCND     FORM      1
.
CMDLINE   DIM       30
FNAME     DIM       8
.
SAVHFD    DIM       6
STAR      INIT      "*"
.
ZERO8     INIT      "0000    "          * for patfndfd
.
.      EXTRA VARIABLES
.
NEGONE    FORM      "-1"
.
PRGID     INIT      "IBAHOS08"
PRGNAM    INIT      "HEALTH FUND CLAIMS REPORT"
VERSION   INIT      "V12.01.00"
.
.*********************************************************************
.         START OF PROGRAM
.*********************************************************************
ML0000    CALL      INIT0000
.
ML1000    CALL      OPTN0000                * Select option
          BRANCH    EXIT,ENDIT
.
ML2000    CALL      SCLC0000                * Select the claim code
          BRANCH    EXIT,ML1000
.
          CALL      CONTQST
          BRANCH    CEXIT,ML3000,ML2000,ML1000
.                         Yes    No     Cancel
.
ML3000    BRANCH    OPTION,ML4000,ML4000    * print/print & reset
.
          CALL      REST0000                * reset only
          GOTO      ML1000
.
.         print the report 
.
ML4000    CALL      CTMP0000
          BRANCH    OVRCD,ML1000            * isbuild failed
.
          READ      CONTROLF,TEN8;*192,CLHFASS
          MOVE      THREE,CURSYS         * Inpatient
          MOVE      SP6,SAVHFD
          MOVE      ONE,ERRFLAG
          MOVE      SP10,ADSTAT
          MOVE      ZERO,TINVACCM
          MOVE      ZERO,TINVTHET
          MOVE      ZERO,TINVMISC
          MOVE      ZERO,TINVTOTL
          MOVE      "60",CLNO
          CLOCK     TIME,CTIMEIS
          DISPLAY   *P10:24,*EL,"Date :",*P27:24,"Invoice :":
                    *P45:24,"Scanning :";
.
          MOVE      CLHFASS,KEY8
          CALL      RDSINV1
.
RDINV   CALL       RDKINV1
        BRANCH     OVRCD OF DISPDA
.
        MATCH      ZEROVISN,PINVADM
        GOTO       RDINV IF EQUAL
.
        MATCH      "00000000",PINVNO
        GOTO       RDINV IF EQUAL        * Cancelled invoice
.
        UNPACK      PINVDTE WITH XCENT,XYEAR,XMON,XDAY
        DISPLAY     *P16:24,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR:
                    *P55:24,PINVNO;
.
        COMPARE     CURSYS,PINVSYS
        GOTO        RDINV IF NOT EQUAL
.
        MOVE        PINVNO,CLHFASS
        MOVE       "INTERIM",ADSTAT
.
        MOVE        PINVADM,KEY8
        CALL        RDMISS1
        BRANCH      OVRCD OF NOADM
.
          MATCH     SP3,CLMCODE
          IF        !@EQUAL
            MATCH     CLMCODE,ACLAIM
            GOTO      RDINV IF NOT EQUAL    * Not the claim code required
          ENDIF
.
          BRANCH    ASTAT,RDINV,PRTADM,INPD10
          GOTO      RDINV
.
INPD10  MOVE       "DISCHGE",ADSTAT
        MOVE        AADMNO,KEY8
        CALL        RDDSCH1
        BRANCH      OVRCD OF NODSCH
.
.
PRTADM  MOVE       SP8,INTFRM
        MOVE       SP8,INTTOD
.
        PACK       KEY22,PINVADM,PINVNO,SP20
        CALL       RDSDTRN1
PRTADM1 CALL       RDKDTRN1
        BRANCH     OVRCD,PRTDAT
.
        MATCH      TADMNO,PINVADM
        GOTO       PRTDAT IF NOT EQUAL
.
        MATCH      TREF,PINVNO
        GOTO       PRTDAT IF NOT EQUAL
.
        COMPARE    ONE,TRECTYPE
        GOTO       PRTADM1 IF NOT EQUAL
.
        PACK       INTFRM,TFCENT,TFYEAR,TFMONTH,TFDAY
        PACK       INTTOD,TTCENT,TTYEAR,TTMONTH,TTDAY
        MOVE       INTFRM,ADATE
        MOVE       INTTOD,DDATE
        GOTO       PRTADM1
.
.       Print the data
.
PRTDAT  MOVE        PINVUR,PURNO
        MOVE        PURNO,KEY8
        CALL        RDMAST1
        BRANCH      OVRCD OF NOUR
.
        MATCH       SP6,AFUNDH
        GOTO        PRTD10 IF NOT EQUAL
.
        MOVE        PFUNDH,AFUNDH
        MOVE        PFNDMM,AFNDMM
.
PRTD10  PACK        KEY42 WITH AFUNDH,PSNAME,PINVADM,PINVNO
        CALL        RDWORK1
        COMPARE     ZERO,OVRCD
        GOTO        INVREC IF EQUAL
.
.       Check Dtran totals against invoice data
.
        COMPARE     ZERO,PINVREB
        GOTO        RDINV IF EQUAL
.
        MATCH       SP6,AFUNDH
        GOTO        RDINV IF EQUAL
.
        DISPLAY     *P36:24,*V2LON,PINVNO,*HOFF;
.
        CALL        WRWORK1
        GOTO        RDINV
.
.
NOADM   DISPLAY     *P1:22,*EL,*B,*V2LON:
                    "** Admission Number ",PINVADM:
                    " missing from Admission File **"
.
        GOTO        RDINV
.
NODSCH   DISPLAY     *P1:22,*EL,*B,*V2LON:
                    "** Admission Number ",PINVADM:
                    " missing Discharge Details **"
.
        GOTO        RDINV
.
NOUR    DISPLAY     *P1:22,*EL,*B,*V2LON:
                    "** U/R Number ",PURNO," missing from Master File **"
.
        GOTO        RDINV
.
INVREC  DISPLAY     *P1:23,*EL,*B,*V2LON:
                    "** Invalid Invoice ",PINVNO," **"
        GOTO        RDINV
.
ENDREP   PRINT        *N,"  ***  END OF REPORT  ***"
.
         DISPLAY      *P1:24,*EL,*P40:24,*V2LON:
                      "** Report Generation Completed **",*W;
.
         CALL         DTMP0000
.
         COMPARE      TWO,OPTION
         GOTO         RESET IF EQUAL
.
         GOTO         ML1000
.
NODATA   DISPLAY      *P1:24,*EL,*P40:24,*V2LON:
                      "** No Available Data **",*W2;
         BRANCH       OPTION OF NODATA1,RESET
.
RESET    WRITAB      CONTROLF,TEN8;*192,CLHFASS
.
NODATA1  GOTO        ML1000
.
.     PRINT ACTUAL DATA 
.
DISPDA  MOVE         SP6,SAVHFD
        MOVE         "60",CLNO
        MOVE         ZERO,OVRFLG
        MOVE         ZERO,BEGIN
.
        MOVE         ZERO,PINVTHET            * Theatre
        MOVE         ZERO,PINVMISC            * Miscellaneous
        MOVE         ZERO,PINVACCM            * Accomodation
        MOVE         ZERO,PINVTOTL            * Total
.
        MOVE         ZERO,PINVTHRB
        MOVE         ZERO,PINVMIRB
        MOVE         ZERO,PINVACRB
        MOVE         ZERO,PINVTORB
.
        DISPLAY      *P1:24,*EL,*P20:24," *** Generate Report ***",*W:
                     *P20:24,*EL,"Admission Number :";
.
        PACK         KEY42 WITH SP30,SP10
        CALL         RDSWORK1
NEXT10  CALL         RDKWORK1
        BRANCH       OVRCD OF ACCTOT1
.
        DISPLAY      *P40:24,*EL,*V2LON,PINVADM;
.
        MATCH        AFUNDH,SAVHFD
        GOTO         NEXT20 IF EQUAL
.
        MOVE         AFUNDH,SAVHFD
.
        COMPARE      ZERO,BEGIN
        GOTO         ACCTOT IF NOT EQUAL
.
NEXT15  CALL         HEAD
        MOVE         ONE,BEGIN

NEXT20  MATCH        ZEROVISN,PINVADM
        GOTO         CANCLI IF EQUAL
.
        MOVE       PGNAME,ANS
        PACK       PNAME WITH ANS,DOT,PSNAME
.
        MOVE       ZERO,PINVTHET            * Theatre
        MOVE       ZERO,PINVMISC            * Miscellaneous
        MOVE       ZERO,PINVACCM            * Accomodation
        MOVE       ZERO,PINVTOTL            * Total
.
        MOVE       ZERO,PINVTHRB
        MOVE       ZERO,PINVMIRB
        MOVE       ZERO,PINVACRB
        MOVE       ZERO,PINVTORB
.
        PACK       KEY22 WITH PINVADM,PINVNO,SP6
        CALL       RDSDTRN1
NEXTT   CALL       RDKDTRN1
        BRANCH     OVRCD OF ENDT
.
        MATCH      PINVNO,TREF
        GOTO       ENDT IF NOT EQUAL
.
        MATCH      TADMNO,PINVADM
        GOTO       ENDT IF NOT EQUAL
.
        BRANCH     TRECTYPE OF ACCM,THET,MISC
        GOTO       NEXTT
.
MISC    ADD        TPATAMTT,PINVMISC
        ADD        TREBATE,PINVMIRB
        ADD        TPATAMTT,PINVTOTL
        ADD        TREBATE,PINVTORB
        GOTO       NEXTT
.
ACCM    ADD        TPATAMTT,PINVACCM
        ADD        TREBATE,PINVACRB
        ADD        TPATAMTT,PINVTOTL
        ADD        TREBATE,PINVTORB
        GOTO       NEXTT
.
THET    ADD        TPATAMTT,PINVTHET
        ADD        TREBATE,PINVTHRB
        ADD        TPATAMTT,PINVTOTL
        ADD        TREBATE,PINVTORB
        GOTO       NEXTT
.
ENDT    UNPACK     CDATE WITH XDAY2,SLASH,XMON2,SLASH,XYEAR2
        UNPACK     ADATE INTO XCENT1,XYEAR1,XMON1,XDAY1
.
.        MATCH      "INTERIM",ADSTAT
.        GOTO       END10 IF EQUAL
.
        UNPACK     DDATE INTO XCENT2,XYEAR2,XMON2,XDAY2
.
END10   COMPARE    "50",CLNO
        CALL       HEAD IF NOT LESS
.
        REP        " 0",XCENT1
        REP        " 0",XYEAR1
        REP        " 0",XMON1
        REP        " 0",XDAY1
        REP        " 0",XCENT2
        REP        " 0",XYEAR2
        REP        " 0",XMON2
        REP        " 0",XDAY2
.
        MOVE       PADD1,DIM25A
        MOVE       PADD2,DIM25B
        MOVE       PSUBRB,DIM20
        MOVE       PPOST,DIM5
.
        PACK       AFNDMM,AFNDMM,SP70
        UNPACK     AFNDMM,DIM10,DIM9
        MATCH      SP70,DIM9
        IF         !@EQUAL
          MOVE     AFNDMM,DIM9
          PACK     DIM10,DIM9,STAR
        ENDIF
.
        PRINT      *1,"!",PINVADM,*10,"! ",*12,PNAME,*38,"! ",*40,DIM10:
                   *51,"! ",*53,PINVNO:
                   *62,"!",*63,XDAY1,SLASH,XMON1,SLASH:
                   XCENT1,XYEAR1,*73,"!",*75,ADSTAT:
                   *84,"!",PINVACCM,*96,"!",PINVTHET:
                   *108,"!",PINVMISC,*120,"!",PINVTOTL:
                   *132,"!",*N:
                   *1,"!",*10,"! ",*12,DIM25A,*38,"! ",*51,"! ",*62,"! ":
                   *73,"! ",*84,"!",PINVACRB,*96,"!",PINVTHRB:
                   *108,"!",PINVMIRB,*120,"!",PINVTORB,*132,"!",*N:
                   *1,"! ",*10,"! ",*12,DIM25B,*38,"! ",*51,"! ",*62,"!":
                   *63,XDAY2,SLASH,XMON2,SLASH,XCENT2,XYEAR2:
                   *73,"! ",*84,"! ",*96,"! ",*108,"! ",*120,"! ",*132,"!",*N:
                   *1,"!",*10,"! ",*12,DIM20,*33,DIM5,*38,"! ",*51,"! ":
                   *62,"! ",*73,"! ",*84,"! ",*96,"! ",*108,"! ",*120,"! ":
                   *132,"!"
.
        ADD        PINVACCM,TINVACCM
        ADD        PINVACRB,TINVACRB
.
        ADD        PINVTOTL,TINVTOTL
        ADD        PINVTORB,TINVTORB
.
        ADD        PINVMISC,TINVMISC
        ADD        PINVMIRB,TINVMIRB
.
        ADD        PINVTHET,TINVTHET
        ADD        PINVTHRB,TINVTHRB
.
        ADD        FOUR,CLNO
        CALL       PAGEND
        GOTO       NEXT10
.
ACCTOT1 MOVE       ONE,OVRFLG
        COMPARE    ZERO,CPAGENO
        GOTO       NODATA IF EQUAL
.
ACCTOT  PRINT      *062,"! ","**  T O T A L S  **":
                   *084,"!",TINVACCM,*096,"!",TINVTHET:
                   *108,"!",TINVMISC,*120,"!",TINVTOTL,*132,"!",*N:
                   *062,"!":
                   *084,"!",TINVACRB,*096,"!",TINVTHRB:
                   *108,"!",TINVMIRB,*120,"!",TINVTORB,*132,"!",*N:
                   *62,"*---------------------------------------":
                   *102,"------------------------------*"
.
        MOVE       ZERO,TINVACCM
        MOVE       ZERO,TINVACRB
.
        MOVE       ZERO,TINVTOTL
        MOVE       ZERO,TINVTORB
.
        MOVE       ZERO,TINVMISC
        MOVE       ZERO,TINVMIRB
.
        MOVE       ZERO,TINVTHET
        MOVE       ZERO,TINVTHRB
.
        ADD        TWO,CLNO
.
        BRANCH     OVRFLG OF ENDREP
        GOTO       NEXT15
.
.       HEADINGS FOR OUTPUT
.
HEAD     CLOCK     TIME,CTIMEIS
         MOVE      "1",CNOUNDLN
         MOVE      "- ",DIM2
         PACK      CPHDROPT,DIM2,CLMDESC
.
         MOVE      SP30,HFNAME
         PACK      KEY14,AFUNDH,ZERO8
         CALL      RDFUND1
.
          CALL      HEAD132
          PRINT     *1,"Health Fund : ",*16,HFNAME:
                    *51,"Period To : ",*65,CDATE,*N,*N:
                    *45,"INPATIENT REPORT",*N,*N;
          ADD       "5",CLNO
          CALL      PAGEND
.
HDCONT    PRINT     *1,"! Adm.",*10,"! ",*38,"! Memb.",*51,"! Invoice":
                    *62,"! From/To",*73,"! Invoice",*84,"! Bed Fees/":
                    *96,"! Theatre/",*108,"! Other/":
                    *120,"! Total/",*132,"!",*N:
                    *1,"! Number",*10,"! Patient Name/Address":
                    *38,"! Number",*51,"! Number",*62,"! Dates":
                    *73,"! Type",*84,"! Rebate",*96,"! Rebate":
                    *108,"! Rebate",*120,"! Rebate",*132,"!"
.
          CALL      PAGEND
          RETURN
.
.          LAST LINE ON THE PAGE
.
PAGEND    PRINT     "*---------------------------------------------":
                    "------------------------------------------":
                    "-------------------------------------------*"
          ADD       ONE,CLNO
.
          RETURN
.
.       A cancelled invoice
.
CANCLI  COMPARE    "57",CLNO
        CALL       HEAD IF NOT LESS
        PRINT      *1,PINVNO,*10,"-- CANCELLED --"
        ADD        ONE,CLNO
        GOTO       NEXT10
.
.
+
RDSWORK1 RESET      KEY42
         READ       PATWORK1,KEY42;;
         RETURN
.
RDWORK1  MOVE       ZERO,OVRCD
         RESET      KEY42
         READ       PATWORK1,KEY42;AFUNDH,PSNAME,XDPINVADM,PINVNO,PGNAME,PNAME:
                             PADD1,PADD2,PSUBRB,PPOST,AFNDMM,ADSTAT:
                             ADATE,DDATE
         GOTO       OVERCOND IF OVER
         MOVE       XDPINVADM,PINVADM
         RETURN
.
RDKWORK1 MOVE       ZERO,OVRCD
         READKS     PATWORK1;AFUNDH,PSNAME,XDPINVADM,PINVNO,PGNAME,PNAME,PADD1:
                             PADD2,PSUBRB,PPOST,AFNDMM,ADSTAT:
                             ADATE,DDATE
         GOTO       OVERCOND IF OVER
         MOVE       XDPINVADM,PINVADM
         RETURN
.
WRWORK1   RESET     KEY42
          WRITE     PATWORK1,KEY42;KEY42,PGNAME,PNAME,PADD1,PADD2,PSUBRB,PPOST:
                             AFNDMM,ADSTAT:
                             ADATE,DDATE
          RETURN
.
UPWORK1  MOVE       PINVADM,XDPINVADM
         UPDATE     PATWORK1;AFUNDH,PSNAME,XDPINVADM,PINVNO,PGNAME,PNAME,PADD1:
                             PADD2,PSUBRB,PPOST,AFNDMM,ADSTAT:
                             ADATE,DDATE
         RETURN 
.
.*********************************************************************
.                   CTMP0000
.         open the temp file
.*********************************************************************
CTMP0000  DISPLAY   *P1:24,*EL,*P10:24,*V2LON:
                    "** Initialising Health Fund Claims Report File **"
.
          CALL      DTMP0000
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME,CMDLINE
          APPEND    " 240 u1-42",CMDLINE
          APPEND    SP30,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          DISPLAY   *P1:12,*EF
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATWORK1,FNAME
          TRAPCLR   IO
          COMPARE   ZERO,OVRCD
          GOTO      CTMP9999 IF EQUAL       * open OK
.
          DISPLAY   *P1:24,*EL,*B,"Temporary Index File ",FNAME," not Found.  ";
          CALL      HITENTER
.
CTMP9999  RETURN      
+
.*********************************************************************
.                   DTMP0000
.         delete temp file
.*********************************************************************
DTMP0000  CLOSE     PATWORK1
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
DTMP9999  RETURN
+
.
          INC       STD001IO
          INC       KEYINCOD
          INC       PATDSCOD
          INC       TFILENAM                     * Generate Temp File Name
          INC       IBASECIO/INC
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       PATMA1IO/INC
          INC       PATDTRIO/INC
          INC       PATINVIO/INC
          INC       PATTRNIO/INC
          INC       PATFN1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATDSCIO/INC
          INC       PATCODIO/INC
          INC       WEBERRIO/INC                 * Web Server Error Logging
.
ENDIT     CALL       DTMP0000
          CHAIN      PGM
          STOP
.
.*********************************************************************
.                   INIT0000                    Called by : ML0000
.         open files
.*********************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P56:24,"Opening patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patinvrf";
          OPEN      PATINVR1,"patinvrf"
.
          DISPLAY   *P64:24,"patdtraf";
          OPEN      PATDTRA1,"patdtraf"
.
          DISPLAY   *P64:24,"patinvrf";
          OPEN      PATINVR2,"patinvrf"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patfn1af";
          OPEN      PATFN1A1,"patfn1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"controlf"; 
          OPEN      CONTROLF,"controlf"
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAME
.
INIT9999  RETURN
+
.*********************************************************************
.                   REST0000                    Called by : CLEAR
.         only reset the invoices
.*********************************************************************
REST0000  READ      CONTROLF,TEN8;*192,CLHFASS
          DISPLAY   *P1:24,*EL,*P10:24,"Date :",*P27:24,"Invoice :":
                    *P45:24,"Scanning :";
          MOVE      THREE,CURSYS            * set to inpatients
          MOVE      CLHFASS,KEY8
          CALL      RDSINV1
.
REST1000  CALL      RDKINV1
          BRANCH    OVRCD OF REST9000
.
          MATCH     ZEROVISN,PINVADM
          GOTO      REST1000 IF EQUAL       * invalid invoice
.
          MATCH     "00000000",PINVNO
          GOTO      REST1000 IF EQUAL       * Cancelled invoice
.
          UNPACK    PINVDTE WITH XCENT,XYEAR,XMON,XDAY
          DISPLAY   *P16:24,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR:
                    *P55:24,PINVNO;
.
          COMPARE   CURSYS,PINVSYS
          GOTO      REST1000 IF NOT EQUAL   * wrong system
.
          MOVE      PINVNO,CLHFASS          * set the last invoice processed
          DISPLAY   *P37:24,*V2LON,PINVNO
          GOTO      REST1000
.
REST9000  WRITAB    CONTROLF,TEN8;*192,CLHFASS
.
REST9999  RETURN
+
.*********************************************************************
.                   OPTN0000                    Called by : ML1000
.         enter the option to run
.*********************************************************************
OPTN0000  MOVE      ZERO,CPAGENO
          MOVE      SP30,DIM30
          MOVE      ZERO,TRNFLG
          DISPLAY   *EL,*P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Print Only":
                    *P1:6,*V2LON,TWO,*HOFF," = Print and Reset":
                    *P1:7,*V2LON,"3",*HOFF," = Reset only"
.
OPTN1000  KEYIN     *P1:9,"Select Option : ",*V2LON,OPTION
.
          MOVE      ONE,EXIT
          COMPARE   ZERO,OPTION
          GOTO      OPTN9999 IF EQUAL
.
          MOVE      FALSE,EXIT
          BRANCH    OPTION,OPTN9999,OPTN9999,OPTN9999
.
          BEEP
          GOTO      OPTN1000
.
OPTN9999  RETURN
+
.******************************************************************************
.*                                  SCLC0000              Called by: ML0000   *
.*                            Enter The Claim Code                            *
.******************************************************************************
SCLC0000  MOVE      "14",ECOL
          MOVE      "11",EVERT
          MOVE      "CL",CODE
          MOVE      SP3,CKYIDEF3            * No default
          MOVE      ZERO,CKYIMAND           * Keyin not mandatory
          MOVE      ONE,CNEWDS              * Don't return for keyin
          DISPLAY   *P1:EVERT,"Claim Code :",*EF;
          CALL      KEYINCOD                * Keyin the claim code
          BRANCH    EXIT,SCLC1000,SCLC9500
.                        Enter    Exit
.
          DISPLAY   *P19:EVERT,TDESC
          MOVE      ACODE,CLMCODE
          MOVE      TDESC,CLMDESC
          MOVE      ZERO,EXIT
          GOTO      SCLC9999
.
SCLC1000  DISPLAY   *P14:EVERT,*V2LON,"All";
          MOVE      SP3,CLMCODE
          MOVE      "All Claim Codes     ",CLMDESC
          MOVE      ZERO,EXIT
          GOTO      SCLC9999
.
SCLC9500  MOVE      ONE,EXIT
SCLC9999  RETURN

+

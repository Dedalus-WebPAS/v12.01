.*****************************************************************************
.* System    :   VINAH Start/End Date Fixit                                  *
.* Program   :   FX923750                                                    *
.* Desc      :   Fixit to populate all the relevant Episode Health Conidtion *
.*               record on ALLDIAAF                                          *
.*****************************************************************************
.* Date      :   11/04/2023                                                  *
.* Author    :   Jacob Jackson                                               *
.* Function  :   Fixit to Update Start, End Dates & HCP Equivalent values    *
.*****************************************************************************
.* Mods      :                                                               *
.*****************************************************************************
.*        V11.03.06  Jacob Jackson     TSK 0923750                           *
.*                   Ensure 5159 code will be inputed with both a start and  *
.*                   end date                                                *
.*        V11.03.05  Jacob Jackson     TSK 0923750                           *
.*                   Add update for 5267 code to add a start date            *
.*        V11.03.04  Jacob Jackson     TSK 0923750                           *
.*                   Only for valid codes which will be populated with an    *
.*                   End Date                                                *
.*        V11.03.03  Jacob Jackson     TSK 0923750                           *
.*                   Ensure only valid HDP codes are made active if inactive *
.*        V11.03.02  Jacob Jackson     TSK 0923750                           *
.*                   Fix problem with label counter displaying on incorrect  *
.*                   description                                             *
.*        V11.03.01  Jacob Jackson     TSK 0923750                           *
.*                   Create FIXIT                                            *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       ALLDIAFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
HDPVALNU  FORM      8
HDPVALST  DIM       4
DATCONT1  FORM      8 
DATCONT2  FORM      8
DATCONT3  FORM      8
DATCONT4  FORM      8
DATCONT5  FORM      8
DATCONT6  FORM      8
TOTLCONT  FORM      8
ENDDATE1  INIT      " (20210630): "
ENDDATE2  INIT      " (20220630): "
SRTDATE1  INIT      " (20190701): "
SRTDATE2  INIT      " (20210701): "
SRTDATE3  INIT      " (20220701): "
DATMESS1  INIT      "Records with HDP code 0-2301, 9998, AXXX adding end date"
DATMESS2  INIT      "Records with HDP code 5159 adding end date"
DATMESS3  INIT      "Records with HDP code 5001-5267 adding start date"
DATMESS4  INIT      "Records with HDP code 5268-5275 adding start date"
DATMESS5  INIT      "Records with HDP code 3000 adding start date"
DATMESS6  INIT      "Number of inactive ALLDIAAF records changed: "
TOTLMESS  INIT      "Number of records in ALLDIAAF found: "
.
.
.
PRGID     INIT      "FX923750"
PRGNAM    INIT      "VINAH Start/End Date Fixit"
VERSION   INIT      "V12.01.00"
+
.*****************************************************************************
.*                              ML0000                                       *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with fixit
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
          GOTO      ML0100
.
ML9999    CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: ML0000     *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                     * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"alldiaaf";
          OPEN      ALLDIAA1,"alldiaaf"
.
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: ML0000     *
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run fixit                               *
.*                        TRUE  (1)  Exit option selected                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: ML0000     *
.*              Loop through each of the values in ALLDIAAF and change       *
.*               them to the requested values.                               *
.*****************************************************************************
.
PROC0000  MOVE      ZERO,DATCONT1
          MOVE      ZERO,DATCONT2
          MOVE      ZERO,DATCONT3
          MOVE      ZERO,DATCONT4
          MOVE      ZERO,DATCONT5
          MOVE      ZERO,DATCONT6
          MOVE      ZERO,TOTLCONT
.          
          PACK      KEY12,SP70
          CALL      RSALDIA1
PROC0100  CALL      RKALDIA1
          BRANCH    OVRCD,PROC9000
.   
          MATCH     SP70,ALDIMHCP
          IF        !@EQUAL
            MOVE      SP70,ALDIMHCP
          ENDIF
          MOVE      ALDIMHDP,HDPVALST
          STRIP     HDPVALST
          TYPE      HDPVALST
          IF        @EQUAL
            MOVE      ALDIMHDP,HDPVALNU
            IF        (HDPVALNU >= 0 & HDPVALNU <= 2301) | HDPVALNU = 9998
              MATCH     "20210630",ALDIEDAT
              IF        !@EQUAL
                MOVE      "20210630",ALDIEDAT
                ADD       ONE,DATCONT1
              ENDIF
              GOTO    PROC0200
            ENDIF
.
            IF        HDPVALNU >= 5001 & HDPVALNU <= 5267
              MATCH     "20210701",ALDISDAT
              IF        !@EQUAL
                MOVE      "20210701",ALDISDAT
                ADD       ONE,DATCONT5
              ENDIF
              IF        HDPVALNU = 5159
                MATCH     "20220630",ALDIEDAT
                IF        !@EQUAL
                  MOVE      "20220630",ALDIEDAT
                  ADD       ONE,DATCONT2
                ENDIF
                GOTO    PROC0200
              ENDIF
            ENDIF
.
            IF        HDPVALNU >= 5268 & HDPVALNU <= 5275
              MATCH     "20220701",ALDISDAT
              IF        !@EQUAL
                MOVE      "20220701",ALDISDAT
                ADD       ONE,DATCONT4
              ENDIF
            ENDIF
.
            IF        HDPVALNU = 3000
              MATCH     "20190701",ALDISDAT
              IF        !@EQUAL
                MOVE      "20190701",ALDISDAT
                ADD       ONE,DATCONT3
              ENDIF
            ENDIF
          ELSE
            CMATCH    "A",ALDIMHDP
            IF        @EQUAL
              MATCH     "20210630",ALDIEDAT
              IF        !@EQUAL
                MOVE      "20210630",ALDIEDAT
                ADD       ONE,DATCONT1
              ENDIF
              GOTO      PROC0200
            ENDIF
          ENDIF
          GOTO      PROC0300
.         
PROC0200  MATCH     "0",ALDIACTV
          IF        @EQUAL
            MATCH     "1",ALDIACTV 
            IF        !@EQUAL
              MOVE      "1",ALDIACTV
              ADD       ONE,DATCONT6
            ENDIF
          ENDIF
.
PROC0300  CALL      UPALDIA1
.
          MOVE      SP70,HDPVALNU
          ADD       ONE,TOTLCONT
          GOTO      PROC0100
.
PROC9000  DISPLAY   *P1:16,*EF,DATMESS1,ENDDATE1,DATCONT1:
                    *P1:17,*EF,DATMESS2,ENDDATE2,DATCONT2:
                    *P1:18,*EF,DATMESS3,SRTDATE2,DATCONT5:
                    *P1:19,*EF,DATMESS4,SRTDATE3,DATCONT4:
                    *P1:20,*EF,DATMESS5,SRTDATE1,DATCONT3:
                    *P1:21,*EF,DATMESS6,DATCONT6:
                    *P1:22,*EF,TOTLMESS,TOTLCONT:
                    *P1:24,*EF;
.
          CALL      HITENTER
.
PROC9999  RETURN
+
.****************************************************************************
.             I/O Includes
.****************************************************************************
          INC       STD001IO
.
          INC       ALLDIAIO/INC

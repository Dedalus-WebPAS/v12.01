.******************************************************************************
.*  System    : OPERATING ROOMS                                               *
.*  Program   : IBAOPR55                                                      *
.*  Desc      : Daily Operating Room Summary List                             *
.******************************************************************************
.*  Date      : 02/01/1991                                                    *
.*  Author    : Rod Knowles                                                   *
.*  Function  : Generate a list of patients in due for surgery in each theatre*
.*  Mods.     :                                                               *
.******************************************************************************
.*        V11.03.02 19/04/2023   Thanh T  TSK 0909393                         *
.*                  Changed PROC3000 to setup KEY22 with OPDAHOSP.            *
.*                  Changed PROC0000 to use RSOPDEA6 instead of RSOPDEA1.     *
.*        V11.03.01 20/03/2023  PJ Le Febour   TSK 0909393                    *
.*                  Amended KEY19 to KEY22 for theatre index changes          *
.******************************************************************************
.*        V10.01.01 03/02/2011  Jill Parkinson CAR 233088                     *
.*                  Added PMSVX1FD                                            *
.*        V9.09.00  06/02/2008  Mike Laming   CAR 160818                      *
.*                  Ported from 6.15 - add MultiHospital IBCNMHOS             * 
.*        V6.06.00  20/05/1999  Jill Habasque SRF 631406                      *
.*                  Returned from obsolete                                    *
.*        V6.03.01  26/03/1996  Greg Horvat      SRF 614668                   *
.*                  Fixed the spelling of anaesthetist & fixed the printing   *
.*                  of the surgeon name in the header                         *
.*                                                                            *
.******************************************************************************
.
.$$$$$$
.    - Keyin Session Date
.      - Loop thru OPRDEAFD
.      - print details
.    - End
.$$$$$$
.
          INC       STD001FD
          INC       BOKRC1FD/INC
          INC       OPRCLIFD/INC
          INC       OPRCONFD/INC
          INC       OPRDEAFD/INC
          INC       OPRSESFD/INC
          INC       PATDO1FD/INC
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATPR1FD/INC
          INC       PATTRNFD/INC
+
.
.         Program Variables and Constants
.
CURRDATE  DIM       8
DESC20    DIM       20
DESC27    DIM       27
DESC30    DIM       30
DIM3A     DIM       3
DIM6      DIM       6
KDATE     DIM       8
KIHOSPID  DIM       3
KIHSNAME  DIM       35
IBCNMHOS  FORM      1
NUMPATS   FORM      3
SAVECLIN  DIM       6
SAVETIME  DIM       5
UNDLN32   INIT      "--------------------------------"
Z30       INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
.
PRGID     INIT      "IBAOPR55"
PRGNAM    INIT      "DAILY THEATRE SUMMARY LIST"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                               ML0000                                   *
.*                  Mainline code - Controlling logic                     *
.**************************************************************************
ML0000    CALL      INIT0000
.
ML1000    CALL      SCRN0000                      * get main options
          BRANCH    EXIT,ML9999                   * Exit selected
.
ML2000    CALL      GDAT0000                      * get date
          CALL      CONTQST                       * ask Continue (Y/N/C)?
          BRANCH    CEXIT,ML3000,ML2000,ML1000    
.
ML3000    CALL      PROC0000                      * do processing
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.****************************************************************************
.*                              INIT0000               Called By: ML0000    *
.*                           Initialization                                 *
.****************************************************************************
INIT0000
.
. remove trailing blanks from title
.
          ENDSET    OPCNODSC
.
INIT9000  CMATCH    SP1,OPCNODSC
          GOTO      INIT9100 IF NOT EQUAL
          BUMP      OPCNODSC,-1
          GOTO      INIT9000 IF NOT EOS
.
INIT9100  LENSET    OPCNODSC
          RESET     OPCNODSC
          REP       UPPLOW,OPCNODSC
.
          CALL      DISPHEAD
.         
          DISPLAY   *P56:24,"Opening bokrc1af";
          OPEN      BOKRC1A1,"bokrc1af"
          DISPLAY   *P64:24,"oprcliaf";
          OPEN      OPRCLIA1,"oprcliaf"
          DISPLAY   *P64:24,"oprdetaf";
          OPEN      OPRDETA6,"oprdetaf"
          DISPLAY   *P64:24,"oprsesaf";
          OPEN      OPRSESA1,"oprsesaf"
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
          DISPLAY   *P64:24,"patpr1af";
          OPEN      PATPR1A1,"patpr1af"
          DISPLAY   *P64:24,"pattranf";
          OPEN      PATTRAN2,"pattranf"
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,FORTY;*81,OPCNODSC
.
INIT9999  RETURN
+
.**************************************************************************
.*                             SCRN0000              Called By: ML0000    *
.*                      Display main menu screen                          *
.**************************************************************************
SCRN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Produce Report":
                    *P1:7,"Select Option : ";
.
SCRN1000  KEYIN     *P17:7,*EL,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION                  * keyin = 0 ?
          GOTO      SCRN9000 IF EQUAL
.
          BRANCH    OPTION,SCRN8000
          BEEP 
          GOTO      SCRN1000
.
SCRN8000  MOVE      FALSE,EXIT
.
          UNPACK    SP30,KIHOSPID,KIHSNAME
          IF        IBCNMHOS = 1
            MOVE      "9",CVERT
            MOVE      "18",CCOL
            CALL      KYHOSPID                * keyin Hospital Id.
            BRANCH    EXIT,SCRN8500
.
            PACK      KIHOSPID,PTHSHOSP,SP10  * Hospital Id.
            PACK      KIHSNAME,PTHSNAME,SP20  * Hospital Name   
          ENDIF
.
SCRN8500  GOTO      SCRN9999
.
SCRN9000  MOVE      TRUE,EXIT
.
SCRN9999  RETURN
+
.***************************************************************************
.*                              GDAT0000             Called by: ML0000     *
.*                            keyin a date                                 *
.***************************************************************************
GDAT0000  PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          MOVE      ONE,CCENTRY
          MOVE      ZERO,CCANLDTE
          MOVE      ONE,CDEFDTE
.
          DISPLAY   *P1:11,*EF,"Date of Operations : ";
          MOVE      TEN1,CVERT                   * set up defaults
          MOVE      "22",CCOL
.
GDAT1000  MOVE      CCC,CCENT
          MOVE      CYY,CYEAR
          MOVE      CMM,CMON
          MOVE      CDD,CDAY
          CALL      KEYDATE                      * keyin a date
          BRANCH    CQUEST,GDAT1000
.
. now check if date is >= current date
.
          PACK      KDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",KDATE
.
          MATCH     KDATE,CURRDATE
          GOTO      GDAT9999 IF LESS
          GOTO      GDAT9999 IF EQUAL
.
.         DISPLAY   *P1:24,*EL,*B,*V2LON:
.                   "Date must be at least today's date.    ";
.         CALL      HITENTER
.         GOTO      GDAT1000
.
GDAT9999  RETURN
+
.*****************************************************************************
.*                              PROC0000                Called By: ML0000    *
.*                       do processing for list                              *
.*****************************************************************************
PROC0000  DISPLAY   *P1:24,*P50:24,*EL,*V2LON,"***  Generating List  ***",*W1;
          MOVE      ZERO,NUMPATS                 * initialize patient counter
          MOVE      ZERO,CPAGENO                 * initialize page counter
          CLOCK     TIME,CTIMEIS
          CALL      HEAD132
.
          UNPACK    KDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *40,"Date : ",CPCDATE;
.         
PROC0500  IF        IBCNMHOS = 1
            MATCH     SP3,KIHOSPID
            IF        @EQUAL
              PRINT     *60,"    - All Hospitals";
            ELSE
              PRINT     *60,"Hospital ID : ",KIHOSPID,SP2,KIHSNAME;
            ENDIF
          ENDIF
          PRINT      *N
.
.
          MOVE      FIVE,CLNO
          MOVE      SP5,SAVETIME
          MOVE      SP6,SAVECLIN
.
          PACK      KEY26,KDATE,SP70             * position at start
          CALL      RSOPDEA6
PROC1000  CALL      RKOPDEA6                     * read next record
          BRANCH    OVRCD,PROC8000               * no more records
.
          MATCH     OPDADATE,KDATE               * still same date?
          GOTO      PROC8000 IF NOT EQUAL        * no
.
          COMPARE   ZERO,OPDASTAT                * status = booked? 
          GOTO      PROC1000 IF NOT EQUAL        * no
.
. we have a valid record
.
.   if 1st record we don't want to check if new session is same as previous
.
          COMPARE   FIVE,CLNO
          GOTO      PROC3000 IF EQUAL
.
.    check if current record belongs to current session
.
          MATCH     OPDATIME,SAVETIME            * same session time?
          GOTO      PROC2000 IF NOT EQUAL        * no
.
          MATCH     OPDACLIN,SAVECLIN            * same clinic?
          GOTO      PROC5000 IF EQUAL            * yes
.
.    check if session is empty
.
PROC2000  MATCH     SP3,OPDACASE
          GOTO      PROC3000 IF EQUAL
.
.  print end of current session
.
          PRINT     *1,"*",UNDLN32,UNDLN32,UNDLN32,UNDLN32,"--*"
          PRINT     *3,"Number of Cases = ",NUMPATS,*N
          ADD       THREE,CLNO                   * increment line counter
          MOVE      ZERO,NUMPATS                 * initialize patient counter
.
. print session heading
.
PROC3000  PACK      KEY22,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN,SP70
          CALL      RDOPSES1
          BRANCH    OVRCD,PROC4000
.
          IF        IBCNMHOS = 1
            MATCH     SP3,KIHOSPID
            IF        !@EQUAL
              MATCH     KIHOSPID,OPSEHOSP
              GOTO      PROC1000 IF NOT EQUAL
            ENDIF
          ENDIF

          MOVE      OPSECLIN,DIM6
          CALL      DESC0000                     * get clinic/doctor description
.
          PRINT     *1,"*",UNDLN32,UNDLN32,UNDLN32,UNDLN32,"--*"
          PRINT     *1,"| ",OPSETHET,*11,OPSECLIN,*18,DESC30,*50,OPSETIME:
                    *56,"/",*58,OPSEENDT;
.
          MOVE      OPSEANAE,DIM6
          CALL      DESC0000                     * get anaeth... description
.
          PRINT     *86,"Anaesthetist : ",DESC30,*132,"|"
          PRINT     *1,"*",UNDLN32,UNDLN32,UNDLN32,UNDLN32,"--*"
          ADD       THREE,CLNO
          GOTO      PROC5000
.
.  missing session details
.
PROC4000  PRINT     *1,"*",UNDLN32,UNDLN32,UNDLN32,UNDLN32,"--*"
          PRINT     *1,"|",*48,"*****  Missing Session Details  *****",*132,"|"
          PRINT     *1,"*",UNDLN32,UNDLN32,UNDLN32,UNDLN32,"--*"
          ADD       THREE,CLNO                   * increment line counter
.
.  print the details of the current record
.
PROC5000  COMPARE   "55",CLNO                    * new page ?
          GOTO      PROC5100 IF LESS             * no
          CALL      PAGE0000                     * yes, new page required
.
PROC5100  MOVE      OPDATIME,SAVETIME            * save session time
          MOVE      OPDACLIN,SAVECLIN            * save session clinic
          MOVE      OPDACOMM,DESC20              * print 1st 20 chars
.
          CALL      GPMI0000                     * get PMI details
          CALL      WARD0000                     * get patients ward
.
          PRINT     *1,"|",*3,OPDACASE,".",*8,OPDADES1,*64,DESC20,*86,PSEX:
                    *88,PSAGE,*92,TWARD,*96,DESC27,*123,OPDAADMN,*132,"|"
          ADD       ONE,CLNO                     * increment line counter
          ADD       ONE,NUMPATS                  * increment no. of patients
.
. now print 2nd line
.  check if operation description 2 is blank
.
          RESET     OPDACOMM,21
          MOVE      OPDACOMM,DESC20
.
          MATCH     SP30,OPDADES2
          GOTO      PROC6000 IF NOT EQUAL        * description non blank
.
. check if chars 21-40 in OPDACOMM is blank.  If yes don't display 2nd line
.
          MATCH     SP20,DESC20
          GOTO      PROC7000 IF EQUAL
.
. line is not blank so display
.
PROC6000  PRINT     *1,"|",*8,OPDADES2,*64,DESC20,*132,"|"
          ADD       ONE,CLNO                     * increment line counter
.
. now print 3rd line
.  check if operation description 3 is blank
.
PROC7000  MATCH     SP30,OPDADES3
          GOTO      PROC1000 IF EQUAL
.
. line is not blank so display
.
          PRINT     *1,"|",*8,OPDADES3,*132,"|"
          ADD       ONE,CLNO                     * increment line counter
          GOTO      PROC1000                     * get next record
.
PROC8000  PRINT     *1,"*",UNDLN32,UNDLN32,UNDLN32,UNDLN32,"--*"
          PRINT     *3,"Number of Cases = ",NUMPATS,*N
          PRINT     *N,*1,"***  End of Report  ***"
.
PROC9999  RETURN
+
.****************************************************************************
.*                              PAGE0000               Called by: PROC0000  *
.*                       set up for a new page                              *
.****************************************************************************
PAGE0000  CALL      HEAD132
.
          UNPACK    KDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *40,"Date : ",CPCDATE;   
.
PAGE1000  IF        IBCNMHOS = 1
            MATCH     SP3,KIHOSPID
            IF        @EQUAL
              PRINT     *60,"    - All Hospitals";
            ELSE
              PRINT     *60,"Hospital ID : ",KIHOSPID,SP2,KIHSNAME;
            ENDIF
          ENDIF
          PRINT      *N
.
          MOVE      FIVE,CLNO
.
          MOVE      OPSECLIN,DIM6
          CALL      DESC0000                     * get clinic/doctor description
.
          PRINT     *1,"*",UNDLN32,UNDLN32,UNDLN32,UNDLN32,"--*"
          PRINT     *1,"| ",OPSETHET,*11,OPSECLIN,*18,DESC30,*50,OPSETIME:
                    *56,"/",*58,OPSEENDT;
.
          MOVE      OPSEANAE,DIM6
          CALL      DESC0000                     * get anaeth... description
.
          PRINT     *86,"Anaesthetist : ",DESC30,*132,"|"
          PRINT     *1,"*",UNDLN32,UNDLN32,UNDLN32,UNDLN32,"--*"
          ADD       THREE,CLNO
          
PAGE9999  RETURN
+
.****************************************************************************
.*                              DESC0000               Called by: PROC0000  *
.*                 get clinic or doctor description                         *
.****************************************************************************
.    check if clinic/doctor from OPRCLIFD
.
DESC0000  MOVE      SP30,DESC30
          PACK      KEY6,DIM6       
          CALL      RDOPCLI1 
          BRANCH    OVRCD,DESC5000               * not on file
          MOVE      OPCLDESC,DESC30              * move to display variable
          GOTO      DESC9999
.
. get clinic/doctor code from PATDOCFD
.
DESC5000  PACK      KEY6,DIM6    
          CALL      RDDOCT1
          BRANCH    OVRCD,DESC9999
          MOVE      DGNAME,PACGNAME
          MOVE      DSNAME,PACSNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,DESC30
.
DESC9999  RETURN
+
.****************************************************************************
.*                              GPMI0000               Called by: PROC0000  *
.*                   get PMI details from PRA or MAS                        *
.****************************************************************************
. get U/R Number from Admission record
.
GPMI0000  PACK      KEY8,OPDAADMN
          CALL      RDMISS1
          BRANCH    OVRCD,GPMI1000
.
          MATCH     "       0",AURNO
          GOTO      GPMI2000 IF EQUAL
          MOVE      AURNO,PURNO
          GOTO      GPMI3000
.
. no Admission record so get U/R Number from BOKRECFD
.
GPMI1000  PACK      KEY8,OPDAADMN
          CALL      RDBKREC1
          BRANCH    OVRCD,GPMI9999
          MATCH     "       0",BKREURNO
          GOTO      GPMI2000 IF EQUAL
          MOVE      BKREURNO,PURNO
          GOTO      GPMI3000
.
. Zero U/R so get PMI details from PATPRAFD
.
GPMI2000  PACK      KEY8,OPDAADMN
          CALL      RDPRAM1
          GOTO      GPMI8000
.
. Existing U/R so get PMI details from PATMASFD
.
GPMI3000  PACK      KEY8,PURNO
          CALL      RDMAST1
.
. now format patient's name
.
GPMI8000  MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,DESC27
.
GPMI9999  RETURN
+
.***************************************************************************
.*                              WARD0000              Called by: PROC0000  *
.*                         get patient's ward                              *
.***************************************************************************
WARD0000  MOVE      OPDAADMN,KEY8 
          PACK      KEY30,KEY8,Z30 
          CALL      RDSTRAN2
          CALL      RDPTRAN2
          BRANCH    OVRCD,WARD1000
.
          MATCH     OPDAADMN,DTADMN              * same admission number?
          GOTO      WARD9999 IF EQUAL
.
WARD1000  MOVE      SP3,TWARD
.
WARD9999  RETURN
+
.******************************************************************************
.*                                 KYHOSPID                                   *
.*                            Key in Hospital Id.                             *
.******************************************************************************
KYHOSPID  MOVE      ZERO,EXIT
.         UNPACK    SP30,KIHOSPID,KIHSNAME
          DISPLAY   *P1:CVERT,"Hospital Id.  : ";
KYHOS100  MOVE      SP6,DIM3A
          KEYIN     *PCCOL:CVERT,*V2LON,DIM3A;
.
.
          PACK      KEY3,DIM3A,SP10
KYHOS200  MATCH     SP3,KEY3
...       GOTO      KYHOS950 IF EQUAL
          GOTO      KYHOS300 IF EQUAL
.
          MATCH     QUEST,KEY3
          IF        @EQUAL
            CALL      GETSCR00
            DISPLAY   *P1:4,*EF,*P35:4,"Hospital Details";
            MOVE      "6",EVERT
            PACK      KEY3,SP70
            CALL      RSPTHSP1
KYHOS210    CALL      RKPTHSP1
            BRANCH    OVRCD,KYHOS290
            DISPLAY   *P1:EVERT,PTHSHOSP,SP4,PTHSNAME;
            ADD       ONE,EVERT
            IF        ECOL < 22
              GOTO      KYHOS210
            ENDIF
.
KYHOS290    DISPLAY   *P1:24,*EL,"Hospital ID : ",UNDLN3;
            KEYIN     *P15:24,*V2LON,DIM3A;
            PACK      KEY3,DIM3A,SP10
            CALL      PUTSCR00
          ENDIF
.
...       MATCH     SP3,KEY3
...       GOTO      KYHOS950 IF EQUAL
.
KYHOS300  UNPACK    SP70,PTHSHOSP,PTHSNAME,PTHSAPPR
          MOVE      "All Hospitals",PTHSNAME
          CALL      RDPTHSP1
          DISPLAY   *PCCOL:CVERT,*V2LON,KEY3,SP4,PTHSNAME
          GOTO      KYHOS999
.
KYHOS950  MOVE      ONE,EXIT
.
KYHOS999  RETURN
+
+
.***************************************************************************
.
          INC       STD001IO
          INC       BOKRC1IO/INC
          INC       OPRCLIIO/INC
          INC       OPRDEAIO/INC
          INC       OPRSESIO/INC
          INC       PATDO1IO/INC
          INC       PATHSPIO/INC
          INC       PATMA1IO/INC
...       INC       PATMX1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATPR1IO/INC
          INC       PATTRNIO/INC
+

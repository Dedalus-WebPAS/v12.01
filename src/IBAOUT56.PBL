.******************************************************************************
.* System         : Outpatient                                                *
.* Program        : IBAOUT56.PBL                                              *
.* Name           : ACT HDP Outpaient Department Extraction                   *
.******************************************************************************
.* Date           : 11/03/2005                                                *
.* Author         : Guomin Fu                                                 *
.* Function       : Extract outpatient details via a date range & load        *
.*                  the details into a text file.                             *
.* Modifications  :                                                           *
.*        V12.00.01 21/05/2025  Nikitha B      TSK 0955096                    *
.*                  Alphanumeric changes                                      *
.*        V10.10.01 17/03/2017  Richa Phogat   TSK 0832066                    *
.*                  Changed DVA Card Number collected from PMCDCNUM.PMSCCDFD  *
.*                  instead of PREPAT.PMIMA1FD                                *
.*        V10.03.01 18/11/2011  Mike Laming     CAR 240184                    *
.*                  Changes to IBAPOSTF Post Code table - added State to Keys *
.*        V9.10.02  15/08/2008 Ian Farnell       CAR 173431                   *
.*                  Added PACK PMI in MOVE0000                                *
.*                  changed OBOUSR4 to OBOUSR1 at MOVE0000                    *
.*        V9.10.01  14/04/2008 Jill Habasque     CAR 166064                   *
.*                  July 2008 changes                                         *
.*        V9.09.02  13/12/2007 Jill Habasque     CAR 140056                   *
.*                  Added out prefix to filename                              *
.*        V9.09.01  03/08/2007 Steve Armstrong   CAR 146975                   *
.*                  Mods to source indigenous status from pmpxabrg instead of *
.*                  pmvxabrg.                                                 *
.******************************************************************************
.*        V9.08.03  06/06/2007 Ebon Clements      139419                      *
.*                  Don't extra clinic types with a clinic group HDP of 999   *
.*        V9.08.02  11/05/2007 Ebon Clements      139419                      *
.*                  2007 Mods                                                 *
.*        V9.08.01  30/04/2007 Sandra Barcham     139508                      *
.*                  Fix I * C on outbb1a2                                     *
.*        V9.04.00  11/03/2005  Guomin Fu     CAR 59383                       *
.*                  Created extract                                           *
.*                                                                            *
.******************************************************************************
+
          INC       STD001FD
.
          INC       IBAPOSFD/INC            * Postcode file
          INC       PATHSPFD/INC
          INC       OUTBOAFD/INC            * Clinic Session Booking Table A
          INC       OUTBB1FD/INC            * Clinic Session Booking Table B
          INC       PATCALAG/INC            * calcage   
          INC       PATCODFD/INC            * Codes file
          INC       PATCONFD/INC            * Patient control file
          INC       PATDRGFD/INC            * Period Date Range
          INC       PATDSCFD/INC            * Discharge file
          INC       PATMA1FD/INC
          INC       PMSPX2FD/INC
          INC       PATVISFD/INC
          INC       PMSVX1FD/INC
          INC       PATVADFD/INC            * Transfer source file
          INC       PATCT1FD/INC
          INC       OUTCTYFD/INC
          INC       PMSCCDFD/INC
.
.        **** layout before 01/07/2008
.
EXTRCASF  FILE      ASCII, FIXED=130
.
.Name     Type    Length    Start  Description
.-------  ----    ------    -----  -----------
.HOSPID    DIM          2        1  Hospital ID
.PIN       DIM          8        3  Patient ID Number
.EVENTID   DIM          8       11  Patient Event Number
.SEX       DIM          1       19  Sex
.DOB       DIM          8       20  Date of birth
.LISTDATE  DIM          8       28  Listing date for care (DDMMCCYY)
.PRESDATE  DIM          8       36  Date Patient presents (DDMMCCYY)
.NRSTATUS  DIM          1       44  New/repeat status
CLINTYPE  DIM          2       45  Clinic Type
HOSPCLIN  DIM          3       47  Hospital Clinic
.INDIGEN   DIM          1       50  Indigenous status
STATE     DIM          1       51  State
.LOCALITY  DIM         50       52  Suburb
.POSTCODE  DIM          4      102  Post Code
ELECT     DIM          1      106  Election Status
.REFSRCE   DIM          2      107  Referral Source (Cat S)
.OPFUNDSC  DIM          2      109  Funding source (CAT CL)
SERVTYPE  DIM          1      111  Non-admitted patient service type
DVANO     DIM          9      112  DVA Identification Number 
.MBS       DIM          9      121  Medical Benifits Schedule Iten
.
..end of record               128
.
. ***** layout after 01/07/2008
EXTRCAS2  FILE      ASCII, FIXED=149
.
.Name     Type    Length    Start  Description
.-------  ----    ------    -----  -----------
HOSPID    DIM          2        1  Hospital ID
PIN       DIM          8        3  Patient ID Number
PMI       DIM          8       11  Patient ID Number     * after 01/07/2008
EVENTID   DIM          8       19  Patient Event Number
SEX       DIM          1       27  Sex 
DOB       DIM          8       28  Date of birth
LISTDATE  DIM          8       36  Listing date for care (DDMMCCYY)
PRESDATE  DIM          8       44  Date Patient presents (DDMMCCYY)
NRSTATUS  DIM          1       52  New/repeat status
OPCLINIC  DIM          4       53  Hospital Clinic
INDIGEN   DIM          1       57  Indigenous status
STATEID   DIM          1       58  State
LOCALITY  DIM         50       59  Suburb
POSTCODE  DIM          4      109  Post Code
OPELECT   DIM          1      113  Election Status
REFSRCE   DIM          2      114  Referral Source (Cat S)
OPFUNDSC  DIM          2      116  Funding source (CAT CL)
OOSTYPE   DIM          1      118  Non-admitted patient service type
MBS       DIM          9      119  Medical Benifits Schedule Iten
SESSID    DIM          20     128  Session Identifier    * after 01/07/2008
CANCER    DIM          1      148  Cancer                * after 01/07/2008
.
..end of record               149     * after 01/07/2008
.
. ----- Program Variables -----
BJDAY     FORM      3
CDYSDAYS  FORM      6                       * For CALCDAYS
CDYSFDTE  DIM       8                       * For CALCDAYS
CDYSTDTE  DIM       8                       * For CALCDAYS
CDYSYEAR  FORM      2                       * For CALCDAYS
CJDAY     FORM      3
CMDLINE   DIM       127
CPTDATE   DIM       8
DATOLIST  DIM       10
DHOUR     DIM       2
DMIN      DIM       2
DSEC      DIM       2
DSPCNT    FORM      5
DTLURGIN  DIM       8
DIM4      DIM       4
DIM4A     DIM       4
DIM8      DIM       8
ENDDTE    DIM       8
ERRMSG    DIM       98
FCMON     FORM      2
FCYEAR    FORM      2
FILENAME  DIM       8
IBCNMHOS  FORM      1
NEWFILE   FORM      1
NRFCDAYS  FORM      3
PSAGECON  DIM       3
PSAGETYP  DIM       1
RECCNT    FORM      4
SITE      DIM       3
STRTDTE   DIM       8
TOTALDAY  FORM      4
.
MTHNAM3   DIM       3
JAN       INIT      "Jan"
FEB       INIT      "Feb"
MAR       INIT      "Mar"
APR       INIT      "Apr"
MAY       INIT      "May"
JUN       INIT      "Jun"
JUL       INIT      "Jul"
AUG       INIT      "Aug"
SEP       INIT      "Sep"
OCT       INIT      "Oct"
NOV       INIT      "Nov"
DEC       INIT      "Dec"
.
PRGID     INIT      "IBAOUT56"
PRGNAM    INIT      "ACT HDP Outpatient Extraction"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                   ML0000                                   *
.*                                 Main Module                                *
.****************************************************************************** 
.
ML0000    CALL      INIT0000                * Initialise variables & open files 
.
ML1000    CALL      OPTN0000                * Choose main option
          BRANCH    OPTION,ML2000
          BRANCH    EXIT,ML9999
.
ML2000    CALL      IDAT0000                * Keyin the end date
          BRANCH    EXIT,ML9999
.
          CALL      KHOS0000                * Keyin hospital id  
          BRANCH    EXIT,ML9999
.
          CALL      FILE0000                * Keyin the extract filename
          BRANCH    EXIT,ML9999
.
          CALL      CONTQST                 * Ok to Continue (Y/N/C) ?
          BRANCH    CEXIT,ML4000,ML2000,ML1000
.
ML4000    DISPLAY   *P1:24,*EL,"Scanning : ",*P35:24,"Loading : ";
          CALL      PROC0000
.
          CALL      MVEXT000                * Move Extract for Web
          GOTO      ML1000
.
ML9999    CHAIN     PGM
.******************************************************************************
.*                                  INIT0000              Called by: ML0000   *
.*                       Initialise Variable & Open Files                     *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen heading
.
          DISPLAY   *P56:24,"Opening controlf"
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA1,"patvisaf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"outbokaf";
          OPEN      OUTBOKA6,"outbokaf"
.
          DISPLAY   *P64:24,"outbb1af";
          OPEN      OUTBB1A1,"outbb1af"
          OPEN      OUTBB1A2,"outbb1af"
.
          DISPLAY   *P64:24,"outctyaf";
          OPEN      OUTCTYA1,"outctyaf"
.
          DISPLAY   *P64:24,"ibapostf";
          OPEN      IBAPOST1,"ibapostf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdrgaf";
          OPEN      PATDRGA1,"patdrgaf"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"pmsccdaf";
          OPEN      PMSCCDA1,"pmsccdaf"
.
          DISPLAY   *P64:24,"patct1cf";
          OPEN      PATCT1C1,"patct1cf"
.
          CALL      IBACLOCK
.
          READ      CONTROLF,TEN;*54,CFILENO
          READ      CONTROLF,NINTY8;*81,PTCNHCOD
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
INIT9000  MOVE      ZERO,EXIT
.
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000              Called by: ML0000   *
.*                                Select Option                               *
.******************************************************************************
OPTN0000  MOVE      ZERO,EXIT
          MOVE      ZERO,OPTION
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Run":
                    *P1:8,"Select Option : ";
OPTN1000  KEYIN     *P17:8,*V2LON,*DV,UNDLN2,*P17:8,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN9000
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                  IDAT0000                                  *
.*        input ending date                              called by : MAINLINE *
.******************************************************************************
IDAT0000  DISPLAY   *P1:10,*EL,"Start Date : ":
                    *P1:12,*EL,"End   Date : ";
.
          MOVE      "10",CVERT
          MOVE      "15",CCOL
          UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          MOVE      ZERO,CHIGHLT
          MOVE      ZERO,CDEFDTE
          CALL      KEYDATE
          BRANCH    OVRCD,IDAT9500
          PACK      STRTDTE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDTE
.
          MOVE      ZERO,EXIT
.
.         set up input requirements for KEYDATE
.
          MOVE      ZERO,CDEFDTE
          MOVE      ZERO,CHIGHLT
          MOVE      "15",CCOL
          MOVE      "12",CVERT
.
.         input ending date
.
IDAT1000  UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          CALL      KEYDATE
          BRANCH    OVRCD,IDAT9500
.
          PACK      ENDDTE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDTE
.
          MOVE      ZERO,EXIT
.
          MATCH     ENDDTE,STRTDTE
          GOTO      IDAT9999 IF EQUAL
          GOTO      IDAT9999 IF LESS
.
          UNPACK    STRTDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P1:24,*EL,*B,*V2LON,"End Date must be greater than ":
                    "Start date ",CPCDATE,".  ";
          CALL      HITENTER
          GOTO      IDAT0000
.
IDAT9500  MOVE      ONE,EXIT
.
IDAT9999  RETURN
.
+
.******************************************************************************
.*                                  FILE0000              Called by: ML0000   *
.*                         Keyin The Extract Filename                         *
.******************************************************************************
FILE0000  MOVE      ZERO,NEWFILE
          MOVE      "OUT",KEY3
          UNPACK    ENDDTE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      CPTDATE,ENDDTE
          CALL      GPERD
          PACK      FILENAME,KEY3,MTHNAM3,CYEAR,SP70
          REPLACE   " 0",FILENAME
.
          DISPLAY   *P1:16,*EL,"Extract Filename : ":
                           *V2LON,*+,FILENAME,".txt";
.
          MATCH     "20080701",ENDDTE
          IF        !@LESS
            MOVE      ONE,NEWFILE
          ENDIF
.
          IF        NEWFILE=1
            PREP      EXTRCAS2,FILENAME
          ELSE
            PREP      EXTRCASF,FILENAME
          ENDIF
.
FILE9999  RETURN
+
.******************************************************************************
.*                                  PROC0000              Called by: ML0000   *
.*                              Run the extract                               *
.******************************************************************************
PROC0000  CALL      HEAD0000
.
          PACK      KEY16,STRTDTE,SP70           
          CALL      RDSBOKB2                * Position on & read a treatment
PROC1000  CALL      RDKBOKB2                  file record
          BRANCH    OVRCD,PROC9999
.
          MATCH     STRTDTE,OBADATE
          GOTO      PROC9999 IF LESS        * vist date< Start date
.
          MATCH     OBADATE,ENDDTE
          GOTO      PROC9999 IF LESS        * End date < visit date
.
          ADD       ONE,DSPCNT              * Increment the record counter
          IF        DSPCNT%10=1
            DISPLAY   *P12:24,*V2LON,OBAOUTNO;
          ENDIF
.
          CALL      VPTD0000                * Validate patient details
          BRANCH    EXIT,PROC1000
.
          CALL      CLEAR000 
          CALL      MOVE0000              
          CALL      WRITE000
.
          GOTO      PROC1000
.
PROC9999  RETURN
+
.******************************************************************************
.*                                  VPTD0000              Called by: GTRE0000 *
.*                          Validate Patient Details                          *
.******************************************************************************
VPTD0000  PACK      KEY8,OBAOUTNO,SP70
          CALL      RDPTVIS1
          BRANCH    OVRCD,VPTD9500
.
          MATCH     " 2",PTVITYPE            * outpatient visit
          GOTO      VPTD9500 IF NOT EQUAL
.
          IF        NEWFILE=1
            MATCH     SP70,OPCLINIC
            IF        @EQUAL
              MOVE      "OPCLINIC is blank",ERRMSG
              CALL      PRNT0000          * Print error message
            ENDIF
          ENDIF
          
          PACK      KEY36,PVIBILL,PVIDATE,SP70
          CALL      RDSBOKA6
          CALL      RDKBOKA6
          IF        OVRCD=1
            MOVE      "Missing booking file record",ERRMSG
            CALL      PRNT0000          * Print error message
            GOTO      VPTD9500
          ELSE
            MATCH     PVIBILL,OBAOUTNO
            IF        !@EQUAL
              MOVE      "Missing booking file record",ERRMSG
              CALL      PRNT0000          * Print error message
              GOTO      VPTD9500
            ENDIF  
          ENDIF
.
          PACK      KEY12,OBASITE,OBACTYP,SP10
          CALL      RDCTYA1
          BRANCH    OVRCD,VPTD2000
.
          MATCH     SP3,OCTGRP
          GOTO      VPTD2000 IF EQUAL
.
          PACK      KEY5,ANSC,ANSG,OCTGRP,SP10
          CALL      RDCODE1
          BRANCH    OVRCD,VPTD2000
.
          MATCH     "999",THCSCOD                * Don't report clinic     
          GOTO      VPTD9500 IF EQUAL
.
VPTD2000  PACK      KEY8,OBAURNO,SP70
          CALL      RDMAST1
          IF        OVRCD=1
            MOVE      "Missing master file record",ERRMSG
            CALL      PRNT0000          * Print error message
            GOTO      VPTD9500
          ENDIF
.
          PACK      KEY8,OBAURNO,SP70
          CALL      RDPMPX21
          IF        OVRCD=1
            MOVE      "Missing master extn file record",ERRMSG
            CALL      PRNT0000          * Print error message
            GOTO      VPTD9500
          ENDIF
.
          PACK      KEY8,OBAOUTNO,SP70
          CALL      RDPMVX11
          IF        OVRCD=1
            MOVE      "Missing visit extension file record",ERRMSG
            CALL      PRNT0000          * Print error message
            GOTO      VPTD9500
          ENDIF
.
          IF        IBCNMHOS=1
            MATCH     PMVXMHOS,PTHSHOSP
            GOTO      VPTD9500 IF NOT EQUAL
          ENDIF
.
VPTD9000  MOVE      ZERO,EXIT
          GOTO      VPTD9999
.
VPTD9500  MOVE      ONE,EXIT                * Ignore flag - yes
          GOTO      VPTD9999
.
VPTD9999  RETURN
+
.******************************************************************************
.*                                  HEAD0000              Called by: ML0000   *
.*                             Print Report Header                 & PRNT0000 *
.******************************************************************************
HEAD0000  CALL      HEAD132                 * Print report header
          PRINT     *N;
          UNPACK    STRTDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *35,"Starting Date : ",CPCDATE
          UNPACK    ENDDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *35,"Ending Date   : ",CPCDATE
.
          CALL      UND132

          PRINT     *1,"| Visit No":
                    "|Error Message",*132,"|"
          CALL      UND132
          ADD       "2",CLNO
.
HEAD9000  MOVE      ZERO,EXIT
HEAD9999  RETURN
+
.******************************************************************************
.*                                  PRNT0000              Called by: VALD0000 *
.*                             Print Error Message                            *
.******************************************************************************
PRNT0000  COMPARE   CLNO,FIFTY8
          CALL      HEAD0000 IF LESS        * Print report header
.
          PACK      ERRMSG,ERRMSG,SP70
          PRINT     *1,"|",OBAOUTNO:
                    "| ",ERRMSG,"|"
          ADD       ONE,CLNO
          PACK      ERRMSG,SP70
.
PRNT9000  MOVE      ZERO,EXIT
PRNT9999  RETURN
+
.******************************************************************************
. Move Extract to Web Directory
.******************************************************************************
MVEXT000  CLEAR     CMDLINE
          APPEND    "ibaout56.us1 ",CMDLINE
          APPEND    FILENAME,CMDLINE
          APPEND    " YTD",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
MVEXT999  RETURN
+
.******************************************************************************
.*                                  MOVE0000              Called by: PROC0000 *
.*                         Move in the extract values                         *
.******************************************************************************
MOVE0000  IF        IBCNMHOS=1
            PACK      HOSPID,PTHSHDPC,SP10            * Hospital ID
          ELSE
            MOVE      CFILENO,HOSPID
          ENDIF
.
          PACK      PIN,OBAURNO,SP70                  * Patient ID Number
          PACK      PMI,OBAURNO,SP70                  * Patient ID Number
          PACK      EVENTID,OBAOUTNO,SP70             * Patient Event Number
.
          MOVE      PSEX,SEX
          REP       "M1F2I3R3U4",SEX                  * Sex
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          PACK      DOB,CDAY,CMON,CCENT,CYEAR,SP70    * Dob
.
          MATCH     SP70,OBABKDTE
          IF        !@EQUAL
            UNPACK     OBABKDTE,CCENT,CYEAR,CMON,CDAY
            PACK       LISTDATE,CDAY,CMON,CCENT,CYEAR * Listing date for care
          ENDIF
.
          MATCH     SP70,OBADATE
          IF        !@EQUAL
            UNPACK    OBADATE,CCENT,CYEAR,CMON,CDAY   
            PACK      PRESDATE,CDAY,CMON,CCENT,CYEAR  * Date Patient presents
          ENDIF
.
          UNPACK    OBADATE,CCENT,CYEAR,CMON,CDAY   
          UNPACK    OBASTRT,CHOUR,ANS,CMIN
          PACK      SESSID,CDAY,CMON,CCENT,CYEAR,OBACLIN,CHOUR,CMIN,SP70
.
          PACK      CANCER,ANSN,SP70
          MATCH     SP3,OBOUSR1
          IF        !@EQUAL
            PACK      KEY5,ANSO,ONE,OBOUSR1,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              MOVE      TCINDC1,CANCER
            ENDIF
          ENDIF
.
          PACK      KEY12,OBASITE,OBACTYP,SP10
          CALL      RDCTYA1
          BRANCH    OVRCD,MOVE1000
          MATCH     SP3,OCTGRP
          GOTO      MOVE1000 IF EQUAL
.
          PACK      KEY5,ANSC,ANSG,OCTGRP,SP10
          CALL      RDCODE1
          BRANCH    OVRCD,MOVE1000
.
          UNPACK    THCSCOD,CLINTYPE                  * Clinic Type
.
MOVE1000  MATCH     SP70,CLINTYPE
          IF        @EQUAL
            MOVE     " 0",CLINTYPE
          ENDIF
.
          MATCH     SP70,OTBBCIND
          IF        !@EQUAL
            PACK      KEY5,ANSC,ANSI,OTBBCIND,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              MOVE      THCSCOD,HOSPCLIN              * Hospital Clinic
              MOVE      THCSCOD,OPCLINIC              * Outpatient Clinic
            ENDIF
          ENDIF
          MATCH     SP70,HOSPCLIN
          IF        @EQUAL
            MOVE      "  0",HOSPCLIN
          ENDIF
.
          PACK      KEY5,ANSC,ANSV,OBAVISIT
          CALL      RDCODE1
          IF        OVRCD<>1
            MATCH     ANSR,TCINDC1
            IF        @EQUAL
              MOVE      TWO,NRSTATUS                * New/Repeat Status
            ELSE
              MOVE      ONE,NRSTATUS
            ENDIF
.
            MOVE      THCSCOD,SERVTYPE
            MOVE      THCSCOD,OOSTYPE
          ENDIF
          REP       " 0",NRSTATUS
.
          MATCH     SP70,PMPXABRG
          IF        !@EQUAL
            PACK      KEY5,ANSV,ANSA,PMPXABRG,SP70
            CALL      RDCODE1
            IF        OVRCD = 0
              MATCH     SP4,THCSCOD
              IF        !@EQUAL
                UNPACK    THCSCOD,INDIGEN             * Indigenous status
              ENDIF
            ENDIF
          ENDIF
          REP       " 9",INDIGEN
.
          PACK      POSTCODE,PPOST,SP70               * Postcode
.
          PACK      KEY56,PPOST,PSUBRB,SP10,PADD4,SP70                *C-240184
          CALL      RDIBPOS1
          IF        OVRCD = 0 
            PACK      STATE,IBPOASGC                * State
            PACK      STATEID,IBPOASGC              * State
            MOVE      IBPOSUBR,LOCALITY             * Suburb
          ELSE                                                        *I-240184
            PACK      KEY56,PPOST,PSUBRB,SP70
            CALL      RSIBPOS1
            CALL      RKIBPOS1
            BRANCH    OVRCD,MOVE4000
            MATCH     PPOST,IBPOPCOD
            IF        @EQUAL
              MATCH     PSUBRB,IBPOSUBR
              IF        @EQUAL
                PACK      STATE,IBPOASGC            * State
                PACK      STATEID,IBPOASGC          * State
                MOVE      IBPOSUBR,LOCALITY         * Suburb
              ENDIF
            ENDIF
          ENDIF                                                 * end *I-240184
MOVE4000  REP       " 0",STATE
          REP       " 0",STATEID
.
          PACK      KEY5,ANSS,SP1,OBASRCR,SP70
          CALL      RDCODE1
          IF        OVRCD=0
            MOVE      THCSCOD,REFSRCE                 * Referral Source
            MATCH     "86",HOSPID                     * Calvary Private
            IF        @EQUAL
              MATCH     " 9",REFSRCE                  * Emergency dept
              IF        @EQUAL
                MOVE      " 3",REFSRCE                * Calvary Public
              ENDIF
            ENDIF
          ENDIF
          MATCH     SP70,REFSRCE
          IF        @EQUAL
            MOVE      "99",REFSRCE
          ENDIF
.
          REP       " 0",ELECT
          REP       " 0",OPELECT
.
          PACK      KEY5,ANSC,ANSL,OBACOMP,SP70
          CALL      RDCODE1
          IF        OVRCD<>1
            MOVE      THCSCOD,OPFUNDSC                * Funding Source
.
            MOVE      ZERO,FORM1
            WHILE     FORM1 <= 5
              LOAD      D1,FORM1,TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5
              MATCH     "V",D1
              IF        @EQUAL
.         Get DVA Number using concession card details
.
i               MOVE      OBAURNO,PURNO
                MOVE      THREE,DIM1                  * set for DVA card type
                CALL      GCCARD00                    * get DVA card details

                MOVE      PMCDCNUM,DVANO                * DVA Number
              ENDIF
              ADD       ONE,FORM1
            DO
          ENDIF
          MATCH     SP70,OPFUNDSC
          IF        @EQUAL
            MOVE      "99",OPFUNDSC
          ENDIF
.
MOVE9999  RETURN
+
.******************************************************************************
.*                                  CLEAR000              Called by: ML0000   *
.*                         Clear the extract variables                        *
.******************************************************************************
CLEAR000  MOVE      SP70,HOSPID  
          MOVE      SP70,PIN     
          MOVE      SP70,EVENTID 
          MOVE      SP70,SEX     
          MOVE      SP70,DOB     
          MOVE      SP70,LISTDATE
          MOVE      SP70,PRESDATE
          MOVE      SP70,NRSTATUS
          MOVE      SP70,CLINTYPE
          MOVE      SP70,HOSPCLIN
          MOVE      SP70,OPCLINIC
          MOVE      SP70,INDIGEN 
          MOVE      SP70,STATE   
          MOVE      SP70,STATEID
          MOVE      SP70,LOCALITY
          MOVE      SP70,POSTCODE
          MOVE      SP70,ELECT   
          MOVE      SP70,OPELECT   
          MOVE      SP70,REFSRCE 
          MOVE      SP70,OPFUNDSC
          MOVE      SP70,SERVTYPE
          MOVE      SP70,OOSTYPE
          MOVE      SP70,DVANO   
          MOVE      SP70,MBS     
          MOVE      SP70,SESSID  
          MOVE      SP70,CANCER  
.
CLEAR999  RETURN
.
+
.******************************************************************************
.*                                  KHOS0000                                  *
.*                      Keyin hospital ID                                     *
.******************************************************************************
KHOS0000  MOVE       ZERO,EXIT
          COMPARE    ONE,IBCNMHOS
          GOTO       KHOS9999 IF NOT EQUAL
.
          MOVE       SP70,PTHSHOSP
          DISPLAY    *P1:14,*EL,"Hospital Id : ";
          MOVE       TEN5,ECOL
          MOVE       TEN4,EVERT
          MOVE       SP3,CKYIDEF3
          MOVE       ONE,CKYIMAND           * Mandatory
          OPEN       PATHSPA1,"pathspaf"
.
          CALL       PATHSPKY
          BRANCH     EXIT,KHOS9000,KHOS9000
.
KHOS3000  DISPLAY   *P20:14,*V2LON,PTHSNAME;
          MOVE       ZERO,EXIT
          GOTO       KHOS9999
.
KHOS9000  MOVE       ONE,EXIT
KHOS9999  RETURN
.
.******************************************************************************
.*                                  WRITE000              Called by: ML0000   *
.*                         Write the extract variables                        *
.******************************************************************************
WRITE000  IF        NEWFILE=1
            WRITE     EXTRCAS2,SEQ;HOSPID,PIN,PMI,EVENTID,SEX,DOB:
                                   LISTDATE,PRESDATE:
                                   NRSTATUS,OPCLINIC,INDIGEN,STATEID:
                                   LOCALITY,POSTCODE,OPELECT,REFSRCE,OPFUNDSC:
                                   OOSTYPE,MBS,SESSID,CANCER
          ELSE
            WRITE     EXTRCASF,SEQ;HOSPID,PIN,EVENTID,SEX,DOB,LISTDATE,PRESDATE:
                                   NRSTATUS,CLINTYPE,HOSPCLIN,INDIGEN,STATE:
                                   LOCALITY,POSTCODE,ELECT,REFSRCE,OPFUNDSC:
                                   SERVTYPE,DVANO,MBS
          ENDIF
.
WRITE999  RETURN
.==============================================================================
.
          INC       STD001IO
          INC       CALCDAYS                * Calculate diff between dates
          INC       CALCAGE 
          INC       PATCOMN3
          INC       PATHSPKY
.
          INC       IBAPOSIO/INC            * Postcode file
          INC       OUTBOAIO/INC            * Clinic Session Booking Table A
          INC       OUTBB1IO/INC            * Clinic Session Booking Table B 
          INC       PATHSPIO/INC
          INC       PATDSCIO/INC            * Discharge file
          INC       PATCODIO/INC            * Codes file
          INC       PATDRGIO/INC            * Date Range
          INC       PATMA1IO/INC            * Master file
          INC       PMSPX2IO/INC
          INC       PATVADIO/INC            * Transfer source file
          INC       PATVISIO/INC
          INC       PMSCCDIO/INC
          INC       PMSVX1IO/INC
          INC       PATCT1IO/INC
          INC       OUTCTYIO/INC
          INC       GCCARD00/PBL
+

.*****************************************************************************
.*  Include name          : PATWSRCH.PBL                                     *
.*  Function              : SEARCH THROUGH THE PMI FILE                      *
.*  Author                : B.G.Ackland                                      *
.*                                                                           *
.*  Entry Points          : SOUND000 - Sound-ex Search                       *
.*                          STRCT000 - Strict Surname Search                 *
.*                          DBRTH000 - DOB Search                            *
.*                                                                           *
.*  Parameters            : CVERT   - Starting line for display              *
.*                          SRCHOPT - Select what appears in                 *
.*                                    the last column of search              *
.*                                    0 = Deceased indicator                 *
.*                                    1 = Programmer Defined                 *
.*                                    2 = First line of Address              *
.*                                    3 = Two line surname enq.              *
.*                                                                           *
.*  EXTERNAL SUBROUTINES  : GETSVAR - Set up SRCHVAL for a U/R.              *
.*                                    Used when SRCHOPT = 1                  *
.*                                  - Set up SRCHDIAG, SRCHWBED              *
.*                                    and SRCHVAR                            *
.*                                    Used when SRCHOPT = 3                  *
.*                                                                           *
.*  RETURNS               : SRCHFLAG - 1 = Continue Enquiry                  *
.*                                     0 = Exit Search                       *
.*                                     2 = Next (DBRTH000 only)              *
.*                          CPPURNO  - U/R number selected                   *
.*                                                                           *
.*  MAJOR INCLUDES NEEDED : IBACOMM  - Common IBA Variables                  *
.*                          PATGSRFD - Surname Index file                    *
.*                          PATMA1FD - PMI file                              *
.*                          PATPR1FD - 0 U/R Master file                     *
.*                          PATMI1FD - Admissions file                       *
.*                          OUTPREFD - Pre-attendance details                *
.*                          PATSNDX  - Sound-ex Algorithm                    *
.*                          PATSNX2  - Soundex for given name                *
.*                          PATTRNFD - Patient bed transfer                  *
.*                                                                           *
.* MODIFICATIONS :                                                           *
.* ***************************************************************************
.*  V12.01.01 13/02/2026  Ebon Clements  TSK 0972731                         *
.*            Set ADMISSNO to GSRBILL which is a right justified zero if     *
.*            ADMISSNO is blank - DISPR100                                   *
.*  V12.00.02 03/06/2025  Ebon Clements  TSK 0955096                         *
.*            Set AADMNO to ZEROVISN - GETS1000                              *
.*  V12.00.01 21/05/2025  Ebon Clements  TSK 0955096                         *
.*            Alphanueric visit number changes                               *
.*  V11.05.01 01/05/2025  Nikitha B      TSK 0960102                         *
.*            Added SP70 to PMPXPFGN and PMPXPFSN at CHKPRV00 to avoid       *
.*            display of surname and givenname on Current Patient Search     *
.*  V11.04.01 14/05/2024  Ebon Clements  TSK 0946706                         *
.*            Fixed waiting list record default key pack - DWAT0000          *
.*  V11.03.05 11/09/2023  Ebon Clements  TSK 0935498                         *
.*            Include gender code for alias display in DOB search - DISPR100 *
.*  V11.03.02 09/08/2022  Thanh T        TSK 0865060                         *
.*            Changed to use PTELEB for work phone instead of PTELEP         *
.*  V11.03.01 13/07/2022  Thanh T        TSK 0865060                         *
.*            Changed SHEAD000 and TABROW00 to show telephone and claim      *
.*            details                                                        * 
.*****************************************************************************
.*  V11.02.01 17/06/2022  Davin          TSK 0913831                         *
.*            Display all alias records when searching by DOB (CHOUT200)     *
.*  V11.01.01 21/05/2021  Don Nguyen     TSK 0900780                         *
.*            Modified PRNLIN00 to make Ward & Hospital description text not *
.*            overlap other columns for Inpatient search. Added tooltip text.*
.*  V11.00.02 29/04/2020  Ebon Clements  TSK 0850105                         *
.*            Added theatre location display to current patient search       *
.*  V11.00.02 29/04/2020  Ebon Clements  TSK 0850105                         *
.*            Added theatre location display to current patient search       *
.*            GTHE1000 - OPCNTHED                                            *
.* ***************************************************************************
.*  V10.13.01 06.11.2018  B.G.Ackland    TSK 0835482                         *
.*            Fix FHIR Alias Output                                          *
.* ***************************************************************************
.*  V10.12.01 09.03.2017  Ebon Clements  TSK 0852062                         *
.*            Fixed set of age range from/to when doing a DOB search         *
.*            SRAGEFR and SRAGETO in SPARA000                                *
.* ***************************************************************************
.*  V10.11.01 13.09.2017  B.G.Ackland    TSK 0835482                         *
.*            Fix FHIR Output Not Showing Patient ID                         *
.*                                                                           *
.* ***************************************************************************
.*        V10.10.05 07/07/2017  Don Nguyen     TSK 0841807                   *
.*                  Restored cell width for 'Date of Birth (Age)' to the     *
.*                  previous value of '105' at TABROW92.                     *
.*                  Added 'padding' to the cells (style) so that the columns *
.*                  are aligned with the header row in TABROW00.             *
.*        V10.10.04 29/05/2017  Peter Vela     TSK 0838833                   *
.*                  FHIR Output Resolve Testing Issue with End of Search     *
.*        V10.10.03 29/05/2017  Peter Vela     TSK 0838833                   *
.*                  Fixed gotos in DISPR100                                  *
.*        V10.10.02 27/04/2017  Peter Vela     TSK 0813019                   *
.*                  Added Abandoned to GTHE0000                              *
.*                  FHIR Resource for Patient Search                         *
.*        V10.10.01 26/04/2017  Thanh T.       TSK 0837189                   *
.*                  Fixed the search error when the patient was deceased and *
.*                  not in the specified age range                           *
.*        V10.08.02 20/09/2016  Jill Parkinson TSK 0826142                   *
.*                  Moved clear of ADMISSNO in GETS0000 to be before deceased*
.*                  check                                                    *
.*        V10.08.01 21/07/2016  Jill Parkinson TSK 0814591                   *
.*                  Added output of alias dob and current dob                *
.*        V10.06.03 23/06/2015  Peter Vela     CAR 305156                    *
.*                  Check for empty AWARD in GDET1310                        *
.*        V10.06.02 10/06/2015  Peter Vela     CAR 316707                    *
.*                  Get ward value from slot template maintenance in         *
.*                  outpatient GDET0000                                      *
.*        V10.06.01 20/05/2015  Ania P         CAR 315557                    *
.*                  Added Hospital Column to Patient Search                  *
.*        V10.05.02 20/10/2014  Ebon Clements  CAR 268970                    *
.*                  Change to use OUTCLIFD index 1 instead of index 2        *
.*        V10.05.01 21/08/2014  Don Nguyen     CAR 303878                    *
.*                  Added ability to display District Health Board (FADDRES3)*
.*                  for NZ states (PTCNHDPS=1)                               *
.*        V10.04.01 22/01/2014  Ebon Clements  CAR 295847                    *
.*                  Changed get OP details by visit to use index 6 - GDET1220*
.*        V10.03.16 12/12/2013  Don Nguyen     CAR 244776                    *
.*                  Fixed bug where patient records were missing from        *
.*                  subsequent results page (i.e. page 2 onwards) when       *
.*                  Alternate ID display turned on.                          *
.*                  Fixed DISPR000 (moved GSRURNO to URNUMBER before calls   *
.*                  to CHCALT00 & DISALI00).                                 *
.*                  Also fixed DISALI00; not output row if DISNUMB>=NORECORD *
.*        V10.03.15 24/10/2013  Don Nguyen     CAR 285649                    *
.*                  Fixed DISALI00 to loop back to DISALI20 when the         *
.*                  condition STARTNUM>=RECNUMB is met.                      *
.*        V10.03.14 24/10/2013  Ebon Clements  CAR 291974                    *
.*                  Added SRCHEBST to exclude EBS created temp U/R numbers   *
.*                  PSTAT=4 from the standard patient search                 *
.*        V10.03.13 17/10/2013  Ebon Clements  CAR 230883                    *
.*                  Added DWAT0000 to select default WL record on WL search  *
.*        V10.03.12 08/10/2013  Don Nguyen     CAR 285649                    *
.*                  Packed SRCHGNAM into KEY98 in RSSTR000                   *
.*        V10.03.11 03/07/2013  Davin          CAR 287520                    *
.*                  Changed key17 to key20 for mrtmas read (doctyp00)        *
.*        V10.03.10 30/04/2013  Peter Vela     CAR 281511                    *
.*                  Loop back if theatre booking is cancelled @ GTHE0300     *
.*        V10.03.09 23/04/2013  Peter Vela     CAR 281511                    *
.*                  Added Theatre Status display if days since op = 0 @      *
.*                  GTHE0000                                                 *
.*        V10.03.08 01/02/2013  Patrick Adair  CAR 263963                    *
.*                  Alter Address line format to display over two lines      *
.*                  using NAMST1CD (clone of NAMSTRCD to allow for 71 chars) *
.*        V10.03.07 15/02/2012  Ebon Clements  CAR 259881                    *
.*                  Added DOB test to strict surname and given name test     *
.*                  in soundex RKSND350 and RPSND350                         *
.*        V10.03.06 13/02/2012  Ebon Clements  CAR 259881                    *
.*                  Clear second given name PTGSGNM2 before calls to SOUNDX2 *
.*        V10.03.05 27/01/2012  Ebon Clements  CAR 259018                    *
.*                  Fixed clear if LPCONDD at GETCON10                       *
.*        V10.03.04 27/01/2012  Ebon Clements  CAR 259024                    *
.*                  Fixed display of create by hospital - FCBY0000           *
.*        V10.03.03 20/12/2011  Mike Laming    CAR 257255                    *
.*                  Rebuild a stripped PTMASNAM for a read in GETCON00       *
.*        V10.03.02 25/11/2011  Peter McMullen CAR 252088                    *
.*                  Fix inconsistent soundex/soundex-strict behaviour        *
.*        V10.03.01 11/11/2011  Jill Parkinson CAR 253901                    *
.*                  Added check if selected visit is another hosp (GETS0000) *
.*        V10.02.07 19/10/2011  Peter McMullen CAR 253341                    *
.*                  Format wards for IP expected or admit point ward         *
.*        V10.02.06 23/09/2011  Jill Parkinson CAR 248486                    *
.*                  Added hospital to status descriptions                    *
.*        V10.02.05 26/08/2011  Jill Parkinson CAR 249295                    *
.*                  Added PTGSGNM2 at TABROW92                               *
.*        V10.02.04 13/07/2011  Steve Armstrong   CAR 240722                 *
.*                  Further mods to cater for second given name as           *
.*                  part of PATGSRFD keys                                    *
.*        V10.02.03 04/07/2011  Peter McMullen CAR 240725                    *
.*                  Mods for PTCNWBDS=2 (short ward/bed description)         *
.*        V10.02.02 25/06/2011 Steve Armstrong    CAR 240722                 *
.*                  Mods to cater for changes to PATLOCFD.                   *
.*                       - LSNAME & LGNAME extended to 40 chars.             *
.*                       - PTLCGNM2 added.                                   *
.*                  Mods to cater for changes to PATGSRFD:                   *
.*                       - GSRSNAM & GSRGNAM extended to 30 chars.           *
.*                       - PTGSGNM2 added.                                   *
.*                       - all indexes changed in length.                    *
.*        V10.02.01 26/05/2011  Peter McMullen CAR 240725                    *
.*                  Output ward short desc instead of ward code              *
.* ***************************************************************************
.*        V10.01.01 23/02/2011  Jill Parkinson CAR 238283                    *
.*                  Added class=nowrap at tabrow90                           *
.* ***************************************************************************
.*        V9.12.04  08/02/2010  Ebon Clements  CAR 215500                    *
.*                  Only display ED discharge date when discharged - GDET0000*
.*        V9.12.03  17/12/2009  Peter McMullen CAR 210130                    *
.*                  Convert to newer Dr name format routine DOCNM000         *
.*        V9.12.02  16/12/2009  Ebon Clements CAR 207836                     *
.*                  Don't display phone extn on the current patient search   *
.*                  if the patient is discharged - GDET0000                  *
.*        V9.12.01  27/11/2009  Saroeun Soeur CAR 209949                     *
.*                  added check PTCNDRSM if ddes or dstat should be used     *
.* ***************************************************************************
.*        V9.11.01  22/04/2009  Jill Habasque CAR 145706                     *
.*                  Added gp access check                                    *
.* ***************************************************************************
.*        V9.10.01  10/07/2008  Ebon Clements CAR 171268                     *
.*                  Ignore cancelled ED visits - GETS0000                    *
.* ***************************************************************************
.*        V9.09.07  18/12/2007  Ebon Clements CAR 155246                     *
.*                  Bold phone extn if currently admitted                    *
.*        V9.09.06  11/12/2007  Ebon Clements CAR 142954                     *
.*                  Added SRCHCL21 for cordless telephone extn               *
.*        V9.09.05  14/11/2007  Jill Habasque CAR 129420                     *
.*                  Added add one to RECNUMB in DISALI00                     *
.*        V9.09.04  12/11/2007  Peter Vela    CAR 151199                     *
.*                  Added new Emergency Cancellation status                  *
.*        V9.09.03  12/10/2007  Davin         CAR 151896                     *
.*                  Added check of ptcnwbds for displaying ward/bed descrip. *
.*        V9.09.02  05/09/2007  Jill Habasque CAR 139874                     *
.*                  Added pmvxmhos to srchcl02 for emr patients              *
.*        V9.09.01  19/09/2007  Jill Habasque CAR 90670 and 101699           *
.*                  Uncommented call to CHDOB000 to allow age range searches *
.*  *****Note*****  Add patweb90 to RUN macro if internal server errors occur*
.* ***************************************************************************
.*        V9.08.02  29/03/2007  Janet George  CAR 109028                     *
.*                  Changed Alias and Alternate ID icons to redirect         *
.*                  to View Only templates from Search page                  *
.*        V9.08.01  08/12/2006  Ebon Clements CAR 123752                     *
.*                  Fixed GDET0000 display of emergency triage date          *
.* ***************************************************************************
.*        V9.05.01  27/03/2006  Jill Habasque CAR 78520                      *
.*                  Added output of Created by                               *
.*        V9.05.B01 20/12/2005  Ebon Clements CAR 76341                      *
.*                  Key length changes for encounter number                  *
.* ***************************************************************************
.*        V9.04.06  11/10/2005  Ebon Clements CAR 77367                      *
.*                  Commented out call to CHDOB000                           *
.*        V9.04.05  08/06/2005  Jill Habasque CAR 63400                      *
.*                  Added upcase of GSRSNAM                                  *
.*        V9.04.04  03/05/2005  Jill Habasque CAR 59276                      *
.*                  Mods to use PBDATE if surname and given name are the same*
.*        V9.04.03  28/01/2005  Davin          CAR 56361                     *
.*                  Added chkrel00 routine (check 'release to' fields)       *
.*        V9.04.02  20/01/2004  Peter Vela     CAR 54756                     *
.*                  Fixed PACK to SRCHCL11 (PRNLIN00)                        *
.*                  10/12/2004  Jill Habasque  CAR 52833                     *
.*                  Added check of PTCNDISA - Display Alternate ID's         *
.*        V9.04.01  07/09/2004  Jill Habasque                                *
.*                  Added line to include trailing zeros in RSAGN000         *
.* ***************************************************************************
.*        V9.03.06  18/06/2004  Jill Habasque CAR 44641                      *
.*                  Added output of Alternate ID's                           *
.*        V9.03.05  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2      *
.*                  - Add Sex Description routine SEXDSC00 with Code "R" -    *
.*                  "Intersex" added                                          *
.*       V9.03.04  26/05/2004 Jill Habasque CAR 48414                        *
.*                 Added new soundex search output in order of given name    * 
.*       V9.03.03  15/04/2004 Jill Habasque CAR 45423                        *
.*                 Mods for oracle                                           * 
.*       V9.03.02  29/09/2003 Sylvek Litewka SRF 43302                       *
.*                 Modified procedure GTHE0000 to read Theatre Operation     * 
.*                 Details file (oprdetaf) for all Booking Status values     *
.*                 (i.e. 0,1,2 and 4 - not just 0)                           *
.*       V9.03.01  21/07/2003 Sylvek Litewka SRF 38345                       *
.*                 Modified procedure GTHE0000 to display the "Estimated     *
.*                 Discharge Time" in the "Disch.Date Disch.Time" column.    *
.* ***************************************************************************
.*       V9.02.26  18/03/2003 Jill Habasque SRF 36585                        *
.*                 Changed to displays ambulatory status for events          *
.*       V9.02.25  06/03/2003 Jill Habasque SRF 36585                        *
.*                 Changed to displays visitors and phone calls for events   *
.*       V9.02.24  27/02/2003 Jill Habasque SRF 17841                        *
.*                 Changed to pack instead of move for gsrsnam               *
.*       V9.02.23  03/12/2002 Jill Habasque SRF 23732                        *
.*                 Changed to display description for outpatient clinic type *
.*                 SRF 33655 Changed to use tward,tbed for discharged patient*
.*       V9.02.22  08/10/2002 Jill Habasque SRF 23810                        *
.*                 Added numloop to stop errors when not enough records found*
.*                 08/11/2002 Ebon Clements SRF 23307                        *
.*                 Added read of pmsvx1af in GDET1400 for inpatient events   *
.*       V9.02.21  17.10.2002 Ebon Clements SRF 23307                        *
.*                 Changed GDET0000 to set up phone number and condition     *
.*                 for patient events                                        *
.*       V9.02.20  03.10.2002 Jill Habasque SRF 22308                        *
.*                 Changed GDET0000 to display the exp ward or admitting     *
.*                 point if award is blank                                   *
.*       V9.02.19  24.09.2002 Ebon Clements SRF 20258                        *
.*                 Changed GDET0000 to display the outpatient clinic type    *
.*                 in the unit field.  OBACTY - SRCHCL03                     *
.*       V9.02.18  29.08.2002 Jill Habasque SRF 18580                        *
.*                 Changed to use GSRDOB instead of pbdate                   *
.*                 SRF 18875 - Added output of alias icon if an alias exists *
.*                 SRF 17625 - Added new index by date of birth and parameter*
.*                 PTCNSTGV - strict given name in soundex                   *
.*       V9.02.17  03.07.2002 Jill Habasque SRF 19723                        *
.*                 Added transferred to other hospital name                  *
.*       V9.02.16  27.05.2002 Jill Habasque 3693                             *
.*                 Mods to display each UR only once                         *
.*       V9.02.15  05.04.2002 Pat Dirito  2724                               *
.*                 Added Patient Extension - SRCHCL18                        *
.*       V9.02.14  03.04.2002 Pat Dirito  2724                               *
.*                 Fixed </td> in PRNLIN00 and the setting of SRCHCL17       *
.*       V9.02.13  15.03.2002 Jill Habasque  2419                            *
.*                 Commented out match for CHKID,UROUTPUT in CHOUT000        *
.*                 to make all alias's display                               *
.*       V9.02.12  16.01.2002 Jill Habasque                                  *
.*                 Added output of op date and days since op                 *
.*       V9.02.11  28.12.2001 Jill Habasque                                  *
.*                 Removed nowrap from TABROW90 so postcode can be seen      *
.*       V9.02.10  28.12.2001 Jill Habasque                                  *
.*                 Fixed nursing extension to only display if phone allowed  *
.*       V9.02.09  27.12.2001 Jill Habasque                                  *
.*                 Mods for new current patient search                       *
.*       V9.02.08  27.11.2001 Glenn Saunders                                 *
.*                 Change views for SRCHFMT 6 to allow Standard (St.Vs) and  *
.*                 RCH to have different views. See KIDSONLY form variable.  *
.*       V9.02.07  26/11/2001 J. Habasque                                    *
.*                 Added read previous admission for current inpatient search*
.*                 in GCUR0000                                               *
.*       V9.02.06  22/10/2001 J. Habasque                                    *
.*                 Moved check for deceased patient                          *
.*       V9.02.05  19/10/2001 J.Tan                                          *
.*                 Mods GCUR0000 to check for deceased patient first  and    *
.*                 check for the current date for outpatients                *
.*       V9.02.04  06/10/2001  Glenn Saunders                                *
.*                ~Chg patient folder determination to stnd routine PATFLD00 *
.*                ~Chg patient privacy determination to field PMPXPRVI.      *
.*                 [Visit file fld PMVXPIND, is not currently populated.]    *
.*       V9.02.03  04/10/2001  Jill Habasque                                 *
.*                 Added routine JAVNAM00 to change ' in names to %60        *
.*       V9.02.02  02/10/2001  J.Tan                                         *
.*                 Mods GCUR0000 to set exit flag for missing adm or disc    *
.*       V9.02.01  28/08/2001  J.Tan                                         *
.*                 Added Theatre status                                      *
.* ***************************************************************************
.*       V9.00.03  14/08/2001  J.Tan                                         *
.*                 Modified for Current Patient Search (SRCHFMT =6)          *
.*       V9.00.02  03/08/2001  J.Tan                                         *
.*                 Mods to display Allied Health and Event patients          *
.*       V9.00.01  19/06/2001  Ebon Clements                                 *
.*                 Added on leave status description if astat is 4           *
.*       V9.00.00  14/05/2001  Ebon Clements                                 *
.*                 Added SRCHFMT five for current patient search             *
.* ***************************************************************************
.*       V5.10.05  04/05/2001  Ebon Clements                                 *
.*                 Added privacy indicatur to status column                  *
.*       V5.10.04  26/04/2001  Ebon Clements                                 *
.*                 Fixed second index key size for reverse sound-ex          *
.*       V5.10.03  17/04/2001  Glenn Saunders                                *
.*                 Remove access to PTCNSNDX and associated processing.      *
.*                 Remove all references to PATSUR file (old surname file).  *
.*       V5.10.02  06/03/2001 Ebon Clements                                  *
.*                 Changed table output of U/R number to be bold             *
.*       V5.10.01  05/03/2001 J.Tan                                          *
.*                 Mods Surname search not to call SOUND0000 (soundex search)*
.* ***************************************************************************
.*       V5.08.03  11/01/2001 J.Tan                                          *
.*                 Mods Date of birth search to use patgsrnf file            *
.*       V5.08.02  04/12/2000 Katie                                          *
.*                 Initialise status variables when there is a zero U/R      *
.*       V5.08.01  05/09/2000  Tonii                                         *
.*                 Mods to accept Unknown and Indeterminate as valid patient *
.*                 sex description                                           *
.*       V5.08.00  19/05/2000 Pat Dirito                                     *
.*                 Added div tags for scroll bars in Patient Search          *
.*                 Table Heading                                             *
.* ***************************************************************************
.*       V5.07.00  25/11/1999  Nicole Harrington                             *
.*                 Added Date of Birth Routine (DBRTH000) from PATSRCH       *
.* ***************************************************************************
.*       V5.06.03  20.01.1999  B.G.Ackland                                   *
.*                 Copied from PATSRCH                                       *
.*                                                                           *
.*       V5.06.02  10/12/1998  Davin                                         *
.*                 Increment array counter (CSRHNO) by 1, not 2, when        *
.*                 displaying previous record (PREVS580 - 2 lines per pat.)  *
.*                 09/12/1998  Steve Armstrong                               *
.*                 Fixed resetting of FORM2 after returning from calc. of    *
.*                 patient age (when displaying 2 lines per patient DISPR580)*
.*****************************************************************************
.*********************************************************************
.*  SOUND000  :  Sound-ex search                                     *
.*********************************************************************
SOUND000  CALL      RDPAR000                    * read system parametres
.
          IF        PTCNSTGV=1
            CALL      SDXAA000                  * using strict given name search
            GOTO      SOUND999
          ENDIF 
.
          IF        PTCNSTGV=2
            CALL      SDXGN000                  * using strict given name search
            GOTO      SOUND999                  * with given name order
          ENDIF 
.
          CALL      SPARA000                     * keyin Search Parameters
          CALL      SHEAD000                     * display header
          MOVE      ZERO,CMSXFLAG
.
.------   Using Closest Match in Soundex Search?
SOUND090  IF        PTCNCMSX=1
            CALL      SCMSX000                   * Search for Strict Matches
            UNPACK    SP70,GSRSKEY,GSRGKEY1,GSRGKEY2  
            IF        DISNUMB>=NORECORD
              GOTO      SOUND910
            ENDIF
          ENDIF
.
. loop thru the sound-ex order records
.
          MOVE      ZERO,FORM1                   * we are reading order records
          CALL      RSSND000                     * Positional Reads Soundex Srch
SOUND100  CALL      RKSND000                     * read next record
          BRANCH    OVRCD,SOUND300               * no more records
          CALL      DISPR000
          IF        DISNUMB>=NORECORD
            GOTO      SOUND910
          ENDIF
          GOTO      SOUND100                      * get next record (normal)
.
SOUND300  CALL      RSRSX000                   * Posit reads Reverse Surname
          BRANCH    EXIT,SOUND900              * not doing Reverse search
SOUND310  CALL      RKRSX000                   * Read Next record
          BRANCH    OVRCD,SOUND900             * no more records
          CALL      DISPR000
          IF        DISNUMB>=NORECORD
            GOTO      SOUND910
          ENDIF
          GOTO      SOUND310                      * get next record (normal)
.
. we have no more records to display
.
SOUND900  BRANCH    FHIRTYPE,SOUND999
          WRITE     HTMLFILE;"</table>"
          WRITE     HTMLFILE;"<p align=center><b>End of Search</b>"
          WRITE     HTMLFILE;"</div>" 
          GOTO      SOUND999                      * get next record (normal)
.
SOUND910  BRANCH    FHIRTYPE,SOUND999
          WRITE     HTMLFILE;"</table>"
          WRITE     HTMLFILE;"</div>" 
.
SOUND999  RETURN
+
.*********************************************************************
.*  STRCT000  :  Strict Surname search                               *
.*********************************************************************
STRCT000  CALL      RDPAR000                     * read system parametres
          CALL      SPARA000                     * keyin Search Parameters
          IF        SRCHFMT=6
            CALL      CHEAD000                   * display current patient
          ELSE
            CALL      SHEAD000                   * display header
          ENDIF
.
          CALL      RSSTR000                     * Positional Reads Strict Surn.
STRCT100  CALL      RKSTR000                     * read next record
          BRANCH    OVRCD,STRCT200               * no more records
          CALL      DISPR000
          IF        DISNUMB>=NORECORD
            GOTO      STRCT910
          ENDIF
          GOTO      STRCT100                      * get next record (normal)
.
. now try Reverse Strict search
.
STRCT200  CALL      RSRST000                     * Position reads Reverse Strict
          BRANCH    EXIT,STRCT900                * not doing Reverse search
          MOVE      ONE,FORM1                    * we are reading reverse recs
.
STRCT210  CALL      REVST000                     * get next record
          BRANCH    OVRCD,STRCT900               * no more records
          CALL      DISPR000
          IF        DISNUMB>=NORECORD
            GOTO      STRCT910
          ENDIF
          GOTO      STRCT210                      * get next record (normal)
.
STRCT900  BRANCH    FHIRTYPE,STRCT999
          WRITE     HTMLFILE;"</table>"
          WRITE     HTMLFILE;"<p align=center><b>End of Search</b>"
          WRITE     HTMLFILE;"</div>" 
          GOTO      STRCT999                      
.
STRCT910  BRANCH    FHIRTYPE,STRCT999
          WRITE     HTMLFILE;"</table>" 
          WRITE     HTMLFILE;"</div>" 
.
STRCT999  RETURN
.**************************************************************************
.*  RPSTR000  :  Read the previous record                                 *
.**************************************************************************
.
. --- using the given surname file ---
.
RPSTR000  BRANCH    SRCHHREC,RPSTR020   * using sdxsys as flag to say whether
                                        * we've had a full read or just a rds
          CALL      RPPTGSR2
          BRANCH    OVRCD,RPSTR999      * no more records
.
RPSTR020  MATCH     SRCHSNAM,GSRSNAM
          GOTO      RPSTR900 IF NOT EQUAL         * no more records
.
          MATCH     SRCHGNAM,SP70
          GOTO      RPSTR100 IF EQUAL
.
          MATCH     SRCHGNAM,GSRGNAM
          GOTO      RPSTR200 IF LESS
          GOTO      RPSTR300 IF NOT EQUAL
.
RPSTR100  MOVE      ZERO,SRCHHREC        * clear flag
          MOVE      ZERO,OVRCD           * valid record
          GOTO      RPSTR999
.
.        Records given name less than that wanted - jump forward
.
RPSTR200  PACK      KEY98,GSRSNAM,SRCHGNAM,SP70,SP70
          CALL      RDPTGSR2
          COMPARE   ZERO,OVRCD
          GOTO      RPSTR020 IF EQUAL
          CALL      RSPTGSR2
          MOVE      ZERO,SRCHHREC        * flag to say we did a rds
          GOTO      RPSTR000
.
.        We have finished the given names for this particular surname
.        try the next surname with the same starting string
.
RPSTR300  MOVE      "~",ANS
          PACK      KEY98,GSRSNAM,ANS,ANS,ANS,SP70,SP70
          CALL      RDPTGSR2
          COMPARE   ZERO,OVRCD
          GOTO      RPSTR020 IF EQUAL
          CALL      RSPTGSR2
          MOVE      ZERO,SRCHHREC        * flag to say we did a rds
          GOTO      RPSTR000
.
RPSTR900  MOVE      ONE,OVRCD            * no more records
.
RPSTR999 RETURN
.*********************************************************************
.*  SCMSX000  :  Closest Matches for Soundex Search -                *
.*               Do strict matches                                   *
.*********************************************************************
SCMSX000  CALL      RSSTR000                     * Positional Reads Strict Surn.
SCMSX100  CALL      RKSTR000                     * read next record
          BRANCH    OVRCD,SCMSX200               * no more records
          CALL      DISPR000
          IF        DISNUMB>=NORECORD
            GOTO      SCMSX999
          ENDIF
          GOTO      SCMSX100                     * go and display this record
.
. now try Reverse Strict search
.
SCMSX200  CALL      RSRST000                     * Position reads Reverse Strict
          BRANCH    EXIT,SCMSX999                * not doing Reverse search
          MOVE      ONE,FORM1                    * we are reading reverse recs
.
SCMSX210  CALL      REVST000                     * get next record
          BRANCH    OVRCD,SCMSX999               * no more records
          CALL      DISPR000
          IF        DISNUMB>=NORECORD
            GOTO      SCMSX999
          ENDIF
          GOTO      SCMSX210                      * get next record (normal)
.
SCMSX999  RETURN
.************************************************************************
.*  RDPAR000  :  Read in the system parametres                          *
.************************************************************************
RDPAR000  READ      CONTROLF,TEN;*246,SRCHDRNG,SRCHYRNG
          READ      CONTROLF,TWENTY1;*53,SRCHRSGS:         * Reverse Surn/Giv
                                     *54,SRCHPCKL          * Picking List
          READ      CONTROLF,FIFTY9;*231,PTCNRGNS
          READ      CONTROLF,SEVENTY7;*243,PTCNCMSX      
          READ      CONTROLF,EIGHTY;*229,PTCNDATE
          READ      CONTROLF,HUND09;*160,PTCNSTGV
          MOVE      ZERO,NUMLOOP
          RETURN
.********************************************************************
.*  RSSND000  :  Initialise for reading via sound-ex                *
.********************************************************************
.
RSSND000  PACK      GSRSNAM,SRCHSNAM,SP70          * set up soundex variable
          MOVE      ZERO,SRCHMACH                    
          CALL      SOUNDEX                        * Compute the soundex key
          MOVE      GSRSKEY,SRCHSNDX               * Save key for test in loop
          SCAN      SRCHMACH,SRCHSNDX       * ignore trailing zeros in match
          BUMP      SRCHSNDX,-1             * for surname soundex
          LENSET    SRCHSNDX
          RESET     SRCHSNDX
.
.    no second given name if gsrgkey2 starts with 0 (as first letter is kept)
.
          CMATCH    SRCHMACH,GSRGKEY2
          GOTO      RSSND600 IF EQUAL
.
          MOVE      ONE,SRCHHREC              * use as flag to say if rd or rds
          PACK      KEY34,GSRSKEY,GSRGKEY1,GSRGKEY2,SP70
          CALL      RDPTGSR3
          COMPARE   ZERO,OVRCD
          RETURN    IF EQUAL
          CALL      RSPTGSR3
          MOVE      ZERO,SRCHHREC             * set to rds                   
          GOTO      RSSND999
.
RSSND600  MOVE      ONE,SRCHHREC              * use as flag to say if rd or rds
          PACK      KEY34,GSRSKEY,GSRGKEY1,SP70
          CALL      RDPTGSR3
          COMPARE   ZERO,OVRCD
          RETURN    IF EQUAL
          CALL      RSPTGSR3
          MOVE      ZERO,SRCHHREC             * set to rds                   
.
RSSND999  RETURN
.************************************************************************
.*  RSSTR000  :  Do positional reads for strict surname                 *
.************************************************************************
.
RSSTR000  MOVE      ONE,SRCHHREC              * use as flag to say if rd or rds
          MOVE      SRCHSNAM,GSRSNAM
          RESET     GSRSNAM
          ENDSET    GSRSNAM
          APPEND    SP70,GSRSNAM
          RESET     GSRSNAM
          MATCH     SP70,SRCHPDOB
          IF        !@EQUAL
            PACK      KEY98,SRCHPDOB,GSRSNAM,SRCHGNAM,SP70,SP70
            CALL      RDPTGSR6
            BRANCH    OVRCD,RSSTR300
          ELSE
            PACK      KEY98,GSRSNAM,SRCHGNAM,SP70,SP70
            CALL      RDPTGSR2              * position on required name
            BRANCH    OVRCD,RSSTR300
          ENDIF
          GOTO      RSSTR999
.
RSSTR300  MATCH     SP70,SRCHPDOB
          IF        !@EQUAL
            CALL      RSPTGSR6               * position because no exact record
          ELSE
            CALL      RSPTGSR2               * position because no exact record
          ENDIF
          MOVE      ZERO,SRCHHREC             * set to rds                   
.
RSSTR999  RETURN
.************************************************************************
.*  RSRST000  :  Do positional reads for Reverse strict surname         *
.************************************************************************
RSRST000  MOVE       ONE,EXIT
          COMPARE    ZERO,SRCHRSGS         * reverse giv seq required
          GOTO       RSRST999 IF EQUAL
.
          MATCH      SRCHGNAM,SP70          * no given name entered,cant reverse
          GOTO       RSRST999 IF EQUAL
.
          MOVE       SRCHGNAM,SSDXSNAM
          SCAN       SP1,SSDXSNAM           * Remove any possible 2nd Given Name
          ENDSET     SSDXSNAM
          APPEND     SP30,SSDXSNAM
          RESET      SSDXSNAM
.
          MATCH      SP70,SRCHPDOB
          IF         !@EQUAL
            PACK       KEY98,SRCHPDOB,SSDXSNAM,SRCHSNAM,SP70,SP70
            CALL       RDPTGSR6
          ELSE
            PACK       KEY98,SSDXSNAM,SRCHSNAM,SP70,SP70
            CALL       RDPTGSR2
          ENDIF
.
          IF         OVRCD=0
            MOVE       ONE,SRCHHREC
          ELSE
            CALL       RSPTGSR2
            MOVE       ZERO,SRCHHREC
          ENDIF
          MOVE      ZERO,EXIT
.
RSRST999  RETURN
.***********************************************************************
.* RKSND000  :  read in the next soundex order record & double soundex *
.***********************************************************************
.
RKSND000  BRANCH    SRCHHREC,RKSND320   * using srchhrec as flag to say whether
                                        * we've had a full read or just a rds
.
          CALL      RKPTGSR3
          BRANCH    OVRCD,RKSND999
          ADD       ONE,NUMLOOP
          IF        NUMLOOP>NUMTLOOP
            MOVE      ONE,OVRCD
            GOTO      RKSND999
          ENDIF
.
RKSND320  MOVE      ZERO,SRCHMACH
          MOVE      ZERO,SRCHHREC       * clear flag for next entry
          MATCH     SRCHSNDX,GSRSKEY    * right surname soundex?
          GOTO      RKSND900 IF NOT EQUAL
.
          MATCH     SRCHGNAM,SP70        * any given name entered?
          GOTO      RKSND350 IF EQUAL
.
          MATCH     SRCHGSX1,GSRGKEY1
          GOTO      RKSND330 IF EQUAL
          GOTO      RKSND325 IF LESS
.
          MOVE      "zzz",GSRGKEY1
          PACK      KEY34,GSRSKEY,GSRGKEY1,SP70
          CALL      RSPTGSR3
          GOTO      RKSND000
.
RKSND325  PACK      KEY34,GSRSKEY,SRCHGSX1,SP70
          CALL      RSPTGSR3
          GOTO      RKSND000
.
RKSND330  MOVE      ZERO,SRCHMACH       * if first char zero - nothing entered
          MATCH     SRCHMACH,GSRGKEY2
          GOTO      RKSND350 IF EQUAL
.
          MATCH     SP1,GSRGKEY2        * no second name on file
          GOTO      RKSND350 IF EQUAL
.
          MATCH     SRCHMACH,SRCHGSX2   * no second given name entered
          GOTO      RKSND350 IF EQUAL
.
          MATCH     SP1,SRCHGSX2
          GOTO      RKSND350 IF EQUAL
.
          MATCH     SRCHGSX2,GSRGKEY2
          GOTO      RKSND350 IF EQUAL
          GOTO      RKSND340 IF LESS
.
          MOVE      "zzz",GSRGKEY2
          PACK      KEY34,GSRSKEY,GSRGKEY1,GSRGKEY2,SP70
          CALL      RSPTGSR3
          GOTO      RKSND000
.
RKSND340  PACK      KEY34,GSRSKEY,GSRGKEY1,SRCHGSX2,SP70
          CALL      RSPTGSR3
          GOTO      RKSND000
.
RKSND350  IF        PTCNCMSX=1
            MATCH     GSRSNAM,SRCHSNAM            * Strict surname match?
            IF        @EQUAL
              MATCH     GSRGNAM,SRCHGNAM          * yes-strict given name match?
              IF        @EQUAL
                MATCH     SP70,SRCHPDOB           * Strict DOB match
                IF        !@EQUAL
                  MATCH     GSRDOB,SRCHPDOB
                  GOTO      RKSND000 IF EQUAL
                ELSE
                  GOTO      RKSND000
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
          MOVE      ZERO,SRCHFLAG                * Valid Record
          MOVE      ZERO,EXIT
          MOVE      ZERO,OVRCD
          GOTO      RKSND999
.
RKSND900  MOVE      ONE,OVRCD                     * no more data
.
RKSND999  RETURN
.***********************************************************************
.* RPSND000  :  read in the next soundex order record & double soundex *
.***********************************************************************
.
RPSND000  BRANCH    SRCHHREC,RPSND320   * using srchhrec as flag to say whether
                                        * we've had a full read or just a rds
          CALL      RPPTGSR3
          BRANCH    OVRCD,RPSND999
.
RPSND320  MOVE      ZERO,SRCHMACH
          MOVE      ZERO,SRCHHREC       * clear flag for next entry
          MATCH     SRCHSNDX,GSRSKEY    * right surname soundex?
          GOTO      RPSND900 IF NOT EQUAL
.
          MATCH     SRCHGNAM,SP70        * any given name entered?
          GOTO      RPSND350 IF EQUAL
.
          MATCH     SRCHGSX1,GSRGKEY1
          GOTO      RPSND330 IF EQUAL
          GOTO      RPSND325 IF LESS
.
          MOVE      "zzz",GSRGKEY1
          PACK      KEY34,GSRSKEY,GSRGKEY1,SP70
          CALL      RSPTGSR3
          GOTO      RPSND000
.
RPSND325  PACK      KEY34,GSRSKEY,SRCHGSX1,SP70
          CALL      RSPTGSR3
          GOTO      RPSND000
.
RPSND330  MOVE      ZERO,SRCHMACH       * if first char zero - nothing entered
          MATCH     SRCHMACH,GSRGKEY2
          GOTO      RPSND350 IF EQUAL
.
          MATCH     SP1,GSRGKEY2        * no second name on file
          GOTO      RPSND350 IF EQUAL
.
          MATCH     SRCHMACH,SRCHGSX2   * no second given name entered
          GOTO      RPSND350 IF EQUAL
.
          MATCH     SP1,SRCHGSX2
          GOTO      RPSND350 IF EQUAL
.
          MATCH     SRCHGSX2,GSRGKEY2
          GOTO      RPSND350 IF EQUAL
          GOTO      RPSND340 IF LESS
.
          MOVE      "zzz",GSRGKEY2
          PACK      KEY34,GSRSKEY,GSRGKEY1,GSRGKEY2,SP70
          CALL      RSPTGSR3
          GOTO      RPSND000
.
RPSND340  PACK      KEY34,GSRSKEY,GSRGKEY1,SRCHGSX2,SP70
          CALL      RSPTGSR3
          GOTO      RPSND000
.
RPSND350  IF        PTCNCMSX=1
            MATCH     GSRSNAM,SRCHSNAM            * Strict surname match?
            IF        @EQUAL
              MATCH     GSRGNAM,SRCHGNAM          * yes-strict given name match?
              IF        @EQUAL
                MATCH     SP70,SRCHPDOB           * Strict DOB match
                IF        !@EQUAL
                  MATCH     GSRDOB,SRCHPDOB
                  GOTO      RPSND000 IF EQUAL
                ELSE
                  GOTO      RPSND000
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
          MOVE      ZERO,SRCHFLAG                * Valid Record
          MOVE      ZERO,EXIT
          MOVE      ZERO,OVRCD
          GOTO      RPSND999
.
RPSND900  MOVE      ONE,OVRCD                     * no more data
.
RPSND999  RETURN
+
.**************************************************************************
.*  RKSTR000  :  get next strict surname order record                     *
.**************************************************************************
.
. --- using the given surname file ---
.
RKSTR000  BRANCH    SRCHHREC,RKSTR020   * using sdxsys as flag to say whether
                                        * we've had a full read or just a rds
          MATCH     SP70,SRCHPDOB
          IF        !@EQUAL
            CALL      RKPTGSR6
          ELSE
            CALL      RKPTGSR2
          ENDIF
          BRANCH    OVRCD,RKSTR999      * no more records
          ADD       ONE,NUMLOOP
          IF        NUMLOOP>NUMTLOOP
            MOVE      ONE,OVRCD
            GOTO      RKSTR999
          ENDIF
.
RKSTR020  MATCH     SP70,SRCHPDOB
          IF        !@EQUAL
            MATCH     GSRDOB,SRCHPDOB
            GOTO      RKSTR900 IF NOT EQUAL
          ENDIF
.
          MATCH     SRCHSNAM,GSRSNAM
          GOTO      RKSTR900 IF NOT EQUAL         * no more records
.
          MATCH     SRCHGNAM,SP70
          GOTO      RKSTR100 IF EQUAL
.
          MATCH     SRCHGNAM,GSRGNAM
          GOTO      RKSTR200 IF LESS
          GOTO      RKSTR300 IF NOT EQUAL
.
RKSTR100  MOVE      ZERO,SRCHHREC        * clear flag
          MOVE      ZERO,OVRCD           * valid record
          GOTO      RKSTR999
.
.        Records given name less than that wanted - jump forward
.
RKSTR200  MATCH     SP70,SRCHPDOB
          IF        !@EQUAL
            PACK      KEY98,SRCHPDOB,GSRSNAM,SRCHGNAM,SP70,SP70
            CALL      RDPTGSR6
            COMPARE   ZERO,OVRCD
            GOTO      RKSTR020 IF EQUAL
            CALL      RSPTGSR6
          ELSE
            PACK      KEY98,GSRSNAM,SRCHGNAM,SP70,SP70
            CALL      RDPTGSR2
            COMPARE   ZERO,OVRCD
            GOTO      RKSTR020 IF EQUAL
            CALL      RSPTGSR2
          ENDIF
          MOVE      ZERO,SRCHHREC        * flag to say we did a rds
          GOTO      RKSTR000
.
.        We have finished the given names for this particular surname
.        try the next surname with the same starting string
.
RKSTR300  MOVE      "~",ANS
          MATCH     SP70,SRCHPDOB
          IF        !@EQUAL
            PACK      KEY98,SRCHPDOB,GSRSNAM,ANS,ANS,ANS,SP70,SP70
            CALL      RDPTGSR6
            COMPARE   ZERO,OVRCD
            GOTO      RKSTR020 IF EQUAL
            CALL      RSPTGSR6
          ELSE
            PACK      KEY98,GSRSNAM,ANS,ANS,ANS,SP70,SP70
            CALL      RDPTGSR2
            COMPARE   ZERO,OVRCD
            GOTO      RKSTR020 IF EQUAL
            CALL      RSPTGSR2
          ENDIF
          MOVE      ZERO,SRCHHREC        * flag to say we did a rds
          GOTO      RKSTR000
.
. --- using the surname file ---
.
RKSTR900  MOVE      ONE,OVRCD            * no more records
.
RKSTR999 RETURN
+
.***************************************************************************
.*  RSRSX000  :  Position on soundex order record for rev surname          *
.***************************************************************************
RSRSX000  MOVE      ONE,EXIT
          COMPARE   ZERO,PTCNRGNS         * reverse giv seq required
          GOTO      RSRSX999 IF EQUAL
.
          MATCH     SRCHGNAM,SP70          * no given name entered, cant reverse
          GOTO      RSRSX999 IF EQUAL
.
          PACK      GSRSKEY,SRCHSNDX,SP10
          REP       " 0",GSRSKEY
          PACK      KEY34,GSRSKEY,SRCHGSX1,SP70
          CALL      RSPTGSR4
.
          MOVE      ZERO,EXIT             * position OK
.
RSRSX999  RETURN
+
.***************************************************************************
.*  RKRSX000  :  Read Next soundex order record for rev surname            *
.***************************************************************************
RKRSX000  CALL      RKPTGSR4
          BRANCH    OVRCD,RKRSX999
.
          ADD       ONE,NUMLOOP
          IF        NUMLOOP>NUMTLOOP
            MOVE      ONE,OVRCD
            GOTO      RKRSX999
          ENDIF
          MATCH     SRCHSNDX,GSRSKEY
          GOTO      RKRSX800 IF NOT EQUAL
.
          MATCH     SRCHGSX1,GSRGKEY2
          GOTO      RKRSX530 IF EQUAL
          GOTO      RKRSX525 IF LESS
.
          MOVE      "zzz",GSRGKEY1
          PACK      KEY34,GSRSKEY,GSRGKEY1,SP70
          CALL      RSPTGSR4
          GOTO      RKRSX000
.
RKRSX525  PACK      KEY34,GSRSKEY,SRCHGSX1,SP70
          CALL      RSPTGSR4
          GOTO      RKRSX000
.
RKRSX530  MOVE      ZERO,SRCHMACH       * if first char zero - nothing entered
          MATCH     SRCHMACH,SRCHGSX2
          GOTO      RKRSX900 IF EQUAL
.
          MATCH     SP1,SRCHGSX2
          GOTO      RKRSX900 IF EQUAL
.
          MATCH     SRCHGSX2,GSRGKEY1
          GOTO      RKRSX900 IF EQUAL
          GOTO      RKRSX540 IF LESS
.
          MOVE      "zzz",GSRGKEY1
          PACK      KEY34,GSRSKEY,GSRGKEY2,GSRGKEY1,SP70
          CALL      RSPTGSR4
          GOTO      RKRSX000
.
RKRSX540  PACK      KEY34,GSRSKEY,GSRGKEY2,SRCHGSX2,SP70
          CALL      RSPTGSR4
          GOTO      RKRSX000
.
RKRSX800  MOVE      ONE,OVRCD                     * invalid record
          GOTO      RKRSX999
.
RKRSX900  MOVE      ZERO,OVRCD
.
RKRSX999  RETURN
+
.***************************************************************************
.*  RSRSG000  :  Postion Read Reverse Surname/Given Name Search - Soundex  *
.***************************************************************************
RSRSG000  MOVE      ONE,EXIT
          COMPARE   ZERO,SRCHRSGS         * reverse giv seq required
          GOTO      RSRSG999 IF EQUAL
.
          MATCH     SRCHGNAM,SP70          * no given name entered, cant reverse
          GOTO      RSRSG999 IF EQUAL
.
          PACK      GSRSNAM,SRCHGNAM,SP70
          SCAN      SP1,GSRSNAM           * Remove any possible 2nd Given Name
          IF        @EQUAL
            APPEND    SP70,GSRSNAM
            RESET     GSRSNAM
          ENDIF
.
          CALL      SOUNDEX
          MOVE      GSRSKEY,SRCHSNDX
          PACK      GSRGNAM,SRCHSNAM,SP70
          PACK      PTGSGNM2,SP70
          CALL      SOUNDX2
          MOVE      GSRGKEY1,SRCHGSX1
          PACK      KEY34,GSRSKEY,GSRGKEY1,SP70
          CALL      RSPTGSR3
          MOVE      ZERO,EXIT                     * Position Ok
.
RSRSG999  RETURN
+
.***************************************************************************
.*  RKRSG000  :  Read Next Reverse Surname/Given Name Search - Soundex     *
.***************************************************************************
RKRSG000  CALL      RKPTGSR3
          BRANCH    OVRCD,RKRSG999
.
          ADD       ONE,NUMLOOP
          IF        NUMLOOP>NUMTLOOP
            MOVE      ONE,OVRCD
            GOTO      RKRSG999
          ENDIF
          MATCH     SRCHSNDX,GSRSKEY              * right surname soundex?
          GOTO      RKRSG900 IF NOT EQUAL
.
          MATCH     SRCHGSX1,GSRGKEY1
          GOTO      RKRSG900 IF NOT EQUAL
          GOTO      RKRSG999                      * valid record
.
RKRSG900  MOVE      ONE,OVRCD                     * no more records
.
RKRSG999  RETURN
+
.***************************************************************************
.* REVST000  :  do the Reverse Surname/Given Name Search - Strict          *
.*              Returns: OVRCD 0=Valid record, 1=No more records           *
.***************************************************************************
REVST000  IF         SRCHHREC=0
            MATCH      SP70,SRCHPDOB
            IF         !@EQUAL
              CALL       RKPTGSR6
              BRANCH     OVRCD,REVST999
            ELSE
              CALL       RKPTGSR2
              BRANCH     OVRCD,REVST999
            ENDIF
          ENDIF
.
          ADD       ONE,NUMLOOP
          IF        NUMLOOP>NUMTLOOP
            MOVE      ONE,OVRCD
            GOTO      REVST999
          ENDIF
          MOVE       ZERO,SRCHHREC
.
          MATCH      SP70,SRCHPDOB
          IF         !@EQUAL
            MATCH      SRCHPDOB,GSRDOB
            GOTO       REVST900 IF NOT EQUAL
          ENDIF
.
          MATCH      SRCHGNAM,GSRSNAM
          GOTO       REVST900 IF NOT EQUAL
.
          MATCH      SRCHSNAM,GSRGNAM
          GOTO       REVST900 IF NOT EQUAL
.
. we have a valid record
.
          MOVE      ZERO,OVRCD
          GOTO      REVST999
.
REVST900  MOVE      ONE,OVRCD                     * no more records
.
REVST999  RETURN
.*********************************************************************
.*  SPARA000  :  Set Search Parameters                               *
.*********************************************************************
SPARA000  PACK      SRCHSNAM,SRCHSNAM,SP70
          STRIP     SRCHSNAM
          PACK      SRCHGNAM,SRCHGNAM,SP70
          MATCH     SRCHGNAM,SP70
          IF        @EQUAL
            CLEAR     SRCHGNAM
            APPEND    SP70,SRCHGNAM
            RESET     SRCHGNAM
            LENSET    SRCHGNAM,1
          ELSE
            STRIP     SRCHGNAM
          ENDIF
.
          PACK      GSRSNAM,SRCHSNAM,SP70
          PACK      GSRGNAM,SRCHGNAM,SP70
          PACK      PTGSGNM2,SP70
.
.         Added the following in the hope of fixing soundex search -
.         please work!!!!!! - Nicole 24/11/1999
.
          UNPACK    SP30,GSRGKEY1,GSRGKEY2 * if so, clear given name keys
          CALL      SOUNDX2                * get soundex keys for given names
          MOVE      GSRGKEY1,SRCHGSX1      * save soundexs for test in search
          SCAN      "0",SRCHGSX1
          BUMP      SRCHGSX1,-1
          LENSET    SRCHGSX1
          RESET     SRCHGSX1
          MOVE      GSRGKEY2,SRCHGSX2
          SCAN      "0",SRCHGSX2
          BUMP      SRCHGSX2,-1
          LENSET    SRCHGSX2
          RESET     SRCHGSX2
.
          MOVE      ZERO,SRAGEFR
          MOVE      999,SRAGETO
.
          MATCH     SP70,SRCHPDOB
          IF        !@EQUAL
            UNPACK    SRCHPDOB,CCENT,CYEAR,CMON,CDAY
            CALL      DBRNG000
            GOTO      SPARA999
          ENDIF
          MATCH     SP70,SRCHPAGE
          CALL      RAGES000 IF NOT EQUAL   * Work out date ranges
.
SPARA999  RETURN
.------------------------------------------------------------
. Given a Age Work Out Date Range for DOB
.------------------------------------------------------------
RAGES000  PACK      SRCHTDAT,CMM,CDD
          REP       " 0",SRCHTDAT
.
          MOVE      CYY,YY
          MOVE      CMM,MM
          MOVE      CDD,DD
          CALL      DMYCON               * Convert to a julian date
          ADD       ONE,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          CALL      JULCON
.
          PACK      SRCHFDAT,MM,DD
          REP       " 0",SRCHFDAT
.
.        We have the from and to dates. Now work out the from and to years.
.        The from ending year = current year - age - year range
.        The to ending year   = current year - age + year range
.
.        The starting year range will be the same if the from date is less than
.        the end year, otherwise it will be one less than the ending year range
.
          PACK      SRCHWORK,CCC,CYY
          REP       " 0",SRCHWORK
.
          STRIP     SRCHPAGE
          MOVE      SRCHPAGE,PSAGE       * Convert age to a form field
.
          READ      CONTROLF,TEN;*246,SRCHDRNG,SRCHYRNG
          MATCH     SP70,SRCHARAN
          IF        !@EQUAL
            SQUEEZE   SRCHARAN
            MOVE      SRCHARAN,SRCHYRNG
            MATCH     SP70,SRCHPAGE
            IF        !@EQUAL
              STRIP     SRCHPAGE
              MOVE      SRCHPAGE,SRAGEFR
              SUBTRACT  SRCHYRNG,SRAGEFR
              MOVE      SRCHPAGE,SRAGETO
              ADD       SRCHYRNG,SRAGETO
            ENDIF
          ENDIF
.   
          MOVE      SRCHWORK,SRCHYEAR
          SUB       PSAGE,SRCHYEAR
          SUB       SRCHYRNG,SRCHYEAR
          MOVE      SRCHYEAR,SRCHFEYR
          MOVE      SRCHYEAR,SRCHFSYR
.
          MOVE      SRCHWORK,SRCHYEAR
          SUB       PSAGE,SRCHYEAR
          ADD       SRCHYRNG,SRCHYEAR
          MOVE      SRCHYEAR,SRCHTEYR
          MOVE      SRCHYEAR,SRCHTSYR
.
          MATCH     SRCHFDAT,SRCHTDAT
          GOTO      KSAGE999 IF NOT LESS
.
.         Subtract one from the starting year range
.
          MOVE      SRCHFSYR,SRCHYEAR
          SUB       ONE,SRCHYEAR
          MOVE      SRCHYEAR,SRCHFSYR
.
          MOVE      SRCHTSYR,SRCHYEAR
          SUB       ONE,SRCHYEAR
          MOVE      SRCHYEAR,SRCHTSYR
.
KSAGE999  RETURN
.**************************************************************************
.*  DBRNG000  :  Work out the valid date range                            *
.*                                                                        *
.*       We have a date of birth. Now work out a range of dates           *
.*       corresponding to this date +/- the day range variable.           *
.*                                                                        *
.*       The from ending year (CCYY) = calculated birth year - year range *
.*       The to ending year   (CCYY) = calculated birth year + year range *
.*                                                                        *
.*       The from date (MMDD) = birth date - day range                    *
.*       The to date   (MMDD) = birth date + day range                    *
.**************************************************************************
DBRNG000  MOVE      CDAY,DD
          MOVE      CMON,MM
          MOVE      CYEAR,YY
          MOVE      CCENT,CC
          CALL      DMYCON
          ADD       SRCHDRNG,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON
.
          READ      CONTROLF,TEN;*246,SRCHDRNG,SRCHYRNG
          MATCH     SP70,SRCHARAN
          IF        !@EQUAL
            SQUEEZE   SRCHARAN
            MOVE      SRCHARAN,SRCHYRNG
          ENDIF
.   
          PACK      SRCHTDAT,MM,DD
          REP       " 0",SRCHTDAT
.
          PACK      SRCHWORK,CC,YY
          REP       " 0",SRCHWORK
          MOVE      SRCHWORK,SRCHYEAR
          SUB       SRCHYRNG,SRCHYEAR
          MOVE      SRCHYEAR,SRCHFEYR
          MOVE      SRCHYEAR,SRCHFSYR
.
          MOVE      SRCHWORK,SRCHYEAR
          ADD       SRCHYRNG,SRCHYEAR
          MOVE      SRCHYEAR,SRCHTEYR
          MOVE      SRCHYEAR,SRCHTSYR
.
.        Get the from date (date of birth minus SRCHDRNG)
.
          MOVE      CDAY,DD
          MOVE      CMON,MM
          MOVE      CYEAR,YY
          MOVE      CCENT,CC
          CALL      DMYCON
          SUB       SRCHDRNG,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON
.
          PACK      SRCHFDAT,MM,DD
          REP       " 0",SRCHFDAT
.
.        The starting year range will be the same if the from date is less than
.        the end year, otherwise it will be one less than the ending year range
.
          MATCH     SRCHFDAT,SRCHTDAT
          GOTO      DBRNG999 IF NOT LESS
.
.        Subtract one from the starting year range
.
          MOVE      SRCHFSYR,SRCHYEAR
          SUB       ONE,SRCHYEAR
          MOVE      SRCHYEAR,SRCHFSYR
.
          MOVE      SRCHTSYR,SRCHYEAR
          SUB       ONE,SRCHYEAR
          MOVE      SRCHYEAR,SRCHTSYR
.
DBRNG999  RETURN
.*************************************************************************
.*  SHEAD000  :  Search Heading                                          *
.*************************************************************************
SHEAD000 BRANCH     FHIRTYPE,SHEAD999
         BRANCH     STARTFLG,SHEAD999
         MOVE       ONE,STARTFLG
         WRITE      HTMLFILE;"<div id=HeadingDivision >"
         WRITE      HTMLFILE;"<table width=100% border=1 cellspacing=0 ":
                             "cellpadding=1 style=#"table-layout: fixed;#">" 
.
         IF         SRCHFMT = 5
           WRITE      HTMLFILE;"<tr><td class=HeadingCell width=145>":
                               "Patient</td>":
                               "<td class=HeadingCell width=50>U/R</td>":
                               "<td class=HeadingCell width=63>":
                               "Date of Birth (Age) </td>";
         ELSE
           WRITE      HTMLFILE;"<tr><td class=HeadingCell width=170>":
                               "Patient</td>":
                               "<td class=HeadingCell width=60>U/R</td>":
                               "<td class=HeadingCell width=105>":
                               "Date of Birth (Age) </td>";
         ENDIF
.        MOVE       "4",COLSPAN
         MOVE       "3",NOFCOL
.
................................................................................
.
         IF         SRCHFMT = 5
           WRITE      HTMLFILE;"<td class=HeadingCell width=30>Adm.No.</td>"
           WRITE      HTMLFILE;"<td class=HeadingCell width=40>Wrd/Bed<br>Wrd Ext.</td>"
           WRITE      HTMLFILE;"<td class=HeadingCell width=65>Adm.Date</td>"
           ADD        "3",NOFCOL
         ENDIF
.
.............................................................................
.
.
...............................................................................
.
         IF         LINKNUMB<>0
           WRITE      HTMLFILE;"<td class=HeadingCell width=60>Links</td>";
.          MOVE       "6",COLSPAN
           ADD        "1",NOFCOL 
         ENDIF
.
................................................................................
.

         IF         SRCHFMT = 5
.          MOVE       "7",COLSPAN
           WRITE      HTMLFILE;"<td class=HeadingCell width=150>Patient Condition<br>Status</td>"
         ELSE
           WRITE      HTMLFILE;"<td class=HeadingCell width=170>Status</td>"
         ENDIF
         ADD        "1",NOFCOL 
.
.............................................................................
.
         IF         DISDOCTY=0
.          MOVE       "5",COLSPAN
           WRITE      HTMLFILE;"<td class=HeadingCell width=75>Doc Type</td></tr>"
           ADD        "1",NOFCOL
         ENDIF
.
         MATCH      "1",EXTNSRCH
         IF         @EQUAL
           WRITE      HTMLFILE;"<td class=HeadingCell width=75>Telephone Nos.</td>"
           WRITE      HTMLFILE;"<td class=HeadingCell width=110>Claim Details</td>"
           ADD        "2",NOFCOL 
         ENDIF
.
         MOVE       NOFCOL,COLSPAN
.
         WRITE      HTMLFILE;"</table>"
         WRITE      HTMLFILE;"</div>"
         WRITE      HTMLFILE;"<div id=BodyDivision style=#"height:250;#">"
         WRITE      HTMLFILE;"<table width=100% border=1 cellspacing=0 ":
                             "cellpadding=1 style=#"table-layout: fixed;#">" 
SHEAD999 RETURN
+
.*************************************************************************
.*  CHEAD000  :  Current Patient Heading                                 *
.*************************************************************************
CHEAD000 BRANCH     FHIRTYPE,CHEAD999
         BRANCH     STARTFLG,CHEAD999
         MOVE       ONE,STARTFLG
         WRITE      HTMLFILE;"<div id=HeadingDivision >"
         WRITE      HTMLFILE;"<table width=100% border=1 cellspacing=0 ":
                             "cellpadding=1 style=#"table-layout: fixed;#">" 
.
         MOVE       ZERO,FORM1
         MOVE       SRCHSYST,FORM1
         BRANCH     FORM1,CHEAD100,CHEAD200,CHEAD300,CHEAD400
.
.        Inpatients
.
         IF         KIDSONLY=ONE
.                                       * RCH custom headings
           WRITE      HTMLFILE;"<tr>":
                               "<td class=HeadingCell width=170>Patient</td>":
                               "<td class=HeadingCell width=40>Ward</td>";
.
           COMPARE    ONE,IBCNMHOS
           IF         @EQUAL
             WRITE      HTMLFILE;"<td class=HeadingCell width=40>Hospital</td>";
           ENDIF
.
           WRITE      HTMLFILE;"<td class=HeadingCell width=40>Unit</td>":
                               "<td class=HeadingCell width=70>Adms.Date</td>":
                               "<td class=HeadingCell width=40>Adms.Time</td>":
                               "<td class=HeadingCell width=70>Disc.Date</td>":
                               "<td class=HeadingCell width=40>Disc.Time</td>":
                               "<td class=HeadingCell width=40>Ext.</td>":
                               "<td class=HeadingCell width=110>":
                               "Disc.Dest.<b>/Condition</td>":
                               "<td class=HeadingCell width=40>U/R Number</td>":
                               "</tr>";
         ELSE
.                                       * Standard headings (St. Vincents)
           WRITE      HTMLFILE;"<tr>":
                               "<td class=HeadingCell width=170>Patient</td>":
                               "<td class=HeadingCell width=45>Ward</td>";
.
           COMPARE    ONE,IBCNMHOS
           IF         @EQUAL
             WRITE      HTMLFILE;"<td class=HeadingCell width=40>Hospital</td>";
           ENDIF
.                               
           WRITE      HTMLFILE;"<td class=HeadingCell width=70>Unit</td>":
                               "<td class=HeadingCell width=70>Adms.Date<br>":
                                                              "Adms.Time</td>":
                               "<td class=HeadingCell width=70>Disc.Date<br>":
                                                              "Disc.Time</td>":
                               "<td class=HeadingCell width=40>Ext.</td>":
                               "<td class=HeadingCell width=170>Disc.Dest.<br>":
                                                              "/Condition</td>":
                               "<td class=HeadingCell width=45>U/R<br>":
                                                              "Number</td>":
                               "</tr>";
         ENDIF
         GOTO       CHEAD900 
.
.         Outpatient
.
CHEAD100 WRITE      HTMLFILE;"<tr><td class=HeadingCell width=250>":
                             "Patient</td>":
                             "<td class=HeadingCell width=100>Location</td>":
                             "<td class=HeadingCell width=45>Ward</td>";
.
         COMPARE    ONE,IBCNMHOS
         IF         @EQUAL
           WRITE      HTMLFILE;"<td class=HeadingCell width=40>Hospital</td>";
         ENDIF
.                             
         WRITE      HTMLFILE;"<td class=HeadingCell width=100>Unit</td>":
                             "<td class=HeadingCell width=120>HCP</td>":
                             "<td class=HeadingCell width=80>Appt Date</td>":
                             "<td class=HeadingCell width=80>Appt Time</td>":
                             "<td class=HeadingCell width=80>":
                             "U/R Number</td></tr>"
         GOTO       CHEAD900
.
.         Emergency
.
CHEAD200 WRITE      HTMLFILE;"<tr><td class=HeadingCell width=250>":
                             "Patient</td>":
                             "<td class=HeadingCell width=70>Location</td>":
                             "<td class=HeadingCell width=45>Ward</td>";
.
         COMPARE    ONE,IBCNMHOS
         IF         @EQUAL
           WRITE      HTMLFILE;"<td class=HeadingCell width=40>Hospital</td>";
         ENDIF
.                             
         WRITE      HTMLFILE;"<td class=HeadingCell width=70>Triage Date</td>":
                             "<td class=HeadingCell width=50>Triage Time</td>":
                             "<td class=HeadingCell width=70>Disc Date</td>":
                             "<td class=HeadingCell width=50>Disc Time</td>":
                             "<td class=HeadingCell width=110>":
                             "Discharge Destination</td>":
                             "<td class=HeadingCell width=50>":
                             "U/R Number</td></tr>"
         GOTO       CHEAD900
.
.        Event
.
CHEAD300 WRITE      HTMLFILE;"<tr><td class=HeadingCell width=250>":
                             "Patient</td>":
                             "<td class=HeadingCell width=80>Ward/Bed</td>":
                             "<td class=HeadingCell width=100>Location</td>":
                             "<td class=HeadingCell width=45>Ward</td>";
.
         COMPARE    ONE,IBCNMHOS
         IF         @EQUAL
           WRITE      HTMLFILE;"<td class=HeadingCell width=40>Hospital</td>";
         ENDIF
.                             
         WRITE      HTMLFILE;"<td class=HeadingCell width=100>Unit</td>":
                             "<td class=HeadingCell width=120>HCP</td>":
                             "<td class=HeadingCell width=80>Appt Date</td>":
                             "<td class=HeadingCell width=80>Appt Time</td>":
                             "<td class=HeadingCell width=60>":
                             "U/R Number</td></tr>"
         GOTO       CHEAD900
.
.         Allied Health
.
CHEAD400 WRITE      HTMLFILE;"<tr><td class=HeadingCell width=250>":
                             "Patient</td>":
                             "<td class=HeadingCell width=45>Ward</td>";
.
         COMPARE    ONE,IBCNMHOS
         IF         @EQUAL
           WRITE      HTMLFILE;"<td class=HeadingCell width=40>Hospital</td>";
         ENDIF
.
         WRITE      HTMLFILE;"<td class=HeadingCell width=90>Unit</td>":
                             "<td class=HeadingCell width=120>HCP</td>":
                             "<td class=HeadingCell width=90>Active Date</td>":
                             "<td class=HeadingCell width=90>Status</td>":
                             "<td class=HeadingCell width=90>":
                             "Last Encounter</td>":
                             "<td class=HeadingCell width=90>":
                             "U/R Number</td></tr>"
.
CHEAD900 
CHEAD999 RETURN
+
.**************************************************************************
.*  DISPR000  :  Subroutine to process a given surname index record       *
.**************************************************************************
DISPR000  ADD       ONE,TOTALREC
          IF        TOTALREC>MAXREC
            IF        FHIRTYPE=0
              WRITE     HTMLFILE;"<tr bgcolor=#FF0000>":
                                 "<td colspan=",COLSPAN," align=center>":
                                 "<b>Search Limit Reached Please Refine ":
                                 "Search Criteria</td>"
            ENDIF
            MOVE      NORECORD,DISNUMB
            GOTO      DISPR999
          ENDIF
.
          MOVE      GSRURNO,URNUMBER             * URNUMBER used by CHCALT00
.
          MATCH     ZEROUR,GSRURNO
          IF        @EQUAL
            MOVE      GSRBILL,PURNO
            MOVE      PURNO,KEY8
            CALL      RDPRAM1                    * Get the pre-adm details
            BRANCH    OVRCD,DISPR999             * No details -> ignore record
            MOVE      GSRURNO,PURNO              * Restore correct U/R No.
          ELSE
            MOVE      GSRURNO,PURNO
            MOVE      PURNO,KEY8
            CALL      RDMAST1
            BRANCH    OVRCD,DISPR999
            MOVE      PURNO,KEY8                 * Still add person to search
            CALL      RDPMPX21                   * if exten file 3 not found
            IF        OVRCD = 1
              MOVE      SP70,PMPXPRVI
            ENDIF
.
.         Exclude EBS created temp U/R numbers from standard search results
.
            IF        SRCHEBST=0 & PSTAT=4
              GOTO      DISPR999                 * Skip EBS Temp U/R
            ENDIF
          ENDIF
.
.         Test for a valid sex
.
          MATCH     SP1,SRCHPSEX
          IF        !@EQUAL
            MATCH     PSEX,SRCHPSEX
            GOTO      DISPR999 IF NOT EQUAL      * Invalid sex - continue
          ENDIF
.
          IF        SRCHFMT=6
            CALL      GCUR0000                   * Get current patients
            BRANCH    EXIT,DISPR999
            ADD       ONE,RECNUMB
            IF        STARTNUM>=RECNUMB
              PROC      CHCALT00
              IF        AIEXIST=1
                CALL      DISALI00               * Display alternate ID records
              ENDIF
              GOTO      DISPR999
            ENDIF
            CALL      CHOUT000                   * Check Output Array
            BRANCH    EXIT,DISPR999
            GOTO      DISPR500
          ENDIF
.
          CALL      CHDOB000                     * Check Date of Birth
          BRANCH    EXIT,DISPR999
.
          IF         SRCHWAIT=1
            CALL      CHKWAT00                   * WL Search so check for 
            BRANCH    EXIT,DISPR999              * any WL record
.
            CALL      DWAT0000                   * Select WL records
            BRANCH    EXIT,DISPR999
          ENDIF
.
.
          CALL      CHOUT000                     * Check Output Array
          BRANCH    EXIT,DISPR999
.
.         Set up the rest of the variables ready to display
.
          MOVE      SP1,ANS
          LOAD      ANS,PCASE,CCNOTES            * Load case notes indicator
.
          MOVE      PSNAME,SRCHSN
          MOVE      PGNAME,SRCHGN
.
.
          MATCH     ZEROUR,GSRURNO
          IF        @EQUAL   
            CALL      STATZ000                   * Get Status
          ELSE
            CALL      GETS0000                   * Get Status
          ENDIF
.
          IF        SRCHFMT=5
            CALL      VLCP0000                   * Validate current patients
            BRANCH    EXIT,DISPR999              * Skip non current patients
          ENDIF
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            PROC      CHCALT00
            IF        AIEXIST=1
              CALL      DISALI00                 * Display alternate ID records
            ENDIF
            GOTO      DISPR999
          ENDIF
.
          MATCH     SP70,SRCHSTA1
          IF        @EQUAL
            MOVE      NBSP,SRCHSTA1
          ENDIF
          MATCH     SP70,SRCHSTA3
          IF        @EQUAL
            MOVE      NBSP,SRCHSTA3
          ENDIF
.
          COMPARE   FIVE,SRCHFMT
          IF        @EQUAL
            CALL      CHKPRV00
          ENDIF
.
          CALL      SRCHZ000                     * Recalculate patients age
          CALL      CALCAGE
.
DISPR100  MATCH     "3",SRCHTYPE
          IF        @EQUAL
            COMPARE   SRAGEFR,PSAGEYRS
            GOTO      DISPR999 IF LESS
.
            COMPARE   PSAGEYRS,SRAGETO
            GOTO      DISPR999 IF LESS
          ENDIF
.
          CALL      PATNAM00                     * Format Patient Name
.
          MATCH     SP70,ADMISSNO                * Blank admissno
          IF        @EQUAL | @EOS
            MOVE      GSRBILL,ADMISSNO           * Set to right justified zero
          ENDIF
.
          REP       " +",URNUMBER
          REP       " +",ADMISSNO
          MOVE      ONE,ALIAFLAG       * Yes Alias
          REP       UPPLOW,GSRSNAM
          MATCH     GSRSNAM,PTMASNAM
          GOTO      DISPR200 IF NOT EQUAL
          MATCH     GSRGNAM,PMPXFNAM
          GOTO      DISPR200 IF NOT EQUAL
          MATCH     PTGSGNM2,PMPXSNAM
          GOTO      DISPR200 IF NOT EQUAL
.
          MATCH     "3",SRCHTYPE       * DOB Search
          IF        @EQUAL
            MATCH     GSRSEX,PSEX
            GOTO      DISPR200 IF NOT EQUAL
          ENDIF
.
          MOVE      ZERO,ALIAFLAG      * Not Alias
.
DISPR200  CALL      TABROW00           * Display row
          ADD       ONE,DISNUMB
          PROC      CHCALT00
          IF        AIEXIST=1
            CALL      DISALI00         * Display alternate ID records
          ENDIF
          GOTO      DISPR999
.
.         Display Current Patient details depends on the system type
.
DISPR500  CALL      GDET0000        * Get patient details
          CALL      PRNLIN00        * print details
          ADD       ONE,DISNUMB
.
DISPR999  RETURN
.------------------------------------------------------------
. Check if Patient on Waiting List
.------------------------------------------------------------
CHKWAT00  PACK      KEY19,PURNO,Z70
          CALL      RDSTREA1
CHKWAT10  CALL      RDPTREA1
          BRANCH    OVRCD,CHKWAT90
          MATCH     WMURNO,PURNO
          GOTO      CHKWAT90 IF NOT EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      CHKWAT99
.
CHKWAT90  MOVE      ONE,EXIT
.
CHKWAT99  RETURN
.
.------------------------------------------------------------
. Default WL record for WL search. Find the first record
. on file for this U/R in status order 1 to 9.
.------------------------------------------------------------
DWAT0000  MOVE      ZERO,WLSTATUS           * Clear WL status
.
DWAT1000  COMPARE   NINE,WLSTATUS           * Exit if status 9
          GOTO      DWAT9000 IF EQUAL
.
          ADD       ONE,WLSTATUS            * Increase status by 1
.
          PACK      KEY20,WLSTATUS,PURNO,Z70
          CALL      RDSTREA2
          CALL      RDPTREA2                * Read WL record
          BRANCH    OVRCD,DWAT1000
.
          COMPARE   WMSTAT,WLSTATUS         * Skip if wrong status
          GOTO      DWAT1000 IF NOT EQUAL
.
          MATCH     WMURNO,PURNO            * Skip if wrong U/R
          GOTO      DWAT1000 IF NOT EQUAL
.
          MOVE      ZERO,EXIT               * Select this records
          GOTO      DWAT9999
.
DWAT9000  MOVE      ONE,EXIT                * Skip no records found
.
DWAT9999  RETURN
.------------------------------------------------------------
. Check Output Array
.------------------------------------------------------------
CHOUT000  MATCH      ZEROUR,GSRURNO
          IF         @EQUAL
            PACK       CHKID,ANSV,GSRBILL
          ELSE
            PACK       CHKID,GSRURNO,SP1
          ENDIF
.
          MOVE       ZERO,F3
          MOVE       ZERO,EXIT
CHOUT100  ADD        ONE,F3
          IF         F3>UROUTEND
            IF         F3>200
              GOTO       CHOUT999
            ENDIF
            MOVE       F3,UROUTEND
            MOVE       CHKID,UROUTPUT[F3]
            GOTO       CHOUT999
          ENDIF
          MATCH      CHKID,UROUTPUT[F3]
          GOTO       CHOUT100 IF NOT EQUAL
.
CHOUT200  MATCH     "3",SRCHTYPE
          GOTO       CHOUT100 IF EQUAL           * 0913831
.
          MOVE       ONE,EXIT
CHOUT999  RETURN
.------------------------------------------------------------
. Output Table Row
.------------------------------------------------------------
TABROW00  IF       FHIRTYPE=1
            CALL     FHRROW00
            GOTO     TABROW99
          ENDIF
          IF       SRCHFMT = 5
            WRITE      HTMLFILE;"<tr valign=top><td width=145":
                         " style=#"padding:0px 5px#">":
                         "<table><tr valign=top><td>";
          ELSE
            WRITE      HTMLFILE;"<tr valign=top><td width=170":
                         " style=#"padding:0px 5px#">":
                         "<table><tr valign=top><td>";
          ENDIF
.
          BRANCH     SRCHFMT,TABROW10,TABROW20,TABROW30,TABROW40,TABROW50
.
. Link to Server
.
TABROW10  WRITE     HTMLFILE;"<a href=",LINKPRG,".pbl?":
                             "&reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&urnumber=",URNUMBER;
TABROW15  IF        ALTIDFLG=1
            WRITE     HTMLFILE;"&admissno=",ADMISSNO:
                          " onclick=#"alert(#'Warning: Alternate ID selected#'":
                          ")#">";
          ELSE
             WRITE    HTMLFILE;"&admissno=",ADMISSNO,">";
          ENDIF
.
          CALL      PATNAM00
          GOTO      TABROW90
.
TABROW20  REP       "+ ",URNUMBER
          REP       "+ ",ADMISSNO
          CALL      JAVNAM00
          WRITE     HTMLFILE;"<a href='javascript:updateParent(#"",JAVFNAME:
                              "#",#"",URNUMBER:
                              "#",#"",ADMISSNO,"#");'>"
          CALL      PATNAM00
          GOTO      TABROW90
.
.
TABROW30  MATCH     "024",TEMPLATE 
          IF        !@EQUAL
            MATCH     "24",TEMPLATE
            IF        !@EQUAL
              GOTO      TABROW35
            ENDIF
          ENDIF
          CALL      CHGP0000                * check for GP Access
          IF        EXIT=1
            WRITE     HTMLFILE;"<a href=#"###" ":
                               "onclick='showPatient(#"patweb07.pbl?":
                               "&reportno=14&template=001":
                               "&urnumber=",URNUMBER:
                               "&admissno=",ADMISSNO:
                               "#",#"",EXIT:
                               "#");'>";
          ELSE
            WRITE     HTMLFILE;"<a href=#"###" ":
                               "onclick='showPatient(#"",LINKPRG,".pbl?":
                               "&reportno=",LINKREP,"&template=",LINKTEM:
                               "&urnumber=",URNUMBER:
                               "&admissno=",ADMISSNO:
                               "#",#"",EXIT:
                               "#");'>";
          ENDIF
.
          GOTO      TABROW90
.
TABROW35  WRITE     HTMLFILE;"<a href=#"###" ":
                             "onclick='showPatient(#"",LINKPRG,".pbl?":
                             "&reportno=",LINKREP,"&template=",LINKTEM:
                             "&urnumber=",URNUMBER:
                             "&admissno=",ADMISSNO:
                             "#");'>";
.
          GOTO      TABROW90
.
TABROW40  WRITE     HTMLFILE;"<a href=":
                             LINKPRG,".pbl?":
                             "&reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&urnumber=",URNUMBER:
                             ">";
          GOTO      TABROW90
.
. Link to Server current patients
.
TABROW50  MATCH     "    9145",URNUMBER
          IF        @EQUAL
            WRITE     HTMLFILE;"<a href=#"javascript: alert(#"merged#")#"";
          ELSE
            WRITE     HTMLFILE;"<a href=";
          ENDIF
          WRITE     HTMLFILE;LINKPRG,".pbl?":
                             "&reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&urnumber=",URNUMBER:
                             "&admissno=",ADMISSNO,">";
          GOTO      TABROW90
.
TABROW90  CALL      PATFLD00  
          PROC      CHCALI00
          PROC      CHCALT00
.
          WRITE     HTMLFILE;"<img src=../images/PatientFolder",KEY3,".gif":
                             " class=Icon></a>":
                             "</td><td class=nowrap>";
.
.
.* ****************************************** Start of Changes         *C-49016
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          MOVE      TDESC,DSEXSDES
.* ******************************************   End of Changes         *C-49016
.
          PACK      DISPNMLG,SP70                                   * CAR 263963

          CLEAR     DISPNMLG                                        * CAR 263963
          MOVE      PADD1,NAME35
          CALL      NAMST100                                        * CAR 263963
          MATCH     SP70,PADD2
          IF        !@EQUAL
            MOVE      PADD2,NAME35
            CALL      NAMST100                                      * CAR 263963
          ENDIF
          RESET     DISPNMLG                                        * CAR 263963     
.                                                                   * CAR 263963
          MOVE      DISPNMLG,FADDRES1                               * CAR 263963
          PACK      DISPNMLG,SP70                                   * CAR 263963
          CLEAR     DISPNMLG                                        * CAR 263963
.
          MATCH     SP70,PSUBRB
          IF        !@EQUAL
            MOVE      PSUBRB,NAME35
            CALL      NAMST100                                      * CAR 263963
          ENDIF
          MATCH     SP70,PADD4
          IF        !@EQUAL
            MOVE      PADD4,NAME35
            CALL      NAMST100                                      * CAR 263963
          ENDIF
          MATCH     SP70,PPOST
          IF        !@EQUAL
            MOVE      PPOST,NAME35
            CALL      NAMST100                                      * CAR 263963
          ENDIF
          RESET     DISPNMLG                                        * CAR 263963
.
          MOVE      DISPNMLG,FADDRES2                               * CAR 263963
.
          IF        PTCNHDPS=1
            MOVE      SP70,FADDRES3
            PROC      NHIDODHB
          ENDIF
.
          MATCH     GSRSNAM,PTMASNAM
          GOTO      TABROW92 IF NOT EQUAL
.
          MATCH     GSRGNAM,PMPXFNAM
          GOTO      TABROW92 IF NOT EQUAL
.
          MATCH     PTGSGNM2,PMPXSNAM
          GOTO      TABROW92 IF NOT EQUAL
.
          MOVE      PBDATE,GSRDOB
.
TABROW92  UNPACK    GSRDOB,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
.
          IF        ALIAFLAG<>0
            CLEAR     DISPNAME
            APPEND    "<b>",DISPNAME
            APPEND    GSRSNAM,DISPNAME
            APPEND    "</b> ",DISPNAME
            PACK      NAME35,GSRGNAM
            CALL      NAMSTR00
            PACK      NAME35,PTGSGNM2
            CALL      NAMSTR00
            APPEND    SP70,DISPNAME
            RESET     DISPNAME
            MOVE      DISPNAME,PATALIAS
            WRITE     HTMLFILE;PATALIAS,"&nbsp;(Alias)";
            IF        AIEXIST=1
              WRITE     HTMLFILE;"<img src=../images/AlternateID_Icon.gif ":
                                "class=Icon onclick=DFrameLink(":
                                "'patweb07.pbl?reportno=1&template=2&urnumber=":
                                URNUMBER,"',70,190,550,300);>"
            ENDIF
            WRITE     HTMLFILE;"<br>",PATFNAME;
          ELSE
            WRITE     HTMLFILE;PATFNAME;
            IF        ALEXIST=1
              WRITE     HTMLFILE;"<img src=../images/AliasIcon.gif class=Icon ":
                                "onclick=DFrameLink(":
                                "'patweb89.pbl?reportno=6&template=2&urnumber=":
                                URNUMBER,"',70,190,550,300);>"
            ENDIF
            IF        AIEXIST=1
              WRITE     HTMLFILE;"<img src=../images/AlternateID_Icon.gif ":
                                "class=Icon onclick=DFrameLink(":
                                "'patweb07.pbl?reportno=1&template=2&urnumber=":
                                URNUMBER,"',70,190,550,300);>"
            ENDIF
          ENDIF
          MOVE      SP1,ANS
          LOAD      ANS,PCASE,CCNOTES    * Load case notes indicator
          IF         SRCHFMT = 5
            MOVE      ADMISSNO,KEY8
            REP       "+ ",KEY8
.                                                                   * CAR 263963
            WRITE     HTMLFILE;"<br>",FADDRES1,"<br>",FADDRES2;
            IF        PTCNHDPS=1
              WRITE     HTMLFILE;"<br><b>",FADDRES3,"</b>";
            ENDIF
            WRITE     HTMLFILE;"</td></tr></table>":
                        "<td width=50 style=#"padding:0px 5px#">":
                          "<b>",CHKID,"</b>",ANS,"<br>",PMEDI,"</td>":
                        "<td width=63 style=#"padding:0px 5px#">",CDAY,SP1:
                          MTHNAM3,SP1,CCENT,CYEAR," ":
                          " (",PSAGECON,PSAGETYP,")<br>",DSEXSDES,"</td>":
                        "<td width=30 style=#"padding:0px 5px#">",KEY8,"</td>";
          ELSE
            IF        ALTIDFLG=1
.                                                                   * CAR 263963
              WRITE     HTMLFILE;"<br>",FADDRES1,"<br>",FADDRES2;
              IF        PTCNHDPS=1
                WRITE     HTMLFILE;"<br><b>",FADDRES3,"</b>";
              ENDIF
              WRITE     HTMLFILE;"</td></tr></table>":
                          "<td width=60 style=#"padding:0px 5px#">":
                          "<b>",PMAIALID,"</b>",ANS,"<br>";
            ELSE
.                                                                   * CAR 263963
              WRITE     HTMLFILE;"<br>",FADDRES1,"<br>",FADDRES2;
              IF        PTCNHDPS=1
                WRITE     HTMLFILE;"<br><b>",FADDRES3,"</b>";
              ENDIF
              WRITE     HTMLFILE;"</td></tr></table>":
                          "<td width=60 style=#"padding:0px 5px#">":
                          "<b>",CHKID,"</b>",ANS,"<br>";
            ENDIF
            WRITE     HTMLFILE;PMEDI,"</td>";
            IF        ALIAFLAG=1
              WRITE     HTMLFILE;"<td width=105 style=#"padding:0px 5px#">":
                          CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR:
                          " (",PSAGECON,PSAGETYP,")(Alias)<br>";
              UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
              MOVE      CMON,F2
              LOAD    MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
              WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1:
                             CCENT,CYEAR,"<br>":
                             DSEXSDES,"</td>";
            ELSE
              WRITE     HTMLFILE;"<td width=105 style=#"padding:0px 5px#">":
                          CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR," ":
                          " (",PSAGECON,PSAGETYP,")<br>",DSEXSDES,"</td>";
            ENDIF
          ENDIF
.
          CALL      ROWLNK00
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
.
          IF        SRCHFMT = 5
            WRITE     HTMLFILE;"<td width=40 style=#"padding:0px 5px#">":
                        SRCHSTA4,"</td>"
            WRITE     HTMLFILE;"<td width=65 style=#"padding:0px 5px#">":
                        SRCHSTA3,"</td>"
.
            COMPARE   THREE,ASTAT
            IF        !@EQUAL         
              CALL      GETCON00
              UNPACK    LPCDATE,CCENT,CYEAR,CMON,CDAY
              PACK      DISPDATE,LBRACKT,CDAY,SLASH,CMON,SP1,LPCTIME,RBRACKT
              WRITE     HTMLFILE;"<td width=150 style=#"padding:0px 5px#"":
                          " nowrap>","<img src=../images/",IMGSRC:
                          " class=ListIcon >",FONTCOL,CONDESC,DISPDATE:
                          "<font color=black><br>",SRCHSTA5,"</br>":
                          SRCHSTA6,"<br>",SRCHSTA7,"<br>",SRCHSTA8;
            ELSE
.
              WRITE     HTMLFILE;"<td width=150 style=#"padding:0px 5px#">":
                          SRCHSTA5,"<br>",SRCHSTA6,"<br>",SRCHSTA7,"<br>":
                          SRCHSTA8
            ENDIF
          ELSE
            MATCH     SP70,PMPXPRVI
            IF        @EQUAL
              MOVE      SP70,TDESC
              WRITE     HTMLFILE;"<td width=170 style=#"padding:0px 5px#">":
                          SRCHSTA1,"<br>",SRCHSTA2,"<br>",TDESC
            ELSE
              MOVE      "PV",KEY2
              PACK      KEY5,KEY2,PMPXPRVI
              CALL      RDCODE1
              WRITE     HTMLFILE;"<td width=170 style=#"padding:0px 5px#">":
                          SRCHSTA1,"<br>",SRCHSTA2:
                          "<br>","Privacy Indicator - ",TDESC
            ENDIF
          ENDIF
.
          BRANCH    DISDOCTY,TABROW95
          CALL      DOCTYP00
          WRITE     HTMLFILE;"<td width=75 style=#"padding:0px 5px#">",TDESC
.
TABROW95  CALL      FCBY0000
          WRITE     HTMLFILE;"<br>",SRCHCL20,"</td>";
.
TABROW96  MATCH     "1",EXTNSRCH
          IF        @EQUAL
            WRITE     HTMLFILE;"<td width=75 style=#"padding:0px 5px#">":
                               "Home: ",PTELEP,"<br>":
                               "Mob:  ",PTMXCELL,"<br>":
                               "Work: ",PTELEB;
            WRITE     HTMLFILE;"</td>";
            MOVE      SP70,TDESC 
            MOVE      "HT",KEY2
            PACK      KEY5,KEY2,PFNDTB
            CALL      RDCODE1
            WRITE     HTMLFILE;"<td width=110 style=#"padding:0px 5px#">":
                               "Fund:   ",PFUNDH,"<br>":
                               "Table:  ",TDESC,"<br>":
                               "Mem No: ",PFNDMM;
            WRITE     HTMLFILE;"</td>";
          ENDIF
.
          WRITE     HTMLFILE;"</tr>"
.
TABROW99  RETURN
+
.
. FHIR Patient Resource
.
FHRROW00  MOVE      ONE,FHRALIAF      * Yes Alias
          MOVE      GSRSNAM,FHRASNM   *  Alias Surname Search
          MOVE      GSRGNAM,FHRAGNM1 *  Alias First Name Search
          MOVE      PTGSGNM2,FHRAGNM2 *  Alias Second Name Search
.        
          REP       UPPLOW,FHRASNM
          MATCH     GSRSNAM,PTMASNAM
          GOTO      FHRROW40 IF NOT EQUAL
.
          MATCH     SP70,GSRGNAM
          GOTO      FHRROW20 IF EQUAL
          MATCH     GSRGNAM,PMPXFNAM
          GOTO      FHRROW40 IF NOT EQUAL
.
FHRROW20  MATCH     SP70,PTGSGNM2
          GOTO      FHRROW30 IF EQUAL
          MATCH     PTGSGNM2,PMPXSNAM
          GOTO      FHRROW40 IF NOT EQUAL
.
FHRROW30  MOVE      ZERO,FHRALIAF      * Not Alias
.
FHRROW40  PACK      KEY8,GSRURNO
          MOVE      KEY8,FHRPATID
          SQUEEZE   FHRPATID
          STRIP     PTITL
          STRIP     PTMASNAM
                                       * Yes Alias
          IF        DISNUMB>0
            WRITE     HTMLFILE;*+,",";
          ENDIF
          WRITE     HTMLFILE;*+,"    {":
                                " #"fullUrl#": #"%BASEURL/Patient/",FHRPATID,"#",":
                                " #"resource#": ";
          CALL      RESPAT00
          WRITE     HTMLFILE;*+,"}";
          RETURN
.------------------------------------------------------------
. Print line
.------------------------------------------------------------
PRNLIN00  REP       " +",URNUMBER
          REP       " +",ADMISSNO
          WRITE      HTMLFILE;"<tr valign=top><td width=170>":
                              "<table><tr valign=top><td>";
.
          WRITE     HTMLFILE;"<a href='",LINKPRG,".pbl?":
                             "&reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&urnumber=",URNUMBER:
                             "&admissno=",ADMISSNO,"'>";
.
          CALL      PATFLD00
          WRITE     HTMLFILE;"<img src='../images/PatientFolder",KEY3,".gif'":
                             " class=Icon></a>":
                             "</td>";
.
          WRITE     HTMLFILE;"<td>",SRCHCL01,"</td></tr></table>";
.
          MATCH     SP70,SRCHCL02
          IF        @EQUAL
             MOVE     NBSP,SRCHCL02
          ENDIF
.
          MATCH     SP70,SRCHCL03
          IF        @EQUAL
             MOVE     NBSP,SRCHCL03
          ENDIF
.
          MATCH     SP70,SRCHCL04
          IF        @EQUAL
             MOVE     NBSP,SRCHCL04
          ENDIF
.
          MATCH     SP70,SRCHCL05
          IF        @EQUAL
             MOVE     NBSP,SRCHCL05
          ENDIF
.
          MATCH     SP70,SRCHCL06
          IF        @EQUAL
             MOVE     NBSP,SRCHCL06
          ENDIF
.
          MATCH     SP70,SRCHCL07
          IF        @EQUAL
             MOVE     NBSP,SRCHCL07
          ENDIF
.
          MATCH     SP70,SRCHCL08
          IF        @EQUAL
             MOVE     NBSP,SRCHCL08
          ENDIF
.
          MATCH     SP70,SRCHCL18
          IF        @EQUAL
             MOVE     NBSP,SRCHCL18
          ENDIF
.
          MATCH     SP70,SRCHCL20
          IF        @EQUAL
             MOVE     NBSP,SRCHCL20
          ENDIF
.
          MATCH     SP70,SRCHCL22
          IF        @EQUAL
             MOVE     NBSP,SRCHCL22
          ENDIF
.
          MOVE      ZERO,FORM1
          MOVE      SRCHSYST,FORM1
          BRANCH    FORM1,PRNLIN10,PRNLIN20,PRNLIN30,PRNLIN40
.
.         Inpatient
.
          IF        KIDSONLY=ONE
.                                   * Royal Childrens Hospital only
            WRITE     HTMLFILE;"<td width=40 ":
                               "style='overflow:hidden;text-overflow:ellipsis'":
                               " title='",SRCHCL02,"'>",SRCHCL02,"</td>";
.
            COMPARE   "1",IBCNMHOS
            IF        @EQUAL
              WRITE     HTMLFILE;"<td width=40 ":
                               "style='overflow:hidden;text-overflow:ellipsis'":
                               " title='",SRCHCL22,"'>",SRCHCL22,"</td>";
            ENDIF
.
            WRITE     HTMLFILE;"<td width=40>",SRCHCL03,"</td>":
                               "<td width=70>",SRCHCL04,"</td>":
                               "<td width=40>",SRCHCL05,"</td>":
                               "<td width=70>",SRCHCL06,"</td>":
                               "<td width=40>",SRCHCL07,"</td>":
                               "<td width=40>",SRCHCL08,"<br>",SRCHCL18,"</td>";
            MATCH     SP70,SRCHCL09
            IF        @EQUAL
               WRITE     HTMLFILE;"<td width=110>",SRCHCL11:
                                  "<br>",SRCHCL12;
            ELSE
               WRITE     HTMLFILE;"<td width=110>",SRCHCL09,"<br>",SRCHCL11:
                                  "<br>",SRCHCL12;
            ENDIF
          ELSE
.                                   * Standard (St. Vincent's)      
            WRITE     HTMLFILE;"<td nowrap ":
                               "style='overflow:hidden;text-overflow:ellipsis'":
                               " title='",SRCHCL02,"'>",SRCHCL02,"</td>";
.
            COMPARE   "1",IBCNMHOS
            IF        @EQUAL
              WRITE     HTMLFILE;"<td width=40 ":
                               "style='overflow:hidden;text-overflow:ellipsis'":
                               " title='",SRCHCL22,"'>",SRCHCL22,"</td>";
            ENDIF

            WRITE     HTMLFILE;"<td>",SRCHCL03,"</td>":
                               "<td nowrap>",SRCHCL04,"<br>":
                                             SRCHCL05,"</td>":
                               "<td nowrap>",SRCHCL06,"<br>":
                                             SRCHCL07,"</td>":
                               "<td>",SRCHCL08,"<br>",SRCHCL18,"</td>";

            WRITE     HTMLFILE;"<td nowrap>";
            MATCH     SP70,SRCHCL09               * Discharge Destination
            IF        !@EQUAL
              WRITE     HTMLFILE;SRCHCL09;        
            ELSE
              MATCH     SP70,SRCHCL14             * OR ( Patient Condition (CO)
              IF        !@EQUAL
                WRITE     HTMLFILE;SRCHCL14,"<br>";
              ENDIF
              MATCH     SP70,SRCHCL15             *      AND Pat.Cond (desc) 
              IF        !@EQUAL
                WRITE     HTMLFILE;SRCHCL15,"<br>";
              ENDIF
              MATCH     SP70,SRCHCL17             *      AND Last Updt tstamp)
              IF        !@EQUAL
                WRITE     HTMLFILE;SRCHCL17,"<br>";
              ENDIF
              STRIP     SRCHCL12                    * Compress Phone/Visitor 
              STRIP     SRCHCL11                    * Output with Last Update
              MOVE      SRCHCL11,DIM70                                 *I-54756
              PACK      SRCHCL11,LSQBRKT,SRCHCL12,COMMA,NBSP,DIM70,RSQBRKT
              WRITE     HTMLFILE;SRCHCL11,NBSP,NBSP,SRCHCL16;
            ENDIF
          ENDIF
.
          MATCH     SP70,SRCHCL13
          IF        @EQUAL
            WRITE     HTMLFILE;"</td>";
          ELSE
            WRITE     HTMLFILE;"<br>",SRCHCL13,"</td>";
          ENDIF
.
          WRITE     HTMLFILE;"<td width=40>",SRCHCL10,"</td></tr>"
          GOTO      PRNLIN99
.
.         Outpatient
.
PRNLIN10  WRITE     HTMLFILE;"<td width=100>",SRCHCL02,"</td>";
.
          COMPARE   "1",IBCNMHOS
          IF        @EQUAL
            WRITE     HTMLFILE;"<td width=40>",SRCHCL22,"</td>";
          ENDIF
.
          WRITE     HTMLFILE;"<td width=100>",SRCHCL03,"</td>":
                             "<td width=120>",SRCHCL04,"</td>":
                             "<td width=80>",SRCHCL05,"</td>":
                             "<td width=80>",SRCHCL06,"</td>":
                             "<td width=80>",SRCHCL07,"</td></tr>"
          GOTO      PRNLIN99
.
.         Emergency
.
PRNLIN20  WRITE     HTMLFILE;"<td width=70>",SRCHCL02,"</td>";
.
          COMPARE   "1",IBCNMHOS
          IF        @EQUAL
            WRITE     HTMLFILE;"<td width=40>",SRCHCL22,"</td>";
          ENDIF
.
          WRITE     HTMLFILE;"<td width=70>",SRCHCL03,"</td>":
                             "<td width=50>",SRCHCL04,"</td>":
                             "<td width=70>",SRCHCL05,"</td>":
                             "<td width=50>",SRCHCL06,"</td>":
                             "<td width=110>",SRCHCL07,"</td>":
                             "<td width=50>",SRCHCL08,"</td></tr>"
          GOTO      PRNLIN99
.
.         Event
.
PRNLIN30  WRITE     HTMLFILE;"<td width=80>",SRCHCL02,"</td>";
.
          COMPARE   "1",IBCNMHOS
          IF        @EQUAL
            WRITE     HTMLFILE;"<td width=40>",SRCHCL22,"</td>";
          ENDIF
.
          WRITE     HTMLFILE;"<td width=100>",SRCHCL03,"</td>":
                             "<td width=100>",SRCHCL04,"</td>":
                             "<td width=120>",SRCHCL05,"</td>":
                             "<td width=80>",SRCHCL06,"</td>":
                             "<td width=80>",SRCHCL07,"</td>":
                             "<td width=60>",SRCHCL08,"</td></tr>"
          GOTO      PRNLIN99
.
.         Allied Health
.
PRNLIN40  WRITE     HTMLFILE;"<td width=90>",SRCHCL02,"</td>";
.
          COMPARE   "1",IBCNMHOS
          IF        @EQUAL
            WRITE     HTMLFILE;"<td width=40>",SRCHCL22,"</td>";
          ENDIF
.
          WRITE     HTMLFILE;"<td width=120>",SRCHCL03,"</td>":
                             "<td width=90>",SRCHCL04,"</td>":
                             "<td width=90>",SRCHCL05,"</td>":
                             "<td width=90>",SRCHCL06,"</td>":
                             "<td width=90>",SRCHCL07,"</td></tr>"
          GOTO      PRNLIN99
.
PRNLIN99  RETURN
+
.------------------------------------------------------------
. Get Medical Records Doc Type
.------------------------------------------------------------
DOCTYP00  MOVE      NBSP,TDESC
          MATCH     ZEROUR,GSRURNO
          GOTO      DOCTYP99 IF EQUAL
          PACK      KEY20,GSRURNO,Z70
          CALL      RSMRMAS3
          CALL      RPMRMAS3
          BRANCH    OVRCD,DOCTYP99
          MATCH     MRMAURNO,GSRURNO
          GOTO      DOCTYP99 IF NOT EQUAL
          MOVE      "TY",KEY2
          PACK      KEY5,KEY2,MRMADOTY
          CALL      RDCODE1
DOCTYP99  RETURN
+
.------------------------------------------------------------
. Row Links
.------------------------------------------------------------
ROWLNK00  COMPARE   ZERO,LINKNUMB
          GOTO      ROWLNK99 IF EQUAL
.
          WRITE     HTMLFILE;"<td width=60>";
          MOVE      ZERO,LINKCNT
.
ROWLNK10  COMPARE   LINKCNT,LINKNUMB
          GOTO      ROWLNK90 IF EQUAL
          ADD       ONE,LINKCNT
          WRITE     HTMLFILE;"<img src=../images/PatientLink",LINKCNT,".gif":
                             " class=Icon":
                             " onclick='patientLink(#"",LINKCNT,"#",":
                             "#"",URNUMBER,"#",":
                             "#"",ADMISSNO,"#");'>";
          GOTO      ROWLNK10
.
ROWLNK90  WRITE     HTMLFILE;"</td>"
.
ROWLNK99  RETURN
.----------------------------------------------------------------------
. Test for a valid date of birth  Exit=0 Ok  Exit=1 Out of Range
.----------------------------------------------------------------------
CHDOB000 MATCH      SP4,SRCHFDAT
         GOTO       CHDOB500 IF EQUAL
.
         UNPACK     PBDATE,CCENT,CYEAR,CMON,CDAY
         PACK       SRCHWORK,CMON,CDAY
.
.        Two different sets of test, depending on whether or not the
.        ending date is less than the starting date, or not.
.
         MATCH      SRCHFDAT,SRCHTDAT
         GOTO       CHDOB250 IF LESS
.
.        We want a date in the date range, and in the year range
.
         MATCH      SRCHFDAT,SRCHWORK
         GOTO       CHDOB900 IF LESS
.
         MATCH      SRCHWORK,SRCHTDAT
         GOTO       CHDOB900 IF LESS
.
         PACK       SRCHWORK,CCENT,CYEAR
         REP        " 0",SRCHWORK
.
         MATCH      SRCHFSYR,SRCHWORK
         GOTO       CHDOB900 IF LESS
.
         MATCH      SRCHWORK,SRCHTSYR
         GOTO       CHDOB900 IF LESS
.
         GOTO       CHDOB500            * Valid record
.
.        We want a date less that the todate, OR after the start date
.        This occurs when the valid date is something like 27/12 to 03/01
.
CHDOB250 MATCH      SRCHFDAT,SRCHWORK
         GOTO       CHDOB270 IF LESS
.
.        We have a date after the starting date
.
         PACK       SRCHWORK,CCENT,CYEAR
         REP        " 0",SRCHWORK
.
         MATCH      SRCHFSYR,SRCHWORK
         GOTO       CHDOB900 IF LESS
.
         MATCH      SRCHWORK,SRCHTSYR
         GOTO       CHDOB900 IF LESS
.
         GOTO       CHDOB500            * Valid record
.
.        We have a date before the to date
.        Check if after the from date
.
CHDOB270 MATCH      SRCHWORK,SRCHTDAT
         GOTO       CHDOB900 IF LESS
.
         PACK       SRCHWORK,CCENT,CYEAR
         REP        " 0",SRCHWORK
.
         MATCH      SRCHFEYR,SRCHWORK
         GOTO       CHDOB900 IF LESS
.
         MATCH      SRCHWORK,SRCHTEYR
         GOTO       CHDOB900 IF LESS
.
CHDOB500 MOVE       ZERO,EXIT
         GOTO       CHDOB999
.
CHDOB900 MOVE       ONE,EXIT
.
CHDOB999 RETURN
. *****************************************************************************
. * SRCHZ000   : Re-calculate the patients age before re-displaying it        *
. * Parameters : PBDATE   Birth Date (CCYYMMDD)                               *
. * Returns    : PSAGE    Patients age                                        *
. *****************************************************************************
.
SRCHZ000  PACK      CTIMEIS,CCC,CYY,CMM,CDD  * Date to calculate age to.
.
.         If the patient is deceased, and a date of death is known, then
.         calculate the age to the date of death, not current date
.
          COMPARE   ONE,PCEASE               * Is the patient dead ?
          GOTO      SRCHZ100 IF NOT EQUAL    * No.
.
          MATCH     SP8,PDECDTE              * Do we have a date of death ?
          GOTO      SRCHZ100 IF EQUAL        * No.
.
          MOVE      PDECDTE,CTIMEIS
.
SRCHZ100  REP       " 0",CTIMEIS
          UNPACK    CTIMEIS,XCENT,XYEAR,XMON,XDAY
.
.         Set up the patients birth date
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY  * Unpack the birthdate
          PACK      SRCHWORK,CCENT,CYEAR    * Pack up the full birth year
          REP       " 0",SRCHWORK
          PACK      SRCHBDAY,CMON,CDAY       * Pack up the annual birthdate
          REP       " 0",SRCHBDAY
          MOVE      SRCHWORK,SRCHYEAR        * Birth year as a numeric
.
          PACK      SRCHWORK,XCENT,XYEAR     * Pack up the full current year
          REP       " 0",SRCHWORK
          MOVE      SRCHWORK,SRCHBRTH        * Current year as a numeric
.
.         Calculate the number of years since birth
.
          SUB       SRCHYEAR,SRCHBRTH
          MOVE      SRCHBRTH,PSAGE           * Save the number years
.
.         Check if the day and month born is less than the current date
.
          PACK      SRCHWORK,XMON,XDAY
          REP       " 0",SRCHWORK
.
          MATCH     SRCHBDAY,SRCHWORK        * Has the birthday past yet ?
          GOTO      SRCHZ800 IF NOT LESS     * Yes.
.
          SUB       ONE,PSAGE                * Haven't reached the birthday yet'
.
SRCHZ800  COMPARE   ONE,PTCNDATE
          GOTO      SRCHZ999 IF NOT EQUAL
.
. ----- Will calculate the patients age in either years, months, weeks, or days.
. ----- the age will be in variable (PSAGECON) and the age type in (PSAGETYP)
.
          COMPARE   ONE,PCEASE               * Is the patient dead ?
          GOTO      SRCHZ900 IF NOT EQUAL    * No.
.
          MATCH     SP8,PDECDTE              * Do we have a date of death ?
          GOTO      SRCHZ900 IF EQUAL        * No.
.
. ------  The patient is dead so set agedate to date of death, the ------
. ------  "X" variables are set at the top of the routine.         ------
.
          PACK      AGEDATE,XCENT,XYEAR,XMON,XDAY
          REP       " 0",AGEDATE
          GOTO      SRCHZ950
          
.
SRCHZ900  PACK      AGEDATE,CCC,CYY,CMM,CDD
.
SRCHZ950  CALL      CALCAGE
.
SRCHZ999  RETURN
.------------------------------------------------------------
.  Get the Patients Status
.------------------------------------------------------------
GETS0000  MOVE      SP70,SRCHSTA1
          MOVE      SP70,SRCHSTA2
          MOVE      SP70,SRCHSTA3
          MOVE      SP70,SRCHSTA4
          MOVE      SP70,SRCHSTA5
          MOVE      SP70,SRCHSTA6
          MOVE      SP70,SRCHSTA7
          MOVE      SP70,SRCHSTA8
          MOVE      SP70,ADMISSNO
          MOVE      PURNO,URNUMBER
.
          COMPARE   ONE,PCEASE                    * is patient deceased ?
          GOTO      GETS8000 IF EQUAL             * yes
          BRANCH    SRCHWAIT,GETS7000
.
. This first loop is to find if there are ANY admissions for this U/R
.
          PACK      KEY24,PURNO,Z70
          CALL      RDSVISA2
GETS1000  CALL      RDPVISA2
          BRANCH    OVRCD,GETS9999
          MATCH     PURNO,PVIURNO
          GOTO      GETS9999 IF NOT EQUAL
.
          MOVE      SP30,PMVXPIND
          UNPACK    SP70,PMVXRPOA,PMVXRCN1,PMVXRCN2,PMVXRHFU,PMVXRDVA
          PACK      KEY8,PVIBILL                 * Read Visit Extn for Privacy
          CALL      RDPMVX11
.
          CALL      CHKREL00                     * check 'release to' fields
.
          MOVE      PVIBILL,ADMISSNO
          IF        IBCNMHOS=1
            MATCH     PMVXMHOS,WBSEHOSP
            IF        !@EQUAL
              MOVE      ZEROVISN,ADMISSNO
            ENDIF
          ENDIF
          BRANCH    PVITYPE,GETS1100,GETS1200,GETS1300
.
          COMPARE   "5",SRCHFMT
          GOTO      GETS1000 IF NOT EQUAL
.
          IF        PVITYPE=9
            MATCH     " 1",PVISYST               * Event ?
            GOTO      GETS1400 IF EQUAL
            MATCH     " 2",PVISYST               * Allied Health ?
            GOTO      GETS1500 IF EQUAL
          ENDIF
          GOTO      GETS1000
.
. A & E Visit
.
GETS1100  PACK      KEY8,PVIBILL
          CALL      RDEMVIS1
          IF        OVRCD = 1
            MOVE     SP70,EMVIDSTA
            MOVE     SP70,EMVITRTM 
          ELSE
            COMPARE   FOUR,EMVISTAT
            GOTO      GETS1000 IF EQUAL
          ENDIF
.         
          UNPACK    PVIDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
.
          CLEAR     SRCHSTA1
          APPEND    "Emergency Visit - ",SRCHSTA1
          APPEND    CDAY,SRCHSTA1
          APPEND    SP1,SRCHSTA1
          APPEND    MTHNAM3,SRCHSTA1
          APPEND    SP1,SRCHSTA1
          APPEND    CCENT,SRCHSTA1
          APPEND    CYEAR,SRCHSTA1
          IF        IBCNMHOS = 1
            PACK      KEY3,PMVXMHOS
            CALL      RDPTHSP1
            IF        OVRCD<>1
              APPEND    " at ",SRCHSTA1
              APPEND    PTHSNAME,SRCHSTA1
              RESET     SRCHSTA1
            ENDIF
          ENDIF

          RESET     SRCHSTA1
.
          CLEAR     SRCHSTA3
          APPEND    CDAY,SRCHSTA3
          APPEND    SP1,SRCHSTA3
          APPEND    MTHNAM3,SRCHSTA3
          APPEND    SP1,SRCHSTA3
          APPEND    CCENT,SRCHSTA3
          APPEND    CYEAR,SRCHSTA3
          RESET     SRCHSTA3
.
          COMPARE   FIVE,SRCHFMT                * Skip setup of descriptions
          GOTO      GETS9999 IF NOT EQUAL       * if not current patient search
.
          MOVE      NBSP,SRCHSTA4
          MOVE      "Emergency",SRCHSTA5
.
          PACK      KEY8,PVIBILL
          CALL      RDEMVIS1
          IF        OVRCD = 1
            MOVE     SP70,EMVIDSTA 
            MOVE     SP70,EMVITRTM
          ENDIF
.
          IF        EMVISTAT=4
            MOVE     SP70,EMVIDSTA 
            MOVE     SP70,EMVITRTM
          ENDIF
.
          CLEAR     SRCHSTA6
          MOVE      "Triage Time:",SRCHSTA6
          ENDSET    SRCHSTA6
          APPEND    EMVITRTM,SRCHSTA6
          RESET     SRCHSTA6
.
          CLEAR     SRCHSTA7
          MOVE      "Disc.Status:",SRCHSTA7
          ENDSET    SRCHSTA7
          MOVE      "ED",KEY2
          MATCH     SP70,EMVIDSTA
          IF        !@EQUAL
            PACK      KEY5,KEY2,EMVIDSTA
            CALL      RDCODE1
            IF        OVRCD = 0
              APPEND    TDESC,SRCHSTA7
            ELSE
              APPEND    "Unknown",SRCHSTA7
            ENDIF
          ELSE
              APPEND    "Unknown",SRCHSTA7
          ENDIF
          RESET     SRCHSTA7
          GOTO      GETS9999
.
. Outpatient Visit
.
GETS1200  UNPACK    PVIDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
.
          CLEAR     SRCHSTA1
          APPEND    "Outpatient Visit - ",SRCHSTA1
          APPEND    CDAY,SRCHSTA1
          APPEND    SP1,SRCHSTA1
          APPEND    MTHNAM3,SRCHSTA1
          APPEND    SP1,SRCHSTA1
          APPEND    CCENT,SRCHSTA1
          APPEND    CYEAR,SRCHSTA1
          IF        IBCNMHOS = 1
            PACK      KEY3,PMVXMHOS
            CALL      RDPTHSP1
            IF        OVRCD<>1
              APPEND    " at ",SRCHSTA1
              APPEND    PTHSNAME,SRCHSTA1
              RESET     SRCHSTA1
            ENDIF
          ENDIF
.
          RESET     SRCHSTA1
.
          IF        SRCHFMT = 5
            CLEAR     SRCHSTA3
            APPEND    CDAY,SRCHSTA3
            APPEND    SP1,SRCHSTA3
            APPEND    MTHNAM3,SRCHSTA3 
            APPEND    SP1,SRCHSTA3
            APPEND    CCENT,SRCHSTA3
            APPEND    CYEAR,SRCHSTA3
            MOVE      NBSP,SRCHSTA4
            RESET     SRCHSTA3
          ENDIF
.
          MOVE      "out",OSTFILE
          MOVE      PVISITE,KEY6
          CALL      RDSITA1
.
          IF        SRCHFMT = 5
            MATCH     OSTFILE,OPNFILE
            IF        !@EQUAL
              MOVE      OSTFILE,OPNFILE
              MOVE      OSTFILE,CFNAME
              RESET     CFNAME,3
              APPEND    FILBOKA1,CFNAME
              RESET     CFNAME
              CLOSE     OUTBOKA3
              OPEN      OUTBOKA3,CFNAME 
            ENDIF
            MOVE      "Outpatient - Site:",SRCHSTA5
            ENDSET    SRCHSTA5
            APPEND    PVISITE,SRCHSTA5
            RESET     SRCHSTA5
          ENDIF
.
          PACK      CFNAME WITH OSTFILE,FILCLIA1
          OPEN      OUTCLIA1,CFNAME
.
          PACK      KEY20,PVISITE,PVIDOCT,PVIDATE,SP70
          CALL      RDCLIA1
          IF        OVRCD=1
            CALL      RDSCLIA1
            CALL      RDPCLIA1
            BRANCH    OVRCD,GETS9999
.
            MATCH     PVISITE,OCASITE
            GOTO      GETS9999 IF NOT EQUAL
.
            MATCH     PVIDOCT,OCACLIN
            GOTO      GETS9999 IF NOT EQUAL
          ENDIF
.
          MOVE      OCADESC,SRCHSTA2
          MATCH     SP6,OCADOCT    
          GOTO      GETS9999 IF EQUAL
.
          IF        SRCHFMT = 5
            MOVE      "Clinic Attending:",SRCHSTA6
          ENDIF
.
          MOVE      OCADOCT,KEY6         * Read the doctor's file to get nam
          CALL      RDDOCT1
          IF        OVRCD=1
            CLEAR     SRCHSTA2
            APPEND    "Invalid Doctor - ",SRCHSTA2
            IF        SRCHFMT = 5
              APPEND    "Invalid Doctor - ",SRCHSTA6
              RESET     SRCHSTA5
            ENDIF
            APPEND    PVIDOCT,SRCHSTA2
            RESET     SRCHSTA2
            GOTO      GETS9999
          ENDIF
          MOVE      DTITL,FMTTITLE
          MOVE      DGNAME,FMTGNAME
          MOVE      DSNAME,FMTSNAME
          CALL      DOCNM000
          MOVE      DOCFNAME,SRCHSTA2
.
          COMPARE   FIVE,SRCHFMT                * Skip setup of descriptions
          GOTO      GETS9999 IF NOT EQUAL       * if not current patient search
.
          APPEND    DOCFNAME,SRCHSTA6
          RESET     SRCHSTA6
          MOVE      "Appointment Time:",SRCHSTA7
          ENDSET    SRCHSTA7
.
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
          PACK      KEY36,PURNO,CURRDATE,SP70
          CALL      RDSBOKA3
GETS1210  CALL      RDKBOKA3
          BRANCH    OVRCD,GETS9999
          MATCH     PURNO,OBAURNO
          GOTO      GETS9999 IF NOT EQUAL
          MATCH     CURRDATE,OBADATE
          GOTO      GETS9999 IF NOT EQUAL
.
          MATCH     PVIBILL,OBAOUTNO
          IF        @EQUAL
            APPEND    OBATIME,SRCHSTA7
            RESET     SRCHSTA7
          ENDIF
          GOTO      GETS9999
.
. In Patient Visit
.
GETS1300  READ      CONTROLF,HUND12;*104,PTCNWBDS     * CAR 151896
          MOVE      PTCNWBDS,F1  * ward bed name option none,long,short
          MOVE      PVIBILL,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD OF GETS1000
          COMPARE   FIVE,ASTAT
          GOTO      GETS1000 IF EQUAL
          MOVE      SP70,SRCHSTA1
          MOVE      SP70,SRCHSTA3
          IF         SRCHFMT = 5
            UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      SRCHSTA3,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
            CLEAR     SRCHSTA4
            PACK      KEY6,AWARD,SP70
            CALL RDWARD1
            IF         F1 > 0 
              IF         F1 = 2
                APPEND     PTWDSDES,SRCHSTA4
              ELSE
                APPEND     WBDESC,SRCHSTA4
              ENDIF
            ELSE
              APPEND     AWARD,SRCHSTA4
            ENDIF
            STRIP      SRCHSTA4
.
            MATCH     SP70,ABED
            IF        !@EQUAL
              APPEND    "/",SRCHSTA4
              PACK      KEY6,AWARD,ABED
              CALL      RDWARD1
              IF        OVRCD = 0
                IF        F1 > 0 
                  IF        F1 = 2
                    APPEND    PTWDSDES,SRCHSTA4
                  ELSE
                    APPEND    WBDESC,SRCHSTA4
                  ENDIF
                ELSE
                  APPEND    ABED,SRCHSTA4
                ENDIF
                APPEND    "<br>",SRCHSTA4
                APPEND    WEXTN,SRCHSTA4
              ELSE
                APPEND    ABED,SRCHSTA4
              ENDIF
            ENDIF
            RESET     SRCHSTA4
          ENDIF
          IF        ASTAT=1
            UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            CLEAR     SRCHSTA1
            APPEND    STATA,SRCHSTA1
            APPEND    SP1,SRCHSTA1
            APPEND    CDAY,SRCHSTA1
            APPEND    SP1,SRCHSTA1
            APPEND    MTHNAM3,SRCHSTA1
            APPEND    SP1,SRCHSTA1
            APPEND    CCENT,SRCHSTA1
            APPEND    CYEAR,SRCHSTA1
            IF        IBCNMHOS = 1
              PACK      KEY3,PMVXMHOS
              CALL      RDPTHSP1
              IF        OVRCD<>1
                APPEND    " at ",SRCHSTA1
                APPEND    PTHSNAME,SRCHSTA1
                RESET     SRCHSTA1
              ENDIF
            ENDIF

          ENDIF
          IF        ASTAT=2
            MOVE      PTCNWBDS,F1  * ward bed name option none,long,short
            UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            CLEAR     SRCHSTA1
            APPEND    STATB,SRCHSTA1
            APPEND    SP1,SRCHSTA1
            APPEND    CDAY,SRCHSTA1
            APPEND    SP1,SRCHSTA1
            APPEND    MTHNAM3,SRCHSTA1
            APPEND    SP1,SRCHSTA1
            APPEND    CCENT,SRCHSTA1
            APPEND    CYEAR,SRCHSTA1
            IF        IBCNMHOS = 1
              PACK      KEY3,PMVXMHOS
              CALL      RDPTHSP1
              IF        OVRCD<>1
                APPEND    " at ",SRCHSTA1
                APPEND    PTHSNAME,SRCHSTA1
                RESET     SRCHSTA1
              ENDIF
            ENDIF
            CLEAR     SRCHSTA2
            APPEND    "Ward: ",SRCHSTA2
            IF        F1 > 0
              PACK      KEY6,AWARD,SP70
              CALL      RDWARD1
              IF        F1 = 2
                APPEND    PTWDSDES,SRCHSTA2
              ELSE
                APPEND    WBDESC,SRCHSTA2
              ENDIF
            ELSE
              APPEND    AWARD,SRCHSTA2
            ENDIF
.           RESET     SRCHSTA2
.           STRIP     SRCHSTA2

            MATCH     SP70,ABED
            IF        !@EQUAL
              APPEND    ", Bed: ",SRCHSTA2
              IF        F1 > 0
                PACK      KEY6,AWARD,ABED
                CALL      RDWARD1
                IF        F1 = 2
                  APPEND    PTWDSDES,SRCHSTA2
                ELSE
                  APPEND    WBDESC,SRCHSTA2
                ENDIF
              ELSE
                APPEND    ABED,SRCHSTA2
              ENDIF
            ENDIF
            RESET     SRCHSTA2
            IF        SRCHFMT = 5
              MOVE      "Current Inpatient - Unit",SRCHSTA5
              ENDSET    SRCHSTA5
              APPEND    ACLSS,SRCHSTA5
              RESET     SRCHSTA5
              MATCH     "1",PMVXVALW
              IF        @EQUAL
                MOVE      "Visitors Allowed: Yes",SRCHSTA6
              ELSE
                MOVE      "Visitors Allowed: No",SRCHSTA6
              ENDIF
              RESET     SRCHSTA6
              MATCH     "1",PMVXPALW
              IF        @EQUAL
                MOVE      "Phone Calls Allowed: Yes Ext.No:",SRCHSTA7
              ELSE
                MOVE      "Phone Calls Allowed: No",SRCHSTA7
              ENDIF
              RESET     SRCHSTA7
            ENDIF
          ENDIF
          IF        ASTAT=3
            MOVE      STATC,SRCHSTA1
            MOVE      PVIBILL,KEY8
            CALL      RDDSCH1
            BRANCH    OVRCD OF GETS9999
            UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            CLEAR     SRCHSTA1
            APPEND    STATCD,SRCHSTA1
            APPEND    SP1,SRCHSTA1
            APPEND    CDAY,SRCHSTA1
            APPEND    SP1,SRCHSTA1
            APPEND    MTHNAM3,SRCHSTA1
            APPEND    SP1,SRCHSTA1
            APPEND    CCENT,SRCHSTA1
            APPEND    CYEAR,SRCHSTA1
            IF        IBCNMHOS = 1
              PACK      KEY3,PMVXMHOS
              CALL      RDPTHSP1
.lll          STRIP     SRCHSTA1
.lll          ENDSET    SRCHSTA1
              APPEND    " at ",SRCHSTA1
              APPEND    PTHSNAME,SRCHSTA1
              RESET     SRCHSTA1
            ENDIF

            IF        SRCHFMT = 5
              MOVE      "Discharged",SRCHSTA5
              RESET     SRCHSTA5
              MOVE      "Date ",SRCHSTA6
              ENDSET    SRCHSTA6
              PACK      SRCHSTA6,SRCHSTA6,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
              RESET     SRCHSTA6
              MOVE      "Time :",SRCHSTA7
              ENDSET    SRCHSTA7
              APPEND    DTIME,SRCHSTA7
              MOVE      "Destination :",SRCHSTA8
              ENDSET    SRCHSTA8
              MOVE      "DD",KEY2
              MATCH     SP3,DDEST
              IF        !@EQUAL
                PACK      KEY5,KEY2,DDEST
                CALL      RDCODE1
                IF        OVRCD = 0
                  APPEND    TDESC,SRCHSTA8
                ELSE
                  APPEND    "Unknown",SRCHSTA8
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          IF        ASTAT=4
            MOVE     SP70,SRCHSTA1
            CLEAR     SRCHSTA1
            APPEND    "On Leave",SRCHSTA1
            IF        IBCNMHOS = 1
              PACK      KEY3,PMVXMHOS
              CALL      RDPTHSP1
.lll          STRIP     SRCHSTA1
.lll          ENDSET    SRCHSTA1
              APPEND    " from ",SRCHSTA1
              APPEND    PTHSNAME,SRCHSTA1
              RESET     SRCHSTA1
            ENDIF
          ENDIF
          IF        SRCHFMT = 5
            IF        ASTAT=4
               MOVE     SP70,SRCHSTA4
               MOVE     "On Leave",SRCHSTA4
            ENDIF
          ENDIF 
          GOTO      GETS9999
.
.         Event
.
GETS1400  PACK      KEY8,PVIBILL,SP70
          CALL      RDPMEVT1
          BRANCH    OVRCD,GETS9999
.
          UNPACK    PMEVADTE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      SRCHSTA3,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          MOVE      SP70,SRCHSTA4
          MOVE      "&nbsp;",SRCHSTA4 
          MATCH     SP3,PMEVPWRD
          IF        !@EQUAL
            MOVE      SP70,SRCHSTA4
            CLEAR     SRCHSTA4
            APPEND    PMEVPWRD,SRCHSTA4
            APPEND    " / ",SRCHSTA4
            APPEND    PMEVPBED,SRCHSTA4
            RESET     SRCHSTA4
          ENDIF
.
          CLEAR     SRCHSTA5
          APPEND    "Attendance Time:",SRCHSTA5
          APPEND    PMEVATIM,SRCHSTA5
          RESET     SRCHSTA5
.
          MOVE      "EV",CATEGORY
          MOVE      PMEVEVNT,CODE
          MATCH     SP3,CODE
          IF        !@EQUAL
            PACK      KEY5,CATEGORY,CODE
            CALL      RDCODE1
            IF        OVRCD=0
              CLEAR     SRCHSTA6
              APPEND    "Event Type:",SRCHSTA6
              APPEND    TDESC,SRCHSTA6
              RESET     SRCHSTA6
            ENDIF
          ENDIF
.
          MOVE      "AC",CATEGORY
          MOVE      PMEVUNIT,CODE
          MATCH     SP3,CODE
          IF        !@EQUAL
            PACK      KEY5,CATEGORY,CODE
            CALL      RDCODE1
            IF        OVRCD=0
              CLEAR     SRCHSTA7
              APPEND    "Unit:",SRCHSTA7
              APPEND    TDESC,SRCHSTA7
              RESET     SRCHSTA7
            ENDIF
          ENDIF
.
          CLEAR     SRCHSTA8
          MATCH     SP70,PMEVDDTE
          GOTO      GETS9999 IF EQUAL
          APPEND    "Discharge Date/Time:",SRCHSTA8
          UNPACK    PMEVDDTE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      KEY12,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          APPEND    KEY12,SRCHSTA8
          APPEND    SP1,SRCHSTA8
          APPEND    PMEVDTIM,SRCHSTA8
          RESET     SRCHSTA8
          GOTO      GETS9999
.
.         Allied Health
.
GETS1500  PACK      KEY8,PVIBILL,SP70
          CALL      RDALREF1                   * read referral table
          BRANCH    OVRCD,GETS9999
.
          UNPACK    ALREDACT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      SRCHSTA3,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          CLEAR     SRCHSTA5
          APPEND    "Referral Management -",SRCHSTA5
          MATCH     "0",ALRESTAT
          GOTO      GETS1510 IF NOT EQUAL
          APPEND    "On Waiting List",SRCHSTA5
          GOTO      GETS1570
.
GETS1510  MATCH     "1",ALRESTAT
          GOTO      GETS1520 IF NOT EQUAL
          APPEND    "Active",SRCHSTA5
          GOTO      GETS1570
.
GETS1520  MATCH     "2",ALRESTAT
          GOTO      GETS1530 IF NOT EQUAL
          APPEND    "Closed",SRCHSTA5
          GOTO      GETS1570
.
GETS1530  MATCH     "3",ALRESTAT
          GOTO      GETS1540 IF NOT EQUAL
          APPEND    "Inactive",SRCHSTA5
          GOTO      GETS1570
.
GETS1540  MATCH     "4",ALRESTAT
          GOTO      GETS1570 IF NOT EQUAL
          APPEND    "Cancelled",SRCHSTA5
.
GETS1570  RESET     SRCHSTA5
          PACK      KEY16,PVIBILL,Z70          * read encounter table
          CALL      RSALENC1
          CALL      RPALENC1
          BRANCH    OVRCD,GETS9999
          MOVE      PVIBILL,KEY8
          MATCH     KEY8,ALENVISN           * Same Visit number ?
          GOTO      GETS9999 IF NOT EQUAL
.
          RESET     SRCHSTA6
          APPEND    "Last Encounter (",SRCHSTA6
          UNPACK    ALENSDAT,CCENT,CYEAR,CMON,CDAY * format date of death
          CALL      PACDATE
          APPEND    CPCDATE,SRCHSTA6
          RESET     SRCHSTA6
          GOTO      GETS9999
.
.         Waiting List Search
.
GETS7000  UNPACK    WMDATE1,CCENT,CYEAR,CMON,CDAY * format date on List
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      KEY12,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          CLEAR     SRCHSTA1
          APPEND    "Date on List ",SRCHSTA1
          APPEND    KEY12,SRCHSTA1
          IF        IBCNMHOS = 1
            PACK      KEY3,WTWMIHSP
            CALL      RDPTHSP1
            IF        OVRCD<>1
              APPEND    " at ",SRCHSTA1
              APPEND    PTHSNAME,SRCHSTA1
              RESET     SRCHSTA1
            ENDIF
          ENDIF

          RESET     SRCHSTA1
          LOAD      KEY14,WMSTAT,WAITST01,WAITST02,WAITST03,WAITST04:
                          WAITST05,WAITST06,WAITST07,WAITST08,WAITST09
          CLEAR     SRCHSTA2
          APPEND    "Status - ",SRCHSTA2
          APPEND    KEY14,SRCHSTA2
          RESET     SRCHSTA2
          GOTO      GETS9999
.
.         Deceased
.
GETS8000  IF        SRCHFMT = 5
            MOVE      SP70,SRCHSTA4
            MOVE      "&nbsp;",SRCHSTA4 
          ENDIF
          UNPACK    SP70,CDAY,CMON,CYEAR,CCENT
          MATCH     SP8,PDECDTE
          GOTO      GETS8100 IF EQUAL
          UNPACK    PDECDTE,CCENT,CYEAR,CMON,CDAY * format date of death
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      KEY12,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          PACK      SRCHSTA1,HTMLSBLD,STATDD,KEY12,HTMLEBLD
          GOTO      GETS9999
.
GETS8100  PACK      SRCHSTA1,HTMLSBLD,STATDD,HTMLEBLD
          GOTO      GETS9999
.
GETS9999  RETURN
.------------------------------------------------------------
. A Zero U/R number Status.
.------------------------------------------------------------
STATZ000  MOVE      SP70,SRCHSTA1
          MOVE      SP70,SRCHSTA2
          MOVE      SP70,SRCHSTA3
.
          BRANCH     GSRSYS,STATZ999,STATZ200,STATZ300
          GOTO       STATZ999
.
.        Outpatient pre-attendance booking
.
STATZ200 MOVE       GSRBILL,KEY8
         CALL       RDPREA1
         BRANCH     OVRCD,STATZ250               * No details, ignore record
.
         UNPACK     OPRDATE,CCENT,CYEAR,CMON,CDAY
         MOVE       CMON,F2
         LOAD       MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
         CLEAR      SRCHSTA1
         APPEND     "Pre-attend for Clinic ",SRCHSTA1
         APPEND     OPRCLIN,SRCHSTA1
         APPEND     " at ",SRCHSTA1
         APPEND     OPRTIME,SRCHSTA1
         APPEND     " on ",SRCHSTA1
         APPEND     CDAY,SRCHSTA1
         APPEND     SP1,SRCHSTA1
         APPEND     MTHNAM3,SRCHSTA1
         APPEND     SP1,SRCHSTA1
         APPEND     CCENT,SRCHSTA1
         APPEND     CYEAR,SRCHSTA1
         APPEND     " (",SRCHSTA1
         APPEND     OPRCTYP,SRCHSTA1
         APPEND     ")",SRCHSTA1
         RESET      SRCHSTA1
         GOTO       STATZ999
.
STATZ250 CALL       RDOTRFL1
         BRANCH     OVRCD,STATZ999
         UNPACK     OTRLDATE,CCENT,CYEAR,CMON,CDAY
         MOVE       CMON,F2
         LOAD       MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
         CLEAR      SRCHSTA1
         APPEND     "Referral made on ",SRCHSTA1
         APPEND     CDAY,SRCHSTA1
         APPEND     SP1,SRCHSTA1
         APPEND     MTHNAM3,SRCHSTA1
         APPEND     SP1,SRCHSTA1
         APPEND     CCENT,SRCHSTA1
         APPEND     CYEAR,SRCHSTA1
         APPEND     " for ",SRCHSTA1
         APPEND     OTRLCONS,SRCHSTA1
         RESET      SRCHSTA1
         GOTO       STATZ999
.
.        Inpatient pre-admission booking
.
STATZ300 MOVE       GSRBILL,AADMNO
         MOVE       AADMNO,KEY8
         CALL       RDMISS1
         BRANCH     OVRCD,STATZ310      * No details, check for a booking
         COMPARE    FIVE,ASTAT          * Check for cancelled pre-admission
         GOTO       STATZ999 IF EQUAL
.
         UNPACK     ADATE,CCENT,CYEAR,CMON,CDAY
         MOVE       CMON,F2
         LOAD       MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
.
         CLEAR      SRCHSTA1
         APPEND     "Pre-admission for ",SRCHSTA1
         APPEND     CDAY,SRCHSTA1
         APPEND     SP1,SRCHSTA1
         APPEND     MTHNAM3,SRCHSTA1
         APPEND     SP1,SRCHSTA1
         APPEND     CCENT,SRCHSTA1
         APPEND     CYEAR,SRCHSTA1
         RESET      SRCHSTA1
         GOTO       STATZ999
.
.        Inpatient booking (from BOKRC1FD)
.
STATZ310 PACK       KEY16,GSRURNO,GSRBILL
         CALL       RDBKREC6
         BRANCH     OVRCD,STATZ999      * No booking. Ignore record
.
         COMPARE    TWO,BKRESTAT        * Check for cancelled booking
         GOTO       STATZ999 IF EQUAL
.
         UNPACK     BKREEDAT,CCENT,CYEAR,CMON,CDAY
         MOVE       CMON,F2
         LOAD       MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
.
         CLEAR      SRCHSTA1
         MATCH      SP70,BKRETYPE
         GOTO       STATZ320 IF EQUAL
         PACK       KEY5,CODEBK,BKRETYPE
         CALL       RDCODE1
         IF         OVRCD=0
           CLEAR      DISPNAME
           MOVE       TDESC,NAME35
           CALL       NAMSTR00
           RESET      DISPNAME
           STRIP      DISPNAME
           APPEND     DISPNAME,SRCHSTA1
           APPEND     SP1,SRCHSTA1
         ENDIF
.
STATZ320 APPEND     "Booking for ",SRCHSTA1
         APPEND     CDAY,SRCHSTA1
         APPEND     SP1,SRCHSTA1
         APPEND     MTHNAM3,SRCHSTA1
         APPEND     SP1,SRCHSTA1
         APPEND     CCENT,SRCHSTA1
         APPEND     CYEAR,SRCHSTA1
         RESET      SRCHSTA1
.
STATZ999 RETURN
+
.------------------------------------------------------------
. GDET0000 - Get patient details
.------------------------------------------------------------
GDET0000  MOVE      SP70,SRCHCL01
          MOVE      SP70,SRCHCL02
          MOVE      SP70,SRCHCL03
          MOVE      SP70,SRCHCL04
          MOVE      SP70,SRCHCL05
          MOVE      SP70,SRCHCL06
          MOVE      SP70,SRCHCL07
          MOVE      SP70,SRCHCL08
          MOVE      SP70,SRCHCL09
          MOVE      SP70,SRCHCL10
          MOVE      SP70,SRCHCL11
          MOVE      SP70,SRCHCL12
          MOVE      SP70,SRCHCL13
          MOVE      SP70,SRCHCL14
          MOVE      SP70,SRCHCL15
          MOVE      SP70,SRCHCL16
          MOVE      SP70,SRCHCL17
          MOVE      SP70,SRCHCL18
          MOVE      SP70,SRCHCL20
          MOVE      SP70,SRCHCL21
          MOVE      SP70,SRCHCL22
.
          CALL      SRCHZ000                     * Recalculate patients age
          CALL      CALCAGE
          IF        IBCNMHOS=1
            PACK      SRCHCL20,CREATBY,PMPXIHOS,SP70
          ENDIF
.
          CALL      CHKPRV00                     * Check if privacy is set
          CALL      PATLN000         * Patient name format for System Parameter
          MOVE      FORMATNM,SRCHCL01
.
          COMPARE   "1",IBCNMHOS
          IF        @EQUAL
            PACK      KEY8,PVIBILL,SP70
            CALL      RDPMVX11
            IF        OVRCD=0
              PACK      KEY3,PMVXMHOS,SP70 
              CALL      RDPTHSP1
              IF        OVRCD=0
                MOVE      PTHSNAME,SRCHCL22
              ENDIF
            ENDIF
          ENDIF
.
          MOVE      PVIBILL,ADMISSNO
          BRANCH    PVITYPE,GDET1100,GDET1200,GDET1300,GDET1600
          IF        PVITYPE=9
            MATCH     " 1",PVISYST               * Event ?
            GOTO      GDET1400 IF EQUAL
            MATCH     " 2",PVISYST               * Allied Health ?
            GOTO      GDET1500 IF EQUAL
          ENDIF
          GOTO      GDET9000
.
.         Emergency details
.
GDET1100  PACK      KEY8,PVIBILL
          CALL      RDEMVIS1
          BRANCH    OVRCD,GDET9000
.
          COMPARE   FOUR,EMVISTAT
          GOTO      GDET9000 IF EQUAL
.
          UNPACK    SP70,PMVXRPOA,PMVXRCN1,PMVXRCN2,PMVXRHFU,PMVXRDVA
          MOVE      SP30,PMVXPIND
          PACK      KEY8,PVIBILL                 * Read Visit Extn for Privacy
          CALL      RDPMVX11
.
          CALL      CHKREL00                     * check 'release to' fields
.
          
          READ      CONTROLF,HUND12;*104,PTCNWBDS     * CAR 151896
          MOVE      PTCNWBDS,F1  * ward bed name option none,long,short
          CLEAR     SRCHCL02
.
          IF        IBCNMHOS=1
            IF        F1 > 0
              PACK      KEY3,PMVXMHOS
              CALL      RDPTHSP1
              IF        OVRCD = 0
                IF       F1 = 1
                  APPEND  PTHSNAME,SRCHCL02
                ELSE
                  APPEND  PTHSSNAM,SRCHCL02
                ENDIF
              ELSE
                APPEND   PMVXMHOS,SRCHCL02
              ENDIF
            ELSE
              APPEND   PMVXMHOS,SRCHCL02
            ENDIF
            MATCH     SP70,EMVILOCN
            IF        !@EQUAL
              APPEND    " - ",SRCHCL02
            ENDIF 
          ENDIF
          APPEND      EMVILOCN,SRCHCL02
          APPEND      SP70,SRCHCL02
          RESET       SRCHCL02
.
          MOVE      EMVIUNIT,CODE
          MOVE      "AC",CATEGORY
          MATCH     SP3,CODE
          IF        !@EQUAL
            PACK      KEY5,CATEGORY,CODE
            CALL      RDCODE1
            IF        OVRCD=0
              MOVE      TDESC,SRCHCL09           * Unit
            ENDIF
          ENDIF
.
          UNPACK    EMVIDATE,CCENT,CYEAR,CMON,CDAY     * Triage Date
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      SRCHCL03,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          MOVE      EMVITIME,SRCHCL04    * Triage time
.
          MATCH     SP70,EMVITRDT
          IF        !@EQUAL
            UNPACK    EMVITRDT,CCENT,CYEAR,CMON,CDAY     * Triage Date
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      SRCHCL03,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          ENDIF
.
          MATCH     SP70,EMVITRTM
          IF        !@EQUAL
            MOVE      EMVITRTM,SRCHCL04    * Triage time
          ENDIF
.
          IF        EMVISTAT=2 | EMVISTAT = 3
            MATCH     SP8,EMVIDDAT
            IF        !@EQUAL
              UNPACK    EMVIDDAT,CCENT,CYEAR,CMON,CDAY   * Discharged date
              MOVE      CMON,F2
              LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV:
                                   DEC
              PACK      SRCHCL05,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
              MOVE      EMVIDTIM,SRCHCL06    * Discharge time
            ENDIF
          ENDIF
.
          MOVE      PVIURNO,SRCHCL08     * U/R Number
.
          IF        PCEASE=1
            MOVE      "Deceased",SRCHCL07
          ELSE
            MATCH     SP5,EMVIDEST
            IF        !@EQUAL
              MOVE      EMVIDEST,KEY5
              CALL      RDPTVAD1
              IF        OVRCD=0
                MOVE      PTVADESC,SRCHCL07     * Destination
              ENDIF
            ENDIF
          ENDIF
.
          MOVE      ONE,EXIT
          GOTO      GDET9999
.
.         Outpatient details
.
GDET1200  MOVE      PVISITE,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD,GDET9000
          MOVE      PVIURNO,SRCHCL07       * U/R Number
.
          PACK      CFNAME,OSTFILE,FILCLIA1      * Clinic file
          OPEN      OUTCLIA1,CFNAME
.
          PACK      KEY20,PVISITE,PVIDOCT,PVIDATE,SP70
          CALL      RDCLIA1
          IF        OVRCD=1
            CALL      RDPCLIA1
            BRANCH    OVRCD,GDET1220
.
            MATCH     PVISITE,OCASITE
            GOTO      GDET1220 IF NOT EQUAL       * different site  
.
            MATCH     PVIDOCT,OCACLIN
            GOTO      GDET1220 IF NOT EQUAL       * different doctor
          ENDIF
.
          MOVE      OCADESC,SRCHCL04       * Clinic description
          MATCH     SP6,OCADOCT
          IF        !@EQUAL
            MOVE      OCADOCT,KEY6
            CALL      RDDOCT1
            IF        OVRCD=1
              MOVE      SP70,SRCHCL04
              CLEAR     SRCHCL04
              APPEND    "Invalid Doctor - ",SRCHCL04
              APPEND    OCADOCT,SRCHCL04
              APPEND    SP70,SRCHCL04
              RESET     SRCHCL04
            ELSE
              MOVE      DTITL,FMTTITLE
              MOVE      DGNAME,FMTGNAME
              MOVE      DSNAME,FMTSNAME
              CALL      DOCNM000
              MOVE      DOCFNAME,SRCHCL04    * doctor name
            ENDIF
          ENDIF
.
.         Read outpatient booking table
.
GDET1220  PACK      CFNAME,OSTFILE,FILBOKA6
          OPEN      OUTBOKA6,CFNAME
          PACK      CFNAME,OSTFILE,FILBOKA3
          OPEN      OUTBOKA3,CFNAME
.
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          PACK      KEY36,PVIBILL,SP70
          CALL      RDSBOKA6
GDET1230  CALL      RDKBOKA6
          BRANCH    OVRCD,GDET1240
.
          MATCH     PVIBILL,OBAOUTNO
          GOTO      GDET1240 IF NOT EQUAL
.
          PACK      CFNAME,OSTFILE,FILBB1A1
          OPEN      OUTBB1A1,CFNAME
.
          PACK      KEY8,PVIBILL,SP70
          CALL      RDBOKB1
          UNPACK    OBADATE,CCENT,CYEAR,CMON,CDAY   * Appointment date
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      SRCHCL05,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          MOVE      OBATIME,SRCHCL06      * Appointment time
.
          MATCH     SP70,OTBBCITM
          IF        @EQUAL
            MOVE      "Booked",SRCHCL09
          ELSE
            MOVE      OTBBCITM,SRCHCL06      * Check in time
            MOVE      NBSP,SRCHCL09
          ENDIF
.
          PACK      CFNAME,OSTFILE,FILCTYA1
          OPEN      OUTCTYA1,CFNAME
          PACK      KEY12,PVISITE,OBACTYP,SP70
          CALL      RDCTYA1
          IF        OVRCD=0
            MOVE      OCTDESC,SRCHCL03
          ELSE
            MOVE      OBACTYP,SRCHCL03             * Display clinic type in unit
          ENDIF
.
          MATCH     SP70,OTBBDPTM
          IF        !@EQUAL
            UNPACK    OBADATE,CCENT,CYEAR,CMON,CDAY   * Appointment date
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      SRCHCL09,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
            MOVE      OTBBDPTM,SRCHCL10      * Departure time
          ENDIF
.
GDET1240  PACK      CFNAME,OSTFILE,FILBB1A1
          CALL      OPOTBB11
          PACK      KEY8,OBAOUTNO,SP70
          CALL      RDBOKB1
          IF        OVRCD=0
            MOVE      "AC",CATEGORY
            MOVE      OTBBDEPT,CODE
            MATCH     SP3,CODE
            IF        !@EQUAL
              PACK      KEY5,CATEGORY,CODE
              CALL      RDCODE1
              IF        OVRCD=0
.               MOVE      TDESC,SRCHCL03        * Unit
                MOVE      OBACTYP,SRCHCL03      * Display clinic type in unit
              ENDIF                            
            ENDIF
          ENDIF
.
          PACK      CFNAME,OSTFILE,FILMA1A1    * Clinic masters
          CALL      OPOTMA11
          PACK      KEY18,OBASITE,OBACLIN,OBADAY,OBASTRT
          CALL      RDMASA1
          IF        OVRCD=0
.           MOVE      "CT",CATEGORY
.           MOVE      OTMALTYP,CODE
.           MATCH     SP3,CODE
.           IF        !@EQUAL
.             PACK      KEY5,CATEGORY,CODE
.             CALL      RDCODE1
.             IF        OVRCD=0
.               MOVE      TDESC,SRCHCL02       * Location description
.             ENDIF
.           ENDIF
          ENDIF
.
          PACK      CFNAME,CFILEPRE,FILSESA1
          OPEN      OUTSESA1,CFNAME
.
          PACK      CFNAME,CFILEPRE,FILHEDA1
          OPEN      OUTHEDA1,CFNAME
.
          PACK      KEY25,OBASITE,OBACLIN,OBADATE,OBASTRT,SP70
          CALL      RDSESA1
          IF        OVRCD=0
            PACK      KEY28,OSESITE,OSECLIN,OSEDAY,OSESTRT,OSESSHNO,OSESSDAT:
                            SP70
            CALL      RDOTHED1
            IF        OVRCD=0
              MOVE      "CT",CATEGORY
              MOVE      OTHELTYP,CODE
              MATCH     SP3,CODE
              IF        !@EQUAL
                PACK      KEY5,CATEGORY,CODE
                CALL      RDCODE1
                IF        OVRCD=0
                  MOVE      TDESC,SRCHCL02       * Location description
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
          UNPACK    SP70,PMVXRPOA,PMVXRCN1,PMVXRCN2,PMVXRHFU,PMVXRDVA
          MOVE      SP30,PMVXPIND
          PACK      KEY8,PVIBILL                 * Read Visit Extn for Privacy
          CALL      RDPMVX11
.
          CALL      CHKREL00                     * check 'release to' fields
.
          MOVE      ONE,EXIT
          GOTO      GDET9999
.
.         Inpatient details
.
GDET1300  MOVE      PVIURNO,SRCHCL10             * U/R Number
          PACK      KEY8,PVIBILL
          CALL      RDMISS1
          BRANCH    OVRCD,GDET9000
.
          UNPACK    SP70,PMVXVALW,PMVXPALW
          MOVE      SP30,PMVXPIND
          UNPACK    SP70,PMVXRPOA,PMVXRCN1,PMVXRCN2,PMVXRHFU,PMVXRDVA
          PACK      KEY8,PVIBILL                 * Read Visit Extn for Privacy
          CALL      RDPMVX11
.
          PACK      KEY8,PVIBILL,SP70            * Read Working Diagnosis file
          CALL      RDPMWOR1
          IF        OVRCD=1
            CALL      CLPMWO00
          ENDIF
.
          MATCH    SP70,PMWOCEXT
          IF       !@EQUAL
            CLEAR    SRCHCL21
            APPEND   "<br>",SRCHCL21
            APPEND   ANSC,SRCHCL21
            APPEND   DASH,SRCHCL21
            APPEND   PMWOCEXT,SRCHCL21
            RESET    SRCHCL21                    * Cordless Extn Number
          ENDIF
.
          CALL      CHKREL00                     * check 'release to' fields
.
          IF        ASTAT=4
            MOVE      "On-Leave",SRCHCL02
            GOTO      GDET1320
          ENDIF
.
          MATCH     SP3,AWARD
          GOTO      GDET1310 IF NOT EQUAL
.
          IF        ASTAT=2|ASTAT=3
            PACK      KEY30,AADMNO,Z70
            CALL      RDSTRAN2
            CALL      RDPTRAN2
            BRANCH    OVRCD,GDET1305
            MATCH     TADMN,AADMNO
            GOTO      GDET1305 IF NOT EQUAL
            MOVE      TWARD,AWARD
            MOVE      TBED,ABED
            GOTO      GDET1310
          ENDIF
.
GDET1305  MATCH     SP3,PTMIXWRD
          IF        !@EQUAL
            MOVE      PTMIXWRD,AWARD 
            PACK      ABED,SP70
            GOTO      GDET1310 
          ELSE
            MATCH     SP3,PMVXAPWD
            IF        !@EQUAL
              MOVE      PMVXAPWD,ABED 
              PACK      ABED,SP70
              GOTO      GDET1310 
            ENDIF
          ENDIF
          MOVE      NBSP,SRCHCL02
          GOTO      GDET1320
.
GDET1310  MATCH     SP70,AWARD
          IF        !@EQUAL
            READ      CONTROLF,HUND12;*104,PTCNWBDS     * CAR 151896
            MOVE      PTCNWBDS,F1  * ward bed name option none,long,short
            CLEAR     SRCHCL02
            PACK      KEY6,AWARD,SP70
            CALL      RDWARD1
            IF        F1 > 0
              IF        F1 = 2
                APPEND    PTWDSDES,SRCHCL02
              ELSE
                APPEND    WBDESC,SRCHCL02
              ENDIF
            ELSE
              APPEND    AWARD,SRCHCL02
            ENDIF
          ENDIF

          MATCH     SP3,ABED
          IF        !@EQUAL
            APPEND    "/",SRCHCL02
            IF        F1 > 0
              PACK      KEY6,AWARD,ABED
              CALL      RDWARD1
              IF        OVRCD<>1
                IF        F1 = 2
                  APPEND    PTWDSDES,SRCHCL02    * use short description
                ELSE
                  APPEND    WBDESC,SRCHCL02      * use long description
                ENDIF
              ENDIF
            ELSE
              APPEND    ABED,SRCHCL02          * use code
            ENDIF
          ENDIF
          APPEND    SP70,SRCHCL02            * Ward/Bed
          RESET     SRCHCL02
.
GDET1320  MOVE      ACLSSFT,CODE       
          CALL      GTRN0000                     * Get dept from trans file
          MOVE      "AC",CATEGORY
          MATCH     SP3,CODE
          IF        !@EQUAL
            PACK      KEY5,CATEGORY,CODE
            CALL      RDCODE1
            IF        OVRCD=0
              MOVE      TDESC,SRCHCL03           * Unit
            ENDIF
          ENDIF
.
          UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY  * Admission date
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      SRCHCL04,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          MOVE      ATIME,SRCHCL05               * Admission time
.
          IF        ASTAT<>3
            GOTO      GDET1350
          ENDIF
.
          MOVE      AADMNO,KEY8
          CALL      RDDSCH1
          BRANCH    OVRCD,GDET1350
.                                                * Discharged date
          UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN: 
                               JUL,AUG,SEP,OCT,NOV,DEC
          PACK      SRCHCL06,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          MOVE      DTIME,SRCHCL07           * Discharged time
.
          COMPARE   "1",PTCNDRSM
          IF        !@EQUAL
            MOVE      "DD",CATEGORY
            MOVE      DDEST,CODE
            MATCH     SP3,CODE
            GOTO      GDET1350 IF EQUAL
.
            PACK      KEY5,CATEGORY,CODE
            CALL      RDCODE1
            BRANCH    OVRCD,GDET1350
.
            MATCH     ANSD,TCINDC1
            IF        @EQUAL
              PACK     SRCHCL09,HTMLSBLD,TDESC,HTMLEBLD
              GOTO     GDET1350
            ENDIF
          ELSE
            PACK      KEY8,ADMISSNO,SP70
            CALL      RDDSCH1
            BRANCH        OVRCD,GDET1350
.
            MOVE      "D ",CATEGORY
            MOVE      DSTAT,CODE
            PACK      KEY5,CATEGORY,CODE,SP70
            CALL      RDCODE1
            BRANCH    OVRCD,GDET1350
.
            MOVE      SP70,SRCHCL09
            RESET     SRCHCL09
            APPEND    TDESC,SRCHCL09     * Discharged Stat
            RESET     SRCHCL09
            GOTO      GDET1350
          ENDIF
.         
          MOVE      TDESC,SRCHCL09       * Discharged Destination
.
          MATCH     "1",TCINDC2          * transferred to other hospital
          GOTO      GDET1350 IF NOT EQUAL
.
          MOVE      PVIBILL,KEY8
          CALL      RDPTDAD1
          BRANCH    OVRCD,GDET1350
          MATCH     SP70,PTDADCTS
          GOTO      GDET1350 IF EQUAL
.
          MOVE      PTDADCTS,KEY5            * transfer destination
          CALL      RDPTVAD1
          IF        OVRCD=0
            MOVE      "T - ",SRCHCL09
            ENDSET    SRCHCL09
            APPEND    PTVADESC,SRCHCL09      * Destination desc.
            RESET     SRCHCL09
          ENDIF
.
GDET1350  MOVE      "Phone Calls: No",SRCHCL14   * Set up defaults for both
          MOVE      "Visitors: No",SRCHCL15      * kids and standard formats
          MOVE      ANSN,SRCHCL11             
          MOVE      ANSR,SRCHCL12             
.
          PACK      KEY6,AWARD,ABED,SP70
          CALL      RDWARD1                     * Read Ward File
          IF        OVRCD=0
            PACK      KEY6,PTWDNSTA,SP70
            CALL      RDNURS1
            IF        OVRCD=0
              PACK      SRCHCL08,ANSN,DASH,NSEXTN  * Nursing Extension number
            ENDIF
          ENDIF
.
          MATCH     "1",PMVXPALW                 * Patient Phone calls allowed?
          IF        @EQUAL
            MOVE      "Phone Calls: Yes",SRCHCL14
            MOVE      ANSP,SRCHCL11
            PACK      KEY6,AWARD,ABED,SP70       * Read Ward File
            CALL      RDWARD1
            IF        OVRCD=0
              IF        ASTAT=2
                PACK      SRCHCL18,HTMLSBLD,ANSP,DASH,WEXTN,HTMLEBLD
              ELSE
                IF        ASTAT<>3
                  PACK      SRCHCL18,ANSP,DASH,WEXTN   * Patient Extension No.
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
          MATCH     "1",PMVXVALW                 * test visitors allowed
          IF        @EQUAL
            MOVE      "Visitors: Yes",SRCHCL15
            MOVE      ANSV,SRCHCL12
          ENDIF
.
          IF        KIDSONLY=ONE
            MOVE      SRCHCL14,SRCHCL11          * for RCH, use long format
            MOVE      SRCHCL15,SRCHCL12             
            MOVE      SP70,SRCHCL14
            MOVE      SP70,SRCHCL15
          ENDIF
.
          CALL      GETCON00                     * Obtain location rec
          MOVE      CONDESC,SRCHCL14             * Condition desc (code) (CO)
          MOVE      LPCONDD,SRCHCL15             * Condition desc-2 (comments) 
.
          MATCH     SP5,LPCONAM     
          IF        !@EQUAL
            PACK      KEY5,CODEAM,LPCONAM
            CALL      RDCODE1
            IF        OVRCD=0
              MOVE      TDESC,SRCHCL16           * Ambultory status      (AM)
            ENDIF 
          ENDIF
.
          MATCH     SP20,LPCDATE                 * Condition timestamp
          IF        !@EQUAL          
            UNPACK    LPCDATE,CCENT,CYEAR,CMON,CDAY      
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      SRCHCL17,FONTCOL,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,SP1:
                               LPCTIME
            MOVE     "<font color=black>",FONTCOL
            RESET     SRCHCL17
            ENDSET    SRCHCL17
            APPEND    FONTCOL,SRCHCL17
          ENDIF
.
          PROC      GTHE0000                     * Get theatre status
          MOVE      ONE,EXIT
          GOTO      GDET9999
.
.         Event
.
GDET1400  MOVE      PVIURNO,SRCHCL08       * U/R Number
          PACK      KEY8,PVIBILL,SP70
          CALL      RDPMEVT1
          BRANCH    OVRCD,GDET9000
.
          MOVE      ANSN,SRCHCL16
          MOVE      ANSR,SRCHCL17
          UNPACK    SP70,PMVXVALW,PMVXPALW,
          UNPACK    SP70,PMVXRPOA,PMVXRCN1,PMVXRCN2,PMVXRHFU,PMVXRDVA
          MOVE      SP30,PMVXPIND
          PACK      KEY8,PVIBILL                 * Read Visit Extn for Privacy
          CALL      RDPMVX11
.
          CALL      CHKREL00                     * check 'release to' fields
.
          MATCH     "1",PMVXPALW                 * Patient Phone calls allowed?
          IF        @EQUAL
            MOVE      ANSP,SRCHCL16
            PACK      KEY6,PMEVPWRD,PMEVPBED,SP70       * Read Ward File
            CALL      RDWARD1
            IF        OVRCD=0
              PACK      SRCHCL12,ANSP,DASH,WEXTN * Patient Extension No.
            ENDIF
          ENDIF
.
          MATCH     "1",PMVXVALW                 * test visitors allowed
          IF        @EQUAL
            MOVE      ANSV,SRCHCL17
          ENDIF
.
          MATCH     SP3,PMEVPWRD
          IF        !@EQUAL
            PACK      KEY6,PMEVPWRD,PMEVPBED
            CALL      FWBD0000   * format ward/bed in SRCHCL02
          ENDIF
.
          MOVE      "LO",CATEGORY
          MOVE      PMEVPLOC,CODE
          MATCH     SP3,CODE
          IF        !@EQUAL
            PACK      KEY5,CATEGORY,CODE
            CALL      RDCODE1
            IF        OVRCD=0
              MOVE      TDESC,SRCHCL03        * Location
            ENDIF
          ENDIF
.
          MOVE      "AC",CATEGORY
          MOVE      PMEVUNIT,CODE
          MATCH     SP3,CODE
          IF        !@EQUAL
            PACK      KEY5,CATEGORY,CODE
            CALL      RDCODE1
            IF        OVRCD=0
              MOVE      TDESC,SRCHCL04        * Unit
            ENDIF
          ENDIF
.
          MATCH     SP8,PMEVADTE
          IF        !@EQUAL
            UNPACK    PMEVADTE,CCENT,CYEAR,CMON,CDAY   * Admission date
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      SRCHCL06,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
            MOVE      PMEVATIM,SRCHCL07      * Admission time
          ENDIF
.
          MATCH     SP8,PMEVDDTE
          IF        !@EQUAL
            UNPACK    PMEVDDTE,CCENT,CYEAR,CMON,CDAY   * Discharge date
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      SRCHCL09,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
            MOVE      PMEVDTIM,SRCHCL10      * Discharge time
          ENDIF
.
          MOVE      "DD",CATEGORY
          MOVE      PMEVDSTT,CODE
          MATCH     SP3,CODE
          IF        !@EQUAL
            PACK      KEY5,CATEGORY,CODE
            CALL      RDCODE1
            IF        OVRCD=0
              MOVE      TDESC,SRCHCL11        * Location
            ENDIF
          ENDIF
.
          MOVE      PMEVACON,KEY10         * HCP
          OPEN      PMSHCPA1,"pmshcpaf"
          CALL      RDPMHCP1
          IF        OVRCD=0
            MOVE      PMHCTITL,FMTTITLE
            MOVE      PMHCGNAM,FMTGNAME
            MOVE      PMHCSNAM,FMTSNAME
            CALL      DOCNM000
            MOVE      DOCFNAME,SRCHCL05    * doctor name
          ENDIF
.
          CALL      GETCON00                     * Obtain location rec
          MOVE      CONDESC,SRCHCL13             * Condition desc (code) (CO)
          MOVE      LPCONDD,SRCHCL14             * Condition desc-2 (comments)
.
          MATCH     SP5,LPCONAM
          IF        !@EQUAL
            PACK      KEY5,CODEAM,LPCONAM
            CALL      RDCODE1
            IF        OVRCD=0
              MOVE      TDESC,SRCHCL18           * Ambultory status      (AM)
            ENDIF
          ENDIF
.
          UNPACK    LPCDATE,CCENT,CYEAR,CMON,CDAY * Condition date time stamp
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      SRCHCL15,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,SP1,LPCTIME
.
          MOVE      ONE,EXIT
          GOTO      GDET9999
.
.         Allied Health
.
GDET1500  MOVE      PVIURNO,SRCHCL07
          PACK      KEY8,PVIBILL,SP70
          CALL      RDALREF1
          BRANCH    OVRCD,GDET9000
.
          MOVE      "AC",CATEGORY
          MOVE      ALREUNIT,CODE
          MATCH     SP3,CODE
          IF        !@EQUAL
            PACK      KEY5,CATEGORY,CODE
            CALL      RDCODE1
            IF        OVRCD=0
              MOVE      TDESC,SRCHCL02        * Unit
            ENDIF
          ENDIF
.
          MOVE      ALREHCP,KEY10         * HCP
          OPEN      PMSHCPA1,"pmshcpaf"
          CALL      RDPMHCP1
          IF        OVRCD=0
            MOVE      PMHCTITL,FMTTITLE
            MOVE      PMHCGNAM,FMTGNAME
            MOVE      PMHCSNAM,FMTSNAME
            CALL      DOCNM000
            MOVE      DOCFNAME,SRCHCL03    * doctor name
          ENDIF
.
          MATCH     SP8,ALREDACT
          IF        !@EQUAL
            UNPACK    ALREDACT,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      SRCHCL04,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          ENDIF
.
          CLEAR     SRCHCL05
          MATCH     "0",ALRESTAT
          GOTO      GDET1510 IF NOT EQUAL
          MOVE      "On Waiting List",SRCHCL05
          GOTO      GDET1570
.
GDET1510  MATCH     "1",ALRESTAT
          GOTO      GDET1520 IF NOT EQUAL
          MOVE      "Active",SRCHCL05
          GOTO      GDET1570
.
GDET1520  MATCH     "2",ALRESTAT
          GOTO      GDET1530 IF NOT EQUAL
          MOVE      "Closed",SRCHCL05
          GOTO      GDET1570
.
GDET1530  MATCH     "3",ALRESTAT
          GOTO      GDET1540 IF NOT EQUAL
          MOVE      "Inactive",SRCHCL05
          GOTO      GDET1570
.
GDET1540  MATCH     "4",ALRESTAT
          GOTO      GDET1570 IF NOT EQUAL
          MOVE      "Cancelled",SRCHCL05
.
GDET1570  PACK      KEY16,PVIBILL,Z70          * read encounter table
          CALL      RSALENC1
          CALL      RPALENC1
          BRANCH    OVRCD,GDET9000
          MOVE      PVIBILL,KEY8
          MATCH     KEY8,ALENVISN           * Same Visit number ?
          GOTO      GDET9000 IF NOT EQUAL
.
          MATCH     SP8,ALENSDAT
          IF        !@EQUAL
            UNPACK    ALENSDAT,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      SRCHCL06,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          ENDIF
          MOVE      ZERO,EXIT
          UNPACK    SP70,PMVXVALW,PMVXPALW,
          UNPACK    SP70,PMVXRPOA,PMVXRCN1,PMVXRCN2,PMVXRHFU,PMVXRDVA
          MOVE      SP30,PMVXPIND
          PACK      KEY8,PVIBILL                 * Read Visit Extn for Privacy
          CALL      RDPMVX11
.
          CALL      CHKREL00                     * check 'release to' fields
.
          GOTO      GDET9999
.
.         Booking details
.
GDET1600  MOVE      PVIURNO,SRCHCL10             * U/R Number
          PACK      KEY8,PVIBILL
          CALL      RDBKREC1
          BRANCH    OVRCD,GDET9000
.
          PACK      KEY6,BKREWARD,BKREEBED
          CALL      FWBD0000   * format ward/bed in SRCHCL02
.
          MOVE      BKREDEPT,CODE
          MOVE      "AC",CATEGORY
          MATCH     SP3,CODE
          IF        !@EQUAL
            PACK      KEY5,CATEGORY,CODE
            CALL      RDCODE1
            IF        OVRCD=0
              MOVE      TDESC,SRCHCL03           * Unit
            ENDIF
          ENDIF
.
          UNPACK    BKREEDAT,CCENT,CYEAR,CMON,CDAY  * Admission date
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      SRCHCL04,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          MOVE      BKREATIM,SRCHCL05               * Admission time
.
          PACK      SRCHCL06,BOOKED,SP70
.
          MOVE      ONE,EXIT
          GOTO      GDET9999
.
GDET9000  MOVE      ONE,EXIT
GDET9999  RETURN
+
.------------------------------------------------------------
. GTRN0000 - Get Department from transfer file
.------------------------------------------------------------
GTRN0000  OPEN      PATTRAN2,"pattranf"
          PACK      KEY30,AADMNO,Z70
          CALL      RDSTRAN2
          CALL      RDPTRAN2
          BRANCH    OVRCD,GTRN9000
          MATCH     AADMNO,TADMN          * same admission ?
          GOTO      GTRN9000 IF NOT EQUAL
          MOVE      TDEPT,CODE
GTRN9000  
GTRN9999  RETURN
+
.*******************************************************************
.*  DBRTH000  :  Date of Birth search                              *
.*               Returns: SRCHFLAG  0=Exit selected                *
.*                                  1=Item selected or Register    *
.*******************************************************************
DBRTH000  CALL      RDPAR000                     * read system parametres
          CALL      SPARA000                     * keyin Search Parameters
          CALL      SHEAD000                     * display header
. --- now loop thru file & display all valid records ---
.
          PACK      KEY98,SRCHPDOB,SP70,SP70
          CALL      RSPTGSR6                      * position read
DBRTH100  CALL      RKPTGSR6
          BRANCH    OVRCD,DBRTH900
.
          ADD       ONE,NUMLOOP
          IF        NUMLOOP>NUMTLOOP
            MOVE      ONE,OVRCD
            GOTO      DBRTH900
          ENDIF
          MATCH     GSRDOB,SRCHPDOB
          GOTO      DBRTH900 IF NOT EQUAL         * no more records
.
          CALL      DISPR000                      * display a record
          IF        DISNUMB>=NORECORD
            GOTO      DBRTH910
          ENDIF
          GOTO      DBRTH100                      * get next record
.
DBRTH900  BRANCH    FHIRTYPE,DBRTH999
          WRITE     HTMLFILE;"</table>"
          WRITE     HTMLFILE;"<p align=center><b>End of Search</b>"
          WRITE     HTMLFILE;"</div>" 
          GOTO      DBRTH999                      * get next record (normal)
.
DBRTH910  BRANCH    FHIRTYPE,DBRTH999
          WRITE     HTMLFILE;"</table>"
          WRITE     HTMLFILE;"</div>"
.
DBRTH999  RETURN
+
.*********************************************************************
.*  SRCHCLR  :  Subroutine to clear the search parameters            *
.*********************************************************************
SRCHCLR   MOVE      SP70,SRCHSNAM
          MOVE      SP70,SRCHGIV
          MOVE      SP1,SRCHSEX
          MOVE      SP10,SRCHFDAT
          MOVE      SP10,SRCHTDAT
          MOVE      SP10,SRCHFSYR
          MOVE      SP10,SRCHTSYR
          MOVE      SP10,SRCHFEYR
          MOVE      SP10,SRCHTEYR
          MOVE      SP10,SRCHPDOB
          MOVE      SP3,SRCHAGE
SRCLR999  RETURN
+
.*******************************************************************
.*  MEDIC000  :  Medicare search                                   *
.*               Returns: SRCHFLAG  0=Exit selected                *
.*                                  1=Item selected or Register    *
.*******************************************************************
MEDIC000  CALL      RDPAR000                     * read system parametres
          CALL      SPARA000                     * keyin Search Parameters
          CALL      SHEAD000                     * display header
. --- now loop thru file & display all valid records ---
.
          OPEN      PATMA1A3,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          PACK      SRCHMEDI,SRCHMEDI,SP70
          MATCH     SP70,SRCHMEDI
          GOTO      MEDIC900 IF EQUAL         * no more records
.
          STRIP     SRCHMEDI        
          PACK      KEY18,SRCHMEDI,SP70           * Full Key
          CALL      RDSMAST3                      * Get First Record
MEDIC100  CALL      RDKMAST3
          BRANCH    OVRCD,MEDIC900                * no more records
          MATCH     SRCHMEDI,PMEDI
          GOTO      MEDIC900 IF NOT EQUAL         * no more records
.
          PACK      KEY115,PURNO,SP70,SP70
          CALL      RSPTGSR1
          CALL      RKPTGSR1
          BRANCH    OVRCD,MEDIC100
          MATCH     PURNO,GSRURNO
          GOTO      MEDIC100 IF NOT EQUAL
.
MEDIC300  CALL      DISPR000                      * display a record
          IF        DISNUMB>=NORECORD
            GOTO      MEDIC910
          ENDIF
          GOTO      MEDIC100                      * get next record
.
MEDIC900  BRANCH    FHIRTYPE,MEDIC999
          WRITE     HTMLFILE;"</table>"
          WRITE     HTMLFILE;"<p align=center><b>End of Search</b>"
          WRITE     HTMLFILE;"</div>" 
          GOTO      MEDIC999                      * get next record (normal)
.
MEDIC910  BRANCH    FHIRTYPE,MEDIC999
          WRITE     HTMLFILE;"</table>"
          WRITE     HTMLFILE;"</div>"
.
MEDIC999  RETURN
.
+
.********************************************************************
.*  GCUR0000  :  Get current patient and check if a patient is current               *
.********************************************************************
GCUR0000  PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          MOVE      ZERO,EXIT
          MOVE      SP70,ADMISSNO
          MOVE      PURNO,URNUMBER
.
          PACK      KEY24,PURNO,Z70
          CALL      RDSVISA2
GCUR0500  CALL      RDPVISA2
          BRANCH    OVRCD,GCUR9000
          MATCH     PURNO,PVIURNO
          GOTO      GCUR9000 IF NOT EQUAL
.
          MOVE      ZERO,FORM1
          MOVE      SRCHSYST,FORM1
.
          MOVE      PVIBILL,ADMISSNO
          BRANCH    PVITYPE,GCUR1000,GCUR2000,GCUR3000
          IF        PVITYPE=9
            MATCH     "0",SRCHSYST
            GOTO      GCUR0500 IF EQUAL
            MATCH     " 1",PVISYST               * Event ?
            GOTO      GCUR4000 IF EQUAL
            MATCH     " 2",PVISYST               * Allied Health ?
            GOTO      GCUR5000 IF EQUAL
          ENDIF
          GOTO      GCUR9000
.
.         Emergency
.
GCUR1000  COMPARE   TWO,FORM1
          GOTO      GCUR9000 IF NOT EQUAL
.
          PACK      KEY8,PVIBILL
          CALL      RDEMVIS1
          BRANCH    OVRCD,GCUR9000
.
          COMPARE   ONE,EMVISTAT
          GOTO      GCUR9000 IF NOT EQUAL
.
          GOTO      GCUR8000
.
.         Outpatient
.
GCUR2000  COMPARE   ONE,FORM1
          GOTO      GCUR9000 IF NOT EQUAL
.
          MATCH     PVIDATE,CURRDATE      * todays outpatient ?
          GOTO      GCUR9000 IF NOT EQUAL
.
          GOTO      GCUR8000
.
.         Inpatients
.
GCUR3000  COMPARE   ZERO,FORM1
          GOTO      GCUR9000 IF NOT EQUAL
.
          MOVE      PVIBILL,ADMISSNO
          MOVE      PVIBILL,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD OF GCUR9000
.
.         Current admission or discharges within X days
.
          BRANCH    ASTAT,GCUR9000,GCUR8000,GCUR3200,GCUR8000,GCUR9000
.
GCUR3200  MOVE      PVIBILL,KEY8
          CALL      RDDSCH1
          BRANCH    OVRCD OF GCUR9000
.
          DAYSEP    DDATE,CURRDATE,DAYDIFF
          COMPARE   DAYDIFF,SRCHSDAY
          GOTO      GCUR9000 IF LESS
.
          COMPARE   ONE,PCEASE               * Is the patient dead ?
          GOTO      GCUR3300 IF EQUAL        * Yes
.
          GOTO      GCUR8000
.
GCUR3300  DAYSEP     PDECDTE,CURRDATE,DAYDIFF
          COMPARE    DAYDIFF,SRCHSDAY
          GOTO       GCUR9000 IF LESS
          GOTO       GCUR8000
.
.         Event
.
GCUR4000  COMPARE   THREE,FORM1
          GOTO      GCUR9000 IF NOT EQUAL
          GOTO      GCUR8000
.
.         Allied health
.
GCUR5000  COMPARE   FOUR,FORM1
          GOTO      GCUR9000 IF NOT EQUAL
.
GCUR8000  MOVE      ZERO,EXIT
          GOTO      GCUR9999
.
GCUR9000  MOVE      ONE,EXIT
GCUR9999  RETURN
+
.********************************************************************
.*  VLCP0000  :  Check to see if a patient is current               *
.*               Returns: EXIT   0=Yes they are a current patient   *
.*                               1=No they are not a current patient*
.********************************************************************
VLCP0000  PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
          MOVE      ZERO,FORM1
          MOVE      SRCHSYST,FORM1
.
          COMPARE   ONE,PCEASE               * Is the patient dead ?
          GOTO      VLCP3300 IF EQUAL        * Yes
.
          MATCH     PURNO,PVIURNO            * Patient is not deceased and 
          GOTO      VLCP9000 IF NOT EQUAL    * they have no visits so reject
.
          BRANCH    PVITYPE,VLCP1000,VLCP2000,VLCP3000 
          COMPARE   "9",PVITYPE
          GOTO      VLCP9000 IF NOT EQUAL
          MATCH     " 1",PVISYST
          GOTO      VLCP8000 IF EQUAL        * Event
          MATCH     " 2",PVISYST
          GOTO      VLCP8000 IF EQUAL        * Allied Health
          GOTO      VLCP9000 
.
. Visit type 1 A & E patients ( EMVISTAT = 1 , Current Patient )
.
VLCP1000  PACK      KEY8,PVIBILL
          CALL      RDEMVIS1
          BRANCH    OVRCD,VLCP9000
.
          COMPARE   ONE,EMVISTAT
          GOTO      VLCP9000 IF NOT EQUAL
.
          GOTO      VLCP8000
.
. Visit type 2 Outpatients ( Todays outpatients )
.
VLCP2000  MATCH     PVIDATE,CURRDATE
          GOTO      VLCP9000 IF NOT EQUAL
.
          GOTO      VLCP8000
.
. Visit type 3 Inpatients ( Current admissions and discharges within X days ) 
.
VLCP3000  BRANCH     ASTAT,VLCP9000,VLCP8000,VLCP3200,VLCP8000,VLCP9000
          GOTO       VLCP9000
.
VLCP3200  DAYSEP     DDATE,CURRDATE,DAYDIFF
          COMPARE    DAYDIFF,SRCHSDAY
          GOTO       VLCP9000 IF LESS
          
          GOTO       VLCP8000
.
VLCP3300  DAYSEP     PDECDTE,CURRDATE,DAYDIFF
          COMPARE    DAYDIFF,SRCHSDAY
          GOTO       VLCP9000 IF LESS
          
          GOTO       VLCP8000
.
VLCP8000  MOVE       ZERO,EXIT                   * Accept record
          GOTO       VLCP9999
.
VLCP9000  MOVE       ONE,EXIT                    * Reject Record
.
VLCP9999  RETURN
.------------------------------------------------------------
. Get Patient Condition Details
.------------------------------------------------------------
GETCON00  MOVE     "<font color=black>",FONTCOL
....      PACK      KEY88,PTMASNAM,PMPXFNAM,PVIBILL                   *D-257255
          PACK      KEY40,PTMASNAM,SP70                               *C-257255
          PACK      KEY88,KEY40,PMPXFNAM,PVIBILL
          CALL      RDLOCA1
          BRANCH    OVRCD,GETCON10
.
          MATCH     SP70,LPCONDC
          GOTO      GETCON15 IF EQUAL
.
          PACK      KEY5,CODECO,LPCONDC
          CALL      RDCODE1
          BRANCH    OVRCD,GETCON15
.
          MOVE      TDESC,CONDESC
.
          MATCH     SP70,TCINDC1
          IF        @EQUAL
            MOVE      ZERO,TCINDC1
          ENDIF
.
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
. 
          MOVE      "720",HOURSDIF
          MOVE      LPCDATE,FIRSTDAT
          UNPACK    LPCTIME,HOUR,D1,MIN
          PACK      FIRSTIME,HOUR,MIN
.
          MOVE      CURRDATE,LASTDATE
          UNPACK    CTIMEIS,HOUR,D1,MIN
          PACK      LASTTIME,HOUR,MIN
.
          CALL      TIMEDIFF
          COMPARE   CALCTIME,HOURSDIF
          IF        @LESS
            MOVE     "<font color=red>",FONTCOL
          ENDIF
          GOTO      GETCON20
.
GETCON10  MOVE      SP70,LPCONDD
          MOVE      SP70,LPCDATE
          MOVE      SP70,LPCTIME
          MOVE      SP70,LPCONAM
GETCON15  MOVE      SP70,CONDESC
          MOVE      ZERO,TCINDC1
.
GETCON20  CLEAR     IMGSRC
          APPEND    "ConditionIcon_",IMGSRC
          IF        PCEASE=1
            APPEND    "D",IMGSRC
            MOVE      "Deceased",CONDESC
          ELSE
            APPEND    TCINDC1,IMGSRC
          ENDIF
          APPEND    ".gif",IMGSRC
          RESET     IMGSRC
.
GETCON99  RETURN
.------------------------------------------------------------
. Format Patient Name (for Javascript)  <b>SURNAME</b>Title Given Names
.*** format the surname and given name by changing any ' to %60 
.------------------------------------------------------------
JAVNAM00  CLEAR     DISPNAME            
          MOVE      SP70,JAVFNAME                  
.
          MATCH     SP70,PTMASNAM
          IF        !@EQUAL
            MOVE      PTMASNAM,JAVSNAME          * surname
          ELSE
            MOVE      PSNAME,JAVSNAME          * surname
          ENDIF
          SCAN      "'",JAVSNAME             * find any apostrophies
          GOTO      JAVNAM10 IF NOT EQUAL
.
          MOVE      JAVSNAME,ENDNAME
          REP       "' ",ENDNAME             * remove the apostrophy
.
          LENSET    JAVSNAME
          RESET     JAVSNAME                 * get all characters before '
          MOVE      JAVSNAME,STRTNAME
.
          REP       "'%",STRTNAME
          STRIP     STRTNAME
          PACK      JAVSNAME,STRTNAME,SIXTY,ENDNAME,SP70 * formatted surname
.
JAVNAM10  MOVE      SP70,STRTNAME
          MOVE      SP70,ENDNAME
.
          MATCH     SP70,PMPXFNAM
          IF        !@EQUAL
            MOVE      PMPXFNAM,JAVGNAME     * given name
          ELSE
            MOVE      PGNAME,JAVGNAME     * given name
          ENDIF
          SCAN      "'",JAVGNAME        * find any apostrophies
          GOTO      JAVNAM20 IF NOT EQUAL
.
          MOVE      JAVGNAME,ENDNAME    * store all chars after apostrophy
          REP       "' ",ENDNAME        * remove the apostrophy
.
          LENSET    JAVGNAME
          RESET     JAVGNAME
          MOVE      JAVGNAME,STRTNAME   * store all chars before the '
.
          REP       "'%",STRTNAME
          STRIP     STRTNAME
          PACK      JAVGNAME,STRTNAME,SIXTY,ENDNAME,SP70  * formatted given name
.
JAVNAM20  REP       "#" ",JAVSNAME        * format the name for display
          REP       "#" ",JAVGNAME
          MOVE      SP70,JAVFNAME
          REP       UPPLOW,JAVSNAME
          PACK      NAME35,PTITL,SP70
          CALL      NAMSTR00
          PACK      NAME35,JAVGNAME,SP70
          CALL      NAMSTR00
          PACK      NAME35,JAVGNAME,SP70
          REP       UPPLOW,JAVSNAME
          APPEND    JAVSNAME,DISPNAME
          APPEND    SP70,DISPNAME
          RESET     DISPNAME
          MOVE      DISPNAME,JAVFNAME
          STRIP     JAVFNAME
.
JAVNAM99  RETURN
.
.------------------------------------------------------------
. CHKPRV00 - Check Patient Privacy 
.------------------------------------------------------------
CHKPRV00  MATCH      SP70,PMPXPRVI            Privacy code populated ?
          IF         !@EQUAL    
            MOVE       "PV",KEY2              Y - Set up the category
            PACK       KEY5,KEY2,PMPXPRVI       - Read to see if it's valid
            CALL       RDCODE1
            IF         OVRCD = 0
              MATCH      "P",TCINDC1            Y - Its there - Privacy set ?
              IF         @EQUAL
                MOVE      SP30,PTITL              Y - Set names to anonymous
                MOVE      SP30,PGNAME
                MOVE      SP70,GSRGNAM
                MOVE      SP70,PMPXFNAM
                MOVE      SP70,PMPXSNAM
                MOVE      SP70,PMPXPFGN
                MOVE      SP70,PMPXPFSN
                MOVE      "ANONYMOUS",PSNAME
                MOVE      "ANONYMOUS",PTMASNAM
                MOVE      "ANONYMOUS",GSRSNAM
              ENDIF
            ENDIF
          ENDIF
.
CHKPRV99  RETURN
.------------------------------------------------------------
. GTHE0000 - Get Theatre Status
.------------------------------------------------------------
          DEFPROC   GTHE0000
.
          INC       OPRCONFD/INC
          INC       OPRDEAFD/INC
          INC       OPRARDFD/INC
          INC       OPROPRFD/INC
          INC       OPRSRGFD/INC
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OPRSRGA1,"oprsrgaf"
          TRAPCLR   IO
          BRANCH    OVRCD,GTHE9999
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OPRDETA2,"oprdetaf"
          TRAPCLR   IO
          BRANCH    OVRCD,GTHE9999
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OPRARDA1,"oprardaf"
          TRAPCLR   IO
          BRANCH    OVRCD,GTHE9999
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OPROPRA1,"opropraf"
          TRAPCLR   IO
          BRANCH    OVRCD,GTHE9999
.
          PACK      KEY20,SP70
          MOVE      PVIBILL,OPDAADMN
          MOVE      PVIBILL,KEY8
          MOVE      FOUR,OPDASTAT         * Booking status
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          PACK      KEY31,OPDAADMN,OPDASTAT,CURRDATE,Z70
          CALL      RSOPDEA2
GTHE0300  CALL      RPOPDEA2
          BRANCH    OVRCD,GTHE2000
.
          MATCH     OPDAADMN,KEY8         * Same admission ?
          GOTO      GTHE2000 IF NOT EQUAL
.
          COMPARE   THREE,OPDASTAT        * Cancelled ?
          GOTO      GTHE0300 IF EQUAL     * Yes
.
          MATCH     CURRDATE,OPDADATE     * Today's date ?
          GOTO      GTHE2000 IF NOT EQUAL
.
          MOVE      OPDAUNIQ,KEY10
          CALL      RDOPARD1
          BRANCH    OVRCD,GTHE2000
.
... CAR 38345 - START
          MATCH     SP70,SRCHCL07           * Is Discharge Time populated
          GOTO      GTHE0500 IF NOT EQUAL   * Yes, don't use Estim Disch Time
          MATCH     SP70,OPARUT11           * Is Estimated Disch Time specified?
          GOTO      GTHE0500 IF EQUAL       * No, don't use Estim Disch Time
.
          RESET     SRCHCL07
          APPEND    "<font color=red>",SRCHCL07
          APPEND    OPARUT11,SRCHCL07
          APPEND    "</font>",SRCHCL07
... CAR 38345 - END
.
GTHE0500  PACK      KEY11,OPDAUNIQ,ONE,SP70
          CALL      RDOPSRG1
          IF        OVRCD=1
            CALL      CLOPRSRG
          ENDIF
.
          MATCH     SP8,OPARATIM          * Check arrival time
          GOTO      GTHE1000 IF EQUAL
          MATCH     "1",OPSGUY05
          IF        @EQUAL
            MOVE      "Abandoned",KEY20
          ELSE
            MOVE      "Holding",KEY20
          ENDIF
.         MOVE      "Holding",KEY20
.
          MATCH     SP8,OPARANAS          * Anaesthetic started ?
          GOTO      GTHE1000 IF EQUAL
          MATCH     "1",OPSGUY05
          IF        @EQUAL
            MOVE      "Abandoned",KEY20
          ELSE
            MOVE      "Anaes",KEY20
          ENDIF
.         MOVE      "Anaest.",KEY20
.
          PACK      KEY11,OPDAUNIQ,ONE
          CALL      RDOPSRG1
          BRANCH    OVRCD,GTHE1000
          MATCH     SP8,OPSGTISS          * Surgery started ?
.         GOTO      GTHE1000 IF EQUAL
          IF        !@EQUAL
            MATCH     "1",OPSGUY05
            IF        @EQUAL
              MOVE      "Abandoned",KEY20
            ELSE
              MOVE      "Theatre",KEY20
            ENDIF
          ENDIF
.         MOVE      "Theatre",KEY20
.
          MATCH     SP8,OPARTIRF          * time in recovery - front ?
          IF        !@EQUAL
            MOVE      "Recover F",KEY20
          ENDIF
.
          MATCH     SP8,OPARTIRB          * time in recovery - back ?
          IF        !@EQUAL
            MOVE      "Recover B",KEY20
          ENDIF
.
          MATCH     SP8,OPARTIRD          * time in recovery - Day ?
          IF        !@EQUAL
            MOVE      "Recover D",KEY20
          ENDIF
.
          MATCH     SP8,OPARTICU          * time went ot icu
          IF        !@EQUAL
            MOVE      "ICU",KEY20
          ENDIF
.
          MATCH     SP8,OPARTIDP          * ready to depart
          IF        !@EQUAL
            MOVE      "Ready to Depart",KEY20
          ENDIF
.
          MATCH     SP8,OPARTETC          * Exit
          IF        !@EQUAL
            MOVE      "Exit",KEY20
          ENDIF
.
GTHE1000  MATCH     SP8,OPARTEXP          * time patient died
          IF        !@EQUAL
            MOVE      "Deceased",KEY20
          ENDIF
.
          MATCH     SP70,KEY20
          GOTO      GTHE9999 IF EQUAL
.
          CLEAR     SRCHCL13
          APPEND    "Status: ",SRCHCL13
          APPEND    KEY20,SRCHCL13
.
          READ      CONTROLF,HUND13;*67,OPCNTHED
.
          MATCH     "1",OPCNTHED            * Display theatre location/banner
          IF        @EQUAL
            PACK      KEY6,OPDATHET,SP70
            CALL      RDOPOPR1
            IF         OVRCD<>1
               STRIP     OPRMDESC
               APPEND    " - ",SRCHCL13
               APPEND    OPRMDESC,SRCHCL13
               RESET     OPRMDESC
            ENDIF
          ENDIF
.
          APPEND    SP70,SRCHCL13
          RESET     SRCHCL13
          GOTO      GTHE9999
.
          INC       CLOPRSRG
          INC       IBAOVRIO/INC
          INC       OPRDEAIO/INC
          INC       OPRARDIO/INC
          INC       OPROPRIO/INC
          INC       OPRSRGIO/INC
.
GTHE2000  PACK      KEY20,SP70
          MOVE      PVIBILL,OPDAADMN
          MOVE      PVIBILL,KEY8
          MOVE      FOUR,OPDASTAT         * Booking status
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          PACK      KEY31,OPDAADMN,OPDASTAT,Z70
          CALL      RSOPDEA2
GTHE3000  CALL      RPOPDEA2
          BRANCH    OVRCD,GTHE9999
.
          MATCH     OPDAADMN,KEY8         * Same admission ?
          GOTO      GTHE9999 IF NOT EQUAL
.
          COMPARE   THREE,OPDASTAT        * Cancelled ?
          GOTO      GTHE3000 IF EQUAL     * Yes, skip this record
.
          MATCH     OPDADATE,CURRDATE     * Future date ?
          GOTO      GTHE3000 IF LESS
.
          UNPACK    OPDADATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
.
          DAYSEP    OPDADATE,CURRDATE,DAYDIFF
          CLEAR     SRCHCL13
          APPEND    "Op Date: ",SRCHCL13
          APPEND    CDAY,SRCHCL13
          APPEND    SP1,SRCHCL13
          APPEND    MTHNAM3,SRCHCL13
          APPEND    SP1,SRCHCL13
          APPEND    CCENT,SRCHCL13
          APPEND    CYEAR,SRCHCL13
.
          COMPARE   ZERO,DAYDIFF
          IF        @EQUAL
            APPEND    "  Status: ",SRCHCL13
            IF        OPDASTAT=0
              APPEND    "Booked",SRCHCL13
            ENDIF
            IF        OPDASTAT=1
              APPEND    "Preadmitted",SRCHCL13
            ENDIF
            IF        OPDASTAT=2
              APPEND    "Admitted",SRCHCL13
            ENDIF
            IF        OPDASTAT=3
              APPEND    "Cancelled",SRCHCL13
            ENDIF
            IF        OPDASTAT=4
              APPEND    "Discharged",SRCHCL13
            ENDIF
          ELSE
            APPEND    "  Days since op: ",SRCHCL13
            APPEND    DAYDIFF,SRCHCL13
          ENDIF
.
          APPEND    SP70,SRCHCL13
          RESET     SRCHCL13
.
GTHE9999  CLOSE     OPRDETA2
          CLOSE     OPRARDA1
          CLOSE     OPROPRA1
          CLOSE     OPRSRGA1
          ENDPROC
.*********************************************************************
.*  SDXAA000  :  Sound-ex search                                     *
.*********************************************************************
SDXAA000  CALL      RDPAR000                     * read system parametres
          CALL      SPARA000                     * keyin Search Parameters
          CALL      SHEAD000                     * display header
          MOVE      ZERO,CMSXFLAG
.
.------   Using Closest Match in soundex Search?
SDXAA090  MATCH     SP70,SRCHPDOB 
          IF        !@EQUAL
            CALL      SDXDB000                  * use date of birth soundex
            GOTO      SDXDB999
          ENDIF
.
          IF        PTCNCMSX=1
            CALL      SCMSX000                   * Search for Strict Matches
            UNPACK    SP70,GSRSKEY,GSRGKEY1,GSRGKEY2  
            IF        DISNUMB>=NORECORD
              GOTO      SDXAA910
            ENDIF
          ENDIF
.
. loop thru the SDXAA-ex order records
.
          MOVE      ZERO,FORM1                   * we are reading order records
          CALL      RSAAA000                     * Positional Reads SDXAAex Srch
SDXAA100  CALL      RKAAA000                     * read next record
          BRANCH    OVRCD,SDXAA900               * no more records
          CALL      DISPR000
          IF        DISNUMB>=NORECORD
            GOTO      SDXAA910
          ENDIF
          GOTO      SDXAA100                   * get next record (normal)
.
. we have no more records to display
.
SDXAA900  BRANCH    FHIRTYPE,SDXAA999
          WRITE     HTMLFILE;"</table>"
          WRITE     HTMLFILE;"<p align=center><b>End of Search</b>"
          WRITE     HTMLFILE;"</div>" 
          GOTO      SDXAA999                      * get next record (normal)
.
SDXAA910  BRANCH    FHIRTYPE,SDXAA999
          WRITE     HTMLFILE;"</table>"
          WRITE     HTMLFILE;"</div>" 
.
SDXAA999  RETURN
.
.*********************************************************************
.*  SDXGN000  :  Sound-ex search - strict given name, output in order*
.*               of given name,dob,surname                           *
.*********************************************************************
SDXGN000  CALL      RDPAR000                     * read system parametres
          CALL      SPARA000                     * keyin Search Parameters
          CALL      SHEAD000                     * display header
          MOVE      ZERO,CMSXFLAG
.
.------   Using Closest Match in soundex Search?
SDXGN090  MATCH     SP70,SRCHPDOB
          IF        !@EQUAL
            CALL      SDXDB000                  * use date of birth soundex
            GOTO      SDXGN999
          ENDIF
.
          IF        PTCNCMSX=1
            CALL      SCMSX000                   * Search for Strict Matches
            UNPACK    SP70,GSRSKEY,GSRGKEY1,GSRGKEY2
            IF        DISNUMB>=NORECORD
              GOTO      SDXGN910
            ENDIF
          ENDIF
.
. loop thru the SDXGN-ex order records
.
          MOVE      ZERO,FORM1                   * we are reading order records
          CALL      RSAGN000                     * Positional Reads SDXAAex Srch
SDXGN100  CALL      RKAGN000                     * read next record
          BRANCH    OVRCD,SDXGN900               * no more records
          CALL      DISPR000
          IF        DISNUMB>=NORECORD
            GOTO      SDXGN910
          ENDIF
          GOTO      SDXGN100                   * get next record (normal)
.
. we have no more records to display
.
SDXGN900  BRANCH    FHIRTYPE,SDXGN999
          WRITE     HTMLFILE;"</table>"
          WRITE     HTMLFILE;"<p align=center><b>End of Search</b>"
          WRITE     HTMLFILE;"</div>"
          GOTO      SDXGN999                      * get next record (normal)
.
SDXGN910  BRANCH    FHIRTYPE,SDXGN999
          WRITE     HTMLFILE;"</table>"
          WRITE     HTMLFILE;"</div>"
.
SDXGN999  RETURN
.
.********************************************************************
.*  RSAAA000  :  Initialise for reading via sound-ex                *
.********************************************************************
RSAAA000  PACK      GSRSNAM,SRCHSNAM,SP70    * set up soundex variable
          MOVE      ZERO,SRCHMACH                    
          CALL      SOUNDEX                  * Compute the soundex key
          MOVE      GSRSKEY,SRCHSNDX         * Save key for test in loop
          SCAN      SRCHMACH,SRCHSNDX        * ignore trailing zeros in match
          BUMP      SRCHSNDX,-1              * for surname soundex
          LENSET    SRCHSNDX
          RESET     SRCHSNDX
          PACK      KEY100,SRCHSNDX,SP70,SP70
.          PACK      KEY100,GSRSKEY,SP70,SP70
          CALL      RSPTGSR5
RSAAA999  RETURN
.
.***********************************************************************
.* RKAAA000  :  read in the next soundex order record & double soundex *
.***********************************************************************
RKAAA000  CALL      RKPTGSR5
          BRANCH    OVRCD,RKAAA999
          ADD       ONE,NUMLOOP 
          IF        NUMLOOP>NUMTLOOP
            MOVE      ONE,OVRCD
            GOTO      RKAAA999
          ENDIF
          MOVE      ZERO,SRCHMACH
          MATCH     SRCHSNDX,GSRSKEY    * right surname soundex?
          GOTO      RKAAA900 IF NOT EQUAL
.
          MATCH     SRCHGNAM,SP70        * any given name entered?
          GOTO      RKAAA100 IF EQUAL
.
          MATCH     SRCHGNAM,GSRGNAM
          GOTO      RKAAA200 IF LESS
          GOTO      RKAAA300 IF NOT EQUAL
.
RKAAA100  MOVE      ZERO,SRCHFLAG                * Valid Record
          MOVE      ZERO,EXIT
          MOVE      ZERO,OVRCD
          GOTO      RKAAA999
.
. Records given name less than that wanted - jump forward
. NO TO BE USED FOR ORACLE
RKAAA200  PACK      KEY100,GSRSKEY,GSRSNAM,SRCHGNAM,SP70,SP70
          CALL      RDPTGSR5
          IF        OVRCD=0
            CALL      RPPTGSR5
            IF        ORACLEDB=1
              PACK      KEY100,GSRSKEY,GSRSNAM,GSRGNAM,SP70,SP70
              CALL      RDPTGSR5
            ENDIF
          ENDIF
          GOTO      RKAAA000
.
. We have finished the given names for this particular surname
. try the next surname with the same starting string
. NO TO BE USED FOR ORACLE
RKAAA300  MOVE      "~",ANS
          PACK      KEY100,GSRSKEY,GSRSNAM,ANS,ANS,ANS,SP70,SP70
          CALL      RSPTGSR5
          GOTO      RKAAA000
.
RKAAA900  MOVE      ONE,OVRCD                     * no more data
.
RKAAA999  RETURN
+
.********************************************************************
.*  RSAGN000  :  Initialise for reading via sound-ex                *
.********************************************************************
RSAGN000  PACK      GSRSNAM,SRCHSNAM,SP70    * set up soundex variable
          MOVE      ZERO,SRCHMACH
          CALL      SOUNDEX                  * Compute the soundex key
          MOVE      GSRSKEY,SRCHSNDX         * Save key for test in loop
          SCAN      SRCHMACH,SRCHSNDX        * ignore trailing zeros in match
          BUMP      SRCHSNDX,-1              * for surname soundex
          LENSET    SRCHSNDX
          RESET     SRCHSNDX
          PACK      KEY108,SRCHSNDX,SP70,SP70
          CALL      RSPTGSR8
RSAGN999  RETURN
.
.***********************************************************************
.* RKAGN000  :  read in the next soundex order record & double soundex *
.***********************************************************************
RKAGN000  CALL      RKPTGSR8
          BRANCH    OVRCD,RKAGN999
          ADD       ONE,NUMLOOP
          IF        NUMLOOP>NUMTLOOP
            MOVE      ONE,OVRCD
            GOTO      RKAGN999
          ENDIF
          MOVE      ZERO,SRCHMACH
          MATCH     SRCHSNDX,GSRSKEY    * right surname soundex?
          GOTO      RKAGN900 IF NOT EQUAL
.
          MATCH     SRCHGNAM,SP70        * any given name entered?
          GOTO      RKAGN100 IF EQUAL
.
          MATCH     SRCHGNAM,GSRGNAM
          GOTO      RKAGN200 IF LESS
          GOTO      RKAGN300 IF NOT EQUAL
.
RKAGN100  MOVE      ZERO,SRCHFLAG                * Valid Record
          MOVE      ZERO,EXIT
          MOVE      ZERO,OVRCD
          GOTO      RKAGN999
.
. Records given name less than that wanted - jump forward
. NO TO BE USED FOR ORACLE
RKAGN200  PACK      KEY108,GSRSKEY,SRCHGNAM,SP70,SP70
          CALL      RDPTGSR8
          IF        OVRCD=0
            CALL      RPPTGSR8
            IF        ORACLEDB=1
              PACK      KEY108,GSRSKEY,GSRGNAM,GSRDOB,GSRSNAM,SP70,SP70
              CALL      RDPTGSR8
            ENDIF
          ENDIF
          GOTO      RKAGN000
.
. We have finished the given names for this particular surname
. try the next surname with the same starting string
. NO TO BE USED FOR ORACLE
RKAGN300  MOVE      "~",ANS
          PACK      KEY108,GSRSKEY,GSRGNAM,ANS,ANS,ANS,SP70,SP70
          CALL      RSPTGSR8
          GOTO      RKAGN000
.
RKAGN900  MOVE      ONE,OVRCD                     * no more data
.
RKAGN999  RETURN
+
.*********************************************************************
.*  SDXDB000  :  Sound-ex search with DOB                            *
.*********************************************************************
SDXDB000  IF        PTCNCMSX=1
            CALL      SCMSX000                   * Search for Strict Matches
            UNPACK    SP70,GSRSKEY,GSRGKEY1,GSRGKEY2
            IF        DISNUMB>=NORECORD
              GOTO      SDXDB910
            ENDIF
          ENDIF
.
. loop thru the SDXDB-ex order records
.
          MOVE      ZERO,FORM1                   * we are reading order records
          CALL      RSXDB000                     * Positional Reads SDXAAex Srch
SDXDB100  CALL      RKXDB000                     * read next record
          BRANCH    OVRCD,SDXDB900               * no more records
          CALL      DISPR000
          IF        DISNUMB>=NORECORD
            GOTO      SDXDB910
          ENDIF
          GOTO      SDXDB100                   * get next record (normal)
.
. we have no more records to display
.
SDXDB900  BRANCH    FHIRTYPE,SDXDB999
          WRITE     HTMLFILE;"</table>"
          WRITE     HTMLFILE;"<p align=center><b>End of Search</b>"
          WRITE     HTMLFILE;"</div>"
          GOTO      SDXDB999                      * get next record (normal)
.
SDXDB910  BRANCH    FHIRTYPE,SDXDB999
          WRITE     HTMLFILE;"</table>"
          WRITE     HTMLFILE;"</div>"
.
SDXDB999  RETURN
.
.********************************************************************
.*  RSXDB000  :  Initialise for reading via sound-ex with DOB       *
.********************************************************************
RSXDB000  PACK      GSRSNAM,SRCHSNAM,SP70    * set up soundex variable
          MOVE      ZERO,SRCHMACH
          CALL      SOUNDEX                  * Compute the soundex key
          MOVE      GSRSKEY,SRCHSNDX         * Save key for test in loop
          SCAN      SRCHMACH,SRCHSNDX        * ignore trailing zeros in match
          BUMP      SRCHSNDX,-1              * for surname soundex
          LENSET    SRCHSNDX
          RESET     SRCHSNDX
          PACK      KEY108,SRCHPDOB,SRCHSNDX,SP70,SP70
          CALL      RSPTGSR7
.         
RSXDB999  RETURN
.***********************************************************************
.* RKXDB000  :  read in the next soundex order record & double soundex *
.***********************************************************************
RKXDB000  CALL      RKPTGSR7
          BRANCH    OVRCD,RKXDB999
          ADD       ONE,NUMLOOP
          IF        NUMLOOP>NUMTLOOP
            MOVE      ONE,OVRCD
            GOTO      RKXDB999
          ENDIF
          MOVE      ZERO,SRCHMACH
          MATCH     SRCHPDOB,GSRDOB     * right date of birth?
          GOTO      RKXDB900 IF NOT EQUAL
          MATCH     SRCHSNDX,GSRSKEY    * right surname soundex?
          GOTO      RKXDB900 IF NOT EQUAL
          MATCH     SRCHGNAM,SP70        * any given name entered?
          GOTO      RKXDB100 IF EQUAL
.
          MATCH     SRCHGNAM,GSRGNAM
          GOTO      RKXDB200 IF LESS
          GOTO      RKXDB300 IF NOT EQUAL
.
RKXDB100  MOVE      ZERO,SRCHFLAG                * Valid Record
          MOVE      ZERO,EXIT
          MOVE      ZERO,OVRCD
          GOTO      RKXDB999
.
. Records given name less than that wanted - jump forward
. NO TO BE USED FOR ORACLE
RKXDB200  PACK      KEY108,SRCHPDOB,GSRSKEY,GSRSNAM,SRCHGNAM,SP70,SP70
          CALL      RDPTGSR7
          IF        OVRCD=0
            CALL      RPPTGSR7
            IF        ORACLEDB=1
              PACK      KEY108,GSRDOB,GSRSKEY,GSRSNAM,GSRGNAM,SP70,SP70
              CALL      RDPTGSR7
            ENDIF
          ENDIF
          GOTO      RKXDB000
.
. We have finished the given names for this particular surname
. try the next surname with the same starting string
. NO TO BE USED FOR ORACLE
RKXDB300  MOVE      "~",ANS
          PACK      KEY108,GSRSKEY,GSRSNAM,ANS,ANS,ANS,SP70,SP70
          CALL      RSPTGSR7
          GOTO      RKXDB000
.
RKXDB900  MOVE      ONE,OVRCD                     * no more data
.
RKXDB999  RETURN
+
.------------------------------------------------------------
. Output Alias indicator
.------------------------------------------------------------
          DEFPROC   CHCALI00
.
          INC       PATGSRFD/INC
.
CHALI010  MOVE      ZERO,ALEXIST
          CLOSE     CONTROLF
          OPEN      PATGSRN1,"patgsrnf"
          MOVE      ZERO,F1           * No Alias
          REP       "+ ",URNUMBER
.
          PACK      KEY115,URNUMBER,SP70,SP70
          CALL      RSPTGSR1
CHALI020  CALL      RKPTGSR1
          BRANCH    OVRCD,CHALI999
.
          MATCH     GSRURNO,URNUMBER
          GOTO      CHALI999 IF NOT EQUAL
.
          REP       UPPLOW,GSRSNAM
          MATCH     GSRSNAM,PTMASNAM
          GOTO      CHALI030 IF NOT EQUAL
.
          MATCH     GSRGNAM,PMPXFNAM
          GOTO      CHALI030 IF NOT EQUAL
.
          MATCH     PTGSGNM2,PMPXSNAM
          GOTO      CHALI030 IF NOT EQUAL
.
          MATCH     GSRDOB,PBDATE
          GOTO      CHALI030 IF NOT EQUAL
.
          MATCH     GSRSEX,PSEX
          GOTO      CHALI030 IF NOT EQUAL
          GOTO      CHALI020
.
CHALI030  MOVE      ONE,ALEXIST       * Yes they have an alias
CHALI999  REP       " +",URNUMBER
          CLOSE     PATGSRN1
          OPEN      CONTROLF,"controlf"
          GOTO      CHALIEND
.
          INC       PATGSRIO/INC
          INC       IBAOVRIO/INC
.
CHALIEND  ENDPROC
.------------------------------------------------------------
. Output Alternate ID Indicator
.------------------------------------------------------------
          DEFPROC   CHCALT00
.
          INC       PMSAIDFD/INC
.
CHALT010  MOVE      ZERO,AIEXIST
          OPEN      PMSAIDA1,"pmsaidaf"
          MOVE      ZERO,F1           * No Alternate ID
          REP       "+ ",URNUMBER
          PACK      KEY31,URNUMBER,SP70
          CALL      RSPMAID1
CHALT020  CALL      RKPMAID1
          BRANCH    OVRCD,CHALT999
          MATCH     PMAIURNO,URNUMBER
          GOTO      CHALT999 IF NOT EQUAL
          MOVE      ONE,AIEXIST       * Yes they have an alternate ID
CHALT999  REP       " +",URNUMBER
          CLOSE     PMSAIDA1
          GOTO      CHALTEND
.
          INC       PMSAIDIO/INC
          INC       IBAOVRIO/INC
.
CHALTEND  ENDPROC
.------------------------------------------------------------
. Output Alternate ID rows
.------------------------------------------------------------
DISALI00  COMPARE   ONE,PTCNDISA
          IF        !@EQUAL
            GOTO      DISALI99
          ENDIF
          REP       "+ ",URNUMBER
          MOVE      ZERO,ALTIDFLG
          PACK      KEY31,URNUMBER,SP70
.
          CALL      RSPMAID1
DISALI20  CALL      RKPMAID1
          BRANCH    OVRCD,DISALI99
.
          MATCH     PMAIURNO,URNUMBER
          GOTO      DISALI99 IF NOT EQUAL
          UNPACK    PMAIALID,DIM12,KEY8
          MATCH     KEY8,URNUMBER
          GOTO      DISALI20 IF EQUAL
          MOVE      ONE,ALTIDFLG
          ADD       ONE,TOTALREC
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      DISALI20
          ENDIF
.
          IF        DISNUMB>=NORECORD
            GOTO      DISALI99
          ENDIF
.
          REP       " +",URNUMBER
          CALL      TABROW00
          ADD       ONE,DISNUMB
          REP       "+ ",URNUMBER
.
          GOTO      DISALI20
.
DISALI99  REP       " +",URNUMBER
          MOVE      ZERO,ALTIDFLG
          RETURN
+
.*****************************************************************************
.*  CHKREL0O
.*  Routine to check release information ('release to' fields)
.*  Exit=0  None of the 'release to' fields are set to no
.*       1  At least one 'release to' field is set to no
.*****************************************************************************
CHKREL00  MOVE      ZERO,RELTFLAG
.
          MATCH     "0",PMVXRPOA       * Check if Release to Enduring POA=no
          GOTO      CHKREL95 IF EQUAL
.
          MATCH     "0",PMVXRCN1       * Check if Release to Contact 1=no
          GOTO      CHKREL95 IF EQUAL
.
          MATCH     "0",PMVXRCN2       * Check if Release to Contact 2=no
          GOTO      CHKREL95 IF EQUAL
.
          MATCH     "0",PMVXRHFU       * Check if Release to Health Fund=no
          GOTO      CHKREL95 IF EQUAL
.
          MATCH     "0",PMVXRDVA       * Check if Release to DVA=no
          GOTO      CHKREL99 IF NOT EQUAL
.
CHKREL95  MOVE      ONE,RELTFLAG
CHKREL99  RETURN
+
.------------------------------------------------------------------------
.  Check for GP Access on websecaf
.------------------------------------------------------------------------
CHGP0000 MOVE       ZERO,EXIT
         MOVE       SP70,GPCODE
         MOVE       SP70,PRACTICE
.
         MATCH      SP70,WBSEGPAC
         GOTO       CHGP9999 IF EQUAL
.
         MATCH      "1",WBSEGPAC           * using registered gp
         IF         @EQUAL
           PACK       GPCODE,PMPXRHC1,SP70
           PACK       PRACTICE,PMPXRH1G,SP70
         ENDIF
.
         MATCH      "2",WBSEGPAC           * using verified gp
         IF         @EQUAL
           PACK       GPCODE,PMPXVGPC,SP70
           PACK       PRACTICE,PMPXVGPP,SP70
         ENDIF
.
         MATCH      GPCODE,WBSEGPCD
         GOTO       CHGP9999 IF EQUAL
.
         MATCH      SP70,PRACTICE
         GOTO       CHGP9000 IF EQUAL
.
         PACK       KEY20,WBSEGPCD,PRACTICE,SP70
         CALL       RDPMHCL1
         BRANCH     OVRCD,CHGP9000
.
         GOTO       CHGP9999
.
CHGP9000 MOVE       ONE,EXIT
.
CHGP9999 RETURN
+
.------------------------------------------------------------------------
.  Format 'created by ' line in field SRCHCL20 according to the
.  PTCNWBDS parameter (code, long name,short name)
.------------------------------------------------------------------------
FCBY0000 MOVE       NBSP,SRCHCL20         * at worst a space
         COMPARE    ONE,IBCNMHOS
         GOTO       FCBY9999 IF NOT EQUAL
.
         MOVE      PTCNWBDS,F1  * ward bed name option none,long,short
         CLEAR      SRCHCL20
.        APPEND     "<br />",SRCHCL20
         APPEND     CREATBY,SRCHCL20
.
         IF         F1 = 0
           APPEND     PMPXIHOS,SRCHCL20
           GOTO       FCBY9000
         ENDIF
.
         MATCH      SP70,PMPXIHOS
         GOTO       FCBY9000 IF EQUAL
.
         PACK       KEY3,PMPXIHOS
         CALL       RDPTHSP1
         BRANCH     OVRCD,FCBY9000
.
         IF         F1 = 2
           APPEND     PTHSSNAM,SRCHCL20
          ELSE
           APPEND     PTHSNAME,SRCHCL20
          ENDIF
.
FCBY9000 RESET     SRCHCL20
FCBY9999 RETURN
+
.------------------------------------------------------------------------
.  Format 'ward/bed'  ' line in field SRCHCL02 according to the
.  PTCNWBDS parameter (code, long name,short name)
.------------------------------------------------------------------------
FWBD0000 MOVE      PTCNWBDS,F1  * ward bed name option none,long,short
         CLEAR     SRCHCL02
         UNPACK    KEY6,D3,KEY3

         IF        F1 = 0
           GOTO      FWBD8000
         ENDIF
.
         PACK       KEY6,D3,SP70
         CALL       RDWARD1
         BRANCH     OVRCD,FWBD8000
.
         IF         F1 = 2
           APPEND     PTWDSDES,SRCHCL02
         ELSE
           APPEND     WBDESC,SRCHCL02
         ENDIF
.
         MATCH      SP3,KEY3
         GOTO       FWBD9999 IF EQUAL
.
         APPEND     "/",SRCHCL02
         PACK       KEY6,D3,KEY3
         CALL       RDWARD1
         IF         OVRCD = 1
           APPEND     KEY3,SRCHCL02
           GOTO       FWBD9999
         ENDIF
.
         IF        F1 = 2
           APPEND     PTWDSDES,SRCHCL02
         ELSE
           APPEND     WBDESC,SRCHCL02
         ENDIF
         GOTO       FWBD9999
.
FWBD8000 APPEND     D3,SRCHCL02
         MATCH      SP3,KEY3
         IF         !@EQUAL
           APPEND     "/",SRCHCL02
           APPEND     KEY3,SRCHCL02
         ENDIF
         GOTO       FWBD99999
.
FWBD9999 RESET    SRCHCL02
         RETURN
+
.******************************************************************************
.                   NHIDODHB
.         Procedure to retrieve the NHI 'District Health Board' for an
.         associated Domicile code.
.******************************************************************************
          DEFPROC   NHIDODHB
.
          INC       NHIMASFD/INC
          INC       NHIDMCFD/INC
.
CATdh     INIT      "dh"
.
          OPEN      NHIDMCA1,"nhidmcaf"
          OPEN      NHIMASA2,"nhimasaf"
.
NHIDHB00  REP       "+ ",URNUMBER
          PACK      KEY8,URNUMBER,SP20
          CALL      RDNHMAS2
          BRANCH    OVRCD,NHIDHB99
.
          MOVE      NHMADOMC,KEY4
          CALL      RDNHDM1
          BRANCH    OVRCD,NHIDHB99
.
          PACK      KEY5,CATdh,NHDMDHB,SP70
          CALL      RDCODE1
          IF        OVRCD=0
            MOVE      TDESC,FADDRES3
          ENDIF
          GOTO      NHIDHB99
.
          INC       NHIDMCIO/INC
          INC       NHIMASIO/INC
          INC       IBAOVRIO/INC
.
NHIDHB99  REP       " +",URNUMBER
          ENDPROC
+

.******************************************************************************
.* System   : PATIENT MANAGEMENT SYSTEM                                       *
.* Program  : IBAHMS11                                                        *
.* Desc     : PRS2 END OF YEAR RESET                                          *
.******************************************************************************
.* Date     : 16/05/91                                                        *
.* Author   : DIG                                                             *
.* Comment  :                                                                 *
.* Modifications :                                                            *
.*        V5.08.01  08/09/2000  Davin                                         *
.*                  Fixed Y2K bug                                             *
.*        V5.07.01  24/08/1999  Davin  SRF 634109                             *
.*                  Read CHCSDTE from correct position in control file        *
.*        V5.06.01  23/09/1998  Greg Horvat      SRF 628225                   *
.*                  Not validating the dates with the correct date range file *
.*                                                                            *
.******************************************************************************
          INC       STD001FD
          INC       PATCONFD/INC
          INC       PATDRGFD/INC
+
.**********************************************************************
.*                       GLOBAL VARIABLES                             *
.**********************************************************************
CPTDATE   DIM       8
.
NEWDATE   DIM       10
.
OLDDATE   DIM       10
OLDYEAR   DIM       4
.
TODAY     DIM       8
.
PRGID     INIT      "IBAHMS11"
PRGNAM    INIT      "PRS2 End Of Year Reset"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                           MAINLINE                                 *
.*                       Controlling Logic                            *
.**********************************************************************
ML0000    CALL      INIT0000                * initialization
.                                             open files
.                                             display header
.
ML1000    CALL      OPTN0000                * main option screen
          BRANCH    EXIT,ML9999
.
          CALL      DATE0000                * get dates for end of year reset
          BRANCH    EXIT,ML9999
.
          CALL      UPDC0000                * see if O.K. to update the control
.                                             file
ML9999    CHAIN     PGM
+
.**********************************************************************
.*                           INIT0000                                 *
.*               Open files and display header                        *
.**********************************************************************
INIT0000  CALL      DISPHEAD                * display screen header
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,EIGHTY8;*116,CHCSDTE
          READ      CONTROLF,SEVENTY7;*1,PTCNDPRS
.
          IF        PTCNDPRS=1
            DISPLAY   *P60:24,"patdprsf";
            OPEN      PATDRGA1,"patdprsf"
          ELSE
            DISPLAY   *P60:24,"patdrgaf";
            OPEN      PATDRGA1,"patdrgaf"
          ENDIF
.
INIT9999  RETURN
+
.**********************************************************************
.*                           OPTN0000                                 *
.*                   Display the main option screen                   *
.**********************************************************************
OPTN0000  MOVE      FALSE,EXIT
          DISPLAY   *P1:4,*EF,*V2LON,ZERO:
                    *P1:5,ONE:
                    *P2:4,*HOFF," = Exit":
                    *P2:5," = Run End of Year Reset":
                    *P1:7,"Select Option : ";
.
.------ keyin option ------
.
OPTN1000  MOVE      ZERO,OPTION
          DISPLAY   *P17:7,*EL,UNDLN1;
          KEYIN     *P17:7,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION             * see if option is zero
          GOTO      OPTN9000 IF EQUAL
.
          BRANCH    OPTION,OPTN9999
.
          BEEP                              * invalid option chosen
.
          GOTO      OPTN1000
.
.------ Exit option chosen ------
.
OPTN9000  MOVE      TRUE,EXIT
.
OPTN9999  RETURN
+
.**********************************************************************
.*                           DATE0000                                 *
.*      Get the previous start of financial year, calculate and       *
.*      validate new date                                             *
.**********************************************************************
.
.------ get the financial year of the current start of financial year ------
.------ parameter ------
.
DATE0000  UNPACK    CHCSDTE,CCENT,CYEAR,CMON,CDAY
          PACK      CPTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",CPTDATE
.
          CALL      GPERD                   * get the financial year for the
.                                             start date parameter
          BRANCH    EXIT,DATE9000
.
          MOVE      DRGYR,OLDYEAR
.
          PACK      CPTDATE,CCC,CYY,CMM,CDD
          REP       " 0",CPTDATE
.
          CALL      GPERD                   * get the financial year for the
.                                             current date
          BRANCH    EXIT,DATE9000
.
          MATCH     OLDYEAR,DRGYR           * error if the current financial
          GOTO      DATE9500 IF EQUAL         year is the same as the start
.                                             date parameter financial year
          PACK      KEY6,DRGYR,SP2
          CALL      RDSDRGA1                * get the first record for the 
          CALL      RDKDRGA1                  current financial year as the
          BRANCH    OVRCD,DATE9000            fromdate will be the new start
.                                             date
          MOVE      FALSE,EXIT
.
          GOTO      DATE9999         
.
DATE9000  DISPLAY   *P1:24,*EL,*B,"Financial Year Start Date":
                           " not on the Date Range File.  ";
          CALL      HITENTER
          MOVE      TRUE,EXIT
.
          GOTO      DATE9999
.
DATE9500  DISPLAY   *P1:24,*EL,*B,"Reset can only be run for a new financial":
                          " year.  ";
          CALL      HITENTER
          MOVE      TRUE,EXIT
.
DATE9999  RETURN
+
.**********************************************************************
.*                              UPDC0000                              *
.*            See if O.K. to update the controlf file                 *
.**********************************************************************
UPDC0000  UNPACK    CHCSDTE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",CCENT
.
          CALL      PACDATE                 * pack up the old start date
.
          MOVE      CPCDATE,OLDDATE
          UNPACK    DRGFDTE,CCENT,CYEAR,CMON,CDAY
.
          CALL      PACDATE                 * pack up the new start date
.
          MOVE      CPCDATE,NEWDATE
.
          DISPLAY   *P1:10,*EL,"Start of Financial Year will be updated from ":
                               *V2LON,OLDDATE,*HOFF," to ",*V2LON,NEWDATE:
                    *P1:12,*V2LON,*BLINKON,"WARNING",*HOFF," Data received ":
                           "for patients discharged prior to the new start":
                    *P9:13,"date will no longer be sent":
                    *P1:24,*EL,"Are you sure you still want to Continue (":
                           *V2LON,ANSY,*HOFF,"/",*V2LON,ANSN,*HOFF,") ? ";
.
UPDC1000  KEYIN     *P49:24,*V2LON,ANS;
.
          MATCH     ANSN,ANS                * see if No entered
          GOTO      UPDC9999 IF EQUAL
.
          MATCH     ANSY,ANS                * see if Yes entered
          GOTO      UPDC1100 IF EQUAL
.
          BEEP                              * invalid keyin
.
          GOTO      UPDC1000
.
UPDC1100  UNPACK    DRGFDTE,CCENT,CYEAR,CMON,CDAY
          PACK      CHCSDTE,CCENT,CYEAR,CMON,CDAY
.
          WRITAB    CONTROLF,EIGHTY8;*116,CHCSDTE
.
          DISPLAY   *P1:24,*EL,*B,"End of Year Reset complete.  ";
          CALL      HITENTER
.
UPDC9999  RETURN
.
. *****************************************************************************
. * GPERD      : Get the period for a date                                    *
. *                                                                           *
. * Parameters : CPTDATE  Date to determine period for (CCYYMMDD)             *
. * Returns    : EXIT     0 = Period found                                    *
. *                       1 = Date not found in date range file               *
. *              DRGYR    Period year for Date (CCYY)                         *
. *              DRGNUM   Period code for Date (MM)                           *
. *                                                                           *
. *****************************************************************************
.
GPERD     IF        PTCNDPRS=1
            OPEN      PATDRGA2,"patdprsf" * Open the PRS2 date range file
          ELSE
            OPEN      PATDRGA2,"patdrgaf" * Open the date range file
          ENDIF
          PACK      KEY14,CPTDATE,SP6   * Key for to find this date in file
          CALL      RDSDRGA2            * Position in Date range file
          CALL      RDKDRGA2            * Get the first record from given date
          BRANCH    OVRCD,GPERD900      * No record found in file
.
          MATCH     DRGFDTE,CPTDATE     * Is the given date before this period
          GOTO      GPERD900 IF LESS    * Yes. Date not in file
.
          MOVE      ZERO,EXIT           * Period found
          GOTO      GPERD999            * Return with the period
.
.         The requested date wasn't in the file
.
GPERD900  UNPACK    SP6,DRGYR,DRGNUM    * Blank out period code
          MOVE      ONE,EXIT            * Period record not found
.
GPERD999  CLOSE     PATDRGA2            * Close the date range file
          RETURN
+
.
          INC       STD001IO
          INC       PATDRGIO/INC

.******************************************************************************
.* System   : Inpatient                                                       *
.* Program  : IBAINP12                                                        *
.* Name     : Set Dates on Coding Changes Files                               *
.******************************************************************************
.* Date     : 09/11/1993                                                      *
.* Author   : Robert Sumsion                                                  *
.* Function : Records with a coding date greater than the date parameter      *
.*            PTCNLDAT and less than todays date will be reset to a new date  *
.*            (entered). The date set to cannot be less than the current run  *
.*            start of the period CHCSST or less than the current value of    *
.*            PTCNLDAT.                                                       *
.* Mods:                                                                      *
.*        V12.00.01 30/05/2025 Jacob Jackson     TSK 0955096                  *
.*                  Alphanumeric changes                                      *
.*        V9.08.03  16/11/2006  Jill Habasque CAR 124845                      *
.*                  Fixed move of PMPRLDAT TO PTCNLDAT                        *
.*        V9.08.02  08/11/2006  Jill Habasque CAR 124180                      *
.*                  Changed branches so WEB doesn't hang                      *
.*        V9.08.01  24/10/2006  Jill Habasque CAR 122010                      *
.*                  Removed CONTQST after "cleanly executed " warning         *
.*        V9.07.01  11/07/2006  Jill Habasque CAR 111827                      *
.*                  Fixed keyin for sites not running multiple PRS2 extracts  *
.*        V9.06.01  28/04/2006  Jill Habasque CAR 103027                      *
.*                  Fixed read on pmsvx1af                                    *
.*        V9.05.01  11/04/2006  Jill Habasque CAR 99867                       *
.*                  Added multi hospital selection                            *
.*        V9.04.02  08/09/2005  Jill Habasque CAR 69844                       *
.*                  Added print statements                                    *
.*        V9.04.01  11/05/2005  Jill Habasque CAR 61744                       *
.*                  Added match for CHCSDTE                                   *
.*        V9.04.01  11/05/2005  Jill Habasque CAR 61744                       *
.*                  Added match for CHCSDTE                                   *
.*        V9.02.01  23/09/2002  Jill Habasque SRF 21990                       *
.*                  Moved position of PTCNLDAT                                *
.*        V5.08.03  10/10/2000  J.Tan                                         *
.*                  Recompiled for WEB Report Scheduler                       *
.*        V5.08.02  13/09/2000  Greg Horvat      SRF 5452                     *
.*                  Copied from release 6.07                                  *
.*        V5.08.01  05/06/2000  Greg Horvat      00/01 PRS2 Mods              *
.*                  Changed to only write to patecmaf & not patcmorf after the*
.*                  1st of July                                               *
.*            V5.07.01  10/06/1999  Davin                                     *
.*                      Put yes/no/cancel in GETD0000                         *
.*            V5.07.B01 17/11/1998  Davin                                     *
.*                      Mods for 507                                          *
.*            V5.02.00  11/10/1994  Matt SRF : 131453                         *
.*                      Stopped update of CHCSDTE in this program             *
.*            V5.03.01  271095  Whirlpool  Prs2 changes                       *
.*                      Fixed loop in PCMO and PECM routine so that routine   *
.*                      so that it doesn't miss patients                      *
.*                      OB YOU BURGED UP SPASTIC !!                           *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
.
          INC       PATHSPFD/INC
          INC       PATCMOFD/INC
          INC       PATCONFD/INC
          INC       PATDSCFD/INC
          INC       PATECMFD/INC
          INC       PMSVX1FD/INC
          INC       PMSPRDFD/INC
.
HOSPCODE  DIM       3
SAVEPNO   FORM      2
SAVKEY16  DIM       16
SAVKEY18  DIM       18
SAVNEWF   DIM       1
REDCNT    FORM      5
RSTDTE    DIM       8
WRTCNT    FORM      5
.
PRGID     INIT      "IBAINP12"
PRGNAM    INIT      "Set Dates on Coding Changes Files"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                   ML0000                                   *
.*                                 Main Module                                *
.******************************************************************************
ML0000    CALL      INIT0000                * Inititalise variables & open files
.
ML1000    CALL      OPTN0000                * Select main option
          BRANCH    EXIT,ML9999
. 
          CALL      IBACLOCK
          CLOCK     TIME,CTIMEIS
          CALL      HEAD80
          CALL      GETHOS00
          CALL      DATE0000                * Keyin the reset date
          MATCH     "IBARSH",PGM
          IF        @EQUAL
            BRANCH    EXIT,ML9999
          ENDIF
.
          BRANCH    EXIT,ML1000
.
          CALL      PCMO0000                * Process patcmorf
          CALL      PECM0000                * Process patecmaf
          CALL      FINI0000                * Finish routine
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                  INIT0000              Called by: ML0000   *
.*                       Initialise Variables & Open Files                    *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen header
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"patcmorf";
          OPEN      PATCMOR1,"patcmorf"
          OPEN      PATCMOR2,"patcmorf"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"pmsprdaf";
          OPEN      PMSPRDA1,"pmsprdaf"
.
          DISPLAY   *P64:24,"patecmaf";
          OPEN      PATECMA1,"patecmaf"
          OPEN      PATECMA2,"patecmaf"
. 
          READ      CONTROLF,EIGHTY8;*116,CHCSDTE         * Start Date for HDP
          READ      CONTROLF,EIGHTY8;*124,CHCSST,*132,CHCSED
          READ      CONTROLF,HUND09;*169,PTCNLDAT
          READ      CONTROLF,HUND10;*247,PTCNPRSM
          MATCH     SP8,PTCNLDAT
          GOTO      INIT1000 IF EQUAL
.
          MATCH     "00000000",PTCNLDAT
          GOTO      INIT9000 IF NOT EQUAL
.
INIT1000  MOVE      CHCSED,PTCNLDAT
          WRITAB    CONTROLF,HUND09;*169,PTCNLDAT
.
INIT9000  MOVE      ZERO,EXIT
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000              Called by: ML0000   *
.*                                Select Option                               *
.******************************************************************************
OPTN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON," 0",*HOFF," = Exit":
                    *P1:5,*V2LON," 1",*HOFF," = Run Conversion":
                    *P1:7,"Select Option : ";
.
OPTN1000  KEYIN     *P17:7,*V2LON,*DV,UNDLN2,*P17:7,OPTION;
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN9000
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                  DATE0000              Called by: ML0000   *
.*                            Keyin The Reset Date                            *
.******************************************************************************
DATE0000  DISPLAY   *P1:10,*EF,"Reset Date : ";
          MOVE      "14",CCOL
          MOVE      "10",CVERT
          MATCH     "1",PTCNPRSM
          IF        @EQUAL
            PACK      CHCSED,PMPREDAT,SP70
          ENDIF
          UNPACK    CHCSED,CCENT,CYEAR,CMON,CDAY
.
          CALL      KEYDATE                 * Keyin the reset date
          BRANCH    CQUEST,DATE0000
          BRANCH    OVRCD,DATE9500
.
          PRINT     *1,"Reset Date : ",CDAY,SLASH,CMON,SLASH,CCENT,CYEAR;
.
          PACK      RSTDTE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",RSTDTE
.
          MATCH     PTCNLDAT,RSTDTE
          GOTO      DATE3000 IF LESS        * Reset date < current reset date
.
          MATCH     RSTDTE,CHCSED
          GOTO      DATE1000 IF NOT EQUAL   * PRS2 end date >= reset date
.
....      CALL      CONTQST                 * Ok to Continue (Y/N/C) ?
....      BRANCH    CEXIT,DATE9000,DATE0000,DATE9500
          GOTO      DATE9000
.
DATE1000  MATCH     CHCSST,RSTDTE
          GOTO      DATE2000 IF NOT LESS    * Reset date >= PRS2 start date
.
          DISPLAY   *P1:24,*EL,*B,"Reset date before the PRS2 start date.  ";
          PRINT     *1,"Reset date before the PRS2 start date.  ";
          CALL      HITENTER
          MATCH     "IBARSH",PGM
          GOTO      DATE9500 IF EQUAL
.
          GOTO      DATE0000
.
DATE2000  DISPLAY   *P1:23,*EF,*B,"Warning: The reset date is within the ":
                    "period of a run already cleanly executed.",*P1:24;
          PRINT     *1,"Warning: The reset date is within the ":
                    "period of a run already cleanly executed."
          CALL      HITENTER
......    CALL      CONTQST
......    BRANCH    CEXIT,DATE9000,DATE0000,DATE9500
          GOTO      DATE9000
.
DATE3000  DISPLAY   *P1:24,*EL,*B,"Reset date cannot be less than previous ":
                    "change date.  ";
          PRINT     *1,"Reset date cannot be less than previous ":
                    "change date.  ";
          CALL      HITENTER
          GOTO      DATE9500
.
DATE9000  MOVE      ZERO,EXIT
          GOTO      DATE9999
.
DATE9500  MOVE      ONE,EXIT
DATE9999  RETURN
+
.******************************************************************************
.*                                  PCMO0000              Called by: ML0000   *
.*                              Process patcmorf                              *
.******************************************************************************
PCMO0000  MATCH     "20000701",CHCSST
          GOTO      PCMO9000 IF NOT LESS    * PRS2 start date >= 1/7/2000
.
          MOVE      ZERO,REDCNT
          MOVE      ZERO,WRTCNT
          DISPLAY   *P1:24,*EL,"Scanning : ",*P35:24,"Updating : ";
.
          PACK      KEY16,CHCSED,Z70
PCMO1000  CALL      RDSCMOR2                * Position on & read a changes file
PCMO2000  CALL      RDKCMOR2                  record
          BRANCH    OVRCD,PCMO9000
.
          ADD       ONE,REDCNT
          IF        REDCNT%20=1
            DISPLAY   *P12:24,*V2LON,CMADMNO;
          ENDIF
.
          MATCH     "1",PTCNPRSM
          GOTO      PCMO2500 IF NOT EQUAL
.
          PACK      KEY8,CMADMNO,SP70
          CALL      RDPMVX11
          BRANCH    OVRCD,PCMO2000
.
          MATCH     HOSPCODE,PMVXMHOS
          GOTO      PCMO2000 IF NOT EQUAL
.
PCMO2500  PACK      KEY8,CMADMNO
          CALL      RDDSCH1                 * Read a discharge file record
          BRANCH    OVRCD,PCMO2000
.
          MATCH     DDATE,RSTDTE
          GOTO      PCMO2000 IF LESS        * Reset coding date < date disch
.
          PACK      SAVKEY16,CMCDATE,CMADMNO
          MOVE      SP1,SAVNEWF
          PACK      KEY16,CMADMNO,SP20
PCMO3000  CALL      RDSCMOR1                * Position on & read a changes file
          CALL      RDKCMOR1                  record
          BRANCH    OVRCD,PCMO4000
.
          MATCH     DADMNO,CMADMNO
          GOTO      PCMO4000 IF NOT EQUAL  * Different admin no
.
          PACK      KEY16,CMADMNO,CMCDATE
          CALL      DECMOR1                * Delete a changes file record
          MOVE      CMNEWFLG,SAVNEWF
          GOTO      PCMO3000
.
PCMO4000  MOVE      DADMNO,CMADMNO
          MOVE      RSTDTE,CMCDATE
          MOVE      SAVNEWF,CMNEWFLG
          MOVE      SP20,CMSPARE
.
          PACK      KEY16,CMADMNO,CMCDATE
          CALL      RDACMOR1                * Check read an episode chn file rec
          COMPARE   ONE,OVRCD
          CALL      WRCMOR1  IF EQUAL       * Write an episode changes file rec
.
          ADD       ONE,WRTCNT
          IF        WRTCNT%20=1
            DISPLAY   *P12:24,*V2LON,CMADMNO;
          ENDIF
.
          MOVE      SAVKEY16,KEY16
          GOTO      PCMO1000
.
PCMO9000  MOVE      ZERO,EXIT
PCMO9999  RETURN
+
.******************************************************************************
.*                                  PECM0000              Called by: ML0000   *
.*                              Process patecmaf                              *
.******************************************************************************
PECM0000  MOVE      ZERO,REDCNT
          MOVE      ZERO,WRTCNT
          DISPLAY   *P1:24,*EL,"Scanning : ",*P35:24,"Updating : ";
.
          PACK      KEY18,CHCSED,Z70
PECM1000  CALL      RSPTECM2                * Position on & read an episode
PECM2000  CALL      RKPTECM2                  changes file record
          BRANCH    OVRCD,PECM9000
.
          ADD       ONE,REDCNT
          IF        REDCNT%20=1
            DISPLAY   *P12:24,*V2LON,PTEMADMN;
          ENDIF
.
          MATCH     "1",PTCNPRSM
          GOTO      PECM2500 IF NOT EQUAL
.
          PACK      KEY8,PTEMADMN,SP70
          CALL      RDPMVX11
          BRANCH    OVRCD,PECM2000
.
          MATCH     HOSPCODE,PMVXMHOS
          GOTO      PECM2000 IF NOT EQUAL
.
PECM2500  PACK      KEY8,PTEMADMN
          CALL      RDDSCH1                 * Read a discharge file record
          BRANCH    OVRCD,PECM2000
.
          MATCH     DDATE,RSTDTE
          GOTO      PECM2000 IF LESS        * Reset coding date < date disch
.
          PACK      SAVKEY18,PTEMCDTE,PTEMADMN,PTEMEPNO
          MOVE      ONE,SAVEPNO
          MOVE      SP1,SAVNEWF
          PACK      KEY18,PTEMADMN,SP20
PECM3000  CALL      RSPTECM1                * Position on & read an episode
          CALL      RKPTECM1                  changes file record
          BRANCH    OVRCD,PECM4000
.
          MATCH     DADMNO,PTEMADMN
          GOTO      PECM4000 IF NOT EQUAL  * Different admin no
.
          PACK      KEY18,PTEMADMN,PTEMEPNO,PTEMCDTE
          CALL      DEPTECM1               * Delete an episode changes file rec
          MOVE      PTEMEPNO,SAVEPNO
          MOVE      PTEMNEWF,SAVNEWF
          GOTO      PECM3000
.
PECM4000  MOVE      DADMNO,PTEMADMN
          MOVE      ZERO,PTEMEPNO
          MOVE      RSTDTE,PTEMCDTE
          MATCH     CHCSDTE,DDATE
          IF        @LESS
            MOVE      DDATE,PTEMCDTE
          ENDIF
          MOVE      SAVNEWF,PTEMNEWF
          MOVE      SP20,PTEMSPAR
.
PECM5000  ADD       ONE,PTEMEPNO
          PACK      KEY18,PTEMADMN,PTEMEPNO,PTEMCDTE
          CALL      RAPTECM1                * Check read an episode chn file rec
          COMPARE   ONE,OVRCD
          CALL      WRPTECM1 IF EQUAL       * Write an episode changes file rec
.
          COMPARE   SAVEPNO,PTEMEPNO
          GOTO      PECM5000 IF LESS        * Episode counter < save episode no
.
          ADD       ONE,WRTCNT
          IF        WRTCNT%20=1
            DISPLAY   *P12:24,*V2LON,PTEMADMN;
          ENDIF
.
          MOVE      SAVKEY18,KEY18
          GOTO      PECM1000
.
PECM9000  MOVE      ZERO,EXIT
PECM9999  RETURN
+
.******************************************************************************
.*                                  FINI0000              Called by: ML0000   *
.*                               Finish Routine                               *
.******************************************************************************
FINI0000  DISPLAY   *P1:21,*EF,*B,"All coding dates have been converted.  ":
                    *P1:22,"Records read    : ",*V2LON,REDCNT,*HOFF:
                    *P1:23,"Records updated : ",*V2LON,WRTCNT,*HOFF,*P1:24;
.
          PRINT     *1,"All coding dates have been converted.  ":
                    *N,"Records read    : ",REDCNT:
                    *N,"Records updated : ",WRTCNT
.
          CALL      HITENTER
.
          MOVE      RSTDTE,PTCNLDAT
          WRITAB    CONTROLF,HUND09;*169,PTCNLDAT
.
          MATCH     "1",PTCNPRSM
          GOTO      FINI9000 IF NOT EQUAL
.
          PACK      KEY3,HOSPCODE,SP70
          CALL      RDPMPRD1
          BRANCH    OVRCD,FINI9000
          MOVE      PTCNLDAT,PMPRLDAT
          CALL      UPPMPRD1
.
FINI9000  MOVE      ZERO,EXIT
FINI9999  RETURN
+
.******************************************************************************
.*                                  GETHOS00                                  *
.*     Get hospital if multi hospital and running multiple extracts           *
.******************************************************************************
GETHOS00  MOVE      ZERO,EXIT
          MOVE      SP70,HOSPCODE
          MOVE      TEN,CVERT
          DISPLAY   *P1:CVERT,*EL,"Hospital Id : ";
          MATCH     "1",PTCNPRSM
          IF        !@EQUAL
            GOTO      GETHOS99
          ENDIF
.
          MOVE      TEN5,ECOL
          MOVE      CVERT,EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,GETHOS95,GETHOS95
          GOTO      GETHOS30
.
GETHOS30  DISPLAY   *P20:CVERT,*V2LON,PTHSNAME;
          PACK      KEY3,PTHSHOSP,SP70
          CALL      RDPMPRD1
          BRANCH    OVRCD,GETHOS90
          PACK      HOSPCODE,PTHSHOSP,SP70
          PACK      CHCSST,PMPRSDAT
          PACK      CHCSED,PMPREDAT
          MATCH     SP8,PMPRLDAT
          GOTO      GETHOS50 IF EQUAL
.
          MOVE      PMPRLDAT,PTCNLDAT
          MATCH     "00000000",PMPRLDAT
          GOTO      GETHOS99 IF NOT EQUAL
.
GETHOS50  MOVE      PMPREDAT,PTCNLDAT
          MOVE      PMPREDAT,PMPRLDAT              
          CALL      UPPMPRD1
          GOTO      GETHOS99
.
GETHOS90  DISPLAY   *P1:24,*EL,"No record on pmsprdaf for this hospital";
          CALL      HITENTER
          MOVE      ONE,EXIT
          GOTO      GETHOS99
.
GETHOS95  MOVE      ONE,EXIT
          GOTO      GETHOS99
.
GETHOS99  RETURN
+
.==============================================================================
.
          INC       STD001IO
.
          INC       PATHSPKY
          INC       PATHSPIO/INC
          INC       PATCMOIO/INC
          INC       PATDSCIO/INC
          INC       PATECMIO/INC
          INC       PMSVX1IO/INC
          INC       PMSPRDIO/INC
+
.


. *****************************************************************************
. *              P R O G R A M  S U M M A R Y                                 *
. *              -------------  -------------                                 *
. *                                                                           *
. *     PROGRAM NAME:     IBAPAT94                                            *
. *                                                                           *
. *     FUNCTION:         DOCTOR IN-PATIENT SCAN                              *
. *                                                                           *
. *     DATE WRITTEN:     17/04/86      (re-written 09/03/2005)               *
. *                                                                           *
. *     AUTHOR:           MALCOLM HAYSE (I.B.A)                               *
. *                                                                           *
. *     MODIFICATIONS:                                                        *
. *                                                                           *
.*        V12.00.01 30/05/2025  Don Nguyen     TSK 0955096                    *
.*                  Alphanumeric visit number changes                         *
.*        V11.00.01 10/03/2020  Jill Parkinson Task 0879163                   *
.*                  Added SEXDSCIO include                                    *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V9.07.01  16/09/2006  Jill Habasque CAR 109852                      *
.*                  Mods to output alternate ID if PTCNDAUR=1                 *
.*        V9.04.00  09/03/2005  Mike Laming   CAR 49016                       *
.*                  Ported from V6.12 and modified to print only              *
. *****************************************************************************
.
          INC       STD001FD   
.
          INC       PATCONFD/INC
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PATMA1FD/INC
          INC       PMSPX2FD/INC
          INC       HL7BMTFD/INC
          INC       PATWR1FD/INC
          INC       PATNOBFD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PMSHCPFD/INC            * Health Care Professional
+
.
.           WORK AREA
.
ADIAG34   DIM       34
.
.
.      EXTRA VARIABLES
.
DNSRCH    DIM       6
DANS      DIM       7
DIM9      DIM       9
DIM11     DIM       11
DIM3      DIM       3  
DIM30     DIM       30 
.
DSMOK     DIM       7
.
DOCCODE   DIM       10
DNAME     DIM       30
DSEX      DIM       6
.
ERRMSG    DIM       50 
ERRNO     FORM      1
SOPT      FORM      1
STBYFLG   FORM      1                       * Used for standby patients only
PNAME     DIM       20
.
SKEY6     DIM       6
.
+
.******************************************************************************
PRGID     INIT      "IBAPAT94"
PRGNAM    INIT      "DOCTOR IN-PATIENT SCAN"
VERSION   INIT      "V12.01.00"
.******************************************************************************
.         START OF PROGRAM
.
ML1000    CALL      INIT0000
          CALL      GDOC0000
          BRANCH    EXIT,ML9000
.
          CALL      SCAN0000                * scan ward/beds
.
ML2000    IF        CPAGENO = 0 & ERRNO = 0
            MOVE      "** No Current Admissions  **",ERRMSG
            MOVE      ONE,ERRNO
            CALL      PTLN0000
          ELSE
            CALL      PAGEND
            PRINT     *1,"Note - ** - Ward/Bed on Stand-By"
          ENDIF
.
ML9000    PRINT     *N,*N,*1,"End of Report"
.
ML9999    CHAIN     PGM
          STOP
.
.******************************************************************************
.                                 INIT0000
.******************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patdo1af";     *
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P64:24,"patcodes";     *
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"pmshcpaf";     *
          OPEN      PMSHCPA1,"pmshcpaf"
.
          DISPLAY   *P64:24,"patwr1af";     *
          OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY   *P64:24,"patnobef";     *
          OPEN      PATNOBE1,"patnobef"
.
          DISPLAY   *P64:24,"patma1af";     *
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"patmi1af";     *
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          MOVE      ZERO,ERRNO
          MOVE      "99",CLNO
.
INIT9999  RETURN
.
.******************************************************************************
.                                 GDOC0000
.                             Get Doctor Code
.******************************************************************************
GDOC0000  MOVE      ZERO,EXIT
          MOVE      ZERO,ERRNO
          MOVE      "UNKNOWN DOCTOR",DSNAME
          MOVE      SP30,DGNAME
.
START     DISPLAY   *P1:3,*EF,*P6:4,"Doctor code :"
.
          MOVE      ZERO,CPAGENO
.
.             INPUT DOCTOR NUMBER
.
GDOC1000  MOVE      SP6,DCODE
          MOVE      SP20,DOCCODE
          MOVE      SP6,DNSRCH
          KEYIN     *P20:4,*EL:
                    *P30:4,"? for Name search",*P20:4,*V2LON,DCODE
          RESET     DCODE
          GOTO      GDOC9000 IF EOS
.
          CMATCH    QUEST,DCODE
          GOTO      GDOC5000 IF EQUAL
.
          MOVE      DCODE,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD OF GDOC2000
.
          GOTO      GDOC9999
.
GDOC2000  PACK      KEY10,DCODE,SP10
          CALL      RDPMHCP1
          BRANCH    OVRCD OF GDOC4000
.
          MOVE      PMHCSNAM,DSNAME
          MOVE      PMHCGNAM,DGNAME
          GOTO      GDOC9999
.
.
.
GDOC4000  MOVE      "- Doctor code does not Exist",DIM30
          PACK      ERRMSG,DCODE,SP2,DIM30,SP20
          MOVE      ONE,ERRNO
          CALL      PTLN0000
          DISPLAY   *P30:4,*B,*V2LON:
                    "** Doctor code does not Exist ** ",*W2
          GOTO      GDOC9000
.
.             INPUT DOCTOR NAME ENQUIRY
.
GDOC5000  CALL      PATDOCDS
          DISPLAY   *P1:4,*EL,*P6:4,"Doctor code :"
          GOTO      GDOC1000
.
.
.
GDOC9000  MOVE      ONE,EXIT
.
GDOC9999  RETURN
.
.******************************************************************************
.                                 SCAN0000
.                            Scan Ward/Bed file
.******************************************************************************
SCAN0000  MOVE      ZERO,EXIT
.
.         GET IN WARD/BED SEQUENCE
.
          DISPLAY   *P40:24,"Scanning Wards :";
.
          MOVE      SP6,KEY6
          CALL      RDSWARD1
SCAN1000  CALL      RDKWARD1
          BRANCH    OVRCD,SCAN9999
.
          BRANCH    WACTIVE,SCAN1000        * Ignore non-active wards
.
          MATCH     SP3,WBED
          GOTO      SCAN3000 IF NOT EQUAL
.
          COMPARE   ONE,WINPUT
          GOTO      SCAN3000 IF NOT EQUAL
.
          PACK      KEY13,WWARD,SP6,SP2
          CALL      RDSNOBE1
SCAN2000  CALL      RDKNOBE1
          BRANCH    OVRCD,SCAN1000
.
          MATCH     WWARD,NBWARD
          GOTO      SCAN1000 IF NOT EQUAL
.
          MOVE      ZERO,STBYFLG
.
          MOVE      NBADMNO,WADMNO
          CALL      PBED0000                * process/print the bed
          GOTO      SCAN2000
.
.                                           * Wards with Beds
SCAN3000  MATCH     ZEROVISN,WADMNO
          GOTO      SCAN1000 IF EQUAL
.
          MOVE      ZERO,STBYFLG
          CALL      PBED0000                * process/print the bed
.
.         Check if patient is on standby
.
          MATCH     ZEROVISN,WSTBY
          GOTO      SCAN1000 IF EQUAL
.                                           * process the "stand-by" patient
          MOVE      WSTBY,WADMNO
          MOVE      ONE,STBYFLG
          CALL      PBED0000                * process/print the bed
.
          GOTO      SCAN1000
.
SCAN9000  MOVE      ONE,EXIT
.
SCAN9999  RETURN
+
.******************************************************************************
.                                  PBED0000
.                     Subroutine to process & print a bed
.******************************************************************************
PBED0000  MOVE      ZERO,EXIT
.
          MOVE      WADMNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,PBED9999          * admission not found
.
          MATCH     DCODE,ADOCTA
          GOTO      PBED9999 IF NOT EQUAL
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,PBED9999
.
PBED1000  MOVE      DUNK,DSMOK
          LOAD      DSMOK,PSMOK,DYES,DNO
.
          MOVE      PGNAME,ANS
          PACK      PNAME WITH ANS,DOT,PSNAME
          REP       ", ",PNAME
.
          PACK      ADIAG34 WITH SP30,SP30
          MOVE      ADIAG1,ADIAG34
.
          UNPACK    ADATE INTO CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          MOVE      SP8,DSEX
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          MOVE      DSEXDESC,DSEX
.
          CALL      PTLN0000                * print a line
.
PBED9999  RETURN
.
.******************************************************************************
.                               PTLN0000
.                            Print data line
.******************************************************************************
PTLN0000  COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          BRANCH    ERRNO,PTLN7000
.
.
.
          BRANCH    STBYFLG,PTLN3000        * Check if patient is on standby
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,HUND10;*249,PTCNDAUR
          MATCH     "1",PTCNDAUR
          GOTO      PTLN2000 IF NOT EQUAL
          MOVE      AURNO,PURNO
          PACK      KEY20,SP70
          PROC      GETALIDS
          MATCH     SP70,KEY20
          IF        !@EQUAL
            UNPACK    KEY20,DIM11,DIM9
            PRINT     *1,"| ",AADMNO,*11,"| ",DIM9;
            GOTO      PTLN2100
          ENDIF
.
PTLN2000  PRINT     *1,"| ",AADMNO,*11,"| ",AURNO;
.
PTLN2100  PRINT     *22,"|",CPCDATE:
                    *34,"| ",AWARD,SLASH,ABED:
                    *45,"| ",PNAME,*68,"| ",ATYPE,*74,"| ",DSEX:
                    *82,"| ",PSAGE,*87,"| ",DSMOK:
                    *96,"| ",ADIAG34,*132,"|"
.
          ADD       ONE,CLNO
          GOTO      PTLN9999
.
PTLN3000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,HUND10;*249,PTCNDAUR
          MATCH     "1",PTCNDAUR
          GOTO      PTLN3100 IF NOT EQUAL
          MOVE      AURNO,PURNO
          PACK      KEY20,SP70
          PROC      GETALIDS
          MATCH     SP70,KEY20
          IF        !@EQUAL
            UNPACK    KEY20,DIM11,DIM9
            PRINT     *1,"| ",AADMNO,*11,"| ",DIM9;
            GOTO      PTLN3200
          ENDIF
.
PTLN3100  PRINT     *1,"| ",AADMNO,*11,"| ",AURNO;
.
PTLN3200  PRINT     *22,"|",CPCDATE:
                    *34,"| ",AWARD,SLASH,ABED,"**":         * stand-by
                    *45,"| ",PNAME,*68,"| ",ATYPE,*74,"| ",DSEX:
                    *82,"| ",PSAGE,*87,"| ",DSMOK:
                    *96,"| ",ADIAG34,*132,"|"
.
          ADD       ONE,CLNO
          GOTO      PTLN9999
.
PTLN7000  PRINT     *1,ERRMSG
.
PTLN9999  RETURN
.
.******************************************************************************
.
.                       LAST LINE ON THE PAGE
.******************************************************************************
PAGEND    PRINT     *1,"*-----------------------------------------":
                    "-----------------------------------------":
                    "------------------------------------------------*"
.
          ADD       ONE,CLNO
          RETURN
.******************************************************************************
.                               HEAD0000
.                         HEADINGS FOR OUTPUT
.******************************************************************************
HEAD0000  MOVE      ZERO,EXIT
.
          IF        CPAGENO > 0
            CALL      PAGEND
          ENDIF
.
          CALL      IBACLOCK
          CALL      HEAD132
          PRINT     *40,"DOCTOR : ",DCODE," ",DSNAME,DGNAME,*N
          CALL      PAGEND
.
          PRINT     *1,"| Admn no",*11,"| U/R no":
                    *22,"| Admitted",*34,"| Ward/Bed":
                    *45,"| Patients name",*68,"|Class",*74,"| Sex":
                    *82,"| Age",*87,"| Smoker":
                    *96,"| Admitting Diagnosis",*132,"|"
          CALL      PAGEND
          MOVE      SEVEN,CLNO
.
HEAD9999  RETURN
.*****************************************************************************
.*  GETALTID
.*  Procedure to get an alternate ID
.*  Exit=0  No alternate ID
.*       1  Alternate ID's exist
.*****************************************************************************
          DEFPROC   GETALIDS
.
          INC       PMSAIDFD/INC
.
          MOVE      ZERO,EXIT
          PACK      KEY20,SP70
          OPEN      PMSAIDA1,"pmsaidaf"
.
          PACK      KEY31,PURNO,SP70
          CALL      RSPMAID1           * Read alternate id Table
GETALI10  CALL      RKPMAID1
          BRANCH    OVRCD,GETALI99
.
          MATCH     PURNO,PMAIURNO     * Check same UR
          GOTO      GETALI99 IF NOT EQUAL
.
          MATCH     "1",PMAIDISU
          GOTO      GETALI10 IF NOT EQUAL
.
          PACK      KEY20,PMAIALID,SP70
          MOVE      ONE,EXIT
          GOTO      GETALI99
.
          INC       PMSAIDIO/INC
          INC       IBAOVRIO/INC
.
GETALI99  CLOSE     PMSAIDA1
.
          ENDPROC
.******************************************************************************
.
          INC       PATDOCDS
          INC       SEXDSCIO
.....     INC       HL7BMTMA
.
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATMA1IO/INC
          INC       PMSPX2IO/INC
          INC       HL7BMTIO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATWR1IO/INC
          INC       PATNOBIO/INC
          INC       PMSHCPIO/INC
.
          INC       STD001IO    
.

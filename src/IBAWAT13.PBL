.******************************************************************************
.*  System       :  Waiting List                                              *
.*  Program      :  IBAWAT13                                                  *
.*  Function     :  Ad-Hoc Enquiry                                            *
.******************************************************************************
.*  Date         :  08/12/92                                                  *
.*  Author       :  Whirlpool                                                 *
.*  Modifictions :                                                            *
.*                                                                            *
.******************************************************************************
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899                         *
.*           Recompiled as WATTR1FD changes                                   *
.******************************************************************************
.*        V10.13.02 05/11/2018  Richa Phogat     TSK 0864090                  *
.*                  Recompiled for DXWATTRE, Added WATTX1FD                   *
.*        V10.13.01 17/10/2018  Don Nguyen       TSK 0838558                  *
.*                  Deleted temp file (TEMP1) on program exit                 *
.*                  30/10/2018  Richa Phogat     TSK 0864090                  *
.*                  Recompiled for DXWATTRE, Added PATWC1FD                   *
.*        V10.12.01 20/04/2018  Peter Vela                  TSK 0855360       *
.*                  Recompiled for DXWATTRE                                   *
.*        V10.05.01 12/01/2015  Ania P                      CAR 261630        *
.*                  Removed use of PORT for temp file naming                  *
.*        V9.04.01  01/03/2005  Peter Vela    CAR 57547                       *
.*                  Recompiled for PATVADFD and PATVADIO                      *
.*        V9.03.02  29/10/2003 Sandra Barcham   43783                         *
.*                  Recompiled for NZHISIIO and NZHISIUP                      *
.*        V9.03.01  29/08/2003 J.Tan   CAR 42933                              *
.*                  Recompiled for PATSRCH                                    *
.*        V5.10.01  21/08/2002 Raghunandan Surve,HPS  SRF18814                *
.*                  Re-compiled for PMSHCPDS                                  *
.*        V5.09.01  05/09/2001  Steve Downing    SRF 12679                    *
.*                  Recompiled for DXPATCOD - Fixed I*C                       *
.*        V5.08.03  01/09/2000  Tonii                                         *
.*                  Mods to accept Unknown and Indeterminate as valid patient *
.*                  sex description                                           *
.*        V5.08.02  21/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.01  08/08/2000  Steve Downing                                 *
.*                  Recompiled for DXWATTRE                                   * 
.*        V5.07.03  03/11/1999 J Habasque                                     *
.*                  Mods to display inactive codes                            *
.*        V5.07.02  26/10/1999 J Habasque                                     *
.*                  Recompiled for WATDSPRO                                   *
.*        V5.07.01  12/08/1999 J.Tan                                          *
.*                  Recompiled for DXWATTRE - source of referral              *
.*       V5.07.B02  23/11/1998  Nicole Harrington                             *
.*                  5.07 changes                                              *
.                   22/10/1998  Glenn Berry      5.07 changes
.******************************************************************************
.*        V5.04.02  16/06/1997  Steve Armstrong                               *
.*                  Fixed bugs in global recompile                            *
. *       V5.04.01  15/04/1997  howellsy   RHA                                *
. *                 Recompiled for WATDSPRO                                   *
.******************************************************************************
.*                  V5.01.02  18/03/93  ROD                                   *
.*                            Add item no.'s to "?" on GP code and practice   *
.*                  V5.01.03  24/03/93 Whirlpool                              *
.*                            Changes to Watahocs                             *
.*                  V5.01.04  08/04/93 Whirlpool                              *
.*                            Changes to Watahocs                             *
.*                  V5.01.05  11/04/93 Graeme Williams                        *
.*                            Recompiled for changes to WATAHSVR              *
.*                            Use CONDSPUR instead of CONPURDS                *
.*                  V5.01.06  15/04/93  ROD                                   *
.*                            Made a fare few changes (Too many to mention)   *
.*                  V5.01.07  16/04/93  ROD                                   *
.*                            Changed category BC to WR                       *
.*                  V5.01.08  22/04/93 DIG                                    *
.*                            Re-Compiled for changes to PATALERT.            *
.*                  V5.01.09  11/05/93  Steve O'Gorman                        *
.*                            Changed procedure details screen to scr painted *
.*                            Changed PMI Heading to be Dudley version        *
.*                  V5.01.10  13/05/93  ROD                                   *
.*                            Re-Compiled for changes to WATAHOCS.            *
.*                  V5.01.11  18/05/93  Steve O'Gorman                        *
.*                            Re-Compiled for changes to WATAHOCS(Suspensions)*
.*                  V5.01.12  26/05/93 Whirlpool                              *
.*                            Re-compiled for invalid security mess(watahocs) *
.*                  V5.01.13  04/06/93  ROD                                   *
.*                            Fix bug when redisplay list of pats found       *
.*                  V5.01.14  07/06/93 Whirlpool                              *
.*                            Added facility to view waiting list comments    *
.*                  V5.01.15  07/07/93 Whirlpool    SRF 123516                *
.*                            Recompiled for changes to Watahocs              *
.*                  V5.01.16  12/07/93 Whirlpool    SRF 123516                *
.*                            Recompiled for changes to Watahocs              *
.*                  V5.01.17  23/07/93 Whirlpool    SRF 440243                *
.*                            Recompiled for changes to Watahocs              *
.*                  V5.01.18  26/08/93  ROD                                   *
.*                            Recompiled for NZHIS                            *
.*                  V5.01.19  22/11/93  Rob Leonard  SRF 440445               *
.*                            Recompiled for changes to WATAHOCS              *
.*                  V5.01.20  23/11/93  Paul Howells       NZHIS              *
.*                            Added PATALRFD & IO for NZHISIIO                *
.*                  V5.01.21  08/11/93  Rob Leonard  SRF 440542               *
.*                            Recompiled for changes to WATAHOCS              *
.*                  V5.01.22  16/12/93  Rob Leonard  SRF 440542               *
.*                            Recompiled for changes to WATAHOCS              *
.*                  V5.01.23  05/01/94  Rob Leonard  SRF 440591               *
.*                            Recompiled for changes to WATAHOCS              *
.*                  V5.01.24  29/06/94  Rob Leonard  SRF 440475               *
.*                            Added ability to print selection and picking list*
.*                  V5.01.25  30/08/94  Max White    SRF 440475               *
.*                            Print program & U/R header when printing         *
.*                            selected procedure details.                      *
.*                  V5.01.26  30/09/94  Paul Howells SRF 440475               *
.*                            Changed print header to 80 columns.              *
.*        V5.02.01  30.11.1994    B.G.Ackland                                 *
.*                  Remove Date of Death Link to NHI/MWS                      *
.*                  Recompile for Latest NHI/MWS Changes                      *
.******************************************************************************
.
          INC       STD001FD
.
          INC       NZHISIFD/INC
          INC       IBAPASFD/INC
          INC       PMIVARS/INC
.
          INC       BOKRC1FD/INC            * booking master file
.
          INC       OUTPREFD/INC
          INC       PATALRFD/INC
          INC       PATCALAG/INC
          INC       PATCODFD/INC
          INC       PATCOMM/INC
          INC       PATDHEAD/INC
          INC       PATNIDFD/INC
          INC       PATDNRFD/INC
          INC       PATSDNFD/INC
          INC       PATSMWFD/INC
          INC       PATCONFD/INC
          INC       PATDO1FD/INC
          INC       PMSHCLFD/INC
          INC       PMSHCPFD/INC
          INC       PMSHCGFD/INC
          INC       PMSVX1FD/INC
          INC       PATMA1FD/INC
          INC       PMSPX2FD/INC
          INC       PATMI1FD/INC
          INC       PATVADFD/INC
          INC       NHIMASFD/INC
          INC       PATPR1FD/INC
          INC       PATGSRFD/INC
          INC       PATWC1FD/INC
          INC       SCRFLDFD/INC       * field file
          INC       SCRPSTFD/INC       * position file
          INC       SCRSBGFD/INC
          INC       SCRSLTFD/INC       * selections file
          INC       WATAHSVR/INC
          INC       WATCONFD/INC
          INC       WATPROFD/INC
          INC       WATSEAFD/INC
          INC       WATSEIFD/INC
          INC       WATSUSFD/INC
          INC       WATRTDFD/INC
          INC       WATTR1FD/INC
          INC       WATTX1FD/INC
          INC       WATMESFD/INC
          INC       IBASEQFD/INC 
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
.
DIM6ARRY  DIM       6[34]
VERT      FORM      2
.
BJDAY     FORM      3
CJDAY     FORM      3    
CKLINKED  FORM      1
CATADES   DIM       20            * category A description
CMNTFLAG  FORM      1
CATEGORY  DIM       2
CODE      DIM       2
CMDLINE   DIM       80
.
DDESCWU   DIM       20           * display variable for referral/unit
DESCDEF1  DIM       20
DESCDEF2  DIM       20
DESCDEF3  DIM       20
DESCDEF4  DIM       20
DESCNOT   DIM       20           * description for short notice (cata WS)
DESCPCLS  DIM       20           * description for patient classif (cata A)
DESCPTYP  DIM       20           * description for Waiting type (cata WT)
DESCTP    DIM       20           * description for priority (cata TP)
DESCWU    DIM       20           * description for clinic/unit (cata WU)
DESCWR    DIM       20           * removal decription
DISPACT   FORM      1
DOCNAME   DIM       32           * doctor title initial.surname for display
DSEX      DIM       7            * descr. sex
.
.                       ----- flags for parameters keyed in or not : 0=no  1=yes
FINISH    INIT      "Finish    "
FLGCO     FORM      1            * proccode
FLGD1     FORM      1            * date on list
FLGD2     FORM      1            * last review date
FLGD3     FORM      1            * scheduled admission
FLGDO     FORM      1            * doctor
FLGES     FORM      1            * estimated stay
FLGPC     FORM      1            * patient category
FLGPL     FORM      1            * patient classification
FLGPT     FORM      1            * priority
FLGSN     FORM      1            * short notice
FLGST     FORM      1            * procedure status
FLGUN     FORM      1            * unit
FLGUR     FORM      1            * UR
FLGRS     FORM      1            * Resident status 
FLGRR     FORM      1            * Removal Reason
FLGRD     FORM      1            * Removal Date
FLGRAD    FORM      1            * Re-Assessment Date
FLGDOS    FORM      1            * Date On System
FLGUDF1   FORM      1            * user define 1
FLGUDF2   FORM      1            * user define 2
FLGUDF3   FORM      1            * user define 3
FLGUDF4   FORM      1            * user define 4
FORM3     FORM      3
FORM8     FORM      8
FRSTGNAM  DIM       40
FULLGNAM  DIM       80
.
KSTCODE   DIM       9
KSTDATE1  DIM       8
KSTDATE2  DIM       8
KSTDATE3  DIM       8
KSTDOC    DIM       6
KSTNOT    DIM       3
KSTPCAT   DIM       3
KSTPCLS   DIM       3
KSTPTY    DIM       3
KSTUNIT   DIM       3
KSTWTYP   DIM       3
KTOCODE   DIM       9
KTODATE1  DIM       8
KTODATE2  DIM       8
KTODATE3  DIM       8
KTODOC    DIM       6
KTOUNIT   DIM       3
KREMREAS  DIM       3            * removal reason
KREMDAT1  DIM       8            * start removal date
KREMDAT2  DIM       8            * end removal date
KREADAT1  DIM       8            * start re-assessment date
KREADAT2  DIM       8            * end re-assessment date
KDOSDAT1  DIM       8            * start date on system
KDOSDAT2  DIM       8            * end date on system
.
LINE27    FORM      "27"
LINE41    FORM      "41"
.
MASK      DIM       20
MODE1     DIM       1
MOPT01    FORM      2             * variable number of optional field 1
MOPT02    FORM      2             * variable number of optional field 2
MOPT03    FORM      2             * variable number of optional field 3
MOPT04    FORM      2             * variable number of optional field 4
MOPT05    FORM      2             * variable number of optional field 5
.
NUM37     FORM      "37"
.
PATDOB    DIM       10           * DOB patient
PATNAME   DIM       30           * name patient
PAGELIMT  FORM      3
PMHGFLAG  FORM      "0"             Flag for display of active GP prac's in ?
.                                   0 = Don't Display Inactive Gp's Prac's
.                                   1 = Display all Gp Practices
.
PMHGQARR  DIM       12[17]        * array for pmshcgds
PMHGQCNT  FORM      2             * count of items on screen
PMHCSNAX  DIM       20              Redefine field for ? Surname
PMHCGNAX  DIM       25              Redefine field for ? Given Name
PMHCPRCX  DIM       6               Redefine field for ? Practice Code
PMHCFLAG  FORM      "0"             Flag for display of active GP in ?
.                                   0 = Don't Display Inactive Gp's
.                                   1 = Display all Gp's
.
PMHCQARR  DIM       16[20]        * array for ? option
PMHCQCNT  FORM      2             * counter for ? option
PMHLFLAG  FORM      "0"                   Flag to display all links in ?
.                                         0 = Don't display inactive links
.                                         1 = Display all links
.
.
HUSRDES1  DIM       20
HUSRDES2  DIM       20
HUSRDES3  DIM       20
HUSRDES4  DIM       20
USERDEF1  DIM       3            * user define code 1
USERDEF2  DIM       3            * user define code 2
USERDEF3  DIM       3            * user define code 3
USERDEF4  DIM       3            * user define code 4
.
REPLY     DIM       2
.
SCNDGNAM  DIM       40
SCRNFLAG  FORM      1
SESNDATE  DIM       8
STATUS    FORM      2
START     INIT      "Start     "
STATDES   DIM       20
STATDES1  DIM       10
STATDES2  DIM       10
.
WORDFILE  DIM       8
.
XPOS      FORM      2
XPOS1     FORM      2
XPOS2     FORM      2
XPOS3     FORM      2
XPOS4     FORM      2
.
YPOS      FORM      2
.
Z15       INIT      "ZZZZZZZZZZZZZZZ"
Z30       INIT      "ZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZ"
.
TEMP1     FILE      ASCII,FIXED=79
+
PRGID     INIT      "IBAWAT13"
PRGNAM    INIT      "Ad-Hoc Enquiry"     
VERSION   INIT      "V12.01.00"
.
.*****************************************************************************
.*                              ML0000                                       *
.*                             Mainline                                      *
.*****************************************************************************
.
ML0000    CALL      INIT0000           * initialization and open files
.
ML3000    CALL      CLRP0000           * clear parameter vars
.
. mainline for watahocs
          CALL      CLER0000
          CALL      IMTH0000           * Initialise match vars.
          CALL      SCRN0000
          MOVE      ONE,DISPACT
          CALL      SIPC0000
          BRANCH    EXIT,ML9999        * Exit selected
          BRANCH    TOTLFLAG,ML4444
.
ML3200    CALL      XTRA0000
          BRANCH    EXIT,ML3000
.
          CALL      DETT0000           * get Procedure descriptions
          CALL      SCRT0000           * detailed Procedure screen
.
ML3500    CALL      PRTC0000           * print Procedure ? EXIT=1 for NO
          BRANCH    EXIT,ML3200,ML9999
.
          CALL      PRND0000           * print Procedure details
          GOTO      ML3200
.
ML4444    CALL      TALY0000           * tally records according to search info
          GOTO      ML3000
.
ML9999    CLOSE     TEMP1,DELETE
          CHAIN     PGM
          STOP
+
.*****************************************************************************
.*                              INIT0000                                     *
.*                        Initialisation section                             *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD
          MOVE      ONE,PRINTOPT         * provide option to print in watahocs
.
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P64:24,"patgsrnf"
          OPEN      PATGSRN1,"patgsrnf"
          OPEN      PATGSRN2,"patgsrnf"
          OPEN      PATGSRN3,"patgsrnf"
          OPEN      PATGSRN4,"patgsrnf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"patpramf";
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
.
          DISPLAY   *P64:24,"patwc1af";
          OPEN      PATWC1A1,"patwc1af"
.
          OPEN      PMSHCGA1,"pmshcgaf"
          OPEN      PMSHCPA1,"pmshcpaf"
          OPEN      PMSHCPA2,"pmshcpaf"
          OPEN      PMSHCLA1,"pmshclaf"
.
          DISPLAY   *P64:24,"watproaf";
          OPEN      WATPROA1,"watproaf"
          OPEN      WATPROA3,"watproaf"
.
          DISPLAY   *P64:24,"watsusaf";
          OPEN      WATSUSA1,"watsusaf"
.
          DISPLAY   *P64:24,"watmesaf";
          OPEN      WATMESA1,"watmesaf"
.
          DISPLAY   *P64:24,"wattr1af";
          OPEN      WATTR1A1,"wattr1af"
          OPEN      WATTR1A2,"wattr1af"
          OPEN      WATTR1A3,"wattr1af"
.
          DISPLAY   *P64:24,"wattx1af";
          OPEN      WATTX1A1,"wattx1af"
.
          DISPLAY   *P64:24,"scrpstaf";
          OPEN      SCRPSTA1,"scrpstaf"          * position file
          OPEN      SCRPSTA2,"scrpstaf"          * position file
          DISPLAY   *P64:24,"scrsbgaf";
          OPEN      SCRSBGA1,"scrsbgaf"          * background file
          DISPLAY   *P64:24,"scrsltaf";
          OPEN      SCRSLTA1,"scrsltaf"          * selection file
          OPEN      SCRSLTA2,"scrsltaf"          * selection file
.
.         DISPLAY   *P64:24,"conpuraf"
.         OPEN      CONPURA1,"conpuraf"
.         OPEN      CONPURA2,"conpuraf" 
.
.         DISPLAY   *P64:24,"concwlaf"
.         OPEN      CONCWLA1,"concwlaf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.         
          CALL      TFILENAM
          MOVE      TFILNAME,WORDFILE
.
INIT9000  READ      CONTROLF,ZERO;*2,CDD,CMM,CYY,CCC
          READ      CONTROLF,TWENTY2;*39,STCNWLAD
          READ      CONTROLF,FIFTY9;*132,CALRDESC,*220,PTCNUADT
          READ      CONTROLF,THIRTY7;*52,HUSR1,HUSRDS1,HUSMAN1:
                                       HUSR2,HUSRDS2,HUSMAN2:
                                       HUSR3,HUSRDS3,HUSMAN3:
                                       HUSR4,HUSRDS4,HUSMAN4:
                    *122,WTCNTPUC,*185,WTCNTPDS,*200,WTCNREAS:
                    *207,WTCNUCLS
.
          MOVE      ONE,CNEWDS
          CALL      UDES0000
          MOVE      SP8,SESNDATE
.
INIT9999  RETURN
.
.****************************************************************************
.*                                UDES0000                                  *
.*     Read user define descriptions from codes file and substitute those   *
.*     read in from controlf.                                               *
.****************************************************************************
UDES0000  PACK      KEY5,CODEW1,SP3
          CALL      RDCODE1
          MOVE      TDESC,HUSRDES1
.
          PACK      KEY5,CODEW2,SP3
          CALL      RDCODE1
          MOVE      TDESC,HUSRDES2
.
          PACK      KEY5,CODEW3,SP3
          CALL      RDCODE1
          MOVE      TDESC,HUSRDES3
.
          PACK      KEY5,CODEW4,SP3
          CALL      RDCODE1
          MOVE      TDESC,HUSRDES4
.
UDES9999  RETURN
+
.**************************************************************************
.*                              CLRP0000                                  *
.*                   clear keyin parameter variables                      *
.**************************************************************************
CLRP0000  MOVE      ZERO,EXIT
          MOVE      SP8,KSTUR
          MOVE      SP8,KTOUR
          PACK      KSTCODE,SP10
          PACK      KTOCODE,SP10
          PACK      KSTDATE1,SP10
          PACK      KTODATE1,SP10
          PACK      KSTDATE2,SP10
          PACK      KTODATE2,SP10
          PACK      KSTDATE3,SP10
          PACK      KTODATE3,SP10
          PACK      KSTUNIT,SP10
          PACK      KTOUNIT,SP10
          PACK      KSTDOC,SP10
          PACK      KTODOC,SP10
          MOVE      ZERO,KSTSTAY
          MOVE      "99999",KTOSTAY
          PACK      KSTPTY,SP10
          PACK      KSTNOT,SP10
          PACK      KSTPCLS,SP10
          PACK      KSTWTYP,SP10
          UNPACK    SP20,STATDES1,STATDES2
          UNPACK    SP20,KREMDAT1,KREMDAT2
          UNPACK    SP20,KREADAT1,KREADAT2
          UNPACK    SP20,KDOSDAT1,KDOSDAT2
          UNPACK    SP30,KREMREAS,DESCWR
          UNPACK    SP30,USERDEF1,USERDEF2,USERDEF3,USERDEF4
          MOVE      ZERO,EXTRFLAG
.
          MOVE      ONE,MATCHF1
          MOVE      ONE,MATCHF2
          MOVE      ONE,MATCHF3
          MOVE      ONE,MATCHF4
          MOVE      ONE,MATCHF5
          MOVE      ONE,MATCHF6
          MOVE      ONE,MATCHF7
          MOVE      ONE,MATCHF8
          MOVE      ONE,MATCHF9
          MOVE      ONE,MATCHF10
          MOVE      ONE,MATCHF11
          MOVE      ONE,MATCHF12
          MOVE      ONE,MATCHF13
          MOVE      ONE,MATCHF14
          MOVE      ONE,MATCHF15
          MOVE      ONE,MATCHF16
          MOVE      ONE,MATCHF17
          MOVE      ONE,MATCHF16
          MOVE      ONE,MATCHF19
          MOVE      ONE,MATCHF20
          MOVE      ONE,MATCHF21
          MOVE      ONE,MATCHF22
          MOVE      ONE,MATCHF23
          MOVE      ONE,MATCHF24
          MOVE      ONE,MATCHF25
          MOVE      ONE,MATCHF26
          MOVE      ONE,MATCHF27
          MOVE      ONE,MATCHF28
          MOVE      ONE,MATCHF29
          MOVE      ZERO,TOTLFLAG
          RETURN
+
.******************************************************************************
.*                                PAT0000                                     *
.*                     Get patient name in a dim20                            *
.******************************************************************************
PAT0000   MOVE      SP30,PATNAME
          MOVE      WMURNO,KEY8
          RESET     KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF PAT9999
          CALL      RDPMPX21
          MOVE      ONE,PACFRMT
          MOVE      PGNAME,PACGNAME
          MOVE      PSNAME,PACSNAME
          MOVE      PTITL,PACTITLE
          CALL      PACNAME
          MOVE      PACFNAME,PATNAME
.
PAT9999   RETURN
+
.****************************************************************************
.*                              DETT0000                                    *
.*          Get descriptions for procedure screen variables                 *
.*          Get patient DOB for display and sex-description                 *
.****************************************************************************
DETT0000  UNPACK    KEY19,WMURNO,WMCODE
          MOVE      SP10,PATDOB
          MOVE      SP20,DSEX
          PACK      PATNAME,SP30,SP20
.
          MOVE      WMURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,DETT9999
          CALL      RDPMPX21
          UNPACK    PBDATE,XCENT,XYEAR,XMON,XDAY
          PACK      PATDOB,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
.
          MOVE      PSEX,ANS
          REPLACE   "M1F2I3U4",ANS
          MOVE      ANS,FORM1
          LOAD      DSEX,FORM1,DMALE,DFEMALE,DINDETER,DUNK
.
          MOVE      ONE,PACFRMT        * setup patient name
          MOVE      PGNAME,PACGNAME
          MOVE      PSNAME,PACSNAME
          MOVE      PTITL,PACTITLE
          CALL      PACNAME
          MOVE      PACFNAME,PATNAME
.
DETT9999  RETURN
+
.******************************************************************************
.*                                DOCT0000                                    *
.*                        Concatenate doctor name                             *
.******************************************************************************
DOCT0000  MOVE      DGNAME,ANS
          PACK      DOCNAME,DTITL,SP1,ANS,DOT,DSNAME
          RETURN
+
.******************************************************************************
.*                                SCRT0000                                    *
.*                       Detailed procedure screen                            *
.*     SCRT was copied from WAT10 patient procedure details enquire screen    *
.******************************************************************************
SCRT0000  DISPLAY   *P1:3,*EF:
                    *P1:4,"U/R No.   : ":
                    *P28:4,"Name : ":
                    *P28:5,"Sex  : ":
                    *P42:5,"Born : ":
                    *P60:5,"Age : ":
                    *P28:6,"Priv : ":
                    *P55:6,"Bus : ":
                    *P13:4,*V2LON,PURNO:
                    *P35:4,PATNAME:
                    *P35:5,DSEX:
                    *P49:5,PATDOB:
                    *P66:5,PSAGE:
                    *P35:6,PTELEP:
                    *P61:6,PTELEB;
.
          MOVE      "11111111",ALRTMASK
          CALL      PATALERT
.
          DISPLAY   *P1:8,*EF
          MOVE      "01",SCRID
          CALL      DISP0000                      * display background
          BRANCH    EXIT,SCRT9999
          CALL      DSPFIELD                      * display fields
.
          IF        STCNWLAD<>2
            GOTO      SCRT9999
          ENDIF
.
          CALL      SEEMORE
          BRANCH    CEXIT,SCRT9999
.
          DISPLAY   *P1:8,*EF
          MOVE      "02",SCRID
          CALL      DISP0000                      * display background
          BRANCH    EXIT,SCRT9999
          CALL      DSPFIELD                      * display fields
.
SCRT9999  RETURN
.
.****************************************************************************
.*                            DISP0000                                      *
.*                  Handles the display of back ground uses SCRID           *
.****************************************************************************
.
DISP0000  MOVE      ZERO,EXIT
          CALL      DSPBGRND                * display the background
          IF        EXIT = 1
            DISPLAY   *P1:24,*EL,*B,"Screen Number ",*V2LON,SCRID,*HOFF:
                          " has not been set up.";
            CALL      HITENTER
            MOVE      ONE,EXIT
          ENDIF
          RETURN
+
.******************************************************************************
.*                                PRTC0000                                    *
.*                 Print these procedure details Comments or Exit ?           *
.******************************************************************************
PRTC0000  MOVE      SP1,ANS
          MOVE      FALSE,EXIT
          CALL      CMNT0000                * check for comments
PRTC1000  IF        CMNTFLAG = 0
            DISPLAY   *P1:24,*EL,"(",*V2LON,ANSP,*HOFF,")rint, View (",*V2LON:
                      ANSC,*HOFF,")omments, (",*V2LON:
                      ANSE,*HOFF,")xit to system menu : ";
            KEYIN     *P51:24,*EL,*V2LON,ANS;
          ELSE
            DISPLAY   *P1:24,*EL,"(",*V2LON,ANSP,*HOFF,")rint, (",*V2LON:
                      ANSE,*HOFF,")xit to system menu : ";
            KEYIN     *P34:24,*EL,*V2LON,ANS;
          ENDIF
          RESET     ANS
          MATCH     SP1,ANS
          GOTO      PRTC9000 IF EQUAL
          MATCH     ANSE,ANS
          GOTO      PRTC9222 IF EQUAL
          MATCH     ANSP,ANS
          GOTO      PRTC9999 IF EQUAL
          IF        CMNTFLAG = 0
            MATCH     ANSC,ANS
            GOTO      PRTC6000 IF EQUAL
          ENDIF
          BEEP
          GOTO      PRTC0000
.
PRTC6000 
          CALL      GETSCR00
          DISPLAY   *P1:8,*EF
          MOVE      FOUR,STATUS
          WORD      WORDFILE,999,2,8,78,22,STATUS
          CALL      PUTSCR00
          GOTO      PRTC1000
PRTC9000
          MOVE      ONE,EXIT           * go back to last screen
          GOTO      PRTC9999
PRTC9222
          MOVE      TWO,EXIT           * exit program
.
PRTC9999  RETURN
+
.******************************************************************************
.*                                PRND0000                                    *
.*                        print Procedure details                             *
.*        Modified 12/06/1990 Bill Dew  (Gee, thanks Bill)                    *
.******************************************************************************
PRND0000  DISPLAY   *P50:24,*BLINKON,*V2LON,*EL,"*** Printing ***";
          CALL      PRNH1111
.
          MOVE        "01",SCRID           * Print 1st PMI Screen
          CALL        SCRPRINT
.
          IF        STCNWLAD=2
            PRINT       *N,*N,*N;
            MOVE        "02",SCRID          * Print 2nd Screen if Being Used
            CALL        SCRPRINT
          ENDIF
.
          PRINT     *N,*N,"*** END OF ENQUIRY ***"
.
PRND9999  RETURN
.
.******************************************************************************
.*                                PRNT0000                                    *
.*                              Print sub details                             *
.******************************************************************************
PRNT0000  DISPLAY   *P1:24,*EL,"UR Number :":
                    *P50:24,*V2LON,*BLINKON,"*** Printing ***";
          MOVE      ONE,PROPTION
          MOVE      ZERO,FORM8
          MOVE      ZERO,CPAGENO
          CALL      PRNC0000           * print front cover heading
          CALL      PRNH0000           * print top page heading
.
          PACK      KEY27,KSTDATE1,SP20
          CALL      RDSTREA3
.
PRNT1000  CALL      RDKTREA3
          BRANCH    OVRCD,PRNT8000
.
          PACK      KEY8,WMURNO
          CALL      RDMAST1
          BRANCH    OVRCD,PRNT1000
          CALL      RDPMPX21
.
          CALL      MTCH0000                   * match against search criteria
          BRANCH    EXIT,PRNT1000
.
          CALL      SISP0000                   * see if on suspension
          BRANCH    EXIT,PRNT1000
.
          CALL      SREC0000                   * show screen single record
          DISPLAY   *P13:24,WMURNO;
          ADD       ONE,FORM8
          ADD       TWO,CLNO
.
          COMPARE   "60",CLNO                  * test for full page
          CALL      PRNH0000 IF NOT LESS
.
          MOVE      WMDESC,KEY12               * truncte treatment desc
          CALL      VSTA0000                   * get status var
          MOVE      TDESC,DIM8                 * truncate status desc
          CALL      PAT0000                    * setup patient name
          MOVE      PATNAME,KEY24              * truncat patient name
.
          PRINT     *1,"| ",WMURNO,*11,"| ",KEY24,*38,"| ",*40,WMCODE:
                    *50,"| ",*52,KEY12,*64,"| ",DATE10,*77,"| ",DATE20:
                    *89,"| ",DATE30,*101,"| ",WMUNIT,*107,"| ":
                    WMDOCTOR,*116,"| ",WMSTAY,*123,"|",DIM8,*132,"|"
.
          CALL      UND132
          GOTO      PRNT1000
.
PRNT8000  PRINT     *N,*N,"Total Number of ",WTCNTPUC," = ",FORM8
          PRINT     *N,*N,"*** End of Report ***"
.
PRNT9999  RETURN
.
.******************************************************************************
.*                                PRNH0000                                    *
.*                              Print heading                                 *
.******************************************************************************
PRNH0000  MOVE      ZERO,CLNO
          CLOCK     TIME,CTIMEIS
          MOVE      ONE,CNOUNDLN
          CALL      HEAD132
          PRINT     *N,*N,*1,"*----------------------------------------":
                          "----------------------------------------":
                          "----------------------------------------":
                          "----------*",*N:
                    *1,"| U/R No",*11,"| Patient Name",*38,"| ",WTCNTPDS:
                    *50,"| Description",*64,"| On List",*77,"| Review":
                    *89,"| Scheduled",*101,"| Unit",*107,"| Doctor":
                    *116,"| Stay",*123,"|Status",*132,"|":
                    *N,*1,"*----------------------------------------":
                     "----------------------------------------------":
                     "--------------------------------------------*"
          ADD       SIX,CLNO 
          GOTO      PRNH9999
.
.==============================================================================
PRNH1111
          MOVE      ZERO,CLNO
          CLOCK     TIME,CTIMEIS
          MOVE      ONE,CNOUNDLN
          CALL      HEAD132
          PRINT     *N,*N,*5,"U/R Number  : ",WMURNO,*30,PATNAME:
                    *65,"D.O.B : ",PATDOB,*N:
                    *5,"Phone (P):",PTELEP," (B):",PTELEB:
                    *N,*N
          ADD       SEVEN,CLNO 
.
PRNH9999  RETURN
+
.******************************************************************************
.*                                PRNC0000                                    *
.*                    Print first page with criteria                          *
.******************************************************************************
PRNC0000  CLOCK     TIME,CTIMEIS
.
          MOVE      ZERO,CPAGENO
          CALL      HEAD132
          PRINT     *N
.
          PRINT     *25,"*-------------------------------":
                    "-------------------------------*"
          PRINT     *25,"|",*39,"S e l e c t i o n     ":
                    "C r i t e r i a ",*88,"|"
          PRINT     *25,"*-------------------------------":
                    "-------------------------------*"
          MOVE      ONE,CLNO
          BRANCH    MATCHF1,PRNC1000
          ADD       ONE,CLNO
          PRINT     *25,"|  U/R Number         : ",KSTUR:
                    *61,"To",*64,KTOUR,*88,"|"
PRNC1000
          BRANCH    MATCHF2,PRNC2000
          ADD       ONE,CLNO
          PRINT     *25,"|  ",WTCNTPDS," Code     : ";
          PACK      STRTCODE,FROMPROC,SP10
          PACK      ENDCODE,TOPROC,SP10
          CALL      PRNG0000
          PRINT     *88,"|"
PRNC2000
          BRANCH    MATCHF3,PRNC3000
          ADD       ONE,CLNO
          PRINT     *25,"|  Est Proc Time      : ",FROMTIME,"      To ",TOTIME:
                    *88,"|"
.
PRNC3000
          BRANCH    MATCHF4,PRNC4000
          ADD       ONE,CLNO
          PRINT     *25,"|  Consultant         : ";
          PACK      STRTCODE,FROMCONS,SP10
          PACK      ENDCODE,TOCONS,SP10
          CALL      PRNG0000
          PRINT     *88,"|"
PRNC4000
          BRANCH    MATCHF5,PRNC5000
          ADD       ONE,CLNO
          PRINT     *25,"|  Patient Class      : ";
          PACK      STRTCODE,FROMPATC,SP10
          PACK      ENDCODE,TOPATC,SP10
          CALL      PRNG0000
          PRINT     *88,"|"
PRNC5000
          BRANCH    MATCHF6,PRNC6000
          ADD       ONE,CLNO
          PRINT     *25,"|  Guar. Adms Date    : ";
          PACK      KEY16,FROMGADD,TOGADD
          CALL      PDAT0000           * print date range
          PRINT     *88,"|"
PRNC6000
          BRANCH    MATCHF7,PRNC7000
          ADD       ONE,CLNO
          PRINT     *25,"|  Do not Admt before : ";
          PACK      KEY16,FROMDNAB,TODNAB
          CALL      PDAT0000           * print date range
          PRINT     *88,"|"
PRNC7000 
          BRANCH    MATCHF8,PRNC8000
          ADD       ONE,CLNO
          PRINT     *25,"|  Date on list       : ";
          PACK      KEY16,FROMDONL,TODONL
          CALL      PDAT0000           * print date range
          PRINT     *88,"|"
.
PRNC8000 
          BRANCH    MATCHF9,PRNC9000
          ADD       ONE,CLNO
          PRINT     *25,"|  Scheduled Adm Date : ";
          PACK      KEY16,FROMDSAD,TODSAD
          CALL      PDAT0000           * print date range
          PRINT     *88,"|"
.
PRNC9000 
          BRANCH    MATCHF10,PRNC9100
          ADD       ONE,CLNO
          PRINT     *25,"|  Procedure Status   : ";
          MOVE      START,STRTCODE
          LOAD      STRTCODE,KSTATUS1,STAT1,STAT2,STAT3,STAT4,STAT5,STAT6,STAT7
          LOAD      ENDCODE,KSTATUS2,STAT1,STAT2,STAT3,STAT4,STAT5,STAT6,STAT7
          CALL      PRNG0000
          PRINT     *88,"|"
.
PRNC9100 
          BRANCH    MATCHF11,PRNC9200
          ADD       ONE,CLNO
          PRINT     *25,"|  List Type          : ";
          PACK      STRTCODE,FROMLSTT,SP10
          PACK      ENDCODE,TOLSTT,SP10
          CALL      PRNG0000
          PRINT     *88,"|"
.
PRNC9200 
          BRANCH    MATCHF12,PRNC9300
          ADD       ONE,CLNO
          PRINT     *25,"|  Estimated Stay     : ";
          PACK      STRTCODE,KSTSTAY,SP10
          PACK      ENDCODE,KTOSTAY,SP10
          CALL      PRNG0000
          PRINT     *88,"|"
.
PRNC9300 
          BRANCH    MATCHF13,PRNC9400
          ADD       ONE,CLNO
          PRINT     *25,"|  Date on System     : ";
          PACK      KEY16,FROMDDOS,TODDOS
          CALL      PDAT0000           * print date range
          PRINT     *88,"|"
.
PRNC9400 
          BRANCH    MATCHF14,PRNE1000
          ADD       ONE,CLNO
          PRINT     *25,"|  Removal Date       : ";
          PACK      KEY16,FROMDREM,TODREM
          CALL      PDAT0000           * print date range
          PRINT     *88,"|"
.
.++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
.
PRNE1000 
          BRANCH    MATCHF15,PRNE2000
          ADD       ONE,CLNO
          PRINT     *25,"|  Removal Reason     : ",REMREAS,*88,"|"
.
PRNE2000
          BRANCH    MATCHF16,PRNE3000
          ADD       ONE,CLNO
          PRINT     *25,"|  Priority Code      : ",PRIORITY,*88,"|"
.
PRNE3000
          BRANCH    MATCHF17,PRNE4000
          ADD       ONE,CLNO
          PRINT     *25,"|  Resident Status    : ",RESISTAT,*88,"|"
.
PRNE4000
          BRANCH    MATCHF18,PRNE5000
          ADD       ONE,CLNO
          PRINT     *25,"|  Speciality         : ",SPECIALT,*88,"|"
.
PRNE5000
          BRANCH    MATCHF19,PRNE6000
          ADD       ONE,CLNO
          PRINT     *25,"|  Distr of Residence : ",DISOFRES,*88,"|"
.
PRNE6000
          BRANCH    MATCHF20,PRNE7000
          ADD       ONE,CLNO
          PRINT     *25,"|  Research Group     : ",RESGROUP,*88,"|"
.
PRNE7000
          BRANCH    MATCHF21,PRNE8000
          ADD       ONE,CLNO
          PRINT     *25,"|  Adminst Category   : ",ADMINCAT,*88,"|"
.
PRNE8000
          BRANCH    MATCHF22,PRNE9000
          ADD       ONE,CLNO
          PRINT     *25,"|  Short Stay         : ",SHRTSTAY,*88,"|"
.
PRNE9000
          BRANCH    MATCHF23,PRNE9100
          ADD       ONE,CLNO
          PRINT     *25,"|  Elective Adms Type : ",EADMTYPE,*88,"|"
.
PRNE9100
          BRANCH    MATCHF24,PRNE9200
          ADD       ONE,CLNO
          PRINT     *25,"|  Purchaser Code     : ",PURCHASR,*88,"|"
.
PRNE9200
          BRANCH    MATCHF25,PRNE9300
          ADD       ONE,CLNO
          PRINT     *25,"|  Referring GP       : ",REFERGP,*88,"|"
.
PRNE9300
          BRANCH    MATCHF26,PRNE9400
          ADD       ONE,CLNO
          PRINT     *25,"|  GP Practice        : ",GPPRAC,*88,"|"
.
PRNE9400
          BRANCH    MATCHF27,PRNE9500
          ADD       ONE,CLNO
          PRINT     *25,"|  GP Practice Code   : ",REGISGP,*88,"|"
.
PRNE9500
          BRANCH    MATCHF28,PRNE9900
          ADD       ONE,CLNO
          MOVE      "No ",DIM3
          IF        DCEASEFL=1
            MOVE      "Yes",DIM3
          ENDIF
          PRINT     *25,"|  Deceased           : ",DIM3,*88,"|"
.
PRNE9900
          COMPARE   ONE,CLNO
          GOTO      PRNE9990 IF NOT EQUAL
          PRINT     *25,"|   No Criteria ",*88,"|"
.
PRNE9990  PRINT     *25,"*-------------------------------":
                    "-------------------------------*"
          RETURN
+
.******************************************************************************
.*        Print Date given date in KEY16                                      *
.******************************************************************************
PDAT0000  UNPACK    KEY16,FDATE1,FDATE2
          MATCH     SP8,FDATE1
          GOTO      PDAT2222 IF NOT EQUAL
          PRINT     START," To ";
          GOTO      PDAT4444
PDAT2222
          UNPACK    FDATE1,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     CPCDATE," To ";
PDAT4444
          MATCH     "999999",FDATE2
          GOTO      PDAT5555 IF NOT EQUAL
          PRINT     FINISH;
          GOTO      PDAT9999
PDAT5555
          MATCH     "zzzzzz",FDATE2
          GOTO      PDAT6666 IF NOT EQUAL
          PRINT     FINISH;
          GOTO      PDAT9999
PDAT6666
          UNPACK    FDATE2,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     CPCDATE;
.
PDAT9999  RETURN
+
.******************************************************************************
.*        Print range of codes selected                                       *
.******************************************************************************
PRNG0000  MATCH     SP10,STRTCODE
          GOTO      PRNG2222 IF NOT EQUAL
          PRINT     START," To ";
          GOTO      PRNG4444
PRNG2222
          PRINT     STRTCODE," To ";
PRNG4444
          MATCH     "zzz",ENDCODE
          GOTO      PRNG6666 IF NOT EQUAL
          PRINT     FINISH;
          GOTO      PRNG9999
PRNG6666
          PRINT     ENDCODE;
PRNG9999  RETURN
+
.******************************************************************************
.*                                SISP0000                                    *
.*      see if procedure suspended       Returns : EXIT=1  proc suspended     *
.******************************************************************************
SISP0000  MOVE      ZERO,EXIT
          PACK      KEY21,WMURNO,WMCODE,WMCNT,SP2
          CALL      RSWTSUS1
          CALL      RKWTSUS1
          BRANCH    OVRCD,SISP9999               * not suspended
          PACK      KEY18,WTSUURNO,WTSUCODE,WTSUCNT
          MATCH     KEY18,KEY21
          GOTO      SISP9999 IF NOT EQUAL
          MOVE      ONE,EXIT                     * suspended
.
SISP9999  RETURN
+
.****************************************************************************
REDISPS   CALL      PUTSCR00
          MOVE      ONE,REFRESH
          RETURN
.
HEAD0000
GETSVAR   RETURN
+
.*********************************************************************
.*                  DISPVARS                    Called by : DSPFIELD *
.*        Routine which calls the required routines to set up the    *
.*        DATA and ATTRIBUTES string with the required values.       *
.*        Para's  : SCRID        screen number                      *
.*********************************************************************
DISPVARS  MOVE      SCRID,FORM2            * get as a form 2
          BRANCH    FORM2,DISP1000,DISP1000
          GOTO      DISP9999
.
.         first, second PMI screen
.
DISP1000  MOVE      SCPSITM,FIELDNO         * set the field number
          CALL      DXWATTRE                * display Procedure details
          GOTO      DISP9999
.
DISP9999  RETURN
+
.*****************************************************************************
.*                               CMNT0000                                    *
.*                            Handle comments                                *
.*****************************************************************************
.
CMNT0000  MOVE      ONE,CMNTFLAG
          PACK      KEY21,WMURNO,WMCODE,WMCNT,SP30
          CALL      RDSMESA1           * position the file pointer start UR
CMNT1000
          CALL      RDKMESA1
          BRANCH    OVRCD,CMNT9999
.
          MATCH     WAURNO,WMURNO      * test same Patient UR
          GOTO      CMNT9999 IF NOT EQUAL
          MATCH     WAPROC,WMCODE      * test same procedure code
          GOTO      CMNT9999 IF NOT EQUAL
          COMPARE   WACNT,WMCNT        * test same unique procedure
          GOTO      CMNT9999 IF NOT EQUAL
.
          IF        CMNTFLAG = 1
            CLOSE     TEMP1,DELETE
            PREP      TEMP1,WORDFILE
          ENDIF
.
          MOVE      ZERO,CMNTFLAG
          WRITE     TEMP1,SEQ;WATEXT
          GOTO      CMNT1000
.
CMNT9999  RETURN
.
.*****************************************************************************
.*                            I/O Section                                    *
.*****************************************************************************
.
          INC       STD001IO
          INC       BOKRC1IO/INC            * booking master file
          INC       CALCAGE
          INC       OUTPREIO/INC
          INC       PATALERT
          INC       PATALRIO/INC
          INC       PATCOMN1
          INC       PATCODKY/PBL
          INC       PATDOCKY/PBL
          INC       PMSHCLIO/INC
          INC       PMSHCPDS
          INC       PMSHCPIO/INC
          INC       PMSHCGDS
          INC       PMSHCGIO/INC
          INC       PMSVX1IO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATMA1IO/INC
          INC       PMSPX2IO/INC
          INC       PATMI1IO/INC
          INC       PATPR1IO/INC
          INC       PATGSRIO/INC
          INC       PATVADIO/INC
          INC       PATWC1IO/INC
          INC       SCRFLDIO/INC       * field file
          INC       SCRPSTIO/INC       * position file
          INC       SCRSBGIO/INC
          INC       SCRSLTIO/INC       * selections file
.
          INC       PATSNDX
          INC       PATSNX2
          INC       WATAHOCS
          INC       WATDSPRO
          INC       WATONSUS
          INC       WATPROIO/INC
          INC       WATTR1IO/INC
          INC       WATTX1IO/INC
          INC       WATRTDIO/INC
          INC       WATSUSIO/INC
          INC       WATSEAIO/INC
          INC       WATSEIIO/INC
          INC       WATMESIO/INC
.
          INC       SEEMORE
          INC       DISPITEM
          INC       DSPBGRND           * display background
          INC       DSPFIELD           * display fields
          INC       SCRPRINT           * print screen painted screen
          INC       DXWATTRE
          INC       DXPATCOD
          INC       DXPATDOC
          INC       NHIMASIO/INC
          INC       NZIBSRCH
          INC       NZHISIIO
          INC       CLPATMAS
          INC       PATNIDIO/INC
          INC       PATDNRIO/INC
          INC       PATSDNIO/INC
          INC       PATSMWIO/INC
          INC       IBAPASIO/INC
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC

OPEN0000
CLOS0000
AGECHK  
NZCHGADD  RETURN

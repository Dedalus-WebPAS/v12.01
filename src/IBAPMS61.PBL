.*******************************************************************************
.* System    :   Inpatients                                                    *
.* Program   :   IBAPMS61                                                      *
.* Desc      :   Expected patients report                                      *
.*******************************************************************************
.* Date      :   30/10/2007                                                    *
.* Author    :   Jill Habasque                                                 *
.* Function  :   This program will print a report of all expected patients     *
.*                                                                             *
.* Modifications                                                               *
.* V11.04.02  15/10/2024  Bella Turco                             TSK 0952443  *
.*            Filtered Theatre, Medical & Preadmission bookings by hosp        *
.* V11.04.01  24/04/2024  J.Tan                                   TSK 0939345  *
.*            Fixed I*C on index 2 oprdetaf                                    *
.* V11.03.01  20.03.2023  DA Sarkies                              TSK 0909393  *
.*            Added hospital Name as key to theatre booking                    *
.* V10.12.01  16/04/2018  Ania P                                  TSK 0855230  *
.*            Fixed ^M printer/printing problem.                               *
.* V10.04.01  15/06/2014 Jill Parkinson                           CAR  301639  *
.*                       Added call to KILL0000 after PROC00000                *
.* V10.03.01  02/01/2013 Patrick Adair                            CAR  261630  *
.*            Removed port number from temp file name                          *
.* V10.01.01  03/02/2011 Jill Parkinson CAR 233088                             *
.*            Added pmsvx1af                                                   *
.*  V9.11.01  08/01/2009  Steve Armstrong  CAR 185927                          *
.*            Removed isadd on temp file and included extra                    *
.*            index in isbuild (now that CMDLINE has been                      *
.*            extended in length from 80 to 100).                              *
.*     ???????? 28/02/2008  Jill Habasque  CAR 151925                          *
.*                          Added read of oprdetaf when getting medical        *
.*                          bookings                                           *
.*     ???????? 08/02/2008  Jill Habasque  CAR 151925                          *
.*                          Added clearing of pacfname                         *
.*******************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATCALAG/INC
          INC       BOKDIAFD/INC
          INC       BOKRC1FD/INC
          INC       BOKRX1FD/INC
          INC       IBACONFD/INC
          INC       OPRDEAFD/INC
          INC       OPRSESFD/INC
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PMSHCPFD/INC
          INC       PATWR1FD/INC
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
. LOCAL VARIABLE DEFINITION
. -------------------------
BJDAY     FORM      3
.
CJDAY     FORM      3
CFNAMEA   DIM       8         * temp file name A
CFNAMEB   DIM       8         * temp file name B
CMDLINE   DIM       100
CODE      DIM       2
DATE8     DATE      8
DIM48     DIM       48
DOCCODE   DIM       10
DOCNAME   DIM       25
DSPMONTH  DIM       9
ENDDATE   DIM       8
ENDSURG   DIM       10
ESURDESC  DIM       40
FROMTIME  DIM       3
FROMSURG  DIM       3
FROMTDES  DIM       40
FROMSDES  DIM       40
HOSCODE   DIM       3
ISBUILD   INIT      "isbuild "
ISERASE   INIT      "iserase "
PATNAME   DIM       28
PSAGETYP  DIM       6                        * age type description
PSAGECON  DIM       3                        * age converted value
SAVCASNM  DIM       3            * saved case number
SAVODATE  DIM       8            * saved operation date
SAVDOCTR  DIM       10           * saved doctor code
STRTDATE  DIM       8
STRTSURG  DIM       10
SSURDESC  DIM       40
TIMEDESC  DIM       25
TOTIMEPD  DIM       3
TOSURGTY  DIM       3
TOTIMDES  DIM       40
TOSRGDES  DIM       40
.
DPCW      INIT      "DPC/W"
DC        INIT      "DC"
IP        INIT      "IP"
AM        INIT      "AM"
ODB4      INIT      "1DB4"
.
JAN       INIT      "January"
FEB       INIT      "February"
MAR       INIT      "March"
APRIL     INIT      "April"
MAY       INIT      "May"
JUNE      INIT      "June"
JULY      INIT      "July"
AUG       INIT      "August"
SEPT      INIT      "September"
OCT       INIT      "October"
NOV       INIT      "November"
DEC       INIT      "December"
.
KEYA      INIT      " 294 U1-8,9-18,19-21,22-29,30-32,280-287":
                    " U280-287,1-8,9-18,19-21,22-29,30-32"
TEMPA     IFILE     SQL, FIXED=294, KEY="U1-8,9-18,19-21,22-29,30-32,280-287"
TEMPB     IFILE     SQL, FIXED=294, KEY="U280-287,1-8,9-18,19-21,22-29,30-32"
.Name     Type      Length    Phys.  Start  Description
.-------  -----     ------    -----  -----  ------------------------------------
XOPRDATE  DIM       8           8        1  Date
XOPRDOCT  DIM       10         10        9  Doctor
XOPTIMEP  DIM       3           3       19  Time Period (cat SP)
XOPSTART  DIM       8           8       22  Session start time
XOPCASNM  DIM       3           3       30  Case number
XOPSURTY  DIM       3           3       33  Surgery type (cat YD)
XPATNAME  DIM       30         30       36  Patient Name
XADMTIME  DIM       8           8       66  Admission Time
XAGE      DIM       3           3       74  Age
XDOB      DIM       8           8       77  Date of Birth
XPRODES1  DIM       55          55      85  Description line 1
XPRODES2  DIM       55          55     140  Description line 2
XPRODES3  DIM       55          55     195  Description line 3
XADMSTAT  DIM       5           5      250  Admission Status
XANAESTH  DIM       10          10     255  Anaesthetist
XURNUMBR  DIM       8           8      265  UR Number
XWARDBED  DIM       7           7      273  Ward/Bed
XADMNUMB  DIM       8           8      280  Admission number
XTHTROOM  DIM       6           6      288  Operating Room
.
.         End of record                294
PRGID     INIT      "IBAPMS61"
PRGNAM    INIT      "Expected patients report     "
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
          CALL      KILL0000               * kill existing temp file
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P64:24,"oprdetaf";
          OPEN      OPRDETA1,"oprdetaf"
          OPEN      OPRDETA2,"oprdetaf"
          OPEN      OPRDETA6,"oprdetaf"
.
          DISPLAY   *P64:24,"oprsesaf";
          OPEN      OPRSESA1,"oprsesaf"
.
          DISPLAY   *P64:24,"bokdiaaf";
          OPEN      BOKDIAA1,"bokdiaaf"
.
          DISPLAY   *P64:24,"bokrc1af";
          OPEN      BOKRC1A1,"bokrc1af"
          OPEN      BOKRC1A4,"bokrc1af"
.
          DISPLAY   *P64:24,"bokrx1af";
          OPEN      BOKRX1A1,"bokrx1af"
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A3,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"pmshcpaf";
          OPEN      PMSHCPA1,"pmshcpaf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          MOVE      ZERO,CLNO
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  IF        IBCNMHOS = 1
            OPEN      PATHSPA1,"pathspaf"
.
            DISPLAY   *P5:5,*EF,"Hospital Id              : ";
            MOVE      THIRTY2,ECOL
            MOVE      FIVE,EVERT
            MOVE      SP3,CKYIDEF3
            MOVE      ONE,CKYIMAND               * set for mandatory
            CALL      PATHSPKY                   * get hospital
            COMPARE   ONE,EXIT                   * exit selected
            GOTO      OPTN9999 IF EQUAL
.
            MOVE      KEY3,HOSCODE
            DISPLAY   *P32:5,*EL,PTHSNAME;
          ENDIF
.
          DISPLAY   *P5:6,"Starting Date            : ":
                    *P5:7,"Ending Date              : "
.
          CALL      STRT0000                  * start date
          BRANCH    EXIT,OPTN9999
.
          CALL      ENDD0000                  * end date
          BRANCH    EXIT,OPTN0000
.
          DISPLAY   *P5:8,"Starting Consultant Code    : ":
                    *P5:9,"Ending Consultant Code      : "
          CALL      KSSU0000             * Keyin start Consultant
          CALL      KESU0000             * Keyin ending Consultant
          CALL      TIMF0000             * Keyin start time period
          CALL      TIMT0000             * Keyin ending time period
          CALL      SURF0000             * Keyin start surgery type
          CALL      SURT0000             * Keyin ending surgery type
.
OPTN9999  RETURN
+
.****************************************************************************
.*                               STRT0000                                   *
.*                       enter start date                                   *
.****************************************************************************
STRT0000  MOVE      THIRTY2,CCOL
          MOVE      SIX,CVERT
          MOVE      CCC,CCENT
          MOVE      SP2,CYEAR
          MOVE      SP2,CMON
          MOVE      SP2,CDAY
          MOVE      ZERO,CHIGHLT
          MOVE      ZERO,CDEFDTE
          CALL      KEYDATE
.
          MATCH     SP2,CDAY
          GOTO      STRT9000 IF EQUAL
          BRANCH    OVRCD,STRT9000
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDATE
          CALL      PACDATE
          MOVE      FALSE,EXIT
          GOTO      STRT9999
.
STRT9000  MOVE      TRUE,EXIT
          GOTO      STRT9999
.
STRT9999  RETURN
+
.****************************************************************************
.*                               ENDD0000                                   *
.*                       enter end date                                     *
.****************************************************************************
ENDD0000  MOVE      THIRTY2,CCOL
          MOVE      SEVEN,CVERT
          MOVE      CCC,CCENT
          UNPACK    STRTDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      ZERO,CHIGHLT
          MOVE      ZERO,CDEFDTE
          CALL      KEYDATE
.
          MATCH     SP2,CDAY
          GOTO      ENDD5000 IF NOT EQUAL
          MOVE      TRUE,EXIT
          GOTO      ENDD9999
.
ENDD5000  PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDATE
          CALL      PACDATE
          MATCH     STRTDATE,ENDDATE
          GOTO      ENDD9200 IF LESS
          GOTO      ENDD9999
.
ENDD9000  DISPLAY   *P1:24,*EL,*B,"Invalid date - ";
          CALL      HITENTER
          GOTO      ENDD0000
.
ENDD9200  DISPLAY   *P1:24,*EL,*B,"TO date cannot be before FROM date. ";
          CALL      HITENTER
          GOTO      ENDD0000
.
ENDD9999  RETURN
+
.**********************************************************************
.*                         KSSU0000                                   *
.*                  Keyin the starting Consultant                     *
.**********************************************************************
.
KSSU0000  MOVE      SP10,STRTSURG
          MOVE      "32",ECOL
          MOVE      "08",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP70,CKYIDEF6
          CALL      PMSHCPKY
          BRANCH    EXIT,KSSU0500,KSSU0000
.
          MOVE      PMHCHCPC,STRTSURG
          GOTO      KSSU1000
.
KSSU0500  MOVE      "Start ",STRTSURG
          DISPLAY   *P32:8,*V2LON,STRTSURG
          CLEAR     SSURDESC
          GOTO      KSSU9999
.
KSSU1000  PACK      KEY10,STRTSURG,SP6
          CALL      RDPMHCP1
          CALL      FDOC0000             * Format doctors name
.
          MOVE      PACFNAME,SSURDESC
          DISPLAY   *P32:8,*EL,*V2LON,STRTSURG:
                    *P40:8,*HOFF,SSURDESC
          GOTO      KSSU9999
.
KSSU4000
          BEEP
          DISPLAY  *P1:24,*EL,"Code is not on file. ";
          CALL     HITENTER
          GOTO     KSSU0000
.
KSSU9999  RETURN
.
.
.**********************************************************************
.*                         KESU0000                                   *
.*                  Keyin the ending surgeon                          *
.**********************************************************************
KESU0000  MOVE      SP10,ENDSURG
          MOVE      "32",ECOL
          MOVE      "09",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          CALL      PMSHCPKY
          BRANCH    EXIT,KESU0500,KESU0000
.
          MOVE      PMHCHCPC,ENDSURG
          GOTO      KESU1000
.
KESU0500  MOVE      "Finish",ENDSURG
          DISPLAY   *P32:9,*V2LON,ENDSURG
          CLEAR     ESURDESC
          MOVE      ZERO,EXIT
          GOTO      KESU9999
.
KESU1000  PACK      KEY10,ENDSURG,SP6
          CALL      RDPMHCP1
          CALL      FDOC0000
          MOVE      PACFNAME,ESURDESC
          DISPLAY   *P32:9,*EL,*V2LON,ENDSURG:
                    *P40:9,*HOFF,ESURDESC
.
.**** test that end code >= start code ****
.
          MATCH     "Start ",STRTSURG
          GOTO      KESU9999 IF EQUAL
.
          MATCH     ENDSURG,STRTSURG
          GOTO      KESU9999 IF LESS
          GOTO      KESU9999 IF EQUAL
          BEEP
          DISPLAY   *P1:24,*EL,"Starting code must be > or = ending code. ";
          CALL      HITENTER
          DISPLAY   *P40:9,SP30

          GOTO      KESU0000
.
KESU9999  RETURN
.
.****************************************************************************
.*                                TIMF0000             Called by: ML1000    *
.* Keyin Time Period from                                                    *
.****************************************************************************
TIMF0000  DISPLAY   *P5:10,"Time Period From : ",SP30
          MOVE      SP70,FROMTIME
          MOVE      SP70,FROMTDES
          MOVE      "32",ECOL
          MOVE      "10",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP70,CKYIDEF3
          PACK      CODE,ANSS,ANSP
          DISPLAY   *PECOL:EVERT,SP70
.
          CALL      PATCODKY
          BRANCH    EXIT,TIMF9000,TIMF9100
.
          DISPLAY   *PECOL:EVERT,*V2LON,ACODE,*HOFF,*P25:EVERT,TDESC
          MOVE      ACODE,FROMTIME
          MOVE      TDESC,FROMTDES
.
TIMF9000  MOVE      ZERO,EXIT
          GOTO      TIMF9999
.
TIMF9100  MOVE      ONE,EXIT
          GOTO      TIMF9999
.
TIMF9999  RETURN
+
.****************************************************************************
.*                                TIMT0000             Called by: ML1000    *
.* Keyin time period to                                                    *
.****************************************************************************
TIMT0000  DISPLAY   *P5:11,"Time period to   : ",SP30
          MOVE      SP70,TOTIMEPD
          MOVE      SP70,TOTIMDES
          MOVE      "32",ECOL
          MOVE      "11",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP70,CKYIDEF3
          PACK      CODE,ANSS,ANSP
          DISPLAY   *PECOL:EVERT,SP70
.
          CALL      PATCODKY
          BRANCH    EXIT,TIMT9000,TIMT9100
.
          DISPLAY   *PECOL:EVERT,*V2LON,ACODE,*HOFF,*P25:EVERT,TDESC
          MOVE      ACODE,TOTIMEPD
          MOVE      TDESC,TOTIMDES
.
TIMT9000  MOVE      ZERO,EXIT
          GOTO      TIMT9999
.
TIMT9100  MOVE      ONE,EXIT
          GOTO      TIMT9999
.
TIMT9999  RETURN
+
.****************************************************************************
.*                                SURF0000             Called by: ML1000    *
.* Keyin Surgery Type from                                                    *
.****************************************************************************
SURF0000  DISPLAY   *P5:12,"Surgery Type From : ",SP30
          MOVE      SP70,FROMSURG
          MOVE      SP70,FROMSDES
          MOVE      "32",ECOL
          MOVE      "12",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP70,CKYIDEF3
          PACK      CODE,ANSY,ANSD
          DISPLAY   *PECOL:EVERT,SP70
.
          CALL      PATCODKY 
          BRANCH    EXIT,SURF9000,SURF9100
.
          DISPLAY   *PECOL:EVERT,*V2LON,ACODE,*HOFF,*P25:EVERT,TDESC
          MOVE      ACODE,FROMSURG
          MOVE      TDESC,FROMSDES
.
SURF9000  MOVE      ZERO,EXIT
          GOTO      SURF9999
.
SURF9100  MOVE      ONE,EXIT
          GOTO      SURF9999
.
SURF9999  RETURN
+
.****************************************************************************
.*                                SURT0000             Called by: ML1000    *
.* Keyin Surgery Type from                                                    *
.****************************************************************************
SURT0000  DISPLAY   *P5:13,"Surgery Type To : ",SP30
          MOVE      SP70,TOSURGTY
          MOVE      SP70,TOSRGDES
          MOVE      "32",ECOL
          MOVE      "13",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP70,CKYIDEF3
          PACK      CODE,ANSY,ANSD
          DISPLAY   *PECOL:EVERT,SP70
.
          CALL      PATCODKY
          BRANCH    EXIT,SURT9000,SURT9100
.
          DISPLAY   *PECOL:EVERT,*V2LON,ACODE,*HOFF,*P25:EVERT,TDESC
          MOVE      ACODE,TOSURGTY
          MOVE      TDESC,TOSRGDES
.
SURT9000  MOVE      ZERO,EXIT
          GOTO      SURT9999
.
SURT9100  MOVE      ONE,EXIT
          GOTO      SURT9999
.
SURT9999  RETURN
+
.**************************************************************************
.*                           PATN0000                                     *
.*                     Format the patients name                           *
.**************************************************************************
PATN0000  MOVE      TWO,PACFRMT
          MOVE      PTITL,PACTITLE
          MOVE      PGNAME,PACGNAME
          MOVE      PSNAME,PACSNAME
          CALL      PACNAME
          MOVE      PACFNAME,PATNAME
PATN9999  RETURN
+
.*********************************************************************
.*                        FDOC0000                                   *
.*                 Format the doctors name                           *
.*********************************************************************
.
FDOC0000  MOVE      SP70,PACFNAME
          MOVE      PMHCSNAM,PACSNAME
          MOVE      PMHCGNAM,PACGNAME
          MOVE      PMHCTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME                  * Format Name
          MOVE      PACFNAME,DOCNAME
.
FDOC9999  RETURN
.**************************************************************************
.*                  HEAD0000                                              *
.*            print the heading                                           *
.**************************************************************************
HEAD0000  IF        CLNO=0
            CALL      FRSTH000
          ENDIF
          PRINT     *N;
          CALL      UND132
          UNPACK    XOPRDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,FORM2           * move month to a form field
          LOAD      DSPMONTH,FORM2,JAN,FEB,MAR,APRIL,MAY,JUNE:
                                   JULY,AUG,SEPT,OCT,NOV,DEC
.
          PRINT     *N,*1,"Date : ",CDAY,SP1,DSPMONTH,SP1,CCENT,CYEAR;
.
.
HEAD1000  PACK      KEY10,XOPRDOCT,SP6
          CALL      RDPMHCP1
          CALL      FDOC0000             * Format doctors name
          MOVE      PACFNAME,SSURDESC
.
          MOVE      SP70,PACFNAME
          MATCH     SP70,XANAESTH
          IF        !@EQUAL
            PACK      KEY10,XANAESTH,SP6
            CALL      RDPMHCP1
            CALL      FDOC0000             * Format doctors name
          ENDIF
.
          MATCH     SP70,XOPTIMEP
          IF        @EQUAL
            PACK      TIMEDESC,SP70
          ELSE
            PACK      KEY5,ANSS,ANSP,XOPTIMEP,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              PACK      TIMEDESC,TDESC
            ENDIF
          ENDIF
. 
          MATCH     SP70,XOPSURTY
          IF        @EQUAL
            PACK      TDESC,SP70
          ELSE
            PACK      KEY5,ANSY,ANSD,XOPSURTY,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              PACK      TDESC,SP70
            ENDIF
          ENDIF
.
          PRINT     *N,*30,"Surgeon : ",SSURDESC,*80,"Anaesthetist : ",PACFNAME;
          PRINT     *N,*1,"Time Period : ",TIMEDESC,*40,"Surgery Type : ",TDESC;
.
          CALL      DASH0000
          PRINT     *N,*1,"|",*8,"|",*28,"|":
                    *37,"|",*42,"|",*51,"|":
                    *100,"|Adm ",*106,"|Op Start":
                    *115,"|UR",*124,"|Ward",*132,"|";
          PRINT     *N,*1,"|Room",*8,"|Patient Name",*28,"|Adm Time":
                    *37,"|Age",*42,"|Adm no",*51,"|Procedure":
                    *100,"|Stat",*106,"|Time":
                    *115,"|Number",*124,"|Bed ",*132,"|";
.
          CALL      DASH0000
          ADD       TEN,CLNO
.
HEAD9999  RETURN
+
FRSTH000  MOVE      ZERO,CLNO
          CALL      IBACLOCK
.
          CALL      HEAD132
.
          IF          IBCNMHOS =1
            PRINT     *N,*1,"Hospital : ",PTHSNAME
          ENDIF
          MOVE      ONE,CNOUNDLN
.
FRSTH999  RETURN
+
***************************************************************************
.*                           DASH0000                                     *
.*                  print 132 dashes                                      *
***************************************************************************
DASH0000  PRINT     *N,"----------------------------------------":
                    "----------------------------------------":
                    "----------------------------------------":
                    "------------";
DASH9999  RETURN
+
.****************************************************************************
.*                               PROC0000                                   *
.*                       Process report                                     *
.****************************************************************************
PROC0000  CALL      CREA0000
.
          MATCH     "Start",STRTSURG
          IF        @EQUAL
            MOVE      SP70,STRTSURG
          ENDIF
.
          MATCH     "Finish",ENDSURG
          IF        @EQUAL
            MOVE      SP70,ENDSURG
          ENDIF
.
          PACK      KEY26,STRTDATE,SP70
          CALL      RSOPDEA6
PROC1000  CALL      RKOPDEA6
          BRANCH    OVRCD,PROC2000
.
          MATCH     OPDADATE,ENDDATE
          GOTO      PROC2000 IF LESS
.
          IF        IBCNMHOS=1
            MATCH     HOSCODE,OPDAHOSP      * If multi hospital, skip record
            GOTO      PROC1000 IF NOT EQUAL * if not for the correct hospital
          ENDIF
.
          MATCH     SP70,STRTSURG
          IF        !@EQUAL
            MATCH     OPDACLIN,STRTSURG
            GOTO      PROC1500 IF EQUAL
            GOTO      PROC1000 IF LESS
          ENDIF
.
          MATCH     SP70,ENDSURG
          IF        !@EQUAL
            MATCH     OPDACLIN,ENDSURG
            GOTO      PROC1500 IF EQUAL
            GOTO      PROC1000 IF NOT LESS
          ENDIF
.
PROC1500  COMPARE   "3",OPDASTAT
          GOTO      PROC1000 IF EQUAL
.
          PACK      KEY22,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN,SP70
          CALL      RDOPSES1
          BRANCH    OVRCD,PROC1000
.
          MATCH     SP70,FROMTIME
          IF        !@EQUAL
            MATCH     OPSETPER,FROMTIME
            GOTO      PROC1600 IF EQUAL
            GOTO      PROC1000 IF LESS
          ENDIF
.
          MATCH     SP70,TOTIMEPD
          IF        !@EQUAL
            MATCH     OPSETPER,TOTIMEPD
            GOTO      PROC1600 IF EQUAL
            GOTO      PROC1000 IF NOT LESS
          ENDIF
.
PROC1600  MATCH     SP70,FROMSURG
          IF        !@EQUAL
            MATCH     OPSEDGSI,FROMSURG
            GOTO      PROC1700 IF EQUAL
            GOTO      PROC1000 IF LESS
          ENDIF
.
          MATCH     SP70,TOSURGTY
          IF        !@EQUAL
            MATCH     OPSEDGSI,TOSURGTY
            GOTO      PROC1700 IF EQUAL
            GOTO      PROC1000 IF NOT LESS
          ENDIF
.
PROC1700  PACK      KEY8,OPDAURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,PROC1000
.
          PACK      KEY8,OPDAADMN,SP70
          CALL      RDBKREC1
          BRANCH    OVRCD,PROC1000
.
          PACK      KEY8,DBKREBOO,SP70
          CALL      RDBKRX11
          BRANCH    OVRCD,PROC1000
.
          PACK      KEY8,DBKREBOO,SP70
          CALL      RDMISS1
          IF        OVRCD<>1
            IF        ASTAT=1
              PACK      BKRXPOWD,PTMIXWRD,SP70
            ELSE
              PACK      BKRXPOWD,AWARD,SP70
            ENDIF
          ENDIF
.
          PACK      KEY18,OPDAADMN,OPDAUNIQ,SP70
          CALL      RDBKDIA1
          IF        OVRCD=1
            PACK      BKDIDES1,SP70
            PACK      BKDIDES2,SP70
            PACK      BKDIDES3,SP70
          ENDIF
.
          CALL      WRTM0000            * Get details and write to tempfile
          GOTO      PROC1000
.
PROC2000  CALL      MDBK0000            * Get medical bookings
          CALL      PDBK0000            * Get preadmission bookings
.
          CALL      PRBK0000
.
PROC9999  RETURN
+
.*********************************************************************
.*                  CREA0000                                         *
.*        Create the Indexed Temp File                               *
.*********************************************************************
CREA0000  CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,CFNAMEA
.
          CALL      KILL0000                       * kill existing temp file
.
.------ Build new indexed temp file ------
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,CFNAMEA,KEYA,SP30
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      TEMPA,CFNAMEA
          OPEN      TEMPB,CFNAMEA
.
CREA9999  RETURN
+
.*********************************************************************
.*                  KILL0000                                         *
.*        Kill the Tempfile if it Already Exists                     *
.*********************************************************************
KILL0000  CLEAR     CMDLINE
          CLOSE     TEMPA
          PACK      CMDLINE,ISERASE,CFNAMEA
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
KILL9999  RETURN
+
.*********************************************************************
.*                  PRBK0000                                         *
.*        Print bookings from tempfile                               *
.*********************************************************************
PRBK0000  MOVE      SP70,SAVODATE
          MOVE      SP70,SAVDOCTR
          MOVE      SP70,SAVCASNM
          PACK      KEY40,SP70
          CALL      RSTEMP1
PRBK1000  CALL      RKTEMP1
          BRANCH    OVRCD,PRBK9000
.
          MATCH     SP70,SAVODATE
          IF        @EQUAL
            PACK      SAVODATE,XOPRDATE,SP70
            PACK      SAVDOCTR,XOPRDOCT,SP70
            CALL      HEAD0000
            GOTO      PRBK1100
          ENDIF
.
          MATCH     SAVODATE,XOPRDATE
          IF        @EQUAL
            MATCH     SAVDOCTR,XOPRDOCT
            GOTO      PRBK1100 IF EQUAL
          ENDIF
.
          CALL      HEAD0000
.
PRBK1100  UNPACK    XDOB,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          IF        CLNO>=58
            CALL      FRSTH000
            CALL      HEAD0000
          ENDIF
.
          MATCH     SAVCASNM,XOPCASNM
          IF        !@EQUAL
            MATCH     Z70,XOPCASNM
            IF        @EQUAL
              PRINT     *N,*1,"|      ***** Other expected bookings *****":
                        *132,"|";
            ENDIF
          ENDIF
.
          PACK      DIM19,XPATNAME
          PACK      DIM48,XPRODES1,SP70
          MATCH     "~~~~~",XOPSTART
          IF        @EQUAL
            MOVE      SP70,XOPSTART
          ENDIF
.
          PACK      KEY5,ANSY,ANSD,XOPSURTY,SP70
          CALL      RDCODE1
          IF        OVRCD<>1
            MATCH     ANSD,TCINDC1
            IF        @EQUAL
              PACK      XADMSTAT,DPCW,SP70
            ENDIF
          ENDIF
.
          PACK      KEY8,XADMNUMB,SP70
          CALL      RDMISS1
          BRANCH    OVRCD,PRBK1500
.
          IF        ASTAT=2|ASTAT=3
            PACK      XADMSTAT,IP,SP70
            GOTO      PRBK2000
          ENDIF
          PACK      KEY8,XADMNUMB,SP70
          CALL      RDBKRX11
          BRANCH    OVRCD,PRBK1500
.
          MATCH     PTMIXWRD,BKRXPOWD
          GOTO      PRBK1500 IF NOT EQUAL
.
          COMPARE   ZERO,ASTAY
          GOTO      PRBK1500 IF NOT EQUAL
.
          PACK      XADMSTAT,DC,SP70
          GOTO      PRBK2000
.
PRBK1500  PACK      KEY5,ANSY,ANSD,XOPSURTY,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,PRBK2000
.
          MATCH     ANSM,TCINDC1
          GOTO      PRBK2000 IF NOT EQUAL
.
          MATCH     BKREADAT,XOPRDATE
          IF        @EQUAL
            PACK      XADMSTAT,AM,SP70
          ENDIF
.
          MOVE      XOPRDATE,DATE8
          SUB       ONE,DATE8
          MATCH     DATE8,BKREADAT
          IF        @EQUAL
            PACK      XADMSTAT,ODB4,SP70
          ENDIF
.
PRBK2000  PRINT     *N,*1,"|",XTHTROOM,*8,"|",DIM19,*28,"|",XADMTIME:
                    *37,"|",XAGE,*42,"|",XADMNUMB,*51,"|",DIM48:
                    *100,"|",XADMSTAT,*106,"|",XOPSTART:
                    *115,"|",XURNUMBR,*124,"|",XWARDBED:
                    *132,"|";
.
          MOVE      XOPRDATE,SAVODATE
          MOVE      XOPRDOCT,SAVDOCTR
          MOVE      XOPCASNM,SAVCASNM
          ADD       ONE,CLNO
.
          MATCH     SP70,XPRODES2
          GOTO      PRBK1000 IF EQUAL
.
          PACK      DIM48,XPRODES2,SP70
          PRINT     *N,*1,"|",*8,"|",*28,"|":
                    *37,"|",*42,"|",*51,"|",DIM48:
                    *100,"|",*106,"|":
                    *115,"|",*124,"|":
                    *132,"|";
          ADD       ONE,CLNO
.
          MATCH     SP70,XPRODES3
          GOTO      PRBK1000 IF EQUAL
.         
          PACK      DIM48,XPRODES3,SP70
          PRINT     *N,*1,"|",*8,"|",*28,"|":
                    *37,"|",*42,"|",*51,"|",DIM48:
                    *100,"|",*106,"|":
                    *115,"|",*124,"|":
                    *132,"|";
          ADD       ONE,CLNO
.
          GOTO      PRBK1000
.
PRBK9000  CALL      DASH0000
          PRINT     *N,*10,"***** End of Report *****"
.
PRBK9999  RETURN
+
.*********************************************************************
.*                  MDBK0000                                         *
.*        Get medical bookings/preadmissions and write to tempfile   *
.*********************************************************************
MDBK0000  PACK      KEY16,STRTDATE,SP70
          CALL      RSBKREC4
MDBK1000  CALL      RKBKREC4
          BRANCH    OVRCD,MDBK9999
.
          MATCH     BKREEDAT,ENDDATE
          GOTO      MDBK9999 IF LESS
.
          MATCH     SP70,STRTSURG
          IF        !@EQUAL
            MATCH     BKREADOC,STRTSURG
            GOTO      MDBK1500 IF EQUAL
            GOTO      MDBK1000 IF LESS
          ENDIF
.
          MATCH     SP70,ENDSURG
          IF        !@EQUAL
            MATCH     BKREADOC,ENDSURG
            GOTO      MDBK1500 IF EQUAL
            GOTO      MDBK1000 IF NOT LESS
          ENDIF
.
          PACK      KEY31,DBKREBOO,SP70
          CALL      RSOPDEA2
          CALL      RKOPDEA2
          BRANCH    OVRCD,MDBK1500
.
          MATCH     OPDAADMN,DBKREBOO
          GOTO      MDBK1000 IF EQUAL
.
MDBK1500  PACK      KEY8,BKREURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,MDBK1000
.           
          PACK      KEY8,DBKREBOO,SP70
          CALL      RDBKRX11
          BRANCH    OVRCD,MDBK1000
.
          IF        IBCNMHOS=1
            MATCH     SP70,BKREWARD
            IF        !@EQUAL
              PACK      KEY6,BKREWARD,SP70
            ELSE
              MATCH     SP70,BKRXPOWD
              IF        !@EQUAL
                PACK      KEY6,BKRXPOWD,SP70
              ENDIF
            ENDIF
            MATCH     SP70,KEY6
            IF        !@EQUAL
              CALL      RDWARD1
              IF        OVRCD<>1
                MATCH     HOSCODE,PTWDHOSN      *If multi hospital, skip record
                GOTO      MDBK1000 IF NOT EQUAL *if not for the correct hospital
              ENDIF
            ENDIF
          ENDIF
.
          PACK      KEY8,DBKREBOO,SP70
          CALL      RDMISS1
          IF        OVRCD<>1
            IF        ASTAT=1
              PACK      BKRXPOWD,PTMIXWRD,SP70
            ELSE
              PACK      BKRXPOWD,AWARD,SP70
            ENDIF
          ENDIF
.
          PACK      KEY18,DBKREBOO,SP9,ONE,SP70
          CALL      RDBKDIA1
          IF        OVRCD=1
            PACK      BKDIDES1,SP70
            PACK      BKDIDES2,SP70
            PACK      BKDIDES3,SP70
          ENDIF     
.         
            
          PACK      OPDADATE,BKREEDAT
          PACK      OPDACLIN,BKREADOC
          PACK      OPSETPER,Z70
          PACK      OPDATIME,Z70
          PACK      OPDACASE,Z70
          PACK      OPSEDGSI,SP70
          PACK      OPDAANAE,SP70
          PACK      OPDAURNO,BKREURNO,SP70
          PACK      OPDAADMN,DBKREBOO,SP70
          PACK      OPSETHET,SP70
.
          PACK      KEY40,DBKREBOO,SP70
          CALL      RSTEMP1A
          CALL      RKTEMP1A
          BRANCH    OVRCD,MDBK8000
.
          MATCH     DBKREBOO,XADMNUMB
          GOTO      MDBK1000 IF EQUAL
.
MDBK8000  CALL      WRTM0000            * Get details and write to tempfile
          GOTO      MDBK1000
.
MDBK9999  RETURN
+
.*********************************************************************
.*                  PDBK0000                                         *
.*        Get preadmissions and write to tempfile                    *
.*********************************************************************
PDBK0000  PACK      KEY16,STRTDATE,SP70
          CALL      RDSMISS3
PDBK1000  CALL      RDKMISS3
          BRANCH    OVRCD,PDBK9999
.
          MATCH     ADATE,ENDDATE
          GOTO      PDBK9999 IF LESS
.
          IF        IBCNMHOS=1
            PACK      KEY8,DAADMNO,SP70
            CALL      RDPMVX11
            BRANCH    OVRCD,PDBK1000
            MATCH     HOSCODE,PMVXMHOS      * If multi hospital, skip record
            GOTO      PDBK1000 IF NOT EQUAL * if not for the correct hospital
          ENDIF
.
          COMPARE   ONE,ASTAT
          GOTO      PDBK1000 IF NOT EQUAL
.
          PACK      BKRXPOWD,PTMIXWRD,SP70
.
          MATCH     SP70,STRTSURG
          IF        !@EQUAL
            MATCH     ADOCTA,STRTSURG
            GOTO      PDBK1500 IF EQUAL
            GOTO      PDBK1000 IF LESS
          ENDIF
.
          MATCH     SP70,ENDSURG
          IF        !@EQUAL
            MATCH     ADOCTA,ENDSURG
            GOTO      PDBK1500 IF EQUAL
            GOTO      PDBK1000 IF NOT LESS
          ENDIF
.
PDBK1500  PACK      KEY8,AURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,PDBK1000
.
          PACK      KEY8,DAADMNO,SP70
          CALL      RDBKREC1
          BRANCH    OVRCD,PDBK2000
.
          PACK      KEY8,DAADMNO,SP70
          CALL      RDBKRX11
          IF        OVRCD=1
            MOVE      SP70,BKRXPOWD
            GOTO      PDBK2000
          ENDIF
.
          PACK      KEY8,DAADMNO,SP70
          CALL      RDBKREC1
          BRANCH    OVRCD,PDBK2000
.
          PACK      KEY18,DBKREBOO,SP9,ONE,SP70
          CALL      RDBKDIA1
          IF        OVRCD=1
            PACK      BKDIDES1,SP70
            PACK      BKDIDES2,SP70
            PACK      BKDIDES3,SP70
          ENDIF
.

          PACK      OPDADATE,BKREEDAT
          PACK      OPDACLIN,BKREADOC
          PACK      OPSETPER,Z70
          PACK      OPDATIME,Z70
          PACK      OPDACASE,Z70
          PACK      OPSEDGSI,SP70
          PACK      OPDAANAE,SP70
          PACK      OPDAURNO,BKREURNO,SP70
          PACK      OPDAADMN,DBKREBOO,SP70
          PACK      OPSETHET,SP70
          GOTO      PDBK2500
.
PDBK2000  PACK      OPDADATE,ADATE
          PACK      OPDACLIN,ADOCTA
          PACK      OPSETPER,Z70
          PACK      OPDATIME,Z70
          PACK      OPDACASE,Z70
          PACK      OPSEDGSI,SP70
          PACK      OPDAANAE,SP70
          PACK      OPDAURNO,AURNO,SP70
          PACK      OPDAADMN,DAADMNO,SP70
          PACK      OPSETHET,SP70
          GOTO      PDBK2500
.
PDBK2500  PACK      KEY40,OPDAADMN,SP70
          CALL      RSTEMP1A
          CALL      RKTEMP1A
          BRANCH    OVRCD,PDBK8000
.
          MATCH     OPDAADMN,XADMNUMB
          GOTO      PDBK1000 IF EQUAL
.
PDBK8000  CALL      WRTM0000            * Get details and write to tempfile
          GOTO      PDBK1000
.
PDBK9999  RETURN
+
.*********************************************************************
.*                  WRTM0000                                         *
.*        Write details to the temporary table                       *
.*********************************************************************
WRTM0000  PACK      XOPRDATE,OPDADATE,SP70
          PACK      XOPRDOCT,OPDACLIN,SP70
          PACK      XOPTIMEP,OPSETPER,SP70
          PACK      XOPSTART,OPDATIME,SP70
          PACK      XOPCASNM,OPDACASE,SP70
.
          PACK      XOPSURTY,OPSEDGSI,SP70
.
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME                 * Format patient name
          MOVE      PACFNAME,XPATNAME       * Patient name
.
          PACK      AGEDATE,CCC,CYY,CMM,CDD
          CALL      CALCAGE                 * Calculate age as of today
          MOVE      PSAGEYRS,XAGE           * Patient age
          MOVE      PBDATE,XDOB             * Patient DOB
.
          PACK      XADMTIME,BKREATIM
          PACK      XPRODES1,BKDIDES1,SP70
          PACK      XPRODES2,BKDIDES2,SP70
          PACK      XPRODES3,BKDIDES3,SP70
          PACK      XADMSTAT,SP70
          PACK      XANAESTH,OPDAANAE,SP70
          PACK      XURNUMBR,OPDAURNO,SP70
          PACK      XWARDBED,BKRXPOWD
          PACK      XADMNUMB,OPDAADMN
          PACK      XTHTROOM,OPSETHET
      PACK      KEY40,XOPRDATE,XOPRDOCT,XOPTIMEP,XOPSTART,XOPCASNM,XADMNUMB,SP70
          CALL      RATEMP1
          IF        OVRCD=1
            CALL      WRTEMP1
          ENDIF
.
WRTM9999  RETURN
+
RATEMP1   RESET     KEY40
          MOVE      ZERO,OVRCD
          READ      TEMPA,KEY40;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTEMP1   RESET     KEY40
          READ      TEMPA,KEY40;;
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMPA;XOPRDATE,XOPRDOCT,XOPTIMEP,XOPSTART,XOPCASNM:
                                XOPSURTY,XPATNAME,XADMTIME,XAGE,XDOB:
                                XPRODES1,XPRODES2,XPRODES3,XADMSTAT,XANAESTH:
                                XURNUMBR,XWARDBED,XADMNUMB,XTHTROOM
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   RESET     KEY40
          WRITE     TEMPA,KEY40;XOPRDATE,XOPRDOCT,XOPTIMEP,XOPSTART,XOPCASNM:
                                XOPSURTY,XPATNAME,XADMTIME,XAGE,XDOB:
                                XPRODES1,XPRODES2,XPRODES3,XADMSTAT,XANAESTH:
                                XURNUMBR,XWARDBED,XADMNUMB,XTHTROOM
          RETURN
.
RSTEMP1A  RESET     KEY40
          READ      TEMPB,KEY40;;
          RETURN
.
RKTEMP1A  MOVE      ZERO,OVRCD
          READKS    TEMPB;XOPRDATE,XOPRDOCT,XOPTIMEP,XOPSTART,XOPCASNM:
                                XOPSURTY,XPATNAME,XADMTIME,XAGE,XDOB:
                                XPRODES1,XPRODES2,XPRODES3,XADMSTAT,XANAESTH:
                                XURNUMBR,XWARDBED,XADMNUMB,XTHTROOM
          GOTO      OVERCOND IF OVER
          RETURN
.
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PMSHCPKY
          INC       PATHSPKY
          INC       PATCODKY
          INC       CALCAGE                 * Calculate patients age
          INC       TFILENAM                     * Generate Temp File Name
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       OPRDEAIO/INC
          INC       OPRSESIO/INC
          INC       BOKDIAIO/INC
          INC       BOKRC1IO/INC
          INC       BOKRX1IO/INC
          INC       PATHSPIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATWR1IO/INC
          INC       PATCODIO/INC
          INC       PMSHCPIO/INC
          INC       WEBERRIO/INC                 * Web Server Error Logging

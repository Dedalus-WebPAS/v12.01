.*****************************************************************************
.*                                                                           *
.*              P R O G R A M  S U M M A R Y                                 *
.*              -------------  -------------                                 *
.*                                                                           *
.*     PROGRAM NAME:     IBAOUT48                                            *
.*                                                                           *
.*     FUNCTION:         CURRENT BOOKING REPORT                              *
.*                                                                           *
.*     DATE WRITTEN:     19/07/88                                            *
.*                                                                           *
.*     AUTHOR:           KRUSTY THE CLOWN                                    *
.*                                                                           *
.*     MODIFICATIONS:    _copy IBAOUT47                                      *
.*                       _no option for surname keyin                        *
.*                                                                           *
.*           V10.13.01   05/12/2018  Don Nguyen       TSK 0838558            *
.*                       Deleted temp file (OUTROLXX) after processing       *
.*           V10.05.01   15/07/2014  Ebon Clements    CAR 261630             *
.*                       Removed port number from temp file name             *
.*           V9.09.01    12/07/2007  Jill Habasque    CAR 137000             *
.*                       Added check for blank name in FNSLOOP               *
.*           V5.08.01    01/08/2000  Steve Armstrong  srf 647085             *
.*                       Fixed setting of KEY20 to use current date when     *
.*                       validating start and end clinic id code.            *
.*           V5.08.B01   13/03/2000  Steve Armstrong  5.08 Mods              *
.*                       Mods for changes to OUTCLIFD (effective date)       *
.*****************************************************************************
.*           V5.07.B02   09/12/1998 Jim Stathopoulos                         *
.*                       Fixed Generated Date                                *
.*           V5.07.B01   2/11/1998  Jim Stathopoulos                         *
.*                       507 Changes                                         *
.*****************************************************************************
. *               V4.00   07.09.88  E.Phan  (I.B.A.)       *
. *                       Fixed report                     *
. *               V4.01   17/11/88 Graeme Williams         *
. *                       Was printing past selected range *
. *               V5.01   06.05.89 S.Barcham               *
. *                       Fix Overprinting                 *
. *                       SRF100435                        *
. *               V5.02   03.06.89 S.Barcham               *
. *                       Split OUTBOK & OUTMAS includes   *
. *                       SRF100751                        *
. *               V5.03   24.06.89 S.Barcham               *
. *                       Recompiled for OUTBB1FD          *
. *            V5.01.01   16/07/89 K.Peak                  *
. *                       Changed Adm & U/R to 8 characters*
. *            V5.01.02   20/02/90 J.Tan  SRF 103726       *
. *                       Fixed display code descrip.      *
. *            V5.01.03   11/07/90 Paul Duncan             *
. *                       recomp for OUTBOA & OUTBOB audits*
. *            V5.01.04   08/11/90 D.Di.Paola   SRF 107029 *
. *                       This program is a piece of shit !*
. *                       Don't people know what structure *
. *                       means. Ive seen better structure *
. *                       on the Westgate Bridge.          *
. *                       Anyway added clinic id sequence  *
. *                       if you can find it !!!!!!        *
. *                       Contract programmers where made  *
. *                       to have contracts put on them !  *
. *            V5.02.01   24/04/95  ROD   SRF 640234       *
. *                       Calm down Dude!  You just stuffed*
. *                       up the above mod. Use Clinic Id  *
. *                       desc for Clinic Id option not    *
. *                       Clinic Type desc for Clinic Id op*
. *                       Why is this box so big to write i*   
. **********************************************************
.
          INC       STD001FD
.
          INC       IBASEQFD/INC
          INC       OUTBOAFD/INC       * Clinic booking file
          INC       OUTBB1FD/INC       * Clinic booking file
          INC       OUTCTYFD/INC       * Clinic type file
          INC       OUTCLIFD/INC       * Clinic ID file
          INC       PATDO1FD/INC       * Doctor file
          INC       PATCODFD/INC       * Codes file - for visit type
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
CMDLINE   DIM       50
CMDKILL   DIM       30
CODE      DIM       2
CODECV    INIT      "CV"
HEADER1   INIT      "Clinic Type Sequence"
HEADER2   INIT      "Clinic Id Sequence  "
.
DESCTYP1  DIM       30        * store start clinic type description
DESCTYP2  DIM       30        * store end clinic type description
DESCID    DIM       30        * store clinic ID description
DIM22     DIM       22
DIM22A    DIM       22
.
ENDCTYP   DIM       6
ENDCLID   DIM       6
ENDDATE   DIM       8
.
FNGNAME   DIM       30
FNFNAME   DIM       30
FNSNAME   DIM       30
FREENEW   FORM      5
FREESPC   FORM      5
FREEREV   FORM      5
FORM5A    FORM      5
FORM5B    FORM      5
FORM5C    FORM      5
FORM5D    FORM      5
FORM5E    FORM      5
FORM5F    FORM      5
.
.
BOOKNEW   FORM      5
BOOKSPC   FORM      5
BOOKREV   FORM      5
.
OUTTMP    DIM       8          /* Filename for temp file         */
OUTROLXX  FILE      ASCII, FIXED=256
SORT1     INIT      "u1-6,7-12,13-20,21-25"     * by clinic type sequence
SORT2     INIT      "u7-12,1-6,13-20,21-25"     * by clinic id sequence
SORTKEY   DIM       21
OUTTMPXX  IFILE     SQL, FIXED=50, KEY=SORTKEY 
.
.  ----- Tempfile consists of following vars ------
.        -----------------------------------
.           Type     Length    Physical   Start
.           ----     ------    --------   -----
.OBACTYP    DIM        6          6          1
.OBACLIN    DIM        6          6          7
.OBADATE    DIM        8          8         13
.OBASTRT    DIM        5          5         21
.BOOKNEW    FORM       5          4         26
.BOOKSPC    FORM       5          4         30
.BOOKREV    FORM       5          4         34
.FREENEW    FORM       5          4         38
.FREESPC    FORM       5          4         42
.FREEREV    FORM       5          4         46
. ----- EOR 50 -----
.
.
OUT       INIT      "out"
.
SAMECLIN  FORM      1
SAVCLIN   DIM       6
SAVCTYP   DIM       6
SAVDATE   DIM       8
SAVSTRT   DIM       5
SAVINDC   DIM       1
SITE      DIM       6
STRCTYP   DIM       6
STRCLID   DIM       6
STRDATE   DIM       8
.
PRGID     INIT      "IBAOUT48"
PRGNAM    INIT      "Current Booking Report"
VERSION   INIT      "V12.01.00"
+
.         Start of program
.
          CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening";
.
          PACK      CFNAME WITH UID,FILBOKA1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTBOKA1,CFNAME
.
          PACK      CFNAME WITH UID,FILBOKA2
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTBOKA2,CFNAME
.
          PACK      CFNAME WITH UID,FILCTYA1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTCTYA1,CFNAME
.
          PACK      CFNAME WITH UID,FILCLIA1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTCLIA1,CFNAME
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,OUTTMP
.
.************************************************************************
.       Main menu
.************************************************************************
.
START     HLINE     *G37,2,51,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Clinic Type Sequence":
                    *P1:6,*V2LON,TWO,*HOFF," = Clinic Id Sequence"
.
REPEAT    KEYIN     *P1:8,*EL,"Please Select : ",*V2LON,OPTION
.
          COMPARE   ZERO,OPTION
          GOTO      ENDIT IF EQUAL
          BRANCH    OPTION TO GETCLIN,GETCLID
          BEEP
          GOTO      REPEAT
.
.*************************************************************************
.*                           CREA0000                                    *
.*                remove and create tempfile                             *
.*************************************************************************
CREA0000
          MOVE      IDDD,SITE
.
          CLEAR     CMDKILL
          APPEND    "iserase ",CMDKILL
          APPEND    OUTTMP,CMDKILL
          RESET     CMDKILL
.
          EXECUTE   CMDKILL,TASKID
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    OUTTMP,CMDLINE
          APPEND    " 50 ",CMDLINE
          APPEND    SORTKEY,CMDLINE
          RESET     CMDLINE
.
CREA9999  RETURN
.
.**********************************************************************
.      GETCLIN            
.**********************************************************************
GETCLIN  MOVE       SORT1,SORTKEY
         DISPLAY    *P51:2,*V2LON,"- ",*+,HEADER1,SP1;
         CALL      CREA0000
         DISPLAY   *P1:10,*EF,"Start Clinic Type :":
                    *P1:11,"End   Clinic Type :":
                    *P1:13,"From Date         :":
                    *P1:14,"To   Date         :"
.
.------------------------------------------------------------------------------
.         Enter Starting clinic type
.------------------------------------------------------------------------------
.
GETCL100  MOVE      SP6,STRCTYP
.
          KEYIN     *P21:11,*EL,*P21:10,*EL,*V2LON,STRCTYP
.
          ENDSET    STRCTYP
          APPEND    SP6,STRCTYP
          RESET     STRCTYP
.
          MATCH     SP6,STRCTYP
          GOTO      GETCL110 IF NOT EQUAL
.
          MOVE      SP6,ENDCTYP
          DISPLAY   *P21:10,*EL,*V2LON,"Start  "
          GOTO      GETCL200
.
GETCL110  PACK      KEY12 WITH SITE,STRCTYP
          CALL      RDCTYA1
          BRANCH    OVRCD TO GETCL120
          MOVE      OCTDESC,DESCTYP1
          GOTO      GETCL130
.
GETCL120  DISPLAY   *P30:24,*V2LON,"*** Invalid Clinic Type ***",*W2:
                    *P1:24,*EL
          GOTO      GETCL100
.
GETCL130  DISPLAY   *P21:10,*EL,*V2LON,STRCTYP,*P30:10,DESCTYP1
.
.------------------------------------------------------------------------------
.         Enter Ending clinic type
.------------------------------------------------------------------------------
.
GETCL200  MOVE      SP6,ENDCTYP
          KEYIN     *P21:11,*EL,*V2LON,ENDCTYP
.
          ENDSET    ENDCTYP
          APPEND    SP6,ENDCTYP
          RESET     ENDCTYP
.
          MATCH     SP6,ENDCTYP
          GOTO      GETCL210 IF NOT EQUAL
.
          MOVE      "ZZZZZZ",ENDCTYP
          DISPLAY   *P21:11,*EL,*V2LON,"Finish "
          GOTO      GETCL300
.
GETCL210  PACK      KEY12 WITH SITE,ENDCTYP
          CALL      RDCTYA1
          BRANCH    OVRCD TO GETCL220
          MOVE      OCTDESC,DESCTYP2
          GOTO      GETCL230
.
GETCL220  DISPLAY   *P30:24,*V2LON,"*** Invalid Clinic Type ***",*W2:
                    *P1:24,*EL
          GOTO      GETCL200
.
GETCL230  DISPLAY   *P21:11,*EL,*V2LON,ENDCTYP,*P30:11,DESCTYP2
          GOTO      GETCL300           * get date ranges
.
.**********************************************************************
.      GETCLID            
.**********************************************************************
GETCLID  MOVE       SORT2,SORTKEY
         DISPLAY    *P51:2,*V2LON,"- ",*+,HEADER2,SP1;
         CALL       CREA0000
         DISPLAY   *P1:10,*EF,"Start Clinic Id   :":
                    *P1:11,"End   Clinic Id   :":
                    *P1:13,"From Date         :":
                    *P1:14,"To   Date         :"
.
.************************************************************************
.         Enter Starting clinic Id  
.************************************************************************ 
.
GTCLD100  MOVE      SP6,STRCLID
.
          KEYIN     *P21:11,*EL,*P21:10,*EL,*V2LON,STRCLID
.
          ENDSET    STRCLID
          APPEND    SP6,STRCLID
          RESET     STRCLID
.
          MATCH     SP6,STRCLID
          GOTO      GTCLD110 IF NOT EQUAL
.
          MOVE      SP6,ENDCLID
          DISPLAY   *P21:10,*EL,*V2LON,"Start  "
          GOTO      GTCLD200
.
GTCLD110  PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          PACK      KEY20,SITE,STRCLID,KEY8
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     SITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     STRCLID,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          BRANCH    OVRCD,GTCLD120
.
          MATCH     SP6,OCADOCT
          GOTO      GTCLD115 IF NOT EQUAL
.        
          MOVE      OCADESC,DESCTYP1
          GOTO      GTCLD130
GTCLD115  MOVE      OCADOCT,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD,GTCLD125
          MOVE      DGNAME,ANS
          PACK      DESCTYP1,ANS,DOT,DSNAME
          GOTO      GTCLD130
.
GTCLD120  DISPLAY   *P1:24,*EL,*B,"Clinic Id not on file.  ";
          CALL      HITENTER
          GOTO      GTCLD100
.
GTCLD125  MOVE      "Unknown Doctor Code           ",DESCTYP1
.
GTCLD130  DISPLAY   *P21:10,*EL,*V2LON,STRCLID,*HOFF,*P30:10,DESCTYP1
.
.**********************************************************************
.         Enter Ending clinic Id  
.**********************************************************************
.
GTCLD200  MOVE      SP6,ENDCLID
          KEYIN     *P21:11,*EL,*V2LON,ENDCLID
.
          ENDSET    ENDCLID
          APPEND    SP6,ENDCLID
          RESET     ENDCLID
.
          MATCH     SP6,ENDCLID
          GOTO      GTCLD210 IF NOT EQUAL
.
          MOVE      "ZZZZZZ",ENDCLID
          DISPLAY   *P21:11,*EL,*V2LON,"Finish "
          GOTO      GETCL300
.
GTCLD210  PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          PACK      KEY20,SITE,ENDCLID,KEY8
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     SITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     ENDCLID,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          BRANCH      OVRCD,GTCLD220
.
          MATCH     SP6,OCADOCT
          GOTO      GTCLD215 IF NOT EQUAL
          MOVE      OCADESC,DESCTYP2
          GOTO      GTCLD230
.
GTCLD215  MOVE      OCADOCT,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD,GTCLD225
          MOVE      DGNAME,ANS
          PACK      DESCTYP2,ANS,DOT,DSNAME
          GOTO      GTCLD230
.
GTCLD220  DISPLAY   *P1:24,*EL,*B,"Clinic Id not on file.  ";
          CALL      HITENTER
          GOTO      GTCLD200
.
GTCLD225  MOVE      "Unknown Doctor Code           ",DESCTYP2 
.
GTCLD230  DISPLAY   *P21:11,*EL,*V2LON,ENDCLID,*HOFF,*P30:11,DESCTYP2
.
.****************************************************************************
.*        Enter Starting date                                               *
.****************************************************************************
.
GETCL300  MOVE      TWENTY1,CCOL
          MOVE      TEN3,CVERT
          MOVE      CVERT,EVERT
          UNPACK    SP6 INTO CDAY,CMON,CYEAR
          MOVE      CCC,CCENT
.
          CALL      KEYDATE
          BRANCH    OVRCD TO GETCL320
          GOTO      GETCL340
GETCL320  BRANCH    OPTION, GETCL100,GTCLD100
.
GETCL340
          PACK      STRDATE WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRDATE
.
.**************************************************************************    
.*        Enter Ending date                                               *
.**************************************************************************     
.
          MOVE      TEN4,CVERT
          MOVE      CVERT,EVERT
          UNPACK    CDATE INTO CDAY,ANS,CMON,ANS,CCENT,CYEAR
.
          CALL      KEYDATE
          BRANCH    OVRCD TO GETCL300
.
          PACK      ENDDATE WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDATE
.
          MATCH     STRDATE,ENDDATE
          GOTO      GETCL400 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,*V2LON,"*** End date must not be ":
                    "before start date *** ",*W,*P1:24,*EL;
          GOTO      GETCL300
.
GETCL400  CALL      CONT0000                  * ok to proceed
          BRANCH    EXIT,GETCL300,START
GETCL420  BRANCH    OPTION,GETCL490,GTCLD490
.
.*********************************************************************
.*                              CONT0000                             *
.*                          OK To proceed                            *
.*********************************************************************
CONT0000  MOVE      FALSE,EXIT
          MOVE      SP1,ANS
          KEYIN     *P1:24,*EL,"Ok to proceed (":
                    *V2LON,*DV,ANSY,*HOFF,*DV,SLASH:
                    *V2LON,*DV,ANSN,*HOFF,*DV,SLASH:
                    *V2LON,*DV,ANSC,*HOFF:
                    ") ? ",*V2LON,ANS;
          CMATCH    ANSY,ANS
          GOTO      CONT9999 IF EQUAL              * ok to proceed
          CMATCH    ANSN,ANS
          GOTO      CONT1000 IF EQUAL              * no, renter 
          CMATCH    ANSC,ANS
          GOTO      CONT2000 IF EQUAL              * cancel
          BEEP
          GOTO      CONT0000
CONT1000  MOVE      ONE,EXIT                       * "NO"
          GOTO      CONT9999
CONT2000  MOVE      TWO,EXIT                       * Cancel
CONT9999  RETURN
.
.------------------------------------------------------------------------------
.         Collate data into temp file in clinic type/clinic id sequence
.------------------------------------------------------------------------------
.
GETCL490  DISPLAY   *P1:18,*EF
          EXECUTE   CMDLINE,TASKID
          DISPLAY   *P56:24,"Opening ",OUTTMP;
          OPEN      OUTTMPXX,OUTTMP
          DISPLAY   *P1:18,*EF
.
          DISPLAY   *P1:24,"Clinic Type :":
                    *P20:24,"Clinic ID :":
                    *P40:24,"Scanning :";
.
.         Initialise variables
.
          MOVE      SP8,SAVDATE
          MOVE      SP6,SAVCLIN
          MOVE      SP5,SAVSTRT
          MOVE      SP6,SAVCTYP
.
          PACK      KEY12 WITH SITE,STRCTYP
          CALL      RDSCTYA1
.
NEXTCTY   CALL      RDKCTYA1
.
          PACK      KEY35 WITH SITE,STRCTYP,SP30
          CALL      RDSBOKA2
GETCL500  
          CALL      RDKBOKA2
          BRANCH    OVRCD TO GETCL700        * No more records - start print
.
          MATCH     OBASITE,SITE
          GOTO      GETCL700 IF NOT EQUAL    * Different site - start print
.
          MATCH     OBACTYP,ENDCTYP
          GOTO      GETCL700 IF LESS         * Past end of range - print
.
          MATCH     OBADATE,ENDDATE
          GOTO      GETCL500 IF LESS         * Past end date - ignore
.
          MATCH     STRDATE,OBADATE
          GOTO      GETCL500 IF LESS         * Before start date - ignore
.
          ADD       ONE,OBASTAT
          BRANCH    OBASTAT TO GETCL505,GETCL505 * Only not booked & booked
          GOTO      GETCL500
.
GETCL505  SUB       ONE,OBASTAT
.
          COMPARE   ZERO,OBAEXSL
          GOTO      GETCL500 IF NOT EQUAL    * Not parent slot - ignore
.
          MATCH     SP3,OBAVISIT
          GOTO      GETCL500 IF EQUAL        * No visit type - ignore record
.
          PACK      KEY5 WITH CODECV,OBAVISIT
          CALL      RDCODE1
          BRANCH    OVRCD TO GETCL500        * No visit type - ignore record
.
          MATCH     OBACLIN,SAVCLIN
          GOTO      GETCL507 IF EQUAL
.
          CALL      GETCL550                 * Write data for old clinic
          GOTO      GETCL509
.
GETCL507  MATCH     OBADATE,SAVDATE
          GOTO      GETCL509 IF EQUAL
.
          CALL      GETCL550                 * Write data for old clinic
.
GETCL509  UNPACK    OBADATE INTO CCENT,CYEAR,CMON,CDAY
.
          DISPLAY   *P14:24,*V2LON,OBACTYP:
                    *P34:24,OBACLIN:
                    *P55:24,OBADATE,SP2,OBATIME;
.
.------------------------------------------------------------------------------
.         Get visit type
.------------------------------------------------------------------------------
.
GETCL510  MOVE      TCINDC1,SAVINDC
.
          CMATCH    ANSN,SAVINDC
          GOTO      GETCL520 IF EQUAL
.
          CMATCH    ANSS,SAVINDC
          GOTO      GETCL530 IF EQUAL
.
          CMATCH    ANSR,SAVINDC
          GOTO      GETCL540 IF EQUAL
          CMATCH    ANSO,SAVINDC
          GOTO      GETCL540 IF EQUAL
.
          GOTO      GETCL500              * No indicator, ignore record
.
.------------------------------------------------------------------------------
.         New visit
.------------------------------------------------------------------------------
.
GETCL520  BRANCH    OBASTAT TO GETCL527
          ADD       ONE,FREENEW
          GOTO      GETCL500
.
GETCL527  ADD       ONE,BOOKNEW
          GOTO      GETCL500
.
.------------------------------------------------------------------------------
.         Special visit
.------------------------------------------------------------------------------
.
GETCL530  BRANCH    OBASTAT TO GETCL537
          ADD       ONE,FREESPC
          GOTO      GETCL500
.
GETCL537  ADD       ONE,BOOKSPC
          GOTO      GETCL500
.
.------------------------------------------------------------------------------
.         Revisit/Other visit
.------------------------------------------------------------------------------
.
GETCL540  BRANCH    OBASTAT TO GETCL547
          ADD       ONE,FREEREV
          GOTO      GETCL500
.
GETCL547  ADD       ONE,BOOKREV
          GOTO      GETCL500
.
.------------------------------------------------------------------------------
.         Subroutine to write data away to temp file
.------------------------------------------------------------------------------
.
GETCL550  MATCH     SP6,SAVCLIN
          GOTO      GETCL570 IF EQUAL
.
          PACK      KEY25 WITH SAVCTYP,SAVCLIN,SAVDATE,SAVSTRT
          READ      OUTTMPXX,KEY25;*26,FORM5A,FORM5B,FORM5C:
                    FORM5D,FORM5E,FORM5F
          GOTO      GETCL560 IF OVER
.
.         This shouldn't occur, but if it does, add old
.         figures to currently calculated ones.
.
          ADD       FORM5A,BOOKNEW
          ADD       FORM5B,BOOKSPC
          ADD       FORM5C,BOOKREV
          ADD       FORM5D,FREENEW
          ADD       FORM5E,FREESPC
          ADD       FORM5F,FREEREV
          UPDATE    OUTTMPXX;*26,BOOKNEW,BOOKSPC,BOOKREV:
                    FREENEW,FREESPC,FREEREV
          GOTO      GETCL570
.
GETCL560  WRITE     OUTTMPXX,KEY25;SAVCTYP,SAVCLIN,SAVDATE,SAVSTRT:
                                   BOOKNEW,BOOKSPC,BOOKREV:
                                   FREENEW,FREESPC,FREEREV
.
GETCL570  MOVE      OBACLIN,SAVCLIN
          MOVE      OBACTYP,SAVCTYP
          MOVE      OBADATE,SAVDATE
          MOVE      OBASTRT,SAVSTRT
.
          MOVE      ZERO,BOOKNEW
          MOVE      ZERO,BOOKSPC
          MOVE      ZERO,BOOKREV
          MOVE      ZERO,FREENEW
          MOVE      ZERO,FREESPC
          MOVE      ZERO,FREEREV
.
          RETURN
.
.
.*****************************************************************************
.         Collate data into temp file in clinic type/clinic id sequence
.         Extraction for option = 2 - Clinic id Sequence
.***************************************************************************** 
.
GTCLD490  DISPLAY   *P1:18,*EF
          EXECUTE   CMDLINE,TASKID
          DISPLAY   *P56:24,"Opening ",OUTTMP;
          OPEN      OUTTMPXX,OUTTMP
          DISPLAY   *P1:18,*EF
.
          DISPLAY   *P1:24,"Clinic Type :":
                    *P20:24,"Clinic ID :":
                    *P40:24,"Scanning :";
.
.         Initialise variables
.
          MOVE      SP8,SAVDATE
          MOVE      SP6,SAVCLIN
          MOVE      SP5,SAVSTRT
          MOVE      SP6,SAVCTYP
.
.
          PACK      KEY20,SITE,STRCLID,SP20
          CALL      RDSCLIA1
          CALL      RDKCLIA1
.
          PACK      KEY28,SITE,STRCLID,SP30
          CALL      RDSBOKA1
GTCLD500  CALL      RDKBOKA1
          BRANCH    OVRCD TO GETCL700        * No more records - start print
.
          MATCH     OBASITE,SITE
          GOTO      GETCL700 IF NOT EQUAL    * Different site - start print
.
          MATCH     OBACLIN,ENDCLID
          GOTO      GETCL700 IF LESS         * Past end of range - print
.
          MATCH     OBADATE,ENDDATE
          GOTO      GTCLD500 IF LESS         * Past end date - ignore
.
          MATCH     STRDATE,OBADATE
          GOTO      GTCLD500 IF LESS         * Before start date - ignore
.
          ADD       ONE,OBASTAT
          BRANCH    OBASTAT,GTCLD505,GTCLD505 * Only not booked & booked
          GOTO      GTCLD500
.
GTCLD505  SUB       ONE,OBASTAT
.
          COMPARE   ZERO,OBAEXSL
          GOTO      GTCLD500 IF NOT EQUAL    * Not parent slot - ignore
.
          MATCH     SP3,OBAVISIT
          GOTO      GTCLD500 IF EQUAL        * No visit type - ignore record
.
          PACK      KEY5 WITH CODECV,OBAVISIT
          CALL      RDCODE1
          BRANCH    OVRCD,GTCLD500        * No visit type - ignore record
.
.         MATCH     OBACLIN,SAVCLIN
.         GOTO      GTCLD507 IF EQUAL
          MATCH     OBACTYP,SAVCTYP
          GOTO      GTCLD507 IF EQUAL
.
          CALL      GTCLD550                 * Write data for old clinic
          GOTO      GTCLD509
.
GTCLD507  MATCH     OBADATE,SAVDATE
          GOTO      GTCLD508 IF EQUAL
.
          CALL      GTCLD550                 * Write data for old clinic
GTCLD508  MATCH     OBASTRT,SAVSTRT
          GOTO      GTCLD509 IF EQUAL
          CALL      GTCLD550 
.
GTCLD509  UNPACK    OBADATE INTO CCENT,CYEAR,CMON,CDAY
.
          DISPLAY   *P14:24,*V2LON,OBACTYP:
                    *P34:24,OBACLIN:
                    *P55:24,OBADATE,SP2,OBATIME;
.
.------------------------------------------------------------------------------
.         Get visit type
.------------------------------------------------------------------------------
.
GTCLD510  MOVE      TCINDC1,SAVINDC
.
          CMATCH    ANSN,SAVINDC
          GOTO      GTCLD520 IF EQUAL
.
          CMATCH    ANSS,SAVINDC
          GOTO      GTCLD530 IF EQUAL
.
          CMATCH    ANSR,SAVINDC
          GOTO      GTCLD540 IF EQUAL
          CMATCH    ANSO,SAVINDC
          GOTO      GTCLD540 IF EQUAL
.
          GOTO      GTCLD500              * No indicator, ignore record
.
.------------------------------------------------------------------------------
.         New visit
.------------------------------------------------------------------------------
.
GTCLD520  BRANCH    OBASTAT TO GTCLD527
          ADD       ONE,FREENEW
          GOTO      GTCLD500
. 
GTCLD527  ADD       ONE,BOOKNEW
          GOTO      GTCLD500
.
.------------------------------------------------------------------------------
.         Special visit
.------------------------------------------------------------------------------
.
GTCLD530  BRANCH    OBASTAT TO GTCLD537
          ADD       ONE,FREESPC
          GOTO      GTCLD500
.
GTCLD537  ADD       ONE,BOOKSPC
          GOTO      GTCLD500
.
.------------------------------------------------------------------------------
.         Revisit/Other visit
.------------------------------------------------------------------------------
.
GTCLD540  BRANCH    OBASTAT TO GTCLD547
          ADD       ONE,FREEREV
          GOTO      GTCLD500
.
GTCLD547  ADD       ONE,BOOKREV
          GOTO      GTCLD500
.
.------------------------------------------------------------------------------
.         Subroutine to write data away to temp file
.------------------------------------------------------------------------------
.
GTCLD550  MATCH     SP6,SAVCTYP 
          GOTO      GTCLD570 IF EQUAL
.
          PACK      KEY25 WITH SAVCLIN,SAVCTYP,SAVDATE,SAVSTRT
          READ      OUTTMPXX,KEY25;*26,FORM5A,FORM5B,FORM5C:
                    FORM5D,FORM5E,FORM5F
          GOTO      GTCLD560 IF OVER
.
.         This shouldn't occur, but if it does, add old
.         figures to currently calculated ones.
.
          ADD       FORM5A,BOOKNEW
          ADD       FORM5B,BOOKSPC
          ADD       FORM5C,BOOKREV
          ADD       FORM5D,FREENEW
          ADD       FORM5E,FREESPC
          ADD       FORM5F,FREEREV
          UPDATE    OUTTMPXX;*26,BOOKNEW,BOOKSPC,BOOKREV:
                    FREENEW,FREESPC,FREEREV
          GOTO      GTCLD570
.
GTCLD560  
          WRITE     OUTTMPXX,KEY25;SAVCTYP,SAVCLIN,SAVDATE,SAVSTRT:
                                         BOOKNEW,BOOKSPC,BOOKREV:
                                         FREENEW,FREESPC,FREEREV
.
GTCLD570  MOVE      OBACLIN,SAVCLIN
          MOVE      OBACTYP,SAVCTYP
          MOVE      OBADATE,SAVDATE
          MOVE      OBASTRT,SAVSTRT
.
          MOVE      ZERO,BOOKNEW
          MOVE      ZERO,BOOKSPC
          MOVE      ZERO,BOOKREV
          MOVE      ZERO,FREENEW
          MOVE      ZERO,FREESPC
          MOVE      ZERO,FREEREV
.
          RETURN
.
.------------------------------------------------------------------------------
.         Read temp file & print
.------------------------------------------------------------------------------
.
GETCL700  MATCH     SP6,SAVCLIN        * Check if there was any data found
          GOTO      GETCL990 IF EQUAL
.
          BRANCH    OPTION,GETCL720,GETCL730 *"1" = Clinic type "2" = Clinic id
GETCL720  CALL      GETCL550           * Write data for old record, option = 1
          GOTO      GETCL750
GETCL730  CALL      GTCLD550           * write data for old record, option = 2
.
GETCL750  MOVE      ZERO,CPAGENO
          MOVE      SP6,SAVCTYP
          MOVE      SP6,SAVCLIN
.
          DISPLAY   *P1:24,*EL,*P40:24,*V2LON,"*** Generating Report ***",*HOFF:
                    *P1:24,*EL,"Clinic Type :":
                    *P30:24,"Clinic ID :":
                    *P50:24,"Date : ";
.
          CALL      PRHEAD
.
          PACK      KEY25 WITH SP30
          READ      OUTTMPXX,KEY25;;
.
GETCL800  
          BRANCH    OPTION,GETCL820,GETCL830
GETCL820  READKS    OUTTMPXX;OBACTYP,OBACLIN,OBADATE,OBASTRT:
                    BOOKNEW,BOOKSPC,BOOKREV:
                    FREENEW,FREESPC,FREEREV
          GOTO      GETCL900 IF OVER
          GOTO      GETCL850
GETCL830  READKS    OUTTMPXX;OBACTYP,OBACLIN,OBADATE,OBASTRT:
                    BOOKNEW,BOOKSPC,BOOKREV:
                    FREENEW,FREESPC,FREEREV
          GOTO      GETCL900 IF OVER
GETCL850
          UNPACK    OBADATE INTO CCENT,CYEAR,CMON,CDAY
.
          DISPLAY   *P15:24,*V2LON,OBACTYP:
                    *P44:24,OBACLIN:
                    *P56:24,OBADATE,SP2,OBASTRT;
.
          CALL      PRLINE
          GOTO      GETCL800
.
GETCL900  PRINT     *1,"|",*31,"|",*61,"|",*74,"|",*82,"|":
                    *90,"|",*98,"|",*107,"|",*115,"|",*123,"|",*132,"|"
          CALL      PAGEND
          PRINT     *N,*N,*5,"*** END OF REPORT *** "
.
          DISPLAY   *P1:24,*EL:
                    *P40:24,*V2LON,"*** Generation Completed *** ",*W;
.
          PREP      OUTROLXX,OUTTMP        /* Delete old temp file */
          CLOSE     OUTROLXX,DELETE
          GOTO      START
.
GETCL990  DISPLAY   *P1:24,*EL,*P40:24,*V2LON,"*** No Data Found ***",*W2;
.
          EXECUTE   CMDKILL,TASKID
          GOTO      START
.
.         PRINTING SUBROUTINES
.------------------------------------------------------------------------------
.         Page Header
.------------------------------------------------------------------------------
.
PRHEAD    BRANCH    OPTION,PRHEAD1A
          CALL      PRCLHD                * option 2 = Clinic id sequence
          RETURN
PRHEAD1A
....      ADD       ONE,CPAGENO
          CLOCK     TIME,CTIMEIS
.
          PACK      KEY12 WITH SITE,OBACTYP
          CALL      RDCTYA1
.
          CALL      HEAD132
          PRINT     *40,"- ",HEADER1," -",*N,*N;
.
          MATCH     SP6,STRCTYP
          GOTO      PRHEAD1 IF NOT EQUAL
          PRINT     *20,"START CLINIC : START";
          GOTO      PRHEAD2
PRHEAD1   PRINT     *20,"START CLINIC : ",STRCTYP,SP2,DESCTYP1;
.
PRHEAD2   UNPACK    STRDATE INTO CCENT,CYEAR,CMON,CDAY
          PRINT     *70,"FROM DATE : ",CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.
          MATCH     "ZZZZZZ",ENDCTYP
          GOTO      PRHEAD3 IF NOT EQUAL
          PRINT     *20,"END   CLINIC : FINISH";
          GOTO      PRHEAD4
PRHEAD3   PRINT     *20,"END   CLINIC : ",ENDCTYP,SP2,DESCTYP2;
.
PRHEAD4   UNPACK    ENDDATE INTO CCENT,CYEAR,CMON,CDAY
          PRINT     *70,"TO   DATE : ",CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.
          CALL      PAGEND
          PRINT     *1,"|",*2,"CLINIC",*31,"|",*33,"CLINIC",*61,"|":
                    *74,"|",*76,"START",*82,"|",*91,"BOOKED",*107,"|":
                    *117,"FREE",*132,"|":
                    *N,"|",*2,"TYPE",*9,"DESCRIPTION":
                    *31,"|",*32,"ID",*39,"DESCRIPTION":
                    *61,"|",*66,"DATE",*74,"|",*76,"TIME":
                    *82,"|",*85,"NEW",*91,"SPECIAL",*99,"REVISIT":
                    *107,"|",*110,"NEW",*116,"SPECIAL",*124,"REVISIT",*132,"|"
          CALL      PAGEND
.
          MOVE      TEN1,CLNO
.
          RETURN
.
.------------------------------------------------------------------------------
.         Page Header
.------------------------------------------------------------------------------
.
PRCLHD    
.....ADD       ONE,CPAGENO
          CLOCK     TIME,CTIMEIS
.
          PACK      KEY20,SITE,OBACLIN,OBADATE
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     SITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     OBACLIN,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          IF        OVRCD = 1
            MOVE      "Unknown clinic",OCADESC
            PACK      OCADESC,OCADESC,SP30
          ENDIF
.
          CALL      HEAD132
          PRINT     *40,"- ",HEADER2," -",*N,*N;
.
          MATCH     SP6,STRCLID
          GOTO      PRCLHD1 IF NOT EQUAL
          PRINT     *20,"START CLINIC ID : START";
          GOTO      PRCLHD2
PRCLHD1   PRINT     *20,"START CLINIC ID : ",STRCLID,SP2,DESCTYP1;
.
PRCLHD2   UNPACK    STRDATE,CCENT,CYEAR,CMON,CDAY
          PRINT     *70,"FROM DATE : ",CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.
          MATCH     "ZZZZZZ",ENDCLID
          GOTO      PRCLHD3 IF NOT EQUAL
          PRINT     *20,"END   CLINIC ID : FINISH";
          GOTO      PRCLHD4
PRCLHD3   PRINT     *20,"END   CLINIC ID : ",ENDCLID,SP2,DESCTYP2;
.
PRCLHD4   UNPACK    ENDDATE,CCENT,CYEAR,CMON,CDAY
          PRINT     *70,"TO   DATE : ",CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.
          CALL      PAGEND
          PRINT     *1,"|",*2,"CLINIC",*31,"|",*33,"CLINIC",*61,"|":
                    *74,"|",*76,"START",*82,"|",*91,"BOOKED",*107,"|":
                    *117,"FREE",*132,"|":
                    *N,"|",*2,"TYPE",*9,"DESCRIPTION":
                    *31,"|",*32,"ID",*39,"DESCRIPTION":
                    *61,"|",*66,"DATE",*74,"|",*76,"TIME":
                    *82,"|",*85,"NEW",*91,"SPECIAL",*99,"REVISIT":
                    *107,"|",*110,"NEW",*116,"SPECIAL",*124,"REVISIT",*132,"|"
          CALL      PAGEND
.
          MOVE      TEN1,CLNO
.
          RETURN
.
.------------------------------------------------------------------------------
.         Print a line
.------------------------------------------------------------------------------
.
PRLINE    COMPARE   "60",CLNO
          GOTO      PRLIN100 IF LESS
.
          CALL      PAGEND
          CALL      PRHEAD
.
PRLIN100  PACK      KEY12 WITH SITE,OBACTYP
          CALL      RDCTYA1
          MOVE      OCTDESC,DESCTYP1
.
          PACK      KEY20,SITE,OBACLIN,OBADATE
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     SITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     OBACLIN,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          IF        OVRCD = 1
            MOVE      "Unknown clinic",OCADESC
            PACK      OCADESC,OCADESC,SP30
            GOTO      PRLIN110
          ENDIF
.
          MATCH     SP6,OCADOCT
          GOTO      PRLIN110 IF EQUAL
.
          PACK      KEY6 WITH OCADOCT
          CALL      RDDOCT1
          MOVE      DSNAME,FNSNAME
          MOVE      DGNAME,FNGNAME
          CALL      FORMNAME
          MOVE      FNFNAME,DESCTYP2
          GOTO      PRLIN120
.
PRLIN110  MOVE      OCADESC,DESCTYP2
.
PRLIN120  UNPACK    OBADATE INTO CCENT,CYEAR,CMON,CDAY
.
          MOVE      DESCTYP1 TO DIM22
          MOVE      DESCTYP2 TO DIM22A
.
          MATCH     OBACTYP,SAVCTYP
          GOTO      PRLIN200 IF EQUAL
.
          PRINT     *1,"|",*31,"|",*61,"|",*74,"|",*82,"|":
                    *90,"|",*98,"|",*107,"|",*115,"|",*123,"|",*132,"|":
                    *N,"|",*2,OBACTYP,*9,DIM22:
                    *31,"|",*32,OBACLIN,*39,DIM22A:
                    *61,"|",*63,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR:
                    *74,"|",*76,OBASTRT,*82,"|",*84,BOOKNEW:
                    *90,"|",*92,BOOKSPC,*98,"|",*100,BOOKREV:
                    *107,"|",*109,FREENEW,*115,"|",*117,FREESPC:
                    *123,"|",*125,FREEREV,*132,"|"
          ADD       TWO,CLNO
          MOVE      OBACTYP,SAVCTYP
          MOVE      OBACLIN,SAVCLIN
          RETURN
.
PRLIN200  MATCH     OBACLIN,SAVCLIN
          GOTO      PRLIN300 IF EQUAL
.
          PRINT     *1,"|",*31,"|",*61,"|",*74,"|",*82,"|":
                    *90,"|",*98,"|",*107,"|",*115,"|",*123,"|",*132,"|":
                    *N,"|",*31,"|",*32,OBACLIN,*39,DIM22A:
                    *61,"|",*63,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR:
                    *74,"|",*76,OBASTRT,*82,"|",*84,BOOKNEW:
                    *90,"|",*92,BOOKSPC,*98,"|",*100,BOOKREV:
                    *107,"|",*109,FREENEW,*115,"|",*117,FREESPC:
                    *123,"|",*125,FREEREV,*132,"|"
          ADD       TWO,CLNO
          MOVE      OBACTYP,SAVCTYP
          MOVE      OBACLIN,SAVCLIN
          RETURN
.
PRLIN300  PRINT     *1,"|",*31,"|":
                    *61,"|",*63,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR:
                    *74,"|",*76,OBASTRT,*82,"|",*84,BOOKNEW:
                    *90,"|",*92,BOOKSPC,*98,"|",*100,BOOKREV:
                    *107,"|",*109,FREENEW,*115,"|",*117,FREESPC:
                    *123,"|",*125,FREEREV,*132,"|"
          ADD       ONE,CLNO
          MOVE      OBACTYP,SAVCTYP
          MOVE      OBACLIN,SAVCLIN
          RETURN
.
.------------------------------------------------------------------------------
.         Page Border
.------------------------------------------------------------------------------
.
PAGEND    PRINT     "*---------------------------------------------------":
                    "--------------------------------------------------------":
                    "-----------------------*"
          ADD       ONE,CLNO
          RETURN
.
.
.******************************************************************************
.
.         Format a name string in the form SURNAME, INITIAL1. INITIAL2. etc
.         Requires : IBACOMM.PBL
.                    Surname in FNSNAME
.                    Given name in FNGNAME
.         Output   : Formatted name in FNFNAME
.
.------------------------------------------------------------------------------
.
FORMNAME  MOVE      SP30,FNFNAME         * Clear destination string
          RESET     FNSNAME              * Reset all strings
          RESET     FNGNAME
          RESET     FNFNAME
          MOVE      FNSNAME,FNFNAME      * Surname 1st part of string
          RESET     FNFNAME,30           * Set form pointer to the end
FNSLOOP   MATCH     SP70,FNFNAME
          GOTO      FNSEND IF EQUAL
          CMATCH    SP1,FNFNAME          * Check if space
          GOTO      FNSEND IF NOT EQUAL  * No, reached last char of surname
          BUMP      FNFNAME,-1           * Yes, bump form pointer back
          GOTO      FNSLOOP              * Repeat test
.
FNSEND    APPEND    COMMA,FNFNAME        * Stick "," after surname
          MOVE      ZERO,OVRCD
          RESET     FNGNAME,0            * Set given name FP to before 1st char
FNGLOOP   BUMP      FNGNAME              * Bump FP
          GOTO      FNEXIT IF EOS        * If end of string then exit
          MOVE      FNGNAME,ANS          * Isolate current character
          CMATCH    SP1,ANS              * Space ?
          GOTO      FNGUNSET IF EQUAL    * Yes, unset flag
          BRANCH    OVRCD TO FNGLOOP     * No, but flag set, so keep on reading
          MOVE      ONE,OVRCD            * Flag not set, so set flag and ...
          APPEND    SP1,FNFNAME          * Space between initials
          APPEND    ANS,FNFNAME          * Add initial to string
          APPEND    DOT,FNFNAME          * Dot after initial
          GOTO      FNGLOOP              * Read next character
FNGUNSET  MOVE      ZERO,OVRCD           * Unset flag
          GOTO      FNGLOOP              * Read next character
.
FNEXIT    RESET     FNFNAME              * The end ...
          MOVE      ZERO,OVRCD           * Clean up ...
          RETURN
.
.
+
.******************************************************************************
.
.         THE END IS NIGH ...
.
.------------------------------------------------------------------------------
.
ENDIT     EXECUTE   CMDKILL,TASKID
.
          CHAIN     PGM
          STOP
.
.
          INC       IBASEQIO/INC
          INC       PATCODIO/INC      * codes file IO
          INC       PATDO1IO/INC      * doctor file IO
          INC       OUTBOAIO/INC      * clinic booking file IO
          INC       OUTCTYIO/INC      * clinic type file IO
          INC       OUTCLIIO/INC      * clinic ID file IO
          INC       WEBERRIO/INC
.
          INC       STD001IO
          INC       TFILENAM
.

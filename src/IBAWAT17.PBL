.******************************************************************************
.* System         : Waiting Lists                Release 5.0n                 *
.* Program        : IBAWAT17.PBL                                              *
.* Name           : Consultant/Procedure Time Maintenance                     *
.******************************************************************************
.* Date           : 27/10/1995                                                *
.* Author         : Justin Coates                                             *
.* Function       : To maintain the Consultant/Procedure Time file            *
.* Modifications  :                                                           *
.*        V5.07.01  27/10/1999 J Habasque                                     *
.*                  Recompiled for changes to WATPROFD - Active/Inactive codes*
.******************************************************************************
.
. ------- FD includes needed -------
.
          INC       STD001FD                * standard variables
          INC       PATCODFD/INC
          INC       PATDO1FD/INC            * doctor file
          INC       WATPROFD/INC            * procedure file
          INC       WATCPTFD/INC            * consultant/procedure time file
          INC       PATCONFD/INC   
.
. ------- program variables -------
.
AUDIFLAG  FORM      1         * type of audit
CODEDOCT  DIM       6         * doctor code
CODEPROC  DIM       9         * procedure code
CODESTAY  FORM      3         * expected length of stay
CODETIME  FORM      5         * expected procedure time 
COL       FORM      3
DESCDOCT  DIM       50        * doctor desc 
DESCPROC  DIM       50        * procedure desc 
DIM3      DIM       3
DISPACT   FORM      1
DOCCODE   DIM       6
HWAUDC    FORM      1
VERT      FORM      2
WTCNTPDS  DIM       9
.
. ------- program constants -------
.
KEYCOL    FORM      "31"
.
PRGID     INIT      "IBAWAT17"
PRGNAM    INIT      "Consultant/Procedure Times Maintenance"
VERSION   INIT      "V12.01.00"
+
.*********************************************************************
.*                  ML0000                                           *
.*        Mainline Code / Controlling Logic                          *
.*********************************************************************
ML0000    CALL      INIT0000                * initialization and open files
.
ML1000    CALL      MENU0000                * select options
          BRANCH    EXIT,ML9999             * EXIT = 1 if 0 chosen in menu
          PERFORM   MOPTION,MODI0000,DELI0000
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.*********************************************************************
.*                  INIT0000                    Called by: ML0000    *
.*        Initialization and open files                              *
.*********************************************************************
INIT0000  CALL      DISPHEAD                * display heading
          MOVE      ONE,CNEWDS              * new ? option
          DISPLAY   *P50:24,"Opening ",*EL;
          DISPLAY   *P58:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
          DISPLAY   *P58:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
          DISPLAY   *P58:24,"watproaf"
          OPEN      WATPROA1,"watproaf"
          OPEN      WATPROA3,"watproaf"
          DISPLAY   *P58:24,"watcptaf"
          OPEN      WATCPTA1,"watcptaf"
          DISPLAY   *P58:24,"controlf"
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,THIRTY7;*185,WTCNTPDS
.
INIT9999  RETURN
+
.*********************************************************************
.*                  MENU0000                    Called by: ML0000    *
.*        Enter the option to perform                                *
.*        Returns : EXIT = 1      Exit selected                      *
.*                  MOPTION = 1   Input/Modify                       *
.*                            2   Delete selected                    *
.*********************************************************************
MENU0000  DISPLAY   *P1:3,*EF,*V2LON:
                    *P1:4,"0",*P1:5,"1",*P1:6,"2",*HOFF:
                    *P2:4," = Exit":
                    *P2:5," = Input/Modify":
                    *P2:6," = Delete":
                    *P1:8,"Select option : "
.
MENU1000  KEYIN     *P17:8,*V2LON,MOPTION
.
          MOVE      ZERO,EXIT
          BRANCH    MOPTION,MENU9999,MENU9999
.
          MOVE      ONE,EXIT
          COMPARE   ZERO,MOPTION
          GOTO      MENU9999 IF EQUAL
.
          BEEP
          GOTO      MENU1000
.
MENU9999  RETURN
+
.*********************************************************************
.*                  MODI0000                    Called by : ML2000   *
.*        Change an item code                                        *
.*********************************************************************
MODI0000  CALL      CLRK0000                * clear variables - key
          CALL      CLRV0000                * clear variables
          CALL      SCRA0000                * display the screen
.
          CALL      DOCT0000
          BRANCH    EXIT,MODI9999           * exit
.
MODI0500  CALL      PROC0000
          BRANCH    EXIT,MODI0000           * nothing entered
.
          PACK      KEY15,CODEDOCT,CODEPROC
          CALL      RDWTCPT1
          IF        OVRCD = ZERO
            CALL      DATB0000
            GOTO      MODI5000
          ENDIF
.
          CALL      STAY0000
          CALL      TIME0000
.
MODI5000  CALL      MAINQST
          COMPARE   ZERO,CCITEM
          GOTO      MODI8000 IF EQUAL
          GOTO      MODI0000 IF LESS
.
          BRANCH    CCITEM,MODI5100,MODI5200
          BEEP
          GOTO      MODI5000
.
MODI5100  CALL      STAY0000
          GOTO      MODI5000
MODI5200  CALL      TIME0000
          GOTO      MODI5000
.
.         Post selected
.
MODI8000  CALL      UPDT0000                * update the record
          CALL      CLRV0000                * clear variables
          GOTO      MODI0500
.
MODI9999  RETURN
+
.*********************************************************************
.*                  DELI0000                    Called by : ML3000   *
.*        Delete an item code                                        *
.*********************************************************************
DELI0000  CALL      CLRV0000                * clear variables
          CALL      SCRA0000                * display the screen
.
          CALL      DOCT0000
          BRANCH    EXIT,DELI9999           * exit
.
DELI0500  CALL      PROC0000
          BRANCH    EXIT,DELI0000           * nothing entered
.
          PACK      KEY15,CODEDOCT,CODEPROC
          CALL      RDWTCPT1
          COMPARE   ZERO,OVRCD
          GOTO      DELI4000 IF EQUAL
.
          DISPLAY   *P1:24,*B,"Combination does not exist.  ",*EL;
          CALL      HITENTER
          GOTO      DELI0500
.
DELI4000  CALL      DATB0000
          CALL      DELQST
          BRANCH    CEXIT,DELI8000,DELI0500,DELI0000
.
DELI8000  CALL      DELT0000                * delete a record
          GOTO      DELI0500
.
DELI9999  RETURN
+
.*********************************************************************
.*        Clear all the input variables
.*********************************************************************
CLRK0000  UNPACK    SP30,CODEDOCT,CODEPROC
          PACK      DESCDOCT,SP30,SP30
          PACK      DESCPROC,SP30,SP30
          CALL      CLRV0000
CLRK9999  RETURN
CLRV0000  MOVE      ZERO,CODESTAY
          MOVE      ZERO,CODETIME
CLRV9999  RETURN
+
.*********************************************************************
.*                  SCRA0000                    Called by : ADDI0000 *
.*        Display the screen for the key variables                   *
.*********************************************************************
SCRA0000  DISPLAY   *P1:3,*EF:
                    *P5:4,"Consultant              : ":
                    *P5:5,"Procedure               : ":
                    *P2:7,*V2LON,"1",*P2:8,"2",*HOFF:
                    *P3:7,". Expected Length of Stay :       (days) ":
                    *P3:8,". Expected Procedure Time :       (mins) "
.
SCRB9999  RETURN
+
.*********************************************************************
.         display the data for change/delete mode
.*********************************************************************
DATB0000  CALL      FIWK0000
          DISPLAY   *PKEYCOL:7,*V2LON,CODESTAY,*PKEYCOL:8,CODETIME
DATB9999  RETURN
+
.*********************************************************************
.*        Copy FIle variables to WorK variables
.*********************************************************************
FIWK0000  MOVE      WTCPDOCT,CODEDOCT
          MOVE      WTCPPROC,CODEPROC
          MOVE      WTCPSTAY,CODESTAY
          MOVE      WTCPTIME,CODETIME
.
FIWK9999  RETURN
+
.*********************************************************************
.*        Copy WorK variables to FIle variables
.*********************************************************************
WKFI0000  MOVE      CODEDOCT,WTCPDOCT
          MOVE      CODEPROC,WTCPPROC
          MOVE      CODESTAY,WTCPSTAY
          MOVE      CODETIME,WTCPTIME
WKFI9999  RETURN
+
.*********************************************************************
.*                  UPDT0000                    Called by : MODI0000 *
.*        Change an existing record                                  *
.*********************************************************************
UPDT0000  DISPLAY   *P50:24,*V2LON,"*** UPDATING ***"
          PACK      KEY15,CODEDOCT,CODEPROC
          CALL      RDWTCPT1
          BRANCH    OVRCD,UPDT5000          * write
          
          MOVE      TWO,AUDIFLAG
          CALL      WATAUDCP
          CALL      WKFI0000                * set up file variables
          CALL      UPWTCPT1                * update record
          MOVE      THREE,AUDIFLAG
          CALL      WATAUDCP
          GOTO      UPDT9000
.
UPDT5000  CALL      WKFI0000                * set up file variables
          CALL      WRWTCPT1                * write record
          MOVE      ONE,AUDIFLAG
          CALL      WATAUDCP
.
UPDT9000  DISPLAY   *P1:24,*EL;
.
UPDT9999  RETURN
+
.*********************************************************************
.*                  DELT0000                    Called by : DELI0000 *
.*        Delete a record                                            *
.*********************************************************************
DELT0000  DISPLAY   *P50:24,*V2LON,"*** DELETING ***"
          PACK      KEY15,CODEDOCT,CODEPROC
          CALL      DEWTCPT1                * delete the item
          MOVE      FOUR,AUDIFLAG
          CALL      WATAUDCP
          DISPLAY   *P1:24,*EL;
.
DELT9999  RETURN
+
.*********************************************************************
.         enter the consultant code
.*********************************************************************
DOCT0000  MOVE      KEYCOL,ECOL
          MOVE      FOUR,EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
.
          CALL      PATDOCKY
          BRANCH    EXIT,DOCT9100,DOCT9100
.
          MOVE      DCODE,CODEDOCT
          MOVE      DSNAME,PACSNAME
          MOVE      DGNAME,PACGNAME
          MOVE      SP4,PACTITLE
          MOVE      TWO,PACFRMT 
          CALL      PACNAME
          MOVE      PACFNAME,DESCDOCT
          DISPLAY   *PECOL:EVERT,DCODE,SP5,DESCDOCT
          MOVE      ZERO,EXIT
          GOTO      DOCT9999
.
DOCT9100  MOVE      ONE,EXIT
DOCT9999  RETURN
+
.*********************************************************************
.         enter the procedure code
.*********************************************************************
PROC0000  MOVE      KEYCOL,ECOL
          MOVE      FIVE,EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP9,CKYIDEF9
          DISPLAY   *PKEYCOL:EVERT,*EL,*PKEYCOL:7,SP5,*PKEYCOL:8,SP5;
.
          CALL      WATPROKY
          BRANCH    EXIT,PROC9100,PROC9100,PROC9100
.
          COMPARE   ONE,WTWPACTV
          IF        @EQUAL
            DISPLAY   *P1:24,*EL,*V2LON,"Inactive Code. ";
            CALL      HITENTER
            GOTO      PROC0000
          ENDIF
          MOVE      WPCODE,CODEPROC
          MOVE      WPDESC,DESCPROC
          DISPLAY   *P45:EVERT,DESCPROC
          MOVE      ZERO,EXIT
          GOTO      PROC9999
.
PROC9100  MOVE      ONE,EXIT
          MOVE      SP9,CODEPROC
PROC9999  RETURN
+
.*********************************************************************
.         enter the expected length of stay
.*********************************************************************
STAY0000  KEYIN     *PKEYCOL:7,*V2LON,*RV,CODESTAY:
                    *PKEYCOL:7,*DV,CODESTAY
          COMPARE   ZERO,CODESTAY
          GOTO      STAY9999 IF NOT LESS
          BEEP
          GOTO      STAY0000
STAY9999  RETURN
+
.*********************************************************************
.         enter the expected procedure time
.*********************************************************************
TIME0000  KEYIN     *PKEYCOL:8,*V2LON,*RV,CODETIME:
                    *PKEYCOL:8,*DV,CODETIME
          COMPARE   ZERO,CODETIME
          GOTO      TIME9999 IF NOT LESS
          BEEP
          GOTO      TIME0000
TIME9999  RETURN
+
.
. ------- I/O includes needed -------
.
          INC       STD001IO                * all standard routines
          INC       PATCODIO/INC
          INC       PATDO1IO/INC            * doctor file
          INC       WATPROIO/INC            * procedure file
          INC       WATCPTIO/INC            * consultant/procedure time file
          INC       PATDOCDS
          INC       PATDOCKY
          INC       WATPROKY
          INC       WATAUDCP                * audits for watcptaf
................................................................................

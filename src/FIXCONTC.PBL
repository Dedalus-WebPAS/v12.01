.***************************************************************************
.* System    :   Inpatients                                                *
.* Program   :   FIXCONTC                                                  *
.* Desc      :   Program to fix up contact (pmspx2af.pmpxconp)             *
.***************************************************************************
.* Date      :   02/10/2012                                                *
.* Author    :   Jill Parkinson                                            *
.* Function  :   This program will add 2 to the value of pmpxconp if this  *
.*               has been entered                                          *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATCONFD/INC
          INC       PMSPX2FD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
COUNTER   FORM      8
.
PRGID     INIT      "FIXCONTC"
PRGNAM    INIT      "Fix Usual PRFA Contact Value "
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      PMSPX2A1,"pmspx2af"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  MOVE      ZERO,COUNTER
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,HUND05;*74,PTCNFXCN
.
          MATCH     ANSZ,PTCNFXCN
          IF        @EQUAL
            DISPLAY   *P1:20,*EL,"Fix has previously been run. ";
            CALL      HITENTER
            GOTO      PROC9999
          ENDIF
.
          DISPLAY   *P1:20,*EL,"Record No. : ";
.
          PACK      KEY8,SP70
          CALL      RSPMPX21
PROC1000  CALL      RKPMPX21
          BRANCH    OVRCD,PROC9000
.
          ADD       ONE,COUNTER
          IF        (COUNTER%100) = 0
            DISPLAY   *P15:20,*V2LON,COUNTER
          ENDIF
.
          MOVE      ZERO,F1
          MOVE      PMPXCONP,F1
          COMPARE   ZERO,F1
          GOTO      PROC1000 IF EQUAL
.
          IF        F1<6
            ASSIGN    F1+2,F1
          ENDIF
          PACK      PMPXCONP,F1
.
          CALL      UPPMPX21
          GOTO      PROC1000
.
PROC9000  DISPLAY   "Finished processsing ";
          CALL      HITENTER
PROC9999  WRITAB    CONTROLF,HUND05;*74,ANSZ
          RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       PMSPX2IO/INC
          INC       STD001IO
.

.***************************************************************************
.* System    :   Eclipse/WebServices                                       *
.* Program   :   Fix Program - FX917394                                    *
.* Desc      :                                                             *
.***************************************************************************
.* Date      :   22/03/2022                                                *
.* Author    :   Jacob                                                     *
.* Function  :   This program will copy outstanding Health Fund Claims     *
.*               from the existing Eclipse table to the new WebServices    *
.*               table to enable customers a seamless transition           *
.* Modifications  :                                                        *
.*       V11.02.03  14/04/2022  Peter Vela    TSK 0918312                  *
.*                  Move SP70  to WBIMCTYP                                 *
.*       V11.02.02  13/04/2022  Peter Vela    TSK 0917394                  *
.*                  Added Status 19 Closed to record transfer              *
.*                  Closed records that have have an                       *
.*                  assessment report pmseahaf and no payment report       *
.*                  pmserdaf                                               *
.*       V11.02.01  04/04/2022  Jacob Jackson TSK 0917394                  *
.*                  Created program - Eclipse to Webservices transfer      *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PRIEAHFD/INC
          INC       PRIECTFD/INC
          INC       PRIIBHFD/INC
          INC       PMSERDFD/INC
          INC       WEBIMCFD/INC
          INC       WEBIMBFD/INC
          
.
. LOCAL VARIABLE DEFINITION
. -------------------------
PIPE      INIT      "|"
DISP0405  INIT      "No of records mapped from 04(old) to 05(new): "
DISP0910  INIT      "No of records mapped from 09(old) to 10(new): "
DISP1011  INIT      "No of records mapped from 10(old) to 11(new): "
DISP1719  INIT      "No of records mapped from 17(old) to 19(new): "
DISP1921  INIT      "No of records mapped from 19(old) to 21(new): "
.
ADDCNT04  FORM      8
ADDCNT09  FORM      8
ADDCNT10  FORM      8
ADDCNT17  FORM      8
ADDCNT19  FORM      8
TOTALCNT  FORM      8
FLAGTEMP  DIM       2
HOSPTYPE  DIM       2
KEYPMECT  DIM       40
.
.
PRGID     INIT      "FX917394"
PRGNAM    INIT      "Eclipse to WebServices Mapping"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      PRIEAHA2,"prieahaf"
          OPEN      PRIECTA6,"priectaf"
          OPEN      PRIIBHA1,"priibhaf"
          OPEN      PMSERDA2,"pmserdaf"
          OPEN      WEBIMCA6,"webimcaf"
          OPEN      WEBIMBA1,"webimbaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = Exit    (0)  Exit program                         *
.*                        Public  (1)                                       *
.*                        Private (2)                                       *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:4,*V2LON,ONE,*HOFF,". Public(PU)":
                    *P1:5,*V2LON,TWO,*HOFF,". Private(PR)"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000:            * public hosp type
                            OPTN9100             * private hosp type 
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          MOVE      "PU",HOSPTYPE
          GOTO      OPTN9999
.
OPTN9100  MOVE      ZERO,EXIT
          MOVE      "PR",HOSPTYPE
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.******************************************************************************
.*                                  HEAD0000              Called by: PRNT0000 *
.*                           Print The Report Header                          *
.******************************************************************************
.
HEAD0000  CALL      HEAD80                       * print report header
          PRINT     *20,"Copy data from Eclipse(priectaf) ":
                        "to WebServices(webimcaf)"
.
          CALL      LINE0000                     * print line
.
          PRINT     *1,PIPE,*3,"Invoice Number":
                    *20,PIPE,*23,"Duplicate Transaction ID found":
                    *80,PIPE 
.
          CALL      LINE0000
.
          MOVE      SEVEN,CLNO                   * set line count
.
HEAD9999  RETURN
+
.****************************************************************************
.*                            LINE0000                 Called by: HEAD0000  *
.*                      draw line across page                     PMST0000  *
.****************************************************************************
.
LINE0000  PRINT     "*-----------------------------------------------":
                    "-------------------------------*"
.
          ADD       ONE,CLNO
.
LINE9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  CALL      HEAD0000
          CALL      CLWEBIMC                   * clear webimcfd var's 
          MOVE      ZERO,ADDCNT04              * initialize counters  
          MOVE      ZERO,ADDCNT09             
          MOVE      ZERO,ADDCNT10            
          MOVE      ZERO,ADDCNT17
          MOVE      ZERO,ADDCNT19
          MOVE      SP70,KEYPMECT           
.
          DISPLAY   *P1:18,*EL,*V2LON,*HOFF,"Processing records.."
.
          PACK      KEY40,SP70
          CALL      RSPRECT6                  * positional read on Eclipse
PROC1000  CALL      RKPRECT6                  * Claim Table (priectaf)
          BRANCH    OVRCD,PROC9000
.
          MATCH     SP70,DPRECFLG
          GOTO      PROC1000 IF EQUAL          
.
          MATCH     " 4",PRECSTAT             * check if already transferred
          GOTO      PROC1000 IF EQUAL      
.
          DISPLAY   *P1:19,"Invoice Number: ",PRECINVN
.
PROC1050  MATCH     " 4",DPRECFLG
          IF        @EQUAL
            MOVE      " 5",FLAGTEMP
            ADD       ONE,ADDCNT04
            GOTO      PROC2000
          ENDIF
.
          MATCH     " 9",DPRECFLG
          IF        @EQUAL
            MOVE      "10",FLAGTEMP
            ADD       ONE,ADDCNT09
            GOTO      PROC2000
          ENDIF
.
          MATCH     "10",DPRECFLG
          IF        @EQUAL
            MOVE      "11",FLAGTEMP
            ADD       ONE,ADDCNT10
            GOTO      PROC2000
          ENDIF
.
          MATCH     "17",DPRECFLG
          IF        @EQUAL
            MOVE      "19",FLAGTEMP
            ADD       ONE,ADDCNT17 
            GOTO      PROC2000
          ENDIF
.
          MATCH     "19",DPRECFLG
          IF        @EQUAL
.
            MATCH     SP70,PRECTRID
            IF        !@EQUAL
.
              PACK      KEY56,PRECTRID,SP70
              CALL      RSPREAH2
              CALL      RKPREAH2
              BRANCH    OVRCD,PROC1000
.
              MATCH     PRECTRID,PRAHFTID
              GOTO      PROC1000 IF NOT EQUAL
.
              PACK      KEY82,PRECTRID,SP70,SP70
              CALL      RSPMERD2
              CALL      RKPMERD2
              BRANCH    OVRCD,PROC1075
.
              MATCH     PRECTRID,PMEDTRID
              GOTO      PROC1075 IF NOT EQUAL
.
              GOTO       PROC1000
.
PROC1075      MOVE      "21",FLAGTEMP
              ADD       ONE,ADDCNT19
              GOTO      PROC2000
.
            ENDIF
.
          ENDIF
.
          GOTO      PROC1000
.
PROC2000  CALL      CLWEBIMC                   * clear webimcfd var's       
          PACK      KEYPMECT,PRECTRID,PRECINVN,PRECBATN,SP70
          PACK      KEY40,PRECTRID,SP70
          CALL      RSWBIMC6
          CALL      RKWBIMC6
          BRANCH    OVRCD,PROC2100
.
          MATCH     SP70,PRECTRID
          GOTO      PROC2100 IF EQUAL  
.
          MATCH     PRECTRID,WBIMTRID
          IF        @EQUAL
                      PRINT *1,PIPE,*3,PRECINVN,*20,PIPE,*23,PRECTRID:
                            *80,PIPE
          ENDIF
.
PROC2100  MOVE      SP70,WBIMCTYP
          MOVE      FLAGTEMP,WBIMFLAG
          CALL      MVPMWB00           * copy claim data to webservices vars
.
          PACK     KEY40,WBIMTRID,WBIMINVN,WBIMBATN,SP70
          CALL     RAWBIMC6            
          IF       OVRCD = 1
            CALL      WRWBIMC6         * write to webservices table
.
            PACK      KEY40,KEYPMECT,SP70
            CALL      RDPRECT6         
            IF        OVRCD = 0
              MOVE      " 4",PRECSTAT
              CALL      UPPRECT6                     * update claim
            ENDIF
.
.
.
            PACK      KEY8,PRECBATN,SP70            
            CALL      RDPRIBH1
            IF        OVRCD=0
.
              PACK      KEY8,PRIBBTHN,SP70
              CALL      RAWBIMB1
              IF        OVRCD=1
.
                MOVE      PRIBBTHN,WBIBBTHN
                MOVE      PRIBHFND,WBIBHFND
                MOVE      PRIBBHTL,WBIBBHTL
                MOVE      PRIBTRIB,WBIBTRIB
                MOVE      PRIBDTBC,WBIBDTBC
                MOVE      PRIBTMBC,WBIBTMBC
                MOVE      PRIBOPER,WBIBOPER
                MOVE      PRIBEETP,WBIBEETP
                MOVE      PRIBEFNM,WBIBEFNM
                MOVE      PRIBLOCN,WBIBLOCN
                MOVE      PRIBSNID,WBIBSNID
                PACK      WBIBSPAR,SP70,SP70              
.
                CALL      WRWBIMB1      * write batch data to webservices table
.
              ENDIF
.
            ENDIF
.
            ADD       ONE,TOTALCNT
.
            PACK      KEY40,KEYPMECT,SP70
            CALL      RSPRECT6 
          ENDIF
.
          GOTO       PROC1000            
.
PROC9000  PRINT     *N,DISP0405,*47,ADDCNT04:
                    *N,DISP0910,*47,ADDCNT09:
                    *N,DISP1011,*47,ADDCNT10:
                    *N,DISP1719,*47,ADDCNT17:
                    *N,DISP1921,*47,ADDCNT19:
                    *N,"----------------------------------":
                    *N,"Total Records Copied: ",TOTALCNT:
                    *N,"----------------------------------"; 
          PRINT     *N,*1,"*** End of Report ***";
.                        
          DISPLAY   *P1:16,*EF,DISP0405,ADDCNT04:
                    *P1:17,*EF,DISP0910,ADDCNT09:
                    *P1:18,*EF,DISP1011,ADDCNT10:
                    *P1:19,*EF,DISP1719,ADDCNT17:  
                    *P1:20,*EF,DISP1921,ADDCNT19:
                    *P1:22,*EF,"Total Records Copied: ",TOTALCNT:  
                    *P1:24,*EF;
          CALL      HITENTER          
.
PROC9999  RETURN
+
. -------------------------------------------------------------------------
.         MVPMWB00      
. -------------------------------------------------------------------------
MVPMWB00  MOVE      PRECHOSP,WBIMHOSP       * Hospital ID
          MOVE      PRECHFND,WBIMHFND       * Health Fund Code
          MOVE      PRECUNIQ,WBIMUNIQ       * Unique Identifier
          MOVE      PRECINVN,WBIMINVN       * Invoice No
          MOVE      PRECBATN,WBIMBATN       * Batch No
          MOVE      PRECURNO,WBIMURNO       * U/R No
          MOVE      PRECPBAT,WBIMPBAT       * Prev Batch No
          MOVE      PRECNBAT,WBIMNBAT       * Next Batch No
          MOVE      PRECTRID,WBIMTRID       * Claim Transaction Id
          MOVE      PRECEETP,WBIMEETP       * Eclipse extract type
          MOVE      PRECAMTC,WBIMAMTC       * Amount Claimed
          MOVE      PRECAMFP,WBIMAMFP       * Amount Paid by Health Fund
          MOVE      PRECAMMP,WBIMAMMP       * Amount Paid by Medicare
          MOVE      PRECPDAT,WBIMPDAT       * Payment Date (ccyymmdd)
          MOVE      PRECSTAT,WBIMSTAT       * Closed Status
          MOVE      PRECFTID,WBIMFTID       * Fund Report Transaction Id
          MOVE      PRECPRAC,WBIMPRAC       * Medical Practice
          MOVE      PRECSPAR,WBIMSPAR       * Spare Variable  
.
MVPMWB99  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       CLWEBIMC
.      
          INC       PRIEAHIO/INC
          INC       PRIECTIO/INC
          INC       PRIIBHIO/INC
          INC       PMSERDIO/INC
          INC       WEBIMCIO/INC
          INC       WEBIMBIO/INC

.******************************************************************************
.* System    : OPERATING ROOM                                                 *
.* Program  :  IBAOPR69                                                       *
.* Desc     :  Cancelled Sessions Report                                      *
.******************************************************************************
.* Date     :  02/01/1991                                                     *
.* Author   :  MMO ( Copied & modified IBAOPR57 by Peter Eddey )              *
.* Mods.    :                                                                 *
.*             V10.04.01 23/06/2014   J.Tan        CAR 261630                 *
.*                       Removed port number from temp file name              *
.*             V5.01.01  18/02/91 ROD                                         *
.*                       Fixed printing of Doctor codes routine               *
.*             V5.01.02  18/10/91 i chung           SRF 110416                *
.*                       Print headings for report with no available data     *
.*             V5.01.03  20/07/92 ROD                                         *
.*                       Display/Print date for period                        *
.*        V5.01.04  04/06/1993  Glenn Berry      SRF 120247                   *
.*                  Changed page breaks to line 60                            *
.*                                                                            *
.******************************************************************************
.$$$$$
          INC       STD001FD
          INC       IBASEQFD/INC            * Sequential Numbers File
          INC       TFILEVAR/INC            * Generate Temp File Name
          INC       WEBERRFD/INC            * Web Server Error Logging
.
          INC       OPRCLIFD/INC
          INC       OPRCONFD/INC
          INC       OPRCSCFD/INC
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PATDRGFD/INC
+
.**********************************************************************
.*                       CONSTANTS                                    *
.**********************************************************************
CODEOC    INIT      "OC"
.
CODOPT    INIT      "- Code Seq."
NAMOPT    INIT      "- Name Seq."
EQUAL30   INIT      "=============================="
DASH30    INIT      "------------------------------"        
+
.**********************************************************************
.*                       GLOBAL VARIABLES                             *
.**********************************************************************
ANAES     DIM       6
ANAESCD   DIM       3
ANAESNAM  DIM       25
.
COUNT     FORM      3
CPTDATE   DIM       8
.
DCOUNT    DIM       3
DIM3      DIM       3
DISPOPTL  DIM       14
DOCNAME   DIM       20
DOCSURN   DIM       20
.
EDRGCNUM  DIM       2
EDRGCYR   DIM       4
EDRGMDSC  DIM       9
ENDDATE   DIM       6
ENDFLAG   FORM      1
ENMON     DIM       2
ENMONF    FORM      2
ENYEAR    DIM       2
.
GRANTOT   FORM      6
INDEX     FORM      1
.
.
MON       FORM      2
MOND      DIM       2
MONTH     FORM      2
MONVAR    FORM      5
.
NUMBER    FORM      6
.
PERD      DIM       3
PERS      FORM      2
PRDOCT    DIM       6
PRTYPE    DIM       3
.
SAVFDATE  DIM       10
SAVTDATE  DIM       10
SDRGCNUM  DIM       2
SDRGCYR   DIM       4
SDRGMDSC  DIM       9
STDATE    DIM       6
STFDATE   FORM      6
SURGCODE  DIM       6
SURGNAME  DIM       40
SVANDATE  FORM      6
SVENDATE  FORM      6
SVSTDATE  FORM      6
.
TEMPDATE  DIM       6
XDOCT     DIM       6
XTYPE     DIM       3
Z3        INIT      "ZZZ"
Z6        INIT      "ZZZZZZ"
.
OPTMPA1   IFILE     SQL, FIXED=82, KEY="U1-6,7-9"
OPTMPA2   IFILE     SQL, FIXED=82, KEY="U10-29,1-6,7-9"
.
CMDLINE   DIM       60
FNAME1    DIM       8
.
. ------- tempfile variables -------
.
.OPCCDOCT DIM       6      6     1   clinic/surgeon
.                                    zzzzzz = grand totals
.                                    
.OPCCCODE DIM       3      3     7   Cancellation Reason
.                                    zzz = total for doctor 
.DOCNAME  DIM       20     20   10   DOCTOR SURNAME
.                                    zzzzzz = grand totals
MON1     FORM      5      4    30
MON2     FORM      5      4    34
MON3     FORM      5      4    38
MON4     FORM      5      4    42
MON5     FORM      5      4    46
MON6     FORM      5      4    50
MON7     FORM      5      4    54
MON8     FORM      5      4    58
MON9     FORM      5      4    62
MON10    FORM      5      4    66
MON11    FORM      5      4    70
MON12    FORM      5      4    74
MON13    FORM      5      4    78
.End of Record                 82 
.___________________________________
.
PRGID     INIT      "IBAOPR69"
PRGNAM    INIT      "Cancelled Sessions Report"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                           MAINLINE                                 *
.**********************************************************************
ML0000    CALL      INIT0000               * initialization and open files
.
ML0100    CALL      OPTN0000               * select option         
          BRANCH    EXIT,ML9999
.
. ---- main processing ----
.
ML4000    CALL      CLR0000                * clear variables
          CALL      FRDT0000               * keyin From Date 
          BRANCH    EXIT,ML0100
.
          CALL      TODT0000               * keyin To Date
          BRANCH    EXIT,ML4000
.
          CALL      CONTQST                * ok to continue ?
          BRANCH    CEXIT,ML4100,ML4000,ML0100
.
ML4100    CALL      TEMP0000               * create tempfile
          CALL      EXTR0000               * extract records accord to range
.
          MOVE      ZERO,CPAGENO
          BRANCH    COPTION,ML5000,ML6000
.
ML5000    CALL      PRNA0000               * print report  code seq
          GOTO      ML0100
.
ML6000    CALL      PRNB0000               * print report surn seq
          GOTO      ML0100
.
ML9999    CALL      KILL0000               * delete tempfile
          CHAIN     PGM          
+
.****************************************************************************
.*                                INIT0000                                  *
.*  The initialization routine is used to display headings and open files   *
.****************************************************************************
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,FORTY;*81,OPCNODSC,*96,OPCNCDSC,*111,OPCNCLSU
.
          DISPLAY   *P64:24,"oprcliaf";
          OPEN      OPRCLIA1,"oprcliaf"
.
          DISPLAY   *P64:24,"oprcscaf";
          OPEN      OPRCSCA1,"oprcscaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P64:24,"patdrgaf";
          OPEN      PATDRGA1,"patdrgaf"
          OPEN      PATDRGA3,"patdrgaf"
.
          PACK      SP30,SP20,SP10
.
          MOVE      ZERO,CHIGHLT
          MOVE      ONE,CNOUNDLN
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,FNAME1
.
INIT9999  RETURN
+
.****************************************************************************
.*                               OPTN0000                                   *
.*                             Select option                                *
.****************************************************************************
OPTN0000  HLINE     *G37,2,53,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Code Sequence":
                    *P1:6,*V2LON,TWO,*HOFF," = Name Sequence":
                    *P1:8,"Select Option :";
.
OPTN1000  KEYIN     *P17:8,*EL,*DV,UNDLN1,*P17:8,*V2LON,COPTION;
          COMPARE   ZERO,COPTION
          GOTO      OPTN9000 IF EQUAL
.
          BRANCH    COPTION,OPTN8000,OPTN8000
          BEEP
          GOTO      OPTN1000
.
OPTN8000  LOAD      DISPOPTL WITH COPTION OF CODOPT,NAMOPT
          MOVE      DISPOPTL,CPHDROPT              * printing heading field
          STRIP     DISPOPTL
          DISPLAY   *P53:2,*V2LON,*+,DISPOPTL,SP1;
          MOVE      FALSE,EXIT
          GOTO      OPTN9999
.
OPTN9000  MOVE      TRUE,EXIT
.
OPTN9999  RETURN
+
.**********************************************************************
.*                             CLR0000                                *
.*                         Clear variables                            *
.**********************************************************************
CLR0000   MOVE      ZERO,COUNT
.
CLR1000   ADD       ONE,COUNT
          STORE     ZERO,COUNT,MON1,MON2,MON3,MON4,MON5,MON6,MON7:
                               MON8,MON9,MON10,MON11,MON12,MON13
          COMPARE   TEN3,COUNT
          GOTO      CLR1000 IF LESS
.
CLR2000   MOVE      ZERO,NUMBER
.
CLR9999   RETURN
+
.**********************************************************************
.*                             FRDT0000                               *
.*                     Keyin From Date (Period)                       *
.**********************************************************************
FRDT0000  DISPLAY   *P1:12,*EF,"Start Date  : ":
                        *P1:13,"End   Date  : ";
.
          MOVE      TEN2,CVERT
          MOVE      TEN5,CCOL
          MOVE      TWENTY4,EVERT
          MOVE      ONE,ECOL
          MOVE      CCC,CCENT
          MOVE      SP2,CYEAR
          MOVE      SP2,CMON
          MOVE      SP2,CDAY
.
          CALL      KEYPER
          BRANCH    OVRCD,FRDT9000
          BRANCH    CQUEST,FRDT8000
.
          PACK      KEY6,ICENT,IYEAR,IMON
          REP       " 0",KEY6
          CALL      RDDRGA3
          BRANCH    OVRCD,FRDT8000
.
          UNPACK    DRGFDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P27:12,CPCDATE
          MOVE      CPCDATE,SAVFDATE
.
          PACK      STDATE,DRGYR,DRGNUM    
          REP       " 0",STDATE
.
          MOVE      DRGMDSC,SDRGMDSC
          MOVE      DRGCNUM,SDRGCNUM
          MOVE      DRGCYR,SDRGCYR
          MOVE      FALSE,EXIT
          GOTO      FRDT9999
.
FRDT8000  DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid Period **    ";
          CALL      HITENTER
          GOTO      FRDT0000
.
FRDT9000  MOVE      TRUE,EXIT
.
FRDT9999  RETURN
+
.**********************************************************************
.*                             TODT0000                               *
.*                     Keyin To Date (Period)                         *
.**********************************************************************
TODT0000  MOVE      TEN3,CVERT
          MOVE      TEN5,CCOL
          MOVE      TWENTY4,EVERT
          MOVE      ONE,ECOL
          MOVE      CCC,CCENT
          MOVE      SP2,CYEAR
          MOVE      SP2,CMON
          MOVE      SP2,CDAY
.
          CALL      KEYPER
          BRANCH    OVRCD,TODT9000
          BRANCH    CQUEST,TODT6000
.
          PACK      KEY6,ICENT,IYEAR,IMON
          REP       " 0",KEY6
          CALL      RDDRGA3
          BRANCH    OVRCD,TODT6000
.
          UNPACK    DRGTDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P27:13,CPCDATE
          MOVE      CPCDATE,SAVTDATE
.
          PACK      ENDDATE,DRGYR,DRGNUM
          REP       " 0",ENDDATE
.
          MOVE      DRGMDSC,EDRGMDSC
          MOVE      DRGCYR,EDRGCYR
          MOVE      DRGCNUM,EDRGCNUM
.
          MATCH     STDATE,ENDDATE
          GOTO      TODT7000 IF LESS
.
          MOVE      STDATE,KEY6        
          REP       " 0",KEY6
          CALL      CALP0000                 * calculate diff in periods
.
          COMPARE   TEN3,PERS
          GOTO      TODT8000 IF NOT LESS
          MOVE      FALSE,EXIT
          GOTO      TODT9999
.
TODT6000  DISPLAY   *P1:24,*EL,*B,*V2LON,"** Invalid Period **    ";
          CALL      HITENTER
          GOTO      TODT0000
.
TODT7000  DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** End Date must be greater than or = Start Date **   ";
          GOTO      TODT8100
.
TODT8000  DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Date range must be less than 13 periods **    "; 
.
TODT8100  CALL      HITENTER
          DISPLAY   *P15:13,*EL;
          GOTO      TODT0000
.
TODT9000  MOVE      TRUE,EXIT
.
TODT9999  RETURN
+
.**********************************************************************
.*                             CALM0000                               *
.*                    Calculate difference in months                  *
.**********************************************************************
CALM0000  CALL      RDSDRGA1
          MOVE      ZERO,MONTH
.
CALM1000  CALL      RDKDRGA1
          BRANCH    OVRCD,CALM9000
.
          PACK      TEMPDATE,DRGYR,DRGNUM
          REP       " 0",TEMPDATE
.
          ADD       ONE,MONTH
          MATCH     TEMPDATE,OPCCDATE
          GOTO      CALM9999 IF LESS
          GOTO      CALM1000
.
CALM9000  ADD       ONE,MONTH
.
CALM9999  RETURN
+
.**********************************************************************
.*                             CALP0000                               *
.*                    Calculate difference in periods                 *
.**********************************************************************
CALP0000  CALL      RDSDRGA1
          MOVE      ZERO,PERS
.
CALP1000  CALL      RDKDRGA1
          BRANCH    OVRCD,CALP9000
.
          PACK      TEMPDATE,DRGYR,DRGNUM
          REP       " 0",TEMPDATE
.
          ADD       ONE,PERS
          MATCH     TEMPDATE,ENDDATE
          GOTO      CALP9999 IF LESS
          GOTO      CALP1000
.
CALP9000  ADD       ONE,PERS
.
CALP9999  RETURN
+
.**********************************************************************
.*                             KILL0000                               *
.*                          Delete tempfile                           *
.**********************************************************************
KILL0000  CLOSE     OPTMPA1
          CLOSE     OPTMPA2
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAME1,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
KILL9999  RETURN
+
.**********************************************************************
.*                             TEMP0000                               *
.*                          Create tempfile                           *
.**********************************************************************
TEMP0000  CALL      KILL0000                * kill tempfile
.
. ----- Create indexed tempfile -----
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAME1,CMDLINE
          APPEND    " 82 ",CMDLINE
          APPEND    "U1-6,7-9 U10-29,1-6,7-9",CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
. ----- Open tempfile -----
.
          OPEN      OPTMPA1,FNAME1
          OPEN      OPTMPA2,FNAME1
.
TEMP9999  RETURN
+
.**********************************************************************
.*                             EXTR0000                               *
.*                Extract records according to selection              *
.**********************************************************************
EXTR0000  DISPLAY  *P1:24,*EL,*V2LON,"** ",*BLINKON,"Extracting Data",*HOFF:
                          *V2LON," **",*HOFF,*P40:24,"Clinic/Surgeon  :  ";
.
          MOVE     ZERO,NUMBER
          PACK     KEY20,STDATE,SP20
          CALL     RSOPCSC1
.
EXTR1000  CALL     RKOPCSC1
          BRANCH   OVRCD,EXTR9999
.
          MATCH    OPCCDATE,ENDDATE
          GOTO     EXTR9999 IF LESS
.
          MATCH    SP3,OPCCCODE
          GOTO     EXTR1000 IF EQUAL
.
          COMPARE  ONE,OPCCCTYP
          GOTO     EXTR1000 IF NOT EQUAL  
.
          DISPLAY  *P59:24,OPCCDOCT;
.
          MOVE     OPCCDOCT,KEY6
          COMPARE  ZERO,OPCNCLSU
          GOTO     EXTR4000 IF EQUAL
.
EXTR3000  CALL     RDDOCT1
          BRANCH   OVRCD,EXTR5000
          MOVE     DSNAME,DOCSURN
          GOTO     EXTR5000
.
EXTR4000  CLEAR    OPCLDESC
          CALL     RDOPCLI1
          PACK     OPCLDESC,OPCLDESC,SP30
          MATCH    SP30,OPCLDESC
          GOTO     EXTR3000 IF EQUAL
          MOVE     OPCLDESC,DOCSURN
.
EXTR5000  MOVE     STDATE,KEY6
          REP      " 0",KEY6
          CALL     CALM0000                * calc num of months
.
          PACK     KEY9,OPCCDOCT,OPCCCODE
          CALL     UPDT0000
.
          PACK     KEY9,OPCCDOCT,Z3
          CALL     UPDT0000
.
          PACK     DOCSURN,Z6,Z6,Z6,Z6
.
          PACK     KEY9,Z6,OPCCCODE
          CALL     UPDT0000
.
          PACK     KEY9,Z6,Z3
          CALL     UPDT0000
          GOTO     EXTR1000
.
EXTR9999  RETURN
+
.**********************************************************************
.*                             UPDT0000                               *
.*                       Write/Update tempfile                        *
.**********************************************************************
UPDT0000  CALL     RDTMP
          BRANCH   OVRCD,UPDT5000
.
          LOAD     NUMBER,MONTH,MON1,MON2,MON3,MON4,MON5,MON6,MON7,MON8:
                                MON9,MON10,MON11,MON12,MON13
          ADD      OPCCNUMB,NUMBER
          STORE    NUMBER,MONTH,MON1,MON2,MON3,MON4,MON5,MON6,MON7,MON8:
                                MON9,MON10,MON11,MON12,MON13
          CALL     UPTMP
          GOTO     UPDT9999
.
UPDT5000  MOVE     ZERO,MON1
          MOVE     ZERO,MON2
          MOVE     ZERO,MON3
          MOVE     ZERO,MON4
          MOVE     ZERO,MON5
          MOVE     ZERO,MON6
          MOVE     ZERO,MON7
          MOVE     ZERO,MON8
          MOVE     ZERO,MON9
          MOVE     ZERO,MON10
          MOVE     ZERO,MON11
          MOVE     ZERO,MON12
          MOVE     ZERO,MON13
.
          STORE    OPCCNUMB,MONTH,MON1,MON2,MON3,MON4,MON5,MON6,MON7,MON8:
                                  MON9,MON10,MON11,MON12,MON13
          CALL     WRTMP
.
UPDT9999  RETURN
+
.**********************************************************************
.*                             PRNA0000                               *
.*                  Print records from the tempfile                   *
.**********************************************************************
PRNA0000  DISPLAY   *P1:24,*EL,*P28:24,*V2LON,"*** Generating Report ***",*W;
          CLOCK     TIME,CTIMEIS
.
          MOVE      "90",CLNO
          MOVE      SP6,PRDOCT
          MOVE      SP3,PRTYPE
.
          MOVE      SP9,KEY9 
          CALL      RSTMP
.
PRNA1000  CALL      RKTMP
          BRANCH    OVRCD,PRNA8000
          CALL      PRNT0000                  * print the records
          GOTO      PRNA1000
.
PRNA8000  COMPARE   ZERO,CPAGENO
          GOTO      PRNA9000 IF NOT EQUAL
          CALL      MHDR0000
.
PRNA9000  CALL      ENDP0000                  * last lines
.
PRNA9999  RETURN
+
.**********************************************************************
.*                             PRNB0000                               *
.*                Print records from the tempfile                     *
.**********************************************************************
PRNB0000  DISPLAY   *P1:24,*EL,*P28:24,*V2LON,"*** Generating Report ***",*W;
          CLOCK     TIME,CTIMEIS
.
          MOVE      "90",CLNO
          MOVE      SP6,PRDOCT
          MOVE      SP3,PRTYPE
.
          PACK      KEY29,SP20,SP10
          CALL      RSTMP2
.
PRNB1000  CALL      RKTMP2
          BRANCH    OVRCD,PRNB8000
          CALL      PRNT0000                  * print the records
          GOTO      PRNB1000
.
PRNB8000  COMPARE   ZERO,CPAGENO
          GOTO      PRNB9000 IF NOT EQUAL
          CALL      MHDR0000
.
PRNB9000  CALL      ENDP0000                  * last lines
.
PRNB9999  RETURN
+
.**********************************************************************
.*                             PRNT0000                               *
.*                Print records from the tempfile                     *
.**********************************************************************
PRNT0000  MOVE      ZERO,ENDFLAG
          COMPARE   "60",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          MOVE      MON1,NUMBER
          ADD       MON2,NUMBER
          ADD       MON3,NUMBER
          ADD       MON4,NUMBER
          ADD       MON5,NUMBER
          ADD       MON6,NUMBER
          ADD       MON7,NUMBER
          ADD       MON8,NUMBER
          ADD       MON9,NUMBER
          ADD       MON10,NUMBER
          ADD       MON11,NUMBER
          ADD       MON12,NUMBER
          ADD       MON13,NUMBER
.
. --- print '--''s ----
.
PRNT0050  MATCH     XDOCT,Z6
          GOTO      PRNT5000 IF EQUAL
.
PRNT0070  MATCH     PRDOCT,XDOCT
          CALL      HEDD0000 IF NOT EQUAL
.
          MATCH     XTYPE,Z3
          GOTO      PRNT2000 IF NOT EQUAL
.
. ---- print '--''s ----
.
          PRINT     *1,DASH30;
          MOVE      ONE,COUNT
          MOVE      THIRTY1,CCOL
.
PRNT0100  PRINT     *CCOL,"------";
          ADD       SEVEN,CCOL
          ADD       ONE,COUNT
          COMPARE   COUNT,PERS                  * all pers yet ????
          GOTO      PRNT0100 IF NOT LESS 
          PRINT     *CCOL,"-------"             * total line
          ADD       ONE,CLNO
.
. ---- print line ----
.
          PRINT     *15,"       Total";
          MOVE      ONE,COUNT
          PRINT     *30;
.
PRNT0500  LOAD      MONVAR,COUNT,MON1,MON2,MON3,MON4:MON5,MON6,MON7,MON8:
                                 MON9,MON10,MON11,MON12,MON13
          PRINT     SP2,MONVAR;
          ADD       ONE,COUNT
.
          COMPARE   COUNT,PERS
          GOTO      PRNT0500 IF NOT LESS
          ADD       ONE,CLNO
          PRINT     SP2,NUMBER
.
. ---- print '--''s ----
.
PRNT0700  PRINT     *1,EQUAL30;                 * Print's 30 "="
          ADD       ONE,CLNO
          MOVE      ONE,COUNT
          MOVE      THIRTY1,CCOL
.
PRNT0800  PRINT     *CCOL,"=======";
          ADD       SEVEN,CCOL
          ADD       ONE,COUNT
.
          COMPARE   COUNT,PERS                  * all pers yet ????
          GOTO      PRNT0800 IF NOT LESS 
          PRINT     *CCOL,"======="             * total line
          GOTO      PRNT9999
.
. ---- print normal line ----
.
PRNT2000  PACK      KEY5,CODEOC,XTYPE
          CALL      RDCODE1
.
          PRINT     *5,TDESC,SP5;
          MOVE      ONE,COUNT
.
PRNT2100  LOAD      MONVAR,COUNT,MON1,MON2,MON3,MON4,MON5,MON6,MON7,MON8:
                                 MON9,MON10,MON11,MON12,MON13
          PRINT     SP2,MONVAR;
          ADD       ONE,COUNT
          COMPARE   COUNT,PERS
          GOTO      PRNT2100 IF NOT LESS
          ADD       ONE,CLNO
          PRINT     SP2,NUMBER
          ADD       "1",CLNO
          GOTO      PRNT9999
.
. ----  doctor code is = 'zzzzzz'  ( means we print totals ) ----
.
PRNT5000   MATCH    PRDOCT,XDOCT
           GOTO     PRNT5010 IF EQUAL
           MOVE     ONE,ENDFLAG
           CALL     HEAD0000
.  
PRNT5010  MATCH     XTYPE,Z3
          GOTO      PRNT7000 IF EQUAL
.
          CLEAR     TDESC
          MOVE      XDOCT,PRDOCT
          PACK      KEY5,CODEOC,XTYPE
          CALL      RDCODE1
.
          PRINT     *5,TDESC,SP5;
          MOVE      ONE,COUNT
.
PRNT5100  LOAD      MONVAR,COUNT,MON1,MON2,MON3,MON4,MON5,MON6,MON7,MON8:
                                 MON9,MON10,MON11,MON12,MON13
          PRINT     SP2,MONVAR;
          ADD       ONE,COUNT
.
          COMPARE   COUNT,PERS
          GOTO      PRNT5100 IF NOT LESS
          PRINT     SP2,NUMBER
          ADD       "1",CLNO
          GOTO      PRNT9999
.
. ---- print '--''s ----
.
PRNT7000  PRINT     *1,DASH30;
          MOVE      ONE,COUNT
          MOVE      THIRTY1,CCOL
.
PRNT7100  PRINT     *CCOL,"------";
          ADD       SEVEN,CCOL
          ADD       ONE,COUNT
          COMPARE   COUNT,PERS                    * all pers yet ????
          GOTO      PRNT7100 IF NOT LESS 
          ADD       ONE,CLNO
          PRINT     *CCOL,"-------"
.
. ---- print line ----
.
          PRINT     *16,"Grand Total",SP3;
          MOVE      ONE,COUNT
.
PRNT7500  LOAD      MONVAR,COUNT,MON1,MON2,MON3,MON4,MON5,MON6,MON7,MON8:
                                 MON9,MON10,MON11,MON12,MON13
          PRINT     SP2,MONVAR;
          ADD       ONE,COUNT
.
          COMPARE   COUNT,PERS
          GOTO      PRNT7500 IF NOT LESS
          PRINT     SP2,NUMBER
          ADD       "1",CLNO
.
. --- print '--''s ----
.
PRNT7700  PRINT     *1,EQUAL30;
          MOVE      ONE,COUNT
          MOVE      THIRTY1,CCOL
.
PRNT7800  PRINT     *CCOL,"=======";
          ADD       SEVEN,CCOL
          ADD       ONE,COUNT
.
          COMPARE   COUNT,PERS                    * all pers yet ????
          GOTO      PRNT7800 IF NOT LESS 
          ADD       ONE,CLNO
          PRINT     *CCOL,"======="                * total line
          GOTO      PRNT9999
.
PRNT9999  RETURN
+
.**********************************************************************
.*                             HEAD0000                               *
.*                          Print headings                            *
.**********************************************************************
HEAD0000  BRANCH    ENDFLAG,HEAD0500
          MOVE      XDOCT,PRDOCT
          MOVE      XTYPE,PRTYPE
.
          CALL      MHDR0000                     * Main Header
          GOTO      HEAD1000
.
HEAD0500  PRINT     " "
          ADD       ONE,CLNO
.
HEAD1000  PRINT     *1,DASH30;
          MOVE      ONE,COUNT
          MOVE      THIRTY1,CCOL
.
HEAD2000  PRINT     *CCOL,"-------";
          ADD       SEVEN,CCOL
          ADD       ONE,COUNT
          COMPARE   COUNT,PERS                    * all pers yet ????
          GOTO      HEAD2000 IF NOT LESS 
          PRINT     *CCOL,"-------"
.
          BRANCH    ENDFLAG,HEAD2500
          GOTO      HEAD2800
.
HEAD2500  PRINT     *15,"Report Summary";
.
HEAD2800  MOVE      THIRTY2,CCOL
          MOVE      STDATE,KEY6
          REP       " 0",KEY6
          CALL      RDDRGA1
          BRANCH    OVRCD,HEAD3000
          UNPACK    STDATE,CCENT,CYEAR,DRGNUM
          GOTO      HEAD5000
.
HEAD3000  CALL      RDKDRGA1
          BRANCH    OVRCD,HEAD7000
.
          PACK      TEMPDATE,DRGYR,DRGNUM
          REP       " 0",TEMPDATE
.
          MATCH     TEMPDATE,ENDDATE
          GOTO      HEAD7000 IF LESS
.
HEAD5000  UNPACK    DRGCYR,CCENT,CYEAR
.
HEAD6000  PRINT     *CCOL,DRGCNUM,SLASH,CYEAR;
          ADD       SEVEN,CCOL
          GOTO      HEAD3000
.
HEAD7000  PRINT     *CCOL," Total"
          PRINT     *1,DASH30;            
          MOVE      ONE,COUNT
          MOVE      THIRTY1,CCOL
.
HEAD8000  PRINT     *CCOL,"------";
          ADD       SEVEN,CCOL
          ADD       ONE,COUNT
.
          COMPARE   COUNT,PERS                    * all pers yet ????
          GOTO      HEAD8000 IF NOT LESS 
          PRINT     *CCOL,"-------"
.
          BRANCH    ENDFLAG,HEAD9999
          MOVE      TEN,CLNO
          CALL      HEDD0000
.
HEAD9999  RETURN
+
.**********************************************************************
.*                             MHDR0000                               *
.*                         Print Main Header                          *
.**********************************************************************
MHDR0000  CALL      HEAD132
.
          PRINT     *40,"From : ",SDRGCNUM,SLASH,SDRGCYR:
                    SP2,SAVFDATE,*N:
                    *40,"To   : ",EDRGCNUM,SLASH,EDRGCYR:
                    SP2,SAVTDATE,*N,*N
.
MHDR9999  RETURN
+
.**********************************************************************
.*                             HEDD0000                               *
.*                         doctor's heading                           *
.**********************************************************************
HEDD0000  MOVE      XDOCT,PRDOCT
          MOVE      XDOCT,KEY6
.
          COMPARE   ZERO,OPCNCLSU
          GOTO      HEDD5000 IF EQUAL
.
HEDD0100  CALL      RDDOCT1
          BRANCH    OVRCD,HEDD0500
.
          MOVE      DTITL,DOCNAME
          ENDSET    DOCNAME
          CALL      HEDD0250
          LENSET    DOCNAME
          MOVE      DGNAME,DIM1
          APPEND    SP1,DOCNAME
          APPEND    DSNAME,DOCNAME
          RESET     DOCNAME
          GOTO      HEDD7000
.
HEDD0250  CMATCH    SP1,DOCNAME                    * get rid of spaces
          RETURN    IF NOT EQUAL                   * in doctors desc
          BUMP      DOCNAME BY -1
          RETURN    IF EOS
          GOTO      HEDD0250
.
HEDD0500  MOVE      "Missing code        ",DOCNAME
          GOTO      HEDD7000
.
HEDD5000  CALL      RDOPCLI1
          BRANCH    OVRCD,HEDD0500
.
          PACK      OPCLDESC,OPCLDESC,SP30
          MATCH     OPCLDESC,SP30
          GOTO      HEDD6000 IF NOT EQUAL
          MOVE      OPCCDOCT,KEY6
          GOTO      HEDD0100
.
HEDD6000  MOVE      OPCLDESC,DOCNAME
.
HEDD7000  PRINT     *1,XDOCT,*9,DOCNAME
          ADD       ONE,CLNO
.
HEDD9999  RETURN
+
.**********************************************************************
.*                             ENDP0000                               *
.*                           End of Report                            *
.**********************************************************************
ENDP0000  PRINT     *N,*N," ***  End of Report  ***"
.
          COMPARE   ZERO,CPAGENO
          GOTO      ENDP9999 IF EQUAL
          DISPLAY   *P1:24,*EL,*P23:24,*V2LON:
                    "*** Report Generation Completed ***",*W;
.
ENDP9999  RETURN
+
.****************************************************************************
.*                      TEMPFILE IO'S                                       *
.****************************************************************************
RDTMP     MOVE      ZERO,OVRCD
          RESET     KEY9
          READ      OPTMPA1,KEY9;XDOCT,XTYPE:
                           DOCSURN,MON1,MON2,MON3,MON4,MON5,MON6,MON7:
                           MON8,MON9,MON10,MON11,MON12,MON13
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTMP     MOVE      ZERO,OVRCD
          RESET     KEY9
          READ      OPTMPA1,KEY9;;
          RETURN
.
RKTMP     MOVE      ZERO,OVRCD
          READKS    OPTMPA1;XDOCT,XTYPE:
                           DOCSURN,MON1,MON2,MON3,MON4,MON5,MON6,MON7:
                           MON8,MON9,MON10,MON11,MON12,MON13
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTMP     RESET     KEY9
          WRITE     OPTMPA1,KEY9;KEY9:
                           DOCSURN,MON1,MON2,MON3,MON4,MON5,MON6,MON7:
                           MON8,MON9,MON10,MON11,MON12,MON13
          RETURN
.
UPTMP     UPDATE    OPTMPA1;XDOCT,XTYPE:
                           DOCSURN,MON1,MON2,MON3,MON4,MON5,MON6,MON7:
                           MON8,MON9,MON10,MON11,MON12,MON13
          RETURN
.
RSTMP2    MOVE      ZERO,OVRCD
          RESET     KEY29
          READ      OPTMPA2,KEY29;;
          RETURN
.
RKTMP2    MOVE      ZERO,OVRCD
          READKS    OPTMPA2;XDOCT,XTYPE:
                           DOCSURN,MON1,MON2,MON3,MON4,MON5,MON6,MON7:
                           MON8,MON9,MON10,MON11,MON12,MON13
          GOTO      OVERCOND IF OVER
          RETURN
+
.==============================================================================
.
          INC       STD001IO
          INC       PATCOMN3
          INC       TFILENAM                * Generate Temp File Name
          INC       IBASEQIO/INC            * Sequential Numbers File
          INC       WEBERRIO/INC            * Web Server Error Logging
.
          INC       OPRCLIIO/INC
          INC       OPRCSCIO/INC
          INC       PATDRGIO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC

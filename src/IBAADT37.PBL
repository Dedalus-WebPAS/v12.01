.******************************************************************************
.* System         :  Patient Billing                                          *
.* Program        :  IBAADT37                                                 *
.* Description    :  Revenue breakdown Listing program                        *
.******************************************************************************
.* Date           :  07/10/2004                                               *
.* Author         :  J.Tan                                                    *
.* Function       :  Recreate for patrfnaf file (Revenue breakdown file)      *
.* Modifications  :                                                           *
.*                                                                            *
.* V12.00.01 14/05/2025  J.Tan          TSK 0955096                           *
.*           Added alpanumeric visit number generation                        *
.*                                                                            *
.* V10.12.01  15.01.2018  J.Tan   TSK 0848146                                 *
.*            Added CACCFEE=5 - Accumulation FI by Claim code                 *
.* V10.04.01  15/06/2014 Jill Parkinson CAR 301639                            *
.*            Moved call TFILENAM to INIT0000 and added extra KILL0000        *
.* V10.03.02  19/12/2012 Patrick Adair                             CAR 261630 *
.*                       Removed port number from temp file name              *
.*         V10.03.01 05/09/2012  J.Tan          CAR 267784                    *
.*                   Mods to check TCINDC19 Claim for Contract Eff.'Disc.From'*
.*         V10.01.01 01/02/2011 J.Tan  CAR 233051                             *
.*                   Added Contract ID to matrix                              *
.*                   V9.12.02 24/11/2009 Davin  CAR 210721                    *
.*                   Recompiled for CHKCMXPT - I*C on patcmxaf file           *
.*                   V9.12.01 08/09/2009 J.Tan  CAR 205303                    *
.*                   Added Casemix Contract ID                                *
.******************************************************************************
.
          INC       STD001FD
.
          INC       AAEDTRFD/INC
          INC       AAEDE1FD/INC
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       OUTDTRFD/INC
          INC       OUTSITFD/INC
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
          INC       PATCFAFD/INC
          INC       PMSCIDFD/INC
          INC       PATCMXFD/INC
          INC       PATDRGFD/INC
          INC       PATDTRFD/INC
          INC       PATHSPFD/INC
          INC       PATDSCFD/INC
          INC       PATFN1FD/INC
          INC       PATFX1FD/INC
          INC       PATMCHFD/INC
          INC       PATMI1FD/INC
          INC       PATINVFD/INC
          INC       PATITMFD/INC
          INC       PMSFCIFD/INC
          INC       PATFINFD/INC
          INC       PATRFNFD/INC
          INC       PATSRBFD/INC
          INC       PATOVBFD/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PMSVX1FD/INC
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
+
.
FINDFILE FILE      ASCII, FIXED=256
PATTEMP  IFILE     SQL, FIXED=63, KEY="U1-3+4-4,5-10,11-14,15-15,16-28,29-36,37-44,45-52"
.
. ----- Tempfile consists of following vars -----
.
. Name          Type      Length     Physical     Start   Description
. ----          ----      ------     --------     -----   -----------
.FINHOSP     DIM       3          3          1      HOSPITAL ID
.DFINSYS     DIM       1          1          4      SYSTEM INDICATOR
.                                                   1= AAE, 2=OUT, 3=INP, 4=GST
.FINSITE     DIM       6          6          5      OUTPATIENT SITE
.FINYEAR     DIM       4          4         11      FINANCIAL YEAR
.FINTYPE     DIM       1          1         15      FINANCIAL TYPE
.FINCODE     DIM       13         13        16      FINANCE CODE
.PINVDTE     DIM       8          8         29      INVOICE DATE
.PINVNO      DIM       8          8         37      INVOICE NUMBER
DUNIQKEY     DIM       8          8         45      UNIQUE KEY
.FINAMT      FORM      9.2        7         53      AMOUNT
FPERIOD      FORM      3          3         60      PERIOD
.                                           63      EOR
. ----- EOR 60 -----
.
.
PGSTTOT   FORM      1
CSTRTYR   DIM       8
CMXFLAG   FORM      1
CONTRCID  DIM       6
CEFFDATE  DIM       8
CNTRCEFR  FORM      1
PATSTCUR  DIM       4
PCFLAG    FORM      1

JRNLCOD  DIM    2
CODEFI   INIT   "FI"
CASHTYP  DIM    1
CASHDESC DIM    12
CRDFLAG FORM   1
CMCODE   DIM    9
SCRDFLAG FORM   1
SCASHDES DIM    12
SUBFIN   DIM    1
NEGONE   FORM    "-1"
NOPERIOD FORM   1
DAYFLG   FORM   1
DIM3     DIM    3
DIM4     DIM    4
DIM6     DIM    6
DIM8     DIM    8
DIM9     DIM    9
DIM13    DIM    13
DIM13A   DIM    13
DIM10    DIM    10
DIM16    DIM    16
DIM22    DIM    22
SDIM10   DIM    10
DIM10A   DIM    10
ENDFLG   FORM   1
FORM3    FORM   3
FORM82   FORM   8.2
HOSPFLAG FORM    1
HOSPID   DIM     3
HOSPNAME DIM       50
IBCNMHOS FORM   1
IBCNUGST FORM   1
AAE      INIT   "A&E"
OUT      INIT   "OUT"
INP      INIT   "INP"
INPAT    INIT   "Inpatient "
AAEPAT   INIT   "A&E       "
OUTPAT   INIT   "Outpatient"
ALLPAT   INIT   "All       "
INVOICE  INIT   "- Invoice "
CASH     INIT   "- Cash    "
JOURNAL  INIT   "- Journals"
ALL      INIT   "- All     "
.
DEFPATH  DIM    60
SJRNLCOD DIM    2
SJRNGCOD DIM    2
SCASHTYP DIM    1
SFINTYPE DIM    1
SFINSYS  FORM   1
SFINSITE DIM    5
STINDATE DIM     8
TODATE   DIM     8
TOTONLY  FORM    1
FROMDATE DIM     8
FRYEAR   DIM     2
MSCFLAG  DIM     1
MISGRP   DIM     3
.
RECFLAG  FORM    1
RECCNT   FORM    8
UNIQKEY  FORM    8
FNAMEI   DIM     8
FNAMES   DIM     8
GNDTOT   FORM    9.2
.
KEY26A   DIM     26
SAVKEY31 DIM     31
SKEY18   DIM     18
SKEY19   DIM     19
SKEY30   DIM     30
SP13     INIT    "             "
SUBOPT   FORM    1
SUBTOT   FORM    9.2
STATUS   FORM    1
SAVAMT   FORM    12.2
TOTAMT   FORM    12.2
OPCND    FORM    1
CMDLINE  DIM     80
.
PRGID     INIT      "IBAADT37"
PRGNAM    INIT      "Financial Summary Listing"
VERSION   INIT      "V12.01.00"
+
.
ML0000    CALL      INIT0000                     * program initialisation
          BRANCH    EXIT,ML9999
.
ML0100    CALL      OPTN0000
          COMPARE   ZERO,OPTION
          GOTO      ML9999 IF EQUAL
.
          MOVE      ONE,SUBOPT
          CALL      KDAT0000                     * keyin date range
          CALL      KIND0000                     * keyin starting invoice date
.
          MOVE      SP70,HOSPID
          IF        IBCNMHOS=1
            CALL      KHOS0000                   * Hospital id
            BRANCH    EXIT,ML0100
          ENDIF
.
          CALL      TOTL0000                     * Total only ?
ML0180    CALL      CONTQST                      * OK to continue ?
          BRANCH    CEXIT,ML1000,ML0100,ML9999   * yes,no,cancel
          GOTO      ML9999
.
ML1000    CALL      CRTT0000
          BRANCH    EXIT,ML9999
          CALL      INVN0000
          BRANCH    NOPERIOD,ML9999
          CALL      WFIN0000         * update patrfnaf
          CALL      PRNT0000         * Print
          CALL      KILL0000
          GOTO      ML0100
.
ML9999    CALL      KILL0000
          CHAIN     PGM
.
.         Program initialisation
.
INIT0000  CALL          DISPHEAD
.
          DISPLAY       *P64:24,"patcodes";
          OPEN          PATCODE1,"patcodes"
.
          DISPLAY       *P64:24,"pathspaf";
          OPEN          PATHSPA1,"pathspaf"
.
          DISPLAY       *P64:24,"patrfnaf";
          OPEN          PATRFNA2,"patrfnaf"
.
          DISPLAY       *P64:24,"patdschf";
          OPEN          PATDSCH1,"patdschf"
.
          DISPLAY       *P64:24,"pmsvx1af";
          OPEN          PMSVX1A1,"pmsvx1af"
.
          DISPLAY       *P64:24,"patdschf";
          OPEN          PATDSCH1,"patdschf"
.
          DISPLAY       *P64:24,"patinvrf";
          OPEN          PATINVR1,"patinvrf"
          OPEN          PATINVR2,"patinvrf"
.
          DISPLAY       *P64:24,"aaedtref";
          OPEN          AAEDTRE1,"aaedtref"
          OPEN          AAEDTRE2,"aaedtref"
.
          DISPLAY       *P64:24,"aaede1af";
          OPEN          AAEDE1A1,"aaede1af"
.
          DISPLAY       *P64:24,"outsitaf";
          OPEN          OUTSITA1,"outsitaf"
.
          DISPLAY       *P64:24,"patdtraf";
          OPEN          PATDTRA1,"patdtraf"
          OPEN          PATDTRA2,"patdtraf"
.
          DISPLAY       *P64:24,"patfn1af";
          OPEN          PATFN1A1,"patfn1af"
.
          DISPLAY       *P64:24,"patovbaf";
          OPEN          PATOVBA1,"patovbaf"
.
          DISPLAY       *P64:24,"patsrbaf";
          OPEN          PATSRBA1,"patsrbaf"
.
          DISPLAY       *P64:24,"patdschf";
          OPEN          PATDSCH1,"patdschf"
.
          DISPLAY       *P64:24,"patdrgaf";
          OPEN          PATDRGA1,"patdrgaf"
          OPEN          PATDRGA2,"patdrgaf"
.
          DISPLAY       *P64:24,"patitemn";
          OPEN          PATITEM1,"patitemn"
.
          DISPLAY       *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY       *P64:24,"patmchgf";
          OPEN          PATMCHG1,"patmchgf"
.
          DISPLAY       *P64:24,"pmsfciaf";
          OPEN          PMSFCIA1,"pmsfciaf"
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAMEI
.
          DISPLAY       *P64:24,"controlf";
          OPEN          CONTROLF,"controlf"
.
          READ          CONTROLF,TEN;*192,CACCFEE
          READ          CONTROLF,TEN9;*212,CFEETYP
          READ          CONTROLF,SEVENTY9;*68,PTCNMFEE
          READ          CONTROLF,ZERO;*43,IBCNMHOS:
                                      *85,IBCNUGST
INIT9999  RETURN
+
.
OPTN0000  CALL      DISPHEAD
          DISPLAY   *P1:4,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Revenue Breakdown List":
                    *P1:10,"Select Option ? ";
.
OPTN1000  KEYIN     *P17:10,*V2LON,OPTION:
                    *P17:10,*DV,OPTION
.
          BRANCH    OPTION,OPTN9999
          COMPARE   ZERO,OPTION
          GOTO      OPTN9999 IF EQUAL
          BEEP
          GOTO      OPTN1000
.
OPTN9999  RETURN
.
.         Keyin date range
.
KDAT0000  DISPLAY    *P1:13,*EF,"Starting Date : "
          MOVE       "13",CVERT
          MOVE       "17",CCOL
.
          OPEN       PATDRGA2,"patdrgaf"
          CALL       PATSTRYR
          UNPACK     CSTRTYR,XCENT,XYEAR,XMON,XDAY
          MOVE       XDAY,IDAY
          MOVE       XMON,IMON
          MOVE       XCENT,ICENT
          MOVE       XYEAR,IYEAR
          CALL       KEYDTE
          PACK       FROMDATE,ICENT,IYEAR,IMON,IDAY
          REP        " 0",FROMDATE
.
          DISPLAY    *P1:15,"  Ending Date :"
          MOVE       "15",CVERT
          MOVE       "17",CCOL
KDAT2000  MOVE       CDD,IDAY
          MOVE       CMM,IMON
          MOVE       CCC,ICENT
          MOVE       CYY,IYEAR
          CALL       KEYDTE
          PACK       TODATE WITH ICENT,IYEAR,IMON,IDAY
          REP        " 0",TODATE
.
          MATCH      FROMDATE,TODATE
          GOTO       KDAT9999 IF NOT LESS
.
          DISPLAY    *P40:24,*V2LON,"** End Date must be >= Start Date **",*W2;
          GOTO       KDAT2000
.
KDAT9999  RETURN
+
.         Keyin starting invoice date
.
KIND0000  DISPLAY    *P1:17,*EF,"Starting Invoice Date : "
          MOVE       "25",CCOL
          MOVE       "17",CVERT
.
          UNPACK     FROMDATE,XCENT,XYEAR,XMON,XDAY
          MOVE       XDAY,IDAY
          MOVE       XMON,IMON
          MOVE       XCENT,ICENT
          MOVE       XYEAR,IYEAR
          CALL       KEYDTE
          PACK       STINDATE,ICENT,IYEAR,IMON,IDAY
          REP        " 0",STINDATE
.
KIND9999  RETURN
+
TOTL0000  MOVE      ZERO,TOTONLY
          DISPLAY   *P1:21,*EL,*V2LON,"Total Only ? ",*HOFF:
                    "(",*V2LON,ANSY,*HOFF,"/",*V2LON,ANSN,*HOFF,")  ";
          KEYIN     ANS;
          REP       "yYnN",ANS
          CMATCH    "N",ANS
          GOTO      TOTL9999 IF EQUAL
          CMATCH    "Y",ANS
          GOTO      TOTL9000 IF EQUAL
          BEEP
          GOTO      TOTL0000
.
TOTL9000  MOVE      ONE,TOTONLY             * total only
TOTL9999  RETURN
+
.
CRTT0000  DISPLAY   *P1:12,*EL,"Building temporary file.  Please wait...";
          CALL      INDZ0000
          DISPLAY   *P1:12,*EF;
          BRANCH    OPCND OF CRTT9000
.
          MOVE      ZERO,EXIT
          GOTO      CRTT9999
.
CRTT9000  MOVE      ONE,EXIT
.
CRTT9999  RETURN
+
.
.         Process invoices
.
INVN0000  DISPLAY    *P1:24,*EL,*P10:24,"Invoice Date :";
.
          MOVE       ZERO,NOPERIOD
          MOVE       ZERO,RECCNT
          MOVE       ZERO,UNIQKEY
          PACK       KEY16,STINDATE,SP20
          CALL       RDSINV2
.
.             Main Loop
.
INVN1000  CALL       RDKINV2
          BRANCH     OVRCD OF INVN9999
          COMPARE    ONE,PTINCMIX                 
          GOTO       INVN1000 IF NOT EQUAL      * Not C/P
          MATCH      "1",PTINCRST 
          GOTO       INVN1000 IF EQUAL          * full credit notes
.
.         Ignore cancelled invoices
.
          MATCH      ZEROVISN,PINVADM
          GOTO       INVN1000 IF EQUAL
.
          ADD        ONE,RECCNT
          DISPLAY    *P30:24,*V2LON,PINVDTE,SP1,RECCNT;
.
          MOVE       ZERO,DAYFLG
          MOVE       SP70,DDATE
          PACK       KEY8,PINVADM
          CALL       RDMISS1
          BRANCH     OVRCD,INVN9200
          IF         ASTAT=3
            CALL       RDDSCH1
            MATCH      ADATE,DDATE
            IF         @EQUAL
              MOVE       ONE,DAYFLG
            ENDIF
          ENDIF
.
          MOVE       SP10,PMVXMHOS
          MOVE       PINVADM,KEY8
          CALL       RDPMVX11
          IF         IBCNMHOS=1
            MATCH      SP10,HOSPID
            IF         !@EQUAL
              MATCH      PMVXMHOS,HOSPID
              GOTO       INVN1000 IF NOT EQUAL
            ENDIF
          ENDIF
.
          MOVE       PTINCMCD,CMCODE
          MATCH      SP70,PTINCNID
          IF         @EQUAL
            PACK      KEY9,ACLAIM,AFUNDH
            CALL      GETCNEFF               * Get Contract Effective From
            BRANCH    EXIT,INVN1000
.
            PACK      PTMIPCMX,CMCODE,SP70
            PACK      KEY18,ACLAIM,AFUNDH,CMCODE,SP70
            MOVE      ADATE,CEFFDATE
            LOAD      CEFFDATE,CNTRCEFR,ADATE,DDATE
.
.           If Contract Effective From is 'For Discharges From', Discharged date
.           is blank and TCINDC19=D, default Effective date to Current date
.
            IF        CNTRCEFR=2
              PACK      DDATE,DDATE,SP70
              MATCH     SP70,DDATE
              IF        @EQUAL
                PACK      KEY5,ANSC,ANSL,ACLAIM
                CALL      RDCODE1
                IF        OVRCD=0
                  MATCH     "D",TCINDC19
                  IF        @EQUAL
                    PACK      CEFFDATE,CCC,CYY,CMM,CDD,SP70
                    REP       " 0",CEFFDATE
                  ENDIF
                ENDIF
              ENDIF
            ENDIF
.
            CALL      VALCMXFN              * get the contract ID (CONTRCID)
            BRANCH    EXIT,INVN1000
          ENDIF
.
.         Determine which system this invoice is for
.
          BRANCH     PINVSYS,INVN1000,INVN1000,INVN2000
          GOTO       INVN1000
.
.         Process lumpsum only
.
INVN2000  DISPLAY    *P50:24,"IP ";
          PACK       KEY22,PINVADM,PINVNO,SP6
          CALL       RDSDTRN1
INVN3000  CALL       RDKDTRN1
          BRANCH     OVRCD,INVN1000
.
          MATCH      PINVADM,TADMNO
          GOTO       INVN1000 IF NOT EQUAL
          MATCH      PINVNO,TREF
          GOTO       INVN1000 IF NOT EQUAL
.
.         Only interested in invoice section
.
          BRANCH     TRECTYPE,INVN4000
          GOTO       INVN3000
.
.         Inpatient accommodation
.
INVN4000  MOVE       ANSA TO FINTYPE
          MOVE       THREE,FINSYS
          MOVE       SP6,FINSITE
          UNPACK     PINVDTE WITH CCENT,CYEAR,CMON,CDAY
          MATCH      SP70,TCLMTYP
          IF         @EQUAL
            MOVE       PINVCLM,TCLMTYP
          ENDIF
.
          MOVE       ZERO,SAVAMT
          MOVE       PTDTLSPT,SAVAMT
          ADD        PTDTLSRB,SAVAMT
          COMPARE    ZERO,SAVAMT
          GOTO       INVN3000 IF EQUAL
.
          MOVE       ZERO,TOTAMT
          MOVE       SP70,PTHFBCAT
          PACK       KEY14,AFUNDH,AFNDTB
          CALL       RDFUND1
.
INVN5000  PACK       KEY30,PTINCNID,TCLMTYP,AFUNDH,PTHFBCAT,PTINCMCD,SP70
          COMPARE    ZERO,DAYFLG
          GOTO       INVN6000 IF EQUAL           * overnight
.
.         Daycase breakdown revenue
.
          CALL       RSPTSRB1
INVN5200  CALL       RKPTSRB1
          BRANCH     OVRCD,INVN8100
          PACK       SKEY30,PTSBCNID,PTSBCODE,PTSBFUND,PTSBTBLT,PTSBCSCD,SP70
          MATCH      KEY30,SKEY30
          GOTO       INVN8100 IF NOT EQUAL
          MOVE       PTSBBRCD,KEY3
          MOVE       PTSBAMNT,FINAMT
          ADD        PTSBAMNT,TOTAMT
          GOTO       INVN8000
.
.         Overnight breakdown revenue
.
INVN6000  CALL       RSPTOVB1
INVN6200  CALL       RKPTOVB1
          BRANCH     OVRCD,INVN8100
          PACK       SKEY30,PTOBCNID,PTOBCODE,PTOBFUND,PTOBTBLT,PTOBCSCD,SP70
          MATCH      KEY30,SKEY30
          GOTO       INVN8100 IF NOT EQUAL
          MOVE       PTOBBRCD,KEY3
          MOVE       PTOBAMNT,FINAMT
          ADD        PTOBAMNT,TOTAMT
.
INVN7000  PACK       FINCODE,ANSA,KEY3
          PACK       KEY5,CODEFI,KEY3
          CALL       RDCODE1
          BRANCH     OVRCD,INVN7800
          MATCH      ANSA,TCINDC2
          GOTO       INVN7800 IF NOT EQUAL
.
          BRANCH    CACCFEE,INVN7100:               * ward/bed claim
                            INVN7200:               * ward/adm. type
                            INVN7300:               * ward/claim/adm.type
                            INVN7400:               * ward/adm.type/claim
                            INVN7500                * claim
          PACK      FINCODE WITH ANSA,KEY3,TADMTYP,SP70
          GOTO      INVN8000
.
INVN7100  PACK      FINCODE WITH ANSA,KEY3,TDWARD,TCLMTYP,SP70
          GOTO      INVN8000
.
INVN7200  PACK      FINCODE,ANSA,KEY3,TDWARD,TADMTYP,SP70
          GOTO      INVN8000
.
INVN7300  PACK      FINCODE,ANSA,KEY3,TDWARD,TCLMTYP,TADMTYP,SP70
          GOTO      INVN8000
.
INVN7400  PACK      FINCODE,ANSA,KEY3,TDWARD,TADMTYP,TCLMTYP,SP70
          GOTO      INVN8000
.
INVN7500  PACK      FINCODE WITH ANSA,TCLMTYP,KEY3,SP70
          GOTO      INVN8000
.
.         Miscellaneous lumpsum breakdown
.
INVN7800  IF        PTCNMFEE = 1
            PACK      FINCODE,ANST,TCLMTYP,KEY3,SP70
          ELSE
            PACK      FINCODE,ANST,KEY3,SP70
          ENDIF
.
INVN8000  PACK       FINDATE,PINVDTE
          REP        " 0",FINDATE
          CALL       CFIN0000
          BRANCH     NOPERIOD,INVN9999
.
          COMPARE    SAVAMT,TOTAMT
          GOTO       INVN8300 IF LESS
          GOTO       INVN8200 IF NOT EQUAL
.
.         Just make sure this is the last record of breakdown revenue
.
          IF         DAYFLG=1
            CALL       RKPTSRB1
            BRANCH     OVRCD,INVN1000
            PACK       SKEY30,PTSBCNID,PTSBCODE,PTSBFUND,PTSBTBLT,PTSBCSCD,SP70
            MATCH      KEY30,SKEY30
            GOTO       INVN1000 IF NOT EQUAL
          ELSE
            CALL       RKPTOVB1
            BRANCH     OVRCD,INVN1000
            PACK       SKEY30,PTOBCNID,PTOBCODE,PTOBFUND,PTOBTBLT,PTOBCSCD,SP70
            MATCH      KEY30,SKEY30
            GOTO       INVN1000 IF NOT EQUAL
          ENDIF
          MOVE       ZERO,ENDFLG
          GOTO       INVN8200
.
INVN8100  MOVE       ONE,ENDFLG
INVN8200  DISPLAY    *P1:20,*EL,KEY30:
                     *P1:22,*EL,*V2LON,*B,"Visit Number: ",PINVADM:
                     " -Total Revenue breakdown is greater than Total Lumpsum.";
          DISPLAY    *P1:24,*EL;
          MOVE       "Overnight",KEY9
          IF         DAYFLG=1
            MOVE       "Daycase  ",KEY9
          ENDIF
          PRINT      *1,KEY9,"- Total Revenue breakdown (":
                     KEY30,") <> Total Lumpsum $":
                     SAVAMT
          DISPLAY    *P1:20,*EL,*P1:22,*EL;
          BRANCH     ENDFLG,INVN1000
.
INVN8300  BRANCH     DAYFLG,INVN5200
          GOTO       INVN6200                 * overnight
.
INVN9200  DISPLAY    *P1:24,*EL,KEY8,"- Missing admission record.";
          GOTO       INVN1000
INVN9999  RETURN
+
.*******************************************************************************
.*        PRNT0000 - Print records from tempfile                               *
.*******************************************************************************
PRNT0000  MOVE       "60",CLNO
          MOVE       ZERO,SUBTOT
          MOVE       ZERO,GNDTOT
          MOVE       ZERO,DAYFLG
          PACK       DIM13,SP20
          PACK       KEY52,SP70
          CALL       RDSTEMP
PRNT1000  CALL       RDKTEMP
          BRANCH     OVRCD,PRNT8000
.
          UNPACK     FINCODE,ANS,MISGRP            * misc.group/item code
.
          COMPARE    ZERO,FINAMT
          GOTO       PRNT1000 IF EQUAL
.
          MATCH      SP70,DIM13
          GOTO       PRNT2000 IF EQUAL
.
          MATCH      FINCODE,DIM13
          GOTO       PRNT2000 IF EQUAL
          CALL       PSUB0000
          BRANCH     TOTONLY,PRNT2000
.
PRNT2000  COMPARE    "59",CLNO
          CALL       HEAD0000 IF NOT LESS
.
          MOVE       ZEROVISN,PINVADM
          PACK       KEY8,PINVNO
          CALL       RDINV1
          BRANCH     TOTONLY,PRNT3000
          IF         IBCNMHOS=1
            PRINT      *1,FINHOSP,*10,FINYEAR;
          ELSE
            PRINT      *1,FINYEAR;
          ENDIF
.
          IF         OVRCD = 1
            PRINT      *30,FINCODE,*47,PINVDTE,*62,PINVNO:
                       *82,"********",*95,FINAMT
          ELSE
            PRINT      *30,FINCODE,*47,PINVDTE,*62,PINVNO,*82,PINVADM,*95,FINAMT
          ENDIF
          ADD        ONE,CLNO
.
PRNT3000  MOVE       FINCODE,DIM13
          ADD        FINAMT,SUBTOT
          ADD        FINAMT,GNDTOT
          GOTO       PRNT1000
.
PRNT8000  CALL       PSUB0000
          CALL       PTOT0000
          PRINT      *1,"** End of Report **"
.
PRNT9999  RETURN
+
.*******************************************************************************
PSUB0000  COMPARE    ZERO,SUBTOT
          GOTO       PSUB9999 IF EQUAL
          CALL       UND132
          PRINT      *1,"SUB-TOTAL - ",DIM13,*93,SUBTOT
          CALL       UND132
          MOVE       ZERO,SUBTOT
PSUB9999  RETURN
+
.*******************************************************************************
PTOT0000  COMPARE    ZERO,GNDTOT
          GOTO       PTOT9999 IF EQUAL
          PRINT      *1,"TOTAL : ",*93,GNDTOT
          CALL       UND132
          MOVE       ZERO,GNDTOT
PTOT9999  RETURN
+
.*******************************************************************************
.*        HEAD0000 - Heading                                                   *
.*******************************************************************************
HEAD0000  CALL       HEAD132
.
          UNPACK     FROMDATE,CCENT,CYEAR,CMON,CDAY
          CALL       PACDATE
          PRINT      *40,"Fromdate : ",CPCDATE;
          UNPACK     TODATE,CCENT,CYEAR,CMON,CDAY
          CALL       PACDATE
          PRINT      *65,"To : ",CPCDATE
.
          CALL       UND132
          PRINT      *1,"Year":
                     *30,"Financial Code",*47,"Invoice date",*62,"Invoice No":
                     *79,"Admission No.",*95,"    Amount"
          CALL       UND132
          ADD        THREE,CLNO
HEAD9999  RETURN
+
.*******************************************************************************
.       Creating an indexed file based on port
.*******************************************************************************
INDZ0000  
.
.       CHECK IF PREVIOUSLY EXISTS..IF SO DELETED IT 
.
          CALL          KILL0000
          DISPLAY       *P1:12,*EF
          CLEAR         CMDLINE
          APPEND        "isbuild ",CMDLINE
          APPEND        FNAMEI,CMDLINE
        APPEND  " 63 u1-3+4-4,5-10,11-14,15-15,16-28,29-36,37-44,45-52",CMDLINE
          RESET         CMDLINE
.
          EXECUTE      CMDLINE,TASKID
          MOVE         ZERO,OVRCD
          TRAP         OVERCOND IF IO
          OPEN         PATTEMP,FNAMEI
          TRAPCLR      IO
.
          MOVE         OVRCD,OPCND
          BRANCH       OPCND,INDZ8000
          GOTO         INDZ9999
.
INDZ8000  DISPLAY      *P20:24,*B,*V2LON:
                       "** The Index File Not Created **",*W2;
INDZ9999  RETURN
+
.*******************************************************************************
.       Deleting an indexed file based on port
.*******************************************************************************
KILL0000  CLOSE         PATTEMP
	  MOVE          ZERO,STATUS
.
          CLEAR         CMDLINE
          APPEND        "iserase ",CMDLINE
          APPEND        FNAMEI,CMDLINE
          RESET         CMDLINE
          EXECUTE       CMDLINE,TASKID
          DISPLAY       *P1:12,*EF
          RETURN
+
.*******************************************************************************
.         CFIN0000 - Find out which financial year and period this is in
.*******************************************************************************
CFIN0000  MOVE      ZERO,NOPERIOD
          UNPACK    FINDATE,DIM8
.
          REP       " 0",DIM8
          MATCH     "00000000",DIM8
          IF        @EQUAL
            MOVE      PINVDTE,DIM8
            REP       " 0",DIM8
            MOVE      DIM8,FINDATE
          ENDIF
.
          MATCH     FROMDATE,DIM8
          GOTO      CFIN9999 IF LESS
          MATCH     DIM8,TODATE
          GOTO      CFIN9999 IF LESS
.
          OPEN      PATDRGA2,"patdrgaf" * Open the date range file
          PACK      KEY14,FINDATE,SP6   * Position to this date
          CALL      RDSDRGA2            * Go to this date in the file
          CALL      RDKDRGA2            * Get the first record on or after date
          BRANCH    OVRCD,CFIN6000      * No records in file.
.
          MATCH     DRGFDTE,FINDATE     * Is the date before the from date ?
          GOTO      CFIN6000 IF LESS    * Yes. Date not found in file.
.
.         Period record found
.
          MOVE      DRGYR,FINYEAR       * Save financial year
          MOVE      DRGNUM,FORM2
          MOVE      FORM2,FOFFSET      * Save period offset
.
.         Check if this is in a prior financial year
.
          PACK      FINDATE,CCC,CYY,CMM,CDD
          REP       " 0",FINDATE
.
CFIN1000  PACK      KEY14,FINDATE,SP6   * Position to this date
          CALL      RDSDRGA2            * Go to this date in the file
          CALL      RDKDRGA2            * Get the first record on or after date
          BRANCH    OVRCD,CFIN7000      * No records in file.
.
          MATCH     DRGFDTE,FINDATE     * Is the date before the from date ?
          GOTO      CFIN7000 IF LESS    * Yes. Date not found in file.
.
          MATCH     DRGYR,FINYEAR       * Is this the same as the request date?
          GOTO      CFIN5000 IF EQUAL   * Yes. Continue Processing
.
.         If this the first day of the financial year, then let all invoiced
.         data go through as normal
.
          MATCH     "01",DRGNUM         * Is this the first period of year ?
          GOTO      CFIN3000 IF NOT EQUAL
.
          MATCH     FINDATE,DRGFDTE     * Is this date the start of period ?
          GOTO      CFIN3000 IF NOT EQUAL
.
.         Only allow invoices to go into the actual year.
.
          CMATCH    ANSA,FINTYPE
          GOTO      CFIN3000 IF NOT EQUAL
.
.         Invoice on the first day of the financial year. Allow to go into
.         previous year.
.
          GOTO      CFIN5000
.
.         Put the figures into the prior year column of report
.
CFIN3000  MOVE      TEN4,FOFFSET        * Put into the Prior year variable
          MOVE      DRGYR,FINYEAR       * Put into the current financial year
.
.         Post the data to the tempfile
.
CFIN5000  ADD       ONE,UNIQKEY
          MOVE      FOFFSET,FPERIOD
          MOVE      UNIQKEY,DUNIQKEY
          MOVE      SP10,FINHOSP
          IF        IBCNMHOS=1
            MOVE      PMVXMHOS,FINHOSP
          ENDIF
          PACK      KEY52,FINHOSP,FINSYS,FINSITE,FINYEAR,FINTYPE,FINCODE:
                          PINVDTE,PINVNO,DUNIQKEY,SP20
.
          CALL      WRTEMP 
.
          GOTO      CFIN9000            * Finished
.
.         The requested date does not exist in the date range file.
.
CFIN6000  UNPACK    FINDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          KEYIN     *P1:24,*EL,*V2LON,*B:
                    *DV,PINVADM," ** Date ",*DV,CPCDATE," not in Period Range":
                    " file. Hit <ENTER> to retry ",*EOFF,ANS;
.
.         CMATCH    EXITCHAR,ANS
          CMATCH    "E",ANS
          GOTO      CFIN8000 IF EQUAL
          GOTO      CFIN0000
.
.         The current date does not exist in the date range file.
.
CFIN7000  UNPACK    FINDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          KEYIN     *P1:24,*EL,*V2LON,*B:
                    *DV,PINVADM," ** Date ",*DV,CPCDATE," not in Period Range":
                    " file. Hit <ENTER> to retry ",*EOFF,ANS;
.
.         CMATCH    EXITCHAR,ANS
          CMATCH    "E",ANS
          GOTO      CFIN8000 IF EQUAL
          GOTO      CFIN1000
.
.         Finished update of fees invoice file
.
CFIN8000  MOVE      ONE,NOPERIOD
CFIN9000  CLOSE     PATDRGA2
CFIN9999  RETURN
+
.*******************************************************************************
.         WFIN0000 - Write new figures to Fees Invoiced files
.*******************************************************************************
WFIN0000  
WFIN1000  DISPLAY   *P1:24,*EL,"Do you want new figures ":
                    "written to the Revenue Fees Invoiced files? ":
                    "(",*V2LON,ANSY,*HOFF,"/",*V2LON,ANSN,*HOFF,") ";
          KEYIN     ANS;
          REP       UPPLOW,ANS
          CMATCH    ANSN,ANS
          GOTO      WFIN9999 IF EQUAL
          CMATCH    ANSY,ANS
          GOTO      WFIN2000 IF EQUAL
          BEEP
          GOTO      WFIN1000
.
WFIN2000  CALL      COPY0000                * backup files
.
          MOVE      ZERO,TOTAMT
          PACK      SAVKEY31,SP70
          PACK      KEY52,SP70
          CALL      RDSTEMP
WFIN2200  CALL      RDKTEMP                 * get first record
          BRANCH    OVRCD,WFIN2600
.
          PACK      KEY31,FINHOSP,FINSYS,FINSITE,FINYEAR,FINTYPE,FINCODE,FPERIOD
          CALL      CLPR0000                * clear this period
          GOTO      WFIN2200
.
WFIN2600  CALL      RDSTEMP
          CALL      RDKTEMP                 * get first record
          ADD       FINAMT,TOTAMT           * accumulate FI amount
          PACK      KEY31,FINHOSP,FINSYS,FINSITE,FINYEAR,FINTYPE,FINCODE,FPERIOD
          MOVE      KEY31,SAVKEY31          * save new key
.
WFIN3000  CALL      RDKTEMP                 * loop through tempfile
          BRANCH    OVRCD,WFIN4000
.
          PACK      KEY31,FINHOSP,FINSYS,FINSITE,FINYEAR,FINTYPE,FINCODE,FPERIOD
.
          MATCH     KEY31,SAVKEY31          * check if key has changed
          IF        @EQUAL
            ADD       FINAMT,TOTAMT         * accumulate FI amount
          ELSE
            MOVE      TOTAMT,SAVAMT         * total to be saved to FI file
            MOVE      FINAMT,TOTAMT         * start new FI total
            CALL      UPFN0000              * update patrfnaf
          ENDIF
          MOVE      KEY31,SAVKEY31          * save new key
          GOTO      WFIN3000                * get next tempfile record
.
WFIN4000  MOVE      TOTAMT,SAVAMT         * total to be saved to FI file
          CALL      UPFN0000              * update patrfnaf
WFIN9999  RETURN
+
.*******************************************************************************
.         CLPR0000 - Clear the fees invoiced for the selected period
.*******************************************************************************
CLPR0000  UNPACK    KEY31,KEY15,KEY13,KEY3
          MOVE      KEY3,FORM3              * get period
          PACK      KEY28,KEY15,SP70
          CALL      RSPTRFN2
CLPR1000  CALL      RKPTRFN2
          BRANCH    OVRCD,CLPR9999
.
          STORE     ZERO,FORM3,FINMTH1,FINMTH2,FINMTH3:
                               FINMTH4,FINMTH5,FINMTH6:
                               FINMTH7,FINMTH8,FINMTH9:
                               FINMTH10,FINMTH11,FINMTH12:
                               FINMTH13,FLSTYR
          CALL      UPPTRFN2                * update record
          GOTO      CLPR1000
.
CLPR9999  RETURN
+
.*******************************************************************************
.         UPFN0000 - Update patrfnaf
.*******************************************************************************
UPFN0000  COMPARE   ZERO,SAVAMT             * don't update with zero amounts
          GOTO      UPFN9999 IF EQUAL
.
UPFN1000  UNPACK    SAVKEY31,KEY14,KEY1,DIM16
.
          UNPACK    SAVKEY31,KEY28,KEY3     * get key to patrfnaf & period
          CALL      RDPTRFN2                * read patrfnaf
          IF        OVRCD<>1
            GOTO      UPFN2000              * update existing record
          ENDIF
.
          UNPACK    KEY28,FINHOSP,DFINSYS,FINSITE,FINYEAR,FINTYPE,FINCODE
          MOVE      DFINSYS,FINSYS
          MOVE      ZERO,FINPER
          MOVE      ZERO,FINMTH1
          MOVE      ZERO,FINMTH2
          MOVE      ZERO,FINMTH3
          MOVE      ZERO,FINMTH4
          MOVE      ZERO,FINMTH5
          MOVE      ZERO,FINMTH6
          MOVE      ZERO,FINMTH7
          MOVE      ZERO,FINMTH8
          MOVE      ZERO,FINMTH9
          MOVE      ZERO,FINMTH10
          MOVE      ZERO,FINMTH11
          MOVE      ZERO,FINMTH12
          MOVE      ZERO,FINMTH13
          MOVE      ZERO,FLSTYR
          MOVE      SP30,FSPARE
          CALL      WRPTRFN2                * write new record with zeros
          GOTO      UPFN1000                * then update with amount
.
UPFN2000  MOVE      KEY3,FORM3
          STORE     SAVAMT,FORM3,FINMTH1,FINMTH2,FINMTH3:
                                 FINMTH4,FINMTH5,FINMTH6:
                                 FINMTH7,FINMTH8,FINMTH9:
                                 FINMTH10,FINMTH11,FINMTH12:
                                 FINMTH13,FLSTYR
          CALL      UPPTRFN2                * update with new amount
UPFN9999  RETURN
+
.********************************************************************
.*                          COPY0000                                *
.*  Backup file patrfnaf                                            *
.********************************************************************
COPY0000  CALL      DEFT0000                * get default path
.
          PACK      CMDLINE,SP70,SP70
          CLEAR     CMDLINE
          APPEND    "cp `ldat patrfnaf.dat` ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    "sptrfnaf.dat",CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      COPY9500
          ENDIF
.
          PACK      CMDLINE,SP70,SP70
          CLEAR     CMDLINE
          APPEND    "cp `ldat patrfnaf.idx` ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    "sptrfnaf.idx",CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      COPY9500
          ENDIF
.
COPY9000  MOVE      ZERO,EXIT
          GOTO      COPY9999
.
COPY9500  MOVE      ONE,EXIT
          DISPLAY   *P1:24,*EL,*B,"Unable to backup file(s).  ":
                    "(Error: ",TASKID,")  ";
          CALL      HITENTER
COPY9999  RETURN
+
.**********************************************************************
.         Get the default directory
.**********************************************************************
DEFT0000  EXECUTE   "ldat patrfnaf.dat > temp.txt",TASKID
.
          PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  DISPLAY   *P1:24,*EL,*B,"Error finding patrfnaf.  ";
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
DEFT9999  RETURN
+
.        ======================================================================
.        KHOS0000: Keyin hospital ID
.        ======================================================================
KHOS0000 DISPLAY    *P1:19,*EL,"Hospital Id : ";
         MOVE       ZERO,HOSPFLAG
         MOVE       TEN5,ECOL
         MOVE       "19",EVERT
         MOVE       SP3,CKYIDEF3
         MOVE       ZERO,CKYIMAND           * Not Mandatory
         OPEN       PATHSPA1,"pathspaf"
.
         CALL       PATHSPKY
         BRANCH     EXIT,KHOS1000,KHOS2000
         MOVE       PTHSHOSP,HOSPID
         MOVE       PTHSNAME,HOSPNAME
         MOVE       ONE,HOSPFLAG
         MOVE       ZERO,EXIT
         GOTO       KHOS9999
.
KHOS1000 MOVE       "All Hospitals",HOSPNAME
         MOVE       SP10,PTHSHOSP
         MOVE       ZERO,EXIT
         GOTO       KHOS9999
.
KHOS2000 MOVE       ONE,EXIT
KHOS9999 RETURN
.
.*******************************************************************************
.         Subroutine to key in a valid date
.
KEYDTE    DISPLAY   *PCCOL:CVERT,*EL
          MOVE      IDAY,CDAY
          MOVE      IMON,CMON
          MOVE      IYEAR,CYEAR 
          MOVE      ICENT,CCENT
          MOVE      "1",CDEFDTE
          MOVE      "0",CHIGHLT
.
          CALL      KEYDATE
.
          MOVE      CDAY,DD
          MOVE      CMON,MM
          MOVE      CYEAR,YY
          MOVE      CCENT,CC
          MOVE      DD,IDAY
          MOVE      MM,IMON
          MOVE      YY,IYEAR
          MOVE      CC,ICENT
.          
          RETURN
+
.*******************************************************************************
.
WRTEMP   PACK      DPINVADM,PINVADM
         RESET     KEY52
         WRITE     PATTEMP,KEY52;KEY52,FINAMT,FPERIOD
         RETURN  
.  
RDSTEMP  RESET     KEY52
         READ      PATTEMP,KEY52;;
         RETURN 
.
RDKTEMP  MOVE      ZERO,OVRCD
         READKS    PATTEMP;FINHOSP,DFINSYS,FINSITE,FINYEAR,FINTYPE,FINCODE:
                           PINVDTE,PINVNO,DUNIQKEY,FINAMT,FPERIOD
         GOTO      OVERCOND IF OVER
         MOVE      DFINSYS,FINSYS
         MOVE      DUNIQKEY,UNIQKEY
         RETURN  
.
.
          INC       PATSTRYR
          INC       PATHSPKY
          INC       VALCMXFN
          INC       VALCONTR
          INC       GETCNEFF
          INC       TFILENAM                     * Generate Temp File Name
          INC       AAEDTRIO/INC
          INC       AAEDE1IO/INC
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       OUTDTRIO/INC
          INC       OUTSITIO/INC
          INC       OUTBB1IO/INC
          INC       PATCFAIO/INC
          INC       PMSCIDIO/INC
          INC       PATCMXIO/INC
         INCLUDE    PATDRGIO/INC
         INC       PATDSCIO/INC
         INC       PATFN1IO/INC
         INC       PATFX1IO/INC
         INC       PATDTRIO/INC
         INC       PATHSPIO/INC
         INC       PATMCHIO/INC
         INC       PATMI1IO/INC
         INC       PATRFNIO/INC
         INC       PATSRBIO/INC
         INC       PATOVBIO/INC
         INC       PATCODIO/INC
         INC       PATINVIO/INC
         INC       PATITMIO/INC
         INC       PMSFCIIO/INC
         INC       PMSVX1IO/INC
          INC       WEBERRIO/INC                 * Web Server Error Logging
         INCLUDE    STD001IO
.
ENDIT    CHAIN      PGM
         STOP

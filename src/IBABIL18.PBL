. *****************************************************************************
. *              P R O G R A M  S U M M A R Y                                 *
. *              -------------  -------------                                 *
. *                                                                           *
. *     PROGRAM NAME:     IBABIL18                                            *
. *                                                                           *
. *     FUNCTION:         MISCELLANEOUS INVOICE LISTING                       *
. *                                                                           *
. *     DATE WRITTEN:     25/08/86                                            *
. *                                                                           *
. *     AUTHOR:           MALCOLM HAYSE (I.B.A)                               *
. *                                                                           *
. *     MODIFICATIONS:                                                        *
. *      V12.00.02  01/09/2025 J.Tan         TSK 0966174                      *
. *                 Mod checking for Credit note invoice                      *
. *      V12.00.01  22/05/2025  Alina Bhari   TSK 0955096                     *
. *                 Added alpanumeric visit number generation -CHKOVR         *
. *       V11.01.01 03/08/2021 J.Tan         TSK 0909134                      *
. *                 Fixed printing itemno for Outpatient and Emergency        *
. *       V10.13.01 07/12/2013 J.Tan         TSK 0859743                      *
. *                 Mod for OTRECTYP=6 for Theatre item                       *
. *       V10.03.03 08/01/2013  J.Tan            CAR 237952                   *
. *                 Mods keyin Misc.item without checking Inactive & date     *
. *       V10.03.02 29/05/2012  J.Tan            CAR 262280                   *
. *                 Fixed printing Invoice date                               *
. *       V10.03.01 16/04/2012  Steve Armstrong  CAR 263414                   *
. *                 Fixed keyin of item number to PACK MCHARGE up with spaces *
. *                 so that KEY29 is set correctly prior to calling PATMCHRD. *
. *****************************************************************************
. *       V10.01.01 17/12/2010  Mike Laming   CAR 233046                      *
. *                 Mods to Misc.Charges PATMCHFD - Key change, logic changes *
. *****************************************************************************
. *       V10.00.01 30/04/2010  J.Tan     CAR 220887                          *
. *                 Mods checking for Active/Inactive of Misc.charge          *
. *****************************************************************************
. *       V9.09.01  06/02/2008  Jill Habasque CAR 160650                      *
. *                 Mods to allow a blank miscellaneous item                  *
. *****************************************************************************
. *       V9.04.01  04/10/2004  Lina Vo      CAR 53798                        *
. *                 Changed program to use PTHSDCLM instead PTCNDCLM for      *
. *                 Multi Hospital                                            *
. *****************************************************************************
. *       V9.03.03  12/09/2003  Lina Vo      CAR 40497                        *
. *                 Changed index and read of patmchgf to include effective   *
. *                 date.                                                     *
. *       V9.03.02  12/08/2003  Lina Vo      CAR 41196                        *
. *                 Recompiled for big change in PATSELFN.                    *
. *       V9.03.01  03/06/2003  Dean Cramer  CAR 38200                        *
. *                 Tidy Outpatient headings                                  *
. *****************************************************************************
. *       V9.00.01  02/08/2001  Dean Cramer                                   *
. *                 Standardise from to range default                         *
. *****************************************************************************
. *        V5.08.01  15/08/2000  Caleb Sharman                                *
. *                  Changed Health Fund table variables to 8 chars           *
. *                       V5.01 13/04/89 Graeme Williams                      *
. *                             New index added to Dtrans                     *
. *                             SRF 5645                                      *
. *                       V5.02 05/05/89 Graeme Williams                      *
. *                             Added PINVCADM to inv file                    *
. *                             SRF 4219                                      *
. *                       V5.03 14/05/89 Denio Di.Paola                       *
. *                             Alter report to allow "8"                     *
. *                             character Admission Number.                   *
. *                             Put in STD001FD                               *
. *                       V5.04 20/05/89 J.Tan                                *
. *                             Added a Date range option                     *
. *                             SRF 100083                                    *
. *                       V5.05 06/06/89 J.Tan                                *
. *                             Mods Locking master file                      *
. *                             (PATIOMAS)     SRF 100809                     *
. *                       V5.06 08/06/89 Graeme Williams                      *
. *                             Fixed I*I from V5.04                          *
. *                             SRF 100083                                    *
. *                       V5.07 30/06/89 DIG                                  *
. *                           Re-Compiled for changes to                      *
. *                           OUTSITFD                                        *
. *                                                                           *
. *                    V5.01.01 05/07/89 S. O'Gorman                          *
. *                             Converted for N.Z.                            *
. *                                                                           *
. *                    V5.01.02 20/11/89 J.Tan                                *
. *                             Add financial option in                       *
. *                             PATSELFN.   SRF 102252                        *
. *                    V5.01.03 23/05/90 J.Tan  SRF 104901                    *
. *                             Mods PATITMFD                                 *
. *                   V5.01.121 27/11/90 Glen Hobby                           *
. *                            Recompiled for changes to                      *
. *                            PATMX1FD                                       *
.*        V5.01.122 12/06/92  Graeme Williams               SRF 114865        *
.*                  Changes for 9 character misc items                        *
.*        V5.03.B1  06/06/95  Paul Howells                  SRF 136735        *
.*                  Recompiled for PATSELFN                                   *
.*        V5.07.00  05/10/99  Andrew Peel                   SRF 635028        *
.*                  Y2K changes                                               *
.******************************************************************************
.
          INC           STD001FD
.
          INC           IBAPASFD/INC
          INC           IBASECFD/INC
          INC           IBACONFD/INC
.
          INC           PATHSPFD/INC
          INC       PATMA1FD/INC
          INC           PATMCHFD/INC
          INC           PATITMFD/INC
          INC           PATINVFD/INC
          INC           PATDTRFD/INC
          INC           AAEDTRFD/INC
          INC           OUTDTRFD/INC
          INC           PATCODFD/INC
          INC           PATSELDT/INC
          INC           OUTSITFD/INC
+
.
.               WORK AREA
.
AFIRST          DIM      6
ALAST           DIM      6
.
DIM8            DIM      8
.
EOYMON          DIM      2
EOYDAY          DIM      2
EOYYEAR         DIM      2
.
KEY21A          DIM      21
KEY26A          DIM      26
.
PNAME           DIM      30
PINVMISC        FORM     8.2
PTCNDCLM        DIM      3
.
STKEY           DIM      8
SFIRST          FORM     8
SP14            INIT     "              "
.
MISCAMTT         FORM      8.2
TINVTOTL         FORM      8.2
TINVMISC         FORM      8.2
.
FIVE9           FORM     "59"
FIRST           DIM      8
LAST            DIM      8
FROMDAT         DIM      8
TODAT           DIM      8
.
.
.
.      EXTRA VARIABLES
.
STMAST          DIM     2
CDESC           DIM     20
CODE            DIM     2
COTHSFN         FORM    1
TIMEIS          DIM     5
CURRDATE        DIM     8
.
XCENT1          DIM     2
XDAY1           DIM     2
XMON1           DIM     2
XYEAR1          DIM     2
.
XCENT2          DIM     2
XDAY2           DIM     2
XMON2           DIM     2
XYEAR2          DIM     2
VERT            FORM    2
.
VERSION   INIT      "V12.01.00"
PRGID     INIT      "IBABIL18"
PRGNAM    INIT      "Miscellaneous Invoice Listing"
+
.
.         START OF PROGRAM
.
          DISPLAY       *ES;
          CALL          DISPHEAD              * call display header routine
          CALL          IBACLOCK
.
          DISPLAY       *P56:24,"Opening patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY       *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY       *P64:24,"pathspaf";
          OPEN          PATHSPA1,"pathspaf"
.
          DISPLAY       *P64:24,"patinvrf";
          OPEN          PATINVR1,"patinvrf"
.
          DISPLAY       *P64:24,"patinvrf";
          OPEN          PATINVR2,"patinvrf"
.
          DISPLAY       *P64:24,"patitemn";
          OPEN          PATITEM1,"patitemn"
.
          DISPLAY       *P64:24,"patmchgf";
          OPEN          PATMCHG1,"patmchgf"
.
          DISPLAY       *P64:24,"patdtraf";
          OPEN          PATDTRA1,"patdtraf"
.
          DISPLAY       *P64:24,"aaedtref";
          OPEN          AAEDTRE1,"aaedtref"
.
          DISPLAY       *P64:24,"patcodes";
          OPEN          PATCODE1,"patcodes"
.
          DISPLAY       *P64:24,"outsitaf";
          OPEN          OUTSITA1,"outsitaf"
.
OPFIL     DISPLAY       *P64:24,"controlf";
          OPEN          CONTROLF,"controlf"
          READ          CONTROLF,TWENTY;*193,PTCNDCLM
.
          MATCH         SP3,PTCNDCLM
          GOTO          INIT1000 IF NOT EQUAL
.
          MOVE          "0  ",PTCNDCLM
.
INIT1000  PACK          CURRDATE,CCC,CYY,CMM,CDD
          REP           " 0",CURRDATE
+
.
.        START OF PROGRAM
.
.           MAIN        MENU
.
.             SELECT OPTION
.
START   MOVE       ZERO,CPAGENO
.
        DISPLAY   *P1:4,*EF:
                  *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                  *P1:5,*V2LON,ONE,*HOFF," = By Invoice Number":
                  *P1:6,*V2LON,TWO,*HOFF," = By Invoice Date"
.
REKSTRT KEYIN     *P1:8,"Option : ",*EL,*V2LON,OPTION
.
        COMPARE   ZERO TO OPTION
        GOTO      ENDIT IF EQUAL
.
BRNCH   BRANCH    OPTION OF KINVR,KDATR
        BEEP
        GOTO      REKSTRT
.
NOMAST  DISPLAY   *P1:24,*EL,*P20:24,*V2LON:
                  "** No Patients on File **",*W3;
        GOTO      START
+
.
.               PRINT VIA INVOICE NUMBER
.
KINVR
        KEYIN      *P1:4,*EF:
                   *P1:6,"Starting Invoice Number : ":
                   *V2LON,*DE,*JR,FIRST
.
        RESET      FIRST
        GOTO       MASTN0 IF NOT EOS
.
        MOVE       "00000001" TO FIRST
.
MASTN0  DISPLAY    *P27:6,*V2LON,FIRST
.
        KEYIN      *P1:8,"  Ending Invoice Number : ":
                   *V2LON,*DE,*JR,LAST
        RESET      LAST
        GOTO       MASTN1 IF NOT EOS
.
        MOVE       "99999999" TO LAST
.
MASTN1
        DISPLAY    *P27:8,*V2LON,LAST
.
        MOVE       SP8,STKEY
        RESET      FIRST
        GOTO       MAIOPT IF EOS  
.
        MOVE       FIRST,SFIRST
        SUB        ONE,SFIRST
        MOVE       SFIRST,STKEY
.
        COMPARE    ZERO,SFIRST
        GOTO       MAIOPT IF NOT LESS
        MOVE       SP8,STKEY
.
        MOVE       FIRST,SFIRST
        SUB        ONE,SFIRST
        GOTO        MAIOPT
.
.
.       VIA INVOICE DATE
.
KDATR   KEYIN       *P1:4,*EF:
                    *P1:6,"Starting Invoice Date : ":
                    *P1:8,"  Ending Invoice Date : "
.
.       Keyin fromdate
.
KFRDAT  UNPACK      SP8 WITH CCENT,CYEAR,CMON,CDAY
        MOVE        "6",CVERT
        MOVE        "24",CCOL
        MOVE        ZERO,CHIGHLT
        MOVE        ZERO,CCENTRY
        CALL        KEYDATE
.
        MATCH       SP2,CDAY
        GOTO        START IF EQUAL
.
        DISPLAY     *P24:6,*EL,*V2LON,CDAY,*HOFF,SLASH,*V2LON,CMON:
                    *HOFF,SLASH,*V2LON,CCENT,CYEAR
.
        PACK        FROMDAT WITH CCENT,CYEAR,CMON,CDAY
        REP         " 0",FROMDAT
        UNPACK      FROMDAT WITH XCENT1,XYEAR1,XMON1,XDAY1
.
        MATCH       FROMDAT,CURRDATE
        GOTO        INVDTE1 IF LESS
.
.
.       Keyin todate
.
KTODAT  MOVE        "8",CVERT
        MOVE        "24",CCOL
        CALL        KEYDATE
.
        DISPLAY     *P24:8,*EL,*V2LON,CDAY,*HOFF,SLASH,*V2LON,CMON:
                    *HOFF,SLASH,*V2LON,CCENT,CYEAR
.
        PACK        TODAT WITH CCENT,CYEAR,CMON,CDAY
        REP         " 0",TODAT
.
        UNPACK      TODAT WITH XCENT2,XYEAR2,XMON2,XDAY2
.
        MATCH       TODAT,CURRDATE
        GOTO        INVDTE2 IF LESS
.
        MATCH       FROMDAT,TODAT
        GOTO        MAIOPT IF NOT LESS
.
        DISPLAY    *P1:24,*B,*EL,*V2LON,"** From Date must be <= ":
                   "To Date **",*W2;
        GOTO       KFRDAT
.
INVDTE1 DISPLAY    *P1:24,*B,*EL,*V2LON,"** From Date must be <= ":
                   "current Date **",*W2;
        GOTO       KFRDAT
.
INVDTE2 DISPLAY    *P1:24,*B,*EL,*V2LON,"** To Date must be Less Than or":
                   " Equal to current Date **",*W2;
        GOTO       KTODAT
.
.      MAIN MENU
.
MAIOPT  DISPLAY    *P1:10,"Item code               :"
        GOTO       KITEM
+
.             INPUT ITEM CODE
.
KITEM   MOVE        SP9,MCHARGE
        KEYIN       *P27:10,*EL,*V2LON,MCHARGE
        PACK        MCHARGE,MCHARGE,SP9
.
SFIELD  KEYIN      *P1:12,*EL,"Ok to continue (":
                              *V2LON,"Y",*HOFF,"/":
                              *V2LON,"N",*HOFF:
			      ") ? ",*V2LON,ANS; 
.
        REP        UPPLOW,ANS
.
        CMATCH     "Y" TO ANS
        GOTO       OKCONT IF EQUAL
.
        CMATCH     "N" TO ANS
        GOTO       BRNCH IF EQUAL
.
        BEEP
        GOTO       SFIELD
.
OKCONT  CALL       PATSELFN
        BRANCH     EXIT,START
.
OKCONT2 KEYIN      *P1:24,*EL,"Ok to continue (":
                              *V2LON,"Y",*HOFF,"/":
                              *V2LON,"N",*HOFF,"/":
			      *V2LON,"C",*HOFF,") ? ",*V2LON,ANS; 
.
        REP        UPPLOW,ANS
.
        CMATCH     "C" TO ANS
        GOTO       ENDIT IF EQUAL
.
        CMATCH     "Y" TO ANS
        GOTO       MASTN3 IF EQUAL
.
        CMATCH     "N" TO ANS
        GOTO       BRNCH IF EQUAL
.
        BEEP
        GOTO       OKCONT2
.
MASTN3  MOVE       ZERO,TINVTOTL
        MOVE       ZERO,TINVMISC
        MOVE       "60",CLNO
.
        DISPLAY    *P1:24,*EL,*P10:24,*EL,"Invoice ## :",*P39:24,"Scanning :";
.
        BRANCH     OPTION OF OPTINUM,OPTIDAT
.
.       By invoice number
.
OPTINUM MOVE       STKEY,KEY8
        CALL       RDSINV1
NXTNUM  CALL       RDKINV1
        GOTO       CHKOVR
.
.       By invoice date
.
OPTIDAT PACK       KEY16 WITH FROMDAT,SP10
        CALL       RDSINV2
NXTDAT  CALL       RDKINV2
.
CHKOVR  BRANCH     OVRCD OF ENDP
.
        MATCH      ZEROVISN,PINVADM
        GOTO       NEXTN IF EQUAL        * Invoice Number Cancelled
.
        DISPLAY     *P70:24,PINVNO;
.
        COMPARE     FSTSYS,PINVSYS
        GOTO        NEXTN IF LESS
.
        COMPARE     PINVSYS,FEDSYS
        GOTO        NEXTN IF LESS
.
        MATCH       FSTSITE,PINVSITE
        GOTO        NEXTN IF LESS
.
        MATCH       PINVSITE,FEDSITE
        GOTO        NEXTN IF LESS
.
        MATCH       "1",PTINCRST
        GOTO        NEXTN IF EQUAL       * Fully Credited
.
        MATCH       SP3,FSTDEPT
        GOTO        CNT10 IF EQUAL
.
        MOVE        PINVSITE,KEY6
        CALL        RDSITA1
        BRANCH      OVRCD,NEXTN
.
        MATCH       FSTDEPT,OSTSYST
        GOTO        NEXTN IF LESS
.
        MATCH       OSTSYST,FEDDEPT
        GOTO        NEXTN IF LESS
.
CNT10   BRANCH      OPTION OF CNT20,CNT30
.
CNT20   MATCH       PINVNO,LAST         * Check with the ending invoice number
        GOTO        ENDP IF LESS
        GOTO        CNT40
.
CNT30   MATCH       PINVDTE,TODAT       * Check with the todate
        GOTO        ENDP IF LESS
.
CNT40   MOVE        PINVUR,PURNO
        MOVE        PURNO,KEY8
        CALL        RDMAST1
.
        UNPACK      PINVDTE WITH XCENT,XYEAR,XMON,XDAY
        DISPLAY     *P50:24,PINVNO;
        CALL        DISPDA
.
NEXTN   BRANCH      OPTION OF NXTNUM,NXTDAT
.
+
.
.           OVER CONDITION FOUND..PRINT LAST LINE OF -- AND DISPLAY MESSAGE
.
ENDP     COMPARE      ZERO,CPAGENO
         GOTO         NOMAST IF EQUAL
         CALL         PAGEND
.
         MATCH        SP70,MCHARGE
         IF           !@EQUAL
           PRINT        *55,"TOTALS",*64,TINVTOTL,*79,TINVMISC
         ELSE
           PRINT        *55,"TOTAL",*89,TINVMISC
         ENDIF
.
         PRINT        *N,"  ***  END OF REPORT  ***"
         DISPLAY      *P1:24,*EL,*P40:24,*EL,*V2LON:
                      "** Report Generation Completed **",*W;
         GOTO         START
+
.
.     PRINT ACTUAL DATA 
.
DISPDA  COMPARE    "57",CLNO
        CALL       HEAD IF NOT LESS
.
        MOVE       PGNAME,ANS
        PACK       PNAME WITH PTITL,SP1,ANS,DOT,PSNAME
.
        MOVE       ZERO,PINVMISC
        PACK       KEY22 WITH PINVADM,PINVNO,SP6
.
        BRANCH     PINVSYS OF AAEDTR,OUTDTR,INPDTR,OUTDTR,NOTIV
.
.       Inpatient  invoice
.
INPDTR  CALL       RDSDTRN1
NXTINP  CALL       RDKDTRN1
        BRANCH     OVRCD OF ENDT
.
        MATCH      PINVNO,TREF
        GOTO       ENDT IF NOT EQUAL
.
        REP        " 0",PTDTCRST
        MATCH      "0",PTDTCRST
        GOTO       NXTINP IF NOT EQUAL      * Credit note
.
.      THEATRE ARE TREATED AS MISCELLANEOUS ITEMS
.
        MATCH      SP70,MCHARGE
        IF         @EQUAL
          BRANCH     TRECTYPE OF NXTINP,NXTINP,INPMISC
        ELSE
          BRANCH     TRECTYPE OF NXTINP,INPMISC,INPMISC
        ENDIF
        GOTO       NXTINP
.
INPMISC MATCH      SP70,MCHARGE
        IF         !@EQUAL
          MATCH      MCHARGE,TITEMNO
          GOTO       NXTINP IF NOT EQUAL
        ELSE
          PRINT      *1,PINVNO,*11,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR:
                     *23,PNAME,*55,PINVADM,*69,TITEMNO,*85,TPATAMTT
        ENDIF
.
        ADD        TPATAMTT,PINVMISC
        GOTO       NXTINP
.
.       Outpatient invoice
.
OUTDTR  MOVE       "out",OSTFILE
        CALL       RDSITA1
.
        CLOSE      OUTDTRO1
        PACK       CFNAME,OSTFILE,FILDTRO1
        OPEN       OUTDTRO1,CFNAME
.
        CALL       RDSDTRO1
.
NXTOUT  CALL       RDKDTRO1
        BRANCH     OVRCD OF ENDT
.
        MATCH      OTINVNO,PINVNO
        GOTO       ENDT IF NOT EQUAL
.
        REP        " 0",OTDTCRST
        MATCH      "0",OTDTCRST
        GOTO       NXTOUT IF NOT EQUAL      * Credit note
.
        BRANCH     OTRECTYP OF OUTMISC
        COMPARE    SIX,OTRECTYP
        GOTO       OUTMISC IF EQUAL      * THEATRE ARE TREATED AS MISC.ITEMS
        GOTO       NXTOUT
.
OUTMISC MATCH      SP70,MCHARGE
        IF         !@EQUAL
          MATCH      MCHARGE,OTITEMNO
          GOTO       NXTOUT IF NOT EQUAL
        ELSE
          PRINT      *1,PINVNO,*11,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR:
                     *23,PNAME,*55,PINVADM,*69,OTITEMNO,*85,OTPATAMT
        ENDIF
.
        ADD        OTPATAMT,PINVMISC
        GOTO       NXTOUT
.
.       Accident and Emergency
.
AAEDTR 
        CALL       RDSDTRE1
NXTAAE  CALL       RDKDTRE1
        BRANCH     OVRCD OF ENDT
.
        MATCH      ATINVNO,PINVNO
        GOTO       ENDT IF NOT EQUAL
.
        REP        " 0",ATDTCRST
        MATCH      "0",ATDTCRST
        GOTO       NXTAAE IF NOT EQUAL      * Credit note
.
        BRANCH     ATRECTYP OF AAEMISC
        GOTO       NXTAAE
.
AAEMISC MATCH      SP70,MCHARGE
        IF         !@EQUAL
          MATCH      MCHARGE,ATITEMNO
          GOTO       NXTAAE IF NOT EQUAL
        ELSE
          PRINT      *1,PINVNO,*11,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR:
                     *23,PNAME,*55,PINVADM,*69,ATITEMNO,*85,ATPATAMT
        ENDIF
.
        ADD        ATPATAMT,PINVMISC
        GOTO       NXTAAE
.
ENDT    COMPARE    ZERO,PINVMISC
        GOTO       NOTIV IF EQUAL
.
        UNPACK     PINVDTE WITH XCENT,XYEAR,XMON,XDAY
        MATCH      SP70,MCHARGE
        IF         !@EQUAL
          PRINT      *1,PINVNO,*11,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR:
                     *23,PNAME,*55,PINVADM:
                     *64,PINVAMT,*79,PINVMISC
        ENDIF
.
        DISPLAY    *P29:24,*V2LON,PINVNO;
        ADD        ONE,CLNO
.
        ADD        PINVAMT,TINVTOTL
        ADD        PINVMISC,TINVMISC
.
NOTIV   RETURN
+
.
.          LAST LINE ON THE PAGE
.
PAGEND    MATCH     SP70,MCHARGE
          IF        !@EQUAL
            PRINT     "----------------------------------------------":
                      "-------------------------------------------"
          ELSE
            PRINT     "---------------------------------------------------":
                      "------------------------------------------------"
          ENDIF
          ADD       ONE,CLNO
          RETURN
+
.
.                 HEADINGS FOR OUTPUT
.
HEAD      ADD       ONE,CPAGENO
          CLOCK     TIME,TIMEIS
.
          COMPARE   ONE,CPAGENO
          CALL      PAGEND IF NOT EQUAL
.
          PRINT     *F,*1,PRGID,*40,CNAME,*116,"DATE : ":
                    CDATE:
                    *N,*1,VERSION:
                    *40,"*** ",PRGNAM," ***":
                    *116,"TIME : ",*123,TIMEIS:
                    *N,*116,"PAGE : ",*128,CPAGENO;
.
          BRANCH    OPTION OF BYINVC
.
          PRINT     *N,*40,"STARTING INVOICE DATE : ":
                            XDAY1,SLASH,XMON1,SLASH,XCENT1,XYEAR1:
                    *N,*40,"  ENDING INVOICE DATE : ":
                            XDAY2,SLASH,XMON2,SLASH,XCENT2,XYEAR2,*N
          GOTO      BRPRNT
.
BYINVC    PRINT     *N,*40,"STARTING INVOICE NUMBER : ",FIRST:
                    *N,*40,"  ENDING INVOICE NUMBER : ",LAST,*N
.
BRPRNT    MATCH     SP70,MCHARGE
          IF        !@EQUAL 
            PRINT     *40,"Miscellaneous Item : ",MCHARGE,"   ",MDESC,*N
          ELSE
            PRINT     *N
          ENDIF
.
          CALL      PRTSEL
          MATCH     SP70,MCHARGE
          IF        !@EQUAL
            PRINT     *1," Invoice",*9,"   Invoice":
                      *23,"Patient Name",*55,"Admission":
                      *64,"    Invoice",*77,"Miscellaneous":
                      *N,"    ##",*9,"     Date",*55,"    ##":
                      *64,"      Total",*77,"        Total"
          ELSE
            PRINT     *1," Invoice",*9,"   Invoice":
                      *23,"Patient Name",*55,"Admission":
                      *65,"Misc Item":
                      *74,"           ",*87,"Miscellaneous":
                      *N,"    ##",*9,"     Date",*55,"    ##":
                      *65,"         ":
                      *74,"           ",*87,"        Total"
          ENDIF
.
          CALL      PAGEND
          MOVE      TEN1,CLNO
          RETURN
+
PRTSEL    COMPARE   ZERO,FSTSYS
          GOTO      AEHEAD IF NOT EQUAL
.
          PRINT     *40,"CONSOLIDATED REPORT",*N,*N;
          COMPARE   ZERO,FSTSYS
          RETURN    IF EQUAL
.
AEHEAD    COMPARE   ONE,FSTSYS
          GOTO      OPHEAD IF NOT EQUAL
.
          PRINT     *40,"ACCIDENT & EMERGENCY REPORT",*N,*N;
          COMPARE   ONE,FSTSYS
          RETURN    IF EQUAL
.
OPHEAD    COMPARE   TWO,FSTSYS
          GOTO      IPHEAD IF NOT EQUAL
.
          PRINT     *40,"OUTPATIENT REPORT",*N;
          MATCH     SP6,FSTSITE
          GOTO      ALSITE IF NOT EQUAL
.
          PRINT     *40,"ALL SITES";
          GOTO      OPDEPT
.
ALSITE    PRINT     *40,"SITE ",FSTSITE;
.
OPDEPT    MATCH     SP3,FSTDEPT
          GOTO      ALDEPT IF NOT EQUAL
.
          PRINT     *54,"ALL DEPARTMENTS",*N,*N;
          GOTO      PRTSEL9
.
ALDEPT    PRINT     *54,"DEPARTMENT ",FSTDEPT,*N,*N;
          GOTO      PRTSEL9
.
IPHEAD    COMPARE   THREE,FSTSYS
          GOTO      OTHEAD IF NOT EQUAL
.
          PRINT     *40,"INPATIENT REPORT",*N,*N;
          GOTO      PRTSEL9
.
OTHEAD    PRINT     *40,"OTHER FINANCIAL REPORT",*N,*N;
.
PRTSEL9   RETURN 
.
         INC       PATCODKY
         INCLUDE    PATSELFN
         INC        PATMCHRD                * Miscellaneous Charges read routine
.
.        INCLUDE    IBADATE
.
         INC       PATHSPIO/INC
         INC       PATMA1IO/INC
         INC       PATCODIO/INC
         INC       PATINVIO/INC
         INC       PATITMIO/INC
         INC       PATMCHIO/INC
         INC       PATDTRIO/INC
         INC       OUTDTRIO/INC
         INC       AAEDTRIO/INC
.
         INC       OUTSITIO/INC
         INC       IBAPASIO/INC
         INC       IBASECIO/INC
         INCLUDE    STD001IO
.
ENDIT    CHAIN      PGM
         STOP


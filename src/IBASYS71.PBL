.******************************************************************************
.* System         : Common                                                    *
.* Program        : IBASYS71.PBL                                              *
.* Name           : Postcode File Master Listing                              *
.******************************************************************************
.* Date           : 03/04/1997                                                *
.* Author         : An IBA programmer                                         *
.* Function       : Print ibapostf records                                    *
.* Modifications  :                                                           *
.*        V10.03.01 17/11/2011  Mike Laming     CAR 240184                    *
.*                  Changes to IBAPOSTF Post Code table - added State to Keys *
.*       V9.03.01   22/05/2003 Lina Vo   CAR 337504                           *
.*                  Added new IBPOASGC                                        *
.*                                                                            *
.******************************************************************************
+
          INC       STD001FD
.
          INC       IBAPOSFD/INC            * Postcode file
.
. ----- Program Variables -----
.
ENDPOST   DIM       4                       * End postcode
ENDSUBR   DIM       45                      * End suburb
.
POSTCODE  DIM       4                       * Postcode
PRTRCCNT  FORM      5                       * Print record counter
.
STRTPOST  DIM       4                       * Start postcode
STRTSUBR  DIM       45                      * Start suburb
SUBURB    DIM       45                      * Suburb
.
. ----- Program Constants -----
.
SP50      INIT      "                                                  "
UNDLN45   INIT      "_____________________________________________"
.
PRGID     INIT      "IBASYS71"
PRGNAM    INIT      "Postcode File Master Listing"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                   ML0000                                   *
.*                                 Main Module                                *
.******************************************************************************
ML0000    CALL      INIT0000                * Initialise variables & open files
          BRANCH    EXIT,ML9999
.
ML1000    CALL      OPTN0000                * Select option
          BRANCH    EXIT,ML9999
          BRANCH    OPTION,ML2000,ML4000
.
ML2000    CALL      POST0000                * Keyin the postcode range
          BRANCH    EXIT,ML1000
.
          CALL      CONTQST                 * Ok to Continue (Y/N/C) ?
          BRANCH    CEXIT,ML3000,ML2000,ML1000
.
ML3000    CALL      PPST0000                * Print the postcode range
          GOTO      ML1000
.
ML4000    CALL      SUBR0000                * Keyin the suburb range
          BRANCH    EXIT,ML1000
.
          CALL      CONTQST                 * Ok to Continue (Y/N/C) ?
          BRANCH    CEXIT,ML5000,ML4000,ML1000
.
ML5000    CALL      PSUB0000                * Print the suburb range
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                  INIT0000              Called by: ML0000   *
.*                      Initialise Variables & Open Files                     *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen header
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      IBAPOST1,"ibapostf"
          TRAPCLR   IO
          IF        OVRCD=1
            DISPLAY   *P1:24,*EL,*B,"Postcode file ibapostf not found.  ";
            CALL      HITENTER
            GOTO      INIT9500
          ENDIF
.
          CLOSE     IBAPOST1
          DISPLAY   *P56:24,"Opening ibapostf";
          OPEN      IBAPOST1,"ibapostf"
          OPEN      IBAPOST2,"ibapostf"
.
          MOVE      ONE,CNOUNDLN
.
INIT9000  MOVE      ZERO,EXIT
          GOTO      INIT9999
.
INIT9500  MOVE      ONE,EXIT
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000              Called by: ML0000   *
.*                                Select Option                               *
.******************************************************************************
OPTN0000  MOVE      ZERO,OPTION
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = By Postcode":
                    *P1:6,*V2LON,"2",*HOFF," = By Suburb":
                    *P1:8,"Select Option : ";
OPTN1000  KEYIN     *P17:8,*V2LON,*DV,UNDLN2,*P17:8,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN9000,OPTN9000
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                  POST0000              Called by: ML0000   *
.*                          Keyin The Postcode Range                          *
.******************************************************************************
POST0000  DISPLAY   *P1:10,*EF,"Starting Postcode : ":
                    *P1:11,"Ending Postcode   : ";
          MOVE      SP4,POSTCODE
.
. ----- Keyin start postcode -----
.
          MOVE      SP4,STRTPOST            * Clear the start postcode
          MOVE      "21",CCOL
          MOVE      "10",CVERT
.
          KEYIN     *PCCOL:CVERT,*EL,*DV,UNDLN4:
                    *PCCOL:CVERT,*V2LON,POSTCODE:
                    *PCCOL:CVERT,*DV,POSTCODE;
.
          ENDSET    POSTCODE
          APPEND    SP4,POSTCODE
          RESET     POSTCODE
.
          MATCH     SP4,POSTCODE
          GOTO      POST1000 IF EQUAL       * Spaces entered
.
          CMATCH    EXITCHAR,POSTCODE
          GOTO      POST9500 IF EQUAL       * Exitchar entered
.
          MOVE      POSTCODE,STRTPOST       * Start postcode
          DISPLAY   *PCCOL:CVERT,*EL,*V2LON,STRTPOST;
          GOTO      POST5000
.
POST1000  MOVE      SP4,STRTPOST            * Start postcode
          DISPLAY   *PCCOL:CVERT,*EL,*V2LON,"Start";
.
. ----- Keyin end postcode -----
.
POST5000  MOVE      SP4,ENDPOST             * Clear the end postcode
          MOVE      "21",CCOL
          MOVE      "11",CVERT
.
          KEYIN     *PCCOL:CVERT,*EL,*DV,UNDLN4:
                    *PCCOL:CVERT,*V2LON,*RV,POSTCODE:
                    *PCCOL:CVERT,*DV,POSTCODE;
.
          ENDSET    POSTCODE
          APPEND    SP4,POSTCODE
          RESET     POSTCODE
.
          MATCH     SP4,POSTCODE
          GOTO      POST6000 IF EQUAL       * Spaces entered
.
          MATCH     STRTPOST,POSTCODE
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Ending Postcode After the Starting ":
                      "Postcode.  ";
            CALL      HITENTER
            GOTO      POST5000
          ENDIF
.
          CMATCH    EXITCHAR,POSTCODE
          GOTO      POST9500 IF EQUAL       * Exitchar entered
.
          MOVE      POSTCODE,ENDPOST        * End postcode
          DISPLAY   *PCCOL:CVERT,*EL,*V2LON,ENDPOST;
          GOTO      POST9000
.
POST6000  MOVE      "~~~~",ENDPOST          * End postcode
          DISPLAY   *PCCOL:CVERT,*EL,*V2LON,"Finish";
.
POST9000  MOVE      ZERO,EXIT
          GOTO      POST9999
.
POST9500  MOVE      ONE,EXIT
POST9999  RETURN
+
.******************************************************************************
.*                                  PPST0000              Called by: ML0000   *
.*                          Print The Postcode Range                          *
.******************************************************************************
PPST0000  MOVE      ZERO,PRTRCCNT           * Reset the print record counter
          DISPLAY   *P1:24,*EL,"Printing : ";
.
          CALL      HPST0000                * Print the postcode header
          PACK      KEY56,STRTPOST,SP50                               *C-240184
          CALL      RSIBPOS1                * Position on & read a postcode
PPST1000  CALL      RKIBPOS1                  file record
          BRANCH    OVRCD,PPST8000
.
          MATCH     ENDPOST,IBPOPCOD
          GOTO      PPST2000 IF EQUAL       * Postcode = end postcode
          GOTO      PPST8000 IF NOT LESS    * Postcode < end postcode
.
PPST2000  DISPLAY   *P12:24,*V2LON,IBPOPCOD;
.
          COMPARE   "60",CLNO
          CALL      HPST0000 IF NOT LESS    * Print the postcode header
.
          PRINT     *1,"|",*5,IBPOPCOD,*12,"|",*14,IBPOSUBR:
                    *60,"|",*63,IBPOSTAT,*68,"| ",IBPOASGC,*80,"|"
          ADD       ONE,CLNO
          ADD       ONE,PRTRCCNT            * Increment the print record cnt
          GOTO      PPST1000
.
PPST8000  CALL      UND80
          PRINT     *N:
                    *1,PRTRCCNT," postcodes printed.":
                    *N,*N:
                    *1,"*** End of Report ***"
.
PPST9000  MOVE      ZERO,EXIT
PPST9999  RETURN
+
.******************************************************************************
.*                                  HPST0000              Called by: ML0000   *
.*                          Print The Postcode Header                         *
.******************************************************************************
HPST0000  IF        CPAGENO<>0
            CALL      UND80
          ENDIF
          CALL      HEAD80
.
          PRINT     *20,"Starting Postcode : ";
          MATCH     SP4,STRTPOST
          IF        @EQUAL
            PRINT     *41,"Start"
          ELSE
            PRINT     *41,STRTPOST
          ENDIF
.
          PRINT     *20,"Ending Postcode   : ";
          MATCH     "~~~~",ENDPOST
          IF        @EQUAL
            PRINT     *41,"Finish"
          ELSE
            PRINT     *41,ENDPOST
          ENDIF
.
          PRINT     *N;
          CALL      UND80
          PRINT     *1,"|",*3,"Postcode",*12,"|",*14,"Suburb":
                    *60,"|",*62,"State",*68,"| ABS ASGC",*80,"|"
          CALL      UND80
          ADD       "4",CLNO
.
HPST9000  MOVE      ZERO,EXIT
HPST9999  RETURN
+
.******************************************************************************
.*                                  SUBR0000              Called by: ML0000   *
.*                           Keyin The Suburb Range                           *
.******************************************************************************
SUBR0000  DISPLAY   *P1:10,*EF,"Starting Suburb : ":
                    *P1:11,"Ending Suburb   : ";
          MOVE      SP50,SUBURB
.
. ----- Keyin start suburb -----
.
          MOVE      SP50,STRTSUBR           * Clear the start suburb
          MOVE      "19",CCOL
          MOVE      "10",CVERT
.
          KEYIN     *PCCOL:CVERT,*EL,*DV,UNDLN45:
                    *PCCOL:CVERT,*V2LON,SUBURB:
                    *PCCOL:CVERT,*DV,SUBURB;
.
          ENDSET    SUBURB
          APPEND    SP50,SUBURB
          RESET     SUBURB
.
          MATCH     SP50,SUBURB
          GOTO      SUBR1000 IF EQUAL       * Spaces entered
.
          CMATCH    EXITCHAR,SUBURB
          GOTO      SUBR9500 IF EQUAL       * Exitchar entered
.
          MOVE      SUBURB,STRTSUBR         * Start suburb
          DISPLAY   *PCCOL:CVERT,*EL,*V2LON,STRTSUBR;
          GOTO      SUBR5000
.
SUBR1000  MOVE      SP50,STRTSUBR           * Start suburb
          DISPLAY   *PCCOL:CVERT,*EL,*V2LON,"Start";
.
. ----- Keyin end suburb -----
.
SUBR5000  MOVE      SP50,ENDSUBR            * Clear the end suburb
          MOVE      "19",CCOL
          MOVE      "11",CVERT
.
          KEYIN     *PCCOL:CVERT,*EL,*DV,UNDLN45:
                    *PCCOL:CVERT,*V2LON,*RV,SUBURB:
                    *PCCOL:CVERT,*DV,SUBURB;
.
          ENDSET    SUBURB
          APPEND    SP50,SUBURB
          RESET     SUBURB
.
          MATCH     SP50,SUBURB
          GOTO      SUBR6000 IF EQUAL       * Spaces entered
.
          MATCH     STRTSUBR,SUBURB
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Ending Suburb After the Starting ":
                      "Suburb.  ";
            CALL      HITENTER
            GOTO      SUBR5000
          ENDIF
.
          CMATCH    EXITCHAR,SUBURB
          GOTO      SUBR9500 IF EQUAL       * Exitchar entered
.
          MOVE      SUBURB,ENDSUBR          * End suburb
          DISPLAY   *PCCOL:CVERT,*EL,*V2LON,ENDSUBR;
          GOTO      SUBR9000
.
SUBR6000  MOVE      "~~~~~~~~~~~~~~~~~~~~",ENDSUBR * End suburb
          DISPLAY   *PCCOL:CVERT,*EL,*V2LON,"Finish";
.
SUBR9000  MOVE      ZERO,EXIT
          GOTO      SUBR9999
.
SUBR9500  MOVE      ONE,EXIT
SUBR9999  RETURN
+
.******************************************************************************
.*                                  PSUB0000              Called by: ML0000   *
.*                           Print The Suburb Range                           *
.******************************************************************************
PSUB0000  MOVE      ZERO,PRTRCCNT           * Reset the print record counter
          DISPLAY   *P1:24,*EL,"Printing : ";
.
          CALL      HSUB0000                * Print the suburb header
          PACK      KEY56,STRTSUBR,SP50                               *C-240184
          CALL      RSIBPOS2                * Position on & read a suburb
PSUB1000  CALL      RKIBPOS2                  file record
          BRANCH    OVRCD,PSUB8000
.
          MATCH     ENDSUBR,IBPOSUBR
          GOTO      PSUB2000 IF EQUAL       * Suburb = end suburb
          GOTO      PSUB8000 IF NOT LESS    * Suburb < end suburb
.
PSUB2000  DISPLAY   *P12:24,*V2LON,IBPOSUBR;
.
          COMPARE   "60",CLNO
          CALL      HSUB0000 IF NOT LESS    * Print the suburb header
.
          PRINT     *1,"|",*5,IBPOPCOD,*12,"|",*14,IBPOSUBR:
                    *60,"|",*63,IBPOSTAT,*68,"| ",IBPOASGC,*80,"|"
          ADD       ONE,CLNO
          ADD       ONE,PRTRCCNT            * Increment the print record cnt
          GOTO      PSUB1000
.
PSUB8000  CALL      UND80
          PRINT     *N:
                    *1,PRTRCCNT," suburbs printed.":
                    *N,*N:
                    *1,"*** End of Report ***"
.
PSUB9000  MOVE      ZERO,EXIT
PSUB9999  RETURN
+
.******************************************************************************
.*                                  HSUB0000              Called by: ML0000   *
.*                           Print The Suburb Header                          *
.******************************************************************************
HSUB0000  IF        CPAGENO<>0
            CALL      UND80
          ENDIF
          CALL      HEAD80
.
          PRINT     *15,"Starting Suburb : ";
          MATCH     SP50,STRTSUBR
          IF        @EQUAL
            PRINT     *33,"Start"
          ELSE
            PRINT     *33,STRTSUBR
          ENDIF
.
          PRINT     *15,"Ending Suburb   : ";
          MATCH     "~~~~~~~~~~~~~~~~~~~~",ENDSUBR
          IF        @EQUAL
            PRINT     *33,"Finish"
          ELSE
            PRINT     *33,ENDSUBR
          ENDIF
.
          PRINT     *N;
          CALL      UND80
          PRINT     *1,"|",*3,"Postcode",*12,"|",*14,"Suburb":
                    *60,"|",*62,"State",*68,"| ABS ASGC",*80,"|"
          CALL      UND80
          ADD       "4",CLNO
.
HSUB9000  MOVE      ZERO,EXIT
HSUB9999  RETURN
.
+
.==============================================================================
.
          INC       STD001IO
.
          INC       IBAPOSIO/INC            * Postcode file
+
.


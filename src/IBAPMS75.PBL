.***************************************************************************
.* System    :   Inpatient                                                 *
.* Program   :   IBAPMS75                                                  *
.* Desc      :   Cross Consultation Report                                 *
.***************************************************************************
.* Date      :   17/10/2007                                                *
.* Author    :   Jill Habasque                                             *
.* Function  :   This program will print a report of Cross Consultations   *
.*               for selected dates/consultants                            *
.***************************************************************************
. V10.02.01  08/07/2011 Glenn Mikoska     CAR 246133
.            Move sp70 to hoscode
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       IBACONFD/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATCRCFD/INC
          INC       PMSHCPFD/INC
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC
          INC       PMSPX2FD/INC
          INC       PATVISFD/INC
          INC       PMSVX1FD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
CMBSSTAT  DIM       1
DOCCODE   DIM       10
DOCNAME   DIM       25
DSPMONTH  DIM       9
ENDDATE   DIM       8
ENDSURG   DIM       10
ESURDESC  DIM       40
HOSCODE   DIM       3
PATNAME   DIM       28
STRTDATE  DIM       8
STRTSURG  DIM       10
SSURDESC  DIM       40
THEATRES  DIM       1
.
JAN       INIT      "January"
FEB       INIT      "February"
MAR       INIT      "March"
APRIL     INIT      "April"
MAY       INIT      "May"
JUNE      INIT      "June"
JULY      INIT      "July"
AUG       INIT      "August"
SEPT      INIT      "September"
OCT       INIT      "October"
NOV       INIT      "November"
DEC       INIT      "December"
.
PRGID     INIT      "IBAPMS75"
PRGNAM    INIT      "Cross Consultation Report"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"pmshcpaf";
          OPEN      PMSHCPA1,"pmshcpaf"
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"patcrcaf";
          OPEN      PATCRCA5,"patcrcaf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA1,"patvisaf"
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          MOVE      ZERO,CLNO
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
OPTN0000  MOVE      ZERO,CEXIT
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          MOVE      SP70,HOSCODE
.
          IF        IBCNMHOS = 1
            OPEN      PATHSPA1,"pathspaf"
.
            DISPLAY   *P5:5,*EF,"Hospital Id              : ";
            MOVE      THIRTY2,ECOL
            MOVE      FIVE,EVERT
            MOVE      SP3,CKYIDEF3
            MOVE      ONE,CKYIMAND               * set for mandatory
            CALL      PATHSPKY                   * get hospital
            COMPARE   ONE,EXIT                   * exit selected
            GOTO      OPTN9999 IF EQUAL
.
            MOVE      KEY3,HOSCODE
            DISPLAY   *P32:5,*EL,PTHSNAME;
          ENDIF
.
          DISPLAY   *P5:6,"Starting Date            : ":
                    *P5:7,"Ending Date              : "
          CALL      STRT0000                  * start date
          BRANCH    EXIT,OPTN9999
.
          CALL      ENDD0000                  * end date
          BRANCH    EXIT,OPTN0000
.
          DISPLAY   *P5:8,"Starting Consultant Code    : ":
                    *P5:9,"Ending Consultant Code      : "
          CALL      KSSU0000             * Keyin start Consultant
          CALL      KESU0000             * Keyin ending Consultant
.
OPTN9999  RETURN
+
.****************************************************************************
.*                               PROC0000                                   *
.*                       Process report                                     *
.****************************************************************************
PROC0000  CALL      HEAD0000
.
          MATCH     "Start",STRTSURG
          IF        @EQUAL
            MOVE      SP70,STRTSURG
          ENDIF
.
          MATCH     "Finish",ENDSURG
          IF        @EQUAL
            MOVE      SP70,ENDSURG
          ENDIF
.
          PACK      KEY39,STRTDATE,SP70
          CALL      RSPTCRC5
PROC1000  CALL      RKPTCRC5
          BRANCH    OVRCD,PROC9999
.
          MATCH     PTCRDATE,ENDDATE
          GOTO      PROC9999 IF LESS
.
          MATCH     SP70,STRTSURG
          IF        !@EQUAL
            MATCH     PTCRDOCT,STRTSURG
            GOTO      PROC1500 IF EQUAL
            GOTO      PROC1000 IF LESS
          ENDIF
.
          MATCH     SP70,ENDSURG
          IF        !@EQUAL
            MATCH     PTCRDOCT,ENDSURG
            GOTO      PROC1500 IF EQUAL
            GOTO      PROC1000 IF NOT LESS
          ENDIF
.
PROC1500  PACK      KEY8,PTCRBILL,SP70
          CALL      RDVISA1
          BRANCH    OVRCD,PROC1000
.
          PACK      KEY8,PTCRBILL,SP70
          CALL      RDPMVX11
          BRANCH    OVRCD,PROC1000
.
          MATCH     HOSCODE,PMVXMHOS
          GOTO      PROC1000 IF NOT EQUAL
.
          PACK      KEY8,PVIURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,PROC1000
          CALL      PATN0000
.
          PACK      KEY10,PTCRDOCT,SP70
          CALL      RDPMHCP1
          CALL      FDOC0000
.
          UNPACK    PTCRDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
PROC3000  COMPARE   FIFTY5,CLNO
          CALL      HEAD0000 IF NOT LESS
          PRINT     *N,*1,"|",PATNAME,*30,"|",PVIURNO,*42,"|",PTCRBILL:
                    *53,"|",DOCNAME,*90,"|",PTCRMISC,*132,"|";
.
          ADD       ONE,CLNO
          GOTO      PROC1000
.
PROC9999  CALL      DASH0000
          PRINT     *N,"***** End of Report *****"
          RETURN
+
.****************************************************************************
.*                               STRT0000                                   *
.*                       enter start date                                   *
.****************************************************************************
STRT0000  MOVE      THIRTY2,CCOL
          MOVE      SIX,CVERT
          MOVE      CCC,CCENT
          MOVE      SP2,CYEAR
          MOVE      SP2,CMON
          MOVE      SP2,CDAY
          MOVE      ZERO,CHIGHLT
          MOVE      ZERO,CDEFDTE
          CALL      KEYDATE
.
          MATCH     SP2,CDAY
          GOTO      STRT9000 IF EQUAL
          BRANCH    OVRCD,STRT9000
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDATE
          CALL      PACDATE
          MOVE      FALSE,EXIT
          GOTO      STRT9999
.
STRT9000  MOVE      TRUE,EXIT
          GOTO      STRT9999
.
STRT9999  RETURN
+
.****************************************************************************
.*                               ENDD0000                                   *
.*                       enter end date                                     *
.****************************************************************************
ENDD0000  MOVE      THIRTY2,CCOL
          MOVE      SEVEN,CVERT
          MOVE      CCC,CCENT
          UNPACK    STRTDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      ZERO,CHIGHLT
          MOVE      ZERO,CDEFDTE
          CALL      KEYDATE
.
          MATCH     SP2,CDAY
          GOTO      ENDD5000 IF NOT EQUAL
          MOVE      TRUE,EXIT
          GOTO      ENDD9999
.
ENDD5000  PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDATE
          CALL      PACDATE
          MATCH     STRTDATE,ENDDATE
          GOTO      ENDD9200 IF LESS
          GOTO      ENDD9999
.
ENDD9000  DISPLAY   *P1:24,*EL,*B,"Invalid date - ";
          CALL      HITENTER
          GOTO      ENDD0000
.
ENDD9200  DISPLAY   *P1:24,*EL,*B,"TO date cannot be before FROM date. ";
          CALL      HITENTER
          GOTO      ENDD0000
.
ENDD9999  RETURN
+
.**********************************************************************
.*                         KSSU0000                                   *
.*                  Keyin the starting Consultant                     *
.**********************************************************************
.
KSSU0000  MOVE      SP10,STRTSURG
          MOVE      "32",ECOL
          MOVE      "08",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP70,CKYIDEF6
          CALL      PMSHCPKY
          BRANCH    EXIT,KSSU0500,KSSU0000
.
          MOVE      PMHCHCPC,STRTSURG
          GOTO      KSSU1000
.
KSSU0500  MOVE      "Start ",STRTSURG
          DISPLAY   *P32:8,*V2LON,STRTSURG
          CLEAR     SSURDESC
          GOTO      KSSU9999
.
KSSU1000  PACK      KEY10,STRTSURG,SP6
          CALL      RDPMHCP1
          CALL      FDOC0000             * Format doctors name
.
          MOVE      PACFNAME,SSURDESC
          DISPLAY   *P32:8,*EL,*V2LON,STRTSURG:
                    *P40:8,*HOFF,SSURDESC
          GOTO      KSSU9999
.
KSSU4000
          BEEP
          DISPLAY  *P1:24,*EL,"Code is not on file. ";
          CALL     HITENTER
          GOTO     KSSU0000
.
KSSU9999  RETURN
.
.
.**********************************************************************
.*                         KESU0000                                   *
.*                  Keyin the ending surgeon                          *
.**********************************************************************
KESU0000  MOVE      SP10,ENDSURG
          MOVE      "32",ECOL
          MOVE      "09",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          CALL      PMSHCPKY
          BRANCH    EXIT,KESU0500,KESU0000
.
          MOVE      PMHCHCPC,ENDSURG
          GOTO      KESU1000
.
KESU0500  MOVE      "Finish",ENDSURG
          DISPLAY   *P32:9,*V2LON,ENDSURG
          CLEAR     ESURDESC
          MOVE      ZERO,EXIT
          GOTO      KESU9999
.
KESU1000  PACK      KEY10,ENDSURG,SP6
          CALL      RDPMHCP1
          CALL      FDOC0000
          MOVE      PACFNAME,ESURDESC
          DISPLAY   *P32:9,*EL,*V2LON,ENDSURG:
                    *P40:9,*HOFF,ESURDESC
.
.**** test that end code >= start code ****
.
          MATCH     "Start ",STRTSURG
          GOTO      KESU9999 IF EQUAL
.
          MATCH     ENDSURG,STRTSURG
          GOTO      KESU9999 IF LESS
          GOTO      KESU9999 IF EQUAL
          BEEP
          DISPLAY   *P1:24,*EL,"Starting code must be > or = ending code. ";
          CALL      HITENTER
          DISPLAY   *P40:9,SP30

          GOTO      KESU0000
.
KESU9999  RETURN
.
.**************************************************************************
.*                           PATN0000                                     *
.*                     Format the patients name                           *
.**************************************************************************
PATN0000  MOVE      TWO,PACFRMT
          MOVE      PTITL,PACTITLE
          MOVE      PGNAME,PACGNAME
          MOVE      PSNAME,PACSNAME
          CALL      PACNAME
          MOVE      PACFNAME,PATNAME
PATN9999  RETURN
+
.*********************************************************************
.*                        FDOC0000                                   *
.*                 Format the doctors name                           *
.*********************************************************************
.
FDOC0000  MOVE      PMHCSNAM,PACSNAME
          MOVE      PMHCGNAM,PACGNAME
          MOVE      PMHCTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME                  * Format Name
          MOVE      PACFNAME,DOCNAME
.
FDOC9999  RETURN
.**************************************************************************
.*                  HEAD0000                                              *
.*            print the heading                                           *
.**************************************************************************
HEAD0000  MOVE      ONE,CNOUNDLN
          IF        CLNO<>0
            CALL      DASH0000
            PRINT     *N
          ENDIF
          MOVE      ZERO,CLNO
          CALL      IBACLOCK
.
          CALL      HEAD132
.
          IF          IBCNMHOS =1
            PRINT     *N,*1,"Hospital : ",PTHSNAME
          ENDIF
.
          UNPACK    STRTDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,FORM2           * move month to a form field
          LOAD      DSPMONTH,FORM2,JAN,FEB,MAR,APRIL,MAY,JUNE:
                                   JULY,AUG,SEPT,OCT,NOV,DEC
.
          PRINT     *N,*1,"Date From : ",CDAY,SP1,DSPMONTH,SP1,CCENT,CYEAR;
.
          UNPACK    ENDDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,FORM2           * move month to a form field
          LOAD      DSPMONTH,FORM2,JAN,FEB,MAR,APRIL,MAY,JUNE:
                                   JULY,AUG,SEPT,OCT,NOV,DEC
.
          PRINT     *60,"Date To : ",CDAY,SP1,DSPMONTH,SP1,CCENT,CYEAR;
.
.
HEAD1000  PRINT     *N,*1,"Consultant From : ",SSURDESC:
                    *60,"Consultant To : ",ESURDESC;
.
          CALL      DASH0000
          PRINT     *N,*1,"|Patient Name",*30,"|UR Number",*42,"|Visit No":
                    *53,"|Doctor",*90,"|Item";
.
          CALL      DASH0000
          ADD       TEN,CLNO
.
HEAD9999  RETURN
+
***************************************************************************
.*                           DASH0000                                     *
.*                  print 132 dashes                                      *
***************************************************************************
DASH0000  PRINT     *N,"----------------------------------------":
                    "----------------------------------------":
                    "----------------------------------------":
                    "------------";
DASH9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       PMSHCPKY
          INC       PATHSPKY
          INC       PATCODIO/INC
          INC       PATHSPIO/INC
          INC       PATCRCIO/INC
          INC       PMSHCPIO/INC
          INC       PATMA1IO/INC
          INC       PMSPX2IO/INC
          INC       PATVISIO/INC
          INC       PMSVX1IO/INC

.----------------------------------------------------------------------
. Program       : CLIWEB10
.
. Function      : Clinical Result Detail Server
.
. Modifications
.
.-------------------------------------------------------------------------------
. V12.00.01 24/10/2025  Don Nguyen   TSK 0956680
.           Fixed bug where the Text Format flag (TEXTFMT) was not set for each
.           Observation read (OBXHTM00) and the globally set (server) value was
.           being used instead. We now reset the TEXTFMT flag with each request.
.           Changed 'resultCom' tag from <span> to <div> for a more accurate
.           HTML layout and better conversion to PDF; TXTSUM00
.           Output <br/> (line break) instead of space for empty lines; TXTSUM00
. V11.05.01 10/04/2025  Peter Vela   TSK 0938168
.           Fixed RDRECT1 read in RESPRQ00
. V11.04.08 17/10/2024  Sunny        TSK 0939070
.           Fixed FHIR api ProcedureRequest dxc-hc-copyto Extension
. V11.04.07 14.10.2024  DA Sarkies     Task 0950013
.           Added skip at GRIDU200 for FHIR to prevent double counting    
. V11.04.06 03.09.2024  DA Sarkies     Task 0951295
.           Removed comma and added counter for commas in JSON
. V11.04.05 03.09.2024  DA Sarkies     Task 0951295
.           Added comma to seperate diagnoses
. V11.04.04 26/07/2024  Peter Vela     Task 0941587
.           Changed to match CODEARRY to long string instead of REDAOSC at FHOBS110
.           26/07/2024  Peter Vela     Task 0949702
.           Recompiled for FHRDATCD
. V11.04.03 16.06.2024  DA Sarkies     TSK 0941587
.           Changed name of variable for consistencey
. V11.04.02 16.06.2024  DA Sarkies     TSK 0941587
.           Changed code so that all diagnosis are return if calling from   
.           resourceDiagnosis. Added code to sort in order of date
. V11.04.01 10.05.2024  DA Sarkies     TSK 0941587
.           Added code to filter by date. Removed pagnation, added count and
.           filter by obs code
. V11.03.05 04.10.2023  DA Sarkies     TSK 0938168
.           Added call to RDRECT1 in RESPRQ00
. V11.03.04 31/07/2023  Peter Vela     TSK 0927262
.           Added dxc-hc-hcp and dxc-hc-speciality extensions
.           to Practitioner User resource
. V11.03.03 11/07/2023  Peter Vela    TSK 0927262
.           ClinicalAide FHIR api ProcedureRequest
.           Added Subject include FHRSUBIN functionality
.           Added Location include FHRLOCIN functionality
.           Added Participant include FHRPARIN functionality
. V11.03.02 04/07/2023 Sunny          TSK 0927262
.           Added Subject include for resource observation functionality
. V11.03.01 14/06/2023  Peter Vela    TSK 0927262
.           ClinicalAide FHIR api
.           Added Subject include FHRSUBIN functionality
.           Added Location include FHRLOCIN functionality
. V11.02.03 01/07/2022  Jill Parkinson Task 0921270
.           Recompiled for RESDETTM - RESF6900
. V11.02.02 11/03/2022  Alina Bhari    Task 0913264
.           Recompiled for RESDETTM - RESF2500,RESF2400
.           Displayed Laboratory description if lab field is set
. V11.02.01 09/02/2022  Thanh T       TSK 0905641
.           Recompiled as OUTMA1FD (PATMASTM) changes
. V11.01.01 19/04/2021  Jill Parkinson Task 0900976
.           Recompiled for RESDETTM - DOCLNK00 newest output first
. V11.00.01 09/12/2020  Thanh T        TSK 0872840
.           Added PMSCCDFD/PMSCCDIO as PMSPX2TM changes
.-------------------------------------------------------------------------------
. V10.13.02 23.01.2019  B.G.Ackland    TSK 0835482
.           FHIR Fix diagnostic results without observations
. V10.13.01 17.12.2018  B.G.Ackland    TSK 0835482 
.           FHIR Fix output of performer.actor 
.-------------------------------------------------------------------------------
. V10.11.11 27/11/2017  Thanh Tieu     Task 0821710
.           Recompiled as PATMASTM changed
. V10.11.10 24.11.2017  B.G.Ackland    TSK 0835482 
.           FHIR Fix DiagnosticReport for Effective Date Output
. V10.11.09 15.09.2017  B.G.Ackland    TSK 0835482 
.           FHIR Fix I * C after Grid view not clearing YEAROPEN
. V10.11.08 15.09.2017  B.G.Ackland    TSK 0835482 
.           FHIR Fix ProcedureRequest Page Output Format
. V10.11.07 15.09.2017  B.G.Ackland    TSK 0835482 
.           FHIR Fix ProcedureRequest Page Output Format
. V10.11.06 15.09.2017  B.G.Ackland    TSK 0835482 
.           FHIR Fix ProcedureRequest Ignore Cancelled Orders - REHARST=X
. V10.11.05 15.09.2017  B.G.Ackland    TSK 0835482 
.           FHIR ProcedureRequest Resource
. V10.11.04 28.08.2017  B.G.Ackland    TSK 0835482 
.           FHIR Output For ProcedureRequest
. V10.11.03 28.08.2017  B.G.Ackland    TSK 0835482 
.           FHIR Output Change to Patient Reference ID to strip spaces
.           FHIR Change Extension Naming
. V10.11.02 03/08/2017  Thanh T.       TSK 0821710
.           Audit admission/outpatient/UR comments. changed PATMASTM/PMSPX2TM.
. V10.11.01 24.07.2017  B.G.Ackland    TSK 0835482 
.           FHIR Output Tabular Results
.-------------------------------------------------------------------------------
. V10.10.05 11.07.2017  B.G.Ackland    TSK 0835482 
.           FHIR Output Testing for ClinicalAide
. V10.10.04 09.05.2017  B.G.Ackland    TSK 0835482 
.           FHIR Output Added Subject to the Observation Resource
.           FHIR Output Added Image Link Extension
. V10.10.03 09.05.2017  B.G.Ackland    TSK 0835482 
.           FHIR Output Resolve Testing Issues 
. V10.10.02 08.05.2017  B.G.Ackland    TSK 0835482 
.           FHIR Output Resolve Testing Issues Incorporate Version Control
. V10.10.01 07.04.2017  B.G.Ackland    TSK 0835482 
.           FHIR output of observations
.----------------------------------------------------------------------
. V10.08.02 17.06.2016  Ania P           CAR 303363
.           Output DELTVIEW
. V10.08.01 30.05.2016  Ania P           CAR 303363
.           Added extra checks for Deleted Rows in CCOLDT00 & GRIDU000
.----------------------------------------------------------------------
. V10.07.02 05.11.2015  Steve Armstrong  CAR 303363
.           Recompiled for changes to RESHEAFD
. V10.07.01 26.10.2015  Steve Armstrong  CAR 323085
.           Removed declaration for LOWUPP (now defined in IBACOMM)
.----------------------------------------------------------------------
. V10.06.02 10.07.2015  B.G.Ackland  CAR 319313
.           Fix preformated text for plain output
. V10.06.01 10.07.2015  B.G.Ackland  CAR 
.           Retain simple HTML Bold in Text Output HTMLE000
.           to ignore encoding of "<b>" and "</b>"
.----------------------------------------------------------------------
. V10.05.04 11.12.2014  B.G.Ackland  CAR 309929
.           Fix for Error Processing of Embedded Documents
. V10.05.03 11.12.2014  B.G.Ackland  CAR 309929
.           Fix for Error Processing of Embedded Documents
. V10.05.02 11.12.2014  B.G.Ackland  CAR 309929
.           Fix for Error Processing of Embedded Documents
. V10.05.01 08.10.2014  Peter Vela      CAR 301518
.           Added Clinical Documents audit functionality OBSAUDFD
.----------------------------------------------------------------------
. V10.04.04 04.06.2014  B.G.Ackland
.           Fix for Error Processing of Embedded Documents
. V10.04.03 07.04.2014  Peter Vela   CAR 286158
.           Recompiled for PATMASTM
. V10.04.02 03.04.2014  B.G.Ackland  CAR 299627
.           New tag for Single Result Comments for Primary Cumulative Result
. V10.04.01 17.12.2013  B.G.Ackland  CAR 289943
.           New tags for format of Text and Notes Components
.----------------------------------------------------------------------
. V10.03.09 06/11/2013  Don Nguyen   CAR 262990
.           Modified UPDRES00 to go back -3 (Results Error Log list)
. V10.03.08 05/09/2013  B.G.Ackland  CAR 
.           New option for latest 10 results by U/R
. V10.03.07 05/09/2012  B.G.Ackland  CAR 275970	
.           Found Error in Grid not setting REHAFUR=
.           Do not output Orders Details that have received result messages (Status=ZZ)
. V10.03.06 21/03/2012  B.G.Ackland  CAR 275970	- New Feature
.           New UR/Visit Result Summary Grid  %CLIWEB10.06.103
.           Related to cliweb1006004 & 005
. V10.03.05 21/03/2012  B.G.Ackland  CAR 265085
.           New Visit Result Summary Single Result Option %CLIWEB10.06.102
. V10.03.04 21/03/2012  B.G.Ackland  CAR 275970	
.           New Result Visit Summary
. V10.03.03 16/01/2012  Sandra Barcham  CAR 256563
.           Recompiled for PATMASTM
. V10.03.02 21/12/2011  B.G.Ackland CAR 275970	
.           Recompiled for changes to RESDETTM.
. V10.03.01 23/11/2011  Ebon Clements   CAR 255738
.           Show ward beds in short description order
.----------------------------------------------------------------------
. V9.12.02  17/06/2009  Jill Habasque CAR 197555
.           Last try to fix I*C on resheaaf
. V9.12.01  29/05/2009  Jill Habasque CAR 197555
.           Reworked more reads to RESHEAFD - added check for OVRCD
. V9.11.02  26/03/2009  Jill Habasque CAR 148026
.           Added move of zero to OVRCD in CMLTAB00
. V9.11.01  12/02/2009  Mike Laming   CAR 148026
.           Reworked reads to RESHEAFD - added check for OVRCD when changing
.           table-year at routines CMFL0000, NXTCML00, UPDHED00, et al.
. V9.09.01  26/02/2008  Jill Habasque CAR 155410
.           Recompiled for RESDETTM - Added NTEDEA00 as a tag on its own
. V9.08.02  09/05/2007  Jill Habasque CAR 140408
.           Recompiled for RESDETTM - Added checks for TX
. V9.08.01  31/01/2007  Peter Vela     CAR 127930
.           Recompiled for MRTMASFD
. V9.05.01  09/03/2006  Ebon Clements CAR 78533
.           Mods for PATCODTM - Hospital specific category codes
. V9.04.02  22/09/2005 Jill Habasque CAR 76314
.           Added replace of " for other fields in CMLROW50
. V9.04.01  15/09/2005 Jill Habasque CAR 76314
.           Added replace of " for redarfr
. V9.03.07  29/06/2004 Jill Habasque CAR 50762
.           Added output of RECTCOD in CMLROW00
. V9.03.06  07/06/2004 Peter Vela CAR 49016
.           2004 Statutory Changes - recompiled for PMSPX2TM
. V9.03.05  26/03/2004 Peter Vela    CAR 13038
.           Recompiled for PATDOCCD - Doctor Name format Parameter
.           12/02/2004 Peter Vela    CAR 38290
.           Added functionality for opening yearly RESLNKFD files
.           (reln1999, reln2000, reln2001, reln2002, etc...)
. V9.03.04  02/03/2004 Peter Vela    CAR 47259
.           Fixed display of value string in CMLROW00
. V9.03.03  05/11/2003 Peter Vela    CAR 44832
.           Packed Patient Name and UR Number in WEBTITLE for Result
.           Details
.           Fixed incorrect BRANCH (RESMRK90) statement
. V9.03.02  24/10/2003 Peter Vela    CAR 43714
.           Packed Patient Name and UR Number in WEBTITLE for Cumulative Result 
.           Details
. V9.03.01  22/05/2003 Jill Habasque SRF 39331
.           Added move of KEY17 to RESULTKY in CMLNTE30
. V9.02.08  24/04/2003 Jill Habasque SRF 37837
.           Fixed multiple obx segments display                  
. V9.02.07  07/02/2003 Tonii CAR 35992
.           Recompiled for PATMISTM - inactive codes in patfundf
. V9.02.06  24/01/2003 Jill Habasque SRF 35208
.           Fixed marking results as read 
. V9.02.05  18/12/2002 Jill Habasque SRF 34657
.           Added new link types 07 and 08
. V9.02.04  17/10/2002 Katie Nelson 
.           Fixed Display of NTE segemnts in RESDETTM.PBL
. V9.02.03  09/10/2002 Peter Vela SRF 17693
.           Recompiled for PATMISTM - Admitting Point
. V9.02.02  06/12/2001 Harvinder 
.                      CMLROW50 - REDAOSD added after RECTDES
. V9.02.01  03/12/2001 Katie Nelson/Harvinder   SRF 648628(nz)
.           Recompiled for NHI tags added to PATMASTM
. V9.01.02  16.08.2001 J.Tan
.           Recompiled for PATMASTM
. V9.01.01  13.08.2001 J.Tan
.           Recompiled for PATMASTM
. V9.00.B03 13.07.2001 Sandra Barcham
.           Replace GP with HCP
. V9.00.B02 14.06.2001 B.G.Ackland
.           Recompile for Changes to PATMASTM
.----------------------------------------------------------------------
.                 V5.10.01 22.10.2001 B.G.Ackland
.                          Trap IO on Dup Key in Audit
.                 V5.09.02 12.01.2001 Bronko Sosic  
.                          Recompiled for PATMASTM / PATMISTM
.                 V5.08.07 01.11.2000 B.G.Ackland   
.                          Fix Collection Summary Cumulative for MHAC
.                 V5.08.06 15.10.2000 B.G.Ackland   
.                          Auditing for Cumulative Results Pages
.                          Change Security Message
.                 V5.08.05 28/09/2000 Bronko Sosic  
.                          Recompiled for PATMASTM / PATMISTM
.                 V5.08.04  16/08/2000  Caleb Sharman
.                          Changed Health Fund variables to be 8 chars
.                 V5.08.03 27.07.2000 B.G.Ackland
.                          Multiple Hospital U/R Link Table
.                 V5.08.02 12.07.2000 B.G.Ackland
.                          Wite Audit for Cumulative View
.                 V5.08.01 07/06/2000 Bronko Sosic
.                          Recompiled for PATMASTM
.----------------------------------------------------------------------
.                 V5.07.12 29.03.2000 B.G.Ackland 
.                          Fix Next/Last Options
.                 V5.07.11 28.03.2000 B.G.Ackland 
.                          Fix Next/Last Options
.                 V5.07.10 08.02.2000 B.G.Ackland 
.                          Reverse Order of Cumulative Notes Display
.                 V5.07.09 08.02.2000 B.G.Ackland 
.                          Ignore null text results
.                 V5.07.08 02.02.2000 B.G.Ackland 
.                          Notes on Cumulative Results 
.                 V5.07.07 01.02.2000 B.G.Ackland 
.                          Update Link for U/R as Marked as Read
.                 V5.07.06 30.12.1999 B.G.Ackland 
.                          Recompiled for IBAWEBDF/IBAWEBCD/PATWRDTM    
.                 V5.07.05 20.12.1999 K.C.Nelson  
.                          Recompiled for IBAWEBDF/IBAWEBCD/PATWRDTM    
.                 V5.07.04 05.11.1999 B.G.Ackland 
.                          Fix Error on Update of Result Link
.                 V5.07.03 03.11.1999 K.C.Nelson  
.                          Recompiled for WEBDATCD/DAYOFWEK          
.                 V5.07.02 05.10.1999 B.G.Ackland
.                          Added Cumulative Collection Summary Option
.                 V5.07.01 23.09.1999 B.G.Ackland
.                          Fix Open Problem for Initial system
.----------------------------------------------------------------------
          INC       IBAWEBDF    
          INC       WEBDATDF
          INC       FHRDATDF
.
          INC       APSMASFD/INC
          INC       AAEDE1FD/INC
          INC       OUTSITFD/INC
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
.
          INC       OBSPCTFD/INC       * Correspondence Storage Location Table
          INC       OBSPCOFD/INC
          INC       OBSAUDFD/INC
          INC       PMSUCHFD/INC
          INC       PMSUCDFD/INC
          INC       PATMASTD/INC
          INC       PATALRFD/INC
          INC       PATHSPFD/INC
          INC       PATIN1FD/INC
          INC       PATNURFD/INC
          INC       PATPR1FD/INC
          INC       PATMA1FD/INC
          INC       PMSHCPFD/INC
          INC       PMSHCGFD/INC
          INC       PMSHCPTD/INC
          INC       PMSHCGTD/INC
          INC       PMSPX2FD/INC
          INC       PMSPX2TD/INC
          INC       PMSVX1FD/INC
          INC       PATMI1FD/INC
          INC       PATCONFD/INC
          INC       PATDHEAD/INC
          INC       PMSCCDFD/INC            * Concession Card Details
          INC       PMSCEXFD/INC
          INC       PMSTLEFD/INC
          INC       PMSRELFD/INC
.
          INC       HL7CODFD/INC
          INC       RESAUDFD/INC
          INC       RESLNKFD/INC
          INC       RESLURFD/INC
          INC       RESLOGFD/INC
          INC       RESLABFD/INC
          INC       RESHEAFD/INC
          INC       RESHEBFD/INC
          INC       RESHECFD/INC
          INC       RESHEDFD/INC
          INC       RESDEAFD/INC
          INC       RESDEBFD/INC
          INC       RESDECFD/INC
          INC       RESCTAFD/INC
          INC       RESCLNFD/INC
          INC       RESPIDFD/INC
          INC       PATCALAG/INC
          INC       PATDO1FD/INC
          INC       PATVISFD/INC
          INC       PATWR1FD/INC
          INC       MRTLOCFD/INC
          INC       SCRMASFD/INC
          INC       PATFN1FD/INC
          INC       MRTMASFD/INC
          INC       NHIMASFD/INC
          INC       NHIETHFD/INC

.
RESABN    DIM       5
RESACK    DIM       5
CMDMOVE   INIT      "mv"      * Command line instruction
CMDLN500  DIM       500       * Command line instruction
CMDLINE   DIM       127       * Command line instruction
CORRESID  FORM      4
COPYFKEY  DIM       20        * Document Copied From Key
CORSFILE  DIM       30
DELTVIEW  FORM      1
SAVISIT   DIM       8
CATWI     INIT      "wi"
ENCODING  DIM       6
FILETYPE  DIM       5
FILEEXTN  DIM       3
CORRURNO  DIM       8
CORRADMN  DIM       8
LINENO    FORM      3
RFTYPE    DIM       30
RFREAS    DIM       30
RPNAME    DIM       30
RTNAME    DIM       30
.
TABTYPE   FORM      1
CFILEPRE  DIM       3
LINKKEYS  DIM       10
SAVKEY13  DIM       13
MARKKEY   DIM       13
LASTRUN   DIM       4
COLLPID   DIM       16
COLLDSS   DIM       10
AUDHRS    DIM       2
AUDMIN    DIM       2
AUDTIME   DIM       5
AUDDATE   DIM       8
AUDUSER   DIM       10
FSTNOTE   FORM      1
MARKEXIT  FORM      1
ABNORCLR  DIM       7 
ABNORDES  DIM       20
STATNAME  DIM       40
CMMCHKEY  DIM       40
.
CMRESKEY  DIM       17[6]
CMARRCNT  FORM      2
CMARREND  FORM      2
.
CHCKNOTE  FORM      1
.
CLRESKEY  DIM       17[6]
CLARRCNT  FORM      2
CLARREND  FORM      2
FORM4B    FORM      4
.
NTARRCNT  FORM      2
LASTYEAR  FORM      1
NEXTYEAR  FORM      1
.
FIRSTVST  FORM      1
FIRSTCOM  FORM      1
FIRSTFLG  FORM      1
MARKFLAG  FORM      1
MAXOBS    FORM      2
OBSCOUNT  FORM      2
CODECONT  FORM      3
CODEARRY  DIM       50[300]         * Result Code Array
SAVEWUID  DIM       10
AUDITKEY  DIM       40
RESULTYR  DIM       4
YEAROPEN  DIM       4
TABLEFMT  DIM       1
SORTRSLT  FORM      1              * Determines direction of sort by date
DISPDIAG  FORM      1
RESLNKYR  DIM       4              * Flags FHIR Resource Diagnosis
HASOBS    FORM      1
HUNDRED9  FORM      "109"
.
RESFNAME  DIM       8
MAXARRAY  INIT      "300"
PREHEA    INIT      "reha"
PREHEB    INIT      "rehb"
PREHEC    INIT      "rehc"
PREHED    INIT      "rehd"
PREDEA    INIT      "reda"
PREDEB    INIT      "redb"
PREDEC    INIT      "redc"
PRELEN    INIT      "reln"    * I CAR 38290
PREAEA    INIT      "reau"    * I CAR 38290
.
HL7TAB    DIM       4
HL7COD    DIM       10
LOCNCODE  DIM       4
.
RESTPRG   DIM       8
RESTREP   DIM       2
RESTTEM   DIM       3
RESLNKFL  DIM       8         * File name var for reslnk CAR 38290
LINKTYPE  DIM       2
LINKKEYF  DIM       10
RESULTKY  DIM       17
RESULTID  DIM       21
STATIMG   DIM       20
RESTIMG   DIM       20
DIM3      DIM       3
DIM4      DIM       4
DIM5      DIM       5
KEY3A     DIM       3
DAYTIME   DIM       3
DATETIME  DIM       25
DOCTCODE  DIM       6
WARDCODE  DIM       3
TEAMCODE  DIM       3
IMAGE     DIM       44
ICOIMAGE  DIM       30
FORM8     FORM      8
SAVRESKY  DIM       17
OBSPCOKY  DIM       20
.
. Cumulative Results Array
.----------------------------------------
CUMLREST  DIM       25[8][99]
CUMLROW   FORM      1
CUMLCOL   FORM      3
SETCOL    FORM      3
ACTNCODE  DIM       3 
ACTNCOMM  DIM       50
MARKSUMM  FORM      1
MARKALLL  FORM      1
DASH      INIT      "-"
DUM127    DIM       127
TESTLINE  DIM       127   * Test Description
LABDESC   DIM       40    * Laboratory Description
CHECKLST  FORM      1     * Check last year flag
HL7STR    DIM       127     * Input String
HL7DLM    DIM       1       * Delimiter
HL7CMP    DIM       127[20] * Output Array
FIELDSTR  DIM       127
STRBUFFR  DIM       127
DIM20     DIM       20
BDIM127   DIM       127
DIM8      DIM       8
FORM3     FORM      3
LINECNT   FORM      3
OBXLINE   FORM      3
F4A       FORM      4
F4B       FORM      4
FILTROCS  DIM       12
FILTROTY  DIM       12
FILTRLAB  DIM       3
NULLFLAG  FORM      1
TEXTFMT   FORM      1
FRSTONLY  FORM      1
FILELINK  DIM       127
.FHRSTAT   DIM       20
FHROBSID  DIM       21
FHRLOW    DIM       20
FHRHIGH   DIM       20
.FHRPATID  DIM       8
.FHRENCID  DIM       16
FHRDIRID  DIM       17
TESTCODE  DIM       32
DIAGTYPC  FORM      2
DIAGTYPE  DIM       3[99]
DIAGTCOD  DIM       14
DIAGTDES  DIM       127
SHOWMETA  FORM      1
FIRSTOBS  FORM      1
CORRID    DIM       4
IMAGELNK  FORM      1
ACKBYDTM  DIM       25
ACKBYWHO  DIM       40
TIMEZONE  DIM       6
PROCREQT  FORM      1
FHROBSFL  FORM      1  * Flag to exclude observation from Diagnostic report
TZFILE    FILE      ASCII, FIXED=256
.
CATTC     INIT      "tc"
.
FHIRBUND  FORM      1
FHRSUBIN  FORM      1  * _include=DiagnosticReport:subject / Observation
FHRLOCIN  FORM      1  * _include=DiagnosticReport:location
FHRPARIN  FORM      1  * _include=DiagnosticReport:participant
.
DISPPCNT  DIM       2
DISPPNAM  DIM       80
DISPPRAC  DIM       10
PRACNAME  DIM       80
PRACADD1  DIM       60
PRACADD2  DIM       60
PRACADD3  DIM       60
PRACADD4  DIM       60
PRACPOST  DIM       8
PRACPEML  DIM       80
PRACPTEL  DIM       20
PRACPFAX  DIM       20
PRACPROV  DIM       10
.
.------------------------------------------------------------
PRGID     INIT      "CLIWEB10"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "Clinical Result Detail Server"
.------------------------------------------------------------
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200,MAIN1300,MAIN1400,MAIN1500:
                             MAIN1600,MAIN1700
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "INVALID OPTION",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  MOVE      ZERO,MARKFLAG
          CALL      RESDET00      * Result Details
          GOTO      MAIN1000
.
MAIN1200  MOVE      ZERO,MARKEXIT
          IF        MARKSUMM=0
            MOVE      ZERO,MARKEXIT
            CALL      RESMRK00      * Mark Result as Seen
          ELSE
            CALL      SUMMRK00      * Mark a Collection
          ENDIF
          GOTO      MAIN1000
.
MAIN1300  MOVE      ZERO,MARKFLAG
          CALL      RESCML00      * Cumulative HL7 Results
          GOTO      MAIN1000
.
MAIN1400  MOVE      ZERO,MARKEXIT
          IF        MARKSUMM=0
            MOVE      ZERO,MARKEXIT
            CALL      RESACT00      * Action Result as Seen
          ELSE
            CALL      SUMACT00      * Action a Collection
          ENDIF
          GOTO      MAIN1000
.
MAIN1500  CALL      UPDRES00        * Re-Link Result
          GOTO      MAIN1000
.
MAIN1600  CALL      RESSUM00        * Results Reporting Summary Display
          GOTO      MAIN1000
.
MAIN1700  CALL      FHROBS00        * FHIR Observation Details
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
.------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------
INIT0000  CALL      INTMAS00
          CALL      GETTZ000
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATWR1A8,"patwr1af"
          OPEN      PMSCEXA1,"pmscexaf"
          OPEN      PMSTLEA1,"pmstleaf"
          OPEN      PMSRELA1,"pmsrelaf"
          OPEN      PMSHCPA1,"pmshcpaf"
          OPEN      AAEDE1A1,"aaede1af"
          MOVE      "out",CFILEPRE
          PACK      CFNAME,CFILEPRE,FILBB1A1
          CALL      OPOTBB11
.
          OPEN      PATHSPA1,"pathspaf"
          OPEN      OUTSITA1,"outsitaf"
          OPEN      IHAPASS1,"ihapassf"
          OPEN      HL7CODA1,"hl7codaf"
          OPEN      PATVISA1,"patvisaf"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
          OPEN      WEBSECA1,"websecaf"
.
          OPEN      RESLOGA1,"reslogaf"
          OPEN      RESLABA1,"reslabaf"
          OPEN      RESLURA1,"resluraf"
          OPEN      RESLURA2,"resluraf"
          OPEN      RESPIDA1,"respidaf"
          OPEN      RESCTAA1,"resctaaf"
          OPEN      RESCLNA1,"resclnaf"
          OPEN      OBSPCOA1,"obspcoaf"
          OPEN      OBSPCTA1,"obspctaf"
          OPEN      OBSAUDA1,"obsaudaf"
.
          CALL      IBACLOCK
          PACK      RESULTYR,CCC,CYY
          REP       " 0",RESULTYR
          CALL      RESOPN00
          IF        OVRCD=1
            MOVE      SP70,YEAROPEN
          ENDIF
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          IF        IBCNMHOS=1
            OPEN      MLTCODA1,"mltcodaf"
            OPEN      MLTCODA2,"mltcodaf"
          ENDIF
.
          RETURN
.------------------------------------------------------------
. Get Time Zone
.------------------------------------------------------------
GETTZ000  MOVE      ZERO,OVRCD
          MOVE      "+0000",KEY5
          TRAP      OVERCOND IF IO
          OPEN      TZFILE,"timezone"
          TRAPCLR   IO
          IF        OVRCD=0
            READ      TZFILE,SEQ;KEY5
          ELSE
            EXECUTE   "date +#"%z#">timezone.txt",TASKID
            TRAP      OVERCOND IF IO
            OPEN      TZFILE,"timezone"
            TRAPCLR   IO
            IF        OVRCD=0
              READ      TZFILE,SEQ;KEY5
            ENDIF
          ENDIF
          CLOSE     TZFILE
          UNPACK    KEY5,KEY3,KEY2
          PACK      TIMEZONE,KEY3,COLON,KEY2
          RETURN
.------------------------------------------------------------
. Open Result Tables
.------------------------------------------------------------
RESOPN00  CLOSE     RESHEAA1
          CLOSE     RESHEAA3
          CLOSE     RESHEBA1
          CLOSE     RESHECA1
          CLOSE     RESHEDA1
          CLOSE     RESDEAA1
          CLOSE     RESDEAA2
          CLOSE     RESDEBA1
          CLOSE     RESDECA1 
          CLOSE     RESLNKA1 
.
          MOVE      SP70,YEAROPEN
          PACK      RESFNAME,PREHEA,RESULTYR
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      RESHEAA1,RESFNAME
          TRAPCLR   IO
          BRANCH    OVRCD,RESOPN99
          OPEN      RESHEAA3,RESFNAME
.
          PACK      RESFNAME,PREHEB,RESULTYR
          OPEN      RESHEBA1,RESFNAME
          PACK      RESFNAME,PREHEC,RESULTYR
          OPEN      RESHECA1,RESFNAME
          PACK      RESFNAME,PREHED,RESULTYR
          OPEN      RESHEDA1,RESFNAME
          PACK      RESFNAME,PREDEA,RESULTYR
          OPEN      RESDEAA1,RESFNAME
          OPEN      RESDEAA2,RESFNAME
          PACK      RESFNAME,PREDEB,RESULTYR
          OPEN      RESDEBA1,RESFNAME
          PACK      RESFNAME,PREDEC,RESULTYR
          OPEN      RESDECA1,RESFNAME
          PACK      RESFNAME,PRELEN,RESULTYR     
          OPEN      RESLNKA1,RESFNAME
.
          MOVE      RESULTYR,YEAROPEN
.
RESOPN99  RETURN
.------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          MOVE      SP70,TEMPLATE
          MOVE      SP70,URNUMBER
          MOVE      SP70,FILTROCS
          MOVE      SP70,FILTRLAB
          MOVE      SP70,FILTROTY
          MOVE      SP70,ADMISSNO
          MOVE      ZERO,STARTNUM
          MOVE      SP70,RESULTKY
          MOVE      SP70,AUDITKEY
          MOVE      SP70,DOCTCODE
          MOVE      SP70,FRSTDATE
          MOVE      SP70,LASTDATE
          MOVE      ZERO,NORECORD
          MOVE      SP70,VYEARMTH
          MOVE      SP70,VIEWTYPE
          MOVE      SP70,TEAMCODE
          MOVE      SP70,WARDCODE
          MOVE      SP70,ACTNCODE
          MOVE      SP70,ACTNCOMM
          MOVE      SP70,RESLNKYR
          MOVE      ZERO,MARKSUMM
          MOVE      ZERO,MARKALLL
          MOVE      ZERO,FHROBSFL
          MOVE      ZERO,PROCREQT
.
          MOVE      ZERO,FHIRBUND
          MOVE      ZERO,FHRSUBIN
          MOVE      ZERO,FHRLOCIN
          MOVE      ZERO,FHRPARIN
          MOVE      ZERO,SORTRSLT
          MOVE      ZERO,DISPDIAG
          MOVE      ZERO,HASOBS   
          MOVE      ZERO,MAXOBS
          MOVE      ZERO,TEXTFMT
.
          MOVE      ONE,CODECONT
          MOVE      MAXARRAY,FORM3
          WHILE     CODECONT<=FORM3
            MOVE      SP70,CODEARRY[CODECONT]      
            ADD       ONE,CODECONT
          DO
          MOVE      ZERO,CODECONT
.
          RETURN
.------------------------------------------------------------
. Set Parameters
.------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "auditkey=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,AUDITKEY
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "marksumm=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,MARKSUMM
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "markalll=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,MARKALLL
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "actncode=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ACTNCODE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "actncomm=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ACTNCOMM
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "urnumber=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,URNUMBER
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtrlab=",QRYNAME
          IF        @EQUAL
            PACK      FILTRLAB,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "procreqt=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,PROCREQT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "fhrobsfl=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FHROBSFL
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtroty=",QRYNAME
          IF        @EQUAL
            PACK      FILTROTY,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtrocs=",QRYNAME
          IF        @EQUAL
            PACK      FILTROCS,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "admissno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ADMISSNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startnum=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTNUM
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "norecord=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NORECORD
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "viewtype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,VIEWTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "frstdate=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FRSTDATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "lastdate=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,LASTDATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "doctcode=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,DOCTCODE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "resultky=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,RESULTKY
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "resultid=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,RESULTID
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "vyearmth=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,VYEARMTH
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "wardcode=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,WARDCODE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "teamcode=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEAMCODE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "deltview=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,DELTVIEW
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "fhirbund=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FHIRBUND
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "fhrsubin=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FHRSUBIN
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "fhrlocin=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FHRLOCIN
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "fhrparin=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FHRPARIN
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "maxobs00=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,MAXOBS   
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "sortrslt=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SORTRSLT 
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "dispdiag=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,DISPDIAG 
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "reslnkyr=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,RESLNKYR 
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "rsltcode=",QRYNAME
          IF        @EQUAL
            MOVE      MAXARRAY,FORM3
            COMPARE   FORM3,CODECONT
            GOTO      SETPAR99 IF EQUAL
.
            ADD       ONE,CODECONT
            PACK      CODEARRY[CODECONT],QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF

.        MATCH     "seqdi",QRYNAME
.         IF        @EQUAL
.           MOVE      MAXARRY,FORM3                                   *C-240107
.           COMPARE   FORM3,SQDCOUNT
.           GOTO      SETPAR99 IF EQUAL
.
.           ADD       ONE,SQDCOUNT
.           PACK      SEQDIAGN[SQDCOUNT],QRYDATA,SP70
.           GOTO      SETPAR99
.         ENDIF
.
SETPAR99  RETURN
.------------------------------------------------------------
. Observation Details
.------------------------------------------------------------
FHROBS00  MOVE      RESULTID,RESULTYR
          MOVE      ZERO,OVRCD
          MATCH     RESULTYR,YEAROPEN
          CALL      RESOPN00 IF NOT EQUAL
          BRANCH    OVRCD,FHROBS90
.
          MOVE      RESULTID,KEY17
          CALL      RDREHA1    * Check Result on File
          BRANCH    OVRCD,FHROBS90
.
          MOVE      RESULTID,KEY21
          CALL      RDREDA1    * Check Result on File
          BRANCH    OVRCD,FHROBS91
.
          CLEAR     WEBTITLE
          APPEND    "Result Details",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,FHROBS99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          GOTO      FHROBS99
.
FHROBS90  MOVE      "Invalid Result Key",WEBTITLE
          CALL      WEBERR00
          GOTO      FHROBS99
.
FHROBS91  MOVE      "Invalid Result ID Key",WEBTITLE
          CALL      WEBERR00
          GOTO      FHROBS99
.
FHROBS99  RETURN
+
.------------------------------------------------------------
. Results Details
.------------------------------------------------------------
RESDET00  MOVE      RESULTKY,RESULTYR
          MOVE      ZERO,OVRCD
          MATCH     RESULTYR,YEAROPEN
          CALL      RESOPN00 IF NOT EQUAL
          BRANCH    OVRCD,RESDET90
.
          MOVE      RESULTKY,KEY17
          CALL      RDREHA1    * Check Result on File
          BRANCH    OVRCD,RESDET90
.
          CALL      RESSEC00   * Check Result Level Security
          BRANCH    EXIT,RESDET99
.
          CALL      WRTAUD00           * Write Audit Record
.
          CALL      GETPID00           * Get Patient Details
          BRANCH    OVRCD,RESDET91
.
          CLEAR     WEBTITLE
          APPEND    "Result Details",WEBTITLE
          RESET     WEBTITLE
.
          STRIP     PSNAME             * I CAR 44832
          STRIP     PGNAME
          PACK      WEBTITLE,WEBTITLE,SP1,PSNAME,COMMA,SP1,PGNAME,SP1,PURNO
          RESET     WEBTITLE
          SETLPTR   PSNAME
          SETLPTR   PGNAME             * end I CAR 44832
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,RESDET99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          GOTO      RESDET99
.
RESDET90  MOVE      "Invalid Result Key",WEBTITLE
          CALL      WEBERR00
          GOTO      RESDET99
.
RESDET91  MOVE      "Invalid Patient ID Key",WEBTITLE
          CALL      WEBERR00
          GOTO      RESDET99
.
RESDET99  RETURN
.------------------------------------------------------------
. Security
.------------------------------------------------------------
RESSEC00  MOVE      ZERO,F2
          MOVE      "OBR04",KEY5
          PACK      KEY32,REHALAB,KEY5,REHAUCS,REHAUSC
          CALL      RDRECT1
          IF        OVRCD=0
            SQUEEZE   RECTSLV
            MOVE      RECTSLV,F2
          ENDIF
          PACK      KEY18,WBSEUID,PRGID
          CALL      RDWBST1
          IF        OVRCD=1
            MOVE      ZERO,WBSTLEV
          ENDIF
          IF        WBSTLEV<F2
            GOTO      RESSEC80
          ENDIF
          MOVE      ZERO,EXIT
          GOTO      RESSEC99
.
RESSEC80  MOVE      "Security Level Inadequate.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
.
RESSEC99  RETURN
.------------------------------------------------------------
. Write Audit Record for who has seen the records
.------------------------------------------------------------
WRTAUD00  BRANCH    MARKFLAG,WRTAUD99
          PACK      REAUSDT,CCC,CYY,CMM,CDD
          REP       " 0",REAUSDT
          CLOCK     TIME,REAUSTM
          MOVE      WBSEUID,REAUUID
          PACK      KEY40,REHARDT,REHARTM,REHARUN,REAUSDT,REAUSTM,REAUUID,SP70
          UNPACK    KEY40,REAURDT,REAURTM,REAURUN,REAUSDT,REAUSTM,REAUUID
          MOVE      KEY40,AUDITKEY
          MOVE      REHARST,REAURST
          MOVE      ZERO,REAUMRK
          MOVE      SP70,REAUACD
          MOVE      SP70,REAUACM
          MOVE      SP70,REAUSPA
.
          PACK      RESLNKFL,PREAEA,REAURDT           * I CAR 38290
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      RESAUDA1,RESLNKFL
          TRAPCLR   IO
.
          COMPARE   ONE,OVRCD
          GOTO      WRTAUD99 IF EQUAL                 * end I CAR 38290
.
          CALL      RAREAU1
          IF        OVRCD=1
            TRAP      OVERCOND IF IO
            CALL      WRREAU1
            TRAPCLR   IO
          ENDIF
WRTAUD99  RETURN
.------------------------------------------------------------
. Get Patient Details
.------------------------------------------------------------
GETPID00  MATCH     "0",REHAFUR
          GOTO      GETPID10 IF EQUAL
.
          MOVE      REHAPID,KEY8
          MOVE      REHAPID,URNUMBER
          MOVE      REHAVIS,ADMISSNO
          CALL      RDMAST1
          BRANCH    OVRCD,GETPID99
.
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          IF        OVRCD=1
            CALL      CLPMSPX2
          ENDIF
.         
          MOVE      PSNAME,REPISNM
          MOVE      PGNAME,REPIGNM
          MOVE      SP1,REPIINT   
          MOVE      PBDATE,REPIDOB
          MOVE      PSEX,REPISEX  
          MOVE      PADD1,REPIAD1 
          MOVE      PADD2,REPIAD2 
          MOVE      PSUBRB,REPIAD3
          MOVE      PADD4,REPIAD4 
          MOVE      PPOST,REPIPC  
          MOVE      PTELEP,REPIPHH
          MOVE      PTELEB,REPIPHB
          GOTO      GETPID99
.
GETPID10  CALL      CLPATMAS
          CALL      CLPMSPX2
          MOVE      REHAPID,KEY16 
          CALL      RDREPI1
          BRANCH    OVRCD,GETPID99
          MOVE      REPISNM,PSNAME
          MOVE      REPIGNM,PGNAME
          MOVE      REPIDOB,PBDATE
          MOVE      REPISEX,PSEX
          MOVE      REPIAD1,PADD1
          MOVE      REPIAD2,PADD2
          MOVE      REPIAD3,PSUBRB
          MOVE      REPIAD4,PADD4
          MOVE      REPIPC,PPOST
          MOVE      REPIPHH,PTELEP
          MOVE      REPIPHB,PTELEB
.
GETPID99  RETURN
.------------------------------------------------------------
. Process Templated Fields
.------------------------------------------------------------
PROFLD00  BRANCH    REPORTNO,PROFLD10,PROFLD20,PROFLD30,PROFLD40,PROFLD50:
                             PROFLD60,PROFLD70
          GOTO      PROFLD99
.
. Result Details
.--------------------------
PROFLD10  BRANCH    FLDSETNO,PROFLD11,PROFLD12,PROFLD13
          GOTO      PROFLD99
.
PROFLD11  CALL      MASF0000
          GOTO      PROFLD99
.
PROFLD12  CALL      RESF0000
          GOTO      PROFLD99
.
PROFLD13  CALL      FHRF0000
          GOTO      PROFLD99
.
PROFLD20  GOTO      PROFLD99
.
. Cumulative Result Details
.--------------------------
PROFLD30  BRANCH    FLDSETNO,PROFLD31,PROFLD32
          GOTO      PROFLD99
.
PROFLD31  CALL      MASF0000
          GOTO      PROFLD99
.
PROFLD32  CALL      CMFL0000
          GOTO      PROFLD99
.
PROFLD40                      * Update only Services
PROFLD50                      * Update only Services
          GOTO      PROFLD99
.
.
. Result Details
.--------------------------
PROFLD60  BRANCH    FLDSETNO,PROFLD61,PROFLD62
          GOTO      PROFLD99
.
PROFLD61  CALL      MASF0000
          GOTO      PROFLD99
.
PROFLD62  CALL      SUMF0000
          GOTO      PROFLD99
.
. Observation Details
.--------------------------
PROFLD70  BRANCH    FLDSETNO,PROFLD71
          GOTO      PROFLD99
.
PROFLD71  CALL      FHIR0000
          GOTO      PROFLD99
.
PROFLD99  RETURN
.------------------------------------------------------------
. Mark Results as Read
.------------------------------------------------------------
RESMRK00  UNPACK    AUDITKEY,KEY30,KEY10
          MATCH     KEY10,WBSEUID
          GOTO      RESMRK95 IF NOT EQUAL
          MOVE      AUDITKEY,RESULTKY
          MOVE      RESULTKY,RESULTYR
          MOVE      ZERO,OVRCD
          MATCH     RESULTYR,YEAROPEN
          CALL      RESOPN00 IF NOT EQUAL
          BRANCH    OVRCD,RESMRK90            * C CAR 44832
.
          MOVE      RESULTKY,KEY17
          CALL      RDREHA1            * Check Result on File
          BRANCH    OVRCD,RESMRK90
.
          PACK      RESLNKFL,PREAEA,AUDITKEY           * I CAR 38290
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      RESAUDA1,RESLNKFL
          TRAPCLR   IO                                 * end I CAR 38290
.
          MOVE      AUDITKEY,KEY40
          CALL      RDREAU1
          IF        OVRCD=0
            MOVE      ONE,REAUMRK
            CALL      UPREAU1 
          ENDIF
.
          PACK      RESLNKFL,PRELEN,RESULTKY          * I CAR 38290
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      RESLNKA1,RESLNKFL
          TRAPCLR   IO
.
          COMPARE   ONE,OVRCD
          GOTO      RESMRK90 IF EQUAL                 * end I CAR 38290
.
          MOVE      "01",LINKTYPE      * Mark Read on U/R
          PACK      LINKKEYF,REHAPID,SP70
          PACK      KEY29,LINKTYPE,LINKKEYF,RESULTKY
          CALL      RDRELN1
          IF        OVRCD=0
            MOVE      ONE,RELNMSN
            MOVE      REAUSDT,RELNSDT
            MOVE      REAUSTM,RELNSTM
            MOVE      PASSCODE,RELNSPC
            CALL      UPRELN1
          ENDIF
.
          PACK      KEY8,REHAPID,SP70
          PACK      KEY25,KEY8,RESULTKY
          CALL      RDRELU1
          IF        OVRCD=0
            MOVE      ONE,RELUMSN
            MOVE      REAUSDT,RELUSDT
            MOVE      REAUSTM,RELUSTM
            MOVE      PASSCODE,RELUSPC
            CALL      UPRELU1
          ENDIF
.
          IF        MARKALLL=1
            CALL      MRKALL00
            GOTO      RESMRK20
          ENDIF
.
          MATCH     SP70,WBSEDOC
          GOTO      RESMRK10 IF EQUAL
.
          MOVE      "03",LINKTYPE
          PACK      LINKKEYF,WBSEDOC,SP70
          PACK      KEY29,LINKTYPE,LINKKEYF,RESULTKY
          CALL      RDRELN1
          IF        OVRCD=0
            MOVE      ONE,RELNMSN
            MOVE      REAUSDT,RELNSDT
            MOVE      REAUSTM,RELNSTM
            MOVE      PASSCODE,RELNSPC
            CALL      UPRELN1
          ENDIF
.
RESMRK10  MATCH     SP70,WBSEWRD
          GOTO      RESMRK20 IF EQUAL
.
          MOVE      "04",LINKTYPE
          PACK      LINKKEYF,WBSEWRD,SP70
          PACK      KEY29,LINKTYPE,LINKKEYF,RESULTKY
          CALL      RDRELN1
          IF        OVRCD=0
            MOVE      ONE,RELNMSN
            MOVE      REAUSDT,RELNSDT
            MOVE      REAUSTM,RELNSTM
            MOVE      PASSCODE,RELNSPC
            CALL      UPRELN1
          ENDIF
.
RESMRK20  MOVE      ZERO,EXIT
          BRANCH    MARKEXIT,RESMRK99
          MOVE      ONE,REPORTNO
          MOVE      ONE,MARKFLAG
          CALL      WINCLS00
          MOVE      ZERO,EXIT
          GOTO      RESMRK99
.
RESMRK90  MOVE      "Invalid Result Key",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      RESMRK99
.
RESMRK95  MOVE      "Invalid Security for Audit Key",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      RESMRK99
.
RESMRK99  RETURN
.------------------------------------------------------------
. Mark All Links as Read  (MARKALL=1)
.------------------------------------------------------------
MRKALL00  PACK      RESLNKFL,PRELEN,RESULTKY          * I CAR 38290
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      RESLNKA3,RESLNKFL
          TRAPCLR   IO
.
          COMPARE   ONE,OVRCD
          GOTO      MRKALL99 IF EQUAL                 * end I CAR 38290
.
          PACK      KEY29,RESULTKY,SP70
          CALL      RSRELN3
MRKALL10  CALL      RKRELN3
          BRANCH    OVRCD,MRKALL99
          PACK      KEY17,RELNRDT,RELNRTM,RELNRUN
          MATCH     RESULTKY,KEY17
          GOTO      MRKALL99 IF NOT EQUAL
          MOVE      ONE,RELNMSN
          MOVE      REAUSDT,RELNSDT
          MOVE      REAUSTM,RELNSTM
          MOVE      PASSCODE,RELNSPC
          CALL      UPRELN3
          GOTO      MRKALL10
MRKALL99  RETURN
.------------------------------------------------------------
. Results Details
.------------------------------------------------------------
RESCML00  MOVE      RESULTKY,RESULTYR
          MOVE      ZERO,OVRCD
          MATCH     RESULTYR,YEAROPEN
          CALL      RESOPN00 IF NOT EQUAL
          BRANCH    OVRCD,RESCML90
.
          MOVE      RESULTKY,KEY17
          CALL      RDREHA1    * Check Result on File
          BRANCH    OVRCD,RESCML90
.
          CALL      RESSEC00           * Check Security
          BRANCH    EXIT,RESCML99
.
          CALL      WRTAUD00           * Write Audit Record
.
          CALL      GETPID00           * Get Patient Details
          BRANCH    OVRCD,RESCML91
.
          CALL      GETCML00           * Get Last 10 Matching Result Keys
.
          CLEAR     WEBTITLE
          APPEND    "Cumulative Result Details",WEBTITLE
          RESET     WEBTITLE
.
          STRIP     PSNAME             * I CAR 43714
          STRIP     PGNAME
          PACK      WEBTITLE,WEBTITLE,SP1,PSNAME,COMMA,SP1,PGNAME,SP1,PURNO
          RESET     WEBTITLE           
          SETLPTR   PSNAME             
          SETLPTR   PGNAME             * end I CAR 43714
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,RESCML99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          GOTO      RESCML99
.
RESCML90  MOVE      "Invalid Result Key",WEBTITLE
          CALL      WEBERR00
          GOTO      RESCML99
.
RESCML91  MOVE      "Invalid Patient ID Key",WEBTITLE
          CALL      WEBERR00
          GOTO      RESCML99
.
RESCML92  MOVE      "Error Accessing Cumulative Results",WEBTITLE
          CALL      WEBERR00
          GOTO      RESCML99
.
RESCML99  RETURN
.------------------------------------------------------------
. Cumulative Result List
.------------------------------------------------------------
GETCML00  MOVE      ZERO,CMARRCNT
          MOVE      ZERO,CLARRCNT
          MOVE      ZERO,CMARREND
          MOVE      ZERO,CLARREND
          MOVE      ZERO,LASTYEAR
          PACK      CMMCHKEY,REHAPID,REHAUCS,REHAUSC,SP70
GETCML05  PACK      KEY57,CMMCHKEY,REHARDT,REHARTM,Z70
          CALL      RSREHA3
GETCML10  CALL      RPREHA3
          BRANCH    OVRCD,GETCML90
          PACK      KEY40,REHAPID,REHAUCS,REHAUSC,SP70
          MATCH     CMMCHKEY,KEY40
          GOTO      GETCML90 IF NOT EQUAL
          MATCH     "ZZ",REHARST
          GOTO      GETCML10 IF EQUAL
.
          IF        CMARRCNT<6
            ADD       ONE,CMARRCNT
            MOVE      CMARRCNT,CMARREND
            PACK      CMRESKEY[CMARRCNT],REHARDT,REHARTM,REHARUN,SP70
          ENDIF
.
          IF        CLARRCNT>0
            PACK      KEY13,REHARDT,REHARTM,REHARUN,SP70
            MATCH     KEY13,CLRESKEY[CLARRCNT]
            GOTO      GETCML10 IF EQUAL
          ENDIF
.
          IF        CLARRCNT<6
            ADD       ONE,CLARRCNT
            MOVE      CLARRCNT,CLARREND
            PACK      CLRESKEY[CLARRCNT],REHARDT,REHARTM,REHARUN,SP70
            GOTO      GETCML10
          ENDIF
          GOTO      GETCML99
.
GETCML90  BRANCH    LASTYEAR,GETCML95
          MOVE      ONE,LASTYEAR
          MOVE      YEAROPEN,F4
          SUB       ONE,F4
          MOVE      F4,RESULTYR
          CALL      RESOPN00          * Check for Results in Previous Year Only
          COMPARE   ZERO,OVRCD
          GOTO      GETCML05 IF EQUAL
.
GETCML95  MOVE      RESULTYR,F4
          ADD       ONE,F4
          MOVE      F4,RESULTYR
          CALL      RESOPN00          * Check for Results in Previous Year Only
.
GETCML99  RETURN
.------------------------------------------------------------
. Cumulative Result Details
.------------------------------------------------------------
CMFL0000  BRANCH    FLDITMNO,CMFL0100,CMFL0200,CMFL0300,CMFL0400,CMFL0500:
                             CMFL0600,CMFL0700,CMFL0800,CMFL0900,CMFL1000:
                             CMFL1100,CMFL1200,CMFL1300,CMFL1400,CMFL1500
          GOTO      CMFL9999
.
CMFL0100  CALL      CMLTAB00
          GOTO      CMFL9999
.
CMFL0200  CALL      LSTCML00
          GOTO      CMFL9999
CMFL0300  CALL      NXTCML00
          GOTO      CMFL9999
.
CMFL0400  CALL      CMLCOL00
          GOTO      CMFL9999
.
CMFL0500  MOVE      ZERO,TEXTFMT
          MOVE      ZERO,FRSTONLY
          CALL      CMLNTE00
          GOTO      CMFL9999
.
CMFL0600  WRITE     HTMLFILE;RESULTKY;
          GOTO      CMFL9999
.
CMFL0700  WRITE     HTMLFILE;AUDITKEY;
          GOTO      CMFL9999
.
CMFL0800  MOVE      RESULTKY,KEY17
          MOVE      RESULTKY,RESULTYR                                 *I-148026
          MOVE      ZERO,OVRCD
          MATCH     RESULTYR,YEAROPEN
          CALL      RESOPN00 IF NOT EQUAL
          BRANCH    OVRCD,CMFL9999                                    *I-148026
.
          CALL      RDREHA1            * Check Result on File
          BRANCH    OVRCD,CMFL9999
          MOVE      "0074",HL7TAB
          MOVE      REHADSS,HL7COD
          CALL      HL7ITM00
          GOTO      CMFL9999
.
CMFL0900  MOVE      RESULTKY,KEY17
          MOVE      RESULTKY,RESULTYR                                 *I-148026
          MOVE      ZERO,OVRCD
          MATCH     RESULTYR,YEAROPEN
          CALL      RESOPN00 IF NOT EQUAL
          BRANCH    OVRCD,CMFL9999                                    *I-148026
.
          CALL      RDREHA1            * Check Result on File
          BRANCH    OVRCD,CMFL9999
          WRITE     HTMLFILE;REHAUSC;
          GOTO      CMFL9999
.
CMFL1000  MOVE      RESULTKY,KEY17
          MOVE      RESULTKY,RESULTYR                                 *I-148026
          MOVE      ZERO,OVRCD
          MATCH     RESULTYR,YEAROPEN
          CALL      RESOPN00 IF NOT EQUAL
          BRANCH    OVRCD,CMFL9999                                    *I-148026
.
          CALL      RDREHA1            * Check Result on File
          BRANCH    OVRCD,CMFL9999
          MOVE      "OBR04",KEY5
          PACK      KEY32,REHALAB,KEY5,REHAUCS,REHAUSC,SP70
          CALL      RDRECT1
          IF        OVRCD=1
            PACK      RECTDES,REHAUSC,SP70
          ENDIF
          MATCH     SP70,RECTDES
          IF        @EQUAL
            PACK      RECTDES,REHAUSC,SP70
          ENDIF
          WRITE     HTMLFILE;RECTDES;
          GOTO      CMFL9999
.
CMFL1100  CALL      AUDTAB00
          GOTO      CMFL9999
.
CMFL1200  MOVE      ONE,TEXTFMT
          MOVE      ZERO,FRSTONLY
          CALL      CMLNTE00
          GOTO      CMFL9999
.
CMFL1300  MOVE      ZERO,TEXTFMT 
          MOVE      ONE,FRSTONLY
          CALL      CMLNTE00
.
CMFL1400  MOVE      ONE,TEXTFMT 
          MOVE      ONE,FRSTONLY
          CALL      CMLNTE00
          GOTO      CMFL9999
.
CMFL1500  MOVE      ZERO,TABTYPE
          CALL      DOCLNK00  * Document Links
          GOTO      CMFL9999
.
CMFL9999  RETURN
.------------------------------------------------------------
. Determine Link to Next Result for the Patient
.------------------------------------------------------------
NXTCML00  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          MOVE      ZERO,F2
          MOVE      ZERO,NEXTYEAR
          MOVE      CMRESKEY[ONE],KEY17
          MATCH     SP70,YEAROPEN                                     *I-148026
          GOTO      NXTCML95 IF EQUAL                                 *I-148026
          MOVE      ZERO,OVRCD
          MATCH     KEY17,YEAROPEN
          CALL      RESOPN00 IF NOT EQUAL
          BRANCH    OVRCD,NXTCML95
.
          CALL      RDREHA1
          BRANCH    OVRCD,NXTCML95
.
          PACK      CMMCHKEY,REHAPID,REHAUCS,REHAUSC,SP70
NXTCML05  PACK      KEY57,CMMCHKEY,REHARDT,REHARTM,SP70
          CALL      RSREHA3
NXTCML10  CALL      RKREHA3
          BRANCH    OVRCD,NXTCML20
          PACK      KEY40,REHAPID,REHAUCS,REHAUSC,SP70
          MATCH     CMMCHKEY,KEY40
          GOTO      NXTCML20 IF NOT EQUAL
          MATCH     "ZZ",REHARST
          GOTO      NXTCML10 IF EQUAL
          PACK      KEY17,REHARDT,REHARTM,REHARUN,SP70
          ADD       ONE,F2
          IF        F2<5
            GOTO      NXTCML10
          ENDIF
.
NXTCML20  BRANCH    NEXTYEAR,NXTCML95
          MOVE      ONE,NEXTYEAR
          MOVE      YEAROPEN,F4
          ADD       ONE,F4
          MOVE      F4,RESULTYR
          CALL      RESOPN00          * Check for Results in Previous Year Only
          COMPARE   ZERO,OVRCD
          GOTO      NXTCML05 IF EQUAL
.
          MOVE      RESULTYR,F4
          SUB       ONE,F4
          MOVE      F4,RESULTYR
          CALL      RESOPN00          * Check for Results in Previous Year Only
.
NXTCML95  MATCH     CMRESKEY[ONE],KEY17
          IF        @EQUAL
            WRITE     HTMLFILE;"alert(#"No More Results#")";
          ELSE
            REP       " +",KEY17
            WRITE     HTMLFILE;"location.href=#"",LINKPRG,".pbl":
                               "?reportno=",LINKREP:
                               "&template=",LINKTEM:
                               "&resultky=",KEY17,"#"";
          ENDIF
NXTCML99  RETURN
.------------------------------------------------------------
. Determine Link to Next Result for the Patient
.------------------------------------------------------------
LSTCML00  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          MATCH     CMRESKEY[ONE],CMRESKEY[CMARREND]
          IF        @EQUAL
            WRITE     HTMLFILE;"alert(#"No More Results#")";
          ELSE
            MOVE      CMRESKEY[CMARREND],KEY17
            REP       " +",KEY17
            WRITE     HTMLFILE;"location.href=#"",LINKPRG,".pbl":
                               "?reportno=",LINKREP:
                               "&template=",LINKTEM:
                               "&resultky=",KEY17,"#"";
          ENDIF
LSTCML99  RETURN
.------------------------------------------------------------
. Cumulative Result Table Output
.------------------------------------------------------------
CMLTAB00  MOVE      CMARREND,CMARRCNT
.
CMLTAB10  MOVE      CMRESKEY[CMARRCNT],RESULTYR
          MOVE      ZERO,OVRCD
          MATCH     RESULTYR,YEAROPEN
          CALL      RESOPN00 IF NOT EQUAL
          BRANCH    OVRCD,CMLTAB30                                    *I-148026
.
          PACK      KEY17,CMRESKEY[CMARRCNT],SP70
          CALL      RDREHA1
.
          IF        FHIRTYPE=1
            COMPARE   CMARREND,CMARRCNT
            IF        !@EQUAL
              WRITE     HTMLFILE;*+,",";
            ENDIF
            WRITE     HTMLFILE;*+," {":
                                  " #"fullUrl#": #"%BASEURL/DiagnosticReport/",FHRDIRID,"#",":
                                  " #"key#": #"",CMRESKEY[CMARRCNT],"#",":
                                  " #"resource#": ";
            CALL      RESDIR00
            WRITE     HTMLFILE;*+,"}";
          ENDIF
.
          PACK      KEY17,CMRESKEY[CMARRCNT],SP70
          PACK      KEY21,CMRESKEY[CMARRCNT],SP70
          CALL      RSREDA1
CMLTAB20  CALL      RKREDA1
          BRANCH    OVRCD,CMLTAB30
          PACK      KEY17,REDARDT,REDARTM,REDARUN
          MATCH     KEY17,CMRESKEY[CMARRCNT]
          GOTO      CMLTAB30 IF NOT EQUAL
.
          MATCH     ANSD,REDASTA       * Dont display deleted records
          GOTO      CMLTAB20 IF EQUAL
.
          MATCH     "NM",REDAVTY       * Must be Numeric Result for Cumulative
          GOTO      CMLTAB25 IF EQUAL
          MATCH     "ST",REDAVTY       * Must be Numeric Result for Cumulative
          GOTO      CMLTAB25 IF EQUAL
          MATCH     "TX",REDAVTY       * Must be Numeric Result for Cumulative
          GOTO      CMLTAB25 IF EQUAL
          MATCH     "SN",REDAVTY       * Must be Numeric Result for Cumulative
          GOTO      CMLTAB25 IF EQUAL
          GOTO      CMLTAB20
.
CMLTAB25  CALL      CMLROW00
          GOTO      CMLTAB20
.
CMLTAB30  SUB       ONE,CMARRCNT
          IF        CMARRCNT>0        
            GOTO      CMLTAB10
          ENDIF
CMLTAB99  RETURN
.
CMLROW00  MOVE      "OBX03",KEY5
          PACK      KEY32,REHALAB,KEY5,REDAOCS,REDAOTY,SP70
          CALL      RDRECT1
          IF        OVRCD=1
            PACK      RECTDES,REDAOTY,SP70
          ENDIF
          MATCH     SP70,RECTDES
          IF        @EQUAL
            PACK      RECTDES,REDAOTY,SP70
          ENDIF
.
          MOVE      SP70,HLCOBGC
          MOVE      "0078",KEY4
          PACK      KEY14,KEY4,REDAABF,SP70
          CALL      RDHLCO1
.
.         SQUEEZE   REDAVST       * D CAR 47259
          STRIP     REDAVST       * I CAR 47259
          MOVELPTR  REDAVST,LENGTH
          SFORMAT   VAR,LENGTH
          MOVE      REDAVST,VAR
          MATCH     "ST",REDAVTY  * Check for null String Results
          GOTO      CMLROW50 IF NOT EQUAL
          MOVE      ONE,NULLFLAG
          MATCH     "null",VAR    * Ignore null results
          GOTO      CMLROW90 IF EQUAL
.
CMLROW50  BRANCH    FHIRTYPE,CMLROW80
          MOVE      ZERO,NULLFLAG
          PACK      RESULTKY,REDARDT,REDARTM,REDARUN
          REP       "#" ",REDARFR
          REP       "#" ",REDAUNI
          REP       "#" ",REDAOSD
          REP       "#" ",RECTCOD
          REP       "#" ",RECTDES
          REP       "#" ",VAR
          WRITE     HTMLFILE;"Results.addRow(":
                             "#"",RECTDES,REDAOSD,SP1,RECTCOD,"#",":
                             "#"",REDARDT,REDARTM,"#",":
                             "#"",VAR,"#",":
                             "#"",REDARFR,"#",":
                             "#"",REDAUNI,"#",":
                             "#"",RESULTKY,"#",":
                             "#"",HLCOBGC,"#",":
                             "#"",RECTDES,REDAOSD,"#",":
                             "#"",REDAVTY,"#")"
          GOTO      CMLROW90
.
CMLROW80  MOVE      ZERO,SHOWMETA
          PACK      FHROBSID,REDARDT,REDARTM,REDARUN,REDASID,SP70   * Save Key
          REP       " -",FHROBSID
          WRITE     HTMLFILE;*+," ,{":
                                " #"fullUrl#": #"%BASEURL/Observation/",FHROBSID,"#",":
                                " #"key#": #"",CMRESKEY[CMARRCNT],"#",":
                                " #"key17#": #"",KEY17,"#",":
                                " #"key21#": #"",KEY21,"#",":
                                " #"resource#": ";
          CALL      RESOBS00
          WRITE     HTMLFILE;*+,"}";
.
CMLROW90  SFORMAT   VAR,127
CMLROW99  RETURN
.
.------------------------------------------------------------
. Mark Results as Read
.------------------------------------------------------------
RESACT00  UNPACK    AUDITKEY,KEY30,KEY10
          MATCH     KEY10,WBSEUID
          GOTO      RESACT95 IF NOT EQUAL
.
          MOVE      AUDITKEY,RESULTKY
          MOVE      RESULTKY,KEY17
          MOVE      RESULTKY,RESULTYR                                 *I-148026
          MOVE      ZERO,OVRCD
          MATCH     RESULTYR,YEAROPEN
          CALL      RESOPN00 IF NOT EQUAL
          BRANCH    OVRCD,RESACT90                                    *I-148026
.
          CALL      RDREHA1            * Check Result on File
          BRANCH    OVRCD,RESACT90
.
          PACK      RESLNKFL,PREAEA,AUDITKEY          * I CAR 38290
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      RESAUDA1,RESLNKFL
          TRAPCLR   IO
.
          COMPARE   ONE,OVRCD
          GOTO      RESACT10 IF EQUAL                 * end I CAR 38290
.
          MOVE      AUDITKEY,KEY40
          CALL      RDREAU1
          BRANCH    OVRCD,RESACT10
.
          MATCH     SP70,REAUACD
          GOTO      RESACT10 IF NOT EQUAL
          MATCH     SP70,REAUACM
          GOTO      RESACT10 IF NOT EQUAL
.
          MOVE      ACTNCODE,REAUACD
          MOVE      ACTNCOMM,REAUACM
          CALL      UPREAU1 
          GOTO      RESACT20 
.
RESACT10  CALL      NEWAUD00
          GOTO      RESACT00 
.
RESACT20  BRANCH    MARKEXIT,RESACT99
          MOVE      ONE,REPORTNO
          MOVE      ONE,MARKFLAG
          CALL      WINCLS00
          GOTO      RESACT99
.
RESACT90  MOVE      "Invalid Result Key",WEBTITLE
          CALL      WEBERR00
          GOTO      RESACT99
.
RESACT95  MOVE      "Invalid Security for Audit Key",WEBTITLE
          CALL      WEBERR00
          GOTO      RESACT99
.
RESACT99  RETURN
.------------------------------------------------------------
. Write Audit Record for who has seen the records
.------------------------------------------------------------
NEWAUD00  PACK      REAUSDT,CCC,CYY,CMM,CDD
          REP       " 0",REAUSDT
          CLOCK     TIME,REAUSTM
          MOVE      WBSEUID,REAUUID
NEWAUD10  PACK      KEY40,REHARDT,REHARTM,REHARUN,REAUSDT,REAUSTM,REAUUID,SP70
          UNPACK    KEY40,REAURDT,REAURTM,REAURUN,REAUSDT,REAUSTM,REAUUID
          MOVE      KEY40,AUDITKEY
          MOVE      REHARST,REAURST
          MOVE      ZERO,REAUMRK
          MOVE      SP70,REAUACD
          MOVE      SP70,REAUACM
          MOVE      SP70,REAUSPA
.
          PACK      RESLNKFL,PREAEA,REAURDT           * I CAR 38290
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      RESAUDA1,RESLNKFL
          TRAPCLR   IO
.
          COMPARE   ONE,OVRCD
          GOTO      NEWAUD99 IF EQUAL                 * end I CAR 38290
.
          CALL      RAREAU1
          IF        OVRCD=0
            UNPACK    REAUSTM,AUDHRS,KEY1,AUDMIN
            MOVE      AUDMIN,F2
            ADD       ONE,F2
            PACK      REAUSTM,AUDHRS,KEY1,F2
            REP       " 0",REAUSTM
            GOTO      NEWAUD10
          ELSE
            TRAP      OVERCOND IF IO
            CALL      WRREAU1
            TRAPCLR   IO
          ENDIF
NEWAUD99  RETURN
.------------------------------------------------------------
. Cumulative Result Table Output - for a Collection
.------------------------------------------------------------
CMLCOL00  MOVE      CLARREND,CLARRCNT
CMLCOL10  MOVE      CLRESKEY[CLARRCNT],RESULTYR
          MOVE      ZERO,OVRCD
          MATCH     RESULTYR,YEAROPEN
          CALL      RESOPN00 IF NOT EQUAL
          BRANCH    OVRCD,CMLCOL99
          PACK      KEY17,CLRESKEY[CLARRCNT],SP70
          CALL      RDREHA1
          PACK      KEY21,CLRESKEY[CLARRCNT],SP70
.
          CALL      CCOLDT00      *  Collection Cumulative Details
.
          SUB       ONE,CLARRCNT
          IF        CLARRCNT>0
            GOTO      CMLCOL10
          ENDIF
CMLCOL99  RETURN
.------------------------------------------------------------
. Output Result Detail Table
.------------------------------------------------------------
CCOLDT00  PACK      KEY21,REHARDT,REHARTM,SP70
          PACK      SAVKEY13,REHARDT,REHARTM,SP70
          MOVE      SP70,LASTRUN
          MOVE      REHADSS,COLLDSS
          MOVE      REHAPID,COLLPID
          CALL      RSREDA1
CCOLDT10  CALL      RKREDA1
          BRANCH    OVRCD,CCOLDT99
.
          PACK      KEY13,REDARDT,REDARTM,SP70
          MATCH     SAVKEY13,KEY13
          GOTO      CCOLDT99 IF NOT EQUAL
.
          MATCH     "D ",REDASTA
          GOTO      CCOLDT10 IF EQUAL
.
          MOVE      ZERO,EXIT
          MATCH     LASTRUN,REDARUN
          CALL      CCOLHD00 IF NOT EQUAL
          BRANCH    EXIT,CCOLDT10
.
          MATCH     "NM",REDAVTY       * Must be Numeric Result for Cumulative
          GOTO      CCOLDT20 IF EQUAL
          MATCH     "ST",REDAVTY       * Must be Numeric Result for Cumulative
          GOTO      CCOLDT20 IF EQUAL
          MATCH     "TX",REDAVTY       * Must be Numeric Result for Cumulative
          GOTO      CCOLDT20 IF EQUAL
          MATCH     "SN",REDAVTY       * Must be Numeric Result for Cumulative
          GOTO      CCOLDT20 IF EQUAL
          GOTO      CCOLDT10
.
CCOLDT20  CALL      CMLROW00
          GOTO      CCOLDT10
.
CCOLDT99  RETURN
.------------------------------------------------------------
. Change in Result Run No
.------------------------------------------------------------
CCOLHD00  MOVE      ZERO,EXIT
          MOVE      REDARUN,LASTRUN
          PACK      KEY17,REDARDT,REDARTM,REDARUN
          MOVE      REDARDT,RESULTYR
          MOVE      ZERO,OVRCD
          MATCH     RESULTYR,YEAROPEN
          CALL      RESOPN00 IF NOT EQUAL
          BRANCH    OVRCD,CCOLHD90
          CALL      RDREHA1
          BRANCH    OVRCD,CCOLHD90
          MATCH     COLLPID,REHAPID
          GOTO      CCOLHD90 IF NOT EQUAL
          MATCH     COLLDSS,REHADSS
          GOTO      CCOLHD90 IF NOT EQUAL
          IF        FHIRTYPE=1
            PACK      FHRDIRID,REHARDT,REHARTM,REHARUN,SP70
            REP       " -",FHRDIRID
            COMPARE   CLARREND,CLARRCNT
            IF        !@EQUAL
              WRITE     HTMLFILE;*+,",";
            ENDIF
            WRITE     HTMLFILE;*+,"{":
                                  " #"fullUrl#": #"%BASEURL/DiagnosticReport/",FHRDIRID,"#",":
                                  " #"resource#": ";
            CALL      RESDIR00
            WRITE     HTMLFILE;*+,"}";
            PACK      KEY17,REHARDT,REHARTM,REHARUN
            PACK      KEY21,REHARDT,REHARTM,REHARUN,SP70
            CALL      RSREDA1
          ENDIF
          MOVE      ZERO,EXIT
          GOTO      CCOLHD99
.
CCOLHD90  PACK      KEY21,REHARDT,REHARTM,REHARUN,Z70
          CALL      RSREDA1
          MOVE      ONE,EXIT
.
CCOLHD99  RETURN
.------------------------------------------------------------
. Update Result to match a patient
.------------------------------------------------------------
UPDRES00  MOVE      RESULTKY,RESULTYR
          MOVE      ZERO,OVRCD
          MATCH     RESULTYR,YEAROPEN
          CALL      RESOPN00 IF NOT EQUAL
          BRANCH    OVRCD,UPDRES95
.
          CALL      LOCLNK00                * Lock link file 
          BRANCH    EXIT,UPDRES99
.
          CALL      UPDHE000                * Update header & details
          BRANCH    EXIT,UPDRES99
.
          CALL      WRLNKS00                * write link records
.
          CALL      DELLNK00                * delete error link
          BRANCH    EXIT,UPDRES99
.
UPDRES90  MOVE      "Result Matched to Patient",WEBTITLE
          CALL      OPENHTML
          BRANCH    EXIT,UPDRES99
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "    alert(#"",WEBTITLE,"#");":
.                            "    history.go(-2);":
                             "    history.go(-3);":
                             "}":
                             "</script>"
          CALL      CLOSHTML
          GOTO      UPDRES99
.
UPDRES95  MOVE      "Invalid Result Key",WEBTITLE
          CALL      WEBERR00
.
UPDRES99  RETURN
.------------------------------------------------------------
. Update any Correspondance Links
.------------------------------------------------------------
UPDCOR00  MOVE      URNUMBER,CORRURNO
          MOVE      ADMISSNO,CORRADMN
          READ      CONTROLF,NINTY1;*219,CWEBCLOC
          PACK      KEY4,CWEBCLOC
          CALL      RDOBPT1
          BRANCH    OVRCD,UPDCOR91
.
          PACK      KEY8,CORRADMN        * validate admissno
          MOVE      CORRADMN,SAVISIT
          MATCH     SP70,CORRADMN
          IF        @EQUAL|@EOS
            MOVE      "00000000",SAVISIT
          ENDIF
.
          SQUEEZE   CORRADMN
          MOVE      CORRADMN,F8
          IF        F8=0
            MOVE      "00000000",SAVISIT
          ENDIF
.
          CALL      RDREF000             * Get Referral Details from RESDEBFD
.
          MOVE      ONE,CORRESID
          PACK      KEY20,CORRURNO,SAVISIT,Z70       
          CALL      RSOBPC1
          CALL      RPOBPC1              * Positions on last value
          BRANCH    OVRCD,UPDCOR10
          MATCH     CORRURNO,OBPCURNO
          GOTO      UPDCOR10 IF NOT EQUAL
          MATCH     SAVISIT,OBPCCVIS
          GOTO      UPDCOR10 IF NOT EQUAL
          MOVE      OBPCCPID,CORRESID    
          ADD       ONE,CORRESID            
.
UPDCOR10  MOVE      CORRESID,OBPCCPID          * Correspondance ID
          MOVE      SAVISIT,OBPCCVIS
          MOVE      CORRURNO,OBPCURNO
.
          CALL      IBACLOCK
          PACK      OBPCDTLU,CCC,CYY,CMM,CDD   * Last update date 
          REP       " 0",OBPCDTLU
          MOVE      CTIMEIS,OBPCTMLU           * Last Update time
.
          MOVE      "HL7ORUMS",OBPCINUS        * Web User ID
          MOVE      "HL7ORUMS",OBPCLUID        * Last Web User ID
          MOVE      ZERO,OBPCMDEL              * Delete indicator not set
.
          PACK      OBPCFEXT,DOT,FILEEXTN,SP70
          REP       LOWUPP,OBPCFEXT             * Make Lowercase Extension
.
          MOVE      OBPTSLID,OBPCSLID           * Storage Location ID
          MOVE      REHARDT,OBPCINDT            * Input date 
          MOVE      REHARTM,OBPCINTM            * Input time 
          MOVE      REHALAB,OBPCCTYP            * Type of Correspondance (Lab Code By Default)
          MATCH     SP70,REHADSS
          IF        !@EQUAL
            MOVE      REHADSS,KEY3              * Type of Correspondance
            PACK      KEY5,CATWI,KEY3
            CALL      RDCODE1
            IF        OVRCD=0
              MOVE      REHADSS,OBPCCTYP    * Type of Correspondance Use DSS if Valid in CATwi
            ENDIF
          ENDIF
          MOVE      SP70,OBPCSLEV       * Security Level
          MOVE      "Diagnoistic Result Attachment",OBPCCOM1       * Comment 1
          MOVE      SP70,OBPCCFRM  
          MOVE      SP70,OBPCCTOO
          MATCH     SP70,RFTYPE
          IF        !@EQUAL
            MOVE      RFTYPE,OBPCCOM1      * Referral Type
          ENDIF
.
          MATCH     SP70,RFREAS
          IF        !@EQUAL
            MOVE      RFREAS,OBPCCOM2      * Referral Reason
          ENDIF
.
          MATCH     SP70,RTNAME
          IF        !@EQUAL
            MOVE      RTNAME,OBPCCTOO      * Referred Too Provider from PRD Seg
          ENDIF
.
          MATCH     SP70,RPNAME
          IF        !@EQUAL
            MOVE      RPNAME,OBPCCFRM      * Referring Provider from PRD Seg
          ENDIF
.
          MOVE      SP70,OBPCUDF1       * User Def Code 1
          MOVE      SP70,OBPCUDF2       * User Def Code 2
          MOVE      SP70,OBPCUYN1       * User Def Y/N 1
          MOVE      SP70,OBPCUYN2       * User Def Y/N 2
          MOVE      SP70,OBPCUDD1       * User Def Date 1
          MOVE      SP70,OBPCUDD2       * User Def Date 2
          MOVE      SP70,OBPCACAT       * Alert Category
          MOVE      SP70,OBPCACOD       * Alert Code          
.
          MOVE      SAVISIT,OBPCCVIS
          MOVE      CORRURNO,OBPCURNO
.
UPDCOR80  PACK      KEY20,OBPCURNO,OBPCCVIS,OBPCCPID
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          CALL      WROBPC1
          TRAPCLR   IO
          IF        OVRCD=1
            ADD       ONE,CORRESID
            MOVE      CORRESID,OBPCCPID     * Correspondance ID
            GOTO      UPDCOR80
          ENDIF
.
          MOVE      ANSA,OBAUTYPE           * Add
          CALL      OBSAUD00                * Clinical Documents Audit
.
          MATCH     "00000000",OBPCCVIS
          IF        @EQUAL
            MOVE      "UR",KEY2
            PACK      CORSFILE,KEY2,OBPCURNO,DASH,OBPCCPID,OBPCFEXT 
          ELSE
            PACK      CORSFILE,OBPCCVIS,DASH,OBPCCPID,OBPCFEXT 
          ENDIF
          STRIP     CORSFILE
          REP       " 0",CORSFILE
.
          STRIP     OBPTSDIR
          PACK      CMDLN500,CMDMOVE,SP1,REDAVST,SP1,OBPTSDIR,CORSFILE,SP70
          EXECUTE   CMDLN500,TASKID
          CALL      LNKRES00                * Link Result and Document
.
          MOVE      ZERO,EXIT               * To go to next page
.
          PACK      KEY8,CORRURNO,SP70      * validate UR
          CALL      RDMAST1
          BRANCH    OVRCD,UPDCOR99
          MATCH     "1",PTMXSIN4
          IF        !@EQUAL
            PACK      KEY8,CORRURNO,SP70    * validate UR
            CALL      RLPTMAS1
            BRANCH    OVRCD,UPDCOR99,UPDCOR99
            MOVE      "1",PTMXSIN4
            CALL      UPMAST1
            CALL      UUPTMAS1
          ENDIF
          GOTO      UPDCOR99
.
UPDCOR90  CLEAR     WEBTITLE
          APPEND    "Invalid U/r Number - ",WEBTITLE
          APPEND    KEY8,WEBTITLE
          RESET     WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      UPDCOR99
.
UPDCOR91  CLEAR     WEBTITLE
          APPEND    "Invalid Storage Location ID From Control File - ",WEBTITLE
          APPEND    KEY4,WEBTITLE
          RESET     WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      UPDCOR99
.
UPDCOR99  RETURN
.--------------------------------------------------------------------------------
. Write Referral Detail into RESDEBFD for error correction processing
.--------------------------------------------------------------------------------
RDREF000  MOVE      SP70,RPNAME
          MOVE      SP70,RTNAME
          MOVE      SP70,RFREAS
          MOVE      SP70,RFTYPE
          MOVE      "pdf",FILEEXTN
          MOVE      "pdf",FILETYPE
.
          MOVE      ONE,LINENO
          PACK      KEY24,REDARDT,REDARTM,REDARUN,REDASID,LINENO
          CALL      RDREDB1
          IF        OVRCD=0
            MOVE      REDBTXT,RFTYPE
          ENDIF
.
          MOVE      TWO,LINENO
          PACK      KEY24,REDARDT,REDARTM,REDARUN,REDASID,LINENO
          CALL      RDREDB1
          IF        OVRCD=0
            MOVE      REDBTXT,RFREAS
          ENDIF
.
          MOVE      THREE,LINENO
          PACK      KEY24,REDARDT,REDARTM,REDARUN,REDASID,LINENO
          CALL      RDREDB1
          IF        OVRCD=0
            MOVE      REDBTXT,RTNAME
          ENDIF
.
          MOVE      FOUR,LINENO
          PACK      KEY24,REDARDT,REDARTM,REDARUN,REDASID,LINENO
          CALL      RDREDB1
          IF        OVRCD=0
            MOVE      REDBTXT,RPNAME
          ENDIF
.
          MOVE      FIVE,LINENO
          PACK      KEY24,REDARDT,REDARTM,REDARUN,REDASID,LINENO
          CALL      RDREDB1
          IF        OVRCD=0
            MOVE      REDBTXT,FILEEXTN
          ENDIF
.
          MOVE      SIX,LINENO
          PACK      KEY24,REDARDT,REDARTM,REDARUN,REDASID,LINENO
          CALL      RDREDB1
          IF        OVRCD=0
            MOVE      REDBTXT,FILETYPE
          ENDIF
.
          RETURN
.------------------------------------------------------------
. Process and Close Clinical Document
.------------------------------------------------------------
LNKRES00  MOVE      ZERO,F2
          PACK      RESULTKY,REHARDT,REHARTM,REHARUN,SP70
          PACK      KEY20,RESULTKY,Z70
          CALL      RSRECL1
          CALL      RPRECL1
          BRANCH    OVRCD,LNKRES10
          PACK      KEY17,RECLRDT,RECLRTM,RECLRUN,SP70
          MATCH     KEY17,RESULTKY
          GOTO      LNKRES10 IF NOT EQUAL
          MOVE      RECLLNO,F2
LNKRES10  ADD       ONE,F2
          MOVE      F2,KEY2
          REP       " 0",KEY2
          PACK      KEY20,RESULTKY,KEY2
          CALL      RDRECL1
          COMPARE   ZERO,OVRCD
          GOTO      LNKRES10 IF EQUAL
.
          UNPACK    KEY20,RECLRDT,RECLRTM,RECLRUN,RECLLNO
          MOVE      OBPCURNO,RECLURN
          MOVE      OBPCCVIS,RECLVIS
          MOVE      OBPCCPID,RECLCID
          MOVE      SP70,RECLSPA
          CALL      WRRECL1
.
LNKRES99  RETURN
.------------------------------------------------------------
.   Update header details
.------------------------------------------------------------
UPDHE000  MOVE      ZERO,EXIT 
          REP       "+ ",URNUMBER 
          PACK      KEY8,URNUMBER
          CALL      RDMAST1
          BRANCH    OVRCD,UPDHE900
.
          UNPACK    RESULTKY,REHARDT,REHARTM,REHARUN
          PACK      KEY17,REHARDT,REHARTM,REHARUN
          MOVE      RESULTKY,RESULTYR                                 *I-148026
          MOVE      ZERO,OVRCD
          MATCH     RESULTYR,YEAROPEN
          CALL      RESOPN00 IF NOT EQUAL
          BRANCH    OVRCD,UPDHE999                                    *I-148026
.
          CALL      RDREHA1
          IF        OVRCD=0
            PACK      REHAPID,URNUMBER,SP70 
            MOVE      ONE,REHAFUR
            PACK      REHAVIS,ADMISSNO,SP70
            PACK      KEY17,REHARDT,REHARTM,REHARUN
            CALL      UPREHA1
          ENDIF
.
          PACK      KEY21,RESULTKY,SP70
          CALL      RSREDA1
UPDHE100  CALL      RKREDA1
          BRANCH    OVRCD,UPDHE999
          PACK      KEY17,REDARDT,REDARTM,REDARUN
          MATCH     KEY17,RESULTKY
          GOTO      UPDHE999 IF NOT EQUAL
          MOVE      REHAPID,REDAPID
          CALL      UPREDA1
.
          MATCH     "ED",REDAVTY
          CALL      UPDCOR00 IF EQUAL    * Update Embedded Document into Correspondance Table
.
          GOTO      UPDHE100
.
UPDHE900  MOVE      "Invalid U/R Number",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
.           
UPDHE999  RETURN
.----------------------------------------------------------------------
.  Write results link record
.----------------------------------------------------------------------
WRLNKS00  MOVE      "01",LINKTYPE
          PACK      LINKKEYS,REHAPID,SP70
          CALL      NEWLNK00
.
          MOVE      ADMISSNO,KEY8
          CALL      RDVISA1            * Check for Visit No Passed in Message
          BRANCH    OVRCD,WRLNKS99     * Not Valid
.
          MOVE      "02",LINKTYPE
          PACK      LINKKEYS,PVIBILL,SP70
          CALL      NEWLNK00
.
          BRANCH    PVITYPE,WRLNKS10,WRLNKS20,WRLNKS30
.
WRLNKS10  PACK      KEY8,PVIBILL
          CALL      RDDETA1
          BRANCH    OVRCD,WRLNKS99
.
          MOVE      "03",LINKTYPE
          PACK      LINKKEYS,ADADOCT,SP70
          CALL      UPDADC00
          CALL      NEWLNK00
.
          MOVE      "07",LINKTYPE
          PACK      LINKKEYS,ADADOCT,SP70
          CALL      NEWLNK00
.
          MOVE      "05",LINKTYPE
          PACK      LINKKEYS,AEDARFGP,SP70
          CALL      NEWLNK00
.
          MOVE      "03",LINKTYPE
          PACK      LINKKEYS,AEDARDOC,SP70
          CALL      NEWLNK00
.
          MOVE      "08",LINKTYPE
          PACK      LINKKEYS,AEDARDOC,SP70
          CALL      NEWLNK00
          GOTO      WRLNKS99
.
WRLNKS20  MOVE      PVISITE,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD,WRLNKS99
.
          MOVE      OSTFILE,CFILEPRE
          PACK      CFNAME,CFILEPRE,FILBB1A1
          CALL      OPOTBB11
.
          PACK      KEY8,PVIBILL
          CALL      RDBOKB1
          BRANCH    OVRCD,WRLNKS99
.
          MOVE      "03",LINKTYPE
          PACK      LINKKEYS,OBADOCT,SP70
          CALL      UPDADC00
          CALL      NEWLNK00
.
          MOVE      "07",LINKTYPE
          PACK      LINKKEYS,OBADOCT,SP70
          CALL      NEWLNK00
.
          MOVE      "05",LINKTYPE
          PACK      LINKKEYS,OTBBRFGP,SP70
          CALL      NEWLNK00
.
          MOVE      "06",LINKTYPE
          PACK      LINKKEYS,PVIDOCT,SP70   * Clinic / doctor
          CALL      NEWLNK00
.
          GOTO      WRLNKS99
.
WRLNKS30  PACK      KEY8,PVIBILL
          CALL      RDMISS1
          BRANCH    OVRCD,WRLNKS99          * No record
.
          MOVE      "04",LINKTYPE           * Ward link
          PACK      LINKKEYS,AWARD,SP70
          CALL      NEWLNK00
.
          MOVE      "03",LINKTYPE           * Attending doctor link
          PACK      LINKKEYS,ADOCTA,SP70
          CALL      UPDADC00
          CALL      NEWLNK00
.
          MOVE      "07",LINKTYPE           * Attending doctor link
          PACK      LINKKEYS,ADOCTA,SP70
          CALL      NEWLNK00
.
          MOVE      "03",LINKTYPE           * Treating doctor link
          PACK      LINKKEYS,ADOCTT,SP70
          CALL      NEWLNK00
.
          MOVE      "03",LINKTYPE           * Referring Doctor link
          PACK      LINKKEYS,ADOCTR,SP70
          CALL      NEWLNK00
.
          MOVE      "08",LINKTYPE           * Referring Doctor link
          PACK      LINKKEYS,ADOCTR,SP70
          CALL      NEWLNK00
          GOTO      WRLNKS99
.
WRLNKS99  RETURN
.------------------------------------------------------------
. New link record
.------------------------------------------------------------
NEWLNK00  MATCH     SP70,LINKKEYS
          GOTO      NEWLNK99 IF EQUAL
.
          PACK      KEY29,LINKTYPE,LINKKEYS,REHARDT,REHARTM,REHARUN
          UNPACK    KEY29,RELNLTY,RELNLKY,RELNRDT,RELNRTM,RELNRUN
.
          PACK      RESLNKFL,PRELEN,RELNRDT           * I CAR 38290
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      RESLNKA1,RESLNKFL
          TRAPCLR   IO
.
          COMPARE   ONE,OVRCD
          GOTO      NEWLNK99 IF EQUAL                 * end I CAR 38290
.
          CALL      RDRELN1
          IF        OVRCD=1
            MOVE      ZERO,RELNMSN            * Marked as seen, 0=no.
            MOVE      SP70,RELNSDT
            MOVE      SP70,RELNSTM
            MOVE      SP70,RELNSPC
            MOVE      REHAFUR,RELNFUR
            MOVE      REHAPID,RELNPID
            MOVE      REHAUCS,RELNUCS         * coding scheme
            MOVE      REHAUSC,RELNUSC         * code
            MOVE      REHADSS,RELNDSS
            MOVE      REHANOR,RELNNOR
            MOVE      REHARST,RELNRST
            MOVE      REHALAB,RELNLAB
            MOVE      ZERO,RELNRTY            * 0=HL7
            PACK      KEY29,RELNLTY,RELNLKY,RELNRDT,RELNRTM,RELNRUN
            CALL      WRRELN1
          ENDIF
.
          MATCH     "01",LINKTYPE
          GOTO      NEWLNK99 IF NOT EQUAL
.
          MOVE      LINKKEYS,KEY8
          PACK      KEY25,KEY8,REHARDT,REHARTM,REHARUN,SP70
          UNPACK    KEY25,RELUURN,RELURDT,RELURTM,RELURUN
          CALL      RDRELU1
          COMPARE   ZERO,OVRCD
          GOTO      NEWLNK99 IF EQUAL
          MOVE      ZERO,RELUMSN            * Marked as seen, 0=no.
          MOVE      SP70,RELUSDT
          MOVE      SP70,RELUSTM
          MOVE      SP70,RELUSPC
          MOVE      REHAFUR,RELUFUR
          MOVE      REHAPID,RELUPID
          MOVE      REHAUCS,RELUUCS         * coding scheme
          MOVE      REHAUSC,RELUUSC         * code
          MOVE      REHADSS,RELUDSS
          MOVE      REHANOR,RELUNOR
          MOVE      REHARST,RELURST
          MOVE      REHALAB,RELULAB
          MOVE      ZERO,RELURTY            * 0=HL7
          CALL      WRRELU1
          MOVE      RELUURN,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,NEWLNK99
          MATCH     "1",PTMXSIN2
          GOTO      NEWLNK99 IF EQUAL
.
          MOVE      RELUURN,KEY8
          CALL      RLPTMAS1
          BRANCH    OVRCD,NEWLNK99,NEWLNK99
          MOVE      "1",PTMXSIN2
          CALL      UPMAST1
          CALL      UUPTMAS1
.
NEWLNK99  RETURN
.------------------------------------------------------------
.  Lock error link
.------------------------------------------------------------
LOCLNK00  PACK      RESLNKFL,PRELEN,RESULTKY          * I CAR 38290
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      RESLNKA3,RESLNKFL
          TRAPCLR   IO
.
          COMPARE   ONE,OVRCD
          GOTO      LOCLNK90 IF EQUAL                 * end I CAR 38290
.
          MOVE      "99",LINKTYPE
          PACK      KEY29,RESULTKY,LINKTYPE,SP70
          CALL      RSRELN3
          CALL      RKRELN3
          BRANCH    OVRCD,LOCLNK90
          PACK      KEY17,RELNRDT,RELNRTM,RELNRUN
          MATCH     KEY17,RESULTKY
          GOTO      LOCLNK90 IF NOT EQUAL
          MATCH     LINKTYPE,RELNLTY
          GOTO      LOCLNK90 IF NOT EQUAL
          PACK      KEY29,RELNRDT,RELNRTM,RELNRUN,RELNLTY,RELNLKY,SP70
          CALL      RLRELN3
          BRANCH    OVRCD,LOCLNK90,LOCLNK95
          MOVE      ZERO,EXIT
          GOTO      LOCLNK99
.
LOCLNK90  MOVE      "ERROR CAN'T FIND ERROR LINK",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      LOCLNK99
.
LOCLNK95  MOVE      "ERROR LINK LOCKED",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      LOCLNK99
.
LOCLNK99  RETURN
.------------------------------------------------------------
.    Delete error link
.------------------------------------------------------------
DELLNK00  PACK      RESLNKFL,PRELEN,RESULTKY          * I CAR 38290
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      RESLNKA3,RESLNKFL
          TRAPCLR   IO
.
          COMPARE   ONE,OVRCD
          GOTO      DELLNK90 IF EQUAL                 * end I CAR 38290
. 
          MOVE      "99",LINKTYPE
          PACK      KEY29,RESULTKY,LINKTYPE,SP70
          CALL      RSRELN3
          CALL      RKRELN3
          BRANCH    OVRCD,DELLNK90
          PACK      KEY17,RELNRDT,RELNRTM,RELNRUN
          MATCH     KEY17,RESULTKY
          GOTO      DELLNK90 IF NOT EQUAL
          MATCH     LINKTYPE,RELNLTY
          GOTO      DELLNK90 IF NOT EQUAL
.
          PACK      KEY29,RELNRDT,RELNRTM,RELNRUN,RELNLTY,RELNLKY
          CALL      DERELN3                 * delete error link
.        
          MOVE      RELNLKY,KEY10  
          CALL      RLRELG1                 * Lock the log file
          BRANCH    OVRCD,DELLNK90,DELLNK95
.
          PACK      KEY10,RELGUID
          CALL      RDRELG1
          IF        OVRCD=0
            SUB       ONE,RELGERR 
            CALL      UPRELG1               * update the log file
          ENDIF
.
          MOVE      ZERO,EXIT
          CALL      UURELN1                 * unlock error link
          CALL      UURELG1                 * Unlock the log file
          GOTO      DELLNK99
.
DELLNK90  MOVE      "ERROR CAN'T FIND LOG RECORD",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      DELLNK99
.
DELLNK95  MOVE      "LOG RECORD LOCKED",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      DELLNK99
.
DELLNK99  RETURN
.------------------------------------------------------------
. Update Attending Doctor in Header Record
.------------------------------------------------------------
UPDADC00  PACK      KEY17,REHARDT,REHARTM,REHARUN
          MOVE      REHARDT,RESULTYR                                  *I-148026
          MOVE      ZERO,OVRCD
          MATCH     RESULTYR,YEAROPEN
          CALL      RESOPN00 IF NOT EQUAL
          BRANCH    OVRCD,UPDADC99                                    *I-148026
.
          CALL      RDREHA1
          BRANCH    OVRCD,UPDADC99
          MOVE      LINKKEYS,REHAADC
          CALL      UPREHA1
UPDADC99  RETURN
.----------------------------------------------------------------------
          INC       PMSUCHIO/INC
          INC       PMSUCDIO/INC
          INC       PATHSPIO/INC
          INC       PATMI1IO/INC
          INC       AAEDE1IO/INC
          INC       OUTBB1IO/INC
          INC       OUTSITIO/INC
          INC       PATALRIO/INC
          INC       PATFN1IO/INC
          INC       MRTMASIO/INC
.------------------------------------------------------------
. Cumulative Result Table Output
.------------------------------------------------------------
CMLNTE00  MOVE      ONE,NTARRCNT
          MOVE      ZERO,CHCKNOTE 
.
CMLNTE10  MOVE      CMRESKEY[NTARRCNT],RESULTYR
          MOVE      ZERO,OVRCD
          MATCH     RESULTYR,YEAROPEN
          CALL      RESOPN00 IF NOT EQUAL
          BRANCH    OVRCD,CMLNTE40
          PACK      KEY17,CMRESKEY[NTARRCNT],SP70
          CALL      RDREHA1
          PACK      KEY21,CMRESKEY[NTARRCNT],SP70
          CALL      RSREDA1
CMLNTE20  CALL      RKREDA1
          BRANCH    OVRCD,CMLNTE40
.
          PACK      KEY17,REDARDT,REDARTM,REDARUN
          MATCH     KEY17,CMRESKEY[NTARRCNT]
          GOTO      CMLNTE40 IF NOT EQUAL 
.
          MATCH     "FT",REDAVTY            
          GOTO      CMLNTE20 IF NOT EQUAL
.
. Comment Found - Display comment
.
          MOVE      "OBX03",KEY5
          PACK      KEY32,REHALAB,KEY5,REDAOCS,REDAOTY,SP70
          CALL      RDRECT1
          IF        OVRCD=1
            PACK      RECTDES,REDAOTY,SP70
          ENDIF
.
          UNPACK    REDARDT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
.
          IF        TEXTFMT=0
            WRITE     HTMLFILE;"<tr><td class=HeadingCell align=center":
                               " colspan=6>",RECTDES,REDAOSD," for ":
                               CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR," at ",REDARTM:
                               "</td></tr>":
                               "<tr><td colspan=6>";
          ELSE  
            WRITE     HTMLFILE;"<tr><td class=ResultID>",RECTDES,REDAOSD," for ":
                               CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR," at ",REDARTM:
                               "</td>":
                               "<td class=ResultTX colspan=5>";
          ENDIF
          MOVE      RESULTKY,SAVRESKY
          MOVE      CMRESKEY[NTARRCNT],RESULTKY
          CALL      TXTDEB00
          MOVE      SAVRESKY,RESULTKY
          WRITE     HTMLFILE;"</td></tr>"
.
          CALL      GETABF00         * Get Abnormal Flag
          CALL      ABOBX000         * Insert Abnormal Cell to Table
          GOTO      CMLNTE20
.
.  Write NTE segments
.
CMLNTE30  MOVE      ONE,CHCKNOTE 
          PACK      KEY24,CMRESKEY[NTARRCNT],SP70
          CALL      RSREDC1
          CALL      RKREDC1
          BRANCH    OVRCD,CMLNTE40    * Note does not exist
          PACK      KEY17,REDCRDT,REDCRTM,REDCRUN,SP70
          MATCH     CMRESKEY[NTARRCNT],KEY17
          GOTO      CMLNTE40 IF NOT EQUAL
.
          UNPACK    REDCRDT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
.
          IF        TEXTFMT=0
            WRITE     HTMLFILE;"<tr><td class=HeadingCell align=center ":
                               "colspan=6> Notes for ":
                               CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR," at ",REDCRTM:
                               "</td></tr>";
            WRITE     HTMLFILE;"<tr><td>";
          ELSE 
            WRITE     HTMLFILE;"<tr><td class=ResultID> Notes for ":
                               CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR," at ",REDCRTM:
                               "</td><td class=ResultTX colspan=5>";
          ENDIF
          PACK      SAVRESKY,RESULTKY
          PACK      RESULTKY,KEY17,SP70
          CALL      NTEDEA00
          PACK      RESULTKY,SAVRESKY
          WRITE     HTMLFILE;"</td></tr>"
.
          GOTO      CMLNTE20
.
CMLNTE40  COMPARE   ONE,CHCKNOTE
          GOTO      CMLNTE30 IF NOT EQUAL
.
          ADD       ONE,NTARRCNT
          IF        NTARRCNT>CMARRCNT 
            GOTO      CMLNTE99
          ENDIF
          COMPARE   ONE,FRSTONLY
          GOTO      CMLNTE99 IF EQUAL
.
          MOVE      ZERO,CHCKNOTE 
          GOTO      CMLNTE10
.
CMLNTE99  RETURN
.
.------------------------------------------------------------
. Visit Summary Results Details
.------------------------------------------------------------
RESSUM00  MATCH     SP70,RESULTKY            * Check for Single Result Only
          GOTO      RESSUM10 IF EQUAL
.
          MOVE      RESULTKY,RESULTYR
          MOVE      ZERO,OVRCD
          MATCH     RESULTYR,YEAROPEN
          CALL      RESOPN00 IF NOT EQUAL
          BRANCH    OVRCD,RESDET90
.
          MOVE      RESULTKY,KEY17
          CALL      RDREHA1    * Check Result on File
          BRANCH    OVRCD,RESDET90
.
          CALL      RESSEC00   * Check Result Level Security
          BRANCH    EXIT,RESDET99
.
          CALL      WRTAUD00           * Write Audit Record
.
          CALL      GETPID00           * Get Patient Details
          BRANCH    OVRCD,RESDET91
          GOTO      RESSUM20
.
RESSUM10  BRANCH    FHIRTYPE,RESSUM15  * No admission no in FHIR request
          MATCH     SP70,ADMISSNO
          GOTO      RESSUM90 IF EQUAL
.
RESSUM15  MOVE      "1",REHAFUR
          MOVE      URNUMBER,REHAPID 
          MOVE      ADMISSNO,REHAVIS
          CALL      GETPID00           * Get Patient Details
          BRANCH    OVRCD,RESSUM91
.
RESSUM20  CLEAR     WEBTITLE
          APPEND    "Result Summary",WEBTITLE
          RESET     WEBTITLE
.
          STRIP     PSNAME             * I CAR 44832
          STRIP     PGNAME
          PACK      WEBTITLE,WEBTITLE,SP1,PSNAME,COMMA,SP1,PGNAME,SP1,PURNO
          RESET     WEBTITLE
          SETLPTR   PSNAME
          SETLPTR   PGNAME             * end I CAR 44832
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,RESSUM99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          GOTO      RESSUM99
.
RESSUM90  MOVE      "Invalid Visit Key",WEBTITLE
          CALL      WEBERR00
          GOTO      RESSUM99
.
RESSUM91  MOVE      "Invalid Patient ID Key",WEBTITLE
          CALL      WEBERR00
          GOTO      RESSUM99
.
RESSUM99  RETURN
.
.------------------------------------------------------------
. FHIR Observation Summary View
.------------------------------------------------------------
FHIR0000  BRANCH    FLDITMNO,FHIR0100
          GOTO      FHIR9999
.
FHIR0100  MOVE      ONE,SHOWMETA
          PACK      FHROBSID,REDARDT,REDARTM,REDARUN,REDASID,SP70   * Save Key
          REP       " -",FHROBSID
.
          IF        FHIRBUND=0
            CALL      RESOBS00
          ELSE
.
            WRITE     HTMLFILE;*+," {":
                                  " #"fullUrl#": #"%BASEURL/Observation/",FHROBSID,"#",":
                                  " #"resource#": ";
            CALL      RESOBS00
            WRITE     HTMLFILE;*+," }";
.
            IF        FHRSUBIN=1
              WRITE     HTMLFILE;*+,",{":
                                    " #"fullUrl#": #"%BASEURL/Patient/",FHRPATID,"#",":
                                    " #"resource#": ";
              CALL      RESPAT00
              WRITE     HTMLFILE;*+," }";
            ENDIF
          ENDIF
          GOTO      FHIR9999
.
FHIR9999  RETURN
.
.------------------------------------------------------------
. Result Summary View
.------------------------------------------------------------
SUMF0000  BRANCH    FLDITMNO,SUMF0100,SUMF0200,SUMF0300,SUMF0400,SUMF0500:
                             SUMF0600,SUMF0700,SUMF0800
          GOTO      SUMF9999
.
SUMF0100  MOVE      ZERO,TEXTFMT
          CALL      VITAB000
          GOTO      SUMF9999
.
SUMF0200  MOVE      ZERO,TEXTFMT
          MOVE      RESULTKY,KEY17
          CALL      RDREHA1            * Check Result on File
          BRANCH    OVRCD,SUMF9999
.
          MOVE      "01",LINKTYPE      * Mark Read on U/R
          PACK      LINKKEYF,REHAPID,SP70
          PACK      KEY29,LINKTYPE,LINKKEYF,RESULTKY
          CALL      RDRELN1
          BRANCH    OVRCD,SUMF9999
.
          CALL      SUMDET00   * Output Result Table
          GOTO      SUMF9999
.
SUMF0300  CALL      GRIDU000            * Visit Cumulative Results Grid by UR
          GOTO      SUMF9999
.
SUMF0400  CALL      GRIDA000            * Visit Cumulative Results Grid by Admission
          GOTO      SUMF9999
.
SUMF0500  CALL      RESVAL00            * Get Most Recent Specific Result Value, Date and Time
          GOTO      SUMF9999
.
SUMF0600  CALL      URTAB000
          GOTO      SUMF9999
.
SUMF0700  MOVE      ONE,TEXTFMT         * Fix <pre> format TXT
          CALL      VITAB000
          GOTO      SUMF9999
.
SUMF0800  MOVE      ONE,TEXTFMT         * Fix <pre> format TXT
          MOVE      RESULTKY,KEY17
          CALL      RDREHA1            * Check Result on File
          BRANCH    OVRCD,SUMF9999
.
          MOVE      "01",LINKTYPE      * Mark Read on U/R
          PACK      LINKKEYF,REHAPID,SP70
          PACK      KEY29,LINKTYPE,LINKKEYF,RESULTKY
          CALL      RDRELN1
          BRANCH    OVRCD,SUMF9999
.
          CALL      SUMDET00   * Output Result Table
          GOTO      SUMF9999
.
SUMF9999  RETURN
.------------------------------------------------------------
. Get latest result value for specific detail result
.   URNUMBER  - Patient
.   FILTRLAB  - Laboratory
.   FILTROCS  - Coding System
.   FILTROTY  - Result Code
.------------------------------------------------------------
RESVAL00  CALL      IBACLOCK
          PACK      RESULTYR,CCC,CYY
          MOVE      ZERO,CHECKLST      * Check Previous Year
.
RESVAL10  MATCH     RESULTYR,YEAROPEN
          CALL      RESOPN00 IF NOT EQUAL
          BRANCH    OVRCD,RESVAL95
.
          PACK      KEY16,URNUMBER,SP70
          PACK      KEY61,KEY16,FILTROCS,FILTROTY,Z70
          CALL      RSREDA2
RESVAL50  CALL      RPREDA2
          BRANCH    OVRCD,RESVAL90
          MATCH     KEY16,REDAPID
          GOTO      RESVAL90 IF NOT EQUAL
          MATCH     FILTROCS,REDAOCS
          GOTO      RESVAL90 IF NOT EQUAL
          MATCH     FILTROTY,REDAOTY
          GOTO      RESVAL90 IF NOT EQUAL
          PACK      KEY17,REDARDT,REDARTM,REDARUN
          CALL      RDREHA1
          BRANCH    OVRCD,RESVAL50
          MATCH     FILTRLAB,REHALAB
          GOTO      RESVAL50 IF NOT EQUAL
.
          WRITE     HTMLFILE;"0|",REDARDT,REDARTM,"|",REDAVTY,"|",REDAVNM,"|",REDAVST,"|",REDAUNI,"|",REDARFR;
          GOTO      RESVAL99
.
RESVAL90  BRANCH    CHECKLST,RESVAL95
          MOVE      ONE,CHECKLST
          MOVE      RESULTYR,F4
          SUB       ONE,F4
          MOVE      F4,RESULTYR
          GOTO      RESVAL10
.
RESVAL95  WRITE     HTMLFILE;"9|No Result Found";
.
RESVAL99  RETURN
.------------------------------------------------------------
. Table Results last 10 results by Visit Number
.------------------------------------------------------------
VITAB000  
          MOVE      "10",NORECORD
          MOVE      ZERO,DISNUMB
          MOVE      ZERO,CHECKLST      * Check for Day Before on First Day of Year
.
          MOVE      ADMISSNO,KEY8
          CALL      RDVISA1            * Check for Visit No Passed in Message
          BRANCH    OVRCD,VITAB999     * Not Valid
.
          CALL      IBACLOCK
          PACK      RESULTYR,CCC,CYY
          PACK      RESLNKFL,PRELEN,RESULTYR
          REP       " 0",RESLNKFL
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      RESLNKA1,RESLNKFL
          TRAPCLR   IO
          COMPARE   ONE,OVRCD
          GOTO      VITAB999 IF EQUAL
.
VITAB090  MATCH     RESULTYR,YEAROPEN
          CALL      RESOPN00 IF NOT EQUAL
.                                              * end I CAR 38290
          MOVE      "02",LINKTYPE
          PACK      KEY29,LINKTYPE,ADMISSNO,SP2,Z70
          CALL      RSRELN1
VITAB100  CALL      RPRELN1
          BRANCH    OVRCD,VITAB900
          PACK      KEY12,RELNLTY,RELNLKY
          PACK      KEY29,LINKTYPE,ADMISSNO,SP2,Z70
          MATCH     KEY12,KEY29
          GOTO      VITAB900 IF NOT EQUAL
          MATCH     "ZZ",RELNRST
          GOTO      VITAB100 IF EQUAL
          MATCH     "F ",RELNRST
          GOTO      VITAB110 IF EQUAL
          MATCH     "C ",RELNRST
          GOTO      VITAB110 IF EQUAL
.
          GOTO      VITAB100 
.
VITAB110  CALL      SUMDET00   * Output Table Row
.
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      VITAB999 IF EQUAL
          GOTO      VITAB100
.
VITAB900  BRANCH    CHECKLST,VITAB999
          MOVE      ONE,CHECKLST
          UNPACK    PVIDATE,KEY4,CMON,CDAY
          MATCH     KEY4,RESULTYR
          GOTO      VITAB999 IF EQUAL
.
          MOVE      RESULTYR,F4
          SUB       ONE,F4
          MOVE      F4,RESULTYR
          PACK      RESLNKFL,PRELEN,RESULTYR
          REP       " 0",RESLNKFL
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      RESLNKA1,RESLNKFL
          TRAPCLR   IO
          COMPARE   ZERO,OVRCD
          GOTO      VITAB090 IF EQUAL
.          
VITAB999  RETURN
.------------------------------------------------------------
. Table Results last 10 results by Visit Number
.------------------------------------------------------------
URTAB000  MOVE      "10",NORECORD
          MOVE      ZERO,DISNUMB
          MOVE      ZERO,CHECKLST      * Check for Day Before on First Day of Year
.
          CALL      IBACLOCK
          PACK      RESULTYR,CCC,CYY
          PACK      RESLNKFL,PRELEN,RESULTYR
          REP       " 0",RESLNKFL
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      RESLNKA1,RESLNKFL
          TRAPCLR   IO
          COMPARE   ONE,OVRCD
          GOTO      URTAB999 IF EQUAL
.
URTAB090  MATCH     RESULTYR,YEAROPEN
          CALL      RESOPN00 IF NOT EQUAL
.                                              * end I CAR 38290
          MOVE      "01",LINKTYPE
          PACK      KEY29,LINKTYPE,URNUMBER,SP2,Z70
          CALL      RSRELN1
URTAB100  CALL      RPRELN1
          BRANCH    OVRCD,URTAB900
          PACK      KEY12,RELNLTY,RELNLKY
          PACK      KEY29,LINKTYPE,URNUMBER,SP2,Z70
          MATCH     KEY12,KEY29
          GOTO      URTAB900 IF NOT EQUAL
.
          MATCH     "ZZ",RELNRST
          GOTO      URTAB100 IF EQUAL
          MATCH     "F ",RELNRST
          GOTO      URTAB110 IF EQUAL
          MATCH     "C ",RELNRST
          GOTO      URTAB110 IF EQUAL
          GOTO      URTAB100 
.
URTAB110  CALL      SUMDET00   * Output Table Row
.
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      URTAB999 IF EQUAL
          GOTO      URTAB100
.
URTAB900  BRANCH    CHECKLST,URTAB999
          MOVE      ONE,CHECKLST
          MOVE      RESULTYR,F4
          SUB       ONE,F4
          MOVE      F4,RESULTYR
          PACK      RESLNKFL,PRELEN,RESULTYR
          REP       " 0",RESLNKFL
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      RESLNKA1,RESLNKFL
          TRAPCLR   IO
          COMPARE   ZERO,OVRCD
          GOTO      URTAB090 IF EQUAL
.          
URTAB999  RETURN
.------------------------------------------------------------
. Output Table Row
.------------------------------------------------------------
SUMDET00  MOVE      RELNRTY,F1
          BRANCH    F1,SUMDET99,SUMDET99   * No Output Required for PIT or Linked Document Based Results
          CALL      HL7ROW00
          MOVE      HLCODES,KEY12
          UNPACK    RELNRDT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;"<h4 class=resultHed>",CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,SP1,RELNRTM:
                             SP1,TESTLINE," (",LABDESC,") - ",KEY12,"</h4>":
                             "<table border=0 class=resultTab>"
          MOVE      ZERO,OBXLINE
          PACK      RESULTKY,RELNRDT,RELNRTM,RELNRUN,SP70
          PACK      KEY21,RESULTKY,SP70
          CALL      RSREDA1
SUMDET50  CALL      RKREDA1
          BRANCH    OVRCD,SUMDET90
.
          MOVE      ZERO,TEXTFMT
          PACK      KEY17,REDARDT,REDARTM,REDARUN,SP70
          MATCH     RESULTKY,KEY17
          GOTO      SUMDET90 IF NOT EQUAL
          MATCH     "ST",REDAVTY  * Check for null String Results
          IF        @EQUAL
            MATCH     "null",REDAVST    * Ignore null results
            GOTO      SUMDET50 IF EQUAL
          ENDIF
.
          CALL      GETABF00           * Get Abnormal Flag
          CALL      OBXHTM00
.
          GOTO      SUMDET50
.
SUMDET90  WRITE     HTMLFILE;"</table>"
SUMDET99  RETURN
+
.------------------------------------------------------------
. Observation Data (HTML)
.------------------------------------------------------------
OBXHTM00  MOVE      "OBX03",KEY5
          PACK      KEY32,RELNLAB,KEY5,REDAOCS,REDAOTY,SP70
          CALL      RDRECT1
          IF        OVRCD=1
            PACK      RECTDES,REDAOTY,SP70
          ENDIF
          MATCH     SP70,RECTDES
          IF        @EQUAL
            PACK      RECTDES,REDAOTY,SP70
          ENDIF
          MATCH     "TX",REDAVTY            * Text
          GOTO      OBXHTM10 IF EQUAL
.
          MATCH     "FT",REDAVTY            * Formatted Text
          GOTO      OBXHTM50 IF NOT EQUAL
          MOVE      ONE,TEXTFMT
.
. Text Result (Text / Formatted Text)
.------------------------------------------------------------
OBXHTM10  MATCH     "1",REDATFL
          IF        @EQUAL
            CALL      TXTSUM00
          ELSE
            MOVE      REDAVST,FIELDSTR
            CALL      HTMLE000
            MOVE      FIELDSTR,REDAVST
.
            WRITE     HTMLFILE;"<tr><td class=resultPad></td><td class=resultNam>",RECTDES,REDAOSD,"</td>"
            WRITE     HTMLFILE;"<td colspan=4 class=resultTxt>",REDAVST,"</td></tr>"
          ENDIF
          GOTO      OBXHTM99
.
. String or Numeric Result
.------------------------------------------------------------
OBXHTM50  PACK      HL7STR,REDAUNI,SP70            * Break the HL7 ^ delimiter in the UNIT field if Present
          CALL      HL7BRK00
          MOVE      HL7CMP[2],REDAUNI
          MATCH     SP70,REDAUNI
          IF        @EQUAL
            MOVE      HL7CMP[1],REDAUNI
          ENDIF
          MOVE      REDAUNI,FIELDSTR
          CALL      HTMLE000
          MOVE      FIELDSTR,REDAUNI
.
          MOVE      REDAVST,FIELDSTR
          CALL      HTMLE000
          MOVE      FIELDSTR,REDAVST
.
          MOVE      REDARFR,FIELDSTR
          CALL      HTMLE000
          MOVE      FIELDSTR,REDARFR
.
          IF        OBXLINE=0
            WRITE     HTMLFILE;"<tr><td class=resultPad></td><td class=resultNamHed>Test</td>"
            WRITE     HTMLFILE;"<td colspan=2 class=resultValHed>Result</td>"
            WRITE     HTMLFILE;"<td class=resultRanHed>Ref Range</td>"
            WRITE     HTMLFILE;"<td class=resultAbnHed>Abnormal</td></tr>"
          ENDIF
          WRITE     HTMLFILE;"<tr><td class=resultPad></td><td class=resultNam>",RECTDES,REDAOSD,"</td>"
          WRITE     HTMLFILE;"<td class=resultVal>",REDAVST,"</td>"
          WRITE     HTMLFILE;"<td class=resultUnt>",REDAUNI,"</td>"
          WRITE     HTMLFILE;"<td class=resultRan>",REDARFR,"</td>"
          WRITE     HTMLFILE;"<td class=resultAbn>",ABNORDES,"</td></tr>"
OBXHTM99  ADD       ONE,OBXLINE
          RETURN
.------------------------------------------------------------
. Output Text details
.------------------------------------------------------------
TXTSUM00  MOVE      ZERO,LINECNT
          PACK      KEY24,REDARDT,REDARTM,REDARUN,REDASID,SP70
          CALL      RSREDB1
TXTSUM10  CALL      RKREDB1
          BRANCH    OVRCD,TXTSUM99
          PACK      KEY21,REDBRDT,REDBRTM,REDBRUN,REDBSID,SP70
          MATCH     KEY21,KEY24
          GOTO      TXTSUM99 IF NOT EQUAL
.
          ADD       ONE,LINECNT
          MOVE      REDBTXT,FIELDSTR
          CALL      HTMLE000
          MOVE      FIELDSTR,REDBTXT
.
          STRIP     REDBTXT
          MOVELPTR  REDBTXT,LENGTH
          COMPARE   ZERO,LENGTH
          IF        @EQUAL
            COMPARE   ONE,LINECNT
            IF        @EQUAL
              MOVE      ZERO,LINECNT
              GOTO      TXTSUM10
            ENDIF
.           WRITE     HTMLFILE;SP1
            WRITE     HTMLFILE;"<br/>";
          ELSE
            MATCH     "Performed at",REDBTXT
            GOTO      TXTSUM20 IF EQUAL
            MATCH     "Tests Completed",REDBTXT
            GOTO      TXTSUM20 IF EQUAL
            MATCH     "Tests Pending",REDBTXT
            GOTO      TXTSUM20 IF EQUAL
            MATCH     "Sample Pending",REDBTXT
            GOTO      TXTSUM20 IF EQUAL
            MATCH     " cc:   , , , , ,",REDBTXT
            GOTO      TXTSUM20 IF EQUAL
            MATCH     "cc:   , , , , ,",REDBTXT
            GOTO      TXTSUM20 IF EQUAL
            MATCH     "cc:  , , , , ,",REDBTXT
            GOTO      TXTSUM20 IF EQUAL
            MATCH     "cc: , , , , ,",REDBTXT
            GOTO      TXTSUM20 IF EQUAL
            COMPARE   ONE,LINECNT
            IF        @EQUAL
              WRITE     HTMLFILE;"<tr><td class=resultPad></td><td colspan=5 class=resultTxt>":
                                 RECTDES,REDAOSD,"<br/><div class=resultCom>";
              IF        TEXTFMT=1
                WRITE     HTMLFILE;"<pre>";
              ENDIF
            ENDIF
            SFORMAT   VAR,LENGTH
            MOVE      REDBTXT,VAR
            WRITE     HTMLFILE;VAR,"<br/>";
          ENDIF
          SFORMAT   VAR,127
          GOTO      TXTSUM10
.
TXTSUM20  COMPARE   ONE,LINECNT
          IF        @EQUAL
            MOVE      ZERO,LINECNT
          ENDIF
          GOTO      TXTSUM10
.
TXTSUM99  IF        LINECNT>0
            IF        TEXTFMT=1
              WRITE     HTMLFILE;"</pre>";
            ENDIF
            WRITE     HTMLFILE;"</div></td></tr>"
          ENDIF
          RETURN
.------------------------------------------------------------
. Get HL7 Details
.------------------------------------------------------------
HL7ROW00  MOVE      SP70,LABDESC
          PACK      TESTLINE,SP70,SP70,SP70
          MOVE      ZERO,EXIT
.
          MOVE      "OBR04",KEY5
          MOVE      RELNUSC,RECTDES
          PACK      KEY32,RELNLAB,KEY5,RELNUCS,RELNUSC
          CALL      RDRECT1
          MOVE      RECTDES,TESTLINE
.
          PACK      KEY3,RELNLAB,SP70
          CALL      RDRELB1
          IF        OVRCD<>1
            PACK      LABDESC,RELBDES,SP70
          ENDIF
.
          MOVE      "0123",KEY4
          MOVE      RELNRST,HLCODES
          PACK      KEY14,KEY4,RELNRST,SP70
          CALL      RDHLCO1
.
          RETURN
.
.----------------------------------------------------------------------
. Routine  : HL7BRK00
.
. Function : Break up HL7 String into components
.
. Example if the following varibles is passed the routine will return
. an HL7CMP of each of the components
.
. HL7STR    : 123456^SMITH^BRUCE^A^^^NZHIS
. HL7DLM    : ^
. HL7CMP[1] : 123456
. HL7CMP[2] : SMITH
. HL7CMP[3] : BRUCE
. HL7CMP[4] : A
. HL7CMP[5] :
. HL7CMP[6] :
. HL7CMP[7] : NZHIS
.
. Inputs    : HL7STR        - String to be Processed
.             HL7DLM        - Delimiter Character (Normally ^)(Optional)
. Output    : HL7CMP[1-20]  - HL7CMP of Component Strings
.
.----------------------------------------------------------------------
HL7BRK00  PACK      HL7DLM,HL7DLM,SP70
          MATCH     SP70,HL7DLM
          IF        @EQUAL
            MOVE      "^",HL7DLM
          ENDIF
.
          PACK      HL7CMP[1],SP70,SP70
          PACK      HL7CMP[2],SP70,SP70
          PACK      HL7CMP[3],SP70,SP70
          PACK      HL7CMP[4],SP70,SP70
          PACK      HL7CMP[5],SP70,SP70
          PACK      HL7CMP[6],SP70,SP70
          PACK      HL7CMP[7],SP70,SP70
          PACK      HL7CMP[8],SP70,SP70
          PACK      HL7CMP[9],SP70,SP70
          PACK      HL7CMP[10],SP70,SP70
          PACK      HL7CMP[11],SP70,SP70
          PACK      HL7CMP[12],SP70,SP70
          PACK      HL7CMP[13],SP70,SP70
          PACK      HL7CMP[14],SP70,SP70
          PACK      HL7CMP[15],SP70,SP70
          PACK      HL7CMP[16],SP70,SP70
          PACK      HL7CMP[17],SP70,SP70
          PACK      HL7CMP[18],SP70,SP70
          PACK      HL7CMP[19],SP70,SP70
          PACK      HL7CMP[20],SP70,SP70
          CLEAR     HL7CMP[1]
          CLEAR     HL7CMP[2]
          CLEAR     HL7CMP[3]
          CLEAR     HL7CMP[4]
          CLEAR     HL7CMP[5]
          CLEAR     HL7CMP[6]
          CLEAR     HL7CMP[7]
          CLEAR     HL7CMP[8]
          CLEAR     HL7CMP[9]
          CLEAR     HL7CMP[10]
          CLEAR     HL7CMP[11]
          CLEAR     HL7CMP[12]
          CLEAR     HL7CMP[13]
          CLEAR     HL7CMP[14]
          CLEAR     HL7CMP[15]
          CLEAR     HL7CMP[16]
          CLEAR     HL7CMP[17]
          CLEAR     HL7CMP[18]
          CLEAR     HL7CMP[19]
          CLEAR     HL7CMP[20]
.
          MOVE      ONE,F2
          PACK      BDIM127,HL7STR,SP70
.
HL7BRK10  SCAN      HL7DLM,BDIM127
          GOTO      HL7BRK90 IF NOT EQUAL
          MOVEFPTR  BDIM127,LENGTH
          SUB       ONE,LENGTH
          IF        LENGTH>0
            RESET     BDIM127
            SFORMAT   VAR,LENGTH
            MOVE      BDIM127,VAR
            MOVE      VAR,HL7CMP[F2]
            SFORMAT   VAR,127
            ADD       ONE,LENGTH
            RESET     BDIM127,LENGTH
            SUB       ONE,LENGTH
          ENDIF
          UNPACK    BDIM127,KEY1,BDIM127
          ADD       ONE,F2
          IF        F2<21
            GOTO      HL7BRK10
          ENDIF
.
HL7BRK90  STRIP     BDIM127
          MOVE      BDIM127,HL7CMP[F2]
.
          MOVE      ONE,F2
          WHILE     F2<21
            PACK      HL7CMP[F2],HL7CMP[F2],SP70,SP70
            ADD       ONE,F2
          DO
.
          RETURN
.*****************************************************************************
.*                           HTMLE000              Called by: Lots           *
.*        Check for special HTML characters, and if found, convert to        *
.*        the corresponding escape sequence:                                 *
.*        Field Separator         "<" becomes "&lt;"                         *
.*        Component separator     ">" becomes "&gt;"                         *
.* Requires: FIELDSTR - raw field string                                     *
.* Returns : FIELDSTR - field string with converted HTML special characters  *
.*                      and stripped of trailing blanks                      *
.*****************************************************************************
HTMLE000  STRIP     FIELDSTR                     * remove trailing spaces
          MOVELPTR  FIELDSTR,FORM3               * get string length
          COMPARE   ZERO,FORM3                   * string length zero ?
          GOTO      HTMLE999 IF EQUAL            * yes - ignore
.
          PACK      STRBUFFR,SP70,SP70           * clear the temporary string
          CLEAR     STRBUFFR
.
.         Loop through the field string checking each character to see
.         if it is a HL7 special character
.
HTMLE050  MOVE      FIELDSTR,ANS                 * load the next character
          MATCH     "<b>",FIELDSTR
          IF        @EQUAL
            APPEND    "<b>",STRBUFFR
            BUMP      FIELDSTR
            GOTO      HTMLE900 IF EOS              * eos - finished
            BUMP      FIELDSTR
            GOTO      HTMLE900 IF EOS              * eos - finished
            BUMP      FIELDSTR
            GOTO      HTMLE900 IF EOS              * eos - finished
            GOTO      HTMLE050
          ENDIF
.
          MATCH     "</b>",FIELDSTR
          IF        @EQUAL
            APPEND    "</b>",STRBUFFR
            BUMP      FIELDSTR
            GOTO      HTMLE900 IF EOS              * eos - finished
            BUMP      FIELDSTR
            GOTO      HTMLE900 IF EOS              * eos - finished
            BUMP      FIELDSTR
            GOTO      HTMLE900 IF EOS              * eos - finished
            BUMP      FIELDSTR
            GOTO      HTMLE900 IF EOS              * eos - finished
            GOTO      HTMLE050
          ENDIF
.
.         Check if character is a pipe
.
          MATCH     ">",ANS                     * is character ">" ?
          IF        @EQUAL
            APPEND    "&gt;",STRBUFFR          * yes - load "&gt;"
            GOTO      HTMLE100                   * get next character
          ENDIF
.
.         Check if character is a caret
.
          MATCH     "<",ANS                    * is character "<" ?
          IF        @EQUAL
            APPEND    "&lt;",STRBUFFR          * yes - load "&lt;"
            GOTO      HTMLE100                   * get next character
          ENDIF
.
.         Check if character is an ampersand
.
          MATCH     "&",ANS                 * is character "&" ?
          IF        @EQUAL
            APPEND    "&amp;",STRBUFFR          * yes - load "&amp;"
            GOTO      HTMLE100                   * get next character
          ENDIF
.
.         No HL7 special characters were found, so load this character
.         into the temporary string
.
          APPEND    ANS,STRBUFFR
.
HTMLE100  BUMP      FIELDSTR                     * position on next character
          GOTO      HTMLE900 IF EOS              * eos - finished
          GOTO      HTMLE050
.
HTMLE900  RESET     STRBUFFR
          MOVE      STRBUFFR,FIELDSTR
.
HTMLE999  RETURN
.
.------------------------------------------------------------
. Table Results last 10 results with numerics by UR Number
.------------------------------------------------------------
GRIDU000  IF        FHIRTYPE=0
            IF        NORECORD=0
              MOVE      "10",NORECORD
            ENDIF
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
          MOVE      ZERO,CHECKLST      * Check for Day Before on First Day of Year
          MOVE      SP70,AUDITKEY
.
          MATCH     "ally",RESLNKYR
          GOTO      GRIDU050 IF EQUAL
          CALL      IBACLOCK
          PACK      RESULTYR,CCC,CYY
          PACK      RESLNKFL,PRELEN,RESULTYR
          REP       " 0",RESLNKFL
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      RESLNKA1,RESLNKFL
          TRAPCLR   IO
          COMPARE   ONE,OVRCD
          GOTO      GRIDU990 IF EQUAL
          GOTO      GRIDU080 
.
GRIDU050  CALL      IBACLOCK
          PACK      RESLNKFL,PRELEN,CCC,CYY
          REP       " 0",RESLNKFL
.
          PACK      DIM4,CCC,CYY
          REP       " 0",DIM4
          MOVE      DIM4,FORM4
          READ      CONTROLF,HUNDRED9;*204,PTCNRSYR
.                                                   *Sorts latest to Earliest
          IF        SORTRSLT=0
            ADD       ONE,FORM4                     * sets first year
            PACK      RESLNKFL,PRELEN,FORM4         * sets table year
            MOVE      FORM4,FORM4A                 
            MOVE      PTCNRSYR,FORM4B               * sets final year
.                                                   * Sorts earliest to lastest
          ELSE
            MOVE      FORM4,FORM4B                  * Sets final year
            ADD       ONE,FORM4B
            MOVE      PTCNRSYR,FORM4
            PACK      RESLNKFL,PRELEN,FORM4         * sets table year
            MOVE      FORM4,FORM4A                  * sets first year
          ENDIF
.
GRIDU051  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      RESLNKA1,RESLNKFL
          TRAPCLR   IO
          COMPARE   ONE,OVRCD
          GOTO      GRIDU800 IF EQUAL
. 
GRIDU080  MATCH     RESULTYR,YEAROPEN
          CALL      RESOPN00 IF NOT EQUAL
.                                              * end I CAR 38290
GRIDU090  MOVE      "01",LINKTYPE
.                                     * sets table position based on sort 
          IF        SORTRSLT=0
            PACK      KEY29,LINKTYPE,URNUMBER,SP2,Z70
          ELSE
            PACK      KEY29,LINKTYPE,URNUMBER,SP70
          ENDIF
          CALL      RSRELN1
.                                   * moves through table based on sort
GRIDU100  IF        SORTRSLT=0
            CALL      RPRELN1
          ELSE
            CALL      RKRELN1
          ENDIF
          BRANCH    OVRCD,GRIDU700
          PACK      KEY12,RELNLTY,RELNLKY
          PACK      KEY29,LINKTYPE,URNUMBER,SP2,Z70
          MATCH     KEY12,KEY29
          GOTO      GRIDU700 IF NOT EQUAL
.                                          Date Range Filter
          MATCH     SP70,FRSTDATE
          IF        !@EQUAL
            MATCH     FRSTDATE,RELNRDT
            IF        @LESS
              GOTO      GRIDU100
            ENDIF     
          ENDIF
          MATCH     SP70,LASTDATE
          IF        !@EQUAL
            MATCH     RELNRDT,LASTDATE
            IF        @LESS
              GOTO      GRIDU100
            ENDIF     
          ENDIF
          MATCH     "ZZ",RELNRST
          GOTO      GRIDU100 IF EQUAL
.
          MATCH     "D ",RELNRST
          GOTO      GRIDU100 IF EQUAL
.
          MATCH     "F ",RELNRST
          GOTO      GRIDU110 IF EQUAL
.
          MATCH     "C ",RELNRST
          GOTO      GRIDU110 IF EQUAL
.                                             Filter to Mimic CLIWEB03 output
          MATCH     "ally",RESLNKYR           * Flags FHIR Resource Diagnostic
          IF        @EQUAL
            MATCH     "ZZ",RELNDSS
            GOTO      GRIDU100 IF EQUAL
          ELSE
            GOTO      GRIDU100 
          ENDIF
.
GRIDU110  CALL      GRIDLN00   * Output Table Row
          BRANCH    EXIT,GRIDU100  * Security too low or invalid header record
.
          IF        FHIRTYPE=1
            GOTO      GRIDU200
          ENDIF
.
          IF        LINECNT>0
            ADD       ONE,RECNUMB
            IF        STARTNUM>=RECNUMB
              GOTO      GRIDU100
            ENDIF
            ADD       ONE,DISNUMB
            MATCH     SP70,AUDITKEY
            IF        @EQUAL
              MOVE      ZERO,MARKFLAG
              CALL      WRTAUD00
            ENDIF
          ENDIF
.
.         Check if Info being sent by FHIR - NORECORD=0 means all records       
.
GRIDU200  IF        FHIRTYPE=1
            ADD       ONE,DISNUMB
            IF        NORECORD=0
              GOTO      GRIDU100 IF EQUAL
            ENDIF
          ENDIF
.
          COMPARE   NORECORD,DISNUMB
          GOTO      GRIDU990 IF EQUAL
          GOTO      GRIDU100
.
GRIDU700  MATCH     "ally",RESLNKYR
          GOTO      GRIDU800 IF EQUAL
          GOTO      GRIDU900         
.                                            Moves to next year table
GRIDU800  COMPARE   FORM4A,FORM4B
          GOTO      GRIDU999 IF EQUAL
.
          IF        SORTRSLT=0
            SUB       ONE,FORM4A
          ELSE
            ADD       ONE,FORM4A
          ENDIF
          MOVE      FORM4A,DIM4
          PACK      RESLNKFL,PRELEN,DIM4
          GOTO      GRIDU051
.
GRIDU900  BRANCH    CHECKLST,GRIDU990
          MOVE      ONE,CHECKLST
          CALL      IBACLOCK
          PACK      RESULTYR,CCC,CYY
          MOVE      RESULTYR,F4
          SUB       ONE,F4
          MOVE      F4,RESULTYR
          PACK      RESLNKFL,PRELEN,RESULTYR
          IF        FHIRTYPE=0
            WRITE     HTMLFILE;"//GRIDU-Opening Last Year:",RESLNKFL
          ENDIF
          MOVE      SP70,YEAROPEN
          REP       " 0",RESLNKFL
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      RESLNKA1,RESLNKFL
          TRAPCLR   IO
          COMPARE   ZERO,OVRCD
          GOTO      GRIDU080 IF EQUAL
.          
GRIDU990  BRANCH    FHIRTYPE,GRIDU995
          MATCH     SP70,AUDITKEY
          IF        !@EQUAL
            WRITE     HTMLFILE;"if (document.getElementById(#"auditkey#")!=null) {":
                               "document.getElementById(#"auditkey#").value=#"",AUDITKEY,"#";}"
          ENDIF
          GOTO      GRIDU999
.
GRIDU995  GOTO      GRIDU999
.GRIDU995  IF        DISNUMB=NORECORD
.            ASSIGN    STARTNUM+NORECORD,F6
.            MOVE      F6,KEY6
.            SQUEEZE   KEY6
.            WRITE     HTMLFILE;*+," "
.            WRITE     HTMLFILE;*+,"nextpage=&startnum=",KEY6
.          ENDIF
.          ASSIGN    STARTNUM-NORECORD,F6
.          IF        F6>=0
.            MOVE      F6,KEY6
.            SQUEEZE   KEY6
.            WRITE     HTMLFILE;*+," "
.            WRITE     HTMLFILE;*+,"prevpage=&startnum=",KEY6
.          ENDIF
.
GRIDU999  RETURN
.------------------------------------------------------------
. Table Results last 10 results with numerics by Admission
.------------------------------------------------------------
GRIDA000  MOVE      "10",NORECORD
          MOVE      ZERO,DISNUMB
          MOVE      ZERO,CHECKLST      * Check for Day Before on First Day of Year
          MOVE      SP70,AUDITKEY
.
          MOVE      ADMISSNO,KEY8
          CALL      RDVISA1            * Check for Visit No Passed in Message
          BRANCH    OVRCD,GRIDA999     * Not Valid
.
          UNPACK    PVIDATE,KEY4,CMON,CDAY
          MOVE      KEY4,F4A
          ADD       ONE,F4A
          MOVE      F4A,RESULTYR       * Visit Year Plus One to Cater for Cross Over Years
.
          CALL      IBACLOCK
          PACK      KEY4,CCC,CYY
          MOVE      KEY4,F4B
          IF        F4A>F4B
            MOVE      F4B,RESULTYR     * If Visit Date plus One > Current Use Year Current Year
          ENDIF
.
          PACK      RESLNKFL,PRELEN,RESULTYR
          REP       " 0",RESLNKFL
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      RESLNKA1,RESLNKFL
          TRAPCLR   IO
          COMPARE   ONE,OVRCD
          GOTO      GRIDA999 IF EQUAL
.
GRIDA090  MATCH     RESULTYR,YEAROPEN
          CALL      RESOPN00 IF NOT EQUAL
.                                              * end I CAR 38290
          MOVE      "02",LINKTYPE
          PACK      KEY29,LINKTYPE,ADMISSNO,SP2,Z70
          CALL      RSRELN1
GRIDA100  CALL      RPRELN1
          BRANCH    OVRCD,GRIDA900
          PACK      KEY12,RELNLTY,RELNLKY
          PACK      KEY29,LINKTYPE,ADMISSNO,SP2,Z70
          MATCH     KEY12,KEY29
          GOTO      GRIDA900 IF NOT EQUAL
          MATCH     "ZZ",RELNRST
          GOTO      GRIDA100 IF EQUAL
.
          MATCH     "F ",RELNRST
          GOTO      GRIDA110 IF EQUAL
          MATCH     "C ",RELNRST
          GOTO      GRIDA110 IF EQUAL
          GOTO      GRIDA100 
.
GRIDA110  CALL      GRIDLN00   * Output Table Row
          BRANCH    EXIT,GRIDA100  * Security too low or invalid header record
.
          IF        LINECNT>0
            ADD       ONE,DISNUMB
            MATCH     SP70,AUDITKEY
            IF        @EQUAL    
              MOVE      ZERO,MARKFLAG
              CALL      WRTAUD00
            ENDIF
          ENDIF
          COMPARE   NORECORD,DISNUMB
          GOTO      GRIDA999 IF EQUAL
          GOTO      GRIDA100
.
GRIDA900  BRANCH    CHECKLST,GRIDA999
          MOVE      ONE,CHECKLST
.
          CALL      IBACLOCK
          PACK      RESULTYR,CCC,CYY
          MOVE      RESULTYR,F4
          SUB       ONE,F4
          MOVE      F4,RESULTYR
          PACK      RESLNKFL,PRELEN,RESULTYR
          WRITE     HTMLFILE;"//GRIDA-Opening Last Year:",RESLNKFL
          MOVE      SP70,YEAROPEN
          REP       " 0",RESLNKFL
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      RESLNKA1,RESLNKFL
          TRAPCLR   IO
          COMPARE   ZERO,OVRCD
          GOTO      GRIDA090 IF EQUAL
.          
GRIDA999  MATCH     SP70,AUDITKEY
          IF        !@EQUAL
            WRITE     HTMLFILE;"if (document.getElementById(#"auditkey#")!=null) {":
                               "document.getElementById(#"auditkey#").value=#"",AUDITKEY,"#";}"
          ENDIF
 
          RETURN
.------------------------------------------------------------
. Cumulative Result Table Output
.------------------------------------------------------------
GRIDLN00  PACK      RESULTKY,RELNRDT,RELNRTM,RELNRUN,SP70
          MOVE      RESULTKY,RESULTYR
          MOVE      ZERO,LINECNT
          MOVE      ZERO,OVRCD
          MATCH     RESULTYR,YEAROPEN
          CALL      RESOPN00 IF NOT EQUAL
          BRANCH    OVRCD,GRIDLN90                                    *I-148026
          PACK      KEY17,RESULTKY,SP70
          CALL      RDREHA1
          BRANCH    OVRCD,GRIDLN95
          CALL      CHKSEC00
          BRANCH    EXIT,GRIDLN95
.
          PACK      KEY21,RESULTKY,SP70
          CALL      RSREDA1
GRIDLN20  MOVE      ZERO,HASOBS
          CALL      RKREDA1

          BRANCH    OVRCD,GRIDLN90
          PACK      KEY17,REDARDT,REDARTM,REDARUN
          MATCH     KEY17,RESULTKY
          GOTO      GRIDLN90 IF NOT EQUAL
. 
          MATCH     ANSD,REDASTA       * Dont display deleted records
          GOTO      GRIDLN20 IF EQUAL
.
          MATCH     "NM",REDAVTY       * Must be Numeric Result for Cumulative
          GOTO      GRIDLN25 IF EQUAL
          MATCH     "ST",REDAVTY       * Must be Numeric Result for Cumulative
          GOTO      GRIDLN25 IF EQUAL
          MATCH     "TX",REDAVTY       * Must be Numeric Result for Cumulative
          GOTO      GRIDLN25 IF EQUAL
          MATCH     "SN",REDAVTY       * Must be Numeric Result for Cumulative
          GOTO      GRIDLN25 IF EQUAL
          GOTO      GRIDLN20
.
GRIDLN25  BRANCH    FHIRTYPE,GRIDLN30
          CALL      CMLROW00
          IF        NULLFLAG=0
            ADD       ONE,LINECNT
          ENDIF
          GOTO      GRIDLN20
.
GRIDLN30  MOVE      ONE,HASOBS
          CALL      FHRTAB00
          MOVE      ZERO,EXIT
          MOVE      ONE,LINECNT
          GOTO      GRIDLN99
.
GRIDLN90  IF        DISPDIAG=1
            CALL      FHRTAB00
          ENDIF
          MOVE      ZERO,EXIT
          GOTO      GRIDLN99
.
GRIDLN95  MOVE      ONE,EXIT
.
GRIDLN99  RETURN
.------------------------------------------------------------
. Security
.------------------------------------------------------------
CHKSEC00  MOVE      ZERO,F2
          MOVE      "OBR04",KEY5
          PACK      KEY32,REHALAB,KEY5,REHAUCS,REHAUSC
          CALL      RDRECT1
          IF        OVRCD=0
            SQUEEZE   RECTSLV
            MOVE      RECTSLV,F2
          ENDIF
          PACK      KEY18,WBSEUID,PRGID
          CALL      RDWBST1
          IF        OVRCD=1
            MOVE      ZERO,WBSTLEV
          ENDIF
          IF        WBSTLEV<F2
            GOTO      CHKSEC80
          ENDIF
          MOVE      ZERO,EXIT
          GOTO      CHKSEC99
.
CHKSEC80  MOVE      ONE,EXIT
.
CHKSEC99  RETURN
.
.------------------------------------------------------------
. Clinical Documents Audit: Add, Update, View and Delete
. Input - OBAUTYPE (A,U,V,D)
.------------------------------------------------------------
OBSAUD00  MOVE      OBPCURNO,OBAUURNO
          MOVE      OBPCCVIS,OBAUVISN
          MOVE      OBPCCPID,OBAUCORR
          CALL      IBACLOCK
          PACK      OBAUDATE,CCC,CYY,CMM,CDD
          REP       " 0",OBAUDATE
          PACK      OBAUTIME,CTIMEIS
          MOVE      USERID,OBAUWEBU
          MOVE      SP70,OBAUREAS
          PACK      OBAUSPAR,SP70
.
          MATCH     ANSU,OBAUTYPE
          IF        @EQUAL
            MOVE      OBPCUDF1,OBAUREAS
          ENDIF
.
          PACK      KEY47,OBAUURNO,OBAUVISN,OBAUCORR,OBAUDATE:
                          OBAUTIME,OBAUWEBU,OBAUTYPE,SP70
          CALL      RAOBAUD1
          IF        OVRCD=1
            CALL      WROBAUD1
          ENDIF
.
OBSAUD99  RETURN
+
.------------------------------------------------------------
. FHIR Diagnostic Report Details
.------------------------------------------------------------
FHRF0000  BRANCH    FLDITMNO,FHRF0100
          GOTO      CMFL9999
.
FHRF0100  MOVE      ZERO,DISNUMB
          MOVE      ZERO,STARTNUM
          MOVE      ZERO,RECNUMB
          IF        PROCREQT=0
            CALL      FHRTAB00  * Bundle response to include observations
          ELSE
           
            IF        FHIRBUND=0 
              CALL      RESPRQ00  * Page response
            ELSE
.
              WRITE     HTMLFILE;*+," {":
                                    " #"fullUrl#": #"%BASEURL/ProcedureRequest/",FHRDIRID,"#",":
                                    " #"resource#": ";
              CALL      RESPRQ00         * Resource Proceedure
              WRITE     HTMLFILE;*+," }";
.
              IF        FHRSUBIN=1
.
                WRITE     HTMLFILE;*+,",{":
                                      " #"fullUrl#": #"%BASEURL/Patient/",FHRPATID,"#",":
                                      " #"resource#": ";
                CALL      RESPAT00
.
                WRITE     HTMLFILE;*+,"}"
              ENDIF
.
              IF        FHRLOCIN=1
.
                MATCH     SP70,REHALAB
                IF        !@EQUAL & !@EOS
.
                  PACK      KEY3,REHALAB,SP70
                  CALL      RDRELB1
                  IF        OVRCD=0
.
                    WRITE     HTMLFILE;*+,",{":
                                          " #"fullUrl#": #"%BASEURL/Location/Laboratory-",REHALAB,"#",":
                                            " #"resource#": ";
                    CALL      RESLOC00          *Resource Location
.
                    WRITE     HTMLFILE;*+,"}"
.
                  ENDIF
.
                ENDIF
.
              ENDIF
.
              IF        FHRPARIN=1
.
                MATCH     SP70,REAUUID
                IF        !@EQUAL & !@EOS
.
                  PACK      KEY10,REAUUID,SP70
                  CALL      RDWBSE1
                  IF        OVRCD=0
.
                    WRITE     HTMLFILE;*+,",{ #"fullUrl#" : #"%BASEURL/Practitioner/User-",REAUUID,"#", ":
                              " #"resource#" : ";
                    CALL     RESPRA00         * FHIR Resource Practitioner Recorder
                    WRITE    HTMLFILE;*+,"}"
.
                  ENDIF
.
                ENDIF
.
                MATCH     SP70,RESULTKY
                IF        !@EQUAL & !@EOS
.
                  PACK      KEY19,RESULTKY,SP70
                  CALL      RSREHC1
FHRF0500          CALL      RKREHC1
                  BRANCH    OVRCD,FHRF9999
.
                  PACK      KEY17,REHCRDT,REHCRTM,REHCRUN,SP70
.
                  MATCH     RESULTKY,KEY17
                  GOTO      FHRF9999 IF NOT EQUAL
.
                  MOVE      REHCCTO,KEY10
                  PACK      KEY10,KEY10,SP70
.
                  MATCH     SP70,KEY10
                  IF        !@EQUAL & !@EOS
.
                    PACK      KEY10,KEY10,SP70
                    CALL      RDPMHCP1
                    IF        OVRCD=0
.
                      WRITE     HTMLFILE;*+,",{ #"fullUrl#" : #"%BASEURL/Practitioner/HCP-",KEY10,"#", ":
                                " #"resource#" : ";
                      CALL     RESASS00         * FHIR Resource Practitioner
                      WRITE    HTMLFILE;*+,"}"
.
                    ENDIF
.
                  ENDIF
.
                  GOTO      FHRF0500
.
                ENDIF
.
              ENDIF
.
            ENDIF
.
          ENDIF
          GOTO      FHRF9999
.
FHRF9999  RETURN
+
.------------------------------------------------------------
. FHIR Diagnostic Report Details
.------------------------------------------------------------
FHRTAB00  IF        STARTNUM>RECNUMB
            GOTO      FHRTAB99
          ENDIF

          PACK      FHRDIRID,REHARDT,REHARTM,REHARUN,SP70
          REP       " -",FHRDIRID
          IF        DISNUMB>0
            WRITE     HTMLFILE;*+,",";
          ENDIF
          IF        FHROBSFL=0

            WRITE     HTMLFILE;*+," {":
                                  " #"fullUrl#": #"%BASEURL/DiagnosticReport/",FHRDIRID,"#",":
                                  " #"resource#": ";
          ENDIF
          CALL      RESDIR00
          IF        FHROBSFL=0
            WRITE     HTMLFILE;*+,"}";
            IF        HASOBS=1
              CALL      FHOBS000
            ENDIF
.
            IF        FHRSUBIN=1
.
              WRITE     HTMLFILE;*+,",{":
                                    " #"fullUrl#": #"%BASEURL/Patient/",FHRPATID,"#",":
                                      " #"resource#": ";
              CALL      RESPAT00           *Patient Resource
.
              WRITE     HTMLFILE;*+,"}"
            ENDIF
.
            IF        FHRLOCIN=1
.
              MATCH     SP70,REHALAB
              IF        !@EQUAL & !@EOS
.
                PACK      KEY3,REHALAB,SP70
                CALL      RDRELB1
                IF        OVRCD=0
.
                  WRITE     HTMLFILE;*+,",{":
                                        " #"fullUrl#": #"%BASEURL/Location/Laboratory-",RECTLAB,"#",":
                                          " #"resource#": ";
                  CALL      RESLOC00        *Location Resource
.
                  WRITE     HTMLFILE;*+,"}"
.  
                ENDIF
.
              ENDIF
.
            ENDIF
.
          ENDIF
.
FHRTAB99  RETURN
+
.------------------------------------------------------------
. FHIR Diagnostic Report
.------------------------------------------------------------
RESDIR00  PACK      FHRPATID,URNUMBER,SP70
          PACK      FHRDIRID,REHARDT,REHARTM,REHARUN,SP70
          PACK      FHROBSID,REHARDT,REHARTM,REHARUN,SP70
          SQUEEZE   FHRPATID
          REP       " -",FHRDIRID
          REP       " -",FHROBSID
.
          PACK      LABDESC,SP70
          PACK      KEY3,REHALAB,SP70
          CALL      RDRELB1
          IF        OVRCD<>1
            PACK      LABDESC,RELBDES,SP70
          ENDIF
.
          MOVE      "OBR04",KEY5
          MOVE      REHAUSC,RECTDES
          PACK      KEY32,REHALAB,KEY5,REHAUCS,REHAUSC
          CALL      RDRECT1
          MOVE      RECTDES,TESTLINE
          MOVE      KEY32,TESTCODE
.
          MOVE      "0074",KEY4
          PACK      KEY14,KEY4,REHADSS,SP70
          MOVE      KEY14,DIAGTCOD
          CALL      RDHLCO1
          IF        OVRCD<>1
            PACK      DIAGTDES,HLCODES,SP70
          ENDIF
.
          MOVE      "0123",KEY4 
          MOVE      REHARST,HLCODES
          PACK      KEY14,KEY4,REHARST,SP70
          CALL      RDHLCO1
.
          MOVE      "true",RESABN
          MATCH     "1",REHANOR
          IF        @EQUAL
            MOVE      "false",RESABN
          ENDIF
.
          MOVE      "01",LINKTYPE      * Mark Read on U/R
          PACK      LINKKEYF,REHAPID,SP70
          PACK      KEY29,LINKTYPE,LINKKEYF,RESULTKY
          CALL      RDRELN1
          MOVE      "false",RESACK
          MATCH     "1",RELNMSN
          IF        @EQUAL
            MOVE      "true",RESACK
          ENDIF
.
          MOVE      SP70,REHAVIS
          MOVE      SP70,REHACID
          MOVE      SP70,REHAFOR
          PACK      KEY17,REHARDT,REHARTM,REHARUN,SP70
          CALL      RDREHA1
.
          MOVE      ZERO,IMAGELNK
          MATCH     SP70,REHACID
          GOTO      RESDIR10 IF EQUAL
          MATCH     SP70,REHAFOR
          GOTO      RESDIR10 IF EQUAL
          MATCH     "RX",REHADSS
          GOTO      RESDIR10 IF NOT EQUAL
.
          MOVE      ONE,IMAGELNK
.
RESDIR10  STRIP     WBTPFIL  *  Template
          MOVE      TESTCODE,KEY32
          STRIP     KEY32
          REP       " -",KEY32
          REP       "\ ",TESTLINE
          WRITE     HTMLFILE;*+,"{ #"resourceType#": #"DiagnosticReport#",":
                    " #"id#" : #"",FHRDIRID,"#",":
                    " #"extension#": [":
                    " { #"url#":#"%BASEURL/extension/webPASService#"":
                        " ,#"valueString#":#"",PRGID,SP1,PRGNAM,SP1,VERSION,"#"},":
                    " { #"url#":#"%BASEURL/extension/webPASTemplate#"":
                        " ,#"valueString#":#"%TEMPLATEHOME/",WBTPFIL," %TEMPLATEVERSION#"},":
                    " {#"url#" : #"%BASEURL/extension/dxc-hc-auditkey#",":
                    "  #"valueString#" : #"",AUDITKEY,"#" },":
                    " {#"url#" : #"%BASEURL/extension/dxc-hc-abnormal#",":
                    "  #"valueBoolean#" : ",RESABN," },":
                    " {#"url#" : #"%BASEURL/extension/dxc-hc-acknowledged#",":
                    "  #"valueBoolean#" : ",RESACK," }, ";
          MATCH     "false",RESACK
          IF        @EQUAL
            WRITE     HTMLFILE;*+," ":
                    " {#"url#" : #"%BASEURL/extension/dxc-hc-workflowstatus#",":
                    "  #"valueCodeableConcept#" : { #"coding#" : [ { ":
                    "      #"code#" : #"pending-acknowledgement#", ":
                    "      #"display#" : #"Pending Acknowledgement#" } ] } }";
          ELSE
            CALL      ACKBY000
            WRITE     HTMLFILE;*+," ":
                    "{#"url#" : #"%BASEURL/extension/dxc-hc-workflowstatus#",":
                    "  #"valueCodeableConcept#" : { #"coding#" : [ { ":
                    "      #"code#" : #"acknowledged#", ":
                    "      #"display#" : #"Acknowledged#" } ] } },":
                    " {#"url#" : #"%BASEURL/extension/dxc-hc-acknowledgeddttm#",":
                    " #"valueDateTime#" : #"",ACKBYDTM,"#" },":
                    " {#"url#" : #"%BASEURL/extension/dxc-hc-acknowledgedby#",":
                    "  #"valueString#" : #"",ACKBYWHO,"#" }";
          ENDIF
          UNPACK    REHARDT,CCENT,CYEAR,CMON,CDAY
          WRITE     HTMLFILE;*+," ],":
                    "  #"subject#":  { #"reference#" : #"Patient/",FHRPATID,"#" }, ":
                    "  #"status#" : #"",HLCODES,"#",":
                    "  #"effectiveDateTime#" : #"",CCENT,CYEAR,DASH,CMON,DASH,CDAY:
                       "T",REHARTM,":00",TIMEZONE,"#",":
                    "  #"category#" : {":
                       "  #"coding#" : [":
                       "     { #"code#"      : #"",DIAGTCOD,"#",":
                       "       #"display#"   : #"",DIAGTDES,"#" } ":
                       "     ] },":
                    "  #"code#" : {":
                       "  #"coding#" : [":
                       "     { #"code#"      : #"",KEY32,"#",":
                       "       #"display#"   : #"",TESTLINE,"#" } ":
                       "     ] },":
                    "  #"performer#" : [ {":
                       "  #"actor#" : ":
                       "     { #"reference#" : #"Location/Laboratory-",REHALAB,"#",":
                       "       #"display#"   : #"",LABDESC,"#" } ":
                       "      } ]";
.
          IF        IMAGELNK=1
            STRIP     REHAFOR
            STRIP     REHACID
            STRIP     WBSEUID
            CLOCK     TIME,CTIMEIS
            WRITE     HTMLFILE;*+,",#"presentedForm#" : ":
                                  " { #"url#" : #"%BASEURL/php/PACSlink.php":
                                          "?username=",WBSEUID:
                                          "&patientId=",REHACID:
                                          "&accessionNumber=",REHAFOR:
                                          "&httprand=",CTIMEIS,"#" }";
          ENDIF
.
          PACK      FHRENCID,URNUMBER,REHAVIS,SP70
          REP       " -",FHRENCID
          MATCH     "       0",REHAVIS
          IF        !@EQUAL
            MATCH     SP70,REHAVIS 
            IF        !@EQUAL
               WRITE    HTMLFILE;*+,",  #"context#":  ":
                          "  { #"reference#" : #"Encounter/",FHRENCID,"#"} ";
            ENDIF
          ENDIF
          CALL     ADOBS000
          WRITE    HTMLFILE;*+,"}";
RESDIR99  RETURN
+
.----------------------------------------------------------------------
. Add Observations Result List
.----------------------------------------------------------------------
ADOBS000  PACK      KEY21,REHARDT,REHARTM,REHARUN,SP70   * Save Key
          PACK      KEY17,REHARDT,REHARTM,REHARUN,SP70   * Save Key
          MOVE      ONE,FIRSTOBS
          CALL      RSREDA1
ADOBS100  CALL      RKREDA1
          BRANCH    OVRCD,ADOBS999
          PACK      KEY21,REDARDT,REDARTM,REDARUN,REDASID,SP70   * Save Key
          MATCH     KEY17,KEY21
          GOTO      ADOBS999 IF NOT EQUAL
          PACK      FHROBSID,REDARDT,REDARTM,REDARUN,REDASID,SP70   * Save Key
          REP       " -",FHROBSID
          IF        FIRSTOBS=1
            MOVE      ZERO,FIRSTOBS
            WRITE     HTMLFILE;*+,", #"result#" : [ ";
          ELSE
            WRITE     HTMLFILE;*+,",";
          ENDIF
          WRITE     HTMLFILE;*+,"{ #"reference#" : #"Observation/",FHROBSID,"#" }";
          GOTO      ADOBS100
.
ADOBS999  IF        FIRSTOBS=0
            WRITE     HTMLFILE;*+,"]";
          ENDIF
          RETURN
+
.----------------------------------------------------------------------
. Add Observations to The Result
.----------------------------------------------------------------------
FHOBS000  PACK      KEY21,REHARDT,REHARTM,REHARUN,SP70   * Save Key
          PACK      KEY17,REHARDT,REHARTM,REHARUN,SP70   * Save Key
          MOVE      ZERO,SHOWMETA
          MOVE      ZERO,OBSCOUNT
          MOVE      ONE,CODECONT
FHOBS050  MATCH     SP70,CODEARRY[CODECONT]
          IF        !@EQUAL
            ADD       ONE,CODECONT
            GOTO      FHOBS050
          ENDIF
          CALL      RSREDA1
FHOBS100  CALL      RKREDA1
          BRANCH    OVRCD,FHOBS999
          PACK      KEY21,REDARDT,REDARTM,REDARUN,REDASID,SP70   * Save Key
          MATCH     KEY17,KEY21
          GOTO      FHOBS999 IF NOT EQUAL
          COMPARE   MAXOBS,ZERO
          IF        !@EQUAL
            COMPARE   OBSCOUNT,MAXOBS
            GOTO      FHOBS999 IF EQUAL
          ENDIF
.
.         Filters by code
.
          MOVE      ONE,CODECONT
          MATCH     SP70,CODEARRY[1]
          GOTO      FHOBS120 IF EQUAL
          MOVE      MAXARRAY,FORM3
FHOBS110  COMPARE   CODECONT,FORM3
          GOTO      FHOBS100 IF EQUAL
          MATCH     SP70,CODEARRY[CODECONT]
          GOTO      FHOBS100 IF EQUAL
          SQUEEZE   REDAOSC
          SQUEEZE   CODEARRY[CODECONT]
.
          MOVE      "OBX03",KEY5
          PACK      KEY32,REHALAB,KEY5,REDAOCS,REDAOTY,SP70
          STRIP     KEY32
          PACK      KEY50,KEY32,REDAOSD,SP70
          STRIP     KEY50
          REP       " -",KEY50
.
          PACK      CODEARRY[CODECONT],CODEARRY[CODECONT],SP70
          PACK      KEY50,KEY50,SP70
.
.          MATCH     CODEARRY[CODECONT],REDAOSC
.
          MATCH     CODEARRY[CODECONT],KEY50
          GOTO      FHOBS120 IF EQUAL
          ADD       ONE,CODECONT
          GOTO      FHOBS110
.
FHOBS120  PACK      FHROBSID,REDARDT,REDARTM,REDARUN,REDASID,SP70   * Save Key
          REP       " -",FHROBSID
          WRITE     HTMLFILE;*+," ,{":
                                " #"fullUrl#": #"%BASEURL/Observation/",FHROBSID,"#",":
                                " #"resource#": ";
          CALL      RESOBS00
          WRITE     HTMLFILE;*+,"}";
          ADD       ONE,OBSCOUNT
          GOTO      FHOBS100
.
FHOBS999  RETURN
+
.------------------------------------------------------------
. FHIR Observations
.------------------------------------------------------------
RESOBS00  MOVE      "OBX03",KEY5
          PACK      KEY32,REHALAB,KEY5,REDAOCS,REDAOTY,SP70
          CALL      RDRECT1
          IF        OVRCD=1
            PACK      RECTDES,REDAOTY,SP70
          ENDIF
          MATCH     SP70,RECTDES
          IF        @EQUAL
            PACK      RECTDES,REDAOTY,SP70
          ENDIF
.
          CALL     FHRSTA00
          STRIP    KEY32
          PACK     KEY50,KEY32,REDAOSD,SP70
          STRIP    KEY50
          REP      " -",KEY50
.
          PACK     FHRPATID,REDAPID,SP70
          SQUEEZE  FHRPATID
          PACK     FHROBSID,REDARDT,REDARTM,REDARUN,REDASID,SP70   * Save Key
          REP      " -",FHROBSID
          STRIP    RECTDES
          STRIP    REDAOSD
          STRIP    WBTPFIL  *  Template
          WRITE    HTMLFILE;*+,"{ #"resourceType#": #"Observation#",":
                    " #"id#" : #"",FHROBSID,"#",";
          IF       SHOWMETA=1
            WRITE    HTMLFILE;*+," #"extension#": [":
                    " { #"url#":#"%BASEURL/extension/webPASService#"":
                        " ,#"valueString#":#"",PRGID,SP1,PRGNAM,SP1,VERSION,"#"},":
                    " { #"url#":#"%BASEURL/extension/webPASTemplate#"":
                        " ,#"valueString#":#"%TEMPLATEHOME/",WBTPFIL," %TEMPLATEVERSION#"}":
                    "  ],";
          ENDIF
          REP      "\ ",RECTDES
          WRITE    HTMLFILE;*+,"  #"status#" : #"",FHRSTAT,"#",":
                      "  #"subject#":  { #"reference#" : #"Patient/",FHRPATID,"#" }, ":
                      "  #"code#" : {":
                         "  #"coding#" : [":
                         "     { #"code#"      : #"",KEY50,"#",":
                         "       #"display#"   : #"",RECTDES,SP1,REDAOSD,"#" } ":
                         "     ] },";
          MATCH    SP70,REDAABF
          IF       !@EQUAL
            MOVE     SP70,HLCOBGC
            MOVE     "0078",KEY4
            PACK     KEY14,KEY4,REDAABF,SP70
            CALL     RDHLCO1
            WRITE    HTMLFILE;*+,"  #"interpretation#" : {":
                           "  #"coding#" : [":
                           "     { #"code#"      : #"",REDAABF,"#",":
                           "       #"display#"   : #"",HLCODES,"#",  ":
                           "       #"extension#" : [ { ":
                           "         #"url#" : #"%BASEURL/extension/highlightColor#",":
                           "         #"valueString#" : #"",HLCOBGC,"#" } ] }":
                           "     ] },";
          ENDIF
.
          MATCH    SP70,REDAPER
          IF       !@EQUAL
            WRITE    HTMLFILE;*+," #"performer#" : [ { #"display#" : #"",REDAPER,"#" }";
            MATCH    SP70,REDAROB
            IF       !@EQUAL
              WRITE    HTMLFILE;*+," ,{ #"display#" : #"",REDAROB,"#" }";
            ENDIF
            WRITE    HTMLFILE;*+,"],";
          ELSE
            MATCH    SP70,REDAROB
            IF       !@EQUAL
              WRITE    HTMLFILE;*+," #"performer#" : [ { #"display#" : #"",REDAROB,"#" }],";
            ENDIF
          ENDIF
.
          MATCH    SP70,REDARFR
          CALL     FHRRRN00 IF NOT EQUAL
.
          MATCH    "NM",REDAVTY     * Numeric
          IF       @EQUAL
            CALL     FHRVQT00
            GOTO     RESOBS90
          ENDIF
          MATCH    "SN",REDAVTY     * Structured Numeric
          IF       @EQUAL
            CALL     FHRVQT00
            GOTO     RESOBS90
          ENDIF
          MATCH    "ED",REDAVTY     * Embedded Document
          IF       @EQUAL
            CALL     FHREMB00
            GOTO     RESOBS90
          ENDIF
          CALL     FHRVST00
.
RESOBS90  CALL     FHRCOM00         * Add Comments (NTE)
          WRITE    HTMLFILE;*+,"}";
.
RESOBS99  RETURN
+
.
. Output FHIR ValueAttachment
.
FHREMB00  MOVE      REDAVST,KEY127
.
.  Get Correspondance Id from Filename Stored in REDAVST
.
FHREMB10  SCAN      DASH,KEY127
          GOTO      FHREMB20 IF NOT EQUAL
          BUMP      KEY127
          GOTO      FHREMB10
FHREMB20  MOVE      KEY127,CORRID    * Correspondance ID
          MOVE      CORRID,F4        * Make numeric
          MOVE      F4,CORRID
.         
          PACK      RESULTKY,REDARDT,REDARTM,REDARUN,SP70
          PACK      KEY20,RESULTKY,SP70
          CALL      RSRECL1
FHREMB30  CALL      RKRECL1
          BRANCH    OVRCD,FHREMB99
          PACK      KEY17,RECLRDT,RECLRTM,RECLRUN,SP70
          MATCH     KEY17,RESULTKY            * Same Result?
          GOTO      FHREMB99 IF NOT EQUAL
          MATCH     CORRID,RECLCID            * Matching Reference ?
          GOTO      FHREMB30 IF NOT EQUAL
          MATCH     "1",RECLDEL               * Deleted?
          GOTO      FHREMB30 IF EQUAL
.
          PACK      KEY20,RECLURN,RECLVIS,RECLCID,SP70
          CALL      RDOBPC1
          BRANCH    OVRCD,FHREMB30
          MATCH     "1",OBPCMDEL              * Deleted?
          GOTO      FHREMB30 IF EQUAL
.
          REP       " -",KEY20
          WRITE     HTMLFILE;*+,"  #"valueAttachment#" : {":
                           " #"url#" : #"Binary/",KEY20,"#"":
                           "}";
FHREMB99  RETURN
+ 
.
. Output FHIR ValueString
.
FHRVST00  MATCH    "1",REDATFL
          GOTO     FHRVST10 IF EQUAL
.
          MATCH    SP70,REDAUNI
          IF       @EQUAL
            REP       "#"'",REDAVST
            WRITE    HTMLFILE;*+,"  #"valueString#" : #"",REDAVST,"#" ";
          ELSE
            CALL     FHRVQT00
          ENDIF
          GOTO      FHRVST99
.
FHRVST10  MOVE      ZERO,FIRSTVST
          PACK      KEY21,REDARDT,REDARTM,REDARUN,REDASID,SP70
          PACK      KEY24,REDARDT,REDARTM,REDARUN,REDASID,SP70
          CALL      RSREDB1
FHRVST20  CALL      RKREDB1
          BRANCH    OVRCD,FHRVST90    * Note does not exist
          PACK      KEY21,REDBRDT,REDBRTM,REDBRUN,REDBSID,SP70
          MATCH     KEY21,KEY24
          GOTO      FHRVST90 IF NOT EQUAL
          REP       "#"'",REDBTXT
          IF        FIRSTVST=0
            MOVE      ONE,FIRSTVST
            WRITE     HTMLFILE;*+,"  #"valueString#" : #"";
          ENDIF
          STRIP     REDBTXT
          MOVELPTR  REDBTXT,LENGTH
          COMPARE   ZERO,LENGTH
          IF        @EQUAL
            WRITE     HTMLFILE;*+,"\n";
            GOTO      FHRVST20
          ENDIF
.
          STRIP     REDBTXT
FHRVST30  MATCH     SP1,REDBTXT
          IF        @EQUAL
            BUMP      REDBTXT
            GOTO      FHRVST30
          ENDIF
.
          MOVELPTR  REDBTXT,LENGTH
          COMPARE   ZERO,LENGTH
          IF        @EQUAL
            WRITE     HTMLFILE;*+,"\n";
            GOTO      FHRVST20
          ENDIF
.
          SFORMAT   VAR,LENGTH
          MOVE      REDBTXT,VAR
          WRITE     HTMLFILE;*+,VAR,"\n";
          GOTO      FHRVST20
.
FHRVST90  IF        FIRSTVST=1
            WRITE     HTMLFILE;*+,"#" ";
          ENDIF
.
FHRVST99  RETURN
.
+
.
. Output FHIR ValueQuantity
.
FHRVQT00  STRIP    REDAVST
          WRITE    HTMLFILE;*+,"  #"valueQuantity#" : {":
                           " #"value#" : #"",REDAVST,"#"";
          MATCH    SP70,REDAUNI
          IF       !@EQUAL
            WRITE    HTMLFILE;*+,", #"unit#"  : #"",REDAUNI,"#" ";
          ENDIF
          WRITE    HTMLFILE;*+,"}";
          RETURN
+
.
. Output FHIR Reference Range
.
FHRRRN00  WRITE    HTMLFILE;*+,"  #"referenceRange#" : [ {":
                                 "#"text#" : #"",REDARFR,"#" ";
.
..          CALL     FHRRFR00
..          MATCH    SP70,FHRLOW
..          IF       !@EQUAL
..            WRITE    HTMLFILE;*+,",#"low#" : {  #"value#" : ",FHRLOW," } ";
..          ENDIF
..          MATCH    SP70,FHRHIGH
..          IF       !@EQUAL
..            WRITE    HTMLFILE;*+,",#"high#" : {  #"value#" : ",FHRHIGH," } ";
..          ENDIF
          WRITE    HTMLFILE;*+,"} ],";
.
FHRRRN99  RETURN
+
.
. FHIR Observation Comment from resdecaf (redcYYYY)
.
FHRCOM00  MOVE      ZERO,FIRSTCOM
          PACK      KEY21,REDARDT,REDARTM,REDARUN,REDASID,SP70
          PACK      KEY24,REDARDT,REDARTM,REDARUN,REDASID,SP70
          CALL      RSREDC1
FHRCOM10  CALL      RKREDC1
          BRANCH    OVRCD,FHRCOM90    * Note does not exist
          PACK      KEY21,REDCRDT,REDCRTM,REDCRUN,REDCSID,SP70
          MATCH     KEY21,KEY24
          GOTO      FHRCOM90 IF NOT EQUAL
          IF        FIRSTCOM=0
            MOVE      ONE,FIRSTCOM
            WRITE     HTMLFILE;*+,",#"comment#" : #"";
          ENDIF
          STRIP     REDCTXT
.
          WRITE     HTMLFILE;*+,REDCTXT,"\n";
          GOTO      FHRCOM10
FHRCOM90  IF        FIRSTCOM=1
            WRITE     HTMLFILE;*+,"#"";
          ENDIF
          RETURN
+
.
. Get FHIR Reference Range
.
FHRRFR00  MOVE     SP70,FHRLOW
          MOVE     SP70,FHRHIGH
          SCAN     "-",REDARFR
          GOTO     FHRRFR10 IF NOT EQUAL
          BUMP     REDARFR
          SCAN     "-",REDARFR
          IF       @EQUAL
            BUMP     REDARFR
            MOVE     REDARFR,FHRHIGH
          ELSE
            MOVE     REDARFR,FHRHIGH
          ENDIF
.
          RESET    REDARFR
          SCAN     "-",REDARFR
          BUMP     REDARFR
          SCAN     "-",REDARFR
          IF       @EQUAL
            BUMP     REDARFR,-1
          ELSE
            RESET    REDARFR
            SCAN     "-",REDARFR
            BUMP     REDARFR,-1
          ENDIF
          MOVEFPTR REDARFR,LENGTH
          RESET    REDARFR
          SFORMAT  VAR,LENGTH
          MOVE     REDARFR,VAR
          MOVE     VAR,FHRLOW
.         
          REP      "+ ",FHRLOW
          REP      "+ ",FHRHIGH
          GOTO     FHRRFR99
.
FHRRFR10  RESET    REDARFR
          SCAN     ">",REDARFR
          GOTO     FHRRFR20 IF NOT EQUAL
          BUMP     REDARFR
          MOVE     REDARFR,FHRLOW
          GOTO     FHRRFR99
.
FHRRFR20  RESET    REDARFR
          SCAN     "<",REDARFR
          GOTO     FHRRFR99 IF NOT EQUAL
          BUMP     REDARFR
          MOVE     REDARFR,FHRHIGH
          GOTO     FHRRFR99
.
FHRRFR99  RESET    REDARFR
          TYPE     FHRLOW
          IF       !@EQUAL
            MOVE     SP70,FHRLOW
            MOVE     SP70,FHRHIGH
          ENDIF
          TYPE     FHRHIGH
          IF       !@EQUAL
            MOVE     SP70,FHRLOW
            MOVE     SP70,FHRHIGH
          ENDIF
          RETURN
.
. Get FHIR Status
.
FHRSTA00  MOVE     REDASTA,FHRSTAT
          MATCH    "F",REDASTA
          IF       @EQUAL
            MOVE     "final",FHRSTAT
            GOTO     FHRSTA99
          ENDIF
.
          MATCH    "D",REDASTA
          IF       @EQUAL
            MOVE     "cancelled",FHRSTAT
            GOTO     FHRSTA99
          ENDIF
.
          MATCH    "C",REDASTA
          IF       @EQUAL
            MOVE     "correction",FHRSTAT
            GOTO     FHRSTA99
          ENDIF
.
          MATCH    "I",REDASTA
          IF       @EQUAL
            MOVE     "registered",FHRSTAT
            GOTO     FHRSTA99
          ENDIF
.
          MATCH    "P",REDASTA
          IF       @EQUAL
            MOVE     "preliminary",FHRSTAT
            GOTO     FHRSTA99
          ENDIF
.
          MATCH    "R",REDASTA
          IF       @EQUAL
            MOVE     "preliminary",FHRSTAT
            GOTO     FHRSTA99
          ENDIF
.
          MATCH    "S",REDASTA
          IF       @EQUAL
            MOVE     "preliminary",FHRSTAT
            GOTO     FHRSTA99
          ENDIF
FHRSTA99  RETURN
.-----------------------------------------------------------
. Get Last Acknowledge By from Audit
.-----------------------------------------------------------
ACKBY000  MOVE      "N/A",ACKBYWHO
          MOVE      "1900-01-01T12:00:00Z",ACKBYDTM
.
          PACK      RESLNKFL,PREAEA,RELNRDT
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      RESAUDA1,RESLNKFL
          TRAPCLR   IO
          COMPARE   ONE,OVRCD
          GOTO      ACKBY999 IF EQUAL
.
          MOVE      WBSEUID,SAVEWUID
          PACK      RESULTKY,RELNRDT,RELNRTM,RELNRUN,SP70
          PACK      KEY40,RESULTKY,Z70
          CALL      RSREAU1
ACKBY100  CALL      RPREAU1
          BRANCH    OVRCD,ACKBY999
          PACK      KEY17,REAURDT,REAURTM,REAURUN,SP70
          MATCH     RESULTKY,KEY17
          GOTO      ACKBY999 IF NOT EQUAL
          MATCH     "1",REAUMRK
          GOTO      ACKBY100 IF NOT EQUAL

          UNPACK    REAUSDT,CCENT,CYEAR,CMON,CDAY
          MOVE      ":00",KEY3
          PACK      ACKBYDTM,CCENT,CYEAR,DASH,CMON,DASH,CDAY,ANST,REAUSTM,KEY3,TIMEZONE
.
          MOVE      REAUUID,KEY10
          CALL      RDWBSE1
          IF        OVRCD=1
            MOVE      REAUUID,WBSENAM
          ENDIF
          MOVE      WBSENAM,ACKBYWHO
.
          MOVE      SAVEWUID,KEY10
          CALL      RDWBSE1
.
ACKBY999  RETURN
+
.------------------------------------------------------------
. FHIR Procedure Request
.------------------------------------------------------------
RESPRQ00  PACK      FHRPATID,URNUMBER,SP70
          PACK      FHRDIRID,REHARDT,REHARTM,REHARUN,SP70
          PACK      RESULTKY,REHARDT,REHARTM,REHARUN,SP70
          PACK      FHROBSID,REHARDT,REHARTM,REHARUN,SP70
          SQUEEZE   FHRPATID
          REP       " -",FHRDIRID
          REP       " -",FHROBSID
.
          PACK      LABDESC,SP70
          PACK      KEY3,REHALAB,SP70
          CALL      RDRELB1
          IF        OVRCD<>1
            PACK      LABDESC,RELBDES,SP70
          ENDIF
.
          MOVE      "0123",KEY4 
          MOVE      REHARST,HLCODES
          PACK      KEY14,KEY4,REHARST,SP70
          CALL      RDHLCO1
          UNPACK    REHARDT,CCENT,CYEAR,CMON,CDAY
.
          MATCH     "S ",REHARST
          IF        @EQUAL
            MOVE      "active",HLCODES
          ENDIF
.
          MATCH     "ZZ",REHARST
          IF        @EQUAL
            MOVE      "completed",HLCODES
          ENDIF
.
          MATCH     "X ",REHARST
          IF        @EQUAL
            MOVE      "cancelled",HLCODES
          ENDIF
.
          MOVE      SP70,REHAVIS
          MOVE      SP70,REHACID
          MOVE      SP70,REHAFOR
          MOVE      REHARDT,RESULTYR
          MOVE      ZERO,OVRCD
          MATCH     RESULTYR,YEAROPEN
          IF        !@EQUAL
            MOVE      RESULTYR,YEAROPEN
            CLOSE     RESHEAA1
            CLOSE     RESHEBA1
            CLOSE     RESHECA1
            CLOSE     RESHEDA1
            CLOSE     RESAUDA1
            CLOSE     RESLNKA1
            PACK      RESFNAME,PREHEA,RESULTYR
            OPEN      RESHEAA1,RESFNAME
            PACK      RESFNAME,PREHEB,RESULTYR
            OPEN      RESHEBA1,RESFNAME
            PACK      RESFNAME,PREHEC,RESULTYR
            OPEN      RESHECA1,RESFNAME
            PACK      RESFNAME,PREHED,RESULTYR
            OPEN      RESHEDA1,RESFNAME
            PACK      RESFNAME,PREAEA,RESULTYR
            OPEN      RESAUDA1,RESFNAME
            PACK      RESFNAME,PRELEN,RESULTYR     
            OPEN      RESLNKA1,RESFNAME
          ENDIF
.
          PACK      KEY17,RESULTKY,SP70
          CALL      RDREHA1
.
          PACK      KEY40,RESULTKY,SP70
          CALL      RSREAU1
          CALL      RKREAU1
.
          MOVE      "OBR04",KEY5
          MOVE      REHAUSC,RECTDES
          PACK      KEY32,REHALAB,KEY5,REHAUCS,REHAUSC
          CALL      RDRECT1
          STRIP     WBTPFIL  *  Template
.         MOVE      TESTCODE,KEY32
          MOVE      RECTDES,TESTLINE
          STRIP     KEY32
          REP       " -",KEY32
          REP       "\ ",TESTLINE
          WRITE     HTMLFILE;*+,"{ #"resourceType#": #"ProcedureRequest#",":
                    " #"id#" : #"",FHRDIRID,"#",":
                    " #"extension#": [":
                    " { #"url#":#"%BASEURL/extension/webPASService#"":
                        " ,#"valueString#":#"",PRGID,SP1,PRGNAM,SP1,VERSION,"#"},":
                    " { #"url#":#"%BASEURL/extension/webPASTemplate#"":
                        " ,#"valueString#":#"%TEMPLATEHOME/",WBTPFIL," %TEMPLATEVERSION#"}";
.
          MOVE      REAUUID,KEY10
          CALL      RDWBSE1
          IF        OVRCD=0
            WRITE     HTMLFILE;*+," ,{#"url#" : #"%BASEURL/extension/dxc-hc-createdby#",":
                      "  #"valueReference#" : { ":
                      "  #"reference#": #"Practitioner/User-",REAUUID,"#",":
                      "  #"display#": #"",WBSENAM,"#" }}";
          ENDIF
.
          PACK      KEY19,RESULTKY,SP70
          CALL      RSREHC1
RESPRQ10  CALL      RKREHC1
          BRANCH    OVRCD,RESPRQ20
          PACK      KEY17,REHCRDT,REHCRTM,REHCRUN,SP70
          MATCH     RESULTKY,KEY17
          GOTO      RESPRQ20 IF NOT EQUAL
.
          MOVE      SP70,KEY6
          MOVE      REHCCTO,KEY10         * reference
          SCAN      "^",KEY10
          IF        @EQUAL
            RESET     KEY10
            MOVE      KEY10,KEY6
          ELSE
            RESET     KEY10
          ENDIF
.
          SCAN      "^",REHCCTO
          MOVEFPTR  REHCCTO,LENGTH
          ADD       ONE,LENGTH
          RESET     REHCCTO,LENGTH
          PACK      KEY50,REHCCTO,SP70    * display
.
          WRITE     HTMLFILE;*+," ,{#"url#" : #"%BASEURL/extension/dxc-hc-copyto#",":
                    "  #"valueReference#" : { ";
.
          MATCH     SP70,KEY6
          IF        @EQUAL
            WRITE     HTMLFILE;*+,"  #"reference#": #"Practitioner/HCP-",KEY10,"#",";
          ELSE
            WRITE     HTMLFILE;*+,"  #"reference#": #"Practitioner/HCP-",KEY6,"#",";
          ENDIF
.
          WRITE    HTMLFILE;*+,"  #"display#": #"",KEY50,"#" }}";
.
          GOTO      RESPRQ10
.
RESPRQ20  MOVE      REHAPOR,KEY7
          WRITE     HTMLFILE;*+," ],":
                    " #"requisition#":  { #"value#": #"",KEY7,"#" }, ":
                    " #"identifier#": [  ":
                    "  { #"type#": { ":
                    "     #"coding#": [ { ":
                    "      #"system#": #"http://hl7.org/fhir/identifier-type#", ":
                    "      #"code#": #"PLAC#" } ], ":
                    "    #"text#": #"Placer#" }, ":
                    "    #"value#": #"",REHAPOR,"#"":
                    "  }";
          MATCH     REHAPOR,REHAFOR  * Populated with same value until a response from the Lab
          IF        !@EQUAL
            WRITE     HTMLFILE;*+," ,{ #"type#": { ":
                      "     #"coding#": [ { ":
                      "      #"system#": #"http://hl7.org/fhir/identifier-type#", ":
                      "      #"code#": #"FILL#" } ], ":
                      "    #"text#": #"Filler#" }, ":
                      "    #"value#": #"",REHAFOR,"#"":
                      "  }";
          ENDIF
          MOVE      REHAPID,KEY8
          PACK      FHRENCID,KEY8,REHAVIS,SP70
          REP       " -",KEY8
          WRITE     HTMLFILE;*+," ],":
                    " #"context#":  { #"reference#" : #"Encounter/",FHRENCID,"#"}, ":
                    " #"subject#":  { #"reference#" : #"Patient/",FHRPATID,"#"}, ":
                    " #"intent#" : #"order#",":
                    " #"status#" : #"",HLCODES,"#",":
                    " #"occurrenceDateTime#" : #"",CCENT,CYEAR,DASH,CMON,DASH,CDAY:
                        "T",REHARTM,":00",TIMEZONE,"#",":
                    " #"category#" : {":
                    "  #"coding#" : [":
                    "   { #"system#"  : #"http://snomed.info/sct#", ":
                    "     #"code#"    : #"103693007#",":
                    "     #"display#" : #"Diagnostic Procedure#" } ":
                    "  ] },":
                    " #"code#" : {":
                    "  #"coding#" : [":
                    "   { #"code#"      : #"",KEY32,"#",":
                    "     #"display#"   : #"",TESTLINE,"#" } ":
                    "  ] },":
                    " #"performer#" : [ {":
                    "  #"actor#" : [":
                    "   { #"reference#" : #"Location/Laboratory-",REHALAB,"#",":
                    "     #"display#"   : #"",LABDESC,"#" } ":
                    "   ] } ],";
          STRIP     REHAOSN
          STRIP     REHAOGN
          UNPACK    REAUSDT,CCENT,CYEAR,CMON,CDAY
.
          WRITE     HTMLFILE;*+," #"authoredOn#" : #"",CCENT,CYEAR,DASH,CMON,DASH,CDAY:
                    "T",REAUSTM,":00",TIMEZONE,"#",":
                    " #"requester#": { ":
                    "  #"agent#": { ":
                    "   #"reference#": #"Practitioner/HCP-",REHAOID,"#",":
                    "   #"display#": #"",REHAOSN,", ",REHAOGN,"#"":
                    "  } },":
                    " #"note#": [ { ":
                    "    #"text#": #"";
.
          MOVE      RESULTKY,KEY17
          CALL      RDREHD1    * Check Result on File
          IF        OVRCD=0
            WRITE     HTMLFILE;*+,REHDRCI,"\n";
          ENDIF
.
          MOVE      "routine",KEY15
          PACK      KEY20,RESULTKY,SP70
          CALL      RSREHB1
RESPRQ50  CALL      RKREHB1
          BRANCH    OVRCD,RESPRQ60
          PACK      KEY17,REHBRDT,REHBRTM,REHBRUN,SP70
          MATCH     RESULTKY,KEY17
          GOTO      RESPRQ60 IF NOT EQUAL
          MATCH     "Priority: ",REHBTXT
          IF        @EQUAL
            UNPACK    REHBTXT,KEY10,KEY15
            GOTO      RESPRQ50
          ENDIF
          WRITE     HTMLFILE;*+,REHBTXT,"\n";
          GOTO      RESPRQ50
.
RESPRQ60  WRITE    HTMLFILE;*+,"#" }  ], ":
                    " #"priority#": #"",KEY15,"#" }";
          RETURN
+
.--------------------------------------------
. Add Contacts to FHIR Patient Resource
.--------------------------------------------
FHRCON00  MOVE      ZERO,F3
          PACK      KEY14,URNUMBER,SP20
          CALL      RSPMCEX1
FHRCON10  CALL      RKPMCEX1
          BRANCH    OVRCD,FHRCON99
          MATCH     URNUMBER,PMCEURNO
          GOTO      FHRCON99 IF NOT EQUAL
          MATCH     "0",PMCEACTV
          GOTO      FHRCON10 IF NOT EQUAL
.
          PACK      KEY5,CATTC,PMCETYPE
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE      PMCETYPE,TDESC
          ENDIF
.
          PACK      KEY4,PMCETITL
          CALL      RDPMTLE1
          IF        OVRCD=1
            MOVE      PMCETITL,PMTLDESC
          ENDIF
.
          PACK      KEY10,PMCERELP,SP70
          CALL      RDPMREL1
          IF        OVRCD=1
            MOVE      PMCERELP,PMRLDESC
          ENDIF
.
          IF        F3>0
            WRITE     HTMLFILE;*+,",";
          ELSE
            ADD       ONE,F3
            WRITE     HTMLFILE;*+,",#"contact#" : [ ";
          ENDIF
          WRITE     HTMLFILE;*+,"{#"relationship#": [ {":
                                "  #"coding#":  [ {":
                                "   #"code#": #"",PMCERELP,"#",":
                                "   #"display#": #"",PMRLDESC,"#" } ] } ],":
                                " #"name#" : {":
                                "  #"use#" : #"usual#",":
                                "  #"family#": #"",PMCESNAM,"#",":
                                "  #"given#" : [#"",PMCEGNAM,"#"";
          MATCH     SP70,PMCEGNM2
          IF        !@EQUAL
            WRITE     HTMLFILE;*+,",#"",PMCEGNM2,"#"";
          ENDIF
          WRITE     HTMLFILE;*+,"],":
                      "      #"prefix#" : [#"",PMTLDESC,"#"]":
                      "     }, ";
.
          WRITE     HTMLFILE;*+," #"address#": [ {":
                                " #"use#": #"home#",":
                                " #"line#": [#"",PMCEADD1,"#"";
          MATCH     SP70,PMCEADD2
          IF        !@EQUAL
            STRIP     PMCEADD2
            WRITE     HTMLFILE;*+,",#"",PMCEADD2,"#"";
          ENDIF
          WRITE     HTMLFILE;*+,"],":
                                " #"city#": #"",PMCEADD3,"#",":
                                " #"state#": #"",PMCEADD4,"#",":
                                " #"postalCode#": #"",PMCEPOST,"#"":
                                " } ],":
                                "#"telecom#": [ ":
                                "{#"system#": #"email#",":
                                " #"value#": #"",PMCEEMAI,"#"},":
                                "{#"system#": #"phone#",":
                                " #"use#": #"home#",":
                                " #"value#": #"",PMCETELP,"#"},":
                                "{#"system#": #"phone#",":
                                " #"use#": #"work#",":
                                " #"value#": #"",PMCETELB,"#"},":
                                "{#"system#": #"phone#",":
                                " #"use#": #"mobile#",":
                                " #"value#": #"",PMCETELM,"#"} ] }";
          GOTO      FHRCON10

FHRCON99  IF        F3>0
            WRITE     HTMLFILE;*+,"  ]";
          ENDIF
          MATCH     PMPXRHC1,SP70
          IF        !@EQUAL
            WRITE     HTMLFILE;*+,",#"generalPractitioner#": [ { #"reference#": #"Practitioner/HCP-",PMPXRHC1,"#" }";
            MATCH     PMPXVGPC,SP70
            IF        !@EQUAL
              WRITE     HTMLFILE;*+,", { #"reference#": #"Practitioner/HCP-",PMPXVGPC,"#" }";
            ENDIF
            WRITE     HTMLFILE;*+," ]";
          ENDIF
          WRITE     HTMLFILE;*+,",#"telecom#": [ ";
          WRITE     HTMLFILE;*+,"{#"system#": #"phone#",":
                                " #"use#": #"home#",":
                                " #"value#": #"",PTELEP,"#"}";
          MATCH     SP70,PMPXPEML
          IF        !@EQUAL
            WRITE     HTMLFILE;*+,",{#"system#": #"email#",":
                                  " #"value#": #"",PMPXPEML,"#"}";
          ENDIF
          MATCH     SP70,PTELEB
          IF        !@EQUAL
            WRITE     HTMLFILE;*+,",{#"system#": #"phone#",":
                                  " #"use#": #"work#",":
                                  " #"value#": #"",PTELEB,"#"}";
          ENDIF
          MATCH     SP70,PTMXCELL
          IF        !@EQUAL
            WRITE     HTMLFILE;*+,",{#"system#": #"phone#",":
                                  " #"use#": #"mobile#",":
                                  " #"value#": #"",PTMXCELL,"#"} ";
          ENDIF
          WRITE     HTMLFILE;*+,"]";
          RETURN
+
.---------------------------------
. FHIR Location Resource
.---------------------------------
RESLOC00  STRIP     RELBDES
          CLEAR     KEY16
          APPEND    "Laboratory-",KEY16
          APPEND    RELBLCD,KEY16
          APPEND    SP70,KEY16
          RESET     KEY16
          STRIP     KEY16
          STRIP     WBTPFIL  *  Template
          WRITE     HTMLFILE;*+," { #"resourceType#": #"Location#",":
                    "   #"id#": #"",KEY16,"#",":
                    " #"extension#": [":
                    " { #"url#":#"%BASEURL/extension/webPASService#"":
                        " ,#"valueString#":#"",PRGID,SP1,PRGNAM,SP1,VERSION,"#"},":
                    " { #"url#":#"%BASEURL/extension/webPASTemplate#"":
                        " ,#"valueString#":#"%TEMPLATEHOME/",WBTPFIL," %TEMPLATEVERSION#"}":
                    " ],":
                    " #"identifier#": {":
                    "   #"value#": #"",KEY16,"#"":
                    " },";
          WRITE     HTMLFILE;*+," #"name#": #"",RELBDES,"#"":
                    " } "
          RETURN
+
.------------------------------------------------------------------------------
. FHIR Encounter Resource Recorder
.------------------------------------------------------------------------------
RESPRA00  MATCH     SP70,WBSEOCG
          IF        !@EQUAL
            MOVE      SP70,DIM20
            MOVE      "w0",CATEGORY
            PACK      KEY5,CATEGORY,WBSEOCG,SP70
            CALL      RDCODE1
            IF        OVRCD=0
              MOVE      TDESC,DIM20
            ENDIF
          ENDIF
.
          MOVE      SP70,KEY10
          MATCH     SP70,WBSEDOC
          IF        !@EQUAL
            PACK      KEY10,WBSEDOC,SP70
          ELSE
            MATCH     SP70,WBSEGPCD
            IF        !@EQUAL
              PACK      KEY10,WBSEGPCD,SP70
            ENDIF
          ENDIF
.
          MATCH     SP70,KEY10
          IF        !@EQUAL & !@EOS
            MOVE      SP70,TDESC
            CALL      RDPMHCP1
            IF        OVRCD=0
              MATCH     SP70,PMHCSPC1
              IF        !@EQUAL
                MOVE      "DT",KEY2
                PACK      KEY5,KEY2,PMHCSPC1,SP70
                CALL      RDCODE1
                IF        OVRCD=1
                  MOVE      SP70,TDESC
                ENDIF
              ENDIF
            ENDIF
            STRIP     KEY10
          ENDIF
.
          STRIP     WBTPFIL  *  Template
          WRITE     HTMLFILE;*+," { #"resourceType#": #"Practitioner#",":
                    "   #"id#": #"User-",REAUUID,"#",":
                    " #"extension#": [":
                    " { #"url#":#"%BASEURL/extension/webPASService#"":
                        " ,#"valueString#":#"",PRGID,SP1,PRGNAM,SP1,VERSION,"#"},":
                    " { #"url#":#"%BASEURL/extension/webPASTemplate#"":
                        " ,#"valueString#":#"%TEMPLATEHOME/",WBTPFIL," %TEMPLATEVERSION#"},":
                    " { #"url#":#"%BASEURL/extension/dxc-hc-role#"":
                        " ,#"valueString#":#"",DIM20,"#"}";
.
          MATCH     SP70,KEY10
          IF        !@EQUAL & !@EOS
            WRITE     HTMLFILE;*+,",{ #"url#": #"%BASEURL/extension/dxc-hc-hcp#"":
                                "  ,#"valueReference#" : #"Practitioner/HCP-",KEY10,"#"}";
          ENDIF
.
          MATCH     SP70,TDESC
          IF        !@EQUAL & !@EOS
            WRITE     HTMLFILE;*+,",{#"url#":#"%BASEURL/extension/dxc-hc-speciality#"":
                                  ",#"valueString#" : #"",TDESC,"#"}";
          ENDIF
.
          WRITE     HTMLFILE;" ],":
                    " #"identifier#": {":
                    "   #"use#": #"official#",":
                    "   #"system#": #"%BASEURL#",":
                    "   #"value#": #"",WBSELOGN,"#"":
                    " },";
          WRITE     HTMLFILE;*+," #"name#": [":
                    "{ #"use#": #"usual#",":
                    "  #"text#": #"",WBSENAM,"#",":
                    "  #"family#": #".#",":
                    "  #"given#": [#"",WBSENAM,"#"] ":
                    "} ]";
.
          MATCH     SP70,WBSEEMAI
          IF        !@EQUAL
             WRITE     HTMLFILE;*+,",";
             WRITE     HTMLFILE;*+," #"telecom#": [":
                       "{ #"system#": #"email#",":
                       "  #"value#": #"",WBSEEMAI,"#" } ]";
          ENDIF
.
          WRITE     HTMLFILE;*+,"  } ";
.
RESPRA99  RETURN
+
.------------------------------------------------------------------------------
. FHIR Practition Resource CopyTo
.------------------------------------------------------------------------------
RESASS00  MOVE      PMHCADR1,PRACNAME       * use hcp details
          MOVE      PMHCADR2,PRACADD1
          MOVE      SP70,PRACADD2
          MOVE      PMHCADR3,PRACADD3
          MOVE      PMHCADR4,PRACADD4
          MOVE      PMHCPOST,PRACPOST
          MOVE      PMHCSEML,PRACPEML
          MOVE      PMHCSTEL,PRACPTEL
          MOVE      PMHCFAXN,PRACPFAX
          MOVE      PMHCPRV1,PRACPROV
          PACK      DISPPRAC,SP70
          PACK      DISPPNAM,SP70,SP70
          PACK      DISPPCNT,SP70
.
          MOVE      "DT",TCODE
          PACK      KEY5,TCODE,PMHCSPC1,SP70
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE      SP70,TDESC
          ENDIF
          CALL      HCPNAM00
          WRITE     HTMLFILE;*+," { #"resourceType#": #"Practitioner#",":
                    "   #"id#": #"HCP-",PMHCHCPC,"#",":
                    " #"extension#": [":
                    " { #"url#":#"%BASEURL/extension/webPASService#"":
                        " ,#"valueString#":#"",PRGID,SP1,PRGNAM,SP1,VERSION,"#"},":
                    " { #"url#":#"%BASEURL/extension/webPASTemplate#"":
                        " ,#"valueString#":#"%TEMPLATEHOME/",WBTPFIL," %TEMPLATEVERSION#"},":
                    " { #"url#":#"%BASEURL/extension/dxc-hc-speciality#"":
                        " ,#"valueString#":#"",TDESC,"#"}":
                    " ],":
                    " #"identifier#": [ ":
                    "  {#"type#": { #"text#" : #"HCP ID#" },":
                    "   #"value#": #"",PMHCHCPC,"#" },":
                    "  {#"type#": { #"text#" : #"Provider Number#" },":
                    "   #"value#": #"",PRACPROV,"#" }":
                    "    ] ,":
                    " #"name#": [ { ":
                    "  #"use#": #"usual#",":
                    "  #"text#": #"",DOCFNAME,"#" ,":
                    "  #"prefix#":[ #"",PMHCTITL,"#" ],":
                    "  #"given#": [ #"",PMHCGNAM,"#" ],":
                    "  #"family#": #"",PMHCSNAM,"#" } ],":
                    "  #"telecom#": [ ":
                    "   { #"system#": #"phone#", #"use#": #"work#", ":
                    "     #"value#": #"",PRACPTEL,"#" },":
                    "   { #"system#": #"email#", #"use#": #"work#", ":
                    "     #"value#": #"",PRACPEML,"#" },":
                    "   { #"system#": #"phone#", #"use#": #"mobile#", ":
                    "     #"value#": #"",PMHCMOBN,"#" },":
                    "   { #"system#": #"fax#",   #"use#": #"work#", ":
                    "     #"value#": #"",PRACPFAX,"#" } ],":
                    " #"address#": [ {":
                    "  #"use#": #"work#",":
                    "  #"line#": [#"",PRACNAME,"#"":
                    ",#"",PRACADD1,"#"";
          MATCH     SP70,PRACADD2
          IF        !@EQUAL
            STRIP     PRACADD2
            WRITE     HTMLFILE;*+,",#"",PRACADD2,"#"";
          ENDIF
          WRITE     HTMLFILE;*+,"],":
                      "       #"city#": #"",PRACADD3,"#",":
                      "       #"state#": #"",PRACADD4,"#",":
                      "       #"postalCode#": #"",PRACPOST,"#"":
                      "      }":
                      "    ]";
          WRITE     HTMLFILE;*+,"  }";
RESASS99  RETURN
+
          INC       IBAWEBCD    
.
          INC       CLPATMAS
          INC       APSMASIO/INC
          INC       PATVISIO/INC
          INC       PATIN1IO/INC
          INC       PATNURIO/INC
          INC       PATPR1IO/INC
          INC       PATMA1IO/INC
          INC       PMSHCPIO/INC
          INC       PMSHCGIO/INC
          INC       PMSHCPTM
          INC       PMSHCGTM
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
          INC       PATDO1IO/INC
          INC       PATWR1IO/INC
          INC       RESAUDIO/INC
          INC       RESLOGIO/INC
          INC       RESLABIO/INC
          INC       RESLNKIO/INC
          INC       RESLURIO/INC
          INC       RESHEAIO/INC
          INC       RESHEBIO/INC
          INC       RESHECIO/INC
          INC       RESHEDIO/INC
          INC       RESDEAIO/INC
          INC       RESDEBIO/INC
          INC       RESDECIO/INC
          INC       RESPIDIO/INC
          INC       RESCTAIO/INC
          INC       RESCLNIO/INC
          INC       OBSPCTIO/INC       * Correspondence Storage Location Table
          INC       OBSPCOIO/INC
          INC       OBSAUDIO/INC
          INC       HL7CODIO/INC
          INC       NHIMASIO/INC
          INC       NHIETHIO/INC
          INC       CLNHIMAS                   * Clear nhimasaf file variables
          INC       PATMASTM    
          INC       CLPMSPX2
          INC       PMSPX2TM
          INC       PATMSXTM
          INC       PATDOCTM
          INC       PATDOCCD
          INC       PATWRDTM
          INC       RESDETTM
          INC       DAYOFWEK
          INC       CALCAGE
          INC       NAMSTRCD
          INC       WEBDATCD
          INC       FHRDATCD
          INC       MRTLOCIO/INC
          INC       PMSCCDIO/INC       * Concession Card Details
          INC       PMSCEXIO/INC
          INC       PMSTLEIO/INC
          INC       PMSRELIO/INC
          INC       SCRMASIO/INC
.

.***************************************************************************
.* System    :   Outpatients                                               *
.* Program   :   FXOPCONV                                                  *
.* Desc      :   Conversion program to load future outpatient appointments *
.***************************************************************************
.* Date      :   05/09/2006                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   This program will read a text file xxxxxxxx.txt and write *
.*           :   outpatient bookings. Two output files will be created in  *
.*           :   the same directory as the upload file. The files will     *
.*           :   be called xxxxxxxx.acp for accepted records and           *
.*           :   xxxxxxxx.dec for rejected records.                        *
.*           :                                                             *
.* Mods      :                                                             *
.***************************************************************************
.         V12.00.01 30.05.2025  DA Sarkies       TSK 0955096               *
.                   Updated for Alpha-Numeric Visit Number                 *
.***************************************************************************
.*        V11.00.01 22/04/2020  Ebon Clements    TSK 0850105               *
.*                  Clear PMCUTSTA and PMCUTLOC before write to pmscuraf   *
.***************************************************************************
.*        V10.11.02 27/11/2017  Thanh Tieu        Tsk 0821710              *
.*                  Recompiled as PATMISTD changed                         *
.*        V10.11.01 20/09/2017  Thanh T.          TSK 0821710              *
.*                  Audit Amission/outpatient visit comments               *
.***************************************************************************
.*        V10.08.01 02/05/2016  Ebon Clements     TSK 0268970              *
.*                  Change to use OUTCLIFD index 1 instead of index 2      *
.*        V10.03.05 11/12/2013  Ebon Clements     CAR 293092               *
.*                  Added notified and confirmed to interpreter audit      *
.*        V10.03.04 14/10/2013  Ania P          CAR 278232                 *
.*                  Refactored code - now using COUNTSLT for session       * 
.*                  recalculation                                          *
.*        V10.03.03 31/01/2013  Ebon Clements   CAR 262292                 *
.*                  Added language to interpreter audit                    *
.*        V10.03.02 31/08/2012  Peter Vela      CAR 266428                 *
.*                  Added VSINCHON before write to visintaf                *
.*        V10.03.01 18/11/2011  Mike Laming     CAR 240184                 *
.*                  Changes to IBAPOSTF Post Code table - added State to Keys *
.*        V10.02.01 22/06/2011  Steve Armstrong   CAR 240722               *
.*                  Mods to cater for changes to PMSCURFD:                 *
.*                       - PMCUSURN & PMCUGNAM extended to 40 chars.       *
.*                       - PMCUGNM2 added.                                 *
.***************************************************************************
.*        V9.11.02 07/04/2009  Peter Vela    CAR 183976                    *
.*                 Moved spaces to PMVXPILC before WRPMVX11                *
.*        V9.11.01 17/02/2009  Peter Vela    CAR 183976                    *
.*                 Moved spaces to PMVXPOEC before WRPMVX11                *
.*        V9.08.01 31/01/2007  Peter Vela    CAR 127930                    *
.*                 Recompled for MRTCONFD                                  *
.*        V9.06.00 06/09/2006  CAR 107391                                  *
.*                 Created program                                         *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
.
          INC       IBACONFD/INC
          INC       IBAPOSFD/INC
          INC       MRTMASFD/INC
          INC       MRTVISFD/INC
          INC       MRTCONFD/INC
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
          INC       OUTBOCFD/INC
          INC       OUTCANFD/INC
          INC       OUTCDCFD/INC
          INC       OUTCLIFD/INC
          INC       OUTCONFD/INC
          INC       OUTCTYFD/INC
          INC       OUTDIAFD/INC
          INC       OUTHEDFD/INC
          INC       OUTMA1FD/INC
          INC       OUTPREFD/INC
          INC       OUTRAPFD/INC
          INC       OUTSESFD/INC
          INC       OUTSITFD/INC
          INC       OUTXSCFD/INC
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PATMA1FD/INC
          INC       PATCONFD/INC
          INC       VISMDTFD/INC
          INC       VISMTXFD/INC
          INC       PATMISTD/INC
          INC       PATHSPFD/INC
          INC       PATRE1FD/INC
          INC       PATVISFD/INC
          INC       PATWVEFD/INC
          INC       PATWC1FD/INC
          INC       PATWMAFD/INC
          INC       PMSCURFD/INC
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
          INC       PMSWX1FD/INC
          INC       VISINTFD/INC
          INC       VISIAUFD/INC
          INC       WEBCONFD/INC
.
ERRORSFL  FILE      ASCII, FIXED=256
FINDFILE  FILE      ASCII, FIXED=256
LOADEDFL  FILE      ASCII, FIXED=256 
.
LOADFILE  FILE      ASCII, FIXED=575 
.
.Name     Type    Length  Pos  Description
.----     ----    ------  ---  -----------
XOPSITE   DIM       6      1    Site 
XOPCLIN   DIM       6      7    Clinic
XOPDATE   DIM       8      13   Date  (CCYYMMDD)
XOPSTRT   DIM       5      21   Start (HH:MM)
XOPSLOT   DIM       3      26   Slot
XOPTIME   DIM       5      29   Slot Time (HH:MM)
XOPCLTYP  DIM       6      34   Clinic Type
XOPURNO   DIM       8      40   U/R
XOPVISIT  DIM       3      48   Visit Type (Cat CV)
XOPBKNO   DIM       8      51   Booking Number
XOPBKDTE  DIM       16     59   Booking Date/Time (CCYYMMDDHH:MM:SS)
XOPSRCR   DIM       3      75   Source of Referral (Cat S)
XOPCOMP   DIM       3      78   Claim Code (Cat CL)
XOPPRTY   DIM       3      81   Priority (Cat PR)
XOPUDF1   DIM       3      84   User Defined 1 (Cat O1)
XOPUDF2   DIM       3      87   User Defined 2 (Cat O2)
XOPUDF3   DIM       3      90   User Defined 3 (Cat 03)
XOPUDF4   DIM       3      93   User Defined 4 (Cat O4)
XOPUNIT   DIM       3      96   Unit (Cat AC)
XOPTRAN   DIM       3      99   Transport required (Cat OB)
XOPRFGP   DIM       10     102  Referring GP
XOPRFPC   DIM       10     112  Referring GP Practice
XOPUDF5   DIM       3      122  User Defined 5 (Cat O5)
XOPUDF6   DIM       3      125  User Defined 6 (Cat O6)
XOPUDF7   DIM       3      128  User Defined 7 (Cat O7)
XOPUDF8   DIM       3      131  User Defined 8 (Cat 08)
XOPDTREF  DIM       8      134  Date of Referral (CCYYMMDD)
XOPDGCD1  DIM       9      142  Diagnosis Code 1
XOPDGCD2  DIM       9      151  Diagnosis Code 2
XOPDGCD3  DIM       9      160  Diagnosis Code 3
XOPDGCD4  DIM       9      169  Diagnosis Code 4
XOPDGCD5  DIM       9      178  Diagnosis Code 5
XOPCMPLT  DIM       50     187  Presenting Complaint
XOPCMNT1  DIM       70     237  Comment Line 1
XOPCMNT2  DIM       70     307  Comment Line 2
XOPCMNT3  DIM       70     377  Comment Line 3
XOPFLDR   DIM       3      447  Folder Selection (Cat FS)
XOPACCN   DIM       20     450  ACC Number
XOPACCDT  DIM       8      470  Accident Date (CCYYMMDD)
XOPACCDD  DIM       8      478  Accident Declined Date (CCYYMMDD)
XOPACCWR  DIM       3      486  ACC Work Related (Cat IQ)
XOPACCDI  DIM       50     489  ACC Accident Description
XOPACCPO  DIM       3      539  ACC Place Where Injury Occurred (Cat IP)
XOPACCAI  DIM       3      542  ACC Activity When Injured (Cat IO)
XOPACCEM  DIM       30     545  ACC Employer
.
.End of Record              575        
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
ADMISSNO  DIM       8
CASEKEYS  DIM       28
CATEGORY  DIM       2
CFILEPRE  DIM       3
CHKGRPIN  FORM      1
CMDLINE   DIM       100
CNTSLOTS  FORM      3
CODE      DIM       2
CURRDATE  DIM       8
DATACHNG  FORM      1 
DIAGOVR   FORM      1
DIM4      DIM       4
DIM60     DIM       60
DISPNAME  DIM       60
DOCTCODE  DIM       6
DOTTXT    INIT      ".txt"
DOTACP    INIT      ".acp"
DOTDEC    INIT      ".dec"
FILENAME  DIM       8
FULLNAME  DIM       12
FULLPATH  DIM       60
HOUR      FORM      2       * Hour Variable
KSECONDS  INIT      ":00"
INTAUDTY  DIM       1
LASTDATE  DIM       8
LOCKCNT   FORM      2
MIN       DIM       2       * Minutes Variable
MINUTES   FORM      6       * Minutes Variable
NEWSLOTK  DIM       28
NEWSLOTS  FORM      3
NEWVTYP   DIM       3
OLDSLOTS  FORM      3
OLDVTYP   DIM       3
PARNSLOT  FORM      3
PATHFACP  DIM       72
PATHFDEC  DIM       72
PATHFNAM  DIM       72
SAVEDESC  DIM       50
SAVEDES2  DIM       50
SAVEDES3  DIM       50
SAVEDES4  DIM       50
SAVEDES5  DIM       50
SAVKEY28  DIM       28
SESSKEYS  DIM       25
SP60      INIT    "                                                            "
STRTDATE  DIM       8
TOTALLOD  FORM      8
TOTALERR  FORM      8
URNUMBER  DIM       8
XERROR    DIM       60
CURRDATX  DIM       8
MAXNOTEN  FORM      6
COMTFLAG  FORM      1
.
.-----------------------------------------------------------------------
PRGID     INIT      "FXOPCONV"
PRGNAM    INIT      "Convert Outpatient Data"
VERSION   INIT      "V12.01.00"
.-----------------------------------------------------------------------
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      KDIR0000               * Keyin directory
          BRANCH    EXIT,ML9999   
.
          CALL      KFIL0000               * Keyin filename
          BRANCH    EXIT,ML9999   
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML5000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML5000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      OUTSITA1,"outsitaf"
          MOVE      "out",CFILEPRE          * Save Current File Prefix
          CALL      OPNOUT00                * Open Outpatient Files
.
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PATRE1A1,"patre1af"
          OPEN      PATVISA1,"patvisaf"
          OPEN      PMSCURA1,"pmscuraf"
          OPEN      PMSPX2A1,"pmspx2af"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      VISINTA1,"visintaf"  .
          OPEN      VISIAUA1,"visiauaf"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
          OPEN      PATWMAB1,"patwmabf"
          OPEN      PMSWX1A1,"pmswx1af"
          OPEN      PATWC1A1,"patwc1af"
          OPEN      IBAPOST1,"ibapostf"
          OPEN      PATWVET1,"patwvetf"
          OPEN      PATDO1A1,"patdo1af"
          OPEN      MRTVISA1,"mrtvisaf"
          OPEN      PATHSPA1,"pathspaf"
          OPEN      OUTPREA1,"outpreaf"
          OPEN      VISMDTA1,"vismdtaf"
          OPEN      VISMTXA1,"vismtxaf"
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,HUND05;*210,PTCNLCLM
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Load outpatient data from ":
                                           "a nominated file"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
.
.********************************************************************
.*                          KDIR0000                                *
.*  Keyin directory                                                 *
.********************************************************************
KDIR0000  MOVE      SP60,FULLPATH
.
          DISPLAY   *P1:24,*EL,"The directory path must end with a slash (/) ";
.
.         Keyin the path for the upload file
.
          KEYIN     *P1:9,*EL,"Keyin Path :":
                    *P14:9,*EL,*DV,*RV,FULLPATH:
                    *P14:9,FULLPATH;
.
KDIR3000  ENDSET    FULLPATH
          APPEND    SP60,FULLPATH
          RESET     FULLPATH
.
          SQUEEZE   FULLPATH
          CMATCH    EXITCHAR,FULLPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,FULLPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR0000
.
          GOTO      KDIR9500
.
KDIR9000  MOVE      ONE,EXIT
.
KDIR9500  DISPLAY   *P1:24,*EL;
.
KDIR9999  RETURN
.
.**********************************************************************
.*                               VALD0000                             *
.*        Check if it a valid directory                               *
.**********************************************************************
VALD0000  MOVE      ZERO,EXIT
          SQUEEZE   DIM60
          ENDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory path must end with a slash (/) ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory does not exist ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT            * Not valid
VALD9999  RETURN
.
.********************************************************************
.*                          KFIL0000                                *
.*  Keyin filename                                                  *
.********************************************************************
KFIL0000  KEYIN     *P1:11,*EF,"Keyin File Name : ",FULLNAME
.
          PACK      FULLNAME,FULLNAME,SP60
.
          MATCH     SP70,FULLNAME           * anything entered ?
          IF        @EQUAL
            DISPLAY   *P1:24,*EL,*B,"The file name is a mandatory field ";
            CALL      HITENTER
            GOTO      KFIL0000
          ENDIF
.
          SQUEEZE   FULLNAME
.
          CMATCH    EXITCHAR,FULLNAME       * Exit
          GOTO      KFIL9000 IF EQUAL
.
          MOVELPTR  FULLNAME,F2
          IF        F2<EIGHT
            DISPLAY   *P1:24,*EL,*B,"The file name must be 8 characters ";
            CALL      HITENTER
            GOTO      KFIL0000
          ENDIF
.
          UNPACK    FULLNAME,FILENAME,DIM4
.
          SCAN      ".",FILENAME
          IF        @EQUAL
            DISPLAY   *P1:24,*EL,*B,"The file name must be 8 characters ";
            CALL      HITENTER
            GOTO      KFIL0000
          ENDIF
.
          MATCH     DOTTXT,DIM4
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"The file name must end in .txt ";
            CALL      HITENTER
            GOTO      KFIL0000
          ENDIF
.
          PACK      PATHFNAM,FULLPATH,FILENAME,DOTTXT
          STRIP     PATHFNAM
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      LOADFILE,PATHFNAM        * open file
          TRAPCLR   IO
.
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*+,*B,PATHFNAM,"does not exist.  ";
            CALL      HITENTER
            GOTO      KFIL0000
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      KFIL9999
.
KFIL9000  MOVE      ONE,EXIT
          GOTO      KFIL9999
.
KFIL9999  RETURN
.
.********************************************************************
.*                          KTMP0000                                *
.*  Create output files                                             *
.********************************************************************
KTMP0000  PACK      PATHFDEC,FULLPATH,FILENAME,DOTDEC
          PREP      ERRORSFL,PATHFDEC
          CLOSE     ERRORSFL,DELETE
          PREP      ERRORSFL,PATHFDEC
.           
          PACK      PATHFACP,FULLPATH,FILENAME,DOTACP
          PREP      LOADEDFL,PATHFACP
          CLOSE     LOADEDFL,DELETE
          PREP      LOADEDFL,PATHFACP
.
KTMP9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
PROC0000  CALL      KTMP0000                * Prep output files
          MOVE      ZERO,TOTALLOD           * Total Record Loaded
          MOVE      ZERO,TOTALERR           * Total Errors
.
PROC1000  READ      LOADFILE,SEQ;XOPSITE,XOPCLIN,XOPDATE,XOPSTRT,XOPSLOT:
                                 XOPTIME,XOPCLTYP,XOPURNO,XOPVISIT,XOPBKNO:
                                 XOPBKDTE,XOPSRCR,XOPCOMP,XOPPRTY,XOPUDF1:
                                 XOPUDF2,XOPUDF3,XOPUDF4,XOPUNIT,XOPTRAN:
                                 XOPRFGP,XOPRFPC,XOPUDF5,XOPUDF6,XOPUDF7:
                                 XOPUDF8,XOPDTREF,XOPDGCD1,XOPDGCD2,XOPDGCD3:
                                 XOPDGCD4,XOPDGCD5,XOPCMPLT,XOPCMNT1,XOPCMNT2:
                                 XOPCMNT3,XOPFLDR,XOPACCN,XOPACCDT,XOPACCDD:
                                 XOPACCWR,XOPACCDI,XOPACCPO,XOPACCAI,XOPACCEM
.
          GOTO      PROC9000 IF OVER
.
          CALL      EDIT0000                * Edit Checks
          BRANCH    EXIT,PROC1000
.
          CALL      WRTBOK00                * Write Booking Record
          BRANCH    EXIT,PROC1000
.
          CALL      PACC0000                * Print Accepted Record
.
          GOTO      PROC1000
.
PROC9000  DISPLAY   *P1:23,*EF,*B,"Finish Upload. Records Loaded: ",TOTALLOD:
                                  "     Errors: ",TOTALERR
          CALL      HITENTER
.
PROC9999  RETURN
.
.**************************************************************************
.*                               EDIT0000             Called by: PROC0000 *
.*                                                                        *
.**************************************************************************
EDIT0000  PACK      KEY6,XOPSITE,SP70
          CALL      RDSITA1
          IF        OVRCD=1
            MOVE      "Site code does not exist in outsitaf",XERROR
            GOTO      EDIT9000
          ENDIF
.
          CALL      COTFIL00                * Check site code prefix
.
          PACK      KEY20,XOPSITE,XOPCLIN,Z70
          CALL      RDSCLIA1
          CALL      RDPCLIA1                * Check clinic id
          IF        OVRCD=0
            MATCH     XOPSITE,OCASITE
            IF        @EQUAL
              MATCH     XOPCLIN,OCACLIN
              GOTO      EDIT0500 IF EQUAL
            ENDIF
          ENDIF
.
          MOVE      "Clinic id does not exist in outcliaf",XERROR
          GOTO      EDIT9000
.
EDIT0500  PACK      KEY12,XOPSITE,XOPCLTYP,SP70
          CALL      RDCTYA1
          IF        OVRCD=1
            MOVE      "Clinic type does not exist in outctyaf",XERROR
            GOTO      EDIT9000
          ENDIF
.
          PACK      SESSKEYS,XOPSITE,XOPCLIN,XOPDATE,XOPSTRT,SP70
          PACK      CASEKEYS,XOPSITE,XOPCLIN,XOPDATE,XOPSTRT,XOPSLOT,SP70
          PACK      NEWSLOTK,XOPSITE,XOPCLIN,XOPDATE,XOPSTRT,XOPSLOT,SP70
          PACK      KEY28,XOPSITE,XOPCLIN,XOPDATE,XOPSTRT,XOPSLOT,SP70
          CALL      RDBOKA1
          IF        OVRCD=1
            MOVE      "Record does not exist in outbokaf",XERROR
            GOTO      EDIT9000
          ENDIF
.
          COMPARE   ZERO,OBASTAT
          IF        !@EQUAL
            MOVE      "Appointment Time Has Been Taken. ",XERROR
            GOTO      EDIT9000
          ENDIF
.
          MATCH     XOPTIME,OBATIME
          IF        !@EQUAL
            MOVE      "Record does not exist in outbokaf(XOPTIME)",XERROR
            GOTO      EDIT9000
          ENDIF
.
          MATCH     XOPCLTYP,OBACTYP
          IF        !@EQUAL
            MOVE      "Record does not exist in outbokaf(XOPCLTYP)",XERROR
            GOTO      EDIT9000
          ENDIF
.
          MATCH     CURRDATE,XOPDATE
          IF        @LESS
            MOVE      "Session date is not in the future",XERROR
            GOTO      EDIT9000
          ENDIF
.
          MOVE      XOPBKNO,ADMISSNO
          MATCH     SP70,ADMISSNO
          IF        @EQUAL
            MOVE      "Booking/Event number is a mandatory field",XERROR
            GOTO      EDIT9000
          ENDIF
.
          PACK      KEY36,XOPBKNO,SP70
          CALL      RDSBOKA6
          CALL      RDKBOKA6
          BRANCH    OVRCD,EDIT1000
.
          MATCH     XOPBKNO,DOBAOUTN
          IF        @EQUAL
            MOVE      "Booking/Event number already exists in outbokaf",XERROR
            GOTO      EDIT9000
          ENDIF
.
EDIT1000  PACK      KEY8,XOPBKNO,SP70
          CALL      RDOTCAN1
          IF        OVRCD=0
            MOVE      "Booking/Event number already exists in outcanaf",XERROR
            GOTO      EDIT9000
          ENDIF     
. 
EDIT1100  MOVE      XOPURNO,URNUMBER
          MATCH     SP70,URNUMBER
          IF        @EQUAL
            MOVE      "Patient U/R number is a mandatory field",XERROR
            GOTO      EDIT9000
          ENDIF
.
          PACK      KEY8,XOPURNO,SP70
          CALL      RDMAST1
          IF        OVRCD=1
            MOVE      "Patient U/R Number does not exist in patma1af",XERROR
            GOTO      EDIT9000
          ENDIF
.
          PACK      KEY8,XOPURNO,SP70
          CALL      RDPMPX21
          IF        OVRCD=1
            MOVE      "Patient U/R Number does not exist in pmspx2af",XERROR
            GOTO      EDIT9000
          ENDIF
.
          MOVE      SP70,SAVEDESC
          MATCH     SP70,XOPDGCD1
          IF        !@EQUAL
            PACK      KEY21,XOPSITE,XOPCLTYP,XOPDGCD1,SP70
            CALL      RDOTCDC1                * Read an ICD operation file rec
            IF        OVRCD=1
              MOVE      "ICD10 Code does not exist in outcdcaf(XOPDGCD1)",XERROR
              GOTO      EDIT9000
            ENDIF
            MOVE      OTCDDESC,SAVEDESC
          ENDIF
.
          MOVE      SP70,SAVEDES2
          MATCH     SP70,XOPDGCD2
          IF        !@EQUAL
            PACK      KEY21,XOPSITE,XOPCLTYP,XOPDGCD2,SP70
            CALL      RDOTCDC1                * Read an ICD operation file rec
            IF        OVRCD=1
              MOVE      "ICD10 Code does not exist in outcdcaf(XOPDGCD2)",XERROR
              GOTO      EDIT9000
            ENDIF
            MOVE      OTCDDESC,SAVEDES2
          ENDIF
.
          MOVE      SP70,SAVEDES3
          MATCH     SP70,XOPDGCD3
          IF        !@EQUAL
            PACK      KEY21,XOPSITE,XOPCLTYP,XOPDGCD3,SP70
            CALL      RDOTCDC1                * Read an ICD operation file rec
            IF        OVRCD=1
              MOVE      "ICD10 Code does not exist in outcdcaf(XOPDGCD3)",XERROR
              GOTO      EDIT9000
            ENDIF
            MOVE      OTCDDESC,SAVEDES3
          ENDIF
.
          MOVE      SP70,SAVEDES4
          MATCH     SP70,XOPDGCD4
          IF        !@EQUAL
            PACK      KEY21,XOPSITE,XOPCLTYP,XOPDGCD4,SP70
            CALL      RDOTCDC1                * Read an ICD operation file rec
            IF        OVRCD=1
              MOVE      "ICD10 Code does not exist in outcdcaf(XOPDGCD4)",XERROR
              GOTO      EDIT9000
            ENDIF
            MOVE      OTCDDESC,SAVEDES4
          ENDIF
.
          MOVE      SP70,SAVEDES5
          MATCH     SP70,XOPDGCD5
          IF        !@EQUAL
            PACK      KEY21,XOPSITE,XOPCLTYP,XOPDGCD5,SP70
            CALL      RDOTCDC1                * Read an ICD operation file rec
            IF        OVRCD=1
              MOVE      "ICD10 Code does not exist in outcdcaf(XOPDGCD5)",XERROR
              GOTO      EDIT9000
            ENDIF
            MOVE      OTCDDESC,SAVEDES5
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      EDIT9999
.
EDIT9000  CALL      PERR0000                * Print Error
.
EDIT9999  RETURN
.
.**************************************************************************
. Open Outpatient Files
.**************************************************************************
OPNOUT00  PACK      CFNAME,CFILEPRE,FILCLIA1  * Get the name of the CLIA1 file
          CLOSE     OUTCLIA1
          OPEN      OUTCLIA1,CFNAME           * Open the file
.
          PACK      CFNAME,CFILEPRE,FILCTYA1  * Get the name of the CTYA1 file
          CLOSE     OUTCTYA1
          OPEN      OUTCTYA1,CFNAME           * Open the file
.
          PACK      CFNAME,CFILEPRE,FILBOKA1  * Get the name of the BOKA1 file
          CLOSE     OUTBOKA1
          OPEN      OUTBOKA1,CFNAME           * Open the file
          PACK      CFNAME,CFILEPRE,FILBOKA6  * Get the name of the BOKA6 file
          CLOSE     OUTBOKA6
          OPEN      OUTBOKA6,CFNAME           * Open the file
.
          PACK      CFNAME,CFILEPRE,FILBB1A1  * Get the name of the BB1A1 file
          CLOSE     OUTBB1A1
          OPEN      OUTBB1A1,CFNAME           * Open the file
.
          PACK      CFNAME,CFILEPRE,FILDIAG1  * Get the name of the DIAG1 file
          CLOSE     OUTDIAG1
          OPEN      OUTDIAG1,CFNAME           * Open the file
.
          PACK      CFNAME,CFILEPRE,FILHEDA1  * Get the name of the HEDA1 file
          CLOSE     OUTHEDA1
          OPEN      OUTHEDA1,CFNAME           * Open the file
.
          PACK      CFNAME,CFILEPRE,FILSESA1  * Get the name of the SESA1 file
          CLOSE     OUTSESA1
          OPEN      OUTSESA1,CFNAME           * Open the file
.
          PACK      CFNAME,CFILEPRE,FILRAPA1  * Get the name of the PAPA1 file
          CLOSE     OUTRAPA1
          OPEN      OUTRAPA1,CFNAME         * Open the Re-Appointment file
.
          PACK      CFNAME,CFILEPRE,FILMA1A1
          CLOSE     OUTMA1A1
          CALL      OPOTMA11                * Open the file
          PACK      CFNAME,CFILEPRE,FILMA1A2
          CLOSE     OUTMA1A2
          CALL      OPOTMA12                * Open the file
.
          PACK      CFNAME,CFILEPRE,FILXSCA1  * Get the name of the XSCA1 file
          CLOSE     OUTXSCA1
          OPEN      OUTXSCA1,CFNAME           * Open the file
.
          PACK      CFNAME,CFILEPRE,FILBOKC1  * Get the name of the BOKC1 file
          CLOSE     OUTBOKC1
          OPEN      OUTBOKC1,CFNAME           * Open the file
.
          PACK      CFNAME,CFILEPRE,FILCANA1  * Get the name of the CANA1 file
          CLOSE     OUTCANA1
          OPEN      OUTCANA1,CFNAME          * Open the cancellation log file
.
          PACK      CFNAME,CFILEPRE,FILCDCA1  * Get the name of the CDCA1 file
          CLOSE     OUTCDCA1
          OPEN      OUTCDCA1,CFNAME          * Open the diagnosis file
.
OPNOUT99  RETURN
.
.*******************************************************************************
. COTFIL00 - Check that the correct outpatient site prefix files are open
.*******************************************************************************
COTFIL00  MATCH     OSTFILE,CFILEPRE         * Save Current File Prefix
          IF        !@EQUAL
            MOVE      OSTFILE,CFILEPRE       * Save Current File Prefix
            CALL      OPNOUT00               * Open prefixed Outpatient Files
          ENDIF
.
COTFIL99  RETURN
.
.**************************************************************************
.*                               PERR0000                                 *
.*                                                                        *
.**************************************************************************
PERR0000  WRITE     ERRORSFL,SEQ;XOPSITE,XOPCLIN,XOPDATE,XOPSTRT,XOPSLOT:
                                 XOPTIME,XOPCLTYP,XOPURNO,XOPVISIT,XOPBKNO:
                                 XERROR
          ADD       ONE,TOTALERR
          MOVE      ONE,EXIT

PERR9999  RETURN
.**************************************************************************
.*                               PACC0000             Called by: PROC0000 *
.*                                                                        *
.**************************************************************************
PACC0000  WRITE     LOADEDFL,SEQ;XOPSITE,XOPCLIN,XOPDATE,XOPSTRT,XOPSLOT:
                                 XOPTIME,XOPCLTYP,XOPURNO,XOPVISIT,XOPBKNO:
                                 XOPBKDTE,XOPSRCR,XOPCOMP,XOPPRTY,XOPUDF1:
                                 XOPUDF2,XOPUDF3,XOPUDF4,XOPUNIT,XOPTRAN:
                                 XOPRFGP,XOPRFPC,XOPUDF5,XOPUDF6,XOPUDF7:
                                 XOPUDF8,XOPDTREF,XOPDGCD1,XOPDGCD2,XOPDGCD3:
                                 XOPDGCD4,XOPDGCD5,XOPCMPLT,XOPCMNT1,XOPCMNT2:
                                 XOPCMNT3,XOPFLDR,XOPACCN,XOPACCDT,XOPACCDD:
                                 XOPACCWR,XOPACCDI,XOPACCPO,XOPACCAI,XOPACCEM
.
          ADD       ONE,TOTALLOD
.
PACC9999  RETURN
.
.**************************************************************************
.*                               WRTBOK00             Called by: PROC0000 *
.*                                                                        *
.**************************************************************************
WRTBOK00  CALL      CLOUTBOA                     * clear bok variables
          CALL      CLOUTBOB
          MOVE      SP70,OTBBCIND
.
          MATCH     SP70,NEWSLOTK
          GOTO      WRTBOK91 IF EQUAL
.
          MOVE      URNUMBER,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,WRTBOK89
          BRANCH    PCEASE,WRTBOK90
          CALL      RDPMPX21
          BRANCH    OVRCD,WRTBOK89
.
          MOVE      NEWSLOTK,KEY28
          CALL      RDBOKA1
          BRANCH    OVRCD,WRTBOK92
.
          MOVE      NEWSLOTK,KEY28
          CALL      RLOTBOA1
          BRANCH    OVRCD,WRTBOK92,WRTBOK93
          COMPARE   NINE,OBASTAT             * Slot Reserved
          GOTO      WRTBOK30 IF EQUAL
          COMPARE   ZERO,OBASTAT             * Make sure record isn't booked
          GOTO      WRTBOK94 IF NOT EQUAL    * Record isn't vacant !!
.
WRTBOK30  MOVE      ZERO,OBASTAT             * Make sure record isn't booked
          CALL      UPBOKA1
.
          MOVE      OBAVISIT,OLDVTYP
          MOVE      XOPVISIT,NEWVTYP
.
          CALL      REAP0000
          BRANCH    EXIT,WRTBOK99
.
          MOVE      NEWSLOTK,KEY28
          CALL      RDBOKA1
.
          CALL      MOVBB000
.
          MOVE      OBACOMP,KEY3
          CALL      LCLM0000                 * get default claim details
.
          CALL      UPBOKA1
          MATCH     OLDVTYP,NEWVTYP
          IF        !@EQUAL
            PACK      SESSKEYS,OBASITE,OBACLIN,OBADATE,OBASTRT,SP70
            MOVE      OLDVTYP,OBAVISIT
            CALL      SUBBOK00
            MOVE      NEWVTYP,OBAVISIT
            CALL      ADDBOK00
            CALL      ADJNEW00
            CALL      ADJOLD00
          ENDIF                             * recalculate clinic counts
.
WRTBOK40  MOVE      OBAOUTNO,KEY8
          CALL      RDDIAG1
          MOVE      OVRCD,DIAGOVR
          IF        OVRCD=1
            MOVE      OBAOUTNO,OPDIOUTN
            MOVE      SP70,OPDICODE
            MOVE      SP70,OPDIDESC
.                                           *** HPS Code Starts Here ***
            MOVE      SP70,OPDICOD2
            MOVE      SP70,OPDIDES2
            MOVE      SP70,OPDICOD3
            MOVE      SP70,OPDIDES3
            MOVE      SP70,OPDICOD4
            MOVE      SP70,OPDIDES4
            MOVE      SP70,OPDICOD5
            MOVE      SP70,OPDIDES5
.                                           *** HPS Code Ends Here ***
            MOVE      SP70,OPDISPAR
          ENDIF
.
          MATCH     SP70,XOPDTREF
          IF        !@EQUAL
            MOVE      OBAOUTNO,KEY8       * I-48256
            CALL      RDOTBOC1
            IF        OVRCD=0
               MOVE      XOPDTREF,OTBCRDAT
               CALL      UPOTBOC1
            ELSE
               MOVE      XOPDTREF,OTBCRDAT
               MOVE      SP70,OTBCSPAR
               CALL      WROTBOC1
            ENDIF
          ENDIF
.
          MOVE      OBAOUTNO,KEY8
          CALL      RDBOKB1
          IF        OVRCD=0
            CALL      MOVBB000
            CALL      UPBOKB1
          ELSE
            CALL      MOVBB000
            CALL      WRBOKB1
            CALL      WRCPAT00           * write to current patient file
            CALL      PRAD0000           * write to person responsible file
          ENDIF
.
          MOVE      OBAOUTNO,KEY8
          IF        DIAGOVR=0
            CALL      UPDIAG1
          ELSE
            CALL      WRDIAG1
          ENDIF
.
          CALL      OUTCOM00                *wrtbok
.
          CALL      UPCLMD00                * Write ACC Details
.
          PACK      KEY8,URNUMBER,SP70
          CALL      RDPMPX21
          BRANCH    OVRCD,WRTBOK99
.
          MATCH     "1",PMPXINTR
          IF        @EQUAL
            PACK      CASEKEYS,OBASITE,OBACLIN,OBADATE,OBASTRT,DOBASLOT,SP70
            CALL      INTUPD00                 * Interpreter Table Processing
          ENDIF
.
          MOVE      NEWSLOTK,SESSKEYS          * Read session details to get
          CALL      SESDET00                   * hospital code
          IF        EXIT=1
            MOVE      SP70,OTHEHOSP
          ENDIF
.
          PACK      KEY8,ADMISSNO,SP70
          CALL      RDPMVX11
          IF        OVRCD=0
            CALL      MVVXTF00
            MOVE      SP70,PMVXWEBU
            PACK      PMVXLUPD,CCC,CYY,CMM,CDD
            REP       " 0",PMVXLUPD
            CLOCK     TIME,PMVXLUPT
            MOVE      PPOST,PMVXPOST
            MOVE      OTHEHOSP,PMVXMHOS
            CALL      GTASGC00                 * get SLA/ASGC code
            CALL      UPPMVX11
          ELSE
            CALL      CLPMSVX1
            CALL      MVVXTF00
            MOVE      ADMISSNO,PMVXVISN
            MOVE      SP70,PMVXWEBC
            MOVE      PPOST,PMVXPOST
            MOVE      OTHEHOSP,PMVXMHOS
            MOVE      SP70,PMVXPOEC 
            MOVE      SP70,PMVXPGID 
            MOVE      SP70,PMVXPILC
            CALL      GTASGC00                 * get SLA/ASGC code
            CALL      WRPMVX11
          ENDIF
.
          PACK      KEY8,ADMISSNO,SP70
          CALL      RDOTXSC1
          IF        OVRCD=1
            CALL      CLOUTXSC         * Clear File fileds
          ENDIF
.
WRTBOK80  MOVE      ZERO,EXIT
          GOTO      WRTBOK99
.
WRTBOK89  MOVE      "Error: Invalid Patient U/R",XERROR
          CALL      PERR0000                * Print Error
          GOTO      WRTBOK99
.
WRTBOK90  MOVE      "Error: Patient is Deceased",XERROR
          CALL      PERR0000                * Print Error
          GOTO      WRTBOK99
.
WRTBOK91  MOVE      "Error: Appointment Details Must be Specified",XERROR
          CALL      PERR0000                * Print Error
          GOTO      WRTBOK99
.
WRTBOK92  MOVE      "Error: Appointment Record Missing",XERROR
          CALL      PERR0000                * Print Error
          GOTO      WRTBOK99
.
WRTBOK93  MOVE      "Error: Appointment Record Locked",XERROR
          CALL      PERR0000                * Print Error
          GOTO      WRTBOK99
.
WRTBOK94  MOVE      "Error: Appointment Time Has Been Taken. ",XERROR
          CALL      PERR0000                * Print Error
          GOTO      WRTBOK99
.
WRTBOK95  MOVE      "Patient already has a booking in this Clinic/Type",XERROR
          CALL      PERR0000                * Print Error
          GOTO      WRTBOK99
.
WRTBOK96  MOVE      "Patient already booked to the Clinic at same time",XERROR
          CALL      PERR0000                * Print Error
          GOTO      WRTBOK99
.
WRTBOK97  MOVE      "Invalid referral criteria for this booking.",XERROR
          CALL      PERR0000                * Print Error
          GOTO      WRTBOK99
.
WRTBOK99  CALL      UUOTBOA1
          RETURN
.
.------------------------------------------------------------
. Get SLA/ASGC code from ibapostf
.------------------------------------------------------------
GTASGC00  MOVE      PPOST,D8
          SQUEEZE   D8
          MOVE      D8,F4
          MOVE      F4,D4
          REP       " 0",D4
          PACK      PPOST,D4,SP10
          PACK      KEY56,PPOST,PSUBRB,SP10,PADD4,SP70                *C-240184
          CALL      RDIBPOS1
          IF        OVRCD=0
            MOVE      IBPOASGC,PMVXASGC
          ELSE                                                        *I-240184
            MOVE      SP10,PMVXASGC
            PACK      KEY56,PPOST,PSUBRB,SP70
            CALL      RSIBPOS1
            CALL      RKIBPOS1
            BRANCH    OVRCD,GTASGC99
            MATCH     PPOST,IBPOPCOD
            IF        @EQUAL
              MATCH     PSUBRB,IBPOSUBR
              IF        @EQUAL
                MOVE      IBPOASGC,PMVXASGC
              ENDIF
            ENDIF
          ENDIF                                                 * end *I-240184
.
GTASGC99  RETURN
.
.*******************************************************************************
.                    Get Session Details for SESSKEYS
.*******************************************************************************
SESDET00  PACK      KEY25,SESSKEYS,SP70
          CALL      RDSESA1
          BRANCH    OVRCD,SESDET90
.
          PACK      KEY28,OSESITE,OSECLIN,OSEDAY,OSESTRT,OSESSHNO,OSESSDAT,SP70
          CALL      RDOTHED1
          BRANCH    OVRCD,SESDET90
.
          PACK      KEY18,OSESITE,OSECLIN,OSEDAY,OSESTRT,SP70
          CALL      RDMASA1
          BRANCH    OVRCD,SESDET90
.
          PACK      KEY28,OSESITE,OSECLIN,OSEDAY,OSESTRT,OSESSHNO,OSESSDAT,SP70
          CALL      RDOTHED1
          BRANCH    OVRCD,SESDET90
.
          PACK      KEY12,OTHESITE,OTHECTYP,SP70
          CALL      RDCTYA1
          BRANCH    OVRCD,SESDET90
.
          PACK      KEY20,OSESITE,OSECLIN,Z70
          CALL      RDSCLIA1          * Read Clinic Record
SESDET10  CALL      RDPCLIA1          * Read Clinic Record
          BRANCH    OVRCD,SESDET90
.
          MATCH     OCASITE,OSESITE
          GOTO      SESDET90 IF NOT EQUAL
.
          MATCH     OCACLIN,OSECLIN
          GOTO      SESDET90 IF NOT EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      SESDET99
.
SESDET90  MOVE      ONE,EXIT
.
SESDET99  RETURN
.
.*******************************************************************************
. Update Interpreter Table
. Input  - OBAURNO
.        - OBAOUTNO
.        - CASEKEYS - for New Booking
.*******************************************************************************
INTUPD00  PACK      KEY8,OBAURNO,SP70
          CALL      RDPMPX21
          BRANCH    OVRCD,INTUPD99
.
          MATCH     "1",PMPXINTR
          GOTO      INTUPD99 IF NOT EQUAL
.
          CALL      CLRINT00
          MOVE      ZERO,DATACHNG                                      *I-40489
.
          PACK      KEY8,OBAOUTNO,SP70
          CALL      RDVSINT1
....      IF        OVRCD=0
....        MOVE      "B",INTAUDTY
....        CALL      INTAUD00
....      ENDIF
.******************************************** Start of Addition        *I-40489
          BRANCH    OVRCD,INTUPD50          * new record - DATACHNG=0
.
          MOVE      ONE,DATACHNG
.                                           * has data changed?
          MATCH     OBADATE,VSINDATE
          GOTO      INTUPD50 IF NOT EQUAL
          MATCH     OBATIME,VSINTIME
          GOTO      INTUPD50 IF NOT EQUAL
          MATCH     CASEKEYS,VSINSUBK
          GOTO      INTUPD50 IF NOT EQUAL
.
          MOVE      ZERO,DATACHNG           * no data changed - end it all
          GOTO      INTUPD99
.
INTUPD50  IF        DATACHNG = 1
            MOVE      "B",INTAUDTY          * "before change"
            CALL      INTAUD00
          ENDIF
.
.********************************************   End of Addition        *I-40489
.
          MOVE      OBAOUTNO,VSINVISN
          MOVE      OBADATE,VSINDATE
          PACK      VSINTIME,OBATIME,KSECONDS
          MOVE      "2",VSINTYPE
          MOVE      CASEKEYS,VSINSUBK
          MOVE      SP70,VSINWUID
.
....      PACK      KEY8,OBAOUTNO,SP70                                  D-40489
....      CALL      RAVSINT1                                            D-40489
....      IF        OVRCD=0                                             D-40489
.                                           * test data change flag    *I-40489
          IF        DATACHNG = 1
            MOVE      ZERO,VSINNOTF                                    *I-40489
            CALL      UPVSINT1
            MOVE      "C",INTAUDTY
            CALL      INTAUD00
          ELSE
            MOVE      ZERO,VSINNOTF
            MOVE      SP70,VSINUDC1
            MOVE      SP70,VSINUDC2
            MOVE      SP70,VSINUDC3
            MOVE      SP70,VSINUDC4
            MOVE      SP70,VSININTP
            MOVE      SP70,VSINCHON
            MOVE      SP70,VSINSPAR
            MOVE      ZERO,VSIANOTF
            MOVE      ZERO,VSIACONF
            CALL      WRVSINT1
            MOVE      "A",INTAUDTY
            CALL      INTAUD00
          ENDIF
          MOVE      ZERO,EXIT
.
INTUPD99  RETURN
.
.*******************************************************************************
.                            Update Booking Comments
.*******************************************************************************
OUTCOM00  MATCH     SP70,XOPCMNT1
          GOTO      OUTCOM99 IF EQUAL
.
          MOVE      OBAOUTNO,KEY8
          CALL      CKVSCM00
.
          IF        COMTFLAG = ZERO
            CALL      NEWCMN00
          ELSE
            CALL      UPDCMN00
          ENDIF
.
OUTCOM99  RETURN
.
.----------------------------------------------------------------------
. check if visit comment exists
.----------------------------------------------------------------------
CKVSCM00  MOVE      ZERO,COMTFLAG
          MOVE      ZERO,MAXNOTEN
          PACK      KEY17,KEY8,VSCMTTYP,Z70      * Key for start of comment
          CALL      RSVSMDT1
CKVSCM20  CALL      RPVSMDT1
          BRANCH    OVRCD,CKVSCM99
.
          MATCH     KEY8,VSMDVISN
          GOTO      CKVSCM99 IF NOT EQUAL
.
          MATCH     VSCMTTYP,VSMDTYPE
          GOTO      CKVSCM99 IF NOT EQUAL
.
          MATCH     "1",VSMDDELT          * is comment deleted
          GOTO      CKVSCM20 IF EQUAL
.
          MOVE      ONE,COMTFLAG
          MOVE      VSMDNOTE,MAXNOTEN
.
CKVSCM99  RETURN
.
.----------------------------------------------------------------------
. Insert visit comment header and text
.----------------------------------------------------------------------
NEWCMN00  MOVE      OBAOUTNO,VSMDVISN        * set admission number
          MOVE      VSCMTTYP,VSMDTYPE
          ADD       ONE,MAXNOTEN
          MOVE      MAXNOTEN,VSMDNOTE
.
          CALL      IBACLOCK
          PACK      CURRDATX,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATX
          CLOCK     TIME,CTIMEIS              * Set created timestamp

          MOVE      CURRDATX,VSMDDATE         * Visit date
          MOVE      CTIMEIS,VSMDTIME          * Visit time
          MOVE      SP70,VSMDUSER
          MOVE      SP70,VSMDOCCG
.
          MOVE      ZERO,VSMDDELT
          MOVE      SP70,VSMDDDAT
          MOVE      SP70,VSMDDTIM
          MOVE      SP70,VSMDDUSE
          MOVE      SP70,VSMDDOCC
          MOVE      SP70,VSMDSPAR
.
          CALL      WRVSMDT1
.
. Insert visit comment text
.
          MOVE      VSMDVISN,VSMTVISN       * set admission number
          MOVE      VSMDTYPE,VSMTTYPE
          MOVE      VSMDNOTE,VSMTNOTE
          MOVE      SP70,VSMTSPAR
.
          MOVE      ONE,F3 
          MOVE      F3,VSMTUNIQ
          MOVE      XOPCMNT1,VSMTCMNT 
          PACK      KEY20,VSMTVISN,VSMTTYPE,VSMTNOTE,VSMTUNIQ,SP70
          CALL      WRVSMTX1
.
          MATCH     SP70,XOPCMNT2
          GOTO      NEWCMN99 IF EQUAL
.
          ADD       ONE,F3
          MOVE      F3,VSMTUNIQ
          MOVE      XOPCMNT2,VSMTCMNT 
          PACK      KEY20,VSMTVISN,VSMTTYPE,VSMTNOTE,VSMTUNIQ,SP70
          CALL      WRVSMTX1
.
          MATCH     SP70,XOPCMNT3
          GOTO      NEWCMN99 IF EQUAL
.
          ADD       ONE,F3
          MOVE      F3,VSMTUNIQ
          MOVE      XOPCMNT3,VSMTCMNT 
          PACK      KEY20,VSMTVISN,VSMTTYPE,VSMTNOTE,VSMTUNIQ,SP70
          CALL      WRVSMTX1
.
NEWCMN99  RETURN
.
.----------------------------------------------------------------------
. Update visit comment text
.----------------------------------------------------------------------
UPDCMN00  MOVE      OBAOUTNO,KEY8
          MOVE      ONE,F3 
          MOVE      F3,VSMTUNIQ
          PACK      KEY20,KEY8,VSCMTTYP,MAXNOTEN,VSMTUNIQ,SP70
          CALL      RDVSMTX1
          IF        OVRCD = 1   
            MOVE      XOPCMNT1,VSMTCMNT
            CALL      WRVSMTX1
          ENDIF    
.
          MATCH     SP70,XOPCMNT2
          GOTO      UPDCMN99 IF EQUAL 
.
          ADD       ONE,F3
          MOVE      F3,VSMTUNIQ
          PACK      KEY20,KEY8,VSCMTTYP,MAXNOTEN,VSMTUNIQ,SP70
          CALL      RDVSMTX1
          IF        OVRCD = 1   
            MOVE      XOPCMNT2,VSMTCMNT
            CALL      WRVSMTX1
          ENDIF    
.
          MATCH     SP70,XOPCMNT3
          GOTO      UPDCMN99 IF EQUAL 
.
          ADD       ONE,F3
          MOVE      F3,VSMTUNIQ
          PACK      KEY20,KEY8,VSCMTTYP,MAXNOTEN,VSMTUNIQ,SP70
          CALL      RDVSMTX1
          IF        OVRCD = 1   
            MOVE      XOPCMNT3,VSMTCMNT
            CALL      WRVSMTX1
          ENDIF    
.
UPDCMN99  RETURN
.
.---------------------------------------------------
. Adjust new slot count
.---------------------------------------------------
ADJNEW00  IF        NEWSLOTS<=0
            GOTO      ADJNEW99
          ENDIF
          MOVE      ZERO,LOCKCNT
.
ADJNEW10  PACK      KEY25,NEWSLOTK,SP70      * Key for selected booking
          CALL      RLOTSES1
          BRANCH    OVRCD,ADJNEW99,ADJNEW90
          SUB       NEWSLOTS,OSESLOTB
          CALL      UPSESA1
          CALL      UUOTSES1
          GOTO      ADJNEW99
.
ADJNEW90  DISPLAY   *W
          ADD       ONE,LOCKCNT
          IF        LOCKCNT<10
            GOTO      ADJNEW10
          ENDIF
ADJNEW99  RETURN
.
.---------------------------------------------------
. Adjust old slot count
.---------------------------------------------------
ADJOLD00  IF        OLDSLOTS<=0
            GOTO      ADJOLD99
          ENDIF
          MOVE      ZERO,LOCKCNT
.
ADJOLD10  PACK      KEY25,NEWSLOTK,SP70      * Key for selected booking
          CALL      RLOTSES1
          BRANCH    OVRCD,ADJOLD99,ADJOLD90
          ADD       OLDSLOTS,OSESLOTB
          CALL      UPSESA1
          CALL      UUOTSES1
          GOTO      ADJOLD99
.
ADJOLD90  DISPLAY   *W
          ADD       ONE,LOCKCNT
          IF        LOCKCNT<10
            GOTO      ADJOLD10
          ENDIF
ADJOLD99  RETURN
.
.---------------------------------------------------
. Reduce Booking Count on Session
.---------------------------------------------------
SUBBOK00  MOVE      ZERO,LOCKCNT
          MOVE      "CV",KEY2
          PACK      KEY5,KEY2,OBAVISIT
          CALL      RDCODE1
          BRANCH    OVRCD,SUBBOK98
          IF        TCFORM6=0
            MOVE      ONE,TCFORM6
          ENDIF
.
SUBBOK10  PACK      KEY25,SESSKEYS,SP70
          CALL      RLOTSES1
          BRANCH    OVRCD,SUBBOK98,SUBBOK90
          SUB       TCFORM6,OSESLOTB
          MATCH     ANSN,TCINDC1
          IF        @EQUAL
            SUB       ONE,OSEBNEW
          ENDIF
          MATCH     ANSR,TCINDC1
          IF        @EQUAL
            SUB       ONE,OSEBRV
          ENDIF
          MATCH     ANSS,TCINDC1
          IF        @EQUAL
            SUB       ONE,OSEBSPC
          ENDIF
          CALL      RDSLT000
          CALL      SUBTIM00
          CALL      UPSESA1
          CALL      UUOTSES1
          MOVE      ZERO,EXIT
          GOTO      SUBBOK99
SUBBOK90  DISPLAY   *W
          ADD       ONE,LOCKCNT
          IF        LOCKCNT<5
            GOTO      SUBBOK10
          ENDIF
SUBBOK98  MOVE      ONE,EXIT
SUBBOK99  RETURN
.
.------------------------------------------------------------
. Get default claim details from previous visit
.------------------------------------------------------------
LCLM0000  MATCH     SP3,KEY3
          GOTO      LCLM7000 IF EQUAL       * no claim code
.
          MATCH     ANSN,PTCNLCLM
          GOTO      LCLM9999 IF EQUAL
.
.         Set the claim type indicator if this is a claim
.
          PACK      KEY5 WITH ANSC,ANSL,KEY3
          CALL      RDCODE1
          BRANCH    OVRCD OF LCLM7000
.
.         Check the indicators for a W, M, or V (these are the claims)
.
          MOVE      ZERO,FORM2
LCLM3000  ADD       ONE,FORM2
          COMPARE   SIX,FORM2
          GOTO      LCLM7000 IF NOT LESS
.
          LOAD      ANS USING FORM2 OF TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5
          CMATCH    "W",ANS
          GOTO      LCLM4000 IF EQUAL
          CMATCH    "M",ANS
          GOTO      LCLM5000 IF EQUAL
          GOTO      LCLM3000
.
.         We have a workers comp. claim, get the previous workers comp details
.
LCLM4000  OPEN     PATWC1A1,"patwc1af"
          OPEN     PATWC1A2,"patwc1af"
          OPEN     PMSWX1A1,"pmswx1af"
          PACK     KEY16,URNUMBER,Z70
          CALL     RDSWCOM2
LCLM4100  CALL     RDPWCOM2
          BRANCH   OVRCD,LCLM7000
          MATCH    PTWCURNO,URNUMBER
          GOTO     LCLM7000 IF NOT EQUAL
.
          MOVE     WCADMNO,KEY8
          CALL     RDVISA1
          BRANCH   OVRCD,LCLM4100
.
          MATCH    ANSI,PTCNLCLM           * Check inpatient visit only
          GOTO     LCLM4200 IF NOT EQUAL   * Any visit
.
          COMPARE  THREE,PVITYPE
          GOTO     LCLM4100 IF NOT EQUAL   * get next visit
.
LCLM4200  MOVE     WCADMNO,KEY8
          CALL     RDPMWX11           * read ext.workers comp file(prev adm)
.
          MOVE     OBAOUTNO,WCADMNO   * current outpatient number
          MOVE     WCADMNO,KEY8
          CALL     RDAWCOM1
          IF       OVRCD=1
            MOVE     OBADATE,PTWCVDAT
            MOVE     OBAURNO,PTWCURNO
            CALL     WRWCOM1          * write workes comp file
          ENDIF
.
          MOVE     OBAOUTNO,PMWXVISN
          CALL     RAPMWX11
          IF       OVRCD=1
            CALL     WRPMWX11         * write extension workers comp file
          ENDIF
.
          GOTO     LCLM9999
.
.         We have a motors accident claim, get the previous workers comp details
.
LCLM5000  OPEN     PATWMAB1,"patwmabf"
          OPEN     PATWMAB2,"patwmabf"
          PACK     KEY16,URNUMBER,Z70
          CALL     RDSWMAB2           * read claim details for previous visit
LCLM5100  CALL     RDPWMAB2
          BRANCH   OVRCD,LCLM7000
          MATCH    PTWMURNO,URNUMBER
          GOTO     LCLM7000 IF NOT EQUAL
          MOVE     MADMNO,KEY8
          CALL     RDVISA1
          BRANCH   OVRCD,LCLM5100
.
          MATCH    ANSI,PTCNLCLM           * Check inpatient visit only
          GOTO     LCLM5200 IF NOT EQUAL   * Any visit
.
          COMPARE  THREE,PVITYPE
          GOTO     LCLM5100 IF NOT EQUAL   * get next visit
.
LCLM5200  MOVE     OBAOUTNO,MADMNO    * current outpatient number
          MOVE     OBAOUTNO,KEY8
          CALL     RDAWMAB1
          IF       OVRCD=1
            MOVE     OBADATE,PTWMVDAT
            MOVE     OBAURNO,PTWMURNO
            CALL     WRWMAB1          * write workes comp file
          ENDIF
          GOTO     LCLM9999
.
LCLM7000  CALL      CLRCLM00
LCLM9999  RETURN
.
.                                           *** HPS Code Ends Here ***
.*******************************************************************************
.            REAP0000: Generate and post a new outpatient number
.*******************************************************************************
REAP0000  PACK      KEY8,XOPBKNO             * Key for visit file
          CALL      RDVISA1                  * Check if visit record exists
          COMPARE   ZERO,OVRCD               * Does this record exist ?
          GOTO      REAP9040 IF EQUAL        * Yes. Try again
.
          PACK      KEY8,XOPBKNO             * Key for pre-attendance file
          CALL      RDPREA1                  * Check if record already exists
          COMPARE   ZERO,OVRCD               * Does this record exist ?
          GOTO      REAP9040 IF EQUAL        * Yes. Try again
.
          PACK      KEY8,XOPBKNO             * Key for bokb file
          CALL      RDABOKB1
          COMPARE   ZERO,OVRCD               * Does this record exist ?
          GOTO      REAP9040 IF EQUAL        * Yes. Try again
.
.         We have a valid outpatient number. Post the Boka data
.
REAP0700  PACK      KEY28,NEWSLOTK,SP70      * Key for selected booking
          CALL      RDBOKA1                  * Read in the booking record
          BRANCH    OVRCD,REAP9020
.
.         Update this record with the booking data
.
          COMPARE   ZERO,OBASTAT             * Make sure record isn't booked
          GOTO      REAP1000 IF EQUAL
          COMPARE   NINE,OBASTAT             * Make sure record isn't booked
          GOTO      REAP9010 IF NOT EQUAL    * Record isn't vacant !!
.
REAP1000  MOVE      XOPBKNO,OBAOUTNO         * Outpatient number
          MOVE      URNUMBER,OBAURNO         * U/R Number
          MOVE      ONE,OBASTAT              * Status = Booked
          MOVE      ZERO,OBAPULL             * Clear pulling list flag
          CLOCK     TIME,CTIMEIS             * Get the current time
          PACK      OBABKDTE,CCC,CYY,CMM,CDD,CTIMEIS
          REP       " 0",OBABKDTE            * Zero-fill booking date/time
          CALL      UPBOKA1
.
          PACK      SESSKEYS,OBASITE,OBACLIN,OBADATE,OBASTRT,SP70
          CALL      ADDBOK00                 * Increment Booking Count
.
          CALL      PPRE0000                 * Updating the pre-attend file data
.
          PACK      KEY8,OBAOUTNO,SP70       * Key for bokb file
          CALL      RDABOKB1                 * Write the bokb record
          IF        OVRCD=1
            MOVE      OBACTYP,OBBCTYP        * Store Clinic Type (SRF 103335)
            MOVE      SP10,OTBBADCS
            MOVE      SP10,OTBBCITM
            MOVE      SP10,OTBBASTM
            MOVE      SP10,OTBBDPTM
            MOVE      SP10,OTBBOUTC
            MOVE      SP70,OTBBCIND          * Clear before write
            CALL      WRBOKB1
            CALL      WRCPAT00           * write to current patient file
            CALL      PRAD0000           * write to person responsible file
          ENDIF
.
          CALL      WRAP0000
.
          MATCH     OLDVTYP,NEWVTYP
          GOTO      REAP8900 IF EQUAL
.
          CALL      EXTSLT00
          GOTO      REAP8900
.
.         variables such as OBAOUTNO and OBAURNO contain the correct data
.
REAP8900  PACK      KEY28,NEWSLOTK,SP70      * Key for selected booking
          CALL      RDBOKA1                  * Read in the booking record
          MOVE      ZERO,EXIT
          GOTO      REAP9999                * Finished posting data
.
.         Booking record has the wrong status
.
REAP9010  MOVE      "Booking Record has wrong status.",XERROR
          CALL      PERR0000                * Print Error
          GOTO      REAP9999                * Finished posting data
.
REAP9020  MOVE      "New Booking Record not Available.",XERROR
          CALL      PERR0000                * Print Error
          GOTO      REAP9999                * Finished posting data
.
REAP9030  MOVE      "Invalid Booking Status for Referral Letter.",XERROR
          CALL      PERR0000                * Print Error
          GOTO      REAP9999                * Finished posting data
.
REAP9040  MOVE      "Bookin/Event Number Already in use(patvisaf).",XERROR
          CALL      PERR0000                * Print Error
          GOTO      REAP9999                * Finished posting data
.
REAP9999  RETURN
.
.------------------------------------------------------------
. Extend Slots
.  Input - NEWVTYP New Visit Type
.          OLDVTYP old Visit Type
.          NEWSLOTK - Slot Key
.------------------------------------------------------------
EXTSLT00  MOVE      NEWVTYP,CODE
          CALL      GETSCT00   * Get Slot Count
          MOVE      CNTSLOTS,NEWSLOTS
.
          MOVE      OLDVTYP,CODE
          CALL      GETSCT00   * Get Slot Count
          MOVE      CNTSLOTS,OLDSLOTS
.
          PACK      KEY28,NEWSLOTK,SP70      * Key for selected booking
          CALL      RDBOKA1                  * Read in the booking record
          MOVE      NEWVTYP,OBAVISIT         * Update to New Visit Type
          CALL      UPBOKA1
          SUB       ONE,OLDSLOTS
          SUB       ONE,NEWSLOTS
.
          MOVE      OBASLOT,PARNSLOT         * Set Parent Slot
.
EXTSLT10  CALL      RDKBOKA1                 * Check the next record
          BRANCH    OVRCD,EXTSLT90           * Finished
          PACK      KEY25,OBASITE,OBACLIN,OBADATE,OBASTRT
          MATCH     KEY25,NEWSLOTK         * Correct session ?
          GOTO      EXTSLT90 IF NOT EQUAL    * No. Finished
          COMPARE   ZERO,NEWSLOTS
          GOTO      EXTSLT30 IF EQUAL
.
          MATCH     OLDVTYP,OBAVISIT
          IF        !@EQUAL
            MOVE      OBAVISIT,CODE          * Check Slot is not Extended
            CALL      GETSCT00               * Get Slot Count
            COMPARE   ONE,CNTSLOTS           * Exit if This Slot Extended
            GOTO      EXTSLT90 IF NOT EQUAL
          ENDIF
.
          MATCH     ANSU,TCINDC6
          GOTO      EXTSLT90 IF EQUAL        * Ignore unavailable slots
.
          MATCH     ANSZ,TCINDC2
          GOTO      EXTSLT90 IF EQUAL        * Ignore over bookings slots
.
          MATCH     ZEROVISN,OBAOUTNO        * Outpatient number
          GOTO      EXTSLT90 IF NOT EQUAL    * Exit if Slot has Booking
.
          SUB       ONE,OLDSLOTS
          SUB       ONE,NEWSLOTS
          MOVE      THREE,OBASTAT            * Update as Extended Slot
          MOVE      NEWVTYP,OBAVISIT
          MOVE      PARNSLOT,OBAEXSL
          CALL      UPBOKA1
          GOTO      EXTSLT10
.
EXTSLT20  CALL      RDKBOKA1     * Read Next Slot
          BRANCH    OVRCD,EXTSLT90
          PACK      KEY25,OBASITE,OBACLIN,OBADATE,OBASTRT
          MATCH     KEY25,NEWSLOTK          * Correct session ?
          GOTO      EXTSLT90 IF NOT EQUAL   * No. Finished
.
EXTSLT30  COMPARE   PARNSLOT,OBAEXSL
          GOTO      EXTSLT90 IF NOT EQUAL
.
          SUB       ONE,OLDSLOTS
          MOVE      ZEROVISN,OBAOUTNO       * Outpatient number
          MOVE      ZEROUR,OBAURNO          * Outpatient number
          MOVE      "R  ",OBAVISIT
          MOVE      ZERO,OBAEXSL            * Update as Not Extended
          MOVE      ZERO,OBASTAT            * Update as Not Extended
          CALL      UPBOKA1
          GOTO      EXTSLT20
.
EXTSLT90  PACK      KEY28,NEWSLOTK,SP70      * Key for selected booking
          CALL      RDBOKA1                  * Read in the booking record
          RETURN
.
.------------------------------------------------------------
. Get Slot Count from Code File
.------------------------------------------------------------
GETSCT00  MOVE      ONE,CNTSLOTS
          MOVE      "CV",CATEGORY
          PACK      KEY5,CATEGORY,CODE
          CALL      RDCODE1
          BRANCH    OVRCD,GETSCT99
          IF        TCFORM6=0
            MOVE      ONE,TCFORM6
          ENDIF
          MOVE      TCFORM6,CNTSLOTS
GETSCT99  RETURN
.
.******************************************************************************
.*          WRAP0000: write to the outrapaf file (Re-appointments File)       *
.******************************************************************************
WRAP0000  READ      CONTROLF,THIRTY1;*226,OTCNRAPA
          IF        OTCNRAPA=1
            MOVE      ADMISSNO,OTRPOBKN   * Old
            MOVE      OBAOUTNO,OTRPRBKN   * New
            PACK      OTRPDATE,CCC,CYY,CMM,CDD
            REP       " 0",OTRPDATE
            CLOCK     TIME,OTRPTIME
            MOVE      PASSCODE,OTRPLOGI
            MOVE      SP30,OTRPSPAR
            PACK      KEY16,OTRPOBKN,OTRPRBKN
            CALL      RDOTRAP1
            IF        OVRCD=1
              CALL      WROTRAP1
            ENDIF
          ENDIF
          RETURN
.
.*******************************************************************************
.  PRAD0000  :  Write the Person Responsible for Account Details for RCH
.*******************************************************************************
PRAD0000  MOVE      ZERO,F1
          MOVE      PMPXCONP,F1
          BRANCH    F1,PRAD1000,PRAD2000,PRAD3000,PRAD4000,PRAD5000
.
. patient is responsible for account
.
          MOVE    PSNAME,PACSNAME
          MOVE    PGNAME,ANS
          PACK    PACGNAME,ANS,DOT
          MOVE    PTITL,PACTITLE
          MOVE    ONE,PACFRMT
          CALL    PACNAME
.
          MOVE    OBAOUTNO,PKADMN
          MOVE    PACFNAME,PKNAME
          MOVE    PADD1,PKADD1
          MOVE    PADD2,PKADD2
          MOVE    PSUBRB,PKSUBR
          MOVE    PADD4,PKADD4
          MOVE    PPOST,PKPOST
          MOVE    PTELEP,PKTELEP
          MOVE    PTELEB,PKTELEB
          MOVE    "SELF      ",PKRELP
.
          GOTO    PRAD7000
.
. contact 1 is responsible for account
.
PRAD1000  MOVE    OBAOUTNO,PKADMN
          MOVE    PMPXMSNA,PACSNAME
          MOVE    PMPXMGNA,ANS
          PACK    PACGNAME,ANS,DOT
          MOVE    PMPXMTLE,PACTITLE
          MOVE    ONE,PACFRMT
          CALL    PACNAME
.
          MOVE    PACFNAME,PKNAME
          MOVE    PMPXMAD1,PKADD1
          MOVE    PMPXMAD2,PKADD2
          MOVE    PMPXMAD3,PKSUBR
          MOVE    PMPXMAD4,PKADD4
          MOVE    PMPXMPOS,PKPOST
          MOVE    PMPXMPNO,PKTELEP
          MOVE    PMPXMBNO,PKTELEB
          MOVE    PMPXMREL,PKRELP
.
          GOTO    PRAD7000
.
. contact 2 is responsible for account
.
PRAD2000  MOVE    OBAOUTNO,PKADMN
          MOVE    PMPXFSNA,PACSNAME
          MOVE    PMPXFGNA,ANS
          PACK    PACGNAME,ANS,DOT
          MOVE    PMPXFTLE,PACTITLE
          MOVE    ONE,PACFRMT
          CALL    PACNAME
.
          MOVE    PACFNAME,PKNAME
          MOVE    PMPXFAD1,PKADD1
          MOVE    PMPXFAD2,PKADD2
          MOVE    PMPXFAD3,PKSUBR
          MOVE    PMPXFAD4,PKADD4
          MOVE    PMPXFPOS,PKPOST
          MOVE    PMPXFPNO,PKTELEP
          MOVE    PMPXFBNO,PKTELEB
          MOVE    PMPXFREL,PKRELP
.
          GOTO    PRAD7000
.
. contact 3 is responsible for account
.
PRAD3000  MOVE    OBAOUTNO,PKADMN
          MOVE    PNKNAME,PKNAME
          MOVE    PNKADD1,PKADD1
          MOVE    PNKADD2,PKADD2
          MOVE    PNKSUBR,PKSUBR
          MOVE    PNKADD4,PKADD4
          MOVE    PNKPOST,PKPOST
          MOVE    PNKTELP,PKTELEP
          MOVE    PNKTELB,PKTELEB
          MOVE    PNKRELP,PKRELP
.
          GOTO    PRAD7000
.
. contact 4 is responsible for account
.
PRAD4000  MOVE    OBAOUTNO,PKADMN
          MOVE    PTMXECNM,PKNAME
          MOVE    PTMXECA1,PKADD1
          MOVE    PTMXECA2,PKADD2
          MOVE    PTMXECA3,PKSUBR
          MOVE    PTMXECA4,PKADD4
          MOVE    PTMXECPC,PKPOST
          MOVE    PTMXECPP,PKTELEP
          MOVE    PTMXECBP,PKTELEB
          MOVE    PTMXECRE,PKRELP
.
          GOTO    PRAD7000
.
. contact 5 is responsible for account
.
PRAD5000  MOVE    OBAOUTNO,PKADMN
          MOVE    PTMXNRNM,PKNAME
          MOVE    PTMXNRA1,PKADD1
          MOVE    PTMXNRA2,PKADD2
          MOVE    PTMXNRA3,PKSUBR
          MOVE    PTMXNRA4,PKADD4
          MOVE    PTMXNRPC,PKPOST
          MOVE    PTMXNRPP,PKTELEP
          MOVE    PTMXNRBP,PKTELEB
          MOVE    PTMXNRRE,PKRELP
.
          GOTO    PRAD7000
.
PRAD7000  PACK    KEY8,OBAOUTNO,SP10
          CALL    RDARESP1
          IF      OVRCD=1
            CALL    WRRESP1
          ELSE
            CALL    UPRESP1
          ENDIF
.
PRAD9500  MOVE      ZERO,EXIT
.
PRAD9999  RETURN
.
.------------------------------------------------------------
. Write to the current patient list
.------------------------------------------------------------
WRCPAT00  CALL      CALRAN00
.
          MATCH     STRTDATE,OBADATE        * before start date
          GOTO      WRCPAT99 IF LESS
.
          MATCH     OBADATE,LASTDATE        * after end date
          GOTO      WRCPAT99 IF LESS
.
          MOVE      DOBAOUTN,PMCUVISN
          MOVE      OBADATE,PMCUDATE
          MOVE      PTMASNAM,PMCUSURN
          MOVE      PMPXFNAM,PMCUGNAM
          MOVE      PMPXSNAM,PMCUGNM2
          MOVE      PURNO,PMCUURNO
          MOVE      "2",PMCUSYST
          MOVE      SP70,PMCULOCN
          MOVE      OBASITE,PMCUOSIT
          MOVE      OTHEHOSP,PMCUHOSP
          MOVE      SP70,PMCUTSTA
          MOVE      SP70,PMCUTLOC
.
          PACK      KEY8,PMCUVISN,SP70
          CALL      RAPMCUR1
          IF        OVRCD=1
            CALL      WRPMCUR1
          ENDIF
.
WRCPAT99  RETURN
+
.------------------------------------------------------------
. Calculate the date range for current patient records
.------------------------------------------------------------
CALRAN00  READ      CONTROLF,HUND01;*49,CWEBSDAY  * no of days for search
          MOVE      SP70,STRTDATE
          MOVE      SP70,LASTDATE
.
. ---  calculate starting date ---
.
          CALL      IBACLOCK
          CALL      DMYCON
          SUB       CWEBSDAY,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON
          PACK      STRTDATE,CC,YY,MM,DD
          REP       " 0",STRTDATE
.
. ---  calculate last date ---
.
          CALL      IBACLOCK
          CALL      DMYCON
          ADD       CWEBSDAY,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON
          PACK      LASTDATE,CC,YY,MM,DD
          REP       " 0",LASTDATE
.
CALRAN99  RETURN
.
.---------------------------------------------------
. Increment Booking Count on Session
.---------------------------------------------------
ADDBOK00  MOVE      ZERO,LOCKCNT
          MOVE      "CV",KEY2
          PACK      KEY5,KEY2,OBAVISIT
          CALL      RDCODE1
          BRANCH    OVRCD,ADDBOK98
          IF        TCFORM6=0
            MOVE      ONE,TCFORM6
          ENDIF
.
ADDBOK10  PACK      KEY25,SESSKEYS,SP70
          CALL      RLOTSES1
          BRANCH    OVRCD,ADDBOK98,ADDBOK90
          ADD       TCFORM6,OSESLOTB
          MATCH     ANSN,TCINDC1
          IF        @EQUAL
            ADD       ONE,OSEBNEW
          ENDIF
          MATCH     ANSR,TCINDC1
          IF        @EQUAL
            ADD       ONE,OSEBRV
          ENDIF
          MATCH     ANSS,TCINDC1
          IF        @EQUAL
            ADD       ONE,OSEBSPC
          ENDIF
          CALL      RDSLT000
          CALL      ADDTIM00
          CALL      UPSESA1
          CALL      UUOTSES1
          MOVE      ZERO,EXIT
          GOTO      ADDBOK99
ADDBOK90  DISPLAY   *W
          ADD       ONE,LOCKCNT
          IF        LOCKCNT<5
            GOTO      ADDBOK10
          ENDIF
ADDBOK98  MOVE      ONE,EXIT
ADDBOK99  RETURN
.
.**************************************************************************
.*  Clear outbokaf                                                        *
.**************************************************************************
CLOUTBOA  MOVE      SP70,OBASITE
          MOVE      SP70,OBACLIN
          MOVE      SP70,OBADATE
          MOVE      SP70,OBASTRT
          MOVE      SP70,DOBASLOT
          MOVE      SP70,OBATIME
          MOVE      SP70,OBACTYP
          MOVE      ZEROUR,OBAURNO
          MOVE      SP70,OBAVISIT
          MOVE      SP70,DOBAOUTN
          MOVE      SP70,DOBASTAT
          MOVE      ZERO,OBADAY
          MOVE      ZERO,OBAEXSL
          MOVE      SP70,OBAFINST
          MOVE      SP70,OBALOCK
          MOVE      ZERO,OBAPXRAY
          MOVE      SP70,OBABKDTE
          MOVE      ZERO,OBAPULL
          MOVE      SP70,OBASESST
          MOVE      SP70,OBADISCH
          MOVE      SP70,DOTBARES
          MOVE      SP70,OBASPARA
          MOVE      ZERO,OBASLOT
          MOVE      ZEROVISN,OBAOUTNO
          MOVE      ZERO,OBASTAT
          MOVE      ZERO,OTBARESV
          RETURN
.
.**************************************************************************
.*  Clear outbb1af                                                        *
.**************************************************************************
CLOUTBOB  MOVE      SP70,OBASRCR
          MOVE      SP70,OBACOMP
          MOVE      SP70,OBACLASS
          MOVE      SP70,OBAINS
          MOVE      SP70,OBAATTN
          MOVE      SP70,OBADOCT
          MOVE      SP70,OBACOMM
          MOVE      SP70,OTBBFUND
          MOVE      SP70,OTBBTBLE
          MOVE      SP70,DOTBBPVI
          MOVE      SP70,OTBBSPAR
          MOVE      SP70,OBADATE
          MOVE      SP70,OBACAT
          MOVE      SP70,OBAPRTY
          MOVE      SP70,OBARSCHD
          MOVE      SP70,OBBPORT
          MOVE      SP70,OBBBOOK
          MOVE      SP70,OBBCTYP
          MOVE      SP70,OBOUSR1
          MOVE      SP70,OBOUSR2
          MOVE      SP70,OBOUSR3
          MOVE      SP70,OBOUSR4
          MOVE      SP70,OBALADMN
          MOVE      SP70,OTBBDEPT
          MOVE      SP70,OTBBTRAN
          MOVE      SP70,OTBBLNGI
          MOVE      SP70,OTBBTMFS
          MOVE      SP70,OTBBTMBO
          MOVE      SP70,OTBBGPFA
          MOVE      SP70,OTBBECRA
          MOVE      SP70,OTBBRFGP
          MOVE      SP70,OTBBGPPC
          MOVE      SP70,OTBBADCS
          MOVE      SP70,OTBBCITM
          MOVE      SP70,OTBBASTM
          MOVE      SP70,OTBBDPTM
          MOVE      SP70,OTBBOUTC
          MOVE      SP70,OTBBSNDL
          MOVE      SP70,OTBBGPPT
          MOVE      SP70,OTBBDOFR
          MOVE      SP70,OTBBOPER
          MOVE      SP70,OBOUSR5
          MOVE      SP70,OBOUSR6
          MOVE      SP70,OBOUSR7
          MOVE      SP70,OBOUSR8
          MOVE      SP70,OTBBRANK
          MOVE      SP70,OTBBFLUP
          MOVE      SP70,OTBBSCAR
          MOVE      SP70,OTBBSPR1
.
          RETURN
.
.*******************************************************************************
. Update Interpreter Audit Table
.  Input - CASEKEYS  Key To Booking
.          INTAUDTY  Audit Type
.*******************************************************************************
INTAUD00  CALL      CLRIAU00
          PACK      VSIACDAT,CCC,CYY,CMM,CDD
          REP       " 0",VSIACDAT
          MOVE      CTIMEIS,VSIACTIM
          MOVE      INTAUDTY,VSIARTYP
.
          MOVE      VSINVISN,VSIAVISN
          MOVE      VSINDATE,VSIADATE
          MOVE      VSINTIME,VSIATIME
.....     MOVE      "2",VSIATYPE                                        D-40489
          MOVE      VSINTYPE,VSIATYPE                                  *I-40489
          MOVE      VSINSUBK,VSIASUBK
          MOVE      VSINWUID,VSIAWUID
          MOVE      PMPXLNG1,VSIALNG1
          MOVE      PMPXLNG2,VSIALNG2
          MOVE      PMPXLNG3,VSIALNG3
          MOVE      VSINNOTF,VSIANOTF
          MOVE      VSINCONF,VSIACONF  
.
          PACK      KEY25,VSIACDAT,VSIACTIM,VSIARTYP,VSIAVISN,SP70
          CALL      RAVSIAU1
          IF        OVRCD=0
            CALL      UPVSIAU1
          ELSE
            CALL      WRVSIAU1
          ENDIF
          MOVE      ZERO,EXIT
INTAUD99  RETURN
.*******************************************************************************
.                   Clear Variables for Interpreter Audit Table
.*******************************************************************************
CLRIAU00  MOVE      SP70,VSIACDAT
          MOVE      SP70,VSIACTIM
          MOVE      SP70,VSIARTYP
          MOVE      SP70,VSIAVISN
          MOVE      SP70,VSIADATE
          MOVE      SP70,VSIATIME
          MOVE      ZERO,VSIATYPE
          MOVE      SP70,VSIASUBK
          MOVE      SP70,VSIAWUID
          MOVE      SP70,VSIALNG1
          MOVE      SP70,VSIALNG2
          MOVE      SP70,VSIALNG3
          MOVE      ZERO,VSIANOTF
          MOVE      ZERO,VSIACONF
          RETURN
.------------------------------------------------------------
.     Clear Claim Details
.------------------------------------------------------------
CLRCLM00  MOVE      SP70,WCENAME
          MOVE      SP70,WCEADD1
          MOVE      SP70,WCEADD2
          MOVE      SP70,WCEADD3
          MOVE      SP70,WCEADD4
          MOVE      SP70,WCEPOST
          MOVE      SP70,WCETELE
          MOVE      SP70,WCACDAT
          MOVE      SP70,WCACCPT
          MOVE      SP70,WCICODE
          MOVE      ZERO,WCCLMNO
.
          MOVE      SP70,VCLMNO
.
          MOVE      ZERO,PTWMTACF
          MOVE      SP70,PTWMTYPE
          MOVE      SP70,MREFNO
          MOVE      SP70,MACDAT
          MOVE      SP70,MPOLIC
          MOVE      SP70,MCREGO
          MOVE      SP70,MOTHDET1
          MOVE      SP70,MOTHDET2
          MOVE      SP70,MOTHDET3
          MOVE      SP70,MMDTRANS
          MOVE      SP70,MENGAGED
          MOVE      SP70,MPINJURE
          MOVE      SP70,MMECHSEV
          MOVE      SP70,MREGNSEV
          MOVE      SP70,MSNAME
          MOVE      SP70,MSTELE
          MOVE      SP70,MACLOC
          MOVE      SP70,PMWXVISN           * Visit Number
          MOVE      SP70,PMWXALOC           * Accident Location
          MOVE      SP70,PMWXCINJ           * of Injury (Cat IK)
          MOVE      SP70,PMWXICOD           * Injury Code (Cat IJ)
          MOVE      SP70,PMWXCNAM           * Contact Name
          MOVE      SP70,PMWXCDTE           * Date Record Created (ccyymmdd)
          MOVE      SP70,PMWXCTIM           * Time Record Created (hh:mm:ss)
          MOVE      SP70,PMWXWEBC           * WEB User Id rec. creator(websecaf)
          MOVE      SP70,PMWXLUPD           * Last Update Date (ccyymmdd)
          MOVE      SP70,PMWXLUPT           * Last Update Time (hh:mm:ss)
          MOVE      SP70,PMWXWEBU           * WEB User Id rec. updater(websecaf)
          MOVE      SP70,PMWXSPAR           * Spare Variable
.
CLRCLM99  RETURN
.*******************************************************************************
.                   Clear Variables for Interpreter Table
.*******************************************************************************
CLRINT00  MOVE      SP70,VSINVISN
          MOVE      SP70,VSINDATE
          MOVE      SP70,VSINTIME
          MOVE      ZERO,VSINTYPE
          MOVE      SP70,VSINSUBK
          MOVE      SP70,VSINWUID
.
          MOVE      ZERO,VSINNOTF                                      *I-40489
          MOVE      ZERO,VSINCONF
          UNPACK    SP20,VSINUDC1,VSINUDC2,VSINUDC3,VSINUDC4           *I-40489
          UNPACK    SP70,VSININTP,VSINCHON,VSINSPAR                    *I-40489
          RETURN
.
.*******************************************************************************
.             PPRE0000 : Post the pre-attendance file data
.*******************************************************************************
PPRE0000  MOVE      OBAOUTNO,KEY8
          CALL      RDPREA1
          BRANCH    OVRCD,PPRE5000
.
          MOVE      OBACLIN,OPRCLIN
          MOVE      OBADATE,OPRDATE
          MOVE      OBASTRT,OPRSTRT
          MOVE      OBASLOT,OPRSLOT
          MOVE      OBATIME,OPRTIME
          MOVE      OBACTYP,OPRCTYP
          CALL      UPPREA1
          GOTO      PPRE9999
.
PPRE5000  MOVE      OBAOUTNO,OPROUTN
          MOVE      OBASITE,OPRSITE
          MOVE      OBACLIN,OPRCLIN
          MOVE      OBADATE,OPRDATE
          MOVE      OBASTRT,OPRSTRT
          MOVE      OBASLOT,OPRSLOT
          MOVE      OBATIME,OPRTIME
          MOVE      OBACTYP,OPRCTYP
          MOVE      SP20,OPRSPAR
          MOVE      OPROUTN,KEY8
          CALL      WRPREA1
.
PPRE9999  RETURN
.
.------------------------------------------------------------
. UPCLMD00 - Write Claim Details
.------------------------------------------------------------
UPCLMD00  MATCH     SP70,XOPACCN            * No ACC Number so exit
          GOTO      UPCLMD99 IF EQUAL
.
          MOVE      ADMISSNO,KEY8
          CALL      RDWCOM1
          IF        OVRCD=1
            CALL      UPCLV000
            MOVE      URNUMBER,PTWCURNO          
            MOVE      OBADATE,PTWCVDAT
.
            CALL      WRWCOM1
.
            MATCH     SP70,PMPXFLDR
            IF        !@EQUAL
              PACK      KEY8,URNUMBER,SP70
              CALL      RDPMPX21
              IF        OVRCD=0
                MOVE      XOPFLDR,PMPXFLDR
                CALL      UPPMPX21
              ENDIF
            ENDIF
          ENDIF
.
          PACK      KEY8,ADMISSNO,SP70
          CALL      RDPMWX11
          IF        OVRCD=1
            CALL      UPCLV000
            MOVE      ADMISSNO,KEY8
            CALL      WRPMWX11
          ENDIF
.
UPCLMD99  RETURN
.
.*******************************************************************************
.             MOVBB000 : Set vars for outpatient booking files
.*******************************************************************************
MOVBB000  MOVE      XOPCLTYP,OBBCTYP
          MOVE      XOPCLTYP,OBACTYP
          MOVE      XOPURNO,URNUMBER
          MOVE      XOPURNO,OBAURNO
          MOVE      XOPVISIT,OBAVISIT
          MOVE      XOPBKNO,ADMISSNO
          MOVE      XOPBKDTE,OBABKDTE
          MOVE      XOPSRCR,OBASRCR
          MOVE      XOPCOMP,OBACOMP
          MOVE      XOPPRTY,OBAPRTY
          MOVE      XOPUNIT,OTBBDEPT
          MOVE      XOPTRAN,OTBBTRAN
          MOVE      XOPRFGP,OTBBRFGP
          MOVE      XOPRFPC,OTBBGPPC
          MOVE      XOPDGCD1,OPDICODE
          MOVE      SAVEDESC,OPDIDESC
          MOVE      XOPDGCD2,OPDICOD2
          MOVE      SAVEDES2,OPDIDES2
          MOVE      XOPDGCD3,OPDICOD3
          MOVE      SAVEDES3,OPDIDES3
          MOVE      XOPDGCD4,OPDICOD4
          MOVE      SAVEDES4,OPDIDES4
          MOVE      XOPDGCD5,OPDICOD5
          MOVE      SAVEDES5,OPDIDES5
          MOVE      XOPUDF1,OBOUSR1
          MOVE      XOPUDF2,OBOUSR2
          MOVE      XOPUDF3,OBOUSR3
          MOVE      XOPUDF4,OBOUSR4
          MOVE      XOPUDF5,OBOUSR5
          MOVE      XOPUDF6,OBOUSR6
          MOVE      XOPUDF7,OBOUSR7
          MOVE      XOPUDF8,OBOUSR8
.
MOVBB999  RETURN
.
.*******************************************************************************
.             MVVXTF00 : Set vars for visit extn file
.*******************************************************************************
MVVXTF00  MOVE      XOPCMPLT,PMVXUDF1
          UNPACK    XOPBKDTE,PMVXCDTE,PMVXCTIM
.
MVVXTF99  RETURN
.*******************************************************************************
.             UPCLV000 : Set vars for workers compe file
.*******************************************************************************
UPCLV000  MOVE      ADMISSNO,PMWXVISN
          MOVE      ADMISSNO,DWCADMNO
          MOVE      XOPACCN,WCCLMNO
          MOVE      XOPACCDT,WCACDAT
          MOVE      XOPACCDD,PMWXADTE
          MOVE      XOPACCWR,PMWXACCF
          MOVE      XOPACCDI,PMWXALOC
          MOVE      XOPACCPO,PMWXPLIN
          MOVE      XOPACCAI,PMWXACTI
          MOVE      XOPACCEM,WCENAME
.
UPCLV999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       CLPMSVX1
          INC       CLOUTXSC
          INC       COUNTSLT
.
          INC       IBAPOSIO/INC
          INC       MRTMASIO/INC
          INC       MRTVISIO/INC
          INC       OUTBOAIO/INC
          INC       OUTBB1IO/INC
          INC       OUTBOCIO/INC
          INC       OUTCANIO/INC
          INC       OUTCLIIO/INC
          INC       OUTCTYIO/INC
          INC       OUTCDCIO/INC
          INC       OUTDIAIO/INC
          INC       OUTHEDIO/INC
          INC       OUTMA1IO/INC
          INC       OUTPREIO/INC
          INC       OUTRAPIO/INC
          INC       OUTSESIO/INC
          INC       OUTSITIO/INC
          INC       OUTXSCIO/INC
          INC       PATCODIO/INC
          INC       VISMDTIO/INC
          INC       VISMTXIO/INC
          INC       PATDO1IO/INC
          INC       PATHSPIO/INC
          INC       PATMA1IO/INC
          INC       PATRE1IO/INC
          INC       PATVISIO/INC
          INC       PATWMAIO/INC
          INC       PATWVEIO/INC
          INC       PATWC1IO/INC
          INC       PMSCURIO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
          INC       PMSWX1IO/INC
          INC       VISINTIO/INC
          INC       VISIAUIO/INC

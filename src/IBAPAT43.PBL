.******************************************************************************
.*                                                                            *
.*                        P R O G R A M   S U M M A R Y                       *
.*                        -------------   -------------                       *
.*                                                                            *
.*     PROGRAM NAME:    IBAPAT43                                              *
.*                                                                            *
.*     FUNCTION:        DISCHARGES WITH/WITHOUT DISEASE                       *
.*                                                                            *
.*     DATE WRITTEN:    14/10/1985                                            *
.*                                                                            *
.*     AUTHOR:          MALCOLM HAYSE                                         *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.*                                                                            *
.*        V12.00.01 30/05/2025  Don Nguyen     TSK 0955096                    *
.*                  Alphanumeric visit number changes                         *
.*        V10.13.01 05/12/2018  Don Nguyen     TSK 0838558                    *
.*                  Deleted temp file (PSTROL) after processing               *
.*        V10.11.01 08/09/2017  Ebon Clements  TSK 0818097                    *
.*                  Added excluded newborn functionality -  EXNB0000          *
.*        V10.08.01 22/11/2016  Ebon Clements  TSK 0319860                    *
.*                  Ignore patecdaf ICD coding lock record - NXDISE           *
.*        V10.05.01 01/01/2015  Ania P         CAR 261630                     *
.*                  Removed use of PORT for temp file naming                  *
.*        V10.03.01 25/11/2013  Patrick Adair  CAR 199297                     *
.*                  Exclude Residential Admissions - NZ only                  *
.*        V10.02.01 28/03/2011  Mike Laming    CAR 240107                     *
.*                  Mods to PATECDaf & PATECOaf variables & Keys - file change*
.*        V9.10.01  29/04/2008  Ebon Clements  CAR 166617                     *
.*                  Added print of procedure codes                            *
.*        V9.09.01  01/11/2007  Peter McMullen CAR 153023 (see below)         *
.*        V9.08.01  01/11/2007  Peter McMullen CAR 153023                     *
.*                  re-apply CAR 80273 (from 6.13.01) exclude borders from    *
.*                  discharge report                                          *
.*        V9.04.04  15/06/2005  Jill Habasque CAR 62292                       *
.*                  Fixed hospital keyin                                      *
.*        V9.04.03  26/11/2004  Lina Vo       CAR 54917                       *
.*                  Fixed report display for the WEB.                         *
.*                  Add Total to report.                                      *
.*        V9.04.02  28/09/2004  Mike Laming   CAR 53568                       *
.*                  Add Health Fund column at - all over the place            *
.*        V9.04.01  23/08/2004  Lina Vo CAR 52901                             *
.*                  Added Multi-Hospital processing                           *
.*        V5.08.02  10/10/2000  J.Tan                                         *
.*                  Recompiled for WEB Report Scheduler                       *
.*        V5.08.01  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.B01 16/03/2000  Greg Horvat                                   *
.*                  Changed to get the coding from the episode coding files   *
.*        V5.07.B02 29/01/1999  Jim Stathopoulos                              *
.*                  Report Modification                                       *
.*                  09/11/1998  Glenn Berry      5.07 changes                 *
.******************************************************************************
.*        V5.06.01  12/08/1998  Steve Armstrong                               *
.*                  Fixed length of doctor telphone number in temp file       *
.******************************************************************************
.*                      V5.01.02  16/11/89 L. P.           SRF 102991         *
.*                                Pack Key10 not Key8 when positioning        *
.*                                patmdiss records for a patient              *
.*                      V5.01.121 04/09/90 i chung         SRF 106567         *
.*                                Include blank Attending/Referring Doc. Code *
.*                                patients for Start to Finish Doc. Code range*
.*                      V5.01.122 27/11/90 Glen Hobby                         *
.*                                Recompiled for changes to PATMX1FD          *
.*                      V5.01.123 19/02/92 i chung         SRF 112524         *
.*                                Added Procedure Codes column.               *
.*                                Added "?" option for Doctor code.           *
.*        V5.01.124 31/12/1992  Graeme Williams            SRF 119179         *
.*                  Fixed error with unpack of DDATE                          *
.*        V5.01.125 25/05/1994 Ian Rutt                                       *
.*                  Fixed Global Recompile Bugs                               * 
.*        V5.04.01  25/06/1997  Howellsy         505                          *
.*                  Use PATDOCKY instead of PATDOCDS                          *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
          INC       IBACONFD/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATDO1FD/INC
          INC       PATDSCFD/INC
          INC       PATECDFD/INC
          INC       PATECOFD/INC
          INC       PATHSPFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATTRNFD/INC
          INC       PMSVX1FD/INC
          INC       IBASEQFD/INC  
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
+
.
.         TEMP FILES DECLARATION
.
PSTROL    FILE      ASCII, FIXED=256
TEMPFILE  IFILE     SQL, FIXED=151, KEY="u1-14"             * was 143  *C-53568
.
. ----- Tempfile consists of following vars -----
.
. Name   Type      Length     Physical     Start   Description
. ----   ----      ------     --------     -----   -----------
.DCODE    DIM       6          6             1
DUNIQUE   DIM       8          8             7
.DNAME21  DIM      21         21            15
.DTELES   DIM      20         20            36
.PURNO    DIM       8          8            56
.DADMNO   DIM       8          8            64
.PSNAME   DIM      20         20            72
.PNAME20  DIM      20         20            92
.PADATE   DIM      10         10           112
.A5TIME   DIM       5          5           122
.PDDATE   DIM      10         10           127
.D5TIME   DIM       5          5           137
.AHFUND   DIM       6          6           142                         *I-53568
.End of Record                             148                         *C-53568
.
.         WORK VARIABLES
.
AHFUND    DIM       6                                                  *I-53568
A5TIME    DIM       5
BOARDFLG  FORM      1     * used to branch over boarder check
.
CMDLINE   DIM       50
COUNT     FORM      8
.
D5TIME    DIM       5
DIM2A     DIM       2
DIM3      DIM       3     * required by PATDOCDS
DIM13     DIM       13
DNAME21   DIM       21
DOCTEMP   DIM       6
DOCCODE   DIM       6
.
EDOC23    DIM       23    * Ending Doctor description
END6      DIM       6
ENDATE    DIM       8
.
FIRST     DIM       1
FIRSTPRO  FORM      1     * First Procedure Code 1 = True, 0 = False
FNAMER    DIM       8
FNAMET    DIM       8
.
GNDTOTAL  FORM      6
.
LNO       FORM      2
.
NODIS     FORM      1
.
PAGENO    FORM      3
PADATE    DIM       10
PDDATE    DIM       10
PFDATE    DIM       10
PROCCODE  FORM      1     * Procedure Codes left to print 0 = False, 1 = True
PTDATE    DIM       10
PNAME20   DIM       20
.
QUESTFLG  FORM      1     * Doctor codes "?" routine flag
.
SAMEF     FORM      1
SDOC23    DIM       23    * Starting Doctor description
START6    DIM       6
STARTD    DIM       8
STATUS    FORM      1
.
.
UNIQUE    FORM      8
UKDOCFLG  FORM      1     * Unknown Doctor flag  (0=Used, 1=Not used)
.
XDIM6     DIM       6
YDIM6     DIM       6
.
.         CONSTANTS
.
CODEP     INIT      "P "
FIVE5     FORM      "55"
FSTREC    INIT      "Start "
LSTREC    INIT      "Finish"
.
SP12      INIT      "            "
SP14      INIT      "              "
SP23      INIT      "                       "
SP28      INIT      "                            "
.
PRGID     INIT      "IBAPAT43"
PRGNAM    INIT      "Discharges With/without Disease Codes"
VERSION   INIT      "V12.01.00"
+
.
.         Display screen header and open files
.
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patecdaf";
          OPEN      PATECDA2,"patecdaf"
.
          DISPLAY   *P64:24,"patecoaf";
          OPEN      PATECOA2,"patecoaf"
.
          DISPLAY   *P64:24,"pattranf"
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"pathspaf";
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P64:24,"pmsvx1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TEN5;*249,CRDRTYP
          READ      CONTROLF,TEN;*66,CINVLAY
          READ      CONTROLF,TWENTY1;*247,PTCNDCQS
          READ      CONTROLF,EIGHTY;*250,PTCNHDPS
.
          MOVE      ONE,BOARDFLG
          COMPARE   EIGHT,CINVLAY
          GOTO      INIT0000 IF NOT EQUAL
          MOVE      ZERO,BOARDFLG
.
INIT0000  CALL      TFILENAM
          MOVE      TFILNAME,FNAMER
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMET
.
          MOVE      ONE,CNEWDS              * set new standard for PATDOCDS
+
.
.         SELECT OPTION
.
.*******************************************************************************
.*       (NOTE: If new option(s) is to be added after Option 4 please check    *
.*              print routine to make sure it handles printing for the new     *
.*              option(s) as well)                                             *
.*******************************************************************************
.
          DISPLAY   *P1:4,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = ":
                    "Discharges Without Disease Code By Attending Doctor":
                    *P1:6,*V2LON,TWO,*HOFF," = ":
                    "Discharges Without Disease Code By Referring Doctor":
                    *P1:7,*V2LON,THREE,*HOFF," = ":
                    "All Discharges By Attending Doctor":
                    *P1:8,*V2LON,FOUR,*HOFF," = ":
                    "All Discharges By Referring Doctor":
                    *P1:10,"Select Option :";
.
REKSTRT   KEYIN     *P17:10,*EL,*DV,UNDLN1,*P17:10,*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
          BRANCH    OPTION OF VALID,VALID,VALID,VALID
          BEEP
          GOTO      REKSTRT
.
VALID     DISPLAY   *P1:14,"Starting Discharge Date  :":
                    *P1:15,"Ending   Discharge Date  :":
                    *P1:17,"Starting Doctor Code     :":
                    *P1:18,"Ending   Doctor Code     :";
.
.         Keyin Start Discharge Date
.
KSTART    MOVE      ZERO,CHIGHLT            * 0 = highlight
          MOVE      ONE,CDEFDTE             * 1 = check day only for default 
.
          MOVE      "29",CCOL               * column for keyin
          MOVE      "14",CVERT              * row for keyin
          MOVE      "50",ECOL               * column for error
          MOVE      "14",EVERT              * row for error
.                                           * 1 = check day only for default
KSTART1   MOVE      SP2,CDAY                * set up defaults
          MOVE      SP2,CMON
          MOVE      SP2,CYEAR
          MOVE      CCC,CCENT
          CALL      KEYDATE
          BRANCH    CQUEST,KSTART1          * "?" keyed
          BRANCH    OVRCD,FINISH            * no date keyed
.
          PACK      STARTD,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STARTD
          PACK      PFDATE WITH CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          REP       " 0",PFDATE
.
.         Keyin End Discharge Date
.
KEND      MOVE      "15",CVERT              * row for keyin
          MOVE      "15",EVERT              * row for error
.
KEND1     MOVE      SP2,CDAY                * set up defaults
          DISPLAY   *P29:15,*V2LON,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR;
          MOVE      SP2,CMON
          MOVE      SP2,CYEAR
          MOVE      CCC,CCENT
          CALL      KEYDATE
          BRANCH    CQUEST,KEND1            * "?" keyed
          BRANCH    OVRCD,CURDAY            * no date keyed
          GOTO      CHKDAT2
.
CURDAY    MOVE      CDD,CDAY
          MOVE      CMM,CMON
          MOVE      CYY,CYEAR
          MOVE      CCC,CCENT
          REP       " 0",CDAY
          REP       " 0",CMON
          REP       " 0",CYEAR
          REP       " 0",CCENT
          DISPLAY   *P29:15,*V2LON,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR;
.
CHKDAT2   PACK      ENDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDATE
          PACK      PTDATE WITH CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          REP       " 0",PTDATE
.
          MATCH     STARTD,ENDATE
          GOTO      KFDOC IF NOT LESS
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Ending Date must be > or equal to Starting Date **  ";
          CALL      HITENTER
          GOTO      KEND
.
.         Keyin Start Doctor Code
.
KFDOC     MOVE      SP23,SDOC23
          MOVE      ONE,QUESTFLG
          MOVE      "17",CCOL1
          DISPLAY   *P40:17,"( ",*V2LON,"?",*HOFF," for Doctors Scan )";
          MOVE      "29",ECOL
          MOVE      "17",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          CALL      PATDOCKY
          BRANCH    EXIT,KFDOC1,KFDOC
.
          MOVE      DCODE,START6
          GOTO      KFDOC2
.
KFDOC1    MOVE      SP6,START6
          DISPLAY   *P29:17,*EL,*V2LON,FSTREC;
          GOTO      KTDOC
.
KFDOC2    MOVE      DGNAME,ANS
          PACK      SDOC23,ANS,DOT,SP1,DSNAME
          MOVE      ONE,UKDOCFLG           * set Unknown Doctor flag to not used
          DISPLAY   *P29:17,*EL,*V2LON,START6,*HOFF,*P40:17,SDOC23;
          GOTO      KTDOC
.
.         Keyin End Doctor Code
.
KTDOC     MOVE      SP23,EDOC23
          MOVE      ONE,QUESTFLG
          MOVE      "18",CCOL1
          DISPLAY   *P40:18,"( ",*V2LON,"?",*HOFF," for Doctors Scan )";
.
          MOVE      "29",ECOL
          MOVE      "18",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          CALL      PATDOCKY
          BRANCH    EXIT,KTDOC2,KTDOC
.
          MOVE      DCODE,END6
          GOTO      KTDOC4
.
KTDOC2    MOVE      SP6,END6
          DISPLAY   *P29:18,*EL,*V2LON,LSTREC;
.
          IF        IBCNMHOS=1
            CALL      KHOS0000
            BRANCH    EXIT,VALID
          ENDIF
          GOTO      CONFIRM
.
KTDOC4    MATCH     START6,END6
          GOTO      KTDOC31 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Ending Code must be > or equal to Starting Code **  ";
          CALL      HITENTER
          GOTO      KTDOC
.
KTDOC31   MOVE      DGNAME,ANS
          PACK      EDOC23,ANS,DOT,SP1,DSNAME
KTDOC32   DISPLAY   *P29:18,*EL,*V2LON,END6,*HOFF,*P40:18,EDOC23;
          GOTO      KTDOC5
.
.       Unknown Doctor flag is only used if Doctor Code range is Start to Finish
.       - added for SRF 106567
.
KTDOC5    BRANCH    UKDOCFLG,KTDOC52
          MOVE      ONE,UKDOCFLG
.
KTDOC52   CALL      KHOS0000
          BRANCH    EXIT,VALID
          GOTO      CONFIRM
.
.******************************************************************************
.*                                  KHOS0000              Called by: ML0000   *
.*                            Enter The Date Ranges                           *
.******************************************************************************
KHOS0000  COMPARE   ONE,IBCNMHOS
          GOTO      KHOS9999 IF NOT EQUAL
.
          DISPLAY   *P1:20,*EL,"Hospital Id : ";
          MOVE      TEN5,ECOL
          MOVE      "20",EVERT
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND           * Not Mandatory
          OPEN      PATHSPA1,"pathspaf"
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHOS2000,KHOS9500
          GOTO      KHOS3000
.
KHOS2000  MOVE      "All Hospitals",PTHSNAME
          MOVE      SP10,PTHSHOSP
          MOVE      ZERO,EXIT
.
KHOS3000  CLOSE     PATHSPA1
.
          DISPLAY   *P20:20,*V2LON,PTHSNAME;
          GOTO      KHOS9999
.
KHOS9500  MOVE      ONE,EXIT
KHOS9999  RETURN
+
.******************************************************************************
.
.         Redisplay Screen after "?" option for Doctor codes
.
RDSP0000  DISPLAY   *P1:4,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = ":
                    "Discharges Without Disease Code By Attending Doctor":
                    *P1:6,*V2LON,TWO,*HOFF," = ":
                    "Discharges Without Disease Code By Referring Doctor":
                    *P1:7,*V2LON,THREE,*HOFF," = ":
                    "All Discharges By Attending Doctor":
                    *P1:8,*V2LON,FOUR,*HOFF," = ":
                    "All Discharges By Referring Doctor":
                    *P1:10,"Select Option : ",*V2LON,OPTION,*HOFF:
                    *P1:14,"Starting Discharge Date  :  ",*V2LON,PFDATE,*HOFF:
                    *P1:15,"Ending   Discharge Date  :  ",*V2LON,PTDATE,*HOFF:
                    *P1:17,"Starting Doctor Code     :  ":
                    *P1:18,"Ending   Doctor Code     :  ";
.
          MATCH     SP6,START6
          GOTO      RDSP1000 IF EQUAL
          DISPLAY   *P29:17,*V2LON,START6,*HOFF,*P40:17,SDOC23;
          GOTO      RDSP9999
.
RDSP1000  DISPLAY   *P29:17,*V2LON,FSTREC;
.
RDSP9999  RETURN
+
.******************************************************************************
.
.         Confirm Date and Doctor Code ranges
.
CONFIRM   DISPLAY   *P1:24,*EL,"Ok to Continue (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ?";
.
CONFIRM1  KEYIN     *P24:24,*DV,UNDLN1,*P24:24,*V2LON,ANS;
.
          RESET     ANS
          GOTO      CONFIRM2 IF EOS
          REP       UPPLOW,ANS
          DISPLAY   *P24:24,*V2LON,ANS;
.
          MATCH     "N",ANS
          GOTO      CLSCR IF EQUAL
          MATCH     "Y",ANS
          GOTO      PROCESS IF EQUAL
.
CONFIRM2  BEEP
          GOTO      CONFIRM1
.
CLSCR     DISPLAY   *P29:14,*EL,*P29:15,*EL,*P29:17,*EL,*P29:18,*EF;
          GOTO      KSTART
.
FINISH    DISPLAY   *P1:14,*EF;
          GOTO      REKSTRT
.
.         Start processing data for report
.
PROCESS   CALL      KILLIDX
          CALL      CREAIDX
          MOVE      FALSE,PROCCODE
.
          OPEN      TEMPFILE,FNAMET
          MOVE      ZERO,PAGENO
          MOVE      "60",LNO
          MOVE      ONE TO UNIQUE
.
NODET     DISPLAY   *P1:24,*EL,*V2LON:
                    *P20:24,"** Scanning Discharge File **",*W2:
                    *HOFF,*P1:24,*EL:
                    *P10:24,"Doctor Code :",*P40:24,"Discharge Date :"
.
.         READ DISCHARGE FILE BASE ON START & END DATE
.
          PACK      KEY16,STARTD,SP10
          CALL      RDSDSCH2
.
LOOP      CALL      RDKDSCH2
          BRANCH    OVRCD OF PRINTR
          REP       " 0",DDATE
          UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          MATCH     SP8,ENDATE
          GOTO      CHKHOS10 IF EQUAL
.
          MATCH     DDATE,ENDATE
          GOTO      PRINTR IF LESS
.
.         Multi-Hospital Processing
.
CHKHOS10  COMPARE   ONE,IBCNMHOS
          GOTO      NEXT10 IF NOT EQUAL
.
          MATCH     SP10,PTHSHOSP
          GOTO      NEXT10 IF EQUAL
.
          MOVE      DADMNO,KEY8
          CALL      RDPMVX11
          BRANCH    OVRCD,LOOP
.
          MATCH     PMVXMHOS,PTHSHOSP
          GOTO      LOOP IF NOT EQUAL
.
NEXT10    DISPLAY   *P57:24,*V2LON,CPCDATE;
.
          BRANCH    OPTION OF NODISE,NODISE,CHKADM,CHKADM
.
NODISE    PACK      KEY16,DADMNO,SP20                                 *C-240107
          CALL      RSPTECD2
NXDISE    CALL      RKPTECD2
          BRANCH    OVRCD,CHKADM
.
          MATCH     DADMNO,PTEDADMN
          GOTO      CHKADM IF NOT EQUAL
.
          COMPARE   ZERO,PTEDEPNO                * Skip coding locked record
          GOTO      NXDISE IF EQUAL
.
          GOTO      LOOP
.
CHKADM    MOVE      DADMNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD OF LOOP
.
          MATCH     AURNO,DURNO
          GOTO      LOOP IF NOT EQUAL
.
          CALL      EXNB0000               * Excluded newborn
          BRANCH    EXIT,LOOP
.
          PACK      KEY5,ANSA,SP1,ATYPE
          CALL      RDCODE1                * Cat."A "
          BRANCH    OVRCD,CHKADM01
          MATCH     "B",TCINDC3            * is this a boarder?
          GOTO      LOOP IF EQUAL          * ignore boarders
.
          BRANCH    BOARDFLG,CHKADM01
          PACK      KEY5,CODEP,ACLSS
          CALL      RDCODE1                * Cat."P "
          BRANCH    OVRCD,CHKADM01
          MATCH     "3",TCINDC1                * boarder patient ?
          GOTO      LOOP IF EQUAL              * ignore if yes
          IF        PTCNHDPS=1
            MATCH     "R",TCINDC7              * Residential Admission?
            GOTO      LOOP IF EQUAL            * ignore if yes
          ENDIF
.
CHKADM01  BRANCH    OPTION,CHKADM04,CHKADM02,CHKADM04,CHKADM02
.
CHKADM02  COMPARE   ONE,CRDRTYP               * Free format Referring Doc used ?
          GOTO      CHKADM03 IF NOT EQUAL     * no
.
          MATCH     ARDRNAM,SP23              * null Free format Referring Doc ?
          GOTO      NEXT14 IF EQUAL           * yes
.
          MOVE      SP6,DOCTEMP
          MOVE      SP20,DTELES
          MOVE      ARDRNAM,DNAME21
          GOTO      NEXT18
.
CHKADM03  MOVE      ADOCTR,DOCTEMP
          GOTO      NEXT11
.
CHKADM04  MOVE      ADOCTA,DOCTEMP
.
NEXT11    MATCH     SP6,START6
          GOTO      NEXT12 IF EQUAL
.
          MATCH     START6,DOCTEMP
          GOTO      LOOP IF LESS
.
NEXT12    MATCH     SP6,END6
          GOTO      NEXT13 IF EQUAL
.
          MATCH     DOCTEMP,END6
          GOTO      LOOP IF LESS
.
NEXT13    MOVE      DOCTEMP,KEY6                 * get Doctor's code and name
          CALL      RDDOCT1
          COMPARE   ZERO,OVRCD
          GOTO      NEXT17 IF EQUAL
.
NEXT14    BRANCH    UKDOCFLG,LOOP                  * added for SRF 106567
          MOVE      "UNKN  ",DOCTEMP                          "
          MOVE      SP20,DTELES                               "
          BRANCH    OPTION,NEXT15,NEXT16,NEXT15,NEXT16
.
NEXT15    MOVE      "Unknown Attending Doc. ",DNAME21         "
          GOTO      NEXT18                                    "
.
NEXT16    MOVE      "Unknown Referring Doc. ",DNAME21         "
          GOTO      NEXT18                                    "
.
NEXT17    MOVE      SP23,DNAME21
          MOVE      DGNAME,ANS
          PACK      DNAME21 WITH ANS,DOT,SP1,DSNAME
.
NEXT18    MOVE      SP23,PNAME20
          MOVE      DURNO,KEY8
          CALL      RDMAST1
.
          MOVE      PGNAME,PNAME20
.
          UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          PACK      PADATE WITH CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          REP       " 0",PADATE
.
          UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
          PACK      PDDATE WITH CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          REP       " 0",PDDATE
.
          MOVE      ATIME,A5TIME
          MOVE      DTIME,D5TIME
          MOVE      AFUNDH,AHFUND                                      *I-53568
.
          DISPLAY   *P24:24,*V2LON,DOCTEMP;
.
          MOVE      UNIQUE,DUNIQUE
          PACK      KEY14 WITH DOCTEMP,DUNIQUE
.
          WRITE     TEMPFILE,KEY14;DOCTEMP,DUNIQUE,DNAME21,DTELES,PURNO:
                                   DADMNO,PSNAME,PNAME20,PADATE,A5TIME:
                                   PDDATE,D5TIME,AHFUND                *C-53568
          ADD       ONE TO UNIQUE
          GOTO      LOOP
.
PRINTR    DISPLAY   *P1:24,*EL,*V2LON,*P29:24,"** Generating Report **",*W2;
.
          CALL      HEADSET
.
          SUB       ONE,UNIQUE
          MOVE      UNIQUE,GNDTOTAL
          DISPLAY   *P1:23,*EF,"Total Records       : ",*V2LON,UNIQUE,*HOFF:
                    *P1:24,"Printing Record No. :",*P35:24,"Doctor Code :";
.
          MOVE      ONE,COUNT
          MOVE      "Y",FIRST
          MOVE      SP14 TO KEY14
          READ      TEMPFILE,KEY14;;
.
READLOP   READKS    TEMPFILE;DCODE,DUNIQUE,DNAME21,DTELES,PURNO:
                             DADMNO,PSNAME,PNAME20,PADATE,A5TIME:
                             PDDATE,D5TIME,AHFUND                      *C-53568
          GOTO      ENDP IF OVER
.
          MOVE      DUNIQUE,UNIQUE
          DISPLAY   *P23:24,*V2LON,COUNT,*P49:24,DCODE;
.
          COMPARE   FIVE5,LNO
          CALL      HEAD IF NOT LESS
.
          MATCH     "Y",FIRST
          GOTO      NOTFST IF NOT EQUAL
          MOVE      "N",FIRST
          MOVE      DCODE,DOCTEMP
.
          BRANCH    OPTION OF NODIS1,NODIS1
          MOVE      "2",SAMEF
          CALL      PRINTD1
          GOTO      NEXTL
.
NODIS1    CALL      NEWREC
          GOTO      NEXTL
.
NOTFST    MATCH     DCODE,DOCTEMP
          GOTO      SAMEH IF EQUAL
          MOVE      DCODE,DOCTEMP
.
          BRANCH    OPTION OF NODIS2,NODIS2
          MOVE      "2",SAMEF
          CALL      PRINTD1
          GOTO      NEXTL
.
NODIS2    CALL      NEWREC
          GOTO      NEXTL
.
SAMEH     BRANCH    OPTION OF NODIS3,NODIS4
          MOVE      "1",SAMEF
          CALL      PRINTD1
          GOTO      NEXTL
.
NODIS3    CALL      LINE11
.
NODIS3A   CALL      PCOD0000                   * Print Procedure Code          
          CALL      LINE22
          CALL      PCOD0000                   * Print Procedure Code          
          CALL      LINE3
          CALL      PCOD0000                   * Print Procedure Code          
          COMPARE   TRUE,PROCCODE              * More Procedure Codes
          CALL      PMCOD000 IF EQUAL          * Yes. Print Procedure Codes
          GOTO      NEXTL
.
NODIS4    COMPARE   ONE,CRDRTYP                * Free format Referring Doctor ?
          GOTO      NODIS3 IF NOT EQUAL        * no
          MATCH     "UNKN  ",DCODE             * unknown Doctor code ?
          GOTO      NODIS3 IF EQUAL            * yes
          CALL      LINE1                      * no
          GOTO      NODIS3A
.
NEXTL     ADD       ONE,COUNT
          GOTO      READLOP
.
NEWREC    CALL      LINE1
          MOVE      TRUE,FIRSTPRO              * First Procedrue Record
          CALL      PCOD0000                   * Print Procedure Code          
.
          BRANCH    OPTION,NEWREC2             * Option 1
          COMPARE   ONE,CRDRTYP                * Free format Referring Doctor ?
          GOTO      NEWREC2 IF NOT EQUAL       * no
.
NEWREC1   CALL      LINE22
          GOTO      NEWREC3
.
NEWREC2   MATCH     "UNKN  ",DCODE             * unknown Doctor code ?
          GOTO      NEWREC1 IF EQUAL           * yes
          CALL      LINE2                      * no
.
NEWREC3   CALL      PCOD0000                   * Print Procedure Code          
          CALL      LINE3
          CALL      PCOD0000                   * Print Procedure Code          
          RETURN
+
.
.         OVER Condition found..print last line of -- and display message
.
ENDP      COMPARE   ZERO,PAGENO
          GOTO      NOREC IF EQUAL
.
          CALL      HLINE
.
          PRINT     *N,*12,"Total No. of Patients : ",GNDTOTAL:
                    *N,*N,"***  End of Report  ***"
.
          DISPLAY   *P1:23,*EF,*P24:24,*V2LON:
                    "** Report Generation Completed **",*W2;
.
          CALL      KILLIDX
          GOTO      FINISH
.
NOREC     DISPLAY   *P1:23,*EF,*P28:24,*V2LON,"** No Record Selected **",*W2;
.
          MATCH     "IBARSH99",PGM
          IF        @EQUAL
            PRINT     *N,*N,"***  No Report Generated ***"
          ENDIF
.
          CALL      KILLIDX
          GOTO      FINISH
.
.         PRINT ACTUAL DATA
.
PRINTD1   MOVE      "1",NODIS
          MOVE      TRUE,FIRSTPRO               * First Procedure Code
          PACK      KEY16,DADMNO,SP20                                 *C-240107
          MOVE      SP9,PTEDCODE
          CALL      RSPTECD2
LOOPX     CALL      RKPTECD2
          BRANCH    OVRCD,NODISS
.
          MATCH     DADMNO,PTEDADMN
          GOTO      NODISS IF NOT EQUAL
.
          BRANCH    NODIS OF LNONE,SECOND,THIRD,FOURTH,FIFTH
.
.....     CALL      LINE3                                               D-53568
          PRINT     *104,PTEDCODE;                                     *C-53568
          CALL      PCOD0000                   * Print Procedure Code        
          SUB       ONE,NODIS
          GOTO      LOOPX
.
FIFTH     CALL      LINE3
          PRINT     *94,PTEDCODE;
          ADD       ONE,NODIS
          GOTO      LOOPX
.
FOURTH    PRINT     *104,PTEDCODE;                                     *C-53568
          CALL      PCOD0000                   * Print Procedure Code          
          ADD       ONE,NODIS
          GOTO      LOOPX
.
THIRD     BRANCH    SAMEF OF THIRD1            * same Doctor code
          COMPARE   FOUR,OPTION                * not the same Doctor code
          GOTO      THIRDA IF NOT EQUAL        * not Option 4
          COMPARE   ONE,CRDRTYP                * Free format Referring Doctor ?
          GOTO      THIRDA IF NOT EQUAL        * no
          GOTO      THIRD1                     * yes
.
THIRDA    MATCH     "UNKN  ",DCODE             * unknown Doctor code ?
          GOTO      THIRD1 IF EQUAL            * yes
          CALL      LINE2                      * no
          PRINT     *94,PTEDCODE;
          GOTO      THIRD2
.
THIRD1    CALL      LINE22
          PRINT     *94,PTEDCODE;
.
THIRD2    ADD       ONE,NODIS
          GOTO      LOOPX
.
SECOND    PRINT     *104,PTEDCODE;                                     *C-53568
          CALL      PCOD0000                   * Print Procedure Code          
          ADD       ONE,NODIS
          GOTO      LOOPX
.
LNONE     BRANCH    SAMEF OF LNONE1            * same Doctor code
          CALL      LINE1                      * not the same Doctor code
          PRINT     *94,PTEDCODE;
          GOTO      LNONE2
.
LNONE1    COMPARE   FOUR,OPTION                * Option 4 ?
          GOTO      LNONE1A IF NOT EQUAL       * no
          COMPARE   ONE,CRDRTYP                * Free format Referring Doctor ?
          GOTO      LNONE1A IF NOT EQUAL       * no
          MATCH     "UNKN  ",DCODE             * unknown Doctor code ?
          GOTO      LNONE1A IF EQUAL           * yes
          CALL      LINE1                      * no
          GOTO      LNONE1B
.
LNONE1A   CALL      LINE11
.
LNONE1B   PRINT     *94,PTEDCODE;
.
LNONE2    ADD       ONE,NODIS
          GOTO      LOOPX
.
NODISS    COMPARE   TRUE,PROCCODE              * More Procedure Codes
          CALL      PMCOD000 IF EQUAL          * Yes. Print Procedure Codes
.
          BRANCH    NODIS OF ONE1,TWO2,THREE3,OKCONT,OKCONT1
          GOTO      OKCONT
.
ONE1      BRANCH    SAMEF OF ONE11             * same Doctor code
          CALL      LINE1                      * not the same Doctor code
          CALL      PCOD0000                   * Print Procedure Code          
.
          COMPARE   FOUR,OPTION                * Option 4 ?
          GOTO      ONE1B IF NOT EQUAL         * no
          COMPARE   ONE,CRDRTYP                * Free format Referring Doctor ?
          GOTO      ONE1B IF NOT EQUAL         * no
.
ONE1A     CALL      LINE22
          GOTO      OKCONT
.
ONE1B     MATCH     "UNKN  ",DCODE             * unknown Doctor code ?
          GOTO      ONE1A IF EQUAL             * yes
          CALL      LINE2                      * no
          GOTO      OKCONT
.
ONE11     COMPARE   FOUR,OPTION                * Option 4 ?
          GOTO      ONE11A IF NOT EQUAL        * no
          COMPARE   ONE,CRDRTYP                * Free format Referring Doctor ?
          GOTO      ONE11A IF NOT EQUAL        * no
          MATCH     "UNKN  ",DCODE             * unknown Doctor code ?
          GOTO      ONE11A IF EQUAL            * yes
          CALL      LINE1                      * no
          GOTO      ONE11B
.
ONE11A    CALL      LINE11
.
ONE11B    CALL      PCOD0000                   * Print Procedure Code          
          CALL      LINE22
          GOTO      OKCONT
.
TWO2      CALL      PCOD0000                   * Print Procedure Code          
          BRANCH    SAMEF OF TWO21             * same Doctor code
          COMPARE   FOUR,OPTION                * not the same Doctor code
          GOTO      TWO2A IF NOT EQUAL         * not Option 4
          COMPARE   ONE,CRDRTYP                * Free format Referring Doctor ?
          GOTO      TWO2A IF NOT EQUAL         * no
          GOTO      TWO21                      * yes
.
TWO2A     MATCH     "UNKN  ",DCODE             * unknown Doctor code ?
          GOTO      TWO21 IF EQUAL             * yes
          CALL      LINE2                      * no
          GOTO      OKCONT
.
TWO21     CALL      LINE22
          GOTO      OKCONT
.
THREE3    BRANCH    SAMEF OF THREE31           * same Doctor code
          COMPARE   FOUR,OPTION                * not the same Doctor code
          GOTO      THREE3A IF NOT EQUAL       * not Option 4
          COMPARE   ONE,CRDRTYP                * Free format Referring Doctor ?
          GOTO      THREE3A IF NOT EQUAL       * no
          GOTO      THREE31                    * yes
.
THREE3A   MATCH     "UNKN  ",DCODE             * unknown Doctor code ?
          GOTO      THREE31 IF EQUAL           * yes
          CALL      LINE2                      * no
          GOTO      OKCONT
.
THREE31   CALL      LINE22
.
OKCONT    CALL      PCOD0000                   * Print Procedure Code          
.
OKCONT1   CALL      LINE3
          CALL      PCOD0000                   * Print Procedure Code          
          RETURN
+
LINE1     PRINT     *1,"| ",DCODE:
                    *10,"| ",DNAME21:
                    *33,"|",DADMNO,"| ",PADATE:
                    *55,A5TIME:
                    *61,"|",PURNO,"| ",PSNAME:
                    *92,"| ";
          ADD       ONE,LNO
          RETURN
.
LINE11    PRINT     *1,"|",*10,"|":
                    *33,"|",DADMNO,"| ",PADATE:
                    *55,A5TIME:
                    *61,"|",PURNO,"| ",PSNAME:
                    *92,"| ";
          ADD       ONE,LNO
          RETURN
.
LINE2     MOVE      DTELES,KEY14
          PRINT     *1,"|":
                    *10,"| PHONE: ",KEY14:
                    *33,"|":
                    *42,"| ",PDDATE:
                    *55,D5TIME:
                    *61,"|":
                    *70,"| ",PNAME20:
                    *92,"| ";
          ADD       ONE,LNO
          RETURN
.
LINE22    PRINT     *1,"|":
                    *10,"|":
                    *33,"|":
                    *42,"| ",PDDATE:
                    *55,D5TIME:
                    *61,"|":
                    *70,"| ",PNAME20:
                    *92,"| ";
          ADD       ONE,LNO
          RETURN
.
LINE3     PRINT     *1,"|":
                    *10,"|":
                    *33,"|":
                    *42,"|":
                    *61,"|":
                    *70,"|":
                    *92,"|";
.
          ADD       ONE,LNO
          RETURN
.
.******************************************** Start of addition        *I-53568
..PRTLNEND  MATCH     SP6,AHFUND
..          IF        @EQUAL
..            PRINT     *113,"|",*124,"|",*132,"|"
..          ELSE
..            PRINT     *113,"|",*124,"| ",AHFUND,"|"
..         ENDIF
...
..          MOVE      SP6,AHFUND
..          RETURN
.********************************************   End of addition        *I-53568
.
.******************************************************************************
.*                             PCOD0000                                       *
.*                         Print Procedure Codes                              *
.******************************************************************************
PCOD0000  
..        COMPARE   ZERO,PTCNPROC            * Printing Procedure Codes ?
..        GOTO      PCOD8000 IF EQUAL        * No.
.
          COMPARE   TRUE,FIRSTPRO            * First Disease Code ?
          GOTO      PCOD1000 IF NOT EQUAL    * No.
.
          MOVE      SP9,PTEOCODE
          PACK      KEY16,DADMNO,SP20                                 *C-240107
          CALL      RSPTECO2
PCOD1000  CALL      RKPTECO2
          BRANCH    OVRCD,PCOD2000
.
          MATCH     DADMNO,PTEOADMN          * Same Admission Number ?
          GOTO      PCOD2000 IF NOT EQUAL    * No, exit routine
.
          MOVE      TRUE,PROCCODE            * Another Procedure code
          MOVE      FALSE,FIRSTPRO           * Not First Procedure Code ?
          GOTO      PCOD3000                 * Print Procedure Code
.
PCOD2000  MOVE      FALSE,PROCCODE           * No More Procedure Codes
          MOVE      SP9,PTEOCODE
.
PCOD3000  PRINT     *113,"| ",PTEOCODE;      * Print Procedure Code
          GOTO      PCOD9000                 * Exit Routine
.
PCOD8000  PRINT     *113,"|";                * Not Printing  * was 119 *C-53568
.
.******************************************** Start of addition         I-53568
PCOD9000  MATCH     SP6,AHFUND
          IF        @EQUAL
            PRINT     *124,"|       |"
          ELSE
            PRINT     *124,"| ",AHFUND,"|"
          ENDIF
          MOVE      SP6,AHFUND
.********************************************   End of addition         I-53568
.
PCOD9999  RETURN
.
.******************************************************************************
.*                             PMCOD000                                       *
.*                      Print More Procedure Codes                            *
.******************************************************************************
PMCOD000  
..        COMPARE   ZERO,PTCNPROC            * Printing Procedure Codes ?
..        GOTO      PMCOD900 IF EQUAL        * No.
.
          COMPARE   TRUE,FIRSTPRO            * First Disease Code ?
          GOTO      PMCOD100 IF NOT EQUAL    * No.
.
          MOVE      SP9,PTEOCODE
          PACK      KEY16,DADMNO,SP20                                 *C-240107
          CALL      RSPTECO2
PMCOD100  CALL      RKPTECO2
          BRANCH    OVRCD,PMCOD200
.
          MATCH     DADMNO,PTEOADMN          * Same Admission Number ?
          GOTO      PMCOD200 IF NOT EQUAL    * No, exit routine
.
          MOVE      TRUE,PROCCODE            * Another Procedure code
          MOVE      FALSE,FIRSTPRO           * Not First Procedure Code ?
.
          CALL      LINE3
          PRINT     *113,"| ",PTEOCODE:      * Print Procedure Code    *C-53568
                    *124,"|       |"                 * was '*132,"|"'  *C-53568
          GOTO      PMCOD000                  * Get next Operation Code
.
PMCOD200  MOVE      FALSE,PROCCODE           * No More Procedure Codes
          GOTO      PMCOD999                 * Exit Routine
.
PMCOD900  PRINT     *113,"|":                * Not Printing  * was 119 *C-53568
                    *124,"|       |"                 * was '*132,"|"'  *C-53568
.
PMCOD999  RETURN
.
.         HEADINGS FOR OUTPUT
.
HEAD      COMPARE   ZERO,PAGENO
          CALL      HLINE IF NOT EQUAL
          CLOCK     TIME,CTIMEIS
.
          ADD       ONE,PAGENO
          PRINT     *F,PRGID,*41,CNAME,*116,"DATE : ",CDATE
.
          MOVE      "8",LNO
          BRANCH    OPTION OF PHEAD1,PHEAD2,PHEAD3,PHEAD4
.
PHEAD1    PRINT     *1,VERSION,*41:
                    "DISCHARGES WITHOUT DISEASES CODE BY ATTENDING DOCTOR":
                    *116,"TIME :   ",CTIMEIS,*N,*116,"PAGE : ",*130,PAGENO
          GOTO      PREST
.
PHEAD2    PRINT     *1,VERSION,*41:
                    "DISCHARGES WITHOUT DISEASES CODE BY REFERRING DOCTOR":
                    *116,"TIME :   ",CTIMEIS,*N,*116,"PAGE : ",*130,PAGENO
          GOTO      PREST
.
PHEAD3    PRINT     *1,VERSION,*41,"ALL DISCHARGES BY ATTENDING DOCTOR":
                    *116,"TIME :   ",CTIMEIS,*N,*116,"PAGE : ",*130,PAGENO
          GOTO      PREST
.
PHEAD4    PRINT     *1,VERSION,*41,"ALL DISCHARGES BY REFERRING DOCTOR":
                    *116,"TIME :   ",CTIMEIS,*N,*116,"PAGE : ",*130,PAGENO
.
PREST     IF        IBCNMHOS =1
            PRINT     *41,"Hospital       : ",PTHSNAME
          ENDIF
.
          PRINT     *41,"Discharge Date : From  ",PFDATE,"  To  ",PTDATE,*N:
                    *41,"Doctor Code    : From   ",XDIM6,"   To   ",YDIM6,*N
.
          CALL      HLINE
          PRINT     *1,"| Doctor":
                    *10,"| Doctor's Name":
                    *33,"| Adm No | Adm.  Date/Time  |U/R No":
                    *70,"| Patient's Surname /":
                    *92,"| Disease Codes":
                    *113,"|Proc Codes":
                    *124,"| Health|",*N:
                    *1,"|  Code":
                    *10,"|":
                    *33,"|":
                    *42,"| Dsch. Date/Time":
                    *61,"|":
                    *70,"|":
                    *75,"Given Names":
                    *92,"|":
                    *113,"|":
                    *124,"| Fund",*132,"|"
          CALL      HLINE
.
          MOVE      "Y",FIRST
          RETURN
+
.
HLINE     PRINT      *1,"*--------------------------------------------------":
                    *52,"---------------------------------------------------":
                   *103,"-----------------------------*"
          ADD       ONE,LNO
          RETURN
+
.         Deleting an Indexed File based on PORT
.
KILLIDX   CLOSE     TEMPFILE
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMET,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          RETURN
+
.
.         Creating Temp Indexed file used for this report
.
CREAIDX   PREP      PSTROL,FNAMER
          WRITE     PSTROL,SEQ;"isbuild ",FNAMET," 151 u1-14"          *C-53568
          WEOF      PSTROL,SEQ
.
          CLEAR     CMDLINE
          APPEND    "sh ",CMDLINE
          APPEND    FNAMER,CMDLINE
          APPEND    ".txt",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CLOSE     PSTROL,DELETE
          RETURN
+
.
HEADSET   MOVE      SP6,XDIM6
          MOVE      SP6,YDIM6
.
          MOVE      START6,XDIM6
          MOVE      END6,YDIM6
.
          MATCH     SP6,START6
          GOTO      CONT3 IF NOT EQUAL
          MOVE      FSTREC,XDIM6
.
CONT3     MATCH     SP6,END6
          RETURN    IF NOT EQUAL
          MOVE      LSTREC,YDIM6
          RETURN
+
.
ENDIT     CHAIN     PGM
          STOP
+
.==============================================================================
.
          INCLUDE   STD001IO
.
          INC       EXCLNEWB
          INC       PATDOCKY
          INC       PATHSPKY
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATDSCIO/INC
          INC       PATECDIO/INC
          INC       PATECOIO/INC
          INC       PATHSPIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATTRNIO/INC
          INC       PMSVX1IO/INC
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC
+

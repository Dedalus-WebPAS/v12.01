.******************************************************************************
.* System   :  Waiting Lists Extract Enquiry                                  *
.* Program  :  IBAWAT56                                                       *
.* Desc     :  Waiting Lists Extract Enquiry Report                           *
.******************************************************************************
.* Date     :  09/09/2009                                                     *
.* Author   :  Saroeun Jaylen Soeur                                           *
.* Mods     :                                                                 *
.*                                                                            * 
.******************************************************************************
.* V11.02.01 10/02/2022  Thanh T       TSK 0889899                            *
.*           Recompiled as WATTR1FD changes                                   *
.******************************************************************************
.*       V11.00.02  20/03/2020  Peter Vela                  TSK 0889143       *
.*                  Fixed OUTLET IO calls                                     *
.*       V11.00.01  19/03/2020  Peter Vela                  TSK 0889143       *
.*                  Recompiled for WATLETFD                                   *
.*       V10.05.01  01/08/2015  Ania P                      CAR 261630        *
.*                  Removed use of PORT for temp file naming                  *
.*       V10.04.01  31/12/2013  Don Nguyen       CAR 294389                   *
.*                  Recompiled for changes to WATLETFD & WATLETIO             *
.*        V9.12.01  15/03/2010  Saroeun Soeur CAR 183899                      *
.*                  fixed report output                                       *
.*        V9.12.00  09/09/2009  Saroeun Soeur CAR 183899                      * 
.*                  Created new waiting list extract report                   *
.*                                                                            *
.******************************************************************************
.------------------------------------------------------------------------------
.         Database file includes
.------------------------------------------------------------------------------
.
          INC       STD001FD
.
          INC       PATCODFD/INC
          INC       PATDO1FD/INC
          INC       PATMA1FD/INC
          INC       WATCONFD/INC
          INC       WATMESFD/INC
          INC       WATPROFD/INC
          INC       WATTR1FD/INC
          INC       WATEPSFD/INC
          INC       WATEXTFD/INC
          INC       PATWR1FD/INC
          INC       WATLETFD/INC
          INC       IBASEQFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
+
.------------------------------------------------------------------------------
.         Constant variables - constants will have a 0 on the 8th char
.------------------------------------------------------------------------------
.
URTITLE0  INIT      "U/R No"
PATITLE0  INIT      "Patient Name"
PRTITLE0  INIT      "Procedure"
DSTITLE0  INIT      "Description"
OLTITLE0  INIT      "On List"
RVTITLE0  INIT      "Review"
SCTITLE0  INIT      "Scheduled"
UNTITLE0  INIT      "Unit"
DOTITLE0  INIT      "Doctor"
SATITLE0  INIT      "Stay"
STTITLE0  INIT      "Status"    
.
SLTITLE0  INIT      "Selection Criteria"
.
EXNUMB_0  INIT      "Extract Number"
EXDISC_0  INIT      "Extract Description"
PROCED_0  INIT      "Procedure Status"
DTLIST_0  INIT      "Date on List"
EXDOCT_0  INIT      "Doctor"
EXRESD_0  INIT      "Resident Status"
LISTDT_0  INIT      "List Date"
REVIDT_0  INIT      "Review Date"
SCHADM_0  INIT      "Sch. Admission Date"
REMVDT_0  INIT      "Removal Date"
PREASS_0  INIT      "Pre-Assessment Date"
DATKEY_0  INIT      "Date Keyed in"
DATNSU_0  INIT      "Date on Suspension"
DATFSU_0  INIT      "Date off Suspension"
URNUMB_0  INIT      "U/R Number"
PROCD2_0  INIT      "Procedure"
CONSUL_0  INIT      "Consultant"
UNITFT_0  INIT      "Unit"
URGENC_0  INIT      "Urgency"
LISTST_0  INIT      "Listing Status"
ESTSTY_0  INIT      "Estimate Stay"
SHORTN_0  INIT      "Short Notice"
ADMSTY_0  INIT      "Admission Type"
REFSOU_0  INIT      "Referral Source"
REMRES_0  INIT      "Removal Reason"
REFEGP_0  INIT      "Referring GP"
EXPPOS_0  INIT      "Exp. Post Op Ward"
ADMITT_0  INIT      "Admitting Point"
LENTSN_0  INIT      "Letter Sent Code"
CHKRES_0  INIT      "Chk Resp. to Letter"
INCDES_0  INIT      "Include Deceased"
CLAIMT_0  INIT      "Claim Type"
.
UNSCHDL0  INIT      "Unschedule"
SCHDULE0  INIT      "Schedule"
PREADMD0  INIT      "Pre-Admitted"
ADMITED0  INIT      "Admitted"
DISCHRG0  INIT      "Discharge"
REMOVED0  INIT      "Removed"
PERFORM0  INIT      "Performed"
NOTPERM0  INIT      "Not Performed"
CANCEL_0  INIT      "Cancelled"
.
START__0  INIT      "Start"
FINISH_0  INIT      "Finish"
NA_____0  INIT      "N/A"
ALL____0  INIT      "All"
YES____0  INIT      "Yes"
NO_____0  INIT      "No"
.
EXCPMSA0  INIT      "Extract for extract id: "
EXCPMSB0  INIT      " is compeleted. " 
.
.         * symbols used in local program
.
FSLASH_0  INIT      "/"
ASTRIX_0  INIT      "*"
+
.------------------------------------------------------------------------------
.         Global variable for include files
.------------------------------------------------------------------------------
.
CODE      DIM       2         * pat/PATCODKY.PBL
DOCCODE   DIM       10        * pat/PATCODKY.PBL 
+
.------------------------------------------------------------------------------
.         Program constants
.------------------------------------------------------------------------------
.
PRGID     INIT      "IBAWAT56"
PRGNAM    INIT      "Waiting List Extract Enquiry Report"
VERSION   INIT      "V12.01.00"
+
.------------------------------------------------------------------------------
.         local variables - all local variables to program end with 56
.------------------------------------------------------------------------------
.
DAY___56  DIM       2
MONTH_56  DIM       2
YEAR__56  DIM       4
EXTID_56  DIM       4         * extract id
.
DIM8__56  DIM       8
DIM4__56  DIM       4
DIM3__56  DIM       3
PROCOD56  DIM       9         * procedure code
URNUMB56  DIM       8         * U/R number
PATNAM56  DIM       30        * patients full name
DESCRI56  DIM       20        * description
ONLIST56  DIM       10        * on list date
REVIEW56  DIM       10        * review date
SCHEDU56  DIM       10        * scheduled date
UNIT__56  DIM       8         * unit
DOCCOD56  DIM       10        * doctor
STAY__56  DIM       6         * length of stay
STATUS56  DIM       10        * status
COUNT_56  FORM      4         * keep track of the number of procedures
.
OPTION56  FORM      1         * temp variable
PDESCA56  DIM       20        * procedure description from
PDESCB56  DIM       20        * procedure description to
EXTDES56  DIM       30        * extract description
EXTDFM56  DIM       10        * extract date from 
EXTDTO56  DIM       10        * extract date to
EXFDOC56  DIM       10        * extract doct from
EXTDOC56  DIM       10        * extract doct to
EXTRES56  DIM       20        * extraxt resident status description
.
EXTLDF56  DIM       10        * extract list date from
EXTLDT56  DIM       10        * extract list date to
EXTRDF56  DIM       10        * extract review date from
EXTRDT56  DIM       10        * extract review date to
EXTSDF56  DIM       10        * extract sch. review date from
EXTSDT56  DIM       10        * extract sch. review date to
EXTRMF56  DIM       10        * extract removal date from
EXTRMT56  DIM       10        * extract removal date to
EXTPAF56  DIM       10        * extract pre-assessment date from
EXTPAT56  DIM       10        * extract pre-assessment date to
EXTKDF56  DIM       10        * extract keyed in date from
EXTKDT56  DIM       10        * extract keyed in date to
EXTSOF56  DIM       10        * extract date on suspension from
EXTSOT56  DIM       10        * extract date on suspension to
EXTSFF56  DIM       10        * extract date off suspension from
EXTSFT56  DIM       10        * extract date off suspension to
EXTURF56  DIM       8         * extract ur number from
EXTURT56  DIM       8         * extract ur number to
EXTPCF56  DIM       20        * extract procedure from
EXTPCT56  DIM       20        * extract procedure to
EXTCNF56  DIM       20        * extract consultant from
EXTCNT56  DIM       20        * extract consultant to
EXTUNF56  DIM       20        * extract unit from
EXTUNT56  DIM       20        * extract unit to
EXTUNR56  DIM       20        * extract urgency
EXTLST56  DIM       20        * extract listing status
EXTESF56  DIM       5         * extract est. length of stay from
EXTEST56  DIM       5         * extract est. length of stay to
EXTSHN56  DIM       20        * extract short notice
EXTADT56  DIM       20        * extract admission type
EXTRFS56  DIM       20        * extract referral source
EXTRMR56  DIM       20        * extract removal reason
EXTRGP56  DIM       20        * extract referring GP
EXTPOW56  DIM       20        * extract exp. post op ward
EXTADP56  DIM       20        * extract admitting point
EXTLSE56  DIM       3         * extract letter sent 
EXTCKR56  DIM       20        * extract cheack response to letter
EXTIDE56  DIM       20        * extract include deceased
EXTCLT56  DIM       20        * extract claim type
.
CATCOD56  DIM       2         * category code temp variable
CMDLIN56  DIM       50        * command line instruction string 
+
.------------------------------------------------------------------------------
.
.------------------------------------------------------------------------------
TMPINXA1  IFILE     SQL,FIXED=144,KEY="u1-8,9-16,17-25,26-27"
.
.Name     Type      Length    Physical  Start     Description
.----     ----      ------    --------  -----     -----------
_ONLIST   DIM       8         8         1         * on list date
_URNUMB   DIM       8         8         9        * U/R number
_PROCOD   DIM       9         9         17        * procedure code
_PROCNT   DIM       2         2         26        * procedure count
_PATNAM   DIM       30        30        28        * patients full name
_DESCRI   DIM       20        20        58        * description
_REVIEW   DIM       10        10        78        * review date
_SCHEDU   DIM       10        10        88        * scheduled date
_UNIT     DIM       8         8         98        * unit
_DOCCOD   DIM       10        10        116       * doctor
_STAY     DIM       6         6         126       * length of stay
_STATUS   DIM       10        10        132       * status
.End of Record                          ---
                                        144
.------------------------------------------------------------------------------
.         MAIN0000 - main entrance of execution
.------------------------------------------------------------------------------
.
MAIN0000  CALL      INIT0000  * initialisation of variables
.
MAIN1000  CALL      CTMP0000  * create temp file
          CALL      GTID0000  * get extract id    
          BRANCH    EXIT,MAIN9999
.
          CALL      RDEX0000  * read extracts
          CALL      PRTR0000  * print report
          CALL      EXCP0000  * extract completed msg
          GOTO      MAIN1000
.
MAIN9999  CHAIN     PGM
+
.------------------------------------------------------------------------------
.         EXCP0000 - Extract completed msg
.------------------------------------------------------------------------------
.
EXCP0000  DISPLAY   *P1:15,*EL,*P20:15,EXCPMSA0,*V2LON,EXTID_56:
                                       *HOFF,EXCPMSB0
.
          CALL      HITENTER
.
EXCP9999  RETURN
+
.------------------------------------------------------------------------------
.         CTMP0000 - create temp file
.------------------------------------------------------------------------------
.
CTMP0000  CLOSE     TMPINXA1
          PACK      CMDLIN56,SP70
          CLEAR     CMDLIN56
          APPEND    "iserase ",CMDLIN56
          APPEND    TFILNAME,CMDLIN56
          RESET     CMDLIN56
          EXECUTE   CMDLIN56,TASKID
.
          PACK      CMDLIN56,SP70
          CLEAR     CMDLIN56
          APPEND    "isbuild ",CMDLIN56
          APPEND    TFILNAME,CMDLIN56
          APPEND    " 144 u1-8,9-16,17-25,26-27 ",CMDLIN56
          RESET     CMDLIN56
          EXECUTE   CMDLIN56,TASKID
          OPEN      TMPINXA1,TFILNAME
.         
CTMP9999  RETURN
+
.------------------------------------------------------------------------------
.         INIT0000 - open/initialise variables
.------------------------------------------------------------------------------
.
INIT0000  CALL      IBACLOCK
          CALL      DISPHEAD
          CALL      TFILENAM
.
          DISPLAY   *P57:24,"Opening patcodes"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P65:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P65:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P65:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P65:24,"watmesaf"
          OPEN      WATMESA1,"watmesaf"
.
          DISPLAY   *P65:24,"watproaf"
          OPEN      WATPROA1,"watproaf"
.
          DISPLAY   *P65:24,"wattr1af"
          OPEN      WATTR1A1,"wattr1af"
.
          DISPLAY   *P65:24,"watepsaf"
          OPEN      WATEPSA1,"watepsaf"
.
          DISPLAY   *P65:24,"watextaf"
          OPEN      WATEXTA1,"watextaf"
.
          OPEN      PATWR1A1,"patwr1af"
          OPEN      WATLETR1,"watletrf"
.
INIT9999  RETURN          
+
.------------------------------------------------------------------------------
.         GTID0000  - get extract Id
.------------------------------------------------------------------------------
.
GTID0000  RESET     EXTID_56
          KEYIN     *P1:11,*EL,"Extract Id : ",*V2LON,EXTID_56;
.
          MATCH     SP70,EXTID_56
          GOTO      GTID9100 IF EOS
.
          CALL      CONTQST   
          BRANCH    CEXIT,GTID9000,GTID0000,GTID9100
          GOTO      GTID9000
.
GTID9000  MOVE      ZERO,EXIT
          GOTO      GTID9999
.
GTID9100  MOVE      ONE,EXIT
          GOTO      GTID9999
. 
GTID9999  RETURN
+
.------------------------------------------------------------------------------
.         RDEX0000 - Read Extract
.------------------------------------------------------------------------------
.
RDEX0000  PACK      KEY23,EXTID_56,SP70
          CALL      RSWTEPS1
RDEX1000  CALL      RKWTEPS1
          BRANCH    OVRCD,RDEX9999
.
          MATCH     EXTID_56,WTEPEID
          GOTO      RDEX9999 IF NOT EQUAL
.
          CALL      GTDT0000  * get patient details
.
          CALL      WRRD0000  * write patient into tempfile
        
          GOTO      RDEX1000
.
RDEX9999  RETURN
+
.------------------------------------------------------------------------------
.         BRDR0000 - Underline
.------------------------------------------------------------------------------
.
BRDR0000  PRINT     *30,"*-------------------------------":
                        "---------------------------------------*"
.
BRDR9999  RETURN
+
.------------------------------------------------------------------------------
.         REPC0000 - report cover sheet
.------------------------------------------------------------------------------
.
REPC0000  PACK      KEY4,EXTID_56
          CALL      RDWTEXT1
          BRANCH    OVRCD,REPC9999
.

          CALL      HEAD132A            *heading with no underline
.
          CALL      BRDR0000
.
          ADD       ONE,CLNO
.
          PRINT     *30,"|",*55,SLTITLE0,*101,"|" 
.
          ADD       ONE,CLNO  
.
          CALL      BRDR0000
.
          ADD       ONE,CLNO
.
          PRINT     *30,"|",*35,EXNUMB_0,*55,":",*57,EXTID_56,*101,"|"
.
          ADD       ONE,CLNO
.
          MATCH     SP70,WTEXDESC
          IF        @EQUAL
            MOVE      NA_____0,WTEXDESC
          ENDIF
          MOVE      WTEXDESC,EXTDES56
.
          PRINT     *30,"|",*35,EXDISC_0,*55,":",*57,EXTDES56,*101,"|"
.          
          ADD       ONE,CLNO
.
          MOVE      WTEXFPRS,OPTION56
          MOVE      START__0,PDESCA56
.
          LOAD      PDESCA56,OPTION56,UNSCHDL0:  * 1
                                      SCHDULE0:  * 2
                                      PREADMD0:  * 3
                                      ADMITED0:  * 4
                                      DISCHRG0:  * 5
                                      REMOVED0:  * 6
                                      PERFORM0:  * 7
                                      NOTPERM0:  * 8
                                      CANCEL_0   * 9
          
          
          MOVE      WTEXTPRS,OPTION56
          MOVE      FINISH_0,PDESCB56
.
          LOAD      PDESCB56,OPTION56,UNSCHDL0:  * 1
                                      SCHDULE0:  * 2
                                      PREADMD0:  * 3
                                      ADMITED0:  * 4
                                      DISCHRG0:  * 5
                                      REMOVED0:  * 6
                                      PERFORM0:  * 7
                                      NOTPERM0:  * 8
                                      CANCEL_0   * 9
.

          PRINT     *30,"|",*35,PROCED_0,*55,":",*57,PDESCA56:
                    *77,"To",*81,PDESCB56,*101,"|"
.
          ADD       ONE,CLNO
.
          MATCH     SP70,WTEXFLDT
          IF        !@EQUAL
            UNPACK    WTEXFLDT,YEAR__56,MONTH_56,DAY___56
            PACK      EXTDFM56,DAY___56,FSLASH_0,MONTH_56,FSLASH_0,YEAR__56
          ELSE
            MOVE    START__0,EXTDFM56
          ENDIF          
.
          MATCH     Z70,WTEXTLDT
          IF        !@EQUAL
            UNPACK    WTEXTLDT,YEAR__56,MONTH_56,DAY___56
            PACK      EXTDTO56,DAY___56,FSLASH_0,MONTH_56,FSLASH_0,YEAR__56
          ELSE
            MOVE    FINISH_0,EXTDTO56
          ENDIF

          PRINT     *30,"|",*35,DTLIST_0,*55,":",*57,EXTDFM56:
                    *77,"To",*81,EXTDTO56,*101,"|"
.
          ADD       ONE,CLNO
.
          MOVE      SP70,TDESC
          MATCH     SP70,WTEXRSTA
          IF        !@EQUAL
            MOVE      "T ",CATCOD56
            PACK      KEY5,CATCOD56,WTEXRSTA
            CALL      RDCODE1
            IF        OVRCD = 1
              MOVE      NA_____0,EXTRES56
            ENDIF
            MOVE       TDESC,EXTRES56
          ELSE
            MOVE      ALL____0,EXTRES56
          ENDIF
.
          PRINT     *30,"|",*35,EXRESD_0,*55,":",*57,EXTRES56:
                    *101,"|"
.
          MATCH     SP70,WTEXFVDT
          IF        !@EQUAL
            UNPACK    WTEXFVDT,YEAR__56,MONTH_56,DAY___56
            PACK      EXTRDF56,DAY___56,FSLASH_0,MONTH_56,FSLASH_0,YEAR__56
          ELSE
            MOVE    START__0,EXTRDF56
          ENDIF
.
          MATCH     Z70,WTEXTVDT
          IF        !@EQUAL
            UNPACK    WTEXTVDT,YEAR__56,MONTH_56,DAY___56
            PACK      EXTRDT56,DAY___56,FSLASH_0,MONTH_56,FSLASH_0,YEAR__56
          ELSE
            MOVE    FINISH_0,EXTRDT56
          ENDIF
.
          PRINT     *30,"|",*35,REVIDT_0,*55,":",*57,EXTRDF56:
                    *77,"To",*81,EXTRDT56,*101,"|"
.
          ADD       ONE,CLNO
.
          MATCH     SP70,WTEXFSDT
          IF        !@EQUAL
            UNPACK    WTEXFSDT,YEAR__56,MONTH_56,DAY___56
            PACK      EXTSDF56,DAY___56,FSLASH_0,MONTH_56,FSLASH_0,YEAR__56
          ELSE
            MOVE    START__0,EXTSDF56
          ENDIF
.
          MATCH     Z70,WTEXTSDT
          IF        !@EQUAL
            UNPACK    WTEXTVDT,YEAR__56,MONTH_56,DAY___56
            PACK      EXTSDT56,DAY___56,FSLASH_0,MONTH_56,FSLASH_0,YEAR__56
          ELSE
            MOVE    FINISH_0,EXTSDT56
          ENDIF
.
          PRINT     *30,"|",*35,SCHADM_0,*55,":",*57,EXTSDF56:
                    *77,"To",*81,EXTSDT56,*101,"|"
. 
          ADD       ONE,CLNO
.
          MATCH     SP70,WTEXFRDT
          IF        !@EQUAL
            UNPACK    WTEXFRDT,YEAR__56,MONTH_56,DAY___56
            PACK      EXTRMF56,DAY___56,FSLASH_0,MONTH_56,FSLASH_0,YEAR__56
          ELSE
            MOVE    START__0,EXTRMF56
          ENDIF
.
          MATCH     Z70,WTEXTRDT
          IF        !@EQUAL
            UNPACK    WTEXTRDT,YEAR__56,MONTH_56,DAY___56
            PACK      EXTRMT56,DAY___56,FSLASH_0,MONTH_56,FSLASH_0,YEAR__56
          ELSE
            MOVE    FINISH_0,EXTRMT56
          ENDIF
.
          PRINT     *30,"|",*35,REMVDT_0,*55,":",*57,EXTRMF56:
                    *77,"To",*81,EXTRMT56,*101,"|"
.
          ADD       ONE,CLNO
.
          MATCH     SP70,WTEXFPAD
          IF        !@EQUAL
            UNPACK    WTEXTPAD,YEAR__56,MONTH_56,DAY___56
            PACK      EXTPAF56,DAY___56,FSLASH_0,MONTH_56,FSLASH_0,YEAR__56
          ELSE
            MOVE    START__0,EXTPAF56
          ENDIF
.
          MATCH     Z70,WTEXTPAD
          IF        !@EQUAL
            UNPACK    WTEXTPAD,YEAR__56,MONTH_56,DAY___56
            PACK      EXTPAT56,DAY___56,FSLASH_0,MONTH_56,FSLASH_0,YEAR__56
          ELSE
            MOVE    FINISH_0,EXTPAT56
          ENDIF
.
          PRINT     *30,"|",*35,PREASS_0,*55,":",*57,EXTPAF56:
                    *77,"To",*81,EXTPAT56,*101,"|"
.
          ADD       ONE,CLNO
.
          MATCH     SP70,WTEXFSYD
          IF        !@EQUAL
            UNPACK    WTEXFSYD,YEAR__56,MONTH_56,DAY___56
            PACK      EXTKDF56,DAY___56,FSLASH_0,MONTH_56,FSLASH_0,YEAR__56
          ELSE
            MOVE    START__0,EXTKDF56
          ENDIF
.
          MATCH     Z70,WTEXTSYD
          IF        !@EQUAL
            UNPACK    WTEXTSYD,YEAR__56,MONTH_56,DAY___56
            PACK      EXTKDT56,DAY___56,FSLASH_0,MONTH_56,FSLASH_0,YEAR__56
          ELSE
            MOVE    FINISH_0,EXTKDT56
          ENDIF
.
          PRINT     *30,"|",*35,DATKEY_0,*55,":",*57,EXTKDF56:
                    *77,"To",*81,EXTKDT56,*101,"|"
.
          ADD       ONE,CLNO
.
          MATCH     SP70,WTEXFSUD
          IF        !@EQUAL
            UNPACK    WTEXFSUD,YEAR__56,MONTH_56,DAY___56
            PACK      EXTSOF56,DAY___56,FSLASH_0,MONTH_56,FSLASH_0,YEAR__56
          ELSE
            MOVE    START__0,EXTSOF56
          ENDIF
.
          MATCH     Z70,WTEXTSUD
          IF        !@EQUAL
            UNPACK    WTEXTSUD,YEAR__56,MONTH_56,DAY___56
            PACK      EXTSOT56,DAY___56,FSLASH_0,MONTH_56,FSLASH_0,YEAR__56
          ELSE
            MOVE    FINISH_0,EXTSOT56
          ENDIF
.
          PRINT     *30,"|",*35,DATNSU_0,*55,":",*57,EXTSOF56:
                    *77,"To",*81,EXTSOT56,*101,"|"
.
          ADD       ONE,CLNO
.
          MATCH     SP70,WTEXFOSD
          IF        !@EQUAL
            UNPACK    WTEXFOSD,YEAR__56,MONTH_56,DAY___56
            PACK      EXTSFF56,DAY___56,FSLASH_0,MONTH_56,FSLASH_0,YEAR__56
          ELSE
            MOVE    START__0,EXTSFF56
          ENDIF
.
          MATCH     Z70,WTEXTOSD
          IF        !@EQUAL
            UNPACK    WTEXTOSD,YEAR__56,MONTH_56,DAY___56
            PACK      EXTSFT56,DAY___56,FSLASH_0,MONTH_56,FSLASH_0,YEAR__56
          ELSE
            MOVE    FINISH_0,EXTSFT56
          ENDIF
.
          PRINT     *30,"|",*35,DATFSU_0,*55,":",*57,EXTSFF56:
                    *77,"To",*81,EXTSFT56,*101,"|"
.
          ADD       ONE,CLNO
.
          MATCH     SP70,WTEXFURN
          IF        !@EQUAL
            MOVE    WTEXFURN,EXTURF56
          ELSE
            MOVE    START__0,EXTURF56
          ENDIF
.
          MATCH     Z70,WTEXTURN
          IF        !@EQUAL
            MOVE   WTEXTURN,EXTURT56
          ELSE
            MOVE    FINISH_0,EXTURT56
          ENDIF
.
          PRINT     *30,"|",*35,URNUMB_0,*55,":",*57,EXTURF56:
                    *77,"To",*81,EXTURT56,*101,"|"
.
          ADD       ONE,CLNO
.
          MATCH     SP70,WTEXFPRO
          IF        !@EQUAL
            MOVE     WTEXFPRO,EXTPCF56
          ELSE
            MOVE    START__0,EXTPCF56
          ENDIF
.
          MATCH     Z70,WTEXTPRO
          IF        !@EQUAL
            MOVE     WTEXTPRO,EXTPCT56
          ELSE
            MOVE     FINISH_0,EXTPCT56
          ENDIF
.
          PRINT     *30,"|",*35,PROCD2_0,*55,":",*57,EXTPCF56:
                    *77,"To",*81,EXTPCT56,*101,"|"
.
          ADD       ONE,CLNO
.
          MATCH     SP70,WTEXFCON
          IF        !@EQUAL
            MOVE     WTEXFCON,EXTCNF56
          ELSE
            MOVE    START__0,EXTCNF56
          ENDIF
.
          MATCH     Z70,WTEXTCON
          IF        !@EQUAL
            MOVE     WTEXTCON,EXTCNT56
          ELSE
            MOVE     FINISH_0,EXTCNT56
          ENDIF
.
          PRINT     *30,"|",*35,CONSUL_0,*55,":",*57,EXTCNF56:
                    *77,"To",*81,EXTCNT56,*101,"|"
.
          ADD       ONE,CLNO
.
          MATCH     SP70,WTEXFUNI
          IF        !@EQUAL
            MOVE     WTEXFUNI,EXTUNF56
          ELSE
            MOVE    START__0,EXTUNF56
          ENDIF
.
          MATCH     Z70,WTEXTUNI
          IF        !@EQUAL
            MOVE     WTEXTUNI,EXTUNT56
          ELSE
            MOVE     FINISH_0,EXTUNT56
          ENDIF
.
          PRINT     *30,"|",*35,UNITFT_0,*55,":",*57,EXTUNF56:
                    *77,"To",*81,EXTUNT56,*101,"|"
.
          ADD       ONE,CLNO
.
          MOVE      SP70,TDESC
          MATCH     SP70,WTEXPRI
          IF        !@EQUAL
            MOVE      "TP",CATCOD56
            PACK      KEY5,CATCOD56,WTEXPRI
            CALL      RDCODE1
            IF        OVRCD = 1
              MOVE      NA_____0,EXTUNR56
            ENDIF
            MOVE       TDESC,EXTUNR56
          ELSE
            MOVE     ALL____0,EXTUNR56
          ENDIF
.
          PRINT     *30,"|",*35,URGENC_0,*55,":",*57,EXTUNR56:
                    *101,"|"
.
          ADD       ONE,CLNO
.
          MOVE      SP70,TDESC
          MATCH     SP70,WTEXLIST
          IF        !@EQUAL
            MOVE      "VL",CATCOD56
            PACK      KEY5,CATCOD56,WTEXLIST
            CALL      RDCODE1
            IF        OVRCD = 1
              MOVE      NA_____0,EXTLST56
            ENDIF
            MOVE       TDESC,EXTLST56
          ELSE
            MOVE     ALL____0,EXTLST56
          ENDIF
.
          PRINT     *30,"|",*35,LISTST_0,*55,":",*57,EXTLST56:
                    *101,"|"
.
          ADD       ONE,CLNO
.
          MOVE      SP70,TDESC
          MATCH     SP70,WTEXNOT
          IF        !@EQUAL
            MOVE      "WS",CATCOD56
            PACK      KEY5,CATCOD56,WTEXNOT
            CALL      RDCODE1
            IF        OVRCD = 1
              MOVE      NA_____0,EXTSHN56
            ENDIF
            MOVE       TDESC,EXTSHN56
          ELSE
            MOVE     ALL____0,EXTSHN56
          ENDIF
.
          PRINT     *30,"|",*35,SHORTN_0,*55,":",*57,EXTSHN56:
                    *101,"|"
.
          ADD       ONE,CLNO
.
          MOVE      SP70,TDESC
          MATCH     SP70,WTEXADTY
          IF        !@EQUAL
            MOVE      "A ",CATCOD56
            PACK      KEY5,CATCOD56,WTEXADTY
            CALL      RDCODE1
            IF        OVRCD = 1
              MOVE      NA_____0,EXTADT56
            ENDIF
            MOVE       TDESC,EXTADT56
          ELSE
            MOVE     ALL____0,EXTADT56
          ENDIF
.
          PRINT     *30,"|",*35,ADMSTY_0,*55,":",*57,EXTADT56:
                    *101,"|"
.
          ADD       ONE,CLNO
.
          MOVE      SP70,TDESC
          MATCH     SP70,WTEXUD1
          IF        !@EQUAL
            MOVE      "W1",CATCOD56
            PACK      KEY5,CATCOD56,WTEXUD1
            CALL      RDCODE1
            IF        OVRCD = 1
              MOVE      NA_____0,EXTRFS56
            ENDIF
            MOVE       TDESC,EXTRFS56
          ELSE
            MOVE     ALL____0,EXTRFS56
          ENDIF
.
          PRINT     *30,"|",*35,REFSOU_0,*55,":",*57,EXTRFS56:
                    *101,"|"
.
          ADD       ONE,CLNO
.
          MOVE      SP70,TDESC
          MATCH     SP70,WTEXRMR
          IF        !@EQUAL
            MOVE      "WR",CATCOD56
            PACK      KEY5,CATCOD56,WTEXRMR
            CALL      RDCODE1
            IF        OVRCD = 1
              MOVE      NA_____0,EXTRMR56
            ENDIF
            MOVE       TDESC,EXTRMR56
          ELSE
            MOVE     ALL____0,EXTRMR56
          ENDIF
.
          PRINT     *30,"|",*35,REMRES_0,*55,":",*57,EXTRMR56:
                    *101,"|"
.
          ADD       ONE,CLNO
.
          MATCH     SP70,WTEXRGP
          IF        !@EQUAL
            MOVE       WTEXRGP,EXTRGP56
          ELSE
            MOVE    NA_____0,EXTRGP56
          ENDIF
.
          PRINT     *30,"|",*35,REFEGP_0,*55,":",*57,EXTRGP56:
                    *101,"|"
.
          ADD       ONE,CLNO
.
          MOVE      SP70,WBDESC
          MATCH     SP70,WTEXPOWD
          IF        !@EQUAL
            PACK      KEY6,WTEXPOWD,SP70
            CALL      RDWARD1
            IF        OVRCD = 0
              MATCH     WWARD,WTEXPOWD
              IF        @EQUAL
                MOVE      WBDESC,EXTPOW56
              ENDIF
            ELSE
              MOVE      NA_____0,EXTPOW56
            ENDIF
          ELSE
            MOVE      NA_____0,EXTPOW56
          ENDIF
.
          PRINT     *30,"|",*35,EXPPOS_0,*55,":",*57,EXTPOW56:
                    *101,"|"
.
          ADD       ONE,CLNO
.
          MOVE      SP70,TDESC
          MATCH     SP70,WTEXADMW
          IF        !@EQUAL
            MOVE      "ap",CATCOD56
            PACK      KEY5,CATCOD56,WTEXADMW
            CALL      RDCODE1
            IF        OVRCD = 1
              MOVE      NA_____0,EXTADP56
            ENDIF
            MOVE       TDESC,EXTADP56
          ELSE
            MOVE     ALL____0,EXTADP56
          ENDIF
.
          PRINT     *30,"|",*35,ADMITT_0,*55,":",*57,EXTADP56:
                    *101,"|"
.
.
          ADD       ONE,CLNO
.
          MOVE      SP70,TDESC
          MATCH     SP70,WTEXCTYP
          IF        !@EQUAL
            MOVE      "CL",CATCOD56
            PACK      KEY5,CATCOD56,WTEXCTYP
            CALL      RDCODE1
            IF        OVRCD = 1
              MOVE      NA_____0,EXTCLT56
            ENDIF
            MOVE       TDESC,EXTCLT56
          ELSE
            MOVE     ALL____0,EXTCLT56
          ENDIF
.
          PRINT     *30,"|",*35,CLAIMT_0,*55,":",*57,EXTCLT56:
                    *101,"|"
.
          ADD       ONE,CLNO
.         
          MOVE      WTEXFSES,F5
          COMPARE   ZERO,F5
          IF        !@EQUAL
            MOVE      WTEXFSES,EXTESF56
          ELSE
            MOVE      NA_____0,EXTESF56
          ENDIF
.
          MOVE      WTEXTSES,F5
          COMPARE   ZERO,F5
          IF        !@EQUAL
            MOVE      WTEXTSES,EXTEST56
          ELSE
            MOVE      NA_____0,EXTEST56
          ENDIF
.
          PRINT     *30,"|",*35,ESTSTY_0,*55,":",*57,EXTESF56:
                    *77,"To",*81,EXTEST56,*101,"|"
.          
          ADD       ONE,CLNO
.
          MATCH     SP70,WTEXLLSC
          IF        !@EQUAL
            PACK      KEY6,WTEXLLSC,SP1,SP1,ZERO,SP70
            CALL      RDLET1
            MATCH     DWLETNO,WTEXLLSC
            IF        @EQUAL
              MOVE      WLTEXT,EXTLSE56
            ENDIF
          ELSE
            MOVE    NA_____0,EXTLSE56
          ENDIF                    
.
          PRINT     *30,"|",*35,LENTSN_0,*55,":",*57,EXTLSE56:
                    *101,"|"
.
          ADD       ONE,CLNO
.
          MOVE      WTEXCRTL,F1
          COMPARE     ZERO,F1
          IF        !@EQUAL
            MOVE      YES____0,EXTCKR56
          ELSE
            MOVE      NO_____0,EXTCKR56
          ENDIF
.
          PRINT     *30,"|",*35,CHKRES_0,*55,":",*57,EXTCKR56:
                    *101,"|"

.
          ADD       ONE,CLNO
.
          MOVE      WTEXINCD,F1
          COMPARE     ZERO,F1
          IF        !@EQUAL
            MOVE      YES____0,EXTIDE56
          ELSE
            MOVE      NO_____0,EXTIDE56
          ENDIF
.
          PRINT     *30,"|",*35,INCDES_0,*55,":",*57,EXTIDE56:
                    *101,"|"
                    
.
          CALL      BRDR0000
.
REPC9999  RETURN
+
.------------------------------------------------------------------------------
.         PRTR000 - print extract report         
.------------------------------------------------------------------------------
.
PRTR0000  MOVE      ZERO,CPAGENO
          CALL      REPC0000
          CALL      HEAD0000
.
          PACK      KEY27,SP70
          CALL      PSTEMP00
PRTR1000  CALL      RNTEMP00
          BRANCH    OVRCD,PRTR8000
. 
          COMPARE   "60",CLNO           * 60 lines per a page
          IF        !@LESS
            CALL      UND132
            CALL      HEAD0000 
          ENDIF
.
          COMPARE   "3",CLNO            * print the column on the 3rd line
          IF @EQUAL
            CALL      CLHD0000          * print column header
          ENDIF
.
          CALL      UND132
.
          MOVE      SP70,ONLIST56
          UNPACK    _ONLIST,YEAR__56,MONTH_56,DAY___56
          PACK      ONLIST56,DAY___56,FSLASH_0,MONTH_56,FSLASH_0,YEAR__56
.         
.         
          SQUEEZE   _UNIT
          MOVE      _UNIT,DIM4__56
          SQUEEZE   _STAY
          MOVE      _STAY,DIM3__56
          SQUEEZE   _STATUS
          MOVE      _STATUS,DIM8__56
          SQUEEZE   _DOCCOD
          SQUEEZE   ONLIST56
.
          PRINT     *1,"|",*2,_URNUMB:
                    *10,"|",*11,_PATNAM:
                    *42,"|",*43,_DESCRI:
                    *63,"|",*64,ONLIST56:
                    *78,"|",*79,_REVIEW:
                    *89,"|",*90,_SCHEDU:
                    *100,"|",*101,DIM4__56:
                    *106,"|",*107,_DOCCOD:
                    *117,"|",*118,DIM3__56:
                    *122,"|",*123,DIM8__56:
                    *132,"|"

.
          ADD       ONE,CLNO
          ADD       ONE,COUNT_56        * count the number of procedures
.
          GOTO      PRTR1000
.
PRTR8000  CALL      UND132
.         
          ADD       ONE,CLNO
.
          PRINT     *1,"Total Number of Procedure(s) = ":
                    *32,COUNT_56
.
          ADD       ONE,CLNO
          PRINT     *1,"Note: (*) next to the patient's name indicates a deleted patient"
.
PRTR9999  RETURN
+
.------------------------------------------------------------------------------
.         CLHD0000 - print column heading
.------------------------------------------------------------------------------
.
CLHD0000  CALL      UND132
.
          PRINT     *1,"|",*2,URTITLE0:
                    *10,"|",*11,PATITLE0:
                    *42,"|",*43,DSTITLE0:
                    *63,"|",*64,OLTITLE0:
                    *78,"|",*79,RVTITLE0:
                    *89,"|",*90,SCTITLE0:
                    *100,"|",*101,UNTITLE0:
                    *106,"|",*107,DOTITLE0:
                    *117,"|",*118,SATITLE0:
                    *122,"|",*123,STTITLE0:
                    *132,"|"
.
CLHD9999  RETURN
+
.------------------------------------------------------------------------------
.         HEAD0000 - print the heading
.------------------------------------------------------------------------------
.
HEAD0000  MOVE      ONE,CLNO
.
          CALL      HEAD132A   *heading with underline
.
HEAD9999  RETURN
+
.------------------------------------------------------------------------------
.         GTDT0000 - get patients details
.------------------------------------------------------------------------------
.
GTDT0000  MOVE      SP70,PROCOD56            * procedure code
          MOVE      SP70,URNUMB56            * U/R number
          MOVE      SP70,PATNAM56            * patients full name
          MOVE      SP70,DESCRI56            * description
          MOVE      SP70,ONLIST56            * on list date
          MOVE      SP70,REVIEW56            * review date
          MOVE      SP70,SCHEDU56            * scheduled date
          MOVE      SP70,UNIT__56            * unit
          MOVE      SP70,DOCCOD56            * doctor
          MOVE      SP70,STAY__56            * length of stay
          MOVE      SP70,STATUS56            * status
          MOVE      SP70,DAY___56
          MOVE      SP70,MONTH_56
          MOVE      SP70,YEAR__56
.
          PACK      KEY8,WTEPURN,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,GTDT9999
.
          * get patient name  
          SQUEEZE   PTITL
          SQUEEZE   PGNAME
          SQUEEZE   PSNAME
.
          MATCH     "1",WTEPDEL 
          IF        @EQUAL
             PACK      PATNAM56,ASTRIX_0,SP1,PTITL,SP1,PGNAME,SP1,PSNAME
          ELSE
             PACK      PATNAM56,PTITL,SP1,PGNAME,SP1,PSNAME
          ENDIF
.
          PACK      KEY19,WTEPURN,WTEPPRO,WTEPCNT,SP70
          CALL      RDTREA1
          BRANCH    OVRCD,GTDT9999
.
          
          * patient UR         (WMURNO  )
          MOVE      WTEPURN,URNUMB56
.
          * get procedure      (WMCODE  )
          MOVE      WMCODE,PROCOD56
.
          * description        (WMDESC  )
          MOVE      WMDESC,DESCRI56
.
          * on list date       (WMDATE1 )
          MATCH     SP70,WMDATE1
          IF        !@EQUAL
            UNPACK    WMDATE1,YEAR__56,MONTH_56,DAY___56
            PACK      ONLIST56,DAY___56,FSLASH_0,MONTH_56,FSLASH_0,YEAR__56
          ENDIF
.
.         * review date        (WMDATE2 )
          MATCH     SP70,WMDATE2
          IF        !@EQUAL
            UNPACK    WMDATE2,YEAR__56,MONTH_56,DAY___56
            PACK      REVIEW56,DAY___56,FSLASH_0,MONTH_56,FSLASH_0,YEAR__56
          ENDIF
.
.         * scheduled date     (WMDATE3 )
          MATCH     SP70,WMDATE3
          IF        !@EQUAL
            UNPACK    WMDATE3,YEAR__56,MONTH_56,DAY___56
            PACK      SCHEDU56,DAY___56,FSLASH_0,MONTH_56,FSLASH_0,YEAR__56
          ENDIF
.
.         * unit               (WMUNIT  )
          MATCH     SP70,WMUNIT
          IF        !@EQUAL
            MOVE      WMUNIT,UNIT__56
          ENDIF
.
.         * doctor             (WMDOCTOR)
          MATCH     SP70,WMDOCTOR
          IF        !@EQUAL
            MOVE      WMDOCTOR,DOCCOD56
          ENDIF
.
.         * stay               (WMSTAY  )
          COMPARE   ZERO,WMSTAY
          IF        !@EQUAL
            MOVE      WMSTAY,STAY__56
          ELSE
            MOVE    "0",STAY__56
          ENDIF
          SQUEEZE   STAY__56
.
.         * status             (DWMSTAT )
          MATCH     SP70,DWMSTAT
          IF        !@EQUAL
            LOAD      STATUS56,WMSTAT,UNSCHDL0:  * 1
                                      SCHDULE0:  * 2
                                      PREADMD0:  * 3
                                      ADMITED0:  * 4
                                      DISCHRG0:  * 5
                                      REMOVED0:  * 6
                                      PERFORM0:  * 7
                                      NOTPERM0:  * 8
                                      CANCEL_0   * 9

          ENDIF
.
GTDT9999  RETURN
+
.------------------------------------------------------------------------------
.
.------------------------------------------------------------------------------
.
WRRD0000  MOVE      SP70,DAY___56                 * initialise local program 
          MOVE      SP70,MONTH_56                * variables
          MOVE      SP70,YEAR__56
.
.         * initialise temp fields
.
          MOVE      SP70,_ONLIST      
          MOVE      SP70,_URNUMB       
          MOVE      SP70,_PROCOD        
          MOVE      SP70,_PROCNT         
          MOVE      SP70,_PATNAM          
          MOVE      SP70,_DESCRI           
          MOVE      SP70,_REVIEW            
          MOVE      SP70,_SCHEDU            
          MOVE      SP70,_UNIT              
          MOVE      SP70,_DOCCOD            
          MOVE      SP70,_STAY            
          MOVE      SP70,_STATUS
.
.         format date YYYYMMDD
.
          UNPACK    ONLIST56,DAY___56,DIM1,MONTH_56,DIM1,YEAR__56
          PACK      _ONLIST,YEAR__56,MONTH_56,DAY___56            * on list date
.
.         move local program variables to temp fields
.
          MOVE      URNUMB56,_URNUMB            * U/R number
          MOVE      PROCOD56,_PROCOD
          MOVE      WTEPCNT,_PROCNT            * procedure code
          MOVE      PATNAM56,_PATNAM            * patients full name
          MOVE      DESCRI56,_DESCRI            * description
          MOVE      REVIEW56,_REVIEW            * review date
          MOVE      SCHEDU56,_SCHEDU            * scheduled date
          MOVE      UNIT__56,_UNIT              * unit
          MOVE      DOCCOD56,_DOCCOD            * doctor
          MOVE      STAY__56,_STAY              * length of stay
          MOVE      STATUS56,_STATUS            * status
.
          PACK      KEY27,_ONLIST,_URNUMB,_PROCOD,_PROCNT,SP70
          CALL      RDTEMP00
          IF        OVRCD = 1
            CALL    WRTEMP00          
          ENDIF
.
WRRD9999  RETURN
+
.------------------------------------------------------------------------------
.         WRTMP00 - write record to temp file
.------------------------------------------------------------------------------
.
WRTEMP00   RESET     KEY27
           WRITE     TMPINXA1,KEY27;KEY27,_PATNAM,_DESCRI,_REVIEW,_SCHEDU:
                                   _UNIT,_DOCCOD,_STAY,_STATUS
.
WRTEMP99   RETURN
+
.------------------------------------------------------------------------------
.
.------------------------------------------------------------------------------
.
RDTEMP00  MOVE      ZERO,OVRCD
          RESET     KEY27
          READ      TMPINXA1,KEY27;_PATNAM,_DESCRI,_REVIEW,_SCHEDU:
                                   _UNIT,_DOCCOD,_STAY,_STATUS
.
          GOTO      OVERCOND IF OVER
.
RDTEMP99  RETURN
+
.------------------------------------------------------------------------------
.
.------------------------------------------------------------------------------
.
PSTEMP00  RESET     KEY27
          READ      TMPINXA1,KEY27;;
.
PSTEMP99  RETURN
+
.------------------------------------------------------------------------------
.
.------------------------------------------------------------------------------
.
RNTEMP00  MOVE      ZERO,OVRCD
          READKS    TMPINXA1;_ONLIST,_URNUMB,_PROCOD,_PROCNT,_PATNAM,_DESCRI:
                             _REVIEW,_SCHEDU,_UNIT,_DOCCOD,_STAY,_STATUS
.
          GOTO      OVERCOND IF OVER
.
RNTEMP99  RETURN
+
.------------------------------------------------------------------------------
.         Library includes 
.------------------------------------------------------------------------------
.
          INC       STD001IO
          INC       PATCODKY
          INC       PATDOCKY
+
.------------------------------------------------------------------------------
.         I/O includes
.------------------------------------------------------------------------------
.
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATMA1IO/INC
          INC       WATMESIO/INC
          INC       WATPROIO/INC
          INC       WATTR1IO/INC
          INC       WATEPSIO/INC
          INC       WATEXTIO/INC
          INC       PATWR1IO/INC
          INC       WATLETIO/INC
          INC       TFILENAM
          INC       IBASEQIO/INC
          INC       WEBERRIO/INC
+
.------------------------------------------------------------------------------



.----------------------------------------------------------------------T
. Program       : FAILTEST
.
. Function      : Server Fail Over Testing
.
. Modifications : 
. V12.00.01 22/05/2025  Alina Bhari   TSK 0955096
.           Added alpanumeric visit number generation 
.           -PATWRD40,CLRWRD00,BDWR1000,BDWR1000,ONLV1000,STBY0000,CALCP100
. V10.04.01 31.03.2014  B.G.Ackland  CAR 299363
.           Replace META Tag Redirect with Javascript in Common RDIREC00
.           Routine
.----------------------------------------------------------------------T
. V10.01.01 10/03/2011 Jill Parkinson CAR 233088    
.           Added pmsvx1af                         
.----------------------------------------------------------------------T
. V9.05.01  09/03/2006  Ebon Clements CAR 78533
.           Mods for PATCODTM - Hospital specific category codes
. V9.05.B01 30/01/2006  Ebon Clements CAR 93186
.           Mods for REDIRECT length change. Recompiled for IBAWEBDF
.----------------------------------------------------------------------T
. V9.03.01  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2      
.           - Add Sex Description routine SEXDSC00 with Code "R" - "Intersex"
.----------------------------------------------------------------------
          INC       IBAWEBDF    
          INC       WEBDATDF    
          INC       PATADWFD/INC        * Ward Audit File
          INC       PATCONFD/INC
          INC       BOKRC1FD/INC
          INC       MRTLOCFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATNOBFD/INC
          INC       PATNURFD/INC
          INC       PATDO1FD/INC
          INC       PATVISFD/INC
          INC       PATWR1FD/INC        * Patient Ward/Bed File   
          INC       PATWBHFD/INC        * Ward Bed History File
          INC       PATONLFD/INC
          INC       PATCALAG/INC
          INC       PATHSPFD/INC        * Hospital File
          INC       PATPR1FD/INC
.
.----------------------------------------------------------------------
.
.  CGI Parameters
.
BEDCOUNT  FORM      2
THISWARD  DIM       3
ENDNOW    FORM      1
BEDSONLY  FORM      1
EMPTONLY  FORM      1               
EFCTDATE  DIM       8      * Expected Date
FORMACTN  DIM       5               
NEXTPAGE  DIM       1      * Nextpage parameter CGI parameter
STARTKEY  DIM       25     * Starting Key
UPDTTYPE  DIM       1      * Update Type
.
PTWRD001  DIM       3      * Ward Number       
PTWRD002  DIM       3      * Ward Bed Number
PTWRD003  DIM       20     * Ward Bed Description
PTWRD004  DIM       8      * Current Patient Admission No.
PTWRD005  DIM       4      * Extension No. 
PTWRD006  FORM      2      * No Lines B/WN Beds (WRD ONLY) 
PTWRD007  DIM       3      * Room Type (Cat RT) (Beds Only)
PTWRD008  DIM       3      * E.F.R (Cat BT) (Beds Only)
PTWRD009  FORM      3      * No. of Beds in Ward (Ward Only)
PTWRD010  FORM      3      * HCS Ward Subscript (Ward Only)
PTWRD011  DIM       3      * Ward Service Code (Ward Only)
PTWRD012  FORM      1      * Plurality No.
PTWRD013  FORM      1      * Ward Only Input (WRD INDICATOR)
PTWRD014  FORM      3      * No. of Beds in Ward (OCC STATS)
PTWRD015  DIM       3      * Ward Classification (Cat CG)
PTWRD016  DIM       8      * Current standby Admission No.
PTWRD017  FORM      1      * Active Indicator 0=active 1=Inactive
PTWRD018  DIM       3      * Hospital/Unit Number
PTWRD019  FORM      1      * Day/Night Indicator 0=Day Only 1=Day & Night 
.                                                2=Night Only 
PTWRD020  DIM       3      * Intended Age Group (Cat IA)
PTWRD021  DIM       3      * Intended Care Intensity (Cat IC)
PTWRD022  DIM       1      * Intended Sex of Patient M/F/E
PTWRD023  DIM       4      * Medical Record Location Code
PTWRD024  DIM       14     * G/L and Cost Centre Code  
PTWRD025  DIM       3      * Nursing station number
.
.  Variable Declarations
.
.
ACTION    DIM       1      * These Variables are declared in PATADWFD, 
ADD       DIM       2      * however are edited out!!  
AMM       DIM       2      * Once the transition is made from old software to
AYY       DIM       2      * WEB it will probably be a good idea to delete  
ACC       DIM       2      * theses declerations and allow them to be declared
OPERCODE  DIM       4      * in PATADWFD!!
.
.
CODERT    INIT      "RT"
EXPDISCH  FORM      1
WARDLINK  DIM       60
CURRDATE  DIM       8
TOTALPRE  FORM      8
TOTALBOK  FORM      8
TOTALBED  FORM      8
TOTALPAT  FORM      8
FLAG      FORM      1
FORM8     FORM      8
SAVKEY6   DIM       6
LASTWARD  DIM       3 
LOCNCODE  DIM       4
PSAGECON  DIM       3
PSAGETYP  DIM       3
WARDCODE  DIM       3
WARDNAME  DIM       20
RECCOUNT  FORM      4
DISPNUMB  FORM      4
ADMNOD8   DIM       8               
GENDER    DIM       8              
DOCTCODE  DIM       6
DOCGNAME  DIM       25
DOCSNAME  DIM       20              
DOCTITLE  DIM       9              
PDIAGNOS  DIM       50
EFTDATE   FORM      8
HISFLAG   FORM      1
PWARD     DIM       3 
PBED      DIM       3 
BEDSTAT   FORM      1
FEMALE    INIT      "F"
MALE      INIT      "M"               
INDETER   INIT      "I"
SAVEWARD  DIM       3
PATCOUNT  FORM      8
PREADMIS  FORM      8
BOOKINGS  FORM      8
SBYCOUNT  FORM      8
TODAY     FORM      8
FILWIDTH  DIM       10
FILHIGHT  DIM       12
FILHIGH   FORM      3 
PERCENT   FORM      3.1
.
SEC1      FORM      "00001"
SACTIVE   FORM      1       * Initialise active indicator
SNOBEDS   FORM      3       * Initialise number of beds
SWBTYPE   DIM       3       * Initialise ward class/room type
.
ONLYDESC  DIM       3
ACTVDESC  DIM       3
ROOMDESC  DIM       25
BEDTDESC  DIM       25
.
.---------------------------------------------------------------------------
PRGID     INIT      "FAILTEST"
VERSION   INIT      "V12.01.00"
PRGNAM    INIT      "Fail Over Server Testing"
.---------------------------------------------------------------------------
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "INVALID OPTION",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
          MOVE      "Hospital Ward/Bed Scan",WEBTITLE
          CALL      WEBHED00
          BRANCH    EXIT,MAIN1000
          CALL      WEBBOD00
          CALL      WEBEND00
          GOTO      MAIN1000
.
MAIN1200  OPEN      CONTROLF,"nofile"
          GOTO      MAIN1000
.
MAIN9999  SHUTDOWN  ""
.------------------------------------------------------------
. Output Next Page Based on CGI Parameter nextpage
.------------------------------------------------------------
NXTPAG00  MOVE      ZERO,F1
          MOVE      NEXTPAGE,F1
          BRANCH    F1,NXTPAG10,NXTPAG20,NXTPAG30,NXTPAG40
.
          CALL      WINCLS00
          GOTO      NXTPAG99
.
NXTPAG10  CALL      RDIREC00
          GOTO      NXTPAG99
.
NXTPAG20  CALL      GOBACK00      * Go Back 1 Html page
          GOTO      NXTPAG99
.
NXTPAG30  CALL      DAH20000      * Go Back 2 html pages
          GOTO      NXTPAG99
.
NXTPAG40  CALL      GOBACK00
          GOTO      NXTPAG99
.
NXTPAG99  RETURN
.-------------------------------------------------------------------------------
.  Goes back 1 page
.-------------------------------------------------------------------------------
GOBACK00  CALL      OPENHTML
          BRANCH    EXIT,GOBACK99
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "    alert(#"",WEBTITLE,"#");":
                             "    self.close();":
                             "    opener.history.go(-1);":
                             "}":
                             "</script>"
          CALL      CLOSHTML
GOBACK99  RETURN
.------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------
INIT0000  OPEN      PATVISA1,"patvisaf"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PATPX1A1,"patpx1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATWR1A1,"patwr1af"
          OPEN      MRTLOCA1,"mrtlocaf"
.
          OPEN      PATMA1A1,"patma1af"  
          OPEN      PATMX1A1,"patmx1af"  
          OPEN      PATMI1A1,"patmi1af"  
          OPEN      PATWR1A1,"patwr1af"  
          OPEN      PATWR1A3,"patwr1af"  
          OPEN      PATDO1A1,"patdo1af"  
          OPEN      BOKRC1A7,"bokrc1af"  
.
          OPEN      PATHSPA1,"pathspaf"   * Hospital File
          OPEN      PATWBHA1,"patwbhaf"  
          OPEN      PATVISA1,"patvisaf"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
          OPEN      PATONLV1,"patonlvf"
          OPEN      PATNOBE1,"patnobef"
          OPEN      PATNURS1,"patnursm"
          OPEN      PATWAUD,"patwaud"
.
          READ      CONTROLF,TEN;*223,CAUDW
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          IF        IBCNMHOS=1
            OPEN      MLTCODA1,"mltcodaf"
            OPEN      MLTCODA2,"mltcodaf"
          ENDIF
.
INIT9999  RETURN
.------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          MOVE      SP70,TEMPLATE
          MOVE      ZERO,STARTNUM
          MOVE      SP70,UPDTTYPE
          MOVE      SP70,NEXTPAGE
          PACK      REDIRECT,SP70,SP70,SP70,SP70
          MOVE      SP70,WARDCODE
          MOVE      SP70,FORMACTN
          MOVE      ZERO,NORECORD
          MOVE      ZERO,EMPTONLY
          MOVE      ZERO,BEDSONLY
          MOVE      ZERO,EXPDISCH
          MOVE      SP70,EFCTDATE
          MOVE      SP70,STARTKEY
.
          MOVE      SP70,PTWRD001 
          MOVE      SP70,PTWRD002 
          MOVE      SP70,PTWRD003 
          MOVE      SP70,PTWRD004 
          MOVE      SP70,PTWRD005 
          MOVE      ZERO,PTWRD006 
          MOVE      SP70,PTWRD007 
          MOVE      SP70,PTWRD008 
          MOVE      ZERO,PTWRD009 
          MOVE      ZERO,PTWRD010 
          MOVE      SP70,PTWRD011 
          MOVE      ZERO,PTWRD012
          MOVE      ZERO,PTWRD013
          MOVE      ZERO,PTWRD014
          MOVE      SP70,PTWRD015
          MOVE      SP70,PTWRD016
          MOVE      ZERO,PTWRD017
          MOVE      SP70,PTWRD018
          MOVE      ZERO,PTWRD019 
          MOVE      SP70,PTWRD020 
          MOVE      SP70,PTWRD021 
          MOVE      SP70,PTWRD022 
          MOVE      SP70,PTWRD023 
          MOVE      SP70,PTWRD024 
          MOVE      SP70,PTWRD025 
.
          RETURN
.------------------------------------------------------------
. Set Parameters
.------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "urnumber=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,URNUMBER
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updttype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDTTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nextpage=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NEXTPAGE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "redirect=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REDIRECT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "admissno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,admissno
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startnum=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTNUM
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startkey=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTKEY
            PACK      STARTKEY,STARTKEY,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "efctdate=",QRYNAME
          IF        @EQUAL
            RESET     QRYDATA 
            MATCH     SP70,QRYDATA
            GOTO      SETPAR99 IF EQUAL
            UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
            CALL      SETMTH00
            PACK      EFCTDATE,CCENT,CYEAR,CMON,CDAY
            REP       " 0",EFCTDATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "norecord=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NORECORD
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "wardcode=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,WARDCODE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "emptonly=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,EMPTONLY
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "bedsonly=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,BEDSONLY
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "formactn=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FORMACTN
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "ptwrd",QRYNAME
          IF        @EQUAL
            CALL      STWRD000
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
.-------------------------------------------------------------------------------
.   Set Parameters for Option 4 - Ward Bed Details
.-------------------------------------------------------------------------------
STWRD000  UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,STWRD001,STWRD002,STWRD003,STWRD004,STWRD005,STWRD006:
                       STWRD007,STWRD008,STWRD009,STWRD010,STWRD011,STWRD012:
                       STWRD013,STWRD014,STWRD015,STWRD016,STWRD017,STWRD018:
                       STWRD019,STWRD020,STWRD021,STWRD022,STWRD023,STWRD024:
                       STWRD025
.
          GOTO      STWRD999
.
STWRD001  SQUEEZE   QRYDATA
          MOVE      QRYDATA,PTWRD001
          PACK      PTWRD001,PTWRD001,SP70       
          GOTO      STWRD999
STWRD002  SQUEEZE   QRYDATA 
          MOVE      QRYDATA,PTWRD002
          PACK      PTWRD002,PTWRD002,SP70       
          GOTO      STWRD999
STWRD003  MOVE      QRYDATA,PTWRD003
          PACK      PTWRD003,PTWRD003,SP70
          GOTO      STWRD999
STWRD004  MOVE      QRYDATA,PTWRD004
          GOTO      STWRD999
STWRD005  MOVE      QRYDATA,PTWRD005
          GOTO      STWRD999
STWRD006  MOVE      QRYDATA,PTWRD006
          GOTO      STWRD999
STWRD007  MOVE      QRYDATA,PTWRD007
          GOTO      STWRD999
STWRD008  MOVE      QRYDATA,PTWRD008
          GOTO      STWRD999
STWRD009  MOVE      QRYDATA,PTWRD009
          GOTO      STWRD999
STWRD010  MOVE      QRYDATA,PTWRD010
          GOTO      STWRD999
STWRD011  MOVE      QRYDATA,PTWRD011
          GOTO      STWRD999
STWRD012  MOVE      QRYDATA,PTWRD012
          GOTO      STWRD999
STWRD013  MOVE      QRYDATA,PTWRD013
          GOTO      STWRD999
STWRD014  MOVE      QRYDATA,PTWRD014
          GOTO      STWRD999
STWRD015  MOVE      QRYDATA,PTWRD015
          GOTO      STWRD999
STWRD016  MOVE      QRYDATA,PTWRD016
          GOTO      STWRD999
STWRD017  MOVE      QRYDATA,PTWRD017
          GOTO      STWRD999
STWRD018  MOVE      QRYDATA,PTWRD018
          GOTO      STWRD999
STWRD019  MOVE      QRYDATA,PTWRD019
          GOTO      STWRD999
STWRD020  MOVE      QRYDATA,PTWRD020
          GOTO      STWRD999
STWRD021  MOVE      QRYDATA,PTWRD021
          GOTO      STWRD999
STWRD022  MOVE      QRYDATA,PTWRD022
          GOTO      STWRD999
STWRD023  MOVE      QRYDATA,PTWRD023
          GOTO      STWRD999
STWRD024  MOVE      QRYDATA,PTWRD024
          GOTO      STWRD999
STWRD025  MOVE      QRYDATA,PTWRD025
          GOTO      STWRD999
.
STWRD999  RETURN
.------------------------------------------------------------
.   Ward: Display,Add,Update,Delete
.------------------------------------------------------------
PATWRD00  RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      PATWRD50 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      PATWRD10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      PATWRD20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete ward formaction
          GOTO      PATWRD30 IF EQUAL
.
          MATCH     "B",UPDTTYPE    * Delete bed formaction
          GOTO      PATWRD40 IF EQUAL
.
          GOTO      PATWRD93
.
.         ************ ADD FORMACTION ***********
.
PATWRD10  CALL      CHKWRD00      * Checks mandatory fields
          BRANCH    EXIT,PATWRD99
.
          PACK      KEY6,PTWRD001,PTWRD002,SP70
          CALL      RDWARD1
          COMPARE   ZERO,OVRCD
          GOTO      PATWRD92 IF EQUAL
.
          MOVE      PTWRD007,WBHRTYP * Room Type (Category RT)
          MOVE      ONE,WACTIVE      * Default to not active
          IF        PTWRD017=2
            MOVE      ZERO,WACTIVE   * active
          ENDIF
          MOVE      PTWRD014,WBHNOCC * No. of beds for occup stats
          MOVE      PTWRD015,WBHCLAS * Ward Classification (Category CG)
          CALL      WRDHIS00         * Write Ward History Record           
          BRANCH    EXIT,PATWRD99
.
          CALL      SAVWRD00         * Moves input variables to file variables
          PACK      KEY6,PTWRD001,SP70
          CALL      WRWARD1          * Writes away the data
.
          MOVE      "A",KEY1         * Set Action Parameter For Audit          
          CALL      WRDAUD00         * Write Audit file if needed  
.
          MOVE      ZERO,EXIT
          GOTO      PATWRD99
.
.         ************ UPDATE FORMACTION **********
.
PATWRD20  PACK      KEY6,PTWRD001,PTWRD002,SP70   * Update Formaction
          CALL      RDWARD1
          BRANCH    OVRCD,PATWRD91
.
          CALL      CHKFLD00      * Checks mandatory fields
          BRANCH    EXIT,PATWRD99
.
          MOVE      "B",KEY1      * Set Action Parameter For Audit          
          CALL      WRDAUD00      * Write Audit file if needed  
.
          PACK      KEY6,PTWRD001,PTWRD002,SP70 
          CALL      RLWARD1       * Read Lock Record      
.
          IF        HISFLAG=1
            CALL      WRDHIS00       * Write Ward History Record           
            BRANCH    EXIT,PATWRD99
          ENDIF
.
          CALL      SAVWRD00      * Moves input variables to file variables
          CALL      UPWARD1       * Writes away the data
          CALL      ULWARD1       * Unlocks IO
.
          MOVE      "C",KEY1      * Set Action Parameter For Audit          
          CALL      WRDAUD00      * Write Audit file if needed  
.
          MOVE      ZERO,EXIT
          GOTO      PATWRD99
.
.         ************ DELETE WARD FORMACTION **********
.
PATWRD30  PACK      KEY6,PTWRD001,SP70   * Delete Formaction
          CALL      RDWARD1
          BRANCH    OVRCD,PATWRD91
.
          CALL      RDSWARD1
PATWRD31  CALL      RDKWARD1             * Can't delete a ward if there are beds
          BRANCH    OVRCD,PATWRD32       
.
          MATCH     PTWRD001,WWARD
          GOTO      PATWRD32 IF NOT EQUAL
.
          GOTO      PATWRD96
.
PATWRD32  PACK      KEY6,PTWRD001,SP70   * Deletes Ward     
          CALL      DEWARD1
.
          CALL      DELHIS00      * Delete Ward History
.
          MOVE      "D",KEY1      * Set Action Parameter For Audit          
          CALL      WRDAUD00      * Write Audit file if needed  
.
          MOVE      ZERO,EXIT
          GOTO      PATWRD99
.
.         ************ DELETE BED FORMACTION **********
.
PATWRD40  PACK      KEY6,PTWRD001,PTWRD002,SP70   * Delete Bed Formaction
          CALL      RDWARD1
          BRANCH    OVRCD,PATWRD94
.
          MATCH     ZEROVISN,WADMNO
          GOTO      PATWRD95 IF NOT EQUAL
.
          PACK      KEY6,WWARD,WBED,SP70
          CALL      DEWARD1       * Delete Bed
.
          CALL      DLBHIS00      * Delete Bed History
.
          MOVE      "D",KEY1      * Set Action Parameter For Audit
          CALL      WRDAUD00      * Write Audit file if needed
.
          MOVE      ZERO,EXIT
          GOTO      PATWRD99
.
.         ************ DISPLAY FORMACTION **********
.
PATWRD50  PACK      KEY6,PTWRD001,PTWRD002,SP70
          CALL      RDWARD1
          IF        OVRCD=1
            CALL      CLRWRD00    * Clear all the file variables
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Ward/Bed Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,PATWRD99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      PATWRD99
.
PATWRD91  MOVE      "Ward does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      PATWRD99
.
PATWRD92  MOVE      "Ward record exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      PATWRD99
.
PATWRD93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      PATWRD99
.
PATWRD94  MOVE      "Bed record does not exist.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      PATWRD99
.
PATWRD95  MOVE      "Can not delete an occupied bed.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      PATWRD99
.
PATWRD96  MOVE      "Can not delete bed records exist.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      PATWRD99
.
PATWRD99  RETURN
.---------------------------------------------------------------------------
.  Clears File Data             
.---------------------------------------------------------------------------
CLRWRD00  MOVE      SP70,WWARD      * WARD NUMBER
          MOVE      SP70,WBED       * WARD BED NUMBER
          MOVE      SP70,WBDESC     * WARD BED DESCRIPTION
          MOVE      SP70,DWADMNO    * CURRENT PATIENT ADMISSION NO.
          MOVE      SP70,WEXTN      * EXTENSION NUMBER
          MOVE      ZERO,WBLINE     * No LINES B/WN BEDS (WRD ONLY)
          MOVE      SP70,WRTYPE     * Room Type (BDS ONLY) (CAT RT)
          MOVE      SP70,WEFRBT     * E.F.R. (BEDS ONLY) (CAT BT)
          MOVE      ZERO,WNBEDS     * No OF BEDS IN WARD (WRD ONLY)
          MOVE      ZERO,WHCSSUB    * HCS WARD SUBSCRIPT (WRD ONLY)
          MOVE      SP70,WSERV      * WARD SERVICE CODE (WARD ONLY)
          MOVE      ZERO,WPLUR      * PLURALITY NUMBER
          MOVE      ZERO,WINPUT     * WARD ONLY INPUT (WRD INDIC) 0=NO, 1=YES
          MOVE      ZERO,WOCCBED    * NO.OF BEDS IN WARD(OCC STATS)
          MOVE      SP70,WCLASS     * WARD CLASSIFICATION (CAT CG)
          MOVE      SP70,DWSTBY     * CURRENT STBY ADMISS NUMBER
          MOVE      ZERO,WACTIVE    * ACTIVE INDICATOR 0=Active, 1=Not Active
          MOVE      SP70,PTWDHOSN   * Hospital/Unit Number
          MOVE      ZERO,PTWDDNIN   * Day/Night Indicator 0=Day Only
.                                   *                     1=Day & Night
.                                   *                     2=Night Only
          MOVE      SP70,PTWDIAGE   * Intended Age Group Cat IA
          MOVE      SP70,PTWDICRE   *      "   Care Intensity Cat IC
          MOVE      SP70,PTWDSEX    *      "   Sex of Patient M/F/E
          MOVE      SP70,PTWDLOCN   * Medical Record Location Code
          MOVE      SP70,PTWDGLDG   * G/L and Cost Centre code
          MOVE      SP70,PTWDNSTA   * Nursing station
          MOVE      SP70,PTWDSPAR   * SPARE
.
CLRWRD99  RETURN
.---------------------------------------------------------------------------
.  Saves CGI Parameters         
.---------------------------------------------------------------------------
SAVWRD00  MOVE      PTWRD001,WWARD     * WARD NUMBER
          MOVE      PTWRD002,WBED      * WARD BED NUMBER
          MOVE      PTWRD003,WBDESC    * WARD BED DESCRIPTION
          MOVE      ZEROVISN,WADMNO        * CURRENT PATIENT ADMISSION NO.
          MOVE      PTWRD005,WEXTN     * EXTENSION NUMBER
          MOVE      PTWRD006,WBLINE    * No LINES B/WN BEDS (WRD ONLY)
          MOVE      PTWRD007,WRTYPE    * Room Type (BDS ONLY) (CAT RT)
          MOVE      PTWRD008,WEFRBT    * E.F.R. (BEDS ONLY) (CAT BT)
          MOVE      PTWRD009,WNBEDS    * No OF BEDS IN WARD (WRD ONLY)
          MOVE      PTWRD010,WHCSSUB   * HCS WARD SUBSCRIPT (WRD ONLY)
          MOVE      PTWRD011,WSERV     * WARD SERVICE CODE (WARD ONLY)
          MOVE      PTWRD012,WPLUR     * PLURALITY NUMBER
.
          MOVE      PTWRD013,WINPUT    * 0 = ward / bed
.                                      * 1 = ward only
.                                      * 2 = ward only + ward only
.
          MOVE      PTWRD014,WOCCBED   * NO.OF BEDS IN WARD(OCC STATS)
          MOVE      PTWRD015,WCLASS    * WARD CLASSIFICATION (CAT CG)
          MOVE      PTWRD016,DWSTBY    * CURRENT STBY ADMISS NUMBER
.
          COMPARE   PTWRD017,TWO
          IF        @EQUAL
            MOVE      ZERO,WACTIVE   * ACTIVE INDICATOR 0=Active,1=Not Active
          ELSE
            MOVE      ONE,WACTIVE
          ENDIF  
.
          MOVE      PTWRD018,PTWDHOSN  * Hospital/Unit Number
          MOVE      PTWRD019,PTWDDNIN  * Day/Night Indicator 0=Day Only
.                                      *                     1=Day & Night
.                                      *                     2=Night Only
          MOVE      PTWRD020,PTWDIAGE  * Intended Age Group Cat IA
          MOVE      PTWRD021,PTWDICRE  *      "   Care Intensity Cat IC
          MOVE      PTWRD022,PTWDSEX   *      "   Sex of Patient M/F/E
          MOVE      PTWRD023,PTWDLOCN  * Medical Record Location Code
          MOVE      PTWRD024,PTWDGLDG  * G/L and Cost Centre code
          MOVE      PTWRD025,PTWDNSTA  * Nursing station number
          MOVE      SP70,PTWDSPAR      * SPARE
.
SAVWRD99  RETURN
.-------------------------------------------------------------------------
. * Checks mandatory fields - add screen
.-------------------------------------------------------------------------
CHKWRD00  RESET     PTWRD002
          MATCH     SP70,PTWRD002
          GOTO      CHKWRD98 IF NOT EQUAL
.
.         Ward maintenance screen
.
          COMPARE   ZERO,PTWRD013
          GOTO      CHKWRD98 IF EQUAL        * Ward/bed
.
.         Ward only - must have E.F.R. Bed type
.
          RESET     PTWRD008
          MATCH     SP3,PTWRD008             * Bed type
          GOTO      CHKWRD92 IF EQUAL
          GOTO      CHKWRD98
.
CHKWRD92  MOVE      "E.F.R. Bed type must be entered for Ward Only!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKWRD99
.
CHKWRD98  MOVE      ZERO,EXIT
CHKWRD99  RETURN
.-------------------------------------------------------------------------
. * Checks effective date - update screen
.-------------------------------------------------------------------------
CHKFLD00  RESET     PTWRD002
          MATCH     SP70,PTWRD002
          GOTO      CHKFLD20 IF NOT EQUAL
.
.         Ward maintenance screen.
.
          COMPARE   ZERO,PTWRD013
          GOTO      CHKFLD10 IF EQUAL        * Ward/bed
.
.         Ward only - must have E.F.R. Bed type
.
          RESET     PTWRD008
          MATCH     SP3,PTWRD008             * Bed type
          GOTO      CHKFLD94 IF EQUAL
.
CHKFLD10  MOVE      ONE,HISFLAG              * set history flag
          MOVE      ONE,FORM1                * Default to not active
          IF        PTWRD017=2
            MOVE      ZERO,FORM1             * active
          ENDIF
.
          RESET     PTWRD015
          COMPARE   WACTIVE,FORM1
          GOTO      CHKFLD91 IF NOT EQUAL
          COMPARE   WOCCBED,PTWRD014          * number of beds
          GOTO      CHKFLD92 IF NOT EQUAL
          MATCH     WCLASS,PTWRD015           * ward class
          GOTO      CHKFLD93 IF NOT EQUAL
          MOVE      ZERO,HISFLAG
          GOTO      CHKFLD98
.
.         Bed maintenance screen
.
CHKFLD20  MOVE      ONE,HISFLAG              * set history flag
          MOVE      ONE,FORM1                * Default to not active
          IF        PTWRD017=2
            MOVE      ZERO,FORM1             * active
          ENDIF
          COMPARE   WACTIVE,FORM1
          GOTO      CHKFLD91 IF NOT EQUAL
.
          RESET     PTWRD007
          MATCH     WRTYPE,PTWRD007          * room type
          GOTO      CHKFLD95 IF NOT EQUAL
          MOVE      ZERO,HISFLAG
          GOTO      CHKFLD98
.
.
CHKFLD91  MATCH     SP70,EFCTDATE    * Check if an Effective Date was entered? 
          GOTO      CHKFLD99 IF NOT EQUAL
.
          MOVE    "Bed status changed, Effective date must be entered!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKFLD99
.
CHKFLD92  MATCH     SP70,EFCTDATE    * Check if an Effective Date was entered? 
          GOTO      CHKFLD99 IF NOT EQUAL
.
          MOVE    "No of Bed changed, Effective date must be entered!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKFLD99
.
CHKFLD93  MATCH     SP70,EFCTDATE    * Check if an Effective Date was entered? 
          GOTO      CHKFLD99 IF NOT EQUAL
.
          MOVE    "Ward Class changed, Effective date must be entered!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKFLD99
.
CHKFLD94  MOVE      "E.F.R Bed type must be entered for Ward Only!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKFLD99
.
CHKFLD95  MATCH     SP70,EFCTDATE    * Check if an Effective Date was entered? 
          GOTO      CHKFLD99 IF NOT EQUAL
.
          MOVE    "Room type changed, Effective date must be entered!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKFLD99
.
CHKFLD98  MOVE      ZERO,EXIT
CHKFLD99  RETURN
+
.----------------------------------------------------------------------
.         (***Copied and Modified from IBAPAT13: WWBH0000 ***)  
.                            WRDHIS00     
.    Write a WARD/BED History record if applicable
.        Parameters :  EFCTDATE - Effective Date cgi parameter
.                                                  called by: PATWRD00  
.----------------------------------------------------------------------
WRDHIS00  PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8                * Current date
          MOVE      KEY8,TODAY               * Move to form field
          MOVE      EFCTDATE,EFTDATE         * Move to form field
          IF        EFTDATE <= TODAY
            GOTO      WRDHIS10    * Same as today ok & before today ok.
          ELSE
            GOTO      WRDHIS90    * Report Error
          ENDIF
.
WRDHIS10  UNPACK    EFCTDATE,CCENT,CYEAR,CMON,CDAY
          CALL      CPRD0000                 * Calculate Previous Days Date
          PACK      WBHDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",WBHDATE
          MOVE      PTWRD001,WBHWARD     * Ward
          MOVE      PTWRD002,WBHBED      * Bed
          MOVE      WRTYPE,WBHRTYP       * Room Type (Category RT)
          MOVE      WACTIVE,WBHACTV      * Not active up to this date 
          MOVE      WOCCBED,WBHNOCC      * No. of beds for occup stats
          MOVE      WCLASS,WBHCLAS       * Ward Classification (Category CG)
          MOVE      TODAY,WBHDCHG        * Record posted today
          CLOCK     TIME,WBHTCHG         * Record posting time, now (HH:MM:SS)
          MOVE      SP70,WBHTIME         * Last Effective Time (HH:MM:SS) 
          MOVE      SP70,WBHSPAR         * Spare .
          PACK      KEY14,WBHWARD,WBHBED,WBHDATE,SP70
          CALL      RAPTWBH1      * Check if record already exists?
          IF        OVRCD = 1                
            CALL      WRPTWBH1    * Record doesn't exist - ok write new record
          ELSE
            GOTO      WRDHIS91    * Report Error
          ENDIF
          GOTO      WRDHIS98
.
WRDHIS90  MOVE      "Cannot Use A Future Date!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      WRDHIS99
.
WRDHIS91  MOVE      "History Record Exists Already!",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      WRDHIS99
.
WRDHIS98  MOVE      ZERO,EXIT
WRDHIS99  RETURN
.------------------------------------------------------------------------------
.                             CPRD0000
.    Subroutine To CALCULATE The Previous Day.   (***Copied from IBAPAT13***)
.         Requires : CDAY, CMON, CYEAR, CCENT
.         Returns  : CDAY, CMON, CYEAR, CCENT
.                                                          called by: WRDHIS00
.------------------------------------------------------------------------------
CPRD0000  MOVE      CDAY,DD              * These variables are
          MOVE      CMON,MM              * used by DMYCON
          MOVE      CYEAR,YY
          MOVE      CCENT,CC
          CALL      DMYCON               * Convert to Julian format
.
          SUB       ONE,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON               * Convert back to "normal" format
.
          MOVE      DD,CDAY
          MOVE      MM,CMON
          MOVE      YY,CYEAR
          MOVE      CC,CCENT
.
CPRD9999  RETURN
.
.------------------------------------------------------------------------
.          CNXT0000 - Calculate the next day
.------------------------------------------------------------------------
CNXT0000  MOVE      CDAY,DD              * These variables are
          MOVE      CMON,MM              * used by DMYCON
          MOVE      CYEAR,YY
          MOVE      CCENT,CC
          CALL      DMYCON               * Convert to Julian format
.
          ADD       ONE,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON               * Convert back to "normal" format
.
          MOVE      DD,CDAY
          MOVE      MM,CMON
          MOVE      YY,CYEAR
          MOVE      CC,CCENT
          PACK      KEY8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",KEY8
.
CNXT9999  RETURN
+
.------------------------------------------------------------------------
.          DELHIS00 - Delete All Ward/Bed History Records
.                                                     called by: PATWRD00
.------------------------------------------------------------------------
DELHIS00  PACK      KEY14,PTWRD001,SP70
          CALL      RSPTWBH1
DELHIS10  CALL      RKPTWBH1
          BRANCH    OVRCD,DELHIS99
.
          MATCH     PTWRD001,WBHWARD
          GOTO      DELHIS99 IF NOT EQUAL
.
          PACK      KEY14,WBHWARD,WBHBED,WBHDATE
          CALL      DEPTWBH1
          GOTO      DELHIS10
.
DELHIS99  RETURN
+
.------------------------------------------------------------------------
.          DLBHIS00 - Delete Ward/Bed History Records
.                                                     called by: PATWRD00
.------------------------------------------------------------------------
DLBHIS00  PACK      KEY14,PTWRD001,PTWRD002,SP70
          CALL      RSPTWBH1
DLBHIS10  CALL      RKPTWBH1
          BRANCH    OVRCD,DLBHIS99
.
          MATCH     PTWRD001,WBHWARD
          GOTO      DLBHIS99 IF NOT EQUAL
.
          MATCH     PTWRD002,WBHBED
          GOTO      DLBHIS99 IF NOT EQUAL
.
          PACK      KEY14,WBHWARD,WBHBED,WBHDATE
          CALL      DEPTWBH1
          GOTO      DLBHIS00
.
DLBHIS99  RETURN
.---------------------------------------------------------------------
. Writes Ward/Wed Audit
.    Parameters :   KEY1 - Set Record Type
.---------------------------------------------------------------------
WRDAUD00  BRANCH    CAUDW,WRDAUD99  * Check if audit needs to be written
.
          CALL      IBACLOCK
          MOVE      CDD,ADD                 * Audit Day 
          MOVE      CMM,AMM                 * Audit Month
          MOVE      CYY,AYY                 * Audit Year
          REP       " 0",AYY
          MOVE      CCC,ACC                 * Audit Century  
          MOVE      CTIMEIS,TIMEIS          * Audit Time 
          MOVE      KEY1,ACTION             * Record Type
          MOVE      PASSCODE,OPERCODE       * Operator
.
.  Move File Variables To Audit Variables 
.
          PACK      KEY6,PTWRD001,SP70     * Read Correct Record(Just in case!) 
          CALL      RDWARD1
          BRANCH    OVRCD,WRDAUD99
.
          MOVE      WWARD,SWWARD
          MOVE      WBED,SWBED
          MOVE      WBDESC,SWBDESC
          MOVE      WADMNO,SWADMNO
          MOVE      WEXTN,SWEXTN
          MOVE      WBLINE,SWBLINE
          MOVE      WRTYPE,SWRTYPE
          MOVE      WEFRBT,SWEFRBT
          MOVE      WNBEDS,SWNBEDS
          MOVE      WHCSSUB,SWHCSSUB
          MOVE      WSERV,SWSERV
          MOVE      WPLUR,SWPLUR 
          MOVE      WINPUT,SWINPUT
          MOVE      WOCCBED,SWOCCBED
          MOVE      WCLASS,SWCLASS
          MOVE      DWSTBY,SDWSTBY
          MOVE      WACTIVE,SWACTIVE
          MOVE      PTWDHOSN,SPTWDHOS
          MOVE      PTWDDNIN,SPTWDDNI
          MOVE      PTWDIAGE,SPTWDIAG
          MOVE      PTWDICRE,SPTWDICR
          MOVE      PTWDSEX,SPTWDSEX
          MOVE      PTWDLOCN,SPTWDLOC
          MOVE      PTWDGLDG,SPTWDGLD
          MOVE      PTWDNSTA,SPTWDNST
. 
          CALL      WRWAUD     * Write Record
.
WRDAUD99  RETURN
.------------------------------------------------------------
. Ward Enquiry Page
.------------------------------------------------------------
WRDENQ00  MATCH     SP70,WARDCODE
          IF        @EQUAL
            MOVE      WBSEWRD,WARDCODE
          ENDIF
          PACK      KEY6,WARDCODE,SP70
          CALL      RDWARD1
          BRANCH    OVRCD,WRDENQ90
          CLEAR     WEBTITLE
          APPEND    "Details of Ward ",WEBTITLE
          APPEND    WBDESC,WEBTITLE
          RESET     WEBTITLE
          CALL      WEBHED00
          BRANCH    EXIT,WRDENQ99
.
          CALL      WEBBOD00
.
          CALL      WEBEND00
.
          GOTO      WRDENQ99
.
WRDENQ90  MOVE      "INVALID WARDCODE SPECIFIED",WEBTITLE
          CALL      WEBERR00
.
WRDENQ99  RETURN
.------------------------------------------------------------
. Called from WEBBOD00
.------------------------------------------------------------
PROFLD00  BRANCH  REPORTNO,PROFLD10
          GOTO    PROFLD99
.
PROFLD10  CALL    WBSC0000
          GOTO    PROFLD99
.
PROFLD99  RETURN
.-----------------------------------------------------------
. Link to Next Group
.-----------------------------------------------------------
NEXTGR00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
.
          WRITE     HTMLFILE;"patweb95.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2;
.
NEXTGR99  RETURN
.-------------------------------------------------------------
. Link to Prev Group
.-------------------------------------------------------------
PREVGR00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3
          IF        F3<0
            MOVE      ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
.
          WRITE     HTMLFILE;"patweb95.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2;
.
PREVGR99  RETURN
.------------------------------------------------------------
. Ward Bed Scan
.------------------------------------------------------------
WBSC0000  BRANCH    FLDITMNO,WBSC0100,WBSC0200,WBSC0300,WBSC0400,WBSC0500:
                             WBSC0600
          GOTO      WBSC9999
.
WBSC0100  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          REP       " +",WARDCODE
          WRITE     HTMLFILE;"patweb95.pbl?reportno=1":
                                 "&template=",FLDPARA,"&":
                                 "startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&wardcode=",WARDCODE:
                                 "&emptonly=",EMPTONLY:
                                 "&bedsonly=",BEDSONLY;
          REP       "+ ",WARDCODE
          GOTO      WBSC9999
.
WBSC0200  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3
          IF        F3<0
            MOVE      ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          REP       " +",WARDCODE
          WRITE     HTMLFILE;"patweb95.pbl?reportno=1":
                                 "&template=",FLDPARA,"&":
                                 "startnum=",KEY3,"&":
                                 "norecord=",KEY2,"&":
                                 "wardcode=",WARDCODE:
                                 "&emptonly=",EMPTONLY:
                                 "&bedsonly=",BEDSONLY;
          REP       "+ ",WARDCODE
          GOTO      WBSC9999
.
WBSC0300  MOVE      ZERO,EXPDISCH 
          CALL      TABC0000                * Displays expected discharge
          GOTO      WBSC9999
.
WBSC0400  MOVE      ONE,EXPDISCH 
          CALL      TABC0000                * Does not have expected discharge
          GOTO      WBSC9999
.
WBSC0500  CALL      WSEL0000                * Ward Selection List (all wards) 
          GOTO      WBSC9999
.
WBSC0600  CALL      BSEL0000                * Selection list for bed wards
          GOTO      WBSC9999
.
WBSC9999  RETURN
.------------------------------------------------------------
. Ward selection
.------------------------------------------------------------
WSEL0000  PACK      KEY6,SP70
          CALL      RDSWARD3
WSEL0100  CALL      RDKWARD3
          BRANCH    OVRCD,WSEL9999
          MATCH     SP70,WBED
          GOTO      WSEL9999 IF NOT EQUAL
          COMPARE   WACTIVE,ZERO                 *List only active wards
          GOTO      WSEL0100 IF NOT EQUAL
.
          PACK      KEY5,QUOTE,WWARD,QUOTE
          WRITE     HTMLFILE;"<option value=",KEY5,">",WBDESC,"   (",WWARD:
                             ")</option>";
          GOTO      WSEL0100
.
WSEL9999  RETURN
.------------------------------------------------------------
. Ward/Bed selection
.------------------------------------------------------------
BSEL0000  PACK      KEY6,SP70
          CALL      RDSWARD3
BSEL0100  CALL      RDKWARD3
          BRANCH    OVRCD,BSEL9999
          MATCH     SP70,WBED
          GOTO      BSEL9999 IF NOT EQUAL
          COMPARE   WACTIVE,ZERO                 *List only active wards
          GOTO      BSEL0100 IF NOT EQUAL
          COMPARE   WINPUT,ZERO
          GOTO      BSEL0100 IF NOT EQUAL
.
          PACK      KEY5,QUOTE,WWARD,QUOTE
          WRITE     HTMLFILE;"<option value=",KEY5,">",WBDESC,"   (",WWARD:
                             ")</option>";
          GOTO      BSEL0100
.
BSEL9999  RETURN
.------------------------------------------------------------
. Table C - Displays patient ward/bed details
.------------------------------------------------------------
TABC0000  MOVE      SP70,GENDER 
          MOVE      SP70,PATFNAME 
          MOVE      SP70,FORMATNM
          MOVE      SP70,DOCFNAME 
          MOVE      SP70,PDIAGNOS 
          MOVE      ZERO,RECCOUNT
          MOVE      ZERO,DISPNUMB
.
          IF        NORECORD=0
            GOTO      TABC9999
          ENDIF
.
          IF        EXPDISCH=0
            WRITE     HTMLFILE;"<tr>":
                                 "<td class=HeadingCell>Ward/Bed</td>":
                                 "<td class=HeadingCell>Bed Type</td>":
                                 "<td class=HeadingCell>U/R</td>":
                                 "<td class=HeadingCell>Patient</td>":
                                 "<td class=HeadingCell>Diagnoses</td>":
                                 "<td class=HeadingCell>Att. Doct.</td>":
                                 "<td class=HeadingCell>Exp. Disch.</td>":
                               "</tr>"
          ELSE
            WRITE     HTMLFILE;"<tr>":
                                 "<td class=HeadingCell>Ward/Bed</td>":
                                 "<td class=HeadingCell>Bed Type</td>":
                                 "<td class=HeadingCell>U/R</td>":
                                 "<td class=HeadingCell>Patient</td>":
                                 "<td class=HeadingCell>Diagnoses</td>":
                                 "<td class=HeadingCell>Att. Doct.</td>":
                               "</tr>"
          ENDIF
.
          MOVE      SP70,LASTWARD
          PACK      KEY6,SP3,WARDCODE,SP70
          CALL      RDWARD3
          IF        OVRCD=0
            CALL      RDPWARD3
          ENDIF
TABC0100  CALL      RDKWARD3
          BRANCH    OVRCD,TABC9999
          MATCH     SP70,WBED
          GOTO      TABC9999 IF NOT EQUAL
.
          COMPARE   ONE,WACTIVE
          GOTO      TABC0100 IF EQUAL
          IF        BEDSONLY=1
            COMPARE   ONE,WINPUT
            GOTO      TABC0100 IF EQUAL
          ENDIF
.
          ADD       ONE,RECCOUNT
          IF        STARTNUM<RECCOUNT
            CALL      WRDROW10
          ENDIF
.
          MOVE      ZERO,ENDNOW       * End of Loop NO
.
          MOVE      WWARD,THISWARD
          IF        WINPUT=0
            CALL      BDWR0000        * Bed only ward
          ELSE
            IF        WINPUT=2
              CALL      BDWR0000      * Ward bed and ward only ward
              MOVE      THISWARD,WWARD
              CALL      NOBW0000
            ELSE
              CALL      NOBW0000      * Ward only ward
            ENDIF
          ENDIF
          BRANCH    ENDNOW,TABC9999   * End of Loop YES
          GOTO      TABC0100
.
TABC9999  RETURN
.------------------------------------------------------------
. Process Bed Ward
.------------------------------------------------------------
BDWR0000  MOVE      ZERO,BEDCOUNT
          PACK      KEY6,THISWARD,SP70
          CALL      RDWARD1
BDWR1000  CALL      RDKWARD1
          BRANCH    OVRCD,BDWR9999
          MATCH     WWARD,THISWARD
          GOTO      BDWR9999 IF NOT EQUAL
.
          IF        EMPTONLY=1
            MATCH     WADMNO,ZEROVISN             * Check if bed is empty
            GOTO      BDWR1000 IF NOT EQUAL       * Not Empty so Ignore
          ENDIF
.
          COMPARE   WACTIVE,ONE                 *Check if bed is active
          GOTO      BDWR1000 IF EQUAL           *Do not display inactive bed
.
          ADD       ONE,BEDCOUNT
          IF        NORECORD=BEDCOUNT
            ADD       ONE,RECCOUNT
            MOVE      ZERO,BEDCOUNT
          ENDIF
.
          ADD       ONE,RECCOUNT
          IF        STARTNUM>=RECCOUNT
            GOTO      BDWR1000
          ENDIF
.
          MATCH     WWARD,LASTWARD
          CALL      WRDROW00 IF NOT EQUAL
.
          MOVE      ZERO,BEDSTAT                * Empty Bed
          MATCH     WADMNO,ZEROVISN             *Check if bed is empty
          GOTO      BDWR2000 IF EQUAL
.
          MATCH     ZEROVISN,WADMNO
          IF        !@EQUAL
            MOVE      ONE,BEDSTAT    * Patient in Bed
            MATCH     ZEROVISN,WSTBY
            IF        !@EQUAL
              MOVE      TWO,BEDSTAT  * Has Patient on Standby
              CALL      STBY0000     * display standy patient
              MOVE      WADMNO,ADMNOD8
              CALL      PATD0000
            ELSE
              CALL      ONLV0000     * Check for on leave patient if no stby
            ENDIF
          ENDIF
.
          MOVE      WADMNO,ADMNOD8
          CALL      PATD0000
          BRANCH    EXIT,BDWR3000
.
          UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          MOVE      ASTAY,ADJDAYS
          CALL      ADJDAT00
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
.
          CALL      PATR0000           * Display patient row
          GOTO      BDWR3000
.
BDWR2000  IF        EXPDISCH=0
            WRITE     HTMLFILE;"<tr align=top>":
                               "<td align=center><a href=#"#" ":
                               "onClick='updateParent(#"",WWARD,"#",":
                                                     "#"",WARDNAME,"#",":
                                                     "#"",WBED,"#",":
                                                     "#"",BEDSTAT,"#");'>":
                                WBED,"</a>":
                               "</td>":
                               "<td>",WEFRBT,"&nbsp;</td>":
                               "<td>&nbsp;</td>":
                               "<td>*** Empty ***</td>":
                               "<td>&nbsp;</td>":
                               "<td>&nbsp;</td>":
                               "<td>&nbsp;</td>":
                               "</tr>";
          ELSE
            WRITE     HTMLFILE;"<tr align=top>":
                               "<td align=center><a href=#"#" ":
                               "onClick='updateParent(#"",WWARD,"#",":
                                                     "#"",WARDNAME,"#",":
                                                     "#"",WBED,"#",":
                                                     "#"",BEDSTAT,"#");'>":
                                WBED,"</a>":
                               "</td>":
                               "<td>",WEFRBT,"&nbsp;</td>":
                               "<td>&nbsp;</td>":
                               "<td>*** Empty ***</td>":
                               "<td>&nbsp;</td>":
                               "<td>&nbsp;</td>":
                               "</tr>";
          ENDIF
          CALL      ONLV0000     * Check for on leave patient
.
BDWR3000  ADD       ONE,DISPNUMB
          IF        NORECORD>DISPNUMB
            GOTO      BDWR1000
          ENDIF
          MOVE      ONE,ENDNOW
.
BDWR9999  RETURN
.------------------------------------------------------------
. Output patient row
.------------------------------------------------------------
PATR0000  COMPARE   EXPDISCH,ZERO
          GOTO      PATR0100 IF NOT EQUAL
.
          WRITE     HTMLFILE;"<tr align=top>":
                             "<td align=center height=30><a href=#"#" ":
                             "onClick='updateParent(#"",WWARD,"#",":
                                                   "#"",WARDNAME,"#",":
                                                   "#"",WBED,"#",":
                                                   "#"",BEDSTAT,"#");'>":
                              WBED,"</a>":
                             "</td>":
                             "<td>",WEFRBT,"</td>":
                             "<td align=center>",AURNO,"&nbsp;</td>":
                             "<td nowrap>",FORMATNM,";</td>":
                             "<td>",PDIAGNOS,"&nbsp;</td>":
                             "<td nowrap>",DOCFNAME,"&nbsp;</td>":
                             "<td nowrap align=center>",CDAY," ",MTHNAM3:
                             " ",CCENT,CYEAR,"&nbsp;</td>":
                             "</tr>";
          GOTO      PATR9999
.
PATR0100 WRITE     HTMLFILE;"<tr align=top>":
                             "<td align=center height=30><a href=#"#" ":
                             "onClick='updateParent(#"",WWARD,"#",":
                                                   "#"",WARDNAME,"#",":
                                                   "#"",WBED,"#",":
                                                   "#"",BEDSTAT,"#");'>":
                              WBED,"</a>":
                             "</td>":
                             "<td>",WEFRBT,"</td>":
                             "<td align=center>",AURNO,"&nbsp;</td>":
                             "<td nowrap>",PATFNAME:
                             " (",GENDER,",",PSAGEYRS,")</td>":
                             "<td>",PDIAGNOS,"&nbsp;</td>":
                             "<td nowrap>",DOCFNAME,"&nbsp;</td>":
                             "</tr>";
.
PATR9999  RETURN
.------------------------------------------------------------
. Output no bed ward
.------------------------------------------------------------
NOBW0000  MOVE      ZERO,FLAG
          PACK      KEY13,WWARD,SP70
          CALL      RDSNOBE1
NOBW0100  CALL      RDKNOBE1
          BRANCH    OVRCD,NOBW9000
          MATCH     WWARD,NBWARD
          GOTO      NOBW9000 IF NOT EQUAL
.
          IF        EMPTONLY=1
            ADD       ONE,RECCOUNT
            IF        STARTNUM<RECCOUNT
              MATCH     NBWARD,LASTWARD
              CALL      WRDROW00 IF NOT EQUAL
              CALL      EBFW0000               *Display only empty bed for ward
              IF        NORECORD<=DISPNUMB
                MOVE      ONE,ENDNOW
              ENDIF
            ENDIF
            GOTO      NOBW9999
          ENDIF
.
.------  The following should be only called once ------
.
          IF        FLAG<>1
            ADD       ONE,RECCOUNT
            IF        STARTNUM<RECCOUNT
              MATCH     NBWARD,LASTWARD
              CALL      WRDROW00 IF NOT EQUAL
              CALL      EBFW0000               *Display only empty bed for ward
            ENDIF
            MOVE      ONE,FLAG
            MOVE      SP70,WBED
            CALL      ONLV0000                 *Check for patient on leave
          ENDIF
.
          MATCH     ZEROVISN,NBADMNO
          GOTO      NOBW0100 IF EQUAL
.
          MOVE      NBADMNO,ADMNOD8
          CALL      PATD0000
          UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          PACK      KEY8,NBADMNO
          CALL      RDMISS1
          BRANCH    OVRCD,NOBW9999
.
          MOVE      ASTAY,ADJDAYS
          CALL      ADJDAT00
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
.
          ADD       ONE,RECCOUNT
          IF        STARTNUM>=RECCOUNT
            GOTO      NOBW0100
          ENDIF
.
          MATCH     NBWARD,LASTWARD
          CALL      WRDROW00 IF NOT EQUAL
          IF        EXPDISCH=0
            WRITE     HTMLFILE;"<tr align=top>":
                               "<td height=30>&nbsp;</td>":
                               "<td height=30>&nbsp;</td>":
                               "<td align=center>",AURNO,"&nbsp;</td>":
                               "<td nowrap>",PATFNAME:
                               " (",GENDER,",",PSAGEYRS,")</td>":
                               "<td>",PDIAGNOS,"&nbsp;</td>":
                               "<td nowrap>",DOCFNAME,"&nbsp;</td>":
                               "<td nowrap align=center>",CDAY," ",MTHNAM3:
                               " ",CCENT,CYEAR,"&nbsp;</td>":
                               "</tr>";
          ELSE
            WRITE     HTMLFILE;"<tr align=top>":
                               "<td height=30>&nbsp;</td>":
                               "<td height=30>&nbsp;</td>":
                               "<td align=center>",AURNO,"&nbsp;</td>":
                               "<td nowrap>",PATFNAME:
                               " (",GENDER,",",PSAGEYRS,")</td>":
                               "<td>",PDIAGNOS,"&nbsp;</td>":
                               "<td nowrap>",DOCFNAME,"&nbsp;</td>":
                               "</tr>";
          ENDIF
          ADD       ONE,DISPNUMB
          IF        NORECORD>DISPNUMB
            GOTO      NOBW0100
          ENDIF
          MOVE      ONE,ENDNOW
          GOTO      NOBW9999
.
NOBW9000  COMPARE   ZERO,FLAG
          GOTO      NOBW9999 IF NOT EQUAL
          ADD       ONE,RECCOUNT
          IF        STARTNUM>=RECCOUNT
            GOTO      NOBW9999
          ENDIF
..          CALL      WRDROW00
          CALL      EBFW0000                *Display empty bed to select
.
NOBW9999  RETURN
.------------------------------------------------------------
. Empty Bed for No Bed Ward
.------------------------------------------------------------
EBFW0000  ADD       ONE,DISPNUMB
          COMPARE   ZERO,EXPDISCH
          GOTO      EBFW0100 IF NOT EQUAL
. 
          WRITE     HTMLFILE;"<tr align=top>":
                           "<td align=center><a href=#"#" ":
                           "onClick='updateParent(#"",WWARD,"#",":
                                                 "#"",WARDNAME,"#",":
                                                 "#"",WBED,"#",":
                                                 "#"",BEDSTAT,"#");'>":
                           WWARD,"</a>":
                           "</td>":
                           "<td>&nbsp;</td>":
                           "<td>*** Empty ***</td>":
                           "<td>&nbsp;</td>":
                           "<td>&nbsp;</td>":
                           "<td>&nbsp;</td>": 
                           "</tr>";
          GOTO      EBFW9999
.
EBFW0100  WRITE     HTMLFILE;"<tr align=top>":
                           "<td align=center><a href=#"#" ":
                           "onClick='updateParent(#"",WWARD,"#",":
                                                 "#"",WARDNAME,"#",":
                                                 "#"",WBED,"#",":
                                                 "#"",BEDSTAT,"#");'>":
                           WWARD,"</a>":
                           "</td>":
                           "<td>&nbsp;</td>":
                           "<td>*** Empty ***</td>":
                           "<td>&nbsp;</td>":
                           "<td>&nbsp;</td>":
                           "<td>&nbsp;</td>":
                           "</tr>";
EBFW9999  RETURN
.------------------------------------------------------------
. Ouput on leave patient
.------------------------------------------------------------
ONLV0000  PACK      KEY12,WWARD,WBED,SP70
          CALL      RDSONLV1
ONLV1000  CALL      RDKONLV1
          BRANCH    OVRCD,ONLV9999
          MATCH     OWARD,WWARD
          GOTO      ONLV9999 IF NOT EQUAL
          MATCH     OBED,WBED
          GOTO      ONLV9999 IF NOT EQUAL
.
          MATCH     ZEROVISN,OADMNO
          GOTO      ONLV9999 IF EQUAL
.
          ADD       ONE,RECCOUNT
          IF        STARTNUM>=RECCOUNT
            GOTO      ONLV1000
          ENDIF
.
          MOVE      OADMNO,ADMNOD8
          CALL      PATD0000
          MOVE      "Patient on leave",PDIAGNOS
          IF        EXPDISCH=0
            WRITE     HTMLFILE;"<tr align=top>":
                               "<td align=center height=30><a href=#"#" ":
                               "onClick='updateParent(#"",WWARD,"#",":
                                                     "#"",WARDNAME,"#",":
                                                     "#"",WBED,"#",":
                                                     "#"",BEDSTAT,"#");'>":
                                WBED,"</a>":
                               "</td>":
                               "<td>",WEFRBT,"</td>":
                               "<td align=center>",AURNO,"&nbsp;</td>":
                               "<td nowrap>",PATFNAME:
                               " (",GENDER,",",PSAGEYRS,")</td>":
                               "<td align=center><b>",PDIAGNOS:
                               "&nbsp;</b></td>":
                               "<td nowrap>",DOCFNAME,"&nbsp;</td>":
                               "<td nowrap align=center>",CDAY," ",MTHNAM3:
                               " ",CCENT,CYEAR,"&nbsp;</td>":
                               "</tr>";
          ELSE
            WRITE     HTMLFILE;"<tr align=top>":
                             "<td align=center height=30><a href=#"#" ":
                             "onClick='updateParent(#"",WWARD,"#",":
                                                   "#"",WARDNAME,"#",":
                                                     "#"",WBED,"#",":
                                                     "#"",BEDSTAT,"#");'>":
                                WBED,"</a>":
                               "</td>":
                               "<td>",WEFRBT,"</td>":
                               "<td align=center>",AURNO,"&nbsp;</td>":
                               "<td nowrap>",PATFNAME:
                               " (",GENDER,",",PSAGEYRS,")</td>":
                               "<td align=center><b>",PDIAGNOS:
                               "&nbsp;</b></td>":
                               "<td nowrap>",DOCFNAME,"&nbsp;</td>":
                               "</tr>";
          ENDIF
.
          ADD       ONE,DISPNUMB
          IF        NORECORD>DISPNUMB
            GOTO      ONLV1000
          ENDIF
          MOVE      ONE,ENDNOW
.
ONLV9999  RETURN
.------------------------------------------------------------
. Output standby patient
.------------------------------------------------------------
STBY0000  MOVE      WSTBY,ADMNOD8
          CALL      PATD0000
          MOVE      "Standby Patient",PDIAGNOS
          COMPARE   ZERO,EXPDISCH
          GOTO      STBY0100 IF NOT EQUAL
.
          ADD       ONE,DISPNUMB
.
          WRITE     HTMLFILE;"<tr align=top>":
                             "<td align=center height=30><a href=#"#" ":
                             "onClick='updateParent(#"",WWARD,"#",":
                                                   "#"",WARDNAME,"#",":
                                                   "#"",WBED,"#",":
                                                   "#"",BEDSTAT,"#");'>":
                              WBED,"</a>":
                             "</td>":
                             "<td>",WEFRBT,"</td>":
                             "<td align=center>",AURNO,"&nbsp;</td>":
                             "<td nowrap>",PATFNAME:
                             " (",GENDER,",",PSAGEYRS,")</td>":
                             "<td align=center><b>",PDIAGNOS,"&nbsp;</b></td>":
                             "<td nowrap>",DOCFNAME,"&nbsp;</td>":
                             "<td nowrap align=center>",CDAY," ",MTHNAM3:
                             " ",CCENT,CYEAR,"&nbsp;</td>":
                             "</tr>";
          GOTO      STBY9999
.
STBY0100  WRITE     HTMLFILE;"<tr align=top>":
                             "<td align=center height=30><a href=#"#" ":
                             "onClick='updateParent(#"",WWARD,"#",":
                                                   "#"",WARDNAME,"#",":
                                                   "#"",WBED,"#",":
                                                   "#"",BEDSTAT,"#");'>":
                              WBED,"</a>":
                             "</td>":
                             "<td>",WEFRBT,"</td>":
                             "<td align=center>",AURNO,"&nbsp;</td>":
                             "<td nowrap>",PATFNAME:
                             " (",GENDER,",",PSAGEYRS,")</td>":
                             "<td align=center><b>",PDIAGNOS,"&nbsp;</b></td>":
                             "<td nowrap>",DOCFNAME,"&nbsp;</td>":
                             "</tr>";
.
STBY9999  RETURN
.------------------------------------------------------------
. Output Ward Heading
.------------------------------------------------------------
WRDROW00  ADD       ONE,RECCOUNT
WRDROW10  PACK      SAVKEY6,WWARD,WBED
          PACK      KEY6,WWARD,SP70
          CALL      RDWARD1
          REP       "' ",WBDESC
          WRITE     HTMLFILE;"<tr>":
                             "<td class=HeadingCell colspan=9 align=center>":
                             " Ward : </b>",WWARD," - ":
                             WBDESC,"    Extension </b> -",WEXTN:
                             "</td></tr>";
          ADD       ONE,DISPNUMB
          MOVE      WWARD,LASTWARD
          MOVE      WBDESC,WARDNAME
          MOVE      SAVKEY6,KEY6
          CALL      RDWARD1
          RETURN
.------------------------------------------------------------
. Patient Details from master file to display in table c
.------------------------------------------------------------
PATD0000  PACK      KEY8,ADMNOD8
          CALL      RDMISS1
          BRANCH    OVRCD,PATD9900
.
          PACK      KEY8,AURNO
          CALL      RDMAST1
          BRANCH    OVRCD,PATD9900
.
          CALL      PATNAM00
.
.* ****************************************** Start of Changes         *C-49016
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          MOVE      DSEXDESC,GENDER
.* ******************************************   End of Changes         *C-49016
.
          PACK      AGEDATE,CCC,CYY,CMM,CDD
          REP       " 0",AGEDATE
          CALL      CALCAGE
          MOVE      ADIAG1,PDIAGNOS
.
          PACK      KEY6,ADOCTA
          CALL      RDDOCT1
          BRANCH    OVRCD,PATD9999
.
          MOVE      DTITL,FMTTITLE
          MOVE      DGNAME,FMTGNAME
          MOVE      DSNAME,FMTSNAME
          CALL      DOCNM000
.
          MOVE      ZERO,EXIT
          GOTO      PATD9999
.
PATD9900  CALL      INVAL000                * Display Invalid Record Row
          MOVE      ONE,EXIT
.
PATD9999  RETURN
.------------------------------------------------------------
. Display an Invalid Record Row
.------------------------------------------------------------
INVAL000  COMPARE   EXPDISCH,ZERO
          GOTO      INVAL010 IF NOT EQUAL 
          WRITE     HTMLFILE;"<tr align=top>":
                             "<td align=center><a href=#"#" ":
                             "onClick='updateParent(#"",WWARD,"#",":
                                                   "#"",WARDNAME,"#",":
                                                   "#"",WBED,"#",":
                                                   "#"",BEDSTAT,"#");'>":
                              WBED,"</a>":
                             "</td>":
                             "<td>",WEFRBT,"</td>":
                             "<td>&nbsp;</td>":
                             "<td><b>*** Invalid Record ***<b></td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>": 
                             "</tr>";
          GOTO      INVAL999
.
INVAL010  WRITE     HTMLFILE;"<tr align=top>":
                             "<td align=center><a href=#"#" ":
                             "onClick='updateParent(#"",WWARD,"#",":
                                                   "#"",WARDNAME,"#",":
                                                   "#"",WBED,"#",":
                                                   "#"",BEDSTAT,"#");'>":
                              WBED,"</a>":
                             "</td>":
                             "<td>",WEFRBT,"</td>":
                             "<td>&nbsp;</td>":
                             "<td><b>*** Invalid Record ***<b></td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "</tr>";
INVAL999  RETURN
.------------------------------------------------------------
. Ward List Table
.------------------------------------------------------------
WLST0000  WRITE     HTMLFILE;"<tr align=center>":
                               "<td class=HeadingCell>Ward</td>":
                               "<td class=HeadingCell align=right>Beds</td>":
                               "<td class=HeadingCell>Extension No</td>":
                               "<td class=HeadingCell>Classification</td>":
                             "</tr>"
          UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
          PACK      KEY6,SP70
          CALL      RDSWARD3
WLST0100  CALL      RDKWARD3
          BRANCH    OVRCD,WLST9999
          MATCH     SP70,WBED
          GOTO      WLST9999 IF NOT EQUAL
          COMPARE   WACTIVE,ZERO                 *List only active wards
          GOTO      WLST0100 IF NOT EQUAL
.
          MOVE      "CG",CATEGORY
          MOVE      WCLASS,CODE
          CALL      GETCOD00
          PACK      KEY5,QUOTE,WWARD,QUOTE
          WRITE     HTMLFILE;"<tr valign=top>":
                              "<td><a href=",LINKPRG,".pbl?":
                               "&reportno=",LINKREP:
                               "&template=",LINKTEM:
                               "&wardcode=",WWARD,">":
                               WBDESC,"</a></td>":
                               "<td align=right>",WOCCBED,"&nbsp;</td>":
                               "<td>",WEXTN,"&nbsp;</td>":
                               "<td>",TDESC,"&nbsp;</td>":
                             "</tr>"
          GOTO      WLST0100
.
WLST9999  RETURN
.------------------------------------------------------------
. Ward List with Update Parent 
.------------------------------------------------------------
WLUPD000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          CALL      WLHED000      * Output Table Heading
.
          IF        NORECORD=0
            MOVE      "15",NORECORD       * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY6,SP70
          CALL      RDSWARD3
WLUPD100  CALL      RDKWARD3
          BRANCH    OVRCD,WLST9999
.
          MATCH     SP70,WBED
          GOTO      WLUPD999 IF NOT EQUAL
          COMPARE   WACTIVE,ZERO                 *List only active wards
          GOTO      WLUPD100 IF NOT EQUAL
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      WLUPD100
          ENDIF
.
          CALL      WLROW000      * Display Ward List Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      WLUPD100 IF NOT EQUAL
.
WLUPD999  RETURN
.--------------------------------------------------------------
.  Ward List  (TABLE HEADING)
.--------------------------------------------------------------
WLHED000  WRITE     HTMLFILE;"<tr>":
                               "<td class=HeadingCell>Ward</td>":
                               "<td class=HeadingCell>Description</td>":
                             "</tr>"
.
WLHED999  RETURN
.--------------------------------------------------------------
.  Ward List Update Parent (TABLE ROW)
.--------------------------------------------------------------
WLROW000  MOVE      "CG",CATEGORY
          MOVE      WCLASS,CODE
          CALL      GETCOD00
          REP       "' ",WBDESC 
.
          WRITE     HTMLFILE;"<tr><td nowrap><a href=#"#" ":
                              "OnClick=#"UpdateParent02('",WWARD,"','",WBDESC:
                              "')#">":
                              "<img src=../images/MaintenanceIcon.gif hspace=4":
                              " border=0 align=absmiddle></a>":
                              WWARD,"<td>",WBDESC,"</td>":
                              "</tr>"
.
WLROW999  RETURN
.------------------------------------------------------------
. Ward Status Table Listing
.------------------------------------------------------------
WSTA0000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
          MOVE      ZERO,TOTALBOK
          MOVE      ZERO,TOTALPRE
          MOVE      ZERO,TOTALBED
          MOVE      ZERO,TOTALPAT
          PACK      KEY6,SP70
          CALL      RDSWARD3
WSTA0100  CALL      RDKWARD3
          BRANCH    OVRCD,WSTA9000
          MATCH     SP70,WBED
          GOTO      WSTA9000 IF NOT EQUAL
          COMPARE   WACTIVE,ZERO              *List only active wards
          GOTO      WSTA0100 IF NOT EQUAL
.
          CALL      CALCP000                  * Calculate Number of Patients
          IF        (WOCCBED=0&PATCOUNT=0&PREADMIS=0&BOOKINGS=0)
.....       GOTO      WSTA0100                * Ignore Ward if All Zero
          ENDIF
.
          MOVE      "CG",CATEGORY
          MOVE      WCLASS,CODE
          CALL      GETCOD00
.
          PACK      KEY5,QUOTE,WWARD,QUOTE
          ASSIGN    WOCCBED-PATCOUNT,F8
          ADD       WOCCBED,TOTALBED
          ADD       PATCOUNT,TOTALPAT
          ADD       PREADMIS,TOTALPRE
          ADD       BOOKINGS,TOTALBOK
          CALL      CALBAR00
          IF        WOCCBED=0
            MOVE      ZERO,PERCENT
          ENDIF
.
          CLEAR     WARDLINK
          APPEND    LINKPRG,WARDLINK
          APPEND    ".pbl?",WARDLINK
          APPEND    "&reportno=",WARDLINK
          APPEND    LINKREP,WARDLINK
          APPEND    "&template=",WARDLINK
          APPEND    LINKTEM,WARDLINK
          APPEND    "&wardcode=",WARDLINK
          APPEND    WWARD,WARDLINK
          RESET     WARDLINK
.
          UNPACK   FILWIDTH,KEY6,KEY3
          WRITE    HTMLFILE;"t.addRow(",QUOTE,WARDLINK,QUOTE,COMMA:
                                         QUOTE,WBDESC,QUOTE,COMMA:
                                         QUOTE,WEXTN,QUOTE,COMMA:
                                         QUOTE,BOOKINGS,QUOTE,COMMA:
                                         QUOTE,PREADMIS,QUOTE,COMMA:
                                         QUOTE,WOCCBED,QUOTE,COMMA:
                                         QUOTE,PATCOUNT,QUOTE,COMMA:
                                         QUOTE,F8,QUOTE,COMMA:
                                         QUOTE,PERCENT,QUOTE,COMMA:
                                         QUOTE,KEY3,QUOTE,");"
.
          GOTO      WSTA0100
.
WSTA9000  MOVE      TOTALBED,WOCCBED
          MOVE      TOTALPAT,PATCOUNT
          MOVE      TOTALBOK,BOOKINGS
          MOVE      TOTALPRE,PREADMIS
          ASSIGN    WOCCBED-PATCOUNT,F8
          CALL      CALBAR00
          UNPACK    FILWIDTH,KEY6,KEY3
          MOVE      "Total            ",KEY20
          WRITE     HTMLFILE;"t.addTotal(",QUOTE,QUOTE,COMMA:
                                           QUOTE,KEY20,QUOTE,COMMA:
                                           QUOTE,SP1,QUOTE,COMMA:
                                           QUOTE,BOOKINGS,QUOTE,COMMA:
                                           QUOTE,PREADMIS,QUOTE,COMMA:
                                           QUOTE,WOCCBED,QUOTE,COMMA:
                                           QUOTE,PATCOUNT,QUOTE,COMMA:
                                           QUOTE,F8,QUOTE,COMMA:
                                           QUOTE,PERCENT,QUOTE,COMMA:
                                           QUOTE,KEY3,QUOTE,");"
.
WSTA9999  RETURN
.------------------------------------------------------------
. Calculate Number of Patients in a Ward
.------------------------------------------------------------
CALCP000  MOVE      WWARD,SAVEWARD
          MOVE      ZERO,SBYCOUNT
          MOVE      ZERO,PATCOUNT
          BRANCH    WINPUT,CALCP200
.
          PACK      KEY6,SAVEWARD,SP70
          CALL      RDWARD1
CALCP100  CALL      RDKWARD1
          BRANCH    OVRCD,CALCP400
          MATCH     SAVEWARD,WWARD         * same ward?
          GOTO      CALCP400 IF NOT EQUAL
          MATCH     ZEROVISN,WADMNO
          IF        !@EQUAL
            ADD       ONE,PATCOUNT
          ENDIF  
          MATCH     ZEROVISN,WSTBY
          IF        !@EQUAL
            ADD       ONE,PATCOUNT
            ADD       ONE,SBYCOUNT
          ENDIF  
          GOTO      CALCP100
.
. No Bed Ward
.
CALCP200  PACK      KEY13,SAVEWARD,SP20
          CALL      RDSNOBE1
CALCP300  CALL      RDKNOBE1                * read next record
          BRANCH    OVRCD,CALCP400
          MATCH     SAVEWARD,NBWARD         * same ward?
          GOTO      CALCP400 IF NOT EQUAL
          ADD       ONE,PATCOUNT
          GOTO      CALCP300
.
. Calculate Pre Admissions & Bookings for Ward for Today
.
CALCP400  MOVE      ZERO,PREADMIS
          MOVE      ZERO,BOOKINGS
          PACK      KEY19,SAVEWARD,CURRDATE,SP70
          CALL      RSBKREC7
CALCP500  CALL      RKBKREC7
          BRANCH    OVRCD,CALCP900
          MATCH     SAVEWARD,BKREWARD
          GOTO      CALCP900 IF NOT EQUAL
          MATCH     BKREEDAT,CURRDATE
          GOTO      CALCP900 IF LESS
          IF        BKRESTAT=0
            ADD       ONE,BOOKINGS
          ENDIF
          IF        BKRESTAT=1
            ADD       ONE,PREADMIS
          ENDIF
          GOTO      CALCP500
.
CALCP900  PACK      KEY6,SAVEWARD,SP70
          CALL      RDWARD1
.
CALCP999  RETURN
.------------------------------------------------------------
. Calculate Bar for Ward
.------------------------------------------------------------
CALBAR00  MOVE      "10",FILHIGH
          ASSIGN    PATCOUNT/WOCCBED*150,F3
          ASSIGN    PATCOUNT/WOCCBED*100.0,PERCENT
          IF        F3>150
            MOVE      "150",F3
          ENDIF
          IF        F3=0
            MOVE      ONE,F3
            MOVE      "1",FILHIGH
          ENDIF
          MOVE      F3,KEY3
          SQUEEZE   KEY3
          CLEAR     FILWIDTH
          APPEND    "WIDTH=",FILWIDTH
          APPEND    KEY3,FILWIDTH
          RESET     FILWIDTH
.
          MOVE      FILHIGH,KEY3
          SQUEEZE   KEY3
          CLEAR     FILHIGHT
          APPEND    "HEIGHT=",FILHIGHT
          APPEND    KEY3,FILHIGHT
          RESET     FILHIGHT
.
CALBAR99  RETURN
.------------------------------------------------------------
. Ward/Bed History 
.------------------------------------------------------------
WRHIS000  PACK      KEY6,PTWRD001,PTWRD002   * read ward/bed file
          CALL      RDWARD1
          BRANCH    OVRCD,WRHIS999
.
          MOVE      ZERO,SACTIVE          * Initialise active indicator
          MOVE      ZERO,SNOBEDS          * Initialise number of beds
          MOVE      SP70,SWBTYPE          * Initialise ward class/room type
.
          MOVE      WACTIVE,SWACTIVE      * Save the current status
          MATCH     SP3,PTWRD002
          IF        !@EQUAL
            MOVE      WRTYPE,SWBTYPE      * Bed history, save room type
          ELSE
            MOVE      WCLASS,SWBTYPE      * Ward history, save ward class
            MOVE      WOCCBED,SNOBEDS     * Ward history, save no of beds
          ENDIF
.
          PACK      KEY14,PTWRD001,PTWRD002,Z70
          CALL      RSPTWBH1
WRHIS010  CALL      RPPTWBH1   
          BRANCH    OVRCD,WRHIS999
.
          MATCH     WBHWARD,PTWRD001
          GOTO      WRHIS999 IF NOT EQUAL
.
          MATCH     WBHBED,PTWRD002
          GOTO      WRHIS999 IF NOT EQUAL
.
          MATCH     SP8,WBHDATE
          GOTO      WRHIS999 IF EQUAL
.
          UNPACK    WBHDATE,CCENT,CYEAR,CMON,CDAY
          CALL      CNXT0000                 * Calculate the next Date
          UNPACK    KEY8,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
.
          COMPARE   ONE,SWACTIVE
          IF        @EQUAL
            MOVE      "No",ACTVDESC
          ELSE
            MOVE      "Yes",ACTVDESC
          ENDIF
          MOVE      WBHACTV,SWACTIVE    * Save the active
.
.         Check if view history for Ward or Bed
.
          MOVE      SP70,TDESC
          MATCH     SP3,PTWRD002
          IF        !@EQUAL
            MATCH     SP3,SWBTYPE
            IF        !@EQUAL
              PACK      KEY5,CODERT,SWBTYPE
              CALL      RDCODE1
              IF        OVRCD=1
                MOVE      "Invalid Room Type",TDESC
              ENDIF
            ENDIF
.
            WRITE     HTMLFILE;"<tr><td>",CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR:
                               "</td><td>",ACTVDESC,"</td><td>",TDESC:
                               "</td></tr>"
            MOVE      WBHRTYP,SWBTYPE          * save room type
          ELSE
            MATCH     SP3,SWBTYPE
            IF        !@EQUAL
              PACK      KEY5,ANSC,ANSG,SWBTYPE
              CALL      RDCODE1
              IF        OVRCD=1
                MOVE      "Invalid Ward Class",TDESC
              ENDIF
            ENDIF
            WRITE     HTMLFILE;"<tr><td>",CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR:
                               "</td><td>",ACTVDESC,"</td><td>",SNOBEDS:
                               "</td><td>",TDESC,"</td></tr>"
            MOVE      WBHCLAS,SWBTYPE          * save ward class
            MOVE      WBHNOCC,SNOBEDS          * save number of beds
          ENDIF
.
          GOTO      WRHIS010
.
WRHIS999  RETURN 
.------------------------------------------------------------
. Table of Beds
.------------------------------------------------------------
WBDTB000  PACK      KEY6,PTWRD001,SP70
          CALL      RDSWARD1
WBDTB100  CALL      RDKWARD1
          BRANCH    OVRCD,WBDTB999
          MATCH     PTWRD001,WWARD
          GOTO      WBDTB999 IF NOT EQUAL
          MATCH     SP70,WBED
          GOTO      WBDTB100 IF EQUAL
.
          MOVE      "BT",CATEGORY
          MOVE      WEFRBT,CODE
          CALL      GETCOD00
          MOVE      TDESC,BEDTDESC
          MOVE      "RT",CATEGORY
          MOVE      WRTYPE,CODE
          CALL      GETCOD00
          MOVE      TDESC,ROOMDESC
          CLEAR     HTMLLINK
          APPEND    "javascript:UpdateBed('",HTMLLINK
          APPEND    WWARD,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    WBED,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
          COMPARE   ZERO,WACTIVE
          IF        @EQUAL
            MOVE      "Yes",ACTVDESC
          ELSE
            MOVE      "No",ACTVDESC
          ENDIF 
.
          WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":
                             "#"",WWARD,"#",":
                             "#"",WBED,"#",":
                             "#"",WBDESC,"#",":
                             "#"",WEXTN,"#",":
                             "#"",ROOMDESC,"#",":
                             "#"",BEDTDESC,"#",":
                             "#"",ACTVDESC,"#",":
                             "#"",PTWDNSTA,"#");"
          GOTO      WBDTB100
.
WBDTB999  RETURN
.------------------------------------------------------------
. Table of Wards : Table Sort  
.------------------------------------------------------------
WRTAB000  PACK      KEY6,SP70
          CALL      RDSWARD3
WRTAB100  CALL      RDKWARD3
          BRANCH    OVRCD,WRTAB999
          MATCH     SP70,WBED
          GOTO      WRTAB999 IF NOT EQUAL
.
          CLEAR     HTMLLINK
          APPEND    "javascript:UpdateWard('",HTMLLINK
          APPEND    WWARD,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
          COMPARE   ZERO,WINPUT
          IF        @EQUAL
            MOVE      "No",ONLYDESC
          ELSE
            MOVE      "Yes",ONLYDESC
          ENDIF 
.
          COMPARE   ZERO,WACTIVE
          IF        @EQUAL
            MOVE      "Yes",ACTVDESC
          ELSE
            MOVE      "No",ACTVDESC
          ENDIF 
.
          WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":
                             "#"",WWARD,"#",":
                             "#"",WBDESC,"#",":
                             "#"",WEXTN,"#",":
                             "#"",ACTVDESC,"#",":
                             "#"",ONLYDESC,"#",":
                             "#"",PTWDNSTA,"#");"
          GOTO      WRTAB100
.
WRTAB999  RETURN
.------------------------------------------------------------
. Format Patient Name <b>SURNAME</b>Title Given Names
.------------------------------------------------------------
PATNAM00  CLEAR     DISPNAME
...       REP       "' ",PSNAME
          REP       "#" ",PSNAME
...       REP       "' ",PGNAME
          REP       "#" ",PGNAME
          MOVE      SP70,PATFNAME
          REP       UPPLOW,PSNAME
          APPEND    "<b>",DISPNAME
          APPEND    PSNAME,DISPNAME
          APPEND    "</b>",DISPNAME
          PACK      NAME35,PTITL,SP70
          CALL      NAMSTR00
          PACK      NAME35,PGNAME,SP70
          CALL      NAMSTR00
          APPEND    SP70,DISPNAME
          RESET     DISPNAME
          MOVE      DISPNAME,PATFNAME
          STRIP     PATFNAME
          RETURN
.------------------------------------------------------------
.
          INC       IBAWEBCD    
          INC       BOKRC1IO/INC
          INC       MRTLOCIO/INC
          INC       PATONLIO/INC
          INC       PATMA1IO/INC    
          INC       PATMI1IO/INC    
          INC       PMSVX1IO/INC    
          INC       PATVISIO/INC
          INC       PATDO1IO/INC
          INC       PATWR1IO/INC        * Ward Bed File
          INC       PATWBHIO/INC        * Ward Bed History File
          INC       PATADWIO/INC        * Ward Audit File
          INC       PATNOBIO/INC
          INC       PATNURIO/INC
          INC       PATHSPIO/INC        * Hospital File
.
          INC       PATPR1IO/INC
.          
          INC       PATDOCTM    
          INC       PATDOCCD    
          INC       PATWRDTM
          INC       CALCAGE    
          INC       NAMSTRCD    
          INC       WEBDATCD    
          INC       DAYOFWEK    
.

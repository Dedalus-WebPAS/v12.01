.*****************************************************************************
.*    Program      : IBAOUT77                                                *
.*    Description  : Clinic List                `                            *
.*                                                                           *
.*    Author       : Whirlpool Worlds best surfer                            *
.*                                                                           *
.*    Modifications:                                                         *
.*****************************************************************************
.*       V11.04.01  25/09/2024  Ebon Clements    TSK 0952159                 *
.*                  Fixed missing print in header for all hospital           *
.*       V11.02.01  09/02/2022  Thanh T          TSK 0905641                 *
.*                  Recompiled as OUTMA1FD changes                           *
.*       V10.11.02  27/11/2017 Thanh T.        TSK 0821710                   *
.*                  Recompiled as PATMISTD changed                           *
.*       V10.11.01  08/09/2017 Thanh T.        TSK 0821710                   *
.*                  Audit Amission/outpatient visit comments                 *
.******************************************************************************
.*       V10.07.01  30/10/2015  Ebon Clements  CAR 268970                    *
.*                  Change to use OUTCLIFD index 1 instead of index 2        *
.*       V10.05.01  17/12/2014  Ebon Clements  CAR 261630                    *
.*                  Removed port number from temp file name                  *
.*       V10.03.03  17/10/2012  Jill Parkinson CAR 273375                    *
.*                  Recompiled for DATECONV - first day of year calculation  *
.*       V10.03.02  27/07/2012  Peter McMullen CAR 266703                    *
.*                  Fix formatting of Dr name in clinic id output line       *
.*       V10.03.01  19/03/2012  Peter McMullen  CAR 259587                   *
.*                  Remove all reference to redundant file patyears          *
.*        V9.12.04  27/11/2009  WeeLee Koo    CAR 210448                     *
.*                  Displayed Preferred Language in description not code     *
.*        V9.12.03  26/11/2009  WeeLee Koo    CAR 210448                     *
.*                  Fixed Preferred Language 1 (PMPXLNG1)                    *
.*                  Allowed Future Date for Date Range (KDRN0000)            *
.*        V9.12.01  06/10/2009  Ebon Clements CAR 205398                     *
.*                  Added report option 3 - By clinic Range                  *
.*        V9.09.03  04/03/2008  Peter Vela    CAR 162914                     *
.*                  Recompiled for KYOUTCLI - readk of outcliaf              *
.*        V9.09.02  21/01/2008  Ebon Clements CAR 133697                     *
.*                  Added IBARSH test to VCLI0000                            *
.*        V9.09.01  28/07/2007  Jill Habasque CAR 103645                     *
.*                  Recompiled for KYOUTCLI - read of pmshcpaf               *
.*        V9.02.01  19/10/2001  Dean Cramer                                  *
.*                  Added heading when no data reported - for web            *
.*        V5.08.02  19/09/2000  Steve Armstrong   srf 647263                 *
.*                  Recompiled for changes to OUTDSCLN                       *
.*        V5.08.01  29/08/2000  Caleb Sharman                                *
.*                  Changed Health Fund variables to be 8 chars              *
.*       V5.07.B01  15/03/2000  Steve Armstrong     5.08 Mods                *
.*                  Mods for changes to OUTCLIFD (effective date)            *
.*****************************************************************************
.*       V5.07.B01  02/11/1998  Jim Stathopoulos                             *
.*                  507 Changes                                              *
.*****************************************************************************
.*        V5.01.01  01/04/1993  Whirlpool                                    *
.*                  Fixed bugs / added display of extracted data             *
.*        V5.01.02  01/04/1993  Graeme Williams                              *
.*                  use CHOUR/CMIN in return from KEYTIME, not IHOUR/IMIN    *
.*                  expand list of errors when validating clinic             *
.*        V5.01.03  01/04/1993  Steve O'Gorman                               *
.*                  Modified to get comments from PATCMNT file and not use   *
.*                  the OBACOMM variable in the BOKB record.                 *
.*        V5.01.04  16/04/1993  ROD                                          *
.*                  Fix sum Whirlpool bugs. Print first name not initial     *
.*        V5.01.05  19/04/1993  ROD                                          *
.*                  Fix more Whirlpool bugs.                                 *
.*        V5.01.06  27/05/1994 Ian Rutt                                       *
.*                  Fixed Global Recompile Bugs                               * 
.*        V5.01.07  20/07/1994  Rob Leonard                                   *
.*                  Fixed hospital keyin                                      * 
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
          INC       OUTMA1FD/INC
          INC       OUTOSFFD/INC
          INC       OUTBOXFD/INC
          INC       OUTCLIFD/INC
          INC       OUTCONFD/INC            * system parameters
          INC       OUTCTYFD/INC
          INC       OUTRSHFD/INC
          INC       VISMDTFD/INC
          INC       VISMTXFD/INC
          INC       PATMISTD/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATMA1FD/INC
          INC       PATYERFD/INC
          INC       PATPR1FD/INC
          INC       PATHSPFD/INC
          INC       PATMI1FD/INC
          INC       PATDO1FD/INC
          INC       PMSHCPFD/INC            * National HCP file
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
.
BOOKPATS  FORM      4
CDYSDAYS  FORM      6
CDYSFDTE  DIM       8
CDYSTDTE  DIM       8
CDYSYEAR  FORM      2
CLINDESE  DIM       25
CLINDESS  DIM       25
CMDLINE   DIM       80
CURRWBED  DIM       7
DAYOWEEK  DIM       3
DESC28    DIM       28
DIM4      DIM       4
DIM14     DIM       14
DIM25     DIM       25
DIM40     DIM       40
DIM6      DIM       6
EDOL      DIM       10
EMPSLOTS  FORM      4
ENDDATE   DIM       8
ECLINTYP  DIM       6
FDDATE    DIM       10
FNDPAT    FORM      1
FRI       INIT      "FRI"
CLINDATE  DIM       8
HOSPNAME  DIM       30
HOSPNAMS  DIM       30
INTRPRTR  DIM       5
KYCLINE   DIM       6
KYCLINS   DIM       6
KYHOSPE   DIM       3
KYHOSPS   DIM       3
MISSING   INIT      "Missing code: "
MON       INIT      "MON"
PATNAME   DIM       30
PKYCLINE  DIM       6
PKYCLINS  DIM       6
PRDOB     DIM       10
PRINTEMP  FORM      1
RESCH     DIM       1
SAT       INIT      "SAT"
SAVCDATE  DIM       8
SAVCLID   DIM       6
SAVCLSE   DIM       6
SAVHOSP   DIM       3
SAVTIME   DIM       5
SCLINTYP  DIM       6
SDOL      DIM       10
SHCTYP    DIM       20
STRTDATE  DIM       8
SUN       INIT      "SUN"
TDDATE    DIM       10
THU       INIT      "THU"
TIME      DIM       5
TODATE    DIM       8
TODAY     DIM       8
TUE       INIT      "TUE"
VSITE     DIM       6
VCLIN     DIM       6
VTIME     DIM       5
WED       INIT      "WED"
WKSWAIT   FORM      3
.
DDAY      DIM       9
Z20       INIT      "zzzzzzzzzzzzzzzzzzzz"
.
.
.-----------------------------------------------------------------------
PRGID     INIT      "IBAOUT77"
PRGNAM    INIT      "Clinic List"
VERSION   INIT      "V12.01.00"
.-----------------------------------------------------------------------
.
.****************************************************************************
.* ML0000  :  Mainline code                                                 *
.****************************************************************************
ML0000    CALL      INIT0000                      * initialisation
.
ML0100    CALL      MENU0000                      * get menu options
          BRANCH    EXIT,ML9999                   * Exit selected
          BRANCH    MOPTION,ML1000,ML5000,ML8000
.                           All   /Single  Clinics/Clinic Range
.
ML1000    CALL      SCRN0000                      * Display thge screen
.
ML1100    CALL      KDAT0000                      * Keyin the date
          BRANCH    EXIT,ML0100
.
ML1200    CALL      KHSP0000                      * Keyin the hospitals
          BRANCH    EXIT,ML1100
.
          CALL      CONTQST   
          BRANCH    CEXIT,ML3000,ML1100,ML0100
ML3000    CALL      RPRT0000                      * Print Report
          GOTO      ML0100
.
.-----    Print the report for a single clinic ------
.
ML5000    CALL      SCRN0000                      * Display the screen
.
ML5100    CALL      KCLI0000                      * Clinic id
          BRANCH    EXIT,ML0100

ML5200    CALL      KDAT0000                      * Clinic Date
          BRANCH    EXIT,ML5100
     
ML5300    CALL      KTIM0000                      * Clinic Time
          BRANCH    EXIT,ML5200
.
          CALL      VCLI0000                      * Validate the clinic
          BRANCH    EXIT,ML5000
.
          CALL      CONTQST
          BRANCH    CEXIT,ML5500,ML5000,ML0100
.
ML5500    CALL      RPRT0000                      * Print Report 
          GOTO      ML0100
.
.-----    Print the report for a clinic Range ------
.
ML8000    CALL      SCRN0000                      * Display the screen
.
ML8100    CALL      KHSP0000                      * Keyin the hospitals
          BRANCH    EXIT,ML0100
.
ML8200    CALL      KCLR0000                      * Clinic id range
          BRANCH    EXIT,ML8100

ML8300    CALL      KDRN0000                      * Clinic Date range
          BRANCH    EXIT,ML8200
.         
ML8400    CALL      KCTY0000                      * Clinic Type range
          BRANCH    EXIT,ML8300
.         
ML8500    CALL      KEMP0000                      * Print Empty Slots
          BRANCH    EXIT,ML8400
.         
          CALL      CONTQST
          BRANCH    CEXIT,ML8600,ML8000,ML0100
.
ML8600    CALL      RPRT0000                      * Print Report
          GOTO      ML0100
.
ML9999    CHAIN     PGM
+
.****************************************************************************
.* INIT0000  :  Initialisation                                              *
.****************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P60:24,"controlf"
          OPEN      CONTROLF,"controlf"
.
          PACK      CFNAME,UID,FILMA1A1
          DISPLAY   *P60:24,CFNAME
          CALL      OPOTMA11
.
          PACK      CFNAME,UID,FILCTYA1
          DISPLAY   *P60:24,CFNAME
          OPEN      OUTCTYA1,CFNAME
.
          PACK      CFNAME,UID,FILBOKA1
          DISPLAY   *P60:24,CFNAME
          OPEN      OUTBOKA1,CFNAME
.
          PACK      CFNAME,UID,FILRSHA1
          DISPLAY   *P60:24,CFNAME
          OPEN      OUTRSHA1,CFNAME
.
          PACK      CFNAME,UID,FILBB1A1
          DISPLAY   *P60:24,CFNAME
          CALL      OPOTBB11
.
          PACK      CFNAME,UID,FILCLIA1
          DISPLAY   *P60:24,CFNAME
          OPEN      OUTCLIA1,CFNAME
.
          PACK      CFNAME,UID,FILBOXA1
          DISPLAY   *P60:24,CFNAME
          OPEN      OUTBOXA1,CFNAME
.
          DISPLAY   *P60:24,"outosfaf"
          OPEN      OUTOSFA1,"outosfaf"
          OPEN      OUTOSFA2,"outosfaf"
.
          DISPLAY   *P64:24,"vismdtaf";
          OPEN      VISMDTA1,"vismdtaf"
          DISPLAY   *P64:24,"vismtxaf";
          OPEN      VISMTXA1,"vismtxaf"
.
          DISPLAY   *P60:24,"pathspaf"
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P60:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P60:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P60:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A8,"patmi1af"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P60:24,"pmsvx1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P60:24,"patpramf"
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
.
          DISPLAY   *P60:24,"patpraxf"
.
          DISPLAY   *P60:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"pmshcpaf";
          OPEN      PMSHCPA1,"pmshcpaf"
.
          DISPLAY   *P1:24,*EL;
.
          MOVE      ONE,CNEWDS
          CALL      IBACLOCK
          PACK      TODAY,CCC,CYY,CMM,CDD
          REP       " 0",TODAY
.
INIT9999  RETURN
+
.****************************************************************************
.* MENU0000  :  Main Menu                                                   *
.****************************************************************************
MENU0000  DISPLAY   *P1:4,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = All Clinics":
                    *P1:6,*V2LON,"2",*HOFF," = Single Clinic":
                    *P1:7,*V2LON,"3",*HOFF," = By Clinic Range"
.
MENU1000  KEYIN     *P1:9,"Select Option ? ":
                    *P17:9,*DV,UNDLN1:
                    *P17:9,*V2LON,MOPTION
.
          IF        MOPTION = 0
            MOVE      ONE,EXIT
            GOTO      MENU9999
          ENDIF
.
          MOVE      ZERO,EXIT
          BRANCH    MOPTION,MENU9999,MENU9999,MENU9999
          BEEP
          GOTO      MENU1000
.
MENU9999  RETURN
+
.**************************************************************************
.*                              SCRN0000                                  *
.*                        Display the required screen                     *
.**************************************************************************
.
SCRN0000  IF        MOPTION  = 1
            DISPLAY   *P1:4,*EF,"Clinic Date     : ":
                      *P1:6,"Hospital  From  : ":
                      *P1:7,"          To    : "

          ENDIF
          IF        MOPTION = 2
            DISPLAY   *P1:4,*EF,"Clinic Id       : ":
                      *P1:5,"Clinic Date     : ":
                      *P1:6,"Clinic Time     : "
          ENDIF
          IF        MOPTION = 3
            DISPLAY   *P1:4,*EF:
                      *P1:6,"Hospital  From  : ":
                      *P1:7,"Hospital To     : ":
                      *P1:8,"From Clinic     : ":
                      *P1:9,"To Clinic       : ":
                      *P1:10,"From Date       : ":
                      *P1:11,"To Date         : ":
                      *P1:12,"From Clinic Type: ":
                      *P1:13,"To Clinic Type  : ":
                      *P1:14,"Print Empty Slots : "
          ENDIF
SCRN9999  RETURN
.**************************************************************************
.*  KCLI0000  :  Keyin a Clinic Id range                                  *
.**************************************************************************
KCLI0000  MOVE      ZERO,EXIT
          UNPACK    SP30,KYCLINS,CLINDATE,TIME
          MOVE      "19",ECOL
          MOVE      "4",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
.
          CALL      KYOUTCLI
          BRANCH    EXIT,KCLI9000,KCLI9000,KCLI9000      * nothing or EXITCHAR
.
          MOVE      OCACLIN,KYCLINS               * valid input
          MOVE      OCACLIN,PKYCLINS              * print variable
          DISPLAY   *P27:4,OCADESC
          MOVE      OCADESC,CLINDESS
.
KCLI8000  MOVE      ZERO,EXIT                     * All is howdy dudey
          GOTO      KCLI9999
.
KCLI9000  MOVE      ONE,EXIT                      * EXITCHAR
.
KCLI9999  RETURN
+
.**************************************************************************
.*  KHSP0000  :  Keyin a Hospital code range                              *
.**************************************************************************
KHSP0000  MOVE      ZERO,EXIT
          MOVE      "19",ECOL
          MOVE      "6",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP3,CKYIDEF3
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHSP2000,KHSP9000        * nothing or EXITCHAR
.
          MOVE      KEY3,KYHOSPS                  * valid input
          DISPLAY   *P27:6,PTHSNAME
          MOVE      PTHSNAME,HOSPNAMS
          GOTO      KHSP4000
.
KHSP2000  MOVE      SP3,KYHOSPS                   * nothing input
          DISPLAY   *P19:6,*V2LON,"Start",*EL
          MOVE      "Start",PTHSNAME
          MOVE      PTHSNAME,HOSPNAMS
.
. keyin end hospital code
.
KHSP4000  MOVE      "19",ECOL
          MOVE      "7",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP3,CKYIDEF3
.
          CALL      PATHSPKY
          BRANCH    EXIT,KHSP5000,KHSP9000        * nothing or EXITCHAR
.
          MOVE      KEY3,KYHOSPE                  * valid input
          DISPLAY   *P27:7,PTHSNAME
          MOVE      PTHSNAME,HOSPNAME
.
. check if valid range
.
          MATCH     KYHOSPS,KYHOSPE
          GOTO      KHSP8000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Invalid range.  ";
          CALL      HITENTER
          GOTO      KHSP0000
.
KHSP5000  MOVE      "zzz",KYHOSPE                 * nothing input
          DISPLAY   *P19:7,*V2LON,"Finish",*EL
          MOVE      "Finish",PTHSNAME
          MOVE      PTHSNAME,HOSPNAME
.
KHSP8000  MOVE      ZERO,EXIT                     * Ok.
          GOTO      KHSP9999
.
KHSP9000  MOVE      ONE,EXIT                      * EXITCHAR
.
KHSP9999  RETURN
+
.**************************************************************************
.*  KDAT0000      Keyin a date 
.**************************************************************************
KDAT0000  MOVE      ZERO,EXIT
          UNPACK    TODAY,CCENT,CYEAR,CMON,CDAY
          MOVE      "19",CCOL
          IF        MOPTION = 1
            MOVE      FOUR,CVERT
          ELSE
            MOVE      FIVE,CVERT
          ENDIF
.
          MOVE      CCC,CCENT
          MOVE      ZERO,CDEFDTE
.
KDAT1000  CALL      KEYDATE
          BRANCH    CQUEST,KDAT5000
          BRANCH    OVRCD,KDAT9500                * anything input
.
          PACK      CLINDATE,CCENT,CYEAR,CMON,CDAY
          PACK      FDDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR    * display var
          REP       " 0",FDDATE 
          MOVE      ZERO,EXIT                     * Continue
          GOTO      KDAT9999
.
KDAT5000  CALL      GETSCR00
          MOVE      IDDD,VSITE
          MOVE      KYCLINS,VCLIN
          CALL      OUTDSMAS
          CALL      PUTSCR00
          GOTO      KDAT0000
. 
KDAT9500  MOVE      ONE,EXIT                      * No (don't continue)
.
KDAT9999  RETURN
+
. *****************************************************************************
.*                             KTIM0000                                       *
.*                      Keyin time for clinic  details                        *
. *****************************************************************************
KTIM0000  MOVE      FALSE,EXIT         * initialize to - time entered
          MOVE      ONE,CCANLDTE
          CALL      GDCT0000           * get default clinic time
          UNPACK    VTIME,CHOUR,ANS,CMIN
.
.         Set up variables for the keyin of the time.
.
          MOVE      "19",CCOL          * Column to keyin the date
          MOVE      SIX,CVERT      
.
          CALL      KEYTIME              * Key in the time
          BRANCH    CQUEST,KTIM5000      * ? option selected.
.
          PACK      CHOUR,CHOUR,SP2
          MATCH     SP2,CHOUR
          GOTO      KTIM9000 IF EQUAL
.
.         We have a valid time
.
          PACK      TIME,CHOUR,COLON,CMIN
          REP       " 0",TIME      * Set up the selected time
.
          MOVE      ZERO,EXIT           * Returning with a time
          GOTO      KTIM9999            * Return with this time
.
KTIM5000  CALL      GETSCR00
          MOVE      IDDD,VSITE
          MOVE      KYCLINS,VCLIN
          CALL      OUTDSMAS
          CALL      PUTSCR00
          GOTO      KTIM0000
.
KTIM9000  MOVE      ONE,EXIT
.
KTIM9999  RETURN

.****************************************************************************
.*                             VCLI0000                                     *
.*                       Validate the clinic                                *
.****************************************************************************
.
VCLI0000  MOVE      ZERO,EXIT
.
          PACK      KEY25,IDDD,KYCLINS,CLINDATE,TIME
          PACK      KEY28,KEY25,SP10
          CALL      RDSBOKA1
VCLI1000  CALL      RDKBOKA1
          BRANCH    OVRCD,VCLI9000
.
          PACK      DIM25,OBASITE,OBACLIN,OBADATE,OBASTRT
          MATCH     DIM25,KEY25
          GOTO      VCLI9000 IF NOT EQUAL
.
          PACK      KEY18,OBASITE,OBACLIN,OBADAY,OBASTRT
          CALL      RDMASA1
          BRANCH    OVRCD,VCLI9100
.
          PACK      KEY34,OTMAHOSP,OMASITE,OTMACLSE,OMACLIN,OBADATE,OMASTART,OMATYP
          CALL      RDOTOSF1
          BRANCH    OVRCD,VCLI9200
.
          GOTO      VCLI9999

.
VCLI9000  MOVE      ONE,EXIT
.
          MATCH     "IBARSH",PGM
          GOTO      VCLI9999 IF EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"Session does not exist. ";
          CALL      HITENTER
          GOTO      VCLI9999
.
VCLI9100  MOVE      ONE,EXIT
.
          MATCH     "IBARSH",PGM
          GOTO      VCLI9999 IF EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"Clinic master does not exist. ";
          CALL      HITENTER
          GOTO      VCLI9999
.
VCLI9200  MOVE      ONE,EXIT
.
          MATCH     "IBARSH",PGM
          GOTO      VCLI9999 IF EQUAL
.
          MOVE      ONE,EXIT
          DISPLAY   *P1:24,*EL,*B,"Session header does not exist. ";
          CALL      HITENTER
.
VCLI9999  RETURN
+
.****************************************************************************
.* RPRT0000  :  Print Report                                                *
.****************************************************************************
RPRT0000  DISPLAY   *P1:24,*EL:
                    *P1:24,"Processing Clinic : ":
                    *P28:24,"Time : ",*P41:24,"Slot :":
                    *P54:24,"U/R :"
.
          CALL      IBACLOCK                      * initialise a few things
          MOVE      ZERO,CPAGENO
          MOVE      ONE,CNOUNDLN
          MOVE      "60",CLNO                     * force new page
          MOVE      SP6,SAVCLID
          MOVE      SP8,SAVCDATE
          MOVE      SP6,SAVCLSE
          MOVE      SP3,SAVHOSP
          MOVE      SP5,SAVTIME
          MOVE      ZERO,EMPSLOTS                 * init # empty slots
          MOVE      ZERO,BOOKPATS                 * init # booked patients
          MOVE      ZERO,FNDPAT                   * assume no bookings found
.
          IF        MOPTION = 2
            GOTO      RPRT1500
          ENDIF
.
          PACK      KEY40,KYHOSPS,SP4
          CALL      RSOTOSF2
RPRT1000  CALL      RKOTOSF2
          BRANCH    OVRCD,RPRT8000                * no more records
.
. check if we have a valid record
.
          MATCH     OTOSHOSP,KYHOSPE              * still in range?
          GOTO      RPRT8000 IF LESS              * no
.
          COMPARE   THREE,MOPTION                 * Clinic ID range option
          GOTO      RPRT1100 IF NOT EQUAL
.
          MATCH     SP70,SCLINTYP
          IF        !@EQUAL
            MATCH     SCLINTYP,OTOSCTYP           * from clinic type Ok.?
            GOTO      RPRT1000 IF LESS            * no
          ENDIF
.         
          MATCH     SP70,ECLINTYP
          IF        !@EQUAL
            MATCH     OTOSCTYP,ECLINTYP           * to clinic type Ok.?
            GOTO      RPRT1000 IF LESS            * no
          ENDIF
.
          MATCH     SP70,KYCLINS
          IF        !@EQUAL
            MATCH     KYCLINS,OTOSCLID           * from clinic id Ok.?
            GOTO      RPRT1000 IF LESS           * no
          ENDIF
.
          MATCH     SP70,KYCLINE
          IF        !@EQUAL
            MATCH     OTOSCLID,KYCLINE            * to clinic id Ok.?
            GOTO      RPRT1000 IF LESS            * no
          ENDIF
.
          MATCH     STRTDATE,OTOSDATE             * from date Ok.?
          GOTO      RPRT1000 IF LESS              * no
.
          MATCH     OTOSDATE,ENDDATE              * to date Ok.?
          GOTO      RPRT1000 IF LESS              * no
.
          GOTO      RPRT1500
.
RPRT1100  MATCH     CLINDATE,OTOSDATE             * Start date Ok.?
          GOTO      RPRT1000 IF LESS              * no
          MATCH     OTOSDATE,CLINDATE             * End date Ok.?
          GOTO      RPRT1000 IF LESS              * no
.
. loop thru bonkings file for patients in the above session
.
RPRT1500  DISPLAY   *P21:24,*V2LON,OTOSCLID,*P35:24,OTOSTIME
          PACK      KEY28,OTOSSITE,OTOSCLID,OTOSDATE,OTOSTIME,SP10
          CALL      RDSBOKA1
RPRT2000  CALL      RDKBOKA1
          IF        MOPTION = 1 | MOPTION = 3
            BRANCH    OVRCD,RPRT1000
          ELSE
            BRANCH    OVRCD,RPRT8000
          ENDIF
.
          PACK      DIM25,OBASITE,OBACLIN,OBADATE,OBASTRT
          MATCH     DIM25,KEY28                   * still same key?
          IF        !@EQUAL
            IF        MOPTION = 1 | MOPTION = 3
              GOTO      RPRT1000 
            ELSE 
              GOTO      RPRT8000 
            ENDIF
          ENDIF
.
          COMPARE   THREE,MOPTION                * By clinic range
          GOTO      RPRT3000 IF NOT EQUAL
.
          IF        OBASTAT=7
            CALL      CLOUTBB1
            CALL      CLPATMIS
            CALL      CLPMSPX2
            MOVE      SP70,PRDOB
            MOVE      SP70,PATNAME
.
            PACK      KEY5,ANSC,ANSV,OBAVISIT,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              MOVE      TDESC,PATNAME
            ENDIF
.
            PACK      KEY28,OBASITE,OBACLIN,OBADATE,OBASTRT,OBASLOT,SP70
            CALL      RDOTBOX1
            IF        OVRCD<>1
              MOVE      OTBXCOMM,PATNAME
            ENDIF
            GOTO      RPRT4100
          ENDIF
.
          COMPARE   ONE,PRINTEMP                 * Print empty slots
          GOTO      RPRT3000 IF NOT EQUAL
.
          IF        OBASTAT=0 | OBASTAT=9
            CALL      CLOUTBB1
            CALL      CLPATMIS
            CALL      CLPMSPX2
            MOVE      SP70,PRDOB
            MOVE      SP70,PATNAME
            GOTO      RPRT4100
          ENDIF
.
RPRT3000  PACK      KEY8,OBAOUTNO
          CALL      RDBOKB1
          BRANCH    OVRCD,RPRT2000
.
          COMPARE   THREE,MOPTION
          GOTO      RPRT4000 IF NOT EQUAL
.
          MOVE      SP10,INTRPRTR
          PACK      KEY8,OBAURNO,SP70
          CALL      RDPMPX21
          IF        OVRCD=1
            CALL      CLPMSPX2
          ELSE 
            MATCH     "1",PMPXINTR
            IF        @EQUAL
              PACK      KEY5,ANSL,ANSA,SP10
              CALL      RDSCODE1
RPRT3100      CALL      RDKCODE1
              BRANCH    OVRCD,RPRT4000
.
              MATCH     PMPXLNG1,ACODE
              GOTO      RPRT3100 IF NOT EQUAL
.
              MOVE      TDESC,INTRPRTR
            ENDIF
          ENDIF
.
RPRT3500  PACK      KEY17,OBAURNO,TWO,SP20
          CALL      RSPTMI18
          CALL      RKPTMI18
          BRANCH    OVRCD,RPRT4000          * not on file
.
          MATCH     OBAURNO,AURNO           * same U/R Number?
          GOTO      RPRT4000 IF NOT EQUAL   * No
.
          COMPARE   TWO,ASTAT
          GOTO      RPRT4000 IF NOT EQUAL
.
          GOTO      RPRT4100
.
RPRT4000  CALL      CLPATMIS
.
. check if we need to print a new Hospital
.
RPRT4100  MATCH     SP3,SAVHOSP
          IF        !@EQUAL
            MATCH     SAVHOSP,OTOSHOSP
            IF        !@EQUAL
              CALL      UND132
              CALL      PAGE0000
              MOVE      SP6,SAVCLID
              MOVE      SP6,SAVCLSE
              MOVE      SP3,SAVHOSP
              MOVE      SP8,SAVCDATE
              MOVE      SP5,SAVTIME
            ENDIF
          ENDIF
.
. check if we need to print a new Clinic Session
.
          MATCH     SP3,SAVCLSE
          IF        !@EQUAL
            MATCH     SAVCLSE,OTOSSESS
            IF        !@EQUAL
              CALL      UND132
              MOVE      ZERO,EMPSLOTS
              MOVE      ZERO,BOOKPATS
              MOVE      SP6,SAVCLSE
              MOVE      SP6,SAVCLID
              MOVE      SP3,SAVHOSP
              MOVE      SP8,SAVCDATE
              MOVE      SP5,SAVTIME
.
              PACK      KEY20,IDDD,OTOSSESS,CLINDATE
              CALL      RDCLIA1
              IF        OVRCD = 1
                CALL      RDPCLIA1
                IF        OVRCD = 0
                  MATCH     IDDD,OCASITE
                  IF        !@EQUAL
                    MOVE      ONE,OVRCD
                  ELSE
                    MATCH     OTOSSESS,OCACLIN
                    IF        !@EQUAL
                      MOVE      ONE,OVRCD
                    ENDIF
                  ENDIF
                ENDIF
              ENDIF
              IF        OVRCD = 1
                MOVE      "Unknown clinic",OCADESC
                PACK      OCADESC,OCADESC,SP30
              ENDIF
.
              CALL      PAGE0000
            ENDIF
          ENDIF
.
. check if we need a new clinic Id
.
          MATCH     SP6,SAVCLID
          IF        !@EQUAL
            MATCH     SAVCLID,OTOSCLID
            IF        !@EQUAL
              CALL      UND132
              CALL      PAGE0000 
              MOVE      ZERO,EMPSLOTS
              MOVE      ZERO,BOOKPATS
              MOVE      SP6,SAVCLSE
              MOVE      SP6,SAVCLID
              MOVE      SP3,SAVHOSP
              MOVE      SP8,SAVCDATE
              MOVE      SP5,SAVTIME
            ENDIF
          ENDIF
.
.
          MATCH     SP5,SAVTIME
          IF        !@EQUAL
            MATCH     SAVTIME,OBASTRT
            IF        !@EQUAL
              CALL      UND132
              CALL      PAGE0000
              MOVE      ZERO,EMPSLOTS
              MOVE      ZERO,BOOKPATS
              MOVE      SP6,SAVCLSE
              MOVE      SP6,SAVCLID
              MOVE      SP3,SAVHOSP
              MOVE      SP8,SAVCDATE
              MOVE      SP5,SAVTIME
            ENDIF
          ENDIF
.
          IF        MOPTION = 3 
            IF        OBASTAT=0 | OBASTAT=7 | OBASTAT=9
              GOTO      RPRT7000
            ENDIF
          ENDIF
.
          CALL      FDAT0000                      * format data for printing
.
RPRT7000  MOVE      ONE,FNDPAT                    * we have found a booking
          COMPARE   "55",CLNO                     * need a new page?
          CALL      PAGE0000 IF NOT LESS          * Print new page stuff
.
. print a line
.
.  -- Get Comments from the patcomn file  ---
.
          MOVE      SP70,OBACOMM
          MOVE      OBAOUTNO,KEY8
          CALL      VSCMNT00
.
          IF        MOPTION=3
            MATCH     SP70,ABED
            IF        !@EQUAL
              PACK      CURRWBED,AWARD,SLASH,ABED
            ELSE
              PACK      CURRWBED,AWARD,SP70
            ENDIF
          ENDIF
.
          MATCH     "       0",OBAURNO
          IF        @EQUAL
            MOVE      SP70,KEY8
          ELSE
            MOVE      OBAURNO,KEY8
          ENDIF
.
          DISPLAY   *P48:24,*V2LON,OBASLOT,*P60:24,KEY8
.
          PACK      PATNAME,PATNAME,SP70
          PRINT     *1,"|",*3,OBASLOT,*8,"|",*10,OBATIME,*16,"|",*18,OBAVISIT:
                    *24,"|",*26,PATNAME,*56,"|",*58,KEY8,*68,"|":
                    *70,PRDOB,*81,"|",*83,OTBBTRAN,*89,"|",*91,INTRPRTR:
                    *97,"|";
.
          IF        MOPTION=3
            MOVE      OBACOMM,KEY20
            PRINT     *100,KEY20,*123,"| ",CURRWBED,*132,"|"
          ELSE
            PRINT     *100,OBACOMM,*132,"|"
          ENDIF

          ADD       ONE,CLNO                      * increment line counter
.
          MOVE      OTOSCLID,SAVCLID
          MOVE      OTOSSESS,SAVCLSE
          MOVE      OTOSHOSP,SAVHOSP
          MOVE      OBADATE,SAVCDATE
          MOVE      OBASTRT,SAVTIME
          GOTO      RPRT2000
.
. we have finished printing patient data so print end of report details
.
RPRT8000  
          COMPARE   ZERO,CPAGENO
          GOTO      RPRT8500 IF NOT EQUAL
.
          CALL      HEAD132    
RPRT8500  CALL      UND132
          PRINT     *N,"*** End of Report ***"
.
RPRT9999  RETURN
.----------------------------------------------------------------------
. Retrieve the 1st comment text line of the latest active visit
. comment details
.----------------------------------------------------------------------
VSCMNT00  PACK      KEY17,KEY8,VSCMTTYP,Z70      * Key for start of comment
          CALL      RSVSMDT1
VSCMNT20  CALL      RPVSMDT1
          BRANCH    OVRCD,VSCMNT99
.
          MATCH     KEY8,VSMDVISN
          GOTO      VSCMNT99 IF NOT EQUAL
.
          MATCH     VSCMTTYP,VSMDTYPE
          GOTO      VSCMNT99 IF NOT EQUAL
.
          MATCH     "1",VSMDDELT          * is comment deleted
          GOTO      VSCMNT20 IF EQUAL
.
. Retrieve the comment text line details
.
          PACK      KEY20,VSMDVISN,VSMDTYPE,VSMDNOTE,SP70
          CALL      RSVSMTX1
VSCMNT60  CALL      RKVSMTX1
          BRANCH    OVRCD,VSCMNT99
.
          MATCH     VSMDVISN,VSMTVISN
          GOTO      VSCMNT99 IF NOT EQUAL
.
          MATCH     VSMDTYPE,VSMTTYPE
          GOTO      VSCMNT99 IF NOT EQUAL
.
          MATCH     VSMDNOTE,VSMTNOTE
          GOTO      VSCMNT99 IF NOT EQUAL
.
          MOVE      VSMTCMNT,OBACOMM    * get 1st comment line
.
VSCMNT99  RETURN
+
.***************************************************************************
.*  PAGE0000  :  Print stuff for new page                                  *
.***************************************************************************
PAGE0000  PACK      PTHSNAME,SP70
.
          MATCH     SP70,OTOSHOSP
          IF        !@EQUAL
            PACK      KEY3,OTOSHOSP,SP70
            CALL      RDPTHSP1
            IF        OVRCD=1
              PACK      PTHSNAME,MISSING,OTOSHOSP     * in case of error
            ENDIF
          ENDIF
.
          MOVE      PTHSNAME,CNAME                * get hospital name
.
          CALL      HEAD132                        * Print 3 line header
          PRINT     *40,CNAME
.
          IF        MOPTION=3
            PRINT     *40,"For : ",SDOL," to ",EDOL
          ELSE
            UNPACK    CLINDATE,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
.
            PRINT     *40,"For : ",CPCDATE
          ENDIF
.
          MOVE      OTOSCTYP,DIM6
          CALL      CLIT0000                      * get clinic desc
          PRINT     *N,*1,"Clinic Type  : ",OTOSCTYP,SP2,OCTDESC
.
          MOVE      OTOSSESS,DIM6
          CALL      CLID0000                      * get clinic desc
          PRINT     *1,"Session      : ",OTOSSESS,SP2,OCADESC,*N
          ADD       "8",CLNO
.
          CALL      PTHD0000                      * print table headings
.
PAGE9999  RETURN
+
.***************************************************************************
.*  FDAT0000  :  Format data for printing                                  *
.***************************************************************************
FDAT0000  MOVE      SP10,PRDOB
          MOVE      "(Missing Rec)",PATNAME
          MATCH     ZEROUR,OBAURNO
          IF        @EQUAL
            PACK      KEY8,OBAOUTNO
            CALL      RDPRAM1
            BRANCH    OVRCD,FDAT9999
          ELSE
            MOVE      OBAURNO,KEY8
            CALL      RDMAST1
            BRANCH    OVRCD,FDAT9999
          ENDIF
.
          MOVE      PGNAME,PATNAME
          STRIP     PATNAME
          ENDSET    PATNAME
          APPEND    SP1,PATNAME
          APPEND    PSNAME,PATNAME
          APPEND    SP30,PATNAME
          RESET     PATNAME
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,PRDOB
.
FDAT9999  RETURN
+
.**************************************************************************
.*  CLID0000  :  get clinic description                                   *
.*               Requires: DIM6 - Clinic code   Returns: OCADESC - desc   *
.**************************************************************************
.
CLID0000  IF        MOPTION=3
            PACK      KEY20,IDDD,OTOSCLID,OTOSDATE
          ELSE
            PACK      KEY20,IDDD,DIM6,CLINDATE
          ENDIF
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     IDDD,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     DIM6,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          IF        OVRCD = 1
            MOVE      "Unknown clinic",OCADESC
            PACK      OCADESC,OCADESC,SP30
          ELSE
            MOVE      OCADOCT,KEY6
            CALL      RDDOCT1
            IF        OVRCD=0
              MOVE      DTITL,PACTITLE
              MOVE      SP30,PACGNAME
              CMOVE     DGNAME,PACGNAME
              MOVE      DSNAME,PACSNAME
              MOVE      ONE,PACFRMT
              CALL      PACNAME
              MOVE      PACFNAME,OCADESC
            ENDIF
          ENDIF
.
CLID9999  RETURN
+
.**************************************************************************
.*  CLIT0000  :  get clinic type description                              *
.*               Requires: DIM6 - Clinic code   Returns: OCTDESC - desc   *
.**************************************************************************
CLIT0000  PACK      OCTDESC,SP70
.
          MATCH     SP70,DIM6
          IF        !@EQUAL
            PACK      KEY12,OTOSSITE,DIM6
            CALL      RDCTYA1
            IF        OVRCD=1
              PACK      OCTDESC,MISSING,DIM6
            ENDIF
          ENDIF
.
CLIT9999  RETURN
+
.**************************************************************************
.*  PTHD0000  :  Print Table Heading                                      *
.**************************************************************************
PTHD0000  MOVE      OTOSCLID,DIM6
          CALL      CLID0000                      * get clinic description
.
          MOVE      "Unk",DAYOWEEK
          MOVE      OCADESC,DESC28
.
          PACK      OBADATE,OBADATE,SP10
          UNPACK    OBADATE,CCENT,CYEAR,CMON,CDAY
.
          MATCH     SP10,OBADATE
          IF        !@EQUAL
            CALL      DATECONV
            LOAD      DAYOWEEK,WEEKDAY,MON,TUE,WED,THU,FRI,SAT,SUN
          ENDIF
.
          PRINT     *1,"Clinic Id    : ",OTOSCLID,SP2,DESC28:
                    SP2,"Day: ",DAYOWEEK,SP2,"Start Time: ",OBASTRT
          CALL      PACDATE
          PRINT     SP1       
.
          CALL      UND132
          PRINT     *1,"| Slot",*8,"| Slot",*16,"| Visit":
                    *24,"|",*56,"| U/R",*68,"| Date of":
                    *81,"| ",*89,"|",*97,"|";
.
          IF        MOPTION=3
             PRINT    *123,"| Ward";
          ENDIF
.
          PRINT     *132,"|"
.
          PRINT     *1,"| No.",*8,"| Time",*16,"| Type":
                    *24,"| Patient Name",*56,"| Number",*68,"| Birth":
                    *81,"| Trans",*89,"| Inter",*97,"| Comments";
.
          IF        MOPTION=3
             PRINT    *123,"| Bed";
          ENDIF
.
          PRINT     *132,"|"
.
          CALL      UND132
          ADD       "3",CLNO
.
PTHD9999  RETURN
+
.*********************************************************************
.         get the default clinic time for the clinic/date entered
.         if there is only one
.*********************************************************************
GDCT0000  MOVE      SP5,VTIME
          MOVE      ZERO,FORM2
          PACK      KEY28,IDDD,KYCLINS,CLINDATE,SP20
.
GDCT1000  CALL      RDSBOKA1
          CALL      RDKBOKA1
          BRANCH    OVRCD,GDCT9999          * end of file
.
          PACK      KEY20,OBASITE,OBACLIN,OBADATE
          MATCH     KEY20,KEY28
          GOTO      GDCT9999 IF NOT EQUAL   * different details
.
          MOVE      OBASTRT,VTIME
.
GDCT9999  RETURN
.
.***********************************************************************
.*                         KCTY0000                                    *
.*                Keyin a range of clinic types                        *
.***********************************************************************
KCTY0000  MOVE      TEN2,EVERT
          MOVE      "19",ECOL
          DISPLAY   *P59:EVERT,*EL
.
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
.
          CALL      KYOUTCTY
          IF        EXIT = 1
           MOVE       ZERO,EXIT
           DISPLAY    *PECOL:EVERT,*V2LON,SP7,"Start "
           MOVE       SP6,SCLINTYP
           GOTO       KCTY5000
          ENDIF
          IF        EXIT = 2
            MOVE      ONE,EXIT
            GOTO      KCTY9999
          ENDIF
          DISPLAY   *PECOL:EVERT,*V2LON,OCTCTYP,*HOFF,SP3,OCTDESC
          MOVE      OCTCTYP,SCLINTYP
.
KCTY5000  MOVE      TEN3,EVERT
          MOVE      "19",ECOL
          CALL      KYOUTCTY
          IF        EXIT = 1
           MOVE       ZERO,EXIT
           DISPLAY    *PECOL:EVERT,*V2LON,SP7,"Finish"
           MOVE       SP70,ECLINTYP
           GOTO       KCTY9000
          ENDIF
          IF        EXIT = 2
            MOVE      ONE,EXIT
            GOTO      KCTY9999
          ENDIF
          DISPLAY   *PECOL:EVERT,*V2LON,OCTCTYP,*HOFF,SP3,OCTDESC
          MOVE      OCTCTYP,ECLINTYP
.
KCTY9000  MATCH     SCLINTYP,ECLINTYP
          IF        @LESS
            DISPLAY    *P1:24,*B,*EL,"Invalid range. ";
            CALL       HITENTER
            GOTO       KCTY0000
          ENDIF
.
KCTY9999  RETURN
.
.**************************************************************************
.*  KCLR0000  :  Keyin a Clinic Id range                                  *
.**************************************************************************
KCLR0000  MOVE      ZERO,EXIT
          UNPACK    SP30,KYCLINS,CLINDATE,TIME
          MOVE      "19",ECOL
          MOVE      "8",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
.
          CALL      KYOUTCLI
          BRANCH    EXIT,KCLR7000,KCLR9000,KCLR7000      * nothing or EXITCHAR
.
          MOVE      OCACLIN,KYCLINS               * valid input
          MOVE      OCACLIN,PKYCLINS              * print variable
          DISPLAY   *P27:8,OCADESC
          MOVE      OCADESC,CLINDESS
          GOTO      KCLR8000
.
KCLR7000  MOVE      SP70,KYCLINS               * valid input
          MOVE      SP70,PKYCLINS              * print variable
          MOVE      "Start",OCADESC
          DISPLAY   *P27:8,OCADESC
          MOVE      OCADESC,CLINDESS
         
.
KCLR8000  UNPACK    SP30,KYCLINE,CLINDATE,TIME
          MOVE      "19",ECOL
          MOVE      "9",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
.
          CALL      KYOUTCLI
          BRANCH    EXIT,KCLR8500,KCLR9000,KCLR8500      * nothing or EXITCHAR
.
          MOVE      OCACLIN,KYCLINE               * valid input
          MOVE      OCACLIN,PKYCLINE              * print variable
          DISPLAY   *P27:9,OCADESC
          MOVE      OCADESC,CLINDESE
          GOTO      KCLR8600 
.
KCLR8500  MOVE      SP70,KYCLINE               * valid input
          MOVE      SP70,PKYCLINE              * print variable
          MOVE      "Finish",OCADESC
          DISPLAY   *P27:9,OCADESC
          MOVE      OCADESC,CLINDESE
.
KCLR8600  IF        @LESS
            DISPLAY   *P1:24,*EL,"Clinic Id Codes must be in Sequence. ";
            CALL      HITENTER
            GOTO      KCLR0000
          ENDIF
.
          MOVE      ZERO,EXIT                     * All ok
          GOTO      KCLR9999
.
KCLR9000  MOVE      ONE,EXIT                      * EXITCHAR
.
KCLR9999  RETURN
.
.*****************************************************************************
.*                              KDRN0000                                     *
.*                          Keyin the date range                             *
.*****************************************************************************
.
KDRN0000  MOVE      ZERO,EXIT
          UNPACK    TODAY,CCENT,CYEAR,CMON,CDAY
          MOVE      "19",CCOL
          MOVE      TEN,CVERT
          MOVE      ONE,CCANLDTE
          MOVE      ZERO,CHIGHLT
.
          DISPLAY   *P18:10,*EL,*P18:13,*EL
          CALL      KEYDATE
          BRANCH    CQUEST,KDRN0000
          BRANCH    OVRCD,KDRN9000                * anything input
.
          MATCH     SP2,CDAY
          IF        @EQUAL
            BEEP
            GOTO      KDRN0000
          ENDIF
.
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDATE
.
.          MATCH     STRTDATE,TODAY
.          IF        @LESS
.            DISPLAY   *P1:24,*EL,*B,"Date Cannot Be into the Future. ";
.            CALL      HITENTER
.            GOTO      KDRN0000
.          ENDIF
.
          PACK      SDOL,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.
KDRN5000  MOVE      TEN1,CVERT
          UNPACK    TODAY,CCENT,CYEAR,CMON,CDAY
          CALL      KEYDATE
          BRANCH    CQUEST,KDRN0000
          BRANCH    OVRCD,KDRN9000                * anything input
.
          MATCH     SP2,CDAY
          IF        @EQUAL
            BEEP
            GOTO      KDRN5000
          ENDIF
.
          PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDATE
.
.          MATCH     ENDDATE,TODAY
.          IF        @LESS
.            DISPLAY   *P1:24,*EL,*B,"Date Cannot Be into the Future. ";
.            CALL      HITENTER
.            GOTO      KDRN5000
.          ENDIF
.
          PACK      EDOL,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.
          MATCH     STRTDATE,ENDDATE
          IF        @LESS
            DISPLAY   *P1:24,*EL,"Dates Must be in Sequence. ";
            CALL      HITENTER
            GOTO      KDRN0000
          ENDIF
.
KDRN8000  MOVE        ZERO,EXIT
          GOTO        KDRN9999
.
KDRN9000  MOVE        ONE,EXIT
          GOTO        KDRN9999
.
KDRN9999  RETURN
.
.**********************************************************************
.*                      KEMP0000                                      *
.* Print empty slots                                                  *
.**********************************************************************
KEMP0000    MOVE       ZERO,PRINTEMP
.
            MOVE       "N",ANS
            KEYIN      *P1:14,*EL,"Print Empty Slots : ",*DV,*RV,ANS:
                       *P21:14,*V2LON,ANS;
            RESET      ANS
.
            CMATCH     "Y",ANS
            GOTO       KEMP9999 IF NOT EQUAL
.
            MOVE       ONE,PRINTEMP
.
KEMP9999    RETURN
+
.
          INC       STD001IO
          INC       OUTBOAIO/INC
          INC       OUTBB1IO/INC
          INC       OUTBOXIO/INC
          INC       OUTCLIIO/INC
          INC       OUTCTYIO/INC
          INC       OUTRSHIO/INC
          INC       OUTOSFIO/INC
          INC       OUTMA1IO/INC
          INC       VISMDTIO/INC
          INC       VISMTXIO/INC
          INC       PATCODIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATDO1IO/INC
          INC       PMSHCPIO/INC            * National HCP file
          INC       PATPR1IO/INC
          INC       PATHSPIO/INC
          INC       PATYERIO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
          INC       PATHSPKY
          INC       KYOUTCLI
          INC       CALCDAYS
          INC       CLOUTBB1
          INC       CLPATMIS
          INC       CLPMSPX2
          INC       OUTDSCLN
          INC       OUTDSMAS
          INC       OUTDSCTY
          INC       DATECONV
          INC       KYOUTCTY

.******************************************************************************
.* System   : Waiting Lists                                                   *
.* Program  : IBAWAT81                                                        *
.* Desc.    : MOVEMENT STATISTICS                                             *
.******************************************************************************
.* Date     : 12/11/1988                                                      *
.* Author   : N.S.                                                            *
.* Function : Print statistics report (movement statistics)                   *
.* Comments :                                                                 *
.* Mods.    :                                                                 *
. *****************************************************************************
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899                         *
.*           Recompiled as WATTR1FD changes                                   *
.******************************************************************************
.*        V5.07.02  26/10/1999 J Habasque                                     *
.*                  Recompiled for WATDSPRO                                   *
.*        V5.07.01  15/10/1999  Steve Armstrong                               *
.*                  Removed reference to CSTRTYR (not used)                   *
.******************************************************************************
.*            V5.01.01  19/06/89 M.Ohleiter                                   *
.*                      Changes for release 5.01                              *
.*                      09/07/89 J.Clark                                      *
.*                      Added Key14 variable required by WATST1FD.INC         *
.*                      10/07/89 K.Peak                                       *
.*                      Added 13x4 periods                                    *
.*            V5.01.02  23/02/90 E.Phan                                       *
.*                      Recompiled for WATPROFD.                              *
.*            V5.01.10  24/08/90 Justin Coates                                *
.*                      Added WTCNTPDS, standardized code, and, 3 search types*
.*                      28/08/90 Bill Dew                                     *
.*                      Fix WAT81 to used the new waiting list stats file,    *
.*                      update tempfile to suit, also redo the report format. *
.*                      See Programmers Notes.  Routines MOVS and MOVR and    *
.*                      any other routine connected with it are removed.  The *
.*                      stats update are done by WAT90 also kill any reference*
.*                      to tempfiles.  This is a real code hackers job.  New  *
.*                      print routine PMBS & service routines to support it.  *
.*            V5.01.11  01/10/90 Bill Dew                                     *
.*                      Fix grandtotal Overall mean                           *
.*            V5.01.12  15/04/91 i chung            SRF 108338                *
.*                      Changed "Close" calcualtion from (i) to (ii) -        *
.*                      (i) Close=Open+Unscheduled-Admitted-Discharges-Removed*
.*                      (ii) Close=Open+Unscheduled-Admitted-Removed          *
.*        V5.01.13  26/05/1994 Ian Rutt                                       *
.*                  Fixed Global Recompile Bugs                               * 
. *       V5.04.01  15/04/1997  howellsy   RHA                                *
. *                 Recompiled for WATDSPRO                                   *
.*        V5.04.02  26/06/1997  Howellsy         505                          *
.*                  Use PATDOCKY instead of PATDOCDS                          *
.*                                                                            *
.******************************************************************************
.    Programmer Notes
.    ----------------
.
.    Open  : No. of patients at start of period on Waiting List
.    Unsch : No. of patients added to Waiting List during the period
.    Sched : No. of patients booked during the period
.    Admit : No. of patients admitted from Waiting List during the period
.    Disch : No. of patients discharge during the period
.    Remove: No. of patients removed during the period
.    Close : No. of patients at remaining end of period 
.
.    Close = Open + Unscheduled - Admitted - Removed
.
.******************************************************************************
.
          INC       STD001FD
          INC       PATCOMM/INC                   
.
          INC       PATCODFD/INC       * codes file
          INC       PATCONFD/INC       * control file
          INC       PATDO1FD/INC       * doctors file
          INC       PATDRGFD/INC       * date range file
.
          INC       WATCONFD/INC       * WL control file
          INC       WATPROFD/INC       * procedure codes file
          INC       WATTR1FD/INC       * patient treatment file
          INC       WATWTAFD/INC       * Movement analysis file
+
. 
.         GLOBAL VARIABLES
.
ALLFLAG   FORM      1        * signals accept all codes
CODE      DIM       2         * for patdscod
CODEDOCT  DIM       6         * doctor code for search
CODEPROC  DIM       9         * procedure code for search
CODEUNIT  DIM       3         * unit/clinic code for search
COL       FORM      2         * watdspro
.
DESCOPT   DIM       27        * option chosen
DESCOPT1  DIM       27        * holds option one, defined in init0000
DESCDOCT  DIM       40        * doctor desc for search
DESCPROC  DIM       40        * procedure desc for search
DESCUNCL  DIM       20        * unit/clinic desc for search
.
DIM3      DIM       3         * patdsdoc
DIM30     DIM       30
DIM34     DIM       34        * shortened procedure description
DISPACT   FORM      1
DOCCODE   DIM       6
.
.
LASTPROC  DIM       9
LASTUNIT  DIM       3
LASTDOCT  DIM       6
.
PERDDATE  DIM       6         * period to use in obtaining data
QUPER     FORM      1         * ? performed flag
RECFLAG   FORM      1         * signal record found.
SAVECCYY  FORM      4         * save start century & year
SCRNFLAG  FORM      1
STCC      DIM       2         * starting date century
STMM      DIM       2         * starting date period
STYY      DIM       2         * starting date year
THISMON   FORM      2         * entered month as offset month from start of year
VERT      FORM      2         * watdspro
.
. ----- These are the number of days patients have been waiting
.
NUMBER    FORM      8        * number of treatment records 
STOTMEAN  FORM      8        * total of column "MEAN"
STOTMAX   FORM      4        * total of column "MAXIM"
STOTNUMB  FORM      8        * number of records for sub totals
GTOTMEAN  FORM      8        * total of column "MEAN"
GTOTMAX   FORM      4        * total of column "MAXIM"
GTOTNUMB  FORM      8        * number of records for grand totals
.
ADDED     FORM      8
ADMT      FORM      8
BOOK      FORM      8
CLOS      FORM      8
DISH      FORM      8
MAX       FORM      4
MEAN      FORM      8
OPEN      FORM      8
REMV      FORM      8
.
STOTADD   FORM      8         * total unscheduled for one proc.code
STOTBOOK  FORM      8         * total scheduled for one proc.code
STOTADMT  FORM      8         * total admitted for one proc.code
STOTDISH  FORM      8         * total discharged for one proc.code
STOTREMV  FORM      8         * total removed for one proc.code
STOTOPEN  FORM      8         * total of column "opening balance"
STOTCLOS  FORM      8         * total of column "closing balance"
.
GTOTADD   FORM      8         * total of column "unscheduled"
GTOTBOOK  FORM      8         * total of column "scheduled"
GTOTADMT  FORM      8         * total of column "admitted"
GTOTDISH  FORM      8         * total of column "discharged"
GTOTREMV  FORM      8         * total of column "removed"
GTOTOPEN  FORM      8         * total of column "opening balance"
GTOTCLOS  FORM      8         * total of column "closing balance"
.
. ------- program constants -------
.
ALL       INIT      "All"
CATWU     INIT      "WU"      * category WU - Unit/Clinic
DASH      INIT      " - "
DESCOPT2  INIT      "Doctor Code Sequence"
DESCOPT3  INIT      "Unit/Clinic Code Sequence"
.
PRGID     INIT      "IBAWAT81"
PRGNAM    INIT      "Movement Statistics"
VERSION   INIT      "V12.01.00"
+
.*********************************************************************
.*                  MAINLINE                                         *
.*        Controlling Logic / Mainline Code                          *
.*********************************************************************
ML0000
.MLQQ      SPLOPEN   "data"
          CALL      INIT0000                * open files     
.
. ---- Statistics report -----
.
ML1000    CALL      KOPT0000                * enter the option
          BRANCH    EXIT,ML9999             * exit chosen
.
ML2000    CALL      CODE0000                * enter the search code    
.
ML3000    CALL      KPER0000                * keyin from/to date
          BRANCH    EXIT,ML1000
.
          CALL      CONTQST                 * ok to continue ?
          BRANCH    CEXIT,ML4000,ML2000,ML1000
.                         Yes    No     Cancel
.
ML4000    CALL      CLRV0000           * clear vars.
          CALL      PMSR0000           * print Movement Stats Report
          GOTO      ML1000
ML9999
.MLQQQ     SPLCLOSE  "data"
          CHAIN     PGM
+
.***************************************************************************
.*                  INIT0000                                               *
.*        Initialisation , opening files                                   *
.***************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,THIRTY7;*185,WTCNTPDS
          READ      CONTROLF,FIFTY9;*27,CPERMTH
.
          DISPLAY   *P64:24,"watproaf";
          OPEN      WATPROA1,"watproaf"
          OPEN      WATPROA3,"watproaf"
.
          DISPLAY   *P64:24,"wattr1af";
          OPEN      WATTR1A1,"wattr1af"
.
          DISPLAY   *P64:24,"watwtaaf";
          OPEN      WATWTAA1,"watwtaaf"
          OPEN      WATWTAA2,"watwtaaf"
          OPEN      WATWTAA3,"watwtaaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
.
          DISPLAY   *P64:24,"patdrgaf";
          OPEN      PATDRGA3,"patdrgaf"
.
          MOVE      ONE,CNEWDS
.
. ------- set up the option description -------
.
          CLEAR     DESCOPT1
          APPEND    WTCNTPDS,DESCOPT1
          APPEND    " Code Sequence",DESCOPT1
          RESET     DESCOPT1
.
INIT9999  RETURN
+
.*********************************************************************
.*                  KOPT0000                    Called by : ML1000   *
.*        Input the search option                                    *
.*********************************************************************
KOPT0000  HLINE     *G37,2,50,80
          DISPLAY   *P1:3,*EF:
                    *V2LON,*P1:4,"0",*P1:5,"1",*P1:6,"2",*P1:7,"3",*HOFF:
                    *P3:4,"= Exit":
                    *P3:5,"= ",DESCOPT1:
                    *P3:6,"= ",DESCOPT2:
                    *P3:7,"= ",DESCOPT3:
                    *P1:9,"Select Option :";
.
KOPT1000  KEYIN     *P17:9,*EL,*DV,UNDLN1,*P17:9,*V2LON,MOPTION;
.
          COMPARE   ZERO,MOPTION
          GOTO      KOPT8000 IF EQUAL
.
          BRANCH    MOPTION,KOPT9000,KOPT9000,KOPT9000
          BEEP
          GOTO      KOPT1000
.
KOPT8000  MOVE      TRUE,EXIT
          GOTO      KOPT9999
.
KOPT9000  LOAD      DESCOPT,MOPTION,DESCOPT1,DESCOPT2,DESCOPT3
          DISPLAY   *P50:2,*V2LON,"- ",*+,DESCOPT,SP1;
          MOVE      FALSE,EXIT
          MOVE      MOPTION,OPTION
.
KOPT9999  RETURN
+
.*********************************************************************
.*                  CODE0000                    Called by : ML2000   *
.*        Keyin procedure code, unit code or doctor code             *
.*        Returns : CODEPROC      the procedure code entered, or,    *
.*                  CODEUNIT      the unit entered or,               *
.*                  CODEDOCT      the doctor code entered            *
.*********************************************************************
CODE0000  BRANCH    MOPTION,CODE1000,CODE2000,CODE3000
.
CODE1000  CALL      KPRO0000                * enter the procedure code
          GOTO      CODE9999
CODE2000
          CALL      KDOC0000                * enter the doctors code
          GOTO      CODE9999
CODE3000
          CALL      KUCC0000                * enter the unit/clinic code
          GOTO      CODE9999
.
CODE9999  RETURN
+
.*********************************************************************
.*                  KPRO0000                    Called by : CODE0000 *
.*        Keyin the Procedure Code                                   *
.*        Returns : CODEPROC      the procedure code                 *
.*                  DESCPROC      the procedure description          *
.    Modified 29/08/1990 Bill Dew
.    Get rid of PRUNDO2 and implement ALLFLAG
.*********************************************************************
KPRO0000  MOVE      ONE,QUPER               * ? not performed
          MOVE      TEN1,CVERT              * row for input
.
          DISPLAY   *P1:11,*EF,WTCNTPDS," Code   : "
.
KPRO1000  KEYIN     *P20:CVERT,*EL,*DV,UNDLN9,*P20:CVERT,*V2LON,CODEPROC:
                    *P20:CVERT,*DV,CODEPROC;
.
          ENDSET    CODEPROC
          APPEND    SP9,CODEPROC
          RESET     CODEPROC
.
          MATCH     SP9,CODEPROC            * was anything entered ?
          GOTO      KPRO4000 IF EQUAL       * no
.
          CMATCH    QUEST,CODEPROC          * was ? entered ?
          GOTO      KPRO2000 IF NOT EQUAL   * no
.
. ------ ? entered ------
.
          MOVE      ZERO,DISPACT            * dont display inactive codes
          CALL      WATDSPRO                * ? routine
          MOVE      ZERO,QUPER              * set ? as performed
          MOVE      "24",CVERT              * input on line 24
          PACK      CODEPROC,SP9            * clear the code
          DISPLAY   *P1:24,WTCNTPDS," Code   : ",*EL;
          GOTO      KPRO1000

. ------ validate what was entered ------
.
KPRO2000  PACK      KEY9,CODEPROC           * key = cat and value entered
          CALL      RDPROA1                 * read procedure file
          BRANCH    OVRCD,KPRO3000          * invalid procedure code
.
          COMPARE   ONE,WTWPACTV
          GOTO      KPRO2500 IF EQUAL
.
          PACK      DESCPROC,WPDESC         * store the desc
          MOVE      ZERO,ALLFLAG
          GOTO      KPRO5000
.
. ------ ERROR: invalid code entered ------
.
KPRO2500  DISPLAY   *P1:24,*B,"Inactive Code.  ",*EL;
          CALL      HITENTER
          GOTO      KPRO3200
.
KPRO3000  DISPLAY   *P1:24,*B,"Invalid ",WTCNTPDS," Code.  ",*EL;
          CALL      HITENTER
KPRO3200  PACK      CODEPROC,SP9            * clear the code
.
          BRANCH    QUPER,KPRO1000          * ? not entered
.
          DISPLAY   *P1:24,WTCNTPDS," Code   : ",*EL;
          GOTO      KPRO1000
.
. ------ only enter hit ------
.
KPRO4000  PACK      CODEPROC,SP6       * set up default value
          PACK      DESCPROC,SP20           * clear description
          MOVE      ONE,ALLFLAG
.
. ------ valid entry ------
.
KPRO5000  BRANCH    QUPER,KPRO8000          * ? not entered
.
          CALL      RDIS0000                * redisplay the screen
          BRANCH    ALLFLAG,KPRO0000        * re-enter when nothing entered
          DISPLAY   *P1:11,WTCNTPDS," Code   : ";
.
KPRO8000  MOVE      CODEPROC,KEY9
          LOAD      KEY9,ALLFLAG,ALL
          DISPLAY   *P20:11,*V2LON,KEY9,*P40:11,*HOFF,DESCPROC;
.
KPRO9999  RETURN
+
.*********************************************************************
.*                  KUCC0000                    Called by : UNCL0000 *
.*        Keyin the Unit/Clinic Code                                 *
.*        Returns : CODEUNIT      the unit/clinic code               *
.*                  DESCUNCL      the unit/clinic description        *
.    Modified 29/08/1990 Bill Dew
.    Get rid of PRUNDO2 and implement ALLFLAG
.*********************************************************************
KUCC0000  MOVE      ONE,QUPER               * ? not performed
          MOVE      TEN1,CVERT              * row for input
.
          DISPLAY   *P1:11,*EF,"Unit/Clinic Code : ";
.
KUCC1000  KEYIN     *P20:CVERT,*EL,*DV,UNDLN3,*P20:CVERT,*V2LON,CODEUNIT:
                    *P20:CVERT,*DV,CODEUNIT;
.
          ENDSET    CODEUNIT
          APPEND    SP3,CODEUNIT
          RESET     CODEUNIT
.
          MATCH     SP3,CODEUNIT            * was anything entered ?
          GOTO      KUCC4000 IF EQUAL       * no
.
          CMATCH    QUEST,CODEUNIT          * was ? entered ?
          GOTO      KUCC2000 IF NOT EQUAL   * no
.
. ------ ? entered ------
.
          PACK      CODE,CATWU              * set up category for codes file
          CALL      PATCODDS                * ? routine
          MOVE      ZERO,QUPER              * set ? as performed
          MOVE      "24",CVERT              * input on line 24
          PACK      CODEUNIT,SP3            * clear the code
          DISPLAY   *P1:24,"Unit/Clinic Code : ",*EL;
          GOTO      KUCC1000

. ------ validate what was entered ------
.
KUCC2000  PACK      KEY5,CATWU,CODEUNIT     * key = cat and value entered
          CALL      RDCODE1                 * read codes file
          BRANCH    OVRCD,KUCC3000          * invalid entry
.
          MOVE      TDESC,DESCUNCL          * set up description
          MOVE      ZERO,ALLFLAG
          GOTO      KUCC5000
.
. ------ ERROR: invalid code entered ------
.
KUCC3000  DISPLAY   *P1:24,*B,"Invalid Unit/Clinic Code.  ",*EL;
          CALL      HITENTER
          PACK      CODEUNIT,SP3            * clear the code
.
          BRANCH    QUPER,KUCC1000          * ? not entered
.
          DISPLAY   *P1:24,"Unit/Clinic Code : ",*EL;
          GOTO      KUCC1000
.
. ------ only enter hit ------
.
KUCC4000  PACK      CODEUNIT,SP3       * set up default value
          PACK      DESCUNCL,SP20      * clear description
          MOVE      ONE,ALLFLAG
.
. ------ valid entry ------
.
KUCC5000  BRANCH    QUPER,KUCC8000          * ? not entered
.
          CALL      RDIS0000                * redisplay the screen
          BRANCH    ALLFLAG,KUCC0000        * re-enter when nothing entered
          DISPLAY   *P1:11,"Unit/Clinic Code : ";
.
KUCC8000  MOVE      CODEUNIT,DIM3
          LOAD      DIM3,ALLFLAG,ALL
          DISPLAY   *P20:11,*V2LON,DIM3,*P40:11,*HOFF,DESCUNCL;
.
KUCC9999  RETURN
+
.*********************************************************************
.*                  KDOC0000                    Called by : CODE0000 *
.*        Keyin the Doctors Code                                     *
.*        Returns : CODEDOCT      the doctor code                    *
.*                  DESCDOCT      the doctor description             *
.    Modified 29/08/1990 Bill Dew
.    Get rid of PRUNDO2 and implement ALLFLAG
.*********************************************************************
KDOC0000  MOVE      TEN1,CVERT              * row for input
.
          DISPLAY   *P1:11,*EF,"Doctor Code      : ";
.
          MOVE      SP10,CODEDOCT
          MOVE      "20",ECOL
          MOVE      "11",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          CALL      PATDOCKY
          BRANCH    EXIT,KDOC4000,KDOC0000
.
          MOVE      DCODE,CODEDOCT
.
KDOC2000  PACK      KEY6,CODEDOCT           * key = cat and value entered
          CALL      FDOC0000                * format docs name
          PACK      DESCDOCT,PACFNAME       * store the name
          MOVE      ZERO,ALLFLAG
          GOTO      KDOC8000
.
. ------ only enter hit ------
.
KDOC4000  PACK      CODEDOCT,SP6       * set up default value
          PACK      DESCDOCT,SP20      * clear description
          MOVE      ONE,ALLFLAG
.
KDOC8000  MOVE      CODEDOCT,KEY6
          LOAD      KEY6,ALLFLAG,ALL
          DISPLAY   *P20:11,*V2LON,KEY6,*P40:11,*HOFF,DESCDOCT;
.
KDOC9999  RETURN
+
.*********************************************************************
.*                  KPER0000                    Called by : ML3000   *
.*        Keyin the period for the report                            *
.*********************************************************************
KPER0000  DISPLAY   *P1:13,*EF,"Period Date      : ";
.
          MOVE      FALSE,EXIT
.
          UNPACK    SP4,CYEAR,CMON
          MOVE      CCC,CCENT
          MOVE      TWENTY,CCOL
          MOVE      TEN3,CVERT
          MOVE      ZERO,CHIGHLT
.
          CALL      KEYPER
          BRANCH    OVRCD,KPER9900
.
          PACK      KEY6,ICENT,IYEAR,IMON
          REP       " 0",KEY6
          CALL      RDDRGA3
          BRANCH    OVRCD,KPER9000
.
          DISPLAY   *P40:13,*V2LON,DRGMDSC;
.
          PACK      PERDDATE,DRGYR,DRGNUM
          REP       " 0",PERDDATE
          MOVE      DRGYR,SAVECCYY       * save the year
          UNPACK    PERDDATE,STCC,STYY,STMM  * start year & period as other vars
.
          UNPACK    DRGFDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P40:14,CPCDATE," to ";
          UNPACK    DRGTDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   CPCDATE
          GOTO      KPER9999
.
KPER9000  DISPLAY   *P1:24,*B,"** Invalid ",CPERMTH,"/Year **    ",*EL;
          CALL      HITENTER
          GOTO      KPER0000
.
KPER9900  MOVE      ONE,EXIT
.
KPER9999  RETURN
+
.****************************************************************************
.                                 CLRV0000
.    CLRV0000  clear all stats vars
.    CLRV1111  clear subtotals and treatment totals
.    CLRV2222  clear treatment totals only
.****************************************************************************
CLRV0000  MOVE      ZERO,GTOTADD
          MOVE      ZERO,GTOTBOOK
          MOVE      ZERO,GTOTADMT
          MOVE      ZERO,GTOTDISH
          MOVE      ZERO,GTOTREMV
          MOVE      ZERO,GTOTOPEN
          MOVE      ZERO,GTOTCLOS
          MOVE      ZERO,GTOTMEAN
          MOVE      ZERO,GTOTMAX
          MOVE      ZERO,GTOTNUMB
CLRV1111
          MOVE      ZERO,STOTADD 
          MOVE      ZERO,STOTBOOK
          MOVE      ZERO,STOTADMT
          MOVE      ZERO,STOTDISH
          MOVE      ZERO,STOTREMV
          MOVE      ZERO,STOTOPEN
          MOVE      ZERO,STOTCLOS
          MOVE      ZERO,STOTMEAN
          MOVE      ZERO,STOTMAX
          MOVE      ZERO,STOTNUMB
CLRV3333
          MOVE      ZERO,NUMBER  
          MOVE      ZERO,MEAN
          MOVE      ZERO,MAX
          MOVE      ZERO,ADDED
          MOVE      ZERO,BOOK 
          MOVE      ZERO,ADMT
          MOVE      ZERO,DISH
          MOVE      ZERO,REMV
          MOVE      ZERO,OPEN
          MOVE      ZERO,CLOS
.
CLRV9999  RETURN
+
.******************************************************************************
.*                               FPTR0000                                     *
.*            Position the fptr depending on which search option              *
.******************************************************************************
FPTR0000  BRANCH    OPTION,FPTR1111,FPTR2222,FPTR3333
.
FPTR1111  PACK      KEY24,PERDDATE,CODEPROC,SP30
.
.FPTRQQ1   DISPLAY   *P1:3,"FPTR> key24[",KEY24,"]"
          CALL      RSWTWTA1           * treatment code order
          GOTO      FPTR9999
FPTR2222
          PACK      KEY24,PERDDATE,CODEDOCT,SP30
.FPTRQQ2   DISPLAY   *P1:3,"FPTR> key24[",KEY24,"]"
          CALL      RSWTWTA2           * doctor code order
          GOTO      FPTR9999
FPTR3333
          PACK      KEY24,PERDDATE,CODEUNIT,SP30
.FPTRQQ3   DISPLAY   *P1:3,"FPTR> key24[",KEY24,"]"
          CALL      RSWTWTA3           * Unit clinic code order
          GOTO      FPTR9999
.
FPTR9999  RETURN
+
.******************************************************************************
.*                                RREC0000                                    *
.******************************************************************************
RREC0000  BRANCH    OPTION,RREC1111,RREC2222,RREC3333
.
RREC1111  CALL      RKWTWTA1
          GOTO      RREC9999
RREC2222
          CALL      RKWTWTA2
          GOTO      RREC9999
RREC3333
          CALL      RKWTWTA3
          GOTO      RREC9999
.
RREC9999  RETURN
+
.*******************************************************************************
.                                 PMSR0000
.    Print Movement statistic report
.    Created 29/08/1990 Bill Dew
.    This routine and others that support it are bonded together very tightly
.    thus if anything is changed may alter its perfectness.  If chnages are
.    required I advise a rewrite.  I state it again "This is not my best work"
.    but it works.
.*******************************************************************************
PMSR0000  MOVE      ZERO,CPAGENO       * zero page counter
          MOVE      ZERO,RECFLAG       * signal no records found
          CLOCK     TIME,CTIMEIS
          MOVE      ONE,CNOUNDLN
          CALL      HEAD0000
          DISPLAY   *P1:24,*EL,*V2LON,"** ",*BLINKON,"Generating Report":
                         *HOFF,*V2LON," **";
.
          BRANCH    MOPTION,PMSR0100,PMSR0200,PMSR0300
.
PMSR0100  DISPLAY   *P43:24,WTCNTPDS,"  :";
          GOTO      PMSR1000
.
PMSR0200  DISPLAY   *P49:24,"Doctor  :";
          GOTO      PMSR1000
.
PMSR0300  DISPLAY   *P47:24,"Unit/Clinic  :";
.
PMSR1000  CALL      FPTR0000           * set file pointer in correct index
.
PMSR2222  CALL      RREC0000           * read movement analysis record
.
.PMSRQQ1   DISPLAY   *P1:20,"PMSR>",WTWTDATE,"|",WTWTDOCT,"|",WTWTUNIT,"|":
.                    WTWTPROC,"|",WTWTMEAN,"|",WTWTMAX,"| eof ",OVRCD:
.                    *N,WTWTOPEN,WTWTADD,WTWTBOOK,WTWTADMT,WTWTDSCH,WTWTCANL;
.PMSRQQK1  KEYIN     ANS
          BRANCH    OVRCD,PMSR8888     * print end of report 
.
          MATCH     PERDDATE,WTWTDATE  * test period dates are same
          GOTO      PMSR8888 IF NOT EQUAL
          MOVE      ONE,RECFLAG        * signal record found
.
          CALL      TALY0000           * tally subtotals 
          BRANCH    EXIT,PMSR5555,PMSR3333,PMSR8888
          GOTO      PMSR2222
PMSR3333
          CALL      PREC0000           * print a treatment total
          CALL      PSTT0000           * print subtotals
          CALL      CLRV1111           * clear vars
          CALL      CALC0000           * add the last read info
          GOTO      PMSR2222
PMSR5555
          CALL      PREC0000           * print a treatment total
          CALL      CLRV3333           * clear stats var
          CALL      CALC0000           * add the last read info
          GOTO      PMSR2222
PMSR8888
          BRANCH    RECFLAG,PMSR8899
          PRINT     *N,"No statistical data available for this period.":
                    *N,*N,"*** End of Report ***"
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** No Statistical Data for period specified **    ";
          CALL      HITENTER
          GOTO      PMSR9999
PMSR8899
          CALL      PGTT0000
          PRINT     *N,"NOTE:-  Close = Open + Unsheduled - Admitted - Removed":
                    *N,*N,*N,"*** End of Report ***"
          DISPLAY    *P1:24,*EL,*V2LON,*P24:24:
                     "** Report Generation Completed **",*W;
.
PMSR9999  RETURN
+
.******************************************************************************
.*                                PREC0000                                    *
.*                 Print a single treatment stats record                      *
.******************************************************************************
PREC0000  BRANCH    MOPTION,PREC0100,PREC0200,PREC0300
.
PREC0100  DISPLAY   *P57:24,*V2LON,WTWTPROC;
          GOTO       PREC1000
.
PREC0200  DISPLAY   *P60:24,*V2LON,WTWTDOCT;
          GOTO       PREC1000
.
PREC0300  DISPLAY   *P63:24,*V2LON,WTWTUNIT;
.
PREC1000  COMPARE   "56",CLNO
          GOTO      PREC3000 IF LESS
          BRANCH    SCRNFLAG,PREC2000
          CALL      DRAWLINE
.
PREC2000  CALL      HEAD0000
.
PREC3000  MOVE      "Unknown",WPDESC
          PACK      KEY9,LASTPROC
          CALL      RDPROA1            * read procedure code file for desc
          MOVE      WPDESC,DIM34
.
          DIV       NUMBER,MEAN
          MOVE      MEAN,FORM4
.
          MOVE      ADDED,CLOS         * calculate the closing numbers
          ADD       OPEN,CLOS
          SUB       ADMT,CLOS
          SUB       REMV,CLOS
.
          PRINT     "| ",LASTPROC,SP1,DIM34," |",OPEN," |",ADDED:
                    " |",BOOK," |",ADMT," |",DISH," |",REMV," |":
                    CLOS," | ",FORM4," | ",MAX," |"
          ADD       ONE,CLNO
.
.PRECQQ1   DISPLAY   *P1:22,*EF,"PREC>",LASTPROC,OPEN,ADDED:
.                    BOOK,ADMT,DISH,REMV,CLOS,FORM4,MAX;
.PRECQQK1  KEYIN     ANS
.
          MOVE      WTWTPROC,LASTPROC  * set the new procedure code
          MOVE      ZERO,SCRNFLAG      * signal last printed was not a line
.
PREC9999  RETURN
+
.******************************************************************************
.*                                 PGTT0000                                   *
.******************************************************************************
PGTT0000  CALL      PREC0000           * print last record
          BRANCH    OPTION,PGTT4444,PGTT2222,PGTT2222
.
PGTT2222  COMPARE   ZERO,ALLFLAG       * test if all codes
          GOTO      PGTT4444 IF EQUAL
          CALL      PSTT0000           * print final subtotals
PGTT4444
          DIV       GTOTNUMB,GTOTMEAN  * evaluate the mean
          MOVE      GTOTMEAN,FORM4
.
          MOVE      GTOTADD,GTOTCLOS   * calculate the closing numbers
          ADD       GTOTOPEN,GTOTCLOS
          SUB       GTOTADMT,GTOTCLOS
          SUB       GTOTREMV,GTOTCLOS
          CALL      DRAWLINE
.
          PRINT     "| Grand Total ",*48,"|",GTOTOPEN," |",GTOTADD," |":
                    GTOTBOOK," |",GTOTADMT," |",GTOTDISH," |",GTOTREMV," |":
                    GTOTCLOS," | ",FORM4," | ",GTOTMAX," |"
          CALL      DRAWLINE
.
PGTT9999  RETURN
+
.*******************************************************************************
.*                                PSTT0000                                     *
.*                            Print subtotals                                  *
.*******************************************************************************
PSTT0000  BRANCH    OPTION,PSTT9999,PSTT1111,PSTT2222
.
PSTT1111  PACK      KEY6,LASTDOCT,SP6
          CALL      FDOC0000
          MOVE      PACFNAME,DIM30
          MOVE      WTWTDOCT,LASTDOCT
          GOTO      PSTT3333
PSTT2222
          PACK      KEY5,ANSW,ANSU,LASTUNIT
          CALL      RDCODE1
          PACK      DIM30,TDESC,SP30
          MOVE      WTWTUNIT,LASTUNIT
PSTT3333
          DIV       STOTNUMB,STOTMEAN
          MOVE      STOTMEAN,FORM4
.
          MOVE      STOTADD,STOTCLOS   * calculate the closing numbers
          ADD       STOTOPEN,STOTCLOS
          SUB       STOTADMT,STOTCLOS
          SUB       STOTREMV,STOTCLOS
          CALL      DRAWLINE
.
          PRINT     "| Subtotal : ",DIM30,*48,"|",STOTOPEN," |",STOTADD," |":
                    STOTBOOK," |",STOTADMT," |",STOTDISH," |",STOTREMV," |":
                    STOTCLOS," | ",FORM4," | ",STOTMAX," |"
          CALL      DRAWLINE
          MOVE      ONE,SCRNFLAG
          ADD       ONE,CLNO
.
PSTT9999  RETURN
+
.*******************************************************************************
.                                 TALY0000
.    Tally the stats for treatment codes
.    EXIT=0 if treatment code is same as the last and stats calculated
.         1 if treatment code change signal print treatment stats
.         2 if unit or doctor code change and print treatment stats
.         3 if unit or doctor code change and not using allflag
.    Created 31/08/1990 Bill Dew : Initially I wanted to keep this routine
.    as simple as possible.  As the program grew so did its complexity, anyway
.    if you cant understand it then your not a good programmer afterall.
.*******************************************************************************
TALY0000  COMPARE   ZERO,GTOTNUMB      * test for very first record
          GOTO      TALY1000 IF NOT EQUAL
          MOVE      WTWTPROC,LASTPROC  * set the last procedure code
          MOVE      WTWTUNIT,LASTUNIT  * set the last unit code
          MOVE      WTWTDOCT,LASTDOCT  * set the last doctor code
TALY1000
.TALYQQ1   DISPLAY   *P1:3,WTWTPROC,LASTPROC,"|",WTWTUNIT,LASTUNIT,"|":
.                    WTWTDOCT,LASTDOCT,"|";
          BRANCH    OPTION,TALY1111,TALY3333,TALY2222
.
.------------------------------------------------------------------------------
TALY1111  BRANCH    ALLFLAG,TALY1144   * test if all codes flag is on
.
TALY1122  MATCH     WTWTPROC,CODEPROC  * test same treatment code
          GOTO      TALY9666 IF NOT EQUAL
          GOTO      TALY4444
.
TALY1144  MATCH     WTWTPROC,LASTPROC  * test change in procedure details
          GOTO      TALY9990 IF NOT EQUAL
          GOTO      TALY4444
.
.------------------------------------------------------------------------------
TALY2222  BRANCH    ALLFLAG,TALY2244   * test if all codes flag is on
          MATCH     WTWTUNIT,CODEUNIT  * test same unit code as keyin
          GOTO      TALY9666 IF NOT EQUAL
          GOTO      TALY1144
.
TALY2244  MATCH     WTWTUNIT,LASTUNIT  * test if new unit code
          GOTO      TALY9555 IF NOT EQUAL
          GOTO      TALY1144
.
.------------------------------------------------------------------------------
TALY3333  BRANCH    ALLFLAG,TALY3344   * test if all codes flag is on
          MATCH     WTWTDOCT,CODEDOCT  * test same doctor code
          GOTO      TALY9666 IF NOT EQUAL
          GOTO      TALY1144
.
TALY3344  MATCH     WTWTDOCT,LASTDOCT  * test doctor same as last
          GOTO      TALY9555 IF NOT EQUAL
          GOTO      TALY1144
.------------------------------------------------------------------------------
TALY4444  CALL      CALC0000           * add up stats
.
TALY9000  MOVE      ZERO,EXIT
          GOTO      TALY9999
.
TALY9555  MOVE      TWO,EXIT
          GOTO      TALY9999
.
TALY9666  MOVE      THREE,EXIT
          GOTO      TALY9999
.
TALY9990  MOVE      ONE,EXIT
.
TALY9999
.
.TALYQQ3   DISPLAY   *P75:3,EXIT;
.TALYQQK3  KEYIN     ANS
.
          RETURN
+
.******************************************************************************
.                                 CALC0000
.    Add up stats for treatment totals, subtotals and grandtotals
.******************************************************************************
CALC0000  ADD       ONE,NUMBER
          ADD       ONE,STOTNUMB
          ADD       ONE,GTOTNUMB
.
          ADD       WTWTMEAN,STOTMEAN
          ADD       WTWTMEAN,GTOTMEAN
          ADD       WTWTMEAN,MEAN
          CALL      MAXM0000           * calculate maximum value sub and grand
.
          ADD       WTWTOPEN,OPEN
          ADD       WTWTADD,ADDED
          ADD       WTWTBOOK,BOOK
          ADD       WTWTADMT,ADMT
          ADD       WTWTDSCH,DISH
          ADD       WTWTCANL,REMV
.
          ADD       WTWTOPEN,STOTOPEN  * Add up subtotals
          ADD       WTWTADD,STOTADD
          ADD       WTWTBOOK,STOTBOOK
          ADD       WTWTADMT,STOTADMT
          ADD       WTWTDSCH,STOTDISH
          ADD       WTWTCANL,STOTREMV
.
          ADD       WTWTOPEN,GTOTOPEN  * add up grand totals
          ADD       WTWTADD,GTOTADD
          ADD       WTWTBOOK,GTOTBOOK
          ADD       WTWTADMT,GTOTADMT
          ADD       WTWTDSCH,GTOTDISH
          ADD       WTWTCANL,GTOTREMV
CALC9999  RETURN
+
.******************************************************************************
.*                                  MAXM0000                                  *
.*                              Set maximum value                             *
.******************************************************************************
MAXM0000  COMPARE   MAX,WTWTMAX        * test if value > current max
          GOTO      MAXM9999 IF LESS
          MOVE      WTWTMAX,MAX        * set the new maximum
.
          COMPARE   STOTMAX,WTWTMAX    * test if value > current max
          GOTO      MAXM9999 IF LESS
          MOVE      WTWTMAX,STOTMAX    * set the new maximum
.
          COMPARE   GTOTMAX,WTWTMAX    * test if value > current max
          GOTO      MAXM9999 IF LESS
          MOVE      WTWTMAX,GTOTMAX    * set the new maximum
.
MAXM9999  RETURN
+
.****************************************************************************
.                                 HEAD0000
.****************************************************************************
HEAD0000  CALL      HEAD132
          PRINT     *39,"(",*+,DESCOPT,")",*N
.
HEAD0010  BRANCH    OPTION,HEAD1111,HEAD2222,HEAD3333
.
HEAD1111  BRANCH    ALLFLAG,HEAD1144
          PRINT     *40,WTCNTPDS," Code   : ",CODEPROC,SP4,DESCPROC
          GOTO      HEAD4444
.
HEAD1144  PRINT     *40,WTCNTPDS," Code   : All ",WTCNTPDS," codes"
          GOTO      HEAD4444
.
HEAD2222  BRANCH    ALLFLAG,HEAD2244
          PRINT     *40,"Doctor Code      : ",CODEDOCT,SP7,DESCDOCT
          GOTO      HEAD4444
.
HEAD2244  PRINT     *40,"Doctor Code      : All Doctor codes"
          GOTO      HEAD4444
.
HEAD3333  BRANCH    ALLFLAG,HEAD3344
          PRINT     *40,"Unit/Clinic Code : ",CODEUNIT,SP10,DESCUNCL
          GOTO      HEAD4444
.
HEAD3344  PRINT     *40,"Unit/Clinic Code : All Unit/Clinic codes"
.
HEAD4444  UNPACK    PERDDATE INTO CCENT,CYEAR,CMON
          PRINT     *40,"Report Date      : ",CMON,SLASH,CCENT,CYEAR;
          UNPACK    DRGFDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     SP6,CPCDATE," to ";
          UNPACK    DRGTDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     CPCDATE,*N
.
          CALL      DRAWLINE
          PRINT     "|",*3,WTCNTPDS,*48,"|   Open  | Unsched |":
                    "  Sched  |  Admit  |  Dischg | Removed |":
                    *111,"Close  | Mean |  Max |"
          CALL      DRAWLINE
          MOVE      TEN1,CLNO
          RETURN
+
.******************************************************************************
.*                                 DRAWLINE                                   *
.******************************************************************************
DRAWLINE  PRINT       *1,"*--------------------------------------------":
                         "--------------------------------------------":
                         "------------------------------------------*"
          ADD         ONE,CLNO
          RETURN
+
.*********************************************************************
.*                  FDOC0000                    Called by : lots     *
.*        Obtain the doctors name and format it                      *
.*        Paras   : KEY6          contains the doctors code          *
.*        Returns : PACFNAME      doctors name (65 chars)            *
.*********************************************************************
FDOC0000  PACK      PACFNAME,SP30,SP30,SP5  * clear the variable
          CALL      RDDOCT1                 * read doct file
          BRANCH    OVRCD,FDOC9999          * invalid code
          MOVE      DTITL,PACTITLE          * title
          MOVE      DGNAME,PACGNAME         * given name
          MOVE      DSNAME,PACSNAME         * surname
          MOVE      TWO,PACFRMT             * format as Surname, Title Given
          CALL      PACNAME                 * format the name
.
FDOC9999  RETURN
+
.*********************************************************************
.*                  RDIS0000                    Called by : CODE0000 *
.*        Redisplay the screen                                       *
.*********************************************************************
RDIS0000  DISPLAY   *V2LON,*P1:4,*EF,"0",*P1:5,"1",*P1:6,"2",*P1:7,"3",*HOFF:
                    *P3:4,"= Exit":
                    *P3:5,"= ",DESCOPT1:
                    *P3:6,"= ",DESCOPT2:
                    *P3:7,"= ",DESCOPT3:
                    *P1:9,"Select Option : ",*V2LON,MOPTION;
.
RDIS9999  RETURN
+
.==============================================================================
.
          INC       STD001IO    
          INC       PATCOMN3
.
          INC       PATCODKY                * ? for codes
          INC       PATDOCKY                * ? for doctors
          INC       WATDSPRO                * ? for procedure codes
.
          INC       PATDRGIO/INC
          INC       PATCODIO/INC            * Codes file
          INC       PATDO1IO/INC            * Doctors file
.
          INC       WATPROIO/INC
          INC       WATTR1IO/INC
          INC       WATWTAIO/INC            * Movement Analysis file

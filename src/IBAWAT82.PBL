.******************************************************************************
.* System   :  BILL                                                           *
.* Program  :  IBAWAT82                                                       *
.* Desc     :  W/L EDI DATA EXTRACTION                                        *
.******************************************************************************
.* Date     :  27/06/2000                                                     *
.* Author   :  Jill Habasque                                                  *
.* Mods.    :                                                                 *
.*                                                                            *
.******************************************************************************
.* V12.00.01 22/05/2025 Thanh T         TSK 0955096                           *
.*           Changed for alphanumeric visit number                            *
.******************************************************************************
.* V11.03.01 08/05/2023  Bella Turco      TSK 0931931                         *
.*           Added check of Cat dh UDF12 and Go live parameter for ICD Ed 12  *
.*           Added PTCNGDX2 to INIT0000                                       *
.* V11.02.01 10/02/2022  Thanh T          TSK 0889899                         *
.*           Recompiled as WATTR1FD changes                                   *
.******************************************************************************
.*        V10.15.03 14/11/2019  Jill Parkinson Task 0884753                   *
.*                  Corrected use of PTCDUD11                                 *
.*        V10.15.02 06/11/2019  Jill Parkinson Task 0883195                   *
.*                  Added read of ICD ED 11 parameter                         *
.*        V10.15.01 01/11/2019  Jill Parkinson Task 0883195                   *
.*                  Added CSYS0000 to get coding system from ICD dates        *
.*        V10.14.05 05/08/2019  Jill Parkinson Task 0863547                   *
.*                  Added extra change to use udf 4 from patcodes for booking *
.*                  facility                                                  *
.*        V10.14.04 30/07/2019  Jill Parkinson Task 0863547                   *
.*                  Mods to use udf 4 from patcodes for booking facility if   *
.*                  multi hospital                                            *
.*        V10.14.03 22/07/2019  Ken Bell       Task 0873318                   *
.*                  Save correct Header and Display counter. The last item    *
.*                  with last BEUNIQ are allocated in Procedire Code order    *
.*                  so cannot get value from last record by effective date    *
.*        V10.14.02 30/05/2019  Jill Parkinson Task 0875633                   *
.*                  Corrected compliance date to be 20190501                  *
.*        V10.14.01 12/03/2019  Ebon Clements  TSK 0851550                    *
.*                  Added ICD10 Edition 11 - BECLSYS = 15                     *
.*        V10.13.01 02/10/2018  Don Nguyen     TSK 0862698                    *
.*                  Added Non-NBRS Unit validation (VALUNT00) for 'Delete'    *
.*                  and 'Erase' records (HIS0000).                            *
.*        V10.12.02 23/05/2018  Don Nguyen     TSK 0857481                    *
.*                  Corrected WRIT0000 to proceed with writing an NBRS header *
.*                  record regardless if any records extracted.               *
.*                  Modified UPBTCH00 to reset batch no. to 1 if exceeded max.*
.*        V10.12.01 22/05/2018  Don Nguyen     TSK 0857481                    *
.*                  Added DHB validation to history file update (UPDT000).    *
.*                  Modified HIS0000 to only set temp file variables (such as *
.*                  BEURNO, BERTYPE, etc) before call to WRITT00.             *
.*                  Corrected WRIT0000 to utilise RPTRTMP1 for initial read.  *
.*                  Initialised BEUNIQ to 1 (HIS0000) and added RPTRTMP1.     *
.*        V10.10.02 28/06/2017  Nicole Torrisi TSK 0841043                    *
.*                  Changed HIS0000 and WATTR000 to use PTHSHDPC for booking  *
.*                  facility (BEBOOKF) when PTCNMHOS=1                        *
.*        V10.10.01 26/04/2017  Ebon Clements  TSK 0832315                    *
.*                  Set the BH records agency code with the district health   *
.*                  board associated number - WRIT0000                        *
.*        V10.09.03 16/02/2017  Jill Parkinson TSK 0833463                    *
.*                  Corrected open of oprdetaf                                *
.*        V10.09.02 30/01/2017  Jill Parkinson TSK 0321194                    *
.*                  Changed to use opdadate of first procedure as Date of Exit*
.*                  for discharged patients to stop issues with mult procs    *
.*        V10.09.01 05/01/2017  Don Nguyen     TSK 0814332                    *
.*                  Modified to use batch number (BATCHNO) from Associate     *
.*                  Field 2 of the DHB code (DHBCODE) entered (Cat dh).       *
.*        V10.08.01 23/03/2016  Don Nguyen     TSK 0303887                    *
.*                  Used new index for record write WRWTNBD1                  *
.*        V10.07.01 19/02/2016  Don Nguyen     TSK 0303887                    *
.*                  Fixed WRIT1100 to preserve any '0' in BELBEID (WTNDLBID)  *
.*        V10.06.03 26/08/2015  Peter Vela     CAR 320318                     *
.*                  Added Increment Batch Number switch                       *
.*        V10.06.02 11/06/2015  Don Nguyen       CAR 303887                   *
.*                  Modifications for CRISP (Sprint) - NBRS (WRWTNB)          *
.*        V10.06.01 23/03/2015 Jill Parkinson CAR 303881                      *
.*                  Mods to only use first 3 characters of Category dh HDP    *
.*        V10.05.04 01/08/2015  Ania P                      CAR 261630        *
.*                  Removed use of PORT for temp file naming                  *
.*        V10.05.03 07/10/2014  Don Nguyen    CAR 303881                      *
.*                  Modified DTFN0000 to call PATCODKY.                       *
.*                  Modified prompt for District Health Board in DTFN0000.    *
.*        V10.05.02 29/08/2014  Don Nguyen    CAR 303881                      *
.*                  Added CHKDHB00 to validate 'District Health Board'.       *
.*                  Modified extract to use the HDP Default of 'District      *
.*                  Health Board' (DHBPREFX) instead of site prefix           *
.*                  (3 characters) that was used previously.                  *
.*                  Added branch statement on EXIT after call to NATN0000.    *
.*        V10.05.01 19/08/2014  Don Nguyen    CAR 303882                      *
.*                  Modified DTFN0000 to use PTCNBRSB for next batch number.  *
.*                  Incremented BATCHNO upon successful extract run.          *
.*        V10.04.01 20/03/2014  Ebon Clements CAR 298038                      *
.*                  2014 changes - Clinical Coding System - BECLSYS           *
.*        V10.03.03 10/10/2013  Peter Vela    CAR 288085                      *
.*                  Added clear to temp file variables in INIT0000            *
.*        V10.03.02 13/03/2012  Mike Laming   CAR 233064                      *
.*                  Further tweek to WTWMACPR (add 6 "0"s to the front)       *
.*        V10.03.01 14/11/2011  Mike Laming   CAR 233064                      *
.*                  Added "Actual Procedure Event No." (WTWMACPR) at REXD1000 *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V10.00.03 18/10/2010  Jill Parkinson CAR 229928                     *
.*                  Added "Other Facility" functionality                      *
.*        V10.00.02 23/03/2010  Jill Parkinson CAR 225333                     *
.*                  Mods to move BKREADAT to booking status date              *
.*        V10.00.01 23/03/2010  Jill Parkinson CAR 216781                     *
.*                  July 2010 mods - added S for Staged/Planned and use bkreadat
.*        V9.11.01  15/04/2009  Ebon Clements CAR 185889                      *
.*                  Added error messages for blank assessor code & group      *
.*        V9.10.02  13/06/2008  Jill Habasque CAR 170722                      *
.*                  Fixed setting of comliance doyr2008 flag                  *
.*        V9.10.01  01/05/2008  Jill Habasque CAR 166097                      *
.*                  July 2008 changes - BEDOMCL, BEASSCD, BEASSGR             *
.*        V9.09.02  31/01/2008  Jill Habasque CAR 160169                      *
.*                  Mods to only use wtwmphsp if bephsvp is blank             *
.*        V9.09.01  29/01/2008  Mike Laming   CAR 153502                      *
.*                  Changes for Extract Record code (BEACODE) "D" & "E"       *
.*        V9.08.04  06/06/2007  Mike Laming   CAR 135646                      *
.*                  Change Date check to set "Compliance" to 01/06/2007 &     *
.*                  "Production" to 01/07/2007 (changes to INIT2000 & SCRN8000*
.*        V9.08.03  03/05/2007  Jill Habasque CAR 137391                      *
.*                  Commented out use of WTWMDRSA and WTMWDOSA                *
.*        V9.08.02  03/04/2007  Mike Laming   CAR 135646                      *
.*                  Fixed record length XXTRTMP1 (to 192)                     *
.*        V9.08.01  23/03/2007  Mike Laming   CAR 135646                      *
.*                  Change to use PMSHCPFD (remove PATDO1FD)                  *
.*                  Increase BECLRES to DIM 10, add 01/07/2007 logic for      *
.*                  BECLRES & BECLGRP, change version to V3.0                 *
.*        V9.07.02  27/07/2006  Jill Habasque  CAR 110312                     *
.*                  Added sending of date booking made for "06" records       *
.*        V9.07.01  19/04/2006  Mike Laming   CAR 95986                       *
.*                  Add PTCNHDPS for controlling BEBPROC for New Zealand      *
.*        V9.06.01  16/05/2006  Steve Armstrong  CAR 103391                   *
.*                  Mods to use DBEUNIQ (DIM) instead of BEUNIQ (FORM) to     *
.*                  WRITE to and READ from the temp file (as this field is    *
.*                  part of the index).                                       *
.*                  Also added a clear routine to clear any pre-existing      *
.*                  temp file records before each run (for Oracle).           *
.******************************************************************************
.*        V9.04.06  08/11/2005  Jill Habasque CAR 83249                       *
.*                  Mods to pack BEEXCAT with one before indicator 8 (instead *
.*                  of zero) for discharged patients                          *
.*        V9.04.05  27/09/2005  Ebon Clements CAR 75351                       *
.*                  Added WTCNPCOD - Report procedure code or procedure       *
.*                  code HCP is NBRS extract                                  *
.*        V9.04.04  24/08/2005  Jill Habasque CAR 51002                       *
.*                  Changed to use WTWMECNT instead of WTWMUNIQ               *
.*        V9.04.03  01/08/2005  Jill Habasque CAR 69743                       *
.*                  Fixed BEHSPEC if using rank                               *
.*        V9.04.02  01/08/2005  Jill Habasque CAR 69743                       *
.*                  Mods to use WMPCAT only if BEHSPEC is blank               *
.*        V9.04.01  04/07/2005  Jill Habasque CAR 60914                       *
.*                  Added use of WTHIBCAN if available                        *
.*        V9.03.05  22/06/2004  Mike Laming  CAR 50869                        *
.*                  Change BEBSCOD to blank for Delete records at HIS1000     *
.*        V9.03.04  28/04/2004  Jill Habasque SRF 49276                       *
.*                  Mods to right justify BELBEID delete and erase records    *
.*        V9.03.03  01/04/2004  Jill Habasque SRF 48708                       *
.*                  Mods to zero fill BELBEID for delete and erase records    *
.*        V9.03.02  31/03/2004  Jill Habasque SRF 48703                       *
.*                  Mods to only send records where wthirtyp<>spaces          *
.*        V9.03.01  22/07/2003  Jill Habasque SRF 34660                       *
.*                  Mods to allow erase records                               *
.*        V9.02.01  24/02/2003  Jill Habasque SRF 34660                       *
.*                  Removed references to watnbraf                            *
.*        V9.02.00  31/10/2002  Lina Vo        SRF 22687                      *
.*                  Ported to V9 and changed location of WTCNCSID in WATCONFD *
.*                  Added MVEXT000 (move extract file to web).                *
.******************************************************************************
.*        V5.10.02  30/09/2002  Steve Downing  SRF 21601                      *
.*                  Added update of WTWMRSFL (Sent flag in wattr1af)          *
.*        V5.10.01  25/09/2002  Steve Downing  SRF 19851                      *
.*                  Fixed unique identifier for E & D records to use WTWMUNIQ *
.******************************************************************************
.*        V5.09.03  17/05/2002  Tonii                                         *
.*                  Mods to check for deferred by code using CAT WR and       *
.*                  WMREMOVE if OVRCD is encountered when using CAT BR and    *
.*                  BKRECANC                                                  *
.*        V5.09.02  24/04/2002  Tonii NBRS Mods 2002                          *
.*                  - Addition of "Version Number" to Header record and prompt*
.*                  - Addition of "Clinical Responsibility" codes,2 new fields*
.*                    - 'Responsible Clinician Group'                         *
.*                    - 'Clinical Responsibility Code'                        *
.*                  - Addition of "Booking Entry Sequence"                    *
.*        V5.09.01  19/11/2001  Tonii NBRS Mods                               *
.*                  Fixed extract of CPAC Scoring Tool                        *
.*                  14/11/2001  Tonii NBRS Mods                               *
.*                  Mods to read parameter from WAT98 to determine where the  *
.*                  CPAC Scoring Id is stored                                 *
.*                  26/09/2001  Tonii SRF 13227                               *
.*                  Fixed packing of BELBEID, made sure WTNBBNUM is right     *
.*                  justified before packing                                  *
.******************************************************************************
.*        V5.08.25  15/11/2000  Jill Habasque                                 *
.*                  Fixed date booked for treatment                           *
.*        V5.08.24  08/11/2000  Jill Habasque                                 *
.*                  Fixed packing of BELBEID                                  *
.*        V5.08.23  05/10/2000  Jill Habasque                                 *
.*                  Added procedure to error message report                   *
.*        V5.08.22  15/09/2000  Jill Habasque                                 *
.*                  Removed CPAC date validation                              *
.*        V5.08.21  14/09/2000  Jill Habasque                                 *
.*                  Removed validation of CPAC date > date certainty given    *
.*        V5.08.20  07/09/2000  Jill Habasque                                 *
.*                  Changed Exit Category to be tcindc8 of Cat D instead of   *
.*                  the HDP as CCI56 uses this                                *
.*        V5.08.19  28/08/2000  Jill Habasque                                 *
.*                  Fixed Date Certainty Given to be extracted for all records*
.*        V5.08.18  23/08/2000  Jill Habasque                                 *
.*                  Changed to ask "update records as sent" if errors found   *
.*        V5.08.17  21/08/2000  Jill Habasque                                 *
.*                  Mods to not include records if TCINDC2 of WMUNIT = N      *
.*        V5.08.16  15/08/2000  Jill Habasque                                 *
.*                  Mods to update records only if no errors are found        *
.*        V5.08.15  10/08/2000  Jill Habasque                                 *
.*                  Added date validation routines                            *
.*        V5.08.14  08/08/2000  Jill Habasque                                 *
.*                  Added replace sp with zero for BELBEID                    *
.*        V5.08.13  02/08/2000  Jill Habasque                                 *
.*                  Remove ".-" from ICD codes                                *
.*        V5.08.12  31/07/2000  Jill Habasque                                 *
.*                  Added check of Cat BC for exit category                   *
.*        V5.08.11  28/07/2000  Jill Habasque                                 *
.*                  Changed to only fill required fields for delete records   *
.*        V5.08.10  27/07/2000  Jill Habasque                                 *
.*                  Changed to only fill required fields for delete records   *
.*        V5.08.09  25/07/2000  Jill Habasque                                 *
.*                  Changed to only fill in Exit cat & date if BS=20          *
.*        V5.08.08  21/07/2000  Jill Habasque                                 *
.*                  Changed number of records to not include BH record        *
.*        V5.08.07  20/07/2000  Jill Habasque                                 *
.*                  Fixed extraction of deferred by                           *
.*        V5.08.06  19/07/2000  Jill Habasque                                 *
.*                  Mods to extract treatment facility                        *
.*        V5.08.05  11/07/2000  Jill Habasque                                 *
.*                  Fixed erase records                                       *
.*        V5.08.04  06/07/2000  Jill Habasque                                 *
.*                  Mods for extracting exit category                         *
.*        V5.08.03  05/07/2000  Jill Habasque                                 *
.*                  Fixed update of wathisaf with record sent flag=Y          *
.*        V5.08.02  29/06/2000  Jill Habasque                                 *
.*                  Recompiled for WATHISFD                                   *
.*        V5.08.01  Jill Habasque                                             *
.*                  Mods to supress blank fields when writing to extract      *
. *****************************************************************************
.
          INC       STD001FD
          INC       IBACONFD/INC
.
          INC       BOKRC1FD/INC
          INC       OPRDEAFD/INC
          INC       PATCONFD/INC
          INC       PMSHCPFD/INC
          INC       PATDSCFD/INC
          INC       PATHSPFD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATNIDFD/INC
          INC       NHIMASFD/INC            * NHI master file
          INC       PATMA1FD/INC
          INC       PATCODFD/INC
          INC       PATTRNFD/INC
          INC       PATWR1FD/INC
          INC       WATCONFD/INC
          INC       WATTR1FD/INC
          INC       WATHISFD/INC
          INC       WATPROFD/INC
          INC       IBASEQFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
          INC       WATNBDFD/INC
          INC       WATNBHFD/INC
.
XXTRACT1  FILE      ASCII, FIXED=256        * extract file
.
.         
. LOCAL VARIABLES   
. ---------------
BATCHNO   DIM       5        * batch number for filename
BEDETTXT  DIM       200      * Extract record BE detail text
.
CAT       DIM       2
CMDLINE   DIM       80       * command line              
CODE      DIM       3
.
DHBPREFX  DIM       3        * HDP Default of 'District Health Board' (Cat dh)
DHBCODE   DIM       3        * District Health Board Code (Cat dh)
DIM4      DIM       4
DIM5      DIM       5
DIM6      DIM       6
DIM8      DIM       8
DIM8A     DIM       8
DIM13     DIM       13
DIM14     DIM       14
DOYR2008  FORM      1
.
ENDDATE   DIM       8        * to date keyed in (ccyymmdd)
ERRFLG    FORM      5
ERRMSG    DIM       40
.
FILENAME  DIM       12       * name of the file created
FORM5     FORM      5
FORM8     FORM      8
FORM8A    FORM      8
.
HPFLAG    DIM       1        * dont include if hpflag=A
.
INCRBATN  DIM       1
EXLINENO  FORM      5        * Extract line no.
.
PROCENV   DIM       4        * processing environment
.
SBMDT     DIM       8
SDBFT     DIM       8
SITEPREF  DIM       3        * site prefix for filename
.
TODAY     DIM       8        * Current date
TOTALREC  FORM      5
.
UNITFLAG  FORM      1
.
.
. PROGRAM CONSTANTS
. -----------------
BHVERSN   DIM       5                  * was 'INIT   "V02.0"'
BHVERS1   DIM       5
BHVERS2   DIM       5
.
CATR9     INIT      "R9"
CATW1     INIT      "W1"
CATW2     INIT      "W2"
CATW3     INIT      "W3"
CATW4     INIT      "W4"
CATX5     INIT      "X5"
CATX6     INIT      "X6"
CATX7     INIT      "X7"
CATX8     INIT      "X8"
CATdh     INIT      "dh"
.
ERASE     INIT      "iserase "
.
FILEEXTN  INIT      "nbr"             * hardcoded filename extension
FNAME     INIT      "wat82a"          * set up for temp file name
.
ISBUILD   INIT      "isbuild "
.
UKEY      INIT      " 192 U1-8,9-16,17-24,25-29"
UKEY08    INIT      " 210 U1-8,9-16,17-24,25-29"
.
Z30       INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
.
.>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
.      BH (Batch Header) Record Layout
.>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
.
BHRTYPE   DIM       2        * record type
BHHAGNCY  DIM       4        * health agency code
BHFILNAM  DIM       12       * extract filename
BHNORECS  DIM       5        * no. of records sent
BHSNDDTE  DIM       8        * date sent
BHFPDATE  DIM       8        * file preparation date
BHFPTIME  DIM       4        * file preparation time
BHPROENV  DIM       4        * NZHIS processing environment
.
.>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
.            * Extract record BE detail textBE (Assessment/Booking Entry Data) Record Layout
.>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
.
.         Temporary File Definition
.         -------------------------
.         
.         Filename : wat82axx.dat          (xx = port id)
.         
XXTRTMP1  IFILE SQL, FIXED=192, KEY="U1-8,9-16,17-24,25-29"
XXTRTMP2  IFILE SQL, FIXED=210, KEY="U1-8,9-16,17-24,25-29"
.
.NAME     TYPE    LENGTH  START    DESCRIPTION
.----     ----    ------  -------- -----------
BEURNO    DIM       8       1      * UR number
BEEFDAT   DIM       8       9      * Effective date
BEEFTIM   DIM       8      17      * Effective time
DBEUNIQ   DIM       5      25      * Unique count for tempfile
.
BERTYPE   DIM       2      30      * Record type
BEACODE   DIM       1      32      * Action A=add C=change D=delete E=erase
BEBOOKF   DIM       4      33      * Booking facility
BELBEID   DIM      14      37      * Local booking entry ID
BEBSCOD   DIM       2      51      * Booking status code
BEBSDAT   DIM       8      53      * Booking status date
BEHCUID   DIM       7      61      * HCU identifier
BEDRFSA   DIM       8      68      * Date of ref for FSA
BEBRSRC   DIM       1      76      * Booking referral source
BEDOFSA   DIM       8      77      * Date of FSA
BECADAT   DIM       8      85      * CPAC assessment date
BECSCOR   DIM       5      93      * CPAC score
BECSSID   DIM       4      98      * CPAC scoring system ID
BEDCGIV   DIM       8     102      * Date certainty given
BEDBMAD   DIM       8     110      * Date booking made
BEDBTRE   DIM       8     118      * Date booked for treatment
BEPHSVP   DIM       2     126      * Principal health service purchaser
BETRTAG   DIM       4     128      * Treatment agency
BETRFAC   DIM       4     132      * Treatment facility
BELHEID   DIM      14     136      * Local Health event identifier
BEHSPEC   DIM       3     150      * Health specialty
BEBPROC   DIM       2     153      * Booked procedure
BECLCOD   DIM       8     155      * Clinical code
BECLCTT   DIM       1     163      * Clinical code table type
BECLSYS   DIM       2     164      * Clinical code system
BEDEFBY   DIM       1     166      * Deferred by
BEDECAT   DIM       8     167      * Date of exit category
BEEXCAT   DIM       2     175      * Exit category
BESPPFL   DIM       1     177      * Staged/Planned procedure flag
BESEQNC   DIM       2     178      * Booking Entry Sequence
BECLGRP   DIM       2     180      * Responsible Clinician Group
BECLRES   DIM       10    182      * Clinical Responsibility Code
BEDOMCL   DIM       4     192      * Domicile code * after 01/07/2008
BEASSCD   DIM       10    196      * Assessor code * after 01/07/2008
BEASSGR   DIM       4     206      * Assessor Group code * after 01/07/2008
BELINEN   DIM       5     210      * Extract line no.
.
.End of Record            197
.End of Record            215      * after 01/07/2008
.
. Redefine FORM fields from key
. -----------------------------
BEUNIQ    FORM      5
+
.---------------------------------------------------------------------- 
OTHFACIL  INIT      "Other Facility"
ZEDS      INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
SP100     INIT      "                                                  ":
                    "                                                  "
.
PRGID     INIT      "IBAWAT82"
PRGNAM    INIT      "W/L EDI INTERFACE DATA EXTRACTION"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                           MAINLINE                                 *
.**********************************************************************
.
ML0000    CALL      INIT0000                  * initialization 
          CALL      CREA0000                  * create temp file
.
ML1000    CALL      SCRN0000                  * display screen
          BRANCH    EXIT,ML9000
.
ML2000    CALL      DTFN0000                  * keyin date/filename
          BRANCH    EXIT,ML1000,ML1000
.
          CALL      CONTQST                   * ok to continue
          BRANCH    CEXIT,ML2500,ML2000,ML1000
.
ML2500    CALL      MAKE0000                  * create the file
.
          CALL      HIS0000                   * get wathisaf records
.
          CALL      WRIT0000                  * write from tempfile to extract
.
          CALL      UPBTCH00                  * update and store next batch no.
.
          CALL      DTOT0000                  * display total records & error
          CALL      MVEXT000                  * move extract file to web     
.
          GOTO      ML1000
.
ML9000    CALL      KILL0000                  * erase temp file
.
ML9999    CHAIN     PGM
+
.**********************************************************************
.*                           INIT0000                                 *
.*                     Initialise variables                           *
.*                     Called : ML0000                                *
.**********************************************************************
.
INIT0000  CALL      DISPHEAD
          CALL      TFILENAM
.
          DISPLAY   *P57:24,"Opening controlf"
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P65:24,"bokrc1af"
          OPEN      BOKRC1A1,"bokrc1af"
          OPEN      BOKRC1A6,"bokrc1af"
.
          DISPLAY   *P65:24,"oprdetaf"
          OPEN      OPRDETA2,"oprdetaf"
.
          DISPLAY   *P65:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P65:24,"pmshcpaf"
          OPEN      PMSHCPA1,"pmshcpaf"
.
          DISPLAY   *P65:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P65:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P65:24,"patdschf"
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P65:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P65:24,"pattranf"
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P65:24,"patwr1af"
          OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY   *P65:24,"wattr1af"
          OPEN      WATTR1A1,"wattr1af"
          OPEN      WATTR1A8,"wattr1af"
.
          DISPLAY   *P65:24,"wathisaf"
          OPEN      WATHISA1,"wathisaf"
.
          DISPLAY   *P65:24,"wathisaf"
          OPEN      WATHISA2,"wathisaf"
.
          DISPLAY   *P65:24,"watproaf"
          OPEN      WATPROA1,"watproaf"
.
          DISPLAY   *P65:24,"pathspaf"
          OPEN      PATHSPA1,"pathspaf"
.
INIT2000  PACK      TODAY,CCC,CYY,CMM,CDD     * todays date
          REP       " 0",TODAY
.
.                                                                     *I-135646
          MOVE      "V03.0",BHVERS1
          MOVE      "V03.0",BHVERS2
          MOVE      ZERO,DOYR2008
          MATCH     "20080701",TODAY        * "Production" start date
          IF        !@LESS
            MOVE      "V04.0",BHVERS1
            MOVE      "V04.0",BHVERS2
            MOVE      ONE,DOYR2008
          ENDIF
          MATCH     "20080601",TODAY        * "Compliance" start date
          IF        !@LESS
            MOVE      "V04.0",BHVERS2
            MOVE      ONE,DOYR2008
          ENDIF
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TEN;*54,CFILENO,*244,CHOSPNUM
          READ      CONTROLF,TWENTY1;*138,PTCNNHII
          READ      CONTROLF,THIRTY7;*236,WTCNBRSR,*244,WTCNCSID
          READ      CONTROLF,EIGHTY;*250,PTCNHDPS                      *I-95986
          READ      CONTROLF,HUND11;*10,WTCNPCOD
          READ      CONTROLF,HUND20;*166,PTCNBRSB
.
          READ      CONTROLF,NINTY8;*149,PTCNGDV2
          READ      CONTROLF,HUND05;*104,PTCNDGET,*158,PTCNDGPV:
                                    *162,PTCNU41G,*163,PTCNU42G,*217,PTCNGDV4:
                                    *227,PTCNGDV5
          READ      CONTROLF,HUND09;*151,PTCNGDV3
          READ      CONTROLF,HUND12;*239,PTCNGDV6
          READ      CONTROLF,HUND10;*61,PTCNGDV7,*69,PTCNGDV8:
                                    *77,PTCNGDV9,*85,PTCNGDVX
          READ      CONTROLF,HUND17;*220,PTCNGDX1
          READ      CONTROLF,HUND28;*231,PTCNGDX2
.
          IF        PTCNNHII = 1
            DISPLAY   *P65:24,"nhimasaf"
            OPEN      NHIMASA1,"nhimasaf"
            OPEN      NHIMASA2,"nhimasaf"
          ELSE
            DISPLAY   *P65:24,"patnidaf"
            OPEN      PATNIDA1,"patnidaf"
          ENDIF
.
          DISPLAY   *P65:24,"watnbhaf"
          OPEN      WATNBHA1,"watnbhaf"
.
          DISPLAY   *P65:24,"watnbdaf"
          OPEN      WATNBDA1,"watnbdaf"
.
          MOVE      ZERO,BEUNIQ
          MOVE      ZERO,ERRFLG
.
          CALL      CLEA0000
.
INIT9999  RETURN
.
.************************************************************************
.*                             CLEA0000                                 *
.*                      Clear the tempfile variables                    *
.************************************************************************
CLEA0000  UNPACK    SP70,BEURNO,BEEFDAT,BEEFTIM
          UNPACK    SP70,BERTYPE,BEACODE,BEBOOKF,BELBEID 
          UNPACK    SP70,BEBSCOD,BEBSDAT,BEHCUID,BEDRFSA 
          UNPACK    SP70,BEBRSRC,BEDOFSA,BECADAT,BECSCOR 
          UNPACK    SP70,BECSSID,BEDCGIV,BEDBMAD,BEDBTRE 
          UNPACK    SP70,BEPHSVP,BETRTAG,BETRFAC,BELHEID
          UNPACK    SP70,BEHSPEC,BEBPROC,BECLCOD,BECLCTT
          UNPACK    SP70,BECLSYS,BEDEFBY,BEDECAT,BEEXCAT
          UNPACK    SP70,BESPPFL,BESEQNC,BECLGRP,BECLRES
          UNPACK    SP70,BEDOMCL,BEASSCD,BEASSGR,BELINEN
          UNPACK    SP70,DIM4,DIM2
          MOVE      ZERO,FORM8A
          MOVE      SP70,HPFLAG
          MOVE      ZERO,UNITFLAG
.
CLEA9999  RETURN
+
.************************************************************************
.*                             MAKE0000                                 *
.*                      Make the file for Extract                       *
.************************************************************************
MAKE0000  PACK      FILENAME,DHBPREFX,BATCHNO,SLASH,FILEEXTN   * from keyins
.         PACK      FILENAME,SITEPREF,BATCHNO,SLASH,FILEEXTN   * from keyins
          RESET     FILENAME
          PREP      XXTRACT1,FILENAME
          REP       "/.",FILENAME
.
          CALL      CLRT0000                     * clear tempfile records
.
MAKE9999  RETURN
+
.********************************************************************** 
.*                             SCRN0000                               *
.*                        Main Option Screen                          *
.*                        Called : ML0000                             *
.**********************************************************************
+
SCRN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF:
                       " = Run Production Data Extraction (",BHVERS1,")":
                    *P1:6,*V2LON,TWO,*HOFF:
                       " = Run Compliance Data Extraction (",BHVERS2,")"
.
SCRN1000  KEYIN     *P1:8,"Select Option : ",*EF,*DV,UNDLN1:
                    *P17:8,*V2LON,OPTION;
.
          BRANCH    OPTION,SCRN8000,SCRN8500
.
          COMPARE   ZERO,OPTION
          GOTO      SCRN9000 IF EQUAL
.
          BEEP
          GOTO      SCRN1000
.
SCRN8000  MOVE      FALSE,EXIT
          MOVE      BHVERS1,BHVERSN                                   *I-135646
          GOTO      SCRN9999
.
.                                                                     *I-135646
SCRN8500  MOVE      FALSE,EXIT              * "Production" extract
          MOVE      BHVERS2,BHVERSN
          MATCH     "20080601",TODAY        * "Compliance" start date
          IF        !@LESS
            MOVE      ONE,DOYR2008
          ENDIF
          GOTO      SCRN9999
.
SCRN9000  MOVE      TRUE,EXIT
.
SCRN9999  RETURN
+
.********************************************************************** 
.*                             DTFN0000                               *
.*              Keyin Date & Filename for Data Extraction             *
.*                        Called : ML0000                             *
.**********************************************************************
.
DTFN0000  MOVE      SP3,SITEPREF                 * site prefix for filename
.         KEYIN     *P1:14,*EF,"Enter three character site prefix : ":
.                   *V2LON,SITEPREF
.         TYPE      SITEPREF
.         GOTO      DTFN0000 IF EQUAL
.
.         RESET     SITEPREF
.         PACK      SITEPREF,SITEPREF,SP3
.         SCAN      SP1,SITEPREF                 * must be 3 chars long
.         IF        @EQUAL
.           DISPLAY   *P1:24,"Must be 3 characters long.     ";
.           CALL      HITENTER
.           GOTO      DTFN0000
.         ENDIF
.
.         MOVE      ZERO,FORM5
.         KEYIN     *P1:16,*EF,"Enter five digit batch number : ":
.                   *V2LON,FORM5
.         MOVE      FORM5,BATCHNO                * batch number for filename
.         REP       " 0",BATCHNO
.
          MOVE      SP4,DHBPREFX
          MOVE      ZERO,EXIT
          DISPLAY   *P1:14,*EF,"Enter District Health Board : ",SP30
          MOVE      "31",ECOL
          MOVE      "14",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP70,CKYIDEF3
          PACK      CODE,CATdh
          DISPLAY   *PECOL:EVERT,SP6
.
          CALL      PATCODKY
          BRANCH    EXIT,DTFN9100,DTFN9200
.
          PACK      DHBPREFX,THCSCOD,SP70
          DISPLAY   *PECOL:EVERT,*V2LON,DHBPREFX,*HOFF,*P36:EVERT,TDESC
.
.         KEYIN     *P1:14,*EF,"Enter District Health Board : ":
.                   *V2LON,DHBPREFX
.
.         RESET     DHBPREFX
.         PACK      DHBPREFX,DHBPREFX,SP4
.         SCAN      SP1,DHBPREFX                 * must be 4 chars long
.         IF        @EQUAL
.           DISPLAY   *P1:24,"Must be 4 characters long.     ";
.           CALL      HITENTER
.           GOTO      DTFN0000
.         ENDIF
.
.         MOVE      PTCNBRSB,BATCHNO             * batch number for filename
          MOVE      SP70,BATCHNO
          MOVE      ACODE,DHBCODE
          PACK      KEY5,CATdh,DHBCODE,SP70
          CALL      RDCODE1
          IF        OVRCD=0
            MATCH     SP70,PTCDASC2
            IF        !@EQUAL
              SQUEEZE   PTCDASC2
              MOVE      PTCDASC2,BATCHNO  * Batch number from Associate Field 2
              RJUSTIFY  BATCHNO
            ENDIF
          ENDIF
.  * overwrite ICD 10 Edition 11 if category dh user defined field 11 is set
          MATCH     SP70,PTCDUD11
          IF        !@EQUAL
            MOVE      PTCDUD11,PTCNGDX1
          ENDIF
.  * overwrite ICD 10 Edition 12 if category dh user defined field 12 is set
          MATCH     SP70,PTCDUD12
          IF        !@EQUAL
            MOVE      PTCDUD12,PTCNGDX2
          ENDIF
.
          REP       " 0",BATCHNO
          MATCH     "00000",BATCHNO
          IF        @EQUAL
            MOVE      ONE,BATCHNO      * Batch number not set; default to 1
            RJUSTIFY  BATCHNO
            REP       " 0",BATCHNO
          ENDIF
.
          DISPLAY   *P1:16,*EF,"Next five digit batch number : ":
                    *V2LON,BATCHNO,*HOFF
.
DTFN3500  MOVE      SP70,INCRBATN
          DISPLAY   *P1:18,*EF,"Increment Batch Number 1=YES : ";
          KEYIN     *V2LON,*RV,INCRBATN;
.
DTFN4000  IF        OPTION=1
            MOVE      "PROD",PROCENV             * proc. environment is PROD
          ELSE
            MOVE      "CMPL",PROCENV             * proc. environment is CMPL
          ENDIF
.
DTFN9000  MOVE      ZERO,EXIT
          GOTO      DTFN9999
.
DTFN9100  MOVE      ONE,EXIT
          GOTO      DTFN9999
.
DTFN9200  MOVE      TWO,EXIT
          GOTO      DTFN9999
.
DTFN9999  RETURN
+
.********************************************************************** 
.*                             HIS0000                                *
.*              Loop through wathisaf and write to tempfile           *
.*                        Called : ML0000                             *
.**********************************************************************
HIS0000   MOVE      ONE,BEUNIQ
          MOVE      ONE,TOTALREC           * Record Total
.
          PACK      KEY32,ANSN,SP70
          CALL      RSWTHIS2
HIS1000   CALL      RKWTHIS2
          BRANCH    OVRCD,HIS9999          * end of file?
.
          MATCH     ANSN,WTHIRECS          * record sent=N
          GOTO      HIS9999 IF NOT EQUAL
.
          MATCH     SP70,WTHIRTYP          * only include nbrs records
          GOTO      HIS1000 IF EQUAL
.
          MOVE      WTHIRTYP,BEACODE       * Action code
          MATCH     ANSE,BEACODE           * erase record?
          IF        @EQUAL
            MOVE      "99",BEBSCOD
            UNPACK    WTHIURNO,D1,BEHCUID
            MOVE      WTHIBSCD,BEBSCOD     * Booking status code
            MOVE      WTHIBOOK,DIM8
            CALL      RJUST000               * SRF 13227
            MOVE      DIM8,WTHIBOOK
            PACK      BELBEID,SP6,WTHIBOOK * Local booking ID
            PACK      KEY19,WTHIURNO,WTHIPROC,DWTHIPCN   * get treatment details
            CALL      RDTREA1
            IF        OVRCD<>1
              PACK      BELBEID,SP5,WTWMECNT * Local booking ID
            ENDIF
            REP       " 0",BELBEID
.
            IF        IBCNMHOS=1
              PACK      KEY3,WTWMIHSP,SP70
              CALL      RDPTHSP1
              IF        OVRCD<>1
                MOVE      PTHSHDPC,BEBOOKF
                MATCH     SP70,PTHSHDHB           * District Health Board setup?
                IF        !@EQUAL
                  PACK      KEY5,CATdh,PTHSHDHB,SP70
                  CALL      RDCODE1
                  IF        OVRCD<>1
                    MATCH     SP70,PTCDUDF4
                    IF        !@EQUAL
                      MOVE      PTCDUDF4,BEBOOKF
                    ENDIF
                  ENDIF
                ENDIF
              ENDIF
            ELSE
              MOVE      CFILENO,BEBOOKF     * Booking facility
            ENDIF
.
            CALL      CHKDHB00              * validate District Health Board
            BRANCH    EXIT,HIS1000
.
            CALL      VALUNT00              * validate Non-NBRS Unit?
.
            GOTO      HIS2000
          ENDIF
.
          MATCH     ANSD,BEACODE           * delete record?
          IF        @EQUAL
            UNPACK    WTHIURNO,D1,BEHCUID
.....       MOVE      WTHIBSCD,BEBSCOD     * Booking status code        D-50869
            MOVE      SP2,BEBSCOD          * Booking status code       *I-50869
            MOVE      WTHIBOOK,DIM8
            CALL      RJUST000               * SRF 13227
            MOVE      DIM8,WTHIBOOK
            PACK      BELBEID,SP6,WTHIBOOK * Local booking ID
            PACK      KEY19,WTHIURNO,WTHIPROC,DWTHIPCN   * get treatment details
            CALL      RDTREA1
            IF        OVRCD<>1
              PACK      BELBEID,SP5,WTWMECNT * Local booking ID
            ENDIF
            REP       " 0",BELBEID
.
            IF        IBCNMHOS=1
              PACK      KEY3,WTWMIHSP,SP70
              CALL      RDPTHSP1
              IF        OVRCD<>1
                MOVE      PTHSHDPC,BEBOOKF
                MATCH     SP70,PTHSHDHB           * District Health Board setup?
                IF        !@EQUAL
                  PACK      KEY5,CATdh,PTHSHDHB,SP70
                  CALL      RDCODE1
                  IF        OVRCD<>1
                    MATCH     SP70,PTCDUDF4
                    IF        !@EQUAL
                      MOVE      PTCDUDF4,BEBOOKF
                    ENDIF
                  ENDIF
                ENDIF
              ENDIF
            ELSE
              MOVE      CFILENO,BEBOOKF     * Booking facility
            ENDIF
.
            CALL      CHKDHB00              * validate District Health Board
            BRANCH    EXIT,HIS1000
.
            CALL      VALUNT00              * validate Non-NBRS Unit?
.
            GOTO      HIS2000
          ENDIF
.
          PACK      KEY19,WTHIURNO,WTHIPROC,DWTHIPCN    * get treatment details
          CALL      RDTREA1
          BRANCH    OVRCD,HIS1000
.
          CALL      CHKDHB00               * validate District Health Board
          BRANCH    EXIT,HIS1000
.
          MATCH     "05",WTHIBSCD          * get deferred by if booking code 05
          IF        @EQUAL
            CALL      DEBY0000
          ENDIF
.
          CALL      NATN0000               * get the national number 
          BRANCH    EXIT,HIS1000
.
          MOVE      NHMANMPI,BEHCUID
.
          CALL      HISF0000               * get values from wathisaf
.
          CALL      WATTR000               * Get wattr1af details
.
          MATCH     "01",BEBSCOD
          GOTO      HIS1500 IF EQUAL
.
          MATCH     "06",BEBSCOD
          GOTO      HIS2000 IF NOT EQUAL
.
HIS1500   MATCH     SP8,BEDBMAD
          IF        @EQUAL
            MOVE      SBMDT,BEDBMAD
          ENDIF
          MATCH     SP8,BEDBTRE
          IF        @EQUAL
            MOVE      SDBFT,BEDBTRE
          ENDIF
.
HIS2000   MOVE      WTHIURNO,BEURNO        * U/R number
          MOVE      WTHIEDAT,BEEFDAT       * effective date
          MOVE      WTHIETIM,BEEFTIM       * effective time
.
          MOVE      "BE",BERTYPE           * Record type
.
          CALL      WRITT00                * write to tempfile
.
          MOVE      WTHIBMDT,SBMDT
          MOVE      WTHIDBFT,SDBFT
          GOTO      HIS1000
.
HIS9999   RETURN
+
.*****************************************************************************
.                            VALUNT00
.                   Validate Non-NBRS Unit
.*****************************************************************************
VALUNT00  MOVE      ZERO,UNITFLAG
.
          MATCH     SP8,WMUNIT
          GOTO      VALUNT99 IF EQUAL
.
          PACK      KEY5,ANSW,ANSU,WMUNIT
          CALL      RDCODE1
          BRANCH    OVRCD,VALUNT99
.
          MATCH     ANSN,TCINDC2
          IF        @EQUAL
            MOVE      ONE,UNITFLAG     * Non-NBRS Unit
          ENDIF
.
VALUNT99  RETURN
+
.*****************************************************************************
.                            RJUST000
.                      Right Justify Fields - SRF 13227
.*****************************************************************************
RJUST000  RESET     DIM8
          STRIP     DIM8         
          MOVE      DIM8,DIM8A      * Save value
          MOVELPTR  DIM8,F2         * Determine the length 
          ASSIGN    8-F2,F2         * Determine how many spaces trailing
          CLEAR     DIM8
          MOVE      SP8,DIM8        * Clear DIM8 for new right justified value
          RESET     DIM8,F2
          APPEND    DIM8A,DIM8
          RESET     DIM8
RJUST999  RETURN
+
.*****************************************************************************
.                            HISF0000
.         Get values for fields from wathisaf        
.*****************************************************************************
.
HISF0000  MOVE      WTHIUCNT,DIM4
          UNPACK    DIM4,DIM2,BESEQNC  
          REP       " 0",BESEQNC
.
          MATCH     ANSA,BEACODE
          IF        @EQUAL
            MOVE      WTHIBSCD,BEBSCOD     * Booking status code
            MOVE      WTHIDCGV,BEDCGIV     * Date certainty given
            MOVE      WTHIBMDT,BEDBMAD     * Date booking made
            MOVE      WTHIDBFT,BEDBTRE     * Date booked for treatment
            MOVE      WTHIDRSA,BEDRFSA       * Date of ref for FSA
            MOVE      WTHIDOSA,BEDOFSA       * Date of FSA
            GOTO      HISF0500
          ENDIF
.
          MATCH     ANSC,BEACODE
          IF        @EQUAL
            MOVE      WTHIDRSA,BEDRFSA       * Date of ref for FSA
            MOVE      WTHIDOSA,BEDOFSA       * Date of FSA
          ENDIF
.
          MOVE     SP8,BEBSCOD             * booking status blank for C or D
.
HISF0500  MATCH    ANSA,BEACODE
          IF       @EQUAL
            MOVE      WTHIEDAT,BEBSDAT     * Booking status date
          ENDIF
.
          MOVE      WTHIRCDT,BECADAT       * date of CPAC assessment
.
          COMPARE   ZERO,WTHIRANK
          IF        !@EQUAL
            MOVE      WTHIRANK,DIM6
            SQUEEZE   DIM6
            IF        WTHIRANK<10
              PACK      BECSCOR,SP4,DIM6
            ELSE
              IF        WTHIRANK<100
                PACK      BECSCOR,SP3,DIM6
              ELSE
                IF        WTHIRANK<1000
                  PACK      BECSCOR,SP2,DIM6
                ELSE
                  IF        WTHIRANK<10000
                    PACK      BECSCOR,SP1,DIM6
                  ENDIF
                ENDIF
              ENDIF
            ENDIF
            REP       " 0",BECSCOR         * CPAC score
            GOTO      HISF1000
          ENDIF
.
          PACK      KEY5,ANST,ANSP,WTHIPRIO   * using priority, not rank
          CALL      RDCODE1
          IF        OVRCD<>1
            MOVE      TCFORM6,FORM5
            MOVE      FORM5,BECSCOR 
            REP       " 0",BECSCOR
          ENDIF
.
HISF1000  PACK      KEY5,ANSH,ANSP,WTHIPHSP * Principal health serv purchaser
          CALL      RDCODE1
          IF        OVRCD<>1
            MOVE      THCSCOD,BEPHSVP
            MOVE      TCINDC1,HPFLAG        * dont include if tcindc1=A
          ENDIF
.
          LOAD      CAT,WTCNBRSR,CATW1,CATW2,CATW3,CATW4,CATX5:
                                 CATX6,CATX7,CATX8
.
          PACK      CODE,WTHIBRSR
.
          PACK      KEY5,CAT,CODE
          CALL      RDCODE1
          IF        OVRCD<>1
            MOVE      THCSCOD,BEBRSRC      * Booking referral source
          ENDIF
.
....      IF        WMSTAT<4|WMSTAT>5
            PACK      KEY5,ANSA,SP1,WTHIPCHS,SP8
            CALL      RDCODE1
            IF        OVRCD<>1
              MOVE      THCSCOD,BEHSPEC    * Health speciality - not admitted
            ENDIF
....      ENDIF
.
.                                          * BEBPROC blank for NZ      *I-95986
          IF        PTCNHDPS <> 1
            MOVE      WTHIBPRO,BEBPROC     * Booked procedure
          ENDIF
.
          MATCH     SP70,WTHIASSR
          GOTO      HISF1500 IF EQUAL
.
          PACK      KEY10,WTHIASSR,SP10
          CALL      RDPMHCP1
          BRANCH    OVRCD,HISF1500
.
          MOVE      PMHCPRV5,BEASSCD      * Assessor Code
.
... Get HDP Equivalent from Codes File for Responsible Clinician Group
.
          PACK      KEY5,CATR9,PMHCUDF5
          CALL      RDCODE1
          IF        OVRCD = 0
            MOVE      THCSCOD,BEASSGR
          ENDIF
.
HISF1500
HISF9999  RETURN
+
.*****************************************************************************
.                            WATTR000
.         Get values for fields from wattr1af        
.*****************************************************************************
WATTR000  MOVE      "O",BECLCTT              * Clinical code table - hardcoded
          MOVE      ZERO,UNITFLAG
.
          CALL      CSYS0000                   * Get coding system
.
WATTR040  MATCH     SP8,WMUNIT
          GOTO      WATTR050 IF EQUAL
.
          PACK      KEY5,ANSW,ANSU,WMUNIT 
          CALL      RDCODE1
          BRANCH    OVRCD,WATTR050
          MATCH     ANSN,TCINDC2
          IF        @EQUAL
            MOVE      ONE,UNITFLAG
          ENDIF
.
WATTR050  MATCH     ANSA,BEACODE
          IF        @EQUAL
.....       MOVE      WTWMDRSA,BEDRFSA       * Date of ref for FSA
.....       MOVE      WTWMDOSA,BEDOFSA       * Date of FSA
          ENDIF
.
          MATCH     ANSC,BEACODE
          IF        @EQUAL
            MOVE      WTWMDTAD,BEDCGIV        * Date certainty given
          ENDIF
.
          MATCH     SP70,BEPHSVP
          GOTO      WATTR060 IF NOT EQUAL
.
          MATCH     SP8,WTWMPHSP
          IF        !@EQUAL
            PACK      KEY5,ANSH,ANSP,WTWMPHSP
            CALL      RDCODE1
            IF        OVRCD<>1
              MOVE      THCSCOD,BEPHSVP
              MOVE      TCINDC1,HPFLAG        * dont include if tcindc1=A
            ENDIF
          ENDIF
. 
.     Check CPAC Scoring ID destination 
.          1 = Waiting List Procedure Code
.          2 = Health Specialty Code (Cat. A)
.          3 = Waiting List Unit Code (Cat. WU)
.
WATTR060  IF        WTCNCSID = 1
            PACK      KEY9,WMCODE,SP8
            CALL      RDPROA1
            IF        OVRCD<>1
              MOVE      ZERO,FORM4
              MOVE      WPRHAT,FORM4
              MOVE      FORM4,BECSSID        * CPAC scoring ID
              REP       " 0",BECSSID
.                                          * BEBPROC blank for NZ      *I-95986
              IF        PTCNHDPS <> 1
                MOVE      WTWPHDPE,BEBPROC
              ENDIF
            ENDIF
          ELSE
            IF        WTCNCSID = 2
              PACK      KEY5,ANSA,SP1,WMPCAT,SP8
              CALL      RDCODE1
              IF        OVRCD<>1
                MOVE      TCFORM6,FORM4
                MOVE      FORM4,BECSSID
                REP       " 0",BECSSID
              ENDIF
            ELSE
              IF        WTCNCSID = 3
                PACK      KEY5,ANSW,ANSU,WMUNIT,SP8
                CALL      RDCODE1
                IF        OVRCD<>1
                  MOVE      TCFORM6,FORM4
                  MOVE      FORM4,BECSSID
                  REP       " 0",BECSSID
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
          MATCH     "1",WTCNPCOD            * Report procedure code HDP
          IF        @EQUAL
            PACK      KEY9,WMCODE,SP8
            CALL      RDPROA1
            IF        OVRCD=0
              MATCH     SP70,WTWPHDPE
              IF        !@EQUAL
                MOVE      WTWPHDPE,WMCODE
              ENDIF
            ENDIF
          ENDIF
.
          REP       ". ",WMCODE
          REP       "- ",WMCODE
          SQUEEZE   WMCODE
          PACK      BECLCOD,WMCODE         * Procedure code
.
          LOAD      CAT,WTCNBRSR,CATW1,CATW2,CATW3,CATW4,CATX5,CATX6,CATX7,CATX8
          LOAD      CODE,WTCNBRSR,WMUDEF1,WMUDEF2,WMUDEF3,WMUDEF4,WMUDEF5:
                                  WMUDEF6,WMUDEF7,WMUDEF8
          PACK      KEY5,CAT,CODE
          CALL      RDCODE1
          IF        OVRCD<>1
            MOVE      THCSCOD,BEBRSRC      * Booking referral source
          ENDIF
.
          IF        IBCNMHOS=1
            PACK      KEY3,WTWMIHSP,SP70
            CALL      RDPTHSP1
            IF        OVRCD<>1
              MOVE      PTHSHDPC,BEBOOKF
              MATCH     SP70,PTHSHDHB           * District Health Board setup?
              IF        !@EQUAL
                PACK      KEY5,CATdh,PTHSHDHB,SP70
                CALL      RDCODE1
                IF        OVRCD<>1
                  MATCH     SP70,PTCDUDF4
                  IF        !@EQUAL
                    MOVE      PTCDUDF4,BEBOOKF
                  ENDIF
                ENDIF
              ENDIF
            ENDIF
          ELSE
            MOVE      CFILENO,BEBOOKF      * Booking facility
          ENDIF
.
          IF        WMSTAT=5
            MATCH     "20",BEBSCOD
            IF        @EQUAL
              MOVE      CFILENO,BETRFAC    * Treating facility
            ENDIF
          ENDIF
          COMPARE   ONE,IBCNMHOS
          GOTO      WATTR100 IF NOT EQUAL  * multi hospital?
.
          IF        WMSTAT<>5
             GOTO     WATTR100
          ENDIF
.
          PACK      KEY8,WMBOOK,SP8
          CALL      RDMISS1
          BRANCH    OVRCD,WATTR100
.
          PACK      KEY30,AADMNO,ZEDS
          CALL      RDSTRAN2                * Position after last pat record
          CALL      RDPTRAN2                * read pat previous record
          BRANCH    OVRCD,WATTR100
.
          MATCH     AADMNO,TADMN
          GOTO      WATTR100 IF NOT EQUAL

          PACK      KEY6,TWARD,SP10         * Multiple hospitals.
          CALL      RDWARD1                 * Get hospital number from
          BRANCH    OVRCD,WATTR100
.                                             ward file.
          PACK      KEY3,PTWDHOSN
          CALL      RDPTHSP1                * Get HDP equiv. for hospital
          BRANCH    OVRCD,WATTR100
.
          MATCH     "20",BEBSCOD
          IF        @EQUAL
            MOVE      PTHSHDPC,BETRFAC     * Treating fac for multi hospital
          ENDIF
.
WATTR100  PACK      BELBEID,SP5,WTWMECNT   * Local booking ID
          REP       " 0",BELBEID
.
          IF        WMSTAT=4 | WMSTAT = 5
            CALL      TAHS0000         * get treatment agency & health specialty
          ELSE 
            MATCH     SP70,BEHSPEC
            IF        @EQUAL
              PACK      KEY5,ANSA,SP1,WMPCAT,SP8
              CALL      RDCODE1
              IF        OVRCD<>1
                MOVE      THCSCOD,BEHSPEC    * Health speciality - not admitted
              ENDIF
            ENDIF
          ENDIF
.
          IF        WMSTAT = 5
            CALL      GEXD0000            * get exit details
          ENDIF
.
          IF        WMSTAT = 6
            CALL      REXD0000            * exit details for removed patients
          ENDIF
.
WATTR300  PACK      KEY5,ANST,ANSP,WMPTY,SP8
          CALL      RDCODE1
          BRANCH    OVRCD,WATTR999
.
          MATCH     ANST,TCINDC4
          IF        @EQUAL
            MOVE      TWO,BESPPFL          * Staged/Planned procedure flag
            GOTO      WATTR400
          ENDIF
.
          MATCH     ANSS,TCINDC4
          IF        @EQUAL
            MOVE      FOUR,BESPPFL          * Staged/Planned procedure flag
            GOTO      WATTR400
          ENDIF
.
          MATCH     ANSP,TCINDC4
          IF        @EQUAL
            MOVE      THREE,BESPPFL
            GOTO      WATTR400
          ENDIF
.
          MOVE      ONE,BESPPFL
.
WATTR400  PACK      KEY10,WMDOCTOR,SP10
          CALL      RDPMHCP1
          BRANCH    OVRCD,WATTR999
.
          MOVE      PMHCPRV5,BECLRES      * Clinical Responsibility Code
.
... Get HDP Equivalent from Codes File for Responsible Clinician Group
.
          PACK      KEY5,CATR9,PMHCUDF5
          CALL      RDCODE1
          IF        OVRCD = 0
            MOVE      THCSCOD,BECLGRP
          ENDIF
.
WATTR999  RETURN
+
.*****************************************************************************
.                            CSYS0000
. Get the coding system based on the date on list
. Waiting List Referral Date falls into any date:
. - prior to ICD 10 Edition 8 Go Live Date, send Clinical Coding System 13
. - within ICD 10 Edition 8 Go Live Date and before ICD 10 Edition 11
.                Go Live Date, send Clinical Coding System 14
. - on or after ICD 10 Edition 11 Go Live Date,send Clinical Coding System 15
. - on or after ICD 10 Edition 12 Go Live Date,send Clinical Coding System 16
.*****************************************************************************
CSYS0000  PACK      BECLSYS,TEN3           * Clinical code system - 13
.
          MATCH     WMDATE1,PTCNGDV8       * added to WL after Ed. 8
          IF        (@EQUAL | @LESS)
            PACK      BECLSYS,TEN4
          ENDIF
          MATCH     WMDATE1,PTCNGDX1       * added to WL after Ed. 11
          IF        (@EQUAL | @LESS)
            PACK      BECLSYS,TEN5
          ENDIF
.
          MATCH     WMDATE1,PTCNGDX2       * added to WL after Ed. 12
          IF        (@EQUAL | @LESS)
            PACK      BECLSYS,TEN6
          ENDIF
.
CSYS9999  RETURN
+
.*****************************************************************************
.                            TAHS0000
. Get the treatment agency & health specialty for admitted/discharged patients
.*****************************************************************************
TAHS0000  PACK     KEY8,WMBOOK,SP8
          CALL     RDMISS1
          IF       OVRCD<>1
            PACK      KEY5,ANSH,ANSA,PTMIAGNC
            CALL      RDCODE1
            IF        OVRCD<>1
              MATCH     "20",BEBSCOD
              IF        @EQUAL
                MOVE      THCSCOD,BETRTAG       * Treatment agency
              ENDIF
            ENDIF
.
            PACK     KEY5,ANSA,SP1,ATYPE,SP8
            CALL     RDCODE1
            IF       OVRCD<>1
....          MOVE     THCSCOD,BEHSPEC  * Health speciality - admitted
            ENDIF
          ENDIF
TAHS9999  RETURN
+
.*****************************************************************************
.                            GEXD0000
. Get the exit details for discharged patients                                
.*****************************************************************************
GEXD0000  PACK      KEY8,WMBOOK
          CALL      RDDSCH1
          BRANCH    OVRCD,GEXD9000
.
          MATCH     "20",BEBSCOD
          GOTO      GEXD9000 IF NOT EQUAL
.
          PACK      KEY8,WMBOOK,SP70
          CALL      RDBKREC1
          IF        OVRCD<>1
            PACK      BEDECAT,BKREADAT,SP70
            MOVE      BKREADAT,BEBSDAT      * Booking status date
          ELSE
            MOVE      DDATE,BEDECAT      * Date of exit
            MOVE      DDATE,BEBSDAT      * Booking status date
          ENDIF
.
.  Task 0321194 - changed to use first theatre date as Date of exit
.  in case of multiple procedures
          PACK      KEY31,WMBOOK,FOUR,SP70
          CALL      RSOPDEA2
          CALL      RKOPDEA2
          BRANCH    OVRCD,GEXD1000
          MOVE      WMBOOK,KEY8
          MATCH     KEY8,OPDAADMN
          GOTO      GEXD1000 IF NOT EQUAL
.
          COMPARE   "4",OPDASTAT
          GOTO      GEXD1000 IF NOT EQUAL
.
          PACK      BEDECAT,OPDADATE,SP70 * Date of exit
          MOVE      OPDADATE,BEBSDAT      * Booking status date
.
GEXD1000  PACK      KEY5,ANSD,SP1,DSTAT
          CALL      RDCODE1
          IF        OVRCD=1
            PACK      BEEXCAT,ANSX,ANSX
            GOTO      GEXD9000
          ENDIF
.
          MATCH     "0",TCINDC8
          IF        @EQUAL
            PACK      BEEXCAT,ONE,TCINDC8
          ELSE
            MATCH     SP1,TCINDC8
            IF        !@EQUAL
              PACK      BEEXCAT,ONE,TCINDC8
            ELSE
              PACK      BEEXCAT,ANSX,ANSX
            ENDIF
          ENDIF
          MATCH     ANSO,TCINDC9
          IF        @EQUAL
            PACK      BELHEID,OTHFACIL,SP70
          ENDIF
.
GEXD9000  MATCH     SP70,BELHEID
          IF        @EQUAL | @EOS
            PACK      BELHEID,SP6,WMBOOK
            REP       " 0",BELHEID
          ENDIF
.
GEXD9999  RETURN
+
.*****************************************************************************
.                            REXD0000
. Get the exit details for patients removed from the waiting list 
.*****************************************************************************
REXD0000  MATCH     "20",BEBSCOD
          GOTO      REXD9999 IF NOT EQUAL
.
          MOVE      WMDATE4,BEDECAT      * Date of exit
          MOVE      WMDATE4,BEBSDAT      * Booking status date
          PACK      KEY5,ANSW,ANSR,WMREMOVE
          CALL      RDCODE1
          BRANCH    OVRCD,REXD2000
.
REXD1000  MOVE      TCFORM6,DIM6
          SQUEEZE   DIM6
          IF        TCFORM6<10
            PACK      BEEXCAT,ZERO,DIM6
          ELSE
            PACK      BEEXCAT,DIM6
          ENDIF
          MATCH     ANSW,TCINDC3
          IF        @EQUAL
            PACK      BELHEID,SP6,WTWMACPR,SP20                       *I-233064
            REP       " 0",BELHEID                                    *I-233064
          ELSE
            MATCH     ANSO,TCINDC9
            IF        @EQUAL
              PACK      BELHEID,OTHFACIL,SP70
            ENDIF
          ENDIF
          GOTO      REXD9999
.
REXD2000  PACK      KEY5,ANSB,ANSC,WMREMOVE
          CALL      RDCODE1
          IF        OVRCD=1
            PACK      BEEXCAT,ANSX,ANSX
          ELSE
            GOTO      REXD1000
          ENDIF
.
REXD9999  RETURN
+
.*****************************************************************************
.                            NATN0000
.                     Get the national number
.*****************************************************************************
NATN0000  MOVE      ZERO,EXIT
          MOVE      SP20,NHMANMPI
.
          IF        PTCNNHII = 1
            PACK      KEY8,WMURNO                * NHI is on so read NHIMAS
            CALL      RDNHMAS2
            BRANCH    OVRCD,NATN9000
            PACK      BEDOMCL,NHMADOMC,SP70
          ELSE
            PACK      KEY10,CHOSINDX,WMURNO      * NHI is off so use PATNID
            CALL      RDPTNID1
            BRANCH    OVRCD,NATN9000
            UNPACK    PTNINMPI,DIM13,NHMANMPI
            PACK      KEY8,WMURNO,SP70
            CALL      RDMAST1
            IF        OVRCD<>1
              PACK      BEDOMCL,PPOST,SP70
            ENDIF
          ENDIF
          GOTO      NATN9999
.
NATN9000  MOVE      ONE,EXIT
.
NATN9999  RETURN
+
.*****************************************************************************
.                            CHKDHB00
.         Validates that 'District Health Board' of the Waiting List Procedure
.         hospital matches the HDP Default entered (DHBPREFX).
.
.*****************************************************************************
CHKDHB00  MOVE      ZERO,EXIT
          MATCH     SP70,WTWMIHSP           * Intended Hospital filled in?
          GOTO      CHKDHB91 IF EQUAL       * no; invalid
.
          PACK      KEY3,WTWMIHSP,SP70
          CALL      RDPTHSP1
          BRANCH    OVRCD,CHKDHB91
.
          MATCH     SP70,PTHSHDHB           * District Health Board setup?
          GOTO      CHKDHB91 IF EQUAL       * no; invalid
.
          PACK      KEY5,CATdh,PTHSHDHB,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,CHKDHB91
.
          MATCH     DHBPREFX,THCSCOD        * matching HDP Default?
          GOTO      CHKDHB91 IF NOT EQUAL   * no; invalid
.
          GOTO      CHKDHB99
.
CHKDHB91  MOVE      ONE,EXIT                * doesn't match
CHKDHB99  RETURN
+
.********************************************************************** 
.*                             WRITT00                                *
.*              Write a record to the tempfile                        *
.*                        Called : ML0000                             *
.**********************************************************************
WRITT00   MATCH     ANSA,HPFLAG        * dont include in extract
          GOTO      WRITT99 IF EQUAL
.
          COMPARE   ONE,UNITFLAG
          GOTO      WRITT99 IF EQUAL
.
          MOVE      BEUNIQ,DBEUNIQ
          IF        DOYR2008=1
            WRITE     XXTRTMP2,KEY29;BEURNO,BEEFDAT,BEEFTIM,DBEUNIQ,BERTYPE:
                                BEACODE: 
                                BEBOOKF,BELBEID,BEBSCOD,BEBSDAT,BEHCUID:
                                BEDRFSA,BEBRSRC,BEDOFSA,BECADAT,BECSCOR:
                                BECSSID,BEDCGIV,BEDBMAD,BEDBTRE,BEPHSVP:
                                BETRTAG,BETRFAC,BELHEID,BEHSPEC,BEBPROC:
                                BECLCOD,BECLCTT,BECLSYS,BEDEFBY,BEDECAT:
                                BEEXCAT,BESPPFL,BESEQNC,BECLGRP,BECLRES:
                                BEDOMCL,BEASSCD,BEASSGR,BELINEN
          ELSE
            WRITE     XXTRTMP1,KEY29;BEURNO,BEEFDAT,BEEFTIM,DBEUNIQ,BERTYPE:
                                BEACODE:
                                BEBOOKF,BELBEID,BEBSCOD,BEBSDAT,BEHCUID:
                                BEDRFSA,BEBRSRC,BEDOFSA,BECADAT,BECSCOR:
                                BECSSID,BEDCGIV,BEDBMAD,BEDBTRE,BEPHSVP:
                                BETRTAG,BETRFAC,BELHEID,BEHSPEC,BEBPROC:
                                BECLCOD,BECLCTT,BECLSYS,BEDEFBY,BEDECAT:
                                BEEXCAT,BESPPFL,BESEQNC,BECLGRP,BECLRES:
                                BELINEN
         ENDIF
.
         MOVE       BEUNIQ,TOTALREC        * Save Record Total
         ADD        ONE,BEUNIQ
WRITT99  CALL       CLEA0000               * clear variables for next record
         RETURN
+
.**********************************************************************
.*                             WRIT0000                               *
.* Loop through the tempfile and write io the extract in UR order     *
.*                        Called : ML0000                             *
.**********************************************************************
WRIT0000  MOVE      "BH",BHRTYPE            * header details
.
          PACK      KEY5,CATdh,DHBCODE,SP70 * DHB of the extract run
          CALL      RDCODE1
          IF        OVRCD<>1
            MOVE      TCFORM6,FORM4
            MOVE      FORM4,BHHAGNCY        * agency code
          ENDIF
.
          PACK      BHFILNAM,FILENAME       * filename
.
.         MOVE      ZERO,BEUNIQ
.         PACK      KEY29,Z30
.         CALL      RSTRTMP1
.         CALL      RPTRTMP1
.
.         MOVE      BEUNIQ,TOTALREC
.         MOVE      BEUNIQ,BHNORECS         * the number of records in extract
          MOVE      TOTALREC,BHNORECS
          REP       " 0",BHNORECS
.
          CALL      IBACLOCK
          CLOCK     TIME,CTIMEIS
.
          PACK      BHSNDDTE,CCC,CYY,CMM,CDD  * date sent to NZHIS
          REP       " 0",BHSNDDTE
          PACK      BHFPDATE,CCC,CYY,CMM,CDD  * file preparation date
          REP       " 0",BHFPDATE
          REP       ": ",CTIMEIS
          SQUEEZE   CTIMEIS
          PACK      BHFPTIME,CTIMEIS        * file preparation time
.
          PACK      BHPROENV,PROCENV        * compliance or test
.
          CALL      WRBHREC                 * write header record
.
          MATCH     "PROD",PROCENV
          GOTO      WRIT0500 IF NOT EQUAL
.
          CALL      CLWATNBH
          MOVE      BHSNDDTE,WTNHXDAT       * Extract date
          MOVE      DHBPREFX,WTNHDHBC       * DHB code
          MOVE      BHHAGNCY,WTNHAGEN       * Health Agency code (Cat HA)
          MOVE      BATCHNO,WTNHBTCH        * Batch no.
          MOVE      PROCENV,WTNHPENV        * Processing Environment
          IF        DOYR2008=1
            MOVE      BHVERS1,WTNHFVER
          ELSE
            MOVE      BHVERS2,WTNHFVER
          ENDIF
          MOVE      ONE,WTNHSTAT            * Processing Status = 'Sent'
          MOVE      BHNORECS,WTNHRECS       * No. records sent
          MOVE      BHFPDATE,WTNHFDAT       * File Preparation Date
          MOVE      BHFPTIME,WTNHFTIM       * File Preparation Time
          MOVE      BHFILNAM,WTNHFNAM       * NBRS File Name (DHBNNNNN.nbr)
.
          PACK      KEY17,WTNHXDAT,WTNHDHBC,WTNHBTCH,WTNHSTAT,SP70
          CALL      RAWTNBH1
          IF        OVRCD=1
            MOVE      ZERO,OVRCD
            TRAP      OVERCOND IF IO
            CALL      WRWTNBH1
            TRAPCLR   IO
          ENDIF
.
WRIT0500  PACK      KEY29,SP70
          CALL      RSTRTMP1     * loop through the tempfile & write to extract
WRIT1000  CALL      RKTRTMP1
          BRANCH    OVRCD,WRIT9999
.
          CALL      VALD0000     * validate data
.
          CALL      STRP0000     * strip spaces before writing
.
.
          ADD       ONE,EXLINENO
          MOVE      EXLINENO,BELINEN
.
          IF        DOYR2008=1
            WRITE     XXTRACT1,SEQ;*+,BERTYPE,COMMA,BEACODE,COMMA:
                                  BEBOOKF,COMMA,BELBEID,COMMA,BEBSCOD:
                                  COMMA,BEBSDAT,COMMA,BEHCUID,COMMA:
                                  BEDRFSA,COMMA,BEBRSRC,COMMA,BEDOFSA:
                                  COMMA,BECADAT,COMMA,BECSCOR,COMMA:
                                  BECSSID,COMMA,BEDCGIV,COMMA,BEDBMAD:
                                  COMMA,BEDBTRE,COMMA,BEPHSVP,COMMA:
                                  BETRTAG,COMMA,BETRFAC,COMMA,BELHEID:
                                  COMMA,BEHSPEC,COMMA,BEBPROC,COMMA:
                                  BECLCOD,COMMA,BECLCTT,COMMA,BECLSYS:
                                  COMMA,BEDEFBY,COMMA,BEDECAT,COMMA:
                                  BEEXCAT,COMMA,BESPPFL,COMMA,BESEQNC:
                                  COMMA,BECLGRP,COMMA,BECLRES:
                                  COMMA,BEDOMCL,COMMA,BEASSCD:
                                  COMMA,BEASSGR
.
            PACK      BEDETTXT,BERTYPE,COMMA,BEACODE,COMMA:
                                  BEBOOKF,COMMA,BELBEID,COMMA,BEBSCOD:
                                  COMMA,BEBSDAT,COMMA,BEHCUID,COMMA:
                                  BEDRFSA,COMMA,BEBRSRC,COMMA,BEDOFSA:
                                  COMMA,BECADAT,COMMA,BECSCOR,COMMA:
                                  BECSSID,COMMA,BEDCGIV,COMMA,BEDBMAD:
                                  COMMA,BEDBTRE,COMMA,BEPHSVP,COMMA:
                                  BETRTAG,COMMA,BETRFAC,COMMA,BELHEID:
                                  COMMA,BEHSPEC,COMMA,BEBPROC,COMMA:
                                  BECLCOD,COMMA,BECLCTT,COMMA,BECLSYS:
                                  COMMA,BEDEFBY,COMMA,BEDECAT,COMMA:
                                  BEEXCAT,COMMA,BESPPFL,COMMA,BESEQNC:
                                  COMMA,BECLGRP,COMMA,BECLRES:
                                  COMMA,BEDOMCL,COMMA,BEASSCD:
                                  COMMA,BEASSGR,SP100
          ELSE
            WRITE     XXTRACT1,SEQ;*+,BERTYPE,COMMA,BEACODE,COMMA:
                                  BEBOOKF,COMMA,BELBEID,COMMA,BEBSCOD:
                                  COMMA,BEBSDAT,COMMA,BEHCUID,COMMA:
                                  BEDRFSA,COMMA,BEBRSRC,COMMA,BEDOFSA:
                                  COMMA,BECADAT,COMMA,BECSCOR,COMMA:
                                  BECSSID,COMMA,BEDCGIV,COMMA,BEDBMAD:
                                  COMMA,BEDBTRE,COMMA,BEPHSVP,COMMA:
                                  BETRTAG,COMMA,BETRFAC,COMMA,BELHEID:
                                  COMMA,BEHSPEC,COMMA,BEBPROC,COMMA:
                                  BECLCOD,COMMA,BECLCTT,COMMA,BECLSYS:
                                  COMMA,BEDEFBY,COMMA,BEDECAT,COMMA:
                                  BEEXCAT,COMMA,BESPPFL,COMMA,BESEQNC:
                                  COMMA,BECLGRP,COMMA,BECLRES
.
            PACK      BEDETTXT,BERTYPE,COMMA,BEACODE,COMMA:
                                  BEBOOKF,COMMA,BELBEID,COMMA,BEBSCOD:
                                  COMMA,BEBSDAT,COMMA,BEHCUID,COMMA:
                                  BEDRFSA,COMMA,BEBRSRC,COMMA,BEDOFSA:
                                  COMMA,BECADAT,COMMA,BECSCOR,COMMA:
                                  BECSSID,COMMA,BEDCGIV,COMMA,BEDBMAD:
                                  COMMA,BEDBTRE,COMMA,BEPHSVP,COMMA:
                                  BETRTAG,COMMA,BETRFAC,COMMA,BELHEID:
                                  COMMA,BEHSPEC,COMMA,BEBPROC,COMMA:
                                  BECLCOD,COMMA,BECLCTT,COMMA,BECLSYS:
                                  COMMA,BEDEFBY,COMMA,BEDECAT,COMMA:
                                  BEEXCAT,COMMA,BESPPFL,COMMA,BESEQNC:
                                  COMMA,BECLGRP,COMMA,BECLRES,SP100
          ENDIF
.
          MATCH     "PROD",PROCENV
          GOTO      WRIT2000 IF NOT EQUAL
.
          CALL      CLWATNBD
.
          MOVE      SP70,WTNDLBID
          MOVE      BELBEID,DIM14
.
WRIT1100  CMATCH    "0",DIM14
          GOTO      WRIT1200 IF NOT EQUAL
          BUMP      DIM14
          GOTO      WRIT1300 IF EOS
          GOTO      WRIT1100
.
WRIT1200  MOVE      DIM14,WTNDLBID
          RJUSTIFY  WTNDLBID                * WL Booking ID
.
WRIT1300  MOVE      BEHCUID,WTNDPNHI        * Patient NHI number
          RJUSTIFY  WTNDPNHI                * (since BEHCUID is only 7 char)
.
          MOVE      BATCHNO,WTNDBTCH        * Batch no.
          MOVE      ONE,WTNDSTAT            * Processing Status = 'Sent'
          MOVE      BEBSCOD,WTNDBSCD        * Booking Status Code
          MOVE      BEBSDAT,WTNDBSDT        * Booking Status Date
          MOVE      BEBOOKF,WTNDFACI        * Event Facility Code
          MOVE      BHSNDDTE,WTNDXDAT       * Extract Sent Date
          MOVE      BEDETTXT,WTNDRDET       * Extract record BE detail text
          MOVE      BELINEN,WTNDLINE
          MOVE      "00",WTNDERRC
.
          PACK      KEY31,WTNDLBID,WTNDBSCD,WTNDBSDT,WTNDLINE,WTNDBTCH:
                          WTNDERRC,SP70
          CALL      RAWTNBD1
          IF        OVRCD=1
            MOVE      ZERO,OVRCD
            TRAP      OVERCOND IF IO
            CALL      WRWTNBD1
            TRAPCLR   IO
          ENDIF
.
WRIT2000  GOTO      WRIT1000
.
WRIT9999  RETURN
+
.**********************************************************************
.*                             UPBTCH00                               *
.*         Update and store the next extract batch number             *
.*                        Called : ML0000                             *
.**********************************************************************
UPBTCH00  MATCH     "1",INCRBATN
          GOTO      UPBTCH99 IF NOT EQUAL
.
          MOVE      ZERO,FORM5
          MOVE      BATCHNO,FORM5
          ADD       ONE,FORM5
.
          IF        FORM5=0
            MOVE      ONE,FORM5
          ENDIF
.
.         MOVE      FORM5,PTCNBRSB
.         RJUSTIFY  PTCNBRSB
.         REP       " 0",PTCNBRSB
.         WRITAB    CONTROLF,HUND20;*166,PTCNBRSB
.
.         Save new batch number in Associate Field 2 for DHB Code (DHBCODE)
.
          PACK      KEY5,CATdh,DHBCODE,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,UPBTCH99
.
          MOVE      FORM5,DIM5
          REP       " 0",DIM5
          MOVE      DIM5,PTCDASC2
          RJUSTIFY  PTCDASC2
          CALL      UPCODE1
.
UPBTCH99  RETURN
+
.**********************************************************************
.*                             VALD0000                               *
.*         Validate data before writing to the extract file           *
.*                        Called : WRIT0000                           *
.**********************************************************************
VALD0000  MATCH     SP8,BEBOOKF
          IF        @EQUAL
            MOVE      "E17. Facility code invalid. ",ERRMSG
            CALL      PRTL0000
          ENDIF
.
          MATCH     SP8,BELBEID
          IF        @EQUAL
            MOVE      "Local booking entry ID is mandatory. ",ERRMSG
            CALL      PRTL0000
          ENDIF
.
          MOVE      ZERO,FORM2                       * was FORM1      *C-153502
          MOVE      BEBSCOD,FORM2
          IF        FORM2 = 1|FORM2 = 2|FORM2 = 3|FORM2 = 4|FORM2 = 5|FORM2 = 6
            MATCH     SP8,BEBSDAT
            IF        @EQUAL
              MATCH     "E",BEACODE                  * overkill ?     *I-153502
              IF        !@EQUAL
                MOVE      "E8. Booking status date is mandatory. ",ERRMSG
                CALL      PRTL0000
              ENDIF
            ENDIF
          ENDIF
.
          MATCH     SP8,BEHCUID
          IF        @EQUAL
            MOVE      "E4. Invalid HCU identifier. ",ERRMSG
            CALL      PRTL0000
          ENDIF
.
          MATCH     "05",BEBSCOD
          IF        @EQUAL
            MATCH     SP8,BEDEFBY
            IF        @EQUAL
              MOVE      "Deferred by is mandatory. ",ERRMSG
              CALL      PRTL0000
            ENDIF
          ENDIF
.
VALD6000  MATCH     "D",BEACODE                                       *C-153502
          GOTO      VALD8000 IF EQUAL
          MATCH     "E",BEACODE
          GOTO      VALD8000 IF EQUAL
.
          MATCH     SP10,BECLRES                 * mandatory from 01/07/2007
          IF        @EQUAL
            MOVE      "Clinical responsibility code not entered ",ERRMSG
            CALL      PRTL0000
          ENDIF
          MATCH     SP2,BECLGRP
          IF        @EQUAL
            MOVE      "Responsible Clinician Group not entered ",ERRMSG
            CALL      PRTL0000
          ENDIF
          MATCH     SP10,BEASSCD
          IF        @EQUAL
            MOVE      "Assessor Code not entered ",ERRMSG
            CALL      PRTL0000
          ENDIF
          MATCH     SP4,BEASSGR
          IF        @EQUAL
            MOVE      "Assessor Group Code not entered ",ERRMSG
            CALL      PRTL0000
          ENDIF
.
VALD8000  CALL      VDOFSA00         * check FSA Dates
.
          CALL      VDBMAD00         * check Date booking made dates
.
          CALL      VCGEX000         * check Certainty & exit dates
.
VALD9999  RETURN
+
.**********************************************************************
.*                             VDOFSA00                               *
.*        Validate Date of FSA & Date Referred to FSA                 *
.**********************************************************************
VDOFSA00  MATCH     SP8,BEDOFSA
          GOTO      VDOFSA99 IF EQUAL
.
          MATCH     SP8,BEDRFSA
          GOTO      VDOFSA10 IF EQUAL
.
          MATCH     BEDRFSA,BEDOFSA
          IF        @LESS
            MOVE      "Date of FSA must be >= Date Referred to FSA. ",ERRMSG
            CALL      PRTL0000
          ENDIF
.
VDOFSA10  MATCH     SP8,BECADAT
          GOTO      VDOFSA99 IF EQUAL
.
          MATCH     BEDOFSA,BECADAT
          IF        @LESS
            MOVE      "CPAC Assessment Date must be >= FSA Date. ",ERRMSG
            CALL      PRTL0000
          ENDIF
.
VDOFSA99  RETURN
+
.**********************************************************************
.*                             VDBMAD00                               *
.*        Validate Date Booking Made & Date booked for treatment      *
.**********************************************************************
VDBMAD00  MATCH     SP8,BEDBMAD
          GOTO      VDBMAD99 IF EQUAL
.
          MATCH     SP8,BEDBTRE
          GOTO      VDBMAD99 IF EQUAL
.
          MATCH     BEDBMAD,BEDBTRE
          IF        @LESS
            MOVE  "Date Booked for Treat. must be >= Date booking made. ",ERRMSG
            CALL   PRTL0000
          ENDIF
.
VDBMAD99  RETURN
+
.**********************************************************************
.*                             VCGEX000                               *
.*        Validate Date Certainty Given & Exit Category Date          *
.**********************************************************************
.
VCGEX000  MATCH     SP8,BEBSDAT
          GOTO      VCGEX999 IF EQUAL
.
          MATCH     SP8,BEDECAT
          GOTO      VCGEX999 IF EQUAL
.
          MATCH     BEBSDAT,BEDECAT
          IF        @LESS
            MOVE      "Date of Exit must be >= Booking Status Date. ",ERRMSG
            CALL      PRTL0000
          ENDIF
.
VCGEX999  RETURN
+
.**********************************************************************
.*                             STRP0000                               *
.*         Strip variables before writing the the extract file        *
.*                        Called : WRIT0000                           *
.**********************************************************************
STRP0000  STRIP     BERTYPE
          STRIP     BEACODE
          STRIP     BEBOOKF
          STRIP     BELBEID
          STRIP     BEBSCOD
          STRIP     BEBSDAT
          STRIP     BEHCUID
          STRIP     BEDRFSA
          STRIP     BEBRSRC
          STRIP     BEDOFSA
          STRIP     BECADAT
          STRIP     BECSCOR
          STRIP     BECSSID
          STRIP     BEDCGIV
          STRIP     BEDBMAD
          STRIP     BEDBTRE
          STRIP     BEPHSVP
          STRIP     BETRTAG
          STRIP     BETRFAC
          STRIP     BELHEID
          STRIP     BEHSPEC
          STRIP     BEBPROC
          STRIP     BECLCOD
          STRIP     BECLCTT
          STRIP     BECLSYS
          STRIP     BEDEFBY
          STRIP     BEDECAT
          STRIP     BEEXCAT
          STRIP     BESPPFL
          STRIP     BESEQNC
          STRIP     BECLGRP
          STRIP     BECLRES
          STRIP     BEDOMCL
          STRIP     BEASSCD
          STRIP     BEASSGR
.
STRP9999  RETURN
+
.******************************************************************************
.*                                  PRTL0000              Called by: VALD0000 *
.*                         Print error message                                *
.******************************************************************************
PRTL0000  IF        ERRFLG=0
            CALL      IBACLOCK
            CLOCK     TIME,CTIMEIS
            CALL      HEAD80
            PRINT     *N,"------------------------------------------":
                         "--------------------------------------":
                      *N,"|U/R Number|Local Booking ID|Procedure| Description":
                      *80,"|":
                      *N,"------------------------------------------":
                         "--------------------------------------"
          ENDIF
.
          PRINT     *1,"| ",BEURNO,*12,"|",BELBEID,*29,"|",BECLCOD,*39:
                    "|",ERRMSG,*80,"|"
          ADD       ONE,ERRFLG
.
PRTL9999  RETURN
+
.******************************************************************************
.*                                  DEBY0000                                  *
.*                   Get Deferred by if booking status is 05                  *
.******************************************************************************
DEBY0000  MATCH     SP70,WTHIBCAN
          GOTO      DEBY0500 IF EQUAL
.
          PACK      KEY5,ANSB,ANSC,WTHIBCAN,SP70
          CALL      RDCODE1
          IF        OVRCD<>1
            MOVE      THCSCOD,BEDEFBY      * Deferred by
          ELSE
            PACK      KEY5,ANSW,ANSR,WMREMOVE
            CALL      RDCODE1
            IF        OVRCD <> 1
              MOVE      THCSCOD,BEDEFBY
            ENDIF
          ENDIF
          GOTO      DEBY9999
.
DEBY0500  PACK      KEY16,WMURNO,SP20
          CALL      RSBKREC6
DEBY1000  CALL      RKBKREC6
          BRANCH    OVRCD,DEBY9999
.
          MATCH     WMURNO,BKREURNO
          GOTO      DEBY9999 IF NOT EQUAL
.
          MATCH     WMCODE,BKREPROC
          GOTO      DEBY1000 IF NOT EQUAL
.
          MATCH     DWMCNT,DBKRECNT
          GOTO      DEBY1000 IF NOT EQUAL
.
          MATCH     SP6,BKRECANC
          GOTO      DEBY1000 IF EQUAL
.
          PACK      KEY5,ANSB,ANSC,BKRECANC
          CALL      RDCODE1
          IF        OVRCD<>1
            MOVE      THCSCOD,BEDEFBY      * Deferred by
          ELSE
            PACK      KEY5,ANSW,ANSR,WMREMOVE
            CALL      RDCODE1
            IF        OVRCD <> 1
              MOVE      THCSCOD,BEDEFBY
            ENDIF
          ENDIF
.
DEBY9999  RETURN
+
.******************************************************************************
.*                                  RJUS0000                                  *
.*                   Move field to be right justified                         *
.******************************************************************************
RJUS0000  MOVE      ZERO,FORM4
          MOVE      SP10,DIM4
          RESET     THCSCOD,4
          GOTO      RJUS2500
.
RJUS2000  BUMP      THCSCOD,-1
          GOTO      RJUS9999 IF EOS
.
RJUS2500  CMATCH    SP1,THCSCOD
          GOTO      RJUS2000 IF EQUAL
.
          MOVEFPTR  THCSCOD,FORM2
.
          SETLPTR   THCSCOD,FORM2
.
RJUS3000  RESET     THCSCOD
          MOVE      THCSCOD,FORM4
          MOVE      FORM4,DIM4
          RESET     DIM4
.
RJUS9999  RETURN
+
.******************************************************************************
.*                                  DTOT0000                                  *
.*        Display total records extracted and error message if applicable     *
.******************************************************************************
DTOT0000  DISPLAY   *P1:24,*V2LON,TOTALREC," records extracted. ";
          CALL      HITENTER
.
          COMPARE   ZERO,ERRFLG
          GOTO      DTOT1000 IF EQUAL
.
          PRINT     *N,"------------------------------------------":
                       "---------------------------------":
                    *N,"***  End of Report ***"
          DISPLAY   *P1:24,*EL,*BLINKON,ERRFLG," errors found. ";
.
DTOT0500  IF        OPTION=1
            DISPLAY   *P25:24,*V2LON,"Do you want to update records":
                               " as sent (Y/N)";
            KEYIN     *P70:24,ANS
            REP       UPPLOW,ANS
            MATCH     ANSY,ANS              * Yes
            GOTO      DTOT1000 IF EQUAL 
            MATCH     ANSN,ANS              * No
            GOTO      DTOT9999 IF EQUAL
            GOTO      DTOT0500
          ENDIF
.
DTOT1000  IF        OPTION=1
            CALL      UPDT000
          ENDIF
.
DTOT9999  RETURN
+
.******************************************************************************
.*                                  UPDT0000                                  *
.*        Update wathisaf records as sent                                     *
.******************************************************************************
UPDT000   PACK      KEY32,ANSN,SP70
          CALL      RSWTHIS2
UPDT300   CALL      RKWTHIS2
          BRANCH    OVRCD,UPDT400          * end of file?
.
          MATCH     ANSN,WTHIRECS          * record sent=N
          GOTO      UPDT400 IF NOT EQUAL
.
          PACK      KEY31,WTHIURNO,WTHIPROC,DWTHIPCN,WTHIEDAT,DWTHIUCN
          CALL      RDWTHIS1
          BRANCH    OVRCD,UPDT300
.
          PACK      KEY19,WTHIURNO,WTHIPROC,DWTHIPCN,SP70
          CALL      RDTREA1
          BRANCH    OVRCD,UPDT300
.
          CALL      CHKDHB00               * validate District Health Board
          BRANCH    EXIT,UPDT300
.
          MOVE      ANSY,WTHIRECS
          CALL      UPWTHIS1
.
          GOTO      UPDT300
.
UPDT400   PACK      KEY20,ANSN,SP70
          CALL      RDSTREA8
UPDT500   CALL      RDKTREA8
          BRANCH    OVRCD,UPDT999
.
          MATCH     ANSY,WTWMRSFL
          GOTO      UPDT999 IF EQUAL
.
          PACK      KEY19,WMURNO,WMCODE,DWMCNT,SP70
          CALL      RDTREA1
          BRANCH    OVRCD,UPDT500
.
          CALL      CHKDHB00               * validate District Health Board
          BRANCH    EXIT,UPDT500
.
          MOVE      ANSY,WTWMRSFL
          CALL      UPTREA1
.
          GOTO      UPDT500
.
UPDT999   RETURN
+
.------------------------------------------------------------
. Move Extract to Web Directory
.------------------------------------------------------------
MVEXT000  MOVE      ZERO,HLEF
          CALL      GETSCR00                     * save screen in case of errors
          CLEAR     CMDLINE
          APPEND    "ibawat82.us1 ",CMDLINE
          APPEND    FILENAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          CALL      PUTSCR00                     * restore screen
.
MVEXT999  RETURN
+
.*****************************************************************************
.*                           CLRT0000               Called by: MAKE0000      *
.*               Clear any pre-existing records from the temporary table     *
.*****************************************************************************
.
CLRT0000  MOVE      SP30,KEY29
          CALL      RSTRTMP1                     * position at start of table
          CALL      RKTRTMP1                     * read next record
          BRANCH    OVRCD,CLRT9999               * eof - finished
.
          PACK      KEY29,BEURNO,BEEFDAT,BEEFTIM,BEUNIQ
          CALL      DETRTMP1                     * delete record
          GOTO      CLRT0000                     * get next record
.
CLRT9999  RETURN
+
.**************************************************************************
.*                              CREA0000               Called by:         *
.*             create a new temporary file                                *
.**************************************************************************
.
CREA0000  CALL      KILL0000                     * remove existing file
.
          CLEAR     CMDLINE
          IF        DOYR2008=1
            PACK      CMDLINE,ISBUILD,TFILNAME,UKEY08
          ELSE
            PACK      CMDLINE,ISBUILD,TFILNAME,UKEY
          ENDIF
          EXECUTE   CMDLINE,TASKID               * create temporary index file
.
          IF        DOYR2008=1
            OPEN      XXTRTMP2,TFILNAME            * open temp index file
          ELSE
            OPEN      XXTRTMP1,TFILNAME            * open temp index file
          ENDIF
.
CREA9999  RETURN
+
.****************************************************************************
.*                              KILL0000               Called by:           *
.*               close and erase the temporary file                         *
.****************************************************************************
.
KILL0000  CLOSE     XXTRTMP1                     * close file
          CLOSE     XXTRTMP2                     * close file
.
          CLEAR     CMDLINE     
          PACK      CMDLINE,ERASE,TFILNAME       * set file erase command
          EXECUTE   CMDLINE,TASKID               * erase temp file
.
KILL9999 RETURN     
+
.******************************************************************************
.*                   Tempfile IO Routines                                     *
.******************************************************************************
.
RSTRTMP1  IF        DOYR2008=1
            READ      XXTRTMP2,KEY29;;
          ELSE
            READ      XXTRTMP1,KEY29;;
          ENDIF
          RETURN
.
RKTRTMP1  MOVE      ZERO,OVRCD
          IF        DOYR2008=1
            READKS    XXTRTMP2;BEURNO,BEEFDAT,BEEFTIM,DBEUNIQ,BERTYPE:
                                BEACODE:
                                BEBOOKF,BELBEID,BEBSCOD,BEBSDAT,BEHCUID:
                                BEDRFSA,BEBRSRC,BEDOFSA,BECADAT,BECSCOR:
                                BECSSID,BEDCGIV,BEDBMAD,BEDBTRE,BEPHSVP:
                                BETRTAG,BETRFAC,BELHEID,BEHSPEC,BEBPROC:
                                BECLCOD,BECLCTT,BECLSYS,BEDEFBY,BEDECAT:
                                BEEXCAT,BESPPFL,BESEQNC,BECLGRP,BECLRES:
                                BEDOMCL,BEASSCD,BEASSGR,BELINEN
          ELSE
            READKS    XXTRTMP1;BEURNO,BEEFDAT,BEEFTIM,DBEUNIQ,BERTYPE:
                                BEACODE:
                                BEBOOKF,BELBEID,BEBSCOD,BEBSDAT,BEHCUID:
                                BEDRFSA,BEBRSRC,BEDOFSA,BECADAT,BECSCOR:
                                BECSSID,BEDCGIV,BEDBMAD,BEDBTRE,BEPHSVP:
                                BETRTAG,BETRFAC,BELHEID,BEHSPEC,BEBPROC:
                                BECLCOD,BECLCTT,BECLSYS,BEDEFBY,BEDECAT:
                                BEEXCAT,BESPPFL,BESEQNC,BECLGRP,BECLRES:
                                BELINEN
          ENDIF
          GOTO      OVERCOND IF OVER
          MOVE      DBEUNIQ,BEUNIQ
          RETURN
.
RPTRTMP1  MOVE      ZERO,OVRCD
          IF        DOYR2008=1
            READKP    XXTRTMP2;BEURNO,BEEFDAT,BEEFTIM,DBEUNIQ,BERTYPE:
                                BEACODE:
                                BEBOOKF,BELBEID,BEBSCOD,BEBSDAT,BEHCUID:
                                BEDRFSA,BEBRSRC,BEDOFSA,BECADAT,BECSCOR:
                                BECSSID,BEDCGIV,BEDBMAD,BEDBTRE,BEPHSVP:
                                BETRTAG,BETRFAC,BELHEID,BEHSPEC,BEBPROC:
                                BECLCOD,BECLCTT,BECLSYS,BEDEFBY,BEDECAT:
                                BEEXCAT,BESPPFL,BESEQNC,BECLGRP,BECLRES:
                                BEDOMCL,BEASSCD,BEASSGR,BELINEN
          ELSE
            READKP    XXTRTMP1;BEURNO,BEEFDAT,BEEFTIM,DBEUNIQ,BERTYPE:
                                BEACODE:
                                BEBOOKF,BELBEID,BEBSCOD,BEBSDAT,BEHCUID:
                                BEDRFSA,BEBRSRC,BEDOFSA,BECADAT,BECSCOR:
                                BECSSID,BEDCGIV,BEDBMAD,BEDBTRE,BEPHSVP:
                                BETRTAG,BETRFAC,BELHEID,BEHSPEC,BEBPROC:
                                BECLCOD,BECLCTT,BECLSYS,BEDEFBY,BEDECAT:
                                BEEXCAT,BESPPFL,BESEQNC,BECLGRP,BECLRES:
                                BELINEN
          ENDIF
          GOTO      OVERCOND IF OVER
          MOVE      DBEUNIQ,BEUNIQ
          RETURN
.
DETRTMP1  RESET     KEY29
          IF        DOYR2008=1
            DELETE    XXTRTMP2,KEY29
          ELSE
            DELETE    XXTRTMP1,KEY29
          ENDIF
          RETURN     
.
WRBHREC   WRITE     XXTRACT1,SEQ;BHRTYPE,COMMA,BHHAGNCY,COMMA:
                                 BHFILNAM,COMMA,BHNORECS,COMMA:
                                 BHSNDDTE,COMMA,BHFPDATE,COMMA:
                                 BHFPTIME,COMMA,BHPROENV,COMMA:
                                 BHVERSN 
          MOVE      ONE,EXLINENO
          RETURN
+
.********************************************************************** 
.*              I/O INCLUDES FOR FILES AND GENERAL ROUTINES           *
.**********************************************************************
.
          INC       STD001IO
          INC       PATCODKY
          INC       CLWATNBD
          INC       CLWATNBH
.
          INC       BOKRC1IO/INC
          INC       OPRDEAIO/INC
          INC       PATCODIO/INC
          INC       PMSHCPIO/INC
          INC       PATDSCIO/INC
          INC       PATHSPIO/INC
          INC       PATNIDIO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATMA1IO/INC
          INC       PATTRNIO/INC
          INC       PATWR1IO/INC
          INC       NHIMASIO/INC            * NHI Master
          INC       WATTR1IO/INC
          INC       WATHISIO/INC
          INC       WATPROIO/INC
          INC       TFILENAM
          INC       IBASEQIO/INC
          INC       WEBERRIO/INC
          INC       WATNBDIO/INC
          INC       WATNBHIO/INC


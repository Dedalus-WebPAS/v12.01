.----------------------------------------------------------------------
. Program       : PATWEB12
.
. Function      : Bed Board Display and Maintenance
.
. Modifications : 
.------------------------------------------------------------------------------
. V12.01.01 09/12/2025  Thanh T        TSK 0960204
.           Changed CLRWRD00 to space out PTWDROOM field
. V12.00.01 28/05/2025  Bella Turco    TSK 0955096
.           Alphanumeric changes                                                
. V11.04.03 27/06/2024  Bella Turco    TSK 0947816
.           Patient onhover details - changed 'Gender' to short desc of Cat G in
.           STAE0000
. V11.04.02 25/06/2024  Bella Turco    TSK 0947816
.           Patient onhover details - changed 'Gender' to short desc of Cat G in
.           STAC0000
. V11.04.01 30/11/2023  Ebon Clements  TSK 0925492
.           Recompile for BEDBDPRO - Multiple theatre bookings per admission
. V11.03.02 24/07/2023 Thanh T        TSK 0930508
.           Recompiled as PMSVX1TM changes 
. V11.03.01 28/02/2023  Peter Vela TSK 0909393
.           Changed KEY for oprsesaf reads
. V11.02.02 25/07/2022  Don Nguyen     TSK 0921821
.           Added 'title' attribute to enable hover text for all icons with
.           mouse hover in Edge/Chrome.
. V11.02.01 10/02/2022  Thanh T        TSK 0905641
.           Recompiled as OUTMA1FD (PATMASTM) changes
. V11.01.08 13/01/2022  Ebon Clements  TSK 0914894
.           Don't update the bed management tables if the expected
.           ward/bed is not received from the screen - UPPBMN00
. V11.01.07 13/09/2021  Ebon Clements  TSK 0910279
.           Recompiled for BEDBDVCD (BEDBDPRO) - Post Op Ward 
. V11.01.06 27/08/2021  Ebon Clements  TSK 0910383
.           Recompiled for BEDBDVCD (BEDBDPRO) - Post Op / Expected Ward
. V11.01.05 22/07/2021  Ebon Clements  TSK 0909330
.           Recompiled for BEDBDPRO - Post op ward 
. V11.01.04 02/07/2021  Thanh T        TSK 0905755
.           Recompiled as BEDBDPRO changes
. V11.01.03 27/04/2021  Thanh T        TSK 0895165
.           Recompiled for OPRARDFD changes
. V11.01.02 23/04/2021  Ebon Clements  Task 0904815
.           Cross Broser Fixes
. V11.01.01 30/03/2021  Jill Parkinson Task 0902038
.           Added check for inactive beds in GTAG0242
. V11.00.02 09/12/2020  Thanh T        TSK 0872840
.           Added PMSCCDFD/PMSCCDIO as PMSPX2TM changes
. V11.00.01 19/03/2020  Peter Vela     TSK 0889143
.           Recompiled for WATLETFD
. V10.15.05 22/01/2020  Thanh T        TSK 0856646
.           Changed DUNL2200 to pass in SJOGFLAG value for BedRequestLink link
. V10.15.04 22/01/2020  Thanh T        TSK 0856646
.           Changed DUNL2200 to pass in SJOGFLAG value for BedRequestLink link
. V10.15.03 15/01/2020  Thanh T        TSK 0856646
.           Changed SPCFEA00 to append 2 &nbsp characters instead of 2 SPACE  
. V10.15.02 07/11/2019  Thanh T        TSK 0856646
.           Changed GETLOS00 to remove the leading spaces for Loss time
. V10.15.01 21/10/2019  Thanh T        TSK 0856646
.           Added GTAG0480, SPCFEA00, SPCFLG00 and GETLOS00
.------------------------------------------------------------------------------
. V10.14.01 04/04/2019  Ania P         TSK 0870267
.           Added PTWDBDST check in GTAG0222
. V10.12.03 04/06/2018  Richa Phogat   TSK 0850937
.           Added field 'PACAFLAG'
. V10.12.02 03/05/2018  Richa Phogat      TSK 0843171
.           Added field FIRSTBOK
. V10.12.01 26/03/2018  Ebon Clements     TSK 0847804
.           Set value of YESTERDY variable for bed board - DBED0000
. V10.11.05 27/11/2017  Thanh Tieu     Task 0821710
.           Recompiled as PATMASTM changed
. V10.11.04 16/10/2017  Thanh T.       TSK 0846191
.           Recomplied for OPTHETAT
. V10.11.03 30/07/2017  Thanh T.       TSK 0821710
.           Audit admission/outpatient/UR comments. changed PATMASTM/PMSPX2TM.
. V10.11.02 26/07/2017  Thanh T.       TSK 0821710
.           Recompiled PASMISTM
. V10.11.01 14/07/2017  Thanh T.       TSK 0821710
.           Audit Amission/outpatient visit comments. PATMISTM changed.
.------------------------------------------------------------------------------
. V10.08.09 12/10/2016  Ebon Clements     TSK 0318770
.           Added theatre audit to ward/bed and post op ward/bed update ALLC0000
. V10.08.08 23/09/2016  Ebon Clements     TSK 0323003
.           Changed speciality mismatch icon to SpecialityMismatch.png
.           instead of docbag1.gif
. V10.08.07 19/09/2016  Peter Vela        TSK 0808920
.           Added PTCNHFSD - Use Health Fund Step Down in Bed Board PATA2000 
. V10.08.06 08/08/2016  Davin             TSK 0321234
.           Added Chronic Condition Alert (PMPXSN11)
. V10.08.05 26/07/2016  Jill Parkinson TSK 0296775
.           Recompiled for BEDBDVCD
. V10.08.03 25/07/2016  Jill Parkinson TSK 0296775
.           Changed the bed board for expected patients (PCEL2000 UCEL0000 and 
.           BEDE2000) to show patients still on the expected list from yesterday
. V10.08.02 27/06/2016  Ebon Clements  TSK 0312728
.           Display theatre recovery bay when theatre status is recovery
. V10.08.01 17/05/2016  Peter Vela     TSK 0819159
.           Recompiled for PMSHRDTM
. V10.07.02 18/03/2016  Peter Vela     CAR 0812828
.           Added validation for theatre bookings at PATA4000
. V10.07.01 15/10/2015  Ebon Clements  CAR 316638
.           Added cancelled/tranferred status display - PATSTS00
. V10.06.02 27/07/2015  Peter Vela     CAR 305842
.           Added Bed Management functionality for Unallocated Bed Board View
. V10.06.01 12/06/2015  Peter Vela     CAR 314162
.           Recompiled for PMSHRDTM
. V10.05.06 02/12/2014  Ebon Clements  CAR 308329
.           Bed board speed issues. Do not read extension file records
.           if no main file record exists. eg bokrc1af and bokrx1af
. V10.05.05 06/11/2014  Don Nguyen     CAR 304059
.           Recompiled for changes to OPRSESIO - added Default Team Completed
.           fields.
. V10.05.04 12/09/2014  Don Nguyen     CAR 296697
.           Added Disability Alert icons (AlertIconDIS.gif) where applicable
. V10.05.03 04/09/2014  Don Nguyen     CAR 296697
.           Modified PATA0000 and EXPA0000 to also display Alert icons for
.           Medical, Micro & Risk Alerts.
.           Modified GETPAT00 to also call RDPMPX21.
. V10.05.02 13/08/2014  Peter Vela     CAR 304860
.           Recompiled for PMSVX1TM
. V10.05.01 11/08/2014  Don Nguyen     CAR 297774 
.           Modified STAE0000 and STAR0000 to display Bed Ready Date/Time for 
.           allocated Bed Requests.
.           Increased size of STATWIND from 600.
.           Recompiled for changes to PMSBRQFD/IO
.------------------------------------------------------------------------------
. V10.04.04 19/06/2014  Jill Parkinson CAR 273374 
.           Added check for OPSGTISS in PATSTS00 to show 'Theatre'
. V10.04.03 07/04/2014  Peter Vela      CAR 286258
.           Recompiled for PATMASTM
. V10.04.02 31.03.2014  B.G.Ackland  CAR 299363
.           Replace META Tag Redirect with Javascript in Common RDIREC00
.           Routine
. V10.04.01 31/12/2013  Don Nguyen     CAR 294389
.           Recompiled for changes to WATLETFD & WATLETIO
.----------------------------------------------------------------------
. V10.03.09 02/12/2013  Ebon Clements            CAR 291161
.                       Do not display theatre today icon if booking i
.                       cancelled.  PATA4000 and EXPA4000
. V10.03.08 22/11/2013  Patrick Adair  CAR 282248
.                       Changed Diagnosis label to read Reason For 
.                       Admission for NZ
. V10.03.07 26/03/2013  Patrick Adair CAR 281548
.                       Recompiled for changes to CLBOKRC1
. V10.03.06 01/11/2012  Saroeun Soeur CAR 275331
.                       CHKD0000 - check if same ur
. V10.03.05 07/09/2012  Peter Vela  CAR 214114
.                       Fixed floating div functionality again
. V10.03.04 04/09/2012  Peter Vela  CAR 214114
.                       Fixed floating div functionality
. V10.03.03 18/07/2012  Peter Vela  CAR 263694
.                       Fixed Expected Bed comparison @ ALLC1200
. V10.03.02 10.07.2012  B.G.Ackland CAR 269028
.                       iPad List Unallocated Bed Board patient GTAG0470
. V10.03.01 10/05/2012  Peter Vela     CAR 232137
.                       Recompiled for BEDBDVCD
. V10.02.03 30/09/2011  Jill Parkinson CAR 248486
.           Recompiled for IBAWEBCD - Restrict Hospital Access
. V10.02.02 04/07/2011  Peter McMullen CAR 240725
.                       Mods to respond to PTCNWBDS=2 (short description)
.           04/07/2011  Steve Armstrong   CAR 240722
.                       Mods to cater for changes to PMSCURFD:
.                            - PMCUSURN & PMCUGNAM extended to 40 chars.
.                            - PMCUGNM2 added.
. V10.02.01 23/06/2011  Peter McMullen CAR 240725
.                       Change ward selection lists to sort by name, not code 
.----------------------------------------------------------------------
. V10.01.02 11/01/2011  Ebon Clements  CAR 215701
.                       Recompiled for BEDBDVCD -  H/F stepdown
. V10.01.01 20/12/2010  Ebon Clements  CAR 229507
.                       Allow update of post op ward/bed for admitted patients
.----------------------------------------------------------------------
. V10.00.06 06/10/2010  Ebon Clements  CAR 231396
.                       Recompiled for BEDBDVCD - Post op records expected ward/bed
. V10.00.05 21/09/2010  Ebon Clements  CAR 208406
.                       Only display an admission once in bed history BEDH0000
. V10.00.04 15/09/2010  Ebon Clements  CAR 208406
.                       Changed bed history to ignore standby patient BEDH0000
. V10.00.03 15/09/2010  Ebon Clements  CAR 210611
.                       Added past expected discharge date highlight
. V10.00.02 14/09/2010  Ebon Clements  CAR 210611
.                       Added on leave icon to bed boards
. V10.00.01 22/07/2010  Peter Vela     CAR 214114
.                       Added floating <div>  functionality to DBED0000
.                       Added floating <div>  functionality to DUNL0000
.----------------------------------------------------------------------
. V9.12.28  23/12/2009  Ebon Clements  CAR 212286
.                       Added BEDBHOSP and BEDBSEQN input cgi vars
. V9.12.27  10/12/2009  Ebon Clements  CAR 212286
.                       Fixed correct GOTO label to RCEL1000 in ECEL0000
. V9.12.26  07/12/2009  Ebon Clements  CAR 211452
.                       Added JSCREF00 - redirect by javascript
. V9.12.25  04/12/2009  Ebon Clements  CAR 208918
.                       Fixed expected discharge date test on tomorrows
.                       unallocated bed board - UCEL0000
. V9.12.24  03/12/2009  Ebon Clements  CAR 204362
.                       Recompiled for BEDBDPRO - Clear post op ward
. V9.12.23  24/11/2009  Ebon Clements            CAR 210736
.                       Remove special characters from PGNAME - GETPAT00
. V9.12.22  18/11/2009  Ebon Clements            CAR 210209 
.                       Fixed post op display on tomorrows unallocated bed board
. V9.12.21  17/11/2009  Ebon Clements            CAR 210209 
.                       Added expected room gender to unallocated bed board
. V9.12.20  16/11/2009  Ebon Clements            CAR 210209
.                       Search all patients regardless of bed board view
. V9.12.19  06/11/2009  Ebon Clements            CAR 209694
.                       Recompiled for BEDBDVCD - Post op expected due date
.                       Fixed room gender display and onhover display
. V9.12.18  06/11/2009  Ebon Clements            CAR 209694
.                       Fixed patient name conflict test - CHKD0000 - CKDE0000
. V9.12.17  06/11/2009  Ebon Clements            CAR 208991
.                       Allow update of admitted patients post op ward SBED0000
. V9.12.16  04/11/2009  Ebon Clements            CAR 208991
.                       Allow update of admitted patients post op ward UCEL0000
. V9.12.15  16/10/2009  Ebon Clements            CAR 294145
.                       Added link to modify available beds ModBed()
. V9.12.14  31/08/2009  Ebon Clements            CAR 199232
.                       Only highlight room if more than one bed - BRMG0000
. V9.12.13  20/08/2009  Ebon Clements            CAR 203507
.                       Changed BEDH0000 to include transfers
. V9.12.12  14/08/2009  Ebon Clements            CAR 203679
.                       Clear OVERLAPF and STBYFLAG in PCEL0000
.                       Fixed room allocation to stop duplication
.                       of master ward record - BEDS5000
. V9.12.11  16/07/2009  Ebon Clements            CAR 199049
.                       Added booking expected ward/bed tags to GTAG0000
. V9.12.10  09/07/2009  Ebon Clements            CAR 200819
.                       Display anaesthetist if booked/pre admitted - STAE0000
. V9.12.09  26/06/2009  Ebon Clements            CAR 198912
.                       Strip apostrophe from diagnosis in STAE0000
. V9.12.08  23/06/2009  Ebon Clements            CAR 199232
.                       Added ROOMCLAS to highlight rooms
.                       Added title/alt to bed cell room name/icon
. V9.12.07  10/06/2009  Ebon Clements            CAR 198625
.                       Added open of mltcodaf
. V9.12.06  02/06/2009  Ebon Clements            CAR 197329
.                       Changed diagnosis and adm time to be bold on expected
.                       patient hover
. V9.12.05  01/06/2009  Ebon Clements            CAR 197329
.                       Don't display red highlight cell if the standby is a
.                       baby linked to the beds current admission
. V9.12.04  31/05/2009  Jill Habasque            CAR 197329
.                       Added call to GETPAT00 in SEXP0000 - noupdate wasnt set
. V9.12.03  26/05/2009  Ebon Clements            CAR 197329
.                       Fixed expected discharge time display - UCEL1000
.                       Added display of arrival time for expected patients
.                       Fixed display of ward not assigned in search
. V9.12.02  20/05/2009  Ebon Clements            CAR 195390
.                       Fixed display of patient/doctors name with apostrophe
. V9.12.01  30/04/2009  Ebon Clements            CAR 195390
.                       Added paediatric icon and expected patient list BEDE0000
.----------------------------------------------------------------------
. V9.11.10  06/01/2008  Ebon Clements            CAR 174080
.                       Unallocated bed board testing bug fixes
. V9.11.09  22/12/2008  Ebon Clements            CAR 174081
.                       Added tomorrows unallocated patients bed board view
. V9.11.08  04/12/2008  Ebon Clements            CAR 174080
.                       Added unallocated patients bed board view
. V9.11.07  04/12/2008  Ebon Clements            CAR 174044
.                       Added view sequence functionality
. V9.11.06  02/12/2008  Ebon Clements            CAR 174082
.                       Added report 5 housekeeping
. V9.11.05  01/12/2008  Ebon Clements            CAR 174057
.                       Added BEDBDPRO
. V9.11.04  24/11/2008  Ebon Clements            CAR 174077
.                       Added update expected/post-op ward bed
.                       Added deceased patient indicator
. V9.11.03  28/10/2008  Ebon Clements            CAR 174077
.                       Added gender by room functionality
. V9.11.02  09/10/2008  Ebon Clements            CAR 174077
.                       Added report 3 - Bed board view
. V9.11.01  08/10/2008  Ebon Clements            CAR 174068
.                       Added urnumber to bed board and expected patient files
. V9.11.00  24/09/2008  Ebon Clements            CAR 174038
.                       Created Web Server
.----------------------------------------------------------------------
.
          INC       IBAWEBDF    
          INC       WEBDATDF
.
          INC       BOKDIAFD/INC
          INC       BOKRC1FD/INC
          INC       BOKRX1FD/INC
          INC       BOKRX1TD/INC
          INC       EMRVISFD/INC
          INC       PATDHEAD/INC
          INC       PATONLFD/INC
          INC       PATTRNFD/INC
          INC       PMSBBDTD/INC
          INC       PMSBBDFD/INC
          INC       PMSEPBFD/INC
          INC       PMSHRDFD/INC
          INC       PMSHRDTD/INC
          INC       PMSMSWFD/INC
          INC       PMSWORTD/INC
          INC       PMSWORFD/INC
          INC       PATDO1FD/INC   * Doctor File
          INC       PATHSPFD/INC
          INC       PATMI1FD/INC
          INC       PATMA1FD/INC
          INC       PATVISFD/INC   * Patients Visits File
          INC       PMSBDCFD/INC
          INC       PMSBRQFD/INC
          INC       PMSVX1FD/INC   * Visit Extension File
          INC       PMSVX1TD/INC
          INC       PATCONFD/INC
          INC       PATALRFD/INC
          INC       PATBMHFD/INC
          INC       PATBMDFD/INC
          INC       PMSREGFD/INC
          INC       PATICDFD/INC
          INC       OPRARDFD/INC
          INC       OPRDEAFD/INC
          INC       OPRSRGFD/INC
          INC       OPRSESFD/INC
          INC       OUTSITFD/INC
          INC       PMSUCHFD/INC
          INC       PMSUCDFD/INC
          INC       PATMASTD/INC
          INC       PATPR1FD/INC
          INC       PMSPX2FD/INC
          INC       PATFN1FD/INC
          INC       PATWR1FD/INC
          INC       MRTMASFD/INC
          INC       NHIMASFD/INC
          INC       NHIETHFD/INC
          INC       PATCALAG/INC
          INC       PMSPX2TD/INC
          INC       PATRE1FD/INC
          INC       PATDGWFD/INC
          INC       VISMDTFD/INC
          INC       VISMTXFD/INC
          INC       PATITMFD/INC
          INC       PATDADFD/INC
          INC       PATVADFD/INC
          INC       PATCMCFD/INC
          INC       PATASFFD/INC
          INC       PATLINFD/INC
.
          INC       PMSCCDFD/INC   * Concession Card Details
          INC       PMSCURFD/INC
          INC       PMSHCGTD/INC
          INC       PMSHCGFD/INC
          INC       PMSHCPFD/INC
          INC       PMSHCPTD/INC
          INC       APSMASFD/INC
          INC       PATIN1FD/INC
          INC       PATDSCFD/INC
          INC       WATCONFD/INC
          INC       WATLETFD/INC
          INC       PATMISTD/INC
.
ARRCNT    FORM      3
ADJTIME   FORM      5
ATYPDESC  DIM       25
BEDBDKEY  DIM       20
BEDBVIEW  DIM       1
BEDBHOSP  DIM       3
BEDBSEQN  FORM      1
BEDDCODE  DIM       3
BEDCOUNT  FORM      2
BEDREQKY  DIM       25
BEDRFLAG  FORM      1
BEDRWARD  INIT      "~~B"
NOTAWARD  INIT      "~~W"
BTYPDESC  DIM       25
CALDAYS   FORM      4
CATD1     INIT      "D1"
CATD2     INIT      "D2"
CATD3     INIT      "D3"
CATD4     INIT      "D4"
CATD5     INIT      "D5"
CATU6     INIT      "U6"
CATU7     INIT      "U7"
CATHW     INIT      "hw"
CATHX     INIT      "hx"
CLBDINDC  FORM      1
CTYPDESC  DIM       25
CURRDATE  DIM       8
DFLTWARD  DIM       3     * ward to me marked 'selected' is select lists
NBRDAYS   FORM      4
TODATE    DIM       8
WORKDATE  DATE      8
YESTERDY  DATE      8
CURRSPEC  DIM       3
WORKTIME  DIM       8
RECCOUNT  FORM      2
ROOMCNTR  FORM      3
WARDCNTR  FORM      3
ROOMCLAS  DIM       7
DIM3      DIM       3
DIM5      DIM       5
DIM6      DIM       6
DIM8      DIM       8
DIM10     DIM       10
DIM20A    DIM       20
DIM20B    DIM       20
DIM20C    DIM       20
DIM20D    DIM       20
DISPDATE  DIM       11
DISPDAT1  DIM       11
DOCTCODE  DIM       6
DUPNMFLG  FORM      1
EMPTONLY  FORM      1
EXADFLAG  FORM      1
EXTRSPEC  DIM       3
EXPPRITY  FORM      1
SAVPRITY  FORM      1
EXPDSDTE  DIM       8        * Calculated Expected Discharge Date
EXPDSTME  DIM       5        * Calculated Expected Discharge Time
FMTPATAG  DIM       10
FILTRSTA  DIM       3
FILTCSTA  DIM       3
FILTWARD  DIM       3
FOLDERSF  DIM       3
FORM3     FORM      3
FORM3A    FORM      3
FORM4B    FORM      4
FORM5     FORM      5
FORM6     FORM      6
FORM8     FORM      8
FROMDATE  DIM       8
FRSTWARD  FORM      1
GEXPGEND  DIM       1
BREQFLAG  DIM       1
SPECFLAG  DIM       1
INPSPCDS  DIM       20
SPCBEDDS  DIM       100
LOSDINDC  FORM      1
KEEPBKTM  DIM       1
HOSPCODE  DIM       3
MBSDESC   DIM       40
MULTIEXP  FORM      2
NEXTPAGE  DIM       1       * Nextpage parameter CGI parameter
NOUPDATE  FORM      1
UPDATHRD  DIM       1
OLDUNIQ   DIM       10
OVERLAPF  FORM      1 
PACAFLAG  DIM       4       * Menuflag
PTRGM001  DIM       10
PTRGM002  DIM       10
PTRGM003  DIM       10
PTMIS068  DIM       3
PTMIS076  DIM       3
POSTOPFL  FORM      1
PROCFLAG  DIM       1
PRFORDER  DIM       2
RECFOUND  FORM      1
ROOMGEND  DIM       1
SAVEADMN  DIM       8
SAVEBRQN  DIM       8
SAVEDATE  DIM       8
SAVETIME  DIM       5
SAVEGENA  DIM       1
SAVEGENB  DIM       1
SAVEGENC  DIM       1
SAVEGEND  DIM       1
SAVELOSD  DIM       3
SAVELOSM  DIM       5
SAVESEQ1  FORM      2
SAVESEQ2  FORM      2
SAVESEQ3  FORM      2
SAVESEQ4  FORM      2
SAVESEQ5  FORM      2
SAVEROOM  DIM       3
SAVETIMA  DIM       8
SAVETIMB  DIM       8
SAVETIMC  DIM       8
SAVETIMD  DIM       8
SAVEADM1  DIM       8
SAVEADM2  DIM       8
SAVEADM3  DIM       8
SAVEWARD  DIM       3
SAVEKEY6  DIM       6
SAVEKEY9  DIM       9
SAVKEY12  DIM       12
SAVKEY20  DIM       20
SAVKEY22  DIM       22
SAVKEY30  DIM       30
SJOGFLAG  DIM       1
STRTTIME  DIM       8
SVWBDESC  DIM       20
SVWINPUT  FORM      1
SPECCNTR  FORM      3
SPECWARD  DIM       2[99]
SPECLOOP  FORM      3
SPCFEADS  DIM       140
LOSTIME   DIM       6
HLCLASS   DIM       10
HOUR      DIM       2
MIN       DIM       2
SVKEY20A  DIM       20
SVKEY20B  DIM       20
SVKEY20C  DIM       20
SVKEY20D  DIM       20
SRCHSNAM  DIM       20
SRCHGNAM  DIM       25
STATWIND  DIM       800
STBYFLAG  FORM      1
.
TADTTYPE  DIM       3       * Theatre Audit type
THEAUDIT  FORM      1
THETFLAG  FORM      1
THETSTAT  DIM       30
THETRBAY  DIM       5
TOMMFLAG  FORM      1
TOMRFLAG  FORM      1
TOMRDATE  DATE      8
TYPEDESC  DIM       20
UPDATFLG  FORM      1
UNALPTOT  FORM      4
UPDTTYPE  DIM       1       * Update Type
VIEWSEQN  FORM      1
VIEWHOSP  DIM       3
WARDCODE  DIM       3
WARDSCNT  FORM      3
WARDSARR  DIM       3[99]
WBEDDESC  FORM      1
ACDESC    DIM       25
CLDESC    DIM       25
BPDESC    DIM       25
FUNDNAME  DIM       80
FIRSTBOK  DIM       1
.
.----------------------------------------------------------------------
PRGID     INIT      "PATWEB12"
VERSION   INIT      "V12.01.01"
PRGNAM    INIT      "Bed Board View"
.----------------------------------------------------------------------
.
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
.
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200,MAIN1300,MAIN1400,MAIN1500
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "Invalid Option",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  CALL      BEDS0000        * Bed Board Display Sequence Maintenance
          BRANCH    EXIT,MAIN1000
.
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1200  CALL      MWRD0000        * Multi Speciality Bed Maintenance
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1300  CALL      BEDB0000        * Bed Board View
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1400  CALL      ALLC0000        * Update Expected/Post-Op Ward/Bed
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN1500  CALL      HOUS0000        * Housekeeping                         
          BRANCH    EXIT,MAIN1000
          CALL      NXTPAG00
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
.------------------------------------------------------------
. Output Next Page Based on CGI Parameter nextpage
.------------------------------------------------------------
NXTPAG00  MOVE      ZERO,F1
          MOVE      NEXTPAGE,F1
          BRANCH    F1,NXTPAG10,NXTPAG20,NXTPAG30,NXTPAG40,NXTPAG50
.
          CALL      WINCLS00
          GOTO      NXTPAG99
.
NXTPAG10  CALL      RDIREC00
          GOTO      NXTPAG99
.
NXTPAG20  CALL      GOBACK00      * Go Back 1 Html page
          GOTO      NXTPAG99
.
NXTPAG30  CALL      DAH20000      * Go Back 2 html pages
          GOTO      NXTPAG99
.
NXTPAG40  CALL      PREDIR00      * Redirect Parent
          GOTO      NXTPAG99
.
NXTPAG50  CALL      JSCREF00      * Execute a javascript
          GOTO      NXTPAG99
.
NXTPAG99  RETURN
.------------------------------------------------------------------------------
.  Redirect Parent URL
.------------------------------------------------------------------------------
PREDIR00  CALL      OPENHTML
          BRANCH    EXIT,PREDIR90
.
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             " parent.location.href=#"",REDIRECT,"#";":
                             "}":
                             "</script>"
          CALL      CLOSHTML
          GOTO      PREDIR99
.
PREDIR90  DISPLAY   "*** Fifo Not Found ***"
.
PREDIR99  RETURN
.-------------------------------------------------------------------------------
.  Goes back 1 page
.-------------------------------------------------------------------------------
GOBACK00  CALL      OPENHTML
          BRANCH    EXIT,GOBACK99
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "    alert(#"",WEBTITLE,"#");":
                             "    self.close();":
                             "    opener.history.go(-1);":
                             "}":
                             "</script>"
          CALL      CLOSHTML
GOBACK99  RETURN
.------------------------------------------------------------------------------
.  Redirect by executing a javascript
.------------------------------------------------------------------------------
JSCREF00  CALL      OPENHTML
          BRANCH    EXIT,JSCREF90
.
          WRITE     HTMLFILE;"<script type=#"text/javascript#" ":
                             "src=#"../js/Standard.js#"></script>":
                             "<script type=#"text/javascript#"> {":
                             REDIRECT:
                             "}":
                             "</script>"
          CALL      CLOSHTML
          GOTO      JSCREF99
.
JSCREF90  DISPLAY   "*** Fifo Not Found ***"
.
JSCREF99  RETURN
.------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------
INIT0000  OPEN      OPRARDA1,"oprardaf"
          OPEN      OPRSRGA1,"oprsrgaf"
          OPEN      OPRSESA1,"oprsesaf"
          OPEN      OPRDETA2,"oprdetaf"
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATWR1A2,"patwr1af"
          OPEN      PATWR1A4,"patwr1af"
          OPEN      PATWR1A5,"patwr1af"
          OPEN      PATWR1A7,"patwr1af"
.
          OPEN      EMRVISA1,"emrvisaf"
          OPEN      BOKDIAA1,"bokdiaaf"
          OPEN      PATONLV1,"patonlvf"
          OPEN      PATTRAN1,"pattranf"
          OPEN      PATTRAN2,"pattranf"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
          OPEN      PATHSPA1,"pathspaf"
          OPEN      PMSREGA1,"pmsregaf"
          OPEN      PATBMHA1,"patbmhaf"
          OPEN      PATBMDA1,"patbmdaf"
          OPEN      PATBMDA2,"patbmdaf"
          OPEN      PATBMDA3,"patbmdaf"
          OPEN      PATBMDA4,"patbmdaf"
.
          OPEN      PMSCCDA1,"pmsccdaf"
          OPEN      PMSBBDA1,"pmsbbdaf"
          OPEN      PMSBBDA2,"pmsbbdaf"
          OPEN      PMSBBDA3,"pmsbbdaf"
          OPEN      PMSBBDA4,"pmsbbdaf"
          OPEN      PMSBBDA5,"pmsbbdaf"
          OPEN      PMSBBDA6,"pmsbbdaf"
          OPEN      PMSBBDA7,"pmsbbdaf"
          OPEN      PMSBRQA1,"pmsbrqaf"
          OPEN      PMSBRQA2,"pmsbrqaf"
          OPEN      PMSCURA2,"pmscuraf"
          OPEN      PMSCURA6,"pmscuraf"
          OPEN      PMSEPBA1,"pmsepbaf"
          OPEN      PMSEPBA2,"pmsepbaf"
          OPEN      PMSEPBA3,"pmsepbaf"
          OPEN      PMSEPBA4,"pmsepbaf"
          OPEN      PMSEPBA5,"pmsepbaf"
          OPEN      PMSEPBA6,"pmsepbaf"
          OPEN      PMSEPBA7,"pmsepbaf"
          OPEN      PMSEPBA8,"pmsepbaf"
          OPEN      PMSHRDA1,"pmshrdaf"
          OPEN      PMSHRDA2,"pmshrdaf"
          OPEN      PMSHCPA1,"pmshcpaf"
          OPEN      PMSMSWA1,"pmsmswaf"
          OPEN      PMSBDCA1,"pmsbdcaf"
          OPEN      PMSWORA1,"pmsworaf"
          OPEN      BOKRC1A1,"bokrc1af"
          OPEN      BOKRX1A1,"bokrx1af"
.
          CALL      INTMAS00     * Open routine for PATMASTM
          OPEN      PATVISA2,"patvisaf"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      PATRE1A1,"patre1af"
          OPEN      PATDGWA1,"patdgwaf"
          OPEN      VISMDTA1,"vismdtaf"
          OPEN      VISMTXA1,"vismtxaf"
          OPEN      PATITEM1,"patitemn"
          OPEN      PATASFA1,"patasfaf"
          OPEN      PATLINK1,"patlinkf"
          OPEN      PATDSCH1,"patdschf"
          OPEN      WATLETR1,"watletrf"
          CALL      OPICD1                  * Open ICD Disease files
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TEN;*94,CAGECOFF
          READ      CONTROLF,TEN9;*212,CFEETYP
          READ      CONTROLF,HUND12;*107,PTCNBMAN
          READ      CONTROLF,HUND12;*104,PTCNWBDS
          READ      CONTROLF,EIGHTY;*250,PTCNHDPS
          READ      CONTROLF,HUND14;*137,PTCNBBAP
          READ      CONTROLF,THIRTY7;*137,HWDSOUR
          READ      CONTROLF,HUND24;*124,PTCNHFSD
.
          MOVE      PTCNWBDS,WBEDDESC
.
          IF        IBCNMHOS=1
            OPEN      MLTCODA1,"mltcodaf"
            OPEN      MLTCODA2,"mltcodaf"
          ENDIF
.
INIT9999  RETURN
.------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          MOVE      SP70,TEMPLATE
          MOVE      ZERO,BEDBSEQN
          MOVE      Z70,BEDBHOSP
          MOVE      SP70,NEXTPAGE
          MOVE      SP70,REDIRECT
          MOVE      SP70,UPDTTYPE
          MOVE      SP70,BEDDCODE
          MOVE      SP70,WARDCODE
          MOVE      SP70,HOSPCODE
          MOVE      SP70,BEDBDKEY
          MOVE      SP70,EXTRSPEC
          MOVE      SP70,PRFORDER
          MOVE      SP70,SRCHSNAM
          MOVE      SP70,SRCHGNAM
          MOVE      Z70,PTMIS068
          MOVE      Z70,PTMIS076
          MOVE      SP70,FILTRSTA
          MOVE      SP70,FILTCSTA
          MOVE      SP70,FILTWARD
          MOVE      SP70,VIEWTYPE
          MOVE      SP70,FRSTDATE
          MOVE      SP70,LASTDATE
          MOVE      SP70,VYEARMTH
          MOVE      SP70,BEDREQKY
          MOVE      ZERO,EMPTONLY
          MOVE      ZERO,TOMRFLAG
          MOVE      SP70,PACAFLAG
          MOVE      SP70,SJOGFLAG
.
          CALL      CPPMBD00
          CALL      CPVXTF00
          CALL      CPBKRX00
          CALL      CPPMHR00
.
          MOVE      ONE,WARDSCNT
          WHILE     WARDSCNT <= 99
            MOVE      SP70,WARDSARR[WARDSCNT]
            ADD       ONE,WARDSCNT
          DO
.
          MOVE      ZERO,WARDSCNT
.
          PACK      PTRGM001,SP70
          PACK      PTRGM002,SP70
          PACK      PTRGM003,SP70
          MOVE      SP70,OLDUNIQ
.
CLRPAR99  RETURN
.------------------------------------------------------------
. Set Parameters
.------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "viewtype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,VIEWTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "bedbhosp=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,BEDBHOSP
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "bedbseqn=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,BEDBSEQN
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "updttype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,UPDTTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "nextpage=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NEXTPAGE
            GOTO      SETPAR99
          ENDIF 
.
          MATCH     "frstdate=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FRSTDATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "lastdate=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,LASTDATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "vyearmth=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,VYEARMTH
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "redirect=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REDIRECT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "tomrflag=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TOMRFLAG
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "wardcode=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,WARDCODE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "beddcode=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,BEDDCODE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "hospcode=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,HOSPCODE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "bedbdkey=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,BEDBDKEY
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "extrspec=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,EXTRSPEC
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "ptmis068=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,PTMIS068
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "ptmis076=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,PTMIS076
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "prforder=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,PRFORDER
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "srchsnam=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SRCHSNAM
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "srchgnam=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SRCHGNAM
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "urnumber=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,URNUMBER
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "admissno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ADMISSNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtrsta=",QRYNAME
          IF        @EQUAL
            PACK      FILTRSTA,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtcsta=",QRYNAME
          IF        @EQUAL
            PACK      FILTCSTA,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "emptonly=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,EMPTONLY
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "filtward=",QRYNAME
          IF        @EQUAL
            PACK      FILTWARD,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "bedreqky=",QRYNAME
          IF        @EQUAL
            PACK      BEDREQKY,QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "pmbbd",QRYNAME
          IF        @EQUAL
            CALL      SPPMBD00
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "wardsarr=",QRYNAME
          IF        @EQUAL
            COMPARE   "99",WARDSCNT
            GOTO      SETPAR99 IF EQUAL
            ADD       ONE,WARDSCNT
            PACK      WARDSARR[WARDSCNT],QRYDATA,SP70
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "pmsvx",QRYNAME
          IF        @EQUAL
            CALL      SPVXTF00
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "bokrx",QRYNAME
          IF        @EQUAL
            CALL      SPBKRX00
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "pmhrd",QRYNAME
          IF        @EQUAL
            CALL      SPPMHR00
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "pacaflag=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,PACAFLAG
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "sjogflag=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SJOGFLAG
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
.
.------------------------------------------------------------
. Bed Board Display Sequence Maintenance
.------------------------------------------------------------
BEDS0000  MATCH     SP70,UPDTTYPE
          GOTO      BEDS7000 IF EQUAL
.
          MATCH     "A",UPDTTYPE            * Add
          GOTO      BEDS1000 IF EQUAL
.
          MATCH     "U",UPDTTYPE            * Update
          GOTO      BEDS2000 IF EQUAL
.
          MATCH     "D",UPDTTYPE            * Delete
          GOTO      BEDS3000 IF EQUAL
.
          MATCH     "B",UPDTTYPE            * Add Bed
          GOTO      BEDS4000 IF EQUAL
.
          MATCH     "E",UPDTTYPE            * Update Bed
          GOTO      BEDS5000 IF EQUAL
.
          MATCH     "F",UPDTTYPE            * Delete Bed
          GOTO      BEDS6000 IF EQUAL
.
          GOTO      BEDS9000
.
BEDS1000  CALL      SEQE0000                * Edit Checks
          BRANCH    EXIT,BEDS9999
.
          CALL      CLPMSBBD 
          PACK      KEY20,PMBBD001,PMBBD002,PMBBD003,PMBBD004,PMBBD010,SP70
          CALL      RAPMBBD1
          COMPARE   ZERO,OVRCD
          GOTO      BEDS9100 IF EQUAL
.
          PACK      PMBDDATC,CCC,CYY,CMM,CDD
          REP       " 0",PMBDDATC
          CLOCK     TIME,PMBDTIMC
          MOVE      USERID,PMBDWEBC
.
          CALL      MVPMBD00
          CALL      WRPMBBD1                     * Add bed board ward
.
          CALL      CLPMSEPB
          PACK      KEY20,PMBBD001,PMBBD002,PMBBD003,PMBBD004,PMBBD010,SP70
          CALL      RAPMEPB1
          COMPARE   ZERO,OVRCD
          GOTO      BEDS1900 IF EQUAL
.         
          MOVE      PMBDHOSP,PMEBHOSP
          MOVE      PMBDWARD,PMEBWARD
          MOVE      PMBDROOM,PMEBROOM
          MOVE      PMBDBEDD,PMEBBEDD
.         
          MOVE      PMBDSEQ1,PMEBSEQ1            
          MOVE      PMBDSEQ2,PMEBSEQ2            
          MOVE      PMBDSEQ3,PMEBSEQ3            
          MOVE      PMBDSEQ4,PMEBSEQ4
          MOVE      PMBDSEQ5,PMEBSEQ5
.
          PACK      PMEBDATC,CCC,CYY,CMM,CDD
          REP       " 0",PMEBDATC
          CLOCK     TIME,PMEBTIMC
          MOVE      USERID,PMEBWEBC
.         
          CALL      WRPMEPB1                     * Add expected patient ward
.
BEDS1900  MOVE      ZERO,EXIT
          GOTO      BEDS9999
.
BEDS2000  CALL      SEQE0000                     * Edit Checks
          BRANCH    EXIT,BEDS9999
.
          PACK      KEY20,PMBBD001,PMBBD002,PMBBD003,PMBBD004,PMBBD010,SP70
          CALL      RDPMBBD1
          BRANCH    OVRCD,BEDS9200
.
          PACK      PMBDDATU,CCC,CYY,CMM,CDD
          REP       " 0",PMBDDATU
          CLOCK     TIME,PMBDTIMU
          MOVE      USERID,PMBDWEBU
.
          CALL      MVPMBD00
          CALL      UPPMBBD1
.
          MOVE      PMBDSEQ1,SAVESEQ1            * Save ward master record
          MOVE      PMBDSEQ2,SAVESEQ2            * sequence number 
          MOVE      PMBDSEQ3,SAVESEQ3
          MOVE      PMBDSEQ4,SAVESEQ4
          MOVE      PMBDSEQ5,SAVESEQ5
.
          PACK      KEY20,PMBBD001,PMBBD002,SP70
          CALL      RSPMBBD1
BEDS2100  CALL      RKPMBBD1
          BRANCH    OVRCD,BEDS2200
.
          MATCH     PMBBD001,PMBDHOSP
          GOTO      BEDS2200 IF NOT EQUAL
.
          MATCH     PMBBD002,PMBDWARD
          GOTO      BEDS2200 IF NOT EQUAL
.
          MOVE      SAVESEQ1,PMBDSEQ1            * Update sequence number
          MOVE      SAVESEQ2,PMBDSEQ2            * in all bed records for
          MOVE      SAVESEQ3,PMBDSEQ3            * this ward
          MOVE      SAVESEQ4,PMBDSEQ4
          MOVE      SAVESEQ5,PMBDSEQ5
.
          CALL      UPPMBBD1                     * Update bed board ward
          GOTO      BEDS2100
.
BEDS2200  PACK      KEY20,PMBBD001,PMBBD002,SP70
          CALL      RDPMEPB1
          IF        OVRCD<>1
            GOTo      BEDS2310
          ENDIF
BEDS2300  CALL      RKPMEPB1
          BRANCH    OVRCD,BEDS2400
.
          MATCH     PMBBD001,PMEBHOSP
          GOTO      BEDS2400 IF NOT EQUAL
.
          MATCH     PMBBD002,PMEBWARD
          GOTO      BEDS2400 IF NOT EQUAL
.
BEDS2310  MOVE      SAVESEQ1,PMEBSEQ1            * Update sequence number
          MOVE      SAVESEQ2,PMEBSEQ2            * in all expected bed records
          MOVE      SAVESEQ3,PMEBSEQ3            * this ward
          MOVE      SAVESEQ4,PMEBSEQ4
          MOVE      SAVESEQ5,PMEBSEQ5
.
          CALL      UPPMEPB1                     * Update expected patient ward
          GOTO      BEDS2300
.
BEDS2400  MOVE      ZERO,EXIT
          GOTO      BEDS9999
.
BEDS3000  PACK      KEY20,PMBBD001,PMBBD002,SP70
          CALL      RSPMBBD1
          CALL      RKPMBBD1
          BRANCH    OVRCD,BEDS3200
.
          MATCH     PMBBD001,PMBDHOSP
          GOTO      BEDS3200 IF NOT EQUAL
.
          MATCH     PMBBD002,PMBDWARD
          GOTO      BEDS3200 IF NOT EQUAL
.
          GOTO      BEDS9300
.
BEDS3200  PACK      KEY20,PMBBD001,PMBBD002,PMBBD003,PMBBD004,PMBBD010,SP70
          CALL      DEPMBBD1                     * Delete bed board ward
.
BEDS3300  PACK      KEY20,PMBBD001,PMBBD002,PMBBD003,SP70
          CALL      RDPMEPB1
          IF        OVRCD<>1
            GOTO      BEDS3310
          ENDIF
          CALL      RKPMEPB1
          BRANCH    OVRCD,BEDS3900
.         
          MATCH     PMBBD001,PMEBHOSP
          GOTO      BEDS3900 IF NOT EQUAL
.         
          MATCH     PMBBD002,PMEBWARD
          GOTO      BEDS3900 IF NOT EQUAL
.         
BEDS3310  PACK      KEY20,PMEBHOSP,PMEBWARD,PMEBROOM,PMEBBEDD,PMEBADMN,SP70
          CALL      DEPMEPB1                     * Delete expected patient ward
          GOTO      BEDS3300
.
BEDS3900  MOVE      ZERO,EXIT
          GOTO      BEDS9999
.
BEDS4000  PACK      KEY20,PMBBD001,PMBBD002,PMBBD003,PMBBD004,PMBBD010,SP70
          CALL      RDPMBBD1
          COMPARE   ZERO,OVRCD
          GOTO      BEDS9100 IF EQUAL
.
          PACK      KEY20,PMBBD001,PMBBD002,SP70
          CALL      RDPMBBD1
          BRANCH    OVRCD,BEDS9200
.
          PACK      PMBDDATC,CCC,CYY,CMM,CDD
          REP       " 0",PMBDDATC
          CLOCK     TIME,PMBDTIMC
          MOVE      USERID,PMBDWEBC
.
          CALL      MVPMBD00
          CALL      WRPMBBD1                     * Write bed board bed
.
          CALL      CLPMSEPB
          PACK      KEY20,PMBBD001,PMBBD002,PMBBD003,PMBBD004,PMBBD010,SP70
          CALL      RAPMEPB1
          COMPARE   ZERO,OVRCD
          GOTO      BEDS4900 IF EQUAL
.
          MOVE      PMBDHOSP,PMEBHOSP
          MOVE      PMBDWARD,PMEBWARD
          MOVE      PMBDROOM,PMEBROOM
          MOVE      PMBDBEDD,PMEBBEDD
.
          MOVE      PMBDSEQ1,PMEBSEQ1
          MOVE      PMBDSEQ2,PMEBSEQ2
          MOVE      PMBDSEQ3,PMEBSEQ3
          MOVE      PMBDSEQ4,PMEBSEQ4
          MOVE      PMBDSEQ5,PMEBSEQ5
.
          PACK      PMEBDATC,CCC,CYY,CMM,CDD
          REP       " 0",PMEBDATC
          CLOCK     TIME,PMEBTIMC
          MOVE      USERID,PMEBWEBC
.
          CALL      WRPMEPB1                     * Write expected patient bed
.
BEDS4900  MOVE      ZERO,EXIT
          GOTO      BEDS9999
.
BEDS5000  PACK      KEY20,BEDBDKEY,SP70
          CALL      RDPMBBD1
          BRANCH    OVRCD,BEDS9200
.
          MATCH     PMBBD003,PMBDROOM       * Room has changed
          IF        !@EQUAL
            PACK      KEY20,PMBBD001,PMBBD002,PMBBD003,PMBBD004,PMBBD010,SP70
            CALL      RAPMBBD1
            COMPARE   ONE,OVRCD
            GOTO      BEDS9100 IF NOT EQUAL     
          ENDIF
.
          PACK      KEY20,BEDBDKEY,SP70
          CALL      RDPMBBD1
          BRANCH    OVRCD,BEDS9200
.
          PACK      PMBDDATU,CCC,CYY,CMM,CDD
          REP       " 0",PMBDDATU
          CLOCK     TIME,PMBDTIMU
          MOVE      USERID,PMBDWEBU
.
          CALL      MVPMBD00
          CALL      UPPMBBD1                     * Update bed board bed
.
          PACK      KEY20,PMBBD001,PMBBD002,SP70
          CALL      RDPMEPB1
          IF        OVRCD<>1
            MATCH     PMBDBEDD,PMEBBEDD
            GOTO      BEDS5500 IF EQUAL
          ENDIF
BEDS5400  CALL      RKPMEPB1
          BRANCH    OVRCD,BEDS5900
.
          MATCH     PMBDHOSP,PMEBHOSP
          GOTO      BEDS5900 IF NOT EQUAL
.
          MATCH     PMBDWARD,PMEBWARD
          GOTO      BEDS5900 IF NOT EQUAL
.
          MATCH     PMBDBEDD,PMEBBEDD
          GOTO      BEDS5400 IF NOT EQUAL
.
BEDS5500  PACK      SAVKEY20,PMEBHOSP,PMEBWARD,PMEBROOM,PMEBBEDD,PMEBADMN,SP70
          MOVE      PMBDROOM,PMEBROOM
.
          CALL      UPPMEPB1
.
          MOVE      SAVKEY20,KEY20
          CALL      RSPMEPB1
          GOTO      BEDS5400
.
BEDS5900  MOVE      ZERO,EXIT
          GOTO      BEDS9999
.
BEDS6000  PACK      KEY20,PMBBD001,PMBBD002,PMBBD003,PMBBD004,PMBBD010,SP70
          CALL      DEPMBBD1                     * Delete bed board bed bed
.
BEDS6100  PACK      KEY20,PMBBD001,PMBBD002,PMBBD003,PMBBD004,SP70
          CALL      RDPMEPB1
          IF        OVRCD<>1
            GOTO      BEDS6110
          ENDIF
          CALL      RKPMEPB1
          BRANCH    OVRCD,BEDS6900
.
          MATCH     PMBBD001,PMEBHOSP
          GOTO      BEDS6900 IF NOT EQUAL
.
          MATCH     PMBBD002,PMEBWARD
          GOTO      BEDS6900 IF NOT EQUAL
.
          MATCH     PMBBD003,PMEBROOM
          GOTO      BEDS6900 IF NOT EQUAL
.
          MATCH     PMBBD004,PMEBBEDD
          GOTO      BEDS6900 IF NOT EQUAL
.
BEDS6110  PACK      KEY20,PMEBHOSP,PMEBWARD,PMEBROOM,PMEBBEDD,PMEBADMN,SP70
          CALL      DEPMEPB1                     * Delete expected patient bed
          GOTO      BEDS6100
.
BEDS6900  MOVE      ZERO,EXIT
          GOTO      BEDS9999
.
BEDS7000  PACK      KEY20,PMBBD001,PMBBD002,PMBBD003,PMBBD004,PMBBD010,SP70
          CALL      RDPMBBD1
          IF        OVRCD=1
            CALL      CLPMSBBD
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Bed Board Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,BEDS9999
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      BEDS9999
.
BEDS9000  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      BEDS9999
.
BEDS9100  MOVE      "Bed Board Record Already Exists.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      BEDS9999
.
BEDS9200  MOVE      "Invalid Bed Board Record.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      BEDS9999
.
BEDS9300  MOVE      "Invalid Ward Deletion - Bed Records Exist.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      BEDS9999
.
BEDS9999  RETURN
.
.------------------------------------------------------------
. Bed Board Sequence Edit Checks
.------------------------------------------------------------
SEQE0000  PACK      KEY20,PMBBD001,SP70
          CALL      RSPMBBD1
SEQE1000  CALL      RKPMBBD1
          BRANCH    OVRCD,SEQE8000
.
          MATCH     PMBBD001,PMBDHOSP            * Same Hospital
          GOTO      SEQE8000 IF NOT EQUAL
.
          MATCH     PMBBD002,PMBDWARD            * Same Ward
          GOTO      SEQE1000 IF EQUAL
.
          MATCH     " 0",PMBBD005
          IF        !@EQUAL
            MATCH     PMBBD005,PMBDSEQ1          * Seq 1
            GOTO      SEQE9000 IF EQUAL
          ENDIF
.
          MATCH     " 0",PMBBD006
          IF        !@EQUAL
            MATCH     PMBBD006,PMBDSEQ2          * Seq 2
            GOTO      SEQE9100 IF EQUAL
          ENDIF
.
          MATCH     " 0",PMBBD007
          IF        !@EQUAL
            MATCH     PMBBD007,PMBDSEQ3          * Seq 3
            GOTO      SEQE9200 IF EQUAL
          ENDIF
.
          MATCH     " 0",PMBBD008
          IF        !@EQUAL
            MATCH     PMBBD008,PMBDSEQ4          * Seq 4
            GOTO      SEQE9300 IF EQUAL
          ENDIF
.
          MATCH     " 0",PMBBD009
          IF        !@EQUAL
            MATCH     PMBBD009,PMBDSEQ5          * Seq 5
            GOTO      SEQE9400 IF EQUAL
          ENDIF
.
          GOTO      SEQE1000
.
SEQE8000  MOVE      ZERO,EXIT
          GOTO      SEQE9999
.
SEQE9000  MOVE      "View 1 Sequence Number Already Exists.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SEQE9999
.
SEQE9100  MOVE      "View 2 Sequence Number Already Exists.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SEQE9999
.
SEQE9200  MOVE      "View 3 Sequence Number Already Exists.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SEQE9999
.
SEQE9300  MOVE      "View 4 Sequence Number Already Exists.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SEQE9999
.
SEQE9400  MOVE      "View 5 Sequence Number Already Exists.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SEQE9999
.
SEQE9999  RETURN
.------------------------------------------------------------
. Process Fields 
.------------------------------------------------------------
PROFLD00  BRANCH    REPORTNO,PROFLD10,PROFLD20,PROFLD30,PROFLD40,PROFLD50
.
          GOTO      PROFLD99 
.
PROFLD10  BRANCH    FLDSETNO,PROFLD11,PROFLD12
          GOTO      PROFLD99
.
PROFLD11  CALL      PMBD0000                     * Bed Board Table
          GOTO      PROFLD99
.
PROFLD12  CALL      GTAG0000                     * General Tags
          GOTO      PROFLD99
.
PROFLD20  BRANCH    FLDSETNO,PROFLD21
          GOTO      PROFLD99
.
PROFLD21  CALL      GTAG0000                     * General Tags
          GOTO      PROFLD99
.
PROFLD30  BRANCH    FLDSETNO,PROFLD31
          GOTO      PROFLD99
.
PROFLD31  CALL      GTAG0000                     * General Tags
          GOTO      PROFLD99
.
PROFLD40  BRANCH    FLDSETNO,PROFLD41,PROFLD42,PROFLD43,PROFLD44,PROFLD45:
                             PROFLD46
          GOTO      PROFLD99
.
PROFLD41  CALL      MASF0000
          GOTO      PROFLD99
.
PROFLD42  CALL      BREC0000
          GOTO      PROFLD99
.
PROFLD43  CALL      BKRX0000
          GOTO      PROFLD99
.
PROFLD44  CALL      MISF0000
          GOTO      PROFLD99
.
PROFLD45  CALL      VXTF0000
          GOTO      PROFLD99
.
PROFLD46  CALL      GTAG0000
          GOTO      PROFLD99
.
PROFLD50  BRANCH    FLDSETNO,PROFLD51,PROFLD52
          GOTO      PROFLD99
.
PROFLD51  CALL      GTAG0000
          GOTO      PROFLD99
.
PROFLD52  CALL      PMHR0000
          GOTO      PROFLD99
.
PROFLD99  RETURN
.
.------------------------------------------------------------
. General Bed Board Tags
.------------------------------------------------------------
GTAG0000  BRANCH    FLDITMNO,GTAG0010,GTAG0020,GTAG0030,GTAG0040,GTAG0050:
                             GTAG0060,GTAG0070,GTAG0080,GTAG0090,GTAG0100:
                             GTAG0110,GTAG0120,GTAG0130,GTAG0140,GTAG0150:
                             GTAG0160,GTAG0170,GTAG0180,GTAG0190,GTAG0200:
                             GTAG0210,GTAG0220,GTAG0230,GTAG0240,GTAG0250:
                             GTAG0260,GTAG0270,GTAG0280,GTAG0290,GTAG0300:
                             GTAG0310,GTAG0320,GTAG0330,GTAG0340,GTAG0350:
                             GTAG0360,GTAG0370,GTAG0380,GTAG0390,GTAG0400:
                             GTAG0410,GTAG0420,GTAG0430,GTAG0440,GTAG0450:
                             GTAG0460,GTAG0470,GTAG0480
.
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      GTAG9999
.
GTAG0010  CALL      SLST0000                * Bed Board Sequence List
          GOTO      GTAG9999
.
GTAG0020  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,GTAG0021,GTAG0022,GTAG0023
.
          PACK      KEY6,WARDCODE,SP70
          CALL      RDWARD1
          IF        OVRCD=1
            PACK      WBDESC,WARDCODE,SP70
          ENDIF
          WRITE     HTMLFILE;WBDESC;
          GOTO      GTAG9999
.
GTAG0021  WRITE     HTMLFILE;WARDCODE;                                
          GOTO      GTAG9999
.
GTAG0022  PACK      DFLTWARD,WARDCODE           * Ward Selection List 
          CALL      WSEL0000                                               
          GOTO      GTAG9999
.
GTAG0023  PACK      KEY6,WARDCODE,SP70
          CALL      RDWARD1
          IF        OVRCD=1
            PACK      WBDESC,WARDCODE,SP70
          ENDIF
.
          WRITE     HTMLFILE;WBDESC;
          GOTO      GTAG9999
.
GTAG0030  CALL      ROML0000                * Room List 
          GOTO      GTAG9999
.
GTAG0040  WRITE     HTMLFILE;HOSPCODE;                  
          GOTO      GTAG9999
.
GTAG0050  CALL      WBEDSL00                * Beds for a ward selection list
          GOTO      GTAG9999
.
GTAG0060  WRITE     HTMLFILE;BEDBDKEY; 
          GOTO      GTAG9999
.
GTAG0070  WRITE     HTMLFILE;PMBDHOSP,PMBDWARD,PMBDROOM,PMBDBEDD,PMBDCADM; 
          GOTO      GTAG9999
.
GTAG0080  MOVE      "CG",CATEGORY
          MOVE      EXTRSPEC,CODE
          CALL      CODITM00
          GOTO      GTAG9999
.
GTAG0090  CALL      LWRD0000                * List ward multi speciality
          GOTO      GTAG9999
.
GTAG0100  CALL      LSPE0000                * List ward multi speciality
          GOTO      GTAG9999
.
GTAG0110  WRITE     HTMLFILE;PRFORDER; 
          GOTO      GTAG9999
.
GTAG0120  CALL      DBED0000                * Display bed board
          GOTO      GTAG9999
.
GTAG0130  WRITE     HTMLFILE;SRCHSNAM; 
          GOTO      GTAG9999
.
GTAG0140  WRITE     HTMLFILE;SRCHGNAM; 
          GOTO      GTAG9999
.
GTAG0150  CALL      SBED0000                * Search bed board for patient
          GOTO      GTAG9999
.
GTAG0160  WRITE     HTMLFILE;BEDDCODE; 
          GOTO      GTAG9999
.
GTAG0170  CALL      BEDH0000             
          GOTO      GTAG9999
.
GTAG0180  PACK      KEY6,WARDCODE,SP70         * Display ward description
          CALL      RDWARD1 
          IF        OVRCD=1
            MOVE      WARDCODE,WBDESC
          ENDIF
.         
          WRITE     HTMLFILE;WBDESC;
.
          MATCH     SP70,BEDDCODE
          IF        @EQUAL
            GOTO      GTAG9999
          ENDIF
.
          IF        WBEDDESC > 0 
            PACK      KEY6,WARDCODE,BEDDCODE,SP70
            CALL      RDWARD1
            IF        OVRCD=1
              MOVE      BEDDCODE,WBDESC
              MOVE      BEDDCODE,PTWDSDES
            ENDIF
            IF        WBEDDESC = 2
              WRITE     HTMLFILE;" - ",PTWDSDES
            ELSE
              WRITE     HTMLFILE;" - ",WBDESC;
            ENDIF
          ELSE
            WRITE     HTMLFILE;" - ",BEDDCODE;
          ENDIF
.
          GOTO      GTAG9999
.
GTAG0190  CALL      BCLS0000
          GOTO      GTAG9999
.
GTAG0200  WRITE     HTMLFILE;UNALPTOT;
          GOTO      GTAG9999
.
GTAG0210  UNPACK    DIM127,D1,KEY1,DIM127          * I SRF 18693
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,GTAG0211,GTAG0212
.
          PACK      KEY6,PTMIXWRD,SP70
          CALL      RDWARD1
          BRANCH    OVRCD,GTAG9999
.
          WRITE     HTMLFILE;WBDESC;              * expected ward description
          GOTO      GTAG9999
.
GTAG0211  WRITE     HTMLFILE;PTMIXWRD;            * ward
          GOTO      GTAG9999
.
GTAG0212  PACK      DFLTWARD,PTMIXWRD
          CALL      WSEL0000 
          GOTO      GTAG9999
.
GTAG0220  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,GTAG0221
.
          WRITE     HTMLFILE;PTMIEBED;
          GOTO      GTAG9999
.
GTAG0221  MATCH     SP70,PTMIXWRD
          GOTO      GTAG9999 IF EQUAL
.
          PACK      KEY6,PTMIXWRD,SP70
          CALL      RDSWARD1
GTAG0222  CALL      RDKWARD1
          BRANCH    OVRCD,GTAG9999
.
          MATCH     PTMIXWRD,WWARD
          GOTO      GTAG9999 IF NOT EQUAL
.
          MATCH     SP70,WBED
          GOTO      GTAG0222 IF EQUAL
.
          MATCH     "1",PTWDBDST
          GOTO      GTAG0222 IF EQUAL
.
          IF        WACTIVE=ONE
            GOTO      GTAG0222
          ENDIF
.
          WRITE     HTMLFILE;"<option value=#"",WBED,"#"";
          MATCH     PTMIEBED,WBED
          IF        @EQUAL
            WRITE     HTMLFILE;" selected";
          ENDIF 
          IF        WBEDDESC > 0 
            IF        WBEDDESC = 2 
              WRITE     HTMLFILE;">",PTWDSDES
            ELSE
              WRITE     HTMLFILE;">",WBDESC
            ENDIF
          ELSE
            WRITE    HTMLFILE;">",WBED 
          ENDIF
          WRITE     HTMLFILE;"</option>"
.
          GOTO      GTAG0222
.
GTAG0230  UNPACK    DIM127,D1,KEY1,DIM127          * I SRF 18693
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,GTAG0231,GTAG0232
.         
          PACK      KEY6,BKRXPOWD,SP70
          CALL      RDWARD1
          BRANCH    OVRCD,GTAG9999
.         
          WRITE     HTMLFILE;WBDESC;              * expected ward description
          GOTO      GTAG9999
.         
GTAG0231  WRITE     HTMLFILE;BKRXPOWD;            * ward
          GOTO      GTAG9999
.         
GTAG0232  PACK      DFLTWARD,BKRXPOWD
          CALL      WSEL0000 
          GOTO      GTAG9999
.
GTAG0240  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,GTAG0241
.
          WRITE     HTMLFILE;BKRXPOBD;
          GOTO      GTAG9999
.
GTAG0241  MATCH     SP70,BKRXPOWD
          GOTO      GTAG9999 IF EQUAL
.
          PACK      KEY6,BKRXPOWD,SP70
          CALL      RDSWARD1
GTAG0242  CALL      RDKWARD1
          BRANCH    OVRCD,GTAG9999
.
          MATCH     BKRXPOWD,WWARD
          GOTO      GTAG9999 IF NOT EQUAL
.
          MATCH     SP70,WBED
          GOTO      GTAG0242 IF EQUAL
.
          IF        WACTIVE=ONE
            GOTO      GTAG0242
          ENDIF
.
          PACK      KEY5,QUOTE,WBED,QUOTE
.
          WRITE     HTMLFILE;"<option value=#"",WBED,"#"";
          MATCH     BKRXPOBD,WBED
          IF        @EQUAL
            WRITE     HTMLFILE;" selected";
          ENDIF
          IF        WBEDDESC > 0
            IF        WBEDDESC = 2 
              WRITE     HTMLFILE;">",PTWDSDES;
            ELSE
              WRITE     HTMLFILE;">",WBDESC;
            ENDIF
          ELSE
            WRITE     HTMLFILE;">",WBED;
          ENDIF
          WRITE     HTMLFILE;"</option>"
.
          GOTO      GTAG0242
.
GTAG0250  WRITE     HTMLFILE;ASTAT;
          GOTO      GTAG9999
.
GTAG0260  WRITE     HTMLFILE;BKRESTAT;
          GOTO      GTAG9999
.
GTAG0270  WRITE     HTMLFILE;THETFLAG;
          GOTO      GTAG9999
.
GTAG0280  CALL      SELV0000 
          GOTO      GTAG9999
.
GTAG0290  CALL      PREV0000
          GOTO      GTAG9999
.
GTAG0300  CALL      NEXT0000      
          GOTO      GTAG9999
.
GTAG0310  MOVE      "BV",CATEGORY
          MOVE      FILTRSTA,CODE
          CALL      CODITM00
          GOTO      GTAG9999
.
GTAG0320  WRITE     HTMLFILE;VIEWTYPE;
          GOTO      GTAG9999
.
GTAG0330  CALL      HLST0000                     * housekeeping list
          GOTO      GTAG9999
.
GTAG0340  PACK      DFLTWARD,FILTWARD
          CALL      WSEL0000
          GOTO      GTAG9999
.
GTAG0350  WRITE     HTMLFILE;BEDREQKY;
          GOTO      GTAG9999
.
GTAG0360  MOVE      "BV",CATEGORY
          MOVE      FILTCSTA,CODE
          CALL      CODITM00
          GOTO      GTAG9999
.
GTAG0370  CALL      SEXP0000                * Search expected patients
          GOTO      GTAG9999
.
GTAG0380  CALL      DUNL0000                * Display Unallocated bed board 
          GOTO      GTAG9999
.
GTAG0390  WRITE     HTMLFILE;EMPTONLY;
          GOTO      GTAG9999
.
GTAG0400  WRITE     HTMLFILE;TOMRFLAG; 
          GOTO      GTAG9999
.
GTAG0410  CALL      BEDE0000
          GOTO      GTAG9999
.
GTAG0420  UNPACK    DIM127,D1,KEY1,DIM127  
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,GTAG0421,GTAG0422
.
          PACK      KEY6,BKREWARD,SP70
          CALL      RDWARD1
          BRANCH    OVRCD,GTAG9999
.         
          WRITE     HTMLFILE;WBDESC;              * expected ward description
          GOTO      GTAG9999   
.         
GTAG0421  WRITE     HTMLFILE;BKREWARD;            * ward
          GOTO      GTAG9999
.             
GTAG0422  PACK      DFLTWARD,BKREWARD
          CALL      WSEL0000 
          GOTO      GTAG9999
.             
GTAG0430  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,GTAG0431
.         
          WRITE     HTMLFILE;BKREEBED;
          GOTO      GTAG9999
.         
GTAG0431  MATCH     SP70,BKREWARD
          GOTO      GTAG9999 IF EQUAL
.         
          PACK      KEY6,BKREWARD,SP70
          CALL      RDSWARD1
GTAG0432  CALL      RDKWARD1
          BRANCH    OVRCD,GTAG9999
.         
          MATCH     BKREWARD,WWARD
          GOTO      GTAG9999 IF NOT EQUAL
.         
          MATCH     SP70,WBED
          GOTO      GTAG0432 IF EQUAL
.         
          WRITE     HTMLFILE;"<option value=#"",WBED,"#"";
          MATCH     BKREEBED,WBED
          IF        @EQUAL
            WRITE     HTMLFILE;" selected";
          ENDIF 
          IF        WBEDDESC > 0 
            IF        WBEDDESC = 2 
              WRITE     HTMLFILE;">",PTWDSDES
            ELSE
              WRITE     HTMLFILE;">",WBDESC
            ENDIF
          ELSE
            WRITE    HTMLFILE;">",WBED 
          ENDIF
          WRITE     HTMLFILE;"</option>"
.
          GOTO      GTAG0432
.
GTAG0440  PACK      KEY10,USERID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,STDA9999
          WRITE     HTMLFILE;WBSEHOSP;           * Web User Hospital Code
          GOTO      GTAG9999
.         
GTAG0450  PACK      KEY10,USERID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,STDA9999
          WRITE     HTMLFILE;WBSEBEDV;           * Web User Bed Board View
          GOTO      GTAG9999
.
GTAG0460  CALL      BEDL0000                     * Bed on leave list          
          GOTO      GTAG9999
.
GTAG0470  CALL      EXPT0000                     * Expected Patient Table
          GOTO      GTAG9999
.
GTAG0480  WRITE     HTMLFILE;SJOGFLAG;
          GOTO      GTAG9999
.
GTAG9999  RETURN
.
.------------------------------------------------------------
. Next Day, Week, Month
.------------------------------------------------------------
NEXT0000  MOVE      TEMPLATE,F3
          MOVE      F3,KEY3
          REP       " 0",KEY3
.
          MOVE      REPORTNO,F2
          MOVE      F2,KEY2
          REP       " 0",KEY2
.
          WRITE     HTMLFILE;"patweb12.pbl":
                             "?reportno=",KEY2:
                             "&template=",KEY3:
                             "&frstdate=",NXTFDATE:
                             "&lastdate=",NXTLDATE:
                             "&viewtype=",VIEWTYPE:
                             "&filtward=",FILTWARD:
                             "&filtrsta=",FILTRSTA:
                             "&filtcsta=",FILTCSTA;
.
NEXT9999  RETURN
.
.------------------------------------------------------------
. Previous Day, Week, Month
.------------------------------------------------------------
PREV0000  MOVE      TEMPLATE,F3
          MOVE      F3,KEY3
          REP       " 0",KEY3
.
          MOVE      REPORTNO,F2
          MOVE      F2,KEY2
          REP       " 0",KEY2
.
          WRITE     HTMLFILE;"patweb12.pbl":
                             "?reportno=",KEY2:
                             "&template=",KEY3:
                             "&frstdate=",PREFDATE:
                             "&lastdate=",PRELDATE:
                             "&viewtype=",VIEWTYPE:
                             "&filtward=",FILTWARD:
                             "&filtrsta=",FILTRSTA:
                             "&filtcsta=",FILTCSTA;
.
PREV9999  RETURN
.
.------------------------------------------------------------
. General Bed Board Tags
.------------------------------------------------------------
SLST0000  PACK      KEY20,WBSEHOSP,SP70
          CALL      RSPMBBD1
SLST1000  CALL      RKPMBBD1
          BRANCH    OVRCD,SLST9999
.
          MATCH     WBSEHOSP,PMBDHOSP
          GOTO      SLST9999 IF NOT EQUAL
.
          MATCH     SP70,PMBDROOM
          GOTO      SLST1000 IF NOT EQUAL
.
          MATCH     SP70,PMBDBEDD
          GOTO      SLST1000 IF NOT EQUAL
.
          MATCH     SP70,PMBDCADM
          GOTO      SLST1000 IF NOT EQUAL
.
          MATCH     "~~W",PMBDWARD
          IF        @EQUAL
            MOVE      "Ward Not Specified",WBDESC
            GOTO      SLST1100
          ENDIF
.
          MATCH     "~~B",PMBDWARD
          IF        @EQUAL
            MOVE      "Bed Requests",WBDESC
            GOTO      SLST1100
          ENDIF
.
          PACK      KEY6,PMBDWARD,PMBDBEDD,SP70
          CALL      RDWARD1
          IF        OVRCD=1
            PACK      WBDESC,PMBDWARD,SLASH,PMBDBEDD,SP70
          ENDIF
.
SLST1100  CLEAR     HTMLLINK
          APPEND    "javascript:UpdateSeq('",HTMLLINK
          APPEND    PMBDHOSP,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    PMBDWARD,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    PMBDROOM,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    PMBDBEDD,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    PMBDCADM,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
          WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":
                             "#"",PMBDWARD,"#",":
                             "#"",PMBDROOM,"#",":
                             "#"",PMBDBEDD,"#",":
                             "#"",PMBDSEQ1,"#",":
                             "#"",PMBDSEQ2,"#",":
                             "#"",PMBDSEQ3,"#",":
                             "#"",PMBDSEQ4,"#",":
                             "#"",PMBDSEQ5,"#",":
                             "#"",WBDESC,"#",":
                             "#"",PMBDHOSP,"#");"
.
          GOTO      SLST1000
.
SLST9999  RETURN
.
.------------------------------------------------------------
. Ward Selection List
.------------------------------------------------------------
WSEL0000  PACK      KEY29,SP70
          IF        IBCNMHOS = 1
            PACK      KEY29,SP3,WBSEHOSP,SP70
          ENDIF
          CALL      RDSWARD7
WSEL0100  CALL      RDKWARD7
          BRANCH    OVRCD,WSEL9999
.
.                         if we have a bed number we have passed all the ward
.                         master records, so exit
          MATCH     SP70,WBED
          GOTO      WSEL9999 IF NOT EQUAL
.
          COMPARE   WACTIVE,ZERO                 *List only active wards
          GOTO      WSEL0100 IF NOT EQUAL
.
          IF        IBCNMHOS = 1
            MATCH      SP3,WBSEHOSP
            IF        !@EQUAL
              MATCH     WBSEHOSP,PTWDHOSN
              GOTO      WSEL9999 IF NOT EQUAL
            ENDIF
          ENDIF
.
.                       write the select option.
          WRITE     HTMLFILE;"<option value=#"",WWARD,"#"";
          MATCH     DFLTWARD,WWARD
          IF        @EQUAL
            WRITE     HTMLFILE;" selected";
          ENDIF
          WRITE     HTMLFILE;">",WBDESC;
.
          IF        IBCNMHOS = 1
            MATCH      SP3,WBSEHOSP
            IF        @EQUAL
              WRITE     HTMLFILE;" - ",PTWDHOSN;
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;"</option>"
          GOTO      WSEL0100
.
WSEL9999  RETURN
.
.------------------------------------------------------------
. Beds for a Ward Selection List
.------------------------------------------------------------
WBEDSL00  PACK      KEY6,WARDCODE,SP70
          CALL      RDSWARD1
WBEDSL10  CALL      RDKWARD1
          BRANCH    OVRCD,WBEDSL99
.
          MATCH     WARDCODE,WWARD
          GOTO      WBEDSL99 IF NOT EQUAL
.
          MATCH     SP70,WBED
          GOTO      WBEDSL10 IF EQUAL
.
          MOVE      WBDESC,KEY20
          PACK      KEY5,QUOTE,WBED,QUOTE
.
          WRITE     HTMLFILE;"<option value=",KEY5,">",KEY20,"   (",WWARD:
                             ")</option>";
.
          GOTO      WBEDSL10
.
WBEDSL99  RETURN
.
.------------------------------------------------------------
. Room/Bed List
.------------------------------------------------------------
ROML0000  PACK      KEY20,HOSPCODE,WARDCODE,SP70
          CALL      RSPMBBD1
ROML1000  CALL      RKPMBBD1
          BRANCH    OVRCD,ROML9999
.
          MATCH     HOSPCODE,PMBDHOSP
          GOTO      ROML9999 IF NOT EQUAL
.
          MATCH     WARDCODE,PMBDWARD
          GOTO      ROML9999 IF NOT EQUAL
.
          MATCH     SP70,PMBDBEDD
          GOTO      ROML1000 IF EQUAL
.
          PACK      KEY6,PMBDWARD,PMBDBEDD,SP70
          CALL      RDWARD1
          IF        OVRCD=1
            PACK      WBDESC,PMBDWARD,SLASH,PMBDBEDD,SP70
          ENDIF
.
          CLEAR     HTMLLINK
          APPEND    "javascript:UpdateRoom('",HTMLLINK
          APPEND    PMBDHOSP,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    PMBDWARD,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    PMBDROOM,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    PMBDBEDD,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    PMBDCADM,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
          WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":
                             "#"",PMBDHOSP,"#",":
                             "#"",PMBDWARD,"#",":
                             "#"",PMBDROOM,"#",":
                             "#"",PMBDBEDD,"#",":
                             "#"",WBDESC,"#");"
.
          GOTO      ROML1000
.
ROML9999  RETURN
.
.------------------------------------------------------------
. Multi Speciality Ward Maintenance Maintenance
.------------------------------------------------------------
MWRD0000  MATCH     SP70,UPDTTYPE
          GOTO      MWRD7000 IF EQUAL
.
          MATCH     "A",UPDTTYPE            * Add/Update
          GOTO      MWRD1000 IF EQUAL
.
          MATCH     "D",UPDTTYPE            * Delete
          GOTO      MWRD3000 IF EQUAL
.
          GOTO      MWRD9000
.
MWRD1000  MOVE      ZERO,ARRCNT
          PACK      KEY6,WARDCODE,SP70
          CALL      RDWARD1
          BRANCH    OVRCD,MWRD9100
.
          CALL      DWRD0000                * Delete multi speciality details
.
          CALL      MVPMMW00
          MOVE      SP70,PMMWBEDC
.
          PACK      KEY12,PMMWHOSP,PMMWWARD,PMMWBEDC,PMMWWCLS,SP70
          CALL      RAPMMSW1
          IF        OVRCD=1
            CALL      WRPMMSW1              * Master ward record
          ENDIF
.
MWRD1100  ADD       ONE,ARRCNT
          IF        ARRCNT>WARDSCNT
            GOTO      MWRD2000
          ENDIF
.
          MATCH     SP70,WARDSARR[ARRCNT]
          GOTO      MWRD1100 IF EQUAL
.
          MOVE      WARDSARR[ARRCNT],PMMWBEDC
.
          PACK      KEY12,PMMWHOSP,PMMWWARD,PMMWBEDC,PMMWWCLS,SP70
          CALL      RAPMMSW1
          IF        OVRCD=1
            CALL      WRPMMSW1
          ENDIF
.
          GOTO      MWRD1100
.
MWRD2000  MOVE      ZERO,EXIT
          GOTO      MWRD9999
.
MWRD3000  PACK      KEY6,WARDCODE,SP70
          CALL      RDWARD1
          BRANCH    OVRCD,MWRD9100
.
          CALL      DWRD0000                * Delete multi speciality details
.
          MOVE      ZERO,EXIT
          GOTO      MWRD9999
.
MWRD7000  PACK      KEY6,WARDCODE,SP70
          CALL      RDWARD1
          BRANCH    OVRCD,MWRD9100
.
          CLEAR     WEBTITLE
          MOVE      "Multi Speciality Ward Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,MWRD9999
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      MWRD9999
.
MWRD9000  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      MWRD9999
.
MWRD9100  MOVE      "Invalid Ward.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      MWRD9999
.
MWRD9999  RETURN
.------------------------------------------------------------
. Populate pmsmswaf file vars, bed value not populated
.------------------------------------------------------------
MVPMMW00  MOVE      PTWDHOSN,PMMWHOSP
          MOVE      WARDCODE,PMMWWARD
          MOVE      EXTRSPEC,PMMWWCLS
.
          SQUEEZE   PRFORDER
          MOVE      ZERO,F2
          MOVE      PRFORDER,F2
          MOVE      F2,PMMWORDP
.
          PACK      PMMWDATC,CCC,CYY,CMM,CDD
          REP       " 0",PMMWDATC
          CLOCK     TIME,PMMWTIMC
          MOVE      USERID,PMMWWEBC
          MOVE      SP70,PMMWWEBU
          MOVE      SP70,PMMWDATU
          MOVE      SP70,PMMWTIMU
          MOVE      SP70,PMMWSPAR
.
MVPMMW99  RETURN
.
.------------------------------------------------------------
. Delete multi speciality ward details
.------------------------------------------------------------
DWRD0000  PACK      KEY12,PTWDHOSN,WARDCODE,SP70
          CALL      RSPMMSW1
DWRD1000  CALL      RKPMMSW1
          BRANCH    OVRCD,DWRD9999
.
          MATCH     PTWDHOSN,PMMWHOSP
          GOTO      DWRD9999 IF NOT EQUAL
.
          MATCH     WARDCODE,PMMWWARD
          GOTO      DWRD9999 IF NOT EQUAL
.
          MATCH     EXTRSPEC,PMMWWCLS
          GOTO      DWRD1000 IF NOT EQUAL
.
          PACK      KEY12,PMMWHOSP,PMMWWARD,PMMWBEDC,PMMWWCLS,SP70
          CALL      DEPMMSW1
.
          GOTO      DWRD1000
.
DWRD9999  RETURN
.
.------------------------------------------------------------
. Ward Multi Speciality List
.------------------------------------------------------------
LWRD0000  PACK      KEY6,WARDCODE,SP70
          CALL      RDSWARD1
LWRD1000  CALL      RDKWARD1
          BRANCH    OVRCD,LWRD9999
.
          MATCH     WARDCODE,WWARD
          GOTO      LWRD9999 IF NOT EQUAL
.
          MATCH     SP70,WBED
          GOTO      LWRD1000 IF EQUAL
.
          WRITE     HTMLFILE;"<tr><td>",WBED,"</td>":
                             "<td>",WBDESC,"</td>":
                             "<td align=center>":
                             "<input type=checkbox name=wardsarr";
.
          PACK      KEY12,PTWDHOSN,WWARD,WBED,EXTRSPEC,SP70
          CALL      RDPMMSW1
          IF        OVRCD=0
            WRITE     HTMLFILE;" checked ";
          ENDIF
.
          WRITE     HTMLFILE;" value='",WBED,"'>":
                             "</td></tr>"
.
          GOTO      LWRD1000
.
LWRD9999  RETURN
.
.------------------------------------------------------------
. Ward Multi Speciality List
.------------------------------------------------------------
LSPE0000  PACK      KEY12,PTWDHOSN,WARDCODE,SP70
          CALL      RSPMMSW1
LSPE1000  CALL      RKPMMSW1
          BRANCH    OVRCD,LSPE9999
.         
          MATCH     PTWDHOSN,PMMWHOSP
          GOTO      LSPE9999 IF NOT EQUAL
.         
          MATCH     WARDCODE,PMMWWARD
          GOTO      LSPE9999 IF NOT EQUAL
.
          MATCH     SP70,PMMWBEDC
          GOTO      LSPE1000 IF NOT EQUAL
.
          MOVE      SP70,TDESC
          MATCH     SP70,PMMWWCLS
          IF        !@EQUAL
            PACK      KEY5,ANSC,ANSG,PMMWWCLS,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      PMMWWCLS,TDESC
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td nowrap><A HREF=#"":
                             "javascript:UpdateSpec('",PMMWWCLS:
                             "','",PMMWWARD,"','",PMMWORDP,"')#">":
                             "<img src='../images/MaintenanceIcon.gif'":
                             " class=Icon></a>":
                             TDESC,"</td>":
                             "<td>",PMMWORDP,"</td>":
                             "</td></tr>"
.
          GOTO      LSPE1000
.
LSPE9999  RETURN
.
.------------------------------------------------------------
. Bed Board View
.------------------------------------------------------------
BEDB0000  CLEAR     WEBTITLE
          MOVE      "Bed Board View",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,BEDB9999
.
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      BEDB9999
.
BEDB9999  RETURN
.
.------------------------------------------------------------
. Bed Board View
.------------------------------------------------------------
DBED0000  MOVE      ZERO,UNALPTOT                * Unallocated patient count
          MOVE      ZERO,ROOMCNTR
          MOVE      ZERO,WARDCNTR
.
          MATCH     Z70,BEDBHOSP
          IF        !@EQUAL
            MOVE      BEDBHOSP,VIEWHOSP
          ELSE
            IF        IBCNMHOS=1
              MOVE      WBSEHOSP,VIEWHOSP          * Default view hospital id
            ELSE                                   * from user
              MOVE      SP70,VIEWHOSP
            ENDIF
          ENDIF
.
          PACK      CURRDATE,CCC,CYY,CMM,CDD     * Set up current date
          REP       " 0",CURRDATE
.
          PACK      WORKDATE,CCC,CYY,CMM,CDD     * Set up current date
          REP       " 0",WORKDATE
          MOVE      CTIMEIS,WORKTIME             * Set up current time
.
          IF        TOMRFLAG=1
            MOVE      WORKDATE,TOMRDATE          * Set tomorrows date for 
            ADD       ONE,TOMRDATE               * tomorrows view
          ELSE
            MOVE      WORKDATE,TOMRDATE          * Set tomorrows date to today
          ENDIF
.
          MOVE      WORKDATE,YESTERDY
          SUB       ONE,YESTERDY
.
          IF        BEDBSEQN=0
            MOVE      ONE,VIEWSEQN                 * Default view sequence to 1
            MOVE      WBSEBEDV,VIEWSEQN
          ELSE
            MOVE      BEDBSEQN,VIEWSEQN
          ENDIF
.
          MOVE      SP70,SAVEWARD                * Ward being processed
          MOVE      ZERO,FRSTWARD                * Is this this first ward
.
          PACK      KEY22,VIEWHOSP,SP1,ONE,SP70
          BRANCH    VIEWSEQN,DBED0100,DBED0200,DBED0300,DBED0400,DBED0500
          GOTO      DBED9999
.
DBED0100  CALL      RSPMBBD3
          GOTO      DBED1000
.
DBED0200  CALL      RSPMBBD4
          GOTO      DBED1000
.
DBED0300  CALL      RSPMBBD5
          GOTO      DBED1000
.
DBED0400  CALL      RSPMBBD6
          GOTO      DBED1000
.
DBED0500  CALL      RSPMBBD7
          GOTO      DBED1000
.
DBED1000  BRANCH    VIEWSEQN,DBED1100,DBED1200,DBED1300,DBED1400,DBED1500
.
DBED1100  CALL      RKPMBBD3
          BRANCH    OVRCD,DBED9000
          GOTO      DBED1700   
.
DBED1200  CALL      RKPMBBD4
          BRANCH    OVRCD,DBED9000
          GOTO      DBED1700   
.
DBED1300  CALL      RKPMBBD5
          BRANCH    OVRCD,DBED9000
          GOTO      DBED1700   
.
DBED1400  CALL      RKPMBBD6
          BRANCH    OVRCD,DBED9000
          GOTO      DBED1700   
.
DBED1500  CALL      RKPMBBD7
          BRANCH    OVRCD,DBED9000
          GOTO      DBED1700   
.
DBED1700  IF        IBCNMHOS=1
            MATCH     VIEWHOSP,PMBDHOSP          * Same hospital id
            GOTO      DBED9000 IF NOT EQUAL
          ENDIF
.
          MATCH     "~~W",PMBDWARD
          GOTO      DBED1000 IF EQUAL
.
          MATCH     "~~B",PMBDWARD
          GOTO      DBED1000 IF EQUAL
.
          MATCH     SP70,SAVEWARD
          IF        @EQUAL
            MOVE      PMBDWARD,SAVEWARD
            GOTO      DBED2000
          ENDIF
.
          MATCH     SAVEWARD,PMBDWARD
          GOTO      DBED3000 IF EQUAL
.
          MOVE      PMBDWARD,SAVEWARD            * Save ward being processed
          MOVE      SP70,SAVEROOM                * Wards room being processed
          MOVE      SP70,ROOMGEND                * Room gender
.
DBED2000  IF        FRSTWARD=1                   
            WRITE     HTMLFILE;"</table></td>"
          ENDIF
.
          MOVE      ONE,FRSTWARD
.
          MOVE      SP70,SAVEROOM                * Wards room being processed
          MOVE      SP70,ROOMGEND                * Room gender
.
          PACK      KEY6,PMBDWARD,SP70           * Display ward code or
          CALL      RDWARD1                      * description
          IF        OVRCD=1
            MOVE      PMBDWARD,WBDESC
          ENDIF
          MOVE      WBDESC,SVWBDESC
.
          ADD       ONE,WARDCNTR
          MOVE      WARDCNTR,DIM3
          LJUSTIFY  DIM3          
          PACK      DIM3,DIM3,SP70
.                                                * Print ward heading section
          WRITE     HTMLFILE;"<td class=WardColumn><table class=BedBoard>":
                             "<tr>":
                             "<td colspan=2>":
                             "<div id=fldiv":
                             DIM3," style=#"position:relative;#">":
                             "<table class=BedBoard><tr>":
                             "<td class=HeadingBedCell bgcolor=#cccccc>":
                             "<img src='../images/EditIcon.gif'":
                             " alt='Modify Available Beds'":
                             " title='Modify Available Beds'":
                             " class=ListIcon onclick='ModBeds(#"":
                               PMBDWARD,"#");'/>":
                             "</td><td class=HeadingPatientCell bgcolor=#cccccc>":
                             "<a href='":
                             "javascript:WardOccupancyLink":
                             "(#"",PMBDWARD,"#")'>":
                             WBDESC,"</a>":
                             "</td></tr></table>":
                           "</div>":
                             "</td></tr>";
          GOTO      DBED1000
.
DBED3000  CALL      BCEL0000                     * Print Bed/Patient Cell
          GOTO      DBED1000
.
DBED9000  IF        FRSTWARD=1
            WRITE     HTMLFILE;"</table></td>"   * Print end of ward
          ENDIF
.
DBED9999  RETURN
.
.------------------------------------------------------------
. Search bed board for patient
.------------------------------------------------------------
SBED0000  MOVE      ZERO,BEDBVIEW               * Bed Board/Unallocated view
          UNPACK    DIM127,D1,BEDBVIEW,DIM127
.
          MOVE      ZERO,RECFOUND
          MATCH     SP70,SRCHSNAM                * Exit if no search criteria
          IF        @EQUAL | @EOS
            MATCH     SP70,SRCHGNAM
            GOTO      SBED9999 IF EQUAL
            GOTO      SBED9999 IF EOS
          ENDIF
.
          MOVE      ONE,VIEWSEQN                 * Default view sequence to 1
          MOVE      WBSEBEDV,VIEWSEQN
.
          WRITE     HTMLFILE;"<tr><td><hr></td></tr>";
.
          IF        IBCNMHOS=1
            PACK      KEY20,WBSEHOSP,SP70
          ELSE
            PACK      KEY20,SP70
          ENDIF
          CALL      RSPMBBD1
SBED1000  CALL      RKPMBBD1
          BRANCH    OVRCD,SBED9000
.
          IF        IBCNMHOS=1
            MATCH     WBSEHOSP,PMBDHOSP
            GOTO      SBED9000 IF NOT EQUAL
          ENDIF
.
. Search Regardless of Bed Board View
.
.         MOVE     ZERO,F2
.         LOAD     F2,VIEWSEQN,PMBDSEQ1,PMBDSEQ2,PMBDSEQ3,PMBDSEQ4,PMBDSEQ5
.         COMPARE  ZERO,F2                  * Skip patients who are in wards
.         GOTO     SBED1000 IF EQUAL        * that are no displayed on bed board
.
          MATCH     SP70,SRCHSNAM
          IF        !@EQUAL 
            MATCH     SRCHSNAM,PMBDSURN
            GOTO      SBED1000 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,SRCHGNAM
          IF        !@EQUAL & !@EOS
            MATCH     SRCHGNAM,PMBDGIVN
            GOTO      SBED1000 IF NOT EQUAL
          ENDIF
.
          MOVE      ONE,RECFOUND
          WRITE     HTMLFILE;"<tr><td>";
.
          MATCH     "1",BEDBVIEW
          GOTO      SBED1500 IF NOT EQUAL
.
          MATCH     "1",PTCNBBAP
          GOTO      SBED1500 IF NOT EQUAL
.
          MATCH     WORKDATE,PMBDTBDT
          GOTO      SBED1500 IF NOT EQUAL
.
          WRITE     HTMLFILE;"<a href='":
                             "javascript:UpdateExpected":
                             "(#"",PMBDURNO,"#",#"",PMBDCADM,"#")'>":
                              PMBDSURN,",",PMBDGIVN:
                             "</a>"
          GOTO      SBED2000
.
SBED1500  WRITE     HTMLFILE;PMBDSURN,",",PMBDGIVN;
.
SBED2000  PACK      KEY6,PMBDWARD,SP70
          CALL      RDWARD1
          IF        OVRCD=1
            MOVE      PMBDWARD,WBDESC
          ENDIF   
          MOVE     WINPUT,SVWINPUT
.
          WRITE    HTMLFILE;SP1,LBRACK,WBDESC;
.
          MATCH    SP70,PMBDBEDD
          IF       @EQUAL
            WRITE    HTMLFILE;RBRACK;
          ELSE
            IF        WBEDDESC > 0
              PACK      KEY6,PMBDWARD,PMBDBEDD,SP70
              CALL      RDWARD1
              IF        OVRCD=1
                MOVE      PMBDBEDD,WBDESC
                MOVE      PMBDBEDD,PTWDSDES
              ENDIF   
              IF       WBEDDESC = 2
                WRITE    HTMLFILE;SLASH,PTWDSDES,RBRACK;
              ELSE
                WRITE    HTMLFILE;SLASH,WBDESC,RBRACK;
              ENDIF
            ELSE
              WRITE    HTMLFILE;SLASH,PMBDBEDD,RBRACK;
            ENDIF
          ENDIF
.
          MATCH     SP70,PMBDBEDD
          IF        @EQUAL
            IF        SVWINPUT<>1
              WRITE     HTMLFILE;"&nbsp;<img src=#"../images/EditIcon.gif#"":
                                 " class=ListIcon onclick='BedAllocation(#"":
                                 PMBDURNO,"#",#"",PMBDCADM,"#");'/>";
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;"</td></tr>";
.
          GOTO      SBED1000
.
SBED9000  COMPARE   ZERO,RECFOUND
          GOTO      SBED9100 IF NOT EQUAL
.
          WRITE     HTMLFILE;"<tr><td align=center><b>End of Current Patient ":
                             "Search</b></td></tr>";
.
SBED9100  WRITE     HTMLFILE;"<tr><td><hr></td></tr>";
.
SBED9999  RETURN
.
.------------------------------------------------------------
. Display ward/bed history - Last 3 admissions that have
. occupied the ward/bed.  Each admission will only display
. once even if they have transferred in and out of a bed 
. multiple times.
.------------------------------------------------------------
BEDH0000  PACK      KEY6,WARDCODE,BEDDCODE,SP70         * Display ward code or
          CALL      RDWARD1                    * description
          IF        OVRCD=1
            PACK      WBDESC,WARDCODE,SP1,LBRACK,BEDDCODE,RBRACK
          ENDIF
.
          MATCH     SP70,PTWDCOMM
          IF        !@EQUAL
            WRITE     HTMLFILE;"<tr><td class=HeadingCell colspan=3>":
                               "Comments</td>":
                               "</tr>":
                               "<tr><td colspan=3>",PTWDCOMM,"</td>":
                               "</tr>";
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td class=HeadingCell>Patient</td>":
                             "<td class=HeadingCell>Discharge Date</td>":
                             "<td class=HeadingCell>Transfer Date</td></tr>"
.
          MOVE      ZERO,RECCOUNT
          MOVE      ZEROVISN,SAVEADM1
          MOVE      ZEROVISN,SAVEADM2
          MOVE      ZEROVISN,SAVEADM3
.
          PACK      KEY30,WARDCODE,BEDDCODE,Z70
          CALL      RDSTRAN1
BEDH1000  CALL      RDPTRAN1
          BRANCH    OVRCD,BEDH9999
.
          MATCH     WARDCODE,TWARD
          GOTO      BEDH9999 IF NOT EQUAL
.
          MATCH     BEDDCODE,TBED
          GOTO      BEDH9999 IF NOT EQUAL
.
          MATCH     WADMNO,TADMN            * Skip record if this is the current
          GOTO      BEDH1000 IF EQUAL       * patient in the ward/bed
.
          MATCH     WSTBY,TADMN             * Skip record if this is the 
          GOTO      BEDH1000 IF EQUAL       * patient on standby in the ward/bed
.
          PACK      KEY8,TURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,BEDH1000
.
          CALL      RDPMPX21
          BRANCH    OVRCD,BEDH1000
.
          IF        PCEASE=1
            MOVE      PDECDTE,AGEDATE            * Date of Death
          ELSE
            PACK      AGEDATE,CCC,CYY,CMM,CDD    * Today's date
          ENDIF
          REP       " 0",AGEDATE
          CALL      CALCAGE
          CALL      PATLN000
.
          MOVE      SP70,DISPDATE                * Clear discharge date
          MOVE      SP70,DISPDAT1                * Clear transfer out date
.
          MATCH     ANSD,TMOVE                   * Discharge record
          IF        @EQUAL
            PACK      KEY8,TADMN,SP70            * Get discharge date
            CALL      RDDSCH1
            IF        OVRCD<>1
              UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
              MOVE      CMON,F2
              LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT:
                                   NOV,DEC
              PACK      DISPDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
              GOTO      BEDH3000
            ENDIF
          ENDIF
.
          PACK      SAVKEY30,TWARD,TBED,TDATE,TTIME,TADMN,SP70
          MOVE      TADMN,DIM8  
.
          PACK      KEY30,TADMN,TDATE,TTIME,SP70
          CALL      RDSTRAN2
BEDH2000  CALL      RDKTRAN2
          BRANCH    OVRCD,BEDH2500
.
          MATCH     DIM8,TADMN
          GOTO      BEDH2500 IF NOT EQUAL
.
          MATCH     WWARD,TWARD
          IF        @EQUAL
            MATCH     WBED,TBED
            GOTO      BEDH2000 IF EQUAL
          ENDIF
.
          MATCH     ANSD,TMOVE                   * Discharge record
          IF        @EQUAL
            UNPACK    TDATE,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      DISPDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          ELSE
            UNPACK    TDATE,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      DISPDAT1,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          ENDIF
.
BEDH2500  PACK      KEY30,SAVKEY30,SP70
          CALL      RDTRAN1
          BRANCH    OVRCD,BEDH1000
.
BEDH3000  MATCH     TADMN,SAVEADM1          * Skip adm if already printed
          GOTO      BEDH1000 IF EQUAL
.
          MATCH     TADMN,SAVEADM2          * Skip adm if already printed
          GOTO      BEDH1000 IF EQUAL
.
          MATCH     TADMN,SAVEADM3          * Skip adm if already printed
          GOTO      BEDH1000 IF EQUAL
.
          MATCH     SAVEADM1,ZEROVISN
          IF        @EQUAL
            MOVE      TADMN,SAVEADM1
          ELSE
            MATCH     SAVEADM2,ZEROVISN
            IF        @EQUAL
              MOVE      TADMN,SAVEADM2
            ELSE
              MATCH     SAVEADM3,ZEROVISN
              IF        @EQUAL
                MOVE      TADMN,SAVEADM3
              ENDIF
            ENDIF
          ENDIF
.
          ADD       ONE,RECCOUNT
.
          WRITE     HTMLFILE;"<tr><td>",FORMATNM,"</td>":
                             "<td>",DISPDATE,"</td>":
                             "<td>",DISPDAT1,"</td></tr>";
.
          COMPARE   THREE,RECCOUNT
          GOTO      BEDH1000 IF NOT EQUAL
.
BEDH9999  RETURN
.
.------------------------------------------------------------
. Display Expected Patients
.------------------------------------------------------------
BEDE0000  IF        IBCNMHOS=1
            MOVE      WBSEHOSP,VIEWHOSP          * Default view hospital id
          ELSE                                   * from user
            MOVE      SP70,VIEWHOSP
          ENDIF
.
          PACK      WORKDATE,CCC,CYY,CMM,CDD     * Set up current date
          REP       " 0",WORKDATE
          MOVE      CTIMEIS,WORKTIME             * Set up current time
.
          IF        TOMRFLAG=1
            MOVE      WORKDATE,TOMRDATE          * Set tomorrows date for
            ADD       ONE,TOMRDATE               * tomorrows view
          ELSE
            MOVE      WORKDATE,TOMRDATE          * Set tomorrows date to today
          ENDIF
.
          MOVE      WORKDATE,YESTERDY  
          SUB       ONE,YESTERDY
.
          PACK      KEY6,WARDCODE,BEDDCODE,SP70         * Display ward code or
          CALL      RDWARD1                    * description
          IF        OVRCD=1
            PACK      WBDESC,WARDCODE,SP1,LBRACK,BEDDCODE,RBRACK
            MOVE      ZEROVISN,WSTBY
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td class=HeadingCell>Patient</td>":
                             "<td class=HeadingCell>Admission Date":
                             "<td class=HeadingCell>Type":
                             "</td></tr>"
.
          PACK      KEY36,VIEWHOSP,WARDCODE,SP70
          CALL      RSPMEPB8
BEDE1000  CALL      RKPMEPB8
          BRANCH    OVRCD,BEDE9999
.           
          IF        IBCNMHOS=1
            MATCH     VIEWHOSP,PMEBHOSP
            GOTO      BEDE9999 IF NOT EQUAL
          ELSE
            MATCH     SP70,PMEBHOSP
            GOTO      BEDE9999 IF NOT EQUAL
          ENDIF
.         
          MATCH     WARDCODE,PMEBWARD
          GOTO      BEDE9999 IF NOT EQUAL
.
          MATCH     BEDDCODE,PMEBBEDD
          GOTO      BEDE1000 IF NOT EQUAL
.
          IF        TOMRFLAG=1
            MATCH     WORKDATE,PMEBEDAT
            GOTO      BEDE1000 IF LESS
.
            MATCH     PMEBEDAT,TOMRDATE
            GOTO      BEDE1000 IF LESS
          ELSE
            MATCH     WORKDATE,PMEBEDAT
            GOTO      BEDE2000 IF EQUAL
            MATCH     YESTERDY,PMEBEDAT
            GOTO      BEDE1000 IF NOT EQUAL
          ENDIF
.
BEDE2000  MOVE      PMEBURNO,URNUMBER
          MOVE      PMEBADMN,ADMISSNO
.
          PACK      KEY8,PMEBURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,BEDE1000
.
          CALL      RDPMPX21
          BRANCH    OVRCD,BEDE1000
.
          IF        PCEASE=1
            MOVE      PDECDTE,AGEDATE            * Date of Death
          ELSE
            PACK      AGEDATE,CCC,CYY,CMM,CDD    * Today's date
          ENDIF
          REP       " 0",AGEDATE
          CALL      CALCAGE
          CALL      PATLN000
.
          UNPACK    PMEBEDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DISPDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          MOVE      SP70,TYPEDESC
.
          MATCH     PMEBADMN,WSTBY
          IF        @EQUAL
            MOVE      "Standby",TYPEDESC
            GOTO      BEDE5000
          ENDIF
.
          MOVE      ZERO,POSTOPFL
          MATCH     PMEBWARD,PMEBPOWD
          IF        @EQUAL
            MATCH     PMEBBEDD,PMEBPOBD
            IF        @EQUAL
              MATCH     SP70,PMEBTBDT
              IF        @EQUAL
                MOVE      ONE,POSTOPFL      * Post op record
              ELSE
                MATCH     WORKDATE,PMEBTBDT
                IF        @EQUAL
                  MOVE      ONE,POSTOPFL      * Post op record
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
          IF          POSTOPFL=1
            MOVE      "Expected Post-Op",TYPEDESC
            GOTO      BEDE5000
          ENDIF
.
          MATCH     "1",PMEBBRPT
          IF        @EQUAL
            MOVE      "Bed Request",TYPEDESC
            GOTO      BEDE5000
          ENDIF
.
          MOVE      "Expected Admission",TYPEDESC
.
BEDE5000  WRITE     HTMLFILE;"<tr><td>",FORMATNM,"</td>":
                             "<td>",DISPDATE," at ",PMEBETIM,"</td>":
                             "<td>",TYPEDESC,"</td></tr>";
.
          GOTO      BEDE1000
.
BEDE9999  RETURN
.
.------------------------------------------------------------
. Display ward/bed closure details
.------------------------------------------------------------
BCLS0000  PACK      KEY6,WARDCODE,BEDDCODE,SP70         * Display ward code or
          CALL      RDWARD1                    * description
          IF        OVRCD=1
            PACK      WBDESC,WARDCODE,SP1,LBRACK,BEDDCODE,RBRACK
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td class=HeadingCell>Closed From</td>":
                             "<td class=HeadingCell>Closed To</td></tr>"
.
          PACK      CURRDATE,CCC,CYY,CMM,CDD     * Set up current date
          REP       " 0",CURRDATE
.
          PACK      WORKDATE,CCC,CYY,CMM,CDD     * Set up current date
          REP       " 0",WORKDATE
          MOVE      CTIMEIS,WORKTIME             * Set up current time
.
          MOVE      WORKDATE,YESTERDY  
          SUB       ONE,YESTERDY
.
          IF        TOMRFLAG=1
            MOVE      WORKDATE,TOMRDATE          * Set tomorrows date for
            ADD       ONE,TOMRDATE               * tomorrows view
          ELSE
            MOVE      WORKDATE,TOMRDATE          * Set tomorrows date to today
          ENDIF
.
          PACK      KEY22,WARDCODE,BEDDCODE,WORKDATE,Z70
          CALL      RSPMBDC1
BCLS1000  CALL      RPPMBDC1
          BRANCH    OVRCD,BCLS9999
.
          MATCH     PMBCWARD,WARDCODE            * Same Ward?
          GOTO      BCLS9999 IF NOT EQUAL        * No, exit.
.
          MATCH     PMBCBED,BEDDCODE             * Same Bed?
          GOTO      BCLS9999 IF NOT EQUAL        * No, exit.
.
          MATCH     SP70,PMBCFRDT                * Is from date blank?
          GOTO      BCLS1000 IF EQUAL            * Yes, get next record.
.
          MATCH     SP70,PMBCFRTM                * Is from time blank?
          GOTO      BCLS1000 IF EQUAL            * Yes, get next record.
.
          MATCH     SP70,PMBCTODT                * Is to date blank?
          GOTO      BCLS1000 IF EQUAL            * Yes, get next record.
.
          MATCH     SP70,PMBCTOTM                * Is to time blank?
          GOTO      BCLS1000 IF EQUAL            * Yes, get next record
.
          MATCH     "1",PMBCFRPR                 * Closure processed
          GOTO      BCLS1000 IF NOT EQUAL
.
          MOVE      SP70,DISPDATE
          UNPACK    PMBCFRDT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DISPDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          MOVE      SP70,DISPDAT1
          UNPACK    PMBCTODT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DISPDAT1,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          PACK      KEY5,ANSB,ANSU,PMBCREAS,SP70
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE      PMBCREAS,TDESC
            MOVE      SP70,TCINDC2
          ENDIF
.        
          WRITE     HTMLFILE;"<tr><td>",DISPDATE," at ",PMBCFRTM,"</td>":
                             "<td>",DISPDAT1," at ",PMBCTOTM,"</td></tr>";
.
          WRITE     HTMLFILE;"<tr><td class=HeadingCell colspan=2>Reason</td>":
                             "<tr>":
                             "<tr><td colspan=2>",TDESC,"</td></tr>";
.
          MATCH     "F",TCINDC2
          IF        @EQUAL
            WRITE     HTMLFILE;"<tr><td colspan=2>",PMBCFTRC,"</td></tr>";
          ENDIF
.
          MATCH     "H",TCINDC1
          GOTO      BCLS9999 IF NOT EQUAL
.
          MOVE      SP70,DOCFNAME
          PACK      KEY10,PMBCDOCT,SP70
          CALL      RDPMHCP1
          BRANCH    OVRCD,BCLS9999
.
          MOVE      PMHCTITL,FMTTITLE
          MOVE      PMHCGNAM,FMTGNAME
          MOVE      PMHCSNAM,FMTSNAME
          CALL      DOCNM000
.
          WRITE     HTMLFILE;"<tr><td colspan=2>",DOCFNAME,"</td></tr>";
.
BCLS9999  RETURN
.
.------------------------------------------------------------
. Format the patient name
.------------------------------------------------------------
GETPAT00  MOVE      SP70,FORMATNM
          MOVE      ONE,NOUPDATE                 * Update of Exp/Post-op W/B
.
          PACK      KEY8,URNUMBER,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,GETPAT90
.
          CALL      RDPMPX21
          IF        OVRCD=1
            CALL      CLPMSPX2
          ENDIF
.
          REP       "'`@ * / : \ $ % & # ",PGNAME
          REP       "'`@ * / : \ $ % & # ",PSNAME
.
          CALL      RDPMPX21
          BRANCH    OVRCD,GETPAT90
.
          IF        PCEASE=1
            MOVE      PDECDTE,AGEDATE            * Date of Death
          ELSE
            PACK      AGEDATE,CCC,CYY,CMM,CDD    * Today's date
          ENDIF
          REP       " 0",AGEDATE
          CALL      CALCAGE
          CALL      FMTA0000
.
          PACK      KEY8,ADMISSNO,SP70
          CALL      RDVISA1
          IF        OVRCD=1
            CALL       CLPATVIS
            CALL       CLPMSVX1
            GOTO       GETPAT10        * Do not perform read of extn file
          ENDIF
.
          CALL      RDPMVX11
          IF        OVRCD=1
            CALL       CLPMSVX1
          ENDIF
.
GETPAT10  PACK      KEY8,ADMISSNO,SP70
          CALL      RDMISS1
          IF        OVRCD=1
            CALL      CLPATMIS
          ELSE
            IF        ASTAT=1 | ASTAT=2
              MOVE      ZERO,NOUPDATE
            ENDIF
          ENDIF
.
          PACK      KEY8,ADMISSNO,SP70
          CALL      RDPMWOR1
          IF        OVRCD=1
            CALL      CLPMWO00
          ENDIF
.
          PACK      KEY8,ADMISSNO,SP70
          CALL      RDBKREC1
          IF        OVRCD=1
            CALL      CLBOKRC1
            CALL      CLBOKRX1
            GOTO      GETPAT80         * Do not perform read of extn file 
          ELSE
            IF        BKRESTAT=0 | BKRESTAT=1 | BKRESTAT=3
              MOVE      ZERO,NOUPDATE
            ENDIF
          ENDIF
.
          PACK      KEY8,ADMISSNO,SP70
          CALL      RDBKRX11
          IF        OVRCD=1
            CALL      CLBOKRX1
          ENDIF
.
GETPAT80  MOVE      ZERO,EXIT
          GOTO      GETPAT99
.
GETPAT90  MOVE      ONE,EXIT
          GOTO      GETPAT99
.
GETPAT99  RETURN
.
.------------------------------------------------------------
. Print the patient cell
.------------------------------------------------------------
BCEL0000  PACK      KEY6,PMBDWARD,PMBDBEDD,SP70  * Display ward code or
          CALL      RDWARD1                      * description
          BRANCH    OVRCD,BCEL9999               
.         
          BRANCH    WACTIVE,BCEL9999
.         
          MATCH     "1",PTWDBDST                 * Bed Closed
          IF        @EQUAL
            WRITE     HTMLFILE;"<tr class=#"BedRow Blocked#">":
                               "<td class=#"BedCell Blocked#">";
.
            IF        WBEDDESC > 0
              IF        WBEDDESC = 2
                WRITE     HTMLFILE;PTWDSDES;
              ELSE
                WRITE     HTMLFILE;WBDESC;
              ENDIF
            ELSE
              WRITE     HTMLFILE;PMBDBEDD;
            ENDIF
.
            WRITE     HTMLFILE;"</td><td class=#"PatientCell Blocked#">":
                               "<a title=#"",SVWBDESC,"#" href='":
                               "javascript:ViewBlocked":
                       "(#"",PMBDWARD,"#",#"",PMBDBEDD,"#",#"",TOMRFLAG,"#")'>":
                               "Blocked</a></td></tr>";
            GOTO      BCEL9999
          ELSE
            MATCH     SP70,PMBDCADM
            IF        @EQUAL
              WRITE     HTMLFILE;"<tr class=#"BedRow Available#">";
            ELSE
              WRITE     HTMLFILE;"<tr class=#"BedRow#">";
            ENDIF
          ENDIF
.
          CALL      BRMG0000                              * Room Gender
.
          WRITE     HTMLFILE;"<td class=#"BedCell ";      * Display Bed Cell
.
          WRITE     HTMLFILE;ROOMCLAS;                    * Display Room Class
.
          MATCH     SP70,PMBDCADM
          IF        !@EQUAL
            MATCH     ANSM,PMBDGEND
            IF        @EQUAL
              WRITE     HTMLFILE;" BlueBackground";
            ELSE
              MATCH     ANSF,PMBDGEND
              IF        @EQUAL
                WRITE     HTMLFILE;" PinkBackground";
              ELSE
                WRITE     HTMLFILE;" GreenBackground";
              ENDIF
            ENDIF
          ELSE
            MATCH     SP70,PMBDROOM
            IF        !@EQUAL
              MATCH     ANSM,ROOMGEND
              IF        @EQUAL
                WRITE     HTMLFILE;" BlueBackground";
              ELSE
                MATCH     ANSF,ROOMGEND
                IF        @EQUAL
                  WRITE     HTMLFILE;" PinkBackground";
                ELSE
                  WRITE     HTMLFILE;" GreenBackground";
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;"#">";
.
          MATCH     SP70,PMBDCADM                * Bed Cell Links
          IF        @EQUAL
            IF        WINPUT<>1
              WRITE     HTMLFILE;"<a title=#"",SVWBDESC,"#" href='":
                                 "javascript:ViewBedHistory":
                                 "(#"",PMBDWARD,"#",#"",PMBDBEDD,"#")'>";
              IF        WBEDDESC > 0
                IF        WBEDDESC = 2
                  WRITE     HTMLFILE;PTWDSDES,"</a>";
                ELSE
                  WRITE     HTMLFILE;WBDESC,"</a>";
                ENDIF
              ELSE
                WRITE     HTMLFILE;PMBDBEDD,"</a>";
              ENDIF
            ENDIF
          ELSE
            MATCH     SP70,PMBDBEDD
            IF        @EQUAL
              IF        WINPUT<>1
                ADD       ONE,UNALPTOT
                WRITE     HTMLFILE;"<img alt=#"",SVWBDESC,"#"":
                                   " title=#"",SVWBDESC,"#"":
                                   " src='../images/EditIcon.gif'":
                                   " class=ListIcon onclick='BedAllocation(#"":
                                   PMBDURNO,"#",#"",PMBDCADM,"#");'/>";
              ENDIF
            ELSE
              WRITE     HTMLFILE;"<a title=#"",SVWBDESC,"#" href='":
                                 "javascript:ViewBedHistory":
                                 "(#"",PMBDWARD,"#",#"",PMBDBEDD,"#")'>";
              IF        WBEDDESC > 0
                IF        WBEDDESC = 2
                  WRITE     HTMLFILE;PTWDSDES,"</a>";
                ELSE
                  WRITE     HTMLFILE;WBDESC,"</a>";
                ENDIF
              ELSE
                WRITE     HTMLFILE;PMBDBEDD,"</a>";
              ENDIF
.
            ENDIF
          ENDIF
.
          PACK      KEY25,PMBDHOSP,PMBDWARD,PMBDBEDD,Z70
          CALL      RSPMHRD1
          CALL      RPPMHRD1
          BRANCH    OVRCD,BCEL0300
.
          MATCH     PMBDHOSP,PMHRHOSP
          GOTO      BCEL0300 IF NOT EQUAL
.
          MATCH     PMBDWARD,PMHRWARD
          GOTO      BCEL0300 IF NOT EQUAL
.
          MATCH     PMBDBEDD,PMHRBEDD
          GOTO      BCEL0300 IF NOT EQUAL
.
          MATCH     SP70,PMHRCSTA
          IF        !@EQUAL
            PACK      KEY5,ANSB,ANSV,PMHRCSTA,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              MATCH     ANSC,TCINDC1                  * Is the bed clean
              IF        !@EQUAL
                WRITE     HTMLFILE;"<br>",PMHRCSTA;
              ENDIF
            ENDIF
          ELSE
            PACK      KEY5,ANSB,ANSV,PMHRRSTA,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              MATCH     ANSC,TCINDC1                  * Is the bed clean
              IF        !@EQUAL
                WRITE     HTMLFILE;"<br>",PMHRRSTA;
              ENDIF
            ENDIF
          ENDIF
.
BCEL0300  WRITE     HTMLFILE;"</td>";
.
.    Patient Cell
.
          CALL      GEXP0000                * Get expected patient detail
.
          WRITE     HTMLFILE;"<td class=#"PatientCell";
.
          WRITE     HTMLFILE;ROOMCLAS;                    * Display Room Class
.
          MATCH     SP70,PMBDCADM           * Current patient details
          IF        @EQUAL
            WRITE     HTMLFILE;"#">";
            MOVE      ZERO,OVERLAPF           * Reset overlap flag
            GOTO      BCEL3100
          ENDIF
.
          MOVE      PMBDURNO,URNUMBER
          MOVE      PMBDCADM,ADMISSNO
          CALL      GETPAT00
          IF        EXIT=1
            WRITE     HTMLFILE;"#">";
            GOTO      BCEL3100
          ENDIF
.
          MOVE      ZERO,OVERLAPF           * Reset overlap flag
.
          MATCH     SP70,SAVKEY20           * No expected patient
          IF        @EQUAL
            MATCH     WORKDATE,PMWOEDAT
            IF        @EQUAL
              MATCH     SP70,PMWOEDTM
              IF        !@EQUAL
                WRITE     HTMLFILE;" Available";
              ENDIF
            ENDIF
            MATCH     SP70,PMWOEDAT
            IF        !@EQUAL
              MATCH     WORKDATE,PMWOEDAT
              IF        @LESS
                WRITE     HTMLFILE;" PastDischargeDate";
              ENDIF
            ENDIF
          ELSE
            IF        STBYFLAG=1
              WRITE     HTMLFILE;" RedBackground";
              MOVE      ONE,OVERLAPF
              GOTO      BCEL1000
            ENDIF
.
            IF        STBYFLAG=2
              GOTO      BCEL1000
            ENDIF
.
            MATCH     WORKDATE,PMWOEDAT
            IF        @EQUAL
              MATCH     SP70,PMWOEDTM
              IF        !@EQUAL
                WRITE     HTMLFILE;" YellowBackground";
              ELSE
                WRITE     HTMLFILE;" RedBackground";
                MOVE      ONE,OVERLAPF
              ENDIF
            ELSE
              WRITE     HTMLFILE;" RedBackground";
              MOVE      ONE,OVERLAPF
            ENDIF
          ENDIF
.
BCEL1000  WRITE     HTMLFILE;"#">";
.
          CALL      STAC0000               * Current patient onhover details
.
          PACK      KEY5,ANSC,ANSC,ACARE,SP70
          CALL      RDCODE1
          IF        OVRCD<>1
            MATCH     ANSD,TCINDC11         * Don't display on bed board
            GOTO      BCEL3100 IF EQUAL
          ENDIF
.
          CALL      PATA0000                * Display alert icons
.
          CALL      PATFLD00                * Use standard Patient Folder sub
          MOVE      KEY3,FOLDERSF           * and store suffix
          MOVE      PGNAME,DIM1             * Given name initial
.
          STRIP     PSNAME
          WRITE     HTMLFILE;"<br /><span class=#"Black Bold#">":
                         "<img src='../images/PatientFolder",FOLDERSF,".gif'":
                " onclick='LinktoPatient(#"",URNUMBER,"#",#"",ADMISSNO,"#");' ":
                             "onmouseover='show_it(#"",STATWIND,"#");'":
                             " onmouseout='hide_it();'":
                             " class=ListIcon \>";
.
          MATCH     "1",PTCNBBAP
          IF        @EQUAL
            MATCH     WORKDATE,PMBDTBDT
            IF        @EQUAL
              WRITE     HTMLFILE;"<a href='":
                               "javascript:UpdateExpected":
                               "(#"",URNUMBER,"#",#"",ADMISSNO,"#")'>":
                                 *+,PSNAME,DOT,DIM1:
                               "</a>"
            ELSE    
              WRITE     HTMLFILE;*+,PSNAME,DOT,DIM1;
            ENDIF   
          ELSE
            WRITE     HTMLFILE;*+,PSNAME,DOT,DIM1;
          ENDIF     
.
          MATCH     WORKDATE,PMWOEDAT
          IF        @EQUAL
            MATCH     SP70,PMWOEDTM
            IF        !@EQUAL
              MOVE      PMWOEDTM,D5
              WRITE     HTMLFILE;SP1,"(",D5,")";
            ENDIF
          ENDIF 
.
          MATCH     SP70,PMBDEWRD
          IF        !@EQUAL
            SQUEEZE   PMBDEWRD
.
            IF        OVERLAPF=1
              WRITE     HTMLFILE;"<span class=#"White Bold#">";
            ELSE
              WRITE     HTMLFILE;"<span class=#"Red Bold#">";
            ENDIF
.
            MATCH     SP70,PMBDEBED
            IF        @EQUAL
              WRITE     HTMLFILE;*+,"(",PMBDEWRD,")";
            ELSE
              WRITE     HTMLFILE;*+,"(",PMBDEWRD,"/",PMBDEBED,")";
            ENDIF
            WRITE     HTMLFILE;"</span>";
            RESET     PMBDEWRD
          ENDIF
.
          MATCH     SP70,PMBDPOWD
          IF        !@EQUAL
            SQUEEZE   PMBDPOWD
            WRITE     HTMLFILE;"<span class=#"Blue#">";
            MATCH     SP70,PMBDPOBD
            IF        @EQUAL
              WRITE     HTMLFILE;*+,"(",PMBDPOWD,")";
            ELSE
              WRITE     HTMLFILE;*+,"(",PMBDPOWD,"/",PMBDPOBD,")";
            ENDIF
            WRITE     HTMLFILE;"</span>";
            RESET     PMBDPOBD
          ENDIF
.
          WRITE     HTMLFILE;"<br /></span>";
.
          RESET     PSNAME
.
BCEL3100  MATCH     SP70,PMBDBEDD           * Expected patient details
          GOTO      BCEL5900 IF EQUAL
.
          PACK      KEY6,PMBDWARD,PMBDBEDD,SP70
          CALL      RDWARD1                      
          BRANCH    OVRCD,BCEL3500
.
BCEL3500  MATCH     SP70,SAVKEY20           * Is there an expected patient
          GOTO      BCEL5900 IF EQUAL
.
          MOVE      SAVKEY20,KEY20          * Restore correct record
          CALL      RDPMEPB1
          BRANCH    OVRCD,BCEL5900
.
          MOVE      PMEBURNO,URNUMBER
          MOVE      PMEBADMN,ADMISSNO
          CALL      GETPAT00
          BRANCH    EXIT,BCEL5900
.
          CALL      STAE0000                * Expected patient onhover details
.
          MATCH     SP70,PMBDCADM
          IF        @EQUAL
            CALL      EXPA0000                * Display expected pat alert icons
            WRITE     HTMLFILE;"<br />"
          ENDIF
. 
          CALL      PATFLD00                * Use standard Patient Folder sub
          MOVE      KEY3,FOLDERSF           * and store suffix
          MOVE      PGNAME,DIM1             * Given name initial
.
          MOVE      ZERO,POSTOPFL
          MATCH     PMEBWARD,PMEBPOWD
          IF        @EQUAL
            MATCH     PMEBBEDD,PMEBPOBD
            IF        @EQUAL
              MATCH     SP70,PMEBTBDT
              IF        @EQUAL
                MOVE      ONE,POSTOPFL      * Post op record
              ELSE
                MATCH     WORKDATE,PMEBTBDT
                IF        @EQUAL
                  MOVE      ONE,POSTOPFL      * Post op record
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
          IF        POSTOPFL=1
            WRITE     HTMLFILE;"<span class=#"Blue Bold#">";
          ELSE
            IF        OVERLAPF=1
              WRITE     HTMLFILE;"<span class=#"White Bold#">";
            ELSE
              WRITE     HTMLFILE;"<span class=#"Red Bold#">";
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;"<img src='../images/PatientFolder",FOLDERSF,".gif'":
                " onclick='LinktoPatient(#"",URNUMBER,"#",#"",ADMISSNO,"#");' ";
.
          IF        STBYFLAG=1 | STBYFLAG=2
            WRITE     HTMLFILE;"onmouseover='show_it(#"",STATWIND,"#");'":
                               " onmouseout='hide_it();'";
          ELSE
            WRITE     HTMLFILE;"onmouseover='show_itE(#"",STATWIND,"#");'":
                               " onmouseout='hide_itE();'";
.
          ENDIF
          WRITE     HTMLFILE;" class=ListIcon \>";
.
          BRANCH    NOUPDATE,BCEL4000
.
          IF        POSTOPFL=1
            WRITE     HTMLFILE;"<a class=#"Blue Bold#"";
          ELSE
            IF        OVERLAPF=1
              WRITE     HTMLFILE;"<a class=#"White Bold#"";
            ELSE
              WRITE     HTMLFILE;"<a class=#"Red Bold#"";
            ENDIF   
          ENDIF
.
          WRITE     HTMLFILE;" href='":
                             "javascript:UpdateExpected":
                             "(#"",PMEBURNO,"#",#"",PMEBADMN,"#")'>";
          STRIP     PSNAME
          WRITE     HTMLFILE;*+,PSNAME,DOT,DIM1,"</a>";
          RESET     PSNAME
          GOTO      BCEL5000
.
BCEL4000  STRIP     PSNAME
          WRITE     HTMLFILE;*+,PSNAME,DOT,DIM1;
          RESET     PSNAME
.
BCEL5000  IF        ASTAT=1
            MATCH     SP70,PMVXATIM
            IF        !@EQUAL
              MOVE      PMVXATIM,D5
              WRITE     HTMLFILE;*+,"(",D5,")";
            ENDIF
          ENDIF
.
          IF        POSTOPFL=1
            MATCH     SP70,PMEBEWRD
            IF        !@EQUAL
              SQUEEZE   PMEBEWRD
              IF        OVERLAPF=1
                WRITE     HTMLFILE;"<span class=#"White Bold#">";
              ELSE
                WRITE     HTMLFILE;"<span class=#"Red Bold#">";
              ENDIF
.
              MATCH     SP70,PMEBEBED
              IF        @EQUAL
                WRITE     HTMLFILE;*+,"(",PMEBEWRD,")";
              ELSE    
                WRITE     HTMLFILE;*+,"(",PMEBEWRD,"/",PMEBEBED,")";
              ENDIF   
              WRITE     HTMLFILE;"</span>";
              RESET     PMEBEWRD
            ENDIF
          ELSE
            MATCH     SP70,PMEBPOWD
            IF        !@EQUAL
              SQUEEZE   PMEBPOWD
              WRITE     HTMLFILE;"<span class=#"Blue#">";
              MATCH     SP70,PMEBPOBD
              IF        @EQUAL
                WRITE     HTMLFILE;*+,"(",PMEBPOWD,")";
              ELSE
                WRITE     HTMLFILE;*+,"(",PMEBPOWD,"/",PMEBPOBD,")";
              ENDIF
              WRITE     HTMLFILE;"</span>";
              RESET     PMEBPOBD
            ENDIF
          ENDIF
.
          IF        STBYFLAG=1 | STBYFLAG=2
            WRITE     HTMLFILE;" (Stby)";
          ENDIF
.
          MATCH     "1",PMEBBRPT
          IF        @EQUAL
            SQUEEZE   AWARD
            MATCH     SP70,ABED
            IF        @EQUAL
              WRITE     HTMLFILE;*+,"(Req)(",AWARD,")";
            ELSE
              WRITE     HTMLFILE;*+,"(Req)(",AWARD,"/",ABED,")";
            ENDIF
            RESET   AWARD
          ENDIF
.
          IF        MULTIEXP=1
            IF        OVERLAPF=1
              WRITE     HTMLFILE;"&nbsp<a class=#"White Bold#"";
            ELSE
              WRITE     HTMLFILE;"&nbsp<a class=#"Red Bold#"";
            ENDIF
            WRITE     HTMLFILE;" href='":
                               "javascript:ViewExpected":
                               "(#"",PMEBWARD,"#",#"",PMEBBEDD,"#",#"0#")'>":
                                "(+)</a>";
          ENDIF
.
          WRITE     HTMLFILE;"</span>";
.
BCEL5900  CALL      ONLV0000              * Check for on leave patients
          WRITE     HTMLFILE;"&nbsp;</td></tr>"
.
BCEL9999  RETURN
.
.------------------------------------------------------------
. Get the expected patient details in priority order
. by date then Standby, expected adm, post op then bed request.
. Date is the highest priority as it's used for tomorrows bed board view.
.------------------------------------------------------------
GEXP0000  MOVE      ZERO,TOMMFLAG          * Tomorrows expects patient loop flag
          MOVE      ZERO,MULTIEXP          * Multiple expected patients
          MOVE      ZERO,STBYFLAG          * Standby flag
          MOVE      SP70,SAVKEY20
          MOVE      SP70,GEXPGEND          * Expected patient gender
.
GEXP1000  MOVE      SP70,SVKEY20A           * Standby
          MOVE      SP70,SVKEY20B           * Expected admission
          MOVE      SP70,SVKEY20C           * Post Op
          MOVE      SP70,SVKEY20D           * Bed request
.
          MOVE      SP70,SAVETIMA           * Standby
          MOVE      SP70,SAVETIMB           * Expected admission
          MOVE      SP70,SAVETIMC           * Post Op
          MOVE      SP70,SAVETIMD           * Bed request
.
          MOVE      SP70,SAVEGENA           * Standby
          MOVE      SP70,SAVEGENB           * Expected admission
          MOVE      SP70,SAVEGENC           * Post Op
          MOVE      SP70,SAVEGEND           * Bed request
.
          MOVE      ZERO,EXADFLAG           * Expected asmission flag
          MOVE      ZERO,POSTFLAG           * Post op  flag
          MOVE      ZERO,BEDRFLAG           * Bed request flag
. 
          MATCH     SP70,PMBDBEDD           * Exit if not a bed record
          GOTO      GEXP9999 IF EQUAL
.
          PACK      KEY20,PMBDHOSP,PMBDWARD,PMBDROOM,PMBDBEDD,SP70
          CALL      RSPMEPB1
GEXP3200  CALL      RKPMEPB1
          BRANCH    OVRCD,GEXP9000
.         
          MATCH     PMBDHOSP,PMEBHOSP
          GOTO      GEXP9000 IF NOT EQUAL
.
          MATCH     PMBDWARD,PMEBWARD
          GOTO      GEXP9000 IF NOT EQUAL
.
          MATCH     PMBDROOM,PMEBROOM
          GOTO      GEXP9000 IF NOT EQUAL
.
          MATCH     PMBDBEDD,PMEBBEDD
          GOTO      GEXP9000 IF NOT EQUAL
.
          MATCH     PMEBADMN,WSTBY
          IF        @EQUAL
            BRANCH    TOMMFLAG,GEXP3200
.
            ADD       ONE,MULTIEXP                 * Multiple expected patients
            GOTO      GEXP4000                   * Standby
          ENDIF
.
          IF        TOMMFLAG=1
            MATCH     TOMRDATE,PMEBEDAT            * Only select tomorrow
            GOTO      GEXP3200 IF NOT EQUAL
          ELSE
            MATCH     WORKDATE,PMEBEDAT            * Only select today
            GOTO      GEXP3300 IF EQUAL
            MATCH     YESTERDY,PMEBEDAT            * Only select today
            GOTO      GEXP3200 IF NOT EQUAL
          ENDIF
.
GEXP3300  ADD       ONE,MULTIEXP                 * Multiple expected patients
.
          MATCH     PMEBWARD,PMEBPOWD
          GOTO      GEXP3400 IF NOT EQUAL
.
          MATCH     PMEBBEDD,PMEBPOBD
          GOTO      GEXP3400 IF NOT EQUAL
.
          IF        TOMMFLAG=1
            MATCH     TOMRDATE,PMEBTBDT
            GOTO      GEXP3400 IF NOT EQUAL
          ELSE
            MATCH     WORKDATE,PMEBTBDT
            GOTO      GEXP3400 IF NOT EQUAL
          ENDIF
.
          MATCH     PMEBEWRD,PMEBPOWD
          IF        @EQUAL
             MATCH    PMEBEBED,PMEBPOBD
             GOTO     GEXP3400 IF EQUAL            
          ENDIF
          GOTO      GEXP6000                     * Post op record
.
GEXP3400  MATCH     "1",PMEBBRPT                 * Bed Request
          GOTO      GEXP7000 IF EQUAL   
.
          GOTO      GEXP5000                     * Expected admission 
.
GEXP4000  MOVE      ONE,STBYFLAG                 * On standby
.
          PACK      KEY16,PMEBURNO,PMBDURNO,SP70
          CALL      RDLINK1
          BRANCH    OVRCD,GEXP4100
.
          PACK      KEY5,ANSL,ANSU,LINKREA,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,GEXP4100
.
          MATCH     ANSM,TCINDC1                 * Mother/Baby Link
          GOTO      GEXP4100 IF NOT EQUAL
.
          MOVE      TWO,STBYFLAG                 * Baby linked to mother
.
GEXP4100  MATCH     SP70,SVKEY20A
          IF        !@EQUAL
            MATCH     SAVETIMA,PMEBETIM
            GOTO      GEXP3200 IF NOT LESS
          ENDIF
.
          MOVE      PMEBETIM,SAVETIMA
          PACK      SVKEY20A,PMBDHOSP,PMBDWARD,PMBDROOM,PMBDBEDD,PMEBADMN,SP70
          MOVE      PMEBGEND,SAVEGENA
          GOTO      GEXP3200
.
GEXP5000  MOVE      ONE,EXADFLAG 
.
          MATCH     SP70,SVKEY20B
          IF        !@EQUAL
            MATCH     SAVETIMB,PMEBETIM
            GOTO      GEXP3200 IF NOT LESS
          ENDIF
.
          MOVE      PMEBETIM,SAVETIMB
          PACK      SVKEY20B,PMBDHOSP,PMBDWARD,PMBDROOM,PMBDBEDD,PMEBADMN,SP70
          MOVE      PMEBGEND,SAVEGENB
          GOTO      GEXP3200
.
GEXP6000  MOVE      ONE,POSTFLAG 
.
          MATCH     SP70,SVKEY20C
          IF        !@EQUAL
            MATCH     SAVETIMC,PMEBETIM
            GOTO      GEXP3200 IF NOT LESS
          ENDIF
.
          MOVE      PMEBETIM,SAVETIMC
          PACK      SVKEY20C,PMBDHOSP,PMBDWARD,PMBDROOM,PMBDBEDD,PMEBADMN,SP70
          MOVE      PMEBGEND,SAVEGENC
          GOTO      GEXP3200
.
GEXP7000  MOVE      ONE,BEDRFLAG 
.
          MATCH     SP70,SVKEY20D
          IF        !@EQUAL
            MATCH     SAVETIMD,PMEBETIM
            GOTO      GEXP3200 IF NOT LESS
          ENDIF
.
          MOVE      PMEBETIM,SAVETIMD
          PACK      SVKEY20D,PMBDHOSP,PMBDWARD,PMBDROOM,PMBDBEDD,PMEBADMN,SP70
          MOVE      PMEBGEND,SAVEGEND
          GOTO      GEXP3200
.
GEXP9000  IF        MULTIEXP>1
            MOVE      ONE,MULTIEXP
          ELSE
            MOVE      ZERO,MULTIEXP
          ENDIF
.
          IF        TOMMFLAG=1
            MATCH     SP70,SAVKEY20         * Exit if there is an expected 
            GOTO      GEXP9999 IF NOT EQUAL * patient for today
          ENDIF
.
          IF        STBYFLAG=1 | STBYFLAG=2
            MOVE      SVKEY20A,SAVKEY20     * Standby
            MOVE      SAVEGENA,GEXPGEND
            GOTO      GEXP9500
          ENDIF
.
          IF        EXADFLAG=1
            MOVE      SVKEY20B,SAVKEY20     * Expected admission 
            MOVE      SAVEGENB,GEXPGEND
            GOTO      GEXP9500
          ENDIF
.
          IF        POSTFLAG=1
            MOVE      SVKEY20C,SAVKEY20     * Post Op
            MOVE      SAVEGENC,GEXPGEND
            GOTO      GEXP9500
          ENDIF
.
          IF        BEDRFLAG=1
            MOVE      SVKEY20D,SAVKEY20     * Bed request
            MOVE      SAVEGEND,GEXPGEND
            GOTO      GEXP9500
          ENDIF
.
GEXP9500  COMPARE   ONE,TOMRFLAG            * Tomorrows bed board
          GOTO      GEXP9999 IF NOT EQUAL
.
          COMPARE   ZERO,TOMMFLAG
          GOTO      GEXP9999 IF NOT EQUAL
.
          MOVE      ONE,TOMMFLAG
.
          IF        MULTIEXP=1
            MOVE      TWO,MULTIEXP
          ELSE       
            MATCH     SP70,SAVKEY20
            IF        !@EQUAL
              MOVE      ONE,MULTIEXP        * Retain multiple expected patients
            ENDIF
          ENDIF
.
          GOTO      GEXP1000
.        
GEXP9999  RETURN
.
.------------------------------------------------------------
. Get the rooms expected patient gender in priority order
. by date then Standby, expected adm, post op then bed request.
. Date is the highest priority as it's used for tomorrows bed board view.
.------------------------------------------------------------
EXPG0000  MATCH     SP70,PMBDROOM          * Exit if no room
          GOTO      EXPG9999 IF EQUAL
          MATCH     SP70,PMBDBEDD          * Exit if not a bed record
          GOTO      EXPG9999 IF EQUAL
.
          MOVE      SP70,GEXPGEND          * Clear Expected patient gender
          MOVE      ZERO,SAVPRITY          * Priority Start at 0
.
          PACK      KEY20,PMBDHOSP,PMBDWARD,PMBDROOM,SP70
          CALL      RSPMEPB1
EXPG1000  CALL      RKPMEPB1
          BRANCH    OVRCD,EXPG9999
.
          MATCH     PMBDHOSP,PMEBHOSP
          GOTO      EXPG9999 IF NOT EQUAL
.
          MATCH     PMBDWARD,PMEBWARD
          GOTO      EXPG9999 IF NOT EQUAL
.
          MATCH     PMBDROOM,PMEBROOM
          GOTO      EXPG9999 IF NOT EQUAL
.
          MATCH     PMBDBEDD,PMEBBEDD            * Ignore Current Room
          GOTO      EXPG1000 IF EQUAL
.
          MATCH     WORKDATE,PMEBEDAT            * Process Current Date
          GOTO      EXPG1100 IF EQUAL
.
          COMPARE   ONE,TOMRFLAG
          GOTO      EXPG1000 IF NOT EQUAL
.
          MATCH     TOMRDATE,PMEBEDAT            * Ignore anything after Tomorrow
          GOTO      EXPG1000 IF NOT EQUAL
.
EXPG1100  CALL      EXPP0000                     * Get Priority  Standby=9, Exp Adm Today=8, Exp Post Op Today=7
          IF        EXPPRITY>SAVPRITY            
            MOVE      EXPPRITY,SAVPRITY          *               Exp Adm Tomorrow=6, Exp Post Op Tomorrow=5 
            MOVE      PMEBGEND,GEXPGEND          *               Bed Request Today=2, Bed Request Tomorrow=1  
          ENDIF
          COMPARE   "9",EXPPRITY                 * Once we have a standby just exit as there is nothing higher
          GOTO      EXPG1000 IF NOT EQUAL
.
EXPG9999  RETURN
.
. Determine Expected Priority
.
EXPP0000  MOVE      ONE,EXPPRITY
          PACK      SAVEKEY6,WWARD,WBED,SP70    * Read ward bed record
          PACK      KEY6,PMEBWARD,PMEBBEDD,SP70    * Read ward bed record
          CALL      RDWARD1
          BRANCH    OVRCD,EXPP1000
          MATCH     PMEBADMN,WSTBY
          IF        @EQUAL
            MOVE      "9",EXPPRITY
            PACK      KEY6,SAVEKEY6
            CALL      RDWARD1
            GOTO      EXPP9999
          ENDIF
          PACK      KEY6,SAVEKEY6
          CALL      RDWARD1
.
EXPP1000  MATCH     PMEBWARD,PMEBPOWD
          GOTO      EXPP2000 IF NOT EQUAL
          MATCH     PMEBBEDD,PMEBPOBD
          GOTO      EXPP2000 IF NOT EQUAL
.
          MATCH     WORKDATE,PMEBTBDT
          GOTO      EXPP2000 IF NOT EQUAL
          MATCH     TOMRDATE,PMEBTBDT
          GOTO      EXPP2000 IF NOT EQUAL

          MATCH     PMEBEWRD,PMEBPOWD
          IF        @EQUAL
             MATCH    PMEBEBED,PMEBPOBD
             GOTO     EXPP2000 IF EQUAL            
          ENDIF
          GOTO      EXPP3000
.
EXPP2000  MATCH     "1",PMEBBRPT                   * Bed Request Lowest Priority
          GOTO      EXPP9000 IF EQUAL   
          MATCH     WORKDATE,PMEBEDAT            
          IF        @EQUAL
            MOVE      "8",EXPPRITY                 * Expected for Today
          ELSE
            MOVE      "6",EXPPRITY                 * Expected for Tomorrow
          ENDIF
          GOTO      EXPP9999
.
EXPP3000  MATCH     WORKDATE,PMEBTBDT            
          IF        @EQUAL
            MOVE      "7",EXPPRITY                 * Post Op for Today
          ELSE
            MOVE      "5",EXPPRITY                 * Post Op for Tomorrow
          ENDIF
          GOTO      EXPP9999
.
EXPP9000  MATCH     WORKDATE,PMEBEDAT            * Bed Request
          IF        @EQUAL
            MOVE      "2",EXPPRITY               * For today Higher than for tomorrow
          ENDIF

EXPP9999  RETURN
.
.------------------------------------------------------------
. Determine the gender of the room if required
.------------------------------------------------------------
BRMG0000  MATCH     SP70,PMBDROOM                * Exit if no room
          IF        @EQUAL
            MOVE      SP70,ROOMCLAS
            GOTO      BRMG9999
          ENDIF
.
          MATCH     PMBDROOM,SAVEROOM
          GOTO      BRMG9999 IF EQUAL
.
          MOVE      SP70,ROOMGEND
          MOVE      SP70,ROOMCLAS
.
          IF        VIEWSEQN=1
            PACK      SAVKEY22,PMBDHOSP,PMBDSEQ1,PMBDWARD,PMBDROOM,PMBDBEDD:
                               PMBDCADM,SP70
            GOTO      BRMG0500
          ENDIF
.
          IF        VIEWSEQN=2
            PACK      SAVKEY22,PMBDHOSP,PMBDSEQ2,PMBDWARD,PMBDROOM,PMBDBEDD:
                               PMBDCADM,SP70
            GOTO      BRMG0500
          ENDIF
.
          IF        VIEWSEQN=3
            PACK      SAVKEY22,PMBDHOSP,PMBDSEQ3,PMBDWARD,PMBDROOM,PMBDBEDD:
                               PMBDCADM,SP70
            GOTO      BRMG0500
          ENDIF
.
          IF        VIEWSEQN=4
            PACK      SAVKEY22,PMBDHOSP,PMBDSEQ4,PMBDWARD,PMBDROOM,PMBDBEDD:
                               PMBDCADM,SP70
            GOTO      BRMG0500
          ENDIF
.
          IF        VIEWSEQN=5
            PACK      SAVKEY22,PMBDHOSP,PMBDSEQ5,PMBDWARD,PMBDROOM,PMBDBEDD:
                               PMBDCADM,SP70
            GOTO      BRMG0500
          ENDIF
.
BRMG0500  MOVE      PMBDROOM,SAVEROOM
          MOVE      ZERO,BEDCOUNT
.
          PACK      SAVEKEY9,PMBDHOSP,PMBDWARD,PMBDROOM,SP70
          PACK      KEY20,PMBDHOSP,PMBDWARD,PMBDROOM,SP70
          CALL      RSPMBBD1
BRMG1000  CALL      RKPMBBD1
          BRANCH    OVRCD,BRMG9000
.
          PACK      KEY9,PMBDHOSP,PMBDWARD,PMBDROOM,SP70
          MATCH     SAVEKEY9,KEY9                * Same hospital, ward and room
          GOTO      BRMG9000 IF NOT EQUAL
.
          ADD       ONE,BEDCOUNT
          IF        BEDCOUNT>1
            MATCH     SP70,ROOMGEND            * Room gender
            GOTO      BRMG9000 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,PMBDCADM                * Current admission 
          GOTO      BRMG1000 IF EQUAL
.
          MOVE      PMBDGEND,ROOMGEND            * Room gender
.
          BRANCH    BEDCOUNT,BRMG1000 
.
BRMG9000  IF        BEDCOUNT>1
            ADD       ONE,ROOMCNTR
            IF        (ROOMCNTR%2) = 0 
              MOVE      " Room1 ",ROOMCLAS       * If more that 1 bed in room
            ELSE                                 * set room border class
              MOVE      " Room2 ",ROOMCLAS
            ENDIF
          ENDIF
.
          MOVE      SAVKEY22,KEY22
.
          IF        VIEWSEQN=1
            CALL      RDPMBBD3
            GOTO      BRMG9999
          ENDIF     
.
          IF        VIEWSEQN=2
            CALL      RDPMBBD4
            GOTO      BRMG9999
          ENDIF     
.
          IF        VIEWSEQN=3
            CALL      RDPMBBD5
            GOTO      BRMG9999
          ENDIF     
.
          IF        VIEWSEQN=4
            CALL      RDPMBBD6
            GOTO      BRMG9999
          ENDIF     
.
          IF        VIEWSEQN=5
            CALL      RDPMBBD7
            GOTO      BRMG9999
          ENDIF     
.
BRMG9999  RETURN
.
.------------------------------------------------------------
. Format Patient Age in Y,M,W or D - 
.------------------------------------------------------------
FMTA0000  MOVE      SP70,FMTPATAG
.
          MATCH     "y",PSAGETYP
          GOTO      FMTA1000 IF NOT EQUAL
.
          CLEAR     FMTPATAG
          APPEND    PSAGEYRS,FMTPATAG
          APPEND    " yrs",FMTPATAG
          RESET     FMTPATAG
          GOTO      FMTA9999
.
FMTA1000  MATCH     "d",PSAGETYP
          GOTO      FMTA2000 IF NOT EQUAL
.
          CLEAR     FMTPATAG
          APPEND    PSAGEDAY,FMTPATAG
          APPEND    " days",FMTPATAG
          RESET     FMTPATAG
          GOTO      FMTA9999
.
FMTA2000  MATCH     "m",PSAGETYP
          GOTO      FMTA3000 IF NOT EQUAL
.
          CLEAR     FMTPATAG
          APPEND    PSAGEMNT,FMTPATAG
          APPEND    " mths",FMTPATAG
          RESET     FMTPATAG
          GOTO      FMTA9999
.
FMTA3000  MATCH     "w",PSAGETYP
          GOTO      FMTA9999 IF NOT EQUAL
.
          CLEAR     FMTPATAG
          APPEND    PSAGEWKS,FMTPATAG
          APPEND    " wks",FMTPATAG
          RESET     FMTPATAG
.
FMTA9999  RETURN
.
.------------------------------------------------------------
. Populate current patient onhover details
.------------------------------------------------------------
STAC0000  MOVE      SP70,STATWIND
.
          CLEAR     STATWIND
          APPEND    "Patient Name: <b>",STATWIND
          APPEND    PGNAME,STATWIND
          APPEND    SP1,STATWIND
          APPEND    PSNAME,STATWIND
.         
          APPEND    "</b><br>U/R Number:<b> ",STATWIND
          APPEND    URNUMBER,STATWIND
.           
          APPEND    "</b><br>Visit Number: ",STATWIND
          APPEND    ADMISSNO,STATWIND
.
          APPEND    "</b><br>Age/",STATWIND
          PACK      KEY5,ANSG,SP70
          CALL      RDCODE1
          IF        OVRCD<>1
            MATCH     SP70,TDESC
            IF        @EQUAL
              APPEND    "Gender",STATWIND
            ELSE
              STRIP     TDESC
              APPEND    TDESC,STATWIND
            ENDIF
          ENDIF
          APPEND    ": ",STATWIND
          APPEND    FMTPATAG,STATWIND
          APPEND    COMMA,STATWIND
          APPEND    PSEX,STATWIND
.         
          APPEND    "<br>Admission Date: ",STATWIND
.           
          MATCH     SP70,ADATE
          IF        !@EQUAL
            UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      DISPDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
            APPEND    DISPDATE,STATWIND
.
            MATCH     SP70,ATIME
            IF        !@EQUAL
              APPEND    " at ",STATWIND
              APPEND    ATIME,STATWIND
            ENDIF
          ENDIF
.
          APPEND    "<br>Exp.Discharge Date: ",STATWIND
          PACK      KEY8,ADMISSNO,SP70
          CALL      RDPMWOR1
          IF        OVRCD<>1
            MATCH     SP70,PMWOEDAT
            IF        !@EQUAL
              UNPACK    PMWOEDAT,CCENT,CYEAR,CMON,CDAY
              MOVE      CMON,F2
              LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG:
                                   SEP,OCT,NOV,DEC
              PACK      DISPDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
              APPEND    DISPDATE,STATWIND
          ENDIF
          ENDIF
.
          REP       "#" ",ADIAG1
          REP       "#' ",ADIAG1
.
          IF        PTCNHDPS=1
            APPEND    "<br>Reason For Admission: ",STATWIND
          ELSE
            APPEND    "<br>Diagnosis: ",STATWIND
          ENDIF
          APPEND    ADIAG1,STATWIND
.
          APPEND    "<br>Speciality: ",STATWIND
          MATCH     SP70,ACLSSFT
          IF        !@EQUAL
            PACK      KEY5,ANSA,ANSC,ACLSSFT,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              APPEND    TDESC,STATWIND
            ENDIF
          ENDIF
.
          APPEND    "<br>Doctor: <b>",STATWIND
          MATCH     SP70,ADOCTA
          IF        !@EQUAL
            MOVE      SP70,DOCFNAME
            PACK      KEY10,ADOCTA,SP70
            CALL      RDPMHCP1
            IF        OVRCD<>1
              MOVE      PMHCTITL,FMTTITLE
              MOVE      PMHCGNAM,FMTGNAME
              MOVE      PMHCSNAM,FMTSNAME
              CALL      DOCNM000
              REP       "'`@ * / : \ $ % & # ",DOCFNAME
              APPEND    DOCFNAME,STATWIND
              APPEND    "</b>",STATWIND
            ENDIF
          ENDIF
.
          APPEND    "</b><br>Anaes: ",STATWIND
          MOVE      SP70,THETSTAT
          MOVE      SP70,THETRBAY
.
          PACK      KEY31,ADMISSNO,TWO,WORKDATE,SP70
          CALL      RSOPDEA2
          CALL      RKOPDEA2
          BRANCH    OVRCD,STAC2000
.
          MATCH     ADMISSNO,OPDAADMN
          GOTO      STAC2000 IF NOT EQUAL
.
          COMPARE   TWO,OPDASTAT
          GOTO      STAC2000 IF NOT EQUAL
.
          MATCH     WORKDATE,OPDADATE
          GOTO      STAC2000 IF NOT EQUAL
.
          PACK      KEY22,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN,SP70
          CALL      RDOPSES1
          IF        OVRCD=1
            CALL      CLOPRSES
          ENDIF
.
          MATCH     SP70,OPSEANAE
          IF        !@EQUAL
            MOVE      SP70,DOCFNAME
            PACK      KEY10,OPSEANAE,SP70
            CALL      RDPMHCP1
            IF        OVRCD<>1
              MOVE      PMHCTITL,FMTTITLE
              MOVE      PMHCGNAM,FMTGNAME
              MOVE      PMHCSNAM,FMTSNAME
              CALL      DOCNM000
              REP       "'`@ * / : \ $ % & # ",DOCFNAME
              APPEND    DOCFNAME,STATWIND
            ENDIF
          ENDIF
.
          PACK      KEY10,OPDAUNIQ,SP70
          CALL      RDOPARD1
          IF        OVRCD=1
            CALL      CLOPRARD
          ENDIF
.
          PACK      KEY11,OPDAUNIQ,ONE,SP70
          CALL      RDOPSRG1
          IF        OVRCD=1
            CALL      CLOPRSRG
          ENDIF
.
          CALL      PATSTS00
.
STAC2000  APPEND    "<br>Theatre Status: ",STATWIND
          APPEND    THETSTAT,STATWIND
          APPEND    SP1,STATWIND
          APPEND    THETRBAY,STATWIND
.
          APPEND    "<br>Pref.Room Type: ",STATWIND
          MATCH     SP70,PMVXPACC
          IF        !@EQUAL
            PACK      KEY5,ANSB,ANSP,PMVXPACC,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              APPEND    TDESC,STATWIND
            ENDIF
          ENDIF
.
          IF        CFEETYP=5
            APPEND    "<br>Contract: ",STATWIND
          ELSE
            APPEND    "<br>Health Fund: ",STATWIND
          ENDIF
.
          MATCH     SP70,AFUNDH
          IF        !@EQUAL
            PACK      KEY14,AFUNDH,ZERO,ZERO,ZERO,ZERO,SP70
            CALL      RDFUND1
            IF        OVRCD<>1
              APPEND    HFNAME,STATWIND
            ENDIF
            PACK      KEY14,AFUNDH,AFNDTB,SP70
            CALL      RDFUND1
            IF        OVRCD<>1
              APPEND    HFNAME,STATWIND
            ENDIF
          ENDIF
.
          APPEND    "<br>Claim Type: ",STATWIND
          MATCH     SP70,ACLAIM
          IF        !@EQUAL
            PACK      KEY5,ANSC,ANSL,ACLAIM,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              APPEND    TDESC,STATWIND
            ENDIF
          ENDIF
.
          CALL      SPCFEA00                * Special Feature codes      
.
          RESET     STATWIND
.
STAC9999  RETURN
.
.------------------------------------------------------------
. Populate expected patient onhover details
.------------------------------------------------------------
STAE0000  MOVE      SP70,STATWIND
.
          CLEAR     STATWIND
          APPEND    "Patient Name: <b>",STATWIND
          APPEND    PGNAME,STATWIND
          APPEND    SP1,STATWIND
          APPEND    PSNAME,STATWIND
.
          APPEND    "</b><br>U/R Number:<b> ",STATWIND
          APPEND    URNUMBER,STATWIND
.
          APPEND    "</b><br>Visit Number: ",STATWIND
          APPEND    ADMISSNO,STATWIND
.
          APPEND    "</b><br>Age/",STATWIND
          PACK      KEY5,ANSG,SP70
          CALL      RDCODE1
          IF        OVRCD<>1
            MATCH     SP70,TDESC
            IF        @EQUAL
              APPEND    "Gender",STATWIND
            ELSE
              STRIP     TDESC
              APPEND    TDESC,STATWIND
            ENDIF
          ENDIF
          APPEND    ": ",STATWIND
          APPEND    FMTPATAG,STATWIND
          APPEND    COMMA,STATWIND
          APPEND    PSEX,STATWIND
.
          APPEND    "<br>Admission Date: ",STATWIND
.
          MATCH     SP70,ADATE
          IF        !@EQUAL
            UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      DISPDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
            APPEND    DISPDATE,STATWIND
.
            MATCH     SP70,ATIME
            IF        !@EQUAL
              APPEND    " at <b>",STATWIND
              APPEND    ATIME,STATWIND
              APPEND    "</b>",STATWIND
            ENDIF
          ENDIF
.
          APPEND    "<br>Exp.LOS: ",STATWIND
          APPEND    ASTAY,STATWIND
.
          IF        PTCNHDPS=1
            APPEND    "<br>Reason For Admission: ",STATWIND
          ELSE
            APPEND    "<br>Diagnosis: ",STATWIND
          ENDIF
          APPEND    "<b>",STATWIND
.
          REP       "#" ",ADIAG1
          REP       "#' ",ADIAG1
.
          APPEND    ADIAG1,STATWIND
          APPEND    "</b>",STATWIND
.
          APPEND    "<br>Speciality: ",STATWIND
          MATCH     SP70,ACLSSFT
          IF        !@EQUAL
            PACK      KEY5,ANSA,ANSC,ACLSSFT,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              APPEND    TDESC,STATWIND
            ENDIF
          ENDIF
.
          APPEND    "<br>Doctor: <b>",STATWIND
          MATCH     SP70,ADOCTA
          IF        !@EQUAL
            MOVE      SP70,DOCFNAME
            PACK      KEY10,ADOCTA,SP70
            CALL      RDPMHCP1
            IF        OVRCD<>1
              MOVE      PMHCTITL,FMTTITLE
              MOVE      PMHCGNAM,FMTGNAME
              MOVE      PMHCSNAM,FMTSNAME
              CALL      DOCNM000
              REP       "'`@ * / : \ $ % & # ",DOCFNAME
              APPEND    DOCFNAME,STATWIND
              APPEND    "</b>",STATWIND
            ENDIF
          ENDIF
.
          APPEND    "</b><br>Anaes: ",STATWIND
          MOVE      SP70,THETSTAT
          MOVE      SP70,THETRBAY
.
          IF        TOMRFLAG=1
            PACK      KEY31,ADMISSNO,TWO,TOMRDATE,Z70
          ELSE
            PACK      KEY31,ADMISSNO,TWO,WORKDATE,Z70
          ENDIF
          CALL      RSOPDEA2
          CALL      RPOPDEA2
          BRANCH    OVRCD,STAE2000
.
          MATCH     ADMISSNO,OPDAADMN
          GOTO      STAE2000 IF NOT EQUAL
.
          IF        TOMRFLAG=1
            MATCH     TOMRDATE,OPDADATE
            IF        !@EQUAL
              MATCH     WORKDATE,OPDADATE
              GOTO      STAE2000 IF NOT EQUAL
            ENDIF
          ELSE
            MATCH     WORKDATE,OPDADATE
            GOTO      STAE2000 IF NOT EQUAL
          ENDIF
.
          PACK      KEY22,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN,SP70
          CALL      RDOPSES1
          IF        OVRCD=1
            CALL      CLOPRSES
          ENDIF
.
          MATCH     SP70,OPSEANAE
          IF        !@EQUAL
            MOVE      SP70,DOCFNAME
            PACK      KEY10,OPSEANAE,SP70
            CALL      RDPMHCP1
            IF        OVRCD<>1
              MOVE      PMHCTITL,FMTTITLE
              MOVE      PMHCGNAM,FMTGNAME
              MOVE      PMHCSNAM,FMTSNAME
              CALL      DOCNM000
              REP       "'`@ * / : \ $ % & # ",DOCFNAME
              APPEND    DOCFNAME,STATWIND
            ENDIF
          ENDIF
.
          PACK      KEY10,OPDAUNIQ,SP70
          CALL      RDOPARD1
          IF        OVRCD=1
            CALL      CLOPRARD
          ENDIF
.
          PACK      KEY11,OPDAUNIQ,ONE,SP70
          CALL      RDOPSRG1
          IF        OVRCD=1
            CALL      CLOPRSRG
          ENDIF
.
          CALL      PATSTS00
.
STAE2000  APPEND    "<br>Theatre Status: ",STATWIND
          APPEND    THETSTAT,STATWIND
          APPEND    SP1,STATWIND
          APPEND    THETRBAY,STATWIND
.
          APPEND    "<br>Pref.Room Type: ",STATWIND
          MATCH     SP70,PMVXPACC
          IF        !@EQUAL
            PACK      KEY5,ANSB,ANSP,PMVXPACC,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              APPEND    TDESC,STATWIND
            ENDIF
          ENDIF
.
          IF        CFEETYP=5
            APPEND    "<br>Contract: ",STATWIND
          ELSE
            APPEND    "<br>Health Fund: ",STATWIND
          ENDIF
.
          MATCH     SP70,AFUNDH
          IF        !@EQUAL
            PACK      KEY14,AFUNDH,ZERO,ZERO,ZERO,ZERO,SP70
            CALL      RDFUND1
            IF        OVRCD<>1
              APPEND    HFNAME,STATWIND
            ENDIF
            PACK      KEY14,AFUNDH,AFNDTB,SP70
            CALL      RDFUND1
            IF        OVRCD<>1
              APPEND    HFNAME,STATWIND
            ENDIF
          ENDIF
.
          APPEND    "<br>Claim Type: ",STATWIND
          MATCH     SP70,ACLAIM
          IF        !@EQUAL
            PACK      KEY5,ANSC,ANSL,ACLAIM,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              APPEND    TDESC,STATWIND
            ENDIF
          ENDIF
.
          PACK      KEY24,PMEBADMN,PMEBEDAT,PMEBETIM,SP70
          CALL      RDPMBRQ1
          IF        OVRCD=0
            MATCH     "2",PMBRRSTA
            IF        @EQUAL
              MATCH     SP70,PMBRRDAT
              IF        !@EQUAL
                UNPACK    PMBRRDAT,CCENT,CYEAR,CMON,CDAY
                MOVE      CMON,F2
                LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
                PACK      DISPDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,SP70
                APPEND    "<br>Ward Bed Ready Date: <b>",STATWIND
                APPEND    DISPDATE,STATWIND
                APPEND    "</b>",STATWIND
                APPEND    "<br>Ward Bed Ready Time: <b>",STATWIND
                APPEND    PMBRRTIM,STATWIND
                APPEND    "</b>",STATWIND
              ENDIF
            ENDIF
          ENDIF
.
          CALL      SPCFEA00                * Special Feature codes      
.
          RESET     STATWIND
.
STAE9999  RETURN
.
.--------------------------------------------------------------------------
. sjog-midland ONLY
. Append special feature codes to STATWIND for onhover details 
.--------------------------------------------------------------------------
SPCFEA00  PACK      SPCFEADS,SP70,SP70 
          CLEAR     SPCFEADS
          MATCH     "1",SJOGFLAG
          GOTO      SPCFEA99 IF NOT EQUAL
. 
          PACK      KEY25,ADMISSNO,SP70
          CALL      RSPMBRQ2
SPCFEA20  CALL      RKPMBRQ2
          BRANCH    OVRCD,SPCFEA99
.
          MATCH     ADMISSNO,PMBRVISN
          GOTO      SPCFEA99 IF NOT EQUAL
.
          MATCH     " ",PMBRRSTA
          GOTO      SPCFEA40 IF EQUAL
.
          MATCH     "1",PMBRRSTA
          GOTO      SPCFEA40 IF EQUAL
.
          MATCH     "2",PMBRRSTA
          GOTO      SPCFEA40 IF EQUAL
.
          GOTO      SPCFEA20
.
SPCFEA40  MATCH     "1",PMBRSFFR
          IF        @EQUAL
            APPEND    "F",SPCFEADS
            APPEND    "&nbsp",SPCFEADS
            APPEND    "&nbsp",SPCFEADS
          ENDIF
.
          MATCH     "1",PMBRSFEP
          IF        @EQUAL
            APPEND    "P",SPCFEADS
            APPEND    "&nbsp",SPCFEADS
            APPEND    "&nbsp",SPCFEADS
          ENDIF
.
          MATCH     "1",PMBRSFCI
          IF        @EQUAL
            APPEND    "CI",SPCFEADS
            APPEND    "&nbsp",SPCFEADS
            APPEND    "&nbsp",SPCFEADS
          ENDIF
.
          MATCH     "1",PMBRSFCO
          IF        @EQUAL
            APPEND    "Com",SPCFEADS
            APPEND    "&nbsp",SPCFEADS
            APPEND    "&nbsp",SPCFEADS
          ENDIF
.
          MATCH     "1",PMBRSFIS
          IF        @EQUAL
            APPEND    "I",SPCFEADS
            APPEND    "&nbsp",SPCFEADS
            APPEND    "&nbsp",SPCFEADS
          ENDIF
.
          MATCH     "1",PMBRSFTE
          IF        @EQUAL
            APPEND    "T",SPCFEADS
            APPEND    "&nbsp",SPCFEADS
            APPEND    "&nbsp",SPCFEADS
          ENDIF
.
          MATCH     "1",PMBRSFRC
          IF        @EQUAL
            APPEND    "RC",SPCFEADS
            APPEND    "&nbsp",SPCFEADS
            APPEND    "&nbsp",SPCFEADS
          ENDIF
.
          MATCH     "1",PMBRSFOT
          IF        @EQUAL
            APPEND    "O",SPCFEADS
            APPEND    "&nbsp",SPCFEADS
            APPEND    "&nbsp",SPCFEADS
          ENDIF
.
          MATCH     "1",PMBRSFNR
          IF        @EQUAL
            APPEND    "None",SPCFEADS
            APPEND    "&nbsp",SPCFEADS
            APPEND    "&nbsp",SPCFEADS
          ENDIF
.
          MATCH     "1",PMBRSFMA
          IF        @EQUAL
            APPEND    "MA",SPCFEADS
            APPEND    "&nbsp",SPCFEADS
            APPEND    "&nbsp",SPCFEADS
          ENDIF
.
          RESET     SPCFEADS
          MATCH     SP70,SPCFEADS
          IF        !@EQUAL
            APPEND    "<br>Special Features: <b>",STATWIND
            APPEND    SPCFEADS,STATWIND
            APPEND    "</b>",STATWIND
            RESET     STATWIND
          ENDIF       
.
SPCFEA99  RETURN
.
.--------------------------------------------------------------------------
. sjog-midland ONLY
. Setup special feature existence flag and speciality bed required desc
. Return: SPCBEDDS
.--------------------------------------------------------------------------
SPCFLG00  PACK      SPCBEDDS,SP70,SP70 
          MOVE      SP70,SPECFLAG
          MOVE      SP70,INPSPCDS
.
SPCFLG10  MATCH     "1",SJOGFLAG
          GOTO      SPCFLG99 IF NOT EQUAL
.
          PACK      KEY25,ADMISSNO,SP70
          CALL      RSPMBRQ2
SPCFLG20  CALL      RKPMBRQ2
          BRANCH    OVRCD,SPCFLG99
.
          MATCH     ADMISSNO,PMBRVISN
          GOTO      SPCFLG99 IF NOT EQUAL
.
          MATCH     " ",PMBRRSTA
          GOTO      SPCFLG30 IF EQUAL
.
          MATCH     "1",PMBRRSTA
          GOTO      SPCFLG30 IF EQUAL
.
          MATCH     "2",PMBRRSTA
          GOTO      SPCFLG30 IF EQUAL
.
          GOTO      SPCFLG20
.
SPCFLG30  MATCH     SP70,PMBRSBED
          IF        !@EQUAL
            PACK      KEY5,ANSC,ANSG,PMBRSBED,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      PMBRSBED,TDESC
            ENDIF
            MOVE    TDESC,INPSPCDS
          ENDIF
.
          CALL      GETLOS00   
.
SPCFLG40  MATCH     "1",PMBRSFFR
          IF        @EQUAL
            MOVE      ASK,SPECFLAG 
            GOTO      SPCFLG50
          ENDIF 
.
          MATCH     "1",PMBRSFEP
          IF        @EQUAL
            MOVE      ASK,SPECFLAG 
            GOTO      SPCFLG50
          ENDIF 
.
          MATCH     "1",PMBRSFCI
          IF        @EQUAL
            MOVE      ASK,SPECFLAG 
            GOTO      SPCFLG50
          ENDIF 
.
          MATCH     "1",PMBRSFCO
          IF        @EQUAL
            MOVE      ASK,SPECFLAG 
            GOTO      SPCFLG50
          ENDIF 
.
          MATCH     "1",PMBRSFIS
          IF        @EQUAL
            MOVE      ASK,SPECFLAG 
            GOTO      SPCFLG50
          ENDIF 
.
          MATCH     "1",PMBRSFTE
          IF        @EQUAL
            MOVE      ASK,SPECFLAG 
            GOTO      SPCFLG50
          ENDIF 
.
          MATCH     "1",PMBRSFRC
          IF        @EQUAL
            MOVE      ASK,SPECFLAG 
            GOTO      SPCFLG50
          ENDIF 
.
          MATCH     "1",PMBRSFOT
          IF        @EQUAL
            MOVE      ASK,SPECFLAG 
            GOTO      SPCFLG50
          ENDIF 
.
          MATCH     "1",PMBRSFNR
          IF        @EQUAL
            MOVE      ASK,SPECFLAG 
            GOTO      SPCFLG50
          ENDIF 
.
          MATCH     "1",PMBRSFMA
          IF        @EQUAL
            MOVE      ASK,SPECFLAG 
            GOTO      SPCFLG50
          ENDIF 
.
. None of the special features has been set
.
          MATCH     SP70,INPSPCDS
          GOTO      SPCFLG50 IF NOT EQUAL 
.
          MATCH     SP70,LOSTIME
          GOTO      SPCFLG99 IF EQUAL 
.
SPCFLG50  CLEAR     SPCBEDDS
          APPEND    SP1,SPCBEDDS
          APPEND    SPECFLAG,SPCBEDDS
          APPEND    SP1,SPCBEDDS
          PACK      INPSPCDS,INPSPCDS,SP70 
          APPEND    INPSPCDS,SPCBEDDS
          RESET     SPCBEDDS 
.
          MATCH     "1",BREQFLAG          * LOS time for bed request only
          GOTO      SPCFLG99 IF NOT EQUAL
.
          MATCH     SP70,LOSTIME
          GOTO      SPCFLG99 IF EQUAL
. 
          SQUEEZE   LOSTIME 
          ENDSET    SPCBEDDS
          APPEND    SP1,SPCBEDDS
          APPEND    "<span class=",SPCBEDDS
          APPEND    HLCLASS,SPCBEDDS 
          APPEND    ">&nbsp;&nbsp;",SPCBEDDS  
          APPEND    LOSTIME,SPCBEDDS
          APPEND    "&nbsp;&nbsp;</span>",SPCBEDDS
          RESET     SPCBEDDS 
.
SPCFLG99  RETURN
.
.------------------------------------------------------------
. Get the LOS time
. Return: LOSTIME
.         HLCLASS - Color coding for LOSTIME 
.------------------------------------------------------------
GETLOS00  MOVE      SP70,LOSTIME
          MOVE      "emArrival0",HLCLASS
          PACK      KEY8,ADMISSNO,SP70
          CALL      RDEMVIS1
          BRANCH    OVRCD,GETLOS99
.
GETLOS20  MOVE      EMVIDATE,FIRSTDAT
          UNPACK    EMVITIME,HOUR,D1,MIN
          PACK      FIRSTIME,HOUR,MIN
          REP       " 0",FIRSTDAT
          REP       " 0",FIRSTIME
.
          PACK      LASTDATE,CCC,CYY,CMM,CDD
          REP       " 0",LASTDATE
          UNPACK    CTIMEIS,HOUR,D1,MIN
          PACK      LASTTIME,HOUR,MIN
          REP       " 0",LASTTIME
          CALL      TIMEDIFF
          IF        CALCTIME > 9999
            MOVE      9999,CALCTIME
          ENDIF
.
          ASSIGN    CALCTIME/SIXTY,FORM3        * Set Hrs
          ASSIGN    FORM3*SIXTY,FORM6           * Set hrs back to mins
          ASSIGN    (CALCTIME-FORM6),FORM2      * Set Minutes
.
          MOVE      FORM3,KEY3
          MOVE      FORM2,KEY2
          REPLACE   " 0",KEY2
          PACK      LOSTIME,KEY3,COLON,KEY2   
.
          MOVE      "emArrival0",HLCLASS
          IF        CALCTIME > 720
            MOVE      "emArrival4",HLCLASS
            GOTO      GETLOS99
          ENDIF
.
          IF        CALCTIME > 240
            MOVE      "emArrival3",HLCLASS
            GOTO      GETLOS99
          ENDIF
.
          IF        CALCTIME > 180
            MOVE      "emArrival2",HLCLASS
            GOTO      GETLOS99
          ENDIF
.
          IF        CALCTIME > 120
            MOVE      "emArrival1",HLCLASS
            GOTO      GETLOS99
          ENDIF
.
GETLOS99  RETURN
.
.-----------------------------------------------------------------------------
. Current Theatre Status of Patient
.-----------------------------------------------------------------------------
PATSTS00  MOVE      SP70,THETSTAT
          MOVE      SP70,THETRBAY
.
          COMPARE   THREE,OPDASTAT
          GOTO      PATSTS05 IF NOT EQUAL
.
          MOVE      "SC",CATEGORY
          MOVE      OPDACANC,CODE
          CALL      GETCOD00
          MATCH     ANST,TCINDC3
          IF        @EQUAL
            MOVE      "Transferred",THETSTAT
          ELSE
            MOVE      "Cancelled",THETSTAT
          ENDIF
          GOTO      PATSTS99
.
PATSTS05  MATCH     SP70,OPARATIM
          IF        !@EQUAL
            MOVE      "Holding",THETSTAT
          ENDIF
.
          MATCH     SP70,OPARANAS
          IF        !@EQUAL
            MOVE      "Anaes",THETSTAT
          ENDIF
.
          MATCH     SP70,OPSGTIPS
          IF        !@EQUAL
            MOVE      "Theatre",THETSTAT
          ENDIF
.
          MATCH     SP70,OPSGTISS
          IF        !@EQUAL
            MOVE      "Theatre",THETSTAT
          ENDIF
.
          MATCH     SP70,OPARTIRF
          IF        !@EQUAL
            MOVE      "Recovery-Front",THETSTAT
.
            MATCH     SP70,OPARUC03
            IF        !@EQUAL
              SQUEEZE   OPARUC03
              PACK      THETRBAY,LBRACK,OPARUC03,RBRACK,SP70
              RESET     OPARUC03
            ENDIF
          ENDIF
.
          MATCH     SP70,OPARTIRB
          IF        !@EQUAL
            MOVE      "Recovery-Back",THETSTAT
.
            MATCH     SP70,OPARUC03
            IF        !@EQUAL
              SQUEEZE   OPARUC03
              PACK      THETRBAY,LBRACK,OPARUC03,RBRACK,SP70
              RESET     OPARUC03
            ENDIF
          ENDIF
.
          MATCH     SP70,OPARTIRD
          IF        !@EQUAL
            MOVE      "Recovery-Day",THETSTAT
.
            MATCH     SP70,OPARUC03
            IF        !@EQUAL
              SQUEEZE   OPARUC03
              PACK      THETRBAY,LBRACK,OPARUC03,RBRACK,SP70
              RESET     OPARUC03
            ENDIF
          ENDIF
.
          MATCH     SP70,OPARTIDP
          IF        !@EQUAL
            MOVE      "Ready To Depart",THETSTAT
          ENDIF
.
          MATCH     SP70,OPARTETC
          IF        !@EQUAL
            MOVE      "Exit",THETSTAT
          ENDIF
.
          MATCH     SP70,OPARTICU
          IF        !@EQUAL
            MOVE      "ICU",THETSTAT
          ENDIF
.
          MATCH     SP70,OPARTEXP
          IF        !@EQUAL
            MATCH     SP70,THETSTAT                 * Is current status blank?
            IF        @EQUAL
              MOVE      "Deceased",THETSTAT         * Yes, set to "Deceased".
            ELSE
              MOVE      "RIP - ",DIM6            * No, prefix with "Dec"
              MOVE      THETSTAT,DIM19              * Save current status
              PACK      THETSTAT,DIM6,DIM19         * Build new status desc
            ENDIF
          ENDIF
.
PATSTS99  RETURN
.
.------------------------------------------------------------
. Populate bed request patient onhover details
.------------------------------------------------------------
STAR0000  MOVE      SP70,STATWIND
.
          CLEAR     STATWIND
          APPEND    "Patient Name: <b>",STATWIND
          APPEND    PGNAME,STATWIND
          APPEND    SP1,STATWIND
          APPEND    PSNAME,STATWIND
.
          APPEND    "</b><br>U/R Number:<b> ",STATWIND
          APPEND    URNUMBER,STATWIND
.
          APPEND    "</b><br>Visit Number: ",STATWIND
          APPEND    ADMISSNO,STATWIND
.
          APPEND    "</b><br>Age/Gender: ",STATWIND
          APPEND    FMTPATAG,STATWIND
          APPEND    COMMA,STATWIND
          APPEND    PSEX,STATWIND
.
          APPEND    "<br>Request Date: ",STATWIND
.
          MATCH     SP70,PMBRREQD
          IF        !@EQUAL
            UNPACK    PMBRREQD,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      DISPDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
            APPEND    DISPDATE,STATWIND
            APPEND    " @ ",STATWIND
            APPEND    PMBRREQT,STATWIND
          ENDIF
.
          APPEND    "<br>Requesting Department: ",STATWIND
.
          MATCH     SP70,PMBRUNIT
          IF        !@EQUAL
            PACK      KEY5,ANSA,ANSC,PMBRUNIT,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              APPEND    PMBRUNIT,TDESC
            ENDIF
            APPEND    TDESC,STATWIND
          ENDIF
.
          IF        PTCNHDPS=1
            APPEND    "<br>Reason For Admission: ",STATWIND
          ELSE
            APPEND    "<br>Diagnosis: ",STATWIND
          ENDIF
.
          MATCH     SP70,PMBRDIAG
          IF        !@EQUAL
            MOVE      PMBRREQD,ICDRDDTE
            PACK      KEY9,PMBRDIAG,SP70
            CALL      RDPTICD1
            IF        OVRCD=1
              MOVE      PMBRDIAG,DDESC
            ENDIF
            APPEND    DDESC,STATWIND
          ENDIF
.
          REP       "#" ",ADIAG1
          REP       "#' ",ADIAG1
.
          APPEND    ADIAG1,STATWIND
.
          APPEND    "<br>Doctor: <b>",STATWIND
          MATCH     SP70,PMBRRHCP
          IF        !@EQUAL
            MOVE      SP70,DOCFNAME
            PACK      KEY10,PMBRRHCP,SP70
            CALL      RDPMHCP1
            IF        OVRCD<>1
              MOVE      PMHCTITL,FMTTITLE
              MOVE      PMHCGNAM,FMTGNAME
              MOVE      PMHCSNAM,FMTSNAME
              CALL      DOCNM000
              REP       "'`@ * / : \ $ % & # ",DOCFNAME
              APPEND    DOCFNAME,STATWIND
              APPEND    "</b>",STATWIND
            ENDIF
          ENDIF
.
STAR8000  APPEND    "<br>Type of Bed Required: ",STATWIND
          MATCH     SP70,PMBRTYPE
          IF        !@EQUAL
            PACK      KEY5,ANSR,ANST,PMBRTYPE,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              APPEND    PMBRTYPE,TDESC
            ENDIF
            APPEND    "<b>",STATWIND
            APPEND    TDESC,STATWIND
            APPEND    "</b>",STATWIND
          ENDIF
.
          IF        CFEETYP=5
            APPEND    "<br>Contract: ",STATWIND
          ELSE
            APPEND    "<br>Health Fund: ",STATWIND
          ENDIF
.
          MATCH     SP70,AFUNDH
          IF        !@EQUAL
            PACK      KEY14,AFUNDH,ZERO,ZERO,ZERO,ZERO,SP70
            CALL      RDFUND1
            IF        OVRCD<>1
              APPEND    "<b>",STATWIND
              APPEND    HFNAME,STATWIND
              APPEND    "</b>",STATWIND
            ENDIF
            PACK      KEY14,AFUNDH,AFNDTB,SP70
            CALL      RDFUND1
            IF        OVRCD<>1
              APPEND    "<b>",STATWIND
              APPEND    HFNAME,STATWIND
              APPEND    "</b>",STATWIND
            ENDIF
          ENDIF
.
          PACK      KEY24,PMEBADMN,PMEBEDAT,PMEBETIM,SP70
          CALL      RDPMBRQ1
          IF        OVRCD=0
            MATCH     "2",PMBRRSTA
            IF        @EQUAL
              MATCH     SP70,PMBRRDAT
              IF        !@EQUAL
                UNPACK    PMBRRDAT,CCENT,CYEAR,CMON,CDAY
                MOVE      CMON,F2
                LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
                PACK      DISPDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,SP70
                APPEND    "<br>Ward Bed Ready Date: <b>",STATWIND
                APPEND    DISPDATE,STATWIND
                APPEND    "</b>",STATWIND
                APPEND    "<br>Ward Bed Ready Time: <b>",STATWIND
                APPEND    PMBRRTIM,STATWIND
                APPEND    "</b>",STATWIND
              ENDIF
            ENDIF
          ENDIF
.
          CALL      SPCFEA00                * Special Feature codes      
.
          RESET     STATWIND
.
STAR9999  RETURN
.
.------------------------------------------------------------
.  Display current patient alert icons
.------------------------------------------------------------
PATA0000  MOVE      ZERO,EXIT
          PROC      DISPALRT                * check for Disability Alerts
          IF        EXIT=0 | EXIT=2
            MATCH     "1",PMPXSIN7
            IF        @EQUAL
              WRITE     HTMLFILE;"<img class=Icon":
                                 " alt='Medical Alerts' title='Medical Alerts'":
                                 " src='../images/AlertIconM.gif'>";
            ENDIF
.
            MATCH     "1",PMPXSIN8
            IF        @EQUAL
              WRITE     HTMLFILE;"<img class=Icon":
                                 " alt='Micro Alerts' title='Micro Alerts'":
                                 " src='../images/AlertIconB.gif'>";
            ENDIF
.
            MATCH     "1",PMPXSIN9
            IF        @EQUAL
              WRITE     HTMLFILE;"<img class=Icon":
                                 " alt='Risk Alerts' title='Risk Alerts'":
                                 " src='../images/AlertIconR.gif'>";
            ENDIF
.
            MATCH     "1",PMPXSN11
            IF        @EQUAL
              WRITE     HTMLFILE;"<img class=Icon":
                                 " alt='Chronic Alerts' title='Chronic Alerts'":
                                 " src='../images/AlertIconC.gif'>";
            ENDIF
.
            MATCH     "1",PTMXSIN1
            IF        @EQUAL
              WRITE     HTMLFILE;"<img class=Icon":
                                 " alt='Alerts' title='Alerts'":
                                 " src='../images/AlertIcon.gif'>";
              GOTO      PATA1000
            ENDIF
.
            MATCH     "2",PTMXSIN1
            IF        @EQUAL
              WRITE     HTMLFILE;"<img class=Icon":
                                 " alt='Alerts' title='Alerts'":
                                 " src='../images/AlertIconBlack.gif'>";
              GOTO      PATA1000
            ENDIF
.
            MATCH     "3",PTMXSIN1
            IF        @EQUAL
              WRITE     HTMLFILE;"<img border=0 align=absmiddle":
                                 " alt='Alerts' title='Alerts'":
                                 " src='../images/AlertIconDelete.gif'>";
              GOTO      PATA1000
            ENDIF
.
            MATCH     SP70,PTMXSIN1
            IF        !@EQUAL
              WRITE     HTMLFILE;"<img class=Icon":
                                 " alt='Alerts' title='Alerts'":
                                 " src='../images/AlertIcon",PTMXSIN1,".gif'>";
              GOTO      PATA1000
            ENDIF
          ENDIF
.
PATA1000  IF        EXIT=1 | EXIT=2
            WRITE     HTMLFILE;"<img class=Icon":
                               " alt='Disability Alerts'":
                               " title='Disability Alerts'":
                               " src='../images/AlertIconDIS.gif'>";
          ENDIF
.
          IF        PSAGEYRS<CAGECOFF
            WRITE     HTMLFILE;"<img class=Icon":
                               " alt='Paediatric' title='Paediatric'":
                               " src='../images/PaediatricIcon.gif'>";
          ENDIF
.
          MATCH     SP70,ACLSSFT           * Blank speciality
          GOTO      PATA2000 IF EQUAL
.
          PACK      KEY5,ANSA,ANSC,ACLSSFT,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,PATA2000
.
          MATCH     ANSD,TCINDC4            * Do do speciality mismatch
          GOTO      PATA2000 IF EQUAL
. 
          MOVE      TCINDC1,DIM1
.
          PACK      KEY6,PMBDWARD,SP70      * Read ward master record
          CALL      RDWARD1  
          BRANCH    OVRCD,PATA2000
.
          MATCH     SP70,WCLASS
          GOTO      PATA2000 IF EQUAL
.
          PACK      KEY5,ANSC,ANSG,WCLASS,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,PATA2000
.
          MATCH     TCINDC1,DIM1            * No speciality mismatch
          GOTO      PATA2000 IF EQUAL
.
          PACK      KEY12,PMBDHOSP,PMBDWARD,PMBDBEDD,SP70
          CALL      RSPMMSW1
PATA1100  CALL      RKPMMSW1
          BRANCH    OVRCD,PATA1200
.
          MATCH     PMBDHOSP,PMMWHOSP
          GOTO      PATA1200 IF NOT EQUAL
.
          MATCH     PMBDWARD,PMMWWARD
          GOTO      PATA1200 IF NOT EQUAL
.
          MATCH     PMBDBEDD,PMMWBEDC
          GOTO      PATA1200 IF NOT EQUAL
.
          PACK      KEY5,ANSC,ANSG,PMMWWCLS,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,PATA1100
.
          MATCH     TCINDC1,DIM1            * No speciality mismatch
          GOTO      PATA2000 IF EQUAL
.
          GOTO      PATA1100
.
PATA1200  WRITE     HTMLFILE;"<img class=Icon":
                             " alt='Speciality Mismatch'":
                             " title='Speciality Mismatch'":
                             " src='../images/SpecialityMismatch.png'>";
.
PATA2000  MATCH     "1",PTCNHFSD
          IF        !@EQUAL
            MATCH     "1",PMBDHFST
            IF        @EQUAL
              WRITE     HTMLFILE;"<img class=Icon":
                                 " alt='Health Fund Stepdown'":
                                 " title='Health Fund Stepdown'":
                                 " src='../images/yellowclock.gif'>";
            ENDIF
          ENDIF
.
PATA3000  MATCH     SP70,PMVXPACC
          GOTO      PATA4000 IF EQUAL
.
          PACK      KEY5,ANSB,ANSP,PMVXPACC,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,PATA4000
.
          MATCH     "0",TCINDC1             * No preferred accommodation
          GOTO      PATA4000 IF EQUAL
.
          MOVE      TCINDC1,DIM1
.
          PACK      KEY6,PMBDWARD,PMBDBEDD,SP70    * Read ward bed record
          CALL      RDWARD1
          BRANCH    OVRCD,PATA4000
.
          PACK      KEY5,ANSR,ANST,WRTYPE,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,PATA4000
.
          MATCH     TCINDC1,DIM1
          GOTO      PATA4000 IF EQUAL
.
          WRITE     HTMLFILE;"<img class=Icon":
                             " alt='Bed Preference Mismatch'":
                             " title='Bed Preference Mismatch'":
                             " src='../images/garnet.gif'>";
.
PATA4000  MATCH     WORKDATE,PMBDTBDT
          GOTO      PATA5000 IF NOT EQUAL
.
          MATCH     SP70,THETSTAT
          IF        @EQUAL
            COMPARE   TWO,BKRESTAT
            GOTO      PATA5000 IF EQUAL
.
            MATCH     SP70,BKRETYPE
            GOTO      PATA5000 IF EQUAL
            GOTO      PATA5000 IF EOS
.
            PACK      KEY5,ANSB,ANSK,BKRETYPE,SP70
            CALL      RDCODE1
            BRANCH    OVRCD,PATA5000
.
            MATCH     "THE ",THCSCOD
            GOTO      PATA5000 IF NOT EQUAL
.
            WRITE     HTMLFILE;"<img class=Icon":
                               " alt='Theatre Today' title='Theatre Today'":
                               " src='../images/OutpatientStatus1.gif'>";
          ELSE
            WRITE     HTMLFILE;"<span class=#"Red Bold#">":
                               THETSTAT,THETRBAY,"</span>";
          ENDIF
.
PATA5000  COMPARE   "1",PCEASE                   * Deceased
          GOTO      PATA6000 IF NOT EQUAL
.
          IF        OVERLAPF=1
            WRITE     HTMLFILE;"<span class=#"White Bold#">":
                               "DECEASED</span>";
          ELSE
            WRITE     HTMLFILE;"<span class=#"Red Bold#">":
                               "DECEASED</span>";
          ENDIF
.
PATA6000  CALL      CHKD0000                     * Duplicate Names
.
          IF        DUPNMFLG=1
            WRITE     HTMLFILE;"<img class=Icon":
                               " alt='Patient Name Conflict'":
                               " title='Patient Name Conflict'":
                               " src='../images/samename.gif'>";
          ENDIF
.
PATA9999  RETURN
.
.------------------------------------------------------------
.  Display expected patient alert icons
.------------------------------------------------------------
EXPA0000  MOVE      ZERO,EXIT
          PROC      DISPALRT                * check for Disability Alerts
          IF        EXIT=0 | EXIT=2
            MATCH     "1",PMPXSIN7
            IF        @EQUAL
              WRITE     HTMLFILE;"<img class=Icon":
                                 " alt='Medical Alerts' title='Medical Alerts'":
                                 " src='../images/AlertIconM.gif'>";
            ENDIF
.
            MATCH     "1",PMPXSIN8
            IF        @EQUAL
              WRITE     HTMLFILE;"<img class=Icon":
                                 " alt='Micro Alerts' title='Micro Alerts'":
                                 " src='../images/AlertIconB.gif'>";
            ENDIF
.
            MATCH     "1",PMPXSIN9
            IF        @EQUAL
              WRITE     HTMLFILE;"<img class=Icon":
                                 " alt='Risk Alerts' title='Risk Alerts'":
                                 " src='../images/AlertIconR.gif'>";
            ENDIF
.
            MATCH     "1",PMPXSN11
            IF        @EQUAL
              WRITE     HTMLFILE;"<img class=Icon":
                                 " alt='Chronic Alerts' title='Chronic Alerts'":
                                 " src='../images/AlertIconC.gif'>";
            ENDIF
.
            MATCH     "1",PTMXSIN1
            IF        @EQUAL
              WRITE     HTMLFILE;"<img class=Icon":
                                 " alt='Alerts' title='Alerts'":
                                 " src='../images/AlertIcon.gif'>";
              GOTO      EXPA1000
            ENDIF
.
            MATCH     "2",PTMXSIN1
            IF        @EQUAL
              WRITE     HTMLFILE;"<img class=Icon":
                                 " alt='Alerts' title='Alerts'":
                                 " src='../images/AlertIconBlack.gif'>";
              GOTO      EXPA1000
            ENDIF
.
            MATCH     "3",PTMXSIN1
            IF        @EQUAL
              WRITE     HTMLFILE;"<img class=Icon":
                                 " alt='Alerts' title='Alerts'":
                                 " src='../images/AlertIconDelete.gif'>";
              GOTO      EXPA1000
            ENDIF
.
            MATCH     SP70,PTMXSIN1
            IF        !@EQUAL
              WRITE     HTMLFILE;"<img class=Icon":
                                 " alt='Alerts' title='Alerts'":
                                 " src='../images/AlertIcon",PTMXSIN1,".gif'>";
              GOTO      EXPA1000
            ENDIF
          ENDIF
.
EXPA1000  IF        EXIT=1 | EXIT=2
            WRITE     HTMLFILE;"<img class=Icon":
                               " alt='Disability Alerts'":
                               " title='Disability Alerts'":
                               " src='../images/AlertIconDIS.gif'>"
          ENDIF
.
          IF        PSAGEYRS<CAGECOFF
            WRITE     HTMLFILE;"<img class=Icon":
                               " alt='Paediatric' title='Paediatric'":
                               " src='../images/PaediatricIcon.gif'>";
          ENDIF     
.
          MATCH     SP70,ACLSSFT           * Blank speciality
          GOTO      EXPA3000 IF EQUAL
.
          PACK      KEY5,ANSA,ANSC,ACLSSFT,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,EXPA3000
.
          MATCH     ANSD,TCINDC4            * Do do speciality mismatch
          GOTO      EXPA3000 IF EQUAL
.
          MOVE      TCINDC1,DIM1
.
          MATCH     "~~W",PMEBWARD          * Ward not assigned
          GOTO      EXPA3000 IF EQUAL    
.
          PACK      KEY6,PMEBWARD,SP70      * Read ward master record
          CALL      RDWARD1
          BRANCH    OVRCD,EXPA3000
.
          MATCH     SP70,WCLASS
          GOTO      EXPA3000 IF EQUAL
.
          PACK      KEY5,ANSC,ANSG,WCLASS,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,EXPA3000
.
          MATCH     TCINDC1,DIM1            * No speciality mismatch
          GOTO      EXPA3000 IF EQUAL
.
          PACK      KEY12,PMEBHOSP,PMEBWARD,PMEBBEDD,SP70
          CALL      RSPMMSW1
EXPA1100  CALL      RKPMMSW1
          BRANCH    OVRCD,EXPA1200
.
          MATCH     PMEBHOSP,PMMWHOSP
          GOTO      EXPA1200 IF NOT EQUAL
.
          MATCH     PMEBWARD,PMMWWARD
          GOTO      EXPA1200 IF NOT EQUAL
.
          MATCH     PMEBBEDD,PMMWBEDC
          GOTO      EXPA1200 IF NOT EQUAL
.
          PACK      KEY5,ANSC,ANSG,PMMWWCLS,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,EXPA1100
.
          MATCH     TCINDC1,DIM1            * No speciality mismatch
          GOTO      EXPA3000 IF EQUAL
.
          GOTO      EXPA1100
.
EXPA1200  WRITE     HTMLFILE;"<img class=Icon":
                             " alt='Speciality Mismatch'":
                             " title='Speciality Mismatch'":
                             " src='../images/SpecialityMismatch.png'>";
.
EXPA3000  MATCH     SP70,PMVXPACC
          GOTO      EXPA4000 IF EQUAL
.
          PACK      KEY5,ANSB,ANSP,PMVXPACC,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,EXPA4000
.
          MATCH     "0",TCINDC1             * No preferred accommodation
          GOTO      EXPA4000 IF EQUAL
.
          MOVE      TCINDC1,DIM1
.
          PACK      KEY6,PMEBWARD,PMEBBEDD,SP70    * Read ward bed record
          CALL      RDWARD1
          BRANCH    OVRCD,EXPA4000
.
          PACK      KEY5,ANSR,ANST,WRTYPE,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,EXPA4000
.
          MATCH     TCINDC1,DIM1
          GOTO      EXPA4000 IF EQUAL
.
          WRITE     HTMLFILE;"<img class=Icon":
                             " alt='Bed Preference Mismatch'":
                             " title='Bed Preference Mismatch'":
                             " src='../images/garnet.gif'>";
.
EXPA4000  MATCH     WORKDATE,PMEBTBDT
          GOTO      EXPA5000 IF NOT EQUAL
.
          MATCH     SP70,THETSTAT
          IF        @EQUAL 
            COMPARE   TWO,BKRESTAT
            GOTO      PATA5000 IF EQUAL
.
            WRITE     HTMLFILE;"<img class=Icon":
                               " alt='Theatre Today' title='Theatre Today'":
                               " src='../images/OutpatientStatus1.gif'>";
          ELSE
            WRITE     HTMLFILE;"<span class=#"Red Bold#">":
                               THETSTAT,THETRBAY,"</span>";
          ENDIF
.
EXPA5000  COMPARE   "1",PCEASE                   * Deceased
          GOTO      EXPA6000 IF NOT EQUAL
.
          IF        OVERLAPF=1
            WRITE     HTMLFILE;"<span class=#"White Bold#">":
                               "DECEASED</span>";
          ELSE
            WRITE     HTMLFILE;"<span class=#"Red Bold#">":
                               "DECEASED</span>";
          ENDIF
.
EXPA6000  CALL      CKDE0000                     * Duplicate Names
.
          IF        DUPNMFLG=1
            WRITE     HTMLFILE;"<img class=Icon":
                               " alt='Patient Name Conflict'":
                               " title='Patient Name Conflict'":
                               " src='../images/samename.gif'>";
          ENDIF
.
EXPA9999  RETURN
.
.------------------------------------------------------------
. Update Expected/Post-Op Ward/Bed
.------------------------------------------------------------
ALLC0000  MATCH     SP70,UPDTTYPE
          GOTO      ALLC7000 IF EQUAL
.         
          MATCH     "U",UPDTTYPE            * Update
          GOTO      ALLC1000 IF EQUAL
.
          GOTO      ALLC9000
.
ALLC1000  MOVE      ZERO,UPDATFLG
          MOVE      ZERO,THEAUDIT
.
          PACK      KEY8,ADMISSNO,SP70
          CALL      RDMISS1 
          BRANCH    OVRCD,ALLC1100
.
          COMPARE   TWO,ASTAT               * admissions only
          GOTO      ALLC1100 IF EQUAL
.
          COMPARE   ONE,ASTAT               * Pre admissions only
          GOTO      ALLC1200 IF NOT EQUAL
.
          MATCH     PTMIS068,PTMIXWRD
          IF        @EQUAL
            MATCH     PTMIS076,PTMIEBED     * No change in expected ward/bed
            GOTO      ALLC1100 IF EQUAL
          ENDIF
.
          MATCH     Z70,PTMIS068
          IF        !@EQUAL
            MOVE      PTMIS068,PTMIXWRD
          ENDIF
.
          MATCH     Z70,PTMIS076
          IF        !@EQUAL
            MOVE      PTMIS076,PTMIEBED
          ENDIF
.
          MOVE      ONE,UPDATFLG
          CALL      UPMISS1
.
ALLC1100  PACK      KEY8,ADMISSNO,SP70
          CALL      RDPMVX11
          BRANCH    OVRCD,ALLC1200
.
          MATCH     BOKRX002,PMVXPOWD
          IF        @EQUAL
            MATCH     BOKRX038,PMVXPOBD          * No change of post-op ward/bed
            GOTO      ALLC1200 IF EQUAL
          ENDIF
.
          MATCH     BOKRX002,Z70
          IF        !@EQUAL
            MOVE      BOKRX002,PMVXPOWD
          ENDIF
.
          MATCH     BOKRX038,Z70
          IF        !@EQUAL
            MOVE      BOKRX038,PMVXPOBD
          ENDIF
.
          MOVE      ONE,UPDATFLG
          CALL      UPPMVX11
.
ALLC1200  PACK      KEY8,ADMISSNO,SP70
          CALL      RDBKREC1
          BRANCH    OVRCD,ALLC1900
.
          COMPARE   THREE,BKRESTAT               * If admitted allow update
          GOTO      ALLC1300 IF EQUAL            * of post op ward
.
          IF        BKRESTAT<>0 & BKRESTAT<>1
            GOTO      ALLC1900                   * Booked and Pre admitted only
          ENDIF
.
          MATCH     Z70,PTMIS068
          IF        @EQUAL
            MATCH     Z70,PTMIS076
            GOTO      ALLC1300 IF EQUAL
          ENDIF
.
          MATCH     PTMIS068,BKREWARD
          IF        @EQUAL
            MATCH     PTMIS076,BKREEBED     * No change in expected ward/bed
            GOTO      ALLC1300 IF EQUAL     
          ENDIF
.         
          MATCH     Z70,PTMIS068
          IF        !@EQUAL
            MOVE      PTMIS068,BKREWARD
          ENDIF
.         
          MATCH     Z70,PTMIS076
          IF        !@EQUAL
            MOVE      PTMIS076,BKREEBED
          ENDIF
.
          MOVE      ONE,UPDATFLG
          CALL      UPBKREC1
.
          CALL      TAUD0000                * write theatre audit
          MOVE      ONE,THEAUDIT
.
ALLC1300  PACK      KEY8,ADMISSNO,SP70
          CALL      RDBKRX11
          BRANCH    OVRCD,ALLC1900
.
          MATCH     BOKRX002,Z70
          IF        @EQUAL
            MATCH     BOKRX038,Z70
            GOTO      ALLC1900 IF EQUAL
          ENDIF
.
          MATCH     BOKRX002,BKRXPOWD            * No change of post-op ward/bed
          IF        @EQUAL
            MATCH     BOKRX038,BKRXPOBD
            GOTO      ALLC1900 IF EQUAL
          ENDIF
.
          MATCH     BOKRX002,Z70
          IF        !@EQUAL
            MOVE      BOKRX002,BKRXPOWD
          ENDIF
.
          MATCH     BOKRX038,Z70
          IF        !@EQUAL
            MOVE      BOKRX038,BKRXPOBD
          ENDIF
.
          MOVE      ONE,UPDATFLG
          CALL      UPBKRX11
.
          IF        THEAUDIT=0
            CALL      TAUD0000                * write theatre audit
          ENDIF
.
ALLC1900  IF        UPDATFLG=1
            CALL      PBDM0000
            PROC      BBUP0000              * Bed Board 
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      ALLC9999
.
ALLC7000  MOVE      ZERO,THETFLAG
          PACK      KEY8,URNUMBER,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,ALLC9100
.
          CALL      RDPMPX21
          BRANCH    OVRCD,ALLC9100
.
          PACK      KEY8,ADMISSNO,SP70
          CALL      RDMISS1
          IF        OVRCD=1
            CALL      CLPATMIS
          ENDIF
.
          PACK      KEY8,ADMISSNO,SP70
          CALL      RDPMVX11
          IF        OVRCD=1
            CALL       CLPMSVX1
          ENDIF
.
          PACK      KEY8,ADMISSNO,SP70
          CALL      RDBKREC1
          IF        OVRCD=1
            CALL      CLBOKRC1
          ELSE
            MOVE      ONE,THETFLAG
          ENDIF
.
          PACK      KEY8,ADMISSNO,SP70
          CALL      RDBKRX11
          IF        OVRCD=1
            CALL      CLBOKRX1
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Update Expected Ward/Bed",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,ALLC9999
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      ALLC9999
.
ALLC9000  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ALLC9999
.
ALLC9100  CLEAR     WEBTITLE
          APPEND    "Invalid U/R Number - ",WEBTITLE
          APPEND    URNUMBER,WEBTITLE
          RESET     WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ALLC9999
.
ALLC9999  RETURN
.
.------------------------------------------------------------
. Write theatre audit if one not already written. Find
. the first theatre record for this admission  and status
.------------------------------------------------------------
TAUD0000  MOVE      ZERO,F1
          LOAD      F1,BKRESTAT,ONE,THREE,TWO,FOUR  * Get status for theatre
.
          PACK      KEY31,ADMISSNO,F1,SP70
          CALL      RSOPDEA2                     * Find first theatre record
          CALL      RKOPDEA2                     * using this admission number
          BRANCH    OVRCD,TAUD9999
.
          MATCH     ADMISSNO,OPDAADMN            * Correct visit number
          GOTO      TAUD9999 IF NOT EQUAL
.
          COMPARE   F1,OPDASTAT                  * Correct status
          GOTO      TAUD9999 IF NOT EQUAL
.
          MOVE      "020",TADTTYPE
          PROC      OPTHETAT                     * write add team audit detail
.
TAUD9999  RETURN

.
.------------------------------------------------------------
. Update Expected/Post-Op Ward/Bed
.------------------------------------------------------------
HOUS0000  MATCH     SP70,UPDTTYPE
          GOTO      HOUS7000 IF EQUAL
.
          MATCH     "A",UPDTTYPE            * Add
          GOTO      HOUS1000 IF EQUAL
.
          MATCH     "U",UPDTTYPE            * Update
          GOTO      HOUS2000 IF EQUAL
.
          MATCH     "D",UPDTTYPE            * Delete
          GOTO      HOUS3000 IF EQUAL
.
          GOTO      HOUS9000
.
HOUS1000  PACK      KEY25,PMHRD001,PMHRD002,PMHRD003,PMHRD004,PMHRD005,SP70
          CALL      RDPMHRD1
          COMPARE   ZERO,OVRCD
          GOTO      HOUS9100 IF EQUAL
.
          CALL      CLPMSHRD      
.
          CALL      MVPMHR00                     * Move cgi to file vars
.
          MOVE      USERID,PMHRWEBU 
          PACK      PMHRDATU,CCC,CYY,CMM,CDD
          REP       " 0",PMHRDATU
          CLOCK     TIME,PMHRTIMU
.
          PACK      KEY25,PMHRHOSP,PMHRWARD,PMHRBEDD,PMHRRDAT,PMHRRTIM,SP70
          CALL      WRPMHRD1
.
          MOVE      ZERO,EXIT
          GOTO      HOUS9999
.
HOUS2000  PACK      KEY25,BEDREQKY,SP70
          CALL      RDPMHRD1
          BRANCH    OVRCD,HOUS9200
.
          CALL      MVPMHR00                     * Move cgi to file vars
.         
          MOVE      USERID,PMHRWEBU
          PACK      PMHRDATU,CCC,CYY,CMM,CDD
          REP       " 0",PMHRDATU
          CLOCK     TIME,PMHRTIMU
.         
          CALL      UPPMHRD1
.
          MOVE      ZERO,EXIT
          GOTO      HOUS9999
.
HOUS3000  PACK      KEY25,BEDREQKY,SP70
          CALL      RDPMHRD1
          BRANCH    OVRCD,HOUS9200
.         
          CALL      DEPMHRD1
.         
          MOVE      ZERO,EXIT
          GOTO      HOUS9999
.
HOUS7000  CALL      CURPER00
          CALL      CALCDT00   * Calculate Next and Last Period Dates
.
          PACK      KEY25,BEDREQKY,SP70
          CALL      RDPMHRD1
          IF        OVRCD=1
            CALL      CLPMSHRD      
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "Housekeeping",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,HOUS9999
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
.
          MOVE      ONE,EXIT
          GOTO      HOUS9999
.
HOUS9000  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      HOUS9999
.
HOUS9100  MOVE      "Housekeeping Request Exists.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      HOUS9999
.
HOUS9200  MOVE      "Housekeeping Request Does Not Exists.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      HOUS9999
.
HOUS9999  RETURN
.
.------------------------------------------------------------
. General Bed Board Tags
.------------------------------------------------------------
HLST0000  PACK      KEY25,FRSTDATE,SP70
.
          CALL      RSPMHRD2
HLST1000  CALL      RKPMHRD2
          BRANCH    OVRCD,HLST9999
.
          MATCH     PMHRRDAT,LASTDATE
          GOTO      HLST9999 IF LESS
.
          IF        IBCNMHOS=1
            MATCH     WBSEHOSP,PMHRHOSP
            GOTO      HLST1000 IF NOT EQUAL
          ENDIF
.           
          MATCH     SP70,FILTWARD
          IF        !@EQUAL
            MATCH     FILTWARD,PMHRWARD
            GOTO      HLST1000 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,FILTRSTA
          IF        !@EQUAL
            MATCH     FILTRSTA,PMHRRSTA
            GOTO      HLST1000 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,FILTCSTA
          IF        !@EQUAL
            MATCH     FILTCSTA,PMHRCSTA
            GOTO      HLST1000 IF NOT EQUAL
          ENDIF
.
HLST1100  PACK      KEY25,PMHRHOSP,PMHRWARD,PMHRBEDD,PMHRRDAT,PMHRRTIM,SP70
.
          CLEAR     HTMLLINK
          APPEND    "javascript:UpdateHouse('",HTMLLINK
          APPEND    KEY25,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
          MOVE      SP70,DIM20A
          MATCH     SP70,PMHRRSTA
          IF        !@EQUAL
            PACK      KEY5,ANSB,ANSV,PMHRRSTA,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      PMHRRSTA,TDESC
            ENDIF
            MOVE      TDESC,DIM20A
          ENDIF
.
          MOVE      SP70,DIM20B
          MATCH     SP70,PMHRCSTA
          IF        !@EQUAL
            PACK      KEY5,ANSB,ANSV,PMHRCSTA,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      PMHRCSTA,TDESC
            ENDIF
            MOVE      TDESC,DIM20B
          ENDIF
.
          MOVE      SP70,DIM20C
          MATCH     SP70,PMHRSTAT
          IF        !@EQUAL
            PACK      KEY5,CATHW,PMHRSTAT,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      PMHRSTAT,TDESC
            ENDIF
            MOVE      TDESC,DIM20C
          ENDIF
.
          MOVE      SP70,DIM20D
          MATCH     SP70,PMHRCRES
          IF        !@EQUAL
            PACK      KEY5,CATHX,PMHRCRES,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      PMHRCRES,TDESC
            ENDIF
            MOVE      TDESC,DIM20D
          ENDIF
.
          PACK      KEY6,PMHRWARD,SP70
          CALL      RDWARD1
          IF        OVRCD=1
            MOVE      PMHRWARD,WBDESC
          ENDIF
.
          WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":      *0
                             "#"",PMHRHOSP,"#",":               *1
                             "#"",PMHRWARD,"#",":               *2
                             "#"",PMHRBEDD,"#",":               *3
                             "#"",PMHRRDAT,"#",":               *4
                             "#"",PMHRRTIM,"#",":               *5
                             "#"",PMHRRDAT,PMHRRTIM,"#",":      *6
                             "#"",PMHRRSTA,"#",":               *7
                             "#"",DIM20A,"#",":                 *8
                             "#"",PMHRCSTA,"#",":               *9
                             "#"",DIM20B,"#",":                 *10
                             "#"",PMHRCDAT,"#",":               *11
                             "#"",PMHRCTIM,"#",":               *12
                             "#"",PMHRCDAT,PMHRCTIM,"#",":      *13
                             "#"",PMHRSTAT,"#",":               *14
                             "#"",DIM20C,"#",":                 *15
                             "#"",PMHRCOM1,"<br>",PMHRCOM2,"#",":  *16
                             "#"",PMHRCRES,"#",":                  *17
                             "#"",DIM20D,"#",":                    *18
                             "#"",WBDESC,"#");"                    *19
.
          GOTO      HLST1000
.
HLST9999  RETURN
.
.------------------------------------------------------------
. Search expected patent bed board for patient
.------------------------------------------------------------
SEXP0000  PACK      CURRDATE,CCC,CYY,CMM,CDD     * Set up current date
          REP       " 0",CURRDATE
.
          PACK      WORKDATE,CCC,CYY,CMM,CDD     * Set up current date
          REP       " 0",WORKDATE
          MOVE      CTIMEIS,WORKTIME             * Set up current time
.
          MOVE      ONE,VIEWSEQN                 * Default view sequence to 1
          MOVE      WBSEBEDV,VIEWSEQN
.
          IF        TOMRFLAG=1
            MOVE      WORKDATE,TOMRDATE          * Set tomorrows date for
            ADD       ONE,TOMRDATE               * tomorrows view
          ELSE
            MOVE      WORKDATE,TOMRDATE          * Set tomorrows date to today
          ENDIF
.
          MOVE      ZERO,RECFOUND
          MATCH     SP70,SRCHSNAM                * Exit if no search criteria
          IF        @EQUAL | @EOS
            MATCH     SP70,SRCHGNAM
            GOTO      SEXP9999 IF EQUAL
            GOTO      SEXP9999 IF EOS
          ENDIF
.
          IF        IBCNMHOS=1
            PACK      KEY20,WBSEHOSP,SP70
          ELSE
            PACK      KEY20,SP70
          ENDIF
          CALL      RSPMEPB1
SEXP1000  CALL      RKPMEPB1
          BRANCH    OVRCD,SEXP9000
.
          IF        IBCNMHOS=1
            MATCH     WBSEHOSP,PMEBHOSP
            GOTO      SEXP9000 IF NOT EQUAL
          ENDIF
.
. Change to Search All Regardless of Bed Board View 
.         MOVE     ZERO,F2
.         LOAD     F2,VIEWSEQN,PMEBSEQ1,PMEBSEQ2,PMEBSEQ3,PMEBSEQ4,PMEBSEQ5
.         COMPARE  ZERO,F2                  * Skip patients who are in wards
.         GOTO     SEXP1000 IF EQUAL        * that are no displayed on bed board
.
          IF        TOMRFLAG=1
            MATCH     WORKDATE,PMEBEDAT
            GOTO      SEXP1000 IF LESS
 
            MATCH     PMEBEDAT,TOMRDATE
            GOTO      SEXP1000 IF LESS
          ELSE
            MATCH     WORKDATE,PMEBEDAT            * Only select today
            GOTO      SEXP1000 IF NOT EQUAL
          ENDIF
. 
          MATCH     SP70,SRCHSNAM
          IF        !@EQUAL
            MATCH     SRCHSNAM,PMEBSURN
            GOTO      SEXP1000 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,SRCHGNAM
          IF        !@EQUAL & !@EOS
            MATCH     SRCHGNAM,PMEBGIVN
            GOTO      SEXP1000 IF NOT EQUAL
          ENDIF
.
          MATCH     "~~B",PMEBWARD
          IF        @EQUAL
            MOVE      "Bed Requests",WBDESC
            GOTO      SEXP4000
          ENDIF
.
          MATCH     "~~W",PMEBWARD
          IF        @EQUAL
            MOVE      "Ward Not Specified",WBDESC
            GOTO      SEXP4000
          ENDIF
.
SEXP4000  MOVE      ONE,RECFOUND
          MOVE      PMEBURNO,URNUMBER
          MOVE      PMEBADMN,ADMISSNO
          CALL      GETPAT00
          IF        NOUPDATE=0
            WRITE     HTMLFILE;"<tr><td class=Red>":
                               "<a class=#"Red Bold#"":
                               " href='":
                               "javascript:UpdateExpected":
                               "(#"",PMEBURNO,"#",#"",PMEBADMN,"#")'>":
                                PMEBSURN,",",PMEBGIVN,"</a>";
          ELSE
            WRITE     HTMLFILE;"<tr><td align=center class=Red>":
                               PMEBSURN,",",PMEBGIVN;
          ENDIF
.
          PACK      KEY6,PMEBWARD,SP70
          CALL      RDWARD1
          IF        OVRCD=1
            MATCH     "~~B",PMEBWARD
            IF        @EQUAL
              MOVE      "Req",WBDESC
            ELSE
              MATCH     "~~W",PMEBWARD
              IF        @EQUAL
                MOVE      "Ward Not Specified",WBDESC
              ELSE
                MOVE      PMEBWARD,WBDESC
              ENDIF
            ENDIF
          ENDIF
          MOVE     WINPUT,SVWINPUT
          WRITE    HTMLFILE;SP1,LBRACK,WBDESC;
.
          MATCH    SP70,PMEBBEDD
          IF       @EQUAL
            WRITE    HTMLFILE;RBRACK;
          ELSE
            IF        WBEDDESC > 0
              PACK      KEY6,PMEBWARD,PMEBBEDD,SP70
              CALL      RDWARD1
              IF        OVRCD=1
                MATCH     "~~B",PMEBBEDD
                IF        @EQUAL
                  MOVE      "Req",WBDESC
                ELSE
                  MATCH     "~~W",PMEBBEDD
                  IF        @EQUAL
                    MOVE      "Ward Not Specified",WBDESC
                  ELSE
                    MOVE      PMEBBEDD,WBDESC
                  ENDIF
                ENDIF
              ENDIF    
              IF WBEDDESC = 2 
                WRITE    HTMLFILE;SLASH,PTWDSDES,RBRACK;
              ELSE
                WRITE    HTMLFILE;SLASH,WBDESC,RBRACK;
              ENDIF
            ELSE
              MATCH     "~~B",PMEBWARD
              IF        @EQUAL
                MOVE      "Req",PMEBBEDD
              ENDIF
              MATCH     "~~W",PMEBWARD
              IF        @EQUAL
                MOVE      SP70,PMEBBEDD
              ENDIF
              WRITE    HTMLFILE;SLASH,PMEBBEDD,RBRACK;
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;"</td></tr>";
.
          GOTO      SEXP1000
.
SEXP9000  COMPARE   ZERO,RECFOUND
          GOTO      SEXP9100 IF NOT EQUAL
.
          WRITE     HTMLFILE;"<tr><td align=center><b>End of Expected Patient ":
                             "Search</b></td></tr>";
.
SEXP9100  WRITE     HTMLFILE;"<tr><td><hr></td></tr>";
.
SEXP9999  RETURN
.
.------------------------------------------------------------
. Unallocated Patients Bed Board View
.------------------------------------------------------------
DUNL0000  MOVE      ZERO,UNALPTOT                * Unallocated patient count
          MOVE      ZERO,ROOMCNTR
          MOVE      ZERO,WARDCNTR
.
          MATCH     Z70,BEDBHOSP
          IF        !@EQUAL
            MOVE      BEDBHOSP,VIEWHOSP
          ELSE
            IF        IBCNMHOS=1
              MOVE      WBSEHOSP,VIEWHOSP          * Default view hospital id
            ELSE                                   * from user
              MOVE      SP70,VIEWHOSP
            ENDIF
          ENDIF
.
          PACK      CURRDATE,CCC,CYY,CMM,CDD     * Set up current date
          REP       " 0",CURRDATE
.
          PACK      WORKDATE,CCC,CYY,CMM,CDD     * Set up current date
          REP       " 0",WORKDATE
          MOVE      CTIMEIS,WORKTIME             * Set up current time
.
          IF        TOMRFLAG=1
            MOVE      WORKDATE,TOMRDATE          * Set tomorrows date for
            ADD       ONE,TOMRDATE               * tomorrows view
          ELSE
            MOVE      WORKDATE,TOMRDATE          * Set tomorrows date to today
          ENDIF
.
          MOVE      WORKDATE,YESTERDY  
          SUB       ONE,YESTERDY
.
          IF        BEDBSEQN=0
            MOVE      ONE,VIEWSEQN               * Default view sequence to 1
            MOVE      WBSEBEDV,VIEWSEQN
          ELSE
            MOVE      BEDBSEQN,VIEWSEQN
          ENDIF
.
          CALL      SPEC0000                     * Load speciality array
.
          MOVE      SP70,SAVEWARD                * Ward being processed
          MOVE      ZERO,FRSTWARD                * Is this this first ward
.
          PACK      KEY22,VIEWHOSP,SP1,ONE,SP70
          BRANCH    VIEWSEQN,DUNL0100,DUNL0200,DUNL0300,DUNL0400,DUNL0500
          GOTO      DUNL9999
.
DUNL0100  CALL      RSPMBBD3
          GOTO      DUNL1000
.
DUNL0200  CALL      RSPMBBD4
          GOTO      DUNL1000
.
DUNL0300  CALL      RSPMBBD5
          GOTO      DUNL1000
.
DUNL0400  CALL      RSPMBBD6
          GOTO      DUNL1000
.
DUNL0500  CALL      RSPMBBD7
          GOTO      DUNL1000
.
DUNL1000  BRANCH    VIEWSEQN,DUNL1100,DUNL1200,DUNL1300,DUNL1400,DUNL1500
.
DUNL1100  CALL      RKPMBBD3
          BRANCH    OVRCD,DUNL9000
          GOTO      DUNL1700
.
DUNL1200  CALL      RKPMBBD4
          BRANCH    OVRCD,DUNL9000
          GOTO      DUNL1700
.
DUNL1300  CALL      RKPMBBD5
          BRANCH    OVRCD,DUNL9000
          GOTO      DUNL1700
.
DUNL1400  CALL      RKPMBBD6
          BRANCH    OVRCD,DUNL9000
          GOTO      DUNL1700
.
DUNL1500  CALL      RKPMBBD7
          BRANCH    OVRCD,DUNL9000
          GOTO      DUNL1700
.
DUNL1700  IF        IBCNMHOS=1
            MATCH     VIEWHOSP,PMBDHOSP          * Same hospital id
            GOTO      DUNL9000 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,SAVEWARD
          IF        @EQUAL
            MOVE      PMBDWARD,SAVEWARD
            GOTO      DUNL2000
          ENDIF
.
          MATCH     SAVEWARD,PMBDWARD
          GOTO      DUNL3000 IF EQUAL
.
          CALL      ECEL0000                     * Ward changed so print prev
.                                                * wards expected patients
          MOVE      PMBDWARD,SAVEWARD            * Save ward being processed
          MOVE      SP70,SAVEROOM                * Wards room being processed
          MOVE      SP70,ROOMGEND                * Room gender
.
DUNL2000  IF        FRSTWARD=1
            WRITE     HTMLFILE;"</table></td>"
          ENDIF
.
          MOVE      ONE,FRSTWARD
.
          MOVE      SP70,SAVEROOM                * Wards room being processed
          MOVE      SP70,ROOMGEND                * Room gender
.
          PACK      KEY6,PMBDWARD,SP70           * Display ward code or
          CALL      RDWARD1                      * description
          IF        OVRCD=1
            CALL      CLRWRD00
            MOVE      WBDESC,SVWBDESC
            GOTO      DUNL2100
          ENDIF
          MOVE      WBDESC,SVWBDESC
.                                                * Print ward heading section
DUNL2100  CALL      CURS0000                     * Current Speciality 
.
          ADD       ONE,WARDCNTR
          MOVE      WARDCNTR,DIM3
          LJUSTIFY  DIM3
          PACK      DIM3,DIM3,SP70
.
          WRITE     HTMLFILE;"<td class=WardColumn><table class=BedBoard>":
                             "<tr>":
                             "<td colspan=2>":
                             "<div id=fldiv":
                             DIM3," style=#"position:relative;#">":
                             "<table class=BedBoard><tr>":
                             "<td class=HeadingBedCell bgcolor=#cccccc>";
.
          MATCH     "~~B",PMBDWARD
          GOTO      DUNL2200 IF EQUAL
.
          MATCH     "~~W",PMBDWARD
          GOTO      DUNL2200 IF EQUAL
.
          WRITE     HTMLFILE;"<img src=#"../images/EditIcon.gif#"":
                             " alt='Modify Available Beds'":
                             " title='Modify Available Beds'":
                             " class=ListIcon onclick='ModBeds(#"":
                               PMBDWARD,"#");'/>";
.
DUNL2200  WRITE     HTMLFILE;"&nbsp;</td><td class=HeadingPatientCell bgcolor=#cccccc>";
.
          MATCH     "~~B",PMBDWARD
          IF        @EQUAL
            WRITE     HTMLFILE;"<a href='":
                               "javascript:BedRequestLink":
                               "(#"",SJOGFLAG,"#")'>":
                               WBDESC,"</a>";
            WRITE     HTMLFILE;"</td></tr></table>":
                               "</div>":
                               "</td></tr>";
            CALL      RCEL0000                   * Request Bed Cells
            GOTO      DUNL1000
          ELSE
            MATCH     "~~W",PMBDWARD
            IF        @EQUAL
              WRITE     HTMLFILE;WBDESC;
            ELSE
              WRITE     HTMLFILE;"<a href='":
                                 "javascript:WardOccupancyLink":
                                 "(#"",PMBDWARD,"#")'>":
                                 WBDESC,"</a>";
            ENDIF
            WRITE     HTMLFILE;"</td></tr></table>":
                               "</div>":
                               "</td></tr>";
            CALL      PCEL0000                   * Expected Patients
            GOTO      DUNL1000
          ENDIF
.
          WRITE     HTMLFILE;"</td></tr></table>":
                             "</div>":
                             "</td></tr>";
          GOTO      DUNL1000
.
DUNL3000  CALL      UCEL0000                     * Print Bed/Patient Cell
          GOTO      DUNL1000
.
DUNL9000  CALL      ECEL0000                     * Ward changed so print prev
.                                                * wards expected patients
          IF        FRSTWARD=1
            WRITE     HTMLFILE;"</table></td>"   * Print end of ward
          ENDIF
.
DUNL9999  RETURN
.
.------------------------------------------------------------
. Print the patient cell for the unallocated patient bed board
. bed request column
.------------------------------------------------------------
RCEL0000  MATCH     "~~B",PMBDWARD
          GOTO      RCEL9999 IF NOT EQUAL
.
          PACK      KEY20,PMBDHOSP,PMBDWARD,SP70
          CALL      RSPMEPB1
RCEL1000  CALL      RKPMEPB1
          BRANCH    OVRCD,RCEL9999
.
          MATCH     PMBDHOSP,PMEBHOSP
          GOTO      RCEL9999 IF NOT EQUAL
.
          MATCH     PMBDWARD,PMEBWARD
          GOTO      RCEL9999 IF NOT EQUAL
.
          MOVE      PMEBURNO,URNUMBER
          MOVE      PMEBADMN,ADMISSNO
.
          PACK      KEY24,PMEBADMN,PMEBEDAT,PMEBETIM,SP70
          CALL      RDPMBRQ1
          BRANCH    OVRCD,RCEL1000
.
          CALL      GETPAT00
          BRANCH    EXIT,RCEL1000
.
          PACK      KEY5,ANSC,ANSC,ACARE,SP70
          CALL      RDCODE1
          IF        OVRCD<>1
            MATCH     ANSD,TCINDC11         * Don't display on bed board
            GOTO      RCEL1000 IF EQUAL
          ENDIF
.
          CALL      STAR0000                * Request patient onhover details
.
          CALL      PATFLD00                * Use standard Patient Folder sub
          MOVE      KEY3,FOLDERSF           * and store suffix
          MOVE      PGNAME,DIM1             * Given name initial
          STRIP     PSNAME
.
          WRITE     HTMLFILE;"<tr class=#"BedRow#">";
          WRITE     HTMLFILE;"<td class=#"BedCell";       * Display Bed Cell
.
          MATCH     ANSM,PMEBGEND
          IF        @EQUAL
            WRITE     HTMLFILE;" BlueBackground";
          ELSE
            MATCH     ANSF,PMEBGEND
            IF        @EQUAL
              WRITE     HTMLFILE;" PinkBackground";
            ELSE
              WRITE     HTMLFILE;" GreenBackground";
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;"#"></td>";
.
          WRITE     HTMLFILE;"<td class=#"PatientCell"
          WRITE     HTMLFILE;"#">";
.
          IF        ASTAT=2
            WRITE     HTMLFILE;"<span class=#"Black Bold#">";
          ELSE
            WRITE     HTMLFILE;"<span class=#"Red Bold#">";
          ENDIF
.
          WRITE     HTMLFILE;"<img src='../images/PatientFolder",FOLDERSF,".gif'":
                " onclick='LinktoPatient(#"",URNUMBER,"#",#"",ADMISSNO,"#");' ";
.
          IF        ASTAT=2
            WRITE     HTMLFILE;"onmouseover='show_it(#"",STATWIND,"#");'":
                               " onmouseout='hide_it();'":
                               " class=ListIcon \>":
                               "<a class=#"Black Bold#"";
          ELSE
            WRITE     HTMLFILE;"onmouseover='show_itE(#"",STATWIND,"#");'":
                               " onmouseout='hide_itE();'":
                               " class=ListIcon \>":
                               "<a class=#"Red Bold#"";
          ENDIF
.
          WRITE     HTMLFILE;" href='":
                             "javascript:UpdateRequest":
                             "(#"",PMEBADMN,"#",#"",DISPDATE,"#",#"":
                             PMBRREQT,"#",#"",PMEBURNO,"#")'>";
.
          WRITE     HTMLFILE;*+,PSNAME,DOT,DIM1,"</a>"
          RESET     PSNAME
.
          IF        ASTAT=1
            MOVE      PTMIXWRD,AWARD
            MOVE      PTMIEBED,ABED
          ENDIF
.
          MATCH     SP70,ABED
          IF        @EQUAL
            WRITE     HTMLFILE;*+,"(",AWARD,")";
          ELSE
            WRITE     HTMLFILE;*+,"(",AWARD,"/",ABED,")";
          ENDIF
.
RCEL2050  MOVE      "1",BREQFLAG
          CALL      SPCFLG00                * Special feature flag & bed desc
.
          MATCH     SP70,SPCBEDDS
          IF        !@EQUAL
            WRITE     HTMLFILE;"<br>";
            WRITE     HTMLFILE;SPCBEDDS;
          ENDIF
.
          WRITE     HTMLFILE;"&nbsp;</span></td></tr>"
.
          GOTO      RCEL1000
.
RCEL9999  RETURN
.
.------------------------------------------------------------
. Print the expected patients for this ward 
.------------------------------------------------------------
ECEL0000  BRANCH    IBCNMHOS,ECEL9999
.
          MATCH     "~~W",SAVEWARD
          IF        !@EQUAL
            MATCH     SP70,CURRSPEC
            GOTO      ECEL9999 IF EQUAL
          ENDIF
.
          PACK      KEY36,SP3,NOTAWARD,SP70
          CALL      RSPMEPB8
ECEL1000  CALL      RKPMEPB8
          BRANCH    OVRCD,ECEL9999
.
          MATCH     SP70,PMEBHOSP
          GOTO      ECEL9999 IF NOT EQUAL
.
          MATCH     "~~W",PMEBWARD
          GOTO      ECEL9999 IF NOT EQUAL
.
          MATCH     SP70,PMEBROOM
          GOTO      ECEL9999 IF NOT EQUAL
.
          MATCH     SP70,PMEBBEDD
          GOTO      ECEL9999 IF NOT EQUAL
.
          IF        TOMRFLAG=1
            MATCH     WORKDATE,PMEBEDAT
            GOTO      ECEL1000 IF LESS
.
            MATCH     PMEBEDAT,TOMRDATE
            GOTO      ECEL1000 IF LESS
          ELSE
            MATCH     WORKDATE,PMEBEDAT
            GOTO      ECEL1000 IF NOT EQUAL
          ENDIF
.
          MOVE      PMEBURNO,URNUMBER
          MOVE      PMEBADMN,ADMISSNO
.
          CALL      GETPAT00
          BRANCH    EXIT,ECEL1000
.
          PACK      KEY5,ANSC,ANSC,ACARE,SP70
          CALL      RDCODE1
          IF        OVRCD<>1
            MATCH     ANSD,TCINDC11         * Don't display on bed board
            GOTO      ECEL1000 IF EQUAL
          ENDIF
.
          CALL      NOTA0000                * Check speciality match
          BRANCH    EXIT,ECEL1000           * Ward not assigned
.
          CALL      STAE0000                * Expected patient onhover details
.
          WRITE     HTMLFILE;"<tr class=#"BedRow#">";
          WRITE     HTMLFILE;"<td class=#"BedCell";       * Display Bed Cell
.
          MATCH     ANSM,PMEBGEND
          IF        @EQUAL
            WRITE     HTMLFILE;" BlueBackground";
          ELSE
            MATCH     ANSF,PMEBGEND
            IF        @EQUAL
              WRITE     HTMLFILE;" PinkBackground";
            ELSE
              WRITE     HTMLFILE;" GreenBackground";
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;"#"></td>";
.
          WRITE     HTMLFILE;"<td class=#"PatientCell"
          WRITE     HTMLFILE;"#">";
.
          CALL      EXPA0000                * Display expected pat alert icons
.
          WRITE     HTMLFILE;"<br />"
.
          CALL      PATFLD00                * Use standard Patient Folder sub
          MOVE      KEY3,FOLDERSF           * and store suffix
          MOVE      PGNAME,DIM1             * Given name initial
          STRIP     PSNAME
.
          MOVE      ZERO,POSTOPFL
          MATCH     PMEBWARD,PMEBPOWD
          IF        @EQUAL
            MATCH     PMEBBEDD,PMEBPOBD
            IF        @EQUAL
              MATCH     SP70,PMEBTBDT
              IF        @EQUAL
                MOVE      ONE,POSTOPFL      * Post op record
              ELSE
                MATCH     WORKDATE,PMEBTBDT
                IF        @EQUAL
                  MOVE      ONE,POSTOPFL      * Post op record
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
          IF        POSTOPFL=1
            WRITE     HTMLFILE;"<span class=#"Blue Bold#">";
          ELSE
            IF        OVERLAPF=1
              WRITE     HTMLFILE;"<span class=#"White Bold#">";
            ELSE
              WRITE     HTMLFILE;"<span class=#"Red Bold#">";
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;"<img src='../images/PatientFolder",FOLDERSF,".gif'":
                " onclick='LinktoPatient(#"",URNUMBER,"#",#"",ADMISSNO,"#");' ";
.
          IF        STBYFLAG=1 | STBYFLAG=2
            WRITE     HTMLFILE;"onmouseover='show_it(#"",STATWIND,"#");'":
                               " onmouseout='hide_it();'";
          ELSE
            WRITE     HTMLFILE;"onmouseover='show_itE(#"",STATWIND,"#");'":
                               " onmouseout='hide_itE();'";
.
          ENDIF
          WRITE     HTMLFILE;" class=ListIcon \>";
.
          BRANCH    NOUPDATE,ECEL4000
.
          IF        POSTOPFL=1
            WRITE     HTMLFILE;"<a class=#"Blue Bold#"";
          ELSE
            IF        OVERLAPF=1
              WRITE     HTMLFILE;"<a class=#"White Bold#"";
            ELSE
              WRITE     HTMLFILE;"<a class=#"Red Bold#"";
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;" href='":
                             "javascript:UpdateExpected":
                             "(#"",PMEBURNO,"#",#"",PMEBADMN,"#")'>";
          STRIP     PSNAME
          WRITE     HTMLFILE;*+,PSNAME,DOT,DIM1,"</a>";
          RESET     PSNAME
          GOTO      ECEL5000
.
ECEL4000  STRIP     PSNAME
          WRITE     HTMLFILE;*+,PSNAME,DOT,DIM1;
          RESET     PSNAME
.
ECEL5000  IF        ASTAT=1
            MATCH     SP70,PMVXATIM
            IF        !@EQUAL
              MOVE      PMVXATIM,D5
              WRITE     HTMLFILE;*+,"(",D5,")";
            ENDIF
          ENDIF
.
          IF        POSTOPFL=1
            MATCH     SP70,PMEBEWRD
            IF        !@EQUAL
              SQUEEZE   PMEBEWRD
              IF        OVERLAPF=1
                WRITE     HTMLFILE;"<span class=#"White Bold#">";
              ELSE
                WRITE     HTMLFILE;"<span class=#"Red Bold#">";
              ENDIF
.
              MATCH     SP70,PMEBEBED
              IF        @EQUAL
                WRITE     HTMLFILE;*+,"(",PMEBEWRD,")";
              ELSE
                WRITE     HTMLFILE;*+,"(",PMEBEWRD,"/",PMEBEBED,")";
              ENDIF
              WRITE     HTMLFILE;"</span>";
              RESET     PMEBEWRD
            ENDIF
          ELSE
            MATCH     SP70,PMEBPOWD
            IF        !@EQUAL
              SQUEEZE   PMEBPOWD
              WRITE     HTMLFILE;"<span class=#"Blue#">";
              MATCH     SP70,PMEBPOBD
              IF        @EQUAL
                WRITE     HTMLFILE;*+,"(",PMEBPOWD,")";
              ELSE
                WRITE     HTMLFILE;*+,"(",PMEBPOWD,"/",PMEBPOBD,")";
              ENDIF
              WRITE     HTMLFILE;"</span>";
              RESET     PMEBPOBD
            ENDIF
          ENDIF
. 
          WRITE     HTMLFILE;"&nbsp;</span></td></tr>"
.
          GOTO      ECEL1000
.
ECEL9999  RETURN
.
.------------------------------------------------------------
. Print the expected patients for this ward that have no
. bed assigned
.------------------------------------------------------------
PCEL0000  MATCH     "~~W",SAVEWARD
          GOTO      PCEL9999 IF EQUAL
.
          MATCH     "~~B",SAVEWARD
          GOTO      PCEL9999 IF EQUAL
.
          MOVE      ZERO,STBYFLAG          * Standby flag
          MOVE      ZERO,OVERLAPF          * Overlap flag
.
          PACK      KEY36,PMBDHOSP,SAVEWARD,SP70
          CALL      RSPMEPB8
PCEL1000  CALL      RKPMEPB8
          BRANCH    OVRCD,PCEL9999
.
          MATCH     PMBDHOSP,PMEBHOSP
          GOTO      PCEL9999 IF NOT EQUAL
.
          MATCH     SAVEWARD,PMEBWARD
          GOTO      PCEL9999 IF NOT EQUAL
.
          MATCH     SP70,PMEBBEDD
          GOTO      PCEL1000 IF NOT EQUAL
.
          IF        TOMRFLAG=1
            MATCH     WORKDATE,PMEBEDAT   
            GOTO      PCEL1000 IF LESS
.
            MATCH     PMEBEDAT,TOMRDATE
            GOTO      PCEL1000 IF LESS
          ELSE
            MATCH     WORKDATE,PMEBEDAT
            GOTO      PCEL2000 IF EQUAL
            MATCH     YESTERDY,PMEBEDAT
            GOTO      PCEL1000 IF NOT EQUAL
          ENDIF
.
PCEL2000  MOVE      PMEBURNO,URNUMBER
          MOVE      PMEBADMN,ADMISSNO
.
          CALL      GETPAT00
          BRANCH    EXIT,PCEL1000
.
          PACK      KEY5,ANSC,ANSC,ACARE,SP70
          CALL      RDCODE1
          IF        OVRCD<>1
            MATCH     ANSD,TCINDC11         * Don't display on bed board
            GOTO      PCEL1000 IF EQUAL
          ENDIF
.
          CALL      STAE0000                * Expected patient onhover details
.
          WRITE     HTMLFILE;"<tr class=#"BedRow#">";
          WRITE     HTMLFILE;"<td class=#"BedCell";       * Display Bed Cell
.
          MATCH     ANSM,PMEBGEND
          IF        @EQUAL
            WRITE     HTMLFILE;" BlueBackground";
          ELSE
            MATCH     ANSF,PMEBGEND
            IF        @EQUAL
              WRITE     HTMLFILE;" PinkBackground";
            ELSE
              WRITE     HTMLFILE;" GreenBackground";
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;"#"></td>";
.
          WRITE     HTMLFILE;"<td class=#"PatientCell"
          WRITE     HTMLFILE;"#">";
.
          CALL      EXPA0000                * Display expected pat alert icons
.
          WRITE     HTMLFILE;"<br />"
.
          CALL      PATFLD00                * Use standard Patient Folder sub
          MOVE      KEY3,FOLDERSF           * and store suffix
          MOVE      PGNAME,DIM1             * Given name initial
          STRIP     PSNAME
.
          MOVE      ZERO,POSTOPFL
          MATCH     PMEBWARD,PMEBPOWD
          IF        @EQUAL
            MATCH     PMEBBEDD,PMEBPOBD
            IF        @EQUAL
              MATCH     SP70,PMEBTBDT
              IF        @EQUAL
                MOVE      ONE,POSTOPFL      * Post op record
              ELSE
                MATCH     WORKDATE,PMEBTBDT
                IF        @EQUAL
                  MOVE      ONE,POSTOPFL      * Post op record
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
          IF        POSTOPFL=1
            WRITE     HTMLFILE;"<span class=#"Blue Bold#">";
          ELSE
            IF        OVERLAPF=1
              WRITE     HTMLFILE;"<span class=#"White Bold#">";
            ELSE
              WRITE     HTMLFILE;"<span class=#"Red Bold#">";
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;"<img src='../images/PatientFolder",FOLDERSF,".gif'":
                " onclick='LinktoPatient(#"",URNUMBER,"#",#"",ADMISSNO,"#");' ";
.
          IF        STBYFLAG=1 | STBYFLAG=2
            WRITE     HTMLFILE;"onmouseover='show_it(#"",STATWIND,"#");'":
                               " onmouseout='hide_it();'";
          ELSE
            WRITE     HTMLFILE;"onmouseover='show_itE(#"",STATWIND,"#");'":
                               " onmouseout='hide_itE();'";
.
          ENDIF
          WRITE     HTMLFILE;" class=ListIcon \>";
.
          BRANCH    NOUPDATE,PCEL4000
.
          IF        POSTOPFL=1
            WRITE     HTMLFILE;"<a class=#"Blue Bold#"";
          ELSE
            IF        OVERLAPF=1
              WRITE     HTMLFILE;"<a class=#"White Bold#"";
            ELSE
              WRITE     HTMLFILE;"<a class=#"Red Bold#"";
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;" href='":
                             "javascript:UpdateExpected":
                             "(#"",PMEBURNO,"#",#"",PMEBADMN,"#")'>";
          STRIP     PSNAME
          WRITE     HTMLFILE;*+,PSNAME,DOT,DIM1,"</a>";
          RESET     PSNAME
          GOTO      PCEL5000
.
PCEL4000  STRIP     PSNAME
          WRITE     HTMLFILE;*+,PSNAME,DOT,DIM1;
          RESET     PSNAME
.
PCEL5000  IF        ASTAT=1
            MATCH     SP70,PMVXATIM
            IF        !@EQUAL
              MOVE      PMVXATIM,D5
              WRITE     HTMLFILE;*+,"(",D5,")";
            ENDIF
          ENDIF
.
          IF        POSTOPFL=1
            MATCH     SP70,PMEBEWRD
            IF        !@EQUAL
              SQUEEZE   PMEBEWRD
              IF        OVERLAPF=1
                WRITE     HTMLFILE;"<span class=#"White Bold#">";
              ELSE
                WRITE     HTMLFILE;"<span class=#"Red Bold#">";
              ENDIF
.
              MATCH     SP70,PMEBEBED
              IF        @EQUAL
                WRITE     HTMLFILE;*+,"(",PMEBEWRD,")";
              ELSE
                WRITE     HTMLFILE;*+,"(",PMEBEWRD,"/",PMEBEBED,")";
              ENDIF
              WRITE     HTMLFILE;"</span>";
              RESET     PMEBEWRD
            ENDIF
          ELSE
            MATCH     SP70,PMEBPOWD
            IF        !@EQUAL
              SQUEEZE   PMEBPOWD
              WRITE     HTMLFILE;"<span class=#"Blue#">";
              MATCH     SP70,PMEBPOBD
              IF        @EQUAL
                WRITE     HTMLFILE;*+,"(",PMEBPOWD,")";
              ELSE
                WRITE     HTMLFILE;*+,"(",PMEBPOWD,"/",PMEBPOBD,")";
              ENDIF
              WRITE     HTMLFILE;"</span>";
              RESET     PMEBPOBD
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;"&nbsp;</span></td></tr>"
.
          GOTO      PCEL1000
.
PCEL9999  RETURN
.
.------------------------------------------------------------
. Print the patient cell for the unallocated patient bed board
.------------------------------------------------------------
UCEL0000  PACK      KEY6,PMBDWARD,PMBDBEDD,SP70  * Display ward code or
          CALL      RDWARD1                      * description
          IF        OVRCD=1
            CALL      CLRWRD00
            MATCH     "~~B",KEY6
            IF        !@EQUAL
              MATCH     "~~W",KEY6
              IF        !@EQUAL
                GOTO      UCEL9999
              ENDIF
            ENDIF
          ENDIF
.
          BRANCH    WACTIVE,UCEL9999
.
          CALL      GEXP0000                * Get expected patient detail
.
          IF        EMPTONLY=1
            MATCH    SP70,PMBDCADM               * Current admission
            GOTO     UCEL9999 IF NOT EQUAL
.
            MATCH    SP70,SAVKEY20               * Expected patient
            GOTO     UCEL9999 IF NOT EQUAL
          ENDIF
.
          IF        TOMRFLAG=1
            CALL      CLBD0000                   * Check if bed closed on
            MOVE      CLBDINDC,PTWDBDST          * tomrdate
          ENDIF
.
          MATCH     "1",PTWDBDST                 * Bed Closed
          IF        @EQUAL
            WRITE     HTMLFILE;"<tr class=#"BedRow Blocked#">":
                               "<td class=#"BedCell Blocked#">";
.
            IF        WBEDDESC > 0 
              IF        WBEDDESC = 2 
                WRITE     HTMLFILE;PTWDSDES;
              ELSE
                WRITE     HTMLFILE;WBDESC;
              ENDIF 
            ELSE
              WRITE     HTMLFILE;PMBDBEDD;
            ENDIF
.
            WRITE     HTMLFILE;"</td><td class=#"PatientCell Blocked#">":
                               "<a href='":
                               "javascript:ViewBlocked":
                       "(#"",PMBDWARD,"#",#"",PMBDBEDD,"#",#"",TOMRFLAG,"#")'>":
                               "Blocked</a></td></tr>";
            GOTO      UCEL9999
          ELSE
            MATCH     SP70,PMBDCADM
            IF        @EQUAL
              WRITE     HTMLFILE;"<tr class=#"BedRow Available#">";
            ELSE
              WRITE     HTMLFILE;"<tr class=#"BedRow#">";
            ENDIF
          ENDIF
.
          CALL      BRMG0000                              * Room Gender
.
          MATCH     SP70,PMBDCADM
          IF        @EQUAL
            MATCH     SP70,ROOMGEND
            IF        @EQUAL
              MATCH     SP70,GEXPGEND
              IF        @EQUAL
                CALL      EXPG0000                        * Get Expected Gender
                MOVE      GEXPGEND,ROOMGEND
              ELSE                                        * room gender
                MOVE      GEXPGEND,ROOMGEND
              ENDIF
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;"<td class=#"BedCell";       * Display Bed Cell
.
          WRITE     HTMLFILE;ROOMCLAS;                    * Display Room Class
.
          MATCH     SP70,PMBDCADM
          IF        !@EQUAL
            MATCH     ANSM,PMBDGEND
            IF        @EQUAL
              WRITE     HTMLFILE;" BlueBackground";
            ELSE
              MATCH     ANSF,PMBDGEND
              IF        @EQUAL
                WRITE     HTMLFILE;" PinkBackground";
              ELSE
                WRITE     HTMLFILE;" GreenBackground";
              ENDIF
            ENDIF
          ELSE
            MATCH     SP70,PMBDROOM
            IF        !@EQUAL
              MATCH     ANSM,ROOMGEND
              IF        @EQUAL
                WRITE     HTMLFILE;" BlueBackground";
              ELSE
                MATCH     ANSF,ROOMGEND
                IF        @EQUAL
                  WRITE     HTMLFILE;" PinkBackground";
                ELSE
                  WRITE     HTMLFILE;" GreenBackground";
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;"#">";
.
          MATCH     SP70,PMBDCADM                * Bed Cell Links
          IF        @EQUAL
            IF        WINPUT<>1
              WRITE     HTMLFILE;"<a title=#"",SVWBDESC,"#" href='":
                                 "javascript:ViewBedHistory":
                                 "(#"",PMBDWARD,"#",#"",PMBDBEDD,"#")'>";
              IF        WBEDDESC > 0
                IF        WBEDDESC = 2
                  WRITE     HTMLFILE;PTWDSDES,"</a>";
                ELSE
                  WRITE     HTMLFILE;WBDESC,"</a>";
                ENDIF
              ELSE
                WRITE     HTMLFILE;PMBDBEDD,"</a>";
              ENDIF
            ENDIF
          ELSE
            MATCH     SP70,PMBDBEDD
            IF        @EQUAL
              IF        WINPUT<>1
                ADD       ONE,UNALPTOT
                WRITE     HTMLFILE;"<img alt=#"",SVWBDESC,"#"":
                                   " title=#"",SVWBDESC,"#"":
                                   " src='../images/EditIcon.gif'":
                                   " class=ListIcon onclick='BedAllocation(#"":
                                    PMBDURNO,"#",#"",PMBDCADM,"#");'/>";
              ENDIF
            ELSE
              WRITE     HTMLFILE;"<a title=#"",SVWBDESC,"#" href='":
                                 "javascript:ViewBedHistory":
                                 "(#"",PMBDWARD,"#",#"",PMBDBEDD,"#")'>";
              IF        WBEDDESC > 0
                IF        WBEDDESC = 2
                  WRITE     HTMLFILE;PTWDSDES,"</a>";
                ELSE
                  WRITE     HTMLFILE;WBDESC,"</a>";
                ENDIF
              ELSE
                WRITE     HTMLFILE;PMBDBEDD,"</a>";
              ENDIF
.
            ENDIF
          ENDIF
.
          PACK      KEY25,PMBDHOSP,PMBDWARD,PMBDBEDD,Z70
          CALL      RSPMHRD1
          CALL      RPPMHRD1
          BRANCH    OVRCD,UCEL0300
.
          MATCH     PMBDHOSP,PMHRHOSP
          GOTO      UCEL0300 IF NOT EQUAL
.
          MATCH     PMBDWARD,PMHRWARD
          GOTO      UCEL0300 IF NOT EQUAL
.
          MATCH     PMBDBEDD,PMHRBEDD
          GOTO      UCEL0300 IF NOT EQUAL
.
          MATCH     SP70,PMHRCSTA
          IF        !@EQUAL
            PACK      KEY5,ANSB,ANSV,PMHRCSTA,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              MATCH     ANSC,TCINDC1                  * Is the bed clean
              IF        !@EQUAL
                WRITE     HTMLFILE;"<br>",PMHRCSTA;
              ENDIF
            ENDIF
          ELSE
            PACK      KEY5,ANSB,ANSV,PMHRRSTA,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              MATCH     ANSC,TCINDC1                  * Is the bed clean
              IF        !@EQUAL
                WRITE     HTMLFILE;"<br>",PMHRRSTA;
              ENDIF
            ENDIF
          ENDIF
.
UCEL0300  WRITE     HTMLFILE;"</td>";
.
.    Patient Cell
.
          WRITE     HTMLFILE;"<td class=#"PatientCell";
.
          MATCH     SP70,PMBDCADM           * Current patient details
          IF        @EQUAL
            WRITE     HTMLFILE;"#">";
            MOVE      ZERO,OVERLAPF           * Reset overlap flag
            GOTO      UCEL3100
          ENDIF
.
          MOVE      PMBDURNO,URNUMBER
          MOVE      PMBDCADM,ADMISSNO
          CALL      GETPAT00
          IF        EXIT=1
            WRITE     HTMLFILE;"#">";
            GOTO      UCEL3100
          ENDIF
.
          MOVE      ZERO,OVERLAPF           * Reset overlap flag
.
          MATCH     SP70,SAVKEY20           * No expected patient
          IF        @EQUAL
            MATCH     WORKDATE,PMWOEDAT
            IF        @EQUAL
              MATCH     SP70,PMWOEDTM
              IF        !@EQUAL
                WRITE     HTMLFILE;" Available";
              ENDIF
            ENDIF
            MATCH     SP70,PMWOEDAT
            IF        !@EQUAL
              MATCH     WORKDATE,PMWOEDAT
              IF        @LESS
                WRITE     HTMLFILE;" PastDischargeDate";
              ENDIF
            ENDIF
            MATCH     TOMRDATE,PMWOEDAT
            IF        @EQUAL
              MATCH     SP70,PMWOEDTM
              IF        !@EQUAL
                WRITE     HTMLFILE;" Available";
              ENDIF
            ENDIF
          ELSE
            IF        STBYFLAG=1
              WRITE     HTMLFILE;" RedBackground";
              MOVE      ONE,OVERLAPF
              GOTO      UCEL1000
            ENDIF
.
            IF        STBYFLAG=2
              GOTO      UCEL1000
            ENDIF
.
            MATCH     TOMRDATE,PMWOEDAT
            IF        @EQUAL
              MATCH     SP70,PMWOEDTM
              IF        !@EQUAL
                WRITE     HTMLFILE;" YellowBackground";
              ELSE
                WRITE     HTMLFILE;" RedBackground";
                MOVE      ONE,OVERLAPF
              ENDIF
            ELSE
              MATCH     WORKDATE,PMWOEDAT
              IF        @EQUAL
                MATCH     SP70,PMWOEDTM
                IF        !@EQUAL
                  WRITE     HTMLFILE;" YellowBackground";
                ELSE
                  WRITE     HTMLFILE;" RedBackground";
                  MOVE      ONE,OVERLAPF
                ENDIF
              ELSE
                WRITE     HTMLFILE;" RedBackground";
                MOVE      ONE,OVERLAPF
              ENDIF
            ENDIF
          ENDIF
.
UCEL1000  WRITE     HTMLFILE;"#">";
.
          CALL      STAC0000               * Current patient onhover details
.
          PACK      KEY5,ANSC,ANSC,ACARE,SP70
          CALL      RDCODE1
          IF        OVRCD<>1
            MATCH     ANSD,TCINDC11         * Don't display on bed board
            GOTO      UCEL3100 IF EQUAL
          ENDIF
.
          CALL      PATA0000                * Display alert icons
.   
          CALL      PATFLD00                * Use standard Patient Folder sub
          MOVE      KEY3,FOLDERSF           * and store suffix
          MOVE      PGNAME,DIM1             * Given name initial
.   
          STRIP     PSNAME
          WRITE     HTMLFILE;"<br /><span class=#"Black Bold#">":
                             "<img src='../images/PatientFolder",FOLDERSF,".gif'":
                " onclick='LinktoPatient(#"",URNUMBER,"#",#"",ADMISSNO,"#");' ":
                             "onmouseover='show_it(#"",STATWIND,"#");'":
                             " onmouseout='hide_it();'":
                              " class=ListIcon \>";
.
          MATCH     "1",PTCNBBAP
          IF        @EQUAL
            MATCH     WORKDATE,PMBDTBDT
            IF        @EQUAL
              WRITE     HTMLFILE;"<a href='":
                               "javascript:UpdateExpected":
                               "(#"",URNUMBER,"#",#"",ADMISSNO,"#")'>":
                                 *+,PSNAME,DOT,DIM1:
                               "</a>"
            ELSE
              WRITE     HTMLFILE;*+,PSNAME,DOT,DIM1;
            ENDIF
          ELSE
            WRITE     HTMLFILE;*+,PSNAME,DOT,DIM1;
          ENDIF
.
          MATCH     PMWOEDAT,CURRDATE
          IF        @EQUAL
            MATCH     SP70,PMWOEDTM
            IF        !@EQUAL
              MOVE      PMWOEDTM,D5
              WRITE     HTMLFILE;SP1,"(*",D5,")";
            ENDIF
          ELSE
            MATCH     PMWOEDAT,TOMRDATE
            IF        @EQUAL
              MATCH     SP70,PMWOEDTM
              IF        !@EQUAL
                MOVE      PMWOEDTM,D5
                WRITE     HTMLFILE;SP1,"(",D5,")";
              ENDIF
            ENDIF
          ENDIF
.
          MATCH     SP70,PMBDEWRD
          IF        !@EQUAL
            SQUEEZE   PMBDEWRD
            IF        OVERLAPF=1
              WRITE     HTMLFILE;"<span class=#"White Bold#">";
            ELSE
              WRITE     HTMLFILE;"<span class=#"Red Bold#">";
            ENDIF
.   
            MATCH     SP70,PMBDEBED
            IF        @EQUAL
              WRITE     HTMLFILE;*+,"(",PMBDEWRD,")";
            ELSE
              WRITE     HTMLFILE;*+,"(",PMBDEWRD,"/",PMBDEBED,")";
            ENDIF
            WRITE     HTMLFILE;"</span>";
            RESET     PMBDEWRD
          ENDIF
.
          MATCH     SP70,PMBDPOWD
          IF        !@EQUAL
            SQUEEZE   PMBDPOWD
            WRITE     HTMLFILE;"<span class=#"Blue#">";
            MATCH     SP70,PMBDPOBD
            IF        @EQUAL
              WRITE     HTMLFILE;*+,"(",PMBDPOWD,")";
            ELSE
              WRITE     HTMLFILE;*+,"(",PMBDPOWD,"/",PMBDPOBD,")";
            ENDIF
            WRITE     HTMLFILE;"</span>";
            RESET     PMBDPOBD
          ENDIF
.
UCEL3050  MOVE      "0",BREQFLAG
          CALL      SPCFLG00                * Special feature flag & bed desc 
          WRITE     HTMLFILE;"<br>",SPCBEDDS;
.
          RESET     PSNAME
.
UCEL3100  MATCH     SP70,PMBDBEDD           * Expected patient details
          GOTO      UCEL5900 IF EQUAL
.
          PACK      KEY6,PMBDWARD,PMBDBEDD,SP70
          CALL      RDWARD1
          BRANCH    OVRCD,UCEL3500
.
UCEL3500  MATCH     SP70,SAVKEY20           * Is there an expected patient
          GOTO      UCEL5900 IF EQUAL
.
          MOVE      SAVKEY20,KEY20          * Restore correct record
          CALL      RDPMEPB1
          BRANCH    OVRCD,UCEL5900
.
          MOVE      PMEBURNO,URNUMBER
          MOVE      PMEBADMN,ADMISSNO
          CALL      GETPAT00
          BRANCH    EXIT,UCEL5900
.
          CALL      STAE0000                * Expected patient onhover details
.
          MATCH     SP70,PMBDCADM
          IF        @EQUAL
            CALL      EXPA0000                * Display expected pat alert icons
            WRITE     HTMLFILE;"<br />"
          ENDIF
.
          CALL      PATFLD00                * Use standard Patient Folder sub
          MOVE      KEY3,FOLDERSF           * and store suffix
          MOVE      PGNAME,DIM1             * Given name initial
.
          MOVE      ZERO,POSTOPFL
          MATCH     PMEBWARD,PMEBPOWD
          IF        @EQUAL
            MATCH     PMEBBEDD,PMEBPOBD
            IF        @EQUAL
              MATCH     SP70,PMEBTBDT
              IF        @EQUAL
                MOVE      ONE,POSTOPFL      * Post op record
              ELSE
                MATCH     TOMRDATE,PMEBTBDT
                IF        @EQUAL
                  MOVE      ONE,POSTOPFL      * Post op record tomorrow 
                ENDIF
                MATCH     WORKDATE,PMEBTBDT
                IF        @EQUAL
                  MOVE      ONE,POSTOPFL      * Post op record today
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
          IF        POSTOPFL=1
            WRITE     HTMLFILE;"<span class=#"Blue Bold#">";
          ELSE
            IF        OVERLAPF=1
              WRITE     HTMLFILE;"<span class=#"White Bold#">";
            ELSE
              WRITE     HTMLFILE;"<span class=#"Red Bold#">";
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;"<img src='../images/PatientFolder",FOLDERSF,".gif'":
                " onclick='LinktoPatient(#"",URNUMBER,"#",#"",ADMISSNO,"#");' ";
.
          IF        STBYFLAG=1 | STBYFLAG=2
            WRITE     HTMLFILE;"onmouseover='show_it(#"",STATWIND,"#");'":
                               " onmouseout='hide_it();'";
          ELSE
            WRITE     HTMLFILE;"onmouseover='show_itE(#"",STATWIND,"#");'":
                               " onmouseout='hide_itE();'";
.
          ENDIF
          WRITE     HTMLFILE;" class=ListIcon \>";
.
          BRANCH    NOUPDATE,UCEL4000
.
          IF        POSTOPFL=1
            WRITE     HTMLFILE;"<a class=#"Blue Bold#"";
          ELSE
            IF        OVERLAPF=1
              WRITE     HTMLFILE;"<a class=#"White Bold#"";
            ELSE
              WRITE     HTMLFILE;"<a class=#"Red Bold#"";
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;" href='":
                             "javascript:UpdateExpected":
                             "(#"",PMEBURNO,"#",#"",PMEBADMN,"#")'>";
          STRIP     PSNAME
          WRITE     HTMLFILE;*+,PSNAME,DOT,DIM1,"</a>";
          RESET     PSNAME
          GOTO      UCEL5000
.
UCEL4000  STRIP     PSNAME
          WRITE     HTMLFILE;*+,PSNAME,DOT,DIM1;
          RESET     PSNAME
.
UCEL5000  IF        POSTOPFL=1
            MATCH     SP70,PMEBEWRD
            IF        !@EQUAL
              SQUEEZE   PMEBEWRD
              IF        OVERLAPF=1
                WRITE     HTMLFILE;"<span class=#"White Bold#">";
              ELSE
                WRITE     HTMLFILE;"<span class=#"Red Bold#">";
              ENDIF
.
              MATCH     SP70,PMEBEBED
              IF        @EQUAL
                WRITE     HTMLFILE;*+,"(",PMEBEWRD,")";
              ELSE
                WRITE     HTMLFILE;*+,"(",PMEBEWRD,"/",PMEBEBED,")";
              ENDIF
              WRITE     HTMLFILE;"</span>";
              RESET     PMEBEWRD
            ENDIF
          ELSE
            MATCH     SP70,PMEBPOWD
            IF        !@EQUAL
              SQUEEZE   PMEBPOWD
              WRITE     HTMLFILE;"<span class=#"Blue#">";
              MATCH     SP70,PMEBPOBD
              IF        @EQUAL
                WRITE     HTMLFILE;*+,"(",PMEBPOWD,")";
              ELSE
                WRITE     HTMLFILE;*+,"(",PMEBPOWD,"/",PMEBPOBD,")";
              ENDIF
              WRITE     HTMLFILE;"</span>";
              RESET     PMEBPOBD
            ENDIF
          ENDIF
.
          IF        STBYFLAG=1 | STBYFLAG=2
            WRITE     HTMLFILE;" (Stby)";
          ENDIF
.
          MATCH     "1",PMEBBRPT
          IF        @EQUAL
            SQUEEZE   AWARD
            MATCH     SP70,ABED
            IF        @EQUAL
              WRITE     HTMLFILE;*+,"(Req)(",AWARD,")";
            ELSE
              WRITE     HTMLFILE;*+,"(Req)(",AWARD,"/",ABED,")";
            ENDIF
            RESET    AWARD
          ENDIF
.
          MOVE      "0",BREQFLAG
          CALL      SPCFLG00                * Special feature flag & bed desc 
          WRITE     HTMLFILE;"<br>",SPCBEDDS;
.
          IF        MULTIEXP=1
            IF        OVERLAPF=1
              WRITE     HTMLFILE;"&nbsp<a class=#"White Bold#"";
            ELSE
              WRITE     HTMLFILE;"&nbsp<a class=#"Red Bold#"";
            ENDIF
            WRITE     HTMLFILE;" href='":
                               "javascript:ViewExpected":
                               "(#"",PMEBWARD,"#",#"",PMEBBEDD,"#",#"1#")'>":
                                "(+)</a>";
          ENDIF
.
          WRITE     HTMLFILE;"</span>";
.
UCEL5900  CALL      ONLV0000              * Check for on leave patients
          WRITE     HTMLFILE;"&nbsp;</td></tr>"
.
UCEL9999  RETURN
.
.------------------------------------------------------------------------------
. If processing tomorrows bed board check if the bed will be close
. tomorrow, TOMRDATE = Tomorrow
.------------------------------------------------------------------------------
CLBD0000  MOVE      ZERO,CLBDINDC
.
          PACK      KEY22,PMBDWARD,WBED,SP70
          CALL      RSPMBDC1
CLBD1000  CALL      RKPMBDC1
          BRANCH    OVRCD,CLBD9999
.         
          MATCH     PMBCWARD,WWARD
          GOTO      CLBD9999 IF NOT EQUAL
.         
          MATCH     PMBCBED,WBED
          GOTO      CLBD9999 IF NOT EQUAL
.         
          MATCH     PMBCFRDT,TOMRDATE
          GOTO      CLBD1000 IF LESS
.         
          MATCH     TOMRDATE,PMBCTODT
          GOTO      CLBD1000 IF LESS
.
          MOVE      ONE,CLBDINDC
.
CLBD9999  RETURN
.
.---------------------------------------------------------------------------
.  Clears File Data
.---------------------------------------------------------------------------
CLRWRD00  MOVE      SP70,WWARD      * WARD NUMBER
          MOVE      SP70,WBED       * WARD BED NUMBER
          MOVE      SP70,WBDESC     * WARD BED DESCRIPTION
          MOVE      SP70,DWADMNO    * CURRENT PATIENT ADMISSION NO.
          MOVE      SP70,WEXTN      * EXTENSION NUMBER
          MOVE      ZERO,WBLINE     * No LINES B/WN BEDS (WRD ONLY)
          MOVE      SP70,WRTYPE     * Room Type (BDS ONLY) (CAT RT)
          MOVE      SP70,WEFRBT     * E.F.R. (BEDS ONLY) (CAT BT)
          MOVE      ZERO,WNBEDS     * No OF BEDS IN WARD (WRD ONLY)
          MOVE      ZERO,WHCSSUB    * HCS WARD SUBSCRIPT (WRD ONLY)
          MOVE      SP70,WSERV      * WARD SERVICE CODE (WARD ONLY)
          MOVE      ZERO,WPLUR      * PLURALITY NUMBER
          MOVE      ZERO,WINPUT     * WARD ONLY INPUT (WRD INDIC) 0=NO, 1=YES
          MOVE      ZERO,WOCCBED    * NO.OF BEDS IN WARD(OCC STATS)
          MOVE      SP70,WCLASS     * WARD CLASSIFICATION (CAT CG)
          MOVE      SP70,DWSTBY     * CURRENT STBY ADMISS NUMBER
          MOVE      ZERO,WACTIVE    * ACTIVE INDICATOR 0=Active, 1=Not Active
          MOVE      SP70,PTWDHOSN   * Hospital/Unit Number
          MOVE      ZERO,PTWDDNIN   * Day/Night Indicator 0=Day Only
.                                   *                     1=Day & Night
.                                   *                     2=Night Only
          MOVE      SP70,PTWDIAGE   * Intended Age Group Cat IA
          MOVE      SP70,PTWDICRE   *      "   Care Intensity Cat IC
          MOVE      SP70,PTWDSEX    *      "   Sex of Patient M/F/E
          MOVE      SP70,PTWDLOCN   * Medical Record Location Code
          MOVE      SP70,PTWDGLDG   * G/L and Cost Centre code
          MOVE      SP70,PTWDNSTA   * Nursing station
          MOVE      SP70,PTWDDOSA
          MOVE      SP70,PTWDFVDW
          MOVE      SP70,PTWDBDST
          MOVE      SP70,PTWDRBSC
          MOVE      SP70,PTWDHKST
          MOVE      SP70,PTWDIFST
          MOVE      SP70,PTWDCRDT
          MOVE      SP70,PTWDCRTM
          MOVE      SP70,PTWDCRID
          MOVE      SP70,PTWDUPDT
          MOVE      SP70,PTWDUPTM
          MOVE      Sp70,PTWDUPID
          MOVE      SP70,PTWDCOMM   * Comments
          MOVE      SP70,PTWDDFVS   * Default Visits
          MOVE      SP70,PTWDDFPH   * Default Phone Calls
          MOVE      SP70,PTWDROOM   * Room (for HL7 purposes only)
          MOVE      SP70,PTWDSPAR   * SPARE
.
.
          MATCH     "~~W",KEY6
          IF        @EQUAL
            MOVE      KEY6,WWARD
            MOVE      "Ward Not Specified",WBDESC
          ENDIF
.           
          MATCH     "~~B",KEY6
          IF        @EQUAL
            MOVE      KEY6,WWARD
            MOVE      "Bed Requests",WBDESC
          ENDIF
. 
CLRWRD99  RETURN
.
.------------------------------------------------------------
. Check if there are patients that have the same
. name.
.------------------------------------------------------------
CHKD0000  BRANCH    IBCNMHOS,CHKD3000
.
          MOVE      PMPXFNAM,DIM1
.
          PACK      KEY96,PTMASNAM,DIM1,SP70,SP70
          CALL      RSPMCUR2
CHKD1000  CALL      RKPMCUR2
          BRANCH    OVRCD,CHKD9000
.
          MATCH     PTMASNAM,PMCUSURN
          GOTO      CHKD9000 IF NOT EQUAL
.
          MATCH     PMBDURNO,PMCUURNO
          GOTO      CHKD1000 IF EQUAL
.
          MATCH     PMBDCADM,PMCUVISN
          GOTO      CHKD1000 IF EQUAL
.
          MATCH     DIM1,PMCUGNAM
          GOTO      CHKD9000 IF NOT EQUAL
.
          GOTO      CHKD9100
.
CHKD3000  MOVE      PMPXFNAM,DIM1
.
          PACK      KEY99,PMBDHOSP,PTMASNAM,DIM1,SP70,SP70
          CALL      RSPMCUR6
CHKD3100  CALL      RKPMCUR6
          BRANCH    OVRCD,CHKD9000
.
          MATCH     PMBDHOSP,PMCUHOSP
          GOTO      CHKD9000 IF NOT EQUAL
.
          MATCH     PMBDURNO,PMCUURNO
          GOTO      CHKD3100 IF EQUAL
.
          MATCH     PTMASNAM,PMCUSURN
          GOTO      CHKD9000 IF NOT EQUAL
.
          MATCH     PMBDCADM,PMCUVISN
          GOTO      CHKD3100 IF EQUAL
.
          MATCH     DIM1,PMCUGNAM
          GOTO      CHKD9000 IF NOT EQUAL
.
          GOTO      CHKD9100
.
CHKD9000  MOVE      ZERO,DUPNMFLG
          GOTO      CHKD9999
.
CHKD9100  MOVE      ONE,DUPNMFLG
          GOTO      CHKD9999
.
CHKD9999  RETURN
.
.------------------------------------------------------------
. Check if there are patients that have the same
. name
.------------------------------------------------------------
CKDE0000  BRANCH    IBCNMHOS,CKDE3000
.
          MOVE      PMPXFNAM,DIM1
.
          PACK      KEY96,PTMASNAM,DIM1,SP70,SP70
          CALL      RSPMCUR2
CKDE1000  CALL      RKPMCUR2
          BRANCH    OVRCD,CKDE9000
.
          MATCH     PTMASNAM,PMCUSURN
          GOTO      CKDE9000 IF NOT EQUAL
.
          MATCH     PMEBADMN,PMCUVISN
          GOTO      CKDE1000 IF EQUAL
.
          MATCH     DIM1,PMCUGNAM
          GOTO      CKDE9000 IF NOT EQUAL
.
          GOTO      CKDE9100
.
CKDE3000  MOVE      PMPXFNAM,DIM1
.
          PACK      KEY99,PMEBHOSP,PTMASNAM,DIM1,SP70,SP70
          CALL      RSPMCUR6
CKDE3100  CALL      RKPMCUR6
          BRANCH    OVRCD,CKDE9000
.
          MATCH     PMEBHOSP,PMCUHOSP
          GOTO      CKDE9000 IF NOT EQUAL
.
          MATCH     PTMASNAM,PMCUSURN
          GOTO      CKDE9000 IF NOT EQUAL
.
          MATCH     PMEBADMN,PMCUVISN
          GOTO      CKDE3100 IF EQUAL
.
          MATCH     DIM1,PMCUGNAM
          GOTO      CKDE9000 IF NOT EQUAL
.
          GOTO      CKDE9100
.
CKDE9000  MOVE      ZERO,DUPNMFLG
          GOTO      CKDE9999
.
CKDE9100  MOVE      ONE,DUPNMFLG
          GOTO      CKDE9999
.
CKDE9999  RETURN
.
.------------------------------------------------------------
. Load the bed board wards primary speciality into the
. array SPECWARD. The first character is the speciality
. and the second character indicated that the speciality 
. has beed processed
.------------------------------------------------------------
SPEC0000  BRANCH    IBCNMHOS,SPEC9999
.
          MOVE      ONE,SPECCNTR
          WHILE     SPECCNTR <= 99
            MOVE      SP70,SPECWARD[SPECCNTR]
            ADD       ONE,SPECCNTR
          DO
.
          MOVE      ZERO,SPECCNTR
          MOVE      SP70,SAVEWARD
.
          PACK      KEY22,VIEWHOSP,SP1,ONE,SP70
          BRANCH    VIEWSEQN,SPEC0100,SPEC0200,SPEC0300,SPEC0400,SPEC0500
          GOTO      SPEC9999
.
SPEC0100  CALL      RSPMBBD3
          GOTO      SPEC1000
.
SPEC0200  CALL      RSPMBBD4
          GOTO      SPEC1000
.
SPEC0300  CALL      RSPMBBD5
          GOTO      SPEC1000
.
SPEC0400  CALL      RSPMBBD6
          GOTO      SPEC1000
.
SPEC0500  CALL      RSPMBBD7
          GOTO      SPEC1000
.
SPEC1000  BRANCH    VIEWSEQN,SPEC1100,SPEC1200,SPEC1300,SPEC1400,SPEC1500
.
SPEC1100  CALL      RKPMBBD3
          BRANCH    OVRCD,SPEC9999
          GOTO      SPEC1700
.
SPEC1200  CALL      RKPMBBD4
          BRANCH    OVRCD,SPEC9999
          GOTO      SPEC1700
.
SPEC1300  CALL      RKPMBBD5
          BRANCH    OVRCD,SPEC9999
          GOTO      SPEC1700
.
SPEC1400  CALL      RKPMBBD6
          BRANCH    OVRCD,SPEC9999
          GOTO      SPEC1700
.
SPEC1500  CALL      RKPMBBD7
          BRANCH    OVRCD,SPEC9999
          GOTO      SPEC1700
.
SPEC1700  IF        IBCNMHOS=1
            MATCH     VIEWHOSP,PMBDHOSP          * Same hospital id
            GOTO      SPEC9999 IF NOT EQUAL
          ENDIF
.
          MATCH     "~~B",PMBDWARD
          GOTO      SPEC1000 IF EQUAL
.
          MATCH     "~~W",PMBDWARD
          GOTO      SPEC1000 IF EQUAL
.
          MATCH     SP70,SAVEWARD
          IF        @EQUAL
            MOVE      PMBDWARD,SAVEWARD
            GOTO      SPEC2000
          ENDIF
.
          MATCH     SAVEWARD,PMBDWARD
          GOTO      SPEC1000 IF EQUAL
.
SPEC2000  MOVE      PMBDWARD,SAVEWARD
.
          PACK      KEY6,PMBDWARD,SP70
          CALL      RDWARD1
          BRANCH    OVRCD,SPEC1000
.
          MATCH     SP70,WCLASS
          GOTO      SPEC1000 IF EQUAL
.
          PACK      KEY5,ANSC,ANSG,WCLASS,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,SPEC1000
.
          MATCH     SP70,TCINDC1
          GOTO      SPEC1000 IF EQUAL
.
          MOVE      ONE,SPECLOOP
          WHILE     SPECLOOP <= SPECCNTR
            MATCH     TCINDC1,SPECWARD[SPECLOOP]
            IF        @EQUAL
              GOTO      SPEC1000
            ENDIF
            ADD     ONE,SPECLOOP
          DO
.
          ADD       ONE,SPECCNTR
          PACK      SPECWARD[SPECCNTR],TCINDC1,ZERO
.
          GOTO      SPEC1000
.
SPEC9999  RETURN
.
.------------------------------------------------------------
. If not multi hospital and printing ward not assigned 
. expected patients, only print patients that don't have
. a speciality match with another bed board ward
. If not multi hospital and printing a normal ward 
. print ward not assigned expected patients at the end of
. the first ward with a matching speciality
.------------------------------------------------------------
NOTA0000  MATCH     "~~W",SAVEWARD
          GOTO      NOTA5000 IF NOT EQUAL
.
          MATCH     SP70,ACLSSFT
          GOTO      NOTA8000 IF EQUAL
.
          PACK      KEY5,ANSA,ANSC,ACLSSFT,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,NOTA8000
.
          MATCH     SP70,TCINDC1
          GOTO      NOTA8000 IF EQUAL
.
          MOVE      ONE,SPECLOOP
          WHILE     SPECLOOP <= SPECCNTR
            MATCH     TCINDC1,SPECWARD[SPECLOOP]
            IF        @EQUAL
              GOTO      NOTA9000
            ENDIF
            ADD     ONE,SPECLOOP
          DO
          GOTO      NOTA8000
.
NOTA5000  MATCH     SP70,ACLSSFT
          GOTO      NOTA9000 IF EQUAL
.
          PACK      KEY5,ANSA,ANSC,ACLSSFT,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,NOTA9000
.
          MATCH     SP70,TCINDC1
          GOTO      NOTA9000 IF EQUAL
.
          MATCH     CURRSPEC,TCINDC1
          GOTO      NOTA8000 IF EQUAL
.
          GOTO      NOTA9000
.
NOTA8000  MOVE      ZERO,EXIT
          GOTO      NOTA9999
.
NOTA9000  MOVE      ONE,EXIT
.  
NOTA9999  RETURN
.
.------------------------------------------------------------
. Check if the current ward primary speciality has been 
. printed before. If not set the wards primary speciality
. in the variable CURRSPEC, and mark the speciality on the
. array SPECWARD as processed
.------------------------------------------------------------
CURS0000  BRANCH    IBCNMHOS,CURS9999
.
          MOVE      SP70,CURRSPEC
.
          MATCH     SP70,WCLASS
          GOTO      CURS9999 IF EQUAL
.
          PACK      KEY5,ANSC,ANSG,WCLASS,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,CURS9999
.
          MATCH     SP70,TCINDC1
          GOTO      CURS9999 IF EQUAL
.
          MOVE      ONE,SPECLOOP
          WHILE     SPECLOOP <= SPECCNTR
            MOVE      SP70,PROCFLAG
            MATCH     TCINDC1,SPECWARD[SPECLOOP]
            IF        @EQUAL
              UNPACK    SPECWARD[SPECLOOP],DIM1,PROCFLAG
              MATCH     "1",PROCFLAG               * Speciality printed
              IF        !@EQUAL
                MOVE      TCINDC1,CURRSPEC
                PACK      SPECWARD[SPECLOOP],CURRSPEC,ONE,SP70
                GOTO      CURS9999
              ELSE
                GOTO      CURS9999
              ENDIF
            ENDIF
            ADD     ONE,SPECLOOP
          DO
.
CURS9999  RETURN
.------------------------------------------------------------
. Check if there are any patients on leave  from a bed
.------------------------------------------------------------
ONLV0000  MATCH     SP70,PMBDBEDD           * Ignore if no bed 
          GOTO      ONLV9999 IF EQUAL
.
          PACK      KEY14,PMBDWARD,PMBDBEDD,SP70
          CALL      RDSONLV1
          CALL      RDKONLV1
          BRANCH    OVRCD,ONLV9999

          MATCH     PMBDWARD,OWARD
          GOTO      ONLV9999 IF NOT EQUAL
.
          MATCH     PMBDBEDD,OBED
          GOTO      ONLV9999 IF NOT EQUAL
.
          WRITE     HTMLFILE;"<img src='../images/HomeLeave.png'":
                             " alt='On Leave' title='On Leave'":
                             " class=ListIcon onclick='ViewOnLeave(#"":
                             PMBDWARD,"#",#"",PMBDBEDD,"#")'>";
.
ONLV9999  RETURN
.
.------------------------------------------------------------
. Display ward/bed on leave patients                  
.------------------------------------------------------------
BEDL0000  PACK      KEY6,WARDCODE,BEDDCODE,SP70         * Display ward code or
          CALL      RDWARD1
          IF        OVRCD=1
            PACK      WBDESC,WARDCODE,SP1,LBRACK,BEDDCODE,RBRACK
          ENDIF
.
          MATCH     SP70,PTWDCOMM
          IF        !@EQUAL
            WRITE     HTMLFILE;"<tr><td class=HeadingCell colspan=3>":
                               "Comments</td>":
                               "</tr>":
                               "<tr><td colspan=3>",PTWDCOMM,"</td>":
                               "</tr>";
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td class=HeadingCell>Patient</td>":
                             "<td class=HeadingCell>Expected Return</td>":
                             "</tr>"
.
          PACK      KEY14,WARDCODE,BEDDCODE,SP70
          CALL      RDSONLV1
BEDL1000  CALL      RDKONLV1
          BRANCH    OVRCD,BEDL9999
.
          MATCH     WARDCODE,OWARD
          GOTO      BEDL9999 IF NOT EQUAL
.           
          MATCH     BEDDCODE,OBED
          GOTO      BEDL9999 IF NOT EQUAL
.
          PACK      KEY8,OADMNO,SP70
          CALL      RDPTMIS1
          BRANCH    OVRCD,BEDL1000
.
          PACK      KEY8,AURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,BEDL1000
.
          CALL      RDPMPX21
          BRANCH    OVRCD,BEDL1000
.
          IF        PCEASE=1
            MOVE      PDECDTE,AGEDATE            * Date of Death
          ELSE
            PACK      AGEDATE,CCC,CYY,CMM,CDD    * Today's date
          ENDIF
          REP       " 0",AGEDATE
          CALL      CALCAGE
          CALL      PATLN000
.
          MOVE      SP70,DISPDATE
          MATCH     SP70,OERDATE
          IF        !@EQUAL
            UNPACK    OERDATE,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      DISPDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td>",FORMATNM,"</td>":
                             "<td>",DISPDATE,SP1,OERTIME,"</td></tr>"
.
          GOTO      BEDL1000
.
BEDL9999  RETURN
.------------------------------------------------------------
. Table of Unallocated expected patients by Date Range
.------------------------------------------------------------
EXPT0000  BRANCH    IBCNMHOS,EXPT9999
          PACK      KEY36,SP3,NOTAWARD,SP3,SP3,FRSTDATE,SP70
          CALL      RSPMEPB8
EXPT1000  CALL      RKPMEPB8
          BRANCH    OVRCD,EXPT9999
          MATCH     SP70,PMEBHOSP
          GOTO      EXPT9999 IF NOT EQUAL
          MATCH     "~~W",PMEBWARD
          GOTO      EXPT9999 IF NOT EQUAL
          MATCH     SP70,PMEBROOM
          GOTO      EXPT9999 IF NOT EQUAL
          MATCH     SP70,PMEBBEDD
          GOTO      EXPT9999 IF NOT EQUAL
          MATCH     PMEBEDAT,LASTDATE
          GOTO      EXPT9999 IF LESS
.
          MOVE      PMEBURNO,URNUMBER
          MOVE      PMEBADMN,ADMISSNO
          CALL      GETPAT00
          BRANCH    EXIT,EXPT1000
          CALL      PATFLD00                * Use standard Patient Folder sub
          MOVE      KEY3,FOLDERSF           * and store suffix
          CALL      PATLN000                * Format Name
.
          MOVE      SP70,DOCFNAME
          MATCH     SP70,ADOCTA
          IF        !@EQUAL
            PACK      KEY10,ADOCTA,SP70
            CALL      RDPMHCP1
            IF        OVRCD<>1
              MOVE      PMHCTITL,FMTTITLE
              MOVE      PMHCGNAM,FMTGNAME
              MOVE      PMHCSNAM,FMTSNAME
              CALL      DOCNM000
              REP       "'`@ * / : \ $ % & # ",DOCFNAME
            ENDIF
          ENDIF
.
          MOVE      SP70,ACDESC
          MATCH     SP70,ACLSSFT
          IF        !@EQUAL
            PACK      KEY5,ANSA,ANSC,ACLSSFT,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              MOVE      TDESC,ACDESC
            ENDIF
          ENDIF
.
          MOVE      SP70,BPDESC
          MATCH     SP70,PMVXPACC
          IF        !@EQUAL
            PACK      KEY5,ANSB,ANSP,PMVXPACC,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              MOVE      TDESC,BPDESC
            ENDIF
          ENDIF
.
          MOVE      SP70,FUNDNAME
          MATCH     SP70,AFUNDH
          IF        !@EQUAL
            CLEAR     FUNDNAME
            PACK      KEY14,AFUNDH,ZERO,ZERO,ZERO,ZERO,SP70
            CALL      RDFUND1
            IF        OVRCD<>1
              APPEND    HFNAME,FUNDNAME
            ENDIF
            PACK      KEY14,AFUNDH,AFNDTB,SP70
            CALL      RDFUND1
            IF        OVRCD<>1
              APPEND    " - ",FUNDNAME
              APPEND    HFNAME,FUNDNAME
            ENDIF
            RESET     FUNDNAME
          ENDIF
.
          MOVE      SP70,CLDESC
          MATCH     SP70,ACLAIM
          IF        !@EQUAL
            PACK      KEY5,ANSC,ANSL,ACLAIM,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              MOVE      TDESC,CLDESC
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;"t.addRow(#"",PMEBURNO,"#",":          * 0 U/R
                             "#"",PMEBADMN,"#",":                   * 1 Admission
                             "#"",FORMATNM,"#",":                   * 2 Patient Name
                             "#"",FOLDERSF,"#",":                   * 3 Folder
                             "#"",PMEBEDAT,PMEBETIM,"#",":          * 4 Expected Date/Time
                             "#"",PMEBEWRD,"#",":                   * 5 Expected Ward
                             "#"",PMEBEBED,"#",":                   * 6 Expected Bed
                             "#"",PMEBTBDT,PMEBTBTM,"#",":          * 7 Theatre Date Time
                             "#"",PMEBPOWD,"#",":                   * 8 Post Op Ward
                             "#"",PMEBPOBD,"#",":                   * 9 Post Op Bed
                             "#"",PMEBABRW,"#",":                   * 10 Allocated Bed Request Ward
                             "#"",PMEBABRB,"#",":                   * 11 Allocated Bed Request Bed
                             "#"",PMEBBRPT,"#",":                   * 12 Bed Request Patient 1=Yes
                             "#"",PSEX,"#",":                       * 13 Gender
                             "#"",FMTPATAG,"#",":                   * 14 Age
                             "#"",DOCFNAME,"#",":                   * 15 Doctor
                             "#"",ACDESC,"#",":                     * 16 Speciality
                             "#"",BPDESC,"#",":                     * 17 Pref Room
                             "#"",CLDESC,"#",":                     * 18 Claim Type
                             "#"",FUNDNAME,"#",":                   * 19 Helath Fund
                             "#"",ADATE,ATIME,"#",":                * 20 Admission Date Time
                             "#"",ASTAY,"#",":                      * 21 Expected LOS
                             "#"",ADIAG1,",",ADIAG2,"#",":          * 22 Diagnosis
                             "#"",PMEBHOSP,"#");"                   * 23 Hospital
          GOTO      EXPT1000
.
EXPT9999  RETURN
.
.-----------------------------------------------------------------------------
. PBDM0000 - Process Regimes and Bed Management
.-----------------------------------------------------------------------------
PBDM0000  MATCH     "1",PTCNBMAN                          * Bed Management
          IF        @EQUAL
            CALL      UPPBMN00
          ENDIF
.
PBDM9999  RETURN
.
.-------------------------------------------------------------------------
.UPPBMN00 - Write/update the bed management table
.-------------------------------------------------------------------------
UPPBMN00  PACK      PTMIS068,PTMIS068,SP70
          PACK      PTMIS076,PTMIS076,SP70
.
          MATCH     SP70,PTMIS068                * Blank expected admission ward
          GOTO      UPPBMN99 IF EQUAL
          GOTO      UPPBMN99 IF EOS
.
          MATCH     Z70,PTMIS068                 * Expected admission ward not
          GOTO      UPPBMN99 IF EQUAL            * received
.
          MOVE      SP70,SAVEBRQN
.
          PACK      KEY30,ADMISSNO,SP70
          CALL      RSPTBMD2
          CALL      RKPTBMD2
          BRANCH    OVRCD,UPPBMN10
.                                                    * Write/update patbmhaf
          MATCH     ADMISSNO,PTBDADMN              * Bed Management Header Table
          IF        @EQUAL
            CALL      DELBMD00
          ENDIF
.
.-------------------------------------------------------------------------
.
UPPBMN10  MOVE      ZERO,LOSDINDC              * Write/update patbmdaf
          MOVE      ADATE,SAVEDATE             * Bed management Detail Tab
          MOVE      ATIME,SAVETIME             * Length of Stay days
          MATCH     "1",KEEPBKTM
          IF        @EQUAL
            PACK      SAVEDATE,BKREEDAT,SP70
            PACK      SAVETIME,BKREATIM,SP70
          ENDIF
.
          MOVE      ZERO,FORM3
.         SQUEEZE   ASTAY
          MOVE      ASTAY,FORM3
UPPBMN30  COMPARE   ZERO,FORM3
          GOTO      UPPBMN50 IF EQUAL
.
          MOVE      TWO,LOSDINDC
.
          MOVE      PTMIS068,PTBDWARD
          MOVE      PTMIS076,PTBDWBED
.
          MOVE      SAVEDATE,PTBDDATE
          MOVE      SAVETIME,PTBDADTM
          MOVE      FORM3,PTBDLOSD
          MOVE      FORM3,FORM3A
          CALL      GTTOTM00
          MOVE      ADJTIME,PTBDLOSM
          MOVE      FORM3A,FORM3
          MOVE      SP70,PTBDOPPE
          MOVE      ADMISSNO,PTBDADMN
.
          MOVE      "2",PTBDSTAT
.
          MOVE      "0",PTBDOVER
          MOVE      SAVEBRQN,PTBDBRQN
          MOVE      SP70,PTBDSPAR
.
          PACK      KEY30,PTBDWARD,PTBDWBED,PTBDDATE,PTBDADTM,SP3,PTBDADMN,SP70
          CALL      RAPTBMD1
          IF        OVRCD=1
            TRAP      OVERCOND IF IO
            CALL      WRPTBMD1
            TRAPCLR   IO
          ELSE
            CALL      UPPTBMD1
          ENDIF
.
          CALL      UPPBMH00
.
          SUB       ONE,FORM3
          UNPACK    SAVEDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      "1",ADJDAYS
          CALL      ADJDAT00
          PACK      SAVEDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      "00:00",SAVETIME
          GOTO      UPPBMN30
.
.-------------------------------------------------------------------------
.
UPPBMN50  CALL      GTTOTM00                       * Write/update patbmdaf
.....     COMPARE   ZERO,ADJTIME                   * Bed Management Detail Tab
.....     GOTO      UPPBMN70 IF EQUAL              * Length of Stay minutes
.
          COMPARE   TWO,LOSDINDC
          IF        @EQUAL
            MOVE      SAVEDATE,PTBDDATE
            MOVE      SAVETIME,PTBDADTM
          ELSE
            MOVE      ADATE,PTBDDATE
            MOVE      ATIME,PTBDADTM
            MATCH     "1",KEEPBKTM
            IF        @EQUAL
              MOVE      BKREEDAT,PTBDDATE
              MOVE      BKREATIM,PTBDADTM
            ENDIF
            MOVE      ONE,LOSDINDC
          ENDIF
.
          MOVE      PTMIS068,PTBDWARD
          MOVE      PTMIS076,PTBDWBED
.
          MOVE      ADJTIME,PTBDLOSM
          MOVE      "  0",PTBDLOSD
          MOVE      SP70,PTBDOPPE
          MOVE      ADMISSNO,PTBDADMN
.
          MOVE      "2",PTBDSTAT
.
          MOVE      "0",PTBDOVER
          MOVE      SAVEBRQN,PTBDBRQN
          MOVE      SP70,PTBDSPAR
.
          PACK      KEY30,PTBDWARD,PTBDWBED,PTBDDATE,PTBDADTM,SP3,PTBDADMN,SP70
          CALL      RAPTBMD1
          IF        OVRCD=1
            TRAP      OVERCOND IF IO
            CALL      WRPTBMD1
            TRAPCLR   IO
          ELSE
            CALL      UPPTBMD1
          ENDIF
.
          MOVE      PTBDDATE,SAVEDATE
          CALL      UPPBMH00
.
.-------------------------------------------------------------------------
.
UPPBMN70  COMPARE   ZERO,LOSDINDC                         * Override previous
          GOTO      UPPBMN99 IF EQUAL                     * Records in patbmdaf
.
          MATCH     SP70,PTMIS068
          GOTO      UPPBMN99 IF EQUAL
          GOTO      UPPBMN99 IF EOS
.
          MOVE      ATIME,DIM5
          PACK      KEY30,PTMIS068,PTMIS076,ADATE,DIM5,SP3,ADMISSNO,SP70
          MATCH     "1",KEEPBKTM
          IF        @EQUAL
           PACK      KEY30,PTMIS068,PTMIS076,BKREEDAT,BKREATIM,SP3,ADMISSNO,SP70
          ENDIF
          CALL      RDPTBMD1
          BRANCH    OVRCD,UPPBMN99
.
          MOVE      PTBDDATE,SAVEDATE
          MOVE      PTBDADTM,SAVETIME
          MOVE      PTBDLOSD,SAVELOSD
          MOVE      PTBDLOSM,SAVELOSM
.
UPPBMN73  CALL      RPPTBMD1
          BRANCH    OVRCD,UPPBMN80
.
          MATCH     PTMIS068,PTBDWARD
          GOTO      UPPBMN80 IF NOT EQUAL
.
          MATCH     PTMIS076,PTBDWBED
          GOTO      UPPBMN80 IF NOT EQUAL
.
          MATCH     SAVEDATE,PTBDDATE
          GOTO      UPPBMN80 IF NOT EQUAL
.
          MOVE      PTBDDATE,STRTDATE
          MOVE      PTBDADTM,STRTTIME
          MOVE      PTBDLOSM,ADJTIME
          MOVE      PTBDLOSD,ADJDAYS
          CALL      GETEDT00
.
          MATCH     EXPDSDTE,SAVEDATE
          IF        @LESS
            GOTO      UPPBMN78
          ENDIF
          GOTO      UPPBMN73 IF NOT EQUAL
.
          MATCH     EXPDSTME,SAVETIME
          IF        @EQUAL | @LESS
            GOTO      UPPBMN78
          ENDIF
.
          GOTO      UPPBMN73
.
UPPBMN78  MOVE      ONE,PTBDOVER
          CALL      UPPTBMD1
.
          GOTO      UPPBMN73
.
.-------------------------------------------------------------------------
.
UPPBMN80  PACK      KEY30,PTMIS068,PTMIS076,ADMISSNO,Z70    * Override following
          CALL      RSPTBMD3                              * Records in patbmdaf
          CALL      RPPTBMD3
          BRANCH    OVRCD,UPPBMN99
.
          MATCH     ADMISSNO,PTBDADMN
          GOTO      UPPBMN99 IF NOT EQUAL
.
.         PACK      KEY30,PTBDWARD,PTBDWBED,PTBDDATE,PTBDADTM,SP3,PTBDADMN,SP70
.         CALL      RDPTBMD1
.         BRANCH    OVRCD,UPPBMN99
.
          MOVE      SAVEDATE,STRTDATE
          MOVE      SAVETIME,STRTTIME
          MOVE      PTBDLOSM,ADJTIME
          MOVE      SAVELOSD,ADJDAYS
          CALL      GETEDT00
.
          PACK      KEY30,PTMIS068,PTMIS076,SAVEDATE,SAVETIME,SP3,ADMISSNO,SP70
          CALL      RDPTBMD1
          BRANCH    OVRCD,UPPBMN99
.
UPPBMN85  CALL      RKPTBMD1
          BRANCH    OVRCD,UPPBMN99
.
          MATCH     PTMIS068,PTBDWARD
          GOTO      UPPBMN99 IF NOT EQUAL
.
          MATCH     PTMIS076,PTBDWBED
          GOTO      UPPBMN99 IF NOT EQUAL
.
          MATCH     ADMISSNO,PTBDADMN
          GOTO      UPPBMN85 IF EQUAL
.
          MATCH     EXPDSDTE,PTBDDATE
          IF        @LESS
            GOTO      UPPBMN88
          ENDIF
          GOTO      UPPBMN85 IF NOT EQUAL
.
          MATCH     EXPDSTME,PTBDADTM
          IF        @EQUAL | @LESS
            GOTO      UPPBMN88
          ENDIF
.
          GOTO      UPPBMN85
.
UPPBMN88  MOVE      "1",PTBDOVER
          CALL      UPPTBMD1
.
          GOTO      UPPBMN85
.
.UPPBMN90  CALL      UPPBMH00
.
UPPBMN99  RETURN
.
.-------------------------------------------------------------------------
.DELBMN00 - Delete old patbmdaf records
.-------------------------------------------------------------------------
DELBMD00  MOVE      SP70,SAVEBRQN
          PACK      KEY30,ADMISSNO,SP70
          CALL      RSPTBMD2
DELBMD10  CALL      RKPTBMD2
          BRANCH    OVRCD,DELBMD99
.
          MATCH     ADMISSNO,PTBDADMN
          GOTO      DELBMD99 IF NOT EQUAL
.
          MOVE      PTBDBRQN,SAVEBRQN
.
      PACK      KEY30,PTBDADMN,PTBDDATE,PTBDADTM,PTBDWARD,PTBDWBED,PTBDOPPE,SP70
          CALL      DEPTBMD2
.
          PACK      KEY14,PTBDWARD,PTBDDATE,PTBDOPPE,SP70
          CALL      RDPTBMH1
          IF        OVRCD=0
            MOVE      ZERO,FORM5
            MOVE      PTBHTOTP,FORM5
            IF        FORM5<>0
              SUB       ONE,FORM5
              MOVE      FORM5,PTBHTOTP
              CALL      UPPTBMH1
            ENDIF
          ENDIF
.
          PACK      KEY30,ADMISSNO,SP70
          CALL      RSPTBMD2
          GOTO      DELBMD10
.
DELBMD99  RETURN
.
.-------------------------------------------------------------------------
.GTTOTM00 - Get total time for regime codes
.-------------------------------------------------------------------------
GTTOTM00  MOVE      ZERO,ADJTIME
          MOVE      ONE,F1
          MOVE      SP70,DIM10
GTTOTM10  COMPARE   F1,FOUR
          GOTO      GTTOTM99 IF EQUAL
          LOAD      DIM10,F1,PTRGM001,PTRGM002,PTRGM003
.
          PACK      DIM10,DIM10,SP70
          MATCH     SP70,DIM10
          IF        @EQUAL|@EOS
            ADD       ONE,F1
            GOTO      GTTOTM10
          ENDIF
.
          PACK      KEY13,DIM10,SP1,SP1,ZERO,SP70
          CALL      RDPMREG1
          IF        OVRCD=1
            ADD       ONE,F1
            GOTO      GTTOTM10
          ENDIF
.
          MOVE      ZERO,FORM3
          SQUEEZE   PMREELOS
          MOVE      PMREELOS,FORM3
          ADD       FORM3,ADJTIME
          ADD       ONE,F1
          GOTO      GTTOTM10
.
GTTOTM99  RETURN
.
.-----------------------------------------------------------------------------
. UPPBMH00 - Write / Update to Bed Management Header file patbmhaf
.-----------------------------------------------------------------------------
UPPBMH00  PACK      KEY14,PTBDWARD,SAVEDATE,SP3,SP70
          CALL      RDPTBMH1
          IF        OVRCD=0
            CALL      DEPTBMH1
          ENDIF
.
          MOVE      SP70,SAVEADMN
          MOVE      ZERO,FORM5
          MOVE      PTBDWARD,DIM3
.
          PACK      KEY30,PTBDWARD,SAVEDATE,SP70
          CALL      RSPTBMD4
UPPBMH10  CALL      RKPTBMD4
          BRANCH    OVRCD,UPPBMH50
.
          MATCH     DIM3,PTBDWARD
          GOTO      UPPBMH50 IF NOT EQUAL
.
          MATCH     SAVEDATE,PTBDDATE
          GOTO      UPPBMH50 IF NOT EQUAL
.
          MATCH     SP70,PTBDOPPE
          GOTO      UPPBMH50 IF NOT EQUAL
.
          MATCH     SAVEADMN,PTBDADMN
          IF        !@EQUAL
            ADD       ONE,FORM5
          ENDIF
.
          MOVE      PTBDADMN,SAVEADMN
          GOTO      UPPBMH10
.
UPPBMH50  MOVE      DIM3,PTBHWARD
          MOVE      SAVEDATE,PTBHDATE
          MOVE      SP70,PTBHOPPE
          MOVE      FORM5,PTBHTOTP
          MOVE      SP70,PTBHSPAR
.
          PACK      KEY14,DIM3,SAVEDATE,SP3,SP70
          CALL      RAPTBMH1
          IF        OVRCD=1
            CALL      WRPTBMH1
          ELSE
            CALL      UPPTBMH1
          ENDIF
.
UPPBMH99  RETURN
+
.-------------------------------------------------------------------------
.GETEDT00 - Get Expected Discharge Date and Time
.-------------------------------------------------------------------------
GETEDT00  COMPARE   ZERO,ADJDAYS
          IF        @EQUAL
            MOVE      STRTDATE,EXPDSDTE
            GOTO      GETEDT50
          ENDIF
.
          UNPACK    STRTDATE,CCENT,CYEAR,CMON,CDAY
          CALL      ADJDAT00
          PACK      EXPDSDTE,CCENT,CYEAR,CMON,CDAY
.
GETEDT50  COMPARE   ZERO,ADJTIME
          IF        @EQUAL
            MOVE      STRTTIME,EXPDSTME
            GOTO      GETEDT99
          ENDIF
.
          CALL      CLDRN000
          MOVE      KEY5,EXPDSTME
.
GETEDT99  RETURN
.
.-----------------------------------------------------------------------------
. CLDRN000 - Calculate Expected Discharge Time
.-----------------------------------------------------------------------------
.
CLDRN000  UNPACK    STRTTIME,CHOUR,ANS,CMIN * unpack the start time
          MOVE      CMIN,FORM2              * minutes as a form2
          MOVE      FORM2,FORM4A            * set up work variable
          ADD       ADJTIME,FORM4A          * the total minutes to add
.
CLDRN200  COMPARE   ZERO,FORM4A
          GOTO      CLDRN300 IF NOT LESS    * positve amount
.
          ADD       "60",FORM4A             * make it positive
          MOVE      CHOUR,OPTION
          SUB       ONE,OPTION
          MOVE      OPTION,CHOUR            * sub 1 hour off time
          GOTO      CLDRN200
.
CLDRN300  MOVE      FORM4A,FORM4B           * save the minutes
.
          DIV       "60",FORM4A             * the total hours to add
          MOVE      CHOUR,FORM2             * hours as a form
          ADD       FORM4A,FORM2            * add the hours
.
CLDRN400  COMPARE   "24",FORM2
          GOTO      CLDRN500 IF LESS        * below 24 hours
.
          SUB       "24",FORM2              * get back to 24 hour time
.
          UNPACK    EXPDSDTE,CCENT,CYEAR,CMON,CDAY
          MOVE      "1",ADJDAYS
          CALL      ADJDAT00
          PACK      EXPDSDTE,CCENT,CYEAR,CMON,CDAY
.
          GOTO      CLDRN400
.
CLDRN500  MOVE      FORM2,CHOUR             * set up the hours
.
          MULT      "60",FORM4A             * the total minutes added
          SUB       FORM4A,FORM4B           * the minutes to add
          MOVE      FORM4B,FORM2            * get minutes as form2
.
          COMPARE   ZERO,FORM2
          GOTO      CLDRN800 IF NOT LESS    * not negative
.
          ADD       "60",FORM2              * make it positive
          MOVE      CHOUR,OPTION
          SUB       ONE,OPTION
          MOVE      OPTION,CHOUR            * sub 1 hour off time
.
CLDRN800  MOVE      FORM2,CMIN              * the minutes
.
          PACK      KEY5,CHOUR,COLON,CMIN * the end time
          REP       " 0",KEY5
.
CLDRN999  RETURN
.
          INC       IBAWEBCD
          INC       PATNAMCD
          INC       PMSBBDTM
          INC       WEBDATCD
.
          INC       BOKDIAIO/INC
          INC       BOKRC1IO/INC
          INC       BOKRX1IO/INC
          INC       EMRVISIO/INC
          INC       OPRARDIO/INC
          INC       OPRSRGIO/INC
          INC       OPRDEAIO/INC
          INC       OPRSESIO/INC
          INC       PATONLIO/INC
          INC       PATTRNIO/INC
          INC       PATWR1IO/INC
          INC       PMSBBDIO/INC
          INC       PMSEPBIO/INC
          INC       PMSMSWIO/INC
          INC       PMSHRDIO/INC
          INC       PMSWORIO/INC
          INC       PATDO1IO/INC   * Doctor File
          INC       PATHSPIO/INC
          INC       PATICDIO/INC
          INC       PATMI1IO/INC
          INC       PATMA1IO/INC
          INC       PATVISIO/INC   * Patients Visits File
          INC       PMSVX1IO/INC   * Visit Extension File
          INC       PATALRIO/INC
          INC       PATBMHIO/INC
          INC       PATBMDIO/INC
          INC       PMSREGIO/INC
          INC       OUTSITIO/INC
          INC       PMSUCHIO/INC
          INC       PMSUCDIO/INC
          INC       PATPR1IO/INC
          INC       PMSPX2IO/INC
          INC       PATFN1IO/INC
          INC       MRTMASIO/INC
          INC       NHIMASIO/INC
          INC       NHIETHIO/INC
          INC       PATRE1IO/INC
          INC       PATDGWIO/INC
          INC       VISMDTIO/INC
          INC       VISMTXIO/INC
          INC       PATITMIO/INC
          INC       PATDADIO/INC
          INC       PATVADIO/INC
          INC       PATCMCIO/INC
          INC       PATASFIO/INC
          INC       PATLINIO/INC
.
          INC       PMSCCDIO/INC   * Concession Card Details
          INC       PMSBDCIO/INC
          INC       PMSBRQIO/INC
          INC       PMSCURIO/INC
          INC       PMSHCGIO/INC
          INC       PMSHCPIO/INC
          INC       APSMASIO/INC
          INC       PATIN1IO/INC
          INC       PATDSCIO/INC
          INC       WATLETIO/INC
.
          INC       BEDBDPRO 
          INC       BOKRECTM
          INC       BOKRX1TM
          INC       CLBOKRC1
          INC       CLBOKRX1
          INC       CLNHIMAS                   * Clear nhimasaf file variables
          INC       CLOPRSES
          INC       CLOPRARD
          INC       CLOPRSRG
          INC       CLPATMIS
          INC       CLPATVIS
          INC       CLPMSEPB
          INC       CLPMSHRD
          INC       CLPMSVX1
          INC       CLPMSPX2
          INC       NHOSPDAY
          INC       OPTHETAT
          INC       PATMASTM
          INC       PATDOCTM
          INC       PMSHCGTM
          INC       PMSHCPTM
          INC       PMSWORTM
          INC       PMSVX1TM
          INC       PATDSCTM
          INC       PMSPX2TM
          INC       PATMSXTM
          INC       PATMISTM
          INC       PMSHRDTM
          INC       PATDOCCD
          INC       CALCAGE
          INC       NAMSTRCD
          INC       DAYOFWEK
          INC       CLPMSBBD

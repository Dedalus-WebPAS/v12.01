.*****************************************************************************
.*    Prog         : FXGENPRI                                                *
.*    Description  : Regenerate Private Practice AMA/MBS Key Words           *
.*                                                                           *
.*    Author       : Pat Dirito                                              *
.*    Date         : 10/12/2003                                              *
.*    Modifications:                                                         *
.*****************************************************************************
.
          INC       STD001FD
.
          INC       PRIITMFD/INC
          INC       PRIIKIFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
RECNUM    FORM      8
ITEMTYP   DIM       2
ITEMDESC  DIM       30
TYPE      FORM      2
SAVITEM   DIM       9
ITEMNO    DIM       9
.
PRGID     INIT      "FXGENPRI"
PRGNAM    INIT      "Regenerate Private Practice AMA/MBS Item Keyword File"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialization and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000
.
ML1000    CALL      PRIKW000               * Regen AMA/MBS Item Keyword
          GOTO      ML9999
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      PRIITEM1,"priitemf"
          OPEN      PRIIKIA1,"priikiaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    =       (0)  Valid option selected                  *
.*                              (1)  Exit option selected                   *
.****************************************************************************
.
. ---- display option screen ----
.
OPTN0000  DISPLAY   *P65:2,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Regenerate Private Practice":
                                            " AMA/MBS Item Keyword Index"
.
          KEYIN     *P1:11,*EL,"Select Option : _",*P17:11,*V2LON,COPTION
          COMPARE   ZERO,COPTION
          GOTO      OPTN9000 IF EQUAL
.
          BRANCH    COPTION,OPTN1000
          BEEP
          GOTO      OPTN0000
.
OPTN1000  MOVE      COPTION,OPTION
.
          MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9000  MOVE      ONE,EXIT
.
OPTN9999  RETURN
.****************************************************************************
.*      Regeneration of ICD9 Disease Key Word Index                         *
.****************************************************************************
PRIKW000  DISPLAY   *P1:24,*EL,"Recalibrating Key Word Index ..."
          MOVE      ZERO,RECNUM
.
          PACK      KEY26,SP70
          CALL      RSPRIK1
PRIKW100  CALL      RKPRIK1
          BRANCH    OVRCD,PRIKW200
.
          ADD       ONE,RECNUM
          IF        RECNUM%100=0
            DISPLAY       *P40:24,*V2LON,RECNUM
          ENDIF
.
          PACK      KEY26,PRIKTFLG,PRIKKITM,PRIKKKWD,SP70
          CALL      DEPRIK1
          GOTO      PRIKW100
.
PRIKW200  DISPLAY   *P1:24,*EL,"Creating AMA/MBS Item Keyword Index..."
          MOVE      ZERO,RECNUM
.
          MOVE      SP70,SAVITEM
          MOVE      ZERO,TYPE            * Do MBS Items first
.
          PACK      KEY22,TYPE,Z70       * Want to read newest Item code one
          CALL      RSPRIT1
PRIKW300  CALL      RPPRIT1  
          BRANCH    OVRCD,PRIKW400
.
          COMPARE   TYPE,PRITFLAG
          GOTO      PRIKW400 IF NOT EQUAL 
.
          MATCH     SAVITEM,PRITITMN    * Only want newest Item Code once
          GOTO      PRIKW300 IF EQUAL  
.
          MOVE      PRITITMN,SAVITEM    * Save Item
.
          ADD       ONE,RECNUM
          IF        RECNUM%100=0
            DISPLAY       *P40:24,*V2LON,RECNUM
          ENDIF
.
          MOVE      PRITFLAG,ITEMTYP  * Input var for PRIIKIUP
          MOVE      PRITITMN,ITEMNO   * Input var for PRIIKIUP
          MOVE      PRITDESC,ITEMDESC * Input var for PRIIKIUP
          PROC      PRIIKIUP          * Update AMA/MBS Item Keyword Index
          GOTO      PRIKW300
.
PRIKW400  MOVE      SP70,SAVITEM
          MOVE      ONE,TYPE             * Do AMA Items
.
          PACK      KEY22,TYPE,Z70       * Want to read newest Item code one
          CALL      RSPRIT1
PRIKW500  CALL      RPPRIT1  
          BRANCH    OVRCD,PRIKW999
.
          COMPARE   TYPE,PRITFLAG
          GOTO      PRIKW999 IF NOT EQUAL 
.
          MATCH     SAVITEM,PRITITMN    * Only want newest Item Code once
          GOTO      PRIKW500 IF EQUAL  
.
          MOVE      PRITITMN,SAVITEM    * Save Item
.
          ADD       ONE,RECNUM
          IF        RECNUM%100=0
            DISPLAY       *P40:24,*V2LON,RECNUM
          ENDIF
.
          MOVE      PRITFLAG,ITEMTYP  * Input var for PRIIKIUP
          MOVE      PRITITMN,ITEMNO   * Input var for PRIIKIUP
          MOVE      PRITDESC,ITEMDESC * Input var for PRIIKIUP
          PROC      PRIIKIUP          * Update AMA/MBS Item Keyword Index
          GOTO      PRIKW500
.
PRIKW999  RETURN
. =========================================================================
.
          INC       STD001IO
.
          INC       PRIITMIO/INC
          INC       PRIIKIIO/INC
          INC       PRIIKIPR

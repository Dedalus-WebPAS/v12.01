.**********************************************************************
.* System   : Operating Room System                                   *
.* Program  : IBAOPR23                                                *
.* Desc     : Operating Room Enquiry                                  *
.**********************************************************************
.* Date     :  11/12/89                                               *
.* Author   :  K.Peak                                                 *
.* Mods.    :  V5.01.01  14/11/1990 Justin Coates                     *
.*                       Modified according to new FD's               *
.*        V5.00     18/12/1990    Justin Coates                       *
.*                  Converted to Version 5                            *
.*        V5.01     02/01/1991    Graeme Williams                     *
.*                  Changed for new OPRORUFD statistics file          *
.*        V5.01.01  21/01/91      ROD                                 *
.*                  Conversion to Release 5.01                        *
.**********************************************************************
.
.$$$$$
.    Operating Room System Operating Room File Enquiry (IBAOPR23 )
.    -------------------------------------------------------------
.
.    - Initialisation
.
.    - Display Header
.            - Open Files
.               OPROPRA1
.               PATCODE1
.               CONTROLF
.
.    - Clear Input Variables
.
.    - Display Screen Format
.
.    - Input Operating Room Code
.            - Read OPROPRA1, check if exists
.
.    - Display Data
.            - Description
.            - Location     
.            - Status 
.            - Read CONTROLF sector 40 position *203 onwards
.                   - If user defined fields switched on accept fields....
.                             - User Defined 1.
.                             - User Defined 2.
.                             - User Defined 3.
.                             - User Defined 4.
.                             - User Defined 5.
.
.    - END
.$$$$$
.
          INC       STD001FD
          INC       OPROPRFD/INC           * Operating Room File
          INC       OPRCONFD/INC           * Operating Room Control file
          INC       OPRORUFD/INC
          INC       PATDRGFD/INC
          INC       PATCODFD/INC           * patient codes file
+
.**********************************************************************
.*                           CONSTANTS                                *
.**********************************************************************
.
CODEY1    INIT      "Y1"
CODEY2    INIT      "Y2"
CODEY3    INIT      "Y3"
CODEY4    INIT      "Y4"
CODEY5    INIT      "Y5"
.
OPT1      INIT      "- Basic Details "
OPT2      INIT      "- Statistics "
.
STATUS    DIM       10
STATUS1   INIT      "Active    "
STATUS2   INIT      "Non Active"
.
+
.**********************************************************************
.*                       GLOBAL VARIABLES                             *
.**********************************************************************
.
CODE      DIM       5
CPTDATE   DIM       8
.
DIM2A     DIM       2
DIM30     DIM       30
.
.
LNO       FORM      2
.
RDESC1    DIM       20              * user define codes description
RDESC2    DIM       20
RDESC3    DIM       20
RDESC4    DIM       20
RDESC5    DIM       20
.
OPTION1   FORM      2
. 
PAGENO    FORM      3
PRDATE    DIM       5
.
STATIND   DIM       1               * status
.
TEN10     FORM      "100"
.
USERDES1  DIM       20              * user define category description
USERDES2  DIM       20
USERDES3  DIM       20
USERDES4  DIM       20
USERDES5  DIM       20
TIMEBOOK  FORM      7
TEMPUTIL  FORM      8.6
UTIL      FORM      3.2
UVERT1    FORM      2
.
VAR       FORM      2
.
PRGNAM    DIM       50
PRGID     INIT      "IBAOPR23"
PRGNAM1   INIT      " Enquiry"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                           MAINLINE                                 *
.**********************************************************************
.
ML0000    CALL      INIT0000               * initialization and open files
.
ML0100    CALL      CLR0000                * clear input variables
          CALL      SCR0000                * display screen format 
.
          CALL      ENTR0000               * enter operating room code
          BRANCH    EXIT,ML9999
.
          CALL      DISP0000               * display details
          CALL      USER0000               * display user define details
.
ML0500    CALL      SFLD0000               * select choice of exit
          BRANCH    OPTION1,ML1000,ML3000,ML2000,ML0100,ML9999
          GOTO      ML0100
.
ML1000    CALL      BASC0000               * display basic details
          GOTO      ML0500
.
ML2000    CALL      PRNT0000               * print enquiry
          CALL      PRST0000
          GOTO      ML0500
.
ML3000    CALL      STAT0000               * display statistics
          GOTO      ML0500
.
ML9999    CHAIN     PGM                    * chain back to program
+
.**********************************************************************
.*                             INIT0000                               *
.*                      Initialization  Routine                       *
.**********************************************************************
.
INIT0000  DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,FORTY;*81,OPCNODSC
.
.         remove blanks from op room desc
.
          ENDSET    OPCNODSC
.
INIT1000  CMATCH    SP1,OPCNODSC
          GOTO      INIT2000 IF NOT EQUAL
.
          BUMP      OPCNODSC BY -1
          GOTO      INIT1000 IF NOT EOS
.
INIT2000  LENSET    OPCNODSC
          RESET     OPCNODSC
.
          PACK      PRGNAM,OPCNODSC,PRGNAM1
          RESET     PRGNAM
          REP       UPPLOW,PRGNAM
.
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening opropraf";
          OPEN      OPROPRA1,"opropraf"
.
          DISPLAY   *P64:24,"opropraf";
          OPEN      OPROPRA2,"opropraf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdrgaf";
          OPEN      PATDRGA1,"patdrgaf"
.
          DISPLAY   *P64:24,"oproruaf";
          OPEN      OPRORUA1,"oproruaf"
.
          MOVE      ONE,CNEWDS              * new ? option
          READ      CONTROLF,FORTY;*203,OPCNUOU1,OPCNUOM1,OPCNUOU2,OPCNUOM2:
                                        OPCNUOU3,OPCNUOM3,OPCNUOU4,OPCNUOM4:
                                        OPCNUOU5,OPCNUOM5
INIT9999  RETURN
+
.**********************************************************************
.*                             CLR0000                                *
.*                 Routine to Clear Local Variables                   *
.**********************************************************************
.
CLR0000   MOVE      ZERO,PAGENO
.
. ----- Clear the Operating Room File Variables -----
.
          MOVE      SP6,OPRMROOM
          PACK      OPRMDESC,SP20,SP20
          PACK      OPRMLOC,SP20,SP20
          UNPACK    SP30,OPRMUSR1,OPRMUSR2,OPRMUSR3,OPRMUSR4,OPRMUSR5
.
          MOVE      ZERO,OPRMSTAT           * new fields to initialize & display
          MOVE      ZERO,OPRMSUN
          MOVE      ZERO,OPRMMON
          MOVE      ZERO,OPRMTUE
          MOVE      ZERO,OPRMWED
          MOVE      ZERO,OPRMTHU
          MOVE      ZERO,OPRMFRI
          MOVE      ZERO,OPRMSAT
.
CLR9999   RETURN
+
.**********************************************************************
.*                            SCRN0000                                *
.*                     Main Data Screen Display                       *
.**********************************************************************
.
SCR0000   PACK      KEY60,PRGNAM
          CALL      DISPLIN2
          DISPLAY   *P1:3,*EF:
                    *P1:4,*+,OPCNODSC,*-," Code",*P25:4," : ":
                    *P1:6,"Description              : ":
                    *P1:7,"Location                 : ":
                    *P1:8,"Status                   : ":
                    *P1:9,"Time available :":
                    *P1:10,"  Sunday                 : ",*P34:10,"mins":
                    *P1:11,"  Monday                 : ",*P34:11,"mins":
                    *P1:12,"  Tuesday                : ",*P34:12,"mins":
                    *P1:13,"  Wednesday              : ",*P34:13,"mins":
                    *P1:14,"  Thursday               : ",*P34:14,"mins":
                    *P1:15,"  Friday                 : ",*P34:15,"mins":
                    *P1:16,"  Saturday               : ",*P34:16,"mins";
.
.------ process screen for user defined fields -----
.
          MOVE      "17",CVERT              * first row number
          COMPARE   ONE,OPCNUOU1
          GOTO      SCR1000 IF NOT EQUAL
          PACK      KEY5,CODEY1,SP3
          CALL      RDCODE1
          BRANCH    OVRCD,SCR1000
          DISPLAY   *P1:CVERT,TDESC,*P25:CVERT," : ";
          ADD       ONE,CVERT
.
.------ display user defined field 2, if valid -----
.
SCR1000   COMPARE   ONE,OPCNUOU2
          GOTO      SCR1100 IF NOT EQUAL
          PACK      KEY5,CODEY2,SP3
          CALL      RDCODE1
          BRANCH    OVRCD,SCR1100
          DISPLAY   *P1:CVERT,TDESC,*P25:CVERT," : ";
          ADD       ONE,CVERT
.
.------ display user defined field 3, if valid ------
.
SCR1100   COMPARE   ONE,OPCNUOU3
          GOTO      SCR1200 IF NOT EQUAL
          PACK      KEY5,CODEY3,SP3
          CALL      RDCODE1
          BRANCH    OVRCD,SCR1200
          DISPLAY   *P1:CVERT,TDESC,*P25:CVERT," : ";
          ADD       ONE,CVERT
.
.------- display user defined field 4, if vaild -----
.
SCR1200   COMPARE   ONE,OPCNUOU4
          GOTO      SCR1300 IF NOT EQUAL
          PACK      KEY5,CODEY4,SP3
          CALL      RDCODE1
          BRANCH    OVRCD,SCR1300
          DISPLAY   *P1:CVERT,TDESC,*P25:CVERT," : ";
          ADD       ONE,CVERT
.
.------- display user defined field 5, if valid ------
.
SCR1300   COMPARE   ONE,OPCNUOU5
          GOTO      SCR9999 IF NOT EQUAL
          PACK      KEY5,CODEY5,SP3
          CALL      RDCODE1
          BRANCH    OVRCD,SCR9999
          DISPLAY   *P1:CVERT,TDESC,*P25:CVERT," : ";
.
SCR9999   RETURN
+
.**********************************************************************
.*                           ENTR00000                                *
.*                  Read Operating Room File for Record               *
.**********************************************************************
.
ENTR0000  MOVE      FALSE,EXIT
.
          KEYIN     *P28:4,*DV,UNDLN6,*EL:
                    *P28:4,*V2LON,OPRMROOM;
.
          ENDSET    OPRMROOM
          APPEND    SP6,OPRMROOM
          RESET     OPRMROOM
.
          DISPLAY   *P28:4,*V2LON,OPRMROOM;
.
          MATCH     SP6,OPRMROOM
          GOTO      ENTR9500 IF EQUAL
.
          MATCH     QUEST,OPRMROOM
          CALL      QEST0000 IF EQUAL
          BRANCH    EXIT,ENTR9500
.
          PACK      KEY6,OPRMROOM
          CALL      RDOPOPR1
          BRANCH    OVRCD,ENTR9000
          MOVE      OPRMSTAT,STATIND
          GOTO      ENTR9999
.
ENTR9000  DISPLAY   *P1:24,*B,*+,OPCNODSC," code not on file.  ";
          CALL      HITENTER
          GOTO      ENTR0000
.
ENTR9500  MOVE      TRUE,EXIT                    * nothing entered
ENTR9999  RETURN
+
.*************************************************************************
.*                           QEST0000                                    *
.*                Handle question mark on theatre code                   *
.*                     called by : ENTR1000                              *
.*************************************************************************
QEST0000  CALL      OPROPRDS
.
QEST1000  DISPLAY   *P1:24,*+,OPCNODSC," code : ",*EL;
          MOVELPTR  OPCNODSC,CCOL           * get logical length
          ADD       "9",CCOL                * allow for " code : "
          KEYIN     *PCCOL:24,*DV,UNDLN6:
                    *PCCOL:24,*V2LON,OPRMROOM
.
          ENDSET    OPRMROOM
          APPEND    SP10,OPRMROOM
          RESET     OPRMROOM
.
          MATCH     SP6,OPRMROOM
          GOTO      QEST9000 IF EQUAL
.
          MATCH     QUEST,OPRMROOM
          GOTO      QEST1000 IF EQUAL
.
          MOVE      OPRMROOM,KEY6
          CALL      RDOPOPR1
          BRANCH    OVRCD,QEST3000
          CALL      SCR0000
          MOVE      FALSE,EXIT
          GOTO      QEST9999
.
QEST3000  DISPLAY   *P1:24,*B,*+,OPCNODSC," code not on file.  ";
          CALL      HITENTER
          GOTO      QEST1000
.
QEST9000  MOVE      TRUE,EXIT
.
QEST9999  RETURN
+
.**************************************************************************
.*                               DISP0000                                 *
.*                   Display Data from the Operating Room File            *
.**************************************************************************
.
DISP0000  MOVE      SP30,TDESC
.
          PACK      KEY60,PRGNAM,OPT1
          CALL      DISPLIN2
.
          DISPLAY   *P28:4,*V2LON,OPRMROOM:
                    *P28:6,*V2LON,OPRMDESC:
                    *P28:7,OPRMLOC;
.
          MOVE      STATUS1,STATUS                 * status description
          LOAD      STATUS,OPRMSTAT,STATUS2
.
          DISPLAY   *P28:8,*V2LON,STATIND,*HOFF:
                    *P34:8,STATUS,*V2LON,*P28:10,OPRMSUN,*P28:11,OPRMMON:
                    *P28:12,OPRMTUE,*P28:13,OPRMWED,*P28:14,OPRMTHU:
                    *P28:15,OPRMFRI,*P28:16,OPRMSAT;
DISP9999  RETURN
+
.**************************************************************************
.*                         USER0000                                       *
.*                     process user defined fields                        *
.**************************************************************************
.
USER0000  MOVE      "17",UVERT1
          COMPARE   ONE,OPCNUOU1
          GOTO      USER1000 IF NOT EQUAL
.
USER0500  MOVE      SP20,USERDES1
          MATCH     SP3,OPRMUSR1
          GOTO      USER1000 IF EQUAL
          DISPLAY   *P28:UVERT1,*V2LON,OPRMUSR1,*HOFF;
          PACK      KEY5,CODEY1,OPRMUSR1
          CALL      RDCODE1
          BRANCH    OVRCD,USER0700
          MOVE      TDESC,USERDES1
.
USER0700  DISPLAY   *P34:UVERT1,USERDES1;
          ADD       ONE,UVERT1
.
.------- process user defined field 2. ----
.
USER1000  COMPARE   ONE,OPCNUOU2
          GOTO      USER2000 IF NOT EQUAL
.
          MATCH     SP3,OPRMUSR2
          GOTO      USER2000 IF EQUAL
.
          DISPLAY   *P28:UVERT1,*V2LON,OPRMUSR2,*HOFF;
.
          MOVE      SP20,USERDES2
          PACK      KEY5,CODEY2,OPRMUSR2
          CALL      RDCODE1
          BRANCH    OVRCD,USER1700
          MOVE      TDESC,USERDES2
.
USER1700  DISPLAY   *P34:UVERT1,USERDES2;
          ADD       ONE,UVERT1
.
.------ process user defined field 3 ----
.
USER2000  COMPARE   ONE,OPCNUOU3
          GOTO      USER3000 IF NOT EQUAL
.
          MATCH     SP3,OPRMUSR3
          GOTO      USER3000 IF EQUAL
.
          DISPLAY   *P28:UVERT1,*V2LON,OPRMUSR3,*HOFF;
.
          PACK      KEY5,CODEY3,OPRMUSR3
          CALL      RDCODE1
          BRANCH    OVRCD,USER2700
          MOVE      TDESC,USERDES3
.
USER2700  DISPLAY   *P34:UVERT1,USERDES3;
          ADD       ONE,UVERT1
.
.------- process user defined field 4. -----
.
USER3000  COMPARE   ONE,OPCNUOU4
          GOTO      USER4000 IF NOT EQUAL
.
          MATCH     SP3,OPRMUSR4
          GOTO      USER4000 IF EQUAL
.
          DISPLAY   *P28:UVERT1,*V2LON,OPRMUSR4,*HOFF;
.
          PACK      KEY5,CODEY4,OPRMUSR4
          CALL      RDCODE1
          BRANCH    OVRCD,USER3700
          MOVE      TDESC,USERDES4
.
USER3700  DISPLAY   *P34:UVERT1,USERDES4;
          ADD       ONE,UVERT1
.
.-------- process user defined field 5 ------
.
USER4000  COMPARE   ONE,OPCNUOU5
          GOTO      USER9999 IF NOT EQUAL
.
          MATCH     SP3,OPRMUSR5
          GOTO      USER9999 IF EQUAL
.
          DISPLAY   *P28:UVERT1,*V2LON,OPRMUSR5,*HOFF;
.
          PACK      KEY5,CODEY5,OPRMUSR5
          CALL      RDCODE1
          BRANCH    OVRCD,USER4700
          MOVE      TDESC,USERDES5
.
USER4700  DISPLAY   *P34:UVERT1,USERDES5;
USER9999  RETURN
+

.*************************************************************************
.*                               PRST0000                                *
.*                         Print   statistics                            *
.*                     called by : ML1000                                *
.*************************************************************************
PRST0000  CALL      PSTH0000
.
          PACK      CPTDATE,CCC,CYY,CMM,CDD
          REP       " 0",CPTDATE
          CALL      GPERD
.
          BRANCH    EXIT,PRST8000
.
          PACK      KEY6,DRGYR,DRGNUM
          CALL      RDDRGA1
.
          GOTO      PRST2000
.
PRST1000  CALL      RDPDRGA1
          BRANCH    OVRCD,PRST9000
.
PRST2000  CALL      PRDT0000
.
          COMPARE   CLNO,FIFTY5
          GOTO      PRST9000 IF EQUAL
.
          GOTO      PRST1000
.
PRST8000  DISPLAY   *P1:24,"Error - Current period not on file. ";
          CALL      HITENTER
.
          MOVE      TRUE,EXIT
          GOTO      PRST9999
.
PRST9000  PRINT     *N,"*** End of report ***"
          MOVE      FALSE,EXIT
.
PRST9999  RETURN
.*************************************************************************
.*                               STAT0000                                *
.*                         Process statistics                            *
.*                     called by : ML1000                                *
.*************************************************************************
STAT0000  CALL      SSRC0000
          PACK      KEY60,PRGNAM,OPT2
          CALL      DISPLIN2
.
          MOVE      NINE,CVERT
          PACK      CPTDATE,CCC,CYY,CMM,CDD
          REP       " 0",CPTDATE
          CALL      GPERD
.
          BRANCH    EXIT,STAT8000
.
          PACK      KEY6,DRGYR,DRGNUM
          CALL      RDDRGA1
.
          GOTO      STAT2000
.
STAT1000  CALL      RDPDRGA1
          BRANCH    OVRCD,STAT9000
.
STAT2000  CALL      DISD0000
.
          COMPARE   CVERT,TWENTY3
          GOTO      STAT9000 IF EQUAL
.
          GOTO      STAT1000
.
STAT8000  DISPLAY   *P1:24,"Error - Current period not on file. ";
          CALL      HITENTER
.
          MOVE      TRUE,EXIT
          GOTO      STAT9999
.
STAT9000  MOVE      FALSE,EXIT
.
STAT9999  RETURN
.**************************************************************************
.*                               UNDL0000                                 *
.*                   Print stats heading                                  *
.**************************************************************************
UNDL0000  PRINT     *1,"------": 
                    *12,"----------------":
                    *32,"--------------":
                    *50,"-----------"
UNDL9999  RETURN
.**************************************************************************
.*                               PSTH0000                                 *
.*                   Print stats heading                                  *
.**************************************************************************
PSTH0000  PRINT     *N,*1,"Period": 
                    *12,"Time Used (Mins)":
                    *32,"Time Available":
                    *50,"Utilization"
          CALL      UNDL0000
          ADD       TWO,CLNO
PSTH9999  RETURN
.**************************************************************************
.*                               SSRC0000                                 *
.*                   Display Statistics screen                            *
.**************************************************************************
SSRC0000  DISPLAY   *P1:3,*EF:
                    *P5:4,*+,OPCNODSC,*-," Code",*P29:4," : ",*V2LON,OPRMROOM:
                    *P5:6,*HOFF,"Description              : ",*V2LON,OPRMDESC:
                    *P1:8,*V2LON,*ULON,"Period":
                    *P12:8,"Time Used (Mins)":
                    *P32:8,"Time Available":
                    *P50:8,"Utilization"
SSRC9999  RETURN
+
.*************************************************************************
.*                               PRDT0000                                *
.*                         Print   a line data                           *
.*                     called by : STAT1000                              *
.*************************************************************************
PRDT0000   PACK     KEY12,DRGYR,DRGNUM,OPRMROOM
           CALL     RDOPORU1
           BRANCH   OVRCD,PRDT9000
.
           UNPACK   DRGCYR,DIM2,DIM2
           PACK     PRDATE,DRGCNUM,SLASH,DIM2
.
           MOVE     OPOUBTIM,TIMEBOOK
           ADD      OPOUTTIM,TIMEBOOK
           ADD      OPOUETIM,TIMEBOOK
.
           MOVE     ZERO,UTIL
           COMPARE  ZERO,OPOUAVAL
           GOTO     PRDT1000 IF EQUAL
           MOVE     TIMEBOOK,TEMPUTIL
           DIV      OPOUAVAL,TEMPUTIL
           MULT     TEN10,TEMPUTIL
           MOVE     TEMPUTIL,UTIL
.
PRDT1000   PRINT    *2,PRDATE,*15,TIMEBOOK,*34,OPOUAVAL:
                    *53,UTIL
           ADD      ONE,CLNO
.
           MOVE     FALSE,EXIT
           GOTO     DISD9999
.
PRDT8000   DISPLAY  *P1:24,*V2LON,"Statistics have not been updated. ";
           CALL     HITENTER
           MOVE     TRUE,EXIT
.
PRDT9000   UNPACK   DRGCYR,DIM2,DIM2
           PACK     PRDATE,DRGCNUM,SLASH,DIM2
           MOVE     ZERO,UTIL
           MOVE     ZERO,TIMEBOOK
           MOVE     ZERO,OPOUAVAL
           PRINT    *2,PRDATE,*15,TIMEBOOK,*34,OPOUAVAL:
                    *53,UTIL
           ADD      ONE,CLNO
. 
PRDT9999   RETURN
           
.*************************************************************************
.*                               DISD0000                                *
.*                         Display a line data                           *
.*                     called by : STAT1000                              *
.*************************************************************************
DISD0000   PACK     KEY12,DRGYR,DRGNUM,OPRMROOM
           CALL     RDOPORU1
           BRANCH   OVRCD,DISD9000
.
           UNPACK   DRGCYR,DIM2,DIM2
           PACK     PRDATE,DRGCNUM,SLASH,DIM2
.
           MOVE     OPOUBTIM,TIMEBOOK
           ADD      OPOUTTIM,TIMEBOOK
           ADD      OPOUETIM,TIMEBOOK
.
           MOVE     ZERO,UTIL
           COMPARE  ZERO,OPOUAVAL
           GOTO     DISD1000 IF EQUAL
           MOVE     TIMEBOOK,TEMPUTIL
           DIV      OPOUAVAL,TEMPUTIL
           MULT     TEN10,TEMPUTIL
           MOVE     TEMPUTIL,UTIL
.
DISD1000   DISPLAY  *P2:CVERT,PRDATE,*P15:CVERT,TIMEBOOK,*P34:CVERT,OPOUAVAL:
                    *P53:CVERT,UTIL
           ADD      ONE,CVERT
.
           MOVE     FALSE,EXIT
           GOTO     DISD9999
.
DISD8000   DISPLAY  *P1:24,*V2LON,"Statistics have not been updated. ";
           CALL     HITENTER
           MOVE     TRUE,EXIT
.
DISD9000   UNPACK   DRGCYR,DIM2,DIM2
           PACK     PRDATE,DRGCNUM,SLASH,DIM2
           MOVE     ZERO,TIMEBOOK
           MOVE     ZERO,OPOUAVAL
           MOVE     ZERO,UTIL
           DISPLAY  *P2:CVERT,PRDATE,*P15:CVERT,TIMEBOOK,*P34:CVERT,OPOUAVAL:
                    *P53:CVERT,UTIL
           ADD      ONE,CVERT
. 
DISD9999   RETURN
           
.**************************************************************************
.*                         SFLD0000                                       *
.*                     Select choice of exit                              *
.**************************************************************************
.
SFLD0000  MOVE      ZERO,OPTION1
.
          KEYIN     *P1:24,*EL," (",*V2LON,"B",*HOFF,")asic Details, (":
                               *V2LON,"S",*HOFF,")tatistics, (":
                               *V2LON,"P",*HOFF,")rint, (":
                               *V2LON,"N",*HOFF,")ext, (":
                               *V2LON,"E",*HOFF,")xit ? ",*V2LON,ANS;
          REP       UPPLOW,ANS
.
          CMATCH    "B",ANS
          GOTO      SFLD1000 IF NOT EQUAL
          MOVE      ONE,OPTION1
          GOTO      SFLD9999
.
SFLD1000  CMATCH    "S",ANS
          GOTO      SFLD2000 IF NOT EQUAL
          MOVE      TWO,OPTION1
          GOTO      SFLD9999
.
SFLD2000  CMATCH    "P",ANS
          GOTO      SFLD3000 IF NOT EQUAL
          MOVE      THREE,OPTION1
          GOTO      SFLD9999
.
SFLD3000  CMATCH    "N",ANS
          GOTO      SFLD4000 IF NOT EQUAL
          MOVE      FOUR,OPTION1
          GOTO      SFLD9999
.
SFLD4000  CMATCH    "E",ANS
          GOTO      SFLD5000 IF NOT EQUAL
          MOVE      FIVE,OPTION1
          GOTO      SFLD9999
.
SFLD5000  BEEP  
          GOTO      SFLD0000
.
SFLD9999  RETURN
+
.**************************************************************************
.*                         BASC0000                                       *
.*                     Display Basic Details                              *
.**************************************************************************
.
BASC0000  MOVE      STATUS1,STATUS
          LOAD      STATUS,OPRMSTAT,STATUS2    * status description
.
BASC2000  CALL      SCR0000                    * display screen
.
          DISPLAY   *P28:4,*V2LON,OPRMROOM; 
.
          CALL      DISP0000                   * display details
          CALL      USER0000                   * display user define details
.
          RETURN
+
.**************************************************************************
.*                         PRNT0000                                       *
.*                     Print Enquiry                                      *
.**************************************************************************
.
PRNT0000  DISPLAY   *P1:24,*EL,*P40:24,*V2LON:
                    "** Printing Enquiry **",*W
          CLOCK     TIME,CTIMEIS
          MOVE      ZERO,CPAGENO
          CALL      HEAD80  
.
          MOVE      STATUS1,STATUS
          LOAD      STATUS,OPRMSTAT,STATUS2        * status description
.
          PRINT     *N,*1,*+,OPCNODSC,*-," Code",*21," : ",OPRMROOM,*N:
                       *1,"Description          : ",OPRMDESC,*N:
                       *1,"Location             : ",OPRMLOC,*N:
                       *1,"Status               : ",OPRMSTAT,SP4,STATUS,*N:
                       *N,"Time Available : ",*N:
                       *1,"  Sunday             : ",OPRMSUN,*N:
                       *1,"  Monday             : ",OPRMMON,*N:
                       *1,"  Tuesday            : ",OPRMTUE,*N:
                       *1,"  Wednesday          : ",OPRMWED,*N:
                       *1,"  Thursday           : ",OPRMTHU,*N:
                       *1,"  Friday             : ",OPRMFRI,*N:
                       *1,"  Saturday           : ",OPRMSAT,*N,*N;
.
.------- print user defined field 1, if valid ------
.
          COMPARE   ONE,OPCNUOU1
          GOTO      PRNT1200 IF NOT EQUAL
.
          MOVE      SP20,USERDES1
          PACK      KEY5,CODEY1,SP3
          CALL      RDCODE1
          BRANCH    OVRCD,PRNT0500
          MOVE      TDESC,USERDES1
.
PRNT0500  PRINT     *1,USERDES1," : ";
          ADD       ONE,CLNO
.
          MOVE      SP20,RDESC1
          MATCH     SP3,OPRMUSR1
          GOTO      PRNT1000 IF EQUAL
          PACK      KEY5,CODEY1,OPRMUSR1
          CALL      RDCODE1
          BRANCH    OVRCD,PRNT1000
          MOVE      TDESC,RDESC1
.
PRNT1000  PRINT     *24,OPRMUSR1,SP2,RDESC1,*N;
.
.------- print user defined field 2, if valid ------
.
PRNT1200  COMPARE   ONE,OPCNUOU2
          GOTO      PRNT2200 IF NOT EQUAL
.
          MOVE      SP20,USERDES2
          PACK      KEY5,CODEY2,SP3
          CALL      RDCODE1
          BRANCH    OVRCD,PRNT1500
          MOVE      TDESC,USERDES2
.
PRNT1500  PRINT     *1,USERDES2," : ";
          ADD       ONE,CLNO
.
          MOVE      SP20,RDESC2
          MATCH     SP3,OPRMUSR2
          GOTO      PRNT2000 IF EQUAL
          PACK      KEY5,CODEY2,OPRMUSR2
          CALL      RDCODE1
          BRANCH    OVRCD,PRNT2000
          MOVE      TDESC,RDESC2
.
PRNT2000  PRINT     *24,OPRMUSR2,SP2,RDESC2,*N;
.
.------- print user defined field 3, if valid ------
.
PRNT2200  COMPARE   ONE,OPCNUOU3
          GOTO      PRNT3200 IF NOT EQUAL
.
          MOVE      SP20,USERDES3
          PACK      KEY5,CODEY3,SP3
          CALL      RDCODE1
          BRANCH    OVRCD,PRNT2500
          MOVE      TDESC,USERDES3
.
PRNT2500  PRINT     *1,USERDES3," : ";
          ADD       ONE,CLNO
.
          MOVE      SP20,RDESC3
          MATCH     SP3,OPRMUSR3
          GOTO      PRNT3000 IF EQUAL
          PACK      KEY5,CODEY3,OPRMUSR3
          CALL      RDCODE1
          BRANCH    OVRCD,PRNT3000
          MOVE      TDESC,RDESC3
.
PRNT3000  PRINT     *24,OPRMUSR3,SP2,RDESC3,*N;
.
.------- print user defined field 4, if valid ------
.
PRNT3200  COMPARE   ONE,OPCNUOU4
          GOTO      PRNT4200 IF NOT EQUAL
.
          MOVE      SP20,USERDES4
          PACK      KEY5,CODEY4,SP3
          CALL      RDCODE1
          BRANCH    OVRCD,PRNT3500
          MOVE      TDESC,USERDES4
.
PRNT3500  PRINT     *1,USERDES4," : ";
          ADD       ONE,CLNO
.
          MOVE      SP20,RDESC4
          MATCH     SP3,OPRMUSR4
          GOTO      PRNT4000 IF EQUAL
          PACK      KEY5,CODEY4,OPRMUSR4
          CALL      RDCODE1
          BRANCH    OVRCD,PRNT4000
          MOVE      TDESC,RDESC4
.
PRNT4000  PRINT     *24,OPRMUSR4,SP2,RDESC4,*N;
.
.------- print user defined field 5, if valid ------
.
PRNT4200  COMPARE   ONE,OPCNUOU5
          GOTO      PRNT5200 IF NOT EQUAL
.
          MOVE      SP20,USERDES5
          PACK      KEY5,CODEY5,SP3
          CALL      RDCODE1
          BRANCH    OVRCD,PRNT4500
          MOVE      TDESC,USERDES5
.
PRNT4500  PRINT     *1,USERDES5," : ";
          ADD       ONE,CLNO
.
          MOVE      SP20,RDESC5
          MATCH     SP3,OPRMUSR5
          GOTO      PRNT5000 IF EQUAL
          PACK      KEY5,CODEY5,OPRMUSR5
          CALL      RDCODE1
          BRANCH    OVRCD,PRNT5000
          MOVE      TDESC,RDESC5
.
PRNT5000  PRINT     *24,OPRMUSR5,SP2,RDESC5,*N;
.
PRNT5200 
          DISPLAY   *P1:24,*EL,*P40:24,*V2LON:
                    "** Report Generation Completed **",*W
PRNT9999  RETURN
+
.********************************************************************** 
.*              I/O INCLUDES FOR FILES AND GENERAL ROUTINES           *
.**********************************************************************
.
         INC       STD001IO
         INC       OPROPRDS
         INC       PATCODKY                * patient codes file routine
         INC       PATCOMN3
         INC       PATDRGIO/INC
         INC       PATCODIO/INC            * patient i/o for codes
         INC       OPROPRIO/INC            * operating room i/o
         INC       OPRORUIO/INC
.

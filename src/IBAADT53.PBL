.******************************************************************************
.* System         : Patient Billing                                           *
.* Program        : IBAADT53.PBL                                              *
.* Name           : Input QLD HDP Mental Health Details                       *
.******************************************************************************
.* Date           : 02/08/1996                                                *
.* Author         : Junior                                                    *
.* Function       : Program to keyin the mental health details required for   *
.*                  the QLD HDP.                                              *
.* Modifications  :                                                           *
.*                                                                            *
.******************************************************************************
.* V12.00.01 22/05/2025 Thanh T         TSK 0955096                           *
.*           Changed for alphanumeric visit number                            *
.******************************************************************************
.* V10.04.01  15/06/2014 Jill Parkinson CAR 301639                            *
.*            Moved call TFILENAM to INIT0000                                 *
.* V10.03.01  19/12/2012 Patrick Adair                             CAR 261630 *
.*                       Removed port number from temp file name              *
.* V10.02.01 07/07/2011  Steve Armstrong  CAR 240722                          *
.*           Added RDPMPX21 prior to calling PMIHEAD                          *
.******************************************************************************
.* V9.04.01  03/05/2005  Mike Laming   CAR 61341                              *
.*           July 2005 Queensland Health changes                              *
.******************************************************************************
.* V9.03.01  29/10/2003 Sandra Barcham   43783                                *
.*           Recompiled for NZHISIIO and NZHISIUP                             *
.******************************************************************************
.* V9.02.01  10/02/2003  Lina Vo                                              *
.*           Removed open of outpreaf - not used.                             *
.* V9.02.00  04/07/2002  J.Tan                                                *
.*           Ported from 6.10                                                 *
.******************************************************************************
.*        V6.09.01  02/05/2002  Greg Horvat      SRF 11639                    *
.*                  Fixed the validation of pension status for repat, invalid,*
.*                  unemployed & sickness benefit patients                    *
.*        V6.06.02  07/01/2000  Tonii Tang  SRF636861                         *
.*                  Recompiled for changes to PATSRCH                         *
.*        V6.06.01  28/05/1999  Andrew Peel                                   *
.*                  Recompiled for PATQMHFD - Added new variable PTQMPSNT     *
.*        V6.05.01  21/10/1998  Nicole Harrington                             *
.*                  Changed to display century on dates                       *
.*        V6.05.B01 10/06/1998  Nick Read                                     *
.*                  Fixed display and key for Admission # - was 6 chars, now 8*
.*        V6.04.01  03/10/1997  Greg Horvat                                   *
.*                  Recompiled for PMIHEAD - Added patient alerts             *
.*                                                                            *
.******************************************************************************
+
          INC       STD001FD
          INC       PATCALAG/INC            * Data variables for CALCAGE
          INC       PATCOMM/INC             * Data variables for PATCOMN1
          INC       PATDHEAD/INC            * Data variables for PMIHEAD
.
          INC       NHIMASFD/INC
          INC       PMSVX1FD/INC
          INC       NZHISIFD/INC
          INC       PATALRFD/INC            * Alert file
          INC       BOKRC1FD/INC            * Booking file
          INC       OUTPREFD/INC            * Outpatients pre attendence file
          INC       PATCONFD/INC            * Control file
          INC       PATCODFD/INC            * Codes file
          INC       PATDO1FD/INC            * Doctors file
          INC       PATDSCFD/INC            * Discharge file
          INC       PATMA1FD/INC            * Master file
          INC       HL7BMTFD/INC
          INC       PATMI1FD/INC            * Admission file
          INC       PATNOBFD/INC            * No bed fee file
          INC       PATPR1FD/INC            * Pre admission file
          INC       PATQMHFD/INC            * QLD mental health file
          INC       PATGSRFD/INC            * Surname file
          INC       PATVISFD/INC            * Visit file
          INC       PATWR1FD/INC            * Ward file
          INC       PMSPX2FD/INC
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
. ----- Tempfile definitions -----
.
TEMP1     IFILE     SQL, FIXED=80, KEY="U1-8"
FILELIN1  INIT      " 80 U1-8"
.
.Name     Type   Length   Physical   Start   Description
.----     ----   ------   --------   -----   -----------
DADMISS   DIM       8         8         1    Admission number
ADMDATE   DIM       10       10         9    Admission date (dd/mm/ccyy)
DSCDATE   DIM       10       10        19    Discharge date (dd/mm/ccyy)
ADMTYPE   DIM       3         3        29    Admission type
ADMTYPED  DIM       20       20        32    Admission type description
ATTDOC    DIM       6         6        52    Attending doctor
ATTDOCD   DIM       22       22        58    Attending doctor description
.
.End of Record                         80
.
. ----- Program Variables -----
.
ADMISS    DIM       8                       * Admission number
BJDAY     FORM      3
.
CHANGE    FORM      1                       * Change flag
CJDAY     FORM      3
CMDLINE   DIM       80
COUNT     FORM      3
.
DIFFRENT  FORM      1                       * Different details flag
DIM8      DIM       8
EMPSTATS  DIM       1                       * Employment status
FNAMEA    DIM       8                       * Tempfile name
FORM3     FORM      3
FRSTGNAM  DIM       40
FULLGNAM  DIM       80
ITEMNUM   FORM      2                       * Selection for tempfile
LINCOUNT  FORM      2                       * Screen line counter
MHLGSTIN  DIM       1                       * MH legal status indicator
NEXT      FORM      1                       * Next screen flag
NMPNUMB   DIM       20
.
PREVIOUS  FORM      1                       * Previous screen flag
PSNSTATS  DIM       1                       * Pension status
PRMSTATS  DIM       1                       * Problem status
PRSPNATM  DIM       1                       * Previous specialised
.                                             nonadmitted treatment
QADMISS   DIM       8                       * Admission number
QUESTION  FORM      1                       * Question mark flag
.
REFTFRCR  DIM       2                       * Referral to further care
SCNDGNAM  DIM       40
TYPOUSAC  DIM       1                       * Type of usual accomodation
.
SCRCOUNT  FORM      2                       * Screen number counter
SCRARRAY  DIM       8[15]                   * Save 1st record on each screen
.
. ----- Program Constants -----
.
CODEA     INIT      "A "
ZED30     INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
.
PRGID     INIT      "IBAADT53"
PRGNAM    INIT      "Input QLD HDP Mental Health Details"
VERSION   INIT      "V12.01.00"
+
.******************************************************************************
.*                                   ML0000                                   *
.*                                 Main Module                                *
.******************************************************************************
ML0000    CALL      INIT0000                * Initialise variables & open files
ML1000    CALL      OPTN0000                * Choose main option
          BRANCH    OPTION,ML2000,ML3000
          BRANCH    EXIT,ML9000
.
ML2000    CALL      KYAD0000                * Keyin the admission number
          BRANCH    EXIT,ML1000
          GOTO      ML4000
.
ML3000    CALL      KYUR0000                * Keyin the U/R number
          BRANCH    EXIT,ML1000
.
          CALL      PMIHEAD                 * Display PMI details
          CALL      SLAD0000                * Select admission
          BRANCH    EXIT,ML3000
.
ML4000    CALL      PATHEAD                 * Display PMI details
          CALL      DSMH0000                * Display the mental health details
          CALL      KYMH0000                * Keyin the mental health details
          BRANCH    EXIT,ML5000
.
          CALL      WUMH0000                * Write / update mental health files
ML5000    BRANCH    OPTION,ML2000
          GOTO      ML3000
.
ML9000    CALL      KILL0000                * Delete the tempfile
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                   INIT0000             Called by: ML0000   *
.*                        Initialise Variable & Open Files                    *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen heading
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P64:24,"patnobef";
          OPEN      PATNOBE1,"patnobef"
.
          DISPLAY   *P64:24,"patpr1af";
          OPEN      PATPR1A1,"patpr1af"
.
          DISPLAY   *P64:24,"patqmhaf";
          OPEN      PATQMHA1,"patqmhaf"
.
          DISPLAY   *P64:24,"patgsrnf";
          OPEN      PATGSRN1,"patgsrnf"
          OPEN      PATGSRN2,"patgsrnf"
          OPEN      PATGSRN3,"patgsrnf"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA1,"patvisaf"
          OPEN      PATVISA2,"patvisaf"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATWR1A4,"patwr1af"
.
          MOVE      "3",SRCHOPT
          MOVE      "3",SRCHSYS
          MOVE      "1",CNEWDS
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,FNAMEA
.
          MOVE      "11111111",ALRTMASK
.
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000              Called by: ML0000   *
.*                                Choose Option                               *
.******************************************************************************
OPTN0000  MOVE      ZERO,OPTION
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = By Admission Number":
                    *P1:6,*V2LON,"2",*HOFF," = By U/R Number":
                    *P1:8,"Select Option : ";
OPTN1000  KEYIN     *P17:8,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN9000,OPTN9000
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                  KYAD0000              Called by: ML0000   *
.*                         Keyin The Admission Number                         *
.******************************************************************************
KYAD0000  DISPLAY   *P1:3,*EF,*P1:4,"Admission Number : ";
KYAD1000  MOVE      SP70,ADMISS
          KEYIN     *P20:4,*V2LON,ADMISS;
.
          MATCH     SP70,ADMISS
          GOTO      KYAD9500 IF EQUAL       * Exit
.
          RJUSTIFY  ADMISS
          MATCH     ZEROVISN,ADMISS
          GOTO      KYAD9500 IF EQUAL
.
          CALL      VDAD0000                * Validate the admission number
          BRANCH    EXIT,KYAD1000
.
          MOVE      ZERO,EXIT
          GOTO      KYAD9999
.
KYAD9500  MOVE      ONE,EXIT
KYAD9999  RETURN
+
.******************************************************************************
.*                                  VDAD0000              Called by: KYAD0000 *
.*                        Validate The Admission Number            & SLAD0000 *
.******************************************************************************
VDAD0000  MOVE      ADMISS,PVIBILL
          PACK      KEY8,PVIBILL
          CALL      RDVISA1                 * Read the visit file
          BRANCH    OVRCD,VDAD1000
.
          PACK      KEY8,ADMISS
          CALL      RDMISS1                 * Read the admission file
          BRANCH    OVRCD,VDAD1000
.
          IF        ASTAT=1 | ASTAT=5
            GOTO      VDAD1000              * Ignore pre-admitted or cancelled
          ENDIF                               patients
.
          IF        ASTAT=3
            PACK      KEY8,ADMISS
            CALL      RDDSCH1               * Read the discharge file
            BRANCH    OVRCD,VDAD2000
          ENDIF
.
          PACK      KEY8,AURNO
          CALL      RDMAST1                 * Read the master file
          BRANCH    OVRCD,VDAD3000
.
          PACK      KEY8,PURNO
          CALL      RDPMPX21
          BRANCH    OVRCD,VDAD3000
.
          MOVE      ZERO,EXIT
          GOTO      VDAD9999
.
VDAD1000  DISPLAY   *P1:24,*EL,*B,"Admission number ",ADMISS," does not ":
                    "exist.  ";
          CALL      HITENTER
          GOTO      VDAD9500
.
VDAD2000  DISPLAY   *P1:24,*EL,*B,"Invalid discharge record for admission ":
                    "number ",ADMISS,".  ";
          CALL      HITENTER
          GOTO      VDAD9500
.
VDAD3000  DISPLAY   *P1:24,*EL,*B,"Invalid U/R number ",AURNO," for admission ":
                    "number ",ADMISS,".  ";
          CALL      HITENTER
.
VDAD9500  MOVE      ONE,EXIT
VDAD9999  RETURN
+
.******************************************************************************
.*                                  KYUR0000              Called by: ML0000   *
.*                            Keyin The U/R Number                            *
.******************************************************************************
KYUR0000  DISPLAY   *P1:3,*EF,*P1:4,"U/R Number : "
          MOVE      "4",CVERT
          MOVE      "14",CCOL
          CALL      KURN                    * Keyin the U/R number
          BRANCH    EXIT,KYUR9500
          BRANCH    OVRCD,KYUR1000
.
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          BRANCH    OVRCD,KYUR1000
.
          MOVE      ZERO,EXIT
          GOTO      KYUR9999
.
KYUR1000  DISPLAY   *P1:24,*EL,*B,"U/R Number does not exist.  ";
          CALL      HITENTER
          GOTO      KYUR0000
.
KYUR9500  MOVE      ONE,EXIT
KYUR9999  RETURN
+
.******************************************************************************
.*                                  SLAD0000              Called by: ML0000   *
.*                       Select Admission Number For U/R                      *
.******************************************************************************
SLAD0000  CALL      OPEN0000                * Open the tempfile
          CALL      LOAD0000                * Load the tempfile with admissions
          BRANCH    EXIT,SLAD9500
.
SLAD1000  CALL      DSAD0000                * Display admissions from tempfile
          CALL      QUES0000                * Admission tempfile question
          BRANCH    EXIT,SLAD9500
          BRANCH    CEXIT,SLAD3000,SLAD4000
.                         Next     Previous
.
SLAD2000  CALL      GADM0000                * Get admission number chosen
          BRANCH    EXIT,SLAD0000
.
          MOVE      ZERO,EXIT
          GOTO      SLAD9999
.
. ----- Next screen -----
.
SLAD3000  ADD       ONE,SCRCOUNT            * Increment screen number
          CALL      RPTEMP1                 * Read next 1st tempfile record
          MOVE      ADMISS,SCRARRAY[SCRCOUNT]
          GOTO      SLAD1000
.
. ----- Previous screen -----
.
SLAD4000  SUB       ONE,SCRCOUNT
          MOVE      SCRARRAY[SCRCOUNT],KEY8
          CALL      RDTEMP1                 * Read 1st pos of previous screen 
          GOTO      SLAD1000
.
SLAD9500  MOVE      ONE,EXIT
SLAD9999  RETURN
+
.******************************************************************************
.*                                  LOAD0000              Called by: SLAD0000 *
.*              Load The Tempfile With Admissions For A Given U/R             *
.******************************************************************************
LOAD0000  MOVE      PURNO,PVIURNO
          PACK      KEY24,PVIURNO,SP30
          CALL      RDSVISA2                * Position on & read the most
LOAD1000  CALL      RDKVISA2                  recent visit file records
          BRANCH    OVRCD,LOAD2000
.
          MATCH     PURNO,PVIURNO
          GOTO      LOAD2000 IF NOT EQUAL   * Different U/R, exit
.
          MOVE      PVIBILL,AADMNO
          PACK      KEY8,AADMNO
          CALL      RDMISS1                 * Read the admission file
          BRANCH    OVRCD,LOAD1000
.
          IF        ASTAT=1 | ASTAT=5
            GOTO      LOAD1000              * Ignore pre-admitted or cancelled
          ENDIF                               patients
.
          MOVE      AADMNO,ADMISS
          UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format the admission date
          MOVE      CPCDATE,ADMDATE
          REP       " 0",ADMDATE
          MOVE      ATYPE,ADMTYPE
          MOVE      "UNKNOWN             ",ADMTYPED
          PACK      KEY5,CODEA,ATYPE
          CALL      RDCODE1                 * Read the codes file
          IF        OVRCD<>1
            MOVE      TDESC,ADMTYPED
          ENDIF
.
          CALL      GDOC0000                * Get doctor details
          CALL      GDSC0000                * Get discharge details
.
          PACK      KEY8,ADMISS
          CALL      WRTEMP1                 * Write to the 1st tempfile
          GOTO      LOAD1000
.
. ----- Read 1st record for displaying -----
.
LOAD2000  PACK      KEY8,ZED30
          CALL      RSTEMP1                 * Position on & read the tempfile
          CALL      RPTEMP1
          BRANCH    OVRCD,LOAD9500
.
          MOVE      ONE,SCRCOUNT
          MOVE      ADMISS,SCRARRAY[SCRCOUNT]
          MOVE      ZERO,EXIT
          GOTO      LOAD9999
.
LOAD9500  DISPLAY   *P1:24,*EL,*B,"No Current Admissions Exist for this U/R.  ";
          CALL      HITENTER
          MOVE      ONE,EXIT
LOAD9999  RETURN
+
.******************************************************************************
.*                                  GDOC0000              Called by: LOAD0000 *
.*                      Get The Attending Doctors Details                     *
.******************************************************************************
GDOC0000  MOVE      ADOCTA,ATTDOC
          MOVE      "UNKNOWN DOCTOR        ",ATTDOCD
          PACK      KEY6,ADOCTA
          CALL      RDDOCT1                 * Read the doctors file
          IF        OVRCD<>1
            MOVE      DGNAME,PACGNAME
            MOVE      DSNAME,PACSNAME
            MOVE      DTITL,PACTITLE
            MOVE      "1",PACFRMT
            CALL      PACNAME               * Format doctors name
            MOVE      PACFNAME,ATTDOCD
          ENDIF
GDOC9999  RETURN
+
.******************************************************************************
.*                                  GDSC0000              Called by: LOAD0000 *
.*                          Get The Discharge Details                         *
.******************************************************************************
GDSC0000  MOVE      SP8,DSCDATE
          BRANCH    ASTAT,GDSC9999,GDSC2000,GDSC3000,GDSC4000,GDSC9999
.                         Pre admn Current  Disch    On leave Cancelled
.
. ----- Current admission -----
.
GDSC2000  MOVE      "CURRENT ",DSCDATE
          GOTO      GDSC9999
.
. ----- Patient discharged -----
.
GDSC3000  PACK      KEY8,AADMNO
          CALL      RDDSCH1                 * Read the discharge file
          IF        OVRCD=1
            MOVE      "ERROR   ",DSCDATE
          ELSE
            UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE               * Format the discharge date
            MOVE      CPCDATE,DSCDATE
            REP       " 0",DSCDATE
          ENDIF
          GOTO      GDSC9999
.
. ----- Patient on leave -----
.
GDSC4000  MOVE      "ON LEAVE",DSCDATE
GDSC9999  RETURN
+
.******************************************************************************
.*                                 DSAD0000               Called by: SLAD0000 *
.*                     Display Admissions From Tempfile                       *
.******************************************************************************
DSAD0000  MOVE      ZERO,EXIT
          DISPLAY   *P1:8,*EF,*V2LON,*ULON:
                    *P1:8,"Itm":
                    *P5:8,"Adm. No.":
                    *P15:8,"Admitted  ":
                    *P26:8,"Discharged":
                    *P38:8,"Admission Type      ":
                    *P59:8,"Attending Doctor      ";
          MOVE      "1",LINCOUNT
          GOTO      DSAD2000
.
DSAD1000  CALL      RPTEMP1                 * Read next record on 1st tempfile
          BRANCH    OVRCD,DSAD9999
.
DSAD2000  MOVE      LINCOUNT,CVERT
          ADD       "8",CVERT               * Set the correct screen position
          DISPLAY   *P1:CVERT,*EL,*V2LON:
                    *P1:CVERT,LINCOUNT,*HOFF,".":
                    *P5:CVERT,ADMISS:
                    *P15:CVERT,ADMDATE:
                    *P26:CVERT,DSCDATE:
                    *P38:CVERT,ADMTYPED:
                    *P59:CVERT,ATTDOCD;
          ADD       ONE,LINCOUNT
          COMPARE   "15",LINCOUNT
          GOTO      DSAD1000 IF LESS        * Maximum admissions on screen,
.                                             exit to ask question
DSAD9999  RETURN
+
.******************************************************************************
.*                                  QUES0000              Called by: SLAD0000 *
.*                         Admission Tempfile Question                        *
.******************************************************************************
QUES0000  MOVE      ZERO,CEXIT
          MOVE      ZERO,EXIT
          MOVE      ZERO,NEXT
          MOVE      ZERO,PREVIOUS
          MOVE      ONE,CCOL
          DISPLAY   *P1:24,*EL,*HOFF,"Select Item, (";
          ADD       "14",CCOL
          CALL      RPTEMP1                 * Read next tempfile record to see
.                                             one exists
          IF        OVRCD<>1
            DISPLAY   *EL,*V2LON,"N",*HOFF,")ext, (";
            CALL      RKTEMP1               * Read back one to restore the
.                                             original record
            MOVE      ONE,NEXT
            ADD       "8",CCOL
          ENDIF
          IF        SCRCOUNT>=2
            DISPLAY   *EL,*V2LON,"P",*HOFF,")revious, (";
            MOVE      ONE,PREVIOUS
            ADD       "12",CCOL
          ENDIF
          DISPLAY   *EL,*V2LON,"E",*HOFF,")xit ? ";
          ADD       "8",CCOL
QUES1000  MOVE      SP2,DIM2
          KEYIN     *PCCOL:24,*EL,*V2LON,*JR,DIM2;
.
          TYPE      DIM2
          GOTO      QUES2000 IF EQUAL       * Numeric found
.
          REP       "N1n1P2p2E3e3",DIM2
          MOVE      DIM2,FORM2
          BRANCH    FORM2,QUES3000,QUES4000,QUES9500
.                         Next     Previous Exit
          BEEP
          GOTO      QUES1000
.
. ----- Validate Numeric Selection -----
.
QUES2000  MOVE      DIM2,ITEMNUM
          IF        ITEMNUM<=0 | ITEMNUM>=LINCOUNT
            BEEP
            GOTO      QUES1000
          ENDIF
          MOVE      "0",CEXIT
          GOTO      QUES9000
.
. ----- Next Screen -----
.
QUES3000  IF        NEXT<>1
            BEEP
            GOTO      QUES1000
          ENDIF
          MOVE      "1",CEXIT
          GOTO      QUES9000
.
. ----- Previous Screen -----
.
QUES4000  IF        PREVIOUS<>1
            BEEP
            GOTO      QUES1000
          ENDIF
          MOVE      "2",CEXIT
.
QUES9000  MOVE      ZERO,EXIT
          GOTO      QUES9999
.
QUES9500  MOVE      ONE,EXIT
QUES9999  RETURN
+
.******************************************************************************
.*                                  GADM0000              Called by: SLAD0000 *
.*                         Get Admission Number Chosen                        *
.******************************************************************************
GADM0000  MOVE      ONE,FORM2
          MOVE      SCRARRAY[SCRCOUNT],KEY8
          CALL      RDTEMP1                 * Read the 1st tempfile
          BRANCH    OVRCD,GADM9500
.
          IF        FORM2>=LINCOUNT | FORM2>ITEMNUM
            GOTO      GADM9500              * Selection out of range
          ENDIF
.
          COMPARE     FORM2,ITEMNUM
          GOTO        GADM1000 IF EQUAL     * Correct record found
.
          REPEAT
            ADD       ONE,FORM2
            CALL      RPTEMP1               * Read backwards until the selected
            BRANCH    OVRCD,GADM9500          record is reached
.
            IF        FORM2>=LINCOUNT | FORM2>ITEMNUM
              GOTO      GADM9500            * Selection out of range
            ENDIF
          UNTIL     FORM2=ITEMNUM
.
GADM1000  CALL      VDAD0000                * Validate the admission number
          BRANCH    EXIT,GADM9500
.
          MOVE      ZERO,EXIT
          GOTO      GADM9999
.
GADM9500  MOVE      ONE,EXIT
GADM9999  RETURN
+
.******************************************************************************
.*                                  DSMH0000              Called by: SLAD0000 *
.*                      Display The Mental Helath Details                     *
.******************************************************************************
DSMH0000  PACK      AGEDATE,ADATE
          CALL      CALCAGE                 * Calculate age as at admin date
.
. ----- Load mental health details -----
.
          UNPACK    SP30,TYPOUSAC,EMPSTATS,PSNSTATS,PRMSTATS,REFTFRCR,MHLGSTIN
          MOVE      AADMNO,PTQMADMN
          PACK      KEY8,PTQMADMN
          CALL      RDPTQMH1                * Read the QLD mental health file
          IF        OVRCD=1
            MOVE      "0",CHANGE            * Insert mode
          ELSE
            MOVE      "1",CHANGE            * Change mode
            MOVE      PTQMTOUA,TYPOUSAC     * Type of usual accomodation
            MOVE      PTQMEMPS,EMPSTATS     * Employment status
            MOVE      PTQMPSNS,PSNSTATS     * Pension status
            MOVE      PTQMPRMS,PRMSTATS     * Problem status
            MOVE      PTQMRTFC,REFTFRCR     * Referral to further care
            MOVE      PTQMMLSI,MHLGSTIN     * MH legal status indicator
            MOVE      PTQMPSNT,PRSPNATM     * Prev special nonadmitted treatment
          ENDIF
.
. ----- Display mental health details -----
.
          DISPLAY   *P1:8,*V2LON," 1.",*HOFF:
                    " Type of usual accomodation              : ":
                    *V2LON,TYPOUSAC:
                    *P1:9,*V2LON," 2.",*HOFF:
                    " Employment status                       : ":
                    *V2LON,EMPSTATS:
                    *P1:10,*V2LON," 3.",*HOFF:
                    " Pension status                          : ":
                    *V2LON,PSNSTATS:
                    *P1:11,*V2LON," 4.",*HOFF:
                    " Problem status                          : ":
                    *V2LON,PRMSTATS:
                    *P1:12,*V2LON," 5.",*HOFF:
                    " Referral to further care                : ":
                    *V2LON,REFTFRCR:
                    *P1:13,*V2LON," 6.",*HOFF:
                    " Legal status indicator                  : ":
                    *V2LON,MHLGSTIN:
                    *P1:14,*V2LON," 7.",*HOFF:
                    " Prev specialised non-admitted treatment : ":
                    *V2LON,PRSPNATM;
DSMH9999  RETURN
+
.******************************************************************************
.*                                  KYMH0000              Called by: SLAD0000 *
.*                       Keyin The Mental Health Details                      *
.******************************************************************************
KYMH0000  MOVE      "1",LINCOUNT
          BRANCH    CHANGE,KYMH1000
.
          REPEAT
            PERFORM   LINCOUNT,KYTA0000,KYES0000,KYPS0000,KYRS0000,KYRC0000:
                               KYLS0000,KYPT0000
            BRANCH    EXIT,KYMH9500
.
            ADD       "1",LINCOUNT
          UNTIL     LINCOUNT>7
.
. ----- Insert mode -----
.
KYMH1000  MOVE      "1",CHANGE              * Change mode
          CALL      MAINQST                 * Select Item, (P)ost, (C)ancel ?
.
          COMPARE   "-1",CCITEM
          GOTO      KYMH9500 IF EQUAL       * Cancel
.
          COMPARE   "0",CCITEM
          GOTO      KYMH9000 IF EQUAL       * Post
.
          IF        CCITEM<1 | CCITEM>7
            BEEP
            GOTO      KYMH1000              * Number out of range
          ENDIF
.
          MOVE      CCITEM,LINCOUNT
          PERFORM   LINCOUNT,KYTA0000,KYES0000,KYPS0000,KYRS0000,KYRC0000:
                             KYLS0000,KYPT0000
          BRANCH    EXIT,KYMH9500
          GOTO      KYMH1000
.
KYMH9000  MOVE      ZERO,EXIT
          GOTO      KYMH9999
.
KYMH9500  MOVE      ONE,EXIT
KYMH9999  RETURN
+
.******************************************************************************
.*                                  KYTA0000              Called by: KYMH0000 *
.*                    Keyin The Type Of Usual Accomodation                    *
.******************************************************************************
KYTA0000  MOVE      "47",CCOL
          MOVE      "8",CVERT
          MOVE      "0",QUESTION            * Not in the ? mark display
KYTA1000  MOVE      TYPOUSAC,DIM1
          KEYIN     *PCCOL:CVERT,*EL,*V2LON,*RV,*JR,DIM1;
.
          ENDSET    DIM1
          APPEND    SP1,DIM1
          RESET     DIM1
.
          SCAN      EXITCHAR,DIM1
          GOTO      KYTA9500 IF EQUAL       * Exitchar
.
          IF        QUESTION=1
            SCAN      QUEST,DIM1
            GOTO      KYTA4000 IF EQUAL     * ? entered
          ELSE
            SCAN      QUEST,DIM1
            GOTO      KYTA3000 IF EQUAL     * ? entered
          ENDIF
.
          TYPE      DIM1
          GOTO      KYTA2000 IF EQUAL       * Numeric found
.
          BEEP
          GOTO      KYTA1000
.
KYTA2000  REP       " 0",DIM1
          MOVE      DIM1,FORM1
          IF        (FORM1<1 | FORM1>8) | FORM1=6
            BEEP
            GOTO      KYTA1000
          ENDIF
.
          PERFORM   QUESTION,PUTSCR00       * Replace the screen
          MOVE      "47",CCOL
          MOVE      "8",CVERT
          MOVE      DIM1,TYPOUSAC
          DISPLAY   *PCCOL:CVERT,*EL,*V2LON,TYPOUSAC;
          MOVE      ZERO,EXIT
          GOTO      KYTA9999
.
. ----- ? entered -----
.
KYTA3000  MOVE      "1",QUESTION            * In the ? mark display
          MOVE      "0",HLEF
          CALL      GETSCR00                * Save the current screen
KYTA4000  DISPLAY   *P1:3,*EF:
                    *P28:4,*V2LON,*ULON,"Type of Usual Accomodation",*HOFF:
                    *P1:6,*V2LON," 1",*HOFF,"  House or flat":
                    *P1:7,*V2LON," 2",*HOFF,"  Independent unit as part of ":
                    "retirement village or similar":
                    *P1:8,*V2LON," 3",*HOFF,"  Hostel or hostel accomodation":
                    *P1:9,*V2LON," 4",*HOFF,"  Psychiatric hospital":
                    *P1:10,*V2LON," 5",*HOFF,"  Acute hospital":
                    *P1:11,*V2LON," 7",*HOFF,"  Other accomodation":
                    *P1:12,*V2LON," 8",*HOFF,"  No usual residence":
                    *P1:24,*HOFF,"Type of usual accomodation :";
          MOVE      "30",CCOL
          MOVE      "24",CVERT
          GOTO      KYTA1000
.
KYTA9500  MOVE      ONE,EXIT
KYTA9999  RETURN
+
.******************************************************************************
.*                                  KYES0000              Called by: KYMH0000 *
.*                         Keyin The Employment Status                        *
.******************************************************************************
KYES0000  MOVE      "47",CCOL
          MOVE      "9",CVERT
          MOVE      "0",QUESTION            * Not in the ? mark display
KYES1000  MOVE      EMPSTATS,DIM1
          KEYIN     *PCCOL:CVERT,*EL,*V2LON,*RV,*JR,DIM1;
.
          ENDSET    DIM1
          APPEND    SP1,DIM1
          RESET     DIM1
.
          SCAN      EXITCHAR,DIM1
          GOTO      KYES9500 IF EQUAL       * Exitchar
.
          IF        QUESTION=1
            SCAN      QUEST,DIM1
            GOTO      KYES4000 IF EQUAL     * ? entered
          ELSE
            SCAN      QUEST,DIM1
            GOTO      KYES3000 IF EQUAL     * ? entered
          ENDIF
.
          TYPE      DIM1
          GOTO      KYES2000 IF EQUAL       * Numeric found
.
          BEEP
          GOTO      KYES1000
.
KYES2000  REP       " 0",DIM1
          MOVE      DIM1,FORM1
.....     IF        (FORM1<1 | FORM1>8) | (FORM1=6 | FORM1=7)           D-61341
          IF        (FORM1<1 | FORM1>8) |  FORM1=7
            BEEP
            GOTO      KYES1000
          ENDIF
.
          IF        FORM1=1 & PSAGEYRS>=18
            DISPLAY   *P1:24,*EL,*B,"Child not at school must be < 18 years ":
                      "old.  ";
            CALL      HITENTER
            BRANCH    QUESTION,KYES5000
            GOTO      KYES1000
          ENDIF
.
          IF        FORM1=3 & PSAGEYRS<=14
            DISPLAY   *P1:24,*EL,*B,"An employed patient must be > 14 ":
                      "years old.  ";
            CALL      HITENTER
            BRANCH    QUESTION,KYES5000
            GOTO      KYES1000
          ENDIF
.
          IF        FORM1=4 & PSAGEYRS<=14
            DISPLAY   *P1:24,*EL,*B,"An unemployed patient must be > 14 ":
                      "years old.  ";
            CALL      HITENTER
            BRANCH    QUESTION,KYES5000
            GOTO      KYES1000
          ENDIF
.
.******************************************** start of addition        *I-61341
          IF        FORM1=6 & PSAGEYRS<=14
            DISPLAY   *P1:24,*EL,*B,"A pensioner must be > 14 ":
                      "years old.  ";
            CALL      HITENTER
            BRANCH    QUESTION,KYES5000
            GOTO      KYES1000
          ENDIF
.********************************************   end of addition        *I-61341
.
          PERFORM   QUESTION,PUTSCR00       * Replace the screen
          MOVE      "47",CCOL
          MOVE      "9",CVERT
          MOVE      DIM1,EMPSTATS
          DISPLAY   *PCCOL:CVERT,*EL,*V2LON,EMPSTATS;
          MOVE      ZERO,EXIT
          GOTO      KYES9999
.
. ----- ? entered -----
.
KYES3000  MOVE      "1",QUESTION            * In the ? mark display
          MOVE      "0",HLEF
          CALL      GETSCR00                * Save the current screen
KYES4000  DISPLAY   *P1:3,*EF:
                    *P32:4,*V2LON,*ULON,"Employment Status",*HOFF:
                    *P1:6,*V2LON," 1",*HOFF,"  Child not at school":
                    *P1:7,*V2LON," 2",*HOFF,"  Student":
                    *P1:8,*V2LON," 3",*HOFF,"  Employed":
                    *P1:9,*V2LON," 4",*HOFF,"  Unemployed":
                    *P1:10,*V2LON," 5",*HOFF,"  Home duties":
                    *P1:11,*V2LON," 6",*HOFF,"  Pensioner":            *I-61341
                    *P1:12,*V2LON," 8",*HOFF,"  Other";
.
KYES5000  DISPLAY   *P1:24,*HOFF,"Employment status :";
          MOVE      "21",CCOL
          MOVE      "24",CVERT
          GOTO      KYES1000
.
KYES9500  MOVE      ONE,EXIT
KYES9999  RETURN
+
.******************************************************************************
.*                                  KYPS0000              Called by: KYMH0000 *
.*                          Keyin The Pension Status                          *
.******************************************************************************
KYPS0000  MOVE      "47",CCOL
          MOVE      "10",CVERT
          MOVE      "0",QUESTION            * Not in the ? mark display
KYPS1000  MOVE      PSNSTATS,DIM1
          KEYIN     *PCCOL:CVERT,*EL,*V2LON,*RV,*JR,DIM1;
.
          ENDSET    DIM1
          APPEND    SP1,DIM1
          RESET     DIM1
.
          SCAN      EXITCHAR,DIM1
          GOTO      KYPS9500 IF EQUAL       * Exitchar
.
          IF        QUESTION=1
            SCAN      QUEST,DIM1
            GOTO      KYPS4000 IF EQUAL     * ? entered
          ELSE
            SCAN      QUEST,DIM1
            GOTO      KYPS3000 IF EQUAL     * ? entered
          ENDIF
.
          TYPE      DIM1
          GOTO      KYPS2000 IF EQUAL       * Numeric found
.
          BEEP
          GOTO      KYPS1000
.
KYPS2000  REP       " 0",DIM1
          MOVE      DIM1,FORM1
          IF        (FORM1<1 | FORM1>8) | FORM1=6
            BEEP
            GOTO      KYPS1000
          ENDIF
.
          MATCH     ANSF,PSEX
          IF        @EQUAL
            IF        FORM1=1 & PSAGEYRS<=59
              DISPLAY   *P1:24,*EL,*B,"An aged female patient needs to be > ":
                        "59 years old.  ";
              CALL      HITENTER
              BRANCH    QUESTION,KYPS5000
              GOTO      KYPS1000
            ENDIF
          ENDIF
.
          MATCH     ANSM,PSEX
          IF        @EQUAL
            IF        FORM1=1 & PSAGEYRS<=64
              DISPLAY   *P1:24,*EL,*B,"An aged male patient needs to be > ":
                        "64 years old.  ";
              CALL      HITENTER
              BRANCH    QUESTION,KYPS5000
              GOTO      KYPS1000
            ENDIF
          ENDIF
.
          IF        FORM1=2 & (PSAGEYRS>=14 | PSAGEYRS<=65)
            DISPLAY   *P1:24,*EL,*B,"Repat patients need to < 15 years old ":
                      "or > 65 years old.  ";
            CALL      HITENTER
            BRANCH    QUESTION,KYPS5000
            GOTO      KYPS1000
          ENDIF
.
          IF        FORM1=3 & (PSAGEYRS>=14 | PSAGEYRS<=65)
            DISPLAY   *P1:24,*EL,*B,"Patients who are invalid need to < 15 ":
                      "years old or > 65 years old.  ";
            CALL      HITENTER
            BRANCH    QUESTION,KYPS5000
            GOTO      KYPS1000
          ENDIF
.
          IF        FORM1=4 & (PSAGEYRS>=14 | PSAGEYRS<=65)
            DISPLAY   *P1:24,*EL,*B,"Unemployed patients need to < 15 years ":
                      "old or > 65 years old.  ";
            CALL      HITENTER
            BRANCH    QUESTION,KYPS5000
            GOTO      KYPS1000
          ENDIF
.
          IF        FORM1=5 & (PSAGEYRS>=14 | PSAGEYRS<=65)
            DISPLAY   *P1:24,*EL,*B,"Patients on sickness benefits need to < ":
                      "15 years old or > 65 years old.  ";
            CALL      HITENTER
            BRANCH    QUESTION,KYPS5000
            GOTO      KYPS1000
          ENDIF
.
          PERFORM   QUESTION,PUTSCR00       * Replace the screen
          MOVE      "47",CCOL
          MOVE      "10",CVERT
          MOVE      DIM1,PSNSTATS
          DISPLAY   *PCCOL:CVERT,*EL,*V2LON,PSNSTATS;
          MOVE      ZERO,EXIT
          GOTO      KYPS9999
.
. ----- ? entered -----
.
KYPS3000  MOVE      "1",QUESTION            * In the ? mark display
          MOVE      "0",HLEF
          CALL      GETSCR00                * Save the current screen
KYPS4000  DISPLAY   *P1:3,*EF:
                    *P34:4,*V2LON,*ULON,"Pension Status",*HOFF:
                    *P1:6,*V2LON," 1",*HOFF,"  Age":
                    *P1:7,*V2LON," 2",*HOFF,"  Repatriation":
                    *P1:8,*V2LON," 3",*HOFF,"  Invalid":
                    *P1:9,*V2LON," 4",*HOFF,"  Unemployment benefit":
                    *P1:10,*V2LON," 5",*HOFF,"  Sickness benefit":
                    *P1:11,*V2LON," 7",*HOFF,"  Other":
                    *P1:12,*V2LON," 8",*HOFF,"  No pension / benefit";
.
KYPS5000  DISPLAY   *P1:24,*HOFF,"Pension status :";
          MOVE      "18",CCOL
          MOVE      "24",CVERT
          GOTO      KYPS1000
.
KYPS9500  MOVE      ONE,EXIT
KYPS9999  RETURN
+
.******************************************************************************
.*                                  KYRS0000              Called by: KYMH0000 *
.*                          Keyin The Problem Status                          *
.******************************************************************************
KYRS0000  MOVE      "47",CCOL
          MOVE      "11",CVERT
          MOVE      "0",QUESTION            * Not in the ? mark display
KYRS1000  MOVE      PRMSTATS,DIM1
          KEYIN     *PCCOL:CVERT,*EL,*V2LON,*RV,*JR,DIM1;
.
          ENDSET    DIM1
          APPEND    SP1,DIM1
          RESET     DIM1
.
          SCAN      EXITCHAR,DIM1
          GOTO      KYRS9500 IF EQUAL       * Exitchar
.
          IF        QUESTION=1
            SCAN      QUEST,DIM1
            GOTO      KYRS4000 IF EQUAL     * ? entered
          ELSE
            SCAN      QUEST,DIM1
            GOTO      KYRS3000 IF EQUAL     * ? entered
          ENDIF
.
          TYPE      DIM1
          GOTO      KYRS2000 IF EQUAL       * Numeric found
.
          BEEP
          GOTO      KYRS1000
.
KYRS2000  REP       " 0",DIM1
          MOVE      DIM1,FORM1
          IF        FORM1<1 | FORM1>2
            BEEP
            GOTO      KYRS1000
          ENDIF
.
          PERFORM   QUESTION,PUTSCR00       * Replace the screen
          MOVE      "47",CCOL
          MOVE      "11",CVERT
          MOVE      DIM1,PRMSTATS
          DISPLAY   *PCCOL:CVERT,*EL,*V2LON,PRMSTATS;
          MOVE      ZERO,EXIT
          GOTO      KYRS9999
.
. ----- ? entered -----
.
KYRS3000  MOVE      "1",QUESTION            * In the ? mark display
          MOVE      "0",HLEF
          CALL      GETSCR00                * Save the current screen
KYRS4000  DISPLAY   *P1:3,*EF:
                    *P34:4,*V2LON,*ULON,"Problem Status",*HOFF:
                    *P1:6,*V2LON," 1",*HOFF,"  No previous admission for ":
                    "psychiatric treatment":
                    *P1:7,*V2LON," 2",*HOFF,"  Previous admission for ":
                    "psychiatric treatment":
                    *P1:24,*HOFF,"Problem status :";
          MOVE      "18",CCOL
          MOVE      "24",CVERT
          GOTO      KYRS1000
.
KYRS9500  MOVE      ONE,EXIT
KYRS9999  RETURN
+
.******************************************************************************
.*                                  KYRC0000              Called by: KYMH0000 *
.*                     Keyin The Referral To Further Care                     *
.******************************************************************************
KYRC0000  MOVE      "47",CCOL
          MOVE      "12",CVERT
          MOVE      "0",QUESTION            * Not in the ? mark display
KYRC1000  MOVE      REFTFRCR,DIM2
          KEYIN     *PCCOL:CVERT,*EL,*V2LON,*RV,*JR,DIM2;
.
          ENDSET    DIM2
          APPEND    SP2,DIM2
          RESET     DIM2
.
          SCAN      EXITCHAR,DIM2
          GOTO      KYRC9500 IF EQUAL       * Exitchar
.
          IF        QUESTION=1
            SCAN      QUEST,DIM2
            GOTO      KYRC4000 IF EQUAL     * ? entered
          ELSE
            SCAN      QUEST,DIM2
            GOTO      KYRC3000 IF EQUAL     * ? entered
          ENDIF
.
          TYPE      DIM2
          GOTO      KYRC2000 IF EQUAL       * Numeric found
.
          BEEP
          GOTO      KYRC1000
.
KYRC2000  REP       " 0",DIM2
          MOVE      DIM2,FORM2
          IF        FORM2<1 | (FORM2>8 & FORM2<>29)
            BEEP
            GOTO      KYRC1000
          ENDIF
.
          PERFORM   QUESTION,PUTSCR00       * Replace the screen
          MOVE      "47",CCOL
          MOVE      "12",CVERT
          MOVE      DIM2,REFTFRCR
          DISPLAY   *PCCOL:CVERT,*EL,*V2LON,REFTFRCR;
          MOVE      ZERO,EXIT
          GOTO      KYRC9999
.
. ----- ? entered -----
.
KYRC3000  MOVE      "1",QUESTION            * In the ? mark display
          MOVE      "0",HLEF
          CALL      GETSCR00                * Save the current screen
KYRC4000  DISPLAY   *P1:3,*EF:
                    *P29:4,*V2LON,*ULON,"Referral To Further Care",*HOFF:
                    *P1:6,*V2LON,"01",*HOFF,"  Not referred":
                    *P1:7,*V2LON,"02",*HOFF,"  Private psychiatrist":
                    *P1:8,*V2LON,"03",*HOFF,"  Other private medical ":
                    "practitioner":
                    *P1:9,*V2LON,"04",*HOFF,"  Mental health / alcohol & ":
                    "drug facility - admitted patient":
                    *P1:10,*V2LON,"05",*HOFF,"  Mental health / alcohol & ":
                    "drug facility - non admitted patient":
                    *P1:11,*V2LON,"06",*HOFF,"  Acute hospital - admitted ":
                    "patient":
                    *P1:12,*V2LON,"07",*HOFF,"  Acute hospital - non admitted ":
                    "patient":
                    *P1:13,*V2LON,"08",*HOFF,"  Community health program":
                    *P1:14,*V2LON,"29",*HOFF,"  Other":
                    *P1:24,*HOFF,"Referral to further care :";
          MOVE      "28",CCOL
          MOVE      "24",CVERT
          GOTO      KYRC1000
.
KYRC9500  MOVE      ONE,EXIT
KYRC9999  RETURN
+
.******************************************************************************
.*                                  KYLS0000              Called by: KYMH0000 *
.*               Keyin The Mental Health Legal Status Indicator               *
.******************************************************************************
KYLS0000  MOVE      "47",CCOL
          MOVE      "13",CVERT
          MOVE      "0",QUESTION            * Not in the ? mark display
KYLS1000  MOVE      MHLGSTIN,DIM1
          KEYIN     *PCCOL:CVERT,*EL,*V2LON,*RV,*JR,DIM1;
.
          ENDSET    DIM1
          APPEND    SP1,DIM1
          RESET     DIM1
.
          SCAN      EXITCHAR,DIM1
          GOTO      KYLS9500 IF EQUAL       * Exitchar
.
          IF        QUESTION=1
            SCAN      QUEST,DIM1
            GOTO      KYLS4000 IF EQUAL     * ? entered
          ELSE
            SCAN      QUEST,DIM1
            GOTO      KYLS3000 IF EQUAL     * ? entered
          ENDIF
.
          TYPE      DIM1
          GOTO      KYLS2000 IF EQUAL       * Numeric found
.
          BEEP
          GOTO      KYLS1000
.
KYLS2000  REP       " 0",DIM1
          MOVE      DIM1,FORM1
          IF        FORM1<1 | FORM1>2
            BEEP
            GOTO      KYLS1000
          ENDIF
.
          PERFORM   QUESTION,PUTSCR00       * Replace the screen
          MOVE      "47",CCOL
          MOVE      "13",CVERT
          MOVE      DIM1,MHLGSTIN
          DISPLAY   *PCCOL:CVERT,*EL,*V2LON,MHLGSTIN;
          MOVE      ZERO,EXIT
          GOTO      KYLS9999
.
. ----- ? entered -----
.
KYLS3000  MOVE      "1",QUESTION            * In the ? mark display
          MOVE      "0",HLEF
          CALL      GETSCR00                * Save the current screen
KYLS4000  DISPLAY   *P1:3,*EF:
                    *P30:4,*V2LON,*ULON,"Legal Status Indicator",*HOFF:
                    *P1:6,*V2LON," 1",*HOFF,"  Involuntary patient for any ":
                    "part of the episode":
                    *P1:7,*V2LON," 2",*HOFF,"  Voluntary patient for all ":
                    "of the episode":
                    *P1:24,*HOFF,"Legal status indicator :";
          MOVE      "26",CCOL
          MOVE      "24",CVERT
          GOTO      KYLS1000
.
KYLS9500  MOVE      ONE,EXIT
KYLS9999  RETURN
+
.******************************************************************************
.*                                  KYPT0000              Called by: KYMH0000 *
.*             Keyin The Previous Specialised NonAdmitted Treatment           *
.******************************************************************************
KYPT0000  MOVE      "47",CCOL
          MOVE      "14",CVERT
          MOVE      "0",QUESTION            * Not in the ? mark display
KYPT1000  MOVE      PRSPNATM,DIM1
          KEYIN     *PCCOL:CVERT,*EL,*V2LON,*RV,*JR,DIM1;
.
          ENDSET    DIM1
          APPEND    SP1,DIM1
          RESET     DIM1
.
          SCAN      EXITCHAR,DIM1
          GOTO      KYPT9500 IF EQUAL       * Exitchar
.
          IF        QUESTION=1
            SCAN      QUEST,DIM1
            GOTO      KYPT4000 IF EQUAL     * ? entered
          ELSE
            SCAN      QUEST,DIM1
            GOTO      KYPT3000 IF EQUAL     * ? entered
          ENDIF
.
          TYPE      DIM1
          GOTO      KYPT2000 IF EQUAL       * Numeric found
.
          BEEP
          GOTO      KYPT1000
.
KYPT2000  REP       " 0",DIM1
          MOVE      DIM1,FORM1
          IF        FORM1<1 | FORM1>2
            BEEP
            GOTO      KYPT1000
          ENDIF
.
          PERFORM   QUESTION,PUTSCR00       * Replace the screen
          MOVE      "47",CCOL
          MOVE      "14",CVERT
          MOVE      DIM1,PRSPNATM
          DISPLAY   *PCCOL:CVERT,*EL,*V2LON,PRSPNATM;
          MOVE      ZERO,EXIT
          GOTO      KYPT9999
.
. ----- ? entered -----
.
KYPT3000  MOVE      "1",QUESTION            * In the ? mark display
          MOVE      "0",HLEF
          CALL      GETSCR00                * Save the current screen
KYPT4000  DISPLAY   *P1:3,*EF:
                    *P16:4,*V2LON,*ULON:
                    "Previous Specialised Nonadmitted Treatment",*HOFF:
                    *P1:6,*V2LON," 1",*HOFF,"  No previous nonadmitted ":
                    "service contact(s) for psychiatric treatment":
                    *P1:7,*V2LON," 2",*HOFF,"  Previous nonadmitted ":
                    "service contact(s) for psychiatric treatment":
                    *P1:24,*HOFF,"Previous treatment :";
          MOVE      "22",CCOL
          MOVE      "24",CVERT
          GOTO      KYPT1000
.
KYPT9500  MOVE      ONE,EXIT
KYPT9999  RETURN
+
.******************************************************************************
.*                                 WUMH0000               Called by: ML0000   *
.*                    Write / Update Mental Health Files                      *
.******************************************************************************
WUMH0000  MOVE      AADMNO,PTQMADMN
          PACK      KEY8,PTQMADMN
          CALL      RDPTQMH1                * Read the QLD mental health file
          BRANCH    OVRCD,WUMH2000
.
          CALL      CFDD0000                * Check for different details
          BRANCH    DIFFRENT,WUMH1000       * Details have changed
.
          GOTO      WUMH9999
.
. ----- Details have changed, update the patqmhaf file -----
.
WUMH1000  MOVE      TYPOUSAC,PTQMTOUA       * Type of usual accomodation
          MOVE      EMPSTATS,PTQMEMPS       * Employment status
          MOVE      PSNSTATS,PTQMPSNS       * Pension status
          MOVE      PRMSTATS,PTQMPRMS       * Problem status
          MOVE      REFTFRCR,PTQMRTFC       * Referral to further care
          MOVE      MHLGSTIN,PTQMMLSI       * MH legal status indicator
          MOVE      PRSPNATM,PTQMPSNT       * Previous specialised treatment
          MOVE      SP30,PTQMSPAR           * Spare variable
.
          CALL      UPPTQMH1                * Update the QLD mental health file
          DISPLAY   *P1:24,*EL,"Updating file ...",*W2;
          GOTO      WUMH9999
.
. ----- Write new record to the patqmhaf file -----
.
WUMH2000  MOVE      TYPOUSAC,PTQMTOUA       * Type of usual accomodation
          MOVE      EMPSTATS,PTQMEMPS       * Employment status
          MOVE      PSNSTATS,PTQMPSNS       * Pension status
          MOVE      PRMSTATS,PTQMPRMS       * Problem status
          MOVE      REFTFRCR,PTQMRTFC       * Referral to further care
          MOVE      MHLGSTIN,PTQMMLSI       * MH legal status indicator
          MOVE      PRSPNATM,PTQMPSNT       * Previous specialised treatment
          MOVE      SP30,PTQMSPAR           * Spare variable
.
          PACK      KEY8,PTQMADMN
          CALL      WRPTQMH1                * Write to the QLD mental hlth file
          DISPLAY   *P1:24,*EL,"Writing to file ...",*W2;
WUMH9999  RETURN
+
.******************************************************************************
.*                                 CFDD0000               Called by: WUMH0000 *
.*                        Check For Different Details                         *
.******************************************************************************
CFDD0000  MATCH     PTQMTOUA,TYPOUSAC
          GOTO      CFDD9500 IF NOT EQUAL   * Diff type of usual accomodation
.
          MATCH     PTQMEMPS,EMPSTATS
          GOTO      CFDD9500 IF NOT EQUAL   * Diff employment status
.
          MATCH     PTQMPSNS,PSNSTATS
          GOTO      CFDD9500 IF NOT EQUAL   * Diff pension status
.
          MATCH     PTQMPRMS,PRMSTATS
          GOTO      CFDD9500 IF NOT EQUAL   * Diff problem status
.
          MATCH     PTQMRTFC,REFTFRCR
          GOTO      CFDD9500 IF NOT EQUAL   * Diff referral to further care
.
          MATCH     PTQMMLSI,MHLGSTIN
          GOTO      CFDD9500 IF NOT EQUAL   * Diff MH legal status indicator
.
          MATCH     PTQMPSNT,PRSPNATM
          GOTO      CFDD9500 IF NOT EQUAL   * Diff prev specialised treatment
.
          MOVE      ZERO,DIFFRENT           * Details haven't changed
          GOTO      CFDD9999
.
CFDD9500  MOVE      ONE,DIFFRENT            * Details have changed
CFDD9999  RETURN
+
.******************************************************************************
.*                                  OPEN0000              Called by: SLAD0000 *
.*                              Open The Tempfile                             *
.******************************************************************************
OPEN0000  CALL      KILL0000                * Delete the 1st tempfile
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMEA,CMDLINE
          APPEND    FILELIN1,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMP1,FNAMEA
OPEN9999  RETURN
+
.******************************************************************************
.*                                  KILL0000              Called by: ML0000   *
.*                             Delete The Tempfile                 & OPEN0000 *
.******************************************************************************
KILL0000  CLOSE     TEMP1
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMEA,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
KILL9999  RETURN
+
.******************************************************************************
.*                       Routines To Keep PATSRCH Happy                       *
.******************************************************************************
SOUNDX2
GETSVAR   RETURN
+
.******************************************************************************
.*                        I/O Routines For The Tempfile                       *
.******************************************************************************
RSTEMP1   RESET     KEY8
          READ      TEMP1,KEY8;;
          RETURN
.
RDTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY8
          READ      TEMP1,KEY8;DADMISS,ADMDATE,DSCDATE,ADMTYPE,ADMTYPED:
                               ATTDOC,ATTDOCD
          GOTO      OVERCOND IF OVER
          MOVE      DADMISS,QADMISS
          MOVE      QADMISS,ADMISS
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMP1;DADMISS,ADMDATE,DSCDATE,ADMTYPE,ADMTYPED:
                          ATTDOC,ATTDOCD
          GOTO      OVERCOND IF OVER
          MOVE      DADMISS,QADMISS
          MOVE      QADMISS,ADMISS
          RETURN
.
RPTEMP1   MOVE      ZERO,OVRCD
          READKP    TEMP1;DADMISS,ADMDATE,DSCDATE,ADMTYPE,ADMTYPED:
                          ATTDOC,ATTDOCD
          GOTO      OVERCOND IF OVER
          MOVE      DADMISS,QADMISS
          MOVE      QADMISS,ADMISS
          RETURN
.
WRTEMP1   RESET     KEY8
          PACK      DADMISS,ADMISS
          WRITE     TEMP1,KEY8;DADMISS,ADMDATE,DSCDATE,ADMTYPE,ADMTYPED:
                               ATTDOC,ATTDOCD
          RETURN
+
.==============================================================================
.
          INC       STD001IO
          INC       CALCAGE                 * Calculate the patients age
          INC       PATALERT                * Patient alerts
          INC       PATCOMN1                * Patient standard library
          INC       PATHEAD                 * Display PMI details
          INC       PATSNDX                 * Change surname into sound ex
          INC       PMIHEAD                 * Display PMI details
          INC       NZIBSRCH
          INC       CLPATMAS
          INC       NZHISIIO
          INC       TFILENAM                     * Generate Temp File Name
.
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       NHIMASIO/INC
          INC       PMSVX1IO/INC
          INC       PATALRIO/INC            * Alert file
          INC       BOKRC1IO/INC            * Booking file
          INC       OUTPREIO/INC            * Outpatients pre attendence file
          INC       PATCODIO/INC            * Codes file
          INC       PATDO1IO/INC            * Doctors file
          INC       PATDSCIO/INC            * Discharge file
          INC       PATMA1IO/INC            * Master file
          INC       HL7BMTIO/INC
.         INC       HL7BMTMA
          INC       PATMI1IO/INC            * Admission file
          INC       PATNOBIO/INC            * No bed fee file
          INC       PATPR1IO/INC            * Pre admission file
          INC       PATQMHIO/INC            * QLD mental health file
          INC       PATGSRIO/INC            * Surname file
          INC       PATVISIO/INC            * Visit file
.         INC       HL7BMTVI
          INC       PATWR1IO/INC            * Ward file
          INC       PMSPX2IO/INC
          INC       WEBERRIO/INC                 * Web Server Error Logging
+
.



.***************************************************************************
.* System    :   Function Keys                                             *
.* Program   :   ANYOPROG                                                  *
.* Desc      :   'Any Other Program' Program                               *
.***************************************************************************
.* Date      :   13/11/92                                                  *
.* Author    :   Steve O'Gorman                                            *
.* Function  :   This program provides a means by which a user may run     *
.*           :   'Any Other Program' in IBACMENU.                          *
.* Mods      :   V6.00.01  29/04/93  DIG                                   *
.*                         Changed to bypass password entry.    SRF 122172 *
.*               C1.00.00  Peter Eddey   for rel 5.02                      *
.*                         Trap error on chain to prevent C*N              *
.*        C1.00.01  13/02/1995  Greg Horvat      SRF 134920 134922 134932  *
.*                  Recompiled for IBAPRINT                                *
.*        C1.00.02  28/03/1996   Whirlpool   (Release 5.04)                *
.*                  Added write to new system usage file ibasusaf          *
. * V10.02.01 01/04/2011  Jill Parkinson CAR 239574                    *
. *           Removed open of ibaprntf                                 *
.*                                                                         *
.***************************************************************************
.
          INC       STD001FD
.
          INC       IBASYSFD/INC
          INC       IBASECFD/INC
          INC       IBAPORFD/INC
          INC       IBACONFD/INC
          INC       IBAIMNFD/INC
          INC       IBAPRNFD/INC
          INC       IBASUSFD/INC
.
.***************************************************************************
.*                               Constants                                 *
.***************************************************************************
.
.
.***************************************************************************
.*                           Working Variiables                            *
.***************************************************************************
.
CNT       FORM      1
.
NOPASSWD  FORM      1
.
PASSCHK   DIM       8
PASSTAB   FORM      3
PASSWD    DIM       8
PROGNAME  DIM       8
.
SPNAME    DIM       8
.
PRGID     INIT      "ANYOPROG"
PRGNAM    INIT      "Any Other Program"
VERSION   INIT      "V12.01.00"
.
.***************************************************************************
.*                                ML0000                                   *
.*                      Main Line Controlling Logic                        *
.***************************************************************************
.
ML0000    CALL      INIT0000                * Initialise
          CALL      ANYP0000                * Run Any Other Program
.
ML9999    CHAIN     SPNAME
          STOP
.
.********************************************************************** 
.*                            INIT0000                                *
.*                       Initialise System                            *
.********************************************************************** 
.
INIT0000  OPEN      IBAPORT1,"ibaportf"
          OPEN      IBASECU1,"ibasecuf"
          OPEN      IBASECU2,"ibasecuf"
          OPEN      IBACODE1,"ibacodef"
          OPEN      IBAPORT1,"ibaportf"
          OPEN      IBASYSA1,"ibasysaf"
.
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*47,IBCNSUSE
          READ      CONTROLF,TWO;*149,USERFLAG,*154,SPNAME
          BRANCH    USERFLAG,INIT1000
.
          OPEN      IBASYSA1,"ibasysaf"    * system tracking file
          OPEN      IBASYSA2,"ibasysaf"    * system tracking file
          OPEN      IBASYSA3,"ibasysaf"    * system tracking file
          OPEN      IBASYSA4,"ibasysaf"    * system tracking file
.
          CALL      UPSYSA1                * end of last program
.
INIT1000  MOVE      PRGID,IBAPPROG
          CALL      WRPORT1                * in out menu
.
.     If we are logging system usage then open the file
.
          IF        IBCNSUSE = 1
            OPEN      IBASUSA1,"ibasusaf"
          ENDIF
.
          CALL      IBACLOCK
          RETURN
.
.********************************************************************** 
.*                            ANYP0000                                *
.*                       Run Any Other Program                        *
.********************************************************************** 
.
ANYP0000  MOVE      SP8,PROGNAME
.
          KEYIN     *P4:17,*EF,"Program Name ? ",*V2LON,PROGNAME
.
          ENDSET    PROGNAME
          APPEND    SP8,PROGNAME
          RESET     PROGNAME
.
          MATCH     SP8,PROGNAME
          GOTO      ANYP9999 IF EQUAL
.
          PACK      KEY16,PROGNAME,SP8
          CALL      RDSSECU2
          CALL      RDKSECU2
          BRANCH    OVRCD,ANYP1000
.
          MATCH     PROGNAME,PSEALIAS
          GOTO      ANYP1000 IF NOT EQUAL
.
          MOVE      PSECNME,PROGNAME
.
ANYP1000  MOVE      PROGNAME,IBAPPROG
          CALL      WRPORT1
.
.   If we are logging system usage then write to ibasusaf
.
         IF        IBCNSUSE = 1
           MOVE      PROGNAME,IBSUPROG
           PACK      IBSUMNTH,CCC,CYY,CMM  
           REP       " 0",IBSUMNTH
           PACK      KEY14,IBSUMNTH,PROGNAME
           CALL      RDIBSUS1
           IF        OVRCD = 1
             MOVE      ONE,IBSUNUMB
             CALL      WRIBSUS1
           ELSE
             ADD       ONE,IBSUNUMB
             CALL      UPIBSUS1
           ENDIF
         ENDIF
         
.
          MOVE      "IBACMENU",CCMENU
.
          TRAP      ANYP9000 IF CFAIL
          CHAIN     IBAPPROG
          
ANYP9000  NORETURN
          TRAPCLR   CFAIL
          BEEP
          DISPLAY   *P1:24,*EL,"Selection not Available. Please Contact I.B.A":
                               " If Required. Tap Enter _"
          KEYIN     *P70:24,*V2LON,ANS
          DISPLAY   *P1:24,*EL

ANYP9999  RETURN
.
.
.***************************************************************************
.*                              NOCLEAR ROUTINE                           *
.***************************************************************************
.
NOCLEAR DISPLAY    *ES,*P15:6,*B,*V2LON:
                   "*** Clearance Level Inadequate ***"
.
        KEYIN     *P1:24,*EL,"Hit ",*V2LON,"<RETURN>",*HOFF:
                              " to continue ",*EOFF,ANS;
        RETURN
.
.***************************************************************************
.*                              I/O includes                              *
.***************************************************************************
.
          INC       STD001IO
.
          INC       IBAWDINC
          INC       IBAPRINT
.
          INC       IBASYSIO/INC
          INC       IBAIMNIO/INC
          INC       IBAPRNIO/INC
          INC       IBASECIO/INC
          INC       IBAPORIO/INC       **** PORT SCANNING
          INC       IBASUSIO/INC       * System Usage

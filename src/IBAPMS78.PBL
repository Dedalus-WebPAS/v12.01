.------------------------------------------------------------------------------
.* System   : Patient Management
.* Program  : IBAPMS78
.* Desc.    : Auto-End Disaster Records Report 
.------------------------------------------------------------------------------
.* Date     : 18/08/2020
.* Author   : Thanh T
.* Function : This program updates the disaster patient header file and produces
.*            a report containing the list of patients disaster records which 
.*            end date is less than the nominated days from today date.  
.------------------------------------------------------------------------------
. V11.02.01  10/02/2022  Thanh T          TSK 0889899
.            Recompiled as WATTR1FD changes
. V11.01.01  22/08/2021  Jill Parkinson Task 0909342
.            Mods to update the dispataf record if no matches at all on disptlaf
. V11.00.03  23/09/2020  Thanh T        TSK 0890610
.            Added Keyin User Id
. V11.00.02  17/09/2020  Thanh T        TSK 0890610
.            Added Keyin User Id              
. V11.00.01  04/09/2020  Thanh T        TSK 0890610
.            Changed to check for blank of referal close date 
. V11.00.00  18/08/2020  Thanh T        TSK 0890610
.            Initial Version
.------------------------------------------------------------------------------
.
.         FILE DESCRIPTION INCLUDES
.
          INC       STDGENDF
          INC       ALLCONFD/INC
          INC       IBACONFD/INC
          INC       ALLREFFD/INC
          INC       EMRVISFD/INC
          INC       PATDSCFD/INC
          INC       DISMASFD/INC
          INC       DISPATFD/INC
          INC       DISPTLFD/INC
          INC       PATCODFD/INC            * Patient Codes File
          INC       PATCONFD/INC
          INC       PATHSPFD/INC            * hospital file
          INC       PATMA1FD/INC            * Patient Master File
          INC       PATMI1FD/INC            * Patient Admission File
          INC       PATWR1FD/INC            * Patient Ward/Bed File
          INC       PATVISFD/INC
          INC       PMSEVTFD/INC
          INC       PMSHCPFD/INC
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC            * Visit Extension file
          INC       OUTBOAFD/INC
          INC       WATTR1FD/INC
          INC       WEBCONFD/INC
          INC       WEBSECFD/INC
          INC       MLTCODFD/INC
.
.         LOCAL VARIABLE DEFINITION
.
.------------------------------------------------------------------------------
PRGID     INIT      "IBAPMS78"
PRGNAM    INIT      "Auto-End Disaster Records Report"
VERSION   INIT      "V12.01.00"
.------------------------------------------------------------------------------
.
DOWNCASE  INIT      "AaBbCcDdEeFfGgHhIiJjKkLlMmNnOoPpQqRrSsTtUuVvWwXxYyZz"
.
JAN       INIT      "January"
FEB       INIT      "February"
MAR       INIT      "March"
APR       INIT      "April"
MAY       INIT      "May"
JUN       INIT      "June"
JUL       INIT      "July"
AUG       INIT      "August"
SEP       INIT      "September"
OCT       INIT      "October"
NOV       INIT      "November"
DEC       INIT      "December"
.
QUOTE     INIT      "#""
.
CATACTIV  FORM      "0"
CURRDATE  DIM       8                         * Current Date
CMDLINE   DIM       60                        * Command Line
WORKDATE  DATE      8
WORKTIME  DIM       8
ENDDATE   DATE      8
ENDDATEX  DIM       8 
ENDDATED  DIM       11 
CFILEPRE  DIM       3
CATEGORY  DIM       2
CODE      DIM       3
DIM127    DIM       127
DIM40     DIM       40
DISPNAME  DIM       60
DISACODE  DIM       9                         * Disaster Work Variable
DISODESC  DIM       24
NAME35    DIM       35
FILTDSOU  DIM       3
FILTDAYS  DIM       3
MINDAYS   FORM      3
HTMLFILE  FIFO      ASCII, FIXED=250
MTHNAM3   DIM       3
PATNAME   DIM       40                        * Patient Name
RECFOUND  DIM       1 
NOFMATCH  FORM      3
NOFUPDT   FORM      5
FORM3     FORM      3
USERID    DIM       10
.
.------------------------------------------------------------------------------
. Controlling Logic (Mainline Code)                         
.------------------------------------------------------------------------------
ML0000    CALL      INIT0000                 * Initialization and open files
.
ML0100    CALL      OPTN0000                 * Select options
          BRANCH    EXIT,ML9999              * Exit = 1 if 0 chosen in menu
          BRANCH    OPTION,ML1000            
.
ML1000    CALL      KDIS0000                 * Keyin disaster id
          CALL      KDAY0000                 * Keyin days
          CALL      KDISOU00                 * Keyin disaster outcome
          CALL      USID0000                 * keyin the User Id
.
          CALL      CONTQST                  * Ok to Continue ?
          BRANCH    CEXIT,ML1100,ML1000,ML0100  *Yes, No, Cancel
ML1100    CALL      PROC0000                 * Yes. Process each record
          GOTO      ML0100                   * Return for next option
.
ML9999    CHAIN     PGM
+
.------------------------------------------------------------------------------
. Initialize variables, display heading, and open files  
.------------------------------------------------------------------------------
INIT0000  CALL      DISPHEAD                 * Display Heading
.
          DISPLAY   *P56:24,"Opening ";
.
          DISPLAY   *P64:24,"controlf"       * Open CONTROLF
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          DISPLAY   *P64:24,"patcodes"       * Open PATCODE1
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patmi1af"       
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A2,"patmi1af"
.
          DISPLAY   *P64:24,"patma1af"       
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"pmspx2af"
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"patwr1af"       
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATWR1A4,"patwr1af"
.
          DISPLAY   *P64:24,"patvisaf"
          OPEN      PATVISA1,"patvisaf"
.
          DISPLAY   *P64:24,"pmshcpaf";
          OPEN      PMSHCPA1,"pmshcpaf"
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"
. 
          DISPLAY   *P64:24,"dismasaf";
          OPEN      DISMASA1,"dismasaf"
.
          DISPLAY   *P64:24,"dispataf";
          OPEN      DISPATA1,"dispataf"
          OPEN      DISPATA2,"dispataf"
.
          DISPLAY   *P64:24,"disptlaf";
          OPEN      DISPTLA1,"disptlaf"
          OPEN      DISPTLA2,"disptlaf"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"emrvisaf";
          OPEN      EMRVISA1,"emrvisaf"
.
          DISPLAY   *P64:24,"allrefaf";
          OPEN      ALLREFA1,"allrefaf"
.
          DISPLAY   *P64:24,"wattr1af";
          OPEN      WATTR1A1,"wattr1af"
          OPEN      WATTR1A2,"wattr1af"
.
          MOVE      "out",CFILEPRE           * Save Current File Prefix
          CALL      OPNOUT00                 * Open Outpatient Files
.
          MOVE      "do",KEY2
          PACK      KEY5,KEY2,SP70
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE      SP70,TDESC
          ENDIF
          MOVE      TDESC,DISODESC
.         
INIT9999  RETURN
+
.------------------------------------------------------------------------------
. Open Outpatient Files
.------------------------------------------------------------------------------
OPNOUT00  PACK      CFNAME,CFILEPRE,FILBOKA3  * Get the name of the BOKA3 file
          CLOSE     OUTBOKA3
          OPEN      OUTBOKA3,CFNAME           * Open the file
          CLOSE     OUTBOKA2
          OPEN      OUTBOKA2,CFNAME           * Open the file
          CLOSE     OUTBOKA6
          OPEN      OUTBOKA6,CFNAME           * Open the file
.
OPNOUT99  RETURN
+
.------------------------------------------------------------------------------
. Obtain the options            
.------------------------------------------------------------------------------
OPTN0000  DISPLAY   *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Auto-End Disaster Records Report" 
          KEYIN     *P1:7,"Please Select : ",*V2LON,OPTION
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9000 IF EQUAL        * Exit
.
          BRANCH    OPTION,OPTN8000
.
          BEEP                               * Error : Invalid Option
          GOTO      OPTN0000
.
OPTN8000  MOVE      FALSE,EXIT               * Set Exit = 0
          GOTO      OPTN9999
.
OPTN9000  MOVE      TRUE,EXIT                * Set Exit = 1
.
OPTN9999  RETURN
+
.------------------------------------------------------------------------------
. Keyin the disaster
.------------------------------------------------------------------------------
KDIS0000  MOVE      SP70,DISACODE
          DISPLAY   *P1:10,*V2LON,*HOFF,"Disaster: "
          KEYIN     *P13:10,*V2LON,DISACODE
.
KDIS9999  RETURN
+
.------------------------------------------------------------------------------
. Keyin end date
.------------------------------------------------------------------------------
KDAY0000  MOVE      TWENTY8,FILTDAYS
          KEYIN     *P1:TEN2,*EF,"Days Post-Discharge/Event End: ":
                    *V2LON,*RV,FILTDAYS,*HOFF
.
          TYPE      FILTDAYS
          GOTO      KDAY1000 IF EQUAL
.
          PACK      FILTDAYS,FILTDAYS,SP3
.
          MATCH     SP3,FILTDAYS
          GOTO      KDAY0000 IF EQUAL
.
          BEEP
          GOTO      KDAY0000
.
KDAY1000  MOVE      FILTDAYS,MINDAYS
.
          COMPARE   ZERO,MINDAYS
          GOTO      KDAY0000 IF LESS
.
KDAY9999  RETURN
+
.------------------------------------------------------------------------------
. Keyin the disaster outcome
.------------------------------------------------------------------------------
KDISOU00  MOVE      SP70,FILTDSOU
          DISPLAY   *P1:TEN4,*V2LON,*HOFF,"Disaster Outcome: "
          MOVE      TEN4,EVERT
          MOVE      TEN9,ECOL
          MOVE      "do",CODE
          MOVE      SP3,CKYIDEF3
          MOVE      ZERO,CKYIMAND
          CALL      PATCODKY
.
          IF        EXIT = ONE
            GOTO      KDISOU00
          ELSE
            IF        EXIT = 2
              MOVE      SP3,FILTDSOU
            ELSE
              MOVE      ACODE,FILTDSOU
              DISPLAY   *P23:TEN4,*EL,*V2LON,TDESC
            ENDIF
          ENDIF
.
KDISOU99  RETURN
.
.****************************************************************************
.         Keyin user id
.****************************************************************************
USID0000  KEYIN     *P1:16,*EL,"User Id         : ",USERID;
          PACK      USERID,USERID,SP70
          MATCH     SP70,USERID
          GOTO      USID0000 IF EQUAL
USID9999  RETURN
+
.------------------------------------------------------------------------------
. Process update/report
.------------------------------------------------------------------------------
PROC0000  DISPLAY   *P1:24,"Printing U/R No. : ";
          MOVE      0,NOFMATCH
          MOVE      0,NOFUPDT
.
          CALL      IBACLOCK
          PACK      WORKDATE,CCC,CYY,CMM,CDD     * Set up current date
          REP       " 0",WORKDATE
          MOVE      CTIMEIS,WORKTIME      
.
          MOVE      ZERO,FORM3
          MOVE      FILTDAYS,FORM3
          MOVE      WORKDATE,ENDDATE
          SUB       FORM3,ENDDATE 
          MOVE      ENDDATE,ENDDATEX
.
          CALL      IBACLOCK
          CALL      PHED0000
.
          PACK      KEY17,DISACODE,SP70
          CALL      RSDSPAT2                   * Positional read
PROC0010  CALL      RKDSPAT2                   * Read next record
          BRANCH    OVRCD,PROC9000             * End of file
.
          DISPLAY   *P20:24,*EL,DSPTURNO,P1,DSPTCODE
.
          MATCH     DISACODE,DSPTCODE
          GOTO      PROC9000 IF NOT EQUAL
.
          MATCH     SP70,DSPTEDAT
          GOTO      PROC0010 IF NOT EQUAL
.
          PACK      KEY8,DSPTURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,PROC0010
.
          PACK      KEY8,DSPTURNO,SP70
          CALL      RDPMPX21
          BRANCH    OVRCD,PROC0010
.
          MOVE      0,NOFMATCH
.
PROC0020  PACK      KEY25,DSPTURNO,DSPTCODE,SP70
          CALL      RSDSPTL1
PROC0025  CALL      RKDSPTL1
          BRANCH    OVRCD,PROC0060
.
          MATCH     DSPTURNO,DSPLURNO
          GOTO      PROC0060 IF NOT EQUAL
.
          MATCH     DSPTCODE,DSPLCODE
          GOTO      PROC0060 IF NOT EQUAL
.
          MOVE      SP70,RECFOUND
.
          PACK      KEY8,DSPLVISN,SP70         * Check visit
          CALL      RDPTVIS1
          IF        OVRCD = 0
            GOTO      PROC0040
          ENDIF
.
. No patvisaf. Check outpatient booking
.
          PACK      KEY36,DSPLVISN,Z70
          CALL      RDSBOKA6
          CALL      RDPBOKA6
          BRANCH    OVRCD,PROC0030
.
          MATCH     DOBAOUTN,DSPLVISN
          GOTO      PROC0030 IF NOT EQUAL
.
          MATCH     OBADATE,ENDDATEX
          IF        @LESS
            MOVE      ANSN,RECFOUND
          ELSE
            MOVE      ANSY,RECFOUND
          ENDIF
.
          GOTO      PROC0050
.
. No patvisaf and no outpatient booking. 
. Check waiting list with dwmstat=6 (removed)
.
PROC0030  MOVE      "6",KEY1
          PACK      KEY20,KEY1,DSPLURNO,Z70
          CALL      RDSTREA2
PROC0032  CALL      RDPTREA2
          BRANCH    OVRCD,PROC0010
.
          MATCH     KEY1,DWMSTAT
          GOTO      PROC0010 IF NOT EQUAL
.
          MATCH     DSPLURNO,WMURNO
          GOTO      PROC0010 IF NOT EQUAL
.
          MOVE      WMBOOK,DIM8
          MATCH     DSPLVISN,DIM8
          GOTO      PROC0032 IF NOT EQUAL
.
          MATCH     WMDATE4,ENDDATEX
          IF        @LESS
            MOVE      ANSN,RECFOUND
          ELSE
            MOVE      ANSY,RECFOUND
          ENDIF
.
          GOTO      PROC0050
.
PROC0040  PACK      KEY8,DSPLVISN,SP70
          CALL      RDPMVX11
          IF        OVRCD = 1
            GOTO      PROC0010
          ENDIF
.  
          IF        PVITYPE = THREE
            CALL      INPAT000
            GOTO      PROC0050   
          ENDIF
.
          IF        PVITYPE = TWO
            CALL      OUPAT000
            GOTO      PROC0050   
          ENDIF
.
          IF        PVITYPE = ONE
            CALL      EMERG000
            GOTO      PROC0050   
          ENDIF
.
          IF        PVITYPE = NINE
            CALL      REFER000
            GOTO      PROC0050   
          ENDIF
.
PROC0050  MATCH     ANSN,RECFOUND
          IF        @EQUAL
            GOTO      PROC0010
          ELSE 
            ADD       ONE,NOFMATCH
            GOTO      PROC0025
          ENDIF   
.
PROC0060  
.....IF        NOFMATCH > 0
            CALL      UPTDIS00    
            CALL      PRTDIS00 
.....     ENDIF
          GOTO      PROC0010
.
PROC9000  CALL      PEND0000                     * Print End of report
.
PROC9999  RETURN
.
.------------------------------------------------------------------------------
. Update disaster header details 
.------------------------------------------------------------------------------
UPTDIS00  PACK      KEY17,DSPTURNO,DSPTCODE,SP70
          CALL      RDDSPAT1
          BRANCH    OVRCD,UPTDIS99
.
          MOVE      WORKDATE,DSPTEDAT
          MOVE      WORKTIME,DSPTETIM               
          MOVE      FILTDSOU,DSPTOTCM
          MOVE      WORKDATE,DSPTUDAT
          MOVE      WORKTIME,DSPTUTIM     
          MOVE      "AUTOEND",DSPTUUSR  
.
          CALL      UPDSPAT1 
          ADD       ONE,NOFUPDT
.
UPTDIS99  RETURN
.
.------------------------------------------------------------------------------
. Check Inpatient type
.------------------------------------------------------------------------------
INPAT000  PACK      KEY8,DSPLVISN,SP70
          CALL      RDMISS1
          BRANCH    OVRCD,INPAT999
.
          MATCH     "5",DASTAT
          GOTO      INPAT999 IF EQUAL
.
          MATCH     "1",DASTAT
          IF        @EQUAL
            MOVE      ANSN,RECFOUND
            GOTO      INPAT999
          ENDIF
.
          MATCH     "2",DASTAT
          IF        @EQUAL
            MOVE      ANSN,RECFOUND
            GOTO      INPAT999
          ENDIF
.
          MATCH     "4",DASTAT
          IF        @EQUAL
            MOVE      ANSN,RECFOUND
            GOTO      INPAT999
          ENDIF
.
          MATCH     "3",DASTAT
          IF        @EQUAL
            CALL      CDISCH00
            GOTO      INPAT999
          ENDIF
.
INPAT999  RETURN
.
.------------------------------------------------------------------------------
. Check discharge date
.------------------------------------------------------------------------------
CDISCH00  PACK      KEY8,DSPLVISN,SP70
          CALL      RDDSCH1
          BRANCH    OVRCD,CDISCH00
.
          MATCH     DDATE,ENDDATEX
          IF        @LESS  
            MOVE      ANSN,RECFOUND
          ELSE
            MOVE      ANSY,RECFOUND
          ENDIF
.
CDISCH99  RETURN
.
.------------------------------------------------------------------------------
. Check outpatient type
.------------------------------------------------------------------------------
OUPAT000  PACK      KEY36,DSPLVISN,Z70   
          CALL      RDSBOKA6
OUPAT200  CALL      RDPBOKA6
          BRANCH    OVRCD,OUPAT999
. 
          MATCH     DOBAOUTN,DSPLVISN
          GOTO      OUPAT999 IF NOT EQUAL
.
          MATCH     OBADATE,ENDDATEX
          IF        @LESS
            MOVE      ANSN,RECFOUND
          ELSE
            MOVE      ANSY,RECFOUND
          ENDIF
.
OUPAT999  RETURN
.
.------------------------------------------------------------------------------
. Check emergency type
.------------------------------------------------------------------------------
EMERG000  PACK      KEY8,DSPLVISN,SP70
          CALL      RDEMVIS1
          BRANCH    OVRCD,EMERG999
.
          MATCH     "4",DEMVISTA
          IF        @EQUAL
            GOTO      EMERG999
          ENDIF
.
          MATCH     "1",DEMVISTA
          IF        @EQUAL
            MOVE      ANSN,RECFOUND
            GOTO      EMERG999
          ENDIF
.
          MATCH     "2",DEMVISTA
          IF        @EQUAL
            GOTO    EMERG500
          ENDIF
.
          MATCH     "3",DEMVISTA
          IF        @EQUAL
            GOTO    EMERG500
          ENDIF
.
          GOTO      EMERG999
.
EMERG500  MATCH     "00000000",EMVIDDAT
          IF        @EQUAL
            MOVE      ANSN,RECFOUND
            GOTO      EMERG999
          ENDIF
.
          MATCH     EMVIDDAT,ENDDATEX
          IF        @LESS
            MOVE      ANSN,RECFOUND
          ELSE
            MOVE      ANSY,RECFOUND
          ENDIF
.
EMERG999  RETURN
.
.------------------------------------------------------------------------------
. Check referral type
.------------------------------------------------------------------------------
REFER000  PACK      KEY8,DSPLVISN,SP70
          CALL      RDALREF1
          BRANCH    OVRCD,REFER999 
.
          MATCH     "0",ALRESTAT
          IF        @EQUAL
            MOVE      ANSN,RECFOUND
            GOTO     REFER999
          ENDIF
.
          MATCH     "1",ALRESTAT
          IF        @EQUAL
            MOVE      ANSN,RECFOUND
            GOTO      REFER999
          ENDIF
.
          MATCH     "2",ALRESTAT
          IF        @EQUAL
            MATCH     SP70,ALREDCLO
            IF        @EQUAL
              MOVE      ANSN,RECFOUND
            ELSE
              MATCH     ALREDCLO,ENDDATEX
              IF        @LESS
                MOVE      ANSN,RECFOUND
              ELSE
                MOVE      ANSY,RECFOUND
              ENDIF
            ENDIF
            GOTO      REFER999
          ENDIF
.
          MATCH     "4",ALRESTAT
          IF        @EQUAL
            MATCH     SP70,ALREDCAN
            IF        @EQUAL
              MOVE      ANSN,RECFOUND
            ELSE
              MATCH     ALREDCAN,ENDDATEX
              IF        @LESS
                MOVE      ANSN,RECFOUND
              ELSE
                MOVE      ANSY,RECFOUND
              ENDIF
            ENDIF
            GOTO      REFER999
          ENDIF
.
          MATCH     "3",ALRESTAT
          IF        @EQUAL
            MATCH     SP70,ALREDINA
            IF        @EQUAL
              MOVE      ANSN,RECFOUND
            ELSE
              MATCH     ALREDINA,ENDDATEX
              IF        @LESS
                MOVE      ANSN,RECFOUND
              ELSE
                MOVE      ANSY,RECFOUND
              ENDIF
            ENDIF
            GOTO      REFER999
          ENDIF
.
          MATCH     "5",ALRESTAT
          IF        @EQUAL
            MATCH     SP70,ALRESRDT
            IF        @EQUAL
              MOVE      ANSN,RECFOUND
            ELSE
              MATCH     ALRESRDT,ENDDATEX
              IF        @LESS
                MOVE      ANSN,RECFOUND
              ELSE
                MOVE      ANSY,RECFOUND
              ENDIF
            ENDIF
            GOTO      REFER999
          ENDIF
.
REFER999  RETURN
+
.------------------------------------------------------------------------------
. Print the head of the report     
.------------------------------------------------------------------------------
PHED0000  CALL      HEAD132
.
          PACK      KEY9,DISACODE,SP70
          CALL      RDDSMAS1
          IF        OVRCD=1
            MOVE      SP70,DSMADESC
          ENDIF
.
          MOVE      "do",KEY2 
          PACK      KEY5,KEY2,FILTDSOU,SP70
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE      SP70,TDESC
          ENDIF
.
          UNPACK    ENDDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      ENDDATED,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          PRINT     *40,"Disaster : ",*66,DISACODE,SP1,DSMADESC
          PRINT     *40,"Discharge/Event End Date: ",*66,FILTDAYS,SP7,ENDDATED
          PRINT     *40,DISODESC,":",*66,FILTDSOU,SP7,TDESC
.
          PRINT     *40," "  
          CALL      UND132
.                                                          
          PRINT     *1,"|",*3,"U/R Number":              
                    *14,"|",*16,"Name":                  
                    *57,"|",*59,"Disaster":               
                    *99,"|",*101,"Disaster ID":           
                    *132,"|" 
          CALL      UND132
.                                                        
          MOVE      TEN,CLNO
.
PHED9999  RETURN
.
.------------------------------------------------------------------------------
. Print the details line
.------------------------------------------------------------------------------
PRTDIS00  ADD       ONE,CLNO                
          IF        CLNO >= 55
            CALL      PHED0000
          ENDIF
.
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,PATNAME        * Patients name
.
          MOVE      SP70,DSMADESC
          PACK      KEY9,DSPTCODE,SP70
          CALL      RDDSMAS1
          IF        OVRCD=1
            MOVE      SP70,DSMADESC 
          ENDIF
          MOVE      DSMADESC,DIM40
. 
          PRINT     *1,"|",*4,DSPTURNO:                  
                    *14,"|",*16,PATNAME:               
                    *57,"|",*59,DIM40:
                    *99,"|",*101,DSPTDIID:
                    *132,"|"
.
PRTDIS99  RETURN
+
.------------------------------------------------------------------------------
. Print the end of the report                                 
.------------------------------------------------------------------------------
PEND0000  CALL      UND132
          PRINT     *N,*1,"No. of records updated: ",NOFUPDT   
          PRINT     *N,"**** End of Report ****" 
.
PEND9999  RETURN
+
.         I/O INCLUDES
.
          INC       STDGENCD
          INC       PATHSPKY
          INC       PATCODKY
          INC       ALLREFIO/INC
          INC       EMRVISIO/INC
          INC       PATDSCIO/INC
          INC       DISMASIO/INC
          INC       DISPATIO/INC
          INC       DISPTLIO/INC
          INC       PATCODIO/INC            * Patient Codes File
          INC       PATHSPIO/INC            * hospital file
          INC       PATMA1IO/INC            * Patient Master File
          INC       PATMI1IO/INC            * Patient Admission File
          INC       PATVISIO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC            * Visit Extension file
          INC       OUTBOAIO/INC
          INC       WATTR1IO/INC
          INC       MLTCODIO/INC
          INC       PATCODTM
          INC       NAMSTRCD

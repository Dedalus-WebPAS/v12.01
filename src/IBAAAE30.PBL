.*****************************************************************************
.*              P R O G R A M  S U M M A R Y                                 *
.*              -------------  -------------                                 *
.*                                                                           *
.*     PROGRAM NAME:    IBAAAE30                                             *
.*                                                                           *
.*     FUNCTION:        DETAILED EMR REVENUE BY DOCTOR RPT                   *
.*                                                                           *
.*     DATE WRITTEN:    18/06/2007                                           *
.*                                                                           *
.*     AUTHOR:          PETER VELA                                           *
.*                                                                           *
.*     MODIFICATIONS:                                                        *
.*    V10.04.02 12/06/2014 Steve Armstrong  CAR 301639                       *
.*              Added call to KILL0000 when exiting the program              *
.*    V10.04.01 07/03/2013 Ania P        CAR 261630                          *
.*              Remove the use of PORT for temp file names                   *
.*****************************************************************************
.*     V9.09.05 13/12/2007 Ebon Clements CAR 157584                          *
.*              Fixed Doctor/Grand total alignment                           *
.*     V9.09.04 16/01/2008 Peter Vela CAR 149792                             *
.*              Modified program to validate by invoice date instead of      *
.*              service date                                                 *
.*     V9.09.03 10/08/2007 Peter Vela CAR 142173                             *
.*              Fixed Doctor Initial                                         *
.*     V9.09.02 08/08/2007 Peter Vela CAR 142173                             *
.*              Fixed Date Range validation                                  *
.*     V9.09.01 18/06/2007 Peter Vela CAR 142173                             *
.*              Created Program                                              *
.*****************************************************************************
.
          INC       STD001FD
.
          INC       AAEDTRFD/INC
.
          INC       IBASEQFD/INC 
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
          INC       PMSHCPFD/INC
          INC       PATVISFD/INC
          INC       PATCODFD/INC
          INC       PATDO1FD/INC          * doctor file pat
          INC       PATMA1FD/INC          * doctor file pat
          INC       PATINVFD/INC
.
.
FROMDATE  DIM       8        * FROMdate ccyymmdd
TODATE    DIM       8        * TOdate ccyymmdd
TOTAMT    FORM      12.2        * Total number of patients
TOTDOC    FORM      12.2
.
CMDLINE   DIM       60        * command line for temporary file
CURDATE   DIM      8         * Current Date dd/mm/yy
DIM2A     DIM      2
DIM3      DIM      3         
DIM10     DIM      10
DIM22     DIM      22
DIM32     DIM      32        * patient name to print
DIM28     DIM      28        * doctor name to print
DOCCODE   DIM       6
DCODE1    DIM       10
DCODE2    DIM       10
FNAMET    DIM       8
.
LSTDOCT   DIM       6
DESCOPT   DIM       23        * option description in heading
.
PTCNDCQS  FORM      1
UNIQUE    FORM      6         * temporary file counter
.
TEMPFILE  IFILE     SQL, FIXED=67, KEY="U1-10,11-18,19-27,28-33"
.
.Name     Type      Size   Physical  Start  Desc
.----     ----      ----   --------  -----  ---------------
.
TMPDRCD   DIM       10     10        1      Doctor Code
TMPINDT   DIM       8      8         11     Invoice Date
TMPITNO   DIM       9      19        19     Item Number
TMPTRNO   DIM       6      6         28     Trans Number
TMPINVN   DIM       8      8         34     Invoice Number
TMPVISN   DIM       8      8         42     Visit/AAE Number
TMPTAMT   DIM       15     15        50     Total Amount
TMPSRVS   DIM       2      2         65     No. of services
.
VAR       FORM      1
.
XCENT1    DIM       2
XYEAR1    DIM       2
XMON1     DIM       2
XDAY1     DIM       2
.
PRGID     INIT      "IBAAAE30"
PRGNAM    INIT      "Detailed Emergency Rev By Doc"
VERSION   INIT      "V12.01.00"
+
.         Start of program
.
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening aaedtref";
          OPEN      AAEDTRE4,"aaedtref"
          OPEN      AAEDTRE6,"aaedtref"
.
          DISPLAY   *P64:24,"pmshcpaf";
          OPEN      PMSHCPA1,"pmshcpaf"
.
          DISPLAY   *P64:24,"pmshcpaf";
          OPEN      PMSHCPA2,"pmshcpaf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA1,"patvisaf"
.
          DISPLAY   *P64:24,"patinvrf";
          OPEN      PATINVR1,"patinvrf"
          OPEN      PATINVR2,"patinvrf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*2,CDD,CMM,CYY,CCC
          READ      CONTROLF,TWENTY1;*247,PTCNDCQS
.
          CALL      TFILENAM
          PACK      FNAMET,TFILNAME,SP70
.
.         Main menu
.
START     MOVE      ZERO,CPAGENO
          MOVE      "60",CLNO
          MOVE      SP6,LSTDOCT
          MOVE      ZERO,TOTAMT
          MOVE      ZERO,TOTDOC
.
          HLINE     *G37,2,50,80
          DISPLAY   *P39:4,*EF:
                    *P1:5,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:6,*V2LON,ONE,*HOFF," = Run Report"
.
          KEYIN     *P1:11,"Option : ",*V2LON,OPTION
.
          COMPARE   ZERO,OPTION
          GOTO      ENDIT IF EQUAL
.
BRNCH     BRANCH    OPTION OF DDOCT
          BEEP
          GOTO      START
.
DDOCT     DISPLAY   *P50:2,*V2LON,"- Doctor Sequence "
          MOVE      "Doctor Sequence          ",DESCOPT
.
START1    DISPLAY   *P1:12,*EF:
                    *P1:12,"From  Date  :":
                    *P1:13,"To    Date  :"
.
          UNPACK    SP6 INTO CYEAR,CMON,CDAY
.
KIBA25    MOVE      TEN5,CCOL
          MOVE      TEN2,CVERT
.
          MOVE      "40",ECOL
          MOVE      CVERT,EVERT
          MOVE      CCC,CCENT
.
FIXMON    CALL      KEYDATE
          BRANCH    OVRCD OF START
.
          PACK      FROMDATE WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",FROMDATE
.
KIBA26    UNPACK    SP6 INTO CYEAR,CMON,CDAY
.
          MOVE      TEN5,CCOL
          MOVE      TEN3,CVERT
          MOVE      "40",ECOL
          MOVE      CVERT,EVERT
.
          MOVE      CCC,CCENT
.
FIXMON2   CALL      KEYDATE
          BRANCH    OVRCD OF KIBA25
.
          PACK      TODATE WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE
.
          MATCH     FROMDATE,TODATE
          GOTO      ERR1 IF LESS
.
          GOTO      CONFIRM
.
ERR1      DISPLAY   *P1:24,*B,*V2LON:
                    "** FROM Date Greater Than TO Date ** ";
          CALL      HITENTER
          DISPLAY   *P1:24,*EL;
          GOTO      START1
.
.
.
CONFIRM   KEYIN     *P1:15,*EL,"Ok to Continue (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF:
                    ") ? ",*V2LON,ANS;
.
          CMATCH    ANSN,ANS
          GOTO      START IF EQUAL
.
          CMATCH    ANSY,ANS
          GOTO      KSING IF EQUAL
.
          BEEP
          GOTO      CONFIRM
.............................................................................
.         Doctor range
.
KSING     DISPLAY   *P1:15,*EL,"From Doctor Code : "
          MOVE      SP70,DCODE1 
          MOVE      "20",ECOL
          MOVE      "15",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP70,CKYIDEF8
          MOVE      ZERO,CKYINACT
          CALL      PMSHCPKY
          BRANCH    EXIT,KSING1,KSING  
          MOVE      PMHCHCPC,DCODE1
          GOTO      KSING2
.
KSING1    MOVE      SP70,DCODE1
          DISPLAY   *P20:15,*EL,"Start"          
.
KSING2    DISPLAY   *P1:16,*EL,"To Doctor Code : "
          MOVE      SP70,DCODE2
          MOVE      "18",ECOL
          MOVE      "16",EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP70,CKYIDEF8 
          MOVE      ZERO,CKYINACT
          CALL      PMSHCPKY
          BRANCH    EXIT,KSING3,KSING
          MOVE      PMHCHCPC,DCODE2
          GOTO      KSING4
.
KSING3    MOVE      Z70,DCODE2
          DISPLAY   *P18:16,*EL,"Finish"  
.
KSING4    MATCH     DCODE1,DCODE2
          GOTO      ERR2 IF LESS
          GOTO      PROC1
.
ERR2      DISPLAY   *P1:24,*B,*V2LON:
                    "** From Doctor Code Greater than To Doctor Code. **";
          CALL      HITENTER
          DISPLAY   *P1:24,*EL;
          GOTO      KSING
.
.*********************************
.
PROC1     CALL      KILL0000
          CALL      CREA0000
.
          OPEN      TEMPFILE,FNAMET
          MOVE      ONE,UNIQUE
.
          DISPLAY   *P1:24,*EL:
                    *P10:24,"Invoice No: ":
                    *P40:24,"Scanning: "
.
          PACK      KEY16,FROMDATE,SP70
          CALL      RSPTINV2
PROC1A    CALL      RKPTINV2
          BRANCH    OVRCD,DOCT1
.
          MATCH     PINVDTE,TODATE
          GOTO      DOCT1 IF LESS 
.
          PACK      KEY23,PINVNO,FIVE,SP70
          CALL      RDSDTRE4
PROC1B    CALL      RDKDTRE4
          BRANCH    OVRCD,PROC1A
.
          MATCH     PINVNO,ATINVNO
          GOTO      PROC1A IF NOT EQUAL
.
          MATCH     SP70,ATDTEDAD
          GOTO      PROC1B IF EQUAL
          GOTO      PROC1B IF EOS
.
          MATCH     DCODE1,ATDTEDAD
          GOTO      PROC1B IF LESS
          MATCH     ATDTEDAD,DCODE2
          GOTO      PROC1B IF LESS
.
          IF        ATRECTYP<>FIVE
            GOTO      PROC1B
          ENDIF
.
          DISPLAY   *P24:24,*V2LON,PINVNO;
.
          PACK      TMPDRCD,ATDTEDAD,SP70
          PACK      TMPINDT,PINVDTE,SP70
          PACK      TMPITNO,ATITEMNO,SP70
          PACK      TMPTRNO,UNIQUE,SP70
          PACK      TMPINVN,ATINVNO,SP70
          PACK      TMPVISN,DATNUMB,SP70
          PACK      TMPTAMT,ATPATAMT,SP70
          PACK      TMPSRVS,ATSERVS,SP70
.
          PACK      KEY33,ATDTEDAD,PINVDTE,ATITEMNO,DATTRANS,SP70 
.
          WRITE     TEMPFILE,KEY33;TMPDRCD,TMPINDT,TMPITNO,TMPTRNO,TMPINVN:
                                   TMPVISN,TMPTAMT,TMPSRVS 
.
          ADD       ONE,UNIQUE
          GOTO      PROC1B
.
.*********************************
.
.         Subroutine for doctor sequence
.
.DOCT1     PACK      KEY40,DCODE1,FROMDATE,SP70
.          CALL      RDSDTRE6
..
.NEXTD4    CALL      RDKDTRE6
.          BRANCH    OVRCD OF ENDP
..
.          MATCH     SP70,ATDTEDAD
.          GOTO      NEXTD4 IF EQUAL
..
.          MATCH     ATDTEDAD,DCODE2
.          GOTO      ENDP IF LESS
..
.          MATCH     FROMDATE,ATDDATE
.          GOTO      NEXTD4 IF LESS
..
.          MATCH     ATDDATE,TODATE
.          GOTO      NEXTD4 IF LESS
..
.          IF        ATRECTYP<>FIVE
.            GOTO      NEXTD4
.          ENDIF
.
DOCT1     PACK      KEY33,SP70
          READ      TEMPFILE,KEY33;;
.
NEXTD4    READKS    TEMPFILE;TMPDRCD,TMPINDT,TMPITNO,TMPTRNO,TMPINVN,TMPVISN:
                             TMPTAMT,TMPSRVS
          GOTO      ENDP IF OVER
.
          PACK      ATDTEDAD,TMPDRCD,SP70
          PACK      PINVDTE,TMPINDT,SP70
          PACK      ATITEMNO,TMPITNO,SP70
          PACK      DATTRANS,TMPTRNO,SP70
          PACK      ATINVNO,TMPINVN,SP70
          PACK      DATNUMB,TMPVISN,SP70
          MOVE      TMPTAMT,ATPATAMT
          MOVE      TMPSRVS,ATSERVS
.
          PACK      KEY10,ATDTEDAD,SP70
          CALL      RDPMHCP1
          IF        OVRCD=0
.
            ENDSET      PMHCTITL
DOCN10      CMATCH      SP1,PMHCTITL
            GOTO        DOCN20 IF NOT EQUAL
            BUMP        PMHCTITL BY -1
            GOTO        DOCN10 IF NOT EOS
.
DOCN20      LENSET      PMHCTITL
            RESET       PMHCTITL 
.
            MOVE        PMHCGNAM,ANS
            PACK        DIM28 WITH PMHCTITL,SP1,ANS,DOT,PMHCSNAM
          ELSE
            MOVE      "Invalid Doctor",DIM28
          ENDIF
.
          MOVE      SP70,DIM22
          PACK      KEY8,DATNUMB,SP70
          CALL      RDPTVIS1
          IF        OVRCD=0
            PACK       KEY8,PVIURNO,SP70
            CALL       RDMAST1
            IF         OVRCD=1
              MOVE       "Invalid Patient",DIM22 
            ENDIF
            MOVE       PGNAME,DIM1
            PACK       DIM22,DIM1,SP1,PSNAME
          ELSE
            MOVE    "Invalid Patient",DIM22 
          ENDIF
.
.         PACK      KEY8,ATINVNO,SP70
.         CALL      RDPTINV1
.         IF        OVRCD=1
.           MOVE      SP70,PINVDTE
.         ENDIF
.
          CALL      PRNTDA
          GOTO      NEXTD4
.
PRNTDA    COMPARE   "55",CLNO
          CALL      HEAD IF NOT LESS
.
          CALL      DOCTOT00
.
          PRINT     *1,"|";
.
          MATCH     ATDTEDAD,LSTDOCT
          GOTO      PRNTDA1 IF EQUAL
.
          PRINT     ATDTEDAD,SP2,DIM28;
          MOVE      ATDTEDAD,LSTDOCT
.
PRNTDA1   MATCH     SP70,PINVDTE
          IF        @EQUAL
            MOVE      "Invalid Dte",CPCDATE
          ELSE  
            UNPACK    PINVDTE,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
          ENDIF
          PRINT     *38,"|",CPCDATE;
.
          PRINT     *49,"| ",DIM22,*73,"| ",DATNUMB,*84,"| ",ATINVNO:
                  *95,"| ",ATITEMNO,*107,"| ",ATSERVS,*112,"| ",ATPATAMT,*132,"|"
          ADD       ONE,CLNO
          ADD       ATPATAMT,TOTAMT
          ADD       ATPATAMT,TOTDOC
          RETURN
.
..............................................................................
.         Printing of grand totals
.
ENDP      CALL      KILL0000
.
          COMPARE   ZERO,CPAGENO
          GOTO      NODATA IF EQUAL
.
          CALL      PAGEND
          PRINT     *1,"| Doctor Total";
          PRINT     *112,"| ",TOTDOC,*132,"|",*N;
          CALL      PAGEND
          PRINT     *1,"| Grand Total";
          PRINT     *112,"| ",TOTAMT,*132,"|",*N;
          CALL      PAGEND
.
          PRINT      *N,*N,"** END OF REPORT **";
          DISPLAY   *P1:24,*EL:
                    *V2LON,"*** Generation Completed *** ";
          CALL      HITENTER
          GOTO      START
.
NODATA    DISPLAY   *P1:24,*EL,*B:
                    *V2LON,"*** No Available Data *** ";
          CALL      HITENTER
          GOTO      START
.
............................................................................
.         Heading
.
HEAD      COMPARE   ZERO,CPAGENO
          CALL      PAGEND IF NOT EQUAL
.
          MOVE      ONE,CNOUNDLN 
          MOVE      DESCOPT,CPHDROPT
          CALL      IBACLOCK    
          CALL      HEAD132
.
          UNPACK    FROMDATE INTO CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          MOVE      DCODE1,DIM10
          MATCH     SP70,DCODE1
          IF        @EQUAL
            MOVE      "Start",DIM10
          ENDIF 
          PRINT     *50,"From : ",*58,CPCDATE," ",DIM10
.
          UNPACK    TODATE INTO CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          MOVE      DCODE2,DIM10
          MATCH     Z70,DCODE2
          IF        @EQUAL
            MOVE      "Finish",DIM10
          ENDIF
          PRINT     *N,*50,"To   : ",*58,CPCDATE," ",DIM10
.
          PRINT     *N
          CALL      PAGEND
 PRINT       *1,"|Doctor      Doctor Name",*38,"|Invce Date",*49,"| Patient Name":
                      *73,"| Vis Num":
                      *84,"| Invoice ",*95,"| Itm Num",*107,"| No":
                      *113,"|       Amount",*132,"|"
          CALL      PAGEND
          MOVE      TEN2,CLNO
.
          MOVE      SP6,LSTDOCT     ** PRINT DOC NAME ON 1ST LINE NEW PAGE
          RETURN
.
.............................................................................
.
PAGEND    PRINT     *1,"*--------------------------------------------":
                    "--------------------------------------------":
                    "------------------------------------------*"
          ADD       ONE,CLNO
          RETURN
.
.
ENDIT     CALL      KILL0000
          CHAIN     PGM
          STOP
.
DOCTOT00  MATCH     SP70,LSTDOCT
          IF        !@EQUAL
            MATCH     ATDTEDAD,LSTDOCT
            IF        !@EQUAL
              CALL      PAGEND
              PRINT     *1,"| Doctor Total ";
              PRINT     *112,"| ",TOTDOC,*132,"|"
              CALL      PAGEND
              MOVE      ZERO,TOTDOC
            ENDIF
          ENDIF
.
DOCTOT99  RETURN
.
KILL0000  CLOSE     TEMPFILE,DELETE
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMET,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          DISPLAY   *P1:14,*EF;
KILL9999  RETURN 
.
CREA0000  CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMET,CMDLINE
          APPEND    " 67 U1-10,11-18,19-27,28-33",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
CREA9999  RETURN
.
          INC       AAEDTRIO/INC
          INC       PMSHCPIO/INC
          INC       PATVISIO/INC
.
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC
.
          INC       PATCODIO/INC
          INC       PATMA1IO/INC
          INC       PATINVIO/INC
.
          INC       PMSHCPKY
          INCLUDE   STD001IO
.

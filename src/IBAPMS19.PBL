.******************************************************************************d
.* System   :  PATIENT MANAGEMENT                                             *
.* Program  :  IBAPMS19                                                       *
.* Desc     :  Doctors Clinical Statistics 2                                  *
.******************************************************************************
.* Date     :  14/05/1990                                                     *
.* Author   :  DIG                                                            *
.* N.B.     :  This program should balance with IBAHOS87 when run for         *
.*             individual periods.  If this program is run over a range of    *
.*             periods, the bed day figures and the occupancy figures will not*
.*             be accurate and IBAHOS87 should be used instead.  This is      *
.*             because the statistics file this program reads is generated for*
.*             individual periods and not ranges of periods.                  *
.*                                                                            *
.******************************************************************************
.* Mods.    :                                                                 *
.* V10.03.02  02/01/2013 Patrick Adair                              CAR 261630 *
.*                       Removed port number from temp file name               *
.*        V10.03.01 18/10/2012  Jill Parkinson CAR 273375                     *
.*                  Recompiled for DATECONV - first day of year calculation   *
.*             V5.07.01  29/07/1999  Steve Downing                            *
.*                       Replaced CALC0000 with DAYSEP                        *
.******************************************************************************
.*             V5.01.01  29/01/91 i chung          SRF 107711                 *
.*                       Fix to collate data using financial year/month       *
.*                       instead of calendar year/month                       *
.*             V5.01.02  21/02/91 i chung          SRF 107716                 *
.*                       Use a work var (WORKOCC) same in size to Av Daily Occ*
.*                       instead of SBOCC itself to work out the % Occupancy  *
.*             V5.01.03  29/08/91 J.Tan            SRF 110186                 *
.*                       Fixed truncation of grand total %occupancy           *
.*             V5.01.04  03/06/93 J.Tan         SRF 121660                    *
.*                       Added ward/bed history file                          *
.*             V5.01.05  30/12/93 I.Rutt     Mt Alvernia                      *
.*                       Added new include PATACDAY                           * 
.*         V5.02.001 02/03/95 Paul Howells     SRF 135026                     *
.*                   Fixed the use of PATWBHFD file.                          *
.*         V5.03.01  15/04/1996  Steve Armstrong                              *
.*                   Mods to use WDACTVDY to calculate active bed days.       *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
          INC       PATCOMM/INC
.
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       PATC2SFD/INC     * clinical statistics 2 file
          INC       PATCODFD/INC     * codes file
          INC       PATCONFD/INC     * control file
          INC       PATDO1FD/INC     * doctors file
          INC       PATDRGFD/INC     * period date range file
          INC       PATWR1FD/INC     * ward file
          INC       PATWBHFD/INC     * ward bed history file
          INC       WDACTVVR/INC
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
+
.**********************************************************************
.*                            CONSTANTS                               *
.**********************************************************************
CODECG    INIT      "CG"
.
OYEAR     FORM      "365"   * used in calc0000
+ 
.**********************************************************************
.*                            VARIABLES                               *
.**********************************************************************
AVDLOCC   FORM      6.2
.
BEDTOT    FORM      6            
.
CHKZER    FORM      5
CMDLINE   DIM       50
CSTPER    DIM       6       * ccyymm
CURPER    DIM       6       * current period
CURRCLAS  DIM       3
.
DAYNUM    FORM      5       * number of days in report period range
DIM4      DIM       4
DISTOT    FORM      5       * discharge total
DNAME     DIM       25      * doctors name working variable
DOCDESC   DIM       17      * doctors name
.
ENDDATE   DIM       10      * enddate
ENDDTE    DIM       10      * enddate
.
FADMDTE   DIM       6       * ccyymm
FDMDATE   DIM       8       * yymmdd
FORM52    FORM      5.2     * working variable for totals
FROMPER   DIM       8       * from period for printing (mm/ccyy)
FROMDATE  DIM       8
.
GRAVDOCC  FORM      6.2
GRDIED    FORM      6
GRDISCH   FORM      6
GRELECT   FORM      6
GREMERG   FORM      6
GRLSR1    FORM      6
GRLSR2    FORM      6
GRLSR3    FORM      6
GRLSR4    FORM      6
GRNOMBDS  FORM      6
GROCC     FORM      3.2
GRTOTBDY  FORM      7
GRTOTPRV  FORM      7
GRTOTPUB  FORM      7
.
JDAYS     FORM      3       * used in calc0000
JYEAR     FORM      2       * used in calc0000
.
.
LYR       FORM      1       * used in calc0000
.
OCCTOT    FORM      3.2     * average daily occupancy total patients
.
PRTFLG    FORM      1
.
SAVWARD   DIM       3 
SBAVDOCC  FORM      6.2
SBDIED    FORM      6
SBDISCH   FORM      6
SBELECT   FORM      6
SBEMERG   FORM      6
SBLSR1    FORM      6
SBLSR2    FORM      6
SBLSR3    FORM      6
SBLSR4    FORM      6
SBNOMBDS  FORM      6
SBOCC     FORM      5.2
SBTOTBDY  FORM      7
SBTOTPRV  FORM      7
SBTOTPUB  FORM      7
STRDATE   DIM       10      * start date
STRDTE    DIM       10      * start date
STATMP    DIM       8
SVHNOCC   FORM      3
.
TODATE    DIM       8       * todate
TADMDTE   DIM       6       * ccyymm
TDMDATE   DIM       8       * yymmdd
TOPER     DIM       8       * to period for printing (mm/ccyy)
TOTDAY    FORM      5
TOJULDD   FORM      3
TOJULYY   FORM      2
TOJULCC   FORM      2 
TOTPERBD  FORM      6  
.
UNTDESC   DIM       13      * unit description
.
WDATE1    DIM       8       * used in calc0000
WDATE2    DIM       8       * used in calc0000
WORKOCC   FORM      6.2     * work variable for "% Occ." - SBOCC
WKDATE    DIM       8
WARTOTBD  FORM      6
WARBEDAC  FORM      8.2
.
ZZZ       INIT      "zzz"
+
.**********************************************************************
.*                        Temp Index File                             *
.**********************************************************************
.
STATMPXX  IFILE SQL, FIXED=50, KEY="U1-3,4-9"
.
.Name     Type      Length     Physical    Start
.----     ----      ------     --------    -----
TMPWRDCL  DIM         3           3          1
TMPDOCT   DIM         6           6          4
TMPEMERG  FORM        5           4         10
TMPELEC   FORM        5           4         14
TMPDSCH   FORM        5           4         18
TMPDEAD   FORM        5           4         22
TMPPRIV   FORM        6           4         26
TMPPUBL   FORM        6           4         30
TMPLSR1   FORM        5           4         34
TMPLSR2   FORM        5           4         38
TMPLSR3   FORM        5           4         42
TMPLSR4   FORM        5           4         46
.
.End of Record                              50
.
PRGID     INIT      "IBAPMS19"
PRGNAM    INIT      "Doctors Clinical Statistics 2"
VERSION   INIT      "V12.01.00"
+
.**********************************************************************
.*                         MAINLINE                                   *
.**********************************************************************
.
ML0000    CALL      INIT0000        * initialisation
.
ML1000    CALL      SCR0000         * select option
          BRANCH    EXIT,ML9000
.
ML1500    CALL      CLR0000         * clear variables
.
          CALL      DAT0000         * keyin period range
          BRANCH    EXIT,ML1000
.
          CALL      CONTQST         * ok to continue y/n ?
          BRANCH    CEXIT,ML2000:   * yes
                          ML1500:   * no
                          ML1000    * cancel
.
ML2000    CALL      CREA0000        * create temporary file
.
          CALL      UC2S0000        * process patc2saf data
          CALL      PRNT0000        * print patc2saf data
          CALL      END0000         * end of report
          GOTO      ML1000
.
ML9000    CALL      KILL0000        * remove temporary file
          CHAIN     PGM
+
.**********************************************************************
.*                         INIT0000                                   *
.*                      Initialisation                                *
.**********************************************************************
INIT0000  CALL      DISPHEAD                * display screen header
.
          DISPLAY   *P56:24,"Opening patc2saf";
          OPEN      PATC2SA1,"patc2saf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"patdrgaf";
          OPEN      PATDRGA1,"patdrgaf"
          OPEN      PATDRGA3,"patdrgaf"
.
          DISPLAY   *P64:24,"patwr1af";
          OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY   *P64:24,"patwbhaf";
          OPEN      PATWBHA1,"patwbhaf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,FIFTY9;*27,CPERMTH
.
INIT9999  RETURN
+
.**********************************************************************
.*                              CLR0000                               *
.*                         Clear Variables                            *
.**********************************************************************
CLR0000   MOVE      ZERO,TMPEMERG
          MOVE      ZERO,TMPELEC
          MOVE      ZERO,TMPDSCH
          MOVE      ZERO,TMPDEAD
          MOVE      ZERO,TMPPRIV
          MOVE      ZERO,TMPPUBL
          MOVE      ZERO,TMPLSR1
          MOVE      ZERO,TMPLSR2
          MOVE      ZERO,TMPLSR3
          MOVE      ZERO,TMPLSR4
.
CLR9999   RETURN
+
.**********************************************************************
.*                              CLRT0000                              *
.*                     Clear the total Variables                      *
.**********************************************************************
CLRT0000  MOVE      ZERO,GRAVDOCC  
          MOVE      ZERO,GRDIED    
          MOVE      ZERO,GRDISCH   
          MOVE      ZERO,GRELECT   
          MOVE      ZERO,GREMERG   
          MOVE      ZERO,GRLSR1    
          MOVE      ZERO,GRLSR2    
          MOVE      ZERO,GRLSR3    
          MOVE      ZERO,GRLSR4    
          MOVE      ZERO,GRNOMBDS  
          MOVE      ZERO,GROCC     
          MOVE      ZERO,GRTOTBDY  
          MOVE      ZERO,GRTOTPRV  
          MOVE      ZERO,GRTOTPUB  
.
.------ clear the sub-total variables ------
.
CLRT5000  MOVE      ZERO,SBAVDOCC  
          MOVE      ZERO,SBDIED    
          MOVE      ZERO,SBDISCH   
          MOVE      ZERO,SBELECT   
          MOVE      ZERO,SBEMERG   
          MOVE      ZERO,SBLSR1    
          MOVE      ZERO,SBLSR2    
          MOVE      ZERO,SBLSR3    
          MOVE      ZERO,SBLSR4    
          MOVE      ZERO,SBNOMBDS  
          MOVE      ZERO,SBOCC     
          MOVE      ZERO,SBTOTBDY  
          MOVE      ZERO,SBTOTPRV  
          MOVE      ZERO,SBTOTPUB  
.
CLRT9999  RETURN
+
.**********************************************************************
.*                         SCR0000                                    *
.*                      Main Option Screen                            *
.**********************************************************************
SCR0000   DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Clinical Statistics 2 Report":
                    *P1:7,"Select Option :";
.
.------ keyin main option ------
.
SCR1000   KEYIN     *P17:7,*EL,*DV,UNDLN1,*P17:7,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      SCR9000 IF EQUAL
.
          BRANCH    OPTION,SCR8000
          BEEP
          GOTO      SCR1000                 * invalid option chosen
.
.------ valid option chosen ------
.
SCR8000   MOVE      FALSE,EXIT
          GOTO      SCR9999
.
.------ exit option chosen ------
.
SCR9000   MOVE      TRUE,EXIT
.
SCR9999   RETURN
+
.**********************************************************************
.*                         DAT0000                                    *
.*                    Keyin Period/Month                              *
.**********************************************************************
DAT0000   DISPLAY   *P1:8,*EF:
                    *P1:11,"From ",CPERMTH," : ":
                    *P1:13,"To   ",CPERMTH," : "
.
          PACK      CPTDATE,CCC,CYY,CMM,CDD
          REP       " 0",CPTDATE
          CALL      GPERD                 * get the period for the current date
          BRANCH    EXIT,DAT7000
.
          PACK      CURPER,DRGYR,DRGNUM
.
.------ Find previous period ------
.
          PACK      KEY6,CURPER
          CALL      RDDRGA1
          CALL      RDPDRGA1
          PACK      CSTPER,DRGCYR,DRGCNUM
.
.------ Keyin the From Period ------
.
DAT1000   UNPACK    CSTPER,CCENT,CYEAR,CMON
          MOVE      ZERO,CHIGHLT         * 0 = highlight 1 = no highlight
          MOVE      "15",CCOL            * column for keyin
          MOVE      TEN1,CVERT           * row for keyin
          MOVE      "58",ECOL            * col for keyin error messages
          MOVE      TEN1,EVERT           * row for keyin error messages
          CALL      KEYPER               * keyin the period
          BRANCH    CQUEST,DAT1000       * ? keyed in
          BRANCH    OVRCD,DAT1000        * no date keyed in
.
.------ Validate the From Period entered ------
.
          PACK      KEY6,ICENT,IYEAR,IMON
          REP       " 0",KEY6
          CALL      RDDRGA3
          BRANCH    OVRCD,DAT2000
.
.------ Pack appropriate start dates for From Period ------
.
          UNPACK    DRGFDTE,CCENT,CYEAR,CMON,CDAY
          MOVE      DRGFDTE,ACTVFDTE
          PACK      FDMDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,STRDATE
          PACK      FADMDTE,DRGYR,DRGNUM         * SRF 107711
.
          UNPACK    KEY6,CCENT,CYEAR,CMON        * From Period for printing
          PACK      FROMPER,CMON,SLASH,CCENT,CYEAR
.
.------ Pack appropriate end dates for From Period ------
.
          UNPACK    DRGTDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,ENDDATE
.
          DISPLAY   *P30:11,*EL,STRDATE," to ",ENDDATE
          GOTO      DAT3000
.
.------ Invalid period entered ------
.
DAT2000   DISPLAY   *P1:24,*EL,*B,*V2LON,CPERMTH:
                    " not found in Date Range file.  ",*HOFF;
          CALL      HITENTER
          GOTO      DAT1000
.
.------ Keyin the To Period ------
.
DAT3000   DISPLAY   *P15:13,*EL;
          UNPACK    FROMPER,CMON,ANS,CCENT,CYEAR
          MOVE      ZERO,CHIGHLT         * 0 = highlight 1 = no highlight
          MOVE      "15",CCOL            * column for keyin
          MOVE      TEN3,CVERT           * row for keyin
          MOVE      TEN3,EVERT           * row for keyin error messages
          CALL      KEYPER               * keyin the period code
          BRANCH    CQUEST,DAT3000       * ? keyed in
.
.------ Validate the To Period entered ------
.
          PACK      KEY6,ICENT,IYEAR,IMON
          REP       " 0",KEY6
          CALL      RDDRGA3
          BRANCH    OVRCD,DAT4000
.
.------ Pack appropriate start dates for To Period ------
.
          UNPACK    DRGFDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                      * pack the date
          MOVE      CPCDATE,STRDTE
.
          UNPACK    KEY6,CCENT,CYEAR,CMON        * to period for printing
          PACK      TOPER,CMON,SLASH,CCENT,CYEAR
.
.------ Pack appropriate end dates for To Period ------
.
          MOVE      DRGTDTE,TODATE
          MOVE      DRGTDTE,ACTVTDTE
          UNPACK    DRGTDTE,CCENT,CYEAR,CMON,CDAY
          PACK      TDMDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                      * pack the date
          MOVE      CPCDATE,ENDDTE
          PACK      TADMDTE,DRGYR,DRGNUM         * SRF 107711
.
          DISPLAY   *P30:13,*EL,STRDTE," to ",ENDDTE
.
.------ Check if the From Period is less than or equal to the To Period ------
.
          MATCH     FADMDTE,TADMDTE
          GOTO      DAT5000 IF LESS
          GOTO      DAT8000
.
.------ Invalid period entered ------
.
DAT4000   DISPLAY   *P1:24,*EL,*B,*V2LON,CPERMTH:
                    " not found in Date Range file.  ",*HOFF;
          CALL      HITENTER
          GOTO      DAT3000
.
.------ From Period must be less than or equal to the To Period ------
.
DAT5000   DISPLAY   *P1:24,*EL,*B,*V2LON,"To ",CPERMTH," must be greater than ":
                                         "or equal to From ",CPERMTH,". ";
          CALL      HITENTER
          GOTO      DAT3000
.
.------ No current period on file ------
.
DAT7000   KEYIN     *P1:24,*EL,*B,*V2LON,"Error: Current date not found in ":
                    "Period file.",*HOFF,"  Hit <",*V2LON,"ENTER",*HOFF:
                    "> to exit ",*EOFF,ANS,*P1:24;
          GOTO      DAT9000
.
.------ valid period range entered ------
.
DAT8000   MOVE      FALSE,EXIT
          GOTO      DAT9999
.
.------ no current period on file ------
.
DAT9000   MOVE      TRUE,EXIT
.
DAT9999   RETURN
+
.**********************************************************************
.*                         UC2S0000                                   *
.*                   Process PATC2SFD Data                            *
.**********************************************************************
UC2S0000  DISPLAY   *P1:24,*EL,*V2LON,"** ",*BLINKON,"Extracting Data",*HOFF:
                               *V2LON," **";
.
.------ Find the number of days in the report period ------
.
          MOVE      FDMDATE,WDATE2       * start date of From Period
          MOVE      TDMDATE,WDATE1       * end date of To Period
.
          DAYSEP    WDATE2,WDATE1,TOTDAY * calculate dates difference
          ADD       ONE,TOTDAY
.
          MOVE      TOTDAY,DAYNUM
.
          DISPLAY   *P35:24,"Doctor : ",*P59:24,"Scanning : ";
.
.------ position on the clinical statistics 2 file ------
.
          PACK      KEY15,FADMDTE,SP20
          CALL      RSPTC2S1
UC2S1000  CALL      RKPTC2S1
          BRANCH    OVRCD,UC2S9999
.
          DISPLAY   *P70:24,*EL,PTC2DOCT;
.
          MATCH     PTC2DATE,TADMDTE    * date still within report period range
          GOTO      UC2S9999 IF LESS
.
          DISPLAY   *P44:24,*V2LON,PTC2DOCT;
.
.------ Write or update the temporary file ------
.
          PACK      KEY9,PTC2CLSS,PTC2DOCT
          CALL      RDTMP1                  * read the temporary file
          BRANCH    OVRCD,UC2S2000
.
          ADD       PTC2EMRG,TMPEMERG
          ADD       PTC2ELCT,TMPELEC
          ADD       PTC2DSCH,TMPDSCH
          ADD       PTC2DEAD,TMPDEAD
          ADD       PTC2PRVP,TMPPRIV
          ADD       PTC2STDP,TMPPUBL
          ADD       PTC2RNG1,TMPLSR1
          ADD       PTC2RNG2,TMPLSR2
          ADD       PTC2RNG3,TMPLSR3
          ADD       PTC2RNG4,TMPLSR4
.
          CALL      UPTMP1                  * update the temp file
          GOTO      UC2S1000
.
.------ write a new record to the temporary file ------
.
UC2S2000  CALL      CLR0000                 * clear all variables
.
          MOVE      PTC2EMRG,TMPEMERG
          MOVE      PTC2ELCT,TMPELEC
          MOVE      PTC2DSCH,TMPDSCH
          MOVE      PTC2DEAD,TMPDEAD
          MOVE      PTC2PRVP,TMPPRIV
          MOVE      PTC2STDP,TMPPUBL
          MOVE      PTC2RNG1,TMPLSR1
          MOVE      PTC2RNG2,TMPLSR2
          MOVE      PTC2RNG3,TMPLSR3
          MOVE      PTC2RNG4,TMPLSR4
.
          CALL      WRTMP1                  * write to the temp file
          GOTO      UC2S1000
.
UC2S9999  RETURN
+
.**********************************************************************
.*                         END0000                                    *
.*                      End of Report                                 *
.**********************************************************************
END0000   COMPARE   ZERO,CPAGENO
          GOTO      END1000 IF NOT EQUAL
.
.------ No data selected ------
.
          DISPLAY   *P1:24,*EL,*P29:24,*V2LON:
                    "** No Record Selected ** ";
          CALL      HITENTER
          PRINT     *N,*N,*N,"*** End of Report ***"
          GOTO      END9999
.
.------ print the last sub-total line ------
.
END1000   CALL      TOTL0000                * print the sub-totals
.
          COMPARE   "45",CLNO
          CALL      HEAD0000 IF NOT LESS    * paginate if required
.
          MOVE      GRTOTBDY,GRAVDOCC
          COMPARE   ZERO,GRAVDOCC           * test if field is zero before
          GOTO      END1050 IF EQUAL          dividing
.
          DIV       DAYNUM,GRAVDOCC
.
END1050   MOVE      GRAVDOCC,WORKOCC
          MULT      "100",WORKOCC
.
          COMPARE   ZERO,WORKOCC            * test if field is zero before
          GOTO      END1070 IF EQUAL          dividing
.
          DIV       GRNOMBDS,WORKOCC
.
END1070   MOVE      WORKOCC,GROCC
.
.------ Print Grand Totals ------
.  
          PRINT     *1,"|Grand Total",*19,"|":
                    *20,GRNOMBDS,*28,GROCC,*35,"|",*36,GREMERG,*43,GRELECT:
                    *51,GRDISCH,*59,GRDIED,*67,"|",*68,GRTOTBDY:
                    *78,GRTOTPUB,*86,GRTOTPRV,*94,GRAVDOCC,*103,"|":
                    *104,GRLSR1,*111,GRLSR2,*118,GRLSR3,*125,GRLSR4,*132,"|"
.
          CALL      PGE0000                 * print underline
.
          PRINT     *N,*N,"Nominal Active Beds (Grouped By Service)":
                          " = Total Bed ":
                          "Complement of all Wards with same Classification":
                    *N,"% Occupancy = (Average Daily Occupancy * 100) / ":
                       "Nominal Active Beds":
                    *N,"Admitted    = No. of Patients Admitted in the Period":
                       " assigned to Admitting H.M.O.",*N,*15,"(Emerg ":
                       "if Admission Category = 2.  All others are Elect)":
                    *N,"Discharged  = No. of Patients Discharged in the Period":
                       " (NOT including Deaths)":
                    *N,"Died        = Discharged in the Period and ":
                       "Discharge Code Indicator = D":
                    *N,"Occupied Bed Days = Occupied Bed Days for the Period ":
                       "for all Patients assigned to Actual H.M.O. (Honorary":
                       " Transfers are accounted for)":
                    *N,"Average Daily Occupancy = Occupied Bed Days / No.":
                       " of Days in Period":
                    *N,"Long Stayers in Hospital are assigned to Current ":
                       "H.M.O. (Honorary Transfers are ignored)":
                    *N,*N,*N,"*** End of Report ***"
.
          DISPLAY   *P1:23,*EF,*P24:24,*V2LON:
                    "** Report Generation Completed ** ";
          CALL      HITENTER
.
END9999   DISPLAY   *P1:14,*EF
          CALL      KILL0000        * remove temporary file
          RETURN
+
.**********************************************************************
.*                         PRNT0000                                   *
.*                        Print Data                                  *
.**********************************************************************
PRNT0000  DISPLAY   *P1:24,*EL,*V2LON,"** ",*BLINKON,"Generating Report":
                           *HOFF,*V2LON," **";
.
          MOVE      ZERO,CPAGENO 
          MOVE      ONE,CNOUNDLN
          CLOCK     TIME,CTIMEIS 
.
          CALL      CLRT0000            * clear all total variables
.
          CALL      HEAD0000            * print page header
.
          MOVE      ZERO,PRTFLG         * printing flag
          MOVE      SP3,CURRCLAS   
          DISPLAY   *P55:24,"Doctor : ";
.
.------ position on the temporary file ------
.
          MOVE      SP9,KEY9
          CALL      RDSTMP1
PRNT1000  CALL      RDKTMP1
          BRANCH    OVRCD,PRNT9999
.
.         Add all the doctors totals together and if the sum of them
.         equals zero do not print the doctor
.
          MOVE      TMPEMERG,CHKZER
          ADD       TMPELEC,CHKZER
          ADD       TMPDSCH,CHKZER
          ADD       TMPDEAD,CHKZER
          ADD       TMPPRIV,CHKZER
          ADD       TMPPUBL,CHKZER
          ADD       TMPLSR1,CHKZER
          ADD       TMPLSR2,CHKZER
          ADD       TMPLSR3,CHKZER
          ADD       TMPLSR4,CHKZER
.
          COMPARE   ZERO,CHKZER
          GOTO      PRNT1000 IF EQUAL
.
          DISPLAY   *P64:24,*V2LON,TMPDOCT;
.
          COMPARE   ZERO,PRTFLG          * see if we are printing for the 
          GOTO      PRNT2000 IF EQUAL      first set of records
.
          MATCH     CURRCLAS,TMPWRDCL    * same ward classification ?
          GOTO      PRNT2000 IF EQUAL
.
          CALL      TOTL0000             * print ward classification totals
.
.------ print the current record ------
.
PRNT2000  MOVE      ONE,PRTFLG           * set print flag
          MOVE      SP20,DOCDESC
          MOVE      TMPDOCT,KEY6
.
          CALL      RDDOCT1              * read the doctors file
          BRANCH    OVRCD,PRNT2050
.
.------ Pack the doctors name ------
.
          CLEAR     DOCDESC
          MOVE      DSNAME,DOCDESC
.
          CALL      FIND0000             * routine to find the end of drs name
.
          APPEND    SP1,DOCDESC
.
          MOVE      DGNAME,ANS
          APPEND    ANS,DOCDESC
          APPEND    ".",DOCDESC
          RESET     DOCDESC
.
.------ Calculate the totals for each criteria ------
.
PRNT2050  MOVE      TMPPRIV,BEDTOT
          ADD       TMPPUBL,BEDTOT
          MOVE      BEDTOT,AVDLOCC
.
          COMPARE   ZERO,AVDLOCC            * test if field is zero before
          GOTO      PRNT2070 IF EQUAL         dividing
.
          DIV       DAYNUM,AVDLOCC
.
PRNT2070  COMPARE   "58",CLNO
          CALL      HEAD0000 IF NOT LESS    * paginate if required
.
          PRINT     *1,"|",DOCDESC,*19,"|":
                    *35,"|",*37,TMPEMERG,*44,TMPELEC,*52,TMPDSCH,*60,TMPDEAD:
                    *67,"|",*69,BEDTOT,*79,TMPPUBL,*87,TMPPRIV,*94,AVDLOCC:
                    *103,"|",*105,TMPLSR1,*112,TMPLSR2,*119,TMPLSR3:
                    *126,TMPLSR4,*132,"|"
          ADD       ONE,CLNO
.
.------ Add totals to get the sub totals for the different ------
.------ ward classifications ------
.
          ADD       TMPDEAD,SBDIED    
          ADD       TMPDSCH,SBDISCH   
          ADD       TMPELEC,SBELECT   
          ADD       TMPEMERG,SBEMERG   
          ADD       TMPLSR1,SBLSR1    
          ADD       TMPLSR2,SBLSR2    
          ADD       TMPLSR3,SBLSR3    
          ADD       TMPLSR4,SBLSR4    
          ADD       BEDTOT,SBTOTBDY  
          ADD       TMPPRIV,SBTOTPRV  
          ADD       TMPPUBL,SBTOTPUB  
.
          MOVE      TMPWRDCL,CURRCLAS
          GOTO      PRNT1000
.
PRNT9999  RETURN
+
.**********************************************************************
.*                         TOTL0000                                   *
.*                        Print Totals                                *
.**********************************************************************
TOTL0000  CALL      NOMB0000                * get nominal beds for this ward 
.                                             classification
          MOVE      SBTOTBDY,SBAVDOCC
.
          COMPARE   ZERO,SBAVDOCC           * test if field is zero before
          GOTO      TOTL0050 IF EQUAL         dividing
.
          DIV       DAYNUM,SBAVDOCC
.
TOTL0050  ADD       SBDIED,GRDIED    
          ADD       SBDISCH,GRDISCH   
          ADD       SBELECT,GRELECT   
          ADD       SBEMERG,GREMERG   
          ADD       SBLSR1,GRLSR1    
          ADD       SBLSR2,GRLSR2    
          ADD       SBLSR3,GRLSR3    
          ADD       SBLSR4,GRLSR4    
          ADD       SBNOMBDS,GRNOMBDS  
          ADD       SBTOTBDY,GRTOTBDY  
          ADD       SBTOTPRV,GRTOTPRV  
          ADD       SBTOTPUB,GRTOTPUB  
          MOVE      SBAVDOCC,WORKOCC
          MULT      "100",WORKOCC
.
          COMPARE   ZERO,WORKOCC            * test if field is zero before
          GOTO      TOTL0070 IF EQUAL         dividing
.
          DIV       SBNOMBDS,WORKOCC
.
TOTL0070  MOVE      WORKOCC,SBOCC
          MOVE      "Unknown",UNTDESC
.
          MATCH     SP3,CURRCLAS            * skip if no current ward 
          GOTO      TOTL1000 IF EQUAL         classification
.
.------ Find the ward classification description ------
.
          PACK      KEY5,CODECG,CURRCLAS
          CALL      RDCODE1                 * read the patient codes file
          BRANCH    OVRCD,TOTL1000
.
          MOVE      TDESC,UNTDESC
.
.------ print an underline ------
.
TOTL1000  CALL      PGE0000
.
          COMPARE   "57",CLNO
          CALL      HEAD0000 IF NOT LESS    * paginate if required
.
          PRINT     *1,"|Tot:",UNTDESC,*19,"|":
                    *20,SBNOMBDS,*26,SBOCC,*35,"|",*36,SBEMERG,*43,SBELECT:
                    *51,SBDISCH,*59,SBDIED,*67,"|",*68,SBTOTBDY:
                    *78,SBTOTPUB,*86,SBTOTPRV,*94,SBAVDOCC,*103,"|":
                    *104,SBLSR1,*111,SBLSR2,*118,SBLSR3,*125,SBLSR4,*132,"|"
          ADD       ONE,CLNO
.
          CALL      DPGE0000                * print double underline
.
          CALL      CLRT5000                * clear the sub totals
.
TOTL9999  RETURN
+
.**********************************************************************
.*                              NOMB0000                              *
.*     Get the nominal beds for the current ward classification       *
.**********************************************************************
.
NOMB0000  MOVE      SP6,KEY6
          CALL      RDSWARD1                * position on the ward file
NOMB1000  CALL      RDKWARD1
          BRANCH    OVRCD,NOMB9999
.
NOMB2000  MATCH     SP3,WBED                     * ward header record ?
          IF        @EQUAL
            MATCH     CURRCLAS,WCLASS            * yes - valid classification ?
            IF        @EQUAL
              MOVE      ONE,ACTVWFLG             * don't want weekend days
              PROC      WDACTVDY                 * yes - get days ward active
              COMPARE   ZERO,ACTVDAYS            * ward active during period ?
              GOTO      NOMB4000 IF EQUAL        * no - ignore
.
              ADD       TOTBED,SBNOMBDS          * update bed total
            ENDIF
          ENDIF
.
.         Position on next ward header record
.
NOMB4000  PACK        KEY6,WWARD,ZZZ
          CALL        RDSWARD1
          GOTO        NOMB1000
.
NOMB9999  RETURN
+
.**********************************************************************
.*                         FIND0000                                   *
.*                   Find the end of the doctors name                 *
.**********************************************************************
FIND0000  CMATCH    SP1,DOCDESC
          GOTO      FIND1000 IF EQUAL
          BUMP      DOCDESC,1
          GOTO      FIND0000 IF NOT EOS
          GOTO      FIND9999 
.
.------ add an extra space to the end of the patients name ------
.
FIND1000  BUMP      DOCDESC,1
          GOTO      FIND9999 IF EOS
          CMATCH    SP1,DOCDESC
          GOTO      FIND9999 IF EQUAL
          GOTO      FIND1000
.
FIND9999  RETURN
+
.**********************************************************************
.*                         HEAD0000                                   *
.*                      Print Headings                                *
.**********************************************************************
HEAD0000  COMPARE   ZERO,CPAGENO            * only print underline if we have
          CALL      PGE0000 IF NOT EQUAL      data
.
          CALL      HEAD132                 * print the page header
.
          PRINT     *40,"From Period : ",FROMPER:
                    " To Period : ",TOPER:
                    *N
.
          CALL      PGE0000                 * print the underline
.
          PRINT     *1,"|",*5,"Unit",*19,"|",*22,"Nom.",*31,"%",*35,"|":
                    *39,"Admitted",*52,"Disch",*61,"Died",*67,"|":
                    *68,"Occupied   Tot STD  Tot PRV  Av.Dly",*103,"|":
                    *106,"Long Stayers in Hospital",*132,"|":
                    *N,*1,"|",*19,"|",*22,"Beds",*30,"Occ.",*35,"|":
                    *37,"Emerg",*45,"Elect",*67,"|":
                    *68,"Bed Days   Bed Days Bed Days  Occ.",*103,"|":
                    *105,"30-60 61-120 121-180   >180",*132,"|"
.
          CALL      PGE0000                 * print the underline
          MOVE      NINE,CLNO
.
HEAD9999  RETURN
+
.**********************************************************************
.*                         PGE0000                                    *
.*                        Print Line                                  *
.**********************************************************************
PGE0000   PRINT     *1,"*--------------------------------------------------":
                    "---------------------------------------------------":
                    "-----------------------------*"
          ADD       ONE,CLNO
.
PGE9999   RETURN
+
.**********************************************************************
.*                         DPGE0000                                   *
.*                    Print Double Line                               *
.**********************************************************************
DPGE0000  PRINT     *1,"*==================================================":
                    "===================================================":
                    "=============================*"
          ADD       ONE,CLNO
.
DPGE9999  RETURN
+
.**********************************************************************
.*                         CREA0000                                   *
.*                    Create Temporary File                           *
.**********************************************************************
CREA0000  DISPLAY   *P1:24,*EL,*P18:24,*V2LON,"** Creating temp index file - ":
                           *BLINKON,"Please wait",*HOFF,*V2LON," **";
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,STATMP
.
          CALL      KILL0000                * kill the temp file if it already
.                                             exists
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    STATMP,CMDLINE
          APPEND    " 50 u1-3,4-9",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      STATMPXX,STATMP
.
CREA9999  RETURN
+
.**********************************************************************
.*                         KILL0000                                   *
.*                    Remove Temporary File                           *
.**********************************************************************
KILL0000  CLOSE     STATMPXX
          PACK      CMDLINE,SP30,SP30
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    STATMP,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
KILL9999  RETURN
+
.
.------ I/O routines for the temp file ------
.
RDTMP1    MOVE      ZERO,OVRCD
          RESET     KEY9
          READ      STATMPXX,KEY9;TMPWRDCL,TMPDOCT,TMPEMERG:
                                  TMPELEC,TMPDSCH,TMPDEAD,TMPPRIV,TMPPUBL:
                                  TMPLSR1,TMPLSR2,TMPLSR3,TMPLSR4 
          GOTO      OVERCOND IF OVER
          RETURN
.
RDATMP1   MOVE      ZERO,OVRCD
          RESET     KEY9
          READ      STATMPXX,KEY9;ANS
          RETURN
.
RDSTMP1   RESET     KEY9
          READ      STATMPXX,KEY9;;
          RETURN
.
RDKTMP1   MOVE      ZERO,OVRCD
          RESET     KEY9
          READKS    STATMPXX;TMPWRDCL,TMPDOCT,TMPEMERG,TMPELEC:
                             TMPDSCH,TMPDEAD,TMPPRIV,TMPPUBL,TMPLSR1,TMPLSR2:
                             TMPLSR3,TMPLSR4 
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTMP1    UPDATE    STATMPXX;TMPWRDCL,TMPDOCT,TMPEMERG,TMPELEC:
                             TMPDSCH,TMPDEAD,TMPPRIV,TMPPUBL,TMPLSR1,TMPLSR2:
                             TMPLSR3,TMPLSR4 
          RETURN
.
WRTMP1    RESET     KEY9
          WRITE     STATMPXX,KEY9;KEY9,TMPEMERG,TMPELEC,TMPDSCH,TMPDEAD:
                                  TMPPRIV,TMPPUBL,TMPLSR1,TMPLSR2,TMPLSR3:
                                  TMPLSR4 
          GOTO      OVERCOND IF OVER
          RETURN
+
.********************************************************************** 
.*              I/O INCLUDES FOR FILES AND GENERAL ROUTINES           * 
.**********************************************************************
.
          INCLUDE   STD001IO
          INCLUDE   CALCDAYS 
          INC       DATECONV
          INCLUDE   PATCOMN3
          INC       WDACTVDY
          INC       TFILENAM                     * Generate Temp File Name
.
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INCLUDE   PATC2SIO/INC       * clinical statistics 2 file
          INCLUDE   PATDRGIO/INC       * period date range file
          INC       PATCODIO/INC       * codes file
          INC       PATDO1IO/INC       * doctors file
          INC       PATWR1IO/INC       * ward file
          INCLUDE   PATWBHIO/INC       * ward bed history file
          INC       WEBERRIO/INC                 * Web Server Error Logging

.******************************************************************************
.*              P R O G R A M  S U M M A R Y                                  *
.*              -------------  -------------                                  *
.*                                                                            *
.*     PROGRAM NAME:    IBAEMR28                                              *
.*                                                                            *
.*     FUNCTION:        EMR Waiting Times Summary                             *
.*                                                                            *
.*     DATE WRITTEN:    30/09/2010                                            *
.*                                                                            *
.*     AUTHOR:          Mike Laming                                           *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.*        V10.04.01 28/04/2014  Steve Armstrong  CAR 261630                   *
.*                  Mods for non-port based tempfile use.                     *
.******************************************************************************
.*        V10.01.01 16/02/2011  Mike Laming      CAR 230583                   *
.*                  Add "REP  ' 0',CURRDATE"                                  *
.*        V10.00.00 05/10/2010  Mike Laming      CAR 230583                   *
.*                  New program                                               *
.******************************************************************************
.
          INC       STD001FD
.
          INC       EMRVISFD/INC
          INC       EMRUNKFD/INC
          INC       IBASEQFD/INC
          INC       PMSHCPFD/INC          * HCP file
          INC       PATMA1FD/INC          * patient master file 
          INC       PATFN1FD/INC          * Health fund file 
          INC       PATCODFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
EMRTEMP1  IFILE SQL, FIXED=225, KEY="U1-50"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
TEMPKEY   DIM       50     50       1     Sequence key
TEMPURNO  DIM       8      8        51    U/R Number 
TEMPVISN  DIM       8      8        59    EMR Visit No.
TEMPPNAM  DIM       50     50       67    Patient Name
TEMPDATE  DIM       8      8        117   Visit Date (CCYYMMDD)
TEMPATIM  DIM       5      5        125   Arrival Time
TEMPSDAT  DIM       8      8        130   Date Seen
TEMPSTIM  DIM       5      5        138   Time Seen
TEMPTTIM  DIM       5      5        143   Triage Time
TEMPWAIT  DIM       5      5        148   Waiting Time 
TEMPDOCN  DIM       50     50       153   Doctor Name
TEMPTRIG  DIM       1      1        203   Triage Cat.
TEMPDSTA  DIM       3      3        204   Discharge Status (Cat."ED")
TEMPTCOL  DIM       1      1        207   
TEMPSPAR  DIM       17     17       208 
.End of Record                      225
.
. LOCAL VARIABLE DEFINITION
. --------------------------.
.
CDYSDAYS  FORM      6                       * For CALCDAYS
CDYSFDTE  DIM       8                       * For CALCDAYS
CDYSTDTE  DIM       8                       * For CALCDAYS
CDYSYEAR  FORM      2                       * For CALCDAYS
CFEETYP   FORM      1                       * used for PATFNDKY
CL1       DIM       5
CL2       DIM       5
CL3       DIM       5
CL4       DIM       5
CL5       DIM       5
CL6       DIM       5
CL7       DIM       5
CMDLINE   DIM       100
CURRDATE  DIM       8
DIM2A     DIM       2
DIM2B     DIM       2
DIM4      DIM       4
DIM25A    DIM       25
DIM23A    DIM       23
DIM50A    DIM       50
FORM2A    FORM      2
FORM2B    FORM      2
FORM5     FORM      5
FORM6     FORM      6
FROMDATE  DIM       8                       * FROMdate ccyymmdd
HFUNDCOD  DIM       6
HFUNDNAM  DIM       30
TRIGNO    FORM      4[9][7]
TTOTAL    FORM      4[7]
LOSHRS    DIM       2
LOSMIN    DIM       2
SEENMINS  FORM      6
MAXMINS   FORM      6
STRTMINS  FORM      6
TEMPFILE  DIM       8
TODATE    DIM       8                       * TOdate ccyymmdd
TOTHRS    FORM      4[5]
TOTPATS   FORM      5
WAITMINS  FORM      5
.
. CONSTANT DEFINITION
. -------------------
CATAA     INIT      "AA"
UKEY      INIT      " 225 U1-50"            * set up for record length & key
SEQ1      INIT      " - U/R Sequence"
SEQ2      INIT      " - Visit No. Sequence"
SEQ3      INIT      " - Patient Sequence"
SEQ4      INIT      " - Visit Date Sequence"
SEQ5      INIT      " - Doctor Sequence"
SEQ6      INIT      " - LoS Sequence"
SEQ7      INIT      " - Claim Type Sequence"
SEQ8      INIT      " - Category Sequence"
.
.
PRGID     INIT      "IBAEMR28"
PRGNAM    INIT      "EMR Waiting Times Summary"
VERSION   INIT      "V12.01.00"
.
.*****************************************************************************
.*                                MAIN0000                                   *
.*                       Controlling Logic (Mainline code)                   *
.*****************************************************************************
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN1000  CALL      OPTN0000               * get user options
          BRANCH    EXIT,MAIN9999
.
MAIN2000  CALL      OKCON000               * ok to continue?
          BRANCH    EXIT,MAIN1000
.
          CALL      CREA0000               * create temp file
          CALL      EXTR0000               * extract data
.
MAIN5000  CALL      PRNT0000               * print grand total
.
MAIN9999  CALL      KILL0000               * delete temp file
          CHAIN     PGM
+
.
.*****************************************************************************
.*                                INIT0000                                   *
.*                             Initialization                                *
.*****************************************************************************
INIT0000  CALL      DISPHEAD
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          DISPLAY   *P64:24,"emrvisaf";
          OPEN      EMRVISA4,"emrvisaf"
.
          DISPLAY   *P64:24,"emrunkaf";
          OPEN      EMRUNKA1,"emrunkaf"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patfn1af";
          OPEN      PATFN1A1,"patfn1af"
.
          DISPLAY   *P64:24,"pmshcpaf";
          OPEN      PMSHCPA1,"pmshcpaf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
...       DISPLAY   *P64:24,"patdo1af";
...       OPEN      PATDO1A1,"patdo1af"
...       OPEN      PATDO1A2,"patdo1af"
.
INIT9999  RETURN
.
.*****************************************************************************
.*                                OPTN0000                                   *
.*                              User Options                                 *
.*****************************************************************************
OPTN0000  MOVE      ZERO,EXIT 
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Print Waiting Times Summary":
                    *P1:7,"Select Option : ";
.
OPTN1000  KEYIN     *P17:7,*DV,UNDLN1,*P17:7,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9000 IF EQUAL
.
          BRANCH    OPTION,OPTN2000
          BEEP
          GOTO      OPTN1000
.
OPTN2000  CALL      KDAT0000                * Key in date range
          BRANCH    EXIT,OPTN0000
.
          GOTO      OPTN9999
.
OPTN9000  MOVE      ONE,EXIT
.
OPTN9999  RETURN
.
.******************************************************************************
.*                                KDAT0000                                    *
.*                          Keyin range of dates                              *
.******************************************************************************
KDAT0000  MOVE      ZERO,EXIT
          MOVE      "19",CCOL
          MOVE      "10",CVERT
          UNPACK    SP70,CCENT,CYEAR,CMON,CDAY
.
          DISPLAY   *P1:10,*EL,"From Date       : ":
                    *P1:11,*EL,"  To Date       : ";
          CALL      KEYDATE
          BRANCH    OVRCD,KDAT9000
.
          PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",FROMDATE
.
          MOVE      "19",CCOL
          MOVE      "11",CVERT
          UNPACK    SP70,CCENT,CYEAR,CMON,CDAY
.
          CALL      KEYDATE
          BRANCH    OVRCD,KDAT0000
.
          PACK      TODATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE
.
          MATCH     CURRDATE,TODATE
          IF        !@LESS
            MOVE      CURRDATE,TODATE
          ENDIF
.
          MATCH     FROMDATE,TODATE
          GOTO      KDAT8000 IF LESS
.
          MOVE      ZERO,EXIT
          GOTO      KDAT9999
.
KDAT8000  DISPLAY   *P43:24,*B,*V2LON:
                    "** FROM Date Greater Than TO Date **",*W2,*P1:24,*EL;
          GOTO      KDAT0000
.
KDAT9000  MOVE      ONE,EXIT
.
KDAT9999  RETURN
.
.*****************************************************************************
.*                                OKCON000                                   *
.*                          Get User Confirmation                            *
.*****************************************************************************
OKCON000  KEYIN     *P1:24,*EL,"Ok to Continue (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF:
                    ") ? ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          CMATCH    ANSN,ANS
          GOTO      OKCON200 IF EQUAL
.
          CMATCH    ANSY,ANS
          GOTO      OKCON100 IF EQUAL
.
          BEEP
          GOTO      OKCON000
.
OKCON100  MOVE      ZERO,EXIT
          GOTO      OKCON999
.
OKCON200  MOVE      ONE,EXIT
.
OKCON999  RETURN
.
.*****************************************************************************
.*                                EXTR0000                                   *
.*                         Extract EMR Visit data                            *
.*****************************************************************************
EXTR0000  MOVE      ZERO,EXIT
          MOVE      ZERO,F1
          REPEAT
            ADD       ONE,F1
            MOVE      ZERO,F2
            REPEAT
              ADD       ONE,F2
              MOVE      ZERO,TRIGNO[F1][F2]
            UNTIL     F2 = 7
            IF        F1 < 8
              MOVE      ZERO,TTOTAL[F1]
            ENDIF
          UNTIL     F1 = 9
.
          PACK      KEY24,FROMDATE,SP70
          CALL      RSEMVIS4
EXTR1000  CALL      RKEMVIS4
          BRANCH    OVRCD,EXTR9999
.
          MATCH     "4",DEMVISTA            * bypass "Cancelled"         
          GOTO      EXTR1000 IF EQUAL
.
          MATCH     EMVIDATE,TODATE         * check that Date is in range
          GOTO      EXTR9999 IF LESS
.
          MATCH     SP6,EMVIDRDT            * check is seen by Doctor
          IF        @EQUAL
            MATCH     SP6,EMVINSDT          * seen by Nurse
            IF        @EQUAL
              MATCH     "1",DEMVISTA        * is patient still current?
              GOTO      EXTR1000 IF NOT EQUAL
            ELSE
              MOVE      EMVINSDT,EMVIDRDT   * move Nurse Date/Time to Doctor
              MOVE      EMVINSTM,EMVIDRTM
            ENDIF
          ENDIF
.
EXTR2000  CALL      FNAM0000                * format Patient name
          CALL      FDOC0000                * format Doctor name
          CALL      FWAIT000                * calc & format Waiting Times
.
          MOVE      EMVIURNO,TEMPURNO
          MOVE      DEMVIADM,TEMPVISN
          PACK      DIM50A,TEMPPNAM,SP70
          MOVE      DIM50A,TEMPPNAM
          MOVE      EMVIDATE,TEMPDATE       * Arrival date
          MOVE      EMVITIME,TEMPATIM       * Arrival time
          MOVE      EMVIDRDT,TEMPSDAT       * Date seen 
          MOVE      EMVIDRTM,TEMPSTIM       * Time seen
          MOVE      EMVITRTM,TEMPTTIM       * Triage time
          MOVE      MAXMINS,FORM5
          MOVE      FORM5,TEMPWAIT          * Waiting Time in minutes
.
EXTR3000  MOVE      "0",TEMPTRIG
          PACK      KEY5,CATAA,EMVITRIG,SP10
          CALL      RDCODE1
          IF        OVRCD = 0
            PACK      TEMPTRIG,TCINDC1,SP2
          ENDIF
.
          PACK      DIM50A,TEMPDOCN,SP70
          MOVE      DIM50A,TEMPDOCN
          MOVE      EMVIDSTA,TEMPDSTA
          PACK      TEMPSPAR,SP70
.
.                                           * create Waiting Times totals
EXTR4000  SQUEEZE   TEMPTRIG
          MOVE      ZERO,F1
          MOVE      TEMPTRIG,F1
          IF        F1 = 0
            MOVE      "8",F1                * fake Triage = 0
          ENDIF
.
          MOVE      SP1,TEMPTCOL            * Time band column
          MOVE      ZERO,WAITMINS
          MOVE      TEMPWAIT,WAITMINS
.
          ADD       ONE,TRIGNO[F1][7]            * count total
          ADD       ONE,TTOTAL[7]
.
          IF        WAITMINS < 11
            ADD       ONE,TRIGNO[F1][1]          * <= 10 mins
            ADD       ONE,TTOTAL[1]
            MOVE      "1",TEMPTCOL
          ELSE
            IF        WAITMINS < 31
              ADD       ONE,TRIGNO[F1][2]        * 11-30 mins
              ADD       ONE,TTOTAL[2]
              MOVE      "2",TEMPTCOL
            ELSE
              IF        WAITMINS < 60
                ADD       ONE,TRIGNO[F1][3]      * 31-59 mins
                ADD       ONE,TTOTAL[3]
                MOVE      "3",TEMPTCOL
              ELSE
                IF        WAITMINS < 120
                  ADD       ONE,TRIGNO[F1][4]    * 1 < 2 hrs  (1:00-1:59)
                  ADD       ONE,TTOTAL[4]
                  MOVE      "4",TEMPTCOL
                ELSE
                  IF        WAITMINS < 180
                    ADD       ONE,TRIGNO[F1][5]  * 2 < 3 hrs  (2:00-2:59)
                    ADD       ONE,TTOTAL[5]
                    MOVE      "5",TEMPTCOL
                  ELSE
                    ADD       ONE,TRIGNO[F1][6]  * 3 >        (3:00 on)
                    ADD       ONE,TTOTAL[6]
                    MOVE      "6",TEMPTCOL
                  ENDIF
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
.                                           * set up sort sequence
EXTR5000  PACK      TEMPKEY,TEMPTRIG,TEMPDATE,TEMPATIM,TEMPVISN,SP70
. 
          PACK      KEY50,TEMPKEY,SP70
          CALL      WRTEMP1
.
          GOTO      EXTR1000
.
EXTR9999  RETURN
.
.*****************************************************************************
.*                                FNAM0000                                   *
.*                    Read Master & format Patient Name                      *
.*****************************************************************************
FNAM0000  MOVE      ZERO,EXIT
          PACK      TEMPPNAM,SP70
.
          PACK      KEY8,EMVIURNO,SP70
          CALL      RDMAST1
          IF        OVRCD=1
            PACK      KEY8,EMVIADMN
            CALL      RDEMUNK1
            IF        OVRCD = 0
              UNPACK    SP70,PTITL,PSNAME,PGNAME
              MOVE      EMUNDET2,PGNAME
              MOVE      EMUNDET1,PSNAME
            ELSE
              MOVE      "Unknown Patient",TEMPPNAM
              GOTO      FNAM9999
            ENDIF
          ENDIF
.
          STRIP     PSNAME
          STRIP     PTITL
          STRIP     PGNAME
          CLEAR     TEMPPNAM
          APPEND    PSNAME,TEMPPNAM
          MOVE      ", ",DIM2
          APPEND    DIM2,TEMPPNAM
          APPEND    PTITL,TEMPPNAM
          APPEND    SP1,TEMPPNAM
          APPEND    PGNAME,TEMPPNAM
          RESET     TEMPPNAM
.
FNAM9999  RETURN
.
.*****************************************************************************
.*                                FDOC0000                                   *
.*                    Read Doctor file & format Doc.Name                     *
.*****************************************************************************
FDOC0000  MOVE      ZERO,EXIT
          PACK      TEMPDOCN,SP70
.
          PACK      KEY10,EMVIDOCT,SP70      * Attending Doctor
          CALL      RDPMHCP1
          IF        OVRCD=1
            MOVE      "Unknown Doctor ",KEY15    
            PACK      TEMPDOCN,KEY15,EMVIDOCT,SP70
            GOTO      FDOC9999
          ENDIF
.
          MATCH     SP5,PMHCINIT
          IF        @EQUAL
            MOVE      PMHCGNAM,KEY1
            MOVE      KEY1,PMHCINIT
          ENDIF
          STRIP     PMHCSNAM
          STRIP     PMHCTITL
          STRIP     PMHCINIT
          CLEAR     TEMPDOCN
          APPEND    PMHCSNAM,TEMPDOCN
          MOVE      ", ",DIM2
          APPEND    DIM2,TEMPDOCN
...       APPEND    PMHCTITL,TEMPDOCN
...       APPEND    SP1,TEMPDOCN
          APPEND    PMHCINIT,TEMPDOCN
          RESET     TEMPDOCN
.
FDOC9999  RETURN
.
.*****************************************************************************
.*                                FWAIT000                                   *
.*                         Calculate Waiting Times                           *
.*****************************************************************************
FWAIT000  MOVE      ZERO,EXIT
          MOVE      SP5,TEMPWAIT
          MATCH     SP8,EMVIDRDT            * is Doc Date/Time seen blank?
          IF        @EQUAL
            MOVE      CURRDATE,EMVIDRDT     * use Current Date/Time
            MOVE      CTIMEIS,EMVIDRTM
          ENDIF
.
          MOVE      EMVIDATE,CDYSFDTE       * Arrival date
          MOVE      EMVIDRDT,CDYSTDTE       * Seen by date
.
          CALL      CALCDAYS                * Calculate diff between dates
          SUB       ONE,CDYSDAYS            * We dont want inclusive dates
.
          MOVE      ZERO,FORM2A
          MOVE      ZERO,FORM2B
          UNPACK    EMVITIME,DIM2A,KEY1,DIM2B
          MOVE      DIM2A,FORM2A            * Arrive time hours
          MOVE      DIM2B,FORM2B            * Arrive time minutes
          ASSIGN    (FORM2A*60)+FORM2B,STRTMINS
.
          MOVE      ZERO,FORM2A
          MOVE      ZERO,FORM2B
          UNPACK    EMVIDRTM,DIM2A,KEY1,DIM2B
          MOVE      DIM2A,FORM2A            * Seen by time hours
          MOVE      DIM2B,FORM2B            * Seen by time minutes
          ASSIGN    (FORM2A*60)+FORM2B,SEENMINS
.
.                                           * calculate Waiting Time in mins
          ASSIGN    ((CDYSDAYS*1440)-STRTMINS+SEENMINS),MAXMINS
.
FWAIT999  RETURN
.
.*****************************************************************************
.*                                PRNT0000                                   *
.*                            Print the Report                               *
.*****************************************************************************
PRNT0000  MOVE      ZERO,EXIT
          MOVE      "99",CLNO
.
          PACK      KEY50,SP70
          CALL      RSTEMP1
PRNT1000  CALL      RKTEMP1
          BRANCH    OVRCD,PRNT3000
.
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          REP       "0 ",TEMPTRIG
          PACK      KEY3,SP1,TEMPTRIG,SP1
          MOVE      TEMPPNAM,DIM25A
          MOVE      TEMPDOCN,DIM23A
          UNPACK    TEMPDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          UNPACK    SP70,CL1,CL2,CL3,CL4,CL5,CL6
          MOVE      TEMPTCOL,F1
          MOVE      "  1  ",KEY5
          STORE     KEY5,F1,CL1,CL2,CL3,CL4,CL5,CL6
.
          PRINT     *1,"|",KEY3,"| ",DIM25A,"|",TEMPURNO,"| ",TEMPATIM:
                    " |",DIM23A,*73,"| ",TEMPTTIM," | ",TEMPSTIM," |":
                    CL1,"|",CL2,"|",CL3,"|",CL4,"|",CL5,"|",CL6,"| ":
                    TEMPDSTA,"  |"
.
          ADD       ONE,CLNO
          GOTO      PRNT1000
.
.
.                                           * print final totals
PRNT3000  CALL      PDOT0000
          COMPARE   "50",CLNO
          CALL      HEAD0000 IF NOT LESS
          ADD       "1",CLNO
.
          IF        TRIGNO[8][7] > 0
            MOVE      "8",F1                * print "Triage 0" counts
            CALL      PTOT0000
          ENDIF
.
          MOVE      ZERO,F1
          REPEAT
            ADD       "1",F1
            IF        F1 < 7 | F1 > 8
              CALL      PTOT0000
            ENDIF
          UNTIL     F1 = 9
.
          CALL      PDOT0000
          ADD       ONE,CLNO
.
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PRINT     *1,"Total Number of Patients        : ",TTOTAL[7]:
                    *89,"|",TTOTAL[1]," |",TTOTAL[2]," |",TTOTAL[3]:
                    " |",TTOTAL[4]," |",TTOTAL[5]," |",TTOTAL[6]," |"
          ADD       ONE,CLNO
.
PRNT9999  RETURN
.
.*****************************************************************************
.*                             Print Totals1                                 *
.*                                PTOT0000                                   *
.*****************************************************************************
PTOT0000  MOVE      ONE,CNOUNDLN
.
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
          MOVE      ZERO,F2
          UNPACK    SP70,CL1,CL2,CL3,CL4,CL5,CL6,CL7
          REPEAT
            ADD       ONE,F2
            MOVE      TRIGNO[F1][F2],FORM4
            IF        FORM4 > 0 | F2 = 7
              MOVE      FORM4,DIM4
              UNPACK    DIM4,DIM1,KEY3
              STORE     KEY3,F2,CL1,CL2,CL3,CL4,CL5,CL6,CL7
            ENDIF
          UNTIL     F2 = 7
.
          IF        F1 = 8
            PRINT     *1,"Number of Patients not Triaged  :  ",CL7:
                      *89,"|",CL1,"|",CL2,"|",CL3,"|",CL4,"|",CL5,"|",CL6:
                      "|"
          ELSE
            PRINT     *1,"Number of Triage ",F1," Patients     :  ",CL7:
                      *89,"|",CL1,"|",CL2,"|",CL3,"|",CL4,"|",CL5,"|",CL6:
                      "|"
          ENDIF
          ADD       ONE,CLNO
.
PTOT9999  RETURN
.
.*****************************************************************************
.*                             Print Heading                                 *
.*                                HEAD0000                                   *
.*****************************************************************************
HEAD0000  MOVE      ONE,CNOUNDLN
          CALL      HEAD132                 * Print the report header
.
          MOVE      "8",CLNO
.
          UNPACK    FROMDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *40,"From : ",CPCDATE;
.
          UNPACK    TODATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *60,"To : ",CPCDATE
.
          CALL      PDOT0000
          PRINT     *1,"|Tri| Patient Name ",*32,"| U/R    |Arrival|":
                    *50," ED Doctor",*73,"|Triage | Time  | <=10|11-30|":
                    *102,"31-59|1 < 2|2 < 3| > 3 |Depart|"
          PRINT     *1,"|Cat|",*32,"| Number | Time  |":
                    *73,"| Time  | Seen  | mins| mins| mins|":
                    *108,"hours|hours|hours|Status|"
          CALL      PDOT0000
.
HEAD9999  RETURN
.
.*****************************************************************************
.*                                PDOT0000                                   *
.*                          Print dotted lines                               *
.*****************************************************************************
.
PDOT0000  PRINT     *1,"*--------------------------------------------":
                    "--------------------------------------------":
                    "------------------------------------------*"
          ADD       ONE,CLNO
.
PDOT9999  RETURN
.
.
.******************************************************************************
.*                                CREA0000                                    *
.*                          Create a tempfile                                 *
.******************************************************************************
CREA0000  CALL      TFILENAM                     * get new tempfile name
          MOVE      TFILNAME,TEMPFILE
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      EMRTEMP1,TEMPFILE
          BRANCH    OVRCD,CREA1000
          TRAPCLR   IO
          CALL      CLER0000                * clear existing temp file
          GOTO      CREA9999
.
CREA1000  CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    TEMPFILE,CMDLINE
          APPEND    UKEY,CMDLINE
          APPEND    SP30,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
          OPEN      EMRTEMP1,TEMPFILE
.
CREA9999  RETURN
+
.******************************************************************************
.*                                CLER0000                                    *
.*                       Delete records from tempfile                         *
.******************************************************************************
CLER0000  TRAP      OVERCOND IF IO
          OPEN      EMRTEMP1,TEMPFILE
          BRANCH    OVRCD,CREA9999
          TRAPCLR   IO
.
CLER2000  MOVE      SP70,KEY50
          CALL      RSTEMP1
          CALL      RKTEMP1
          BRANCH    OVRCD,CLER9999
.
          PACK      KEY50,TEMPKEY,SP70
          CALL      DETEMP1
          GOTO      CLER2000
.
CLER9999  RETURN
+
.******************************************************************************
.*                                KILL0000                                    *
.*                          Clear up temp Tables                              *
.******************************************************************************
KILL0000  CLOSE     EMRTEMP1
          CALL      CLER0000
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    TEMPFILE,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
KILL9999  RETURN
+
.*****************************************************************************
.*                              TEMP File I/Os                               *
.*                                                                           *
.*****************************************************************************
RATEMP1   MOVE      ZERO,OVRCD
          RESET     KEY50
          READ      EMRTEMP1,KEY50;ANS
          GOTO      OVERCOND IF OVER
          RETURN                   
.                                  
RSTEMP1   MOVE      ZERO,OVRCD     
          RESET     KEY50          
          READ      EMRTEMP1,KEY50;;
          RETURN
.
RDTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY50
          READ      EMRTEMP1,KEY50;TEMPKEY,TEMPURNO,TEMPVISN,TEMPPNAM:
                                   TEMPDATE,TEMPATIM,TEMPSDAT,TEMPSTIM:
                                   TEMPTTIM,TEMPWAIT,TEMPDOCN,TEMPTRIG:
                                   TEMPDSTA,TEMPTCOL,TEMPSPAR
          GOTO      OVERCOND IF OVER
          RETURN

.
RKTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY50
          READKS    EMRTEMP1;TEMPKEY,TEMPURNO,TEMPVISN,TEMPPNAM:
                                   TEMPDATE,TEMPATIM,TEMPSDAT,TEMPSTIM:
                                   TEMPTTIM,TEMPWAIT,TEMPDOCN,TEMPTRIG:
                                   TEMPDSTA,TEMPTCOL,TEMPSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
RPTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY50
          READKP    EMRTEMP1;TEMPKEY,TEMPURNO,TEMPVISN,TEMPPNAM:
                                   TEMPDATE,TEMPATIM,TEMPSDAT,TEMPSTIM:
                                   TEMPTTIM,TEMPWAIT,TEMPDOCN,TEMPTRIG:
                                   TEMPDSTA,TEMPTCOL,TEMPSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY50
          WRITE     EMRTEMP1,KEY50;TEMPKEY,TEMPURNO,TEMPVISN,TEMPPNAM:
                                   TEMPDATE,TEMPATIM,TEMPSDAT,TEMPSTIM:
                                   TEMPTTIM,TEMPWAIT,TEMPDOCN,TEMPTRIG:
                                   TEMPDSTA,TEMPTCOL,TEMPSPAR
          RETURN
.
UPTEMP1   UPDATE    EMRTEMP1;TEMPKEY,TEMPURNO,TEMPVISN,TEMPPNAM:
                                   TEMPDATE,TEMPATIM,TEMPSDAT,TEMPSTIM:
                                   TEMPTTIM,TEMPWAIT,TEMPDOCN,TEMPTRIG:
                                   TEMPDSTA,TEMPTCOL,TEMPSPAR
          RETURN
.
DETEMP1   RESET     KEY50
          DELETE    EMRTEMP1,KEY50
          RETURN
+
.*****************************************************************************
.*                             I/O's Includes                                *
.*                                                                           *
.*****************************************************************************
.
          INC       STD001IO
          INC       PATFNDKY                * Keyin a health fund
          INC       CALCDAYS                * Calculate diff between dates
          INC       TFILENAM
.
          INC       EMRVISIO/INC
          INC       EMRUNKIO/INC
          INC       IBASEQIO/INC
...       INC       PATDO1IO/INC            * doctor table
          INC       PATMA1IO/INC
          INC       PATFN1IO/INC
          INC       PATCODIO/INC
          INC       PMSHCPIO/INC            * HCP table
          INC       WEBERRIO/INC
.
.

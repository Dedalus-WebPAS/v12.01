.***************************************************************************
.* System    :   Patient Management System                                 *
.* Program   :   FXPTADTH                                                  *
.***************************************************************************
.* Date      :   23/01/2006                                                *
.* Author    :   Peter Vela                                                *
.* Mods      :                                                             *
.*              V9.05.B00 23/01/2006 Peter Vela CAR 81907                  *
.*                        Created program - Update patient at death        *
.***************************************************************************
.
          INC       STD001FD
.
.
. FILE DESCRIPTION INCLUDES
. -------------------------
.
.         INC       HL7BMTFD/INC
          INC       PATMA1FD/INC
          INC       PATCALAG/INC
          INC       PATDHEAD/INC
          INC       PATCONFD/INC
.
BJDAY     FORM      3
CJDAY     FORM      3
.
PRGID     INIT      "FXPTADTH"
PRGNAM    INIT      "Update patient age at death"
VERSION   INIT      "V12.01.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select FROM and TO
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
.
          CALL      PROC0000               * process records for update
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                     * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"          * patient master file
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"          * patient master file
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Valid option selected                  *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Update deceased patients' age"
.
          DISPLAY   *P1:11,*EF,*V2LON,"WARNING: PLEASE BACKUP patmastf BEFORE RUNNING THIS PROGRAM."
.
OPTN1000  KEYIN     *P1:7,*EL,"Select option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION
          GOTO      OPTN9500 IF EQUAL                 * exit
.
          BRANCH    COPTION,OPTN2000
.
          BEEP
          GOTO      OPTN1000
.
OPTN2000  CALL      CONTQST                          * Ok to continue ?
          BRANCH    CEXIT,OPTN9000,OPTN1000,OPTN9500 * Yes, no, cancel
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.****************************************************************************
.*                                PROC0000            Called by: ML0000     *
.*              read through master file                                    *
.****************************************************************************
PROC0000  DISPLAY   *P1:24,*EL,"Processing U/R Number: ";
          MOVE      SP10,KEY8
          CALL      RDSMAST1                     * position at start of file
PROC1000  CALL      RDKMAST1                     * read next record
          BRANCH    OVRCD,PROC9000               * end of file
.
          DISPLAY   *P30:24,*EL,*V2LON,PURNO;
.
          COMPARE   ONE,PCEASE
          GOTO      PROC1000 IF NOT EQUAL 
.
          MATCH     SP70,PDECDTE
          GOTO      PROC1000 IF EQUAL
.
          MOVE      PDECDTE,AGEDATE
          REP       " 0",AGEDATE
.
          CALL      CALCAGE
.
          COMPARE   PSAGEYRS,PSAGE
          GOTO      PROC1000 IF EQUAL
.
          MOVE      PSAGEYRS,PSAGE
.
          CALL      UPMAST1
          GOTO      PROC1000
.
PROC9000  DISPLAY   *P1:24,*EL,"Processing complete.  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       CALCAGE
.          INC       HL7BMTMA
.          INC       HL7BMTIO/INC
          INC       PATMA1IO/INC

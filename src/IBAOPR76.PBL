.***************************************************************************
.* Program       : IBAOPR76                                                *
.* Desc          : Theatre Upload Program                                  *
.***************************************************************************
.* Date          : 11/09/2001                                              *
.* Author        : Pat Dirito                                              *
.* Modifications :                                                         *
.***************************************************************************
. V12.00.02 08/08/2025  Ebon Clements  TSK 0964891
.           Fixed wattx1af read for ASA score - WESE0000
. V12.00.01 28/05/2025 Thanh T         TSK 0955096    
.           Changed for alphanumeric visit number    
.***************************************************************************
. V11.05.02 18.03.2025  DA Sarkies    Task 0955909     
.           Updated the ESIS extract to add the ASAS code  
. V11.05.01 29/11/2024  Thanh T        TSK 0939466
.           Added Distance in kms field
.----------------------------------------------------------------------------
. V11.04.02 23/09/2024 J.Tan           TSK 0950577
.           Recompiled for PATIPERH - Fixed Hold Invoice audit issue 
. V11.04.01 11/12/2023  Ebon Clements    TSK 0941016
.           Allow 7 and 8 character U/R numbers. Remove leading zero - SETLOD00
. V11.03.02 10/08/2023  Nikitha B        TSK 0935669
.                       Added WTEEMANU,WTEEPTWT,WTEESGID,
.                       WTEERADT and WTEETCMP at CLRWTESE
. V11.03.01 05/04/2023  Bella Turco      TSK 0911166
.                       Recompiled for PATIPERH - Hold Invoice Audit
. V11.02.01 10/02/2022  Thanh T          TSK 0889899
.                       Recompiled as WATTR1FD changes
. V11.01.02 09/11/2021  J.Tan           TSK 0880410
.                       Recompiled for PATIPERH - added Hospital and System
. V11.01.01 18/02/2021  J.Tan           TSK 0888639
.                       Added PMMITMNO - Theatre Team no
.                       Increased MBS counter to DIM3
. V11.00.02 22/10/2020  J.Tan           TSK 0898866
.                       Recompiled for PATIPERH - on hold invoice
. V11.00.01 12/03/2020  J.Tan          TSK 0838262
.                       Added Effective from and to date to MBS Item file
. V10.13.03 23/10/2018  Tracey Nguyen    TSK 0859743
.                       Added PMMIHOSI - Hopsital Indicator on PMSMTIFD
. V10.13.02 19/09/2018  J.Tan            TSK 0863727
.                       Added Restrictive Override code
. V10.13.01 21/08/2017  J.Tan        TSK 0859742
.                       Added ECLIPSE new fields from pmsmtiaf 
. V10.11.01 31/08/2017  J.Tan            TSK 0837479
.                       Added PMMICTYP - Consumption Type
. V10.05.02 16/02/2015  J.Tan       CAR 311352
.                       Mods to write to Invoice Pending file
. V10.05.01 04/08/2014  Peter Vela  CAR 302164
.                       Added PMMIRBRS - Rebate Reason CAT Rr
. V10.04.02 23/06/2014  J.Tan       CAR 261630
.                       Removed port number from temp file name
. V10.04.01 17/01/2014  J.Tan        CAR 295008
.                       Mods to set PMMIDTCR,PMMITMCR,PMMIIDCR
.           20/01/2014  J.Tan        CAR 249828 
.                       Added PMMIPMBS - Patient MBS Team and Counter 
. V10.03.04 02/12/2013  Ebon Clements  CAR 293571
.                       Re read previous transfer record (if adding 2 min)
.                       prior to write of new tran record  WRTTRN00
. V10.03.03 05/09/2012  J.Tan          CAR 267784
.                       Mods Check for TCINDC19 Claim Contract Eff.'Disc.From'
. V10.03.02 23/08/2012  J.Tan         CAR 270279
.                       Mods to PACK MISCODE with MISCODE,SP70
. V10.03.01 23/08/2012  J.Tan         CAR 270279
.                       Fixed KEY29 for reading Misc.Charges
. V10.01.04 16/03/2011  J.Tan         CAR 233924
.                      Mods to update claim type on transfer file
. V10.01.03 16/12/2010  Mike Laming   CAR 233046
.                      Mods to Misc.Charges PATMCHFD - Key change, logic changes
. V10.01.02  01/12/2010 Peter Vela        CAR 233148
.                       Initialised pattranf variables to spaces before write
. V10.01.01  01/12/2010  J.Tan  CAR 233034
.                       Mods to Bed fees file to use HF type
. V10.00.02  18/08/2010 J.Tan         CAR 222031
.                       Mods to set PMMIDUPD and PMMITUPD
. V10.00.01  30/04/2010 J.Tan         CAR 220887
.                       Mods checking for Active/Inactive of Misc.charge
.  V9.12.02  19/10/2009 J.Tan         CAR 207359
.                       Fixed Not to update Transfer record if PTTREPSD=2 
.  V9.12.01  26/08/2009  Peter McMullen CAR 171901
.                       remove MAPCAT00 call
.***************************************************************************
.  V9.09.01  14/06/2007 Mike Laming   CAR 119158
.                       Changes to fix RDMCHG1 for Misc.Charges Effective Date
.  V9.08.02  07/05/2007 Peter Vela CAR SJOG ED Billing
.                       Initialised new pmsmtiaf variables
.  V9.08.01  08/03/2007 Jill Habasque CAR 119751
.                       Mods to pack SAVDATE in CTME0000
.  V9.04.06  29/11/2005 Ebon Clements CAR 86062
.                       Set manually added record to no for esis episode file
.  V9.04.05  07/06/2005 Jill Habasuqe  CAR 60165
.                       Added wteeardt
.  V9.04.04  11/04/2005 Mike Laming   CAR 60397
.                       Mods to use PATPOR HFPOR instead of tfpat1 tfhf1 f4.2
.  V9.04.03  11/01/2005 J Habasque CAR 56200
.                       Added update of WTEERREM
.  V9.04.02  10/12/2004 J Habasque CAR 54731
.                       Added write to wateseaf
.  V9.04.01  05/10/2004 Davin  CAR 53798  
.                       Changed program to use pthsdclm instead of ptcndclm
.  V9.03.05  11/06/2004 J.Tan  CAR 50035  
.                       Mods to set mechanical vent var from pmsmtiaf
.  V9.03.04  12/05/2003 J.Tan  CAR 49788
.                       Fixed getting primary MBS item
.  V9.03.03  29/10/2003 S.Litewka  CAR 44328
.                       Added clear of PTMMDESC field when writing new record
.                       to patmmbsf.
.  V9.03.02  22/10/2003 J.Tan  CAR 44172  
.                       Mods for pmsmtiaf file 
.  V9.03.01  12/09/2003 Lina Vo    CAR 40497
.                       Changed index and read of patmchgf.                  
.  V9.02.38  04/03/2002 J.Tan   SRF 34466
.                       Mods to check for prev.visit for multiple daycase visits
.  V9.02.37  17/12/2002 J.Tan  
.                       Fixed printing errors
.  V9.02.36  13/12/2002 J.Tan  
.                       Fixed printing errors
.  V9.02.35  22/11/2002 J.Tan  
.                       Fixed printing errors
.  V9.02.34  22/11/2002 J.Tan  
.                       Fixed deleting DTR records
.  V9.02.33  20/11/2002 J.Tan  
.                       Mods to check date/time before deleting dtr records
.  V9.02.32  12/11/2002 J.Tan  
.                       Fixed printing errors
.  V9.02.31  07/11/2002 J.Tan  
.                       Mods to ignore PMAC or RMH
.  V9.02.30  05/11/2002 J.Tan  
.                       Add items for current invoiced patients
.  V9.02.29  01/11/2002 J.Tan  
.                       Mods report 
.  V9.02.28  29/10/2002 J.Tan  
.                       Mods to print claim code and merged U/R
.  V9.02.27  28/10/2002 J.Tan  
.                       Mods to display admission number on report
.  V9.02.26  21/10/2002 J.Tan  
.                       Fixed problem with multiple adms/looping
.  V9.02.25  11/10/2002 J.Tan  19170
.                       Mods for automatic and manual upload
.  V9.02.24  17/09/2002 J.Tan  20635
.                       Mods not to check if invoice printed
.  V9.02.23  13/03/2002 J.Tan
.                       Mods to check the end day of first stepdown for 
.                       primary MBS
.  V9.02.22  06/03/2002 J.Tan
.                       Mods error messages
.  V9.02.21  06/03/2002 J.Tan
.                       Mods to check for blank suggested classification
.  V9.02.20  05/03/2002 J.Tan
.                       Mods to check end time of opr. against disc.time
.  V9.02.19  26/02/2002 J.Tan
.                       Mods not to use theatre fees for overseas
.                       Mods to check for same date for primary MBS
.  V9.02.18  25/02/2002 J.Tan
.                       Mods to use MBS amount for getting the primary MBS
.  V9.02.17  25/02/2002 J.Tan
.                       Mods to update transfer records from operation date
.                       with the suggested classification
.  V9.02.16  15/02/2002 J.Tan
.                       Mods to remove leading zero for MBS code
.  V9.02.15  14/02/2002 J.Tan
.                       Fixed error message invalid mbs code
.  V9.02.14  23/01/2002 J.Tan
.                       Added daycase suggested classification
.  V9.02.13  22/01/2002 Dean Cramer
.                       Set up for web front end                                
.  V9.02.12  16/01/2002 J.Tan
.                       Charge zero amount of MBS item for not overseas patients
.  V9.02.11  09/01/2002 J.Tan
.                       Fixed recalculating charging for transfer file
.  V9.02.10  07/01/2002 J.Tan
.                       Set time to adm.time if sugg.class starts from adms.
.  V9.02.09  20/12/2001 J.Tan
.                       Mods to read number of pre-op days from controlf
.  V9.02.08  07/12/2001 Tonii                                        
.                       Recompiled for PATDINVS- Casemix Mods     
.  V9.02.07  07.12.2001 J.Tan
.                       Mods to read prothesis/misc.code (002) record type
.  V9.02.06  29.11.2001 J.Tan
.                       Mods reading the indicator for charging using MBS amount
.  V9.02.05  16.11.2001 J.Tan
.                       Mods generating the U/R number
.  V9.02.04  02.11.2001 J.Tan
.                       Mods to check for overseas patients
.  V9.02.03  02.11.2001 J.Tan
.                       Mods not to charge theatre
.  V9.02.02  24.10.2001 J.Tan
.                       Mods to get primary mbs
.  V9.02.01  22.10.2001 Pat Dirito    
.                       Re-edited some code in.. 
.  V9.02.00  03.10.2001 Pat Dirito    
.----------------------------------------------------------------------
.
          INC       STD001FD
          INC       IBASEQFD/INC            * Sequential Numbers File
          INC       TFILEVAR/INC            * Generate Temp File Name
          INC       WEBERRFD/INC            * Web Server Error Logging
.
. -------------------------
. FILE DESCRIPTION INCLUDES
. -------------------------
.
          INC       BOKRC1FD/INC 
          INC       PATIPEFD/INC     * Invoice pending file
          INC       PATBFEFD/INC     * Bed Fees File
          INC       PATCODFD/INC     * Patent Codes Table
          INC       PATCONFD/INC     * Patient Control File
          INC       PATCFAFD/INC     * Claim Code Future Actions
          INC       PMSCIDFD/INC     * Contract ID File
          INC       PATDSCFD/INC     * Patient Discharge File
          INC       PATDTRFD/INC     * Debitor Transaction Table
          INC       PATDRGFD/INC     * Period Date Range File
          INC       PATFX1FD/INC     * PATIENT HEALTH FUND EXTENSION TABLE
          INC       PATFHIFD/INC     * HF Invoice on hold table
          INC       PATMI1FD/INC     * Admission File
          INC       PATMMBFD/INC     * Patient MBS Table
          INC       PATMA1FD/INC     * Patient Master File
          INC       PATITMFD/INC     * Patient Item File
          INC       PATTRNFD/INC     * Patient Transfer File
          INC       PATVISFD/INC     * Visit File
          INC       PATWR1FD/INC     * Ward/Bed File
          INC       PATRCAUD/INC     * PATIENT BILLING RATE CHANGE AUDIT FILE
          INC       PATDINVS/INC
          INC       PATFN1FD/INC
          INC       PATHSPFD/INC
          INC       PATTFEFD/INC
          INC       PATMCHFD/INC
          INC       PATONHFD/INC
          INC       PMSMTIFD/INC
          INC       PMSVX1FD/INC
          INC       OPRCONFD/INC
          INC       OPRUPLFD/INC     * Theatre upload history file
          INC       WATESEFD/INC
          INC       WATTR1FD/INC
          INC       WATTX1FD/INC
.
. -------------------------
. LOCAL VARIABLE DEFINITION
. -------------------------
UPLDFILE  FILE      ASCII,FIXED=256
UPLFILE   DIM       80
NEWFILE   DIM       80
NOFILE    FORM      1
.
PATPOR    FORM      12.2                                               *I-60397
HFPOR     FORM      12.2                                               *I-60397
ADDFLAG   FORM      1    * Variables for CTME0000 
ADMISSNO  DIM       8    * Admission Number
LSTADMNO  DIM       8    * Admission Number
ATYFLAG   DIM       1    * Admission type flag
.CURDATE   DIM       8
CLMCODE   DIM       3
COUNTCAR  FORM      2
CALDAYS   FORM      4
CONSLTNT  DIM       5    * Consultant
CATEGORY  DIM       2
CODE      DIM       3
CSTRTYR   DIM       8
CMDLINE   DIM       127
CURRDATE  DIM       8
DIM1A     DIM       1
DIM18     DIM       18
DIM18A    DIM       18
DIM127    DIM       127
DIM20A    DIM       20
DIM30     DIM       30
DIM55     DIM       55
DURATION  FORM      4 
DAILYCH   FORM      6.2
ENDSTR    FORM      2
ENDDATE   DIM       8
ERRNUMB   FORM      1
ENDTIME   FORM      4
FILEXTN   FORM      2
FILENAM   DIM       15
FILENM1   DIM       15
FORM4D    FORM      4
FORM5     FORM      5
FNAMEA    DIM       8
HUNDRED5  FORM      "105"
IBCNMHOS  FORM      1        * Multihospital indicator
IMPNAME   DIM       80
KEY4A     DIM       4
MBSCODE   DIM       9
MINICH    FORM      6.2
MINADD    FORM      2
LBTYPE    DIM       3
OPRNUMBR  DIM       10   * Admission Number 1 
OPSTARTD  DIM       8    * Operation Start Date
OPSTRTIM  DIM       4    * Operation Start Time
OPENDTIM  DIM       4    * Operation End Time
OPSTARTM  DIM       8    * Operation Start Time with format
OPRENDTM  DIM       8    * Operation End Time with format
.OPCNPODY  FORM      2
MISCODE   DIM       9    * Miscellaneous code
PATHNAM   DIM       40
PATHNM1   DIM       40
PROCODE   DIM       10   * Procedure Code   
PROQUANT  DIM       5    * Prothesis Quanty
RUNFLAG   FORM      1
RECTYPE   DIM       3    * Record Type 001,002
STARTSTR  FORM      2
SURGEON   DIM       5    * Surgeon
SUGGCLSS  DIM       3    * Suggested Class
STARTIME  FORM      4
SFORM4D   FORM      4    * End here 
SP14      INIT      "              "
THCFLG    FORM      2
TRANSNUM  FORM      6    * Transaction Number
TRNSFLAG  FORM      1    * Successful Update of Transfer File Flag
URNUMBER  DIM       8
UNITRECN  DIM       12   * Unit Record Number
USFILE    INIT      "ibaopr76.us1"
CODECL    INIT      "CL"
SKIPFLAG  FORM      1
.
.RECORD 001              Position     MBS Item
.RECTYPE   DIM       3      1         * Record Type "001"
.UNITRECN  DIM       12     4         * Unit Record Number
.OPRNUMBR  DIM       10     16        * Admission Number 1 
.OPSTARTD  DIM       8      26        * Operation Start Date
.DUMMY     DIM       8      34
.OPSTRTIM  DIM       4      42        * Operation Start Time
.OPENDTIM  DIM       4      46        * Operation End Time
.KEY1      DIM       1      50
.KEY4A     DIM       4      51        * for "PMAC" or "RMH"
.KEY9      DIM       9      55
.PROCODE   DIM      10      64        * Theatre/MBS Item code
.KEY4      DIM       4      74        
.KEY4      DIM       4      78
.SURGEON   DIM       5      88        * Surgeon
.
.RECORD 002              Position     * Miscellaneous item 
.RECTYPE   DIM       3      1         * Record Type "002"
.UNITRECN  DIM       12     4         * Unit Record Number
.OPRNUMBR  DIM       10     16        * Admission Number 1 
.OPSTARTD  DIM       8      26        * Operation Start Date
.DUMMY     DIM       8      34
.OPSTRTIM  DIM       4      42        * Operation Start Time
.OPENDTIM  DIM       4      46        * Operation End Time
.KEY1      DIM       1      50
.KEY4A     DIM       4      51        * for "PMAC" or "RMH"
.DUMMY     DIM       9      55
.MISCODE   DIM       9      64        * Miscellaneous code
.PROQUANT  DIM       5      73        * Prothesis Quanty
.DUMMY     DIM       10     78
.SURGEON   DIM       5      88        * Surgeon
.
.
TMPFILE   IFILE     SQL, FIXED=30, KEY="U1-12,13-20"
TMPKEY    INIT      " 30 U1-12,13-20"
.
.Name     Type    Length  Physical  Start   Description
.----     ----    ------  --------  -----   -----------
XUNITNO   DIM       12     12        1     Unit Record Number
XADMNO    DIM        8      8       13     Admission number
XSPARE    DIM        9      9       21     Spare
.End of Record                       30 
.
PRGID     INIT      "IBAOPR76"
PRGNAM    INIT      "Theatre Upload Program"
VERSION   INIT      "V12.01.00"
+
.****************************************************************************
.*                            MAIN0000                                      *
.*                      Controlling Logic (Mainline code)                   *
.****************************************************************************
MAIN0000  CALL      INIT0000
          CALL      DISPHEAD 
.
          CALL      OPTN0000
          BRANCH    EXIT,MAIN9999          * exit
.
          MOVE      OPCNPTUP,PATHNAM
          MOVE      OPCNFTUP,FILENAM
.
          BRANCH    COPTION,MAIN0100          * Automatic theatre upload
          GOTO      MAIN0150
.
MAIN0100  DISPLAY   *P1:10,"Path Name  : ",*P15:10,*V2LON,PATHNAM,*HOFF:
                    *P1:11,"File Name  : ",*P15:11,*V2LON,FILENAM,*HOFF
          GOTO      MAIN0200
.
.         Manual theatre upload will keyin path and file name
.
MAIN0150  PACK      KEY55,SP70
          PACK      PATHNM1,SP70
          PACK      FILENM1,SP70
.
          KEYIN     *P1:10,"Path and File Name  : ",*P23:10,*RV,*DV,KEY55:
                    *P23:10,*EL,*V2LON,KEY55 
          GOTO      MAIN9999 IF EOS    
          ENDSET    KEY55
.
          CALL      STPF0000                  * Set path and file name
          MATCH     SP10,FILENAM
          GOTO      MAIN0160 IF NOT EQUAL
          DISPLAY   *P1:24,*EL,*V2LON,*B,"File name must not be blank. ";
.         CALL      HITENTER 
          GOTO      MAIN0150
.
MAIN0160  PACK      DIM55,SP70
.
          KEYIN     *P1:13,"Heading File Name   : ",*P23:13,*RV,*DV,DIM55:
                    *P23:13,*EL,*V2LON,DIM55 
          GOTO      MAIN0170 IF EOS    
.
          GOTO      MAIN0200
.
MAIN0170  MOVE      KEY55,DIM55 
. 
MAIN0200  CALL      CONTQST       * Ok to continue ?
          BRANCH    CEXIT,MAIN1000,MAIN0000,MAIN9999   * Yes, No, Cancel
.
MAIN1000  CALL      CFIL0000      * check if theatre upload was done previously
          BRANCH    EXIT,MAIN0000
.
          CALL      PREP0000
          BRANCH    EXIT,MAIN9999
.
.
          CALL      LOAD0000      * Load data to tempfile
          CALL      READ0000      * Read text file & process information 
.
MAIN9999  CHAIN     PGM           * chain back to program
+
.****************************************************************************
.*                                INIT0000                                  *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
INIT0000  CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
          CALL      HEAD132
          OPEN      BOKRC1A1,"bokrc1af"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PATBFEE1,"patbfeef"
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATDTRA1,"patdtraf"
          OPEN      PATTRAN1,"pattranf"
          OPEN      PATTRAN2,"pattranf"
          OPEN      PATITEM1,"patitemn"
          OPEN      PATVISA1,"patvisaf"
          OPEN      PATVISA2,"patvisaf"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMMBS1,"patmmbsf"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATCODE2,"patcodes"
          OPEN      PATFN1A1,"patfn1af"
          OPEN      PATFX1A1,"patfx1af"
          OPEN      PATCFAA1,"patcfaaf"
.
          OPEN      PATHSPA1,"pathspaf"
          OPEN      PATTFEE1,"pattfeef"
          OPEN      PATMCHG1,"patmchgf"
          OPEN      PATWR1A1,"patwr1af"
          OPEN      OPRUPLA1,"opruplaf"
          OPEN      PMSMTIA1,"pmsmtiaf"
          OPEN      PMSMTIA2,"pmsmtiaf"
          OPEN      WATESEA1,"wateseaf"
          OPEN      WATTR1A1,"wattr1af"
          OPEN      WATTX1A1,"wattx1af"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      PATIPEN1,"patipenf"
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TEN;*217,CAUDQ
          READ      CONTROLF,TEN5;*247,CSTDOWN
          READ      CONTROLF,TEN9;*212,CFEETYP
          READ      CONTROLF,TWENTY;*193,PTCNDCLM,*211,PTCNCNDY
          READ      CONTROLF,TWENTY1;*120,PTCNADDC
          READ      CONTROLF,FORTY2;*138,OPCNPODY:
                                    *140,OPCNPTUP,OPCNFTUP,OPCNBRUP
          READ      CONTROLF,SEVENTY9;*248,PTCNIPEN
          READ      CONTROLF,HUNDRED5;*130,PTCNAUAT,PTCNWSPC,PTCNADB1,PTCNADB2:
                                           PTCNADB3,PTCNADB4,PTCNDFAN
.                                     
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,FNAMEA
.
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          BRANCH    CAUDQ,INIT9999
          OPEN      PATRCAUD,"patrcaud"
.
INIT9999  RETURN
+
.**************************************************************************
.*                            OPTN0000                                     
.**************************************************************************
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Automatic Theatre Upload":
                    *P1:6,*V2LON,TWO,*HOFF,". Manual Theatre Upload"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000,OPTN9000    
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.**************************************************************************
.*                               CFIL0000                                 *
.**************************************************************************
CFIL0000  IF        COPTION=1
            PRINT     *20,"Automatic Theatre Upload"
          ELSE
            PRINT     *20,"Manual Theatre Upload"
          ENDIF
.
          MOVE      ZERO,EXIT
          MOVE      ONE,FILEXTN
          PACK      CURDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURDATE
          MOVE      CURDATE,OPUPDATE
          PACK      KEY71,OPUPDATE,SP70
          CALL      RSOPUP1
CFIL1000  CALL      RKOPUP1
          BRANCH    OVRCD,CFIL1210
.
          MATCH     CURDATE,OPUPDATE
          GOTO      CFIL1210 IF NOT EQUAL
.
          UNPACK    OPUPDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,KEY10
.
          COMPARE   TWO,COPTION
          GOTO      CFIL1200 IF EQUAL     * manual
. 
          PACK      UPLFILE,SP70
          CLEAR     UPLFILE
          APPEND    OPCNBRUP,UPLFILE
          CALL      FEND0000
          APPEND    FILENAM,UPLFILE
          CALL      FEND0000
          APPEND    SP70,UPLFILE
          RESET     UPLFILE
          MATCH     UPLFILE,OPUPMANF
          GOTO      CFIL1000 IF NOT EQUAL
.
          ADD       ONE,FILEXTN           * File extension number
          GOTO      CFIL1000                  
.
CFIL1200  MATCH     DIM55,OPUPMANF
          GOTO      CFIL1000 IF NOT EQUAL
          ADD       ONE,FILEXTN           * File extension number
          GOTO      CFIL1000
.
CFIL1210  BRANCH    FILEXTN,CFIL9999
          GOTO      CFIL1300                  
.
.         Automate upload will always use system parameter file name, just
.         print warning on the report
.
CFIL1300  BRANCH    COPTION,CFIL2000
.
.         MOVE      SP1,ANS
.         DISPLAY   *P1:23,*EF,*B,"Warning: Theatre Upload had been run on ":
.                   *V2LON,KEY10,"  ",OPUPTIME,*HOFF;
.         GOTO      CFIL9999
.
CFIL2000  IF        OPUPTYPE=1
            PRINT     *1,"Warning: Automatic Theatre Upload had been run on ":
                         KEY10,"  ",OPUPTIME
          ELSE
            PRINT     *1,"Warning: Manual Theatre Upload had been run on ":
                         KEY10,"  ",OPUPTIME
          ENDIF
          GOTO      CFIL9999
.
CFIL9000  MOVE      ONE,EXIT
CFIL9999  RETURN
+
.**************************************************************************
.*                            PREP0000                                     
.**************************************************************************
PREP0000  MOVE      ZERO,EXIT 
          CALL      SFIL0000             * Setup path and file name
          MOVE      ZERO,RUNFLAG
          MOVE      ZERO,NOFILE
          MOVE      ZERO,OVRCD
.
          MATCH     SP3,UPLFILE
          GOTO      PREP9100 IF EQUAL
.
.         Check if file with date already exists
.
.         PACK      KEY6,CMM,CDD,CYY
.         REP       " 0",KEY6
.
.         CLEAR     NEWFILE
.         APPEND    UPLFILE,NEWFILE
.         CALL      FSTR0000
.         APPEND    KEY6,NEWFILE
.         APPEND    "1",NEWFILE
.         APPEND    SP70,NEWFILE
.         RESET     NEWFILE
.
.         TRAP      OVERCOND IF IO
.         OPEN      UPLDFILE,NEWFILE
.         TRAPCLR   IO
.         IF        OVRCD=0
.           PRINT     *N,*1,"File has been run multiple times. "
.           PRINT     *20,"The Copy File already exists: ",NEWFILE
.           MOVE      ONE,RUNFLAG      * flag for file with today date exists
.         ENDIF
.
.         Check if the actual file exists
.
PREP1000  STRIP     UPLFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      UPLDFILE,UPLFILE
          TRAPCLR   IO
          BRANCH    OVRCD,PREP9000
.
          PACK      DIM127,USFILE,SP1,UPLFILE,SP70,SP70
          EXECUTE   DIM127,TASKID
.
          IF        COPTION=1
             PRINT     *N,*1,"Upload file Name: ",UPLFILE
          ELSE
             PRINT     *N,*1,"Upload file Name: ",DIM55   
          ENDIF
          GOTO      PREP9900
.
.         If this is automate run, use filename with today's day and month
.         if this is manual, display message file does not exist
.
PREP9000  MOVE      ONE,NOFILE               * flag for keyin file not exists
          IF        COPTION=1
            PRINT     *N,"Upload file not found : ",UPLFILE
          ELSE
.           DISPLAY   *P1:23,*EF,"File does not exist ",DIM55:
.                                " The Copy file will be used. "
.           CALL      HITENTER
            PRINT     *N,"File does not exist ",DIM55 
          ENDIF
.
.         If the copy file exist then use the copy file
.
          COMPARE   ZERO,RUNFLAG
          GOTO      PREP9020 IF EQUAL      * the Copy file does not exist
          PRINT     *20,"The Copy file is used : ",NEWFILE
          OPEN      UPLDFILE,NEWFILE       * open file with date
          GOTO      PREP9900
.
PREP9020  MOVE      ONE,EXIT 
          GOTO      PREP9999 
.
PREP9100  PRINT     *N,"Leading spaces in file name",UPLFILE;
          MOVE      ONE,EXIT 
.
PREP9900  CALL      RNAM0000       * Rename file with MMDD at end of filename
PREP9999  RETURN
+
.**************************************************************************
.*                               LOAD0000                                 *
.*   Read in and save variables. The Process information and save it to   *
.*   the tempfile          
.**************************************************************************
LOAD0000  CALL      CTMP0000    * Create tempfile  
          PRINT     *1,"U/R",*10,"Adm.",*20,"Opr.Date",*35,"Start",*45,"End ":
                    *60,"Code",*75,"Qty",*82,"Claim",*88,"Description"
.
          MOVE      ZERO,SKIPFLAG
          OPEN      UPLDFILE,NEWFILE
.
LOAD0100  CALL      CLRVAR00    * Clear Variables   
          READ      UPLDFILE,SEQ;RECTYPE,KEY120
          GOTO      LOAD9999 IF OVER
.
          MATCH     "001",RECTYPE
          GOTO      LOAD1000 IF EQUAL 
          MATCH     "002",RECTYPE
          GOTO      LOAD2000 IF EQUAL 
          GOTO      LOAD0100     * Read next record just in case.     
.
LOAD1000  UNPACK    KEY120,UNITRECN,OPRNUMBR,OPSTARTD,KEY8,OPSTRTIM:
                           OPENDTIM,KEY1,KEY4A,KEY9,KEY5,PROCODE:          * 10
                           KEY4,KEY4,CONSLTNT,SURGEON                * 15 
          GOTO      LOAD3000
.
LOAD2000  UNPACK    KEY120,UNITRECN,OPRNUMBR,OPSTARTD,KEY8,OPSTRTIM:
                           OPENDTIM,KEY1,KEY4A,KEY9,MISCODE,PROQUANT:      * 10
                           KEY10,SURGEON                             * 15
          CALL      CMCD0000     * Check miscellaneous charge code
.
LOAD3000  MATCH     "PMAC",KEY4A
          GOTO      LOAD0100 IF EQUAL  * Ignore, read next record 
          MATCH     "RMH ",KEY4A
          GOTO      LOAD0100 IF EQUAL  * Ignore, read next record
.
          CALL      SETLOD00     * Set UR & Admission No's to 8 chars
          BRANCH    EXIT,LOAD0100
          CALL      VALDAT00      * Get admission no for the UR & Validate Data 
          BRANCH    EXIT,LOAD0100 * invalid data or invoice printed
.
.         Write U/R & admission to tempfile and remove current existing items
.
          MOVE      UNITRECN,XUNITNO
          MOVE      PVIBILL,XADMNO
          PACK      KEY20,XUNITNO,XADMNO
          CALL      RATEMP
          COMPARE   ZERO,OVRCD
          GOTO      LOAD4000 IF EQUAL     * already exists
.
          PACK      XSPARE,SP70
          CALL      WRTEMP       * write to tempfile for the U/R and admission
.
LOAD4000  CALL      DREC0000     * Delete items from pmsmtiaf and patmmbsf
          GOTO      LOAD0100     * next record
.
LOAD9999  RETURN
+
.**************************************************************************
.*                               READ0000                                 *
.*   Read in and save variables. The Process information and save it to   *
.*   the appropriate files.
.**************************************************************************
READ0000  OPEN      UPLDFILE,NEWFILE
          MOVE      ONE,SKIPFLAG
.
READ0100  CALL      CLRVAR00    * Clear Variables   
          READ      UPLDFILE,SEQ;RECTYPE,KEY120
          GOTO      READ9000 IF OVER
.
          MATCH     "001",RECTYPE
          GOTO      READ1000 IF EQUAL 
.
          MATCH     "002",RECTYPE
          GOTO      READ2000 IF EQUAL 
.
          GOTO      READ0100     * Read next record just in case.     
.
READ1000  UNPACK    KEY120,UNITRECN,OPRNUMBR,OPSTARTD,KEY8,OPSTRTIM:
                           OPENDTIM,KEY5,KEY9,KEY5,PROCODE:          * 10
                           KEY4,KEY4,CONSLTNT,SURGEON                * 15 
          GOTO      READ3000
.
READ2000  UNPACK    KEY120,UNITRECN,OPRNUMBR,OPSTARTD,KEY8,OPSTRTIM:
                           OPENDTIM,KEY5,KEY9,MISCODE,PROQUANT:      * 10
                           KEY10,SURGEON                             * 15
.
          CALL      CMCD0000     * Check miscellaneous charge code
.
READ3000  CALL      SETLOD00     * Set UR & Admission No's to 8 chars
          CALL      VALDAT00     * Get admission no for the UR & Validate Data 
.
.         Check if this admission has invoice printed.
.         if the admission number exits in tempfile, then it's ok to add item
.
          PACK      KEY20,UNITRECN,PVIBILL
          CALL      RATEMP
          BRANCH    OVRCD,READ0100   * item is rejected as inv already printed
.
          CALL      PROC0000     * process items 
          GOTO      READ0100     * Read next record just in case.     
.
READ9000  CALL      KTMP0000     * Kill tempfile
.
          PACK      OPUPDATE,CCC,CYY,CMM,CDD
          REP       " 0",OPUPDATE
          MOVE      CTIMEIS,OPUPTIME
.
          IF        COPTION=1
            MOVE      PATHNAM,OPUPPATH
            MOVE      FILENAM,OPUPFILE
.
.           MOVE      FILENAM,OPUPMANF
            PACK      OPUPMANF,SP70
            CLEAR     OPUPMANF
            APPEND    OPCNBRUP,OPUPMANF
            CALL      FMAN0000
            APPEND    FILENAM,OPUPMANF
            CALL      FMAN0000
            APPEND    SP70,OPUPMANF
            RESET     OPUPMANF
            MOVE      ONE,OPUPTYPE         * Automatic
.
          ELSE
            MOVE      PATHNM1,OPUPPATH
            MOVE      FILENM1,OPUPFILE
            MOVE      DIM55,OPUPMANF
            MOVE      ZERO,OPUPTYPE        * Manual
          ENDIF
.
          PACK      KEY71,OPUPDATE,OPUPTIME,OPUPPATH,OPUPFILE
          CALL      RAOPUP1
          IF        OVRCD=1
            CALL      WROPUP1
          ENDIF
.
READ9999  RETURN
+
.**************************************************************************
.*                                CLRVAR00                                *
.**************************************************************************
CLRVAR00  MOVE      SP70,UNITRECN
          MOVE      SP70,OPRNUMBR
          MOVE      SP70,OPSTARTD
          MOVE      SP70,OPSTRTIM
          MOVE      SP70,OPENDTIM
          MOVE      SP70,PROCODE
          MOVE      SP70,MISCODE
          MOVE      SP70,CONSLTNT
          MOVE      SP70,SURGEON
          MOVE      SP70,PROQUANT  
.
CLRVAR99  RETURN
.
.**************************************************************************
.*                                CMCD0000                                *
.**************************************************************************
CMCD0000  MOVE      MISCODE,KEY9
          MOVE      SP10,MISCODE
.
CMCD1000  CMATCH    "0",KEY9
          GOTO      CMCD2000 IF NOT EQUAL
          BUMP      KEY9
          GOTO      CMCD1000 IF NOT EOS
.
CMCD2000  MOVE      KEY9,MISCODE
          RESET     MISCODE
CMCD9999  RETURN
+
.**************************************************************************
.*                                STPF0000                                *
.*               Set path and file name from a string                     *
.**************************************************************************
STPF0000  MOVE      ZERO,EXIT
          MOVE      ONE,STARTSTR
          MOVE      ONE,ENDSTR
          RESET     PATHNM1,0
          RESET     FILENM1,0
.
STPF1000  CMATCH    "\",KEY55 
          GOTO      STPF2000 IF EQUAL
          CMATCH    "/",KEY55 
          GOTO      STPF2000 IF EQUAL
          BUMP      KEY55,-1
          GOTO      STPF1000 IF NOT EOS
          GOTO      STPF3000
.
STPF2000  MOVEFPTR  KEY55,ENDSTR
          COMPARE   STARTSTR,ENDSTR
          GOTO      STPF3000 IF NOT LESS
          MOVE      ONE,EXIT
          GOTO      STPF9999
.
STPF3000  RESET     KEY55,STARTSTR
          SETLPTR   KEY55,ENDSTR
          APPEND    KEY55,PATHNM1
          APPEND    SP70,PATHNM1
          RESET     PATHNM1
.
          ADD       ONE,ENDSTR
          RESET     KEY55,ENDSTR
          SETLPTR   KEY55,55
          APPEND    KEY55,FILENM1
          APPEND    SP70,FILENM1
          RESET     FILENM1
.
STPF9999  RETURN
+
.**************************************************************************
.*                                STBF0000                                *
.*               Set browser file name from archive                       *
.**************************************************************************
STBF0000  MOVE      ZERO,EXIT
          MOVE      ONE,STARTSTR
          MOVE      ONE,ENDSTR
          ENDSET    DIM55
.
STBF1000  CMATCH    "\",DIM55
          GOTO      STBF2000 IF EQUAL
          CMATCH    "/",DIM55
          GOTO      STBF2000 IF EQUAL
          BUMP      DIM55,-1
          GOTO      STBF1000 IF NOT EOS
          GOTO      STBF3000
.
STBF2000  MOVEFPTR  DIM55,ENDSTR
          COMPARE   STARTSTR,ENDSTR
          GOTO      STBF3000 IF NOT LESS
          MOVE      ONE,EXIT
          GOTO      STBF9999
.
STBF3000  ADD       ONE,ENDSTR
          RESET     DIM55,ENDSTR
          SETLPTR   DIM55,55
          APPEND    DIM55,DIM20   
          APPEND    SP70,DIM20  
          RESET     DIM55  
.
          MOVE      ONE,STARTSTR
          MOVE      ONE,ENDSTR
          ENDSET    DIM20
.
STBF4000  CMATCH    ".",DIM20
          GOTO      STBF5000 IF EQUAL
          BUMP      DIM20,-1
          GOTO      STBF4000 IF NOT EOS
          GOTO      STBF6000
.
STBF5000  MOVEFPTR  DIM20,ENDSTR
          COMPARE   STARTSTR,ENDSTR
          GOTO      STBF6000 IF NOT LESS
          MOVE      ONE,EXIT
          GOTO      STBF9999
.
STBF6000  SUBTRACT  ONE,ENDSTR
          RESET     DIM20
          SETLPTR   DIM20,ENDSTR
          APPEND    DIM20,DIM20A  
          APPEND    SP70,DIM20A 
          RESET     DIM20A 
.
STBF9999  RETURN
+
.**************************************************************************
.*                               SETLOD00                                 *
.*     Sets UR & Admission No's from 12 chars to 8 chars.                 *
.*     INPUT VARIABLES:  UNITRECN,OPRNUMBR,PROCODE,OPSTRTIM,OPEDNTIM      *
.*     OUTPUT VARIABLES: URNUMBER,ADMISSNO,MBSCODE,OPSTARTM,OPRENDTM      *
.**************************************************************************
SETLOD00  MOVE      ZERO,EXIT
.
          UNPACK    SP70,KEY4,KEY1,KEY7
          UNPACK    UNITRECN,KEY4,KEY1,KEY7
          REP       "0 ",KEY1                    * Remove leading zero
          PACK      URNUMBER,KEY1,KEY7,SP70
.
          MATCH     SP70,KEY7
          GOTO      SETLOD90 IF EQUAL
.
.  ***** SET PROCEDURE CODE   *****
.
          SQUEEZE   PROCODE                 * left justify field
          PACK      PROCODE,PROCODE,SP70    * clear rest of field
          STRIP     PROCODE                 * remove blanks
          MOVE      PROCODE,MBSCODE         * Move item 
          RESET     MBSCODE                 * this field is now Left Just.
.
.  ***** SET STARTIME & ENDTIME TO COLON FORMAT   *****
.
          UNPACK    OPSTRTIM,DIM2,KEY2
          PACK      OPSTARTM,DIM2,COLON,KEY2,COLON,SP70
          REP       " 0",OPSTARTM
.
          UNPACK    OPENDTIM,DIM2,KEY2
          PACK      OPRENDTM,DIM2,COLON,KEY2,COLON,SP70
          REP       " 0",OPRENDTM
          GOTO       SETLOD99
.
SETLOD90  MOVE       ONE,EXIT
SETLOD99  RETURN
+
.*****************************************************************************
.*                                  VALDAT00                                 *
.*    Get Admission No for this Patient                                      *
.*****************************************************************************
VALDAT00  MOVE      ZERO,ERRNUMB
          MOVE      ZERO,EXIT
          MOVE      SP10,ADMISSNO
          MOVE      SP10,LSTADMNO   
          MOVE      SP10,CLMCODE
          PACK      KEY8,URNUMBER,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,VALDAT80 
.
          PACK      KEY24,URNUMBER,Z70
          CALL      RDSVISA2
VALDAT10  CALL      RDPVISA2
          BRANCH    OVRCD,VALDAT90
.
          MATCH     URNUMBER,PVIURNO          * Check UR number
          GOTO      VALDAT90 IF NOT EQUAL
.
          COMPARE   THREE,PVITYPE             * Must be an Inpatient 
          GOTO      VALDAT10 IF NOT EQUAL
.
          PACK      KEY8,PVIBILL,SP70         * Read Admission File
          CALL      RDMISS1
          BRANCH    OVRCD,VALDAT10
.
.         Must be current admiss or disch
.
          IF        (ASTAT < 2)|(ASTAT > 4)
            GOTO      VALDAT10   * Read Previous Visit record
          ENDIF 
.
          MATCH     SP10,LSTADMNO
          IF        @EQUAL
            MOVE      PVIBILL,LSTADMNO * most recent admission no
          ENDIF
.
          MATCH     ADATE,OPSTARTD  * Operation Date must be >= Admission Date
          GOTO      VALDAT40 IF LESS
.
          MOVE      PVIBILL,ADMISSNO
          MOVE      ACLAIM,CLMCODE
.
          MATCH     ADATE,OPSTARTD  * Operation Date = Admission Date
          IF        @EQUAL
            MATCH     ATIME,OPSTARTM  * Operation time must be >= admission time
            IF        @LESS 
              GOTO      VALDAT40
.             BRANCH    SKIPFLAG,VALDAT98
.             CALL      PREC0000      * print details
.             GOTO      VALDAT92      * error
            ENDIF
          ENDIF
.
          MOVE      SP10,DDATE
          MOVE      SP10,DTIME
          COMPARE   ASTAT,THREE        * Discharged Patient
          GOTO      VALDAT30 IF NOT EQUAL
.
.   * Validate a Discharge Patient
.
          PACK      KEY8,AADMNO,SP70  * Get Discharge Date
          CALL      RDDSCH1 
          BRANCH    OVRCD,VALDAT10
.
          MATCH     OPSTARTD,DDATE  * Operation Date must be <= Discharge Date
          GOTO      VALDAT50 IF LESS
.
          MATCH     OPSTARTD,DDATE  * Operation Date = Discharge Date
          GOTO      VALDAT30 IF NOT EQUAL
.
          MATCH     OPSTARTM,DTIME  * Oper.time must be <= Discharged time
          IF        @LESS 
            BRANCH    SKIPFLAG,VALDAT98
            CALL      PREC0000         * print details
            GOTO      VALDAT94      * error
          ENDIF
.
          MATCH     OPRENDTM,DTIME  * Oper.end time must be <= Disc.time
          IF        @LESS
            BRANCH    SKIPFLAG,VALDAT98
            CALL      PREC0000         * print details
            GOTO      VALDAT96      * error
          ENDIF
.
VALDAT30  CALL      CINV0000                * Check if invoice printed
          BRANCH    EXIT,VALDAT99
          GOTO      VALDAT99
.
VALDAT40  MOVE      ONE,ERRNUMB    * invalid operation date/time agains admiss.
          MOVE      SP10,ADMISSNO
          GOTO      VALDAT10
.
VALDAT50  MOVE      TWO,ERRNUMB    * invalid operation date/time agains disc.
          MOVE      SP10,ADMISSNO
          GOTO      VALDAT10
.
VALDAT80  BRANCH    SKIPFLAG,VALDAT99
          CALL      PREC0000         * print details
          PRINT     *88,"Master Record not found."
          MOVE      ONE,EXIT
          GOTO      VALDAT99
.
VALDAT90  BRANCH    SKIPFLAG,VALDAT99
          CALL      PREC0000         * print details
          BRANCH    ERRNUMB,VALDAT92,VALDAT94,VALDAT96
.
          IF        PSTAT=1
            PRINT     *88,"U/R merged with ",PPSNAME
          ELSE
            PRINT     *88,"Visit Record not found"
          ENDIF
          GOTO      VALDAT98
.
VALDAT92  BRANCH    SKIPFLAG,VALDAT98
          PRINT     *88,"Operation date/time is less than ":
                    "Admission date"
          GOTO      VALDAT98
.
VALDAT94  BRANCH    SKIPFLAG,VALDAT98
          MOVE      ZERO,FORM5
          DAYSEP    DDATE,OPSTARTD,FORM5
          IF        FORM5<3
            PRINT     *88,"Operation date/time is ":
                      "greater than Discharged date"
          ELSE
            IF        PSTAT=1
              PRINT     *88,"U/R merged with ",PPSNAME
            ELSE
              PRINT     *88,"No Visit found."
            ENDIF
          ENDIF
          GOTO      VALDAT98
.
VALDAT96  BRANCH    SKIPFLAG,VALDAT98
          PRINT     *88,"Operation End Time is ":
                    "greater than Discharged date"
.
VALDAT98  MOVE      ONE,EXIT
VALDAT99  RETURN
+
.**************************************************************************
.*                               CINV0000                                 *
.*                Check if invoice printed                                *
.**************************************************************************
CINV0000  MOVE      ZERO,EXIT
          PACK      KEY8,PVIBILL,SP70       * Read Admission File
          CALL      RDMISS1
          BRANCH    OVRCD,CINV2000
          MOVE      ACLAIM,CLMCODE
.
          COMPARE   ZERO,AINVPRT            * Check if Invoices are > 0       
          GOTO      CINV9999 IF EQUAL 
.
          COMPARE   TWO,ASTAT
          GOTO      CINV9999 IF EQUAL       * current patient
.
          CALL      CHKR0000                * Check if any MBS/Misc.
          COMPARE   ZERO,EXIT
          GOTO      CINV9999 IF EQUAL       * no item found
.
          BRANCH    SKIPFLAG,CINV9999
          CALL      PREC0000         * print details
          PRINT     *88,"Invoice already printed with item(s)."
          GOTO      CINV9000
.
CINV1000  BRANCH    SKIPFLAG,CINV9999
          CALL      PREC0000         * print details
          PRINT     *88,"Invoice already printed."
          GOTO      CINV9000
.
CINV2000  BRANCH    SKIPFLAG,CINV9000
          CALL      PREC0000         * print details
          PRINT     *88,"Missing Admission record. "
CINV9000  MOVE      ONE,EXIT
CINV9999  RETURN
+
.**************************************************************************
.*                               PROC0000                                 *
.*   Processing the item                                                  *     
.**************************************************************************
PROC0000  IF        IBCNMHOS=1
            MOVE      SP10,PMVXMHOS
            MOVE      PVIBILL,KEY8
            CALL      RDPMVX11
            MOVE      SP10,PTHSDCLM
            MOVE      PMVXMHOS,KEY3
            CALL      RDPTHSP1
          ENDIF
.
          MATCH     "001",RECTYPE
          GOTO      PROC3000 IF NOT EQUAL   * Check miscellaneous charge
.
.    * Write Record to MBS File           
.
          CALL      WRTMBS00     * Write Record to MBS file
          BRANCH    EXIT,PROC9999
.
          CALL      WRTMTI00      * Write to pmsmtiaf File
          BRANCH    EXIT,PROC9999
.
          CALL      VALIDT00      * Validate Admission Number & Admission Type
          BRANCH    TRNSFLAG,PROC9999
.
          CALL      PODAY000      * Check for pre-op days
.
          CALL      WRTTRN00  * Write Transfer Recored for Operation Date/Time
          CALL      UPDTRN00  * Update Transfer File
          CALL      UPDADM00  * Update Admission file
          GOTO      PROC9999 
.
.         Write misc.item to misc.theatre item file
.
PROC3000  CALL      WRTMTI00  * write to pmsmtiaf file
          GOTO      PROC9999
.
PROC9999  RETURN
+
.**************************************************************************
.*                               WRTMBS00                                 *
.*   First read item file to check that we actually have a valid MBS No.  *     
.*   Write a record to the MBS file                                       *
.**************************************************************************
WRTMBS00  MOVE      ZERO,EXIT 
          PACK      KEY9,MBSCODE,SP70
          PACK      KEY17,KEY9,OPSTARTD,SP70
          CALL      PATITMRD                 * Read on Item File 
          BRANCH    OVRCD,WRTMBS90    
.
          PACK      KEY11,AADMNO,Z70
          CALL      RDSMMBS1                 * Positional Read            
          CALL      RDPMMBS1                 * Read Previous Record (patmmbfs)
          BRANCH    OVRCD,WRTMBS10
.
          MATCH     AADMNO,MMADMN  
          GOTO      WRTMBS10 IF NOT EQUAL
.
          ADD       ONE,MMRECN
          GOTO      WRTMBS20
.
WRTMBS10  MOVE      ONE,MMRECN
.
WRTMBS20  MOVE      MBSCODE,MMCODE      * MBS Code
          MOVE      AADMNO,MMADMN       * Admission No
          MOVE      OPSTARTD,MMDATE     * Operation Date 
          MOVE      OPSTARTM,MMSTIM     * Operation Start Time
          MOVE      OPRENDTM,MMETIM     * Operation End Time  
          MOVE      SP70,PTMMDESC       * Free Format Description
          MOVE      SP70,MMSPARE 
.
          PACK      KEY11,MMADMN,MMRECN,SP70
          CALL      RDAMMBS1                 
          IF        OVRCD=1 
            CALL      WRMMBS1           * Write Record to patmmbsf
          ENDIF  
          CALL      WESE0000
          GOTO      WRTMBS98
.
WRTMBS90  MOVE      ONE,EXIT 
          CALL      PREC0000         * print details
          PRINT     *88,"Invalid Item code."
          GOTO      WRTMBS99
.
WRTMBS98  MOVE      ZERO,EXIT 
WRTMBS99  RETURN
.******************************************************************************
.*                                 WESE0000                                   *
.*                Write/update wateseaf (episode details)                     *
.******************************************************************************
WESE0000  READ      CONTROLF,EIGHTY;*250,PTCNHDPS
.
          COMPARE   THREE,PTCNHDPS
          GOTO      WESE9999 IF NOT EQUAL   * Not in Victoria
.
          MOVE      AADMNO,KEY8
          CALL      RDBKREC1
          BRANCH    OVRCD,WESE9999
.
          PACK      KEY19,BKREURNO,BKREPROC,BKRECNT,SP70
          CALL      RDTREA1
          BRANCH    OVRCD,WESE9999

          PACK      KEY17,WTWMECNT,Z70
          CALL      RSWTESE1
          CALL      RPWTESE1
          BRANCH    OVRCD,WESE9999
.
          MATCH     WTEEUNIQ,WTWMECNT
          GOTO      WESE1000 IF NOT EQUAL
.
          MATCH     WTEEADAT,ADATE     * admission date already on file
          GOTO      WESE1000 IF NOT EQUAL
.
          MATCH     WTEEINSD,ACLAIM    * claim type already on file
          GOTO      WESE9999 IF EQUAL
.
          MATCH     WTEEREMD,MMDATE    * operation date = removal date 
          GOTO      WESE1000 IF NOT EQUAL
.
WESE1000  PACK      WTEEADAT,ADATE
          PACK      WTEEINSD,ACLAIM
          PACK      WTEEREMD,MMDATE
          REP       " 0",WTEEREMD
          MOVE      SP70,WTEERREM
          PACK      KEY5,ANSW,ANSR,SP70
          CALL      RDSCODE1
WESE1500  CALL      RDKCODE1
          BRANCH    OVRCD,WESE2000
          MATCH     "WR",TCODE
          GOTO      WESE2000 IF NOT EQUAL
.
          MATCH     ANSW,TCINDC3
          IF        @EQUAL
            MOVE      ACODE,WTEERREM
            GOTO      WESE2000
          ENDIF
          GOTO      WESE1500
.
WESE2000  PACK      WTEEEDAT,SP70
          PACK      WTEEWEBU,SP70
          PACK      WTEEUDAT,SP70
          PACK      WTEEUTIM,SP70
          MOVE      ZERO,WTEEMANU
          PACK      WTEEASAS,SP70
.
          CALL      CLWATTX1
          PACK      KEY19,WMURNO,WMCODE,WMCNT,SP70
WESE2100  CALL      RDWTTX11
          BRANCH    OVRCD,WESE2500
.
          MOVE      WTTXASAS,WTEEASAS
.
WESE2500  PACK      KEY17,WTEEUNIQ,WTEEEDAT,SP70
          CALL      RAWTESE1
          IF        OVRCD=0
            PACK      WTEEWEBU,PASSCODE,SP70
            CALL      IBACLOCK
            PACK      WTEEUDAT,CCC,CYY,CMM,CDD
            REP       " 0",WTEEUDAT
            CLOCK     TIME,WTEEUTIM
            REP       " 0",WTEEUTIM
            CALL      UPWTESE1
          ELSE
            PACK      WTEEWEBC,PASSCODE,SP70
            CALL      IBACLOCK
            PACK      WTEECDAT,CCC,CYY,CMM,CDD
            REP       " 0",WTEECDAT
            CLOCK     TIME,WTEECTIM
            REP       " 0",WTEECTIM
            CALL      WRWTESE1
          ENDIF
.
WESE9999  RETURN
+
.******************************************************************************
.*                                 CHKESE00                                   *
.*                Check if any wateseaf details have changed                  *
.******************************************************************************
CHKESE00  MOVE      ZERO,EXIT
.
          MATCH     WTEEADAT,ADATE
          GOTO      CHKESE90 IF NOT EQUAL
.
          MATCH     WTEEINSD,ACLAIM
          GOTO      CHKESE90 IF NOT EQUAL
.
          MOVE      ONE,EXIT                 * No Changes
.
CHKESE90  GOTO      CHKESE99
.
CHKESE99  RETURN
.
CLRWTESE  PACK      WTEEUNIQ,SP70
          PACK      WTEEURNO,SP70
          PACK      WTEEADAT,SP70
          PACK      WTEEDEST,SP70
          PACK      WTEEINSD,SP70
          PACK      WTEEPLOS,SP70
          PACK      WTEEPROC,SP70
          PACK      WTEEPDES,SP70
          PACK      WTEERREM,SP70
          PACK      WTEERDAT,SP70
          PACK      WTEEREMD,SP70
          PACK      WTEESREF,SP70
          PACK      WTEESSPC,SP70
          PACK      WTEEWEBC,SP70
          PACK      WTEECDAT,SP70
          PACK      WTEECTIM,SP70
          PACK      WTEEWEBU,SP70
          PACK      WTEEUDAT,SP70
          PACK      WTEEUTIM,SP70
          PACK      WTEEARDT,SP70
          PACK      WTEESPAR,SP70
          PACK      WTEEMANU,SP70
          PACK      WTEEPTWT,SP70
          PACK      WTEESGID,SP70
          PACK      WTEERADT,SP70
          PACK      WTEETCMP,SP70
.
          RETURN
.**************************************************************************
.*                               WRTMTI00                                 *
.*   Inputs: Already done a read on Item File so can populate "pmsmtiaf", *
.*           with "patitemn" and uploaded information.                    *  
.**************************************************************************
WRTMTI00  MOVE     ZERO,EXIT
          MATCH    "001",RECTYPE
          IF       @EQUAL
            MOVE      ZERO,PATPOR       * default to zero Amount       *C-60397
            MOVE      ZERO,HFPOR                                       *C-60397
            PACK      KEY5,CODECL,ACLAIM
            CALL      RDCODE1
            IF        OVRCD=0
              MATCH     "1",TCINDC1     * check for overseas patient
              CALL      GETTHR IF EQUAL * Get theatre charges for overseas
            ENDIF
          ELSE
            CALL     GMSC0000          * Get miscellaneous charges
          ENDIF
          BRANCH   OVRCD,WRTMTI90
.
          MOVE     AADMNO,KEY8
          CALL     TRVISA1           * Get the next Transaction No.
          MOVE     PVITRAN,TRANSNUM
.
          PACK     KEY14,AADMNO,TRANSNUM,SP20
          CALL     RAPMMTI1
          IF       OVRCD=1
            CALL     SETMTI00               * Set up Misc.Theatre item variables
            CALL     WRPMMTI1               * write record to the file
          ENDIF
.
          IF        PTCNIPEN = 0
            MOVE      AADMNO,IPADMNO
            MOVE      THREE,IPSYSTM
            MOVE      SP70,IPSITE
            MOVE      SP70,PENDHOSP
            IF        IBCNMHOS=1
              MOVE      PMVXMHOS,PENDHOSP           * Hospital ID
            ENDIF
            MOVE      " 3",PENDSYST                 * Inpatient
.
            MOVE      OPSTARTD,PENDSDAT             * Operation date
            MOVE      ADATE,PENDADAT                * admission date
            MOVE      SP70,PENDDDAT
            IF        ASTAT=3
              MOVE      AADMNO,KEY8
              CALL      RDDSCH1
              IF        OVRCD=0
                MOVE      DDATE,PENDDDAT            * discharged date
              ENDIF
            ENDIF
            MOVE      AFUNDH,PENDFUND
            MOVE      ACLAIM,PENDCLAM
            CALL      IPRH0000
.
            MOVE      SEVEN,PTIPRSTA                * Add item
            MOVE      SP70,PTIPSVAR
.
            PACK      KEY8,IPADMNO,SP70
            CALL      RAIPEN1
            IF        OVRCD=1
              CALL      WRIPEN1
            ELSE
              CALL      UPIPEN1
            ENDIF
          ENDIF
.
          MOVE     ZERO,EXIT
          GOTO     WRTMTI99
.
WRTMTI90  MOVE     ONE,EXIT
WRTMTI99  RETURN
.*****************************************************************************
.*                              SETMTI00                                     *
.*     setup variables for Misc.Theatre item file                            *
.*****************************************************************************
SETMTI00  MOVE     AADMNO,PMMIVISN     * Admission Number
          MOVE     TRANSNUM,PMMITRAN   * Transaction Number
          MOVE     PVIURNO,PMMIURNO    * U/R number
          MOVE     " 3",PMMISYST
          PACK     PMMIITEM,MBSCODE,SP20    * Item No. 
          MOVE     IDESC,PMMIDESC      * Description
          MOVE     SP70,PMMIDES2       * Description 2
          MOVE     OPSTARTD,PMMITDAT   * Operation date
          MATCH    "001",RECTYPE
          IF       @EQUAL
            MOVE     TWO,PMMIRTYP      * Theatre Record Type
          ELSE
            MOVE     THREE,PMMIRTYP    * Miscellaneous Record Type
          ENDIF
          MOVE     ZERO,PMMIAMTG       * Amount Gov Portion 
          MOVE     ZERO,PMMIAMTH       * Orignial rate
.
          MOVE     PATPOR,PMMIAMTP     * Amount Patient Portion        *C-60397
          MOVE     HFPOR,PMMIRBAT      * Estimated Fund Rebate         *C-60397
          MOVE     PMMIRBAT,PMMIAMTT   * Gross total amount
          ADD      PMMIAMTP,PMMIAMTT
.
          MATCH    SP5,PROQUANT
          IF       !@EQUAL
            MOVE     ZERO,FORM5
            MOVE     PROQUANT,FORM5
            IF       FORM5<>0
              MULT      FORM5,PMMIAMTT
              MULT      FORM5,PMMIAMTP
              MULT      FORM5,PMMIRBAT
            ENDIF
          ENDIF
.
          PACK     PMMITDOC,CONSLTNT,SP10     * Treating Doctor
          MOVE     ZERO,PMMISERV
          MOVE     PROQUANT,PMMISERV   * Number of Services
          MOVE     SURGEON,PMMIODOC    * Operating Doctor
          MOVE     SP70,PMMISESS       * Session
          MOVE     IMISGRP,PMMIMGRP    * MISC Group
          MOVE     SP70,PMMIBTCH       * BTC No. For MISC
.
          MOVE     PTITGSTA,PMMIGSTA   * GST applicable
          MOVE     PTITGSTC,PMMIGSTC   * GST Payable Code
          MOVE     ZERO,PMMIGSTM       * GST Amount
          MOVE     SP70,PMMIREFN       * Receipt No.
          MOVE     ZERO,PMMIINVN       * No. of invoices to be printed
          MOVE     "0",PMMIMVBR
          MOVE     SP70,PMMICNTR
          MOVE     SP70,PMMISUNQ
          MOVE     SP70,PMMIINCT
          MOVE     SP70,PMMISUBN
          MOVE     SP70,PMMIEDAD
          MOVE     SP70,PMMISVTM
          MOVE     SP70,PMMIDUPD
          MOVE     SP70,PMMITUPD
          MOVE     SP70,PMMIWUSR
          PACK     PMMIDTCR,CCC,CYY,CMM,CDD
          REP      " 0",PMMIDTCR
          CALL     IBACLOCK
          MOVE     CTIMEIS,PMMITMCR
          MOVE     PASSCODE,PMMIIDCR
          MOVE     SP70,PMMITMNO
          MOVE     SP70,PMMIPMBS
          MOVE     SP70,PMMIRBRS
          MOVE     SP70,PMMICTYP
          PACK     PMMIACOI,SP70       * After Care Override Indicator
          PACK     PMMIDSOV,SP70       * Duplicate Service Override
          PACK     PMMISTXT,SP70       * Service Text
          PACK     PMMILSPN,SP70       * Location Specific Practice No(LSPN)
          PACK     PMMIMPOV,SP70       * Multi Procedure Override
          PACK     PMMINMPT,SP70       * Number of Patients Seen
          PACK     PMMISDCD,SP70       * Self Deem Code (Cat Sd)
          PACK     PMMITDUR,SP70       * Time Duration (Mins)
          PACK     PMMIROVR,SP70       * Restricted Override Code (Cat Ro)
          PACK     PMMIHOSI,SP70       * Hospital Indicator         *T0859743
          MOVE     SP70,PMMIDKSM       * Distance in kms
.
          MOVE     SP70,PMMISPAR       * Spare
.
SETTRA99  RETURN
+
.**************************************************************************
.*                               VALIDT00                                 *
.*             Validate Admission Number & Admission Type                 *
.**************************************************************************
VALIDT00  MOVE      ZERO,TRNSFLAG      * Successful Update of Transfer File Flag
. 
          COMPARE   ZERO,PTCNAUAT      * Automaitc Update Type of Admission
          GOTO      VALIDT90 IF EQUAL 
.
.   Check no. of invoices on Admission file if > 0 then cannot update Transfer
.   & need not bother with Admission Type checks.
.
          PACK      KEY8,PVIBILL,SP70       * Read Admission File
          CALL      RDMISS1
          BRANCH    OVRCD,VALIDT10
          MOVE      ACLAIM,CLMCODE
.
          MATCH     SP3,ATYPE
          GOTO      VALIDT30 IF EQUAL       * Blank admission type
.
          PACK      KEY5,ANSA,SP1,ATYPE
          CALL      RDCODE1
          BRANCH    OVRCD,VALIDT30
.
          MATCH     ANSX,TCINDC7
          GOTO      VALIDT30 IF EQUAL       * Exclude this admission type
.
.   Read Item file to get suggested class.
.
          CALL      GATYP000           * Get admission type for the oper. date
.
          CALL      XMBS0000           * Get the primary MBS
          MATCH     SP3,PRIMMBS
          IF        @EQUAL
            MOVE      MBSCODE,PRIMMBS
          ENDIF
.
          PACK      KEY9,PRIMMBS,SP70
          PACK      KEY17,KEY9,OPSTARTD,SP70
          CALL      PATITMRD                 * Read on Item File
          BRANCH    OVRCD,VALIDT40 
.
          MATCH     SP3,ICLSS
          GOTO      VALIDT50 IF EQUAL        * no suggested classification
.
.  Not Equal so set Admission Type to that of Item File
.
          MOVE      ICLSS,SUGGCLSS         * Admission Type
          MATCH     SP3,SUGGCLSS
          GOTO      VALIDT50 IF EQUAL
.
.  Get suggested class. for day case
.
          IF        ASTAT=3
            MATCH     DDATE,ADATE    
            IF        @EQUAL
              CALL      DAYCAS00           * Doing Day Case Banding
            ENDIF
          ENDIF
.
.  Match Admission Type with that of the Item file & drop out if equal 
.
          MATCH     LATYPE,SUGGCLSS        * Admission Type
          GOTO      VALIDT90 IF EQUAL
.
          GOTO      VALIDT99        * Successful Update of Transfer File Flag
.
VALIDT10  CALL      PREC0000         * print details
          PRINT     *88,"Adm.: ",PVIBILL," Missing Admission record. "
          GOTO      VALIDT90
.
VALIDT30  CALL      PREC0000         * print details
          PRINT     *88,"Adm.: ",PVIBILL," has adm.type (atype):",ATYPE
          GOTO      VALIDT90
.
VALIDT40  CALL      PREC0000         * print details
          PRINT     *88,"Adm.: ",PVIBILL," has invalid Primary MBS Item":
                    PRIMMBS
          GOTO      VALIDT90
.
VALIDT50  CALL      PREC0000         * print details
          PRINT     *88,"Adm.: ",PVIBILL," has Primary MBS Item":
                    PRIMMBS," with no suggested classification"
          GOTO      VALIDT90
.
VALIDT90  MOVE      ONE,TRNSFLAG    * Unsuccessful Update of Transfer File Flag
VALIDT99  RETURN
+
.***************************************************************************
.   Get the admission type for the operation date.
.***************************************************************************
GATYP000  MOVE      ATYPE,LATYPE          * default to admissions
          MOVE      SP10,LBTYPE
.
          PACK      KEY30,AADMNO,SP70
          CALL      RDSTRAN2               * Position on index 2
GATYP100  CALL      RDKTRAN2               * Read Record
          BRANCH    OVRCD,GATYP999
.
          MATCH     AADMNO,TADMN
          GOTO      GATYP999 IF NOT EQUAL
.
          MATCH     ANSA,TMOVE
          IF        @EQUAL
            PACK      KEY6,TWARD,TBED
            CALL      RDWARD1
            IF        OVRCD=0
              MOVE      WEFRBT,LBTYPE        * Current bed type
            ENDIF
          ENDIF
.
          MATCH     TDATE,OPSTARTD
          GOTO      GATYP999 IF LESS
          GOTO      GATYP200 IF NOT EQUAL
.
          MATCH     TTIME,OPSTARTM
          GOTO      GATYP999 IF LESS
.
GATYP200  MOVE      TATYPE,LATYPE
          MATCH     SP3,TRBTYP
          IF        !@EQUAL
            MOVE      TRBTYP,LBTYPE
          ENDIF
          GOTO      GATYP100
.
GATYP999  RETURN
+
.***************************************************************************
.*                               UPDADM00                                  *
.*  Update Admission File 
.***************************************************************************
UPDADM00  PACK      KEY8,PVIBILL,SP70       
          CALL      RLPTMIS1           * Read & Lock Admission File
          BRANCH    OVRCD,VALIDT99
.
          MOVE      SUGGCLSS,ATYPE     * Set Admission type 
          CALL      UPMISS1            * Update & Unlock Admission File
          CALL      UUPTMIS1           * Read & Lock Admission File
.
UPDADM99  RETURN
.**************************************************************************
.*                               UPDTRN00                                 *
.*  Update The Transfer File with new Admission type                       *
.*  Recalculate Bed Fees on the Transfer File if needed                    *
.**************************************************************************
UPDTRN00  MOVE      SP3,LATYPE
          MOVE      ADATE,FROMDATE
.
          PACK      KEY30,AADMNO,SP30
          CALL      RDSTRAN2
UPDTRN10  CALL      RDKTRAN2
          BRANCH    OVRCD,UPDTRN99
.
          MATCH     TADMN,AADMNO  
          GOTO      UPDTRN99 IF NOT EQUAL
.
          CMATCH    "D",TMOVE          * Use Previous record variables
          GOTO      UPDTRN50 IF EQUAL
.
.         Get the new bed fee rate
.
          MOVE      ZERO,PATPOR                          * was PATFEE  *C-60397
          MOVE      ZERO,HFPOR                           * was HFFEE   *C-60397
          MOVE      SP1,COLFEE
.
          MOVE      TWARD,LWARD
          MOVE      TBED,LBED
          MOVE      TRBTYP,LRBTYP
          MOVE      TRBTYP,SAVBTYP
          MOVE      TATYPE,SAVATYP
.
.  Save the key of trans file
.
          PACK      DIM30,TADMN,TDATE,TTIME,TWARD,TBED
.
.  Check if we have a change in admission type
.
          MATCH     TATYPE,LATYPE
          GOTO      UPDTRN15 IF EQUAL
.
          MOVE      TATYPE,LATYPE
          BRANCH    PTCNCNDY,UPDTRN15           * don't reset day count
          MOVE      TDATE,FROMDATE
.
.  Get the number no of days in hospital up to this trans date,
.  to work out the bed fees
.
UPDTRN15  MOVE      ZERO,TOTDAY
          MATCH     ADATE,DDATE
          GOTO      UPDTRN20 IF EQUAL
.
          MOVE      TDATE,TODATE
          CALL      NHOSPDAY
          MOVE      NBRDAYS,TOTDAY
          ADD       ONE,TOTDAY
.
UPDTRN20  ADD       ADYHOSP,TOTDAY
.
.  Reposition to the prior trans record,As the NHOSPDAY uses trans file
.
          MOVE      DIM30,KEY30
          CALL      RDTRAN2
.
          MOVE      TDISC,SDISC
          MOVE      TALLW,SALLW
.
UPDTRN40  MOVE      TDATE,DIM8
          MOVE      TOTDAY,TOTDAYS
          MOVE      TATYPE,ATYPE
.
          PACK      KEY9,ACLAIM,AFUNDH,SP70
          CALL      GETCNEFF               * Get Contract Effective From
          BRANCH    EXIT,UPDTRN92
.
          PACK      KEY27,ACLAIM,AFUNDH,AFNDTB,ATYPE,SAVBTYP,SP70
          CALL      GETBFEES               * Get bed fees
          BRANCH    EXIT,UPDTRN90
.
.  Save the computer generated rate for the change rate audit file
.
          MOVE      SBFPAT,RCGPAT
          MOVE      SBFHF,RCGHF
          MOVE      SBFENDDY,REDAYS
          SUB       TOTDAY,REDAYS
          ADD       ONE,REDAYS
.
          MOVE      SBFENDDY,SAVEND
          MOVE      ZERO,SAVAEND
          MOVE      SBFPAT,PATPOR                        * was PATFEE  *C-60397
          MOVE      SBFHF,HFPOR                          * was HFFEE   *C-60397
          MOVE      SDISC,NDISC
          MOVE      SALLW,NLWAMT
          MOVE      SP2,AALLOW
          MOVE      LRBTYP,SAVBTYP
.
UPDTRN50  MOVE      PATPOR,TRATEF                        * was PATFEE  *C-60397
          MOVE      ZERO,PTTRADPT
          MOVE      HFPOR,TRATEH                         * was HFFEE   *C-60397
          MOVE      ZERO,PTTRADRB
          MOVE      ZERO,PTTRAEND
          IF        PTTREPSD<>2
            MOVE      ZERO,PTTREPSD
          ENDIF
          MOVE      NDISC,TDISC
          MOVE      NLWAMT,TALLW
          MOVE      SAVBTYP,TRBTYP
          MOVE      SAVEND,TRBEND
          MOVE      SAVEND,REDAYS
          SUB       TOTDAY,REDAYS
          ADD       ONE,REDAYS
          MOVE      ZERO,TEXTRA
          MOVE      SAVATYP,TATYPE
.
          PACK      TRCDATE,CCC,CYY,CMM,CDD
          REP       " 0",TRCDATE
          CLOCK     TIME,TRCTIME
          MOVE      "IBAOPR76",PTTRUUID
.
          CALL      UPTRAN2
          PACK      DIM30,TADMN,TDATE,TTIME,TWARD,TBED
.
.  Write  to Rate Change Audit file if system parameter is switched on (0)
.
          BRANCH    CAUDQ,UPDTRN80           * added for V5.01.15 - SRF 105195
.
UPDTRN60  CALL      RDSRCA
.
          COMPARE   ZERO,RSTATUS
          GOTO      UPDTRN70 IF EQUAL
.
.          PRINT     *N,"Rate Change Audit in use, Waiting........"
          GOTO      UPDTRN60
.
UPDTRN70  CALL      WRIRCA
          MOVE      AADMNO,RADMNO
          MOVE      PGNAME,ANS
          PACK      RNAME,ANS,DOT,PSNAME
          MOVE      ACLAIM,RCLAIM
          MOVE      AFUNDH,RFUND
          MOVE      AFNDTB,RHFTAB
          MOVE      SAVBTYP,RBTYPE
.
          MOVE      ZERO,RNPAT        * New rate = rate - disc - allow
          MOVE      PATPOR,RNPAT                         * was PARFEE  *C-60397
          SUB       NDISC,RNPAT
          SUB       NLWAMT,RNPAT
          MOVE      HFPOR,RNFUND     * from  PATDSBFE    * was HFFEE   *C-60397
          MOVE      TDATE,RDATE
          MOVE      TATYPE,RATYPE
          MOVE      ZERO,ROPAT
          MOVE      SP3,RCLSS
          MOVE      SP3,RCHST
          MOVE      PASSCODE,ROPER       * Operator code
.
          CALL      WRRCA
.
.  Reposition to the previous trans record
.
UPDTRN80  MOVE      DIM30,KEY30
          CALL      RDTRAN2
          GOTO      UPDTRN10  
.
UPDTRN90  PRINT     *N,"Default Bed Fee Not Set Up Yet - ":
                    "Record has partially been updated, ":
                    "Please contact Supervisor"
          MOVE      ONE,EXIT 
          GOTO      UPDTRN99
.
UPDTRN92
          PRINT     *N,"Bed Fee Not Set Up Yet - ":
                    "Contract Effective Detail Not found, ":
                    "Please contact Supervisor"
          MOVE      ONE,EXIT 
          GOTO      UPDTRN99
.
UPDTRN99  RETURN
.**************************************************************************
.*                               PODAY000                                 *
.*   The number of pre-op days allowed - n                                *     
.* - if the no of days b/w admission date and operation date > n          *
.*   then suggested classification will start from the operation date     *
.* - if the no of days b/w admission date and operation date <= n         *
.*   then suggested classification will start from the admission date     *
.**************************************************************************
PODAY000  REP       " 0",ADATE
          REP       " 0",OPSTARTD
          MOVE      OPSTARTD,SAVDATE      * Transfer Date
          DAYSEP    ADATE,OPSTARTD,F2
.
          COMPARE   F2,OPCNPODY
          GOTO      PODAY999 IF LESS
          MOVE      ADATE,SAVDATE
          MOVE      ATIME,OPSTARTM
.
PODAY999  RETURN
+
.**************************************************************************
.*                               WRTTRN00                                 *
.*  Write record for The Uploaded Operation Date & Time                   *
.*  Check if date is equal, if so need to check Operation Time against    *
.*  Transfer Time. Operation Time cannot be equal to the Transfer time of * 
.*  the read record.                                                      *
.*                                                                        *
.**************************************************************************
WRTTRN00  PACK      KEY30,AADMNO,SAVDATE,OPSTARTM,Z70 
          CALL      RDSTRAN2               * Position on index 2
          CALL      RDPTRAN2
          BRANCH    OVRCD,WRTTRN99
.
          MATCH     AADMNO,TADMN
          GOTO      WRTTRN99 IF NOT EQUAL
.
          MATCH     TDATE,SAVDATE    * If Operation Date = Admission Date
          IF        @EQUAL
            MATCH     TTIME,OPSTARTM  * Check if times are equal
            IF        @EQUAL
              ASSIGN    TWO,MINADD
              ASSIGN    ONE,ADDFLAG
              CALL      CTME0000      * If so Increment Transfer time by 2min
              MOVE      TTIME,OPSTARTM
              GOTO      WRTTRN00      * Re Read prev record for new date & time
            ENDIF
          ENDIF
.
          MOVE      AADMNO,TADMN    * Admission No. 
          MOVE      SAVDATE,TDATE    * Transfer Date
          MOVE      OPSTARTM,TTIME  * Transfer Time
          MOVE      "C",TMOVE       * Change record
          MOVE      SUGGCLSS,TATYPE * Set Admission Type
.
          PACK      KEY30,TADMN,TDATE,TTIME,TWARD,TBED,SP70
          CALL      RDATRAN2               
          IF        OVRCD=1
.
            MOVE      ZERO,PTTREPSD
            MOVE      ZERO,PTTRLSPT
            MOVE      ZERO,PTTRLSRB
            MOVE      ZERO,PTTRAEND
            MOVE      ZERO,PTTRADPT
            MOVE      ZERO,PTTRADRB
            MOVE      SP70,PTTRNHAC
            MOVE      SP70,PTTROPER
            MOVE      SP70,PTTRAUAT
            MOVE      SP70,PTTRCUID
            MOVE      SP70,PTTRLTSC
            MOVE      ACLAIM,PTTRCLTY
.
            PACK      PTTRCDAT,CCC,CYY,CMM,CDD
            REP       " 0",PTTRCDAT
            CLOCK     TIME,PTTRCTIM
            MOVE      "IBAOPR76",PTTRUCID
            UNPACK    SP70,TRCDATE,TRCTIME,PTTRUUID
            CALL      WRTRAN2
          ELSE
            PACK      TRCDATE,CCC,CYY,CMM,CDD
            REP       " 0",TRCDATE
            CLOCK     TIME,TRCTIME
            MOVE      "IBAOPR76",PTTRUUID
            CALL      UPTRAN2
          ENDIF
.
.         Update the rest of the records
.
          PACK      KEY30,TADMN,TDATE,TTIME,TWARD,TBED,SP70
          CALL      RDTRAN2
WRTTRN20  CALL      RDKTRAN2
          BRANCH    OVRCD,WRTTRN99
.
          MATCH     AADMNO,TADMN
          GOTO      WRTTRN99 IF NOT EQUAL
.
          MOVE      SUGGCLSS,TATYPE * Set Admission Type
          PACK      TRCDATE,CCC,CYY,CMM,CDD
          REP       " 0",TRCDATE
          CLOCK     TIME,TRCTIME
          MOVE      "IBAOPR76",PTTRUUID
.
          CALL      UPTRAN2
          GOTO      WRTTRN20
.
WRTTRN99  RETURN
.**************************************************************************
.*                               DAYCAS00                                 *
.*   Get Admission Type, Based on Rules for Day Case Banding.             *
.*   Admission type will be set to one of the Day case parameters         *
.*   depending on the rule.                                               * 
.*   First up work out operation duration in Mins.                        *
.*   PARAMETERS: PTCNADB1...4 - Day Case Banding                          *
.*               PTCNDFAN - Defualt Anaesthetic Code for Day Proc         *
.*   RETURNED PARAMETERS: SUGGCLSS - Suggested Class                      *
.**************************************************************************
DAYCAS00  MATCH     SP3,PTITDCSC
          GOTO      DAYCAS10 IF EQUAL
          MOVE      PTITDCSC,SUGGCLSS  * Use suggested class.for daycase
          GOTO      DAYCAS99
.
DAYCAS10  UNPACK    OPSTARTM,CHOUR,ANS,CMIN
          MOVE      CHOUR,FORM4        * FORM4 will be totmins
          MOVE      CMIN,FORM2
.
          MULT      "60",FORM4         * convert hours to minutes
          ADD       FORM2,FORM4        * add the minutes  
          MOVE      FORM4,STARTIME     * Start time now in Minutes
.
          UNPACK    OPRENDTM,CHOUR,ANS,CMIN
          MOVE      CHOUR,FORM4        * FORM4 will be totmins
          MOVE      CMIN,FORM2
.
          MULT      "60",FORM4         * convert hours to minutes
          ADD       FORM2,FORM4        * add the minutes
          MOVE      FORM4,ENDTIME      * End Time now in Minutes
.
          ASSIGN    ENDTIME-STARTIME,DURATION  * Get Duration..
.
.  Read Categories Codes file with Anaesthetist Code
.
          MOVE      "OA",CATEGORY
          MOVE      PTCNDFAN,CODE
          CALL      GETCOD00
          MOVE      TCINDC1,FORM1   * convert to form easier to check on!!!
.
          IF        (FORM1 = 2)|(FORM1 = 3)|(FORM1 = 4)          
            IF        DURATION < SIXTY
              MOVE      PTCNADB3,SUGGCLSS     
              GOTO      DAYCAS99
            ELSE
              MOVE      PTCNADB4,SUGGCLSS     
              GOTO      DAYCAS99
            ENDIF
          ENDIF     
          IF        FORM1 = 1
            MOVE      PTCNADB2,SUGGCLSS     
            GOTO      DAYCAS99
          ENDIF
.
.  Ok if none of the above conditions have been met then Band 1 applies
.
          MOVE      PTCNADB1,SUGGCLSS     
.
DAYCAS99  RETURN
.------------------------------------------------------------
. CTME0000  : Set Transfer time to be one second before current time
. Return values : TDATE,TTIME
.------------------------------------------------------------
CTME0000  UNPACK    TTIME,CHOUR,ANS,CMIN,DIM127
          MOVE      CHOUR,FORM2
          MOVE      FORM2,FORM4D
          MULT      "60",FORM4D
          MOVE      CMIN,FORM2
          ADD       FORM2,FORM4D            * This is now total time in minutes
          IF        ADDFLAG=1
            ADD       MINADD,FORM4D         * Add minutes
          ELSE
            SUB       MINADD,FORM4D         * Subtract minutes
          ENDIF
.
          MOVE      FORM4D,SFORM4D          * Save the total time
          IF        FORM4D>=1440
            UNPACK    TDATE,CCENT,CYEAR,CMON,CDAY
            MOVE      CCENT,CC
            MOVE      CYEAR,YY
            MOVE      CMON,MM
            MOVE      CDAY,DD
            CALL      DMYCON
            ADD       ONE,JULDAY
            MOVE      JULDAY,JWKDAY
            MOVE      JULYR,JWKYER
            MOVE      JULCC,JWKCC
            CALL      JULCON
            PACK      TDATE,CC,YY,MM,DD
            REP       " 0",TDATE
            PACK      SAVDATE,CC,YY,MM,DD
            REP       " 0",SAVDATE
          ENDIF
.
          ASSIGN    FORM4D/SIXTY,FORM4D
          ASSIGN    FORM4D*SIXTY,FORM4D
          SUB       FORM4D,SFORM4D
          MOVE      SFORM4D,F2
          MOVE      CMIN,FORM2
          IF        F2<FORM2
            MOVE      CHOUR,FORM2
            IF        FORM2=23
              ASSIGN    ZERO,FORM2          * If Hours are 23,make it as 00
            ELSE
              ADD       ONE,FORM2           * Increment Hours by 1
            ENDIF
            MOVE      FORM2,CHOUR
            REP       " 0",CHOUR
          ENDIF
          MOVE      F2,CMIN
          REP       " 0",CMIN
          PACK      TTIME,CHOUR,COLON,CMIN,DIM127
.
CTME9999  RETURN
.------------------------------------------------------------
. Get Code Description
.------------------------------------------------------------
GETCOD00  MOVE      SP70,TDESC
          MOVE      SP70,ACODE
          UNPACK    SP70,TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5:
                         TCINDC6,TCINDC7,TCINDC8,TCINDC9,TCINDC10:
                         TCINDC11
          MATCH     SP70,CODE
          GOTO      GETCOD99 IF EQUAL
.
          PACK      KEY5,CATEGORY,CODE
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE      SP70,ACODE
            MOVE      SP70,TDESC
            UNPACK    SP70,TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5:
                           TCINDC6,TCINDC7,TCINDC8,TCINDC9,TCINDC10:
                           TCINDC11
          ENDIF
GETCOD99  RETURN
+
.**************************************************************************
.        Subroutine to get the miscellaneous charge
.**************************************************************************
GMSC0000  MOVE      ZERO,EXIT
          UNPACK    SP20,PTHFBCAT      * change "H/F Table" to "Table Type"
          PACK      KEY14,AFUNDH,AFNDTB,SP20
          CALL      RDFUND1            * "Table Type code" now in PTHFBCAT
.
          PACK      MISCODE,MISCODE,SP70
          MOVE      OPSTARTD,XDATE     * using OPSTARTD (use CURRDATE if blank)
          MATCH     SP70,OPSTARTD
          IF        @EQUAL
            MOVE      CURRDATE,XDATE
          ENDIF
.
GMSC1000  PACK      KEY29,ACLAIM,AFUNDH,PTHFBCAT,MISCODE,XDATE,SP10
          CALL      PATMCHRD                * read Misc.Charges file 
          COMPARE   ZERO,EXIT
          GOTO      GMSC7000 IF EQUAL
.
.                                           * - try a blank health fund
GMSC2000  PACK      KEY29,ACLAIM,SP6,SP3,MISCODE,XDATE,SP10
          CALL      PATMCHRD                * read Misc.Charges file 
          COMPARE   ZERO,EXIT
          GOTO      GMSC7000 IF EQUAL
.
.                                           * - last chance, try default Claim
GMSC3000  IF        IBCNMHOS=1
            PACK      KEY29,PTHSDCLM,SP6,SP3,MISCODE,XDATE,SP10
          ELSE
            PACK      KEY29,PTCNDCLM,SP6,SP3,MISCODE,XDATE,SP10
          ENDIF
          CALL      PATMCHRD                * read Misc.Charges file 
          COMPARE   ZERO,EXIT
          GOTO      GMSC8000 IF NOT EQUAL
.
GMSC7000  MOVE      MPATPOR,PATPOR                       * was TFPAT1  *C-60397
          MOVE      MHFPOR,HFPOR                         * was TFHF1   *C-60397
          MOVE      MISCODE,MBSCODE
          MOVE      MMSCGRP,IMISGRP
          MOVE      MDESC,IDESC
          MOVE      PTMCGSTA,PTITGSTA  * GST applicable
          MOVE      PTMCGSTC,PTITGSTC  * GST Payable Code
          MOVE      ZERO,OVRCD
          GOTO      GMSC9999
.
GMSC8000  CALL      PREC0000         * print details
          PRINT     *88,"Invalid Miscellaneous Charge."
          MOVE      ONE,OVRCD
          GOTO      GMSC9999
.
GMSC9999  RETURN
+
.**************************************************************************
.        Subroutine to get the theatre charge record for a theatre fee
.**************************************************************************
GETTHR  PACK      KEY9,MBSCODE,SP70
        PACK      KEY17,KEY9,OPSTARTD,SP70
        CALL      PATITMRD                 * Read on Item File
        BRANCH    OVRCD,GETT99
.
        PACK      KEY5,CODECL,ACLAIM
        CALL      RDCODE1
        IF        OVRCD=0
          MATCH     "1",TCINDC2
          IF        @EQUAL
            MOVE      QIAMT,PATPOR      * use MBS Amount * was TFPAT1  *C-60398
            MOVE      ZERO,HFPOR                         * was TFHF1   *C-60398
          ENDIF
         ENDIF
GETT99   RETURN
+
.******************************************************************************
.* XMBS000 : Get the primary MBS (most expensive MBS)                         *
.******************************************************************************
XMBS0000  CALL      GSTD0000                * get the end of first stepdown
.
          MOVE      SP10,PRIMMBS            * initialise primary MBS code
          MOVE      ZERO,FORM12
          OPEN      PATDTRA3,"patdtraf"
          PACK      KEY18,AADMNO,TWO,SP20
          CALL      RDSDTRN3
XMBS1000  CALL      RDKDTRN3
          BRANCH    OVRCD,XMBS2000
.
          MATCH     AADMNO,TADMNO           * compare admission number
          GOTO      XMBS2000 IF NOT EQUAL
.
          COMPARE   TWO,TRECTYPE
          GOTO      XMBS2000 IF NOT EQUAL   * not a theatre item
.
          PACK      KEY8,TFCENT,TFYEAR,TFMONTH,TFDAY
          REP       " 0",KEY8
.
          MATCH     KEY8,ENDDATE 
          GOTO      XMBS1000 IF LESS        * not within the first stepdown
.
          PACK      KEY9,TITEMNO
          PACK      KEY17,KEY9,KEY8,SP70
          CALL      PATITMRD
          BRANCH    OVRCD,XMBS1000          * not a valid item code
.
.         MOVE      TPATAMTP,FORM12A        * secondary item
.         ADD       TREBATE,FORM12A
.
          MOVE      QIAMT,FORM12A
.
          COMPARE   FORM12A,FORM12
          GOTO      XMBS1000 IF NOT LESS
.
          MOVE      TITEMNO,PRIMMBS         * set primary MBS code
          MOVE      FORM12A,FORM12
          GOTO      XMBS1000
.
.         Check with the Misc.Theatre item file
.
XMBS2000  MOVE      "2",PMMIRTYP
          PACK      KEY15,DAADMNO,PMMIRTYP,SP20
          CALL      RSPMMTI2
XMBS2200  CALL      RKPMMTI2
          BRANCH    OVRCD,XMBS9999
.
          MATCH     PMMIVISN,DAADMNO
          GOTO      XMBS9999 IF NOT EQUAL   * Different admission
.
          MATCH     "2",PMMIRTYP
          GOTO      XMBS9999 IF NOT EQUAL   * not a theatre item
.
          MATCH     PMMITDAT,ENDDATE 
          GOTO      XMBS2200 IF LESS        * not within the first stepdown
.
          PACK      KEY9,PMMIITEM
          PACK      KEY17,KEY9,PMMITDAT,SP70
          CALL      PATITMRD
          BRANCH    OVRCD,XMBS2200          * not a valid item code
.
.         MOVE      PMMIAMTP,FORM12A        * secondary item
.         ADD       PMMIRBAT,FORM12A
.
          MOVE      QIAMT,FORM12A
.
          COMPARE   FORM12A,FORM12
          GOTO      XMBS2200 IF NOT LESS
.
          MOVE      PMMIITEM,PRIMMBS         * set primary MBS code
          MOVE      FORM12A,FORM12
          GOTO      XMBS2200
.
XMBS9999  RETURN
+
.**************************************************************************
.*                               GSTD0000                                 *
.*        Generate the end date of first stepdown                         *
.**************************************************************************
GSTD0000  MOVE      OPSTARTD,ENDDATE         * default to operation date
          MOVE      ZERO,FORM3
         
          COMPARE   THREE,ASTAT
          GOTO      GSTD9999 IF EQUAL       * Day case
.
          MOVE      SP70,PTHFBCAT
          PACK      KEY14,AFUNDH,AFNDTB,SP10
          CALL      RDFUND1
.
          PACK      KEY9,ACLAIM,AFUNDH,SP70
          CALL      GETCNEFF               * Get Contract Effective From
          BRANCH    EXIT,UPDTRN92
.
          PACK      DIM18,ACLAIM,AFUNDH,PTHFBCAT,LATYPE,LBTYPE
GSTD1000  PACK      KEY27,DIM18,SP70
          CALL      RDSBFEE1
GSTD2000  CALL      RDKBFEE1
          BRANCH    OVRCD,GSTD9000
.
          PACK      DIM18A,BFCLM,BFHFUND,PTBFHTYP,BFATYPE,BFBTYPE
          MATCH     DIM18,DIM18A
          GOTO      GSTD9000 IF NOT EQUAL
.
.         Set the Effective date used for calculation
.
          MOVE      ADATE,CEFFDATE
          LOAD      CEFFDATE,CNTRCEFR,ADATE,DDATE
.
.         If Contract Effective From is 'For Discharges From', Discharged date
.         is blank and TCINDC19=D, default Effective date to Current date
.
          IF        CNTRCEFR=2
            PACK      DDATE,DDATE,SP70
            MATCH     SP70,DDATE
            IF        @EQUAL
              PACK      KEY5,CODECL,ACLAIM
              CALL      RDCODE1
              IF        OVRCD=0
                MATCH     "D",TCINDC19
                IF        @EQUAL
                  PACK      CEFFDATE,CCC,CYY,CMM,CDD,SP70
                  REP       " 0",CEFFDATE
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
          MOVE      PTBFCNID,KEY6           * Contract ID
          CALL      VALCONTR                * Validate Contract id
          BRANCH    EXIT,GSTD2000
          MOVE      PTBFCNID,CONTRCID       * Contract ID
.
          COMPARE   ZERO,BFENDDY
          GOTO      GSTD2000 IF EQUAL
.
          MOVE      BFENDDY,FORM3           * end day of first stepdown
          SUB       ONE,FORM3
.
          UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,CC
          MOVE      CYEAR,YY
          MOVE      CMON,MM
          MOVE      CDAY,DD
          CALL      DMYCON
          ADD       FORM3,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON
          PACK      ENDDATE,CC,YY,MM,DD
.
GSTD9000  REP       " 0",ENDDATE 
GSTD9999  RETURN
+
. =========================================================================
.         DREC0000 - Delete records 
. =========================================================================
DREC0000  CALL      DMBS0000                 * Delete MBS items
          CALL      DMTI0000                 * Delete Misc.Theatre items
DREC9999  RETURN
+
. =========================================================================
.         DMBS0000 - Delete MBS items
. =========================================================================
DMBS0000  PACK      KEY11,AADMNO,SP10
          CALL      RDSMMBS1                 * Positional Read            
DMBS1000  CALL      RDKMMBS1                 * Read next Record (patmmbfs)
          BRANCH    OVRCD,DMBS9999
.
          MATCH     AADMNO,MMADMN  
          GOTO      DMBS9999 IF NOT EQUAL
.
          MATCH     MMDATE,OPSTARTD
          GOTO      DMBS1000 IF NOT EQUAL
.
          PACK      KEY11,MMADMN,MMRECN
          CALL      DEMMBS1
          GOTO      DMBS1000
.
DMBS9999  RETURN
+
. =========================================================================
.         DMTI0000 - Delete MBS items
. =========================================================================
DMTI0000  PACK      KEY14,XADMNO,SP20
          CALL      RSPMMTI1                 * Positional Read            
DMTI1000  CALL      RKPMMTI1                 * Read next Record (patmmbfs)
          BRANCH    OVRCD,DMTI9999
.
          MATCH     XADMNO,PMMIVISN  
          GOTO      DMTI9999 IF NOT EQUAL
.
          MATCH     PMMITDAT,OPSTARTD
          GOTO      DMTI1000 IF NOT EQUAL
.
          PACK      KEY14,PMMIVISN,PMMITRAN
          CALL      DEPMMTI1
          GOTO      DMTI1000
.
DMTI9999  RETURN
+
.******************************************************************************
.*                       Create tempfile                                      *
.******************************************************************************
CTMP0000  CALL      KTMP0000                     * Kill tempfile
          CLEAR     CMDLINE                        * create gp tempfile
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMEA,CMDLINE
          APPEND    TMPKEY,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TMPFILE,FNAMEA
CTMP9999  RETURN
+
.******************************************************************************
.*                       Kill tempfile                                        *
.******************************************************************************
KTMP0000  CLOSE     TMPFILE,DELETE
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMEA,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
KTMP9999  RETURN
+
.******************************************************************************
.*          Rename theatre upload file with MMDD                              *
.******************************************************************************
RNAM0000  BRANCH    NOFILE,RNAM9999   * Skip if original file not exist
          PACK      KEY6,CDD,CMM,CYY
          REP       " 0",KEY6
.
          MOVE      SP10,KEY2
          MOVE      FILEXTN,KEY2
          REP       " 0",KEY2
          IF        COPTION=1
            CLEAR     NEWFILE
            APPEND    UPLFILE,NEWFILE
            CALL      FDOT0000
            APPEND    KEY6,NEWFILE
            APPEND    KEY2,NEWFILE
            APPEND    ".txt",NEWFILE
            APPEND    SP70,NEWFILE
            RESET     NEWFILE
          ELSE
            CLEAR     NEWFILE
            APPEND    PATHNAM,NEWFILE
            CALL      FSTR0000
            CALL      STBF0000
            APPEND    DIM20A,NEWFILE
            CALL      FSTR0000
            APPEND    KEY6,NEWFILE
            APPEND    KEY2,NEWFILE
            APPEND    ".txt",NEWFILE
            APPEND    SP70,NEWFILE
            RESET     NEWFILE
          ENDIF
.
          PACK      CMDLINE,SP70,SP70
          CLEAR     CMDLINE
          APPEND    "cp ",CMDLINE
          APPEND    UPLFILE,CMDLINE
          APPEND    SP1,CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Unable to Copy New file.  ";
            MOVE      ONE,EXIT
.           CALL      HITENTER
            GOTO      RNAM9999
          ENDIF
.
.         PACK      CMDLINE,SP70,SP70
.         CLEAR     CMDLINE
.         APPEND    "rm ",CMDLINE
.         APPEND    UPLFILE,CMDLINE
.         APPEND    SP70,CMDLINE
.         RESET     CMDLINE
.         EXECUTE   CMDLINE,TASKID
.
.         CMATCH    "0",TASKID
.         IF        !@EQUAL
.           DISPLAY   *P1:24,*EL,*B,"Unable to Remove old file.  ";
.           MOVE      ONE,EXIT
.           CALL      HITENTER
.           GOTO      RNAM9999
.         ENDIF
.
RNAM9999  RETURN
+
.******************************************************************************
.*                       Setup path and file name                             *
.******************************************************************************
SFIL0000  PACK      UPLFILE,SP70
          CLEAR     UPLFILE
          BRANCH    COPTION,SFIL1000,SFIL2000
.
SFIL1000  APPEND    PATHNAM,UPLFILE
          CALL      FEND0000
          APPEND    FILENAM,UPLFILE
.
          GOTO      SFIL9000
.
SFIL2000  APPEND    PATHNM1,UPLFILE
          CALL      FEND0000
          APPEND    FILENM1,UPLFILE
.
SFIL9000  APPEND    SP70,UPLFILE
          RESET     UPLFILE
.
SFIL9999  RETURN
+
.******************************************************************************
.*                       Find end of string                                   *
.******************************************************************************
FMAN0000  CMATCH    SP1,OPUPMANF
          GOTO      FMAN9999 IF NOT EQUAL
          BUMP      OPUPMANF,-1
          GOTO      FMAN0000 IF NOT EOS
          RESET     OPUPMANF
FMAN9999  RETURN
+
.******************************************************************************
.*                       Find end of string                                   *
.******************************************************************************
FEND0000  CMATCH    SP1,UPLFILE
          GOTO      FEND9999 IF NOT EQUAL
          BUMP      UPLFILE,-1
          GOTO      FEND0000 IF NOT EOS
          RESET     UPLFILE
FEND9999  RETURN
+
.******************************************************************************
.*                       Find end of string                                   *
.******************************************************************************
FSTR0000  CMATCH    SP1,NEWFILE
          GOTO      FSTR9999 IF NOT EQUAL
          BUMP      NEWFILE,-1
          GOTO      FSTR0000 IF NOT EOS
          RESET     NEWFILE
FSTR9999  RETURN
+
.******************************************************************************
.*                       Find a dot char                                      *
.******************************************************************************
FDOT0000  CMATCH    SP1,NEWFILE
          GOTO      FDOT1000 IF EQUAL
          CMATCH    ".",NEWFILE
          GOTO      FDOT1000 IF NOT EQUAL
          BUMP      NEWFILE,-1
          GOTO      FDOT9999
.
FDOT1000  BUMP      NEWFILE,-1
          GOTO      FDOT0000 IF NOT EOS
          RESET     NEWFILE
FDOT9999  RETURN
+
.******************************************************************************
.*       Check if any MBS or Misc in dtraf and Misc.Theatre item (pmsmtiaf)   *
.******************************************************************************
CHKR0000  OPEN      PATDTRA3,"patdtraf"
          MOVE      ZERO,EXIT
          PACK      KEY18,AADMNO,TWO,SP20    * check for MBS/Misc.item
          CALL      RDSDTRN3
CHKR1000  CALL      RDKDTRN3
          BRANCH    OVRCD,CHKR2000
.
          MATCH     AADMNO,TADMNO           * compare admission number
          GOTO      CHKR2000 IF NOT EQUAL
.
          BRANCH    TRECTYPE,CHKR2000,CHKR9000,CHKR9000
          GOTO      CHKR9999
.
.         Check with misc.theatre item file
.
CHKR2000  MOVE      "2",PMMIRTYP
          PACK      KEY15,DAADMNO,PMMIRTYP,SP20
          CALL      RSPMMTI2
          CALL      RKPMMTI2
          BRANCH    OVRCD,CHKR9999
.
          MATCH     DAADMNO,PMMIVISN
          GOTO      CHKR9999 IF NOT EQUAL
.
.         Found a MBS or Misc.item
.
CHKR9000  MOVE      ONE,EXIT
CHKR9999  RETURN
+
.******************************************************************************
.*                       Print upload record                                  *
.******************************************************************************
PREC0000  UNPACK    OPSTARTD,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,KEY10
.
          MATCH     SP10,ADMISSNO
          IF        @EQUAL
            MOVE      LSTADMNO,ADMISSNO      * Use most recent admiss no
          ENDIF
.
          MATCH     "001",RECTYPE           * MBS or Misc.item
          GOTO      PREC4000 IF NOT EQUAL   * Misc.
.
          PRINT     *1,URNUMBER,*10,ADMISSNO,*20,KEY10:
                    *35,OPSTRTIM,*45,OPENDTIM:
                    *55,"MBS: ",*60,MBSCODE,*82,CLMCODE;
          GOTO      PREC9999
.
.         Misc.item
.
PREC4000  PRINT     *1,URNUMBER,*10,ADMISSNO,*20,KEY10:
                    *35,OPSTRTIM,*45,OPENDTIM:
                    *55,"ITM: ",*60,MISCODE,*75,PROQUANT,*82,CLMCODE;
.
PREC9999  RETURN
+
.******************************************************************************
.*                       I/O Routines For The Tempfile                        *
.******************************************************************************
RATEMP    MOVE      ZERO,OVRCD
          RESET     KEY20
          READ      TMPFILE,KEY20;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSTEMP    RESET     KEY20
          READ      TMPFILE,KEY20;;
          RETURN
.
RKTEMP    MOVE      ZERO,OVRCD
          RESET     KEY20
          READKS    TMPFILE;XUNITNO,XADMNO,XSPARE
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP    RESET     KEY20
          WRITE     TMPFILE,KEY20;XUNITNO,XADMNO,XSPARE
          RETURN
.
RDTEMP    MOVE      ZERO,OVRCD
          RESET     KEY20
          READ      TMPFILE,KEY20;XUNITNO,XADMNO,XSPARE
          GOTO      OVERCOND IF OVER
          RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       TFILENAM                * Generate Temp File Name
          INC       IBASEQIO/INC            * Sequential Numbers File
          INC       WEBERRIO/INC            * Web Server Error Logging
.
          INC       BOKRC1IO/INC
          INC       PATIPEIO/INC     * Invoice pending file
          INC       PATCFAIO/INC     * Claim Code Future Actions
          INC       PATDRGIO/INC
          INC       PATCODIO/INC   
          INC       PMSCIDIO/INC     * Contract ID File
          INC       PATDSCIO/INC     
          INC       PATDTRIO/INC     
          INC       PATFX1IO/INC     *PATIENT HEALTH FUND EXTENSION TABLE
          INC       PATFHIIO/INC     * HF Invoice on hold table
          INC       PATITMIO/INC    
          INC       PATTRNIO/INC     
          INC       PATVISIO/INC     
          INC       PATWR1IO/INC     * Ward/Bed File
          INC       PATMI1IO/INC     
          INC       PATMMBIO/INC     
          INC       PATFN1IO/INC
          INC       PATHSPIO/INC
          INC       PATTFEIO/INC
          INC       PATMCHIO/INC
          INC       PATONHIO/INC
          INC       PMSMTIIO/INC
          INC       PMSVX1IO/INC
          INC       OPRUPLIO/INC     * Theatre upload history file
          INC       WATESEIO/INC
          INC       WATTR1IO/INC
          INC       WATTX1IO/INC
.
          INC       PATIPERH
          INC       CLWATTX1
          INC       PATSTRYR
          INC       GETCNEFF
          INC       GETBFEES
          INC       PATBFEIO/INC
          INC       PATMA1IO/INC
          INC       PATRCAIO/INC
          INC       NHOSPDAY
          INC       PATMCHRD                * Miscellaneous Charges read routine
          INC       PATITMRD
